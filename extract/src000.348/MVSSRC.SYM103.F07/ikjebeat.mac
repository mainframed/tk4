         TITLE 'BEAT      EDIT ATTENTION EXIT ROUTINE -- IKJEBEAT'
IKJEBEAT CSECT
*A520000-522000                                                  A42949
*D522000,561000-564000                                           A42949
*A463200-464400,536000-540000,561000-701000,732600-733800        A42953
*A762640-762920,787000-811000,820000-827000,854000-859000        A42953
*A874200-875400,910400,921000-930000,966000                      A42953
*C867000                                                         A42953
*D537000-540000,570000-726000,786000-813000,822000-828000        A42953
*D852000-861000,921000-930000                                    A42953
*A557000,767000,780600-782100,935000-943000                      A42960
*D558000,765000-768000,936000-942000,960000-972000               A42960
*A522700-524100                                                 YA02022
*D72900-732000,873000                                          @Y30NQKK
         EXTRN IKJEBMA2
***********************************************************************
*MODULE NAME -- IKJEBEAT                                              *
*                                                                     *
*DESCRIPTIVE NAME -- EDIT ATTENTION EXIT ROUTINE                      *
*                                                                     *
*COPYRIGHT -- N/A                                                     *
*                                                                     *
*CHANGE ACTIVITY -- SEE INFORMATION PRECEDING PROLOGUE                *
*                                                                     *
*STATUS -- VERSION NO. 02, VS2 RELEASE 3                              *
*                                                                     *
*FUNCTION --                                                          *
*                                                                     *
*       THIS ROUTINE GAINS CONTROL AFTER AN ATTN INTERRUPT WHILE THE  *
*   EDIT COMMAND PROCESSOR IS BEING EXECUTED.  UPON ENTRY, IKJEBEAT   *
*   STOPS ANY ATTACHED SUBTASKS PRIOR TO HANDLING THE ATTENTION.      *
*   THE ATTENTION SCHEDULING ROUTINE WILL HAVE ISSUED A TGET WITHOUT  *
*   TRANSFER OF DATA (THIS IS DETERMINED BY PARAMETERS ON THE STAX    *
*   MACRO ISSUED BY IKJEBEMA IN ESTABLISHING THE ATTENTION EXIT).     *
*   THE TGET RETURN CODE IS CHECKED. IF ZERO, THE INPUT IS A NULL     *
*   LINE.  OTHERWISE, A PUTGET IS ISSUED (WITH) THE 'ATTN'            *
*   OPTION, TO (1) PROCESS '?' WHEN ENTERED AT THE TERMINAL,          *
*   AND (2) RETURN A LINE OF INPUT FROM THE TERMINAL.                 *
*                                                                     *
*       WHEN THE INPUT LINE RETURNED BY PUTGET IS NULL, THE           *
*   BUFFER IT OCCUPIES IS FREEMAINED AND CONTROL IS RETURNED TO       *
*   THE CALLER.                                                       *
*                                                                     *
*       WHEN THE INPUT LINE RETURNED BY PUTGET CONTAINS ONE OR        *
*       MORE CHARACTERS FROM THE TERMINAL, THE FOLLOWING ACTIONS      *
*       ARE TAKEN --                                                  *
*                                                                     *
*       1.  FREEMAIN OF PREVIOUS BUFFER OBTAINED FOR THIS             *
*           ROUTINE (IF ANY)                                          *
*                                                                     *
*       2.  REPLACEMENT OF BUFFER ADDRESS IN THE ATTENTION ECB        *
*           WITH THE ADDRESS OF THE BUFFER RETURNED BY PUTGET.        *
*                                                                     *
*       3.  ITF ATTENTION PROCESSING (IF APPLICABLE).  THIS           *
*           CONSISTS OF INVOKING THE IKJNC311 MODULE VIA LINK.        *
*                                                                     *
*       4.  ISSUING POST MACRO FOR ATTENTION ECB.                     *
*                                                                     *
*       5.  INDICATING IN THE EDIT COMMUNICATION AREA THAT            *
*           THE TERMINAL IS THE CURRENT INPUT SOURCE.                 *
*                                                                     *
*       BEFORE RETURNING TO THE CALLER, THIS ROUTINE ISSUES THE       *
*   STATUS MACRO TO RE-START DISPACTCHABLE SUBTASKS.  IT DOES         *
*   SO REGARDLESS OF THE CONTENTS OF THE INPUT LINE ENTERED AT        *
*   THE TERMINAL.                                                     *
*                                                                     *
*ENTRY POINTS --                                                      *
*                                                                     *
*   IKJEBEAT -- FROM THE ATTENTION SCHEDULING ROUTINE, AFTER AN       *
*               ATTENTION INTERRUPT HAS OCCURRED, AND A LINE OF INPUT *
*               HAS BEEN ENTERED AT THE TERMINAL.                     *
*                                                                     *
*INPUT --                                                             *
*                                                                     *
*   1.  REGISTER ONE ADDRESSES THE ATTENTION EXIT PARAMETER LIST --   *
*                                                                     *
*       A.  ADDRESS OF THE TAIE, THE TERMINAL ATTENTION INTERRUPT     *
*           ELEMENT, CONTAINING THE INTERRUPT ADDRESS AND REGISTERS,  *
*           THE BUFFER LENGTH, AND THE TGET RETURN CODE               *
*                                                                     *
*       B.  ADDRESS OF THE INPUT BUFFER                               *
*                                                                     *
*       C.  ADDRESS OF USER AREA (IN THIS CASE THE EDIT COMMUNICATION *
*           AREA)                                                     *
*                                                                     *
*OUTPUT --                                                            *
*                                                                     *
*   THIS ROUTINE SETS NO RETURN CODES.  OUTPUT CONSISTS OF POSTING    *
*   THE ATTENTION ECB, INDICATING IN THE EDIT COMMUNICATION           *
*   AREA THAT THE CURRENT SOURCE OF INPUT IS THE TERMINAL, AND        *
*   UPDATING THE BUFFER ADDRESS IN THE ECB.                           *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*                                                                     *
*   IKJPTGT -- PUTS MODE MESSAGE TO TERMINAL, PROCESSES PREVIOUS      *
*              SECOND-LEVEL MESSAGES, AND OBTAINS INPUT FROM          *
*              THE TERMINAL.                                          *
*                                                                     *
*   MACROS USED --                                                    *
*                                                                     *
*      STATUS - USED TO STOP ATTACHED SUB-TASKS                       *
*      PUTGET - BUILDS PARAMETERS FOR AND INVOKES IKJPTGT             *
*      IKJEBECA - MAPPING MACRO FOR EDIT COMMUNICATION AREA           *
*      IKJTAIE - MAPPING MACRO FOR TERMINAL ATTENTION INTERRUPT       *
*                ELEMENT                                              *
*      IKJIOPL - MAPPING MACRO FOR TMP I/O SERVICE ROUTINE            *
*                PARAMETER LIST                                       *
*      IKJPGPB - MAPPING MACRO FOR PUTGET PARAMETER BLOCK             *
*                                                                     *
*EXITS,NORMAL -- RETURN TO THE CALLING ROUTINE WITH NO RETURN CODE SET*
*                                                                     *
*EXITS,ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*                                                                     *
*   EDIT COMMUNICATION AREA -- CONTAINS THE ATTENTION WORKAREA AND    *
*                              VARIOUS CONTROL INFORMATION FOR        *
*                              THIS ROUTINE                           *
*                                                                     *
*   CAATNWKA -- A WORKAREA IN THE COMMUNICATION AREA CONTAINING SPACE *
*               FOR A SAVE AREA AND PARAMETER LISTS. IT IS DESCRIBED  *
*               BY THE DSECT ATWORKSV.                                *
*                                                                     *
*ATTRIBUTES -- REFRESHABLE, ENABLED, AND NON-PRIVILEGED               *
*                                                                     *
*NOTES -- REGISTER DEFINITIONS AS DESCRIBED BELOW. THIS ROUTINE IS    *
*         CHARACTER CODE INDEPENDENT.                                 *
***********************************************************************
         EJECT
***********************************************************************
*
* THE FOLLOWING EQU'S ALLOW SYMBOLIC ADDRESSING OF REGISTERS
*
***********************************************************************
PARMREG0 EQU   0             REGISTER 0 IS A PARAMETER REGISTER
PARMREG1 EQU   1             REGISTER 1 IS A PARAMETER REGISTER
COMMREG  EQU   9             REGISTER 9 CONTAINS COMM AREA ADDR
BASEREG  EQU   11            REGISTER 11 IS THE BASE REGISTER
DATAREG  EQU   12            REGISTER 12 IS USED TO ADDRESS THE
*                            WORK AREA FOR THIS MODULE
SAVEREG  EQU   13            REGISTER 13 CONTAINS THE SAVE AREA
*                            ADDRESS
RETCDREG EQU   15            RETURN CODES ARE PASSED IN REGISTER 15
REG2     EQU   2             WORK REGISTER
REG3     EQU   3             WORK REGISTER
REG4     EQU   4             WORK REGISTER
REG5     EQU   5             WORK REGISTER
REG6     EQU   6             WORK REGISTER
REG7     EQU   7             WORK REGISTER
REG8     EQU   8             WORK REGISTER
         EJECT
***********************************************************************
*
*                             SPECIAL EQU'S
*
***********************************************************************
         SPACE 2
* DISPLACEMENTS AND LENGTHS
D0       EQU   0             DISPLACEMENT OF 0
D1       EQU   1             DISPLACEMENT OF 1
D2       EQU   2             DISPLACEMENT OF 2
D4       EQU   4             DISPLACEMENT OF 4
D8       EQU   8             DISPLACEMENT OF 8
D72      EQU   72            DISPLACEMENT OF 72
D255     EQU   255           DISPLACEMENT OF 255
D256     EQU   256           DISPLACEMENT OF 256
L0       EQU   0             LENGTH OF ZERO BYTES
L4       EQU   4             LENGTH OF FOUR BYTES
L8       EQU   8             LENGTH OF EIGHT BYTES
L12      EQU   12            LENGTH OF TWELVE BYTES
L16      EQU   16            LENGTH OF SIXTEEN BYTES
L20      EQU   20            LENGTH OF TWENTY BYTES
         SPACE 2
ATEDITCA EQU   8             DISPLACEMENT FOR ESTABLISHING
*                            ADDRESSABILITY TO COMMUNICATION AREA
         SPACE
ATPTTAIE EQU   0              DISPLACEMENT OF TAIE ADDR IN PARMLIST
         SPACE
NOBRANCH EQU   0             SPECIFIES NO BRANCH IN BCTR
         SPACE
NOBUFFER EQU   8              MASK FOR BCR INSTRUCTION.          A42953
         SPACE
SUBPL1   EQU   1             SUBPOOL ONE                         A42953
         EJECT
***********************************************************************
*
*                             PROLOG
*
***********************************************************************
         SPACE 2
         SAVE  (14,12),,*     ENTRY LINKAGE
         BALR  BASEREG,NOBRANCH    ESTABLISH ADDRESSABILITY FOR
         USING *,BASEREG           THIS MODULE
         LR    REG2,PARMREG1 COPY PARAMETER LIST POINTER
         STATUS STOP          STOP ANY ACTIVE SUB-TASKS
         L     COMMREG,ATEDITCA(,REG2) ESTABLISH COMMUNICATIONS
         USING IKJEBECA,COMMREG        AREA ADDRESSABILITY
         LA    PARMREG1,ATWORKSV     ADDRESS THIS RTN'S SAVE AREA
         ST    PARMREG1,D8(,SAVEREG) FORWARD CHAIN CALLER'S SAVE AREA
         ST    SAVEREG,D4(,PARMREG1) BACK CHAIN CURRENT SAVE AREA
         LR    SAVEREG,PARMREG1 INITIALIZE SAVE REGISTER
         XC    ATWORKA(ATLNWKA),ATWORKA ZERO WORK AREA
         MVC   ATTMPLST(L8),CATMPLST  INITIALIZE TMP SERVICE     A42949
*                                       ROUTINE PARAMETER LIST,  A42949
*                                       WITHOUT ADDR OF ATTN ECB A42949
         LA    REG3,ATWORKA+8      MAKE ADDR OF ATTN ECB        YA02022
         ST    REG3,ATWORKA+8      POINT TO ITSELF WITH ZEROS   YA02022
*                                  IN THE HIGH ORDER BYTE       YA02022
         SPACE 2
***********************************************************************
*
* THE TGET RETURN CODE IN THE TAIE IS CHECKED. IF ZERO, A NULL LINE
* WAS ENTERED AT THE TERMINAL. OTHERWISE PUTGET IS ISSUED TO     A42953
* (1) PROCESS ANY QUESTION MARKS ENTERED AT THE TERMINAL, AND    A42953
* (2) TO OBTAIN AN INPUT LINE FROM THE TERMINAL.                 A42953
*
***********************************************************************
         SPACE 2
         L     REG3,ATPTTAIE(,REG2) ADDRESS TAIE
         CLI   TAIETGET-TAIE(REG3),L0  WAS TGET RETURN CODE ZERO
         BE    ATNOLINE       YES, BRANCH TO NULL LINE EXIT      A42960
         EJECT
**************************************************************** A42953
*                                                                A42953
* A PUTGET IS ISSUED TO OBTAIN THE LINE ENTERED AT THE TERMINAL. A42953
* BY SPECIFYING THE 'ATTN' KEYWORD, THE MODE MESSAGE IS PRINTED  A42953
* ONLY AFTER A '?' IS ENCOUNTERED, AND INPUT IS FROM THE         A42953
* TERMINAL.                                                      A42953
*                                                                A42953
**************************************************************** A42953
         SPACE
         MVC   ATPARM(L16),ATPTGTPB MOVE PARM BLOCK TO DYNAMIC   A42953
*                                  AREA.                         A42953
         LA    PARMREG1,ATPARM OBTAIN ADDRESS OF PARAMETER BLOCK A42953
         ST    PARMREG1,ATTMPLST+IOPLIOPB-IOPL CHAIN BLOCK       A42953
*                                  TO PARAMETER LIST.            A42953
         LA    REG2,CAMODEMG  OBTAIN MODE MESSAGE OUTPUT         A42953
*                               DESCRIPTOR ADDRESS               A42953
         PUTGET OUTPUT=((REG2),SINGLE,ATTN),MF=(E,ATTMPLST)      A42953
         LTR   RETCDREG,RETCDREG CHECK IF LINE WAS SUCCESSFULLY  A42953
*                                  OBTAINED FROM TERMINAL.       A42953
         BNZ   ATNOLINE       IF ERROR OCCURRED, ACTION IS SAME  A42953
*                               AS FOR NULL LINE.                A42953
         EJECT
**************************************************************** A42953
*                                                                A42953
* A LINE HAS BEEN OBTAINED FROM THE TERMINAL.  IF THIS LINE IS   A42953
* NULL, IT MUST BE FREEMAINED BEFORE TAKING NULL LINE EXIT.      A42953
* OTHERWISE, THE INPUT BUFFER IS ASSUMED TO CONTAIN A SUBCOMMAND A42953
*                                                                A42953
**************************************************************** A42953
         SPACE
         L     REG8,ATPARM+PGPBIBUF-PGPB OBTAIN BUFFER ADDRESS.  A42953
         LA    REG3,D4        OBTAIN LENGTH OF NULL-LINE BUFFER. A42953
         SH    REG3,D0(,REG8) SUBTRACT ACTUAL BUFFER LENGTH.     A42953
         BM    ATSCMD         IF RESULT NEGATIVE, LINE IS TO     A42953
*                                  BE TREATED AS A SUBCOMMAND.   A42953
*                                                                A42953
* OTHERWISE, THE LINE IS NULL.  FREEMAIN BUFFER JUST OBTAINED.   A42953
*                                                                A42953
         ST    REG8,ATBUFFER  SPECIFY ADDRESS TO BE FREEMAINED.  A42953
         BAL   REG6,ATFREEBF  FREE NULL LINE BUFFER              A42953
         B     ATNOLINE       BRANCH TO TAKE NULL LINE EXIT.     A42953
         EJECT                                                   A42953
**************************************************************** A42953
*                                                                A42953
* LINE ENTERED IS TO BE TREATED AS A SUBCOMMAND --               A42953
*                                                                A42953
*   1. FREEMAIN ANY BUFFER OBTAINED THROUGH PREVIOUS ATTENTION   A42953
*      PROCESSING, AND STORE ADDRESS OF NEW BUFFER.              A42953
*                                                                A42953
*   2. PREFORM ITF ATTENTION PROCESSING AS NECESSARY.            A42953
*                                                                A42953
*   3. ISSUE POST FOR ATTENTION ECB.                             A42953
*                                                                A42953
*   4. INDICATE THAT TERMINAL IS CURRENT SOURCE OF INPUT.        A42953
*                                                                A42953
*   5. FINAL PROCESSING CONSISTS OF ISSUING STATUS MACRO TO      A42953
*      RESTART DIPATCHABLE SUBTASKS.  THIS PROCESS IS PERFORMED  A42953
*      UNCONDITIONALLY -- IT APPLIES TO SUBCOMMAND AND NULL      A42953
*      LINE INPUT.                                               A42953
*                                                                A42953
**************************************************************** A42953
         SPACE
ATSCMD   EQU   *                                                 A42953
         L     REG3,CAATNBUF  OBTAIN ATTENTION BUFFER            A42953
         ST    REG3,ATBUFFER  SPECIFY BUFFER ADDRESS TO BE       A42953
*                             FREEMAINED.                        A42953
         ST    REG8,CAATNBUF  SAVE NEW INPUT BUFFER ADDRESS      A42953
         BAL   REG6,ATFREEBF  BRANCH TO FREE BUFFER.             A42953
         NI    CACFLAG2,D255-CAMODMSG  INSURE THAT CONTROLLER DOES
*                                      NOT REISSUE MODE MESSAGE
         EJECT
**************************************************************** A42953
*                                                                A42953
* PERFORM ITF PROCESSING IF NECESSARY, POST ATTN ECB.            A42953
*                                                                A42953
**************************************************************** A42953
         SPACE
         OC    CAPTCHK(L4),CAPTCHK IS ITF IN SYSTEM
         BZ    ATPOST              NO,POST ECB
         TM    CACFLAG1,CAINITSC   IS ITF INIT IN PROCESS
         BO    ATPOST              YES, SKIP ITF ATTN PROCESSING
         CLI   CADSCODE,CABASIC    WAS THE DATA SET A BASIC DATA SET
         BE    ATITF               YES,GO PROCESS ITF
         CLI   CADSCODE,CAIPLI     WAS THE DATA SET AN IPLI DATA SET
         BNE   ATPOST              NO,POST ECB
ATITF    EQU   *
         LINK  EP=IKJNC311         INVOKE ITF ROUTINE
         SPACE
ATPOST   EQU   *
         POST  CAATTN              POST ATTENTION ECB
         SPACE
         NI    CACFLAG4,D255-CAINPROC INDICATE TERMINAL INPUT LINE
         EJECT
**************************************************************** A42953
*                                                                A42953
*                           EXIT PROCESSING                      A42953
*                                                                A42953
**************************************************************** A42953
         SPACE
ATEXIT   EQU   *                                                 A42960
         STATUS START         START ANY STOPPED SUB-TASKS
         L     SAVEREG,D4(,SAVEREG) RESTORE CALLER'S SAVE
*                                   REGISTER
         RETURN (14,12)       RETURN TO CALLER
         SPACE 3
* NULL LINE EXIT. RESTART AUTOMATIC LINE PROMPTING IN CASE       A42960
* ATTENTION OCCURRED WHILE IN INPUT MODE                         A42960
         SPACE
ATNOLINE EQU   *                                                 A42960
         RTAUTOPT  ,          RESTART AUTOMATIC LINE PROMPT      A42960
         B     ATEXIT         RETURN                             A42960
         EJECT
**************************************************************** A42953
*                                                                A42953
* THE FOLLOWING SUBROUTINE IS INVOKED TO FREEMAIN EXCESS         A42953
* BUFFERS.  ZEROS IN THE LOW-ORDER THREE BYTES OF THE STORAGE    A42953
* WORD'ATBUFFER' INDICATE THAT NO BUFFER EXISTS TO BE FREED.     A42953
*                                                                A42953
**************************************************************** A42953
         SPACE 2
ATFREEBF EQU   *
         OC    ATBFRPT(L'ATBFRPT),ATBFRPT CHECK IF BUFFER EXISTS A42953
*                                  TO BE FREED.                  A42953
         BCR   NOBUFFER,REG6  IF NOT, RETURN TO CALLER.          A42953
         L     PARMREG1,ATBUFFER OBTAIN BUFFER ADDRESS.          A42953
         L     PARMREG0,ATFREEWD SUBPOOL NUMBER                  A42953
         AH    PARMREG0,D0(,PARMREG1) OBTAIN LENGTH OF BUFFER.   A42953
         FREEMAIN R,LV=(0),A=(1) RELEASE STORAGE.                A42953
         BR    REG6           RETURN TO CALLER.                  A42953
         SPACE 2
***********************************************************************
*
*                             LOCAL STORAGE
*
***********************************************************************
         SPACE 2
* PARAMETER BLOCK FOR PUTGET SERVICE ROUTINE                     A42953
ATPTGTPB PUTGET TERMPUT=(EDIT,BREAKIN),TERMGET=(EDIT,WAIT),MF=L  A42953
         SPACE 2
ATFREEWD DS    0A             FULLWORD FOR R-FORM OF FREEMAIN    A42953
         DC    AL1(SUBPL1)    SUBPOOL ONE                        A42953
         DC    AL3(L0)        FOR FREEMAIN LENGTH                A42953
         EJECT
ATCOMMA  IKJEBECA             DEFINE EDIT COMMUNICATIONS AREA
         EJECT
         ORG   CAATNWKA       DEFINE WORKAREA IN COMMUNICATION AREA
         SPACE 2
ATWORKSV DS    0F             DEFINE IKJEBEAT WORK/SAVE AREA
ATSAVEA  DS    18F            SAVE AREA FOR THIS MODULE
* THIS AREA IS CLEARED TO ZERO UPON ENTRY TO IKJEBEAT
ATWORKA  DS    0F             WORK AREA FOR THIS MODULE
ATTMPLST DS    3F             TMP STANDARD PARAMETER LIST
ATSRPLST DS    F              TMP SERVICE ROUTINE PARAMETER LIST
ATPARM   DS    5F             PARAMETER LIST FOR SERVICE ROUTINE
         SPACE
ATLNWKA  EQU   *-ATWORKA      LENGTH OF WORK AREA ONLY           A42953
         SPACE
         ORG   ATPARM         REUSE CORE
ATWKAREA DS    5F             WORK AREA FOR IKJEBMA2
         ORG   ATPARM         REUSE CORE
ATBUFFER DS    0A             ADDRESS OF BUFFER TO BE FREED.     A42953
ATBFRECB DS    X              HIGH ORDER CONTAINS ECB INFO.      A42953
ATBFRPT  DS    AL3            3-BYTE BUFFER ADDRESS.             A42953
         ORG   ,              RESUME WORK AREA MAP.              A42953
         EJECT
**************************************************************** A42960
*                                                                A42960
*                TERMINAL ATTENTION INTERRUPT ELEMENT            A42960
*                                                                A42960
**************************************************************** A42960
         SPACE
         IKJTAIE
         EJECT
         IKJIOPL
         EJECT
         IKJPGPB                                                 A42953
         SPACE
         END
