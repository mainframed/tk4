BEHE     TITLE 'EDIT SUBCOMMAND - HELP - IKJEBEHE'
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
* STATUS --                                                           *
*    VERSION NO. 01, OS/360, RELEASE NO. 20                           *
*                                                                     *
* FUNCTION --                                                         *
*    THE HELP SUBCOMMAND PROVIDES THE USER WITH EXPLANATIONS OF THE   *
*    EDIT COMMAND AND THE SUBCOMMANDS AVAILABLE UNDER EDIT. THIS      *
*    ROUTINE RECEIVES CONTROL FROM THE CONTROLLER ROUTINE, IKJEBEMA,  *
*    WHICH PASSES A POINTER TO A BUFFER CONTAINING THE HELP SUBCOMMAND*
*    ENTERED AT THE TERMINAL. THE BUFFER CONTAINS AN OFFSET TO THE    *
*    HELP OPERANDS IN ITS SECOND HALFWORD. THIS OFFSET IS SET TO ZERO,*
*    AND CONTROL IS PASSED TO THE COMMAND INVOKER ROUTINE, IKJEBECI,  *
*    VIA XCTL. THE COMMAND INVOKER ATTACHES THE SYSTEM HELP COMMAND TO*
*    PROCESS THE REQUEST.                                             *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IKJEBEHE  -  THE MAIN AND ONLY ENTRY POINT                  *
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 CONTAINS A POINTER TO THE EDIT COMMUNICATIONS AREA.   *
*    THE WORD 'CAPTIBFR' OF THE COMMUNICATIONS AREA CONTAINS A POINTER*
*    TO AN INPUT BUFFER, WHICH CONTAINS THE HELP SUBCOMMAND ENTERED AT*
*    THE TERMINAL.                                                    *
*                                                                     *
* OUTPUT --                                                           *
*    A TWO WORD PARAMETER LIST PASSED TO THE COMMAND INVOKER ROUTINE. *
*    THE FIRST WORD CONTAINS A POINTER TO THE EDIT COMMUNICATIONS     *
*    AREA, AND THE SECOND CONTAINS A POINTER TO A BUFFER. THE FIRST   *
*    HALFWORD OF THIS BUFFER CONTAINS THE LENGTH OF THE BUFFER, AND   *
*    THE SECOND HALFWORD CONTAINS ZERO. THE REMAINDER OF THE BUFFER   *
*    CONTAINS THE HELP SUBCOMMAND AS ENTERED AT THE TERMINAL.         *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         IKJEBECI  -  THE COMMAND INVOKER ROUTINE                    *
*         EDIT COMMUNICATION AREA, BUILT BY THE INITIALIZATION        *
*           ROUTINE, IKJEBEIN.                                        *
*                                                                     *
* EXITS,NORMAL --                                                     *
*    THIS ROUTINE EXITS TO THE COMMAND INVOKER ROUTINE, IKJEBECI, VIA *
*    XCTL. UPON EXIT REGISTER 1 CONTAINS A POINTER TO THE PARAMETER   *
*    LIST DESCRIBED UNDER 'OUTPUT'.                                   *
*                                                                     *
* EXITS,ERROR -- N/A                                                  *
*                                                                     *
* TABLES/WORK AREAS -- N/A                                            *
*                                                                     *
* ATTRIBUTES --                                                       *
*    REFRESHABLE,ENABLED,NON-PRIVILEGED                               *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    NONE                                                             *
*                                                                     *
* NOTES --                                                            *
*    USE OF THE IKJEBESV MACRO FOR STANDSRD ENTRY LINKAGE REQUIRES    *
*    THAT A BASE REGISTER BE DEFINED WITH THE SYMBOLIC NAME 'BASEREG',*
*    AND THAT ANOTHER REGISTER BE DEFINED WITH THE SYMBOLIC NAME      *
*    'COMMREG', FOR USE IN ADDRESSING THE EDIT COMMUNICATIONS AREA.   *
*                                                                     *
***********************************************************************
         SPACE 2
IKJEBEHE CSECT
         SPACE 2
***********************************************************************
*                                                                     *
*    EQUATES OF SYMBOLIC REGISTERS
*                                                                     *
***********************************************************************
PARMREG1 EQU   1                       PARAMETER POINTER
ADDRREG  EQU   5                       ADDRESSING REGISTER
WORKREG  EQU   6                       WORK REGISTER
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING
BASEREG  EQU   11                      BASE REGISTER
SAVEREG  EQU   13                      SAVE AREA REGISTER
RETREG   EQU   14                      RETURN TO CALLER
RETCDREG EQU   15                      RETURN CODE
         SPACE 2
***********************************************************************
*                                                                     *
*    EQUATES OF SYMBOLIC CONSTANTS
*                                                                     *
***********************************************************************
         SPACE
D2       EQU   2                       DISPLACEMENT OF 2 (DECIMAL).
D4       EQU   4                       DISPLACEMENT OF 4 (DECIMAL).
D12      EQU   12                      DISPLACEMENT OF 12 (DECIMAL).
         EJECT
***********************************************************************
*                                                                     *
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY BOTH
*    IN THIS ROUTINE AND IN THE COMMUNICATION AREA
*                                                                     *
***********************************************************************
         SPACE
*    SAVE CALLER'S REGISTERS AND ESTABLISH ADDRESSABILITY
         IKJEBESV (14,12),T,*
         SPACE
         USING IKJEBECA,COMMREG        ADDRESSABILITY IN COMMUNICATION
*                                      AREA
         SPACE 2
***********************************************************************
*                                                                     *
*    SET UP PARAMETER LIST FOR IKJEBECI                               *
*                                                                     *
***********************************************************************
         LA    PARMREG1,COWORD1        LET PARMREG1 POINT TO PARMLIST
         ST    COMMREG,COWORD1         LET 1ST PARAM WORD POINT TO
*                                      COMM. AREA
         L     ADDRREG,CAPTIBFR        ADDRESS OF INPUT COMMAND BUFFER
*                                      INTO ADDRREG
         ST    ADDRREG,COWORD2         SECOND WORD OF PARAMETER LIST
*                                      POINTS TO INPUT COMMAND BUFFER
         SPACE 2
***********************************************************************
*                                                                     *
*    SET SECOND HALFWORD OF INPUT COMMAND BUFFER TO ZERO              *
*                                                                     *
***********************************************************************
         SPACE
         SR    WORKREG,WORKREG         SET WORKREG TO ZERO
         STH   WORKREG,D2(ADDRREG)     SET SECOND HALFWORD OF BUFFER
*                                      TO ZERO
         SPACE 2
***********************************************************************
*                                                                     *
*    TRANSFER CONTROL TO THE COMMAND INVOKER ROUTINE                  *
*                                                                     *
***********************************************************************
         ST    SAVEREG,CANXTSVA        SET POINTER TO NEXT AVAILABLE
*                                      SAVE AREA TO POINT TO SAVE AREA
*                                      USED BY THIS ROUTINE.
         L     SAVEREG,D4(SAVEREG)     SAVEREG POINTS TO PREVIOUS SAVE
*                                      AREA
         L     RETREG,D12(SAVEREG)     RETURN ADDRESS INTO RETREG
         SPACE
         MVC   COXCTL(XCTLL),HEXCTL    MOVE LIST FOR XCTL INTO EDIT
*                                      COMMUNICATION AREA
         SPACE
         XCTL  (2,12),MF=(E,(1)),SF=(E,COXCTL)
         SPACE 3
***********************************************************************
*                                                                     *
*    CONSTANT AREA FOR HELP SUBCOMMAND
*                                                                     *
***********************************************************************
HEXCTL   XCTL  EP=IKJEBECI,SF=L
XCTLEND  EQU   *
XCTLL    EQU   XCTLEND-HEXCTL
         SPACE 2
         IKJEBECA
         SPACE 2
         SPACE
***********************************************************************
*                                                                     *
*    COWORD1 AND COWORD2 ARE TO ADDRESS THE FIRST TWO WORDS OF THE    *
*    SUBCOMMAND WORK AREA OF THE EDIT COMMUNICATIONS AREA             *
*                                                                     *
***********************************************************************
         SPACE
         ORG   CASCWKA                 SET LOCATION COUNTER TO OFFSET
*                                      OF SUBCOMMAND WORD AREA
COWORD1  DS    F                       FIRST WORD OF PARAMETER LIST
COWORD2  DS    F                       SECOND WORD OF PARAMETER LIST
COXCTL   DS    20F                     AREA FOR THE LIST FORM OF XCTL
         END
