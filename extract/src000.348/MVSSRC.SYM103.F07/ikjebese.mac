BESE TITLE 'TSO EDIT COMMAND  STRING SEARCH SERVICE ROUTINE  IKJEBESE'
IKJEBESE CSECT
         SPACE
*A245500,247000-255000,291000,309000-316000,339000-347000,375000 A45363
*A414000-439000,453000-477000,508000,516500-518500,519200-519500 A45363
*A521500,561000-573000,621700-625900,636000-639000,657000-659000 A45363
*A662200-662700,667000-710000,768700-770100,778000-782000        A45363
*A789000-844000,858200-858500,864000-871000,880000-881000,894000 A45363
*A922000-922500,942000-944000,960500-962000,981000-982000        A45363
*C306000,521000,552000-555000,597000-609000,753000,945000        A45363
*C951000-957000                                                  A45363
*D249000-261000,285000,291000-300000,309000-318000,324000-330000 A45363
*D339000-345500,375000,414000-441000,453000-490000,498000-501000 A45363
*D561000-579000,612000-615000,621000-624000,633000-639000        A45363
*D645000-651000,657000-660000,666000-726000,756000-762000        A45363
*D778000-780000,789000-851000,864000-876000,894000-903000,922000 A45363
*D933000-936000,942000-945000,981000                             A45363
*A369500,424500-428600                                           A50468
*C369000                                                         A50468
*A917500-917700,922020                                          YA00036
*C917000,918000                                                 YA00036
*A600500,606500                                                  YM5001
*C606000                                                         YM5001
*                 ABEND 0C4 ON NULL LINE LAST IN BUFFER        @ZA12720
         SPACE
***********************************************************************
*                                                                     *
*STATUS -- VERSION NO. 01, OS/360 RELEASE NO. 20                      *
*                                                                     *
*FUNCTION -- THIS ROUTINE WILL SCAN  A RANGE OF LINES AT EVERY        *
*   OFFSET FOR A GIVEN STRING. TEXT DATA SETS ARE CHECKED ACROSS      *
*   RECORDS FOR THE STRING ALSO.                                      *
*                                                                     *
*ENTRY POINT -- IKJEBESE                                              *
*                                                                     *
*INPUT -- REGISTER ZERO CONTAINS A POINTER TO THE EDIT COMMUNICATION  *
*   AREA, DEFINED BY THE IKJEBECA MACRO. REGISTER ONE POINTS TO       *
*   A PARAMETER LIST CONTAINING                                       *
*        WORD 1 - ADDRESS OF UTILITY DATASET DCB                      *
*        WORD 2 - BYTES 0-1 -- OFFSET AT WHICH TO BEGIN SEARCH        *
*                 BYTES 2-3 -- LENGTH OF SEARCH STRING                *
*        WORD 3 - ADDRESS OF SEARCH STRING                            *
*        WORD 4 - POINTER TO BUFFER TO BE USED, CONTAINING FIRST RCD  *
*        WORD 5 - POINTER TO DOUBLE WORD CONTAINING RANGE OF LINE     *
*                 NUMBERS TO SEARCH, BEGINNING WITH NUMBER OF LINE    *
*                 CURRENTLY IN THE BUFFER. IF THE FIRST LINE NUMBER   *
*                 OF THIS RANGE IS NEGATIVE THE SECOND NUMBER IS      *
*                 INSTEAD A COUNT OF THE NUMBER OF RECORDS TO SEARCH  *
*                                                                     *
*OUTPUT -- RETURN CODES IN REGISTER 15 ARE (IN HIGH ORDER BYTE)       *
*   X'00' - TEXT FOUND IN ONE LINE                                    *
*   X'04' - TEXT FOUND ACROSS LINES                                   *
*   X'08' - TEXT NOT FOUND                                            *
*   X'0C' - I/O ERROR ON UTILITY DATASET                              *
*                                                                     *
*   OFFSET OF TEXT IN DATA PORTION OF LINE, ORIGIN ONE, IS IN         *
*   LOW ORDER TWO BYTES OF REGISTER 15                                *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*        IKJEBEUT -- READS UTILITY DATASET                            *
*        MACROS USED --                                               *
*        IKJEBESV -- ENTRY LINKAGE                                    *
*        IKJEBESV -- ENTRY LINKAGE                                    *
*        IKJEBERT -- EXIT LINKAGE                                     *
*        IKJEBESH -- SETS UP PARAMETER LIST AND LINKS TO IKJEBEUT     *
*                                                                     *
*EXITS, NORMAL -- RETURN TO CALLER WITH RETURN CODE                   *
*                                                                     *
*EXITS, ERROR -- RETURN TO CALLER, RETURN CODE X'0C' FOR I/O ERROR    *
*                                                                     *
*TABLES/WORKAREAS -- THE CASCWKA FIELD OF THE COMMUNICATIONS AREA,    *
*   DEFINED IN THE IKJEBECA MACRO, IS USED FOR                        *
*   WORK SPACES.  THE BUFFER POINTED TO BY THE CALLER IS ALSO         *
*   USED FOR SOME WORK SPACE.                                         *
*                                                                     *
*ATTRIBUTES -- REFRESHABLE, ENABLED, NON-PRIVILEGED.                  *
*                                                                     *
*NOTES -- THERE IS NO CHARACTER CODE DEPENDENCY.                      *
*   IF A STRING IS FOUND ACROSS LINES, THE SECOND RECORD IS PLACED    *
*   AT A SPECIFIC OFFSET IN THE BUFFER, TO WHICH IKJEBECH IS          *
*   SENSITIVE.                                                        *
***********************************************************************
         EJECT
         SPACE
*  REGISTER AND SPECIAL EQUATES
PARMREG0 EQU   0                   PARAMETER REGISTER
PARMREG1 EQU   1                   PARAMETER REGISTER
WRA      EQU   2                   GENERAL REGISTER
WRB      EQU   3                   GENERAL REGISTER
WRC      EQU   4                   GENERAL REGISTER
WRD      EQU   5                   GENERAL REGISTER
WRE      EQU   6                   GENERAL REGISTER
WRF      EQU   7                   GENERAL REGISTER
COMMREG  EQU   9                   COMM AREA ADDR REGISTER
BASEREG  EQU   11                  PROG CSECT BASE REGISTER
DATAREG  EQU   12                  SPECIAL REGISTER
SAVEREG  EQU   13                  SAVE AREA PTR REGISTER
RETREG   EQU   14                  RETURN ADDR REGISTER
RETCDREG EQU   15                  RETURN CODE REGISTER
FOUNDAOP EQU   X'04'               OP FOR FOUND ACROSS LINES
NOTFOUNP EQU   X'08'               OP FOR TEXT NOT FOUND
IOERROP  EQU   X'0C'               OP FOR I/O ERROR
FOUNDOP  EQU   X'00'               OP FOR TEXT FOUND IN ONE LINE
DATPTR   EQU   10                  PTR REG TO DATA
ENDPTR   EQU   8                   REG PTR TO END OF DATA
NEXTRD   EQU   X'02'               CODE FOR READ NEXT
EOPARM   EQU   X'80'               CODE FOR END OF PARM LIST
L0       EQU   0                   LENGTH
L4       EQU   4                   LENGTH
RET4     EQU   4                   RETURN CODE
L3       EQU   3                   LENGTH
L1       EQU   1                   LENGTH
L8       EQU   8                   LENGTH
L2       EQU   2                   LENGTH
L12      EQU   12                  LENGTH
SCNDOFF  EQU   264                 OFFSET FOR 2ND RCD MOVE WHEN FOUND
L255     EQU   255                 LENGTH
RDCR     EQU   X'00'               UT CODE FOR READ CURRENT RCD
         EJECT
*  ENTRY LINKAGE
         IKJEBESV (14,12),,,COMMREG=NO
         LR    COMMREG,PARMREG0    POINTER TO COMMUNICATION AREA
         USING IKJEBECA,COMMREG    ADDRESSABILITY FOR COMMAREA DSECT
         SPACE 3
*  INITIALIZE WORK AREAS
         ST    PARMREG1,INPARMAD   SAVE ADDR OF INPUT PARMS      A45363
         SR    RETCDREG,RETCDREG   INIT RC TO ZERO
         ST    RETCDREG,SERTCD     SAVE RETURN CODE              A45363
         LM    WRA,WRF,L0(PARMREG1)  MOVE INPUT PARAMETERS       A45363
         STM   WRA,WRF,DCBPTR          INTO WORKAREA             A45363
         SPACE
*  FIFTH INPUT WORD (IN REG WRE) IS A POINTER TO LINE RANGE      A45363
         SPACE
         LM    WRA,WRB,L0(WRE)     MOVE LINE RANGE VALUES        A45363
         STM   WRA,WRB,FIRST         INTO WORK AREA              A45363
         LTR   WRA,WRA             TEST FIRST LINE NUMBER
         BNM   SRCH                IF NOT -1 (*) CONTINUE
         LTR   WRB,WRB             TEST COUNT
         BZ    NOTFOUN             IF ZERO, CONSIDER TEXT NOT FOUND
SRCH     L     WRB,BUFPTR          POINT TO RECORD
         BAL   RETREG,POINDATA     LINK TO GET POINTER TO DATA
*                                  PORTION AND END OF DATA PORTION
         EJECT
*  PTR TO DATA IS IN DATPTR, PTR TO END OF DATA IN ENDPTR
         ST    DATPTR,LINEAD       SAVE DATA PTR
         AH    DATPTR,TXTOFF       INCREMENT INIT DATA PTR BY OFFSET
         ST    ENDPTR,EOFIRSDT     SAVE END OF FIRST DATA PORTION PTR
         L     PARMREG1,BUFPTR     ADDRESS OF REC IN BUFFER      A45363
         LA    WRB,CATEMPBF        PTR TO TEMP BUFFER
         BAL   RETREG,MOVBUF       BR TO MOVE RECORD TO TEMP BFR A45363
         LH    WRC,SECHARCT        MAX NUMBER OF CHAR TO SCAN    A45363
         LTR   WRC,WRC             IS COUNT POSITIVE             A45363
         BNP   SE000200            NO, TREAT AS ALL CHAR SCANNED A45363
         SPACE
*  SET UP REGISTERS FOR COMPARE LOOP                             A45363
         SPACE
         LH    WRA,TXTLNTH         LENGTH OF SEARCH STRING       A45363
         BCTR  WRA,PARMREG0        DECREMENT FOR COMPARE EXECUTE A45363
         L     WRB,TXTPTR          ADDR OF SEARCH STRING         A45363
         SPACE
SRCHLOOP EQU   *                                                 A45363
         LA    WRD,L1(WRA,DATPTR)  ADDR FIRST BYTE PAST COMPARE  A45363
         CR    WRD,ENDPTR          WOULD COMPARE EXTEND PAST REC A45363
         BH    NOTFND              IF SO, BRANCH
         EX    WRA,COMPARE         COMPARE
         BE    FOUND               IF FOUND, BRANCH
         LA    DATPTR,L1(,DATPTR)  ADDRESS NEXT BYTE TO SCAN     A45363
         BCT   WRC,SRCHLOOP        SCAN UNTIL CHAR COUNT ZERO    A45363
         B     SE000200            CANNOT CROSS BDY IF COUNT 0   A45363
         SPACE
NOTFND   EQU   *                                                 A45363
         STH   WRC,SECHARCT        SAVE COUNT                    A45363
         CLI   CADSCODE,CATEXT     IS DSTYPE TEXT                A45363
         BNE   SE000200            NO, NO SCAN ACROSS BDY        A45363
         S     DATPTR,LINEAD       YES, RESET OFFSET FOR         A45363
         STH   DATPTR,TXTOFF         SCAN ACROSS REC BDY         A45363
         TM    CACFLAG2,CARECFM    IS REC FORMAT VARIABLE        A45363
         BZ    TEXTYPE             YES, CHECK STRING ACROSS BDY  A45363
         TM    SEFLAGS,SEMODSW     NO. HAS REC BEEN MODIFIED     A45363
         BZ    TEXTYPE             NO, CHECK STRING ACROSS BDY   A45363
SE000200 EQU   *                                                 A45363
         SR    WRC,WRC             RESET SEARCH OFFSET           A45363
         STH   WRC,TXTOFF            TO ZERO                     A45363
         SPACE
*  CHECK FOR MORE LINES TO SCAN
         L     WRA,FIRST           LINE NUM FOR FIRST LINE
         LTR   WRA,WRA             TEST
         BM    ASTER               IF NEGATIVE, * COUNT FORM WAS USED
         L     WRA,BUFPTR          PTR TO BUFFER
         CLC   L0(L4,WRA),LAST     COMPARE CURRENT TO LAST KEY
         BE    NOTFOUN             IF LAST LINE PROCESSED,       A50468
*                                  BRANCH TO SET RC 8            A50468
         EJECT
*  READ NEXT RECORD INTO BUFFER POINTED TO BY BUFPTR             A45363
RNR      LA    WRA,BEUTABA         ADDR OF PARM LIST
         L     WRB,DCBPTR          PTR TO CURRENT DCB
         L     WRC,BUFPTR          PTR TO INPUT BUFFER
         ST    WRB,BEUTABA         STORE INTO PARM LIST
         ST    WRC,BEUTABB         ST IN UT PARMS PTR TO KEY
         MVI   BEUTABA,NEXTRD      UT CODE FOR READ NEXT RCD
         ST    WRC,BEUTABC         PTR TO INPUT BUFFER IN UT PARMLIST
         OI    BEUTABC,EOPARM      CODE FOR END OF PARM LIST
         IKJEBESH   (COMMREG),IKJEBEUT,PARAM=((COMMREG),(WRA)),        X
               MF=(E,SHAREA)
         LTR   RETCDREG,RETCDREG   TEST RETURN CODE
         BNZ   SE000300            NOT ZERO, GO CHECK FOR ERROR  A45363
         TM    CAATTN,CAATTNIS     HAS ATTENTION BEEN ISSUED     A45363
         BO    SE000250            YES, EXIT, RETURN CODE ZERO   A45363
         L     WRA,FIRST           OBTAIN FIRST LINE NUMBER      A50468
         LTR   WRA,WRA             CHECK FOR LINE COUNT CASE     A50468
         BM    SE000230            IF 'COUNT' OF LINES, CONTINUE A50468
*                                  PROCESSING                    A50468
         CLC   L0(L4,WRC),LAST     CHECK IF RECORD KEY EXCEEDS   A50468
*                                  LAST LINE NO. OF RANGE        A50468
         BH    NOTFOUN             IF SO, BRANCH TO SET RC 8     A50468
SE000230 EQU   *                                                 A50468
         LH    WRA,L4(,WRC)        LENGTH OF RECORD READ LESS    A45363
         SH    WRA,SECONST4          LENGTH OF LL00 HEADER       A45363
         STH   WRA,SECHARCT        SAVE MAX POSSIBLE NUMBR CHAR  A45363
         LA    WRB,CATEMPBF          ADDR OF BUFFER
         BAL   RETREG,MOVBUF       BRANCH TO MOVE RCD
         B     SRCH                GO TO SEARCH
         EJECT
*  STRING WAS FOUND IN ONE LINE
FOUND    EQU   *                                                 A45363
         BCTR  WRC,PARMREG0        UPDATE COUNT FOR LAST SCANNED A45363
         STH   WRC,SECHARCT        SAVE COUNT OF CHAR NOT SCAND  A45363
         S     DATPTR,LINEAD       OFFSET TO STRING FOUND        A56363
         LA    DATPTR,L1(,DATPTR)  INCREM FOR ORIGIN 1           A56363
         ST    DATPTR,SERTCD       SAVE OFFSET                   A56363
*  HIGH ORDER BYTE OF SERTCD IS ZERO, THE CORRECT RETURN CODE    A45363
         SPACE 3
*  EXIT LINKAGE
OUT      EQU   *                   PROGRAM EXIT
         L     PARMREG1,BUFPTR     PTR TO RCD IN BUFFER
         LA    WRB,CATEMPBF+L12    PTR TO VERIFY BUFFER
         BAL   RETREG,MOVBUF       BRANCH TO MOVE RCD
SE000250 EQU   *                                                 A45363
         L     WRA,LAST            FOR * COUNT FORM OF CHANGE
         L     WRB,LINRNPTR        RETURN RESIDUAL COUNT LEFT
         ST    WRA,L4(WRB)          IN ORIGINAL COUNT WORD
         L     WRB,INPARMAD        ADDR OF ORIGINAL PARM LIST    A45363
         LH    WRA,SECHARCT        COUNT OF CHAR REMAINING       A45363
         STH   WRA,SECHARCT-SEPARMS(,WRB)  RETURN COUNT IN       A45363
*                                            ORIGINAL HALFWORD   A45363
         L     RETCDREG,SERTCD     RET CODE AND OFFSET           A45363
         IKJEBERT   (14,12),,RC=(15)
         SPACE 3
* STRING NOT FOUND AND NO MORE LINES TO CHECK                    A45363
NOTFOUN  EQU   *                                                 A45363
         MVI   SERTCD,NOTFOUNP     CODE FOR STRING NOT FOUND     A45363
         B     SE000250            EXIT                          A45363
    EJECT
*  FOR SEARCH ACROSS LINE BOUNDARIES (TEXT DSTYPE),              A45363
*  A RECORD IS READ IN LOCATE MODE                               A45363
TEXTYPE  EQU   *
         LA    WRA,BEUTABA         ADDR OF PARM LIST
         L     WRB,DCBPTR          PTR TO DCB
         ST    WRB,BEUTABA         STORE IN PARM LIST
         L     WRB,BUFPTR          PTR TO KEY OF LAST RCD
         ST    WRB,BEUTABB         STORE IN PARM LIST
         MVI   BEUTABA,NEXTRD
         OI    BEUTABB,EOPARM
         IKJEBESH   (COMMREG),IKJEBEUT,PARAM=((COMMREG),(WRA)),        X
               MF=(E,SHAREA)
         LTR   RETCDREG,RETCDREG   TEST RC
         BZ    SETUPLN             IF SUCCESSFUL BRANCH
SE000300 EQU   *                                                 A45363
         CH    RETCDREG,SECONST4   WAS EOD ENCOUNTERED           A45363
         BE    NOTFOUN             IF EOD, BRANCH TO NOT FOUND RTN
*                                  I/O ERROR CONDITION           A45363
         MVI   SERTCD,IOERROP      RETURN CODE FOR I/O ERROR     A45363
         B     SE000250            EXIT                          A45363
         EJECT
*  PTR TO NEW RCD IS IN PARMREG1
SETUPLN  EQU   *
         LR    WRB,PARMREG1        ADDR OF RCD
         BAL   RETREG,POINDATA     LINK TO POINT AT BEG , END OF DATA
*  DATPTR NOW POINTS AT DATA PORTION OF RCD, ENDPTR TO EOD
         SR    ENDPTR,DATPTR       LENGTH OF DATA IN REC TWO     A45363
         BNP   NODATA              BRANCH IF DATA LEN=0        @ZA12720
         L     WRA,EOFIRSDT        ADDR FIRST BYTE AFTER REC ONE A45363
         BCTR  ENDPTR,PARMREG0     DECR. SECOND REC. LEN FOR MVC YM5001
         EX    ENDPTR,MOVESEC      CONCATENATE SECOND REC BEHIND A45363
*                                    FIRST                       YM5001
         LA    ENDPTR,L1(,ENDPTR)  INCR. BY 1 FOR TRUE LENGTH    YM5001
NODATA   EQU  *                                                @ZA12720
         A     ENDPTR,EOFIRSDT     RESET ENDPTR TO LAST BYTE+1   A45363
         L     DATPTR,LINEAD       BEGINNING OF FIRST RCD DATA
         AH    DATPTR,TXTOFF       RESET POINTER BY OFFSET
         XC    TXTOFF,TXTOFF       ZERO OFFSET
         SPACE
*  SET UP REGISTERS FOR SEARCH LOOP                              A45363
         SPACE
         LH    WRA,TXTLNTH         LENGTH OF SEARCH STRING LESS  A45363
         BCTR  WRA,PARMREG0          ONE FOR COMPARE EXECUTE     A45363
         L     WRB,TXTPTR          ADDR OF SEARCH STRING         A45363
         SPACE
CROSRCH  EQU   *                                                 A45363
         C     DATPTR,EOFIRSDT     IS PTR BEYOND FIRST RCD
         BNL   NOTAC               IF SO, TEXT NOT ACROSS LINES
         LA    WRD,L1(WRA,DATPTR)  ADDR FIRST BYTE PAST COMPARE  A45363
         CR    WRD,ENDPTR          WOULD COMPARE EXTEND PAST REC A45363
         BH    NOTAC               IF SO, BRANCH
         EX    WRA,COMPARE         EXECUTE COMPARE
         LA    DATPTR,L1(,DATPTR)  ADDRESS NEXT RECORD BYTE      A45363
         BNE   CROSRCH             CONTINUE SCAN IF NOT FOUND    A45363
         EJECT
*  STRING WAS FOUND ACROSS LINES
         SPACE
* ONE PLUS ADDR OF STRING FOUND IN CONCATENATED REC IS IN DATPTR A45363
*  ADDR OF COMPLETE SECOND RECORD IS IN PARMREG1                 A45363
*  COMPLETE FIRST RECORD IS IN CATEMPBF                          A45363
*  BOTH RECORDS ARE RETURNED TO CALLER IN BUFFER ADDRESSED BY    A45363
*  FULLWORD LABELED BUFPTR                                       A45363
         SPACE
FOUNDAC  EQU   *
         L     WRC,BUFPTR          GET RETURN BUFFER ADDR        A45363
         LA    WRB,SCNDOFF(,WRC)   RETURN REC TWO AT THIS ADDR   A45363
         BAL   RETREG,MOVBUF       MOVE REC TWO                  A45363
         SPACE
         LR    WRB,WRC             RETURN REC ONE AT THIS ADDR   A45363
         LA    PARMREG1,CATEMPBF   ADDR OF REC ONE               A45363
         BAL   RETREG,MOVBUF       MOVE REC ONE                  A45363
         SPACE
         S     DATPTR,LINEAD       OFFSET TO STRING, ORIGIN ONE  A45363
         ST    DATPTR,SERTCD       SAVE OFFSET FOR RETURN        A45363
         MVI   SERTCD,FOUNDAOP     SET RETURN CODE               A45363
         B     OUT                 EXIT                          A45363
         SPACE 3
*  STRING NOT FOUND, CHECK FOR END OF SEARCH RANGE AND ATTN
NOTAC    EQU   *                   CHECK FOR FINISHED WITH SEARCH
         L     WRA,FIRST           FIRST LINE NUM
         LTR   WRA,WRA             TEST
         BM    ASTERAC             IF NEGATIVE, * COUNT WAS USED
         CLC   L0(L4,PARMREG1),LAST  COMPARE RCD 2 KEY TO LAST
         BH    NOTFOUN             IF OVER RANGE, GO TO SET RC=8
RESETATN EQU   *                   CHECK FOR ATTN
         TM    CAATTN,CAATTNIS     WAS ATTN ISSUED
         BO    SE000250            YES, EXIT, RET CODE ZERO      A45363
         L     WRB,BUFPTR          ADDR OF BUFFER
         BAL   RETREG,MOVBUF       BRANCH TO MOVE RCD
         LH    WRB,L4(,WRB)        RECORD LENGTH LESS LENGTH     A45363
         SH    WRB,SECONST4          OF LL00 HEADER              A45363
         STH   WRB,SECHARCT        SAVE MAX NUMBR OF DATA BYTES  A45363
         LA    WRB,CATEMPBF        POINT TO BUFFER
         BAL   RETREG,MOVBUF       BRANCH TO MOVE RCD
         B     SRCH                GO TO SEARCH LOOP
         EJECT
*******                                                          A45363
*                                                                A45363
*  THIS ROUTINE SETS DATPTR TO THE ADDR OF THE FIRST BYTE OF     A45363
*  DATA, AND ENDPTR TO THE ADDR OF THE BYTE FOLLOWING THE        A45363
*  LAST BYTE OF DATA, OF THE RECORD POINTED TO BY REGISTER WRB.  A45363
*  THE SEQUENCE FIELD OF A NUMBERED RECORD IS NOT CONSIDERED     A45363
*  TO BE DATA.                                                   A45363
         SPACE
POINDATA EQU   *                                                 A45363
         MVC   LHALF(L2),L4(WRB)   ALIGN LENGTH
         LH    WRC,LHALF           LENGTH OF RECORD
         LA    DATPTR,L8(,WRB)     ADDRESS FIRST BYTE OF RECORD  A45363
         TM    CACFLAG1,CANONUM    IS DATA SET NOT LINE NUMBERED A45363
         BO    SE000500            YES, NONUM, SKIP NUM PROCESS  A45363
         SR    WRA,WRA             CLEAR                         A45363
         IC    WRA,CALINE          OFFSET TO SEQUENCE FIELD      A45363
         BCT   WRA,SE000510        DOES SEQ FIELD BEGIN IN COL 1 A45363
*                                  YES, DATA FOLLOWS SEQ FIELD   A45363
         SPACE
         IC    WRA,CALENGTH        LENGTH OF SEQUENCE FIELD      A45363
         AR    DATPTR,WRA          ADDR FIRST BYTE PAST SEQ FLD  A45363
SE000500 EQU   *                                                 A45363
         LA    ENDPTR,L4(WRC,WRB)  ADDR FIRST BYTE PAST REC DATA A45363
         BR    RETREG              RETURN TO CALLER
         SPACE
*  SEQUENCE FIELD IS AT END OF RECORD, USE OFFSET IN REG WRA     A45363
SE000510 EQU   *                                                 A45363
         LA    ENDPTR,L8(WRA,WRB)  ADDR OF FIRST BYTE OF SEQ FLD A45363
         BR    RETREG              RETURN TO CALLER              A45363
         SPACE 3
*******
*
*  FOR * COUNT RANGE, CHECK FOR END OF RANGE
ASTER    EQU   *                   CHECK FOR LINES TO SCAN, * CT FRM
         LA    WRB,RNR             RETURN ADDRESS FOR THIS ENTRY A45363
         B     SE000600            BRANCH AROUND SECOND ENTR     A45363
ASTERAC  EQU   *                   * COUNT CHECK FOR TEXT DS
         LA    WRB,RESETATN        RETURN ADDRESS FOR THIS ENTRY A45363
SE000600 EQU   *                                                 A45363
         L     WRA,LAST            COUNT
         BCT   WRA,DECA            DECREMENT AND TEST
         B     NOTFOUN             IF NO MORE RCDS, BRANCH
DECA     ST    WRA,LAST            STORE DECREMENTED COUNT
         BR    WRB                 CONTINUE PROCESSING           A45363
         EJECT
         SPACE 3
*  MOVE ROUTINE AND MVCS AND CLCS FOR EXS
MOVBUF   EQU   *                   RTN TO MOVE RCD
         MVC   L0(L8,WRB),L0(PARMREG1)  MOVE KEY, CONTROL       YA00036
         LH    WRE,L4(,PARMREG1)   OBTAIN LENGTH OF RECORD      YA00036
         BCTR  WRE,PARMREG0        DECR LENGTH FOR MOVE         YA00036
         SH    WRE,SECONST4        ADJUST LEN FOR CONTROL WORD  YA00036
         EX    WRE,MOVERECD        MOVE RECORD TO BUFFER        YA00036
         BR    RETREG              RETURN TO CALLER
COMPARE  CLC   L0(L0,WRB),L0(DATPTR)  COMPARE FOR EX FOR STRING
MOVESEC  MVC   L0(L0,WRA),L0(DATPTR)  MOVE FOR CONCATENATING REC A45363
MOVERECD MVC   L8(L0,WRB),L8(PARMREG1) MOVE RECORD TO BFR       YA00036
         SPACE 3
*******                                                          A45363
*  CONSTANTS                                                     A45363
         SPACE
SECONST4 DC    H'4'                FOR COMPARES, AND LL00 LENGTH A45363
         EJECT
         IKJEBECA
         EJECT
*  WORK AREAS
         ORG   CASRWKA             SERVICE ROUTINE WORKAREA
EOFIRSDT DS    1F                  ADDR OF END OF FIRST RCD DATA
LINEAD   DS    1F                  ADDR OF DATA PORTION OF RCD
SHAREA   DS    0F                  LABEL REFERENCED IN SH MACRO, A45363
*                                    NOT CURRENTLY USED          A45363
INPARMAD DS    A                   ADDR OF INPUT PARM LIST       A45363
         SPACE
*  SAVE WORDS FOR INPUT PARAMETERS                               A45363
         SPACE
SEPARMS  DS    0F                  INPUT PARM LIST               A45363
DCBPTR   DS    1F                  UTILITY DS DCB ADDR           A45363
TXTOFF   DS    1H                  INIT OFFSET TO BEGIN SEARCH
TXTLNTH  DS    1H                  LENGTH OF SEARCH STRING       A45363
TXTPTR   DS    1F                  ADDR OF SEARCH STRING         A45363
BUFPTR   DS    1F                  ADDR OF RECORD TO SCAN        A45363
LINRNPTR DS    1F                  PTR TO LINE RANGE
         DS    X                   RESERVED                      A45363
SEFLAGS  DS    X                   ONE BYTE OF FLAGS             A45363
SEMODSW  EQU   X'80'               LINE MODIFIED, 1-YES/0-NO     A45363
SECHARCT DS    H                   MAX NUMBER OF CHAR TO SCAN    A45363
         SPACE
FIRST    DS    1F                  FIRST LINE NUMBER IN RANGE
LAST     DS    1F                  LAST LINE NUMBER IN RANGE
BEUTABA  DS    1F                  IKJEBEUT PARM
BEUTABB  DS    1F                  LIST
BEUTABC  DS    1F                  IKJEBEUT PARM PTR TO STGE FOR RCD
LHALF    DS    H                   HALFWORD FOR LENGTH ALIGNMENT
         DS    H                   RESERVED                      A45363
SERTCD   DS    F                   RETURN CODE AND OFFSET        A45363
         SPACE
         ORG   ,                   RESTORE COUNTER
         SPACE 4
         END
