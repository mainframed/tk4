BEEX TITLE 'EDIT UTILITY DATA SET FINAL PROCESSING ROUTINE -IKJEBEEX'
*C0-999999                                                     @Y30NQKG
*          FIX BOA ABEND ON LARGE DATA SET                     @ZA12679
IKJEBEEX CSECT
*
***********************************************************************
*                                                                     *
*STATUS -- VERSION NO. 02, VS     RELEASE 03   PTF# UZ03405   @ZA09167*
*                                              LABEL EXDAIR   @ZA09167*
*                                                                     *
*COPYRIGHT N/A                                                        *
*                                                                     *
*FUNCTION -- PERFORMS THE FINAL PROCESSING ON THE UTILITY DATA SET    *
*   BY PERFORMING THE FOLLOWING OPERATIONS -                          *
*       -RELEASE THE CORE FOR THE BUFFERS                             *
*       -CLOSES THE UTILITY DATA SET                                  *
*       -UNALLOCATES THE UTILITY DATA SET                             *
*       -DELETES THE ACCESS ROUTINE LOAD MODULE (IKJEBEAA)            *
*       -RELEASE THE DIRECTORY BLOCK CORE
*       -RELEASES THE WORKAREA CORE                                   *
*                                                                     *
*ENTRY POINTS -- AT IKJEBEEX VIA A LINK OR LOAD-CALL                  *
*                                                                     *
*INPUT -- REGISTER 1 CONTAINS THE ADDRESS OF THE EDIT COMMUNICATIONS  *
*           AREA, IKJEBECA.                                           *
*         REGISTER 0 CONTAINS THE ADDRESS OF THE UTILITY WORKAREA,    *
*           UTILWORK.                                                 *
*                                                                     *
*OUTPUT --    THE OPERATIONS DESCRIBED UNDER 'FUNCTION' ABOVE WILL    *
*   HAVE BEEN PERFORMED.                                              *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*       IKJDAIR              -UNALLOCATES THE DATA SET                *
*       MACROS USED                                                   *
*         FREEMAIN           -RELEASE BUFFER AND WORKAREA CORE        *
*         DELETE             -DELETES ACCESSING LOAD MODULE (IKJEBEAA)*
*         CLOSE              -CLOSES UTILITY DATA SET                 *
*                                                                     *
*EXITS,NORMAL -- RETURNS ON REGISTER 14, ALL REGISTERS INTACT.        *
*                                                                     *
*EXITS,ERROR --  NONE                                                 *
*                                                                     *
*TABLES/WORK AREAS -- DEPENDENT ON THE UTILITY WORKAREA FOR-          *
*       -ADDRESS OF BUFFERS TO RELEASE                                *
*       -DCB TO CLOSE                                                 *
*   DEPENDENT ON THE EDIT COMMUNICATIONS AREA (IKJEBECA) FOR-         *
*       -INVOKING IKJDAIR TO UNALLOCATE THE DATA SET                  *
*       -INVOKING IKJEBEMS FOR WRITING OUT MESSAGES                   *
*                                                                     *
*ATTRIBUTES --  REFERESHABLE,ENABLED,NON-PRIVILEGED                   *
*                                                                     *
*NOTES -- REGISTER CONVENTIONS AS DESCRIBED BELOW.                    *
*                                                                     *
*   THIS ROUTINE IS CHARACTER CODE INDEPENDENT                        *
*                                                                     *
***********************************************************************
         SPACE 2
         EJECT
*   THE FOLLOWING ARE REGISTER ASSIGNMENTS
PARMREG0 EQU   0                       PARM POINTER
PARMREG1 EQU   1                       PARM POINTER
WORK2    EQU   2                       WORK REGISTER
WORK3    EQU   3                       WORK REGISTER
WORK5    EQU   5                       WORK REGISTER
WORK6    EQU   6                       WORK REGISTER
COMMREG  EQU   9                       COMMUNICATIONS AREA ADDRESSING
BASEREG  EQU   11                      BASE REGISTER
DATAREG  EQU   12                      UTILITY WORK AREA ADDRESSING
SAVEREG  EQU   13                      SAVE AREA ADDRESSING
RETREG   EQU   14                      RETURN REGISTER
RETCDREG EQU   15                      RETURN CODE
         SPACE 3
*   DEFINITIONS
SPONE    EQU   1                       SUBPOOL NUMBER
X0       EQU   0                       OFFSET OF ZERO
X2       EQU   2                       OFFSET OF TWO
X8       EQU   8                       OFFSET OF EIGHT
X26      EQU   26                      USED IN CLEARING MESSAGE AREA
X28      EQU   28                      OFFSET OF TWENTY-EIGHT
X240     EQU   240                     USED TO ADDRESS TRANSLATE TABLE
ONE      EQU   1                       CONSTANT OF 1
THREE    EQU   3                       CONSTANT OF 3
FOUR     EQU   4                       CONSTANT OF 4
FIVE     EQU   5                       CONSTANT OF 5
SIX      EQU   6                       CONSTANT OF 8
EIGHT    EQU   8                       CONSTANT OF 8
MGCOREB  EQU   40                      OFFSET TO MESSAGE BLOCK
MGCOREBO EQU   MGCOREB+6               OFFSET TO 2ND LEVEL OFFSET
MGCOREBL EQU   MGCOREB+5               OFFSET TO LENGTH FIELD
MGCOREBM EQU   MGCOREB+8               OFFSET TO 2ND LEVEL TEXT
MGCORB2  EQU   MGCOREB+16              OFFSET TO NEXT MESSAGE BLOCK
MGCORB2O EQU   MGCORB2+6               OFFSET TO 2ND 2ND LEVEL OFFSET
MGCORB2L EQU   MGCORB2+5               OFFSET TO 2ND 2ND LEVEL LENGTH
MGCORB2M EQU   MGCORB2+8               OFFSET TO 2ND 2ND LEVEL TEXT
         SPACE 3
*   MESSAGE REFERENCES
         SPACE 2
         IKJEBEMI (313,424)
         EJECT
*   PERFORM THE INITIALIZATION OPERATIONS HERE
         IKJEBESV  (14,12),T,*             SAVE THE REGISTERS
         SPACE
         USING IKJEBECA,COMMREG
         LR    DATAREG,PARMREG0        UTILITY WORK AREA ADDRESS
         USING UTILWORK,DATAREG
         LA    WORK3,TEMPAREA          TEMPORARY WORKAREA FOR THIS
         USING EXWORK,WORK3              ROUTINE
         XC    EXWORK(EXEND-EXWORK),EXWORK  CLEAR WORK AREA
         SPACE 2
*   RELEASE THE CORE FOR THE BUFFER BLOCKS
         LH    WORK5,DCBBFNO           NO. OF BUFFERS
         LH    WORK2,DCBBLKSI          BLOCK SIZE
         LA    WORK6,BUF1              1ST BUFFER ADDRESS
         USING BUFFER,WORK6            ADDRESSABILITY
EX01     L     PARMREG1,BUFBLOCK       BLOCK ADDRESS
         FREEMAIN R,LV=(WORK2),A=(1),SP=SPONE
         LA    WORK6,L'BUF1(,WORK6)    NEXT BUFFER ADDRESS
         BCT   WORK5,EX01              FREE NEXT BLOCK
         SPACE 2
*   CLOSE THE DCB HERE
         MVC   EXCLOSE(CLOSEEND-CLOSE),CLOSE  LIST FORM OF CLOSE MACRO
         LA    WORK2,UTILDCB           ADDRESS OF DCB
         LA    PARMREG1,EXCLOSE        ADDRESS OF LIST FORM
         CLOSE ((WORK2)),MF=(E,(1))        CLOSE THE DCB
         EJECT
*   UNALLOCATE THE DATA SET HERE
EXDAIR   EQU   *
         MVC   DAPB18(DAIRSIZE),DAIRCODE DAIR LIST TO WORKAREA
         MVC   DA18DDN(L'DCBDDNAM),DCBDDNAM DDNAME TO LIST
         LA    PARMREG1,DAPB18         DAIR PARM LIST ADDRESS
         ST    PARMREG1,CATMPLST+DAPLDAPB-DAPL TO PARM LIST
         L     PARMREG1,CAPTTMP        ADDRESS OF CPPL LIST
         USING CPPL,PARMREG1
         MVC   CATMPLST+DAPLPSCB-DAPL(L'CPPLPSCB),CPPLPSCB PSCB ADDRESS
         DROP  PARMREG1
         LA    PARMREG1,CATMPLST       TOTAL PARM LIST ADDRESS
         CALLTSSR  EP=IKJDAIR          UNALLOCATE THE DATA SET
         LTR   RETCDREG,RETCDREG       WAS UNALLOCATION SUCCESSFUL
         BNZ   EXSYSER                 NO, ISSUE ERROR MESSAGE @ZA09167
*                                      YES, CONTINUE           @ZA09167
         SPACE 2
*   DELETE THE ACCESSING ROUTINES HERE
EX1      EQU   *
         DELETE EP=IKJEBEAA            DELETE THE ACCESS METHOD
         SPACE 2
*   FREE THE DIRECTORY BLOCK CORE HERE
         L     PARMREG1,DBUFBLCK       BLOCK ADDRESS
         SR    WORK2,WORK2             CLEAR WORK REG          @ZA12679
         ICM   WORK2,THREE,DSDBSIZE    SIZE OF BLOCK           @ZA12679
         FREEMAIN R,LV=(WORK2),A=(1),SP=SPONE RELEASE CORE
         SPACE 2
*   FREE THE WORKAREA AND BUFFER CORE
         LR    PARMREG1,DATAREG        WORKAREA ADDRESS
         LH    WORK2,UWSIZE            SIZE OF AREA
         FREEMAIN R,LV=(WORK2),A=(1),SP=SPONE FREE WORKAREA
         SPACE 2
*   RETURN TO CALLING ROUTINE HERE
         IKJEBERT (14,12),T
         EJECT
         SPACE 2
***********************************************************************
*   SYSTEM ERROR IN ATTEMPTING TO UNALLOCATE DATA SET
EXSYSER  EQU   *
         CH    RETCDREG,RC12           WAS THERE A DYNAMIC ALLOC ERROR
         BE    EXUNLCER                YES, GO GIVE MESSAGE
         XC    CASRWKA+MGCOREB(X26),CASRWKA+MGCOREB  CLEAR MESSAGE AREA
         MVC   CASRWKA(MLSYSERE-MLSYSER),MLSYSER  MOVE MESSAGE BLOCK
         LA    WORK2,CASRWKA+MGCOREB   GET ADDRESS OF 2ND LEVEL
         ST    WORK2,CASRWKA+EIGHT     INITIALIZE AREA
         LA    WORK2,CASRWKA+MGCORB2   GET ADDRESS OF 2ND 2ND LEVEL
         ST    WORK2,CASRWKA+MGCOREB   POINT TO NEXT INSERTION
         MVI   CASRWKA+MGCOREBL,L'IKJDAIR+FOUR  SET LENGTH FIELD
         LA    WORK2,M3131IN1          GET RELATIVE OFFSET
         STH   WORK2,CASRWKA+MGCOREBO  STORE IN MESSAGE BLOCK
         MVC   CASRWKA+MGCOREBM(L'IKJDAIR),IKJDAIR  TEXT INTO BLOCK
         LA    WORK2,M3131IN2          GET 2ND RELATIVE OFFSET
         STH   WORK2,CASRWKA+MGCORB2O  STORE IN MESSAGE BLOCK
         SRL   RETCDREG,ONE            SET UP FOR INSERTION PICK UP
         LH    WORK2,CODETAB-X2(RETCDREG) GET CODE IN REG
         STH   WORK2,CASRWKA+MGCORB2M  STORE 2ND INSERTION
         MVI   CASRWKA+MGCORB2L,SIX    MOVE LENGTH INTO BLOCK
         SPACE 2
MSG      EQU   *
         IKJEBEML ,,,,MF=(E,CASRWKA)   PUT OUT MESSAGE
         B     EX1                     GO DELETE ACCESSING ROUTINES
         SPACE 2
***********************************************************************
*   NOT ABLE TO UNALLOCATE DATA SET
EXUNLCER EQU   *
         MVC   CASRWKA(MLUNALCE-MLUNALC),MLUNALC  MOVE 1ST LEVEL
         XC    CASRWKA+MGCOREB(X8),CASRWKA+MGCOREB  CLEAR MESSAGE AREA
         LA    WORK2,CASRWKA+MGCOREB   ADDRESS OF SECOND LEVEL AREA
         ST    WORK2,CASRWKA+EIGHT     POINT TO 2ND LEVEL BLOCK
         LA    WORK2,M4241IN1          GET RELATIVE POINTER
         STH   WORK2,CASRWKA+MGCOREBO  PUT IN 2ND LEVEL BLOCK
         MVI   CASRWKA+MGCOREBL,EIGHT  SET UP LENGTH
         SPACE
*        CONVERT RETURN CODE TO HEX CHARACTERS USING TRANSLATE TABLE.
         UNPK  CASRWKA+MGCOREBM(FIVE),EXWORK+DA18DARC-DAPB18(THREE)
         TR    CASRWKA+MGCOREBM(FOUR),TRANTAB-X240
         B     MSG                     GO PUT OUT MESSAGE
         EJECT
         DROP  BASEREG,COMMREG,DATAREG,WORK3
         SPACE 2
*   IKJEBEEX CONSTANT AREA
RC12     DC    H'12'                   DAIR RETURN CODE 12 - DYNAMIC
*                                      ALLOCATION ERROR
CODETAB  DS    0H
         DC    C'04081216202428'       MSG INSERTION TABLE
TRANTAB  DC    C'0123456789ABCDEF'     TRANSLATE TABLE
IKJDAIR  DC    C'DAIR'                 MESSAGE INSERTION
         SPACE 2
*   CLOSE - LIST FORM
CLOSE    CLOSE  0,MF=L
CLOSEEND EQU   *
         SPACE
***********************************************************************
*   SYSTEM ERROR MESSAGE, LIST FORM
MLSYSER  IKJEBEML M313,M3131,,,MF=L
MLSYSERE EQU   *
         SPACE 2
***********************************************************************
*   MESSAGE - DATA SET NOT UNALLOCATED - LIST FORM
MLUNALC  IKJEBEML M424,M4241,,,MF=L
MLUNALCE EQU   *
         SPACE 2
*   PARAMETER LIST FOR DAIR --- UTILITY DATA SET UNALLOCATION
         DS    0D
DAIRCODE DC    H'24'                   ENTRY CODE
FLAGS    DC    H'0'                    RETURN SETTINGS
DARC     DC    H'0'                    ERROR CODE---DYNAMIC ALLOCATION
CTRC     DC    H'0'                    ERROR CODE---CATALOG
DSNAMPTR DC    F'0'                    POINTER TO DSNAME
DDNAME   DC    8C' '                   DDNAME
MEMBER   DC    8C' '                   MEMBER NAME
SYSCLS   DC    CL2' '                  SYSOUT CLASS
DISP2    DC    X'04'                   DISPOSITION---DELETE
CONTROL  DC    X'00'                   DAIR CONTROL
DAIRSIZE EQU   *-DAIRCODE              LENGTH OF PARM LIST
         SPACE 2
*   DESCRIPTION OF USE OF THE SUBROUTINE WORK AREA IN 'TEMPAREA'
***********************************************************************
         IKJDAP18
EXWORK   EQU   DAPB18
EXCLOSE  CLOSE 0,MF=L                  LIST FORM OF CLOSE
EXEND    EQU   *                       END OF WORK AREA
***********************************************************************
         IKJCPPL
***********************************************************************
          IKJDAPL
***********************************************************************
         EJECT
         IKJEBEUW
         EJECT
         IKJEBECA
         EJECT
         CVT
***********************************************************************
         END
