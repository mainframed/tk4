         TITLE 'OS360 - TSO  EDIT COMMAND  IKJEBEMR-ITF INTERFACE'
IKJEBEMR CSECT
***********************************************************************
*                                                                     *
* STATUS -- VERSION NO. 001, OS/360 RELEASE NO. 20                    *
*                                                                     *
* FUNCTION -- THIS MODULE PROVIDES FOR THE RETRANSLATION OF THE ITF   *
*    REVERSE POLISH DATA SET AFTER A MERGE OR RENUMBER OPERATION. THE *
*    SYNTAX CHECKER IS CALLED TO DELETE THE OLD INTERMEDIATE DATA SET *
*    (BIT1 OF BYTE1 OF THE OPTION WORD SET TO 1). USING IKJEBEUT, THE *
*    EDIT UTILITY DATA SET IS READ AND PASSED LINE BY LINE TO THE SYN-*
*    TAX CHECKER TO BUILD A NEW INTERMEDIATE DATA SET (BIT0 OF BYTE1  *
*    SET TO 1).                                                       *
*                                                                     *
* ENTRY POINTS -- IKJEBEMR, MAIN ENTRY POINT.                         *
*                                                                     *
* INPUT -- REGISTER ONE POINTS TO THE COMMUNICATION AREA.             *
*                                                                     *
* OUTPUT -- THREE RETURN CODES CAN BE PASSED IN REGISTER 15.          *
*                                                                     *
*    0 - SUCCESSFUL COMPLETION                                        *
*                                                                     *
*    4 - UNSUCCESSFUL COMPLETION                                      *
*                                                                     *
*    8 - SYNTAX CHECKER NOT OPERATIONAL                               *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*                                                                     *
*         SYNTAX CHECKER CALLED TO DELETE AND BUILD THE POLISH DS     *
*                                                                     *
*         IKJEBEUT       CALLED TO READ THE UTILITY DATA SET          *
*                                                                     *
*         IKJEBEMS       CALLED TO SEND MESSAGES                      *
*                                                                     *
*         IKJEBEEX       CALLED TO DELETE THE NEW UTILITY DATA SET    *
*                                                                     *
*         MACROS USED --                                              *
*                                                                     *
*         IKJEBEML       INVOKES IKJEBEMS                             *
*                                                                     *
*         IKJEBEMG       CREATES MESSAGE INSERTION PARMLIST           *
*                                                                     *
*         IKJEBEMI       DEFINES EQUATES FOR MESSAGES                 *
*                                                                     *
*         IKJEBESV       ENTRY LINKAGE                                *
*                                                                     *
*         IKJEBESH       INVOKES EDIT MODULES                         *
*                                                                     *
*         IKJEBECA       DEFINES EDIT COMMUNICATION AREA              *
*                                                                     *
*         IKJEBERT       EXIT LINKAGE                                 *
*                                                                     *
*         CALL           INVOKES THE SYNTAX CHECKER                   *
*                                                                     *
* EXITS, NORMAL -- TO CALLER WITH RETURN CODE IN REGISTER 15          *
*                                                                     *
* EXITS, ERROR -- NONE                                                *
*                                                                     *
* TABLES/WORK AREAS -- IKJEBECA, THE EDIT COMMUNICATION AREA          *
*                                                                     *
*    CASRWKA   IKJEBEUT PARMLIST, SYNTAX CHECKER PARMLIST             *
*                                                                     *
*    CATEMPBF  READ-INTO BUFFER, MESSAGE PARMLIST                     *
*                                                                     *
* ATTRIBUTES -- REFRESHABLE, ENABLED, NON-PRIVILEGED                  *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- NONE                                   *
*                                                                     *
* NOTES -- STANDARD CONVENTIONS                                       *
*                                                                     *
***********************************************************************
         EJECT
*THE FOLLOWING EQU'S ALLOW SYMBOLIC ADDRESSING OF REGISTERS.
         SPACE
PARMREG0 EQU   0                       CONTAINS ADDR OF COMMAREA FOR UT
PARMREG1 EQU   1                       CONTAINS ADDR OF COMM AREA ON
*                                      ENTRY. CONTAINS PARAMETERS FOR
*                                      CALLS TO MS AND SYNTAX CHECKER
WKREG    EQU   2                       GENERAL WORK REGISTER
MSGREG   EQU   4                       ADDRESSABILITY
COMMREG  EQU   9                       CONTAINS ADDR OF COMMUNICATION
*                                      AREA
BASEREG  EQU   11                      BASE REGISTER FOR THIS CSECT
DATAREG  EQU   12                      NOT USED
SAVEREG  EQU   13                      CONTAINS ADDR OF SAVE AREA
RETREG   EQU   14                      CONTAINS ADDR OF RETURN ENTRY PT
CALLREG  EQU   15                      CONTAINS ADDR OF UT
RETCDREG EQU   15                      CONTAINS ADDR OF ENTRY PT,
*                                      CONTAINS RETURN CODE ON EXIT
         SPACE
*THE FOLLOWING EQU'S ALLOW SYMBOLIC REFERENCE TO CHARACTER AND DECIMAL
*CONSTANTS.
         SPACE
D0       EQU   0                       DISPLACEMENT OF 0
D4       EQU   4                       DISPLACEMENT OF 4
D8       EQU   8                       DISPLACEMENT OF 8
KONE     EQU   1                       CONSTANT OF 1
KFOUR    EQU   4                       CONSTANT OF 4
KEIGHT   EQU   8                       CONSTANT OF 8
K12      EQU   12                      CONSTANT OF 12
K256     EQU   256                     CONSTANT OF 256
MREIGHTY EQU   X'80'                   BIT MASK
MRALL    EQU   X'FF'                   BIT MASK
MROUTEC  EQU   X'02'                   READ NEXT ENTRY CODE FOR UT
MRDELCD  EQU   X'40'                   DELETE OPTION CODE
MRPRESV  EQU   X'47'                   BIT MASK
MRPRES   EQU   X'87'                   BIT MASK
MRINCD   EQU   X'80'                   INPUT OPTION CODE
MRRDF    EQU   X'04'                   READ FIRST RECORD ENTRY CODE
MRM      EQU   C'M'                    CHARACTER M
         EJECT
***********************************************************************
         IKJEBESV (14,12),,*           ENTRY LINKAGE
         SPACE
         USING IKJEBECA,COMMREG        COMM AREA ADDRESSABILITY
         L     MSGREG,MRMSGADR         ADDR OF MESSAGE CSECT
         USING IKJEBMRM,MSGREG         ADDRESSABILITY OF MSG CSECT
***********************************************************************
*                                                                     *
*THE SYNTAX CHECKER IS CALLED TO DELETE THE REVERSE POLISH DATA SET.  *
*                                                                     *
***********************************************************************
         L     CALLREG,CAPTCHK         ADDR OF SYNTAX CHECKER
         LTR   CALLREG,CALLREG         SYNTAX CHECKER PRESENT
         BZ    MR001000                BR NO TO TERMINATE
         LA    PARMREG1,CASYNLST       ADDR OF PARMLIST FOR CHECKER
         LA    WKREG,MRSYNPLT          ADDR OF DELETE PARMLIST
         ST    WKREG,CASYNBFR          FIRST WD OF PARMLIST
         OI    CASYNCD2,MRDELCD        SET OPTION WORD CODE FOR DELETE
         NI    CASYNCD2,MRPRESV        PRESERVE BITS 5-7
         XC    MRSTART(L'MRSTART),MRSTART SET START LINE TO BINARY0'S
         MVC   MRSTOP(KFOUR),MRBNINES  SET END LINE TO BINARY 9'S
         CALL  (15)                    GO TO CHECKER TO DELETE REVERSE
*                                      POLISH DATA SET
         SPACE
         NI    CASYNCD2,MRALL-MRDELCD  RESET OPTION WD CD BYTE
MR000100 EQU   *
         B     MR000100+D4(RETCDREG)   SYN CHK RC BR TABLE
         B     MR000200                00  SUCCESSFUL COMPLETION
         B     MR000150                04  ERROR, BUF CHECK COMPLETE
         B     MR001300                08  ERROR, BUF CHECK INCOMPLETE
         B     MR001300                12  LAST STMT IN BUF INCOMPLETE
         B     MR001400                16  NOT ENOUGH CORE
         B     MR001650                20  SYN CHKER NOT OPERATIONAL
         EJECT
***********************************************************************
*                                                                     *
*IKJEBEUT IS CALLED TO READ THE EDIT UTILITY DATA SET, ONE LINE AT A  *
*TIME, UNTIL THE ENTIRE UTILITY HAS BEEN READ.                        *
*                                                                     *
***********************************************************************
MR000150 EQU   *
         TM    CACFLAG1,CASCANSW       IS SCAN ALLOWED
         BNZ   MR001625                BR YES TO SET RC=4
MR000200 L     WKREG,CAPTCDCB          PTR TO CURRENT DCB
         ST    WKREG,MRUTPLST          FIRST WORD OF PARMLIST
         MVI   MRUTPLST,MRRDF          ENTRY CODE=X'04' READ FIRST RCD
         XC    MRUTWD2(L'MRUTWD2),MRUTWD2 SET SCND WD OF PLIST TO ZERO
         MVI   MRUTWD2,MREIGHTY        PARMLIST DELIMITER=X'80'
MR000300 LA    PARMREG1,MRUTPLST       ADDR OF PARMLIST FOR UT
         LR    PARMREG0,COMMREG        ADDR OF COMM AREA
         L     CALLREG,CAPTUT          ADDR OF IKJEBEUT
         BALR  RETREG,CALLREG          BR TO READ RECORD
         SPACE
MR000400 EQU   *
         B     MR000400+D4(RETCDREG)   UT RETURN CODE BR TABLE
         B     MR000500                00  SUCCESSFUL COMPLETION
         B     MR001000                04  EOD
         B     MR001600                08  IO ERROR
         EJECT
***********************************************************************
*                                                                     *
*EACH LINE READ BY IKJEBEUT IS PASSED TO THE SYNTAX CHECKER IN        *
*MRSYNBFR TO BUILD A NEW REVERSE POLISH DATA SET.                     *
*                                                                     *
***********************************************************************
MR000500 OI    CASYNCD2,MRINCD         OPTION WD CD=INPUT
         NI    CASYNCD2,MRPRES         PRESERVE BITS 5-7
         TM    CACFLAG2,CARECFM        RECFM=VARIABLE
         BZ    MR000900                BR YES
         MVC   MRSYNBFR+D4(K256),D8(PARMREG1) STRIP KEY & LL00
MR000600 L     WKREG,MRBUFHDR          HEADER FOR BUFFER
         ST    WKREG,MRSYNBFR
         LA    WKREG,MRSYNBFR          PTR TO BUFFER
         ST    WKREG,CASYNBFR          STORE IN PARMLIST
         LA    PARMREG1,CASYNLST       ADDR OF CHECKER PARMLIST
         L     CALLREG,CAPTCHK         ADDR OF SYNTAX CHECKER
         CALL  (15)                    GO TO CHECKER WITH ONE LINE
         SPACE
         NI    CASYNCD2,MRALL-MRINCD   RESET OPTION WD CD BYTE
         LTR   RETCDREG,RETCDREG       RC = 0
         BZ    MR000800                BR TO CONTINUE
         LA    WKREG,KFOUR             CONSTANT OF 4
         CR    RETCDREG,WKREG          RETURN CODE = 4
         BE    MR000800                BR YES TO CONTINUE
         B     MR000100                BR TO PUT ERROR MSG
         SPACE
MR000800 MVI   MRUTPLST,MROUTEC        YES SET UT ENTRY CODE=X'02'
         B     MR000300                LOOP UNTIL ALL LINES ARE READ
MR000900 MVC   MRSYNBFR+D4(K256),D4(PARMREG1) STRIP KEY ONLY
         B     MR000600                CONTINUE PROCESSING
         SPACE
MR001000 SR    RETCDREG,RETCDREG       RC=0
MR001100 IKJEBERT (14,12),,RC=(15)     EXIT LINKAGE
         SPACE
         EJECT
***********************************************************************
*                                                                     *
*WHEN RETURN CODES 8 AND 12 ARE RECEIVED, MESSAGE 313 IS ISSUED.      *
*WHEN RETURN CODE 16 IS PASSED, MESSAGE 312 IS ISSUED.                *
*                                                                     *
***********************************************************************
MR001300 EQU   *
         MVC   MRLEV1(MRM313L),MRM313  SET UP LEV1 PLIST
         LA    WKREG,MRL1IN1           CHAIN
         ST    WKREG,MRIN2PT           PARMLISTS
         MVC   MRIN1(D8),CASYNAME      NAME OF CHECKER
         LA    WKREG,MRL2IN2           CHAIN
         ST    WKREG,MRL1IN1           PARMLISTS
         MVI   MRIN1L,K12              LENGTH OF INSERTION
         MVI   MRIN1O,M3131IN1         INSERTION POINT
         MVC   MRL2IN2(MRMG3L),MRMG3   SET UP INSERT2 PLIST
         SRL   RETCDREG,KONE           SHIFT R15 FOR INDEXING
         LH    WKREG,MRRCTAB(RETCDREG) PICK UP PRINTABLE CHARACTER
         STH   WKREG,MRIN2             AND MOVE TO INSERTION
         B     MR001500                BR TO PUT MESSAGE
         SPACE
MR001400 MVC   MRLEV1(MRM312L),MRM312  SET UP LEV1 PLIST
         LA    WKREG,MRL1IN1           CHAIN
         ST    WKREG,MRIN1PT           PARMLISTS
         L     WKREG,CAPTSCMD          PTR TO SUBCOMMAND
         CLI   D0(WKREG),MRM           1ST LETTER=M
         BE    MR001450                BR YES
         MVC   MRL1IN1(MRMG2L),MRMG2   SET UP INSERTION FOR RENUM
         B     MR001500                BR TO PUT MESSAGE
MR001450 MVC   MRL1IN1(MRMG1L),MRMG1   SET UP INSERTION FOR MERGE
         B     MR001500                BR TO PUT MESSAGE
         SPACE
MR001500 EQU   *
         IKJEBEML ,,,,MF=(E,MRPARM)    LINK TO IKJEBEMS
         SPACE
         B     MR001625                BR TO SET RETURN CODE
***********************************************************************
*                                                                     *
*IF RETRANSLATION COULD NOT BE ACCOMPLISHED, THE NEW EDIT UTILITY DATA*
*SET IS DELETED BY A CALL TO IKJEBEEX, AND THE OLD UTILITY IS RESTORED*
*TO 'CURRENT' STATUS.                                                 *
*                                                                     *
***********************************************************************
MR001600 EQU   *
         L     WKREG,CAPTCDCB          PTR TO CURRENT DCB
         IKJEBESH (COMMREG),IKJEBEEX,PARAM=((WKREG),(COMMREG)),        X
               MF=(E,MRPARM)
         SPACE
         MVC   CAPTCDCB(L'CAPTCDCB),CAPTPDCB SET CURRENT = OLD UTIL DS
         XC    CAPTPDCB,CAPTPDCB       NO PREVIOUS DCB
         LA    RETCDREG,K12            SET RETURN CODE TO 12
         B     MR001100                RETURN
MR001625 EQU   *
         LA    RETCDREG,KFOUR          SET RC=4
         B     MR001100                RETURN
         SPACE
MR001650 EQU   *
         LA    RETCDREG,KEIGHT         SET RETURN CODE = 8
         B     MR001100                BR TO RETURN
         SPACE
         IKJEBEMI (312,313)            MESSAGE EQUATE MACRO
         EJECT
MRMSGADR DC    V(IKJEBMRM)             ADDR OF MSG CSECT
         SPACE
IKJEBMRM CSECT
         SPACE
MRM312   IKJEBEML M312,0,0,0,MF=L
MRM312L  EQU   *-MRM312
         SPACE
MRM313   IKJEBEML M313,M3131,0,0,MF=L
MRM313L  EQU   *-MRM313
         SPACE
         DS    0F
MRMG1    IKJEBEMG 0,M312IN1,'MERGE'
MRMG1L   EQU   *-MRMG1
         SPACE
         DS    0F
MRMG2    IKJEBEMG 0,M312IN1,'RENUM'
MRMG2L   EQU   *-MRMG2
         SPACE
         DS    0F
MRMG3    IKJEBEMG 0,M3131IN2,'00'
MRMG3L   EQU   *-MRMG3
         SPACE
MRRCTAB  DS    0H                       TABLE OF
         DC    C'00040812'              PRINTABLE RETURN CODES
IKJEBEMR CSECT
         SPACE
         DS    0F
         SPACE
MRBNINES DC    F'99999999'             ENDING LINE NUMBER
MRBUFHDR DC    X'00010000'             HEADER FOR BUFFER
***********************************************************************
         EJECT
         IKJEBECA                      COMMUNICATIONS AREA
         EJECT
         ORG   CASRWKA                 WORKAREA
         SPACE
MRSYNPLT DS    0F                      SYNTAX CHECKER PARMLIST
MRRESV   DS    F                       RESERVED
MRSTART  DS    F                       STRTING LINE NUMBER FOR DELETE
MRSTOP   DS    F                       ENDING LINE NUMBER FOR DELETE
         SPACE
MRUTPLST DS    F                       PARMLIST FOR IKJEBEUT
MRUTWD2  DS    F                       SECOND WORD
MRUTWD3  DS    F                       THIRD WORD
         SPACE
         ORG   CATEMPBF                WORK AREA
         SPACE
MRPARM   DS    12F                     MESSAGE PARMLISTS
MRLEV1   EQU   MRPARM                  LEV1 PARMLISTS
MRIN1PT  EQU   MRLEV1+4                PTR TO LEV1 INSERTION PLIST
MRIN2PT  EQU   MRLEV1+8                PTR TO LEV2 INSERTION PLIST
MRL1IN1  EQU   MRLEV1+12               INSERTION1 PARMLIST
MRIN1L   EQU   MRL1IN1+5               LENGTH OF INSERTION
MRIN1O   EQU   MRL1IN1+7               INSERTION POINT
MRIN1    EQU   MRL1IN1+8               INSERTION1
MRL2IN2  EQU   MRIN1+16                INSERTION2 PARMLIST
MRIN2L   EQU   MRL2IN2+4               LENGTH OF INSERTION2
MRIN2    EQU   MRIN2L+4                INSERTION2
         SPACE
         DS    0F
MRSYNBFR DS    F                       BUFFER FOR
         DS    256C                    SYNTAX CHECKER
         SPACE
         END
