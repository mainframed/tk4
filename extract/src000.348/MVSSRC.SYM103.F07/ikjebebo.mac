BEBO     TITLE 'EDIT SUBCOMMAND - BOTTOM - IKJEBEBO'
***********************************************************************
* STATUS-  VERSION NO. 01, OS/360 RELEASE NO. 20                      *
*                                                                     *
* FUNCTION-  IKJEBEBO IS AN EDIT SUBCOMMAND WHICH IS USED TO MOVE THE *
*    POSITION OF THE CURRENT LINE POINTER '*' TO THE LAST LINE OF THE *
*    DATA SET BEING EDITED.                                           *
*                                                                     *
* ENTRY POINTS-  IKJEBEBO IS THE MAIN AND ONLY ENTRY POINT.           *
*                                                                     *
* INPUT-  REGISTER 1 CONTAINS A POINTER TO EDIT'S COMMUNICATION AREA. *
*    THE LAST RECORD OF THE UTILITY DATA SET IS READ.                 *
*                                                                     *
* OUTPUT-  A RETURN CODE IN REGISTER 15. POSSIBLE RETURN CODES ARE-   *
*    0 - OPERATION WAS SUCCESSFUL                                     *
*    12 - I/O ERROR                                                   *
*                                                                     *
* EXTERNAL REFERENCES-                                                *
*         IKJEBEMS     -ISSUES APPROPRIATE MESSAGES TO TERMINAL USER  *
*         IKJEBEUT     -READS UTILITY DATA SET                        *
*    MACROS USED-                                                     *
*         IKJEBESH     -PROVIDES LINKAGE FOR EDIT SERVICE ROUTINES    *
*         IKJEBECA     -LAYOUT AND DESCRIPTION OF COMMUNICATION AREA  *
*         IKJEBESV     -STANDARD ENTRY LINKAGE AND ADDRESSABILITY     *
*         IKJEBERT     -STANDARD EXIT LINKAGE                         *
*         IKJEBEML     -STANDARD MESSAGE TO TERMINAL USER             *
*                                                                     *
* EXITS,NORMAL-  STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 0 IN  *
*    REGISTER 15                                                      *
*                                                                     *
* EXITS,ERROR- STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 12 IN   *
*    REGISTER 15                                                      *
*                                                                     *
* TABLES/WORK AREAS-  THE EDIT SUBCOMMAND WORK AREA IN THE COMMUNICA- *
*    TION AREA IS USED AND THE LAST RECORD OF THE UIILITY DATA SET IS *
*    READ IN TO THE TEMPORARY BUFFER POOL IN THE COMMUNICATION AREA   *
*    STARTING AT LOCATION CATEMPBF+12.                                *
*                                                                     *
* ATTRIBUTES-  REFRESHABLE, ENABLED, NON-PRIVILEGED                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY-  NONE                                    *
*                                                                     *
* NOTES-  STANDARD LINKAGE CONVENTIONS USED                           *
***********************************************************************
         EJECT
IKJEBEBO CSECT
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC REGISTERS
***********************************************************************
PARMREG0 EQU   0                       PARAMETER POINTER
PARMREG1 EQU   1                       PARAMETER POINTER
TESTREG  EQU   2                       WORK REGISTER
BINREG   EQU   3                       WORK REGISTER
WORKREG  EQU   5                       WORK REGISTER
UTREG    EQU   8                       WORK REGISTER
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING
BASEREG  EQU   11                      BASE REGISTER
DATAREG  EQU   12                      WORK REGISTER
SAVEREG  EQU   13                      SAVE AREA POINTER
RETREG   EQU   14                      RETURN TO CALLER
RETCDREG EQU   15                      RETURN CODE
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC CONSTANTS
***********************************************************************
BM0      EQU   0                       CONSTANT OF 0
BM3      EQU   3                       CONSTANT OF 3
BM4      EQU   4                       CONSTANT OF 4
BM8      EQU   8                       CONSTANT OF 8
BM9      EQU   9                       CONSTANT OF 9
BM12     EQU   12                      CONSTANT OF 12
IOERROR  EQU   12                      IOERROR RET. CODE
LAST     EQU   X'05'                   READ LAST RECORD CODE
END      EQU   X'80'                   END OF PARAMETER LIST CODE
         SPACE 2
***********************************************************************
*    MESSAGE REFERENCES
***********************************************************************
         IKJEBEMI (501)
         SPACE 2
***********************************************************************
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY IN
*    THIS ROUTINE AND IN THE COMMUNICATION AREA
***********************************************************************
         IKJEBESV (14,12),T,*          SAVE CALLER'S REGISTERS AND
*                                      ESTABLISH ADDRESSABILITY
         USING IKJEBECA,COMMREG        ADDRESSABILITY IN COMM. AREA
         SPACE 2
***********************************************************************
*    CALL IKJEBEUT THROUGH IKJEBESH TO READ LAST RECORD OF UTILITY
*    DATA SET
***********************************************************************
         MVC   COWORD1(BM4),CAPTCDCB   PUT ADDRESS OF CURRENT UTILITY
*                                      DCB IN THE 3 LOW ORDER BYTES OF
*                                      1ST PARAM WORD
         MVI   COWORD1,LAST            PUT READ LAST RECORD CODE IN
*                                      THE HIGH ORDER BYTE OF 1ST
*                                      PARAM WORD
         XC    COWORD2(BM4),COWORD2    2ND PARAM WORD DOES NOT CONTAIN
*                                      ANY POINTER
         LA    TESTREG,CATEMPBF+BM12
         ST    TESTREG,COWORD3         PUT POINTER TO TEMP. BUFFER
*                                      POOL IN COMM. AREA WHERE THE
*                                      RECORD IS TO BE PLACED IN 3
*                                      LOW ORDER BYTES OF 3RD PARAM.
*                                      WORD
         MVI   COWORD3,END             PUT END OF PARAMETER LIST
*                                      CODE IN HIGH ORDER BYTE OF
*                                      3RD PARAM. WORD
         LA    UTREG,COWORD1           LET UTREG POINT TO PARMLIST
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A)
         SPACE 2
***********************************************************************
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING LAST RECORD
***********************************************************************
         LTR   RETCDREG,RETCDREG       TEST RET. CODES FROM IKJEBEUT
         BZ    SET                     BRANCH IF ZERO TO SET
         C     RETCDREG,TESTEMP        COMPARE ERETURN CODE WITH 4
         BZ    EMPTY                   RC=4 THEN GO TO EMPTY
         B     IOERR                   RC=8 THEN GO TO IOERR
         SPACE 2
***********************************************************************
*    '*' VALUE IS SET TO KEY OF LAST RECORD AND 'THE LINE TO BE
*    VERIFIED SWITCH' IS TURNED ON
***********************************************************************
SET      MVC   CACURNUM(BM4),CATEMPBF+BM12  PUT KEY OF LAST RECORD IN
*                                      CURRENT LINE NUMBER '*'
         OI    CACFLAG1,CALNTOVF       TURN 'THE LINE TO BE VERIFIED
*                                      SWITCH' ON
         SPACE 2
***********************************************************************
*    STANDARD EXIT LINKAGE TO CONTROLLER
***********************************************************************
RETURN   SR    RETCDREG,RETCDREG       SET RETURN CODE TO 0
EXIT     EQU   *
         IKJEBERT (14,12),T,RC=(15)    RELOAD CALLER'S REGISTERS AND
*                                      RETURN CONTROL TO CALLER
         SPACE 2
***********************************************************************
*    UTILITY DATA SET IS EMPTY
***********************************************************************
EMPTY    MVC   B(BM12),INLIST
         SPACE 2
***********************************************************************
*    MESSAGE- 'NO LINES IN DATA SET' IS ISSUED HERE
***********************************************************************
ISSUE    IKJEBEML M501,MF=(E,B)
NORMAL   XC    CACURNUM(BM4),CACURNUM  SET CURRENT LINE VALUE '*' TO 0
         B     RETURN                  BRANCH UNCOND. TO RETURN
         SPACE 2
***********************************************************************
*    I/O ERROR IN READING UTILITY DATA SET
***********************************************************************
IOERR    LA    RETCDREG,IOERROR
         B     EXIT                    BRANCH UNCOND. TO EXIT TO
*                                      CONTROLLER
         SPACE 3
***********************************************************************
*    CONSTANT AREA FOR IKJEBEBO
***********************************************************************
TESTEMP  DC    F'4'                    FULL WORD OF 4
INLIST   IKJEBEML M501,MF=L
         SPACE 2
         IKJEBECA
         SPACE 2
         ORG   CASCWKA                 EDIT SUBCOMMAND WORK AREA
A        DS    5F                      5 FULLWORDS FOR IKJEBESH MACRO
B        DS    10F                     10 FULLWORDS FOR IKJEBEML MACRO
COWORD1  DS    F                       1ST PARAM WORD FOR UT
COWORD2  DS    F                       2ND PARAM WORD FOR UT
COWORD3  DS    F                       3RD PARAM WORD FOR UT
         END
