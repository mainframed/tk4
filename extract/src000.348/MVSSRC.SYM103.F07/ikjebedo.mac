BEDO     TITLE 'EDIT SUBCOMMAND - DOWN - IKJEBEDO'
IKJEBEDO CSECT
*A420500,436000-445500,476000-477000,478100-479200,482000-499600 A45119
*A702500-703500,770100-771500                                    A45119
*C420000,606000                                                  A45119
*D436000-444000,476000,482000-502000,516000-518000,522000-572000 A45119
*D626000,712000-724000,772000                                    A45119
***********************************************************************
* STATUS-  VERSION NO. 01, OS/360 RELEASE NO. 20                      *
*                                                                     *
* FUNCTION-  IKJEBEDO IS AN EDIT SUBCOMMAND WHICH IS USED TO CAUSE THE*
*    CURRENT LINE POINTER '*' TO BE MOVED TOWARD THE END OF THE DATA  *
*    SET.  IF THE DOWN SUBCOMMAND CAUSES THE CURRENT LINE POINTER '*' *
*    TO POINT TO THE LAST LINE OF THE SET, OR IF THIS SUBCOMMAND IS   *
*    ISSUED WITH THE '*' VALUE ALREADY POINTING AT THE LAST LINE OF   *
*    THE DATA SET, THE USER WILL BE NOTIFIED.  IF DURING THE PROCESS  *
*    OF READING THE UTILITY DATA SET, AN ATTENTION IS ISSUED BY THE   *
*    TERMINAL USER, CONTROL WILL BE RETURNED TO THE CONTROLLER        *
*    IMMEDIATELY WITH A RETURN CODE OF ZERO IN REGISTER 15.           *
*                                                                     *
* ENTRY POINTS-  IKJEBEDO, MAIN ENTRY POINT                           *
*                                                                     *
* INPUT-  REGISTER 1 CONTAINS A POINTER TO EDIT'S COMMUNICATION AREA. *
*    A PART OF OR THE WHOLE UTILITY DATA SET IS READ AS REQUIRED.     *
*                                                                     *
* OUTPUT-  A RETURN CODE IN REGISTER 15. POSSIBLE RETURN CODES ARE-   *
*    0 - OPERATION WAS SUCCESSFUL.                                    *
*    8 - STACK NEEDS FLUSHING                                         *
*    12 - I/O ERROR.                                                  *
*                                                                     *
* EXTERNAL REFERENCES-                                                *
*         IKJPARS      -SCANS OPERAND                                 *
*         IKJEBEUT     -READS UTILITY DATA SET                        *
*         IKJEBEMS     -MESSAGES TO USER AND LINE VERIFICATION        *
*    EDIT MACROS USED-                                                *
*         IKJEBESV     -STANDARD ENTRY LINKAGE AND ADDRESSABILITY     *
*         IKJEBERT     -STANDARD EXIT LINKAGE                         *
*         IKJEBECA     -LAYOUT AND DESCRIPTION OF COMMUNICATION AREA  *
*         IKJEBEML     -STANDARD MESSAGE TO TERMINAL USER             *
*         IKJEBEMG     -MESSAGE INSERITON                             *
*         IKJEBESH     -PROVIDES LINKAGE TO EDIT SERVICE ROUTINES     *
*    SYSTEM MACROS USED-                                              *
*         LINK         -CALLS IKJPARS                                 *
*         IKJPARM      -GENERATE PCL CSECT                            *
*         IKJIDENT     -GENERATE COUNT FIELD IN PCL                   *
*         IKJENDP      -CLOSE PCL CSECT                               *
*         IKJRLSA      -RELEASE PCL CORE                              *
*         IKJPPL       -PARSE PARAMETER LIST                          *
*         SAVE         -SAVE PARSE'S REGISTERS                        *
*         RETURN       -RETURN TO PARSE                               *
*                                                                     *
* EXITS,NORMAL-  STANDARD EXIT TO THE CONTROLLER WITH RETURN CODE = 0 *
*    IN REGISTER 15                                                   *
*                                                                     *
* EXITS,ERROR-  STANDARD EXIT TO THE CONTROLLER WITH RETURN CODE = 12 *
*    OR 8 IN REGISTER 15.                                             *
* TABLES/WORK AREAS-  THE EDIT SUBCOMMAND WORK AREA IN THE            *
*    COMMUNICATIONS AREA IS USED                                      *
*                                                                     *
* ATTRIBUTES-  REFRESHABLE, ENABLED, NON-PRIVILEGED                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY-  THIS SUBCOMMAND IS CHARACTER CODE       *
*    DEPENDENT AND HAS TO BE REASSEMBLED WHEN CHARACTER CODE CHANGES  *
*                                                                     *
* NOTES-  STANDARD LINKAGE CONVENTIONS USED                           *
***********************************************************************
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC REGISTERS
***********************************************************************
PARMREG0 EQU   0                       PARAMETER POINTER
PARMREG1 EQU   1                       PARAMETER POINTER
TESTREG  EQU   2                       WORK REGISTER
BINREG   EQU   3                       WORK REGISTER
ADDREG   EQU   4                       WORK REGISTER
DCBREG   EQU   5                       WORK REGISTER
WORKREG  EQU   6                       WORK REGISTER
PDLREG   EQU   7                       POINTER TO PDL
UTREG    EQU   8                       PARAMETERS FOR IKJEBEUT POINTER
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING
TEMPREG  EQU   10                      WORK REGISTER
BASEREG  EQU   11                      BASE REGISTER
DATAREG  EQU   12                      WORK REGISTER
SAVEREG  EQU   13                      SAVE AREA REGISTER
RETREG   EQU   14                      RETURN TO CALLER
RETCDREG EQU   15                      RETURN CODE
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC CONSTANTS
***********************************************************************
DN0      EQU   0                       CONSTANT OF 0
DN1      EQU   1                       CONSTANT OF 1
DN3      EQU   3                       CONSTANT OF 3
DN4      EQU   4                       CONSTANT OF 4
DN8      EQU   8                       CONSTANT OF 8
DN12     EQU   12                      CONSTANT OF 12
DN16     EQU   16                      CONSTANT OF 16
END      EQU   X'80'                   END OF PARAMETER LIST CODE
LAST     EQU   X'05'                   READ LAST RECORD CODE
NEXT     EQU   X'02'                   READ NEXT RECORD CODE
SW1      EQU   X'80'
ALL      EQU   X'FF'
         SPACE 2
***********************************************************************
*    MESSAGE REFERENCES
***********************************************************************
         IKJEBEMI (312,313,500,501)
         EJECT
***********************************************************************
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY BOTH
*    IN THIS ROUTINE AND IN THE COMMUNICATION AREA
***********************************************************************
         IKJEBESV (14,12),T,*          SAVE CALLER'S REGISTERS AND
*                                      ESTABLISH ADDRESSABILITY
         USING IKJEBECA,COMMREG        ADDRESSABILITY IN COMMUNICATION
*                                      AREA
         MVI   SW,DN0                  CLEAR SWITCH
         SPACE 2
***********************************************************************
*    OPERAND TEST
***********************************************************************
         TM    CAPTIBFR,CAOPERND       TEST WHETHER THERE ARE OPERANDS
         BNO   DEFAULT                 NO OPERAND, THEN GO TO DEFAULT
         SPACE 2
***********************************************************************
*    CALL IKJPARSE TO SCAN OPERAND
***********************************************************************
OPERAND  LA    PARMREG1,CATMPLST       LET REG. 1 PT. TO TMP SERVICE
*                                      RTN. PARAMETER LIST
         MVC   CATMPLST+PPLPCL-PPL(DN4),ADDNPCL  PUT POINTER TO PCL IN
*                                                PPLPCL
         LA    TESTREG,CAPTPRSD
         ST    TESTREG,CATMPLST+PPLANS-PPL  PUT POINTER TO ANS. PLACE
*                                           IN PPLANS
         MVC   CATMPLST+PPLCBUF-PPL(DN4),CAPTIBFR  PUT ADDRESS OF
*                                      INPUT BUFFER IN PPLCBUF
         ST    COMMREG,CATMPLST+PPLUWA-PPL  PUT POINTER TO COMM. AREA
*                                           IN PPLVWA
         LINK  EP=IKJPARS
         SPACE 2
***********************************************************************
*    CHECK RETURN CODES FROM PARSE SERVICE ROUTINE AND ESTABLISH
*    ADDRESSABILITY IN PDL DSECT
***********************************************************************
         L     PDLREG,CAPTPRSD         PUT PTR. TO PDL IN PDLREG
         USING DNPDL,PDLREG            ESTABLISH ADDRESSABILITY IN
*                                      PDL DSECT
         LTR   RETCDREG,RETCDREG       CHECK RET. CODE FROM PARSE
         BZ    CONV                    IF SUCCESSFUL COMPLETION THEN GO
*                                      TO CONV
         C     RETCDREG,DNDC4          TEST WHETHER R.C.=4
         BE    FLUSH                   R.C.=4 THEN GO TO FLUSH
         C     RETCDREG,DNDC8          TEST WHETHER R.C.=8
         BE    NORMAL                  R.C.=8 THEN GO TO NORMAL
         C     RETCDREG,DNDC12         TEST WHETHER R.C.=12
         BE    MSG5                    R.C.=12 THEN GO TO ISSUE MSG5
         C     RETCDREG,DNDC16         TEST WHETHER R.C.=16
         BE    MESSAGE3                R.C.=16 THEN GO TO ISSUE MSG3
         SPACE 2
***********************************************************************
*    CONVERSION OF OPERAND 'COUNT' TO BINARY
***********************************************************************
CONV     L     TESTREG,COUNT           LET TESTREG CONTAIN ADDRESS
*                                      OF OPERAND
         LH    ADDREG,COUNT+DN4        PUT LENGTH OF OPERAND IN 2
*                                      LOW ORDER BYTES OF ADDREG
         BCTR  ADDREG,PARMREG0         LENGTH OF OPERAND - 1
         EX    ADDREG,PACKED           CONVERT OPERAND FROM ZONED
*                                      DECIMAL TO PACKED DECIMAL
         CVB   BINREG,COPACKED         CONVERT OPERAND FROM PACKED
*                                      DECIMAL TO BINARY
         SPACE 2
***********************************************************************
*    CALL IKJEBEUT THROUGH IKJEBESH TO READ LAST RECORD OF UTILITY
*    DATA SET
***********************************************************************
READLAST MVC   COWORD1(DN4),CAPTCDCB   PUT ADDRESS OF UTILITY DCB IN 3
*                                      LOW ORDER BYTES OF 1ST PARAM.
*                                      WORD
         MVI   COWORD1,LAST            PUT READ LAST RECORD CODE IN
*                                      HIGH ORDER BYTE OF 1ST PARAM
*                                      WORD
         XC    COWORD2(DN4),COWORD2    NO POINTER IS PUT IN 2ND
*                                      PARAM. WORD
         LA    TESTREG,CATEMPBF+DN12   PUT ADDRESS OF BUFFER POOL IN
*                                      3RD PARAM WORD
         ST    TESTREG,COWORD3
         MVI   COWORD3,END             PUT END OF PARAMETER CODE IN
*                                      HIGH ORDER BYTE OF 3RD PARAM
*                                      WORD
         LA    UTREG,COWORD1           LET UTREG POINT TO PARMLIST
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A)
         SPACE 2
***********************************************************************
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING LAST RECORD
***********************************************************************
         LTR   RETCDREG,RETCDREG       TEST RETURN CODE FROM IKJEBEUT
*                                      AFTER READING LAST RECORD
         BZ    CONT1                   BRANCH IF ZERO TO CONT1
         C     RETCDREG,DNDC4          TEST WHETHER R.C.=4
         BE    EMPTY                   BRANCH IF EQUAL TO EMPTY
IOERR    LA    RETCDREG,DN12           OTHERWISE, SET R.C. TO 12
         SPACE 2
***********************************************************************
*    STANDARD EXIT LINKAGE TO CONTROLLER
***********************************************************************
EXIT     TM    CAPRSPDL,CAFREEDL       TEST WHETHER PARSE PDL REQUIRES
*                                      FREEMAIN
         BNZ   RET                     PDL NOT EXIST THEN RETURN TO
*                                      CONTROLLER
         IKJRLSA CAPRSPDL              RELEASE PARSE PDL CORE
         OI    CAPRSPDL,CAFREEDL       TURN ON THE BIT TO INDICATE PDL
*                                      WAS FREED
RET      EQU   *
         IKJEBERT (14,12),T,RC=(15)
         SPACE 2
***********************************************************************
*    IF NO OPERAND IS SPECIFIED IN THE SUBCOMMAND, THEN IT IS DEFAULTED
*    TO 1
***********************************************************************
DEFAULT  LA    BINREG,DN1              PUT 1 IN BINREG
         B     READLAST                BRANCH TO READ LAST RECORD OF
*                                      DATA SET
         SPACE 2
***********************************************************************
*    DATA SET IS EMPTY
***********************************************************************
EMPTY    EQU   *                                                 A45119
         XC    B(DN12),B               ZERO PARAMETER LIST AREA  A45119
         IKJEBEML M501,MF=(E,B)        ISSUE MESSAGE1- 'NO LINES IN
*                                      DATA SET'
         XC    CACURNUM(DN4),CACURNUM  SET CURRENT LINE NUMBER TO ZERO
NORMAL   SR    RETCDREG,RETCDREG       SET RETURN CODE TO ZERO
         B     EXIT                    STANDARD EXIT LINKAGE TO
*                                      CONTROLLER
FLUSH    LA    RETCDREG,DN8            INDICATE STACK NEEDS FLUSHING
         B     EXIT                    BRANCH TO MAKE EXIT
         SPACE 2
**************************************************************** A45119
*                                                                A45119
*    DATA SET IS NOT EMPTY.                                      A45119
*                                                                A45119
*    CHECK IF CURRENT LINE (*) IS LAST LINE IN DATA SET.         A45119
*    IF SO, INFORM USER THAT END OF DATA HAS BEEN REACHED AND    A45119
*    TERMINATE SUBCOMMAND.  IF NOT, SAVE THE KEY OF THE LAST     A45119
*    LINE AND READ RECORD FOLLOWING THE CURRENT LINE.            A45119
*                                                                A45119
**************************************************************** A45119
         SPACE 2
CONT1    EQU   *                                                 A45119
         L     TESTREG,CATEMPBF+DN12   OBTAIN KEY OF LAST LINE   A45119
         C     TESTREG,CACURNUM        CHECK IF '*' IS LAST LINE A45119
         BE    DO000020                IF SO, BR TO INFORM USER  A45119
         ST    TESTREG,DOLASTLN        IF NOT, SAVE KEY OF LAST  A45119
*                                      LINE IN DATA SET          A45119
         L     TESTREG,CACURNUM        OBTAIN CURRENT LINE KEY   A45119
         ST    TESTREG,COKEYA          SPECIFY AS KEY FOR        A45119
*                                      IKJEBEUT                  A45119
*                                      IS LOCATED IN SUBCMD. WORKAREA
         LR    WORKREG,BINREG          PUT OPERAND 'COUNT' IN BINREG
READ     MVI   COWORD1,NEXT            PUT READ NEXT RECORD CODE IN
*                                      HIGH ORDER BYTE OF 1ST
*                                      PARAM WORD
         LA    TESTREG,COKEYA          LET 2ND PARAM. WORD POINT TO
         ST    TESTREG,COWORD2         COKEYA
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A)
         SPACE 2
***********************************************************************
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING NEXT RECORD
***********************************************************************
         LTR   RETCDREG,RETCDREG       TEST RET. CODE FROM IKJEBEUT
         BZ    CONT2                   BRANCH IF ZERO TO CONT2
         C     RETCDREG,DNDC4          TEST WHETHER R.C.=4
         BE    DO000010                IF END-OF-DATA, BRANCH TO A45119
*                                      ISSUE APPROPRIATE MESSAGE A45119
         B     IOERR                   R,C,=8 THEN GO TO IOERR
CONT2    EQU   *                                                 A45119
         BCT   WORKREG,LOOP            DECREMENT COUNT BY ONE    A45119
*                                      AND BR IF RESULT GT ZERO  A45119
         L     TESTREG,DOLASTLN        OBTAIN KEY OF LAST LINE   A45119
         C     TESTREG,CATEMPBF+DN12   IS LINE JUST READ THE     A45119
*                                      LAST LINE                 A45119
         BE    DO000040                IF SO, BRANCH TO INFORM   A45119
*                                      USER LAST LINE REACHED    A45119
         OI    CACFLAG1,CALNTOVF       OTHERWISE, INDICATE A     A45119
*                                      LINE TO VERIFY            A45119
         B     SET                     BR TO SET * VALUE AND     A45119
*                                      RETURN CONTROL            A45119
         SPACE 2
**************************************************************** A45119
*                                                                A45119
*    END-OF-DATA HAS BEEN REACHED.  ISSUE IKJ52500I IN ITS       A45119
*    APPROPRIATE FORM.                                           A45119
*                                                                A45119
**************************************************************** A45119
         SPACE 2
DO000010 EQU   *                       ENTER HERE WHEN EOD       A45119
*                                      REACHED ON READ NEXT      A45119
*                                      OPERATION.                A45119
         TM    CACFLAG1,CAVRFYSW       CHECK IF VERIFY IN EFFECT A45119
         BO    DO000030                IF SO, ISSUE MESSAGE WITH A45119
*                                      TWO INSERTIONS            A45119
DO000020 EQU   *                       ENTER HERE TO PROCESS     A45119
*                                      'TERMINATION' INSERTION   A45119
*                                      ONLY FOR IKJ525001.       A45119
         LA    TESTREG,DOEODTRM        OBTAIN ADDRESS OF         A45119
*                                      INSERTION LIST            A45119
         B     DO000070                BR TO SEND MSG TO USER    A45119
DO000030 EQU   *                       PROCESS TWO INSERTIONS -  A45119
         MVC   DOMSINS1(DOEODTLN),DOEODTRM MOVE FIRST INSERTION  A45119
*                                      TO WORK AREA              A45119
         LA    TESTREG,DOEODVFY        OBTAIN ADDR OF INSERT 2   A45119
         ST    TESTREG,DOMSINS1        CHAIN INSERTIONS          A45119
         LA    TESTREG,DOMSINS1        OBTAIN ADDRESS OF         A45119
*                                      CHAIN OF INSERTIONS       A45119
         B     DO000060                BRANCH TO SEND MESSAGE    A45119
DO000040 EQU   *                       ENTER HERE IF LINE TO     A45119
*                                      BE DESIGNATED AS CURRENT  A45119
*                                      IS THE LAST LINE.         A45119
         TM    CACFLAG1,CAVRFYSW       CHECK IF VERIFY IN EFFECT A45119
         BO    DO000050                IF SO, BRANCH TO PROCESS  A45119
*                                      REQUIRED INSERTION.       A45119
         SR    TESTREG,TESTREG         SPECIFY NO INSERTIONS.    A45119
         B     DO000070                BRANCH TO SEND MESSAGE.   A45119
DO000050 EQU   *                       INCLUDE 'VERIFY' INSERT   A45119
*                                      IN MSG IKJ52500I.         A45119
         LA    TESTREG,DOEODVFY        OBTAIN ADDRESS OF         A45119
*                                      INSERTION LIST            A45119
DO000060 EQU   *                                                 A45119
         OI    CACFLAG1,CALNTOVF       INDICATE LINE TO BE       A45119
*                                      VERIFIED                  A45119
DO000070 EQU   *                       ISSUE MSG IKJ52500I.      A45119
         XC    B(DN12),B               ZERO IKJEBEMS PARM LIST   A45119
         IKJEBEML M500,,(TESTREG),,MF=(E,B) INVOKE IKJEBEMS      A45119
         SPACE 2
***********************************************************************
*    '*' VALUE IS SET TO LAST LINE READ
***********************************************************************
SET      MVC   CACURNUM(DN4),CATEMPBF+DN12  SET '*' VALUE TO KEY OF
*                                      LAST LINE READ
         SPACE 2
         B     NORMAL                  BRANCH TO NORMAL RETURN TO
*                                      CONTROLLER
LOOP     TM    CAATTN,CAATTNIS         TEST WHETHER ATTN IS ISSUED BY
*                                      TERMINAL USER
         BO    NORMAL                  IF ATTN IS ISSUED THEN BRANCH
*                                      TO SET R.C. TO ZERO AND RET.
*                                      TO CONTROLLER
NO       MVC   COKEYA(DN4),CATEMPBF+DN12  IF ATTN IS NOT ISSUED THEN
*                                      SET VALUE OF COKEYA TO KEY OF
*                                      LAST LINE READ
         B     READ                    BRANCH TO READ THE NEXT RECORD
         SPACE 2
***********************************************************************
*    MESSAGE3- 'NOT ENOUGH MAIN STORAGE TO EXECUTE DOWN' IS
*    ISSUED HERE
***********************************************************************
MESSAGE3 EQU   *                                                 A45119
         IKJEBEML M312,,SUBCMD,,MF=(E,B)
         B     FLUSH                   BRANCH TO SET R.C. TO 8 AND
*                                      RETURN TO CONTROLLER
MSG5     EQU   *
         SPACE 2
***********************************************************************
*    MSG5- 'COMMAND SYSTEM ERROR+  IKJPARS ERROR CODE 12' IS
*    ISSUED HERE
***********************************************************************
         IKJEBEML M313,M3131,,INS12,MF=(E,B)
         B     FLUSH                   BRANCH TO SET R.C. TO 8 AND
*                                      RETURN TO CONTROLLER
         SPACE 2
***********************************************************************
*    VALIDITY CHECKING ROUTINE FOR DOWN OPERAND
***********************************************************************
DVALID   SAVE  (14,12),T,*             SAVE PARSE'S REGISTERS
         BALR  TEMPREG,DN0             SET UP ADDRESSABILITY
         USING *,TEMPREG               ADDRESSABILITY IN VALIDITY
*                                      CHECKING ROUTINE
         L     TESTREG,DN0(PARMREG1)   PUT POINTER TO COUNT IN TESTREG
         L     BINREG,DN0(TESTREG)     PUT COUNT IN BINREG
         LH    UTREG,DN4(TESTREG)      PUT LENGTH OF COUNT IN 2 LOW
*                                      ORDER BYTES OF UTREG
         BCTR  UTREG,PARMREG0          LENGTH OF COUNT - 1
         EX    UTREG,COMPARE           TEST WHETHER COUNT=0
         BE    EXIT1                   EQUAL TO ZERO THEN GO TO EXIT1
         SR    RETCDREG,RETCDREG       PUT R.C.=0 IN REG. 15
EXIT2    RETURN (14,12),T,RC=(15)      RETURN TO PARSE
EXIT1    LA    RETCDREG,DN4            PUT R.C.=4 IN REG. 15
         B     EXIT2                   BRANCH TO RET. TO PARSE
         SPACE 3
***********************************************************************
*    CONSTANT AREA FOR IKJEBEDO
***********************************************************************
DNDC4    DC    F'4'                    FULL WORD OF 4
DNDC8    DC    F'8'                    FULL WORD OF 8
DNDC12   DC    F'12'                   FULL WORD OF 12
DNDC16   DC    F'16'                   FULL WORD OF 16
ADDNPCL  DC    A(IKJEBDO0)             ADCON OF PCL
ZERO     DC    8C'0'                   8 BYTES OF EBCDIC ZERO
PACKED   PACK  COPACKED(DN8),DN0(DN0,TESTREG)
COMPARE  CLC   DN0(DN0,BINREG),ZERO
         SPACE 2
***********************************************************************
*    MESSAGE INSERTION MACRO - IKJEBEMG IS ISSUED HERE
***********************************************************************
DOEODTRM IKJEBEMG ,M500IN1,'DOWN TERMINATED AT '                 A45119
DOEODTLN EQU   *-DOEODTRM              LENGTH OF INSERT LIST     A45119
DOEODVFY IKJEBEMG ,M500IN2,', LAST LINE IS:'                     A45119
SUBCMD   IKJEBEMG ,M312IN1,'DOWN'
INS12    IKJEBEMG CODE12,M3131IN1,'IKJPARS'
CODE12   IKJEBEMG 0,M3131IN2,'12'
         SPACE 2
         SPACE 2
***********************************************************************
*    MACROS OF PARSE
***********************************************************************
IKJEBDO0 IKJPARM DSECT=DNPDL
COUNT    IKJIDENT 'DOWN OPERAND',MAXLNTH=8,FIRST=NUMERIC,OTHER=NUMERIC,*
               PROMPT='DOWN OPERAND',VALIDCK=DVALID
         IKJENDP
         SPACE 2
         IKJPPL
         SPACE 2
         IKJEBECA
         ORG   CASCWKA                 EDIT SUBCOMMAND WORK AREA
COPACKED DS    D                       DOUBLE WORD FOR PACKED
*                                      DECIMAL OF 'COUNT'
COWORD1  DS    F                       1ST PARAM WORD FOR UT
COWORD2  DS    F                       2ND PARAM WORD FOR UT
COWORD3  DS    F                       3RD PARAM WORD FOR UT
COKEYA   DS    F                       CURRENT REFERNCED VALUE FOR UT
A        DS    5F                      5 FULLWORDS FOR IKJEBESH MACRO
B        DS    10F                     10 FULL WORDS FOR IKJEBEML MACRO
SW       DS    X                       SWITCH FOR DET. WHETHER ONE
*                                      MORE LINE SHOULD BE READ  A45119
DOLASTLN DS    F                       SAVE AREA FOR KEY OF LAST A45119
*                                      LINE IN UTILITY DATA SET  A45119
DOMSINS1 DS    0F                      DYNAMIC INSERTION AREA    A45119
         DS    XL(DOEODTLN)            LENGTH RESERVED IS FOR    A45119
*                                      INSERTION 1, IKJ52500I    A45119
         END
