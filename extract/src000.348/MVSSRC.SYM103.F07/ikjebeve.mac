 TITLE 'TSO ''EDIT'' COMMAND - ''VERIFY'' SUBCOMMAND - IKJEBEVE'
IKJEBEVE CSECT
***********************************************************************
*                                                                     *
* STATUS -- VERSION NO. 01, OS/360 RELEASE NO. 20                     *
*                                                                     *
* FUNCTION -- THIS ROUTINE IS THE VERIFY SUBCOMMAND WHICH RESETS THE  *
*    VERIFY SWITCH IN THE COMMUNICATION AREA.                         *
*                                                                     *
*    VERIFY     - VERIFY SWITCH(CAVRFYSW) IN EDIT COMM AREA TURNED ON *
*    VERIFY ON  -   ''     ''      ''     ''  ''   ''   ''    ''   ON *
*    VERIFY OFF -   ''     ''      ''     ''  ''   ''   ''    ''   OFF*
*                                                                     *
* ENTRY POINTS -- IKJEBEVE, MAIN ENTRY POINT                          *
*                                                                     *
* INPUT -- REGISTER 1 POINTS TO THE COMMUNICATION AREA                *
*                                                                     *
* OUTPUT -- TWO RETURN CODES ARE POSSIBLE IN REGISTER 15              *
*    0 -- SUCCESSFUL COMPLETION                                       *
*    8 -- UNSUCCESSFUL COMPLETION                                     *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         IKJPARS   - CHECKS SYNTAX OF THE SUBCOMMAND                 *
*         IKJEBEMS  - SEND MESSAGES TO THE USER                       *
*         MACROS USED                                                 *
*         LINK     - PASSES CONTROL TO IKJPARS                        *
*         IKJPARM  - ) CONSTRUCTS                                     *
*         IKJKEYWD - )  PARAMETER CONTROL &                           *
*         IKJNAME  - )   PARAMETER DESCRIPTOR                         *
*         IKJENDP  - )    LISTS (PCL,PDL)                             *
*         IKJRLSA  - FREES STORAGE FOR PDL                            *
*         IKJEBECA - DEFINES DUMMY SECTION FOR EDIT COMM AREA         *
*         IKJEBESV - GENERATES ENTRY LINKAGE ) CHAINED                *
*         IKJEBERT - GENERATES EXIT LINKAGE  )  SAVE AREAS            *
*         IKJEBEMG - GENERATES A FORMATTED MESSAGE INSERTION          *
*         IKJEBEML - GENERATES MESSAGE SELECTION PARM LIST & CALLS    *
*                     IKJEBEMS THRU IKJEBESR                          *
*                                                                     *
* EXITS, NORMAL -- TO IKJEBEMA WITH RETURN CODE 0 IN REGISTER 15.     *
*                                                                     *
* EXITS, ERROR -- NONE                                                *
*                                                                     *
* TABLES/WORK AREAS -- IKJEBECA, EDIT COMMUNICATION AREA              *
*                                                                     *
* ATTRIBUTES -- REFRESHABLE, ENABLED, NON-PRIVILEGED                  *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- NONE                                   *
*                                                                     *
* NOTES -- STANDARD CONVENTIONS                                       *
*                                                                     *
***********************************************************************
 EJECT
***********************************************************************
*                             ENTRY CODE                              *
***********************************************************************
 SPACE
         IKJEBESV (14,12),,*
 SPACE
***********************************************************************
*                             PROCESSING                              *
***********************************************************************
         USING IKJEBECA,COMMREG         ESTABLISH ADDRESSABILITY
 SPACE
***********************************************************************
* IF SUBCOMMAND HAS OPERAND, CALL PARSE.  OTHERWISE BRANCH TO TURN ON *
* VERIFY SWITCH.                                                      *
***********************************************************************
 SPACE
         TM    CAPTIBFR,CAOPERND        IS THERE AN OPERAND
         BZ    SETON                    NO,BRANCH TO SET SW ON
         MVC   CATMPLST+PPLPCL-PPL(WORD),VERFYPCL YES,SET PT PCL
         LA    WORKREG,CAPTPRSD
         ST    WORKREG,CATMPLST+PPLANS-PPL SET PT ANSWER PLACE
         MVC   CATMPLST+PPLCBUF-PPL(WORD),CAPTIBFR SET PT INPUT BFR
PARSE    EQU   *
         LA    PARMREG1,CATMPLST        SET PT TO PARM LIST
         LINK  EP=IKJPARS
         B     PARSEBT(RETCDREG)        BRANCH ACCORDING TO RET CODE
PARSEBT  EQU   *        **********RETURN CODE************ACTION********
         B     SUCCESS   00 (SUCCESSFUL COMPLETION) - CONTINUE
         B     BADRETCD  04 (UNABLE TO PROMPT     ) - EXIT
         B     SETRETCD  08 (ATTENTION INTERRUPT  ) - EXIT
         B     LOGICERR  12 (INVALID PARAMETERS   ) - ISSUE MSG
         B     NOROOM    16 (NO SPACE AVAILABLE   ) - ISSUE MSG
 SPACE
***********************************************************************
* IF 'ON' WAS SPECIFIED TURN ON THE VERIFY SWITCH.  OTHERWISE TURN    *
* IT OFF.                                                             *
***********************************************************************
 SPACE
SUCCESS  EQU   *
         L     PDLBASE,CAPTPRSD         SET PDL ADDRESSABILITY
         USING VERFYPDL,PDLBASE         ESTABLISH ADDRESSABILITY
         CLI   OPERAND+ONE,ON           WAS ON SPECIFIED
         BE    SETON                    YES,BRANCH TO TURN ON VERIFY SW
         NI    CACFLAG1,VEALL-CAVRFYSW  NO, TURN OFF VERIFY SWITCH
         B     RELEASE                  BRANCH TO FREE PARSE CORE
SETON    EQU   *
         OI    CACFLAG1,CAVRFYSW        TURN ON VERIFY SWITCH
 SPACE
***********************************************************************
* IF PARSE WAS CALLED FREE THE CORE USED FOR THE PDL                  *
***********************************************************************
         TM    CAPRSPDL,CAFREEDL        WAS PARSE CALLED
         BNZ   SETRETCD                 BR NO
 SPACE
RELEASE  EQU   *                        YES,RELEASE CORE
         IKJRLSA CAPTPRSD               FREE PDL CORE
         OI    CAPRSPDL,CAFREEDL        SET PDL SWITCH ON
 SPACE
***********************************************************************
*                             EXIT CODE                               *
***********************************************************************
 SPACE
SETRETCD EQU   *
         SR    RETCDREG,RETCDREG        SET RETURN CODE TO ZERO
RETRN    EQU   *
         IKJEBERT (14,12),RC=(15)
 SPACE
***********************************************************************
* THE FOLLOWING CODE IS BRANCHED-TO FROM THE PARSE BRANCH TABLE TO    *
* ISSUE MESSAGES OR TO RELEASE CORE.                                  *
***********************************************************************
 SPACE
LOGICERR MVC   CASCWKA(MSGLISTL),LLOGICER SET PARM LIST
         B     ISSUEMSG                 BRANCH TO ISSUE MSG
NOROOM   MVC   CASCWKA(MSGLISTL),LNOROOM  SET PARM LIST
ISSUEMSG EQU   *
@@@@@@@@ IKJEBEML ,,,,MF=(E,CASCWKA)    CALL IKJEBEMS THRU IKJEBESR
         SPACE
BADRETCD EQU   *
         LA    RETCDREG,EIGHT           SET RETURN CODE TO 8
         B     RETRN                    BR TO EXIT LINKAGE
 EJECT
***********************************************************************
*                             CONSTANTS                               *
***********************************************************************
 SPACE
VERFYPCL DC    V(IKJEBVE0)              ADDR OF PCL
 SPACE
* MESSAGE SELECTION LIST-FORM MACROS -
 SPACE
VERIFY   IKJEBEMG  0,M312IN1,'VERIFY'
CODE     IKJEBEMG 0,M3131IN2,'12'
PARSEX   IKJEBEMG  CODE,M3131IN1,'PARSE'
LLOGICER IKJEBEML  M313,M3131,0,PARSEX,MF=L
LNOROOM  IKJEBEML  M312,0,VERIFY,0,MF=L
* MESSAGE MANIPULATION EQUATES
MSGLISTL EQU   *-LNOROOM
DWD      EQU   8                        DOUBLE WORD LENGTH
 EJECT
***********************************************************************
* MACROS TO GENERATE PARAMETER CONTROL LIST AND PROVIDE SYMBOLIC      *
* REFERENCE TO PARAMETER DESCRIPTOR LIST.                             *
***********************************************************************
 SPACE
IKJEBVE0 IKJPARM DSECT=VERFYPDL
OPERAND  IKJKEYWD DEFAULT='ON'
         IKJNAME 'ON'
         IKJNAME 'OFF'
         IKJENDP
 EJECT
         IKJEBECA
 EJECT
         IKJPPL
IKJEBEVE CSECT                          RESUME PROGRAM CSECT
***********************************************************************
*                             EQUATES                                 *
***********************************************************************
 SPACE
* REGISTERS -
 SPACE
PARMREG0 EQU   0        NOT USED
PARMREG1 EQU   1        PARAMETER REGISTER
WORKREG  EQU   3        WORK REGISTER
PDLBASE  EQU   4        BASE REGISTER FOR PDL
COMMREG  EQU   9        BASE REGISTER FOR EDIT COMMUNICATION AREA
BASEREG  EQU   11       GENERAL BASE REGISTER
DATAREG  EQU   12       NOT USED
SAVEREG  EQU   13       NOT USED
RETREG   EQU   14       NOT USED
RETCDREG EQU   15       RETURN CODE REGISTER
 SPACE
* CONSTANTS -
WORD     EQU   4        FOUR BYTES
ONE      EQU   1        CONSTANT OF ONE
ON       EQU   X'01'    BIT MASK
VEALL    EQU   X'FF'    BIT MASK
EIGHT    EQU   8        CONSTANT OF 8
 SPACE
* MESSAGE NUMBERS -
 SPACE
         IKJEBEMI (312,313)
 SPACE
         END
