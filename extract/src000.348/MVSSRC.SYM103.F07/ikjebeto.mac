BETO     TITLE 'EDIT SUBCOMMAND - TOP - IKJEBETO'
***********************************************************************
* STATUS-  VERSION NO. 01, OS/360 RELEASE NO. 20                      *
*                                                                     *
* FUNCTION-  IKJEBETO IS AN EDIT SUBCOMMAND WHICH IS USED TO MOVE THE *
*    POSITION OF THE CURRENT LINE POINTER '*' TO THE VERY BEGINNING OF*
*    THE DATA SET.  THAT IS, '*' TAKES ON A POSITION BEFORE THE FIRST *
*    RECORD OF A NON-NUMBERED DATA SET AND TAKES ON THE VALUE OF LINE *
*    NUMBER ZERO FOR A LINE-NUMBERED DATA SET.                        *
*                                                                     *
* ENTRY POINTS-  IKJEBETO IS THE MAIN AND ONLY ENTRY POINT            *
*                                                                     *
* INPUT-  REGISTER 1 CONTAINS A POINTER TO EDIT'S COMMUNICATION AREA. *
*    THE FIRST RECORD OF THE UTILITY DATA SET IS READ.                *
*                                                                     *
* OUTPUT-  A RETURN CODE IN REGISTER 15. POSSIBLE RETURN CODES ARE-   *
*    0 - OPERATION WAS SUCCESSFUL                                     *
*    12 - I/O ERROR                                                   *
*                                                                     *
* EXTERNAL REFERENCES-                                                *
*         IKJEBEMS     -ISSUES APPROPRIATE MESSAGES TO TERMINAL USER  *
*         IKJEBEUT     -READS UTILITY DATA SET                        *
*    MACROS USED-                                                     *
*         IKJEBESH     -PROVIDES LINKAGE FOR EDIT SERVICE ROUTINES    *
*         IKJEBECA     -LAYOUT AND DESCRIPTION OF COMMUNICATION AREA  *
*         IKJEBESV     -STANDARD ENTRY LINKAGE AND ADDRESSABILITY     *
*         IKJEBERT     -STANDARD EXIT LINKAGE                         *
*         IKJEBEML     -STANDARD MESSAGE TO TERMINAL USER             *
*                                                                     *
* EXITS,NORMAL-  STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 0 IN  *
*    REGISTER 15                                                      *
*                                                                     *
* EXITS,ERROR-  STANDARD EXIT TO CONTROLLER WITH RETURN CODE = 12 IN  *
*    REGISTER 15                                                      *
*                                                                     *
* TABLES/WORK AREAS-  THE EDIT SUBCOMMAND WORK AREA IN THE EDIT       *
*    COMMUNICATION AREA IS USED AND THE FIRST RECORD OF THE UTILITY   *
*    DATA SET IS READ INTO THE TEMPORARY BUFFER POOL IN THE COMMUNICA-*
*    TION AREA STARTING AT LOCATION CATEMPBF+12.                      *
*                                                                     *
* ATTRIBUTES-  REFRESHABLE, ENABLED, NON-PRIVILEGED.                  *
*                                                                     *
* CHARACTER CODE DEPENDENCY-  THIS SUBCOMMAND IS CHARACTER CODE       *
*    DEPENDENT AND HAS TO BE REASSEMBLED WHEN CHARACTER CODE CHANGES  *
*                                                                     *
* NOTES-  STANDARD LINKAGE CONVENTIONS USED                           *
***********************************************************************
         EJECT
IKJEBETO CSECT
*
*A512500-513500                                                 YA00043
*C320000                                                        YA00043
*D516000-612000,652000-680000,776000-780000,788000-792000       YA00043
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC REGISTERS
***********************************************************************
PARMREG0 EQU   0                       PARAMETER POINTER
PARMREG1 EQU   1                       PARAMETER POINTER
TESTREG  EQU   2                       WORK REGISTER
BINREG   EQU   3                       WORK REGISTER
WORKREG  EQU   5                       WORK REGISTER
UTREG    EQU   8                       WORK REGISTER
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING
BASEREG  EQU   11                      BASE REGISTER
DATAREG  EQU   12                      WORK REGISTER
SAVEREG  EQU   13                      SAVE AREA POINTER
RETREG   EQU   14                      RETURN TO CALLER
RETCDREG EQU   15                      RETURN CODE
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC CONSTANTS
***********************************************************************
TM0      EQU   0                       CONSTANT OF ZERO
TM3      EQU   3                       CONSTANT OF 3
TM4      EQU   4                       CONSTANT OF 4
TM8      EQU   8                       CONSTANT OF 8
TM12     EQU   12                      CONSTANT OF 12
FIRST    EQU   X'04'                   READ 1ST RECORD CODE
END      EQU   X'80'                   END OF PARMLIST CODE
IOERROR  EQU   12                      IOERROR RET. CODE
         SPACE 2
***********************************************************************
*    MESSAGE REFERENCES
***********************************************************************
         IKJEBEMI (501)                                         YA00043
         SPACE 2
***********************************************************************
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY IN
*    THIS ROUTINE AND IN THE COMMUNICATION AREA
***********************************************************************
         IKJEBESV (14,12),T,*          SAVE CALLER'S REGS. AND
*                                      ESTABLISH ADDRESSABILITY
         USING IKJEBECA,COMMREG        ADDRESSABILITY IN COMM. AREA
         SPACE 2
***********************************************************************
*    CALL IKJEBEUT THROUGH IKJEBESH TO READ FIRST RECORD OF UTILITY
*    DATA SET
***********************************************************************
         MVC   COWORD1(TM4),CAPTCDCB   PUT ADDRESS OF CURRENT UTILITY
*                                      DCB IN THE 3 LOW ORDER BYTES OF
*                                      1ST PARAM. WORD
         MVI   COWORD1,FIRST           PUT X'04' IN THE HIGH ORDER
*                                      BYTE OF 1ST PARAM. WORD
         XC    COWORD2(TM4),COWORD2    2ND PARAM. WORD DOES NOT
*                                      CONTAIN ANY POINTER
         LA    TESTREG,CATEMPBF+TM12
         ST    TESTREG,COWORD3         PUT POINTER TO TEMP. BUFFER
*                                      POOL IN COMM. AREA WHERE THE
*                                      RECORD IS TO BE PLACED IN 3
*                                      LOW ORDER BYTES OF 3RD PARAM.
*                                      WORD
         MVI   COWORD3,END             PUT END OF PARAMETER LIST
*                                      CODE IN HIGH ORDER BYTE OF
*                                      3RD PARAM. WORD
         LA    UTREG,COWORD1           LET UTREG PT.TO PARMLIST
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A)
*                                  SERVICE RTN. HANDLER MACRO
         SPACE 2
***********************************************************************
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING FIRST RECORD
***********************************************************************
         LTR   RETCDREG,RETCDREG       TEST RET. CODES FROM IKJEBEUT
         BZ    SET                     BRANCH IF ZERO TO SET
         C     RETCDREG,TESTEMP        COMPARE RETURN CODE WITH 4
         BZ    EMPTY                   RC=4 THEN GO TO EMPTY
         B     IOERR                   RC=8 THEN GO TO IOERR
         SPACE 2
***********************************************************************
*    '*' VALUE IS SET TO ZERO
***********************************************************************
SET      XC    CACURNUM(TM4),CACURNUM  SET CURRENT LINE VALUE '*' TO 0
         SR    BINREG,BINREG
         C     BINREG,CATEMPBF+TM12    TEST WHETHER THERE IS RECORD 0
         BNE   RETURN                  NO, VERIFICATION         YA00043
*                                      UNNECESSARY              YA00043
         OI    CACFLAG1,CALNTOVF       YES, SPECIFY LINE TO BE  YA00043
*                                      VERIFIED                 YA00043
         SPACE 2
***********************************************************************
*    STANDARD EXIT LINKAGE TO CONTROLLER
***********************************************************************
RETURN   SR    RETCDREG,RETCDREG       SET RETURN CODE TO 0
EXIT     EQU   *
         IKJEBERT (14,12),T,RC=(15)    RELOAD CALLER'S REGISTERS AND
*                                      RETURN CONTROL TO CALLER
         SPACE 2
***********************************************************************
*    UTILITY DATA SET IS EMPTY
***********************************************************************
EMPTY    MVC   B(TM12),INLIST
         SPACE 2
***********************************************************************
*    MESSAGE- 'NO LINES IN DATA SET' IS ISSUED HERE
***********************************************************************
ISSUE    IKJEBEML M501,MF=(E,B)
NORMAL   XC    CACURNUM(TM4),CACURNUM  SET CURRENT LINE VALUE '*' TO 0
         B     RETURN                  BRANCH UNCOND. TO RETURN
         SPACE 2
***********************************************************************
*    I/O ERROR IN READING UTILITY DATA SET
***********************************************************************
IOERR    LA    RETCDREG,IOERROR        SET RET. CODE TO 12
         B     EXIT                    BRANCH UNCOND. TO EXIT TO
*                                      CONTROLLER
         SPACE 3
***********************************************************************
*    CONSTANT AREA FOR IKJEBETO
***********************************************************************
TESTEMP  DC    F'4'                    FULL WORD OF 4
INLIST   IKJEBEML M501,MF=L
         SPACE 2
         IKJEBECA
         SPACE 2
         ORG   CASCWKA                 EDIT SUBCOMMAND WORK AREA
A        DS    5F                      5 FULLWORDS FOR IKJEBESH MACRO
B        DS    10F                     10 FULLWORDS FOR IKJEBEML MACRO
COWORD1  DS    F                       1ST PARAM WORD FOR UT
COWORD2  DS    F                       2ND PARAM WORD FOR UT
COWORD3  DS    F                       3RD PARAM WORD FOR UT
         END
