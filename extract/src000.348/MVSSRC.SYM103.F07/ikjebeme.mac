         TITLE 'TSO EDIT COMMAND  MERGE SUBCOMMAND  IKJEBEME'
IKJEBEME CSECT
*A159100,159260-159292,160400,312700-313100,314700-316800,317700 A42948
*A317900-317980,318900-320999,525100-525492,707200-707220        A42948
*C314200,317000,317800,321000                                    A42948
*D313200,318000-318200,368000                                    A42948
*A159560,159800-159820,339500-341300,450691-450693,451657-451659 A45104
*A486600-486900,487200-487800,525600-526100,526332-527521,707050 A45104
*A707120-707160,739500-739800,745500-745800,783600-783800        A45104
*C450690-450698,707000                                           A45104
*D340000-341200,486600,487200,526000-527000                      A45104
*A486500-486520                                                  A50953
*C486000                                                         A50953
*A041700,159296-159297,159860,420024,420090-420367               A50476
*A663600-663900,747500-747600,782420                             A50476
*C041600,073600,102600,691600,782400                             A50476
*D146500,147200,691900-692000,764000,782500-783200,803100-803200 A50476
*D803400,803800                                                  A50476
*A361500                                                        YA00039
*A491746-491750,491790                                          YA00044
*A262500-262700,420024-420026,420186-420210                      Y01676
*C420090                                                         Y01676
*D420024,420110-420210,663600-663900,747500-782400,803020-803600 Y01676
*A364020-364500,C362800                                          Y02676
***********************************************************************
*                                                                     *
* STATUS VERSION NO. 01, OS/360 RELEASE NO. 20                        *
*                                                                     *
* FUNCTION -- IKJEBEME IS THE EDIT MERGE SUBCOMMAND. IT WILL EITHER   *
*    A) COPY ALL OR PART OF THE EDIT DATA SET INTO ITSSELF AT SOME    *
*    SPECIFIED LOCATION OR B) MERGE ALL OR PART OF ANOTHER DATA SET   *
*    INTO THE ONE BEING EDITED.                                       *
*                                                                     *
*    IKJPARS SCANS THE OPERANDS AND PROMPTS FOR VALID SYNTAX. IKJEBEDA*
*    IS CALLED TO ALLOCATE A TEMPORARY QSAM DATA SET TO WHICH THE EDIT*
*    UTILITY DATA SET IS COPIED BY IKJEBEFC. A MODEL OF THE SYSTEM    *
*    MERGE COMMAND IS BUILT AND PASSED TO IKJEBECI WHICH THEN INVOKES *
*    SYSTEM MERGE. IF SUCCESSFUL, IKJEBECO IS CALLED TO CREATE ANOTHER*
*    UTILITY DATA SET AND COPY THE MERGED DATA SET TO IT. IF THE DATA *
*    SET TYPE IS ITF, IKJEBEMR IS CALLED TO RE-TRANSLATE THE INTER-   *
*    MEDIATE DATA SET. IKJEBEEX IS GIVEN CONTROL TO DELETE THE OLD    *
*    UTILITY DATA SET. CONTROL IS RETURNED TO IKJEBEMA WITH THE RETURN*
*    CODE IN REGISTER 15.                                             *
*                                                                     *
* ENTRY POINTS -- IKJEBEME, MAIN ENTRY POINT                          *
*                                                                     *
* INPUT -- REGISTER ONE POINTS TO THE COMMUNICATION AREA              *
*                                                                     *
* OUTPUT -- FOUR RETURN CODES ARE POSSIBLE                            *
*                                                                     *
*    0 - SUCCESSFUL COMPLETION                                        *
*                                                                     *
*    8 - UNSUCCESSFUL COMPLETION                                      *
*                                                                     *
*    12 - I/O ERROR IN THE UTILITY DATA SET                           *
*                                                                     *
*    16 - SYNTAX CHECKER NOT OPERATIONAL                              *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*                                                                     *
*         IKJPARS  TMP SERVICE ROUTINE,SCANS OPERANDS AND PROMPTS FOR *
*         SYNTAX VALIDITY                                             *
*                                                                     *
*         IKJPUTL  I/O SERVICE ROUTINE, PUTS MODE MESSAGE TO     A50476
*                  THE TERMINAL                                  A50476
*                                                                     *
*         IKJEBEDA EDIT SERVICE ROUTINE, ALLOCATES QSAM DATA SET AND  *
*         MARKS DSE ENTRY                                             *
*                                                                     *
*         IKJEBEFC EDIT FINAL COPY ROUTINE, COPIES EDIT UTILITY DATA  *
*         SET TO QSAM DATA SET                                        *
*                                                                     *
*         IKJEBECI EDIT COMMAND INVOKER, INVOKES SYSTEM MERGE         *
*                                                                     *
*         IKJEBECO EDIT INITIAL COPY ROUTINE, CREATES UTILITY DATA    *
*         SET AND COPIES MERGED DATA SET TO IT                        *
*                                                                     *
*         IKJEBEEX EDIT FINAL PROCESSING ROUTINE, UNALLOCATES THE     *
*         EDIT UTILITY DATA SET                                       *
*                                                                     *
*         IKJEBEMS EDIT MESSAGE SELECTION ROUTINE, CAUSES TERMINAL    *
*         MESSAGES TO BE PRINTED                                      *
*                                                                     *
*         IKJEBEUT EDIT UTILITY ROUTINE, PERFORMS READS AND DELETES   *
*         ON THE UTILITY DATA SET                                     *
*                                                                     *
*         IKJEBEMR DELETES ITF REVERSE POLISH DATA SET AND BUILDS A   *
*         NEW ONE                                                     *
*                                                                     *
*         MACROS USED                                                 *
*                                                                     *
*         PUTLINE  CALLS IKJPUTL                                 A50476
*                                                                     *
*         LINK     PASSES CONTROL TO NON-EDIT MODULES                 *
*                                                                     *
*         IKJEBEML ADDRESSES PARAMETER LIST AND INVOKES IKJEBEMS      *
*                                                                     *
*         IKJEBESV ENTRY LINKAGE AND ADDRESSABILITY                   *
*                                                                     *
*         IKJEBERT EXIT LINKAGE                                       *
*                                                                     *
*         IKJEBEMI GENERATES EQUATES FOR MESSAGES                     *
*                                                                     *
*         IKJEBEMG GENERATES PARMLIST FOR MESSAGE INSERTION           *
*                                                                     *
*         IKJEBECA GENERATES COMMUNICATION AREA DSECT                 *
*                                                                     *
*         IKJPARM  ESTABLISHES BEGINNING OF PARAMETER CONTROL LIST    *
*         FOR IKJPARS                                                 *
*                                                                     *
*         IKJPOSIT ESTABLISHES PARAMETER CONTROL ENTRIES FOR DSNAME   *
*                                                                     *
*         IKJIDENT ESTABLISHES PARAMETER CONTROL ENTRIES FOR LINE     *
*         NUMBERS                                                     *
*                                                                     *
*         IKJKEYWD ESTABLISHES PARAMETER CONTROL EMTRIES FOR RENUM    *
*                                                                     *
*         IKJSUBF  ESTABLISHES PARAMETER CONTOL ENTRIES FOR INCREMENT *
*         AND LINE NUMBER                                             *
*                                                                     *
*         IKJNAME  RENUM KEYWORD NAME                                 *
*                                                                     *
*         IKJENDP  END OF PARAMETER CONTROL LIST                      *
*                                                                     *
*         IKJPPL   MAPPING MACRO OF PARSE PARMLIST                    *
*                                                                     *
*         IKJPTPB  MAPPING MACRO OF PUTLINE PARM BLOCK           A50476
*                                                                     *
*         IKJCPPL  MAPPING MACRO OF COMMAND PROCESSOR PARMLIST        *
*                                                                     *
*         IKJIOPL  MAPPING MACRO OF I/O SERVICE ROUTINE PARMLIST      *
*                                                                     *
*         IKJECT   MAPPING MACRO OF ENVIRONMENT CONTROL TABLE         *
*                                                                     *
*         IKJRLSA  RELEASES CORE USED BY IKJPARS FOR PDL              *
*                                                                     *
* EXITS, NORMAL -- TO IKJEBEMA WITH RETURN CODE IN REGISTER 15        *
*                                                                     *
* EXITS, ERROR -- NONE                                                *
*                                                                     *
* TABLES/WORK AREAS -- 1 IKJEBECA, THE EDIT COMMUNICATION AREA        *
*    CONTAINS PARAMETER LIST STORAGE FOR SERVICE ROUTINES NEEDED BY   *
*    IKJEBEME, A WORK AREA, THE POINTER TO THE INPUT BUFFER, AND      *
*    NUMEROUS FLAG BYTES                                              *
*    2 IKJPARS PARAMETER CONTROL LIST SPECIFIES POSITIONAL OPERANDS   *
*    THAT IKJPARS IS EXPECTED TO SCAN OFF                             *
*    3 IKJPARS PARAMETER DESCRIPTOR LIST CONTAINS POINTERS TO AND LEN-*
*    GTHS OF PARSED OPERANDS                                          *
* ATTRIBUTES -- REFRESHABLE, ENABLED, NON-PRIVILEGED                  *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- NONE.                                  *
*                                                                     *
* NOTES -- STANDARD CONVENTIONS                                       *
*                                                                     *
***********************************************************************
         EJECT
*THE FOLLOWING EQU'S ALLOW SYMBOLIC ADDRESSING OF REGISTERS
         SPACE
PARMREG0 EQU   0                       CONTAINS ADDR OF COMMAREA FOR UT
PARMREG1 EQU   1                       CONTAINS PARAMETERS FOR CALLS TO
*                                      PARSE, DA, UT, AND SYNCHECKER
ANSREG   EQU   2                       CONTAINS ADDR OF PDL
LENREG   EQU   2                       CONTAINS LENGTH OF OPERAND
*                                      BEING MOVED TO BUFFER
INBREG   EQU   3                       CONTAINS ADDR OF INPUT BUFFER
LLNREG   EQU   3                       CONTAINS DISPLACEMENT TO CURRENT
*                                      POSITION IN BUFFER
WKREG    EQU   4                       GENERAL REGISTER
MCMDREG  EQU   5                       CONTAINS ADDR OF OPERAND TO BE
*                                      MOVED TO BUFFER
TLNREG   EQU   6                       CONTAINS TOTAL LENGTH OF BUFFER
MSGREG   EQU   7                       ADDRESSABILITY OF MESSAGE CSECT
ECTREG   EQU   8                       ADDRESSABILITY OF THE ECT
WKREGB   EQU   8                       BRANCH REGISTER IN MOVE ROUTINE
COMMREG  EQU   9                       CONTAINS ADDR OF COMMUNICATION
*                                      AREA
BASEREG  EQU   11                      BASE REGISTER FOR THIS CSECT
DATAREG  EQU   12                      CONTAINS ADDR OF PDL
SAVEREG  EQU   13                      CONTAINS ADDR OF SAVE AREA
RETREG   EQU   14                      CONTAINS ADDR OF RETURN ENTRY PT
CALLREG  EQU   15                      CONTAINS BRANCH ADDR
RETCDREG EQU   15                      CONTAINS ADDR OF RETURN PT,
*                                      RETURN CODE ON EXIT.
*THE FOLLOWING EQU'S ALLOW SYMBOLIC REFERENCE TO CHARACTER
*AND DECIMAL CONSTANTS
         SPACE
D0       EQU   0                       DISPLACEMENT OF 0
D1       EQU   1                       DISPLACEMENT OF 1
D2       EQU   2                       DISPLACEMENT OF 2         A42948
D3       EQU   3                       DISPLACEMENT OF 3
D4       EQU   4                       DISPLACEMENT OF 4
D7       EQU   7                       DISPLACEMENT OF 7
D12      EQU   12                      DISPLACEMENT OF 12        A42948
D14      EQU   14                      DISPLACEMENT OF 14        A42948
D16      EQU   16                      DISPLACEMENT OF 16        A42948
D20      EQU   20                      DISPLACEMENT OF 20        A42948
D22      EQU   22                      DISPLACEMENT OF 22        A42948
L0       EQU   0                       LENGTH OF ZERO            A50476
L10      EQU   10                      LENGTH OF TEN             A50476
KONE     EQU   1                       CONSTANT OF 1
KTWO     EQU   2                       CONSTANT OF 2
KTHREE   EQU   3                       CONSTANT OF 3
KFOUR    EQU   4                       CONSTANT OF 4
KFIVE    EQU   5                       CONSTANT OF 5
KSIX     EQU   6                       CONSTANT OF 6
KSEVEN   EQU   7                       CONSTANT OF 7             A45104
KEIGHT   EQU   8                       CONSTANT OF 8
KTEN     EQU   X'0A'                   CONSTANT OF HEX 10
KTWELVE  EQU   12                      CONSTANT OF 12
K16      EQU   16                      CONSTANT OF 16
K40      EQU   40                      CONSTANT OF 40
NE       EQU   7                       'NOT EQUAL' MASK FOR BCR  A45104
GE       EQU   10                      'GREATER THAN OR EQUAL'   A45104
*                                      MASK FOR BCR              A45104
MEZERO   EQU   X'00'                   BIT MASK
MERDC    EQU   X'00'                   READ CURRENT RCD CODE     A50476
MERDP    EQU   X'01'                   READ FIRST RECORD UT ENTRY CODE
MEFORTY  EQU   X'40'                   BIT MASK
MEEIGHTY EQU   X'80'                   BIT MASK
MELEN    EQU   4                       DISPLACEMENT IN PDL ENTRY
MEFLG    EQU   6                       DISPLACEMENT IN PDL ENTRY
MEOFF    EQU   X'00'                   BIT MASK
MEQ      EQU   X'40'                   DSNAME QUOTED BIT IN PDL
MEOPRD   EQU   X'80'                   OPERAND PRESENT BIT IN PDL
LENGTH   EQU   0                       CONSTANT OF 0
MEBLANK  EQU   C' '                    CHARACTER BLANK
SLASH    EQU   C'/'                    CHARACTER SLASH           A42948
MEASTRK  EQU   C'*'                    CHARACTER ASTERISK
MELPAREN EQU   C'('                    CHARACTER LEFT PARENTHESIS
MERPAREN EQU   C')'                    CHARACTER RIGHT PARENTHESIS
MEQUOTE  EQU   C''''                   CHARACTER QUOTE
MECZERO  EQU   C'0'                    CHARACTER ZERO
NOBR     EQU   0                       CONSTANT OF 0
MEALL    EQU   X'FF'                   BIT MASK
         EJECT
***********************************************************************
         IKJEBESV (14,12),,*           ENTRY LINKAGE
         SPACE
         USING IKJEBECA,COMMREG        COMMUNICATIONS AREA
         NI    MEDAALP,MEZERO          SET ALLOC DONE SW OFF
         L     MSGREG,MEMSGADR         ADDR OF MSG CSECT
         USING IKJEBMEM,MSGREG         ADDRESSABILITY OF MSG CSECT
         XC    MEPARMM(K40),MEPARMM    ZERO OUT PARM LIST FOR MSGS
         XC    MECODE,MECODE           INIT RETURN CODE TO ZERO
         TM    CAPTIBFR,CAOPERND       OPERANDS PRESENT
         BNO   ME002600                BR TO PUT OUT MSG
         EJECT
**********************************************************************
*                                                                    *
*IKJPARS WILL SCAN THE OPERANDS ON THE SUBCOMMAND AND PROMPT FOR     *
*REQUIRED OPERANDS THAT ARE MISSING. PARSE REQUIRES A SEVEN WORD     *
*PARAMETER LIST. THE FIRST TWO WORDS (PTRS TO THE UPT AND THE ECT)   *
*ARE INITIALIZED BY THE TMP. THE THIRD WORD (PTR TO THE EDIT         *
*ATTENTION ECB) IS INITIALIZED BY THE EDIT CP. THE FOLLOWING CODE    *
*INITIALIZES THE FOURTH THROUGH THE SEVENTH WORDS (PTRS TO THE PCL,  *
*THE ANSWER PLACE WHERE PARSE WILL PLACE THE ADDRESS OF THE PDL,     *
*THE COMMAND BUFFER, AND THE USER WORK AREA.                         *
*                                                                    *
**********************************************************************
         LA    PARMREG1,CATMPLST       ADDR OF TMP SR PARMLIST
         USING PPL,PARMREG1            ADDRESSABILITY OF PPL DSECT
         L     WKREG,MEPCLADR          ADDRESS OF PCL
         ST    WKREG,PPLPCL            FOURTH WD OF PARMLIST
         LA    ANSREG,CAPTPRSD         ADDR OF ANSWER PLACE WHERE ADDR
         ST    ANSREG,PPLANS           OF PDL WILL BE PLACED BY PARSE
         L     INBREG,CAPTIBFR         ADDR OF INPUT BUFFER
         ST    INBREG,PPLCBUF          SIXTH WORD OF PARMLIST
         ST    SAVEREG,PPLUWA          ADDR OF USER WORK AREA
         DROP  PARMREG1
         LINK  EP=IKJPARS              LINK TO PARSE TO SCAN SUBCMD
         SPACE
ME000100 EQU   *
         B     ME000100+D4(RETCDREG)   PARSE RETURN CODE BR TABLE
         B     ME000200                00  SUCCESSFUL COMPLETION
         B     ME003250                04  UNABLE TO PROMPT
         B     ME002000                08  ATTENTION ISSUED
         B     ME002900                12  INVALID PARAMETERS
         B     ME002800                16  NOT ENOUGH CORE
         EJECT
***********************************************************************
*                                                                     *
*IF ONLY ONE LINE NUMBER IS GIVEN, PARSE ASSUMES IT IS LINENUM1.      *
*IKJEBEME ASSUMES IT IS LINENUM3 AS LINENUM2 MUST BE PRESENT IF LINE- *
*NUM1 IS GIVEN. IF TWO NUMBERS ARE GIVEN, BOTH PARSE AND IKJEBEME     *
*ASSUME THEY ARE LINENUM1 AND LINENUM2. THE FOLLOWING CODE CHECKS TO  *
*SEE IF LINENUM2 IS GIVEN AND MOVES LINENUM1 TO LINENUM3 WHEN ONLY ONE*
*NUMBER IS GIVEN.                                                     *
*                                                                     *
***********************************************************************
ME000200 L     DATAREG,CAPTPRSD        ESTABLISH ADDRESSABILITY OF THE
         USING MEPCLL,DATAREG          PDL FROM PARSE
         TM    MEPCE3+MEFLG,MEOPRD     IS LINENUM2 PRESENT
         BO    ME000300                YES BR  ALL LINE NUMBERS PRESENT
         TM    MEPCE2+MEFLG,MEOPRD     IS LINENUM1 PRESENT
         BZ    ME000300                NO BR ONLY LINENUM3 IS GIVEN
         LA    WKREG,MEPCE2            ADDR OF PTR TO LTNENUM1
         MVC   MEPCE4(KEIGHT),D0(WKREG) MOVE POINTERS TO LINENUM3
         NI    MEPCE2+MEFLG,MEALL-MEOPRD TURN SWITCH FOR LINENUM1 OFF
         EJECT
***********************************************************************
*                                                                     *
*IKJEBEDA WILL ALLOCATE A SEQUENTIAL DATA SET WITH A DISPOSITION OF   *
*NEW, CATLG, DELETE.                                                  *
*                                                                     *
***********************************************************************
ME000300 NI    CASAFLAG,MEZERO         SET SAVEDS FLAG SW OFF
         MVI   CASAFLG2,CASADQTY+CASANCTG SPECIFY NEW,CATLG      Y01676
*                                      ALLOCATION WITH DOUBLED   Y01676
*                                      SPACE REQUEST             Y01676
         BAL   WKREG,ME002480          BR TO CALL IKJEBEDA
         OI    MEDAALP,MEEIGHTY        SETSW ON FOR ALLOC DONE
***********************************************************************
*                                                                     *
*IKJEBEFC WILL COPY THE EDIT UTILITY DATA SET INTO THE QSAM DATA SET  *
*ALLOCATED BY DAIR.                                                   *
*                                                                     *
***********************************************************************
         IKJEBESH (COMMREG),IKJEBEFC,PARAM=((COMMREG)),MF=(E,MEPARMM)
         SPACE
ME000400 EQU   *
         B     ME000400+D4(RETCDREG)   FC RETURN CODE BR TABLE
         B     ME000500                00  SUCCESSFUL COMPLETION
         B     ME002700                04  EMPTY DATA SET
         B     ME003250                08  QSAM ERROR
         B     ME003300                12  I/O ERROR
***********************************************************************
*                                                                     *
*IKJEBEDA WILL UNALLOCATE THE SEQUENTIAL DATA SET WITH DISPOSITION=   *
*OLD,KEEP.                                                            *
*                                                                     *
***********************************************************************
ME000500 EQU   *
         OI    CASAFLAG,CASADISP       SET SWITCH
         BAL   WKREG,ME002480          BR TO CALL IKJEBEDA
         EJECT
***********************************************************************
*                                                                     *
*THE FOLLOWING SECTION BUILDS A MODEL OF THE SYSTEM MERGE COMMAND IN  *
*A BUFFER FROM INFORMATION OBTAINED FROM THE SUBCOMMAND OPERANDS. THE *
*SYSTEM COMMAND HAS THE FORMAT                                        *
*        MERGE DSNAME1 (LINENUM1 LINENUM2) DSNAME2 (LINENUM3)         *
*DSNAME1 WILL BE THE NAME GIVEN BY THE USER OR THE DSNAME FROM IKJDAIR*
*IF THE USER SPECIFIED *.                                             *
*LINENUMS 1 AND 2 ARE AS SPECIFIED BY THE USER ON THE SUB-COMMAND.    *
*DSNAME2 WILL BE THE DSNAME FROM IKJDAIR.                             *
*LINENUM3 IS EITHER AS SPECIFIED ON THE SUB-COMMAND OR IF '*' IS      *
*SPECIFIED, THE CURRENT VALUE OF THE LINE POINTER IS USED.            *
*IF RENUM IS SPECIFIED, IT WILL BE INCLUDED IN THE MODEL COMMAND.     *
*                                                                     *
***********************************************************************
         LA    LLNREG,MECMOPDS         INIT DISP TO BEG OF MODEL OPDS
         LA    TLNREG,KTEN             LENGTH OF HEADER+COMMAND
         L     MCMDREG,MEPCE1          PTR TO DSNAME
         LA    LENREG,KONE             LENGTH OF ASTERISK        A42948
         CH    LENREG,MEPCE1+D4        IS LENGTH EQUAL TO ONE    A42948
         BNE   ME000525                IF NOT BRANCH AROUD CHECK A42948
         CLI   D0(MCMDREG),MEASTRK     DSNAME=*                  A42948
         BNE   ME000525                BR NO TO LOAD DA DSNAME   A42948
         LH    LENREG,CASADSNL         LENGTH OF DA DSNAME       A42948
         LA    MCMDREG,CASADSN         PTR TO DA DSNAME          A42948
         BAL   WKREG,ME002550          BR TO MOVE QUOTE          A42948
         B     ME000850                BR TO CONT PROCESSING     A42948
ME000525 LH    LENREG,MEPCE1+MELEN     GET LENGTH OF DSN         A42948
         TM    MEPCE1+MEFLG,MEQ        DSNAME QUOTED
         BO    ME000600                BR YES
         BAL   WKREG,ME002500          MOVE PARTIALLY QUALIFIED DSN
         TM    MEPCE1+D14,MEOPRD       WAS MEMBER SPECIFIED      A42948
         BNO   ME000800                IF NOT CONTINUE CHECKS    A42948
         BAL   WKREG,ME002505          PERFORM MEMBER MOVE       A42948
         MVC   D0(KTWO,LLNREG),MERPB   MOVE FINAL PAREND AND BLK A42948
         LA    LLNREG,KTWO(LLNREG)     UPDATE DISPLACEMENT       A42948
         LA    TLNREG,KTWO(TLNREG)     UPDATE TOTAL LENGTH       A42948
         B     ME000800                CONTINUE CHECKS           A42948
ME000600 BAL   WKREG,ME002550          BR TO MOVE QUOTE
         TM    MEPCE1+D14,MEOPRD       WAS MEMBER SPECIFIED      A42948
         BNO   ME000800                IF NOT CONTINUE CHECKS    A42948
         BCTR  LLNREG,D0               DECREMENT DISPLACEMENT    A42948
         BAL   WKREG,ME002505          MOVE MEMBER NAME          A42948
         MVC   D0(D3,LLNREG),MERPBQ    MOVE IN PAREND,QUOTE,BLK  A42948
         LA    LLNREG,D3(LLNREG)       UPDATE DISPLACEMENT       A42948
         LA    TLNREG,D2(TLNREG)       UPDATE TOTAL LENGTH       A42948
ME000800 TM    MEPCE1+D22,MEOPRD       WAS PASSWORD SPECIFIED    A42948
         BNO   ME000850                IF NOT CONTINUE CHECKS    A42948
         BCTR  LLNREG,D0               DECREMENT DISPLACEMENT    A42948
         MVI   D0(LLNREG),SLASH        MOVE IN SLASH             A42948
         LA    LLNREG,D1(LLNREG)       UPDATE DISPLACEMENT       A42948
         LH    LENREG,MEPCE1+D20       GET PASSWORD LENGTH       A42948
         L     MCMDREG,MEPCE1+D16      GET PASSWORD DISPLACEMENT A42948
         BAL   WKREG,ME002500          MOVE IN PASSWORD          A42948
ME000850 TM    MEPCE2+MEFLG,MEOPRD     LINENUM1 GIVEN            A42948
         BZ    ME000900                NO BR FOR SECOND DSNAME
         LH    LENREG,MEPCE2+MELEN     YES GET LINENUM1 LENGTH
         L     MCMDREG,MEPCE2          PTR TO LINENUM1
         BAL   WKREG,ME002500          BR TO MOVE SUBROUTINE
         LH    LENREG,MEPCE3+MELEN     LENGTH OF LINENUM2
         L     MCMDREG,MEPCE3          PTR TO LINENUM2
         BAL   WKREG,ME002500          BR TO MOVE SUBROUTINE
ME000900 LH    LENREG,CASADSNL         LENGTH OF DA DSNAME
         LA    MCMDREG,CASADSN         DAIR DSNAME PTR
         BAL   WKREG,ME002550          BR TO MOVE DSNAME
         L     MCMDREG,MEPCE4          PTR TO LINE NUMBER
         CLI   D0(MCMDREG),MEASTRK     LINENUM3=*
         BE    ME002400                YES BR TO GET CURRENT LINE PTR
         LH    LENREG,MEPCE4+MELEN     NO GET LENGTH OF LINENUM3
ME001100 BAL   WKREG,ME002500          BR TO MOVE SUBROUTINE
         CLI   MEPCE6+D1,KONE          CHECK IF NUM/NONUM        A45104
*                                      KEYWORD SPECIFIED         A45104
         BL    ME001180                IF NOT, CONTINUE PROCESS  A45104
         BH    ME001160                IF 'NONUM', BRANCH TO     A45104
*                                      PROCESS                   A45104
         MVC   D0(KTHREE,LLNREG),MENUM MOVE 'NUM' KEYWD TO MODEL A45104
         TM    MEPCS21+MEFLG,MEOPRD    CHECK IF LINE NUMBER      A45104
*                                      POSITION ENTERED          A45104
         BO    ME001120                IF SO, BRANCH TO PROCESS  A45104
*                                      SUBFIELD                  A45104
         MVI   D3(LLNREG),MEBLANK      IF NOT, MOVE BLANK        A45104
         LA    LLNREG,KFOUR(,LLNREG)   INCREMENT DISPLACEMENT    A45104
         LA    TLNREG,KFOUR(,TLNREG)   INCREMENT TOTAL LENGTH    A45104
         B     ME001180                BRANCH TO CONTINUE        A45104
ME001120 EQU   *                                                 A45104
         MVC   D3(MENUMLEN-D3,LLNREG),MENUM+D3 MOVE SUBFIELD     A45104
*                                      MAP TO MODEL BUFFER       A45104
         LH    LENREG,MEPCS21+MELEN    OBTAIN LENGTH OF OPERAND  A45104
         BCTR  LENREG,D0               DECREMENT FOR MOVE        A45104
         L     WKREG,MEPCS21           OBTAIN ADDRESS OF LINE    A45104
*                                      NUMBER POSITION ENTERED   A45104
         EX    LENREG,MEMVCLNP         MOVE LINE NUMBER POSITION A45104
*                                      OPERAND                   A45104
         TM    MEPCS22+MEFLG,MEOPRD    CHECK IF LINE NUMBER      A45104
*                                      LENGTH SPECIFIED          A45104
         BZ    ME001140                IF NOT, UPDATE REGISTERS  A45104
         L     WKREG,MEPCS22           OBTAIN ADDRESS OF LINE    A45104
*                                      NUMBER LENGTH ENTERED     A45104
         MVC   MENUMLNL-MENUM(KONE,LLNREG),D0(WKREG) MOVE DIGIT  A45104
*                                      TO SUBFIELD               A45104
ME001140 EQU   *                                                 A45104
         LA    LLNREG,MENUMLEN(,LLNREG) INCREMENT DISPLACEMENT   A45104
         LA    TLNREG,MENUMLEN(,TLNREG) INCREMENT TOTAL LENGTH   A45104
         B     ME001180                 BRANCH TO CONTINUE       A45104
MEMVCLNP MVC   MENUMLNP-MENUM(*-*,LLNREG),D0(WKREG) MOVE LINE    A45104
*                                       NUMBER POSITION TO MODEL A45104
ME001160 EQU   *                                                 A45104
         MVC   D0(KSIX,LLNREG),MENONUM MOVE 'NONUM ' OPERAND     A45104
         LA    LLNREG,KSIX(,LLNREG)    INCREMENT DISPLACEMENT    A45104
         LA    TLNREG,KSIX(,TLNREG)    INCREMENT TOTAL LENGTH    A45104
ME001180 EQU   *                                                 A45104
         TM    CACFLAG1,CANONUM        DS LINE-NUMBERED          A45104
         BNZ   ME002460                BR NOT TO MOVE NONUM2     A45104
         B     ME002370                IF LINE-NUMBERED, BRANCH  A45104
*                                      TO MOVE NUM2 KEYWORD      A45104
*                                      (WITH SUBFIELD)           A45104
ME001200 MVC   D0(KFIVE,LLNREG),MERENUM MOVE 'RENUM' TO MODEL
         LA    LLNREG,KFIVE(LLNREG)    INCREMENT DISPLACEMENT
         LA    TLNREG,KFIVE(TLNREG)    INCREMENT TOTAL LENGTH
         TM    MEPCS11+MEFLG,MEOPRD    LINENUM PRESENT
         BZ    ME001400                NO BR TO TOTAL
         MVI   D0(LLNREG),MELPAREN     YES MOVE '(' TO MODEL
         LA    LLNREG,KONE(LLNREG)     LENGTH=1
         LA    TLNREG,KONE(TLNREG)     TOTAL LENGTH
         LH    LENREG,MEPCS11+MELEN    LENGTH OF LINENUM
         L     MCMDREG,MEPCS11         PTR TO LINENUM
         BAL   WKREG,ME002500          BR TO MOVE SUBROUTINE
         TM    MEPCS12+MEFLG,MEOPRD    INCREMENT PRESENT
         BZ    ME001300                NO BR TO MOVE RIGHT PAREN
         LH    LENREG,MEPCS12+MELEN    LENGTH OF INCREMENT
         L     MCMDREG,MEPCS12         PTR TO INCREMENT
         BAL   WKREG,ME002500          BR TO MOVE ROUTINE
ME001300 BCTR  LLNREG,NOBR             TOTAL DISP-1   NO BLANK
         MVI   D0(LLNREG),MERPAREN     MOVE ')' TO MODEL
         LA    LLNREG,KONE(LLNREG)     ACCOUNT FOR PARENTHESIS  YA00039
ME001400 EQU   *
         CLI   CADSCODE,CABASIC        DS TYPE=BASIC
         BNE   ME001425                BR NO                     Y02676
         MVC   D0(KSIX,LLNREG),MEBASIC MOVE ' BASIC' TO MODEL
         LA    TLNREG,KSIX(TLNREG)     TOTAL LENGTH
         B     ME001450                BR TO COMPLETE MODEL      Y02676
ME001425 EQU   *                                                 Y02676
         CLI   CADSCODE,CAVBASIC       DSTYPE=VSBASIC            Y02676
         BNE   ME001450                BR NO                     Y02676
         MVC   D0(KEIGHT,LLNREG),MEVBASIC MOVE 'VSBASIC' TO MODELY02676
         LA    TLNREG,KEIGHT(TLNREG)   TOTAL LENGTH              Y02676
ME001450 STH   TLNREG,MCMLEN           TOTAL LENGTH OF MODEL COMMAND
         MVC   MCMMRG(KSIX),MEMERGE    MOVE 'MERGE ' TO MODEL
         XC    MCMRES(L'MCMRES),MCMRES ZERO HALFWORD FOR SCAN
         EJECT
***********************************************************************
*                                                                     *
*IKJEBECI WILL ATTACH THE SYSTEM MERGE COMMAND, IKJEBMIN, WHICH WILL  *
*DO THE ACTUAL MERGE. A TWO-WORD PARAMETER LIST IS PASSED TO IKJEBECI.*
*WORD1 IS A POINTER TO THE COMMUNICATIONS AREA  WORD2 IS A POINTER TO *
*THE BUFFER CONTAINING THE MODEL COMMAND.                             *
*                                                                     *
***********************************************************************
         ST    COMMREG,MECIPLST        PTR TO COMM AREA
         LA    WKREG,MCMLEN            ADDR OF BUFFER
         ST    WKREG,MECIPLST+D4       SECOND WORD OF PARMLIST
         LA    WKREG,MECIPLST          ADDR OF PARMLIST
         IKJEBESH (COMMREG),IKJEBECI,PARAM=((WKREG)),MF=(E,MEPARMM)
         SPACE
         TM    CAATTN,CAATTNIS         ATTENTION ISSUED
         BO    ME001800                BR YES TO TERMINATE
         LTR   RETCDREG,RETCDREG       NO RETURN CODE=0
         BNZ   ME003250                BR NO TO TERMINATE
         EJECT
***********************************************************************
*                                                                     *
*IKJEBEDA IS CALLED TO RE-ALLOCATE THE OUTPUT QSAM DATA SET.          *
*                                                                     *
***********************************************************************
         OI    CASAFLAG,CASAUNCG       SECOND REQUEST FOR ALLOCATION
         BAL   WKREG,ME002480          BR TO CALL IKJEBEDA
***********************************************************************
*                                                                     *
*IKJEBECO WILL COPY THE MERGED (QSAM) DATA SET TO A  NEW UTILITY DATA *
*SET. IF THE DATA SET TYPE IS NOT IPLI OR BASIC, IKJEBEEX WILL UN-    *
*ALLOCATE THE UTILITY DATA SET. THE CORE USED FOR THE PDL BY IKJPARS  *
*IS RELEASED,  AND CONTROL IS RETURNED TO IKJEBEMA WITH A RETURN CODE *
*OF ZERO IN REGISTER 15.                                              *
*                                                                     *
***********************************************************************
         MVC   CAPTPDCB(L'CAPTPDCB),CAPTCDCB HAVE CURRENT DCB PTR TO
*                                      PREVIOUS DCB PTR
         XC    CAPTCDCB(L'CAPTCDCB),CAPTCDCB ZERO CURRENT DCB PTR
         NI    CASAFLAG,MEALL-CASAINCP INIT COPY SW OFF
         IKJEBESH (COMMREG),IKJEBECO,PARAM=((COMMREG)),MF=(E,MEPARMM)
         SPACE
ME001500 EQU   *
         B     ME001500+D4(RETCDREG)   CO RETURN CODE BR TABLE
         B     ME001600                00  NORMAL COMPLETION
         B     ME002470                04  EMPTY DATA SET
         B     ME002350                08  UNSUCCESSFUL OPERATION
         SPACE
***********************************************************************
*                                                                     *
*IKJEBEDA IS CALLED TO UNALLOCATE THE OUTPUT QSAM DATA SET.           *
*                                                                     *
***********************************************************************
ME001600 EQU   *
         OI    CASAFLAG,CASADISP+CASAUNCG SET SWITCH
         BAL   WKREG,ME002480          BR TO CALL IKJEBEDA
         NI    MEDAALP,MEALL-MEEIGHTY  SET ALLOC DONE SW OFF
         EJECT
***********************************************************************
*                                                                     *
*IF THE DATA SET TYPE IS IPLI OR BASIC, IKJEBEMR IS CALLED TO DELETE  *
*THE OLD POLISH DATA SET AND BUILD A NEW ONE.                         *
*                                                                     *
***********************************************************************
         OI    CASAFLAG,CASAINCP       SET INIT COPY SW ON
         OI    CACFLAG2,CADSMODS       INDICATE DATA SET MODIFIED
         L     WKREG,CACURNUM          CURRENT LINE PTR
         ST    WKREG,CAIMLLNO          LAST LINE USED IN INPUT MODE
         CLI   CADSCODE,CAIPLI         DATA SET TYPE IPLI
         BE    ME001650                YES BR TO DELETE POLISH DS
         CLI   CADSCODE,CABASIC        DS TYPE=BASIC
         BNE   ME001700                BR NO TO CONTINUE
ME001650 EQU   *
         IKJEBESH (COMMREG),IKJEBEMR,PARAM=((COMMREG)),MF=(E,MEPARMM)
         SPACE
ME001675 EQU   *
         B     ME001675+D4(RETCDREG)   MR RC BRANCH TABLE
         B     ME001700                00  SUCCESSFUL OPERATION
         B     ME002300                04  UNSUCCESSFUL OPERATION
         B     ME002200                08  SYN CHECKER NOT OPERATIONAL
         B     ME003250                12  I/O ERROR ON NEW UTIL
         SPACE
ME001700 L     WKREG,CAPTCDCB          CURRENT DCB PTR
         MVC   CAPTCDCB(L'CAPTCDCB),CAPTPDCB MOVE PREVIOUS DCB PTR TO
*                                      CURRENT DCB PTR
         ST    WKREG,CAPTPDCB          SAVE CURRENT IN PREVIOUS
         BAL   WKREGB,ME002495         BR TO CLOSE OLD UTILITY
         EJECT
***********************************************************************
*                                                                     *
* THE MODE MESSAGE BIT IS TURNED ON--IKJEBEMA WILL PUT           Y01676
* OUT THE 'EDIT' MESSAGE.                                        Y01676
*                                                                     *
***********************************************************************
         SPACE
         OI    CACFLAG2,CAMODMSG   TURN ON BIT FOR 'EDIT' MSG    Y01676
         SPACE 2
**************************************************************** Y01676
*                                                                Y01676
* READ CURRENT LINE REFERENCE INTO VERIFY BUFFER AND SET 'LINE   Y01676
* TO BE VERIFIED' SWITCH ON.                                     Y01676
*                                                                Y01676
**************************************************************** Y01676
         SPACE
ME001750 EQU   *                                                 A50476
         L     WKREG,CAPTCDCB          OBTAIN ADDRESS OF CURRENT A50476
*                                      UTILITY DCB               A50476
         ST    WKREG,MEUTPLST          STORE IN FIRST PARAMETER  A50476
*                                      WORD                      A50476
         MVI   MEUTPLST,MERDC          SPECIFY READ CURRENT OP   A50476
         LA    WKREG,CACURNUM          OBTAIN RECORD KEY ADDR    A50476
         ST    WKREG,MEUTWD2           STORE IN LIST             A50476
         LA    WKREG,CATEMPBF+D12      OBTAIN ADDRESS OF VERIFY  A50476
*                                      BUFFER                    A50476
         ST    WKREG,MEUTWD3           STORE IN LIST             A50476
         MVI   MEUTWD3,MEEIGHTY        MARK END-OF-LIST          A50476
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),MEUTPLST), A50476X
               MF=(E,MEPARMM)          INVOKE IKJEBEUT           A50476
         LTR   RETCDREG,RETCDREG       CHECK IF READ SUCCESSFUL  A50476
         BNZ   ME003300                IF NOT, BRANCH TO         A50476
*                                      INDICATE I/O ERROR        A50476
         OI    CACFLAG1,CALNTOVF       IF SO, INDICATE A LINE    A50476
*                                      TO BE VERIFIED            A50476
         EJECT
ME001800 TM    MEDAALP,MEEIGHTY        DAIR ALLOCATION PERFORMED
         BZ    ME001900                NO BR TO RELEASE PARSE CORE
***********************************************************************
*                                                                     *
*IKJEBEDA WILL UNALLOCATE THE SEQUENTIAL DATA SET WITH DISPOSITION=   *
*OLD,KEEP.                                                            *
*                                                                     *
***********************************************************************
         OI    CASAFLAG,CASADISP+CASAUNCG SET SWITCH
         NI    MEDAALP,MEALL-MEEIGHTY  TURN OFF ALLOC SWITCH
         BAL   WKREG,ME002480          BR TO CALL IKJEBEDA
ME001900 TM    CAPRSPDL,CAFREEDL       PDL SW ON
         BNZ   ME002000                NO SKIP RELEASE
         IKJRLSA CAPTPRSD              RELEASE PARSE TABLE CORE
         OI    CAPRSPDL,CAFREEDL       TURN PDL SW ON
         SPACE
ME002000 EQU   *
         LH    RETCDREG,MECODE         SET RETURN CODE
         IKJEBERT (14,12),,RC=(15)     RESTORE REGISTERS AND RETURN
*                                      WITH RETURN CODE IN REGISTER 15
         EJECT
ME002200 EQU   *
         LA    WKREG,K16               SET RETURN CODE = 16
         STH   WKREG,MECODE            SAVE RETURN CODE
         L     WKREG,CAPTCDCB          PTR TO NEW DCB
         MVC   CAPTCDCB,CAPTPDCB       MOVE PTR TO OLD DCB
         ST    WKREG,CAPTPDCB          PTR TO NEW DCB
         BAL   WKREGB,ME002495         BR TO CLOSE OLD UTILITY DS
         B     ME001900                BR TO END PROCESSING
         SPACE
ME002300 EQU   *
         L     WKREG,CAPTCDCB          POINTER TO NEW UTILITY DS DCB
         MVC   CAPTCDCB(L'CAPTCDCB),CAPTPDCB SWITCH UTIL DCB POINTERS
         ST    WKREG,CAPTPDCB          SAVE POINTER TO NEW DCB
ME002350 EQU   *
         BAL   WKREGB,ME002495         BR TO CLOSE OLD DCB
         B     ME003250                BR TO SET RETURN CODE
         SPACE
ME002370 EQU   *
         MVC   D0(KFIVE,LLNREG),MENUM2 MOVE 'NUM2(' TO MODEL     A45104
         ST    LLNREG,MESAVE           SAVE ADDRESS OF NUM2      A45104
*                                      KEYWORD FOR POSSIBLE      A45104
*                                      LATER USE                 A45104
         LA    LLNREG,KFIVE(LLNREG)    INCREMENT DISPLACEMENT    A45104
         LA    TLNREG,KFIVE(TLNREG)    INCREMENT TOTAL           A45104
         SR    LENREG,LENREG           CLEAR REGISTER
         IC    LENREG,CALINE           BEGINNING SEQUENCE COLUMN
         BAL   WKREGB,ME002490         BR TO CONVERT SUB-ROUTINE
         LA    LENREG,KTHREE           MAX LENGTH = 3
         LA    MCMDREG,KFIVE(MCMDREG)  INCREMENT POINTER
ME002380 EQU   *
         CLI   D0(MCMDREG),MECZERO     IS DIGIT = 0
         BNE   ME002385                BR NO TO STORE
         LA    MCMDREG,KONE(MCMDREG)   INCREMENT POINTER
         BCT   LENREG,ME002380         DECREMENT LENGTH AND LOOP
ME002385 EQU   *
         BAL   WKREG,ME002500          MOVE VALUE TO MODEL
         SR    LENREG,LENREG           CLEAR REGISTER
         IC    LENREG,CALENGTH         LENGTH OF SEQUENCE FIELD
         BAL   WKREGB,ME002490         BR TO CONVERT ROUTINE
         L     WKREG,D4(MCMDREG)       CONVERTED LENGTH
         STC   WKREG,D0(LLNREG)        MOVE TO MODEL
         MVI   D1(LLNREG),MERPAREN     MOVE ')' TO MODEL
         LA    LLNREG,KTWO(LLNREG)     INCREMENT DISPLACEMENT
         LA    TLNREG,KTWO(TLNREG)     INCREMENT TOTAL LENGTH
         BAL   WKREG,ME002540          MOVE BLANK TO MODEL       A45104
         BAL   WKREGB,ME002580         BRANCH TO SET NUM KEYWORD A45104
*                                      AND SUBFIELD AS NECESSARY A45104
         B     ME001200                BR TO FINISH MODEL
         SPACE
ME002400 TM    CACFLAG1,CANONUM        DS LINE-NUMBERED
         BNZ   ME002410                BR NO TO FIND REL REC NUM
         L     LENREG,CACURNUM         CURRENT LINE
         BAL   WKREGB,ME002490         BR TO CONVERT LINE NUMBER
         B     ME001100                BR TO MAIN ROUTINE
         SPACE
ME002410 LA    LENREG,KONE             SET COUNT=1
         L     WKREG,CAPTCDCB          PTR TO CURRENT DCB
         ST    WKREG,MEUTPLST          FIRST WD OF PARMLIST
         MVI   MEUTPLST,MERDP          ENTRY CODE=X'01' READ PREVIOUS
         LA    WKREG,CACURNUM          PTR TO BINARY KEY OF RCD
         ST    WKREG,MEUTWD2           STORE IN UT PARMLIST
         XC    MEUTWD3(L'MEUTWD3),MEUTWD3 NO BUFFER
         MVI   MEUTWD3,MEEIGHTY        PARMLIST DELIMITER
ME002420 LA    PARMREG1,MEUTPLST       ADDR OF PARMLIST
         LR    PARMREG0,COMMREG        ADDR OF COMM AREA
         L     CALLREG,CAPTUT          ADDR OF IKJEBEUT
         BALR  RETREG,CALLREG          BR TO READ RECORD
         SPACE
ME002430 EQU   *
         B     ME002430+D4(RETCDREG)   UT RETURN CODE BR TABLE
         B     ME002440                00  SUCCESSFUL
         B     ME002450                04  EOD
         B     ME003300                08  I/O ERROR
ME002440 LA    LENREG,KONE(LENREG)     COUNT=COUNT+1
         XC    MEUTWD2(L'MEUTWD2),MEUTWD2 NO PTR TO KEY. USE UT'S
*                                      INTERNAL REFERENCE
         MVI   MEUTWD2,MEEIGHTY        PARMLIST DELIMITER
         B     ME002420                BR TO READ NEXT RECD
ME002450 BAL   WKREGB,ME002490         BR TO CONVERT REL REC NUM
         B     ME001100                BR TO MOVE RELATIVE       A50953
*                                      RECORD NUMBER AND TO      A50953
*                                      COMPLETE BFR PROCESSING   A50953
ME002460 MVC   D0(KSEVEN,LLNREG),MENONUM2 MOVE 'NONUM2 ' TO      A45104
*                                      MODEL BUFFER              A45104
         LA    TLNREG,KSEVEN(TLNREG)   INCR TOTAL LENGTH         A45104
         BAL   WKREGB,ME002580         BRANCH TO SET NONUM       A45104
*                                      KEYWORD IF NECESSARY      A45104
         B     ME001450                BR TO MAIN ROUTINE
         SPACE
ME002470 EQU   *
         BAL   WKREGB,ME002495         BR TO CLOSE NEW UTILITY DCB
         B     ME002700                BR TO PUT OUT MSG
         SPACE
***********************************************************************
*                                                                     *
*THIS SUBROUTINE CALLS IKJEBEDA FOR ALLOCATION OR UNALLOCATION OF THE *
*TEMPORARY QSAM DATA SET.                                             *
*                                                                     *
***********************************************************************
ME002480 EQU   *
         IKJEBESH (COMMREG),IKJEBEDA,PARAM=((COMMREG)),MF=(E,MEPARMM)
         SPACE
         LTR   RETCDREG,RETCDREG       RC = 0
         BCR   8,WKREG                 BR YES TO COMPLETE
         LA    ANSREG,KEIGHT           CONSTANT OF 8
         AH    RETCDREG,MECODE         ADD ANY RC ALREADY GOTTEN
         CR    RETCDREG,ANSREG         IS RC STILL 8
         BE    ME002485                BR YES
         SR    RETCDREG,ANSREG         SET HIGHER RC
ME002485 EQU   *
         STH   RETCDREG,MECODE         SAVE RC
         B     ME001800                BR TO TERMINATE
         SPACE
***********************************************************************
*                                                                     *
*THIS SUBROUTINE CONVERTS A NUMBER TO DECIMAL AND SETS UP REGISTERS   *
*FOR THE MOVE ROUTINE.                                                *
*                                                                     *
***********************************************************************
ME002490 CVD   LENREG,MEPACK           CONVERT NUMBER TO DECIMAL
         UNPK  MELNUM(KEIGHT),MEPACK+D3(KFIVE) UNPACK NUMBER
         OI    MELNUM+D7,MECZERO       INSURE LAST DIGIT IS CHAR ZERO
         LA    LENREG,KEIGHT           LENGTH OF NUMBER
         LA    MCMDREG,MELNUM          PTR TO CONVERTED NUMBER
         BR    WKREGB                  RETURN TO CALLER
***********************************************************************
*                                                                     *
*THIS CODE CALLS IKJEBEEX TO CLOSE A UTILITY DATA SET AND SWITCHES THE*
*DCB POINTERS UPON RETURN.                                            *
*                                                                     *
***********************************************************************
ME002495 EQU   *
         L     WKREG,CAPTCDCB          POINTER TO CURRENT DCB
         LTR   WKREG,WKREG             CHECK DATA SET ALLOCATED YA00044
         BZ    ME002497                IF NOT, BYPASS CLOSE     YA00044
         IKJEBESH (COMMREG),IKJEBEEX,PARAM=((WKREG),(COMMREG)),        *
               MF=(E,MEPARMM)
         SPACE
ME002497 EQU   *                                                YA00044
         MVC   CAPTCDCB(L'CAPTCDCB),CAPTPDCB RESTORE PTR TO OTHER DCB
         XC    CAPTPDCB,CAPTPDCB       ZERO OLD POINTER
         BR    WKREGB                  RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*THE FOLLOWING CODE IS A SUB-ROUTINE TO MOVE THE OPERAND POINTED TO   *
*BY MCMDREG TO THE MODEL COMMAND BUFFER. THE TOTAL DISPLACEMENT AND   *
*BUFFER LENGTH TO DATE ARE ACCUMULATED AND A BLANK IS PLACED IN THE   *
*BUFFER FOLLOWING THE OPERAND.                                        *
*                                                                     *
***********************************************************************
ME002500 LA    WKREGB,ME002540         REGULAR MOVE BRANCH POINT
ME002525 BCTR  LENREG,NOBR             DECR LENGTH BY ONE FOR EXECUTE
         EX    LENREG,MECMDMVC         MOVE OPERAND TO MODEL
         LA    LENREG,KONE(LENREG)     PLUS ONE FOR EXECUTE
         AR    LLNREG,LENREG           TOTAL DISPLACEMENT
         AR    TLNREG,LENREG           TOTAL LENGTH
         BR    WKREGB                  RETURN TO DSN MOVE IF NECESSARY
ME002540 MVI   D0(LLNREG),MEBLANK      MOVE BLANK TO MODEL
         LA    LLNREG,KONE(LLNREG)     PLUS ONE FOR BLANK
         LA    TLNREG,KONE(TLNREG)     TOTAL LENGTH
         BR    WKREG                   RETURN TO MAIN ROUTINE
MECMDMVC MVC   D0(LENGTH,LLNREG),D0(MCMDREG) MOVE OPERAND TO MODEL
***********************************************************************
*                                                                     *
*THE FOLLOWING SECTION OF CODE MOVES A QUOTE (') BEFORE AND AFTER THE *
*DSNAMES TO INSURE FULLY-QUALIFIED DATA SET NAMES.                    *
*                                                                     *
***********************************************************************
ME002550 MVI   D0(LLNREG),MEQUOTE      MOVE QUOTE TO MODEL
         LA    LLNREG,KONE(LLNREG)     DISPLACEMENT +1
         LA    TLNREG,KONE(TLNREG)     TOTAL LENGTH
         LA    WKREGB,ME002575         DSN MOVE BRANCH POINT
         B     ME002525                BR TO DO MOVE
ME002575 MVC   D0(KTWO,LLNREG),MEQB    MOVE '  TO MODEL
         LA    LLNREG,KTWO(LLNREG)     DISP+2 FOR QUOTE AND BLANK
         LA    TLNREG,KTWO(TLNREG)     TOTAL LENGTH + 2
         BR    WKREG                   BR TO MAIN ROUTINE
*                                                                A42948
*THE FOLLOWING SECTION OF CODE MOVES IN A LEFT PAREND AND THE    A42948
*MEMBER NAME                                                     A42948
*                                                                A42948
ME002505 BCTR  LLNREG,D0               DECREMENT DISPLACEMENT    A42948
         MVI   D0(LLNREG),MELPAREN     MOVE IN LEFT PAREND       A42948
         LA    LLNREG,D1(LLNREG)       UPDATE DISPLACEMENT       A42948
         LH    LENREG,MEPCE1+D12       GET LENGTH OF NAME        A42948
         L     MCMDREG,MEPCE1+KEIGHT   GET DISPLACEMENT OF NAME  A42948
         BAL   WKREGB,ME002525         PERFORM ACTUAL MOVE       A42948
         BR    WKREG                   BR TO CALLER              A42948
         EJECT
**************************************************************** A45104
*                                                                A45104
*   THE FOLLOWING SECTION OF CODE SETS THE 'NUM' OR 'NONUM'      A45104
*   (ACCORDING TO THE LINE NUMBER STATUS OF THE EDIT DATA SET)   A45104
*   WHEN BOTH OF THE FOLLOWING CONDITIONS ARE TRUE --            A45104
*                                                                A45104
*       1.  NEITHER THE 'NUM' NOR THE 'NONUM' KEYWORD WAS        A45104
*           ON THE SUBCOMMAND.                                   A45104
*                                                                A45104
*       2.  '*' WAS SPECIFIED (OR DEFAULTED) AS 'DATA SET NAME'  A45104
*           ON THE SUBCOMMAND.                                   A45104
*                                                                A45104
**************************************************************** A45104
ME002580 EQU   *                                                 A45104
         CLI   MEPCE6+D1,KONE          CHECK IF NUM/NONUM KEYWD  A45104
*                                      ENTERED ON SUBCOMMAND     A45104
         BCR   GE,WKREGB               IF EITHER SPECIFIED,      A45104
*                                      RETURN TO CALLER          A45104
         L     WKREG,MEPCE1            OBTAIN PDE ADDRESS FOR    A45104
*                                      'DATA SET NAME'           A45104
         CLI   D0(WKREG),MEASTRK       CHECK IF DSNAME='*'       A45104
         BCR   NE,WKREGB               IF NOT, RETURN TO CALLER  A45104
         TM    CACFLAG1,CANONUM        CHECK IF EDIT DATA SET    A45104
*                                      IS 'NONUM'                A45104
         BO    ME002585                IF SO, BRANCH TO SET      A45104
*                                      'NONUM' KEYWORD           A45104
         L     WKREG,MESAVE            OBTAIN ADDRESS OF NUM2    A45104
*                                      KEYWORD (IN MODEL BUFFER) A45104
         LA    WKREG,D4(,WKREG)        OBTAIN ADDRESS OF NUM2    A45104
*                                      KEYWORD'S SUBFIELD        A45104
         LR    LENREG,LLNREG           MAKE COPY OF CURRENT BFR  A45104
*                                      ADDRESS                   A45104
         SR    LENREG,WKREG            COMPUTE LENGTH OF SUBFLD  A45104
         BCTR  LENREG,D0               ADJUST LENGTH FOR MVC     A45104
         MVC   D0(KTHREE,LLNREG),MENUM MOVE 'NUM' KEYWORD TO     A45104
*                                      MODEL                     A45104
         EX    LENREG,MEMVCNUM         COPY NUM2 SUBFIELD AFTER  A45104
*                                      NUM KEYWORD               A45104
         LA    LLNREG,KFOUR(LENREG,LLNREG) INCREMENT             A45104
*                                      DISPLACEMENT IN BFR       A45104
         LA    TLNREG,KFOUR(LENREG,TLNREG) INCREMENT TOTAL       A45104
*                                      LENGTH OF BFR             A45104
         BR    WKREGB                  RETURN TO CALLER          A45104
ME002585 EQU   *                                                 A45104
         MVC   D7(KFIVE,LLNREG),MENONUM MOVE NONUM KEYWORD TO    A45104
*                                      MODEL BUFFER              A45104
         LA    TLNREG,KFIVE(,TLNREG)   INCREMENT BUFFER LENGTH   A45104
         BR    WKREGB                  RETURN TO CALLER          A45104
MEMVCNUM MVC   D3(*-*,LLNREG),D0(WKREG) MOVE SUBFIELD AFTER NUM  A45104
*                                      KEYWORD                   A45104
         EJECT                                                   A45104
***********************************************************************
*                                                                     *
*THE FOLLOWING MACROS SET UP PARMLISTS FOR ALL MESSAGES PUT OUT BY    *
*IKJEBEME.                                                            *
*                                                                     *
***********************************************************************
ME002600 EQU   *
         MVC   MELEV1(KTWO),MEM550     SET UP LEVEL1 PLIST
         B     ME003200                BR TO PUT OUT MSG
         SPACE
ME002700 EQU   *
         OI    MEDAALP,MEFORTY         SET EMPTY DS SWITCH ON
         MVC   MELEV1(KTWO),MEM501     SET UP LEVEL1 PLIST
         B     ME003200                BR TO PUT OUT MSG
         SPACE
ME002800 EQU   *
         MVC   MELEV1(KTWO),MEM312     SET UP LEVEL1 PLIST
         LA    WKREG,MEL1IN1           ADDR OF INSERTION PLIST
         ST    WKREG,MEIN1PT           CHAIN PLISTS
         MVC   MEL1IN1(MEMG1L),MEMG1   LEVEL1 INSERTION
         B     ME003200                BR TO PUT OUT MSG
         SPACE
ME002900 EQU   *
         MVC   MEL1IN1(MEMG3L),MEMG3   SET UP INSERTION PLIST
         B     ME003100                BR TO FINISH PLIST
         SPACE
ME003000 EQU   *
         MVC   MEL1IN1(MEMG4L),MEMG4   SET UP INSERTION PLIST
ME003100 EQU   *
         MVC   MELEV1(KFOUR),MEM313    SET UP LEVEL1 PLIST
         LA    WKREG,MEL1IN1           ADDR OF INSERTION PLIST
         ST    WKREG,MEIN2PT           CHAIN PLISTS
         LA    WKREG,MEL2IN2           ADDR OF INSERTION PLIST
         ST    WKREG,MEL1IN1           CHAIN PLISTS
         MVC   MEL2IN2(MEMG2L),MEMG2   LEVEL2 INSERTION2
         SRL   RETCDREG,KONE           ADJUST R15 FOR INDEIXING
         LH    WKREG,MECDTAB(RETCDREG) PICK UP PRINTABLE CHARACTER
         STH   WKREG,MEIN2             STORE IN INSERT2
ME003200 EQU   *
         IKJEBEML ,,,,MF=(E,MEPARMM)   ML MACRO  E FORM
         SPACE
         TM    MEDAALP,MEFORTY         IS EMPTY DS SW ON
         BNZ   ME003275                BR YES
ME003250 EQU   *
         LA    WKREG,KEIGHT            SET RETURN CODE TO 8
         STH   WKREG,MECODE            SAVE RC
         B     ME001800                BR TO COMPLETE
ME003275 EQU   *
         NI    MEDAALP,MEALL-MEFORTY   TURN EMPTY DS SW OFF RC=0
         B     ME001800                RETURN TO MAIN ROUTINE
         SPACE
ME003300 EQU   *
         LA    WKREG,KTWELVE           SET RETURN CODE = 12
         STH   WKREG,MECODE            SAVE RETURN CODE
         B     ME001800                BR TO TERMINATE
         SPACE
***********************************************************************
         SPACE
         IKJEBEMI (312,313,501,550)    MESSAGE EQUATE MACRO
*
         SPACE
MEMSGADR DC    V(IKJEBMEM)             ADDR OF MSG CSECT
         SPACE
IKJEBMEM CSECT
         SPACE
MEM312   EQU   *
         DC    AL2(M312)               REL LEV1 MSG NUMBER
         SPACE
MEM313   EQU   *
         DC    AL2(M313)               REL LEV1 MSG NUMBER
         DC    AL2(M3131)              REL LEV2 MSG NUMBER
         SPACE
MEM550   EQU   *
         DC    AL2(M550)               REL LEV1 MSG NUMBER
         SPACE
MEM501   EQU   *
         DC    AL2(M501)               REL LEV1 MSG NUMBER
         SPACE
         DS    0F
MEMG1    EQU   *
         IKJEBEMG 0,M312IN1,'MERGE'
MEMG1L   EQU   *-MEMG1
         SPACE
         DS    0F
MEMG2    EQU   *
         IKJEBEMG 0,M3131IN2,'00'
MEMG2L   EQU   *-MEMG2
         SPACE
         DS    0F
MEMG3    IKJEBEMG 0,M3131IN1,'PARSE'
MEMG3L   EQU   *-MEMG3
         SPACE
         DS    0F
MEMG4    IKJEBEMG 0,M3131IN1,'PUTLINE'                           A50476
MEMG4L   EQU   *-MEMG4
         SPACE
         DS    0F
IKJEBEME CSECT
         SPACE
MECDTAB  DS    0H
         DC    C'  040812'             TABLE OF PRINTABLE CHARACTERS
         DC    C'162024'               EXTENSTION OF TABLE
         SPACE
MEMERGE  DC    CL6'MERGE '             MERGE OPERAND FOR MODEL
MERENUM  DC    CL5'RENUM'              RENUM OPERAND FOR MODEL
MENONUM  DC    CL6'NONUM '             NONUM OPERAND FOR MODEL   A45104
MENONUM2 DC    CL7'NONUM2 '            NONUM2 OPERAND FOR MODEL  A45104
MENUM    DC    C'NUM('                 NUM OPERAND FOR MODEL
MENUMLNP DC    CL6' '                  POSITION FOR LINE NUMBER  A45104
MENUMLNL DC    CL1' '                  LENGTH FOR LINE NUMBER    A45104
MENUMEND DC    C') '                   TRAILER FOR NUM MODEL     A45104
MENUMLEN EQU   *-MENUM                 LENGTH OF MODEL ENTRY     A45104
MENUM2   DC    CL5'NUM2('              NUM2 OPERAND FOR MODEL    A45104
MERPB    DC    C') '                   RIGHT PAREND AND BLANK    A42948
MERPBQ   DC    C')'' '                 PAREND QUOTE AND BLANK    A42948
MEQB     DC    C''' '                  QUOTE AND BLANK FOR DSN MOVE
MEBASIC  DC    C' BASIC'               BASIC OPERAND FOR MODEL
MEVBASIC DC    C' VSBASIC'             VSBASIC OPERAND FOR MODEL Y02676
         DS    0F
MEPCLADR DC    V(IKJEBME0)             ADDRESS OF PCL
         EJECT
IKJEBME0 IKJPARM  DSECT=MEPCLL         BEGINNING OF PCL
         SPACE
MEPCE1   IKJPOSIT DSTHING,PROMPT='''FROM'' DATA SET NAME',             X
               HELP=('THE NAME OF A DATA SET FROM WHICH DATA WILL BE TAX
               KEN AS INPUT TO THE MERGE.')
         SPACE
MEPCE2   IKJIDENT 'LINE NUMBER',MAXLNTH=8,FIRST=NUMERIC,OTHER=NUMERIC
         SPACE
MEPCE3   IKJIDENT 'LINE NUMBER',MAXLNTH=8,FIRST=NUMERIC,OTHER=NUMERIC
         SPACE
MEPCE4   IKJIDENT 'LINENUMBER OR *',ASTERISK,MAXLNTH=8,                X
               FIRST=NUMERIC,OTHER=NUMERIC,DEFAULT='*'
         SPACE
MEPCE5   IKJKEYWD
         SPACE
         IKJNAME  'RENUM',SUBFLD=MEPCS1
         SPACE
MEPCE6   IKJKEYWD
         IKJNAME 'NUM',SUBFLD=MEPCS2                             A45104
         IKJNAME 'NONUM'                                         A45104
         SPACE
MEPCS1   IKJSUBF
         SPACE
MEPCS11  IKJIDENT 'LINE NUMBER',MAXLNTH=8,FIRST=NUMERIC,OTHER=NUMERIC
         SPACE
MEPCS12  IKJIDENT 'INCREMENT',MAXLNTH=8,FIRST=NUMERIC,OTHER=NUMERIC
         SPACE
MEPCS2   IKJSUBF
MEPCS21  IKJIDENT 'LINE NUMBER POSITION',MAXLNTH=5,              A45104X
               FIRST=NUMERIC,OTHER=NUMERIC                       A45104
MEPCS22  IKJIDENT 'LINE NUMBER LENGTH',MAXLNTH=1,FIRST=NUMERIC   A45104
         IKJENDP
         EJECT
***********************************************************************
COMMAREA IKJEBECA                      COMMUNICATION AREA
         EJECT
***********************************************************************
         ORG   CASCWKA                 WORK AREA
         SPACE
MEPARMM  DS    10F                     MESSAGE PARMLIST
MELEV1   EQU   MEPARMM                 LEVEL1
MEIN1PT  EQU   MELEV1+4                PTR TO 1ST LEVEL INSERTION
MEIN2PT  EQU   MELEV1+8                PTR TO LEV2 INSERTION
MEL1IN1  EQU   MELEV1+12               LEVEL1 INSERTION
MEIN1L   EQU   MEL1IN1+4               LENGTH OF LEVEL1 INSERTION
MEIN1    EQU   MEIN1L+4                LEVEL1 INSERTION
MEEND    EQU   MEIN1+8                 END OF INSERT1 PLIST
MEL2IN2  EQU   MEEND                   LEVEL2 INSERTION2
MEIN2L   EQU   MEL2IN2+4               LENGTH OF INSERTION2
MEIN2    EQU   MEIN2L+4                INSERTION2
         SPACE
MEUTPLST DS    F                       UT PARMLIST
MEUTWD2  DS    F                       RESERVED
MEUTWD3  DS    F                       BYTE1=X'80'  PARMLIST DELIMITER
         SPACE
MEDAALP  DS    X                       FLOW CONTROL SWITCH
*                                      X'80'=QSAM ALLOC DONE
*                                      X'40'=DS IS EMPTY
         SPACE
MECIPLST DS    2F                      COMMAND INVOKER PARMLIST
         SPACE
MECODE   DS    H                       RETURN CODE
MESAVE   DS    F                       USED TO SAVE AN ADDRESS   A45104
*                                      OF DATA IN MODEL BUFFER   A45104
         SPACE
         ORG   CABFRPL                 WORK AREA
         SPACE
MCMLEN   DS    H                       LENGTH OF MODEL COMMAND BUFFER
MCMRES   DS    H                       RESERVED
MCMMRG   DS    CL6                     MERGE OPERAND
MECMOPDS DS    CL146                   OPERANDS FOR MODEL COMMAND
         SPACE
         DS    0D
MEPACK   DS    D                       AREA FOR CONVERTING LINENUM3
MELNUM   DS    D                       AREA FOR UNPACKING LINENUM3
         EJECT
         IKJPPL                        PARSE PARMLIST DSECT
         END
