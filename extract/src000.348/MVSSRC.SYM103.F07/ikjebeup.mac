BEUP     TITLE 'EDIT SUBCOMMAND - UP - IKJEBEUP'
***********************************************************************
* STATUS-  VERSION NO. 01, OS/360 RELEASE NO. 20                      *
*                                                                     *
* FUNCTION-  IKJEBEUP IS AN EDIT SUBCOMMAND WHICH IS USED TO CAUSE THE*
*    CURRENT LINE POINTER '*' TO BE MOVED TOWARD THE BEGINNING OF THE *
*    DATA SET.  IF THE OPERATION OF THIS SUBCOMMAND CAUSES THE        *
*    CURRENT LINE POINTER TO REACH THE FIRST RECORD OF THE DATA SET   *
*    OR IF THIS SUBCOMMAND IS ISSUED WITH THE '*' VALUE ALREADY       *
*    POINTING AT THE FIRST LINE OF THE DATA SET, THE USER WILL BE     *
*    NOTIFIED. IF DURING THE PROCESS OF READING THE UTILITY DATA SET, *
*    ATTENTION IS ISSUED BY THE TERMINAL USER, CONTROL WILL BE        *
*    RETURNED TO THE CONTROLLER IMMEDIATELY WITH A RETURN CODE OF     *
*    ZERO IN REGISTER 15.                                             *
*                                                                     *
* ENTRY POINTS-  IKJEBEUP IS THE MAIN AND ONLY ENTRY POINT            *
*                                                                     *
* INPUT-  REGISTER 1 CONTAINS A POINTER TO EDIT'S COMMUNICATION AREA. *
*    A PART OF OR THE WHOLE UTILITY DATA SET IS READ AS REQUIRED.     *
*                                                                     *
* OUTPUT-  A RETURN CODE IN REGISTER 15. POSSIBLE RETURN CODES ARE-   *
*    0 - OPERATION WAS SUCCESSFUL.                                    *
*    8 - STACK NEEDS FLUSHING                                         *
*    12 - I/O ERROR                                                   *
*                                                                     *
* EXTERNAL REFERENCES-                                                *
*         IKJPARS      -SCANS OPERAND                                 *
*         IKJEBEUT     -READS UTILITY DATA SET                        *
*         IKJEBEMS     -MESSAGES TO USER AND LINE VERIFICATION        *
*    EDIT MACROS USED-                                                *
*         IKJEBESH     -PROVIDES LINKAGE TO EDIT SERVICE ROUTINES     *
*         IKJEBEML     -STANDARD MESSAGE TO USER                      *
*         IKJEBEMG     -MESSAGE INSERTION                             *
*         IKJEBECA     -LAYOUT AND DESCRIPTION OF COMMUNICATION AREA  *
*         IKJEBESV     -STANDARD ENTRY LINKAGE AND ADDRESSABILITY     *
*         IKJEBERT     -STANDARD EXIT LINKAGE                         *
*    SYSTEM MACROS USED-                                              *
*         LINK         -CALLS IKJPARS                                 *
*         IKJPARM      -GENERATE PCL CSECT                            *
*         IKJIDENT     -GENERATE COUNT FIELD IN PCL                   *
*         IKJENDP      -CLOSE PCL CSECT                               *
*         IKJRLSA      -RELEASE PDL CORE                              *
*         SAVE         -SAVE PARSE'S REGISTERS                        *
*         RETURN       -RETURN TO PARSE                               *
*         IKJPPL       -PARSE PARAMETER LIST                          *
*                                                                     *
* EXITS,NORMAL-  STANDARD EXIT TO THE CONTROLLER WITH RETURN CODE = 0 *
*    IN REGISTER 15.                                                  *
*                                                                     *
* EXITS,ERROR-  STANDARD EXIT TO THE CONTROLLER WITH RETURN CODE = 12 *
*    OR 8 IN REGISTER 15.                                             *
*                                                                     *
* TABLES/WORK AREAS-  THE EDIT SUBCOMMAND WORK AREA IN THE COMMUNCA-  *
*    TION AREA IS USED                                                *
*                                                                     *
* ATTRIBUTES-  REFRESHABLE, ENABLED, NON-PRIVILEGED                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY-  THIS SUBCOMMAND IS CHARACTER CODE       *
*    DEPENDENT AND HAS TO BE REASSEMBLED WHEN CHARACTER SET CODE      *
*    CHANGES.                                                         *
*                                                                     *
* NOTES-  STANDARD LINKAGE CONVENTIONS ARE USED                       *
***********************************************************************
IKJEBEUP CSECT
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC REGISTERS
***********************************************************************
PARMREG0 EQU   0                       PARAMETER POINTER
PARMREG1 EQU   1                       PARAMETER POINTER
TESTREG  EQU   2                       WORK REGISTER
BINREG   EQU   3                       WORK REGISTER
ADDREG   EQU   4                       WORK REGISTER
DCBREG   EQU   5                       WORK REGISTER
WORKREG  EQU   6                       WORK REGISTER
PDLREG   EQU   7                       POINTER TO PDL
UTREG    EQU   8                       PARAMETERS FOR IKJEBEUT POINTER
COMMREG  EQU   9                       COMMUNICATION AREA ADDRESSING
TEMPREG  EQU   10                      WORK REGISTER
BASEREG  EQU   11                      BASE REGISTER
DATAREG  EQU   12                      WORK REGISTER
SAVEREG  EQU   13                      SAVE AREA REGISTER
RETREG   EQU   14                      RETURN TO CALLER
RETCDREG EQU   15                      RETURN CODE
         SPACE 2
***********************************************************************
*    EQUATES OF SYMBOLIC CONSTANTS
***********************************************************************
UP0      EQU   0                       CONSTANT OF 0
UP1      EQU   1                       CONSTANT OF 1
UP3      EQU   3                       CONSTANT OF 3
UP4      EQU   4                       CONSTANT OF 4
UP8      EQU   8                       CONSTANT OF 8
UP12     EQU   12                      CONSTANT OF 12
UP16     EQU   16                      CONSTANT OF 16
END      EQU   X'80'                   END OF PARAMETER LIST CODE
FIRST    EQU   X'04'                   READ 1ST RECORD CODE
PREV     EQU   X'01'                   READ PREVIOUS RECORD CODE
SW1      EQU   X'80'
ALL      EQU   X'FF'
         SPACE 2
***********************************************************************
*    MESSAGE REFERENCES
***********************************************************************
         IKJEBEMI (312,313,501,505)
         EJECT
***********************************************************************
*    STANDARD ENTRY LINKAGE AND ESTABLISHMENT OF ADDRESSABILITY BOTH
*    IN THIS ROUTINE AND IN THE COMMUNICATION AREA
***********************************************************************
         IKJEBESV (14,12),T,*
         LR    COMMREG,PARMREG1
         USING IKJEBECA,COMMREG
         MVI   SW,UP0                  CLEAR SWITCH
         SPACE 2
***********************************************************************
*    OPERAND TEST
***********************************************************************
         TM    CAPTIBFR,CAOPERND       TEST WHETHER THERE OPERANDS
         BNO   DEFAULT                 NO OPERAND, THEN GO TO DEFAULT
         SPACE 2
***********************************************************************
*    CALL IKJPARSE TO SCAN OPERAND
***********************************************************************
OPERAND  LA    PARMREG1,CATMPLST       LET REG. 1 PT. TO TMP SERVICE
*                                      RTN. PARAMETER LIST
         MVC   CATMPLST+PPLPCL-PPL(UP4),ADUPPCL  PUT POINTER TO PCL IN
*                                                PPLPCL
         LA    TESTREG,CAPTPRSD
         ST    TESTREG,CATMPLST+PPLANS-PPL  PUT POINTER TO ANS. PLACE
*                                           IN PPLANS
         MVC   CATMPLST+PPLCBUF-PPL(UP4),CAPTIBFR  PUT ADDRESS OF
*                                      INPUT BUFFER IN PPLCBUF
         ST    COMMREG,CATMPLST+PPLUWA-PPL  PUT POINTER TO COMM. AREA
*                                           IN PPLVWA
         LINK  EP=IKJPARS
         SPACE 2
***********************************************************************
*    CHECK RETURN CODES FROM PARSE SERVICE ROUTINE AND ESTABLISH
*    ADDRESSABILITY IN PDL DSECT
***********************************************************************
         L     PDLREG,CAPTPRSD         PUT PTR. TO PDL IN PDLREG
         USING UPPDL,PDLREG            ADDRESSABILITY IN PDL
         LTR   RETCDREG,RETCDREG       CHECK RET. CODE FROM PARSE
         BZ    CONV                    IF SUCCESSFUL COMPLETION THEN GO
*                                      TO CONV
         C     RETCDREG,UPDC4          TEST WHETHER R.C.=4
         BE    FLUSH                   R.C.=4 THEN RET. TO FLUSH
         C     RETCDREG,UPDC8          TEST WHETHER R.C.=8
         BE    NORMAL                  R.C.=8 THEN RET. TO CONTROLLER
         C     RETCDREG,UPDC12         TEST WHETHER R.C.=12
         BE    MSG5                    R.C.=12 THEN GO TO ISSUE MSG5
         C     RETCDREG,UPDC16         TEST WHETHER R.C.=16
         BE    MESSAGE3                R.C.=16 THEN GO TO ISSUE MSG3
         SPACE 2
***********************************************************************
*    CONVERSION OF OPERAND 'COUNT' TO BINARY
***********************************************************************
CONV     L     TESTREG,COUNT           LET TESTREG CONT. ADDRESS OF
*                                      OPERAND
         LH    ADDREG,COUNT+UP4        PUT LENGTH OF OPERAND IN 2
*                                      LOW ORDER BYTES OF ADDREG
         BCTR  ADDREG,PARMREG0         LENGTH OF OPERAND - 1
         EX    ADDREG,PACKED           CONVERT OPERAND FROM ZONED
*                                      DECIMAL TO PACKED DECIMAL
         CVB   BINREG,COPACKED
         SPACE 2
***********************************************************************
*    CALL IKJEBEUT THROUGH IKJEBESH TO READ 1ST RECORD OF UTILITY
*    DATA SET
***********************************************************************
READFRST MVC   COWORD1(UP4),CAPTCDCB   PUT ADDRESS OF UTILITY DCB IN 3
*                                      LOW ORDER BYTES OF 1ST
*                                      PARAM WORD
         MVI   COWORD1,FIRST           PUT READ 1ST RECORD CODE IN
*                                      HIGH ORDER BYTE OF 1ST PARAM
*                                      WORD
         XC    COWORD2(UP4),COWORD2    NO POINTER IS PUT IN 2ND PARAM
*                                      WORD
         LA    TESTREG,CATEMPBF+UP12
         ST    TESTREG,COWORD3         PUT ADDRESS OF BUFFER POOL IN
*                                      3RD PARAM WORD
         MVI   COWORD3,END             PUT END OF PARAMETER LIST
*                                      CODE IN HIGH ORDER BYTE OF
*                                      3RD PARAM WORD
         LA    UTREG,COWORD1           LET UTREG PT. TO PARMLIST
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A)
*                                      SERVICE RTN. HANDLER MACRO
         SPACE 2
***********************************************************************
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING 1ST RECORD
***********************************************************************
         LTR   RETCDREG,RETCDREG       TEST RETURN CODE FROM IKJEBEUT
         BZ    CONT1                   BRANCH IF ZERO TO CONT1
         C     RETCDREG,UPDC4          TEST WHETHER R.C.=4
         BE    EMPTY                   BRANCH IF EQUAL TO EMPTY
IOERR    LA    RETCDREG,UP12           SET R.C. TO 12
         SPACE 2
***********************************************************************
*    STANDARD EXIT LINKAGE TO CONTROLLER
***********************************************************************
EXIT     TM    CAPRSPDL,CAFREEDL       TEST WHETHER PARSE PDL REQUIRES
*                                      FREEMAIN
         BNZ   RET                     PDL NOT EXIST THEN RETURN TO
*                                      CONTROLLER
         IKJRLSA CAPRSPDL
         OI    CAPRSPDL,CAFREEDL       TURN ON THE BIT TO INDICATE PDL
*                                      WAS FREED
RET      EQU   *
         IKJEBERT (14,12),T,RC=(15)
         SPACE 2
***********************************************************************
*    IF NO OPERAND IS SPECIFIED IN THE SUBCOMMAND, THEN IT IS DEFAULTED
*    TO 1
***********************************************************************
DEFAULT  LA    BINREG,UP1
         B     READFRST                BRANCH TO READ 1ST RECORD OF
*                                      UTILITY DATA SET
         SPACE 2
***********************************************************************
*    DATA SET IS EMPTY
***********************************************************************
EMPTY    MVC   B(UP12),INLIST1
         IKJEBEML M501,MF=(E,B)        ISSUE MESSAGE1- 'NO LINES IN
*                                      DATA SET'
         XC    CACURNUM(UP4),CACURNUM  SET CURRENT LINE NUMBER TO ZERO
NORMAL   SR    RETCDREG,RETCDREG       SET RETURN CODE TO ZERO
         B     EXIT                    BRANCH UNCOND. TO CHECK WHETHER
*                                      PARSE WAS CALLED
FLUSH    LA    RETCDREG,UP8            INDICATE STACK NEEDS FLUSHING
         B     EXIT                    BRANCH TO DO SOME CHECKS
         SPACE 2
***********************************************************************
*    READ PREVIOUS RECORD OF DATA SET STARTING WITH THE RECORD PREVIOUS
*   TO THE CURRENT ONE
***********************************************************************
CONT1    MVC   COKEYA(UP4),CACURNUM    PUT '*' VALUE TO COKEYA IN
*                                      SUBCMD. WORKAREA
         LR    WORKREG,BINREG          PUT OPERAND 'COUNT' IN WORKREG
READ     MVI   COWORD1,PREV            PUT READ PREVIOUS RECORD CODE
*                                      IN HIGH ORDER BYTE OF 1ST
*                                      PARAM WORD
         LA    TESTREG,COKEYA          LET 2ND PARAM WORD POINT TO
         ST    TESTREG,COWORD2         COKEYA
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A)
*                                      SERVICE RTN. HANDLER MACRO
         SPACE 2
***********************************************************************
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING PREVIOUS RECORD
***********************************************************************
         LTR   RETCDREG,RETCDREG       TEST RETURN CODE FROM IKJEBEUT
         BZ    CONT2                   BRANCH IF ZERO TO CONT2
         C     RETCDREG,UPDC4          TEST WHETHER R.C.=4
         BE    TOP                     BRANCH ON EQUAL TO TOP
         B     IOERR                   R.C.=8 THEN GO TO IOERR
         SPACE 2
***********************************************************************
*    ISSUE MESSAGE2 'TOP OF DATA SET' WHENEVER FIRST RECORD OF DATA SET
*    IS REACHED
***********************************************************************
TOP      OI    SW,SW1                  TURN SW1 ON
ISSUE1   MVC   B(UP12),INLIST2         MESSAGE2- 'TOP OF DATA SET'
         IKJEBEML M505,MF=(E,B)
         B     SET                     GO TO SET * VALUE
CONT2    BCT   WORKREG,LOOP            SUB 1 FROM WORKREG AND GO
*                                      TO LOOP IF CONTENT OF
*                                      WORKREG NOT =0
         SPACE 2
***********************************************************************
*    '*' VALUE IS SET TO LAST LINE READ
***********************************************************************
SET      MVC   CACURNUM(UP4),CATEMPBF+UP12  SET '*' VALUE TO KEY OF
*                                      LAST LINE REFERENCED
VERIFY   OI    CACFLAG1,CALNTOVF       TURN THE LINE TO BE VERIFIED
*                                      SWITCH ON
         SPACE 2
***********************************************************************
*    IF 'TOP OF DATA SET' HAS NOT BEEN ISSUED, READ A RECORD PREVIOUS
*    TO THE ONE WITH THE '*' VALUE AGAIN
***********************************************************************
TEST     TM    SW,SW1                  TEST WHETHER SW1 IS ON
         BO    TURNOFF1                SW1 IS ON THEN GO TO TURNOFF1
         MVC   COKEYA(UP4),CATEMPBF+UP12
         MVI   COWORD1,PREV
         LA    TESTREG,COKEYA
         ST    TESTREG,COWORD2
         MVI   COWORD2,END
         IKJEBESH (COMMREG),IKJEBEUT,PARAM=((COMMREG),(UTREG)),MF=(E,A)
         SPACE 2
***********************************************************************
*    CHECK RETURN CODE FROM IKJEBEUT AFTER READING ONE MORE RECORD
***********************************************************************
         LTR   RETCDREG,RETCDREG
         BZ    NORMAL                  R.C.=0 THEN GO TO NORMAL
         C     RETCDREG,UPDC4
         BNE   IOERR                   R.C. NOT =0,4 THEN GO TO IOERR
ISSUE2   MVC   B(UP12),INLIST2
         IKJEBEML M505,MF=(E,B)
         B     NORMAL                  BRANCH UNCOND. TO NORMAL
*                                      RETURN TO CONTROLLER
TURNOFF1 NI    SW,ALL-SW1
         B     NORMAL                  BRANCH UNCOND. TO NORMAL RET.
*                                      TO CONTROLLER
LOOP     TM    CAATTN,CAATTNIS         TEST WHETHER ATTN IS ISSUED
         BO    NORMAL                  BRANCH ON ATTN ISSUED TO
*                                      NORMAL RETURN TO CONTROLLER
NO       MVC   COKEYA(UP4),CATEMPBF+UP12  SET VALUE OF COKEYA TO KEY
*                                      OF LAST LINE READ
         B     READ                    BRANCH TO READ ANOTHER RECORD
         SPACE 2
***********************************************************************
*    MESSAGE3- 'NOT ENOUGH MAIN STORAGE TO EXECUTE UP' IS
*    ISSUED HERE
***********************************************************************
MESSAGE3 MVC   B(UP12),INLIST3
         IKJEBEML M312,,SUBCMD,,MF=(E,B)
         B     FLUSH                   BRANCH TO SET R.C. TO 8 AND
*                                      RETURN CONTROL TO CONTROLLER
MSG5     EQU   *
         SPACE 2
***********************************************************************
*    MSG5- 'COMMAND SYSTEM ERROR+  IKJPARS ERROR CODE 12' IS ISSUED
*    HERE
***********************************************************************
         MVC   B(UP12),INLIST5
         IKJEBEML M313,M3131,,INS12,MF=(E,B)
         B     FLUSH                   BRANCH TO SET R.C. TO 8 AND
*                                      RETURN TO CONTROLLER
         SPACE 2
***********************************************************************
*    VALIDITY CHECKING ROUTINE FOR UP OPERAND
***********************************************************************
UVALID   SAVE  (14,12),T,*             SAVE PARSE'S REGISTERS
         BALR  TEMPREG,UP0             ESTABLISH ADDRESSABILITY
         USING *,TEMPREG               ADDRESSABILITY IN VALIDITY
*                                      CHECKING ROUTINE
         L     TESTREG,UP0(PARMREG1)   PUT POINTER TO COUNT IN TESTREG
         L     BINREG,UP0(TESTREG)     PUT COUNT IN BINREG
         LH    UTREG,UP4(TESTREG)      PUT LENGTH OF COUNT IN 2 LOW
*                                      ORDER BYTE OF UTREG
         BCTR  UTREG,PARMREG0          LENGTH OF COUNT - 1
         EX    UTREG,COMPARE           TEST WHETHER COUNT=0
         BE    EXIT1                   EQUAL TO ZERO THEN GO TO EXIT1
         SR    RETCDREG,RETCDREG       PUT R.C.=0 IN REG. 15
EXIT2    RETURN (14,12),T,RC=(15)      RETURN TO PARSE
EXIT1    LA    RETCDREG,UP4            PUT R.C.=4 IN REG. 15
         B     EXIT2                   BRANCH TO RET. TO PARSE
         SPACE 3
***********************************************************************
*    CONSTANT AREA FOR IKJEBEUP
***********************************************************************
UPDC4    DC    F'4'                    FULL WORD OF 4
UPDC8    DC    F'8'                    FULLWORD OF 8
UPDC12   DC    F'12'                   FULLWORD OF 12
UPDC16   DC    F'16'                   FULLWORD OF 16
ADUPPCL  DC    A(IKJEBUP0)             ADCON OF PCL
ZERO     DC    8C'0'                   CONSTANT OF ZERO
PACKED   PACK  COPACKED(UP8),UP0(UP0,TESTREG)
COMPARE  CLC   UP0(UP0,BINREG),ZERO
         SPACE 2
***********************************************************************
*    MESSAGE INSERTION MACRO - IKJEBEMG IS ISSUED HERE
***********************************************************************
SUBCMD   IKJEBEMG ,M312IN1,'UP'
INS12    IKJEBEMG CODE12,M3131IN1,'IKJPARS'
CODE12   IKJEBEMG 0,M3131IN2,'12'
         SPACE 2
***********************************************************************
*    LIST FORMS OF IKJEBEML MACRO
***********************************************************************
INLIST1  IKJEBEML M501,MF=L
INLIST2  IKJEBEML M505,MF=L
INLIST3  IKJEBEML M312,,SUBCMD,,MF=L
INLIST5  IKJEBEML M313,M3131,,INS12,MF=L
         SPACE 2
***********************************************************************
*    MACROS OF PARSE
***********************************************************************
         DS    0F
IKJEBUP0 IKJPARM DSECT=UPPDL
COUNT    IKJIDENT 'UP OPERAND',MAXLNTH=8,FIRST=NUMERIC,OTHER=NUMERIC,  *
               PROMPT='UP OPERAND',VALIDCK=UVALID
         IKJENDP
         SPACE 2
         IKJPPL
         SPACE 2
         IKJEBECA
         ORG   CASCWKA                 EDIT SUBCOMMAND WORKAREA
COPACKED DS    D                       DOUBLE WORD FOR PACKED DECIMAL
*                                      OF 'COUNT'
COWORD1  DS    F                       1ST PARAM WORD FOR UT
COWORD2  DS    F                       2ND PARAM WORD FOR UT
COWORD3  DS    F                       3RD PARAM WORD FOR UT
COKEYA   DS    F                       CURRENT REFERENCED VALUE FOR UT
A        DS    5F                      5 FULLWORDS FOR IKJEBESH MACRO
B        DS    10F                     10 FULLWORDS FOR IKJEBEML MACRO
SW       DS    X                       SWITCH FOR DET. WHETHER ONE MORE
*                                      RECORD SHOULD BE READ
         END
