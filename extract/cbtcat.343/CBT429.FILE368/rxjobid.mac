RXJOBID  TITLE 'REXX function return current jobid '
RXJOBID  CSECT
RXJOBID  AMODE 31
RXJOBID  RMODE ANY
*------------------------------------------------------*
*   Entry                                              *
*   Program could be useful to return your current     *
*   to the caller. Program should be RENT              *
*   INVOKE AS FOLLOWS                                  *
*   /* REXX */                                         *
*   X=RXJOBID()                                        *
*   SAY ""X""                                          *
*------------------------------------------------------*
         BAKR  R14,0               Save regs, ARs etc
         LR    R12,R15             Base register
         USING RXJOBID,R12
         MODID ,                   Eyecatcher
         LR    R10,R1              Save parmlist address
         STORAGE OBTAIN,LENGTH=WORKL
         LR    R11,R1              Save parmlist address
         ST    R1,8(,R13)          chain save areas
         ST    R13,4(,R1)          old into new
         LR    R13,R1              setup NEW SAVE/WORK AREA
         USING SAVEAREA,R13        address getmaing using
*------------------------------------------------------*
*   Get the Jobid                                      *
*------------------------------------------------------*
         L     R2,16                 Get CVT pointer
         L     R2,0(R2)              Get TCB pointer
         L     R2,4(R2)              Get TCB address
         L     R2,X'B4'(,R2)         Get JSCB  TCB+X'B4'
         L     R2,X'13C'(,R2)        Get JSCBSSIB JSCB+X'13C'
         LA    R2,X'C'(R2)           Get SSIBJBID SSIB+X'C'
         MVC   XJOBID(8),0(R2)       Move R2 to JOBID.
*------------------------------------------------------*
*   Set up the Evaluation block to return values to the*
*   caller                                             *
*------------------------------------------------------*
         USING EFPL,R10
         L     R5,EFPLEVAL
         L     R5,0(,R5)
         USING EVALBLOCK,R5
         MVC   EVALBLOCK_EVLEN,=X'00000008' MOVE LEN OF DATA
         MVC   EVALBLOCK_EVDATA(8),XJOBID   DATA TO EVALBLK
         SPACE
*------------------------------------------------------*
*   Free storage and return                            *
*------------------------------------------------------*
RETURN   EQU   *
         STORAGE RELEASE,ADDR=(11),LENGTH=WORKL
         SLR   R15,R15                      RC=0
         PR                                 Return
         EJECT
WORK     DSECT
SAVEAREA DS    18F
XJOBID   DS    CL8
WORKL    EQU   *-WORK
         IEESMCA                            SMC
         IRXEVALB                           REXX Eval block
         IRXEFPL                            Extended function parmlst
         REGS                               Regs
         END
