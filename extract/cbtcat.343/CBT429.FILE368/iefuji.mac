         TITLE 'IEFUJI ACF2 ACCOUNT VERIFICATION'
IEFUJI   CSECT
* STATUS - CHANGE LEVEL 9  AGLC 10-21-96
* STATUS - CHANGE LEVEL 8  AGLC 08-03-95
* STATUS - CHANGE LEVEL 7  AGLC 06-05-95
* STATUS - CHANGE LEVEL 6  AGLC 04-28-95
* STATUS - CHANGE LEVEL 5  AGLC 03-25-93
* STATUS - CHANGE LEVEL 4  AGLC 09-09-91
* STATUS - CHANGE LEVEL 3  AGLC 08-26-91
* STATUS - CHANGE LEVEL 2  AGLC 04-24-91
* STATUS - CHANGE LEVEL 1  IBM  ?
* FUNCTION
*    THIS MODULE CALCULATES THE AMOUNT OF TIME THAT A JOB WAS
*    ENQUEUED AWAITING INITIATION. THIS TIME AND THE EFFECTIVE JOB
*    PRIORITY ARE WRITTEN TO THE SMF DATA SET AS RECORD TYPE 128.
*    MODIFCATION ARE MADE TO THIS PROGRAM TO SUPPORT ACCOUNT
*    VERIFICATION AT THE JOB LEVEL. ALL NON PRODUCTION USES MUST
*    GET SUPPORT AT THE JOB LEVEL.
* ENTRY POINTS -
*         IEFUJI
* INPUT
*    REGISTER 1 POINTS TO A LIST OF 4 BYTE ADDRESSES FOR THE
*    FOLLOWING FOUR PARAMETERS:
*    1 - 36 BYTES OF THE FOLLOWING JOB INFORMATION:
*    JOBNAME (8 BYTES)
*    TIME STAMP (8 BYTES)
*    SYSTEM ID (4 BYTES)
*    USER ID (8 BYTES)
*    USER COMMUNICATIONS AREA (4 BYTES)
*    WHEN ENTERED, THIS FIELD CONTAINS THE TIME OF DAY
*    AT WHICH THE JOB WAS ENQUEUED (SUPPLIED BY THE
*    IEFUJV ROUTINE). THIS FIELD IS ZEROED BEFORE THE
*    ROUTINE IS EXITED.
*    2 - 20 BYTES CONTAINING THE PROGRAMMERS NAME
*    3 - 1 BYTE INDICATING THE EFFECTIVE JOB PRIORITY
*    4 - JOB ACCOUNTING FIELDS
* OUTPUT
*    A SMF RECORD IS WRITTEN TO THE SMF DATA SET USING THE
*    SMFWTM MACRO. THE FORMAT OF THIS RECORD IS DESCRIBED BY THE
*    DSECT FOR THE SMF RECORD.
*    REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING RETURN CODES:
*    0 - CONTINUE PROCESSING
*    4 - CANCEL
* EXTERNAL REFERENCES -  NONE
* EXITS,NORMAL -  RETURN TO CALLER     RC=0
* EXITS,ERROR - RETURN TO CALLER    RC=4 - CANCEL REQUEST
* TABLES/WORK AREAS -  DSECT FOR SMF RECORD EMPLOYED
* ATTRIBUTES - STANDARD
* CHARACTER CODE DEPENDENCY - NONE
* NOTES - GETMAIN FREEMAIN SMFWTM AND TIME MACROS WERE USED
*         ACF2 AND PSA MAPPING MACROS WERE USED
         EJECT
         ACFREGS
D0       EQU   0                       DISP OF 0
D12      EQU   12                      DISP OF 12
D16      EQU   16                      DISP OF 16
D20      EQU   20                      DISP OF 20
D31      EQU   31                      DISP OF 31
D32      EQU   32                      DISP OF 32
D97      EQU   97                      DISP OF 97
D110     EQU   110                     DISP OF 110
L1       EQU   1                       LENGTH OF 1
L4       EQU   4                       LENGTH OF 4
L6       EQU   6                       LENGTH OF 6
L7       EQU   7                       LENGTH OF 7
L8       EQU   8                       LENGTH OF 8
L16      EQU   16                      LENGTH OF 16
M2       EQU   2                       MASK OF 2
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
         USING *,R15
         B     HERE                    BRANCH AROUND EYECATCHERS
         DC    CL8'IEFUJI'             MODULE NAME
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
HERE     STM   R14,R12,D12(R13)        SAVE REGS
         DROP  R15
         BALR  R11,R0                  SET UP BASE
         USING *,R11                   REGISTER
         LM    R2,R4,D0(R1)            GET PARAMETER ADDRESSES
         L     R7,12(R1)               POINTS AT ACCOUNT FIELD
         L     R10,16(R1)              POINTS AT SUBSYS FIELD
         L     R8,0(R1)                POINTS AT JMR
         USING JMR,R8                  USING THE JMR
         GETMAIN   R,LV=SMFLEN         GET CORE FOR SMF RECORD   A48478
         LR    R5,R1
         USING SMFDSECT,R5             BASE FOR SMF DSECT
         GETMAIN   R,LV=WORKLEN        GET CORE FOR WTO PROCESS
         LR    R12,R1
         USING WTODSECT,R12            WTO DSECT DSECT
SMFBEGN  DS    0H
         TIME      BIN
         ST    R0,TIME                 TIME FOR SMF RECORD
         ST    R1,DATE                 DATE FOR SMF RECORD
         MVC   ENQTIME(L4),D32(R2)     GET JOB ENQUEUE TIME
         XC    D32(L4,R2),D32(R2)      ZERO COMMUNICATIONS WORD
         C     R0,ENQTIME              CHECK FOR A NEW DAY
         BC    M2,SAMEDAY              ENQUEUED SAME DAY
         A     R0,HR24                 ADD 24 HOURS (86400 SECONDS)
SAMEDAY  DS    0H
         S     R0,ENQTIME              DETERMINE TIME JOB WAS ON QUEUE
         ST    R0,ENQTIME              PUT TIME IN SMF RECORD
         MVC   PRTY(L1),D0(R4)         GET PRIORITY CODE
         MVC   SYSID(L4),D16(R2)       GET SYSTEM ID
         MVC   HEADER(L6),SMFRCD       GET HEADER
         MVC   JOBLOG(L16),D0(R2)      GET JOB LOG
*        MVC   USERID(L8),D20(R2)      GET USER ID
         MVC   CLASS(L1),D31(R2)       GET JOBCLASS
         L     R2,16                   GET CVT POINTER
         L     R3,0(R2)                GET TCB WORDS
         L     R4,12(R3)               GET CURRENT ASCB POINTER
         L     R6,172(R4)              GET POINTER TO JOBNAME
         LTR   R6,R6                   IS  IT BATCH
         BZ    RETURN0                 NO, EXIT
         ACFINCVT R3,NONE=RETURN0      FIND ACF2 CVT
         USING ACCVT,R3                ADDRESS IT
         ACFGUCB R4,NONE=RETURN0,      GET ACF2 ACUCB                  X
               INLINE=YES,SYS=AOS2
         USING ACUCB,R4                ADDRESS ACUCB
         L     R6,ACULRECP             POINT AT LOGONID RECORD
         USING LIDREC,R6               ADDRESS LOGONID
         MVC   JOBNAME,JMRJOB          MOVE JOBNAME FROM JMR
         MVC   USERID,LIDLID           POINT TO LOGONID
         MVC   ACFACCT,LIDACCT         MOVE ACF ACCOUNT NUMBER
         MVC   JOBACCT,2(R7)           GET JOB ACCOUNT NUMBER
         MVC   SUBSYS(4),0(R10)        SUBSYSTEM
         CLC   SUBSYS(4),=C'ASCH'      IS IT AN APPC JOB
         BE    RETURN0                 YES, EXIT
         CLC   SUBSYS(4),=C'OMVS'      IS IT AN OMVS JOB
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(4),=C'1111'     RESERVED
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(4),=C'2222'     APPC
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(4),=C'3333'     EMERGENCY BYPASS
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(4),=C'4444'     IS IT RJEJOB
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(4),=C'5555'     IS IT JOBTRAC OR OTHERS
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(7),=C'1550SYS'  IS IT CICS
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(7),=C'9999PCI'  IS IT CICS PRODREG
         BE    PCICCHK                 YES, VALIDATE
         CLC   ACFACCT(7),=C'9999TCI'  IS IT CICS TESTREG
         BE    DCICCHK                 YES, VALIDATE
*        CLC   ACFACCT(7),=C'1543TSM'  IS IT MVS TECH
*        BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(7),=C'9999MUS'  IS IT ROSCOE
         BE    RETURN0                 YES, EXIT
         CLC   ACFACCT(4),=C'7777'     NON-TSO OR NON-CICS BATCH
         BE    PSTCCHK                 YES, VALIDATE
*        CLC   ACFACCT(4),=C'8888'     IS IT TSO BATCH PRODUCTION*TMP*
*        BE    PTSOCHK                 YES, CHECK FOR TSO        *TMP*
         CLC   ACFACCT(4),=C'9999'     IS IT CICS BATCH PRODUCTION
         BE    PCICCHK                 YES, VALIDATE
AGLJOBN  DS    0H
         LR    R2,R7                   POINT AT JOB ACCOUNT NUMBER
         LA    R8,8                    MAXIMUM LENGTH OF LOGONID
         LA    R9,LIDLID               POINT TO LOGONID
LIDLOOP  CLI   0(R9),C' '              BLANK YET
         BE    LIDBLNK                 YES - STOP SCAN
         LA    R9,1(R9)                POINT TO NEXT CHARACTER
         BCT   R8,LIDLOOP              AND AROUND AGAIN
LIDBLNK  LA    R8,LIDLID               POINT TO LOGONID
         SR    R9,R8                   COMPUTE LENGTH OF LOGONID
         BCTR  R9,R0                   SUBTRACT ONE FOR EX INST
         CLC   LIDLID+6(1),=C'1'       CHECK FOR MULTIPLE LIDS
         BNL   LIDMULT                 HAS A MULTI-LID
LIDCOMP  EX    R9,COMPLID              COMPARE ON LOGONI
         BNE   BADNAM
AGLACCT  DS    0H
*        CLI   0(R2),X'01'             IS IT MORE THAN 1 ACCT FIELD
*        BNE   BADDEV                  YES, FAIL IT
*        CLI   1(R2),X'07'             IS ACCT FIELD SEVEN BYTES
*        BNE   BADDEV                  YES, FAIL IT
*        MVC   JOBACCT,2(R2)           GET JOB ACCOUNT NUMBER
         CLC   ACFACCT(4),=C'8888'     IS IT TSO BATCH PRODUCTION
         BE    PTSOCHK                 YES, PROD ACCOUNT CHECKS
         B     DTSOCHK                 NO, I/S ACCOUNT CHECKS
PSTCCHK  CLC   ACFACCT(4),JOBACCT      CHECK JOBCARD AND ACF2 EQUAL
         BNE   BADSTC                  YES, CHECK FOR GOOD STC ACCT
         CLC   CLASS,=C'F'             IS IT STC BTATCH CLASS F
         BE    RETURN0                 PASS ON THRU
         B     BADSTC
PTSOCHK  CLC   ACFACCT(4),JOBACCT      CHECK JOBCARD AND ACF2 EQUAL
         BNE   BADTSO                  YES, CHECK FOR GOOD TSO ACCT
         CLC   CLASS,=C'G'             IS IT FAST    CLASS G
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'C'             IS IT SLOW    CLASS C
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'R'             IS IT EMERGENCY
         BE    RETURN0                 PASS ON THRU
         B     BADTSO
PCICCHK  CLC   ACFACCT(4),JOBACCT      CHECK JOBCARD AND ACF2 EQUAL
         BNE   BADCIC                  YES, CHECK FOR GOOD CICS ACCT
         CLC   CLASS,=C'Q'             IS IT CICS BATCH CLASS Q
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'5'             IS IT SYS CICS JOBS
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'1'             IS IT SYS CICS JOBS
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'L'             IS IT SYS CICS JOBS  SPECIAL
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'M'             IS IT SYS CICS JOBS  SPECIAL
         BE    RETURN0                 PASS ON THRU
         B     BADCIC
DCICCHK  CLC   ACFACCT(4),JOBACCT      CHECK JOBCARD AND ACF2 EQUAL
         BNE   BADTIC                  YES, CHECK FOR GOOD CICS ACCT
         CLC   CLASS,=C'Z'             IS IT TCICS BATCH CLASS Z
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'1'             IS IT TCICS BATCH CLASS  1
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'2'             IS IT TCICS BATCH CLASS  2
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'3'             IS IT TCICS BATCH CLASS  3
         BE    RETURN0                 PASS ON THRU
         CLC   CLASS,=C'4'             IS IT TCICS BATCH CLASS  4
         BE    RETURN0                 PASS ON THRU
         B     BADTIC
DTSOCHK  CLC   ACFACCT(7),JOBACCT      CHECK JOBCARD AND ACF2 EQUAL
         BE    CISACCT                 YES, CHECK FOR GOOD CIS ACCT
         CLC   ACFACCT+4(3),=C'CIS'    IS IT CIS IN ACF2
         BE    BADCIS                  YES, FORMAT CIS  AND KILL
         CLC   ACFACCT+4(3),=C'CIT'    IS IT CIT IN ACF2
         BE    BADCIS                  YES, FORMAT CIS  AND KILL
         CLC   ACFACCT+4(3),=C'FIS'    IS IT FIS IN ACF2
         BE    BADCIS                  YES, FORMAT CIS  AND KILL
         CLC   ACFACCT+4(3),=C'FIT'    IS IT FIT IN ACF2
         BE    BADCIS                  YES, FORMAT CIS  AND KILL
         B     BADDEV                  NO,  FORMAT REGULAR AND KILL
CISACCT  CLC   ACFACCT+4(3),=C'CIS'    IS IT CIS IN ACF2
         BE    CHECK2                  YES,GO CHECK SPECIAL CLASS
         CLC   ACFACCT+4(3),=C'CIT'    IS IT CIT IN ACF2
         BE    CHECK2                  YES,GO CHECK SPECIAL CLASS
         CLC   ACFACCT+4(3),=C'FIS'    IS IT FIS IN ACF2
         BE    CHECK2                  YES,GO CHECK SPECIAL CLASS
         CLC   ACFACCT+4(3),=C'FIT'    IS IT FIT IN ACF2
         BE    CHECK2                  YES,GO CHECK SPECIAL CLASS
CHECKZ   CLC   CLASS,=C'Z'             REGULAR DEVELOPMENT  CLASS
         BNE   BADDEV                  NO, CANCEL IT
         B     RETURN0                 YES, GOOD BOY
CHECK2   CLC   CLASS,=C'2'             IS IT SPECIAL CLASS 2
         BE    RETURN0                 PASS ON THRU
CHECK3   CLC   CLASS,=C'3'             IS IT SPECIAL CLASS 3
         BE    RETURN0                 PASS ON THRU
CHECK4   CLC   CLASS,=C'4'             IS IT SPECIAL CLASS 4
         BE    RETURN0                 PASS ON THRU
BADCIS   MVC   WTODIS(WTOLENC),WTOSKELC
         B     RETURN4
BADNAM   DS    0H
         MVC   WTODIS(WTOLEN),WTOSKEL
         B     RETURN4
BADDEV   DS    0H
         MVC   WTODIS(WTOLEN2),WTOSKEL2
         B     RETURN4
BADTSO   DS    0H
         MVC   WTODIS(WTOLEN3),WTOSKEL3
         B     RETURN4
BADSTC   DS    0H
         MVC   WTODIS(WTOLEN4),WTOSKEL4
         MVC   WTODIS+33(1),=C'F'
         B     RETURN4
BADCIC   DS    0H
         MVC   WTODIS(WTOLEN4),WTOSKEL4
         MVC   WTODIS+33(1),=C'Q'
         B     RETURN4
BADTIC   DS    0H
         MVC   WTODIS(WTOLEN4),WTOSKEL4
         MVC   WTODIS+33(1),=C'Z'
         B     RETURN4
COMPLID  CLC   LIDLID(0),JOBNAME
LIDMULT  DS    0H
         BCTR  R9,R0                   SUBTRACT ONE FOR EX INST
         B     LIDCOMP
RETURN0  DS    0H
         LA    R10,0
         MVC   STATUS,=C'Y'
         B     EXIT
RETURN4  DS    0H
         LA    R10,4                   CAN BE CHANGED
         MVC   STATUS,=C'N'            NOT A GOOD JOBCARD
         MVC   WTODIS+18(7),LIDACCT
         MVC   WTODIS+48(7),LIDLID
         WTO   MF=(E,WTODIS)
EXIT     DS    0H
         LA    R1,SMFOUT               GET SMF BUFFER @
         SMFWTM    (1)                 WRITE SMF RECORD TYPE 128
         LR    R1,R5                   RELOAD FREEMAIN
         FREEMAIN  R,LV=SMFLEN,A=(1)
         LR    R1,R12                  RELOAD FREEMAIN
         FREEMAIN  R,LV=WORKLEN,A=(1)
         LR    R15,R10
         RETURN (14,12),RC=(15)        RETURN AND CANCEL
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DATA CONSTANTS                                             *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
HR24     DC    F'8640000'              24 HRS IN SECS
SMFRCD   DC    X'006C00000280'         SMF RECORD TYPE 128     @YM04803
WTOSKEL  WTO   'AGL001 - ACCT ????    AND JOBNAME PREFIXED  ??????? IS X
               NEEDED ',ROUTCDE=(11),MF=L
WTOLEN   EQU  *-WTOSKEL
WTOSKEL2 WTO   'AGL002 - ACCT ????   , CLASS Z, AND USERID  ??????? IN X
               JOBNAME',ROUTCDE=(11),MF=L
WTOLEN2  EQU  *-WTOSKEL2
WTOSKEL3 WTO   'AGL003 - ACCT ????   , CLASS G OR C, USERID ??????? IN X
               JOBNAME',ROUTCDE=(11),MF=L
WTOLEN3  EQU  *-WTOSKEL3
WTOSKEL4 WTO   'AGL004 - ACCT ????   , CLASS ? FROM USERID  ??????? IS X
               NEEDED ',ROUTCDE=(11),MF=L
WTOLEN4  EQU  *-WTOSKEL4
WTOSKELC WTO   'AGL00C - ACCT ????   , CLASS 2,3, OR 4 WITH ??????? IN X
               JOBNAME',ROUTCDE=(11),MF=L
WTOLENC  EQU  *-WTOSKELC
         LTORG
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
*   DEFINE DSECT FOR JOB INITIATION RECORD (TYPE X'80')               *
*-- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --*
WTODSECT DSECT
WTODIS   DS    CL69
WORKLEN  EQU  *-WTODSECT
SMFDSECT DSECT
         DS    1H                      ALIGN TIME/DATE ON FWD
SMFOUT   EQU   *
HEADER   DS    6C                      HEADER
TIME     DS    1F                      TIME
DATE     DS    1F                      DATE
SYSID    DS    1F                      SYSTEM ID
JOBLOG   DS    4F                      JOB LOG
USERID   DS    CL8                     USER ID FROM ACF2
ENQTIME  DS    1F                      ENQUEUE TIME
PRTY     DS    1C                      PRIORTY
JOBNAME  DS    CL8                     JOBNAME FROM JES2
CLASS    DS    CL1                     JOBCLASS ON CARD
JOBACCT  DS    CL7                     ACCOUNT NUMBER  FROM SMF
ACFACCT  DS    CL40                    ACCOUNT NUMBER  FROM ACF
STATUS   DS    CL1                     Y FOR SUCCESS AND N FOR FAILED
SUBSYS   DS    CL4                     SUBSYSTEM
SMFLEN   EQU   *-SMFDSECT
         LIDREC                        LOGONID RECORD
         ACUCB                         ACF UCB
         ACFASVT                       ACF SSVT
         ACCVT                         ACF CVT
         IHAPSA                        PREFIX SAVE AREA
         IHAASCB                       ADDRESS SPACE CONTROL BLOCK
         IEFJMR                        JOB MANAGEMENT RECORD
         END
