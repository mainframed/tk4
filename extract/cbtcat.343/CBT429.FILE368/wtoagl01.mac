WTOAGL01 TITLE 'MESSAGE PROCESSOR TO START VTAM APPL'
         SPACE 1
*********************************************************************
*                                                                   *
*   WTOAGL01: MPF ROUTINE TO START  AGLSVTMB                        *
*                                                                   *
*   EXIT:     NORMAL  -  RC=0                                       *
*                                                                   *
*             ERROR   -  RC=4                                       *
*                                                                   *
*   MODS:     AUG.  9,1988   -   CHANGED GETMAIN TO SUBPOOL 230  RP *
*   MODS: AGL OCT.  2,1992   -   MADE CODE CHANGE TO GET IT TO WORK *
*   MODS: AGL JAN. 16,1993   -   CUSTOMIZED FOR AGLC                *
*                                                                   *
*********************************************************************
WTOAGL01 CSECT ,
WTOAGL01 AMODE 31
WTOAGL01 RMODE ANY
         SPACE 2
CMDSVC   EQU   34
K0       EQU   0
K1       EQU   1
K2       EQU   2
K4       EQU   4
K8       EQU   8
K12      EQU   12
K16      EQU   16
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12),,WTOAGL01_&SYSDATE  SAVE REGISTERS
         USING WTOAGL01,R12                SET UP BASE
         USING CVT,R3                      SETUP CVT ADDRESS
         USING DATA,R13                    SETUP DATE AREA ADDRESS
         LR    R12,R15                     LOAD BASE REG
         L     R8,K0(R1)                   SAVE INPUT PARM
         GETMAIN RU,LV=DATALEN,SP=230,LOC=BELOW GET STORAGE       (RP)
         ST    R13,K4(R1)                  SAVE CALLERS WORK AREA
         ST    R1,K8(R13)                  SAVE MY WORK AREA
         LR    R13,R1                      LOAD SAVE AREA ADDRESS
         SPACE 1
STARTCMD DS    0H
         SPACE 1
         L     R3,CVTPTR
         MVC   CMDL1+17(1),CVTSNAME+3
         MVC   CMDAREA1(CMDLEN1),CMDL1     MOVE IN START COMMAND
         SLR   R0,R0                       CLEAR REG 0
         LA    R1,CMDAREA1                 POINT TO START OF COMMAND
         SVC   CMDSVC                      ISSUE START COMMAND
         SPACE 1
EXIT     DS    0H
         LR    R1,R13                      LOAD GETMAINED AREA
         L     R13,K4(R13)                 POINT TO CALLERS SAVE AREA
         FREEMAIN RU,LV=DATALEN,A=(1),SP=230  FREEMAIN AREA       (RP)
         LM    14,12,12(13)                RESTORE REGS
         LA    R15,0                       SET RC=0
         BSM   0,R14                       GOBACK, IN CALLERS MODE
         EJECT
CMDL1    DS    0F
         DC    AL2(CMDLEN1),AL2(00)        LENGTH OF STRING
         DC    C'START AGLSVTMX'           COMMAND PLUS BLANK
CMDLEN1  EQU   *-CMDL1
         SPACE 2
DATA     DSECT
SAVE     DS    18F
CMDAREA1 DS    0F,XL(CMDLEN1)
DATALEN  EQU   *-DATA
         CVT DSECT=YES
         END
