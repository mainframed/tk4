         TITLE 'NCZ96103 - LOCAL VTAM PRINT PROGRAM                    *
               - PROGRAM DOCUMENTATION'
*****                                                             *****
*                                                                     *
* ID:          NCZ96103 - LOCAL VTAM PRINT PROGRAM                    *
*                                                                     *
* NCZ96103.1 PROGRAM DESCRIPTION                                      *
*                                                                     *
* A DATASET IS READ AND WRITTEN TO A 3287 TYPE PRINTER.  IF THE DCB   *
* INDICATES ASA CONTROL CHARACTERS, THESE ARE TRANSLATED TO 3287      *
* CONTROL CHARACTERS.  THE PROGRAM WILL PERFORM PAGEINATION IF        *
* REQUESTED IN THE PARMFIELD.  THE PRINTER NODE NAME ALSO USED TO     *
* CONSTRUCT THE VTAM ACB NAME.                                        *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ96103.2 USER INSTRUCTIONS                                        *
*                                                                     *
* THE PROGRAM SHOULD BE RUN AS A BATCH JOB.  THE INPUT FILE TO BE     *
* PRINTED HAS THE DDNAME 'INPUT':                                     *
*                                                                     *
*  //STEPNAME  EXEC PGM=NCZ96103,PARM='PPPP,NN'                       *
*  //STEPLIB  DD  DISP=SHR,DSN=MAC1.LOAD                              *
*  //INPUT    DD  DISP=SHR,DSN=YOUR.PRINT.FILE                        *
*                                                                     *
* PARM INPUT - PPPP - PRINTER NAME - 4 CHARACTER VTAM NODE NAME       *
*                     (ALSO USED TO CONTRUCT ACB NAME)                *
*              NN   - NUMBER OF LINES PER PAGE                        *
*                                                                     *
* DEFAULTS - NONE                                                     *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ96103.3 MESSAGES                                                 *
*                                                                     *
* MC3051I - MC3056I  SEE ISD INFO (MESSAGES)                          *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ96103.4 ABEND CODES                                              *
*                                                                     *
* 1 - VTAM ERROR                                                      *
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*                                                                     *
* NCZ96103.5 IMPLEMENTATION METHOD                                    *
*                                                                     *
* STANDARD ASSEMBLY AND LINK-EDIT                                     *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ96103.6 AMENDMENT HISTORY                                        *
*                                                                     *
* MAINTAIN THE RECORD BELOW.                                          *
*                                                                     *
* CHANGE HISTORY:                                                     *
* DATE      INITS VERSION COMMENTS                                    *
*                                                                     *
* 25 FEB 83 EMM   V01     CREATED (BASED ON NCZ96101)                 *
* 20 APR 83 EMM   V02     ALTERED TO WAIT FOR PRINTER TO BE RELEASED  *
*                          FROM ANOTHER SUBSYSTEM.                    *
* 22 APR 83 EMM   V03     REMOVED RESTRICTION OF LOCAL TERMINALS ONLY *
* 07 MAY 83 RH    V03     SHOW RPLREQ IN TYPE 2 ERROR MESSAGE         *
* 19 AUG 83 EMM   V03     VTAM ACB NAME IS CONSTRUCTED FROM NODE NAME *
* 24 NOV 83 RH    V03     CORRECTED CODING ERROR FOR LRECL < 133      *
* 19 FEB 85 MKS   V03     ADDED FACILITY TO PRINT DATESETS WHICH ARE  *
*                         IN MACHINE CODE                             *
*                                                                     *
* KNOWN PROBLEM(S):                                                   *
* 25 FEB 83 EMM: IF 'NO LINE SKIP' IS SPECIFIED ON A LINE, THEN THE   *
*                LAST CHARACTER OF THE PREVIOUS LINE WILL BE          *
*                OVERWRITTEN                                          *
*                                                                     *
* SUGGESTED ENHANCEMENTS:                                             *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ96103.7 MODULARISATION DETAILS                                   *
*                                                                     *
* RECORD-MODE ACF/VTAM MACROS ARE USED TO WRITE THE DATA TO THE 3287  *
* REFER TO 'ACF/VTAM PROGRAMMING' FOR DETAILS.                        *
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*                                                                     *
* NCZ96103.8 RETURN CODES AND REGISTER USE                            *
*                                                                     *
* RETURN CODES:RC = 0   INPUT FILE HAS SUCCESSFULLY BEEN PRINTED      *
*                                                                     *
*              RC = 4   FAILURE EITHER TO OPEN VTAM ACB OR ACQUIRE    *
*                       PRINTER                                       *
*                                                                     *
*              RC = 8   ERROR WHEN WRITING TO PRINTER                 *
*                                                                     *
* REG USAGE:   R0-R2 - WORK REGISTERS                                 *
*              R3 -  ADDRESS OF INPUT RECORD                          *
*              R4 -  LENGTH OF INPUT RECORD                           *
*              R5 -  ADDRESS OF CURRENT 'SLOT' IN OUTPUT BUFFER       *
*              R6 -  NO OF BYTES REMAINING IN OUTPUT BUFFER           *
*              R7 -  NOT USED                                         *
*              R8-R12 - WORK REGISTERS                                *
*              R13 - BASE REGISTER                                    *
*              R14 - LINKAGE                                          *
*              R15 - ENTRY ADDRESS, RETURN CODE                       *
*                                                                     *
*****                                                             *****
         TITLE 'NCZ96103 - LOCAL VTAM PRINT PROGRAM                    *
               - PROGRAM CODE'
         PRINT NOGEN
NCZ96103 MSETUP 13
         L     R1,0(R1)
         ST    R1,PARMADR         SAVE PARMFIELD ADDRESS
         BAL   R14,INIT           INITIAL PROCESSING
         LTR   R15,R15            DID IT WORK OK?
         BNZ   EXIT               - NO, SOMETHING NOT OPENED
*
         MVC   INAREA,=CL134' '   CLEAR INPUT AREA
         GET   INPUT,INAREA       GET FIRST RECORD
         BAL   R14,PROC           PROCESS THE DATASET
EXIT     BAL   R14,END            END PROCESSING
         MCLEAR RC=(15)
*
********************
*
* INITIAL PROCESSING - OPEN ACB AND INPUT FILE
*                      SET PRINTER NAME
*                      ACQUIRE PRINTER
*                      IF OPENS OR ACQUIRE FAIL, SET RC=4, ELSE 0
*
INIT     ST    R14,INITSAV
*
* SET PARM OPTIONS
*
INI010   MVC   NEWNAME,=CL8' '    CLEAR NEW NAME
         MVI   PAGEFLAG,0         SET NO PAGE SKIP
         MVI   PAGFORCE,0
         BAL   R14,PARM           SET THE FIELDS
         LTR   R15,R15            OK?
         BNZ   INIEX4             - NO
*
         MVC   ACBNAME+1(4),NEWNAME  MOVE PRINTER ID TO ACBNAME
         MVC   ACBNAME+5(4),=C'PRNT' COMPLETE ACBNAME
*
         MVI   VTAMERR,C'3'       SET VTAM ERROR TYPE
         OPEN  ACB
         LTR   R15,R15            WAS OPEN SUCCESSFUL?
         BNZ   VTAM               - NO
*
         OPEN  (INPUT,(INPUT))    OPEN INPUT PRINT FILE
         TM    INPUT+48,X'10'     WAS OPEN SUCCESSFUL
         BZ    INIEX4             - NO
*
         LH    R2,INPUT+82        CHECK INPUT RECORD LENGTH
         CH    R2,=H'133'         NO MORE THAN 133 CHARS ALLOWED
         BL    INI015             - OK
         BE    INI013             - OK
         WTO   MF=(E,WTO5)
         BH    INIEX4             - TOO LONG
*
* IF REC LENGTH IS 133, FIRST CHAR MUST BE ASA OR MC CHAR
*
INI013   TM    INPUT+36,X'04'     ASA CHARS PRESENT?
         BNZ   INI015             - YES
         TM    INPUT+36,X'02'     MACHINE CHARS PRESENT?
         BNZ   INI015             - YES
         WTO   MF=(E,WTO6)
         B     INIEX4             - EXIT
*
INI015   MVI   VTAMERR,C'1'       SET VTAM ERROR TYPE
         MODCB AM=VTAM,                                                *
               NIB=PRINTER,                                            *
               NAME=(*,NEWNAME)
         LTR   R15,R15            DID IT MODIFY OK?
         BNZ   VTAM
*
INI020   MVI   VTAMERR,C'2'       SET VTAM ERROR TYPE
         SIMLOGON RPL=RPL         INDICATE PRINTER REQUIRED
         LTR   R15,R15            SIMLOGON OK?
         BNZ   VTAM               - NO
         LA    R3,2
         OPNDST RPL=RPL           COMPLETE SESSION
INI030   LTR   R15,R15            WAS IT OPENED?
         BNZ   VTAM               - NO
         LR    R2,R0
         LTR   R2,R2
         BZ    INI040             - YES
         CH    R2,=H'8'           IS REQUEST RETRIEABLE
         BNE   VTAM               - NO
         EXECRPL RPL=(1)          - YES, RETRY
         BCT   R3,INI030                 MAX OF 2 TIMES
         B     VTAM
INI040   MVI   VTAMERR,C'1'       SET VTAM ERROR TYPE
         TESTCB NIB=PRINTER,      WAS PRINTER ACQUIRED                 *
               AM=VTAM,                                                *
               CON=YES
         BNE   VTAM               - NO
*
         ZAP   LCT,LINECT         INITIALIZE LINE COUNT
*
INIEX0   LA    R15,0
         B     INIOUT
INIEX4   LA    R15,4
INIOUT   L     R14,INITSAV
         BR    R14
*
INITSAV  DS    A
*
********************
*
* LOGEXIT - VTAM LOGON EXIT
*
LOGEXIT  BR    R14
*
********************
*
* MAIN PROCESSING - BRANCH TO CLEAR ROUTINE
*                   READ INPUT RECORD
*                   BRANCH TO FORMAT ROUTINE
*                   ADD FORMATTED RECORD TO PRINT BUFFER
*                   WHEN BUFFER FULL (OR EOD), OUTPUT BUFFER
*
PROC     ST    R14,PROCSAV
PRO010   BAL   R14,CLEAR          CLEAR OUTPUT BUFFER, ETC
PRO020   LA    R3,INAREA          SET RECORD ADDRESS
         BAL   R14,FORMAT         FORMAT THE RECORD
         LR    R1,R4              LOAD RECORD LENGTH
         BCTR  R1,R0              SUBTRACT 1
         EX    R1,BUFADD          MOVE RECORD TO PRINT BUFFER
         LA    R5,0(R4,R5)        INCREASE BUFFER ADDRESS
         SR    R6,R4              DECREASE BYTES LEFT IN BUFFER
*
         MVC   INAREA,=CL134' '   CLEAR INPUT AREA
         GET   INPUT,INAREA       GET NEXT RECORD
         TM    INPUT+36,X'04'     ASA CHARS PRESENT?
         BNZ   PRO024             - YES
         TM    INPUT+36,X'02'     MACHINE CHARS PRESENT?
         BNZ   PRO026             - YES
         B     PRO030             - NO
PRO024   CLI   INAREA,C'+'        (ASA) IS NEXT LINE NO LINE SKIP
         BNE   PRO030
         B     PRO028
PRO026   CLI   INAREA,X'01'       (MC)  IS NEXT LINE NO LINE SKIP
         BNE   PRO030
PRO028   BCTR  R5,R0              - YES, POINT BACK AT END OF LAST LINE
         MVI   0(R5),X'0D'        OVERWRITE WITH CR
         LA    R5,1(R5)           RESET R5
*
PRO030   CH    R6,=H'134'         ROOM FOR MORE?
         BNL   PRO020             - YES, GO GET SOME MORE
*
PRO040   MVI   0(R5),X'19'        INDICATE END OF MESSAGE
         LA    R12,OUTPUT         CALC LENGTH OF MESSAGE
         SR    R5,R12
         LA    R5,1(R5)           ADD ONE FOR EM CHAR
*
         MVI   VTAMERR,C'1'       SET VTAM ERROR TYPE
         MODCB RPL=RPL,           SET LENGTH OF MESSAGE                *
               RECLEN=(5)
         LTR   R15,R15            OK?
         BNZ   VTAM               - NO, OH DEAR!!!
*
         MVI   VTAMERR,C'2'       SET VTAM ERROR TYPE
         SEND  RPL=RPL,                                                *
               BRACKET=(BB,EB),                                        *
               CONTROL=DATA,                                           *
               POST=RESP,                                              *
               STYPE=REQ,                                              *
               RESPOND=(NEX,FME,NRRN),                                 *
               AREA=OUTPUT
         LTR   R15,R15            WAS IT OK?
         BNZ   VTAM               - NO, OH SH.....
*
         CLI   EOF,1              ANY MORE TO DO?
         BNE   PRO010             - YES
*
PROEX0   LA    R15,0
         B     PROOUT
PROEX8   ABEND 1,DUMP
PROOUT   L     R14,PROCSAV
         BR    R14
*
PROEOD   MVI   EOF,1              INDICATE END OF FILE
         B     PRO040             WRITE OUT FINAL MESSAGE
*
PROCSAV  DS    A
BUFADD   MVC   0(0,R5),0(R3)      MOVE INPUT RECORD TO OUTPUT BUFFER
*
********************
*
* END - RELEASE PRINTER, CLOSE ACB AND INPUT FILE AS NECESSARY
*       SET FINAL RETURN CODE
*
END      ST    R14,ENDSAV
         LR    R12,R15            SAVE RETURN CODE
         TESTCB NIB=PRINTER,      WAS PRINTER ACQUIRED                 *
               AM=VTAM,                                                *
               CON=YES
         BNE   END010             - NO
         CLSDST RPL=RPL           - YES, RELEASE IT
*
END010   TM    INPUT+48,X'10'     WAS INPUT FILE OPEN
         BZ    END020             - NO
         CLOSE INPUT
*
END020   TESTCB ACB=ACB,          WAS ACB OPEN                         *
               AM=VTAM,                                                *
               OFLAGS=OPEN
         BNE   END030             - NO
         CLOSE ACB                - YES, CLOSE IT
*
END030   LR    R15,R12            RESET RETURN CODE
         L     R14,ENDSAV
         BR    R14
*
ENDSAV   DS    A
*
********************
*
* VTAM - PRODUCE ERROR MESSAGES FOR VTAM ERRORS
*
VTAM     ST    R14,E14            SAVE R14
         CLI   VTAMERR,C'1'       VTAM ERROR TYPE 1 (MODCB ERROR)
         BNE   VTA010
         ST    R15,WK4
         UNPK  V1R15(3),WK4+3(2)
         TR    V1R15,HEXTAB-240
         MVI   V1R15+2,C' '
         ST    R0,WK4
         UNPK  V1R0(3),WK4+3(2)
         TR    V1R0,HEXTAB-240
         MVI   V1R0+2,C' '
         WTO   MF=(E,WTO1)
*
VTA010   CLI   VTAMERR,C'2'       VTAM ERROR TYPE2 (RPL BASED)
         BNE   VTA020
         ST    R15,WK4
         UNPK  V2R15(3),WK4+3(2)
         TR    V2R15,HEXTAB-240
         MVI   V2R15+2,C' '
         SHOWCB RPL=RPL,          GET ERROR CODES FROM RPL             *
               AM=VTAM,                                                *
               FIELDS=(RTNCD,FDBK2,REQ),                               *
               AREA=VTAMAREA,                                          *
               LENGTH=12
         UNPK  V2RCD(3),VTRCD+3(2)
         TR    V2RCD,HEXTAB-240
         MVI   V2RCD+2,C' '
         UNPK  V2FBK(3),VTFBK+3(2)
         TR    V2FBK,HEXTAB-240
         MVI   V2FBK+2,C' '
         UNPK  V2REQ(3),VTREQ+3(2)
         TR    V2REQ,HEXTAB-240
         MVI   V2REQ+2,C' '
         WTO   MF=(E,WTO2)
*
VTA020   CLI   VTAMERR,C'3'       VTAM ERROR TYPE3 (ACB BASED)
         BNE   VTA030
         ST    R15,WK4
         UNPK  V3R15(3),WK4+3(2)
         TR    V3R15,HEXTAB-240
         MVI   V3R15+2,C' '
         SHOWCB ACB=ACB,          GET ERROR CODE FROM ACB              *
               AM=VTAM,                                                *
               FIELDS=ERROR,                                           *
               AREA=VTAMAREA,                                          *
               LENGTH=4
         UNPK  V3ERR(3),VTAMAREA+3(2)
         TR    V3ERR,HEXTAB-240
         MVI   V3ERR+2,C' '
         WTO   MF=(E,WTO3)
*
VTA030   EQU   *
         ABEND 1,DUMP
*
********************
*
* PARM - SET PARMFIELD OPTIONS (TERMINAL ID AND PAGE SIZE)
*
PARM     ST    R14,PARMSAV
         L     R1,PARMADR         RELOAD PARMFIELD ADDRESS
         LH    R2,0(R1)           GET LENGTH
         CH    R2,=H'7'           CORRECT PARM LENGTH?
         BNE   PAREX4             - NO
         LA    R1,2(R1)           POINT AT FIRST FIELD
*
*   CHECK PARMFIELD
*
         CLI   4(R1),C','         COMMA IN RIGHT PLACE?
         BNE   PAREX4             - NO
         TRT   5(2,R1),NUMTAB
         BNZ   PAREX4
*
*   SET OPTIONS
*
         MVC   NEWNAME(4),0(R1)   SET PRINTER NAME
         PACK  LINECT,5(2,R1)     SET PAGESIZE
         CP    LINECT,=P'0'
         BE    PAREX0
         MVI   PAGEFLAG,1
*
PAREX0   LA    R15,0
         B     PAROUT
PAREX4   WTO   MF=(E,WTO4)
         LA    R15,4
PAROUT   L     R14,PARMSAV
         BR    R14
*
PARMSAV  DS    A
*
********************
*
* CLEAR - CLEAR OUTPUT BUFFER
*         INITIALIZE POINTER AND COUNT REGISTERS
*
CLEAR    ST    R14,CLRSAV
         LA    R8,OUTPUT          SET UP TO CLEAR OUTPUT AREA
         LA    R9,LOUTPUT
         LA    R10,0
         IC    R11,X'40'
         SLL   R11,24
         MVCL  R8,R10
*
         LA    R5,OUTPUT          R5 - POINTS TO OUTPUT AREA
         MVC   0(2,R5),=X'F5C8'   MOVE IN WRITE COMMAND
         LA    R5,2(R5)           STEP FORWARD IN BUFFER
         LA    R6,LOUTPUT         R6 - BYTES LEFT IN OUTPUT AREA
         SH    R6,=H'2'
*
         L     R14,CLRSAV
         BR    R14
*
CLRSAV   DS    A
*
********************
*
* FORMAT - TRANSLATE ASA CHARS TO 3287 CONTROL CHARS
*          ADD FORM FEED FOR FIRST RECORD (IF NOT THERE)
*          SET LENGTH OF RECORD
*
FORMAT   ST    R14,FORSAV
         LA    R4,132             SET OUTPUT LENGTH
         TM    INPUT+36,X'04'     ASA CHARS PRESENT?
         BNZ   FORA10             - YES
         TM    INPUT+36,X'02'     MACHINE CHARS PRESENT?
         BNZ   FORM10             - YES
         B     FOR050
*   FORMAT ASA CONTROL CHARACTERS
FORA10   LA    R4,133             RESET OUTPUT LENGTH
         CLI   0(R3),C'0'         DOUBLE SPACE?
         BNE   FORA20
         MVI   0(R3),X'15'
         SP    LCT,=P'2'
         B     FOR060
*
FORA20   CLI   0(R3),C'-'         TRIPLE SPACE?
         BNE   FORA30
         MVI   0(R5),X'15'        ADD EXTRA LINE FEED TO OUTPUT BUFFER
         LA    R5,1(R5)
         BCTR  R6,R0
         MVI   0(R3),X'15'
         SP    LCT,=P'3'
         B     FOR060
*
* IF FIRST CHAR IS NEW PAGE OR ANYTHING NOT ABOVE, WIPE IT OFF
*
FORA30   CLI   0(R3),C'1'         NEW PAGE?
         BNE   FORA40
         MVI   PAGFORCE,1         FORCE NEW PAGE
         SP    LCT,=P'100'     AND SET LINE COUNT NEGATIVE (MESSY!)
FORA40   BCTR  R4,R0              RESET LINE LENGTH
         LR    R2,R4
         BCTR  R2,R0              SET LENGTH FOR MOVE
         EX    R2,WIPEMOVE        WIPE OFF FIRST CHAR
         B     FOR050
*
*   FORMAT MACHINE CONTROL CHARACTERS
FORM10   LA    R4,133             RESET OUTPUT LENGTH
         CLI   0(R3),X'11'        DOUBLE SPACE?
         BNE   FORM20
         MVI   0(R3),X'15'
         SP    LCT,=P'2'
         B     FOR060
*
FORM20   CLI   0(R3),X'19'        TRIPLE SPACE?
         BNE   FORM30
         MVI   0(R5),X'15'        ADD EXTRA LINE FEED TO OUTPUT BUFFER
         LA    R5,1(R5)
         BCTR  R6,R0
         MVI   0(R3),X'15'
         SP    LCT,=P'3'
         B     FOR060
*
* IF FIRST CHAR IS NEW PAGE OR ANYTHING NOT ABOVE, WIPE IT OFF
*
FORM30   CLI   0(R3),X'89'        NEW PAGE?
         BNE   FORM40
         MVI   PAGFORCE,1         FORCE NEW PAGE
         SP    LCT,=P'100'     AND SET LINE COUNT NEGATIVE (MESSY!)
FORM40   BCTR  R4,R0              RESET LINE LENGTH
         LR    R2,R4
         BCTR  R2,R0              SET LENGTH FOR MOVE
         EX    R2,WIPEMOVE        WIPE OFF FIRST CHAR
*
FOR050   SP    LCT,=P'1'          INDICATE ONE LINE
*
* INPUT LINE IS NOW IN A FORM WHICH CAN BE SENT TO THE 3287 AND LINE
* LENGTH HAS BEEN ADJUSTED AS NECESSARY
* CONDITION CODE HAS BEEN SET FOR TEST FOR NEW PAGE
*
FOR060   BP    FOR080             NEW PAGE NOT NEEDED
         CLI   PAGFORCE,1         NEW PAGE FORCED?
         BE    FOR070             - YES
         CLI   PAGEFLAG,1         ARE WE USING LINECT
         BNE   FOR080             - NO
FOR070   MVC   0(2,R5),=X'0C0D'   PUT FF AND CR IN BUFFER
         LA    R5,2(R5)
         SH    R6,=H'2'
         ZAP   LCT,LINECT         RESET LINE COUNT
         MVI   PAGFORCE,0
FOR080   EQU   *
         L     R14,FORSAV
         BR    R14
*
FORSAV   DS    A
WIPEMOVE MVC   0(0,R3),1(R3)      WIPE FIRST CHAR FROM INPUT LINE
*
********************
*
         TITLE 'NCZ96103 - LOCAL VTAM PRINT PROGRAM                    *
               - DATA DEFINITION'
         LTORG
*
INPUT    DCB   DDNAME=INPUT,                                           *
               MACRF=GM,                                               *
               DSORG=PS,                                               *
               EODAD=PROEOD
*
         DC    CL4'ACB'
ACB      ACB   AM=VTAM,                                                *
               APPLID=ACBNAME,                                         *
               EXLST=EXLST,                                            *
               MACRF=LOGON
*
         DC    CL4'RPL'
RPL      RPL   AM=VTAM,                                                *
               ACB=ACB,                                                *
               NIB=PRINTER,                                            *
               AREA=OUTPUT,                                            *
               OPTCD=(SYN,ACCEPT)
*
PRINTER  NIB   MODE=RECORD
*
EXLST    EXLST AM=VTAM,                                                *
               LOGON=LOGEXIT
*
PARMADR  DC    A(0)
E14      DC    A(0)
WK4      DC    F'0'
NEWNAME  DC    CL8' '
VTAMAREA DC    CL12' '
         ORG   VTAMAREA
VTRCD    DC    CL4' '
VTFBK    DC    CL4' '
VTREQ    DC    CL4' '
ACBNAME  DC    C'8'
         DC    CL8' '
LCT      DC    PL2'0'
LINECT   DC    PL2'0'
EOF      DC    X'0'
PAGEFLAG DC    X'0'
PAGFORCE DC    X'0'
VTAMERR  DC    C'0'
NUMTAB   DC    240X'FF',10X'0',6X'FF'
HEXTAB   DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
*                     1         2         3         4
*               4567890123456789012345678901234567890123456789012345678
WTO1     WTO   'MC3051I:TYPE 1 ERROR: R15=XX , R0=XX  ',MF=L
V1R15    EQU   WTO1+30,2
V1R0     EQU   WTO1+38,2
WTO2     WTO   'MC3052I:TYPE 2 ERROR: R15=XX , RTNCD=XX , FDBK2=XX , RE*
               Q=XX  ',MF=L
V2R15    EQU   WTO2+30,2
V2RCD    EQU   WTO2+41,2
V2FBK    EQU   WTO2+52,2
V2REQ    EQU   WTO2+61,2
WTO3     WTO   'MC3053I:TYPE 3 ERROR: R15=XX , ERROR=XX  ',MF=L
V3R15    EQU   WTO3+30,2
V3ERR    EQU   WTO3+41,2
WTO4     WTO   'MC3054I:INVALID PARM FIELD',ROUTCDE=(11),MF=L
WTO5     WTO   'MC3055I:INVALID LRECL SIZE',ROUTCDE=(11),MF=L
WTO6     WTO   'MC3056I:INVALID RECFM',ROUTCDE=(11),MF=L
*
INAREA   DC    CL134' '
*
OUTPUT   DS    CL2                WRITE COMMANDS
         DS    10CL133            LINES
         DS    C                  END OF MESSAGE
LOUTPUT  EQU   *-OUTPUT
*
********************
*
         END   NCZ96103
