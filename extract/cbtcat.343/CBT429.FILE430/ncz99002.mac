NCZ99002 TITLE 'CHARACTER STRING HANDLER                               *
               - PROGRAM DOCUMENTATION'
*****                                                             *****
*                                                                     *
* ID:          NCZ99002 - CHARACTER STRING HANDLER                    *
*                                                                     *
* NCZ99002.1 PROGRAM DESCRIPTION                                      *
*                                                                     *
* THIS SUBROUTINE CAN BE CALLED FROM AN ASSEMBLER PROGRAM TO SCAN A   *
* CHARACTER STRING AND RETURN THE ADDRESSES AND LENGTHS OF FIELDS     *
* WITHIN THE STRING. A FIELD IS DEFINED AS A STRING OF CHARACTERS     *
* DELIMITED BY SEPARATOR CHARACTERS WHICH ARE PROVIDED IN A LIST.     *
* THE INTENTION IS TO ASSIST WITH ANALYSIS OF CONTROL STATEMENTS      *
* AND PARAMETERS IN ASSEMBLER PROGRAMS.                               *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ99002.2 USER INSTRUCTIONS                                        *
*                                                                     *
* THE SUBROUTINE IS INVOKED BY THE NORMAL OS 'CALL' INTERFACE WITH 6  *
* PARAMETERS:                                                         *
*                                                                     *
* P1 - ADDRESS OF CHARACTER STRING TO BE ANALYSED                     *
* P2 - LENGTH OF CHARACTER STRING                                     *
* P3 - ADDRESS OF LIST OF SEPARATOR CHARACTERS                        *
* P4 - NUMBER OF SEPARATOR CHARACTERS IN LIST                         *
* P5 - ADDRESS OF LIST OF FIELD ADDRESSES AND LENGTHS                 *
* P6 - NUMBER OF ENTRIES IN FIELD LIST (IE NUMBER OF FIELDS REQUIRED) *
*                                                                     *
* P1, P3, P5 ARE FULLWORD ADDRESSES.                                  *
* P2, P4, P6 ARE HALFWORDS.                                           *
*                                                                     *
* THE SUBROUTINE RETURNS THE ADDRESS(ES) OF THE FIELDS IN THE FIELD   *
* ADDRESS LIST. EACH ENTRY IN THIS LIST HAS THE FORMAT:               *
* 1 BYTE FIELD LENGTH (ABSOLUTE, NOT 'MACHINE LENGTH')                *
* 3 BYTES FIELD START ADDRESS                                         *
*                                                                     *
* THE SUBROUTINE ZEROISES THE FIELD ENTRIES DURING INITIALISATION.    *
*                                                                     *
* THERE ARE SOME RESTRICTIONS:                                        *
*                                                                     *
* - THE LENGTH OF A FIELD MAY NOT EXCEED 255 BYTES.                   *
*                                                                     *
* - A MAXIMUM OF 64 FIELDS MAY BE REQUESTED.                          *
*                                                                     *
* IF INVALID PARAMETERS ARE PASSED THE SUBROUTINE WILL RETURN WITH    *
* A NON-ZERO RETURN CODE AND INDETERMINATE RESULTS. NO DATA AREAS     *
* WILL BE MODIFIED EXCEPT FOR THE FIELD ADDRESS LIST.                 *
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*                                                                     *
* NCZ99002.3 MESSAGES                                                 *
*                                                                     *
* NONE                                                                *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ99002.4 ABEND CODES                                              *
*                                                                     *
* NONE                                                                *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ99002.5 IMPLEMENTATION METHOD                                    *
*                                                                     *
* MOVE TO MAC1.ASM AND PASMAL.                                        *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ99002.6 AMENDMENT HISTORY                                        *
*                                                                     *
* CHANGE HISTORY:                                                     *
* DATE      INITS VERSION COMMENTS                                    *
*                                                                     *
* 01 SEP 83 RH    V01     CREATED                                     *
*                                                                     *
* SUGGESTED ENHANCEMENTS:                                             *
*                                                                     *
* RECORD DETAILS BELOW.                                               *
*                                                                     *
* DD MMM YY WHO : COMMENTS                                            *
*                                                                     *
*****                                                             *****
         EJECT
*****                                                             *****
*                                                                     *
* NCZ99002.7 MODULARISATION DETAILS                                   *
*                                                                     *
* SIMPLE STRUCTURE. NO OS CALLS. DOES NOT USE 'TRT'.                  *
*                                                                     *
*****                                                             *****
         SPACE
*****                                                             *****
*                                                                     *
* NCZ99002.8 PROGRAM LOGIC OVERVIEW                                   *
*                                                                     *
* OMITTED.
*                                                                     *
*                                                                     *
*****                                                             *****
         TITLE 'CHARACTER STRING HANDLER                               *
               - PROGRAM PROLOGUE'
*****                                                             *****
*                                                                     *
* ID:          NCZ99002 - CHARACTER STRING HANDLER                    *
*                                                                     *
* ENTRY INTFCE: NORMAL OS.                                            *
*                                                                     *
* EXIT  INTFCE: NORMAL OS.                                            *
*                                                                     *
* RETURN CODES:RC = 0   - ALL PROCESSING OK                           *
*                                                                     *
*              RC = 4   - SERIOUS ERROR                               *
*                                                                     *
* REG USAGE:   R0 -                                                   *
*              R1 -                                                   *
*              R2 -> CHARACTER STRING                                 *
*              R3 =  # OF CHARS IN STRING                             *
*              R4 -> LIST OF SEPARATOR CHARS                          *
*              R5 =  # OF SEPARATOR CHARS                             *
*              R6 -> CURRENT FIELD ENTRY                              *
*              R7 =  # OF FIELDS IN LIST                              *
*              R8 -                                                   *
*              R9 -                                                   *
*              R10 -                                                  *
*              R11 -                                                  *
*              R12 -                                                  *
*              R13 - BASE, SAVE AREA                                  *
*              R14 -                                                  *
*              R15 -                                                  *
*                                                                     *
*****                                                             *****
         TITLE 'CHARACTER STRING HANDLER                               *
               - PROGRAM CODE'
         PRINT NOGEN
NCZ99002 MSETUP 13
         MVI   FLAGS,X'00'     CLEAR FLAGS
*
         LM    R2,R7,0(R1)     LOAD ADDRESSES OF P1 TO P6
         LH    R3,0(R3)        # OF STRING CHARACTERS
         LH    R5,0(R5)        # OF SEPARATORS
         LH    R7,0(R7)        # OF FIELDS
         LTR   R2,R2           Q IS STRING POINTER ZERO?
         BZ    BADRC           Y
         LTR   R3,R3           Q IS # OF STRING CHARS ZERO?
         BZ    BADRC           Y
         LTR   R4,R4           Q IS FIELD SEP POINTER ZERO?
         BZ    BADRC           Y
         LTR   R5,R5           Q IS # OF FIELD SEP CHARS ZERO?
         BZ    BADRC           Y
         LTR   R7,R7           Q IS # OF FIELDS ZERO?
         BZ    BADRC           Y
         CH    R7,=H'64'       Q MORE THAN 64 FIELDS?
         BH    BADRC           Y
         LTR   R6,R6           Q IS FIELD LIST POINTER ZERO?
         BZ    BADRC           Y
*
PARMOK   LR    R8,R7           COPY # OF FIELDS
         SLL   R8,2            GET # 0F BYTES OCCUPIED BY FIELD ENTRIES
         BCTR  R8,0            MAKE MACHINE LENGTH
         EX    R8,EXXC         CLEAR FIELD LIST
         B     EXAMINE
         EJECT
NEXTCHAR LA    R2,1(R2)
         BCT   R3,EXAMINE
         TM    FLAGS,INFIELD   Q - IN A FIELD ?
         BZ    EXIT            N SO WE ARE FINISHED
         OI    FLAGS,ENDSTRNG  Y INDICATE WE HAVE HIT THE END OF STRING
         B     COMPUTE
         SPACE
EXAMINE  LR    R8,R5           COPY # OF SEP CHAR
         LR    R9,R4           COPY ADDRESS OF SEP CHARS
SEPTEST  CLC   0(1,R2),0(R9)   Q IS IT A SEP CHAR?
         BE    GOTSEP          Y
         LA    R9,1(R9)        N STEP SEP POINTER
         BCT   R8,SEPTEST      CHECK NEXT SEP
*
NOSEP    TM    FLAGS,INFIELD   Q IN A FIELD?
         BO    NEXTCHAR        Y CHECK NEXT CHAR
         OI    FLAGS,INFIELD   N INDICATE START OF FIELD
         ST    R2,FLDSTART     KEEP ADDRESS OF START OF FIELD
         B     NEXTCHAR        CHECK NEXT CHAR
         SPACE
GOTSEP   TM    FLAGS,INFIELD   Q IN FIELD?
         BZ    NEXTCHAR        N DO NOT CARE
*                              Y END OF FIELD
COMPUTE  LR    R10,R2          COPY STRING POINTER
         S     R10,FLDSTART    COMPUTE FIELD LENGTH
         MVC   0(4,R6),FLDSTART STORE START ADDRESS
         CH    R10,=H'256'     Q TOO BIG FOR 1 BYTE?
         BH    BADRC           Y
LENOK    STC   R10,0(R6)       STORE ADDRESS
         NI    FLAGS,X'FF'-INFIELD INDICATE END OF FIELD
         TM    FLAGS,ENDSTRNG  Q HAVE WE PROCESSED ALL CHARS ?
         BO    EXIT            Y
         LA    R6,4(R6)
         BCT   R7,NEXTCHAR     CHECK NEXT CHAR
         SPACE
EXIT     MCLEAR RC=0
BADRC    MCLEAR RC=4
         EJECT
         LTORG
FLAGS    DS    X
INFIELD  EQU   X'80'
ENDSTRNG EQU   X'40'
FLDSTART DS    F
         DS    0H
EXXC     XC    0(0,R6),0(R6)
         END
