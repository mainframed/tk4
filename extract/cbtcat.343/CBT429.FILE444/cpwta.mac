.# CREATE CPWT,ASM,'CHANNEL PROGRAM TAPE WRITE','RON TATUM'
CPWT      CSECT                                                CPW01010
*                                                              CPW01020
          ENTRY CPWTOPEN,CPWOPEN,CPWTCLS,CPWCLS,CPWTM,CPWERG
*
*  SUBROUTINE ENTRY POINTS AND PARAMETERS REQUIRED:
*
*   CPWTOPEN (CPWOPEN ALTERNATE NAME) - OPEN THE DATA SET -
*         ANSWER, (DDNAME) - ANSWER MUST BE A 5 WORD ARRAY;
*         DDNAME, IF FURNISHED, MUST BE AN 8 CHARACTER LITERAL
*         MATCHING THE DDNAME ON THE DD CARD DESCRIBING THE TAPE.
*         IF NOT FURNISHED, THE DDNAME DEFAULTS TO  'WRITEOUT'.
*
*   CPWTCLS (ALIAS CPWCLS) - CLOSE THE DATA SET.  NO PARAMETERS.
*
*   CPWT - WRITE ONE PHYSICAL RECORD.
*         ANSWER, DATA COUNT - ANSWER FOR STATUS, ETC., DATA TO
*         BE WRITTEN, COUNT OF BYTES TO WRITE (MAX 512K).
*
*  CPWTM - WRITE A TAPE MARK.
*          ANSWER - STATUS AFTER OPERATION.
*
*  CPWERG - ERASE GAP (NOMINALLY 3-1/2 INCHES).
*           ANSWER - STATUS.
*
*  THE LIMIT FOR CPWT COUNT IS 512K BYTES BECAUSE WE FELT WHEN
*  WE WROTE THE PROGRAM INITIALLY THAT 9 CCW'S OF 65K WAS SUFFICIENT
*  EVEN FOR WELL LOGS.
*
          STM   14,12,12(13)
          LR    12,15
          USING CPWT,12
          LR    2,13
          LA    13,SAVEWT
          ST    13,8(,2)
          ST    2,SAVEWT+4
          L     11,0(,1)      11 ALWAYS POINTS TO USER'S ANSWER
          USING ANSWER,11     ARRAY.
          LM    2,3,4(,1)
          LA    2,0(,2)       DATA ADDRESS
          L     3,0(,3)       VALUE OF COUNT
          LA    4,1
          STH   4,1
          STH   4,IOBWTBCI   SET TO INCREMENT BLOCK COUNT
          L     5,MAX
          LA    4,9
          LA    6,CP
          MVI   IOBWT,X'02'   MAY NOT NEED RELATED CP'S
CPWTLP    CR    3,5
          BC    12,CPWTLAST   LAST CCW TO BUILD?
          OI    IOBWT,X'80'   TURN ON CD BIT IN CCW
          L     7,WTN
          OR    7,2
          ST    7,0(,6)
          L     7,WTN+4
          ST    7,4(,6)
          SR    3,5           DECREMENT BYTES TO WRITE
          AR    2,5           INCR CCW PTR & DATA ADDRESS
          LA    6,8(,6)
          BCT   4,CPWTLP      BUILD ANOTHER CCW IF NEEDED
*  IF MORE THAT 9 REQUIRED, ABORT THE WHOLE THING
          ABEND 146,DUMP,STEP
CPWTLAST  L     7,WTF         CONSTRUCE LAST CCW (#9 OR LESS)
          OR    7,2           SAY NO MORE IN CHAIN, ETC.
          ST    7,0(,6)
          L     7,WTF+4
          OR    7,3
          ST    7,4(,6)
          LA    7,10
          SR    7,4
          ST    7,NUMCCW
          BAL   10,EXCP       FINALLY GO TRY TO WRITE SOMETHING
*
RETURN    L     13,SAVEWT+4
          LM    14,12,12(13)
          MVI   12(13),X'FF'
          BR    14
*
CPWERG    STM   14,12,12(13)
          USING CPWERG,15
          L     12,ACPWT
          DROP  15
          LR    2,13
          LA    13,SAVEWT
          ST    13,8(,2)
          ST    2,SAVEWT+4
          L     11,0(,11)     DSECT PTR
          SR    4,4
          STH   4,IOBWTBCI    DON'T INCREMENT BLOCK COUNT
          LD    0,ERG         MOVE CCW(S)
          STD   0,CP
          MVI   IOBWT,X'02'   NO RELATED CCW'S
          LA    10,RETURN
          B     EXCP          FILE MARK THE TAPE, BACKSPACE OVER
*
CPWTM     STM   14,12,12(13)  HERE, JUST WRITE AN  EOF
          USING CPWTM,15
          L     12,ACPWT
          DROP  15
          LR    2,13
          LA    13,SAVEWT
          ST    13,8(,2)
          ST    2,SAVEWT+4)
          L     11,0(,1)
          LA    1,DCBWT
          USING IHADCB,1      MUST DEAL WITH THINGS IN DCB
          SR    4,4
          ST    4,DCBBLKCT
          DROP  1
          STH   4,IOBWTBCI
          LD    0,WTM
          STD   0,CP
          MVI   IOBWT,X'02'   NO CHAINING
          LA    10,RETURN
*
EXCP      SR    1,1
          ST    1,ECBWT       DO IT AND WAIT AROUND
          EXCP  IOBWT
          WAIT  ECB=ECBWT
*
          CLI   ECBWT,X'7F'   TEST FOR ERRORS
          BC    8,EXCPW6       - BIF NONE
*
          TM    IOBWTCSW+4,X'0C'  DEVICE & CHANNEL END?
          BC    1,EXCPW2           - YES, BOTH
*
EXCPW1    L     1,FM3         SET ANSWER TO -3
          B     EXCPW7
*
EXCPW2    TM    IOBWTCSW+4,X'02'   UNIT CHECK?
          BC    8,EXCPW4      NO, MUST BE SOMETHING ELSE
*
          TM    IOBWT+2,X'F7'   EQUIPMENT MALFUNCTION?
          BC    5,EXCPW1      YES, JUST REPORT IT TO CALLER
*
          TM    IOBWT+2,X'08'    DATA CHECK? (HOW ON EARTH?)
          BC    8,EXCPW3      MAYBE WE HIT EOT MARKER
          L     1,FM2
          B     EXCPW7
*
EXCPW3    TM    IOBWT+3,X'08' LOAD POINT? (ON A WRITE???)
          BC    1,EXCPW5
* AH, UNIT CHECK, NOT LOAD POINT, NO PROBLEM.
*
*  BUT IF THE BC 1 DOESN'T HAPPEN, WE MUST GIVE IT UP
*
          ABEND 144,DUMP,STEP
*
EXCPW4    TM    IOBWTCSW+4,X'01'   UNIT EXCEPTION?
          BC    1,EXCPW5           BIF SO
          B     EXCPW6             PROBABLY A SYSTEM RETRY
*
EXCPW5    L     1,FM1         SAY HARDWARE UNIT EXCEPTION
          B     EXCPW7
*
EXCPW6    SR    1,1
EXCPW7    ST    1,AIER        MAJOR RETURN STATUS FOR CALLER
          MVC   ASTATUS(1),IOBWTCSW+4  HDW STATUS BYTE
          MVC   ASTATUS+1(2),IOBWT+2  BYTES 1&2 FOR UNIT CHECK
          BR    10            STANDARD EXIT IF NO ABENDS
*
*
CPWTOPEN  STM   14,12,12(13)   ATTACH DCB TO DDNAME
CPWOPEN   EQU   *-4
          USING CPWTOPEN,15
          L     12,ACPWT
          DROP  15
          LR    2,13
          LA    13,SAVEWT
          ST    13,8(,2)
          S     2,SAVEWT+4
          L     11,0(,1)
          LA    9,DCBWT
          USING IHADCB,9
          LTR   11,11         NEG SAYS ONLY ONE PARM, USE DFLT
          BC    4,DFLTOPEN
          L     2,4(,1)
OPEN2     MVC   DCBDDNAM(8),0(2)   FAKE OR REAL NAME TO DCB
          OPEN  (DCBWT,(OUTPUT))
*
          TM    DCBOFLGS,X'10'   SUCCESSFUL OPEN?
          BC    8,OPENFAIL       NO
          SR    5,5
          ST    5,DCBBLKCT      SAY NO BLOCKS WRITTEN
OPEN3     ST    5,AIER
          B     RETURN
*
OPENFAIL  LA    5,1
          B     OPEN3     COMPLAIN ABOUT DCB NO OPEN
*
DFLTOPEN  LA    2,DFLTDDWT    USE DEFAULT NAME, GOOD AS ANY
          B     OPEN2
*
CPWCLS    EQU   *
CPWTCLS   STM   14,12,12(13)  THIS IS NONSENSE UNLESS ...
          USING CPWTCLS,15
          L     12,ACPWT
          DROP  15
          LR    2,13
          LA    13,SAVEWT
          ST    13,8(,2)
          ST    2,SAVEWT+4
          CLOSE DCBWT
          B     RETURN
*
*
*
SAVEWT    DS    9D
CP        DC    9D'0'
*
*HERE ARE THE VARIOUS CCW'S WE MAY NEED
*
WTN       DC    X'01'         COMMAND FOR WRITE
          DC    AL3(0)
          DC    X'A0'         CD,SLI
          DC    AL3(65535)    MAX COUNT; ANYBODY SAYS 32K IS MAX FOR
*                             CCW IS JUST PLAIN WRONG!!!!
WTF       DC    X'01'
          DC    AL3(0)
          DC    X'20'         SLI
          DC    AL3(1)
*
WTM       DC    X'1F'         CONTROL - WRITE FILE MARK
          DC    AL3(CP)
          DC    X'20'         SLI
          DC    AL3(1)
*
ERG       DC    X'17'         CONTROL - ERASE GAP
          DC    AL3(CP)
          DC    X'20'         SLI
          DC    AL3(1)
*
ACPWT     DC    A(CPWT)
FM1       DC    F'-1'
FM2       DC    F'-2'
FM3       DC    F'-3'
MAX       DC    F'65535'      MAX BYTES PER CCW
NUMCCW    DC    F'0'
DFLTDDWT  DC    CL8'WRITEOUT'     FAKE DDNAME
*
ECBWT     DC    F'0'          EVENT CONTROL BLOCK
IOBWT     DC    F'0'          INPUT/OUTPUT BLOCK
          DC    A(ECBWT)
*
IOBWTCSW  DC    2F'0'
IOBWTACP  DC    A(CP)         ADDR OF CHANNEL PROG
IOBWTDCB  DC    A(DCBWT)       & DCB IT USES
          DC    F'0'
IOBWTBCI  DC    H'1'          BLOCK COUNT INCREMENT
IOBWTECT  DC    H'0'          ERROR COUNTS
*
DCBWT     DCB   MACRF=(E),DEVD=TA,DSORG=PS,IOBAD=IOBWT,DDNAME=NONE
*
ANSWER    DSECT
AIER      DS    F             1=OPEN FAILED, 0=OK, -1=EOT,
*                             -2=DATA CHECK, -3=HARDWARE FAILURE
APART     DS    F             PARTIAL COMPLETION, 0=O.K.
APARTIAL  DS    F             MAINTAIN COMPAT WITH CPRD
ASTATUS   DS    2F            UNIT STATUS & SENSE BYTES
*
          DCBD  DSORG=PS,DEVD=TA
*
          END
.# STORE CPWT



