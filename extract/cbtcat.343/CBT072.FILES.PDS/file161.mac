*              /* LEVEL=0003 */
XXBLDLST TITLE 'PROGRAM TO LIST OUT DYNAMIC BLDL TABLES'
XXBLDLST START
*
*        CODE TO PRINT DYNAM-BLDL STATISTICS
*
*
*   DYNAM-BLDL LEVEL  CODE  DEPENDENCIES
*        SEE '$$$' LABELS IN IGC018XX CODE
*
$$$DIS04 EQU   X'318'         ADDR(FCOUNT/MCOUNT/LINKTAB)
*
*   END OF CODE LEVEL DEPENDENCIES
*
*
**
***      EQUATE REGISTERS AND SET UP DYNAMIC SAVEAREA
**
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
RA       EQU   10
R11      EQU   11
RB       EQU   11
R12      EQU   12
RC       EQU   12
R13      EQU   13
RD       EQU   13
R14      EQU   14
RE       EQU   14
R15      EQU   15
RF       EQU   15
         B     26(RF)                   BRANCH AROUND ID
         DC    AL1(21),CL8'XXBLDLST'    PROGRAM NAME
         DC    CL8'&SYSDATE'            DATE
         DC    CL5'&SYSTIME'            TIME
         STM   RE,RC,12(RD)             SAVE REGS IN HI SAVE AREA
         LR    RC,RF                    SET UP BASE
         USING XXBLDLST,RC              OF R12
         USING SAVEAREA,RD              DYNAMIC SAVEAREA BASE + WORK
         LR    R2,R1                    SAVE PARAMS
         LR    RB,RD                    SAVE CALLERS SAVE AREA ADDR
         GETMAIN R,LV=GETSIZE1          GET SOME CORE
         LR    RD,R1                    SET DSECT BASE
         ST    RB,SAVE1+4               SAVE CALLERS SAVE AREA ADDR
         ST    RD,8(RB)                 SAVE OURS IN CALLERS
         EJECT
*
**
***      CHECK OUT PARM FIELD PASSED
**
*
         L     R2,0(R2)                 PARM FIELD PTR
         LH    R3,0(R2)       PARM LENGTH
         LTR   R3,R3
         BZ    NOPARM         NO PARM TO DO
         LA    R2,1(R3,R2)    ADDR LAST BYTE OF PARM
T1       CLI   0(R2),C'S'     SUMMARY ONLY REQUESTED
         BNE   T2
         OI    CTLFLAG,X'80'  YES,FLAG AS SUMMARY ONLY
T2       DS    0H             NEXT PARM TEST......
         BCTR  R2,0           ADDR PREVIOUS CHAR
         BCT   R3,T1          PROCESS IT IF ANY..
         EJECT
*
**
***      FIND IGC018XX IN CORE AND CHECK CODE LEVEL
**
*
NOPARM   DS    0H
         L     R3,X'10'       CVT
         L     R3,X'C8'(,R3)  SSCVT
         L     R3,X'84'(,R3)  SVCTABLE
         LA    R3,X'90'(,R3)  BLDL ENTRY
         L     R2,0(R3)                 CODE
         CLC   =C'01-04-77',12(R2) Q.IS THIS SUPPORTED LEVEL
         BNE   UNSUPORT       NO ,TELL USER & ABEND
         EJECT
*
**
***      MOVE TABLE FROM IGC018XX TO OUR AREA
**
*
         LA    R2,$$$DIS04(R2)
         LA    R3,LINKTAB
         L     R4,SORT-LINKTAB(R2)
         ST    R4,SORT
         L     R4,HITS-LINKTAB(R2)
         ST    R4,HITS
*
         LM    R5,R7,TABSTAT
         L     R9,TABSTAT-LINKTAB(R2)
MOVEDATA MVC   0(64,R5),0(R9)
         AR    R9,R6
         BXLE  R5,R6,MOVEDATA
         EJECT
*
**
***      MAIN PROCESSING
**
*
         OPEN  (SYSPRINT,(OUTPUT))
         USING ENTRY,R5
         LM    R5,R7,TABSTAT
PRINTIT  MVC   OUTLINE+1(79),OUTLINE CLEAR OUT LINE
         L     R9,RECENT      Q.ANY ACTION
         LTR   R9,R9          ELSE DONT PRINT LINE
         BZ    PRINTED
         TM    CTLFLAG,X'80'  SUMMARY ONLY
         BO    PRINTED
         LH    R9,RECENT
         LTR   R9,R9
         BZ    PRECENT
         CVD   R9,DWD
         UNPK  OUTLINE+3(5),DWD+5(3)
         OI    OUTLINE+7,X'F0'
PRECENT  DS    0H
         LH    R9,TOTAL MG
         LTR   R9,R9
         BZ    PTOTAL
         CVD   R9,DWD
         UNPK  OUTLINE+9(5),DWD+5(3)
         OI    OUTLINE+13,X'F0'
PTOTAL   DS    0H
         MVC   OUTLINE+16(8),DATA
         SR    R9,R9
         IC    R9,DATA+59     CONCAT NO
         IC    R9,CHARTABL(R9)
         STC   R9,OUTLINE+26
         UNPK  OUTLINE+35(7),DATA+8(4)
         UNPK  OUTLINE+41(7),DATA+11(4)
         UNPK  OUTLINE+47(7),DATA+14(4)
         UNPK  OUTLINE+53(7),DATA+17(4)
         UNPK  OUTLINE+59(7),DATA+20(4)
         UNPK  OUTLINE+65(7),DATA+23(4)
         UNPK  OUTLINE+71(7),DATA+26(4)
         MVI   OUTLINE+75,C' '
         NC    OUTLINE+35(40),=40X'0F'
         TR    OUTLINE+35(40),CHARTABL
         PUT   SYSPRINT,OUTLINE
PRINTED  BXLE  R5,R6,PRINTIT
         PUT SYSPRINT,BLANKS
         LH    R9,SORT
         CVD   R9,DWD
         UNPK  SUMMARY+10(5),DWD+5(3)
         OI    SUMMARY+14,X'F0'
         LH    R9,HITS
         CVD   R9,DWD
         UNPK  SUMMARY+20(5),DWD+5(3)
         OI    SUMMARY+24,X'F0'
         LH    R9,MISSES
         CVD   R9,DWD
         UNPK  SUMMARY+31(5),DWD+5(3)
         OI    SUMMARY+35,X'F0'
         PUT SYSPRINT,SUMMARY
         EJECT
*
**
***      FREE DYNAMIC SAVEAREA AND RETURN
**
*
RET      DS    0H
         LR    R1,RD                    ADDR OF AREA TO FREE
         L     RD,4(RD)                 HI SAVE AREA ADDR
         FREEMAIN R,LV=GETSIZE1,A=(1)   FREE UP CORE
         LM    RE,RC,12(RD)             RESORE CALLERS REGS
         BR    RE                       RETURN
         EJECT
*
**
***      WTO WRONG LEVEL IN IGC018XX CODE
**
*
UNSUPORT DS    0H
         WTO   'XXBLDLST - WRONG LEVEL OF IGC018XX CODE'
         B     RET                      HOME
         EJECT
         LTORG
DWD      DC    D'0'
BLANKS   DC    CL80' '
SUMMARY  DC    CL80'    SORTS=     ,HIT=     ,MISS=                   '
OUTLINE  DC    CL80' '
CHARTABL DC    C'0123456789ABCDEF'
CTLFLAG  DS    X'00'          CONTROL FLAG FROM PARMS
*                             X'80' - PRINT SUMMARY ONLY
         EJECT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=80,             X
               RECFM=FA,BLKSIZE=80
         EJECT
ENT      EQU   100
ENTX     EQU   37
LINKTAB  DS    0D
TABSTAT  DC    A(START)
TABSIZE  DC    F'64'
TABEND   DC    A(LASTENT)
TABLAST  DC    A(LASTENT)
TABSLOT  DC    A(START+ENTX*64)
SORT     DC    H'0'
ENTRIES  DC    AL2(ENT)
HITS     DC    H'0'
MISSES   DC    H'0'
START    DC    ((ENT-1)*64)X'0'
LASTENT  DC    64X'0'
LINKEND  DS    0F
         EJECT
*
**
***      TABLE ENTRY DSECT
**
*
ENTRY    DSECT
RECENT   DS    H
TOTAL    DS    H
DATA     DS    0F
         EJECT
*
**
***      DSECT FOR GETMAINED WORK AREA
**
*
SAVEAREA DSECT
SAVE1    DS    18F
GETEND1  DS    0D
GETSIZE1 EQU   GETEND1-SAVE1
         END
