*          DATA SET CBT380     AT LEVEL 004 AS OF 06/15/77
         MACRO
&NAME    DEBCHK &CBAD,&TYPE=,&AM=,&MF=O
.*
.*                       *****************
.*                       *   VARIABLES   *
.*                       *  DECLARATION  *
.*                       *****************
.*
         LCLB  &AM0,&AM1,&AM2,&AM3,&AM4,&AM5,&AM6,&AM7
         LCLB  &TYPEA,&TYPED,&TYPEV,&TYPEP,&ERR
         LCLC  &REG,&LABEL,&AMFC,&AMSC
         LCLA  &A
.*
.*                       **********
.*                       * VERIFY *
.*                       *  INPUT *
.*                       **********
.*
         AIF   ('&MF' EQ 'O').DBCK020   BRANCH IF &MF OMITTED
         AIF   ('&MF' EQ 'L').DBCK040   BRANCH IF &MF=L
         IHBERMAC 1001,MF,&MF
&ERR     SETB  1
.*
.*                       TEST &CBAD
.*
.DBCK020 AIF   (T'&CBAD NE 'O').DBCK040 BRANCH IF &CBAD SPECIFIED
&ERR     SETB  1                        SET ERROR INDICATOR
         IHBERMAC  2
.*
.*                       ESTABLISH AND TEST &AM VALUE
.*
.DBCK040 AIF   (T'&AM NE 'O').DBCK060   BRANCH IF &AM SPECIFIED
         AIF   ('&MF' NE 'L').DBCK120   BRANCH IF NOT LIST FORM
         IHBERMAC 1006,AM
&ERR     SETB  1
         AGO   .DBCK140
.DBCK060 ANOP
&AMFC    SETC  '&AM'(1,1)               FIRST CHARACTER OF &AM
         AIF   ('&AMFC' NE '(').DBCK080 BRANCH IF &AM TO BE CALCULATED
         AIF   ('&MF' EQ 'L').DBCK100   BRANCH IF LIST FORM
         AGO   .DBCK120                 &AM IS VALID
.DBCK080 ANOP
&AM0     SETB  ('&AM' EQ 'ISAM')
&AM1     SETB  ('&AM' EQ 'BDAM')
&AM2     SETB  ('&AM' EQ 'SAM' OR '&AM' EQ 'BPAM')
&AM3     SETB  ('&AM' EQ 'TAM')
&AM4     SETB  ('&AM' EQ 'GAM')
&AM5     SETB  ('&AM' EQ 'TCAM')
&AM6     SETB  ('&AM' EQ 'EXCP')
&AM7     SETB  0
         AIF   (&AM0 OR &AM1 OR &AM2 OR &AM3 OR &AM4 OR &AM5 OR &AM6   X
               OR &AM7).DBCK120         BRANCH IF &AM VALID
.DBCK100 ANOP
&ERR     SETB  1                        SET ERROR INDICATOR
         IHBERMAC 1001,AM,&AM
.*
.*                       ESTABLISH AND TEST &TYPE VALUE
.*
.DBCK120 AIF   ('&MF' EQ 'L').DBCK140   BRANCH IF LIST FORM
&TYPEA   SETB  ('&TYPE' EQ 'ADD')
&TYPED   SETB  ('&TYPE' EQ 'DELETE')
&TYPEP   SETB  ('&TYPE' EQ 'PURGE')
.*                                      VERIFY IS DEFAULT TYPE
&TYPEV   SETB  ('&TYPE' EQ 'VERIFY' OR T'&TYPE EQ 'O')
.*                                      BRANCH IF NO ERROR
         AIF   (&TYPEA OR &TYPED OR &TYPEV OR &TYPEP).DBCK140
         IHBERMAC 1001,TYPE,&TYPE
         AGO   .DBCK160                 EXIT BECAUSE OF ERROR
.DBCK140 AIF   (NOT &ERR).DBCK220       BRANCH IF NO SYNTAX ERRORS
.*                                      GENERATE NAME IF NO CODE GENNED
.*
.*                       ERROR EXIT
.*
.DBCK160 AIF   ('&NAME' EQ '').DBCK200  DON'T GENERATE NAME IF NULL
         AIF   ('&MF' NE 'L').DBCK180   BRANCH IF NOT LIST FORM
&NAME    DS    0C
         MEXIT
.DBCK180 ANOP
&NAME    DS    0H
.DBCK200 MEXIT
.*
.*                       ************
.*                       * GENERATE *
.*                       *   CODE   *
.*                       ************
.*
.DBCK220 ANOP
&LABEL   SETC  '&NAME'                  SET LABEL FOR FIRST GENERATED
         AIF   ('&MF' EQ 'L').DBCK340   BRANCH IF LIST FORM
.*                                      LINE OF CODE
.*
.*                       FUNCTION TYPE VALUE
.*
.*                         VERIFY       0
.*                         ADD          1
.*                         DELETE       2
.*                         PURGE        3
.*
         AIF   (&TYPEV).DBCK240         BRANCH IF VERIFY
&A       SETA  &TYPEA*1+&TYPED*2+&TYPEP*3 SET AM TYPE
&LABEL   LA    0,&A                     LOAD TYPE VALUE
&LABEL   SETC  ''                       LABEL NOT TO BE GENERATED AGAIN
.DBCK240 AIF   (T'&AM NE 'O').DBCK260   BRANCH IF &AM SPECIFIED
         AIF   (NOT &TYPEV).DBCK420     BRANCH IF ADD OR DELETE TYPE
&LABEL   SR    0,0                      LOAD TYPE AND AM VALUE
         AGO   .DBCK420
.*
.*                       ACCESS METHOD TYPE VALUE
.*
.*                          ISAM        80
.*                          BDAM        40
.*                          SAM/BPAM    20
.*                          TAM         10
.*                          GAM         08
.*                          TCAM        04
.*                          EXCP        02
.*                          OMITTED     00
.*
.DBCK260 ANOP
&REG     SETC  '0'                      INITIALIZE TO REG 0
         AIF   (&TYPEV).DBCK280         BRANCH IF TYPE IS VERIFY
&REG     SETC  '15'                     USE REG 15 IF ADD OR DELETE
.DBCK280 AIF   ('&AMFC' NE '(').DBCK340 BRANCH IF &AM TO BE CALCULATED
&AMSC    SETC  '&AM'(2,1)               SECOND CHARACTER OF &AM
         AIF   ('&AMSC' EQ '(').DBCK320 BRANCH IF &AM A REGISTER
.*
.*                       &AM VALUE TO BE INSERTED FROM AN ADDRESS
.*
         AIF   (NOT &TYPEV).DBCK300     BRANCH IF NOT VERIFY
&LABEL   SR    0,0                      LOAD TYPE VALUE
&LABEL   SETC  ''                       LABEL NOT TO BE GENERATED AGAIN
.DBCK300 ANOP
&AMFC    SETC  '&AM(1)'                 ADDRESS OF AM VALUE
&LABEL   ICM   0,8,&AMFC                INSERT AM VALUE IN HI BYTE
         AGO   .DBCK420
.*
.*                       &AM VALUE IN A REGISTER
.*
.DBCK320 ANOP
&A       SETA  K'&AM-4                  LENGTH OF REGISTER
&AMFC    SETC  '&AM'(3,&A)              REGISTER CONTAINING AM VALUE
&LABEL   LR    &REG,&AMFC               LOAD AM VALUE
         AGO   .DBCK380                 GO TO SHIFT TO HIGH BYTE
.*
.*                       &AM VALUE TO BE CALCULATED
.*
.DBCK340 ANOP
&A       SETA  128*&AM0+64*&AM1+32*&AM2+16*&AM3+8*&AM4+4*&AM5+2*&AM6
         AIF   ('&MF' NE 'L').DBCK360   BRANCH IF NOT LIST FORM
&NAME    DC    AL1(&A)
         MEXIT
.DBCK360 ANOP
&LABEL   LA    &REG,&A                  LOAD AM VALUE
.DBCK380 SLL   &REG,24                  SHIFT TO HIGH ORDER BYTE
.DBCK400 AIF   (&TYPEV).DBCK420
         OR    0,15                     OR AM TYPE INTO REGISTER 0
.*
.*                       LOAD CB ADDRESS AND SVC ROUTING ID
.*
.DBCK420 IHBINNRA &CBAD                 LOAD CBAD INTO REGISTER 1
         LA    15,2                     DEBCHK SVC ID
         SVC   117
         MEND
*          DATA SET CBT380T    AT LEVEL 001 AS OF 06/09/77
DEBE     TITLE 'OPERATING SYSTEM/360 MULTIFUNCTION UTILITY - VERSION 2'
OSDEBE   START 0
***********************************************************************
*
*  THE PURPOSE OF OSDEBE IS TO PROVIDE THE USER OF 360 OPERATING
*  SYSTEM THE FLEXIBILITY OF THE ORIGINAL DEBE.  FLEXIBILITY CAN
*  ONLY BE OBTAINED BY THE ELIMINATION OF DD CARDS.  ALL FUNCTIONS
*  WHICH ARE AVAILABLE WITH BPS DEBE ARE PROVIDED IN OSDEBE.
*  IN ADDITION, MANY NEW FEATURES HAVE BEEN ADDED; FOR A COMPLETE
*  DESCRIPTION, SEE THE OSDEBE PROGRAM MANUAL.
*
***********************************************************************
*
*  CORE REQUIREMENTS - ABOUT 16K (K=1024), 8K OF WHICH IS USED
*  AS AN I/O AREA FOR TAPE OPERATIONS.
*
*  ERROR RECOVERY - THE OPERATING SYSTEM PERFORMS ALL STANDARD
*  ERROR RECOVERY.  IF IT CANNOT RECOVER FROM AN ERROR, OSDEBE
*  WILL NOT EVEN TRY.
*
*  ALL REPLIES TO OSDEBE ARE OF THE WTOR FORMAT,
*          REPLY 00,'TP'
*  WHENEVER ANY INFORMATION IS REQUESTED BY OSDEBE, A REPLY OF
*  'EJ' WILL TERMINATE THE SUBROUTINE OF OSDEBE.  A REPLY OF 'EJ'
*  IN RESPONSE TO THE REQUEST FOR THE PROGRAM ID WILL TERMINATE
*  OSDEBE AND CONTROL IS RETURNED TO THE OPERATING SYSTEM.
*
*  ONCE A ROUTINE IS RUNNING IN OSDEBE, IT CAN ONLY BE TERMINATED
*  BY CANCELLING THE JOB, AND THE JOB MUST BE RESCHEDULED AGAIN.
*  AN ALTERNATIVE TO CANCELLING THE JOB IS TO CAUSE AN I/O ERROR
*  ON ONE OF THE UNITS BEING USED.  AN I/O ERROR WILL CAUSE THE
*  SUBROUTINE OF OSDEBE TO TERMINATE, BUT OSDEBE WILL STILL BE
*  IN CONTROL.
*
***********************************************************************
         EJECT
*
*  INITIALIZATION
*
         USING  OSDEBE,15
         B     AROUND
         DC    C'OSDEBE-REVISED: CONTACT SYSTEMS PROGRAMMING '
         DC    C'IF ANY PROBLEMS.'
K4096    DC    F'4096'
AROUND   DS    0H
         SAVE  (14,12)
BEGIN    BALR  12,0
         USING *,12,9
         LR    9,12
         A     9,K4096
BEGIN1   EQU   *
         LA    14,SAVEAREA
         ST    13,SAVEAREA+4
         ST    14,8(0,13)
         LR    13,14
         MODESET KEY=ZERO,MODE=SUP     GET INTO SUP STATE        DAL
         GETMAIN R,LV=564,SP=252
         LR    8,1
         LA    3,TDAREA
         LA    3,80(3)  GET ADDRESS OF WORKAREA SKELETONS
         MVC   0(232,8),0(3)
         MVC   232(232,8),232(3)
         USING WORKAREA,8
         LA    3,INDEB
         ST    3,AET1
         LA    3,APPEND
         ST    3,AET2
         ST    3,AET3
         ST    3,AET4
         ST    3,AET5
         ST    3,AET6
         ST    3,AET14
         ST    3,AET15
         ST    3,AET16
         ST    3,AET17
         ST    3,AET18
         LA    3,OUTDEB
         ST    3,AET7-1
         MVI   AET7-1,X'04'
         ST    3,AET13
         LA    3,INDCB
         ST    3,AET8-1
         MVI   AET8-1,X'0F'
         ST    3,AET12
         LA    3,INIOVEC
         ST    3,AET9-1
         MVI   AET9-1,X'02'
         LA    3,INECB
         ST    3,AET10-1
         MVI   AET10-1,X'7F'
         LA    3,INCCW
         ST    3,AET11
         LA    3,OUTDCB
         ST    3,AET19-1
         MVI   AET19-1,X'0F'
         ST    3,AET23
         LA    3,OUTIOVEC
         ST    3,AET20-1
         MVI   AET20-1,X'02'
         LA    3,OUTECB
         ST    3,AET21-1
         MVI   AET21-1,X'7F'
         LA    3,OUTCCW
         ST    3,AET22
         SPACE 5
*  GET CVT POINTER
         L     3,16
*  GET POINTER TO THE TCB POINTERS
         L     2,0(0,3)
*  GET TCB POINTER
         L     3,4(0,2)
*  GET PROTECT KEY FROM TCB FOR THE DEB'S
         OC    INDEB+24(1),28(3)
         OC    OUTDEB+24(1),28(3)
*  GET TCB ADDRESS FOR THE DEB'S
         ST    3,INDEB
         ST    3,OUTDEB
         L     11,8(,3)                ADDR OF TCB DEB QUEUE     DAL
         LA    11,0(,11)               CLR HIGH BYTE NATCH       DAL
         LTR   11,11                   NO DEBS ON QUEUE
         BNZ   CHKDEBAD                NO CHK DEBS FOR NEXT      DAL
         IC    15,8(,3)                SAVE BYTE                 DAL
         LA    1,INDEB                 ADR OF OUR FIRST DEB      DAL
         ST    1,8(,3)                 STORE IT IN TCB           -AL
         STC   15,8(,3)                RESTORE BYTE
         B     DEBCHK                  GO DO DEBCHK MACRO        DAL
CHKDEBAD L     15,4(,11)               ADDR NEXT DEB
         LA    15,0(,15)               CLR HIGH
         LTR   15,15                   END OF CHAIN              DAL
         BZ    STOREDEB                YES STORE DEB ADDR IN DEB DAL
         LR    11,15                   NO CHK NEXT DEB
         B     CHKDEBAD
STOREDEB LA    1,INDEB                 ADDR OUR DEB
         IC    15,4(,11)               SAVE BYTE                 DAL
         ST    1,4(,11)                STORE INTO PREV LAST DEB  DAL
         STC   15,4(,11)               RETURN BYTE TO WHENCE T   DAL
DEBCHK   EQU   *
         LA    11,OUTDCB               GET DCB ADDR              DAL
         DEBCHK (11),TYPE=ADD          PUT DEB ON DEB TABLE      DAL
         LA    11,INDCB                GET DCB ADDR              DAL
         DEBCHK (11),TYPE=ADD          PUT DEB ON DEB TABLE      DAL
     SPACE 2
*   GET SUBPOOL WORK AREA FOR TAPE I/O OPERATIONS
     SPACE 2
         GETMAIN  R,LV=32768,SP=0
         ST    1,TAPEAREA        STORE ADDRESS OF TAPE WORK I/O AREA
     SPACE 2
*  STORE THE ADDRESS IN THE TWO   TAPE CCW'S
     SPACE 2
         MVC   TDINCCW+1(3),TAPEAREA+1
         MVC   TTCCWOUT+1(3),TAPEAREA+1
         EJECT
*
*  MAIN PROGRAM
*  ALL ROUTINES RETURN HERE WHEN THEY ARE FINISHED.
*
ASKAGN   EQU   *
         NI    INIOB,X'3F'         TURN OFF CHAIN FLAGS IN IOB'S
         NI    OUTIOB,X'3F'
         SPACE 2
         NI    TD3+1,X'0F'   RESET SWITCHES SET IN TP ROUTINE
         NI    TD6+1,X'0F'
         SPACE 3
         WTOR  'DEBE: ENT PROG ID - XX',ID,2,REPLYECB
         BAL   11,WAITANS    GO WAIT FOR A REPLY
         OC    ID(2),BLANKS
         CLC   ID(2),=C'EJ'  IS SHE DONE? - EOJ
         BE    EOJ           YES, RETURN TO THE OPERATING SYSTEM
         CLC   ID(2),=C'BF'  BACKSPACE A FILE
         BE    BF
         CLC   ID(2),=C'BS'  BACKSPACE A NUMBER OF RECORDS
         BE    BS
         CLC   ID(2),=C'BT'  BLOCK CARD IMAGE ON TAPE
         BE    BT
         CLC   ID(2),=C'CC'  CARD TO CARD
         BE    CC
         CLC   ID(2),=C'CP'  CARD TO PRINTER
         BE    CP
         CLC   ID(2),=C'CT'  CARD TO TAPE
         BE    CT
         CLC   ID(2),=C'DP'  LIST TAPE TO PRINTER
         BE    DP
         CLC   ID(2),=C'HD'  TAPE LABEL ONTO SYSTEM CONSOLE
         BE    HD
         CLC   ID(2),=C'KC'  COUNT CARDS
         BE    KC
         CLC   ID(2),=C'KR'  COUNT RECORDS
         BE    KR
         CLC   ID(2),=C'OX'  BAD
         BE    BADID
         CLC   ID(2),=C'RU'  REWIND AND UNLOAD
         BE    RU
         CLC   ID(2),=C'RW'  REWIND
         BE    RW
         CLC   ID(2),=C'SF'  SPACE FORWARD FILE
         BE    SF
         CLC   ID(2),=C'SR'  SKIP # OF RECORDS FORWARD
         BE    SR
         CLC   ID(2),=C'TC'  TAPE TO CARD
         BE    TC
         CLC   ID(2),=C'TD'  TAPE TO PRINT HEX
         BE    TD
         CLC   ID(2),=C'TM'  WRITE TAPE MARK
         BE    TM
         CLC   ID(2),=C'TP'  TAPE TO PRINT HEX
         BE    TP
         CLC   ID(2),=C'TT'  TAPE TO TAPE
         BE    TT
         CLC   ID(2),=C'WT'  WRITE TO TAPE MARK
         BE    WT
         CLC   ID(2),=C'WL'  WRITE A LABEL
         BE    WL                                            *BW 7/76*
*        CLC   ID(2),=C'XP'
*        BE    BADID
*        CLC   ID(2),=C'XR'
*        BE    BADID
*        CLC   ID(2),=C'XS'
*        BE    BADID
*        CLC   ID(2),=C'XA'
*        BE    BADID
*        CLC   ID(2),=C'XB'
*        BE    BADID
BADID    WTO   'DEBE: BAD ID - TRY AGAIN'
         B     ASKAGN
         SPACE 3
EOJ      EQU   *
         LA    11,INDCB                DCB ADDR                  DAL
         DEBCHK (11),TYPE=DELETE       DELETE DEB FROM DEB TABLE DAL
         LA    11,OUTDCB               DCB ADDR                  DAL
         DEBCHK (11),TYPE=DELETE       DELETE DEB FROM DEB TABLE DAL
         L     1,16          CVT
         L     1,0(,1)       TCB QUEUE
         L     1,4(,1)       TCB ADDR
         L     10,8(,1)      DEB QUEUE
         LA    10,0(,10)     CLEAR HIGH BYTE
STOREZER IC    10,8(,1)      SAVE BYTE OF TCB
         ST    11,8(,1)      PUT SYS DEB ADDR IN TCB
         STC   10,8(,1)      PUT BACK BYTE
         B     EOJA
CHKEND   L     1,4(,10)      NEXT DEB ON Q
         LA    1,0(,1)       CLEAR BYTE
         CR    1,11          OUR DEB YET
         BE    CHKDEBND      YES PROCESS
         L     10,4(,10)     NO ,GET NEXT DEB ON QUEUE
         B     CHKEND        NO LOOK AT NEXT DEB
CHKDEBND L     15,OUTDEB+4   ADDR OF OUR LAST DEBS NEXT POINTER
         LA    15,0(,15)     ZERO OUT BYTE
         IC    11,4(,10)     SAVE BYTE
         ST    15,4(,10)     STORE ADDR OF NEXT INTO LAST DEB
         STC   11,4(,10)     RESTORE BYTE
EOJA     EQU   *
         L     13,4(0,13)
         RETURN (14,12),RC=0
         EJECT
*
*  SUBPROGRAMS CALLED BY ID
*
*  BACKSPACE A FILE ON TAPE
         SPACE 1
BF       EQU   *
         BAL   10,TPOUTSET   DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),BFCCW   MOVE IN THE BF CCW
         B    SFEXCP
         SPACE 1
*  FORWARD SPACE A FILE ON TAPE
         SPACE 1
SF       EQU   *
         BAL   10,TPOUTSET   DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),SFCCW   MOVE IN SF CCW
SFEXCP   EQU   *
         BAL   11,MULTGET
SFLOOP   EQU   *
         BAL   11,IOOUT      GO EXECUTE THE CCW
         CLI   OUTECB,X'7F'  WAS EVERYTHING OK
         BNE   TAPERR
         BCT   10,SFLOOP          TRY AGAIN
         B     ASKAGN             ALL DONE
MULTGET  EQU   *
         ST    11,SV11            SAVE 11
         WTOR  'DEBE: NR FILES TO BE PROCESSED - XXXX',                +
               TDAREA,4,REPLYECB
         BAL   11,WAITANS
         OC    TDAREA(4),BLANKS
         CLC   TDAREA(2),=C'EJ'
         BE    ASKAGN             NEW ROUTINE
         OI    TDAREA+3,X'F0'
         PACK  TDWK,TDAREA(4)     PACK NUMBER
         CVB   10,TDWK            MAKE NUMBER BINARY
         L     11,SV11
         BR    11                 RETURN
         EJECT
*  BACKSPACE NNNN RECORDS ON TAPE
         SPACE 1
BS       EQU   *
         SPACE 1
*  SKIP NNNN RECORDS ON TAPE
         SPACE 1
SR       EQU   *
         BAL   10,TPOUTSET   DETERMINE WHICH TAPE UNIT AND SET UP
         WTOR  'DEBE: NR REC TO BE SKIPPED - XXXX',TDAREA,4,REPLYECB
         BAL   11,WAITANS    GO WAIT FOR A REPLY
         OC    TDAREA(2),BLANKS
         CLC   TDAREA(2),=C'EJ'  END THIS ROUTINE
         BE    ASKAGN        YES, EXIT THIS ROUTINE
         OI    TDAREA+3,X'F0'      MAKE A GOOD SIGN FOR CVB
         PACK  TDWK,TDAREA(4)
         CVB   2,TDWK        PUT NUMBER OF REC'S IN REG 2
         CLC   ID(2),=C'SR'
         BE    SREX
BSEX     MVC   OUTCCW+8(8),BSCCW   SET UP BS CCW
BSAGN    EQU   *
         BAL   11,IOOUT      EXECUTE THE CCW
         CLI   OUTECB,X'7F'  EVERYTHING OK
         BE    BCT6          YES, DO AGAIN TILL COUNT = 0
         B     TAPERR        NO, END THIS ROUTINE
BCT6     EQU   *
         BCT   2,BSAGN       DECREMENT THE COUNT AND DO AGAIN
         B     ASKAGN        ALL FINISHED WITH THIS ROUTINE
SREX     MVC   OUTCCW+8(8),SRCCW   SET UP SR CCW
         B     BSAGN
         EJECT
*  CARD TO CARD REPRODUCE
         SPACE 1
CC       EQU   *
         MVI   INMODSTK,X'41'      SET UP READER'S DCB
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1       USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD    STORE UCB ADDRESS IN DEB
         MVI   OUTMDSTK,X'42'
         MVI   OUTDEVT,X'43'
         MVC   D1(3),PUNCH1        USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,OUTDEBMD    STORE UCB ADDRESS IN DEB
         MVC   INCCW(8),CP1CCW     SET UP READ CCW
         MVC   OUTCCW(8),S2CCW
CCLOOP   EQU   *
         BAL   11,IOIN       READ A CARD
         CLI   INECB,X'7F'         EVERYTHING OK
         BE    CC1                 YES, BRANCH
         TM    INSTAT+4,X'01'    EOF
         BC    1,PCHDN
         B     RDRERROR      READER ERROR, END THIS ROUTINE
CC1      EQU   *
         BAL   11,IOOUT      PUNCH A CARD
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BE    CCLOOP        YES, LOOP
*
PCHERROR WTO   'DEBE: PUNCH ERROR - EOJ'
PCHDN    EQU   *
         MVC   OUTCCW(8),ROCCW
         LA    11,ASKAGN
         B     IOOUT
         EJECT
*  CARD TO PRINTER - 80/80 LIST
         SPACE 1
CP       EQU   *
*  SET UP READER'S DCB AND DEB
         MVI   INMODSTK,X'41'
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1       USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD          STORE UCB ADDRESS IN DEB
         BAL   11,OPENPRNT   OPEN SYSPRINT IF NOT OPEN
CP2      EQU   *
         BAL   11,SKIPTO1    SKIP TO CHANNEL 1
*  SET UP READER CCW
CPGO     MVC   INCCW(8),CP1CCW     SET UP READ CCW
CPLOOP   EQU   *
         BAL   11,IOIN       READ A CARD
         CLI   INECB,X'7F'   EVERYTHING OK
         BE    CP1           YES, BRANCH
         TM    INSTAT+4,X'01'      EOF?
         BC    1,TDEOJ
RDRERROR WTO   'DEBE: READER ERROR - EOJ'
         B     ASKAGN
CP1      EQU   *
         MVC   TDOUTAR(80),TDAREA      CLEAR UPPER PRINT LINE AFTER
         MVC   TDOUTAR+80(52),=CL52' ' MOVING DATA TO PRINT LINE
         BAL   11,TDPRINT    PRINT A CARD
         B     CPLOOP        LOOP
         EJECT
*  CARD TO TAPE
         SPACE 1
CT       EQU   *
*  SET UP READER'S DCB AND DEB
         MVI   INMODSTK,X'41'
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1       USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD    STORE UCB ADDRESS IN DEB
         MVC   INCCW(8),CP1CCW     SET UP READ CCW
*  SET UP TAPE'S DCB AND DEB
         BAL   10,TPOUTSET   DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),CTCCW   SET UP WRITE CCW
         SR    2,2           CLEAR A COUNTER
CTLOOP   EQU   *
         BAL   11,IOIN       READ A CARD
         CLI   INECB,X'7F'         EVERYTHING OK
         BE    CT1           YES, BRANCH
         TM    INSTAT+4,X'01'      EOF
         BC    1,CTEOJ
         B     RDRERROR      READER ERROR, END THIS ROUTINE
CT1      EQU   *
         LA    2,1(2)        INCREMENT A COUNTER
         BAL   11,IOOUT      WRITE TO TAPE
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BE    CTLOOP        YES, LOOP
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
         B     TAPERR
CTEOJ    EQU   *
         BAL   11,WTM
         B     ASKAGN
         EJECT
* LIST TAPE (DEBLOCKED) ON PRINTER
         SPACE 1
DP       EQU   *
         BAL   11,OPENPRNT        OPEN PRINTER
         BAL   11,SKIPTO1         RESTORE CARRIAGE
         BAL   10,TAPINSET        GET A TAPE
         MVC   INCCW+8(8),TDINCCW
         BAL   11,TDPRINT         PRINT HEADER
         BAL   11,SPACE1
         WTOR  'DEBE: ENT LRECL - XXXX',TDAREA,4,REPLYECB
         BAL   11,WAITANS
         OC    TDAREA(4),BLANKS   SHIFT TO UPPER CASE
         CLC   TDAREA(2),=C'EJ'
         BE    ASKAGN
         OI    TDAREA+3,X'F0'     MAKE A BINARY NUMBER
         PACK  TDWK,TDAREA(4)
         CVB   4,TDWK
         BAL   10,TAPEBLOK
DPLOOP1  EQU   *
         SR    2,2
         BAL   11,IOIN            READ A TAPE RECORD
         CLI   INECB,X'7F'        OK?
         BE    DP1
         TM    INSTAT+4,X'01'     EOF?
         BC    1,TDEOJ
         B     TAPERROR
DP1      EQU   *
         LH    10,INSTAT+6        RES COUNT
         LH    3,TDINCCW+6        REQUESTED
         SR    3,10               GET LENGTH
DPLOOP2  EQU   *
         L     10,TAPEAREA    GET ADDRESS OF SP 0 WORK AREA
         AR    10,2
         MVC   TDOUTAR(132),BLANKS
         LR    5,4
         BCTR  5,0                DECREMENT GR5
         EX    5,DPMVC            *MVC TDOUTAR(L),0(10)
         BAL   11,TDPRINT         PRINT LINE
         AR    2,4                UP POINTER
         CR    2,3                DONE?
         BL    DPLOOP2
         TM    NBRSW,X'FF'
         BO    DPLOOP1
         BCT   6,DPLOOP1
         B     TDEOJ
DPMVC    MVC   TDOUTAR(0),0(10)
         EJECT
* BLOCK CARDIMAGE ON TAPE
         SPACE 1
BT       EQU   *
         MVI   INMODSTK,X'41'     PRIME CONTROL BLOCKS
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD
         MVC   INCCW(8),CP1CCW
         BAL   10,TPOUTSET        GET OUTPUT TAPE
         MVC   OUTCCW+8(8),TTCCWOUT
         WTOR  'DEBE: ENT BLK FACTOR - XXXX',TDAREA,4,REPLYECB
         BAL   11,WAITANS
         OC    TDAREA(4),BLANKS
         CLC   TDAREA(2),=C'EJ'
         BE    ASKAGN
         OI    TDAREA+3,X'F0'
         PACK  TDWK,TDAREA(4)
         CVB   2,TDWK             PUT IN GR2
         MH    2,=H'80'
BTLOOP1  EQU   *
         SR    4,4
         LA    3,TDAREA
BTLOOP2  EQU   *
         ST    3,TDWK
         MVC   INCCW+1(3),TDWK+1
         BAL   11,IOIN            READ A CARD
         CLI   INECB,X'7F'        OPERATION OK?
         BE    BT1
         TM    INSTAT+4,X'01'     EOF?
         BC    1,BTEOJ
         B     RDRERROR
BT1      EQU   *
         LA    3,80(3)            MOVE POINTER
         LA    4,80(4)            INCR COUNTER
         CR    2,4
         BNE   BTLOOP2            TRY AGAIN
         STH   4,OUTCCW+14        COUNT FIELD
         BAL   11,IOOUT           WRITE TAPE
         CLI   OUTECB,X'7F'       OK?
         BNE   TAPERR
         B     BTLOOP1            PRAY WE GET OUT OF THIS LOOP!
BTEOJ    EQU   *
         LTR   4,4
         BZ    CTEOJ
         STH   4,OUTCCW+14
         BAL   11,IOOUT
         CLI   OUTECB,X'7F'       OPERATION OK?
         BNE   TAPERR
         B     CTEOJ              WRITE T/M
         EJECT
*  TAPE TO CARD, DEBLOCKING
         SPACE 1
TC       EQU   *
         BAL   10,TAPINSET         SET UP TAPE
         MVC   INCCW+8(8),TDINCCW  SET UP READ CCW
         MVI   OUTDEVT,X'43'
         MVI   OUTMDSTK,X'42'
         MVC   D1(3),PUNCH1
         BAL   14,UCBLOKUP
         ST    2,OUTDEBMD
         MVC   OUTCCW(8),DBCCW     PRIME CCW
DBLOOP1  EQU   *
         SR    2,2
         BAL   11,IOIN             READ A RECORD
         CLI   INECB,X'7F'         OK?
         BE    DB1                 YES, BRANCH
         TM    INSTAT+4,X'01'      EOF?
         BC    1,PCHDN
         B     TAPERROR
DB1      EQU   *
         LH    10,INSTAT+6         GET RES CNT
         LH    3,TDINCCW+6         GET START CNT
         SR    3,10                GET LENGTH
DBLOOP2  L     10,TAPEAREA    GET ADDRESS OF SP 0 WORK AREA
         AR    10,2
         MVC   TDAREA2(80),0(10)
         BAL   11,IOOUT            PUNCH CARD
         CLI   OUTECB,X'7F'        OK?
         BNE   PCHERROR            NO, BRANCH
         LA    2,80(2)             MOVE POINTER
         CR    2,3                 DONE?
         BL    DBLOOP2             NO, GO AGAIN
         B     DBLOOP1             NEXT RECORD
         EJECT
*  PRINT A TAPE
         SPACE 1
TP       EQU   *
*  TP USES THE TD ROUTINE WITHOUT THE HEX-TO-CHARACTER CONVERSION
         OI    TD3+1,X'F0'         SET SWITCHES IN TD
         OI    TD6+1,X'F0'
         SPACE 1
*  PRINT A TAPE IN HEX UNLESS SWITCHES SET BY TP
         SPACE 1
TD       EQU   *
         BAL   11,OPENPRNT   OPEN SYSPRINT IF NECESSARY
         BAL   11,SKIPTO1     SKIP TO CHANNEL 1
*  SET UP TAPE'S DCB AND DEB
         BAL   10,TAPINSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   INCCW+8(8),TDINCCW  SET UP READ CCW
         BAL   11,TDPRINT    PRINT THE HEADING LINE
         BAL   11,SPACE1      SPACE A LINE
         SP    RECCNT(3),RECCNT(3) CLEAR THE RECORD COUNTER
         BAL   10,TAPEBLOK
TDLOOP   EQU   *
         BAL   11,IOIN        READ A RECORD
         CLI   INECB,X'7F'         EVERYTHING OK
         BE    TD1           YES, BRANCH
         TM    INSTAT+4,X'01'      EOF
         BC    1,TDEOJ       YES, GO END ROUTINE
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
         B     TAPERROR
TD1      LH    2,INSTAT+6          GET RESIDUAL COUNT
         LH    3,TDINCCW+6         GET BEGINING COUNT
         SR    3,2                 GET BYTES READ IN
         CVD   3,TDWK
         MVI   TDOUTAR,C' '        CLEAR PRINT LINE
         MVC   TDOUTAR+1(131),TDOUTAR
         MVC   TDOUTAR+102(23),RECHDG  SET UP LINE INFORMATION
         ED    TDOUTAR+119(6),TDWK+5
         AP    RECCNT(3),COND1(1)      INCREMENT RECORD COUNTER
         ED    TDOUTAR+105(6),RECCNT
*     GET READY FOR HEX CONVERSION
         L     2,TAPEAREA   GET ADDRESS OF SP 0 WORKAREA
         L     5,TAPEAREA   GET ADDRESS OF SP 0 WORKAREA
         LA    7,100
TD6      BC    0,TD7         SET TO BRANCH BY TP ROUTINE
*  R2 POINTS TO THE INPUT AREA
*  R5 POINTS TO THE OUTPUT AREA
         L     5,=A(TDAREA2)
         LA    7,50
         BAL   14,UNPK       GO CONVERT 50 BYTES TO 100 HEX
TD7      EQU   *
         CR    3,7           SHORT LINE TO BE PRINTED
         BL    TD3           YES, GO DO THE MOVE
         MVC   TDOUTAR(100),0(5)   ELSE DO 100 BYTE MOVE
TDPR     EQU   *
         BAL   11,TDPRINT     PRINT A LINE
         MVI   TDOUTAR,C' '        CLEAR OUTPUT AREA
         MVC   TDOUTAR+1(131),TDOUTAR
         SR    3,7           DECREMENT BLKSIZE
         BC    12,TD9         BRANCH IF BLOCK FINISHED
         AR    2,7            ELSE, INCREMENT COUNTERS
         AR    5,7
         B     TD6           AND LOOP
TD3      BC    0,TD4         SET TO BRANCH BY TP ROUTINE
         LR    11,3          GET REMAINDER OF DATA IN 11
         AR    11,3          DOUBLE IT BECAUSE IT'S UNPACKED
         BCTR  11,0          DECREMENT ONE FOR THE MOVE
         EX    11,MVCOML
         B     TDPR          GO PRINT SHORT LINE
TD4      EQU   *
         BCTR  3,0
         EX    3,MVCOML
         B     TDPR           GO PRINT SHORT LINE
MVCOML   MVC   TDOUTAR(0),0(5)        COMMON MOVE INSTRUCTION
UNPK     EQU   *             CONVERT 50 BYTES TO 100 BYTES HEX
         UNPK  0(15,5),0(8,2)
         UNPK  14(15,5),7(8,2)
         UNPK  28(15,5),14(8,2)
         UNPK  42(15,5),21(8,2)
         UNPK  56(15,5),28(8,2)
         UNPK  70(15,5),35(8,2)
         UNPK  84(15,5),42(8,2)
         UNPK  98(3,5),49(2,2)
         TR    0(100,5),TDPTABLE-240
         BR    14
TD9      EQU   *
         BAL   11,SPACE1     SPACE A LINE AFTER WHOLE BLOCKED
         TM    NBRSW,X'FF'
         BO    TDLOOP
         BCT   6,TDLOOP
         MVC   INCCW+8(8),TDINCCW  SET UP READ CCW
         BAL   10,TAPEBLOK
         BAL   11,MULTGET         GET FACTOR
TTLOOP   EQU   *
         BAL   11,IOIN        READ A BLOCK
         CLI   INECB,X'7F'
         BE    TT1           EVERYTHING OK, BRANCH
         TM    INSTAT+4,X'01'
         BC    1,TTEST
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
TAPERROR EQU   *
         WTO   'DEBE: INPUT TAPE ERROR - EOJ'
         B     ASKAGN
TT1      LH    2,INSTAT+6          GET RESIDUAL COUNT
         LH    3,TDINCCW+6         GET BEGINNING COUNT
         SR    3,2           COMPUTE BYTES READ IN
         STH   3,TTCCWOUT+6        SET UP WRITE CCW
         MVC   OUTCCW+8(8),TTCCWOUT
         BAL   11,IOOUT      WRITE A BLOCK
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BNE   TTERR
         TM    NBRSW,X'FF'
         BO    TTLOOP
         BCT   6,TTLOOP
         B     ASKAGN
TTERR    EQU   *
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
TAPERR   EQU   *
         WTO   'DEBE: OUTPUT TAPE ERROR - EOJ'
         B     ASKAGN
TTEST    EQU   *
         LA    11,TTLOOP
         BCT   10,WTM
         B     CTEOJ
         EJECT
*      TYPE TAPE HEADER LABELS ONTO SYSTEM CONSOLE
HD       EQU   *
         BAL   10,TAPINSET   WHICH TAPE UNIT
         MVC   INCCW+8(8),TDINCCW
         MVI   HDRCTR,X'1C'         RESET HEADER COUNTER
HDRLOOP  EQU   *
         BAL   11,IOIN
         CLI   INECB,X'7F'
         BE    HDREADOK
         TM    INSTAT+4,X'01'
         BC    1,HDREOJ
         B     TAPERROR
HDREADOK EQU   *
         L     11,TAPEAREA   GET ADDRESS OF TAPE INPUT AREA
         MVC   TYPER+8(80),0(11)  MOVE FIRST 80 CHARS TO WTO MESSAGE
TYPER    WTO   '                                                       X
               TEST LINE                '
         AP    HDRCTR(1),HDRCTR+1(1)      ADD ONE TO HEADER COUNTER
         CP    HDRCTR(1),HDRCTR+2(1)    CHECK FOR END OF LOOP
         BNH   HDRLOOP
HDREOJ   B     ASKAGN
HDRCTR   DC    P'1',P'1',P'3'
         DS    0H
    SPACE   5
*OX      EQU   *   TURN OFF THE OFFLINE PROHIBITION        **BW SIK**
*        NI    OFLNTEST+1,X'0F'  TURN OF BR AT OFFLINE TEST**BW SIK**
*        B     ASKAGN                                      **BW SIK**
         SPACE 5
RU       EQU   *
         BAL   10,TPOUTSET
         MVC   OUTCCW+8(8),RUCCW
         BAL   11,IOOUT
         B     ASKAGN
         SPACE 3
RW       EQU   *
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),RWCCW   SET UP REWIND CCW
         BAL   11,IOOUT            EXECUTE THE REWIND
         B     ASKAGN
         SPACE 3
WT       EQU   *
         BAL   10,TPOUTSET     DETERMINE WHICH TAPE UNIT AND SET UP
         B     CTEOJ         GO WRITE A TAPE MARK
         SPACE 3
TM       EQU   *
         BAL   10,TPOUTSET
         BAL   11,MULTGET
TM1      EQU   *
         BAL   11,WTM
         BCT   10,TM1
         B     ASKAGN
         SPACE 3
KR       EQU   *
         BAL   10,TAPINSET
         MVC   INCCW+8(8),TDINCCW
         B     KNTR
         SPACE 3
KC       EQU   *
         MVI   INMODSTK,X'41'
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD
         MVC   INCCW(8),CP1CCW
         B     KNTR
WL       EQU   *              WRITE A TAPE LABEL            *BW 7/76*
         BAL   10,TPOUTSET    REQUEST OUTPUT TAPE           *BW 7/76*
         MVC   OUTCCW+8(8),RWCCW  MOVE IN REWIND CCW        *BW 7/76*
         OI    OUTCCW+12,X'60'    SET CMD CHAIN & SLI FLAG  *BW 7/76*
         MVC   OUTCCW+16(8),=X'0300000020000001' NO-OP      *BW 7/76*
         BAL   11,IOOUT       GO DO REWIND                  *BW 7/76*
         SPACE 1                                            *BW 7/76*
*   WRITE VOL TAPE LABEL                                    *BW 7/76*
         SPACE 1                                            *BW 7/76*
         MVI   TDAREA,X'40'   CLEAR I/O AREA                *BW 7/76*
         MVC   TDAREA+1(79),TDAREA                          *BW 7/76*
         WTOR  'DEBE: REPLY VOLID - XXXXXX',TDAREA+4,6,     *BW 7/76*  X
               REPLYECB                                     *BW 7/76*
         BAL   11,WAITANS                                   *BW 7/76*
         WTOR  'DEBE: REPLY OWNER ID - CCCCCCCCC',          *BW 7/76*  X
               TDAREA+41,10,REPLYECB                        *BW 7/76*
         BAL   11,WAITANS                                   *BW 7/76*
         MVC   TDAREA(4),=C'VOL1'                           *BW 7/76*
         MVI   TDAREA+10,X'F0'  NO SECURITY FLAG            *BW 7/76*
         MVC   OUTCCW+8(8),CTCCW  MOVE IN WRITE CCW         *BW 7/76*
         BAL   11,IOOUT                                     *BW 7/76*
         SPACE 1                                            *BW 7/76*
*   WRITE DUMMY HEADER LABEL                                *BW 7/76*
         SPACE 1                                            *BW 7/76*
         MVC   TDAREA(3),=C'HDR'                            *BW 7/76*
         MVI   TDAREA+4,X'F0' ZERO OUT REST OF LABEL        *BW 7/76*
         MVC   TDAREA+5(75),TDAREA+4                        *BW 7/76*
         BAL   11,IOOUT       WRITE DUMMY TAPE LABEL        *BW 7/76*
         SPACE 1                                            *BW 7/76*
*   WRITE A TAPE MARK AND EXIT                              *BW 7/76*
         SPACE 1                                            *BW 7/76*
         B     CTEOJ                                        *BW 7/76*
         EJECT
*XP      EQU   *                                           **BW SIK**
*        WTOR  'DEBE: ENT NEW PUNCH ADDR - XXX',HOLD,3,REPLYECB SIK**
*        BAL   11,WAITANS                                  **BW SIK**
*        OC    HOLD(3),BLANKS                              **BW SIK**
*        CLC   HOLD(2),=C'EJ'                              **BW SIK**
*        BE    ASKAGN                                      **BW SIK**
*        MVC   PUNCH1(3),HOLD                              **BW SIK**
*        B     ASKAGN                                      **BW SIK**
*        SPACE 3                                           **BW SIK**
*XR      EQU   *                                           **BW SIK**
*        WTOR  'DEBE: ENT NEW READER ADDR - XXX',HOLD,3,REPLYECB IK**
*        BAL   11,WAITANS                                  **BW SIK**
*        OC    HOLD(3),BLANKS                              **BW SIK**
*        CLC   HOLD(2),=C'EJ'                              **BW SIK**
*        BE    ASKAGN                                      **BW SIK**
*        MVC   READER1(3),HOLD                             **BW SIK**
*        B     ASKAGN                                      **BW SIK**
*        SPACE 3                                           **BW SIK**
*XS      EQU   *                                           **BW SIK**
*        WTOR  'DEBE: ENT NEW PRINT ADDR - XXX',HOLD,3,REPLYECB SIK**
*        BAL   11,WAITANS                                  **BW SIK**
*        OC    HOLD(3),BLANKS                              **BW SIK**
*        CLC   HOLD(2),=C'EJ'                              **BW SIK**
*        BE    ASKAGN                                      **BW SIK**
*        MVC   PRINT1(3),HOLD                              **BW SIK**
*        B     ASKAGN                                      **BW SIK**
*XA      EQU   *                                           **BW SIK**
*        OI    SYSPRTSW,X'FF'                              **BW SIK**
*        B     ASKAGN                                      **BW SIK**
*XB      EQU   *                                           **BW SIK**
*        XC    SYSPRTSW(1),SYSPRTSW                        **BW SIK**
*        B     ASKAGN                                      **BW SIK**
         EJECT
*
*  SUBROUTINES USED BY THE SUBPROGRAMS CALLED BY ID
*
*  OPEN PRINT FILE
         SPACE 1
OPENPRNT EQU   *
         TM    SYSPRTSW,X'FF'
         BNO   OPSYSPR
         MVI   OUTMDSTK,X'09'     PRIME DCB
         MVI   OUTDEVT,X'48'
         MVC   D1(3),PRINT1       PRINTER ADDRESS
         BAL   14,UCBLOKUP
         ST    2,OUTDEBMD         STORE IN DEB
         B     OPENOK
OPSYSPR  EQU   *
         TM    OPENSW,X'FF'
         BC    1,OPENOK
         OPEN  (PRINT,OUTPUT)
         OI    OPENSW,X'FF'
*                                                          **BW SIK**
*    ASK FOR PRINTOUT DESTINATION                          **BW SIK**
*                                                          **BW SIK**
         ST    11,SAV11                                    **BW SIK**
         WTOR  'DEBE: ENTER MESSAGE TO IDENTIFY PRINTOUT DESTINATION - X
               UP TO 30 CHARS',HEADERX,30,REPLYECB         **BW SIK**
         BAL   11,WAITANS
         MVC   TDOUTAR(100),HEADER     MOVE HDR FOR PRINT  **BW SIK**
*                                                          **BW SIK**
         LA    7,45                    SET UP PRINT LOOP   **BW SIK**
*                                                          **BW SIK**
HDRLP    EQU   *                                           **BW SIK**
         MVI   TDA,X'40'                                   **BW SIK**
         PUT   PRINT,TDA                                   **BW SIK**
         BCT   7,HDRLP                                     **BW SIK**
         L     11,SAV11                                    **BW SIK**
OPENOK   EQU   *
         BR    11
SAVER9   DS    F                                           **BW SIK**
         SPACE 3
*  COMMON PRINT ROUTINE
         SPACE 1
TDPRINT  EQU   *
         ST    11,SV11
         TR    TDOUTAR(132),PRNTABLE   ELIMINATE UNPRINTABLE CHARS.
         TM    SYSPRTSW,X'FF'
         BO    TDPRT1
         MVI   TDA,X'09'
         PUT   PRINT,TDA
         B     LINECT
TDPRT1   EQU   *
         MVC   OUTCCW(8),PTCCW
         BAL   11,IOOUT           PRINT LINE
         CLI   OUTECB,X'7F'       PRINT OK?
         BNE   PTERROR            NO, BRANCH
         B     LINECT
         SPACE 1
*  SKIP PRINTER TO CHANNEL 1
         SPACE 1
         BR    11
SKIPTO1  EQU   *
         ST    11,SAV11           SAVE RETURN ADCON
         TM    SYSPRTSW,X'FF'
         BO    SKTO1
         MVI   TDA,X'89'
         MVI   TDOUTAR,C' '
         MVC   TDOUTAR+1(131),TDOUTAR
         PUT   PRINT,TDA
         B     SKTO1A
SKTO1    EQU   *
         MVC   OUTCCW(8),SKCCW
         BAL   11,IOOUT           SKIP TO CHANNEL 1
         CLI   OUTECB,X'7F'       SKIP OK?
         BNE   PTERROR            NO, BRANCH
SKTO1A   EQU   *
         SR    0,0                CLEAR COUNTER
         STH   0,C
         L     11,SAV11
         BR    11                 RETURN
         SPACE 1
*  SPACE PRINTER ONE LINE
         SPACE 1
SPACE1   EQU   *
         ST    11,SV11
         TM    SYSPRTSW,X'FF'
         BO    SPC1
         MVI   TDA,X'09'
         MVI   TDOUTAR,C' '
         MVC   TDOUTAR+1(131),TDOUTAR
         PUT   PRINT,TDA
         B     LINECT
SPC1     EQU   *
         MVC   OUTCCW(8),SPCCW
         BAL   11,IOOUT           SKIP LINE
         CLI   OUTECB,X'7F'       SKIP OK?
         BNE   PTERROR
LINECT   EQU   *
         LH    1,C                UPDATE LINE COUNT
         LA    0,1(1)
         STH   0,C
         L     11,SV11
         CH    0,CLIMIT           END OF PAGE
         BH    SKIPTO1
         BR    11                 EXIT ROUTINE
PTERROR  EQU   *
         WTO   'DEBE: PRINT ERROR - EOJ'
         B     ASKAGN
         SPACE 3
*  RECORD COUNTER
         SPACE 1
KNTR     EQU   *
         SR    2,2
KNTLOOP  EQU   *
         BAL   11,IOIN
         CLI   INECB,X'7F'
         BE    KNT1
         TM    INSTAT+4,X'01'
         BC    1,KNTDN
         CLC   ID(2),=C'KC'
         BE    RDRERROR
         B     TAPERROR
KNT1     EQU   *
         LA    2,1(2)
         B     KNTLOOP
KNTDN    EQU   *
         MVC   COUNT(6),=X'402020202020'
         CVD   2,TDWK
         ED    COUNT(6),TDWK+5
         LA    1,MESSAGE
         SVC   35
         B     ASKAGN
         EJECT
*  ROUTINE TO CONVERT MM INTO A SET MODE COMMAND
CONVRTMM EQU   *
         STM   2,3,SAVEUM    SAVE WORK REGISTERS
         IC    2,MM+1        GET SECOND BYTE OF MM
         TM    MM+1,X'F0'    IS IT NUMERIC
         BC    1,MMMM1       YES, BRANCH
         AH    2,=H'9'       CONVERT ALPHA TO NUMERIC
MMMM1    SRDL  2,4           SAVE IT
         IC    2,MM          GET FIRST BYTE OF MM
         TM    MM,X'F0'      IS IT NUMERIC
         BC    1,MMMM2       YES, BRANCH
MMMM2    SLDL  2,4
         STC   2,MM          PUT MM  BACK IN HEX
         LM    2,3,SAVEUM    RESTORE REGISTERS
         BR    14
SAVEUM   DS    2F
         SPACE 3
*  ROUTINE TO GET UCB ADDRESS WHEN GIVEN DEVICE ADDRESS
         SPACE 1
UCBLOKUP EQU   *
         ST    11,SAV11
UCBLOK   EQU   *
         L     R2,16
         L     R5,40(R2)
UCBCHK   LH    R2,0(R5)
         LTR   R2,R2
         BZ    INCUCB
         CLC   0(2,R5),=XL2'FFFF'    LAST UCB IN SYSTEM
         BNE   CKADDR                  NO LOOK AT IT TO SEE IF GOOD
         WTO   'UCB NOT FOUND'
         B     ASKAGN
CKADDR   CLC   13(3,R2),D1
         BE    UCBFOUND
INCUCB   LA    R5,2(R5)
         B     UCBCHK
UCBFOUND TM    2(R2),X'FF'
         BNO   BADEVICE
*        TM    3(2),X'08'         ALLOCATED?               **BW SIK**
*        BC    1,ALOCK            YES; BRANCH              **BW SIK**
*        TM    3(2),X'80'      TEST FOR OFFLINE DEVICE     **BW SIK**
         SPACE 5                                           **BW SIK**
********************************************************** **BW SIK**
*                                                          **BW SIK**
*     THE OFFLINE DEVICE FUNCTION HAS BEEN ARTIFICIALLY    **BW SIK**
*     DISABLED -- TAPE DEVICES MUST NOW BE INDICATED BY    **BW SIK**
*     A DD CARD IN THE DEBE JCL - THE DD NAME IS AT THE    **BW SIK**
*     DISCRETION OF THE USER                               **BW SIK**
*                                                          **BW SIK**
*     THIS ROUTINE CHECKS TO SEE THAT THE UCB REQUESTED    **BW SIK**
*     IN ANY TAPE OPERATION HAS BEEN ALLOCATED AT STEP     **BW SIK**
*     INITIATION BY CHECKING FOR THE UCB ADDRESS IN THE    **BW SIK**
*     TIOT CHAIN FOR THE DEBE TASK -- IF THE UCB ADDRESS   **BW SIK**
*     IS NOT LOCATED THEN THE OPERATION IN PROGRESS IS     **BW SIK**
*     TERMINATED WITH THE ERROR MESSAGE --                 **BW SIK**
*                                                          **BW SIK**
*     'DEBE: REQUESTED DEVICE NOT ALLOCATED, OPERATION     **BW SIK**
*        TERMINATED'                                       **BW SIK**
*                                                          **BW SIK**
*     THE OPERATOR IS THEN PROMPTED FOR THE NEXT OPERATION **BW SIK**
*     TO BE PERFORMED                                      **BW SIK**
*                                                          **BW SIK**
********************************************************** **BW SIK**
*                                                          **BW SIK**
         STM   8,10,MYSAVE             SAVE REGS           **BW SIK**
         L     8,16                    R8 --> CVT          **BW SIK**
         L     9,0(0,8)                R9 --> TCB PTRS     **BW SIK**
         L     10,4(0,9)               R10 --> CURR TCB(ME)**BW SIK**
         L     8,12(0,10)              R8 --> TIOT         **BW SIK**
         LA    8,24(,8)                R8 --> 1 ST ENTRY   **BW SIK**
NEXTUCB  CLI   0(8),X'00'              LAST ENTRY ??       **BW SIK**
         BE    NOTALLOC                YES - DEV NOT ALLOC **BW SIK**
         CH    2,18(0,8)               IS UCB IN TIOT ENTRY *BW SIK**
         BE    DEVOK                   YES - DEVICE OK     **BW SIK**
         LA    8,20(,8)                NO - GET NEXT ENTRY **BW SIK**
         B     NEXTUCB                 NEXT UCB            **BW SIK**
*                                                          **BW SIK**
DEVOK    EQU   *                                           **BW SIK**
         LM    8,10,MYSAVE             RESTORE REGS        **BW SIK**
         BR    14                                          **BW SIK**
*                                                          **BW SIK**
NOTALLOC EQU   *                                           **BW SIK**
         LM    8,10,MYSAVE             RESTORE REGS        **BW SIK**
         WTO   'DEBE: REQUESTED DEVICE NOT ALLOCATED - OPERATION TERMINX
               ATED'                                       **BW SIK**
         B     ASKAGN                  GET NEXT FUNCTION   **BW SIK**
*                                                          **BW SIK**
MYSAVE   DS    3F                                          **BW SIK**
*OFLNTEST BNO  DEVOFLIN     BRANCH WILL BE TURNED OFF BY 'OX' COMMAND
*RETRN    EQU   *                                            DEBE9590
*        L     11,SAV11
*        BR    14
*        CMSREG                                       AXC
R0      EQU  0                                        AXC
R1      EQU  1                                        AXC
R2      EQU  2                                        AXC
R3      EQU  3                                        AXC
R4      EQU  4                                        AXC
R5      EQU  5                                        AXC
R6      EQU  6                                        AXC
R7      EQU  7                                        AXC
R8      EQU  8                                        AXC
RB      EQU  11                                       AXC
RC      EQU  12                                       AXC
RD      EQU  13                                       AXC
RE      EQU  14                                       AXC
RF      EQU  15                                       AXC
         LTORG
         SPACE 3
*  ROUTINE TO ASK FOR THE INPUT TAPE
         SPACE 1
TAPINSET DS    0H
         WTOR  'DEBE: INPUT TAPE - MMXXX',MM,5,REPLYECB
         BAL   11,WAITAPE    WAIT FOR A REPLY AND SET UP MODE
         MVC   INTRTCH(1),MM       SET UP TAPE'S DCB
         MVI   INDEVT,X'81'
         CLI   18(2),X'80'       IS IT A TAPE (2 POINTS TO UCB)
         BNE   TPINBD
         ST    2,INDEBMOD          STORE UCB ADDRESS IN DEB
         MVC   INDEBMOD(1),MM      PUT SET MODE CMD IN DEB
         OI    INIOB,X'40'         SET ON CHAINING
         MVC   INCCW(1),MM       PUT SET MODE IN CCW
         MVC   INCCW+1(7),=X'00000060000001'
         BR    10
TPINBD   EQU   *
         WTO   'DEBE: NOT A TAPE'
         B     TAPINSET
         SPACE 3
*  ROUTINE TO ASK FOR THE OUTPUT TAPE
         SPACE 1
TPOUTSET EQU   *
         WTOR  'DEBE: OUTPUT TAPE - MMXXX',MM,5,REPLYECB
         BAL   11,WAITAPE    WAIT FOR REPLY  AND SET UP MM
         MVC   OUTTRTCH(1),MM
         MVI   OUTDEVT,X'81'
         CLI   18(2),X'80'       IS IT A TAPE
         BNE   TPOUTBD
         ST    2,OUTDEBMD          STORE UCB ADDRESS IN DEB
         MVC   OUTDEBMD(1),MM
         OI    OUTIOB,X'40'      SET ON CHAINING FLAG
         MVC   OUTCCW(1),MM      BUILD SET MODE COMMAND
         MVC   OUTCCW+1(7),=X'00000060000001'
         BR    10
TPOUTBD  EQU   *
         WTO   'DEBE: NOT A TAPE'
         B     TPOUTSET
TAPEBLOK EQU   *
         XC    NBRSW(1),NBRSW
         WTOR  'DEBE: NR BLK TO PROCESS OR ALL-XXX',TDAREA,3,REPLYECB
         BAL   11,WAITANS
         OC    TDAREA(3),BLANKS
         CLC   TDAREA(2),=C'EJ'
         BE    ASKAGN
         CLC   TDAREA(3),=C'ALL'
         BE    DOALL
         OI    TDAREA+2,X'F0'
         PACK  TDWK,TDAREA(3)
         CVB   6,TDWK
         BR    10
DOALL    EQU   *
         OI    NBRSW,X'FF'
         BR    10
         SPACE 3
*  COMMON I/O ROUTINE FOR OUTPUT
         SPACE 1
IOOUT    EQU   *
         XC    OUTECB(4),OUTECB
         EXCP  OUTIOB
         WAIT  ECB=OUTECB
         NI    OUTDCB+44,X'3F'
         BR    11
         SPACE 3
*  COMMON I/O ROUTINE FOR INPUT
         SPACE 1
IOIN     EQU   *
         XC    INECB(4),INECB
         EXCP  INIOB
         WAIT  ECB=INECB
         NI    INDCB+44,X'3F'
         BR    11
         SPACE 3
*  COMMON WAIT FOR ALL REPLIES
WAITANS  EQU   *
         WAITR ECB=REPLYECB
         XC    REPLYECB(4),REPLYECB
         BR    11
         SPACE 3
*  ROUTINE TO WAIT ON THE REPLY AND CONVERT THE MM INTO A SET MODE
WAITAPE  EQU   *
         WAIT  ECB=REPLYECB
         XC    REPLYECB(4),REPLYECB
         OC    MM(5),=C'     '     MAKE ALL CAPS
         CLC   MM(2),=C'EJ'
         BE    ASKAGN
         MVC   TDOUTAR(5),MM  GET MMXXX FOR PRINTING ON LISTING
         MVI   TDOUTAR+5,C' '
         MVC   TDOUTAR+6(126),TDOUTAR+5
         MVC   TDOUTAR+5(24),=CL24' - MMXXX FOR INPUT TAPE.'
         MVC   TDOUTAR+50(47),=CL47'*** THIS IS NOT A DATA RECORD FROM X
               THE TAPE ***'
         BAL   14,CONVRTMM
         OI    MM,X'03'            SET MODE COMMAND NOW IN MM
         MVC   D1(3),XXX
         BAL   14,UCBLOKUP
         BR    11
         SPACE 3
*  ROUTINE TO WRITE A TAPEMARK
WTM      EQU   *
         ST    11,SV11
         MVC   OUTCCW+8(8),WTCCW       SET UP WTM CCW
         BAL   11,IOOUT      WRITE A TAPE MARK
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BNE   TAPERR        NO, BRANCH
         L     11,SV11
         BR    11
         SPACE 3
*  UNIT ALLOCATED ROUTINE
ALOCK    EQU   *
         LA    1,WTORMSG
NEWADRS  EQU   *     ASK FOR A NEW DEVICE ADDRESS
         SVC   35               ISSUE WTOR
         BAL   11,WAITANS
         OC    HOLD(3),BLANKS
         CLC   HOLD(2),=C'EJ'     NEW TASK?
         BE    ASKAGN
         MVC   D1(3),HOLD
         B     UCBLOK     AVOID RE-SAVING REG11
         SPACE  3
*  NO UCB FOUND
BADEVICE EQU   *
         LA    1,BDWTOR
         SVC   35                 ISSUE WTOR
         BAL   11,WAITANS
         OC    D1(3),BLANKS
         CLC   D1(2),=C'EJ'
         BE    ASKAGN
         B     UCBLOK             TRY AGAIN
         SPACE 3
DEVOFLIN EQU   *     DEVICE ADDRESS GIVEN IS OFFLINE
         MVC   OFFDEV(3),UNNUM
         LA    1,ALLOCMSG
         B     NEWADRS
         SPACE 1
APPEND   BR    14
         EJECT
*  CONSTANTS AND CCW'S      * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
HOLD     DS    D
READER1  DC    CL3'012'
PUNCH1   DC    CL3'013'
PRINT1   DC    CL3'002'
         SPACE 3
BFCCW    CCW   X'2F',SFCCW,X'20',1
BSCCW    CCW   X'27',BSCCW,X'20',1
CP1CCW   CCW   2,TDAREA,X'20',80
CP2CCW   CCW   9,TDAREA,X'20',80
CTCCW    CCW   1,TDAREA,X'20',80
DBCCW    CCW   X'41',TDAREA2,X'20',80
PTCCW    CCW   9,TDOUTAR,X'20',132
ROCCW    CCW   X'41',BLANKS,X'20',80
RUCCW    CCW   15,RUCCW,X'20',1
RWCCW    CCW   7,RWCCW,X'20',1
SFCCW    CCW   X'3F',SFCCW,X'20',1
SKCCW    CCW   X'8B',BLANKS,X'20',1
SPCCW    CCW   11,BLANKS,X'20',1
SRCCW    CCW   X'37',BSCCW,X'20',1
S2CCW    CCW   X'41',TDAREA,X'20',80
TDINCCW  CCW   2,TDAREA,X'20',32767
TTCCWOUT CCW   1,TDAREA,X'20',32767
WTCCW    CCW   31,TDAREA,X'20',1
TAPEAREA DC    F'0'      ADDRESS OF TAPE I/O WORK AREA IN SP 0
PRNTABLE DC    CL75' ',C'. (+ &&',CL10' ',C'$*)  -/',CL9' '
         DC    C',%',CL14' ',C'#@''= ',CL65' '
         DC    C'ABCDEFGHI',CL7' ',C'JKLMNOPQR',CL8' '
         DC    C'STUVWXYZ',CL6' ',C'0123456789',CL6' '
         SPACE 3
BLANKS   DC    132C' '
         SPACE 1
TDWK     DS    D
SAVEAREA DC    18F'0'
TDPTABLE DC    C'0123456789ABCDEF'
         DS    0F
MESSAGE  DC    AL2(THERE-*)     MESSAGE LENGTH
         DC    AL2(0)
         DC    C'DEBE: '
COUNT    DC    C'NUMBER '
         DC    C'RECORDS PROCESSED'
THERE    EQU   *
WTORMSG  DS    0F
         DC    AL1(3)
         DC    AL3(HOLD)
         DC    A(REPLYECB)
         DC    AL2(28)
         DC    AL2(0)
         DC    C'DEBE: UNIT '
UNNUM    DS    CL3
         DC    C' ALLOCATED'
BDWTOR   DS    0F
         DC    AL1(3)
         DC    AL3(D1)
         DC    A(REPLYECB)
         DC    AL2(26)
         DC    AL2(0)
         DC    C'DEBE: UNIT '
BDUN     DC    C'XXX INVALID'
ALLOCMSG DS    0F
         DC    AL1(3),AL3(HOLD),A(REPLYECB),AL2(29),AL2(0)
         DC    C'DEBE: UNIT '
OFFDEV   DC    C'XXX IS OFFLINE'
D1       DC    C'0'
D2       DC    C'0'
D3       DC    C'0'
ID       DC    C'00'
MM       DC    C'00'
XXX      DC    C'000'
OF       DC    F'15'
REPLYECB DC    F'0'
HEADER   DC    35C'*'                                      **BW SIK**
HEADERX  DC    30C' '                                      **BW SIK**
         DC    35C'*'                                      **BW SIK**
SAV11    DS    F
TDA      DC    C' '
TDOUTAR  DS    CL132
RECCNT   DC    X'00000F'
COND1    DC    X'1F'
RECHDG   DC    C'REC '
         DC    X'2020202020'
         DC    C', LENGTH '
         DC    X'2020202020'
C        DC    H'0'
CLIMIT   DC    H'58'
SV11     DS    F
         SPACE 3
SYSPRTSW DC    X'00'
NBRSW    DC    X'00'
OPENSW   DC    X'00'
PRINT    DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FBM,LRECL=133,  X
               BLKSIZE=133
         LTORG
         SPACE 3
TDAREA2  DS    CL101
TDAREAAD DS    0F            SET TDAREA ON FULLWD BOUNDARY
TDAREA   DS    CL80
WORKAREA CSECT
*  SEE THE OS SYSTEM CONTROL BLOCKS MANUAL FOR THE FORMATS OF THE
*  FOLLOWING DCB'S, DEB'S, ECB'S, AND IOB'S.
*  DCB FOR INPUT DATA SET
         SPACE 1                       
INDCB    DS    0F
         DC    4F'0'
INTRTCH  EQU   *
INMODSTK EQU   *
         DC    X'00'
INDEVT   DC    X'00'
INDENS   DC    X'00'
         DC    X'00'
         DC    5F'0'
         DC    H'0'
         DC    BL2'1101000000001000'
AET1     DC    A(INDEB)
         DC    X'10000000'
         DC    5F'0'
         SPACE 1                       
*  DEB FOR INPUT DATA SET
         SPACE 1                       
INIOVEC  EQU   *
AET2     DC    A(APPEND)
AET3     DC    A(APPEND)
AET4     DC    A(APPEND)
AET5     DC    A(APPEND)
AET6     DC    A(APPEND)
         DC    3F'0'
         DC    X'06000000'
INDEB    DS    0F
         DC    F'0'
         DC    X'04'
AET7     DC    AL3(OUTDEB)
         DC    X'C0000000'
         DC    X'30000000'
         DC    2F'0'
         DC    X'0F'
AET8     DC    AL3(INDCB)
         DC    X'02'
AET9     DC    AL3(INIOVEC)
INDEBMOD DC    X'00'
INDEBUCB DC    X'000000'
         DC    F'0'
         SPACE 1                       9
*  ECB FOR INPUT DATA SET
         SPACE 1                       9
INECB    DC    F'0'
         SPACE 1
*  IOB FOR INPUT DATA SET
         SPACE 1
INIOB    DS    0F
         DC    X'0200'
INSENS   DC    H'0'
         DC    X'7F'
AET10    DC    AL3(INECB)
INSTAT   DC    2F'0'
AET11    DC    A(INCCW)
AET12    DC    A(INDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
INDASD   DC    X'00'
INSEEK   DC    XL7'00'
         SPACE 1
*  CCW'S FOR INPUT DATA SET
         SPACE 1
INCCW    DS    5D
         SPACE 3
*  DCB FOR OUTPUT DATA SET
         SPACE 1
OUTDCB   DS    0F
         DC    4F'0'
OUTTRTCH EQU   *
OUTMDSTK EQU   *
         DC    X'00'
OUTDEVT  DC    X'00'
OUTDENS  DC    X'00'
         DC    X'00'
         DC    5F'0'
         DC    H'0'
         DC    BL2'1101000000001000'
AET13    DC    A(OUTDEB)
         DC    X'10000000'
         DC    5F'0'
         SPACE 1
*  DEB FOR OUTPUT DATA SET
         SPACE 1
OUTIOVEC EQU   *
AET14    DC    A(APPEND)
AET15    DC    A(APPEND)
AET16    DC    A(APPEND)
AET17    DC    A(APPEND)
AET18    DC    A(APPEND)
         DC    3F'0'
         DC    X'06000000'
OUTDEB   DS    0F
         DC    F'0'
         DC    X'17000000'
         DC    X'C0000000'
         DC    X'30000000'
         DC    2F'0'
         DC    X'0F'
AET19    DC    AL3(OUTDCB)
         DC    X'02'
AET20    DC    AL3(OUTIOVEC)
OUTDEBMD DC    X'00'
OUTDBUCB DC    X'000000'
         DC    F'0'
         SPACE 1
*  ECB FOR OUTPUT DATA SET
         SPACE 1
OUTECB   DC    F'0'
         SPACE 1
*  IOB FOR OUTPUT DATA SET
         SPACE 1
OUTIOB   DS    0F
         DC    XL2'0200'
OUTSENS  DC    H'0'
         DC    X'7F'
AET21    DC    AL3(OUTECB)
OUTSTAT  DC    2F'0'
AET22    DC    A(OUTCCW)
AET23    DC    A(OUTDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
OUTDASD  DC    X'00'
OUTSEEK  DC    XL7'00'
         SPACE 1
*  CCW'S FOR OUTPUT DATA SET
         SPACE 1
OUTCCW   DS    5D
         SPACE 3
         END   OSDEBE
