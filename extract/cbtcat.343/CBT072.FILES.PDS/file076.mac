*          DATA SET CBT394     AT LEVEL 005 AS OF 05/14/76
CBT394   CSECT
         SAVE  (14,12)
         LR    R3,R15              BASE REG
         USING CBT394,R3
         ST    R13,SAVE+4          SAVE AREA
         LA    R13,SAVE               CONVENTIONS
         L     R1,0(0,R1)          PARM ADDR
         MVZ   TIME+4(2),2(R1)     CHECK PARM FOR NUMERIC
         CLC   TIME+4(2),=C'00'    PARM NUMERIC
         BNE   ABEND               NO, GO ABEND
         MVC   TIME+4(2),2(R1)     SET TIME INTERVAL
         MVC   ABND(1),4(R1)       SET ABEND IND
         L     R4,16               CVT ADDR
         L     R4,560(0,R4)        GDA ADDR
         L     R5,24(0,R4)         SQA SPQE
         L     R6,4(0,R5)          SQA DQE CHAIN START
         SPACE 3
DQESTART L     R12,12(0,R6)        LOAD FIRST DQE SIZE
         L     R7,4(0,R6)          NEXT DQE
         SPACE
DQELOOP  LTR   R7,R7               ANY MORE DQES
         BZ    PWAIT               NO, GO PRINT AND WAIT
         A     R12,12(0,R7)        ADD TO SQA SIZE
         L     R7,4(0,R7)          NEXT DQE ADDR
         B     DQELOOP             GO CHECK NEXT
         SPACE 3
PWAIT    TM    WTORECB,X'40'       FORCE DISPLAY
         BO    DISPLAY             GO DISPLAY
         C     R12,SQASIZE         SIZE CHANGE YET
         BE    WAIT                NO, NO MSG
         SPACE 3
DISPLAY  ST    R12,SQASIZE         SET NEW SQA SIZE
         SRL   R12,10              DIVIDE BY 1024
         CVD   R12,DOUBLE          CONVERT SIZE TO DECIMAL
         MVC   SQAWTO+37(6),=X'402021202020'
         ED    SQAWTO+37(6),DOUBLE+5
         EJECT
SQAWTO   WTO   'CBT394 -   CURRENT SQA SIZE - XXXXXK',DESC=2
         SPACE 3
         TM    WTORECB,X'40'       POSTED
         BZ    ABNDCHK             NO WTOR
         MVI   WTORECB,X'00'
         CLC   REPLY(4),=C'STOP'
         BE    EOJ
         WTOR  'CBT394 -   ENTER  ''STOP''  TO TERMINATE,  ''SIZE''  TOX
                DISPLAY SQA SIZE',REPLY,4,WTORECB
         SPACE 3
WAIT     STIMER WAIT,DINTVL=TIME     WAIT 10 SECS
         B     DQESTART            CHECK SQA SIZE AGAIN
         EJECT
ABEND    ABEND 77
         SPACE 3
ABNDCHK  CLI   ABND,C'S'           SLIP TRAP ABEND REQUESTED
         BNE   WAIT                NO, CONTINUE
         L     R1,=X'C0073000'     TAKE ABEND
         SVC   13
         SPACE 3
EOJ      L     R13,SAVE+4
         RETURN (14,12),RC=0
         EJECT
         LTORG
SAVE     DS    18F
         DS    0D
DOUBLE   DC    2F'0'
TIME     DC    C'00000010'          .1 SECONDS
SQASIZE  DC    F'0'
WTORECB  DC    X'40000000'
REPLY    DC    CL4' '
ABND     DC    C' '
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
