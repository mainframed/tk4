*          DATA SET ANBSUBFIND AT LEVEL 003 AS OF 30/06/76
         TITLE 'FNDVAL,FNDTXT,FNDPRS - OBTAIN KEYWORDS FROM A BUFFER'
FNDVAL   CSECT
         BC    15,12(15)     BRANCH AROUND IDENTIFIER
         DC    X'06'
         DC    CL7'FNDVAL'
         MVI   FLAG-FNDVAL(15),X'00'   NUMERIC FIELD
         B     MENTRY-FNDVAL(15)
*
*         THIS WILL RETURN THE INTEGER VALUE OF ENTRIES IN THE
*      PARMLIST OF THE FORM ....,KEYWORD=VALUE,.... N AND KEY ARE
*      SET BY THE USER. KEY WILL CONTAIN THE KEYWORD ( INCLUDING
*      THE '=' SIGN ) AND N WILL GIVE THE NUMBER OF CHARACTERS
*      IN KEY.
*         THE ROUTINE WILL RETURN IN M THE NUMBER OF DIGITS IN
*      THE VALUE ( NOT INCLUDING THE SIGN ) AND IF M > 0 THE
*      BINARY VALUE IN VALUE.
*
         ENTRY FNDTXT
FNDTXT   EQU   *
         BC    15,12(15)
         DC    X'06',CL7'FNDTXT'
         MVI   FLAG-FNDTXT(15),X'01'   TEXT
         B     MENTRY-FNDTXT(15)
*
*         THIS WILL RETURN THE TEXT VALUE OF ENTRIES
*      OF THE FORM ....,KEYWORD=TEXT,....
         ENTRY FNDPRS
FNDPRS   BC    15,12(15)
         DC    X'06'
         DC    CL7'FNDPRS'
         MVI   FLAG-FNDPRS(15),X'02'   KEYWORD
*
*         THIS WILL INDICATE THE PRESENCE OF AN ENTRY OF THE FORM
*      ....,KEYWORD,.... N AND KEY ARE AS ZA06AS EXCEPT THAT
*      IN GENERAL KEY WILL CONTAIN NO '=' CHARACTER. IF THE
*      KEYWORD IN KEY IS FOUND IN THE PARMLIST THE 3RD ARGUMENT
*      L IS SET TO 1 (.TRUE.) AND IF IT IS NOT FOUND IT IS SET TO
*      ZERO (.FALSE.).
*
MENTRY   STM   14,7,12(13)   SAVE REGISTERS
         LM    3,6,8(1)                PICK UP ARGUMENTS
         BALR  7,0           ESTABILISH .....
         USING *,7           ..... BASE REGISTER
         SR    0,0           PICK UP LENGTH ......
         IC    0,3(3)        .... OF KEYWORD
         LTR   3,0           TEST FOR ......
         BNZ    BR8          .... ZERO LENGTH
         LR    2,3           EXIT .....
         B     BR5           .... IF ZERO
*
*      INITIALIZE AND SET UP FOR SEARCH.
*
BR8      EQU   *
         LM    1,2,0(1)                LOAD PARAM ADDRESSES
         L     0,0(1)                  LOAD BUFFER LENGTH
         LA    1,0(2)                  LOAD BUFFER ADDRESS
*
**  LOOK FOR LAST NON-BLANK CHARACTER IN BUFFER
*
         LR    2,1                     LOAD ADDRESS OF BUFFER
         AR    2,0                     ADD BUFFER LENGTH
         BCTR  2,0                     REDUCE BY ONE
NEXT     EQU   *
         CLI   0(2),C' '               BLANK?
         BNE   LSTBLNK                 NO,STOP
         BCTR  2,0                     REDUCE ADDRESS BY ONE
         BCT   0,NEXT                  REDUCE BUFFER LENGTH
LSTBLNK  EQU   *
         SR    2,2
         MVI   FLAG2,X'00'   PUT FOUND FLAG OFF
         BCTR  3,0           MODIFY .......
         STC   3,KEYTEST+1   .... COMPARE INSTRUCTION .....
         STC   3,INCR+3      ... AND INCREMENT INSTRUCTION
         LA    3,1(3)        RESTORE TO PROPER VALUE
         AR    0,1           POINT AT END OF PARMLIST
         BCTR  1,0           REDUCE TO ENTER LOOP
         B     KEYTEST       GO AND TEST FOR KEYWORD
*
*      KEYWORD SEARCH
*
COMMA    CLI   FLAG2,X'00'   HAS KEYWORD BEEN FOUND ?
         BNE   ENDSRCH       YES: GO AND FINISH OFF
KEYTEST  CLC   0(0,4),1(1)   IS IT THE KEYWORD ?
         BNE   NXTCOMA       NO: GO AND LOOK FOR NEXT COMMA
         LA    1,1(1)        YES: POINT AT KEYWORD
         ST    1,KEYWORD     SAVE POINTER TO KEYWORD
         MVI   FLAG2,X'FF'   MARK AS FOUND
INCR     LA    1,0(1)        MOVE POINTER TO END OF KEYWORD
         B     NXTCOMA       GO AND LOOK FOR NEXT COMMA
COMATEST CLI   0(1),C','     IS IT A COMMA ?
         BE    COMMA         YES: BRANCH THEN
NXTCOMA  LA    1,1(1)        UPDATE PARMLIST POINTER
         CR    1,0           LOOP TILL .....
         BL    COMATEST      ..... END OF PARMLIST
         CLI   FLAG2,X'00'   HAS KEYWORD BEEN FOUND ?
         BE    BR5           NO: FINISH THEN
*
*      END OF SEARCH
*
ENDSRCH  S     1,KEYWORD     LENGTH OF KEYWORD FIELD
         SR    1,3           LENGTH OF KEYWORD VALUE
         CLI   FLAG,X'02'    DID WE COME .....
         BNE   BR1           .... IN AT ZA06CS ?
         LTR   1,1           YES: ARE THE LENGTHS .....
         BZ    BR7           .... THE SAME ?
         MVI   FLAG2,X'00'   NO: SET NOT FOUND
         L     1,KEYWORD     PICK UP POSITION IN PARMLIST ....
         B     INCR          ... AND GO AND HAVE ANOTHER TRY
BR7      LA    2,1           SET THIRD ARGUMENT TRUE
BR5      ST    2,0(5)        SET THIRD ARGUMENT
         B     FINISH
BR1      LTR   2,1
         BZ    BR5
         L     14,KEYWORD    POINT AT ....
         LA    14,0(14,3)    .... VALUE FIELD
         CLI   FLAG,X'00'    DID WE COME IN .....
         BNE   BR6           .... AT ZA06AS ?
         BCTR  1,0           YES: ONE OFF FOR PACK
         CLI   0(14),C'-'    IS THE NUMBER .....
        BNE    BR2           .... NEGATIVE
         LA    14,1(14)      YES: MOVE POINTER UP ONE
         BCTR  1,0           REDUCE LENGTH AGAIN
         EX    1,PACK        PACK AND CONVERT ......
         CVB   4,BUF         .... VALUE TO BINARY
         LNR   4,4           MAKE VALUE NEGATIVE
         B     BR3
BR2      EX    1,PACK        PACK AND CONVERT .....
         CVB   4,BUF         .... VALUE TO BINARY
BR3      ST    4,0(6)        SET FOURTH ARGUMENT
         LA    2,1(1)        NO. OF DIGITS
         B     BR5           GO AND FINISH OFF
BR6      LR    2,1           LENGTH OF TEXT
         BCTR  1,0           ONE OFF FOR MOVE
         EX    1,MOVE
         ST    2,0(5)        SET THIRD ARGUMENT
*
*      RETURN TO CALLER
*
FINISH   LM    14,7,12(13)   RESTORE REGISTERS
         MVI   12(13),X'FF'  SET TRACE RETURN
         BR    14
*
*      WORK AREA.
*
BUF      DC    D'0'          CVB WORKSPACE
KEYWORD  DC    A(0)          ADDRESS OF KEY WORD IN PARMLIST
PACK     PACK  BUF(8),0(0,14)
MOVE     MVC   0(0,6),0(14)
FLAG     DC    X'00'         ENTRY INDICATOR
FLAG2    DC    X'00'         KEYWORD FOUND INDICATOR
HZERO    DC    H'0'
         END
*          DATA SET ANBCOMMON  AT LEVEL 003 AS OF 30/06/76
COMMON   CSECT
NOKEYS   DC    F'8'
PTRS     DC    A(NOS)
         DC    A(ADDR)
         DC    A(TITLES)
NOS      DC    H'0'
         DC    H'0'
         DC    H'4'
         DC    H'4'
         DC    H'8'
         DC    H'8'
         DC    H'12'
         DC    H'12'
ADDR     DC    V(DISPS)
         DC    V(DISPR)
         DC    V(DISPQ)
         DC    V(DISPH)
TITLES   DC    CL8'JOBS'
         DC    CL8'J'
         DC    CL8'SYS'
         DC    CL8'S'
         DC    CL8'ENQ'
         DC    CL8'Q'
         DC    CL8'H'
         DC    CL8'HUW'
         END
*          DATA SET ANBDISPJ   AT LEVEL 003 AS OF 30/06/76
DISPS    CSECT
         B     ENTRYS-DISPS(15)
         DC    X'07',CL7'DISPS'
ENTRYS   EQU   *
*
**  SNAPSHOT DISPLAY ROUTINE
**  INPUT PARAMETERS AFFECTING THIS ROUTINE ARE
**   ASID/A=   START ASID FOR DISPLAY
**   RESET/R   RESET DISPLAY TO START AT ASID 1
**  LINKAGE TO ROUTINE IS
**    CALL DISPS(SCREEN,FLAG LEN BUFFER)
**  FLAG IS SET TO X'FF' IF THERE IS ANY DATA
**  IN THE BUFFER.
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW15     EQU   15
*
         STM   14,12,12(13)        SAVE REGISTERS
         BALR  RB2,0
         USING *,RB2
         ST    13,SAVE+4
         LR    RW3,13
         LA    13,SAVE
         ST    13,8(RW3)           CHAIN SAVE AREAS
*
**  LOOK FOR PARAMETERS
*
         L     RW4,0(1)            SCREEN BUFFER ADDRESS
         L     RW3,4(1)         ADDRESS OF FLAG
         TM    0(RW3),X'FF'          DATA?
         BZ    NODATA              NO,DON'T SEARCH
         MVC   FNDLST(8),8(1)      MOVE IN PARAM ADDRESSES
*
**  LOOK FOR A=
*
         LA    RW3,KEY1
         ST    RW3,KEYAD         STORE KEY ADDRESS
         LA    RW3,2             KEY LENGTH
         ST    RW3,KEYLEN
         LA    RW1,FNDLST        POINT AT PARAM LIST
         L     RW15,=V(FNDVAL)
         BALR  14,15
*
**  LOOK FOD ASID=
*
         LA    RW3,KEY2
         ST    RW3,KEYAD         STORE KEY ADDRESS
         LA    RW3,5             KEY LENGTH
         ST    RW3,KEYLEN
         BALR  14,15
         L     RW3,VALUE           PICK UP RESULT...
         N     RW3,=F'255'         TAKE MODULUS
         ST    RW3,VALUE           STORE NEW VALUE
         LTR   RW3,RW3             ...TEST
         BP    OKVAL               BRANCH IF OK
         LA    RW3,1               SET UP DEFAULT...
         ST    RW3,VALUE           ...AND STORE
OKVAL    EQU   *
*
**  LOOK FOR RESET
*
         LA    RW3,KEY3
         ST    RW3,KEYAD           STORE KEY ADDRESS
         L     RW15,=V(FNDPRS)
         BALR  14,15
         L     RW3,THERE      PICK UP FLAG
         LTR   RW3,RW3        RESET PRESENT?
         BZ    NOTRES         BRANCH NO
         LA    RW3,1          RESET ASID
         ST    RW3,VALUE      ...AND STORE
NOTRES   EQU   *
*
**  LOOK FOR R
*
         LA    RW3,KEY4
         ST    RW3,KEYAD            STORE KEY ADDRESS
         LA    RW3,1                KEY LENGTH
         ST    RW3,KEYLEN
         BALR  14,15
         L     RW3,THERE          PICK UP FLAG...
         LTR   RW3,RW3            ...AND TEST IT
         BZ    NODATA             BRANCH IF NOT PRESENT
         LA    RW3,1              LOAD DEFAULT VALUE...
         ST    RW3,VALUE          ...AND STORE
NODATA   EQU   *
         LM    RW5,RW7,TITLE
         MVCL  RW4,RW6            MOVE IN HEADINGS
*
**  SET UP INTERVAL AND TIME
*
         TIME  BIN
         LR    RW1,RW0            SAVE TIME
         S     RW0,LAST           GET INTERVAL
         LTR   RW0,RW0            TEST...
         BP    VALID              ...AND BRANCH IF POSITIVE
         LA    RW0,1              LOAD DEFAULT
VALID    EQU   *
         ST    RW0,INTVAL         STORE INTERVAL
         ST    RW1,LAST           ...AND LAST TIME
*
**  INSERT TOD INTO SCREEN
*
         TIME DEC
         ST    RW0,TOD               STORE TOD
         MVC   PACK(4),TOD           INSERT SIGN
         S     RW4,=F'160'          POINT AT START OF SCREEN
         ED    0(12,RW4),PACK       ...AND EDIT
         MVI   0(RW4),X'40'          BLANK OUT FILL CHAR
         A     RW4,=F'160'          RESET POINTER
*
**  RW4 POINTS TO SCREEN BUFFER
**  RW3 WILL POINT TO ASCB Q IN ASVT
**  RW5 WILL POINT TO CURRENT ASCB
**  RW6 WILL POINT TO CURRENT OUCB
*
         L     RW3,16           CVT POINTER
         L     RW3,556(RW3)     ASVT POINTER
         LA    RW3,524(RW3)     ASVT Q
         L     RW5,VALUE        PICK UP START ASID...
         SLL   RW5,2            ...AND SHIFT TO INDEX
         AR    RW3,RW5          INTO ASVTQ
         LA    RW5,19           MAX LIMIT FOR SCREEN...
*
NXTONE   EQU   *
*
         ST    RW5,COUNTER      ...INTO STORE
         L     RW5,0(RW3)       ASCB ADDRESS
         LA    RW5,0(RW5)       REMOVE TOP BYTE
         LTR   RW5,RW5          TEST FOR END OF Q
         BZ    RETURN           TES,RETURN
         TM    0(RW3),X'80'     IN USE?
         BZ    INCPTR           YES,BRANCH
NOASCB   EQU   *
         LA    RW3,4(RW3)       INCREMENT POINTER IN ASVT
         L     RW5,COUNTER      PICK UP COUNTER
         B     NXTONE           TRY AGAIN
INCPTR   EQU   *
         CLC   0(4,RW5),=C'ASCB'         ASCB?
         BNE   NOASCB
         L     RW6,144(RW5)              OUCB ADDRESS
         CLC   0(4,RW6),=C'OUCB'         OUCB?
         BNE   NOASCB
*
**  SET DEFAULT TYPE TO BATCH (B)
*
         MVI   13(RW4),C'B'
*
**  GET JOBNAME
*
         L     RW7,172(RW5)           BATCH JOBNAME POINTER
         LTR   RW7,RW7                VALID?
         BNZ   BATCH                  YES,BRANCH
         MVI   13(RW4),C'S'           SET TYPE TO START (S)
         L     RW7,176(RW5)           START/MOUNT/LOGON POINTER
         LTR   RW7,RW7                VALID?
         BZ    NONAME                 NO,IGNORE
BATCH    EQU   *
         MVC   0(8,RW4),0(RW7)        MOVE IN JOBNAME
NONAME   EQU   *
*
**  TEST FOR MOUNT/LOGON
*
         TM    18(RW6),X'20'          LOGON?
         BZ    NOTLOG                 NO,BRANCH
         MVI   13(RW4),C'L'           SET TYPE TO LOGON (L)
         B     ENDTYPE
NOTLOG   EQU   *
         TM    18(RW6),X'10'          MOUNT?
         BZ    ENDTYPE                NO,BRANCH
         MVI   13(RW4),C'M'           SET TYPE TO MOUNT (M)
ENDTYPE  EQU   *
*
**  TEST FOR NS/R IN OR OUT
*
         TM    103(RW5),X'01'              NS/R?
         BZ    NOTNS                       NO,BRANCH
         MVC   15(4,RW4),=C'NS/R'
         B     NONSWP
NOTNS    EQU   *
         MVC   15(4,RW4),=C' IN '          SET TO IN
         TM    102(RW5),X'84'              OUT?
         BZ    NONSWP                      NOO,BRANCH
         MVC   15(4,RW4),=C' OUT'
NONSWP   EQU   *
*
**  CONVERT ASID
*
         LH    RW7,36(RW5)          PICK UP ASID
         CVD   RW7,DWD              CONVERT TO DECIMAL...
         MVC   9(3,RW4),EDIT+2       ...AND INSERT EDIT PATTERN
         ED    8(4,RW4),DWD+6
*
**  CONVERT DISPATCHING PRIORITY
*
         SR    RW7,RW7
         IC    RW7,43(RW5)         INSERT DPRTY
         CVD   RW7,DWD             CONVERT TO DECIMAL...
         MVC   21(3,RW4),EDIT+2       ...AND INSERT EDIT PATTERN
         ED    20(4,RW4),DWD+6       EDIT
*
**  SET UP ALLOCATED FRAME COUNT
*
         LH    RW7,152(RW5)          GET FRAME COUNT
         CVD   RW7,DWD               CONVERT TO DECIMAL...
         MVC   26(3,RW4),EDIT+2       ...AND INSERT EDIT PATTERN
         ED    25(4,RW4),DWD+6       EDIT
*
**  GET SWAP COUNT
*
         LH    RW7,30(RW6)           GET SWAP COUNT
*
         SR    RW8,RW8
         L     RW9,80(RW5)       TIME LIMIT IN APPROX SECS
         SLDL  RW8,4
         M     RW8,CONVERT         CONVERT TO SECS
         LTR   RW9,RW9             TEST FRACTION
         BNM   NORND1
         LA    RW8,1(RW8)          ROUND UP
NORND1   EQU   *
         CVD   RW8,DWD             CONVERT TO DECIMAL...
         MVC   36(5,RW4),EDIT             ...AND INSERT EDIT PATTERN
         ED    35(6,RW4),DWD+5           EDIT
*
**  GET TIME LEFT
*
         SR    RW8,RW8
         L     RW9,80(RW5)        STEP TIME
         S     RW9,64(RW5)
         S     RW9,200(RW5)       SRB TIME
         SLDL  RW8,4
         M     RW8,CONVERT        CONVERT TO TRUE SECS
         LTR   RW9,RW9            TEST FRACTION
         BNM   NORND2
         LA    RW8,1(RW8)         ROUND UP
NORND2   EQU   *
         CVD   RW8,DWD            CONVERT TO DECIMAL...
         MVC   47(4,RW4),EDIT+1           ...AND INSERT EDIT PATTERN
         MVO   PACK(5),DWD+5(3)        SHIFT UP
         ED    46(5,RW4),PACK+2         EDIT
         TM    103(RW5),X'80'         VALID TIMING?
         BZ    OKTIME
         MVC   36(5,RW4),INVAL
         MVC   47(4,RW4),INVAL+1
OKTIME   EQU   *
*
**  GET TIME GONE
*
         SR    RW8,RW8
         L     RW9,64(RW5)        STEP TIME
         A     RW9,200(RW5)       ADD SRB TIME
         SLDL  RW8,4
         M     RW8,CONVERT        CONVERT TO TRUE SECS
         LTR   RW9,RW9            TEST FRACTION
         BNM   NORND3
         LA    RW8,1(RW8)         ROUND UP
NORND3  EQU   *
         CVD   RW8,DWD            CONVERT TO DECIMAL...
         MVC   42(4,RW4),EDIT+1             ...INSERT EDIT PATTERN
         MVO   PACK(5),DWD+5(3)          SHIFT UP
         ED    41(5,RW4),PACK+2           EDIT
*
**  GET ASID AS INDEX
*
         ICM   RW9,15,65(RW5)      STEP TIME
         ICM   RW8,15,201(RW5)     SRB TIME
         AR    RW8,RW9
         LR    RW9,RW8
         LH    RW7,36(RW5)         GET ASID...
         SLL   RW7,2              ...AND SHIFT FOR INDEX
         S     RW9,TIME(RW7)      SUBTRACT LAST VALUE
         LTR   RW9,RW9            TEST THAT POSITIVE
         BP    NOTNEW             BRANCH IF OK
         SR    RW9,RW9
NOTNEW   EQU   *
         ST    RW8,TIME(RW7)      STORE NEW VALUE
         SR    RW8,RW8
         SLDL  RW8,12              MULTIPLY BY 4096
         D     RW8,=F'100'
         SR    RW8,RW8
         D     RW8,INTVAL         GET PERCENTAGE
         SLL   RW8,1              MULTIPLY REMAINDER
         C     RW8,INTVAL
         BL    NORND4
         LA    RW9,1(RW9)         ROUND UP
NORND4   EQU   *
         CVD   RW9,DWD            CONVERT TO DECIMAL...
         MVC   52(3,RW4),EDIT+2             ...INSERT EDIT PATTERN
         ED    51(4,RW4),DWD+6             EDIT
         MVI   55(RW4),C'%' ...AND INSERT %
*
**  GET I/O REQUESTS
*
         LH    RW8,40(RW5)              NO OF I/O REQUESTS
         CVD   RW8,DWD                  CONVERT TO DECIMAL
         MVC   58(5,RW4),EDIT            ...INSERT EDIT PATTERN
         ED    57(6,RW4),DWD+5          EDIT
         LR    RW9,RW8                  SAVE VALUE
         S     RW9,IO(RW7)              GET CHANGE
         ST    RW8,IO(RW7)              AND STORE NEW VALUE
         LTR   RW9,RW9                  CHECK THAT POSITIVE
         BP    OKIO                     YES BRANCH
         SR    RW9,RW9
OKIO     EQU   *
         SR    RW8,RW8
         MH    RW9,=H'100'              SCALE UP
         D     RW8,INTVAL               GET RATE
         SLL   RW8,1                     MULTIPLY REMAINDER
         C     RW8,INTVAL
         BL    NORND5
         LA    RW9,1(RW9)               ROUND UP
NORND5   EQU   *
         CVD   RW9,DWD                  CONVERT TO DECIMAL
         MVC   64(4,RW4),EDIT+1         ...INSERT EDIT PATTERN
         MVO   PACK(5),DWD+5(3)         SHIFT UP
         ED    63(5,RW4),PACK+2       EDIT
*
**  GET SERVICVE RATE
*
         L     RW8,44(RW6)               SERVICE ACCUMULATOR
         CVD   RW8,DWD                   CONVERT TO DECIMAL...
         MVC   70(5,RW4),EDIT            ...INSERT EDIT PATTERN
         ED    69(6,RW4),DWD+5          EDIT
         LR    RW9,RW8                   SAVE VALUE
         S     RW9,SERVICE(RW7)          GET CHANGE
         ST    RW8,SERVICE(RW7)          STORE NEW VALUE
         LTR   RW9,RW9                   VALID?
         BP    OKSERV                    YES BRANCH
         SR    RW9,RW9
OKSERV   EQU   *
         SR    RW8,RW8
         MH    RW9,=H'100'               SCALE UP
         D     RW8,INTVAL                GET RATE
         SLL   RW8,1          MULTIPLY REMAINDER
         C     RW8,INTVAL
         BL    NORND6
         LA    RW9,1(RW9)     ROUND UP
NORND6   EQU   *
         CVD   RW9,DWD                   CONVERT TO DECIMAL...
         MVC   76(4,RW4),EDIT+1            INSERT EDIT PATTERN
         MVO   PACK(5),DWD+5(3)         SHIFT UP
         ED    75(5,RW4),PACK+2          EDIT
*
**  INCREMENT POINTERS
*
         LA    RW3,4(RW3)              NEXT ASVT ENTRY
         LA    RW4,80(RW4)             NEXT SCREEN ENTRY
         L     RW5,COUNTER             PICK UP COUNTER
         BCT   RW5,NXTONE             GO BACK FOR MORE
*
**  RETURN
*
RETURN   EQU   *
         L     13,4(13)
         LM    14,12,12(13)        RESTORE REGISTERS
         BR    14
*
**  LITERALS AND CONSTANTS
*
         LTORG
*
SAVE     DS    18F
FNDLST   DS    2F
         DC    A(KEYLEN)
KEYAD    DS    F
         DC    A(THERE)
         DC    A(VALUE)
KEYLEN   DS    F
THERE    DS    F
CONVERT  DC    F'281474976'
VALUE    DC    F'1'
KEY1     DC    CL8'A='
KEY2     DC    CL8'ASID='
KEY3     DC    CL8'RESET'
KEY4     DC    CL8'R'
TITLE    DC    F'160'
         DC    A(HEADER)
         DC    F'160'
HEADER   DC    X'F021204B20204B20204B2020',8C' ',C'DISP',6C' ',C'SWAP'
         DC    4C' ',C'STEP+SRB CPU TIME',4C' ',C'I/O REQS'
         DC    C'    SERVICE  JOBNAME ASID'
         DC    C'   STAT PRTY FMCT  CNT  TOTAL GONE LEFT'
         DC    C' RATE  TOTAL RATE  TOTAL RATE'
DWD      DS    D
PKAREA   DS    D
TOD      DS    F
PACK     DC    X'000000000C'
EDIT     DC    X'2020202120'
INVAL    DC    C'    -'
COUNTER  DC    F'0'
LAST     DC    F'0'
INTVAL   DC    F'0'
TIME     DC    255F'0'
IO       DC    255F'0'
SERVICE  DC    255F'0'
         END
*          DATA SET ANBDISPS   AT LEVEL 005 AS OF 12/07/76
*          DATA SET ANBDISPS   AT LEVEL 004 AS OF 12/07/76
*          DATA SET ANBDISPS   AT LEVEL 003 AS OF 30/06/76
DISPR    CSECT
         B     ENTRY-DISPR(15)
         DC    X'07',CL7'DISPR'
ENTRY    EQU   *
*
**  ROUTINE CREATES SYSTEM DISPLAY
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW10     EQU   10
RW15     EQU   15
*
         STM   14,12,12(13)       SAVE REGISTERS
         BALR  RB2,0
         USING *,RB2
         ST    13,SAVE+4
         LR    RW3,13
         LA    13,SAVE
         ST    13,8(RW3)
*
**  LINKAGE IS CALL DISPR(SCREEN,FLAG,LEN,BUFFER)
**  FLAG IS SET TO X'FF'
**  IF THERE IS ANY DATA IN THE BUFFER
*
         L     RW3,0(1)           PICK UP SCREEN ADDRESS
         LR    RW4,RW3
         LM    RW5,RW7,CHARS      MOVE IN CHARATERS
         MVCL  RW4,RW6
*
**  SET UP INTERVAL AND TIME OF DAY
*
         TIME  BIN
         LR    RW1,RW0
         S     RW0,LAST           GET INTERVAL
         LTR   RW0,RW0            VALID?
         BP    VALID              YES,BRANCH
         LA    RW0,1              SET UP DEFAULT
VALID    EQU   *
         ST    RW0,INTVAL         STORE INTERVAL...
         ST    RW1,LAST           ...AND NEW VALUE
*
**M INSERT TOD INTO SCREEN
*
         TIME  DEC
         ST    RW0,TOD            STORE TOD...
         ED    0(12,RW3),TOD      ...AND EDIT
         MVI   0(RW3),X'40'       BLANK OUT FILL CHARACTER
*
**  GET PAGE/SWAP/VIO RATES
*
         L     RW4,16             ---> CVT...
         L     RW5,356(RW4)       ---> PVT
         LA    RW4,232(RW5)
         LA    RW8,8              NO OF ENTRIES.
         LA    RW9,PAGERATE
         LA    RW10,830(RW3)      ---> SCREEN
NXTRATE  EQU   *
         L     RW6,0(RW4)         PICK UP NEW VALUE
         LR    RW7,RW6
         S     RW7,0(RW9)         GET CHANGE
         ST    RW6,0(RW9)         STORE NEW VALUE
         SR    RW6,RW6
         MH    RW7,=H'100'        SCALE UP
         D     RW6,INTVAL         COMPUTE RATE
         SLL   RW6,1              CHECK REMAINDER
         C     RW6,INTVAL         FOR ROUND-UP
         BL    NORND1
         LA    RW7,1(RW7)         ROUND UP
NORND1   EQU   *
         CVD   RW7,DWD            CONVERT TO DECIMAL...
         ED    0(6,RW10),DWD+5  ...AND EDIT
         LA    RW10,40(RW10)
         LA    RW9,4(RW9)
         LA    RW4,4(RW4)
         BCT   RW8,NXTRATE        GO FOR NEXT ENTRY
*
**  GET AVQ AND PLPA+CSA SIZE
*
         LH    RW4,2(RW5)         AVQ COUNT
         CVD   RW4,DWD            CONVERT TO DECIMAL...
         ED    550(6,RW3),DWD+5 ...AND EDIT
         LH    RW4,292(RW5)       CSA+PLPA COUNT
         CVD   RW4,DWD            CONVERT TO DECIMAL...
         ED    150(6,RW3),DWD+5 ...AND EDIT
*
**  GET PRIMARY AND SECONDARY SLOT USAGES
*
         L     RW4,16          ---> CVT...
         L     RW4,704(RW4)    ---> ASMVT
         L     RW5,104(RW4)    PRIMARY TOTAL
         CVD   RW5,DWD         CONVERT TO DECIMAL...
         ED    1151(6,RW3),DWD+5 ...AND EDIT
         MVI   1151(RW3),C'/'  RESET FILL CHARACTER
         L     RW5,108(RW4)    VIO SLOTS
         A     RW5,112(RW4)    +NON-VIO SLOTS
         A     RW5,116(RW4)     +BAD SLOTS
         CVD   RW5,DWD          CONVERT TO DECIMAL...
         ED    1145(6,RW3),DWD+5   ...AND EDIT
         L     RW5,448(RW4)     SECONDARY TOTAL
         CVD   RW5,DWD          CONVERT TO DECIMAL...
         ED    1191(6,RW3),DWD+5   ...AND EDIT
         MVI   1191(RW3),C'/'   REMOVE FILL CHARACTER
         L     RW5,452(RW4)     SECONDARY USED
         CVD   RW5,DWD          CONVERT TO DECIMAL...
         ED    1185(6,RW3),DWD+5   ...AND EDIT
*
**  GET PAGE AND CPU UTILIZATIONS - ZERO COUNTERS
*
         LA    RW4,SYSCPU        AREA START
         LA    RW5,56            AREA LENGTH
         SR    RW6,RW6
         SR    RW7,RW7
         MVCL  RW4,RW6
*
         L     RW4,16        ---> CVT...
         L     RW4,556(RW4)  ---> ASVT
         LA    RW4,528(RW4)  ---> ASVT Q.
NXTONE   EQU   *
         L     RW5,0(RW4)
         LA    RW5,0(RW5)    REMOVE TOP BYTE
         LTR   RW5,RW5       END OF Q?
         BZ    ENDQ          YES,BRANCH
         TM    0(RW4),X'80'  IN USE?
         BZ    INCPTR        YES,BRANCH ROUND
NOASCB   EQU   *
         LA    RW4,4(RW4)    ---> NEXT ENTRY
         B     NXTONE
INCPTR   EQU   *
         CLC   0(4,RW5),=C'ASCB'  VALID ASCB?
         BNE   NOASCB             NO,BRANCH
         L     RW6,144(RW5)       ---> OUCB
         CLC   0(4,RW6),=C'OUCB'  VALID OUCB?
         BNE   NOASCB             NO,BRANCH
*
**  FIND TYPE OF ADDRESS SPACE AND INCREMENT TOTALS
*
         L     RW7,172(RW5)      ---> JOBNAME
         LTR   RW7,RW7           BATCH?
         BZ    NOTBATCH          NO,BRANCH
*
**  CHECK FOR HUW OR CICS
*
         CLC   0(8,RW7),CICS     CICS?
         BE    HUWCICS           YES,BRANCH
         CLC   0(8,RW7),HUW      HUW?
         BE    HUWCICS           YES,BRANCH
*
**  ADD ONTO BATCH TOTALS
*
         LH    RW7,152(RW5)         FRAME COUNT
         A     RW7,PAGBAT           ADD RUNNING TOTAL...
         ST    RW7,PAGBAT           ...AND STORE
         L     RW7,BATUSER          BATCH COUNT
         LA    RW7,1(RW7)           INCREMENT...
         ST    RW7,BATUSER          ...AND STORE
         TM    102(RW5),X'84'       OUT?
         BZ    NOTOUT
         L     RW7,BOUTUSER         LOAD OUT COUNT
         LA    RW7,1(RW7)           INCREMENT
         ST    RW7,BOUTUSER         AND STORE
         B     NOTIN
NOTOUT   EQU   *
         L     RW7,BINUSER          LOAD IN COUNT
         LA    RW7,1(RW7)           INCREMENT
         ST    RW7,BINUSER          AND STORE
NOTIN    EQU   *
         LA    RW7,BATCPU           ---> CPU COUNTER...
         B     INCPU                ...AND BRANCH TO ADD
NOTBATCH EQU   *
         TM    18(RW6),X'20'         TSO USER
         BZ    NOTSO                 NO,BRANCH
         LH    RW7,152(RW5)          PAGE COUNT
         A     RW7,PAGTSO            ADD TO RUNNING COUNT...
         ST    RW7,PAGTSO            ...AND STORE
         L     RW7,TSOUSER           LOAD TSO USER COUNT
         LA    RW7,1(RW7)            INCREMENT...
         ST    RW7,TSOUSER           ...AND STORE
         LA    RW7,TSOCPU            ---> CPU COUNTER...
         B     INCPU                 ...AND BRANCH TO ADD
NOTSO    EQU   *
         L     RW7,176(RW5)          ---> JOBNAME
         LTR   RW7,RW7
         BZ    NOASCB                BRANCH IF INVALID
         CLC   0(8,RW7),CICS         CICS?
         BE    HUWCICS               YES,BRANCH
         CLC   0(8,RW7),HUW          HUW?
         BE    HUWCICS               YES,BRANCH
         CLC   0(8,RW7),INIT         SPARE INITIATOR?
         BNE   NOTINIT               NO,BRANCH
         L     RW7,INITUSER          LOAD INIT COUNT
         LA    RW7,1(RW7)            INCREMENT...
         ST    RW7,INITUSER          ...AND STORE
         B     NOTSYS
NOTINIT  EQU   *
         L     RW7,SYSUSER           LOAD SYSTEM COUNT
         LA    RW7,1(RW7)            INCREMENT...
         ST    RW7,SYSUSER           ...AND STORE
NOTSYS   EQU   *
         LH    RW7,152(RW5)          PAGE COUNT
         A     RW7,PAGSYS            ADD TO RUNNING TOTAL...
         ST    RW7,PAGSYS            ...AND STORE
         LA    RW7,SYSCPU            ---> CPU COUNTER...
         B     INCPU                 ...AND BRANCH TO ADD
HUWCICS  EQU   *
         ICM   RW8,15,201(RW5)    SRB TIME
         AR    RW8,RW9
         LR    RW9,RW8
         LH    RW6,36(RW5)        ASID
         SLL   RW6,2              SHIFT TO USE AS INDEX...
         S     RW9,CPUSAGE(RW6)   ...INTO CPU COUNTERS
         ST    RW8,CPUSAGE(RW6)   STORE NEW VALUE
         LTR   RW9,RW9
         BM    NOASCB         DONT ADD ON IF NEGATIVE
         A     RW9,0(RW7)         ADD TO TOTAL FOR TYPE
         ST    RW9,0(RW7)         AND STORE
         B     NOASCB             TRY NEXT
ENDQ     EQU   *
*
**  WRITE OUT CPU FIELDS
*
         LA    RW6,4              NUMBER OF ENTRIES
         LA    RW7,190(RW3)       ---> SCREEN
         LA    RW8,BATCPU         ---> NEXT ENTRY
         L     RW5,SYSCPU         BATCH CPU TOTAL
NXTCPU   EQU   *
         SR    RW4,RW4
         SLDL  RW4,12
         D     RW4,=F'100'
         SR    RW4,RW4
         D     RW4,INTVAL         CALCULATE RATE
         SLL   RW4,1              LOOK FOR REMAINDER
         C     RW4,INTVAL
         BL    NORND2
         LA    RW5,1(RW5)         ROUND UP
NORND2   EQU   *
         CVD   RW5,DWD            CONVERT TO DECIMAL...
         ED    0(6,RW7),DWD+5     ...AND EDIT
         LA    RW7,80(RW7)        ---> NEXT SLOT
         L     RW5,0(RW8)         NEXT VALUE
         LA    RW8,4(RW8)         ---> NEXT VALUE
         BCT   RW6,NXTCPU         PROCESS NEXT ENTRY
*
**  EDIT PAGE FIELDS
*
         LA    RW6,4              NUMBER OF ENTRIES
         LA    RW7,230(RW3)       ---> SCREEN
         LA    RW8,PAGSYS         ---> PAGE COUNTS
NXTPAG   EQU   *
         L     RW5,0(RW8)         LOAD PAGE COUNT
         CVD   RW5,DWD            CONVERT TO DECIMAL
         ED    0(6,RW7),DWD+5     AND EDIT
         LA    RW7,80(RW7)        ---> NEXT SLOT
         LA    RW8,4(RW8)         ---> NEXT VALUE
         BCT   RW6,NXTPAG         PROCESS NEXT ENTRY
*
**  EDIT USER COUNTS
*
         LA    RW6,6            NUMBER OF ENTRIES
         LA    RW7,590(RW3)     ---> SCREEN
         LA    RW8,SYSUSER      ---> VALUE
NXTCNT   EQU   *
         L     RW5,0(RW8)       LOAD USER COUNT
         CVD   RW5,DWD          CONVERT TO DECIMAL...
         ED    0(6,RW7),DWD+5     ...AND EDIT
         LA    RW7,40(RW7)      ---> NEXT SLOT
         LA    RW8,4(RW8)       ---> NEXT VALUE
         BCT   RW6,NXTCNT       PROCESS NEXT ENTRY
*
**  LOOK THRU SRM CONTROL BLOCKS
*
         L     RW4,16           ---> CVT...
         L     RW4,604(RW4)     ---> RMCT...
         L     RW5,4(RW4)       ---> CCT
         LH    RW6,110(RW5)         CPU UTILIZATION
         CVD   RW6,DWD              CONVERT TO DECIMAL...
         ED    510(6,RW3),DWD+5     ...AND EDIT
         TM    124(RW5),X'80'       LOW PRIORITY USER?
         BZ    NOFLG1
         MVC   1440(25,RW3),MSG1    MOVE IN MESSAGE
NOFLG1   EQU   *
         L     RW5,8(RW4)          ---> ICT
*
**  PUT UP UNDER/OVER UTILIZED BITS
*
         L     RW7,72(RW5)        OVER-UTILIZED
         LA    RW8,4
         LA    RW9,1270(RW3)       ---> SCREEN
NXTFLG1  EQU   *
         SR    RW6,RW6
         SLDL  RW6,1
         LTR   RW6,RW6             TEST BIT SHIFTED
         BZ    NOFLG2
         MVI   0(RW9),C'1'         SET ON IN SCREEN
NOFLG2   EQU   *
         LA    RW9,1(RW9)          ---> NEXT SCREEN CHAR
         BCT   RW8,NXTFLG1
         L     RW7,76(RW5)        UNDER-UTILIZED
         LA    RW8,4
         LA    RW9,1230(RW3)      ---> SCREEN
NXTFLG2  EQU   *
         SR    RW6,RW6
         SLDL  RW6,1
         LTR   RW6,RW6            TEST TOP BIT
         BZ    NOFLG3
         MVI   0(RW9),C'1'        SET BIT ON IN SCREEN
NOFLG3   EQU   *
         LA    RW9,1(RW9)         ---> NEXT SCREEN CHAR
         BCT   RW8,NXTFLG2
*
**  TEST FOR FLAGS IN ICT
*
         TM    86(RW5),X'40'      LOAD BALANCING?
         BZ    NOFLG4
         MVC   1468(25,RW3),MSG2  MOVE IN MESSAGE
NOFLG4   EQU   *
         TM    86(RW5),X'80'      CHANNEL OVERLOADED?
         BZ    NOFLG5
         MVC   1495(25,RW3),MSG3   MOVE IN MESSAGE
NOFLG5   EQU   *
         L     RW5,12(RW4)           ---> MCT
         TM    144(RW5),X'80'        SQA FIRST LEVEL?
         BZ    NOFLG6
         MVC   1520(25,RW3),MSG4     MOVE IN MESSAGE
NOFLG6   EQU   *
         TM    144(RW5),X'40'        SQA SECOND LEVEL?
         BZ    NOFLG7
         MVC   1548(25,RW3),MSG5     MOVE IN MESSAGE
NOFLG7   EQU   *
         TM    144(RW5),X'20'        AVQ LOW?
         BZ    NOFLG8
         MVC   1575(25,RW3),MSG6     MOVE IN MESSAGE
NOFLG8   EQU   *
         TM    145(RW5),X'80'        ASM FIRST LEVEL?
         BZ    NOFLG9
         MVC   1600(25,RW3),MSG7     MOVE IN MESSAGE
NOFLG9   EQU   *
         TM    145(RW5),X'40'        ASM SECOND LEVEL?
         BZ    NOFLG10
         MVC   1628(25,RW3),MSG8     MOVE IN MESSAGE
NOFLG10  EQU   *
*
**  PUT IN SYSTEM WORKLOAD
*
         L     RW5,20(RW4)        ---> RMCA
         LH    RW5,6(RW5)         SYSTEM WORKLOAD*256
         MH    RW5,=H'100'        SCALE UP
         SRA   RW5,8              DIVIDE BY 256
         CVD   RW5,DWD            CONVERT TO DECIMAL
         ED    110(7,RW3),DWD+5    AND EDIT IN
*
**  FIND UCM - ANY JOBS WAITING FOR WTO BUFFERS?
*
         L     RW4,16             ---> CVT...
         L     RW4,100(RW4)       ---> UCM
         LA    RW4,256(RW4)       ---> WTO BUFFER QUEUE
         C     RW4,0(RW4)         ANY ENTRIES?
         BE    NOFLG11
         MVC   1655(25,RW3),MSG9  MOVE IN MESSAGE
NOFLG11  EQU   *
*
**  FIND LCH TABLE
*
         L     RW4,16             ---> CVT
         L     RW4,140(RW4)       ---> LCH TABLE
         LA    RW5,CHNNLS         ---> CHANNEL COUNTERS
         LA    RW6,4              NUMBER OF ENTRIES
         LA    RW7,1310(RW3)      ---> SCREEN
NXTCHNL  EQU   *
         LH    RW8,24(RW4)        NUMBER OF I/O REQUESTS
         LR    RW9,RW8
         S     RW9,0(RW5)          COMPUTE RATE
         ST    RW8,0(RW5)          STORE NEW VALUE
         SR    RW8,RW8
         LTR   RW9,RW9             VALID?
         BP    OKCHNNL
         SR    RW9,RW9
OKCHNNL  EQU   *
         MH    RW9,=H'100'         SCALE UP
         D     RW8,INTVAL          GET RATE
         SLL   RW8,1               CHECK REMAINDER
         C     RW8,INTVAL
         BL    NORND3
         LA    RW9,1(RW9)          ROUND UP
NORND3   EQU   *
         CVD   RW9,DWD             CONVERT TO DECIMAL
         ED    0(6,RW7),DWD+5      AND EDIT
         LA    RW7,40(RW7)         ---> NEXT SCREEN SLOT
         LA    RW5,4(RW5)
         LA    RW4,32(RW4)         ---> NEXT ENTRY
         BCT   RW6,NXTCHNL
*
**  RETURN
*
         L     13,4(13)
         LM    14,12,12(13)        RESTORE REGISTERS
         BR    14
*
**  LITERALS AND CONSTANTS
*
         LTORG
*
SAVE     DS    18F
TITLES   DC    X'F021204B20204B20204B2020',CL68' '
         DC    CL30'SYSTEM WORKLOAD',X'402021204B2020',C'   '
         DC    CL30'NO OF PLPA+CSA PAGES',X'402020202120',4C' '
         DC    CL30'SYSTEM CPU UTILIZATION',X'402020202120',C'%   '
         DC    CL30'NO OF SYSTEM PAGES',X'402020202120',4C' '
         DC    CL30'BATCH CPU UTILIZATION',X'402020202120',C'%   '
         DC    CL30'NO OF BATCH PAGES',X'402020202120',4C' '
         DC    CL30'TSO CPU UTILIZATION',X'402020202120',C'%   '
         DC    CL30'NO OF TSO PAGES',X'402020202120',4C' '
         DC    CL30'HUW+CICS CPU UTILIZATION',X'402020202120',C'%   '
         DC    CL30'NO OF HUW+CICS PAGES',X'402020202120',4C' '
         DC    CL30'TOTAL CPU UTILIZATION',X'402020202120',C'%   '
         DC    CL30'NO OF AVAILABLE PAGES',X'402020202120',4C' '
         DC    CL30'NO OF SYSTEM USERS',X'402020202120',4C' '
         DC    CL30'NO OF TSO USERS',X'402020202120',4C' '
         DC    CL30'NO OF BATCH USERS',X'402020202120',4C' '
         DC    CL30'NO OF SPARE INITS',X'402020202120',4C' '
         DC    CL30'NO OF BATCH USERS IN',X'402020202120',4C' '
         DC    CL30'NO OF BATCH USERS OUT',X'402020202120',4C' '
         DC    CL30'PAGE-IN RATE',X'402020202120',4C' '
         DC    CL30'PAGE-OUT RATE',X'402020202120',4C' '
         DC    CL30'VIO-IN RATE',X'402020202120',4C' '
         DC    CL30'VIO-OUT RATE',X'402020202120',4C' '
         DC    CL30'VIO RECLAIM RATE',X'402020202120',4C' '
         DC    CL30'SWAP-IN RATE',X'402020202120',4C' '
         DC    CL30'SWAP-OUT RATE',X'402020202120',4C' '
         DC    CL30'PAGE RECLAIM RATE',X'402020202120',4C' '
         DC    CL25'PRIMARY SLOT USAGE'
         DC    X'402020202120402020202120404040'
         DC    CL25'SECONDARY SLOT USAGE'
         DC    X'402020202120402020202120404040'
         DC    CL30'CHANNELS UNDERUTILIZED',C'0000',CL6' '
         DC    CL30'CHANNELS OVERUTILIZED',C'0000',CL6' '
         DC    CL30'CHANNEL 0 I/O RATE',X'402020202120',4C' '
         DC    CL30'CHANNEL 1 I/O RATE',X'402020202120',4C' '
         DC    CL30'CHANNEL 2 I/O RATE',X'402020202120',4C' '
         DC    CL30'CHANNEL 3 I/O RATE',X'402020202120',4C' '
CHARS    DC    F'1440'
         DC    A(TITLES)
         DC    F'1440'
TOD      DC    X'000000000C'
INTVAL   DS    F
LAST     DC    F'0'
SYSCPU   DS    F
BATCPU   DS    F
TSOCPU   DS    F
HUWCPU   DS    F
SYSUSER  DS    F
TSOUSER  DS    F
BATUSER  DS    F
INITUSER DS    F
BINUSER  DS    F
BOUTUSER DS    F
PAGSYS   DS    F
PAGBAT   DS    F
PAGTSO   DS    F
PAGHUW   DS    F
PAGERATE DC    8F'0'
DWD      DS    D
CPUSAGE  DC    255F'0'
CICS     DC    CL8'CICS'
HUW      DC    CL8'HUW1'
INIT     DC    CL8'INIT'
CHNNLS   DC    4F'0'
MSG1     DC    CL25'LOW PRTY USER NOT DISP'
MSG2     DC    CL25'I/O LOAD BALANCING ACTIVE'
MSG3     DC    CL25'SOME LCHS OVERLOADED'
MSG4     DC    CL25'SQA FIRST LEVEL SHORTAGE'
MSG5     DC    CL25'SQA SECOND LEVEL SHORTAGE'
MSG6     DC    CL25'AVQ BELOW LIMIT'
MSG7     DC    CL25'ASM FIRST LEVEL SHORTAGE'
MSG8     DC    CL25'ASM SECOND LEVEL SHORTAGE'
MSG9     DC    CL25'WTO BUFFER SHORTAGE'
         END
*          DATA SET ANBDISPQ   AT LEVEL 002 AS OF 30/06/76
DISPQ    CSECT
         B     ENTRY-DISPQ(15)
         DC    X'07',CL7'DISPQ'
ENTRY    EQU   *
*
**  ENQ DISPLAY ROUTINE
**  INPUT PARAMETERS AFFECTING THIS ROUTINE ARE
**    MAJOR=
**    MINOR=   MINOR QCB NAME
**    DSN=     SYSDSN/MINOR NAME
**    RESET/R
**    ASID=/A=
**    JOB=/J=
**  LINKAGE IS CALL DISPQ(SCREEN,FLAG,LEN,BUFFER)
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW15     EQU   15
*
         STM   14,12,12(13)
         BALR  RB2,0
         USING *,RB2
         ST    13,SAVE+4
         LR    RW3,13
         LA    13,SAVE
         ST    13,8(RW3)
*
**  FIND ASVTQ
*
         L     RW3,16            ---> CVT
         L     RW3,556(RW3)      ---> ASVT
         LA    RW3,524(RW3)      ---> ASVT Q
         ST    RW3,ASVTQ
*
** SET UP REG3 ---> SCREEN IMAGE
**        REG0 ---> COUNTER
*
         L     RW3,0(RW1)
         L     RW4,4(1)         ADDRESS OF FLAG
         TM    0(RW4),X'FF'     ANY DATA IN BUFFER?
         BZ    NOREAD           NO BRANCH
*
**  SEARCH BUFFER FOR KEYWORDS
*
         MVC   FNDLST(8),8(1)   LEN AND BUFFER ADDRESS
*
**  LOOK FOR MAJOR=
*
         MVI   VALUE,C' '       BLANK OUT RESULT FIELD
         MVC   VALUE+1(79),VALUE
         LA    RW4,6
         ST    RW4,KEYLEN       KEY LENGTH
         LA    RW4,KEY1
         ST    RW4,KEYAD        KEY ADDRESS
         LA    RW1,FNDLST       PARAM LIST
         L     RW15,=V(FNDTXT)
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4          KEYWORD PRESENT?
         BZ    NOMAJ            NO IGNORE
         MVC   QNAME(8),VALUE   SET QNAME...
         NI    TYPE,X'FC'
         OI    TYPE,X'01'       ...AND TYPE
NOMAJ    EQU   *
         TM    TYPE,X'02'       MAJOR SET?
         BZ    NOMIN
*
**  LOOK FOR MINOR=
*
         MVI   VALUE,C' '       BLANK OUT RESULT FIELD
         MVC   VALUE+1(79),VALUE
         LA    RW4,KEY2
         ST    RW4,KEYAD        KEY ADDRESS
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4          KEYWORD PRESENT
         BZ    NOMIN           NO IGNORE
         MVC   RNAME(44),VALUE  SET RNAME...
         OI    TYPE,X'03'       ...AND TYPE
NOMIN    EQU   *
*
**  LOOK FOR DSN=
*
         LA    RW4,4
         ST    RW4,KEYLEN            KEY LENGTH
         MVI   VALUE,C' '          BLANK OUT RESULT FIELD
         MVC   VALUE+1(79),VALUE
         LA    RW4,KEY3
         ST    RW4,KEYAD           KEY ADRESS
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4             KEYWORD PRESENT?
         BZ    NODSN               NO IGNORE
         MVC   QNAME(8),SYSDSN     SET QNAME
         MVC   RNAME(44),VALUE     SET RNAME
         OI    TYPE,X'03'          SET TYPE
NODSN    EQU   *
*
**  LOOK FOR RESET/R
*
         LA    RW4,5
         ST    RW4,KEYLEN          KEY LENGTH
         LA    RW4,KEY4
         ST    RW4,KEYAD           KEY ADDRESS
         L     RW15,=V(FNDPRS)
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4             KEYWORD PRESENT?
         BNZ   RESET               YES BRANCH ON
         LA    RW4,1
         ST    RW4,KEYLEN          KEY LENGTH
         LA    RW4,KEY5
         ST    RW4,KEYAD           KEY ADDRESS
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4             KEY PRESENT?
         BZ    NORESET             NO IGNORE
RESET    EQU   *
         MVI   TYPE,X'00'          RESET TYPE
         LA    RW4,KEY9
         ST    RW4,KEYAD           KEY ADDRESS
         L     RW15,=V(FNDTXT)
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4             KEYWORD PRESENT?
         BNZ   GETJOB              YES BRANCH ON
*
         LA    RW4,2
         ST    RW4,KEYLEN           KEY LENGTH
         LA    RW4,KEY8
         ST    RW4,KEYAD            KEY ADDRESS
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4              KEYWORD PRESENT
         BZ    NOJOB                NO IGNORE
GETJOB   EQU   *
*
**  FIND ASID CORRESPONDING TO JOBNAME
*
         L     RW4,ASVTQ
         LA    RW4,4(RW4)          POINT AT ASCB Q
NXTASCB  EQU   *
         L     RW5,0(RW4)          ASCB PTR
         LA    RW5,0(RW5)          REMOVE TOP BYTE
         LTR   RW5,RW5             END OF Q?
         BZ    NOJOB               YES JOBNAME NOT FOUND
         TM    0(RW4),X'80'        ASCB IN USE?
         BNZ   INCASCB             NO TRY NEXT
         L     RW7,172(RW5)        JOBNAME PTR
         LTR   RW7,RW7             VALID?
         BNZ   GOTPTR              YES BRANCH ON
         L     RW7,176(RW5)
         LTR   RW7,RW7             VALID?
         BZ    INCASCB             NO TRY NEXT ASCB
GOTPTR   EQU   *
         CLC   0(8,RW7),VALUE      MATCH?
         BE    GOTASCB             YES BRANCH OUT
INCASCB  EQU   *
         LA    RW4,4(RW4)
         B     NXTASCB             GO FOR NEXT
GOTASCB  EQU   *
         MVC   ASID(2),36(RW5)     MOVE IN ASID
         OI    TYPE,X'04'          SET TYPE
         B     NOREAD
NOJOB    EQU   *
*
**  LOOK FOR ASID=/A=
*
         LA    RW4,KEY7
         ST    RW4,KEYAD
         L     RW15,=V(FNDVAL)
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4              KEYWORD PRESENT?
         BNZ   GOTASID
         LA    RW4,5
         ST    RW4,KEYLEN           KEY LENGTH
         LA    RW4,KEY6
         ST    RW4,KEYAD            KEY ADDRESS
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4              KEYWORD PRESENT?
         BZ    NOASID              NO IGNORE
GOTASID  EQU   *
         OI    TYPE,X'04'          SET TYPE
         MVC   ASID(2),VALUE+2     STORE ASID
         B     NOREAD
*
**  LOOK FOR WAIT/W
*
NOASID   EQU   *
         LA    RW4,4
         ST    RW4,KEYLEN          KEY LENGTH
         LA    RW4,KEY10
         ST    RW4,KEYAD           KEY ADDRESS
         L     RW15,=V(FNDPRS)
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4           KEYWORD PRESENT?
         BNZ   GOTWAIT           YES BRANCH ON
         LA    RW4,1
         ST    RW4,KEYLEN        KEY LENGTH
         LA    RW4,KEY11
         ST    RW4,KEYAD         KEY ADDRESS
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4           KEYWORD PRESENT?
         BZ    NOREAD            NO IGNORE
GOTWAIT  EQU   *
         NI    TYPE,X'F3'        REMOVE UNWANTED BITS
         OI    TYPE,X'08'        SET WAIT BIT
NOREAD   EQU   *
*
**  INSERT TOD INTO SCREEN
*
         MVC   0(80,RW3),HEAD
         TIME  DEC
         ST    RW0,PACK
         ED    0(12,RW3),PACK         EDIT INTO SCREEN IMAGE
         MVI   0(RW3),C' '            BLANK OUT FILL CHAR
         LA    RW3,80(RW3)
         LA    RW0,20
         L     RW4,16            ---> CVT
         L     RW5,640(RW4)
         ST    RW5,THISMAJ
         L     RW6,644(RW4)
         ST    RW6,LASTMAJ
NXTMAJ   EQU   *
*
**  RW5 ---> CURRENT MAJOR QCB
*
         MVC   MAJOR(8),16(RW5)        STORE MAJOR NAME
         L     RW6,8(RW5)              FIRST MINOR
         ST    RW6,THISMIN
         L     RW7,12(RW5)             LAST MINOR
         ST    RW7,LASTMIN
*
**  TEST IF MAJOR REQUIRED
**  IF NOT ---> INCMAJ
*
         TM    TYPE,X'03'               TEST FOR EXPLICIT MAJOR
         BZ    SCAN1                    NO IGNORE
         CLC   MAJOR(8),QNAME           RIGHT ONE?
         BNE   INCMAJ                   NO IGNORE THIS MAJOR
SCAN1    EQU   *
         TM    TYPE,X'0F'          ASID ONLY WANTED?
         BNZ   NXTMIN              YES BRANCH ON
         LA    RW8,NOTMAJS              UNREQUIRED MAJORS
         LA    RW7,5
NXTREJ   EQU   *
         CLC   MAJOR(8),0(RW8)
         BE    INCMAJ
         LA    RW8,8(RW8)
         BCT   RW7,NXTREJ
NXTMIN   EQU   *
*
**  RW6 ---> CURRENT MINOR
*
         SR    RW4,RW4
         IC    RW4,16(RW6)        MINOR NAME LENGTH
         BCTR  RW4,0              REDUCE BY ONE
         LTR   RW4,RW4
         BM    INCMIN             AVOID IF -VE LENGTH
         C     RW4,=F'43'         MAX LENGTH
         BNH   OKLEN
         LA    RW4,43
OKLEN    EQU   *
         MVI   MINOR,C' '
         MVC   MINOR+1(43),MINOR
*
**  CHECK IF MINOR NAME SHOULD BE IN HEX
*
         LA    RW8,HEXENQS           POINT AT HEX MAJOR NAMES
         LA    RW7,6
NXTHEX   EQU   *
         CLC   MAJOR(8),0(RW8)       MATCH?
         BE    HEXPRNT               YES BRANCH
         LA    RW8,8(RW8)
         BCT   RW7,NXTHEX
         CLC   MAJOR(8),HEXALSO      TRY HEX SOMETIMES MAJOR
         BNE   NOTHEX
         CLI   20(RW6),C'J'          RIGHT MINOR?
         BNE   NOTHEX                NO IGNORE
HEXPRNT  EQU   *
*
**  PRINT OUT MINOR IN HEX
*
         LA    RW4,1(RW4)
         C     RW4,=F'22'          MAX LENGTH
         BNH   OKLEN2
         LA    RW4,22              SET MAX LENGTH
OKLEN2   EQU   *
         LA    RW7,20(RW6)         POINT AT MINOR NAME
         LA    RW5,MINOR           POINT AT RECEIVING AREA
NXTBYTE  EQU   *
         SR    RW8,RW8
         SR    RW9,RW9
         IC    RW8,0(RW7)         PICK UP ONE BYTE
         SRDL  RW8,4              DROP RIGHT NIBBLE
         N     RW8,=F'15'         AND OUT UNWANTED BITS
         IC    RW8,CHARS(RW8)     PICK UP CHARACTER
         STC   RW8,0(RW5)        AND STORE
         LA    RW5,1(RW5)
         SR    RW8,RW8           ZERO REGISTER
         SLDL  RW8,4             AND INSERT NIBBLE
         N     RW8,=F'15'        AND OUT UNWANTED BITS
         IC    RW8,CHARS(RW8)    PICK UP CHAR
         STC   RW8,0(RW5)        AND STORE
         LA    RW5,1(RW5)
         LA    RW7,1(RW7)
         BCT   RW4,NXTBYTE
         B     NOMOVE
NOTHEX   EQU   *
*
**  MOVE IN IN CHARACTER FORM
*
         EX    RW4,MOVEMIN       MOVE IN MINOR NAME
NOMOVE   EQU   *
         L     RW4,8(RW6)
         ST    RW4,THISQEL
         L     RW5,12(RW6)
         ST    RW5,LASTQEL
         TM    17(RW6),X'80'       SYSTEM?
         BZ    NOT1
         MVC   SCOPE(8),SYSTEM
         B     GONE1
NOT1     EQU   *
         TM    17(RW6),X'40'       SYSTEMS?
         BZ    NOT2
         MVC   SCOPE(8),SYSTEMS
         B     GONE1
NOT2     EQU   *
         MVC   SCOPE(8),STEP
GONE1    EQU   *
*
**  TEST IF MINOR IS REQUIRED
**  IF NOT ---> INCMIN
*
         TM    TYPE,X'02'            EXPLICIT MINOR REQUIRED?
         BZ    SCAN2                 NO
         CLC   MINOR(44),RNAME       CORRECT ONE?
         BNE   INCMIN                NO IGNORE
SCAN2    EQU   *
         TM    TYPE,X'0C'              ASID ONLY WANTED?
         BZ    SCAN3                   NO BRANCH ROUND
         TM    TYPE,X'08'        WAIT OPTION?
         BZ    SCAN4             NO IGNORE
*
**  ONLY USE ENQS WHERE THERE IS A HOLD-UP
*
         MVI   CNT,X'00'         RESET BITS
LOOKQEL  EQU   *
         TM    12(RW4),X'80'     EXCL OR SHR?
         BZ    EXCLQEL
         TM    CNT,X'02'         PREVIOUS EXCL?
         BNZ   GOODQEL           YES WANT THIS ONE
         MVI   CNT,X'01'         SET SHR BIT
         B     NXTLOOK
EXCLQEL  EQU   *
         TM    CNT,X'03'         ANY PREVIOUS ENQ?
         BNZ   GOODQEL           YES WANT THIS ONE
         MVI   CNT,X'02'         SET EXCL BIT
NXTLOOK  EQU   *
         C     RW4,LASTQEL       END OF CHAIN?
         BE    INCMIN            YES IGNORE THIS ONE
         LR    RW7,RW4
         L     RW4,0(RW7)
         LTR   RW4,RW4
         BZ    INCMIN
         C     RW7,4(RW4)       CORRECT BACK PTR?
         BNE   INCMIN
         B     LOOKQEL          TRY NEXT
SCAN4    EQU   *
*
**  LOOK DOWN QEL CHAIN FOR CORRECT ASID
*
TRYQEL   EQU   *
         CLC   14(2,RW4),ASID          CORRECT ASID?
         BE    GOODQEL
         C     RW4,LASTQEL             LAST ONE?
         BE    INCMIN                  YES TRY NEXT MINOR
         LR    RW7,RW4
         L     RW4,0(RW7)              NEXT QEL
         LTR   RW4,RW4                 END OF CHAIN
         BZ    INCMIN
         C     RW7,4(RW4)              CORRECT BACK PTR?
         BNE   INCMIN
         B     TRYQEL                  TRY NEXT
GOODQEL  EQU   *
         L     RW4,THISQEL             RESTORE QEL ADDRESS
SCAN3    EQU   *
         C     RW0,=F'1'               ROOM FOR ENTRY?
         BNH   RETURN                  NO IGNORE
         MVC   0(80,RW3),LINE             MOVE LINE INTO SCREEN
         MVC   6(8,RW3),MAJOR             MAJOR NAME
         MVC   21(8,RW3),SCOPE            SCOPE
         MVC   36(44,RW3),MINOR
         LA    RW5,80(RW3)
         LA    RW3,160(RW3)
         BCT   RW0,DOWN1
         B     RETURN
DOWN1    EQU   *
         LA    RW6,5
*
NXTQEL   EQU   *
*
**  RW4 ---> CURRENT QEL
*
         LH    RW7,14(RW4)           ASID
         SLL   RW7,2                 SHIFT FOR INDEX
         A     RW7,ASVTQ             ADD AS OFFSET INTO ASVT
         TM    0(RW7),X'80'          ASCB IN USE?
         BNZ   GONE2
         L     RW7,0(RW7)           ASCB
         L     RW8,172(RW7)          JOBNAME POINTER
         LTR   RW8,RW8              VALID?
         BNZ   NOT3
         L     RW8,176(RW7)
         LTR   RW8,RW8              VALID?
         BZ    GONE2
NOT3     EQU   *
         MVC   1(8,RW5),0(RW8)      MOVE IN JOBNAME
GONE2    EQU   *
         MVC   10(4,RW5),SHR        SET TO SHARED
         TM    12(RW4),X'80'        EXCL?
         BNZ   GONE3
         MVC   10(4,RW5),EXCL       SET TO EXCL
GONE3    EQU   *
         LA    RW5,16(RW5)
         BCT   RW6,INCQEL
*
**  DOWN ONE LINE
*
         LR    RW5,RW3
         LA    RW6,5
         LA    RW3,80(RW3)
         BCT   RW0,INCQEL
         B     RETURN
*
INCQEL   EQU   *
         L     RW7,THISQEL         RESTORE ADDRESS
         C     RW7,LASTQEL         LAST ONE?
         BE    TSTQEL
         L     RW4,0(RW7)          NEXT QEL
         LTR   RW4,RW4             END OF CHAIN?
         BZ    TSTQEL
         C     RW7,4(RW4)          CORRECT BACK PTR?
         BNE   TSTQEL
         ST    RW4,THISQEL
         B     NXTQEL
*
TSTQEL   EQU   *
         C     RW6,=F'5'           ANY QELS ON LINE
         BNL   NOLINE              NO BRANCH
         BCT   RW0,INCMIN          REDUCE LINE COUNT
         B     RETURN
NOLINE   EQU   *
         S     RW3,=F'80'          POINT BACK A LINE
*
INCMIN   EQU   *
         L     RW7,THISMIN         RESTORE ADDRESS
         C     RW7,LASTMIN         LAST ONE?
         BE    INCMAJ
         L     RW6,0(RW7)          NEXT ONE
         LTR   RW6,RW6             END OF CHAIN?
         BZ    INCMAJ
         C     RW7,4(RW6)          CORRECT BACK PTR?
         BNE   INCMAJ
         ST    RW6,THISMIN
         B     NXTMIN
*
INCMAJ   EQU   *
         L     RW7,THISMAJ
         C     RW7,LASTMAJ          LAST ONE?
         BE    RETURN
         L     RW5,0(RW7)           NEXT ONE
         LTR   RW5,RW5              END OF CHAIN?
         BZ    RETURN
         C     RW7,4(RW5)           CORRECT BACK PTR?
         BNE   RETURN
         ST    RW5,THISMAJ
         B     NXTMAJ
*
RETURN   EQU   *
         L     13,4(13)
         LM    14,12,12(13)
         BR    14
*
        LTORG
*
SAVE     DS    18F
THISMAJ  DS    F
THISMIN  DS    F
THISQEL  DS    F
LASTMAJ  DS    F
LASTMIN  DS    F
LASTQEL  DS    F
MAJOR    DC    CL8' '
MINOR    DC    CL44' '
SCOPE    DC    CL8' '
SYSTEM   DC    CL8'SYSTEM'
SYSTEMS  DC    CL8'SYSTEMS'
STEP     DC    CL8'STEP'
SHR      DC    CL4'SHR'
EXCL     DC    CL4'EXCL'
NOTMAJS  DC    CL8'SYSDSN'
         DC    CL8'SYSVSAM'
         DC    CL8'SYSZOPEN'
         DC    CL8'SYSIKJUA'
         DC    CL8'HUWLNFL'
LINE     DC    CL80'MAJOR          SCOPE          MINOR'
ASVTQ    DS    F
PACK     DC    X'000000000C'
TYPE     DC    X'00'
MOVEMIN  MVC   MINOR(0),20(RW6)
ASID     DS    H
QNAME    DC    CL8' '
RNAME    DC    CL44' '
HEAD     DC    X'F021204B20204B20204B2020',23C' '
         DC    A(KEYLEN)
KEYAD    DS    F
         DC    A(THERE)
         DC    A(VALUE)
KEYLEN   DS    F
THERE    DS    F
VALUE    DC    CL80' '
SYSDSN   DC    CL8'SYSDSN'
CHARS    DC    C'0123456789ABCDEF'
KEY1     DC    CL8'MAJOR='
KEY2     DC    CL8'MINOR='
KEY3     DC    CL8'DSN='
KEY4     DC    CL8'RESET'
KEY5     DC    CL8'R'
KEY6     DC    CL8'ASID='
KEY7     DC    CL8'A='
KEY8     DC    CL8'J='
KEY9     DC    CL8'JOB='
KEY10    DC    CL8'SCAN'
KEY11    DC    CL8'S'
CNT      DC    X'00'
         END
*          DATA SET ANBDISPH   AT LEVEL 002 AS OF 30/06/76
DISPH    CSECT
         B     ENTRY-DISPH(15)
         DC    X'07',CL7'DISPH'
ENTRY    EQU   *
*
**  ROUTINE DISPLAYS Q LENGTHS FOR HUW
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW10     EQU   10
RW11     EQU   11
*
         STM   14,12,12(13)
         BALR  RB2,0
         USING *,RB2
         ST    13,SAVE+4
         LR    RW3,13
         LA    13,SAVE
         ST    13,8(RW3)
*
         L     RW3,0(1)          ---> SCREEN IMAGE
*
**  ANY DATA IN BUFFER?
*
         L     RW4,4(1)          ---> FLAG
         TM    0(RW4),X'FF'
         BZ    NOREAD            NO BRANCH ROUND
*
**  LOOK FOR RESET/R
*
         MVC   FNDLST(8),8(1)    ADDRESSES OF LEN AND BUFFER
         LA    RW4,5
         ST    RW4,KEYLEN        KEY LENGTH
         LA    RW4,KEY1
         ST    RW4,KEYAD         KEY ADDRESS
         LA    1,FNDLST          PARAM LIST
         L     15,=V(FNDPRS)
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4           KETWORD PRESENT?
         BNZ   RESET             YES BRANCH ON
         LA    RW4,1
         ST    RW4,KEYLEN        KEY ;ENGTH
         LA    RW4,KEY2
         ST    RW4,KEYAD         KEY ADDRESS
         BALR  14,15
         L     RW4,THERE
         LTR   RW4,RW4           KEYWORD PRESENT?
         BZ    NOREAD
RESET    EQU   *
         MVI   ASYNC,X'00'
         MVC   ASYNC+1(19),ASYNC  RESET ASYNC COUNTERS
         MVI   SESSION,X'00'
         MVC   SESSION+1(255),SESSION RESET SESSION COUNTERS
NOREAD   EQU   *
*
         LR    RW4,RW3
         LM    RW5,RW7,CHARS
         MVCL  RW4,RW6           MOVE IN TITLES
*
**  INSERT TOD INTO SCREEN IMAGE
*
         TIME  DEC
         ST    0,TOD
         ED    0(12,RW3),TOD     EDIT IN TOD
         MVI   0(RW3),X'40'      BLANK OUT FILL CHAR
*
**  FIND HUW CSA PAGE
*
         L     RW4,16           ---> CVT
         L     RW4,204(RW4)     ---> HARCVT
         L     RW4,16(RW4)      ---> HUWCVT
         L     RW4,8(RW4)        ---> HUW CSA PAGE
         SR    RW5,RW5
         LA    RW6,4             NO OF ENTRIES
         LA    RW7,1080(RW4)     ---> DATA
         LA    RW8,110(RW3)      ---> SCREEN ENTRY
NXTENT   EQU   *
         IC    RW5,0(RW7)        PICK UP VALUE
         CVD   RW5,DWD           CONVERT
         ED    0(4,RW8),DWD+6    AND EDIT
         LA    RW8,40(RW8)       ---> NEXT SCREEN ENTRY
         LA    RW7,1(RW7)        ---> NEXT DATA ENTRY
         BCT   RW6,NXTENT
         LH    RW5,110(RW4)         SUB-TASK Q
         SH    RW5,108(RW4)
         SRA   RW5,2             DIVIDE BY 4
         LTR   RW5,RW5              VALID?
         BNM   OKVAL
         LA    RW5,64(RW5)          ADD 64
OKVAL    EQU   *
         CVD   RW5,DWD              CONVERT
         ED    0(4,RW8),DWD+6       AND EDIT
*
*
**  COMPUTE TOTALS FOR NO OF EVENTS FOR ASYNC Q
*
         LA    RW1,5           NO OF ENTRIES
         LA    RW6,400(RW4)    ---> COUNTERS
         LA    RW7,ASYNC          ---> STORED VALUES
         SR    RW5,RW5
NXTSYN   EQU   *
         A     RW5,0(RW6)      ADD TO TOTAL
         S     RW5,0(RW7)         SUBTRACT LAST VALUE
         LA    RW6,4(RW6)      ---> NEXT COUNTER
         LA    RW7,4(RW7)         ---> NEXT VALUE
         BCT   RW1,NXTSYN
*
**  SET UP FOR HISTOGRAM
*
         LA    RW11,ASYNC    ---> STORED VALUES
         LA    RW10,521(RW3)
         LA    RW1,5         NO OF ENTRIES
         LA    RW8,400(RW4)  ---> COUNTERS
         LA    RW9,531(RW3)  ---> SCREEN IMAGE
NXTLIN   EQU   *
         L     RW7,0(RW8)    PICK UP VALUE
         LR    RW0,RW7       SAVE VALUE
         S     RW7,0(RW11)   SUBTRACT LAST VALUE
         ST    RW0,0(RW11)   UPDATE LAST VALUE
         SR    RW6,RW6       ZERO TOP HALF
         M     RW6,=F'100000'    SCALE UP
         LTR   RW5,RW5       VALID DIVISOR?
         BNZ   NOTINC1
         LA    RW5,1         SET TO NON-ZERO
NOTINC1  EQU   *
         DR    RW6,RW5       COMPUTE FRACTION
         C     RW7,=F'99990'   GREATER THAN FIELD MAX?
         BL    NOTRUN1
         L     RW7,=F'99990'       SET MAX
NOTRUN1  EQU   *
         CVD   RW7,DWD           CONVERT TO DECIMAL
         ED    0(6,RW10),DWD+5   AND EDIT
         LTR   RW7,RW7
         BZ    INC1           BRANCH IF VALUE ZERO
         SR    RW6,RW6
         D     RW6,=F'500'       CONVERT TO NEAREST HALF %
         SLL   RW6,1         LOOK AT REMAONDER
         CR    RW6,RW5       > HALF?
         BL    NORND1
         LA    RW7,1(RW7)    ROUND UP
NORND1   EQU   *
         LA    RW7,1(RW7)     ALLOW FOR ZERO
*
**  CHECK THAT ANSWER IS IN LIMITS 1-21
*
         LR    RW6,RW9
         C     RW7,=F'1'
         BH    OKLO1
         LA    RW7,1          SET LO DEFAULT
OKLO1    EQU   *
         C     RW7,=F'21'
         BL    OKHI1
         LA    RW7,21         SET HI DEFAULT
OKHI1    EQU   *
         MVI   0(RW6),C'*'    MOVE IN *
         LA    RW6,1(RW6)     ---> NEXT SLOT
         BCT   RW7,OKHI1
*
INC1     EQU   *
         LA    RW11,4(RW11)
         LA    RW10,80(RW10)
         LA    RW9,80(RW9)    ---> NEXT LINE IN SCREEN IMAGE
         LA    RW8,4(RW8)     ---> NEXT COUNTER
         BCT   RW1,NXTLIN     DO NEXT LINE
*
**  CONPUTE TOTALS FOR SESSION Q LENGTH
*
         LA    RW7,SESSION     ---> LAST VALUES
         LA    RW1,64           NO OF ENTRIES
         LA    RW6,688(RW4)     ---> COUNTERS
         SR    RW5,RW5
NXTSES   EQU   *
         A     RW5,0(RW6)       ADD IN COUNTER TO TOTAL
         S     RW5,0(RW7)      SUBTRACT LAST VALUE
         LA    RW7,4(RW7)      ---> NEXT VALUE
         LA    RW6,4(RW6)       ---> NEXT COUNTER
         BCT   RW1,NXTSES
*
**  SET UP FOR HISTOGRAM
*
         LA    RW11,SESSION     ---> LAST VALUES
         LA    RW10,480(RW3)    ---> SCREEN IMAGE
         LA    RW1,13           DO FIRST 13 LINES
         LA    RW8,688(RW4)     ---> COUNTERS
         LA    RW9,492(RW3)     ---> SCREEN IMAGE
NXTBAR   EQU   *
         L     RW7,0(RW8)       PICK UP VALUE
         LR    RW0,RW7          SAVE VALUE
         S     RW7,0(RW11)      GET CHANGE
         ST    RW0,0(RW11)      SAVE NEW VALUE
         SR    RW6,RW6          ZERO TOP HALF
         M     RW6,=F'100000'       SCALE UP
         LTR   RW5,RW5           VALID DIVISOR?
         BNZ   NOTINC2
         LA    RW5,1             SET TO ONE
NOTINC2  EQU   *
         DR    RW6,RW5          COMPUTE FRACTION
         C     RW7,=F'99990'    GREATER THAN FIELD MAX?
         BL    NOTRUN2
         L     RW7,=F'99990'       SET MAX
NOTRUN2  EQU   *
         CVD   RW7,DWD         CONVERT TO DECIMAL
         ED    0(6,RW10),DWD+5 AND EDIT
         LTR   RW7,RW7         ZERO?
         BZ    INC2            YES,IGNORE
         SR    RW6,RW6         ZERO TOP HALF
         D     RW6,=F'500'
         SLL   RW6,1            LOOK AT REMAINDER
         CR    RW6,RW5          > HALF ?
         BL    NORND2
         LA    RW7,1(RW7)       ROUND UP
NORND2   EQU   *
         LA    RW7,1(RW7)      ALLOW FOR ZERO
*
**  CHECK THAT ANSWER IS IN LIMITS 1-21
*
         LR    RW6,RW9
         C     RW7,=F'1'
         BH    OKLO2
         LA    RW7,1          SET LO DEFAULT
OKLO2    EQU   *
         C     RW7,=F'21'
         BL    OKHI2
         LA    RW7,21          SET HI DEFAULT
OKHI2    EQU   *
         MVI   0(RW6),C'*'
         LA    RW6,1(RW6)       ---> NEXT SLOT
         BCT   RW7,OKHI2
*
INC2     EQU   *
         LA    RW11,4(RW11)
         LA    RW10,80(RW10)
         LA    RW9,80(RW9)      ---> NEXT LINE IN SCREEN IMAGE
         LA    RW8,4(RW8)       ---> NEXT COUNTER
         BCT   RW1,NXTBAR       DO NEXT LINE
*
**  COMPUTE REMAINDER
*
         LA    RW1,51
         SR    RW7,RW7
NXTREM   EQU   *
         L     RW0,0(RW8)       GET NEW VALUE
         AR    RW7,RW0          ADD TO TOTAL
         S     RW7,0(RW11)      SUBTRACT OLD VALUE
         ST    RW0,0(RW11)      STORE NEW VALUE
         LA    RW11,4(RW11)     ---> NEXT SLOT
         LA    RW8,4(RW8)        ---> NEXT COUNTER
         BCT   RW1,NXTREM
         SR    RW6,RW6           ZERO TOP HALF
         M     RW6,=F'100000'        SCALE UP
         LTR   RW5,RW5           VALID DIVISOR?
         BNZ   NOTINC3
         LA    RW5,1
NOTINC3  EQU   *
         DR    RW6,RW5           COMPUTE FRACTION
         CVD   RW7,DWD           CONVERT TO DECIMAL
         ED    0(6,RW10),DWD+5   AND EDIT
         LTR   RW7,RW7
         BZ    INC3          IGNORE IF ZERO
         SR    RW6,RW6
         D     RW6,=F'500'
         SLL   RW6,1             LOOK AT REMAINDER
         CR    RW6,RW5           > HALF?
         BL    NORND3
         LA    RW7,1(RW7)          ROUND UP
NORND3   EQU   *
         LA    RW7,1(RW7)         ALLOW FOR ZERO
*
**  CHECK THAT VALUE IS IN LIMITS 1-21
*
         C     RW7,=F'1'
         BH    OKLO3
         LA    RW7,1           SET LO DEFAULT
OKLO3    EQU   *
         C     RW7,=F'21'
         BL    OKHI3
         LA    RW7,21          SET HI DEFAULT
OKHI3    EQU   *
         MVI   0(RW9),C'*'
         LA    RW9,1(RW9)      ---> NEXT SLOT
         BCT   RW7,OKHI3
INC3     EQU   *
         L     13,4(13)
         LM    14,12,12(13)
         BR    14
*
         LTORG
*
SAVE     DS    18F
FNDLST   DS    2F
         DC    A(KEYLEN)
KEYAD    DS    F
         DC    A(THERE)
KEYLEN   DS    F
THERE    DS    F
KEY1     DC    CL8'RESET'
KEY2     DC    CL8'R'
DWD      DS    D
ASYNC    DC    5F'0'
SESSION  DC    64F'0'
CHARS    DC    F'1680'
         DC    A(TITLES)
         DC    F'1680'
TOD      DC    X'000000000C'
TITLES   DC    X'F021204B20204B20204B2020',CL68' '
         DC    CL30'ASYNC TASK Q LENGTH',X'40202120',6C' '
         DC    CL30'PAGING TASK Q LENGTH',X'40202120',6C' '
         DC    CL30'SESSION TASK Q LENGTH',X'40202120',6C' '
         DC    CL30'UNUSED',X'40202120',6C' '
         DC    CL30'SUB-TASK Q LENGTH',X'40202120',6C' '
         DC    40C' '
         DC    12C' ',C'0 1 2 3 4 5 6 7 8 9 10',17C' '
         DC    C'0 1 2 3 4 5 6 7 8 9 10',7C' '
         DC    12C' ',11C'+-',17C' ',11C'+-',7C' '
         DC    X'4021204B2020',C'   0 ',30C' '
         DC    X'21204B2020',C' 0 ',29C' '
         DC    X'4021204B2020',C'   1 ',30C' '
         DC    X'21204B2020',C' 1 ',29C' '
         DC    X'4021204B2020',C'   2 ',30C' '
         DC    X'21204B2020',C' 2 ',29C' '
         DC    X'4021204B2020',C'   3 ',30C' '
         DC    X'21204B2020',C' 3 ',29C' '
         DC    X'4021204B2020',C'   4 ',30C' '
         DC    X'21204B2020',C' 4 ',29C' '
         DC    X'4021204B2020',C'   5 ',38C' '
         DC    C'ASYNC Q LENGTH',16C' '
         DC    X'4021204B2020',C'   6 ',68C' '
         DC    X'4021204B2020',C'   7 ',68C' '
         DC    X'4021204B2020',C'   8 ',68C' '
         DC    X'4021204B2020',C'   9 ',68C' '
         DC    X'4021204B2020',C'  10 ',68C' '
         DC    X'4021204B2020',C'  11 ',68C' '
         DC    X'4021204B2020',C'  12 ',68C' '
         DC    X'4021204B2020',C' >12 ',68C' '
         DC    11C' ',C'SESSION Q LENGTH',53C' '
         END
