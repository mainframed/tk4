CHKDUMPS CSECT
         SAVE  (14,12)
         LR    R3,R15
         USING CHKDUMPS,R3
         ST    R13,SAVE+4
         LA    R13,SAVE
         L     R4,76
         L     R4,572(R4)   A(RTCT) INTO 4
         LA    R4,36(R4)   POINT TO RTCTSDDS FIELDS
         L     R5,=F'10'   LIMIT TO 10 SCANS
         SPACE 3
TEST1    CLC   0(2,R4),=X'0000'   IS THIS A DUMP DATA SET
         BE    OUT   NO, WE ARE THRU
         MVC   MSG1+17(2),0(R4)  MOVE DUMP NUMBER TO MESSAGE
         MVC   MSG2+8(2),0(R4)   MOVE DUMP NO. TO MESSAGE2
         MVC   FILE+42(2),0(R4)   FILL IN DDNAME IN DCB
         OPEN  (FILE,INPUT)
         LA    R6,3                    NO. OF RECS TO SCAN FOR HDR
         LA    R7,1                    RECORD POINTER
         EJECT
NEXTREC  MVC   ECB1(4),=F'0'
         MVI   RIA,X'40'
         MVC   RIA+1(250),RIA    BLANK OUT THE R.I.A.
         MVC   MTCIOB(40),ZERO40
         OI    IOBFLAG1,X'42'
         MVI   IOBFLAG2,X'10'
         MVC   IOBECBPT(3),=AL3(ECB1)
         MVC   IOBSTART(3),=AL3(CHPGM)
         MVC   IOBDCBPT(3),=AL3(FILE)
         L     R1,FILE+44              DEB ADDR
         MVC   IOBSEEK+3(4),38(R1)     CCHH
         STC   R7,IOBSEEK+7            SET RECORD NUMBER
         EXCP  MTCIOB
         WAIT  ECB=ECB1
         CLC   RIA(2),=X'FFFF'         HDR RECORD
         BE    TEST2                   YES, CONTINUE
         LA    R7,1(0,R7)              ADD 1 TO RECORD POINTER
         BCT   R6,NEXTREC              GO CHK NEXT RECORD FOR HDR
         B     NEXT                    TRIED 3 RECORDS, HDR MISSING
         EJECT
TEST2    MVI   SW1,X'FF'   INDICATE THAT AT LEAST ONE DATA SET IS FULL
         MVC   CLOCK(8),RIA+12
         MVC   MSG2+13(8),RIA+4        MODULE NAME
         MVC   MSG2+21(6),=C' -    '
         MVC   MSG2+27(61),RIA+20
         LA    R1,PARMLIST
         LA    R2,1
         L     R10,=V(TODCNVRT)
         BALR  R14,R10
         MVC   MSG1+31(2),ANSWER+2
         MVC   MSG1+34(3),ANSWER+4
         MVC   MSG1+39(8),ANSWER+7
         SPACE 3
MSG1     WTO   'SYS1.DUMPXX IS FULL AT YY.DDD  HH.MM.SS',              X
               ROUTCDE=(1,2,3,4,5,6,7,8,9,10,11,12,13)
         SPACE 3
MSG2     WTO   'NN...XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXC
               XXXXXXXXXXXXXXXXXXXXXXXXX',                             X
               ROUTCDE=(1,2,3,4,5,6,7,8,9,10,11,12,13)
         EJECT
NEXT     CLOSE FILE
         LA    R4,12(R4)   STEP TO NEXT RTCT ENTRY
         BCT   R5,TEST1   REPEAT PROCESSING FOR NEXT DUMP
         SPACE 3
OUT      EQU   *
         CLI   SW1,X'00'   WERE ANY DUMPS FULL
         BE    ALLEMPTY
         SPACE 3
FINISHED EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         EJECT
BADREC   EQU   *
         WTO   'UNABLE TO INTERPRET DUMP DATA SET',                    X
               ROUTCDE=(1,2,3,4,5,6,7,8,9,10,11,12,13)
         ABEND 1111,DUMP
         SPACE 3
ALLEMPTY WTO   'NO SYS1.DUMP DATA SETS ARE FULL',                      X
               ROUTCDE=(1,2,3,4,5,6,7,8,9,10,11,12,13)
         B     FINISHED
         EJECT
         LTORG
SAVE     DS    18F
ECB1     DC    F'0'
SW1      DC    X'00'
ZERO40   DC    40X'00'
PARMLIST DC    A(TIMEZONE)
         DC    A(CLOCK)
         DC    A(ANSWER)
TIMEZONE DC    F'0'
         DC    2F'0'
CLOCK    DC    D'0'
         DC    2F'0'
ANSWER   DC    30X'00'
         DC    2F'0'
         EJECT
CHPGM    CCW   X'31',IOBSEEK+3,X'40',5
         CCW   X'08',CHPGM,X'40',5
         CCW   X'06',RIA,X'20',5000
         DS    0D
         SPACE 3
MTCIOB   EQU   *
IOBFLAG1 DC    X'00'
IOBFLAG2 DC    X'00'
IOBSENS0 DC    X'00'
IOBSENS1 DC    X'00'
IOBECBCC DC    X'00'
IOBECBPT DC    3X'00'
IOBFLAG3 DC    X'00'
IOBCSW   DC    7X'00'
IOBSIOCC DC    X'00'
IOBSTART DC    3X'00'
         DC    X'00'
IOBDCBPT DC    3X'00'
IOBRESTR DC    4X'00'
IOBINCAM DC    2X'00'
         DC    2X'00'
IOBSEEK  DC    8X'00'   MBBCCHHR
         DC    2F'0'
         EJECT
FILE     DCB   DDNAME=IN,DSORG=PS,IOBAD=MTCIOB,MACRF=(E)
         EJECT
         SPACE 3
RIA      DC    5000C'X'
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
