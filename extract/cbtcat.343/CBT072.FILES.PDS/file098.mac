*          DATA SET ANBMONS    AT LEVEL 001 AS OF 04/03/76
         TITLE 'MONS - SNAPSHOT MONITOR'
MONS     START
         B     ENTRY-MONS(15)
         DC    X'06',CL7'MONS'
ENTRY    EQU   *
*
*   ROUTINE SNAPS OUT FIELDS FROM ASCB,OUCB,PFTE
*   LINKAGE IS MONS(INPUT,MONRT,TIME)
*
*   OUTPUT IS OF THE FORM
*   MONS,LENGTH,TIME,JOBNAME,ASID,SNAPSHOT
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW15      EQU   15
*
         STM   14,12,12(13)
         BALR  RB2,0
         USING *,RB2
         LR    RW3,13
         ST    13,SAVE+4
         LA    13,SAVE
         ST    13,8(RW3)
*
*
         LM    RW3,RW5,0(1)            LOAD PARAMETERS
         L     RW4,0(RW4)              LOAD ADRESS OF MONRT
         ST    RW4,ADRT
         L     RW5,0(RW5)              LOAD TIME
         ST    RW5,TIME                STORE TIME
*
*   CHAIN THROUGH CONTROL BLOCKS
*
         L     RW5,76                  CVT POINTER
         ST    RW5,ADCVT
         L     RW5,556(RW5)            ASVT POINTER
         LA    RW5,528(RW5)            FIRST ASCB POINTER
AGAIN    EQU   *
         L     RW6,0(RW5)              ASCB ADDRESS
         LA    RW6,0(RW6)              REMOVE TOP BYTE
         LTR   RW6,RW6                 END OF ASVT Q
         BZ    RETURN
         TM    0(RW5),X'80'            ASCB IN USE?
         BO    INCR
         CLC   ASCB(4),0(RW6)          ASCB?
         BNE   RETURN
*
*   SNAP OPTION  MOVE DATA FROM ASCB,OUCB
*
         L     RW7,172(RW6)            JBNT POINTER
         LTR   RW7,RW7                 INITIATED PROGRAM?
         BNZ   NAMOK
         L     RW7,176(RW6)
         LTR   RW7,RW7                 START/MOUNT/LOGON
         BNZ   NAMOK
         LA    RW7,NULL                MOVE IN BLANKS
NAMOK    EQU   *
         MVC   JOBNAME(8),0(RW7)       MOVE JOBNAME
         LH    RW7,36(RW6)             ASID
         ST    RW7,ASID
         SR    RW3,RW3
         STH   RW3,ASCBDP              ZERO STORES
         STH   RW3,OUCBNPG
         ST    RW3,OUCBWMS
         MVC   ASCBSEQN(2),38(RW6)     NO IN DISP Q
         MVC   ASCBDP+1(1),43(RW6)     DISP PRTY
         MVC   ASCBEJST(8),64(RW6)     ELAPSED JOB TIME
         MVC   ASCBJSTL(4),80(RW6)     TOTAL JOB TIME
         MVC   ASCBFLAG(2),102(RW6)    FLAGS
         MVC   ASCBSRBT(8),200(RW6)    SRB TIME
         MVC   ASCBIOSM(2),40(RW6)      I/O REQUEST COUNT
         MVC   ASCBVSC(2),120(RW6)     VAM SLOT COUNT
         MVC   ASCBNVSC(2),122(RW6)    NON-VAM COUNT
*
         L     RW7,144(RW6)            OUCB
         MVC   OUCBNPG+1(1),24(RW7)
         MVC   OUCBWMR(2),72(RW7)
         MVC   OUCBRMR(4),84(RW7)
         MVC   OUCBWSS(2),58(RW7)
         MVC   OUCBPSO(2),56(RW7)
         MVC   OUCBFLAG(1),18(RW7)     TYPE OF JOB
         MVC   OUCBFLAG+1(1),21(RW7)   TYPE OF WAIT
         MVC   OUCBSWC(2),30(RW7)      TRANSACTION SWAP COUNT
         MVC   OUCBFLG2(2),16(RW7)     SWAP FLAGS
         MVC   OUCBFLG3(2),19(RW7)     ALGORITHM/TRANSACTION FLAGS
         MVC   OUCBFLG4(2),22(RW7)     # ENQHOLDS,DONTSWAPS
         MVC   OUCBFLG5(2),74(RW7)     CPU PROFILE
         MVC   OUCBFLG6(2),76(RW7)     I/O PROFILE
         MVC   OUCBFLG7(2),78(RW7)     PAGE PROFILE
         MVC   OUCBWMS(4),44(RW7)           INTERVAL SERVICE
         L     RW4,172(RW6)                 BATCH JOBNAME POINTER
         LTR   RW4,RW4                      ZERO?
         BZ    NOTBATCH
         NI    OUCBFLAG,X'BF'               REMOVE START BIT
NOTBATCH EQU   *
         L     RW4,ADCVT
         L     RW4,356(RW4)            PVT POINTER
         L     RW8,12(RW4)             PFT POINTER
         LR    RW9,RW8
         AH    RW8,16(RW4)             FIRST ACTIVE PFTE
         AH    RW9,18(RW4)             LAST ACTIVE PFTE
         SR    RW3,RW3
         STH    RW3,ALLOC
         STH   RW3,LSQA                ZERO COUNTERS
         STH   RW3,LOCAL
         STH   RW3,FIXED
         ST    RW3,VIO
LOOP     EQU   *
         CR    RW8,RW9
         BH    PFTEND
         LH    RW4,0(RW8)
         C     RW4,ASID
         BNE   NOTASID
         TM    13(RW8),X'10'           VIO?
         BNO   NOTVIO
         L     RW3,VIO
         LA    RW3,1(RW3)              INCREMENT VIO COUNT
         ST    RW3,VIO
         B     NOTASID
NOTVIO   EQU   *
         LH    RW3,ALLOC
         LA    RW3,1(RW3)              INCREMENT ALLOC COUNT
         STH   RW3,ALLOC
         TM    12(RW8),X'20'           LSQA?
         BNO   NOTSQA
         LH    RW3,LSQA
         LA    RW3,1(RW3)              INCREMENT LSQA
         STH   RW3,LSQA
         B     NOTASID
NOTSQA   EQU   *
         LH    RW3,LOCAL
         LA    RW3,1(RW3)              INCREMENT LOCAL
         STH   RW3,LOCAL
         TM    13(RW8),X'20'           V=R
         BNO   NOTASID
         LH    RW3,FIXED
         LA    RW3,1(RW3)              INCREMENT FIXED
         STH   RW3,FIXED
NOTASID  EQU   *
         LA    RW8,16(RW8)             INCREMENT PFTE POINTER
         B     LOOP
*
*   WRITE OUT RECORD FOR ONE ASID
*
PFTEND   EQU   *
         LA    RW1,ADARGS
         L     RW15,ADRT
         BALR  14,15
*
*
INCR     EQU   *
         LA    RW5,4(RW5)              INCREMENT ASCB POINTER
         B     AGAIN
*
RETURN   EQU   *
         L     13,4(13)
         LM    14,12,12(13)
         BR    14
*
*   STORAGE AND CONSTANTS
*
LENGTH   DC    F'23'
NAME     DC    CL8'MONS'
IFLAG    DC    F'0'
*
ADARGS   DC    A(NAME)
         DC    A(LENGTH)
         DC    A(TIME)
         DC    A(IFLAG)
*
*
         DS    0D
TIME     DS    F
JOBNAME  DC    C'        '
ASID     DS    F
ASCBEJST DS    D
ASCBSEQN DS    H
ASCBDP   DS    H
ASCBJSTL DS    F
ASCBSRBT DS    D
ASCBFLAG DS    H
ALLOC    DS    H
OUCBNPG  DS    H
OUCBWMR  DS    H
OUCBRMR  DS    F
OUCBWMS  DS    F
OUCBWSS  DS    H
OUCBPSO  DS    H
OUCBSWC  DS    H
OUCBFLG2 DS    H
OUCBFLAG DS    H
ASCBIOSM DS    H
ASCBVSC  DS    H
ASCBNVSC DS    H
OUCBFLG3 DS    H
OUCBFLG4 DS    H
OUCBFLG5 DS    H
LSQA     DS    H
OUCBFLG6 DS    H
LOCAL    DS    H
OUCBFLG7 DS    H
FIXED    DS    H
VIO      DS    F
*
ADCVT    DS    F
ADRT     DS    F
ASCB     DC    C'ASCB'
NULL     DC    C'        '
SAVE     DS    18F
*
         LTORG
         END
