*          DATA SET ANBSUBTSO  AT LEVEL 003 AS OF 30/06/76
SUBMAN   CSECT
         B     ENTRY-SUBMAN(15)
         DC    X'07',CL7'SUBMAN'
ENTRY    EQU   *
*
**  MAIN DISPLAY ROUTINE FOR JES2 SUBTASK
**  ROUTINE PERFORMS I/O FOR DISPLAY ROUTINES
**  AND SUPPLIES THEM WITH THE ADDRESSES OF
**  ROUTINES TO EXTRACT DATA FROM THE OPTION
**  BUFFERS SUPPLIED TO THEM
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW15     EQU   15
*
         STM   14,12,12(13)              SAVE REGISTERS
         BALR  RB2,0
         USING *,RB2
BASE     EQU   *
         ST    13,SAVE+4
         LR    RW3,13
         LA    13,SAVE
         ST    13,8(RW3)                 CHAIN SAVE AREAS
*
**  SET UP DEFAULTS :
**  DISPLAY=SNAPSHOT,INTERVAL=10
**  REGISTER USAGE IS :
**  RW3  DISPLAY NO * 4
*
         SR    RW3,RW3                   SET DEFAULT DISPLAY
NXTDSP   EQU   *
*
**  CLEAR SCREEN AREA TO BLANKS
*
         LM    RW4,RW7,CLEAR
         MVCL  RW4,RW6
*
**  CALL CURRENT DISPLAY
*
         LA    RW1,PARMLIST              POINT AT PARAM LIST
         L     RW15,=V(COMMON)          PARAM AREA
         L     RW15,8(RW15)             ROUTINE ADDRESSES
         AR    RW15,RW3                 ADD OFFSET
         L     RW15,0(RW15)             PICK UP ADDRESS
         BALR  14,15
*
**  WRITE OUT SCREEN FULL
*
         TPUT  SCREEN,SCRNLEN,FULLSCR
*
**  ISSUE READ AND STIMER
*
         TGET  BUFFER,BUFLEN
         TR    BUFFER(86),TRTABLE          TRANSLATE TO EBCDIC
*
**  LOOK FOR PARAMETERS
*
         MVI   DATA,X'FF'         SET UP READ FLAG
         MVI   ERASE,X'FF'              SET TO ERASE SCREEN
*
**  READ COMPLETED - LOOK FOR PARAMETERS
**  DISPLAY,D,INTERVAL,I
*
         MVC   VALUE(8),NULL          BLANK OUT VALUE
*
**  LOOK FOR DISPLAY=
*
         LA    RW5,8                  KEYLENGTH 1
         ST    RW5,KEYLEN
         LA    RW5,KEY1
         ST    RW5,KEYAD              STORE KEY ADDRESS
         LA    RW1,FNDLST             POINT AT PARAM LIST
         L     RW15,=V(FNDTXT)
         BALR  14,15                  LINK TO ROUTINE
*
**  LOOK FOR D=
*
         LA    RW5,2                  KEYLENGTH 2
         ST    RW5,KEYLEN
         LA    RW5,KEY2
         ST    RW5,KEYAD              STORE KEY ADDRESS
         BALR  14,15                  LINK TO ROUTINE
*
**  CHECK IF D=STOP
*
         CLC   VALUE(4),=C'STOP'
         BE    RETURN
         CLC   VALUE(8),NULL          CHANGED?
         BE    NODISP                 NO
*
**  LOOK FOR MATCHING DISPLAY NUMBER
*
         L     RW5,=V(COMMON)         PARAM AREA
         L     RW6,12(RW5)          ---> TITLES
         L     RW7,4(RW5)           ---> OFFSETS
         L     RW5,0(RW5)           NO OF ENTRIES
NXTENT   EQU   *
         CLC   VALUE(8),0(RW6)        CORRECT ENTRY?
         BE    NEWDIS
         LA    RW6,8(RW6)             INCREMENT
         LA    RW7,2(RW7)
         BCT   RW5,NXTENT             DECREMENT
         B     NODISP                 NO MATCH - IGNORE
NEWDIS   EQU   *
         LH    RW7,0(RW7)             PICK UP DISPLAY NO
         LTR   RW7,RW7                VALID?
         BM    NODISP                 NO - OGNORE
         LR    RW3,RW7                SET NEW DISPLAY
NODISP   EQU   *
*
**  LOOK FOR I=
*
         XC    VALUE(4),VALUE         ZERO VALUE
         LA    RW5,KEY4
         ST    RW5,KEYAD              STORE KEY ADDRESS
         LA    RW1,FNDLST             POINT AT PARAM LIST
         L     RW15,=V(FNDVAL)        ROUTINE ADDRESS
         BALR  14,15                  LINK TO ROUTINE
*
**  LOOK FOR OMTERVAL=
*
         LA    RW5,9                  KEYLENGTH 3
         ST    RW5,KEYLEN
         LA    RW5,KEY3
         ST    RW5,KEYAD              STORE KEY ADDRESS
         BALR  14,15                  LINK TO ROUTINE
         L     RW5,VALUE
         LTR   RW5,RW5                VALID VALUE?
         BNP   NOINT                  NO - IGNORE
         C     RW5,=F'60'             GREATER THA MAX?
         BH    NOINT                  YES - IGNORE
         MH    RW5,=H'100'
         ST    RW5,INTVAL             STORE NEW INTERVAL
NOINT    EQU   *
         B     NXTDSP
*
**  ERROR - INCORRECT DCB FOR OPEN
*
ERROR    EQU   *
         ABEND 101,DUMP               ***** ERROR *****
RETURN   EQU   *
         L     13,4(13)
         LM    14,12,12(13)
         BR    14                     EXIT
*
**  CONSTANTS AND VARIABLES
*
         LTORG
SAVE     DS    18F
PARMLIST DC    A(SCRNDAT)
         DC    A(DATA)
         DC    A(LEN)
ADBUF    DC    A(BUFFER)
DATA     DC    X'00'
LEN      DC    F'86'
RLN      EQU   1
ERASE    DC    X'FF'
DISP     DS    F
BUFLEN   EQU   86
BUFFER   DC    86C' '
CLEAR    DC    A(SCRNDAT)
         DC    F'1680'
         DC    F'0'
         DC    X'40',AL3(0)
SCREEN   DC    X'1D60114040'
SCRNDAT  DC    1680C' '
         DC    C'ENTER SUB-TASK COMMANDS :'
         DC    X'1D40115B6013'
SCRNLEN  EQU   *-SCREEN
KEY1     DC    CL8'DISPLAY='
KEY2     DC    CL8'D='
KEY3     DC    CL12'INTERVAL='
KEY4     DC    CL8'I='
FNDLST   DC    A(LEN)
ADBUF2   DC    A(BUFFER)
         DC    A(KEYLEN)
KEYAD    DS    F
         DC    A(THERE)
         DC    A(VALUE)
KEYLEN   DS    F
THERE    DS    F
VALUE    DS    2F
NULL     DC    CL8' '
INTVAL   DC    F'1000'
*
TRTABLE  DC    75C',',C'.',50C',',C'=,,'
         DC    C'ABCDEFGHI',7C','
         DC    C'JKLMNOPQR',8C','
         DC    C'STUVWXYZ',6C','
         DC    C'0123456789',7C','
         DC    C'ABCDEFGHI',7C','
         DC    C'JKLMNOPQR',8C','
         DC    C'STUVWXYZ',6C','
         DC    C'0123456789',6C','
         END
