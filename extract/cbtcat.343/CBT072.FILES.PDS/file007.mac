*          DATA SET CBT202     AT LEVEL 003 AS OF 08/08/75
*          DATA SET HNB050     AT LEVEL 010 AS OF 05/10/75
*          DATA SET HNB050     AT LEVEL 009 AS OF 01/26/75
*          DATA SET HNB050     AT LEVEL 008 AS OF 01/19/75
*          DATA SET HNB050     AT LEVEL 007 AS OF 01/10/75
*          DATA SET HNB050     AT LEVEL 005 AS OF 01/02/75
*          DATA SET HNB050     AT LEVEL 004 AS OF 01/02/75
*          DATA SET HNB050     AT LEVEL 003 AS OF 12/31/74
*          DATA SET HNB050     AT LEVEL 002 AS OF 12/31/74
*          DATA SET HNB050     AT LEVEL 001 AS OF 12/30/74
         MACRO                                                AXC
         MVSIEFYS &MSGADDR,&MSGLEN                            AXC
         CNOP  0,4                                            AXC
         MVC   36(4,12),&MSGADDR    MOVE MESSAGE ADDRESS AND  AXC
         MVC   42(2,12),&MSGLEN     LENGTH TO SYSTEM TABLE    AXC
         L     15,=V(IEFYS)         BRANCH AND LINK TO MSG    AXC
         BALR  14,15                ROUTINE                   AXC
         MEND                                                 AXC
IEFACTRT CSECT                                                      *
R0 EQU 0                                                            *
R1 EQU 1                                                            *
R2 EQU 2                                                            *
R3 EQU 3                                                            *
R4 EQU 4                                                            *
R5 EQU 5                                                            *
R6 EQU 6                                                            *
R7 EQU 7                                                            *
R8 EQU 8                                                            *
R9 EQU 9                                                            *
R10 EQU 10                                                          *
R11 EQU 11                                                          *
R12 EQU 12                                                          *
R13 EQU 13                                                          *
R14 EQU 14                                                          *
R15 EQU 15                                                          *
         SPACE 2
          TITLE 'INITIALIZATION'
*        R2    PROGRAM BASE                                         *
*        R3    POINTS TO EXIT PARAMETER LIST
*        R4    BASE FOR SMF RECORD                                  *
*        R12   NOT USED ( LINKAGE AS ENTRY FOR IEFYS)               *
*        R13   SAVE AREA POINTER AND POINTER TO 45F FOR IEFYS       *
         EJECT                                                      *
         USING IEFACTRT,R15                                         *
         STM   R14,R12,12(R13)                                      *
         LR    R2,R15                                               *
         DROP  R15                                                  *
         USING IEFACTRT,R2                                          *
         LR    R3,R1              SAVE POINTER                      *
         LR    R10,R0             SAVE CODE                         *
         L     R4,EXTRDW(R3)      POINT TO SMF RECORD               S
         LR    R11,R13            SAVE SAVE POINTER                 *
         GETMAIN   R,LV=GOTEND-GOTTEN                               *
         LR    R13,R1             POINT TO GETMAIN'D AREA           *
         ST    R11,4(R13)         THE WAY BACK                      *
         LR    R11,R13                                              *
         USING GOTTEN,R11                                           *
         MVI   MSG1,C' '          CLEAR OUTPUT AREA                 *
         MVC   MSG1+1(MSGS-1),MSG1                                  *
         LA    R9,MSG                                               *
         ST    R9,MSGADDR          SAVE MSG ADDR
         SR    R9,R9                                                *
         LA    R9,96               SAVE LENGTH OF MSG
         STH   R9,MSGLEN                                            *
         MVI   BITS,X'00'          CLEAR STATUS
         SRA   R10,4              CHECK TERMINATION TYPE            *
         BNZ   JOBTRM              VALUE IS 16
         TITLE 'STEP TERMINATION MESSAGE FORMATTING ROUTINE'
         STM   R7,R9,FORYS        SAVE 7-9                          S
         SPACE 2
         MVC   WK1,SMF4SIT(R4)    JOB/STEP INIT TIME                S
         BAL   R7,TIMECVT                                           S
         MVC   M1SIT(8),TIME                                        S
           SPACE 2
         MVC   WK1,SMF4TME(R4)    JOB/STEP TERM TIME                S
         BAL   R7,TIMECVT                                           S
         MVC   M1SET(8),TIME                                        S
           SPACE 2
         LM    R7,R9,FORYS        RESTORE 7-9                       S
         SPACE 2
         L     R7,SMF4TME(R4)     TIME ENDED                        S
         L     R8,SMF4SIT(R4)     TIME STARTED                      S
         CR    R8,R7              PAST MIDNIGHT ??                  S
         BL    NOT12AM            NO..                              S
         A     R8,=F'8640000'     YES..ADD 24 HOURS                 S
NOT12AM  DS    0H                                                   S
         SR    R7,R8              STOP-START                        S
         LA    R7,50(0,R7)        ROUND                             S
         XR    R6,R6              CLEAR WORK                        S
         D     R6,=F'100'         CONVERT TO SECONDS                S
         CVD   R7,PAKT                                              S
         MVC   M1ELPS-1(6),=XL6'402020202120'                       S
         ED    M1ELPS-1(6),PAKT+5      ELAPSED TIME                 S
          SPACE 2
         L     R6,EXTSTPTT(R3)                                      S
         SR    R7,R7                                                S
         ICM   R7,7,0(R6)                                           S
         CVD   R7,PAKT                                              S
         MVC   M1TCB-1(9),=XL9'4020202020214B2020'                  S
         ED    M1TCB-1(9),PAKT+4  TCB (STEP CPU) TIME               S
         SPACE 2
         LA    R6,SMF4SRB(R4)                                       S
         SR    R7,R7                                                S
         ICM   R7,7,0(R6)                                           S
         CVD   R7,PAKT                                              S
         MVC   M1SRB-1(9),=XL9'4020202020214B2020'                  S
         ED    M1SRB-1(9),PAKT+4  SRB (STEP) TIME                   S
         SPACE 2
         MVC   M1JN(8),SMF4JN(R4) JOBNAME                           S
         MVC   M1SN(8),SMF4SN(R4) STEP NAME                         S
         MVC   M1PN(8),SMF4PN(R4) PROGRAM NAME                      S
         SPACE 2
         LH    R6,SMF4RLCT(R4)                                      S
         LA    R6,4(R6,R4)        A(RELOCATE SECTION)               S
         L     R6,SMF4NSW(R6)     R6= SWAP OUT COUNT                S
         C     R6,=F'9999999'                                       S
         BH    SWAPOFLO           TOO MUCH TO PRINT                 S
         CVD   R6,PAKT                                              S
         MVC   WORK10(10),=XL10'40202020202020202120'               S
         ED    WORK10(10),PAKT+3                                    S
         MVC   M1SWAPS(8),WORK10+2                                  S
         B     NOOFLO                                               S
         SPACE 2
SWAPOFLO MVC   M1SWAPS(8),=8C'*'                                    S
         SPACE 2
NOOFLO   L     R7,EXTPRM(R3)                                        S
         L     R8,EXTUSER(R7)                                       S
         AR    R8,R6                                                S
         ST    R8,EXTUSER(R7)                                       S
         SR    R6,R6                                                S
         LH    R6,SMF4RLCT(R4)                                      S
         LA    R5,4(R6,R4)        A(RELOCATE SECTION)               S
*GIBBONS LA    R5,4(R6,R4)        A(RELOCATE SECTION)               S
*ORISIT  LA    R5,0(R6,R4)             A(RELOCATE SECTION)          S
         L     R7,SMF4PIN(R5)     PAGE INS                          S
         CVD   R7,PAKT                                              S
         MVC   M2PI(8),=XL8'4020202020202120'                       S
         ED    M2PI(8),PAKT+4                                       S
         SPACE 2
         L     R7,SMF4POUT(R5)    PAGE OUTS                         S
         CVD   R7,PAKT                                              S
         MVC   M2PO(8),=XL8'4020202020202120'                       S
         ED    M2PO(8),PAKT+4                                       S
         SPACE 2
         L     R7,SMF4PSI(R5)     PAGES SWAPPED IN                  S
         CVD   R7,PAKT                                              S
         MVC   M2SI(8),=XL8'4020202020202120'                       S
         ED    M2SI(8),PAKT+4                                       S
         SPACE 2
         L     R7,SMF4PSO(R5)     PAGES SWAPPED OUT                 S
         CVD   R7,PAKT                                              S
         MVC   M2SO(8),=XL8'4020202020202120'                       S
         ED    M2SO(8),PAKT+4                                       S
         SPACE 2
         L     R7,SMF4VPI(R5)     VIO PAGES IN                      S
         CVD   R7,PAKT                                              S
         MVC   M2VI(8),=XL8'4020202020202120'                       S
         ED    M2VI(8),PAKT+4                                       S
         SPACE 2
         L     R7,SMF4VPO(R5)     VIO PAGES OUT                     S
         CVD   R7,PAKT                                              S
         MVC   M2VO(8),=XL8'4020202020202120'                       S
         ED    M2VO(8),PAKT+4                                       S
         SPACE 2
         XC    WK1(4),WK1         STORAGE USED                      S
         MVC   WK1+2(2),SMF4SYST(R4)                                S
         L     R7,WK1                                               S
         XC    WK1(4),WK1                                           S
         MVC   WK1+2(2),SMF4H0ST(R4)                                S
         L     R8,WK1                                               S
         AR    R7,R8                                                S
         CVD   R7,PAKT                                              S
         MVC   M2SU-1(6),=XL6'402020202120'                         S
         ED    M2SU-1(6),PAKT+5                                     S
         SPACE 2
         MVI   M2SU+5,C'K'                                          S
         MVI   M2SU-1,C'V'                                          S
         TM    SMF4STGT(R4),X'00'                                   S
         BZ    VIRT                                                 S
         MVI   M2SU-1,C'R'                                          S
           SPACE 2
VIRT     DS    0H                                                   S
         XC    WK1(4),WK1                                           S
         MVC   WK1+3(1),SMF4SNO(R4)    STEP NUMBER                  S
         L     R6,WK1                                               S
         CVD   R6,PAKT                                              S
         UNPK  UPAKT(15),PAKT(8)                                    S
         MVZ   UPAKT(15),=15X'FF'                                   S
         MVC   M1SNO(3),UPAKT+12                                    S
           SPACE 2
         UNPK  M1CC(5),SMF4CC(3,R4)    COMPLETION CODE              S
         MVI   M1CC+4,C' '                                          S
         NC    M1CC+1(3),=3X'0F'                                    S
         TR    M1CC+1(3),=C'0123456789ABCDEF'                       S
         MVI   M1CC,C'S'                                            S
           SPACE 2
         TM    SMF4STI(R4),FLUSHED                                  S
         BZ    NOTFLUSH                                             S
         MVC   M1CC(4),=CL4'NXEQ'                                   S
         B     WTOIT                                                S
           SPACE 2
NOTFLUSH DS    0H                                                   S
         TM    SMF4STI(R4),ABENDED                                  S
         BO    BADEND                                               S
         MVI   M1CC,C'R'                                            S
         B     WTOIT                                                S
BADEND   DS    0H                                                   S
         TM    SMF4CC(R4),X'80'                                     S
         MVI   M1CC,C'S'                                            S
         BZ    WTOIT                                                S
         MVI   M1CC,C'U'                                            S
         B     WTOIT
         TITLE 'JOB TERMINATION MESSAGE FORMATTING ROUTINE'
JOBTRM   DS    0H                                                   J
         OI    BITS,X'01'                                           J
         STM   R7,R9,FORYS      SAVE REGS 7-9
         SPACE 2
         MVC   M1JN(8),SMF5JN(R4)      JOB NAME                     J
         MVC   WK1,SMF4SIT(R4)    JOB/STEP INIT TIME                J
         BAL   R7,TIMECVT                                           J
         MVC   M2SIT(8),TIME                                        J
          SPACE 2
         MVC   WK1,SMF4TME(R4)    JOB/STEP TERM TIME                J
         BAL   R7,TIMECVT                                           J
         MVC   M2SET(8),TIME                                        J
          SPACE 2
         LM    R7,R9,FORYS        RESTORE 7-9                       J
         SPACE 2
         L     R7,SMF4TME(R4)     TIME ENDED                        J
         L     R8,SMF4SIT(R4)     TIME STARTED                      J
         CR    R8,R7              PAST MIDNIGHT ??                  J
         BL    NOT12AM1           NO..                              J
         A     R8,=F'8640000'     YES..ADD 24 HOURS                 J
NOT12AM1 DS    0H                                                   J
         SR    R7,R8              STOP-START                        J
         LA    R7,50(0,R7)        ROUND                             J
         XR    R6,R6              CLEAR WORK                        J
         D     R6,=F'100'         CONVERT TO SECONDS                J
         CVD   R7,PAKT                                              J
         MVC   M2ELPS-1(6),=XL6'402020202120'                       J
         ED    M2ELPS-1(6),PAKT+5      ELAPSED TIME                 J
         SPACE 2
         L     R6,SMF5JT(R3)      TCB TIME                          J
         SR    R7,R7                                                J
         ICM   R7,7,0(R6)                                           J
         CVD   R7,PAKT                                              J
         MVC   M2TCB-1(9),=XL9'4020202020214B2020'                  J
         ED    M2TCB-1(9),PAKT+4                                    J
         SPACE 2
         SPACE 2
         LA    R6,SMF5SRB(R4)     SRB TIME                          J
         SR    R7,R7                                                J
         ICM   R7,7,0(R6)                                           J
         CVD   R7,PAKT                                              J
         MVC   M2SRB-1(9),=XL9'4020202020214B2020'                  J
         ED    M2SRB-1(9),PAKT+4                                    J
         SPACE 2
         L     R7,SMF5TTAT(R4)    TOTAL TRANS. ACTIVE TIME          J
         SLL   R7,10                                                J
         SR    R6,R6                                                J
         D     R6,=F'1000'                                          J
         CVD   R7,PAKT                                              J
         MVC   M2TTAT(8),=XL8'4020202020202120'                     J
         ED    M2TTAT(8),PAKT+4                                     J
         SPACE 2
         MVI   M2SWAPS-1,C' '          CLEAR SMALL AMOUNT           J
         L     R7,EXTPRM(R3)      TOTAL SWAPS                       J
         L     R6,EXTUSER(R7)                                       J
         C     R6,=F'9999999'     TOO MUCH TO PRINT ??              J
         BH    SOFLO                                                J
         CVD   R6,PAKT                                              J
         MVC   WORK10(10),=XL10'40202020202020202120'               J
         ED    WORK10(10),PAKT+3                                    J
         MVC   M2SWAPS(8),WORK10+2                                  J
         B     SOFLO1                                               J
         SPACE 2
SOFLO    MVC   M2SWAPS(8),=8C'*'                                    J
         SPACE 2
SOFLO1   L     R6,SMF5SVUN(R4)    SERVICE UNITS                     J
         CVD   R6,PAKT                                              J
         MVC   M2SVUN(8),=XL8'4020202020202120'                     J
         ED    M2SVUN(8),PAKT+4                                     J
         SPACE 2
         XC    WK1(4),WK1         PERF GROUP NUMBER                 J
         MVC   WK1+2(2),SMF5PGNO(R4)                                J
         L     R7,WK1                                               J
         LA    R7,0(,R7)                                            J
         CVD   R7,PAKT                                              J
         MVC   M2PGNO-1(4),=XL4'40202120'                           J
         ED    M2PGNO-1(4),PAKT+6                                   J
         SPACE 2
         B     WTOIT                                                J
         TITLE 'RETURN PROCESSING'
RETURN   DS    0H                                                   *
         L     R11,4(R13)                                           *
         LR    R5,R13                                               *
         FREEMAIN  R,LV=GOTEND-GOTTEN,A=(5)                         *
         LR    R13,R11                                              *
         LM    R14,R12,12(R13)                                      *
         LA    R15,0(0,0)                                           *
         BR    R14                                                  *
         TITLE 'WTO PROCESSING'
WTOIT    DS    0H
         LA    R1,279                  DSS FLAG ADDRESS IN PSA      *
         TM    0(R1),X'FF'             IF ON DON'T PRINT ON CONS    *
         BO    NOWTO                   IT'S ON .. DON'T PRINT       *
         LA    R1,WTOAREA                                           *
         MVC   WTOAREA(4),=XL4'00648000'                            *
         MVC   ENDWTO-4(4),=XL4'00004020'   ROUTCDE=(2,11)          *
         MVC   WTOAREA+4(96),=CL96' '    BLANK AREA
         MVC   WTOAREA+4(32),MSG1                                   *
         MVC   WTOAREA+38(12),=CL12'------------'
         MVC   WTOAREA+51(4),M1CC
         TM    BITS,X'01'     IS THIS JOB TERM
         BZ    NJT            NO
         MVC   WTOAREA+51(4),=CL4' EOJ'
NJT      DS    0H
         SVC   35                                                   *
         TITLE 'SYSOUT PROCESSING'
NOWTO    MVC   MSG(96),MSG1                                         *
         LA    R13,FORYS           POINT TO 45F AREA
         MVSIEFYS    MSGADDR,MSGLEN                                   *
         LR    R13,R11                                              *
         TM    BITS,X'01'         IS THIS A ENRTY FROM JOBTERM ?
         BO    RETURN             YES..DON'T DO MSG2                *
          SPACE 2
         MVC   MSG(96),MSG2
         LA    R13,FORYS           POINT TO 45F AREA
         MVSIEFYS    MSGADDR,MSGLEN                                   *
         LR    R13,R11                                              *
         B     RETURN
         TITLE 'TIME CONVERSION ROUTINE'
TIMECVT  DS    0H
         L     R9,WK1                                               *
         SR    R8,R8                                                *
         D     R8,=F'100'         TO SECONDS                        *
         SR    R8,R8                                                *
         D     R8,=F'3600'        HOURS                             *
         CVD   R9,PAKT                                              *
         UNPK  HOUR,PAKT                                            *
         LR    R9,R8                                                *
         SR    R8,R8                                                *
         D     R8,=F'60'          MIN                               *
         CVD   R9,PAKT                                              *
         UNPK  MINUTE,PAKT                                          *
         CVD   R8,PAKT            SEC                               *
         UNPK  SECOND,PAKT                                          *
         OC    TIME(8),=8X'F0'                                      *
         MVI   COL1,C'.'                                            *
         MVI   COL2,C'.'                                            *
         BR    R7                 RETURN                            *
         TITLE 'EQUATES AND WORK AREAS'
FLUSHED  EQU   X'01'                                                *
ABENDED  EQU   X'02'                                                *
SMF4TME  EQU   X'06'         STEP TERM TIME                         *
SMF4JN   EQU   X'12'         JOB NAME                               *
SMF4SNO  EQU   X'2A'         STEP NUMBER                            *
SMF4SIT  EQU   X'2B'         STEP INIT TIME                         *
SMF4CC   EQU   X'37'         COMPLETION CODE (STEP)                 *
SMF4PN   EQU   X'3A'         PROGRAM NAME                           *
SMF4SN   EQU   X'42'         STEP NAME                              *
SMF4SYST EQU   X'4C',2       SYSTEM AREA STORAGE USED               *
SMF4H0ST EQU   X'4E',2       PRIVATE AREA STORAGE USED              *
SMF4STI  EQU   X'57',1       STEP TERM INDICATOR                    *
*                  00 = VIRT                                        *
*                  01 = REAL                                        *
SMF4SRB  EQU   X'63',3       STEP SRB TIME                          *
SMF4STGT EQU   X'66'         RECORD INDICATORS SMF4RIN              *
SMF4RLCT EQU   X'68'         OFFSET TO RELOCATE SECTION             *
SMF4PIN  EQU   0,4           PAGE-INS                               *
SMF4POUT EQU   4,4           PAGE-OUTS                              *
SMF4NSW  EQU   8,4           NUMBER OF SWAPS                        *
SMF4PSI  EQU   12,4          PAGES SWAPPED IN                       *
SMF4PSO  EQU   16,4          PAGES SWAPPED OUT                      *
SMF4VPI  EQU   20,4          VIO PAGE INS                           *
SMF4VPO  EQU   24,4          VIO PAGE OUTS                          *
         EJECT
SMF5JT   EQU   12,4                                                 *
SMF5TME  EQU   X'06',4       TERM TIME                              *
SMF5JBN  EQU   X'12'         JOB NAME                               *
SMF5JN   EQU   SMF5JBN                                              *
SMF5JIT  EQU   X'2B',4       JOB INIT TIME                          *
SMF5SRBT EQU   X'4D',3       JOB CPU TIME UNDER SRB                 *
SMF5SRB  EQU   SMF5SRBT                                             *
SMF5TJS  EQU   X'50',4       TOTAL JOB SERVICE UNITS                *
SMF5SVUN EQU   SMF5TJS                                              *
SMF5TTAT EQU   X'54',4       TOTAL TRANS. ACTIVE TIME               *
SMF5JCPU EQU   X'75',3       JOB CPU TIME UNDER TCB                 *
SMF5PGNO EQU   X'5C',2       PERF. GROUP NUMBER.                    *
           EJECT
*** DISPLACEMENTS OF ADDRESS PASSED IN REG 1 AT ENTRY
EXTPRM   EQU   0,4                                                  *
EXTJN    EQU   4,4                JOBNAME EBCDIC                    *
EXTPGMR  EQU   8,4                PROGRAMMER NAME                   *
EXTJOBTT EQU   12,4               JOB CPU (TCB) TIME BYTE 4 =#JCARD *CC
EXTJACT  EQU   16,4               JOB ACCT DATA                     *
EXTSTPTT EQU   20,4               STEP CPU (TCB) TIME -BYTE 4= EXEC *CC
EXTSACT  EQU   24,4               STEP ACCT INFO                    *
EXTTFLG1 EQU   28,4               FLAGS                             *
EXTCC    EQU   32,4               COMPLETION CODE                   *
TIME     DS    0CL8                                                 *
HOUR     DS    CL2                                                  *
COL1     DS    CL1                                                  *
MINUTE   DS    CL2                                                  *
COL2     DS    CL1                                                  *
SECOND   DS    CL2                                                  *
BITS     DS    CL1                MUST BE JUST B4 UPAKT             *
UPAKT    DS    CL15                                                 *
         DS    CL2                                                  *
WORK10   DS    CL10                                                 *
MSGADDR  DS    F                  A(MSG)                            *
MSG      DS    CL96               'TEXT FOR SYSOUT'                 *
MSGLEN   DS    H                  MSG LENGTH                        *
WTOAREA  WTO   '*******************************************************C
               *****************************************',          *  C
               ROUTCDE=(2,11),MF=L                                  *
ENDWTO   EQU   *                                                    *
         EJECT                                                      *
MSG1     DS    0CL96                                                *
         DS    CL1                                                  *
M1JN     DS    CL8                JOBNAME                           *
         DS    CL1                                                  *
M2PGNO   EQU   *                  PERFORMANCE GROUP NUMBER
M1SNO    DS    CL3                STEP NUMBER                       *
         DS    CL1                                                  *
M2SVUN   EQU   *                  SERVICE UNITS
M1SN     DS    CL8                STEPNAME                          *
         DS    CL1                                                  *
M2TTAT   EQU   *              TOTAL TRANS ACTIVE TIME
M1PN     DS    CL8                PROGRAM NAME                      *
         DS    CL1                                                  *
M2SIT    EQU   *                                                    *
M1SIT    DS    CL8                STEP INIT                         *
         DS    CL1                                                  *
M2SET    EQU   *                                                    *
M1SET    DS    CL8                STEP TERM                         *
         DS    CL1                                                  *
M2TCB    EQU   *                                                    *
M1TCB    DS    CL8                TCB (STEP) TIME                   *
         DS    CL1                                                  *
M2SRB    EQU   *                                                    *
M1SRB    DS    CL8                SRB TIME (FOR STEP)               *
         DS    CL1                                                  *
M2ELPS   EQU   *
M1ELPS   DS    CL5                ELAPSED TIME                      *
         DS    CL1                                                  *
M2SWAPS  EQU   *                                                    *
M1SWAPS  DS    CL8                SWAPS THIS STEP                   *
         DS    CL1                                                  *
         DS    CL1            REGION REQUEST(REAL OR VIRTUAL)
M2SU     DS    CL5                STORAGE USED                      *
         DS    CL2                                                  *
M1CC     DS    CL4                COMPLETION CODE                   *
         DS    CL1                                                  *
M1END    EQU   *                                                    *
M1LEN    EQU   *-MSG1                                               *
         EJECT
MSG2     DS    0CL96              PAGE INFO FOR SYSOUT              *
         DS    CL11                                                 *
M2PI     DS    CL8                PAGE INS                          *
         DS    CL1                                                  *
M2PO     DS    CL8                PAGE OUTS                         *
         DS    CL1                                                  *
M2SI     DS    CL8                PAGES SWAPPED IN                  *
         DS    CL1                                                  *
M2SO     DS    CL8                PAGES SWAPPED OUT                 *
         DS    CL1                                                  *
M2VI     DS    CL8                VIO PAGES IN                      *
         DS    CL1                                                  *
M2VO     DS    CL8                VIO PAGES OUT                     *
         DS    CL32                                                 *
M2END    EQU   *                                                    *
M2LEN    EQU   *-MSG2                                               *
MSGS     EQU   *-MSG1                                               *
         DS    CL2                                                  *
GOTEND   EQU   *                                                    *
         END
