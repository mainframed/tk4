ASDF     CSECT
         SAVE  (14,12)
         LR    RG3,RG15
         USING ASDF,RG3
         ST    RG13,SAVE+4
         LA    RG13,SAVE
         MODESET KEY=ZERO,MODE=SUP
RG1      EQU   7
RG0      EQU   0
RG2      EQU   2
RG3      EQU   3
RG4      EQU   4
RG5      EQU   5
RG6      EQU   6
RG8      EQU   8
RG9      EQU   9
RG10     EQU   10
RG11     EQU   11
RG12     EQU   12
RG13     EQU   13
RG14     EQU   14
RG15     EQU   15
LNKRG1   EQU   8
BLANK    EQU   X'40'
COMMA    EQU   X'6B'
INQUIRY  EQU   *
         SR    RG4,RG4
         SR    RG2,RG2
         BC    0,WTOR
         MVC   ADR(4),FULLZERO
         MVI   FIND,X'FF'
         MVC   FIND+1(9),FIND
         MVI   STHOLD1,BLANK
         MVC   STHOLD1+1(3),STHOLD1
         MVI   STHOLD2,X'00'
         MVC   STHOLD2+1(7),STHOLD2
         MVI   CONSLOUT+4,BLANK
         MVC   CONSLOUT+5(19),CONSLOUT
         MVI   ANS1,BLANK
         MVC   ANS1+1(49),ANS1
WTOR     EQU   *
         WTOR  'ENTER D,ST,EXP OR END/VIRTUAL CORE ZAP',ANS1,50,ECB1
         WAIT  ECB=ECB1
         MVC   ECB1(4),FULLZERO
         OC    ANS1(50),FIFTYBLK
         CLI   ANS1,C'D'
         BE    DISPLY
         CLC   ANS1(2),=C'ST'
         BE    STORE
         CLC   ANS1(3),=C'END'
         BE    FINISH
         CLC   ANS1(3),=C'EXP'
         BE    EXPLAIN
         B     ERROR
EXPLAIN  EQU   *
         WTO   'VCOREZAP IS USED TO DISPLAY OR STORE VIRTUAL'
         WTO   'STORAGE IN THE COMMON AREA OR IN L.P.A.'
         WTO   'THE COMMAND DXXXXXX WILL DISPLAY 8 HEX BYTES'
         WTO   'STARTING AT THE ADDRESS SPECIFIED.  ALL SIX HEX'
         WTO   'CHARACTERS OF THE ADDRESS MUST BE SPECIFIED.'
         WTO   'THE COMMAND STXXXXXX YYYY WILL STORE UP TO 8 HEX'
         WTO   'BYTES STARTING AT LOCATION XXXXXX. THE BYTES TO BE'
         WTO   'STORED ARE SPECIFIED AS YYYY, WHERE YY MAY RANGE FROM'
         WTO   'ONE THRU EIGHT BYTES OF HEX CHARACTERS.'
         WTO   'VCOREZAP OPERATES ON VIRTUAL STORAGE.  IT DOES NOT'
         WTO   'EXAMINE OR MODIFY ANY DATA ON PAGING DATA SETS.'
         WTO   'SO IT IS POSSIBLE TO MODIFY VIRTUAL STORAGE AND THEN'
         WTO   'HAVE THE ORIGINAL UNMODIFIED PAGE BROUGHT IN LATER'
         WTO   'FROM THE PAGING DATA SET.'
         B     INQUIRY
DISPLY   EQU   *
         MVI   ANS1,BLANK
         LA    RG1,ANS1
         BAL   LNKRG1,SCAN
         MVC   ADR(4),DBUFEND-4
         BAL   RG8,CHECKCSA
         CLC   CSASTART(4),ADR
         BC    0,LOWABEND   CHANGED TO 0 TO ACCESS NUCLEUS
         L     RG9,ADR
         MVC   FIND(8),0(RG9)
         L     RG2,ADCON1
         L     RG5,ADCON2
         UNPK  0(15,RG5),0(8,RG2)
         UNPK  14(15,RG5),7(8,RG2)
         TR    0(29,RG5),TDPTABLE-240
         OC    CONSLOUT+4(20),FIFTYBLK
         LA    1,CONSLOUT
         WTO   MF=(E,(1))
         B     INQUIRY
STORE    EQU   *
         MVI   ANS1+1,BLANK
         LA    RG1,ANS1+1
         BAL   LNKRG1,SCAN
         MVC   ADR(4),DBUFEND-4
         BAL   RG8,CHECKCSA
         CLC   CSASTART(4),ADR
         BC    0,LOWABEND   CHANGED TO 0 TO ACCESS NUCLEUS
         L     RG9,ADR
         L     RG3,=F'8'  COUNTER TO CONTROL MAX NO OF 8BYTES
         LA    RG10,STHOLD2  POINT TO OUTPUT HOLD AREA
         LA    RG11,ANS1+9   POINT TO INPUT DATA
         MVC   STLENGTH(4),FULLZERO
SETSTORE B     CHKRANG0
MVSTH1   MVC   STHOLD1+1(2),0(RG11)
         CLI   1(RG11),BLANK
         BE    ODDABEND
         LA    RG1,STHOLD1
         BAL   LNKRG1,SCAN
         MVC   0(1,RG10),DBUFEND-1
         L     RG8,STLENGTH
         A     RG8,=F'1'
         ST    RG8,STLENGTH
         LA    RG11,2(RG11)
         LA    RG10,1(RG10)
         CLI   0(RG11),BLANK
         BE    DOSTORE   NO MORE INPUT
         BCT   RG3,SETSTORE   CONTROL FOR MAX OF 8BYTES
         B     DOSTORE
DOSTORE  L     RG8,STLENGTH
         BCTR  RG8,0  SUBTRACT 1 FOR MVC LENGTH VALUE
         EX    RG8,EXSTORE  MOVE INTO VIRTUAL STORAGE
         B     INQUIRY
EXSTORE  MVC   0(0,RG9),STHOLD2  LENGTH COMES FROM RG8 IN EXECUTE INST.
ODDABEND EQU   *
         WTO   'ODD NO. OF DIGITS ENTERED/REQUEST IGNORED'
         B     INQUIRY
CHKRANGE CLI   0(RG4),C'A'
         BC    4,BADRANGE
         CLI   0(RG4),C'9'
         BC    2,BADRANGE
         CLI   0(RG4),C'0'
         BC    10,GOODRANG
         CLI   0(RG4),C'F'
         BC    2,BADRANGE
GOODRANG BR    RG3
BADRANGE EQU   *
         WTO   'INVALID HEX DIGITS ENTERED/REQUEST IGNORED'
         B     INQUIRY
CHKRANG0 STM   RG3,RG4,SAVER34
         LR    RG4,RG11
         BAL   RG3,CHKRANGE
         LR    RG4,RG11
         LA    RG4,1(RG4)
         BAL   RG3,CHKRANGE
         LM    RG3,RG4,SAVER34
         B     MVSTH1
         LA    RG1,ANS1+8
         BAL   LNKRG1,SCAN
         MVC   DATA(4),DBUFEND-4
         L     RG10,ADR
         MVC   0(4,RG10),DATA
         B     INQUIRY
ERROR    EQU   *
         WTO   'UNKNOWN COMMAND/REQUEST IGNORED'
         B     INQUIRY
LOWABEND EQU   *
         WTO   'ADDRESS IS BELOW COMMON AREA-REQUEST IGNORED'
         B     INQUIRY
FINISH   L     RG13,SAVE+4
         RETURN (14,12),RC=0
***********************************************************************
* THIS SUBROUTINE DECODE NUMERIC FIELDS ON CONTROL CARDS
*******************
SCAN2    EQU   *
         LA    RG1,1(RG1)
SCAN     CLI   0(RG1),BLANK             IS THIS CHAR BLANK
         BC    8,SCAN2                  YES- ADVANCE
         MVI   TBUF,C'1'               INIT TBUF TO X'F1'        A43687
         MVC   TBUF+1(71),TBUF   FOR TR TABLES ON HEX TEST       A43687
         LA    RG4,TBUF                 INIT STORAGE ADDRESS
SCAN4    LR    RG2,RG1                  START OF DATA FIELD
SCAN1    CLI   0(RG1),COMMA             DELIMITER
         BC    8,SCAN3                  YES-SAVE TEMP
         CLI   0(RG1),BLANK             END OF DATA
         BC    8,SCAN3                  YES-SAVE AND EXIT
         LA    RG1,1(RG1)               NO-INC PTR
         BC    15,SCAN1                 CONTINUE
         SPACE 1
SCAN3    LR    RG5,RG1                  TURN TO WK REG
         SR    RG5,RG2                  GET NO OF CHAR SCANNED
         BZ    SCAN3A                   SKIP MOVE IF ZERO         M2816
         BCTR  RG5,0                    DECR. BY 1
         EX    RG5,SAVEM                STORE IN TEMP BUF
SCAN3A   CLI   0(RG1),BLANK             EXIT ON BLANK             M2816
         BC    8,SCAN5                  YES-RIGHT JUSTIFY
         CLI   1(RG1),BLANK             BLANK FLOOWING COMMA
         BC    8,SCAN5                  YES, DISCARD
         LA    RG1,1(RG1)               INC CDBUF PTR
         LA    RG4,1(RG5,RG4)           ADVANCE TEMP BUFFER
         BC    15,SCAN4                 CONTINUE
         SPACE 1
SCAN5    AR    RG4,RG5                  LAST POSITION TEMP-1
         S     RG4,CON1                 GET NO OF CHAR
         LR    RG2,RG4                  COPY REGISTER
         SRL   RG2,1                    DIVIDE BY 2
         SLL   RG2,1                    MULTIPLY BY 2
         CR    RG4,RG2                  COMPARE WITH ORIGNIAL
         SRL   RG4,1                    DIVIDE BY 2
         MVC   DBUF(72),TBUF           SAVE ORIGINAL HEX INPUT   A43687
         NC    TBUF(72),CON2           NORMALIZE FOR TRANSLATE   A43687
         TR    TBUF(72),TRANTBL        TRANSLATE TO BINARY       A43687
         TR    TBUF(72),TRANTBL2-X'F0'  RETRANSLATE TO HEX       A43687
         CLC   DBUF(72),TBUF           SAME AS ORIGINAL          A43687
         XC    DBUF(72),DBUF           YES-CLEAR BUFFER          A43687
         NC    TBUF(72),CON2            NORMALIZE FOR TRANSLATE
         TR    TBUF(72),TRANTBL         TRANSLATE TO BINARY
         LA    RG2,TBUF                 POINTER TO TEMP START
         LA    RG5,DBUFEND              DATA BUFFER END
         SR    RG5,RG4                  START OF SAVE DATA
         LR    RG6,RG5                  SAVE START ADDR OF DATA
SCAN6    PACK  0(7,RG5),0(13,RG2)       CONVERT TO HEX
         LA    RG5,6(RG5)               INC DATA STG
         LA    RG2,12(RG2)              INC TEMP STG
         CL    RG5,CON3                 END OF DATA BLOCK
         BC    4,SCAN6                  CONTINUE IF NOT AT END
         BCR   15,LNKRG1
         SPACE 1
         SPACE 1
SAVEM    MVC   0(1,RG4),0(RG2)          SAVE DATA IN TBUF
*******************************************************************
*THIS SUBROUTINE IS USED TO CHECK ANY ADDRESSES REQUESTED
* WE WILL NOT ALLOW THE USER TO LOOK AT OR CHANGE ANY ADDRESS
*  THAT IS NOT IN EITHER THE COMMON AREA OR IN L.P.A.
*  THAT IS BECAUSE THOSE ADDRESSES WOULD BE IN THE PRIVATE ADDR. SPACE
* AND WOULD BE OF NO USE TO THE PERSON USING THIS FACILITY
* ADDRESSES IN LOW-CORE ARE MAPPED AS REAL ADDRESSES BY VM/370
*  AND SO THEY CAN BE CHANGED VIA NORMAL VM COMMANDS
CHECKCSA EQU   *
         L     RG9,76
         L     RG9,560(RG9)   A(GDA)
         L     RG9,8(RG9)    A(PQE) FOR CSA
         MVC   CSASTART(4),24(RG9)   A(BEGINNING OF CSA)
         BR    RG8
CSASTART DC    F'0'
*******************************************************************
         SPACE 2
***********************************************************************
FILLER   DC    70F'0'      FILLER FOR TRANSLATE TABEL BEGINNING
CON1     DC    A(TBUF-1)                INIT ADDR OF BUF
CON3     DC    A(DBUFEND)               CONST FOR LOOP CONTL.
CON2     DC    18XL4'1F1F1F1F'          MASK FORTRAN
DBUF     DS    CL36
DBUFEND  DS    0C                       END OF DATA BUFFER
         DS    CL36                     PAD
TBUF     DS    CL72                     TEMP STG OF DATA
         DC    7X'00'                      OVERLAP AREA FROM MOVE
TRANTBL  DC    X'00FAFBFCFDFEFF'
         DC    9X'00'
TRANTBL2 DC    C'0123456789ABCDEF'
ECB1     DC    F'0'
FULLZERO DC    F'0'
FIFTYBLK DC    50X'40'
ANS1     DC    50X'40'
ADR      DC    F'0'
FIND     DC    8X'FF'
DUMMY    DC    25X'FF'
MARKER   DC    X'EEEEEEEE'
DATA     DC    F'0'
STLENGTH DC    F'0'
STHOLD1  DC    4X'40'
*STHOLD1 IS THE AREA TO BE PASSED FOR CONVERSION
STHOLD2  DC    8X'00'
*STHOLD2 IS THE AREA WHERE HEX DATA IS HELD TEMP. AFTER CONVERSION
SAVER34  DC    D'0'
ADCON1   DC    A(FIND)
ADCON2   DC    A(CONSLOUT+4)
ADCON3   DC    A(CONSLOUT)
CONSLOUT DC    AL2(20)
         DC    X'0000'
         DC    20X'40'
FILLER1  DC    70F'0'
TDPTABLE DC    C'0123456789ABCDEF'
         LTORG
SAVE     DS    18F
         END
