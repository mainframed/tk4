*          DATA SET CBT395     AT LEVEL 001 AS OF 05/05/76
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*                                                                     *
*                             OPRINT                                  *
*                          ------------                               *
*                                                                     *
*                                                                     *
*    FUNCTION -      TO PRINT DATASETS GENERATING PAGE EJECTS WHEN    *
*                    AN   #$#   IS ENCOUNTERED.   RECORD LENGTH IS    *
*                    CONTROLED BY PARMS AS IS NUMBER OF COPIES.       *
*                                                                     *
*    INPUT -         DDNAME=INPUT,  DATASET TO BE PRINTED.            *
*                                                                     *
*    OUTPUT -        DDNAME=OUTPUT,  SYSOUT DATA SET.                 *
*                                                                     *
*    PARMS -         'CCLLL'                                          *
*                       CC  -     THE NUMBER OF COPIES DESIRED.       *
*                       LLL  -    THE OUTPUT RECORD LENGTH FOR DATA   *
*                                 MOVING ONLY.  DCB LRECL IS 133.     *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
OPRINT   CSECT
         SAVE  (14,12)
         LR    R3,R15              BASE REG
         USING OPRINT,R3
         ST    R13,SAVE+4          SAVE AREA
         LA    R13,SAVE               CONVENTIONS
         L     R1,0(0,R1)          PARM ADDR
         MVC   COPIES(2),2(R1)
         MVC   RECLEN(3),4(R1)     SAVE INPUT RECORD LENGTH
         PACK  DOUBLE(8),COPIES(2)
         CVB   R4,DOUBLE           NO. OF COPIES
         PACK  DOUBLE(8),RECLEN(3)
         CVB   R5,DOUBLE           GET LRECL
         CH    R5,=H'132'          MAX LRECL
         BH    ABEND               OVER MAX, ABEND
         BCTR  R5,0                SET MOVE LENGTH
         OPEN  (OUTPUT,(OUTPUT))
OPEN     OPEN  (INPUT)
GET      GET   INPUT,WORK          GET A RECORD
         CLC   WORK(3),=C'#$#'     SKIP TO CHANNEL 1 IND
         BE    PUTSKIP             YES,GO SKIP
         EX    R5,PRTMOVE          MOVE INPUT TO OUTPUT
         PUT   OUTPUT,PRINT        PUT A RECORD
         B     GET                 GET NEXT RECORD
PUTSKIP  PUT   OUTPUT,PRINT-1      SKIP TO CHANNEL 1
         B     GET                 GET NEXT RECORD
PRTMOVE  MVC   PRINT+1(1),WORK     EXECUTED MOVE
ABEND    ABEND 1
EOJ      CLOSE (INPUT)
         BCT   R4,OPEN
         CLOSE (OUTPUT)
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         LTORG
SAVE     DS    18F
         DS    0D
DOUBLE   DC    2F'0'
RECLEN   DS    CL3
COPIES   DS    CL2
WORK     DS    CL132
         DC    X'8B'
PRINT    DC    X'09',CL132' '
         EJECT
INPUT    DCB   DDNAME=INPUT,DSORG=PS,RECFM=FB,MACRF=(GM),EODAD=EOJ
OUTPUT   DCB   DDNAME=OUTPUT,DSORG=PS,RECFM=FBM,LRECL=133,BLKSIZE=1330,X
               MACRF=(PM)
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
