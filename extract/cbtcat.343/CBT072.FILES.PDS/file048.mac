*          DATA SET CBT500     AT LEVEL 002 AS OF 12/16/75
         TITLE 'LISTVOL - DOCUMENTATION'
*.....................................................................*
*.                                                                   .*
*.   LISTVOL                                                         .*
*.                                                                   .*
*        LISTVOL   LEVEL('DSNAME')×DA('DSLIST')                       *
*                  VOL('SER')                                         *
*        'DSNAME' IS EITHER HIGHER LEVEL QUALIFIERS OF CATALOG        *
*                 STRUCTURE OR CONTAINS EMBEDDED ASTERISK AS          *
*                 PART OF THE NAME.                                   *
*        'DSLIST' IS LIST OF DATASETS.                                *
*        'SER'    IS LEADING CHARACTERS OF VOLUME SERIAL. ONLY        *
*                 DATASETS CATALOGED ON VOLUMES BEGINNING THUSLY      *
*                 WILL BE LISTED.                                     *
*              NOW THIS CODE IS BORROWED FROM THE LISTS COMMAND FROM  *
*              SHARE, AND IS A VERY MUCH STRIPPED DOWN VERSION OF     *
*              THAT COMMAND.                                          *
*              THE REAL WORK IS DONE IN SUBROUTINE LOCINDEX, AND SINCE*
*              I WROTE THAT MYSELF, I SUGGEST YOU LOOK THERE FOR      *
*              INSIGHT INTO MVS CATALOG MANAGEMENT.                   *
*        LEVEL CURRENT TO ALL TSO SUBJECT TO WHICH LOCINDEX YOU HAVE  *
*                                                                     *
*.....................................................................*
         TITLE 'LISTVOL - LIST VOLUME COMMAND'
LISTVOL  START 0
         STM   R14,R12,12(R13)
         USING LISTVOL,R15
         ST    R13,SAVE+4
         LA    R13,SAVE
         B     BEGIN
SAVE     DC    18F'0'
         DROP  R15
         USING SAVE,R13
         TITLE 'LISTVOL - GETMAIN AND PARSE'
BEGIN    LR    R4,R1
         USING CPPL,R4
         LA    R0,LWORK            GET LENGTH OF WORK AREA
         AH    R0,=H'4096'         GET EXTRA 4K FOR LOCINDEX WORK
         ICM   R0,B'1000',=FL1'1'  SUBPOOL 1
         GETMAIN R,LV=(0)
         LR    R12,R1               SAVE ADDR OF WORK AREA
         USING WORKAREA,R12
         LA    R0,LOCINDXW          POINT TO LOCINDEX WORK AREA
         ST    R0,PARM+12           SAVE ADDR IN PARM LIST
         MVI   PARM+12,X'80'        INDICATE END OF PARM LIST
         MVI   WXTNT+4,X'04'
         LA    R3,WPPL             GET ADDR OF PPL
         USING PPL,R3
         L     R0,CPPLCBUF
         ST    R0,PPLCBUF          SAVE ADDR OF COMMAND BUFFER
         L     R0,CPPLUPT
         ST    R0,PPLUPT           SAVE ADDR OF UPT
         L     R0,CPPLPSCB
         LR    R10,R0              GET ADDR OF USERID FROM PSCB
         SR    R11,R11
         IC    R11,7(R10)          GET LENGTH OF USERID
         BCTR  R11,0
AUTHRZD  OI    FLAG,X'80'
LECT     L     R0,CPPLECT
         ST    R0,PPLECT           SAVE ADDR OF ECT
         LA    R0,CPECB
         ST    R0,PPLECB           SAVE ADDR OF ECB
         XC    CPECB,CPECB         CLEAR ECB
         L     R0,=A(PARSLIST)
         ST    R0,PPLPCL           SAVE ADDR OF PCL
         LA    R0,WPDL
         ST    R0,PPLANS           SAVE ADDR OF PDL AREA
         XC    PPLUWA,PPLUWA       CLEAR UWA
         LR    R1,R3               PASS ADDR OF PPL TO PARSE
         LINK  EP=IKJPARS          LINK TO PARSE SERVICE ROUTINE
         LTR   R15,R15             PARSE ERROR
         BNZ   RETC                 YES
         TM    PPLANS,X'FF'        PARSE ERROR
         BO    RETC                 YES
         DROP  R3,R4
         L     R3,WPDL             GET ADDR OF PDL
         USING IKJPARMD,R3
         TM    EXTENT+1,X'01'      EXTENT SPECIFIED?
         BZ    *+8                  NO
         OI    FLAG,X'20'           YES, SET FLAG BIT
         TM    DSNLIST+6,X'80'      DSNAME SPECIFIED?
         BO    DSNRTN                YES
         TM    LEV+6,X'80'          INDEX LEVEL SPECIFIED?
         BO    LEVEL                 YES
         B     EUSLO                NO PARMS - LISTS FOR USERID
         TITLE 'LISTVOL - DATASET NAME ROUTINE'
DSNRTN   LA    R4,DSNLIST           POINT TO DSN LIST
         LA    R0,L'MSG1
         LA    R1,MSG1
         SVC   93                  TPUT HEADING
DSNAME   L     R5,0(R4)             POINT TO DSNAME
         LH    R6,4(R4)             GET LENGTH OF DSNAME
         BCTR  R6,0
         MVI   DSN1,X'40'
         MVC   DSN1+1(43),DSN1     BLANK DSN WORK AREA
         LA    R8,DSN1              POINT TO DSN WORK AREA
         TM    6(R4),X'40'          DSNAME CONTAINED IN QUOTES?
         BO    MVCDSNAM               YES
         EX    R11,USIDMVC         MOVE USERID TO WORK AREA
         LA    R8,1(R11,R8)        BUMP PAST USERID
         MVI   0(R8),C'.'          MOVE PERIOD TO WORK AREA
         LA    R8,1(0,R8)          BUMP PAST
MVCDSNAM EX    R6,DSNMVC           MOVE DSNAME TO WORK AREA
         MVC   VOLS,BLANKS         MOVE BLANKS TO VOLSER WORK AREA
         TM    FLAG,X'80'          AUTHORIZED USER?
         BZ    LOCDSN               NO
         TM    VOL+6,X'80'         VOLUME SPECIFIED?
         BZ    LOCDSN               NO, LOCATE DSN
         LA    R1,VOLS             POINT TO VOLSER WORK AREA
         L     R5,VOL              POINT TO VOLUME PARAMETER
         LH    R6,VOL+4            GET LENGTH OF VOL PARM
         BCTR  R6,0
         EX    R6,MVCVOLS          MOVE VOL PARM TO VOLSER WORK AREA
         B     BALOBTN             BYPASS LOCATE
LOCDSN   OI    FLAG,X'10'
         LOCATE INDS
         LTR   R15,R15              DID IT FIND VOL SER
         BNZ   ERR03                 NO WRITE ERROR MSG
         NI    FLAG,X'EF'
         MVC   VOLS(6),BUF1+6      YES - MOVE IN VOL SER
         SPACE 2
BALOBTN  TPUT  VOLS,52
RETDSN   L     R4,24(R4)           GET DSN LINK POINTER
         C     R4,ENDLIST          END OF LIST?
         BE    RETC                 YES
         B     DSNAME               NO, GET NEXT DSNAME
         TITLE 'LISTVOL - LEVEL ROUTINE'
EUSLO    EQU   *
**     FOR ENTIRE USERID LOGGED ON
         MVC   USERID(7),0(R10)        MOVE IN USERID
         B     GOTUS               BRANCH TO GOT USERID
         SPACE 3
LEVEL    EQU   *
         L     R5,LEV              POINT TO INDEX STRUCTURE
         LH    R6,LEV+4            GET LENGTH OF INDEX STRUCT
         BCTR  R6,0
         LA    R8,USERID           POINT TO WORK AREA
         EX    R6,DSNMVC           MOVE INDEX STRUCTURE TO WORK AREA
GOTUS    EQU   *
         TM    VOL+8,X'80'
         BZ    NVOLL
         LA    R1,VOLZ
         L     R5,VOL              POINT TO VOLUME PARAMETER
         LH    R6,VOL+4            GET LENGTH OF VOL PARM
         BCTR  R6,0
         STH   R6,LENVOLS
         EX    R6,MVCVOLS
NVOLL    LA    R0,L'MSG1
         LA    R1,MSG1
         SVC   93                  TPUT HEADING
         OI    FLAG,X'40'          SET FLAG FOR DATA SETS FOR USERID
LOOP     EQU   *                   LOOP FOR DATASET IN USERID
         L     R15,=V(LOCINDEX)    POINT TO EP(LOCINDEX)
         LA    R1,PARM             POINT TO PARM LIST
         BALR  R14,R15             GO TO LOCINDEX
         B     *+4(R15)            BRANCH , DEP ON RETURN CODE
         B     OK
         B     NOUSER              4-PRINT MSG & RETRUN TO SYS
         B     OK
         B     OK
         B     RETC                16-FINISHED-RETURN TO SYS
         B     RDERR               20-PRINT MSG&RETRUN TO SUS
OK       TM    VOL+6,X'80'
         BZ    NVOL2
         LH    R1,LENVOLS
         EX    R1,CLCVOLS
         BNE   LOOP
NVOL2    TPUT  VOLS,52
         B     LOOP                GET NEXT DSNAME FROM LOCINDEX
         SPACE 3
         TITLE 'LISTVOL - EOJ AND ERROR ROUTINES'
RETC     EQU   *
         L     13,SAVE+4           LOAD R13 PREVIOUS SPACE AREA
         LM    2,12,28(13)         RELOAD REGISTERS
         L     14,12(13)           LOAD RETURN ADDRESS
         MVI   12(13),X'FF'        INDICATE CONTROL RETURN CALLING PROG
         BCR   15,14               RETURN TO CALLING PROGRAM
         SPACE 3
ERR01    EQU   *
* NO VALID COMMAND
         TPUT  ERMSG1,18
         B     RETC                BRANCH TO RETURN CODE
* DATASET NAME NOT FOUND
ERR03    MVC   MSG2(L'ERMSG9),ERMSG9
         MVC   MSG2+L'ERMSG9(44),DSN1
         LA    R0,L'ERMSG9+44
         LA    R1,MSG2
         SVC   93
         TM    FLAG,X'40'
         BO    LOOP
         TM    FLAG,X'10'
         B     RETDSN
         SPACE 3
RDERR    MVC   MSG2(L'ERMSG8),ERMSG8
         MVC   MSG2+L'ERMSG8(44),DSN1
         LA    R0,L'ERMSG8+44
         LA    R1,MSG2
         SVC   93
         B     LOOP                CONTINUE THRU LOOP
         SPACE 3
NOUSER   MVC   MSG2(L'ERMSG3),ERMSG3
         MVC   MSG2+L'ERMSG3(44),USERID
         LA    R0,L'ERMSG3+44
         LA    R1,MSG2
         SVC   93
         B     RETC                RETURN TO SYSTEM
         SPACE 3
ERRF     EQU   *
         TPUT  ERMSG10,20
         B     RETC
USIDMVC  MVC   0(0,R8),0(R10)
DSNMVC   MVC   0(0,R8),0(R5)
MVCVOLS  MVC   0(0,R1),0(R5)
CLCVOLS  CLC   VOLS(0),VOLZ
         TITLE 'LISTVOL - DATA AREAS'
ENDLIST  DS    0F
         DC    XL4'FF000000'
* SETUP CONSTANTS
VOLDSCTK DC    H'0'                NUM DSCH ON A TRK
VOLF4CHR DC    XL5'00'             CCHHR OF DSCB
VOLNO    DC    H'0'                NUM VOLUMES PROCESSED
PEXCTR   DC    H'0'                CTR FOR TATAL NUM FREE SPACE
LASTTRK  DC    H'0'
VOLS     DC    CL6' '                 VOLUME SER
         DC    C'  '
DSN1     DC    CL44' '              DATASET NAME
USERID   DC    CL44' '              USERID
FLAG     DC    X'00'
DSORG    DC    X'00'
BLANKS   DC    CL6' '
VOLZ     DC    CL6' '
LENVOLS  DC    H'0'
BUF1     DS    0D
         DS    265C
TRTBL    EQU   *-240
         DC    C'0123456789ABCDEF'
MASKED   DC    XL6'402020202020'   MASK FIELD FOR EDIT
         SPACE 3
* MESSAGE TO BE PRINTED
MSG1     DC    C'VOLUME  DATASET NAME             '
MSG2     DC    CL76' '
MSG4     DC    C'VOLUME  DATASET NAME             '
         SPACE 3
* ERROR MESSAGES
ERMSG1   DC    C'NO VALID COMMAND'
ERMSG3   DC    C'INVALID LEVEL - '
ERMSG8   DC    C'ERROR READING CATALOG DSN - '
ERMSG9   DC    C'DSN NOT FOUND - '
ERMSG10  DC    C'UNSUPPORTED KEYWORD'
         LTORG
         EJECT
* CAMLST FOR DSCB3
SEEKCAM  CAMLST SEEK,VTOCCHHR,VOLS,BUF1
         SPACE 2
* CAMLST LOCATE DSN OV VOL SER
INDS     CAMLST NAME,DSN1,,BUF1
         SPACE 2
* CAMLST FOR DSCB 1
SERCHCAM CAMLST SEARCH,DSN1,VOLS,BUF1
         EJECT
* CONSTANTS AND WORK AREAS
HWK1     DC    H'0'                HALF WORD WORK AREA
VOLTKCYL DC    H'19'               # TRKS/CYL
VTOCCHHR DC    XL5'0'              TRACK ADDR WORK AREA
WKD      DC    D'0'                DOUBLE WORK WORK AREA
         SPACE 3
* PARM SETUP FOR GETDSN
PARM     DC    A(USERID)           USERID ADDRESS
         DC    A(DSN1)             DSN ADDRESS
         DC    A(VOLS)             VOL SER ADDRESS
         DC    A(0)                ADDRESS FOR GETMAIN
         SPACE 3
* EQUATE REGISTERS
         EQUATE
RCC      EQU   R8
RHH      EQU   R7
RR       EQU   R6
         TITLE 'LISTVOL - PARSE PARAMETER LIST'
PARSLIST IKJPARM
DSNKYD   IKJKEYWD
         IKJNAME 'DATASET',SUBFLD=DSNSUBF
LEVL     IKJKEYWD
         IKJNAME 'LEVEL',SUBFLD=LVL
VOLUME   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=VLM
EXTENT   IKJKEYWD
         IKJNAME 'EXTENTS'
DSNSUBF  IKJSUBF
DSNLIST  IKJPOSIT DSNAME,LIST
LVL      IKJSUBF
LEV      IKJIDENT 'LEVEL',OTHER=ANY,MAXLNTH=44
VLM      IKJSUBF
VOL      IKJIDENT 'VOLUME',LIST,FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=6
         IKJENDP
         IKJPPL
         IKJCPPL
         TITLE 'LISTVOL - WORK AREA'
WORKAREA DSECT
WPPL     DS    7A
CPECB    DS    F
WPDL     DS    F
OBTNSAVE DS    10F
XTNTS    DS    16XL10
WXTNT    DS    CL5
         DS    0D
LOCINDXW EQU   *
LWORK    EQU   *-WORKAREA
         END
