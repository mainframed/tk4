*              /* LEVEL=0005 */
IGC018XX CSECT
*
*  IGC018 ENTERED IN SYSTEM KEY
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*USE UNEQUATED REGISTER NOTATION TO DENOTE SVC-REG VALUES
         USING *,R6
ST01     B     ST02
         DC    C'IGC018XX01-04-77'     CODE ID
ST02     MVI   ST01+3,ST03-ST01        BYPASS INISH HEREAFTER
         L     R12,IGC018AD            ADDR BLDL MODULE
         MVC   X'462'(12,R12),PATCH1   PRE EXCP TRAP
         MVC   X'4D2'(12,R12),PATCH2   POST EXCP TRAP
ST03     L     R6,IGC018AD             TRUE BLDL ADDR.
         BR    R6                      GO TO BLDL
         DROP  R6
         EJECT
*
* 'BLDL' TO ISSUE EXCP, R6 ADDR SVRB EXT S.A. , WE USE +X'62'
*
*
*   OVER WRITE BLDL CODE FROM LABEL 'READ'
*   NOTE WE ENTER IN USER PROTECT KEY
*        UNLESS REQUEST IS FOR CVT-LINKLIB-DCB,THEN PK ZERO
*
         DS    0H
         USING *,R9
NWB1     TM    X'61'(6),X'A0' Q.SYSTEM LINKLIB AND BLDL         75035
$$$DIS01 BNO   RET1           NO ,NOT INTERESTED
         TM    X'62'(6),X'C0'   Q.FIRST TIME
         BNZ   RET1               NO ,LIST OR NTH CONCATENATION
         CLI   X'13A'(13),1        Q.CHECK LINKLIB              75035
         BNE   RET1                NO ,PLAY SAFE                75035
         CLC   =H'1',0(10)      Q.MUTIPLE BLDL
         BNE   NOTOURS            YES,FLAG AS NOT OURS AND RETN
         TM    3(7),X'40'       Q.NORMAL SORT OF MODULE NAME
         BNO   NOTOURS            NO ,FLAG AS NOT FOR US
NWB1COMM STM   2,7,80(13)         SAVE WORKREGS IN BLDL SAVEAEA
         L     R0,0(7)            1ST WORD OF MODULE NAME
         L     R5,4(7)            2ND
         L     R4,LINKADDR        ADDR.LINKLIB LIBRARY TABLE
         USING TABHEAD,R4
RETRY1   LM    R1,R3,TABSTART     REGS FOR  'BXLE'
         USING ENTRY,R1
FINDMOD  C     R0,DATA          Q.1ST WORD MATCH
         BNE   BXLE               NO TRY NEXT ENTRY
         C     R5,DATA+4        Q.2ND WORD MATCH
         BE    MATCH              YES FOUND IT IN A SLOT
BXLE     BXLE  R1,R2,FINDMOD      NO ,TRY ALL THE SLOTS
         LH    R3,MISSES          KEEP STATS ON
         LA    R3,1(,R3)          HOW MANY LOOKUPS
         STH   R3,MISSES          FAIL PER TABLE
         OI    X'62'(6),X'40'     FLAG SVRB AS NOT FOUND
         B     RESTORE1           EXIT
MATCH    LR    R3,R0              1ST WORD OF NAME
         X     R3,HEX40           SET TO 'LOCKED' FORMAT
SER1     CS    R0,R3,DATA       Q.SET LOCK FOR ENTRY
         BNE   RETRY1             NO ,LOST IT RE-LOOKUP
         C     R5,DATA+4        Q.CHECK REALLY OUR ENTRY
         BE    UPDATE1            YES ,USE IT
UNSERIA  OI    DATA+3,X'40'       NO ,LOST IT, RESET LOCK
         B     RETRY1             LOOKUP AGAIN
UPDATE1  LH    R3,RECENT          MAINTAIN
         LA    R3,1(,R3)          STATISTICS
         STH   R3,RECENT          ON SLOT USE
         SH    R2,H5              CALC. OFFSET FOR CONCATENATION
         IC    R3,DATA(R2)        CONCAT. NO.
         STC   R3,X'139'(13)      SAVE FOR 'BLDL'
         MVC   2(64,13),DATA      MOVE PDS DATA TO WORKAREA
UNSERIB  OI    DATA+3,X'40'       RESET LOCK FOR SLOT
         OI    5(13),X'40'        RESET NAME IN WORKAREA
         LH    R3,HITS            MAINTAIN
         LA    R3,1(,R3)          TABLE
         STH   R3,HITS            STATISTICS
         LA    8,2(,13)           **BLDL** ADDR DATA IN WORKAREA
         LA    14,31              **BLDL** SET MASK FOR CONCATN.
         B     RESTORE2           RESUME AFTER EXCP DONE IN BLDL
NOTOURS  OI    X'62'(6),X'80' FLAG AS NOT OURS
         B     RET1
RESTORE1 LM    2,7,80(13)     RESTORE WORK REGISTERS
RET1     L     R9,X'74'(6)    RESTORE BASE REG
         STCM  7,7,X'121'(13) **BLDL**
         LA    1,X'140'(13)       **BLDL**
         SVC   0                  **BLDL**
         TM    316(13),X'40'      **BLDL**  SU7 MOD
         BC    1,X'47C'(,12)      **BLDL**  SU7 MOD
         B     X'472'(12)         RESUME NORMAL BLDL CODE
         DROP  R9
         EJECT
*
* IGC018 DIRECTORY SEARCH FOUND MODULE
*
NWB2     DS    0H
         USING *,R9
         TM    X'62'(6),X'40'   Q.DID WE SHOW INTEREST(NOMATCH)
$$$DIS02 BNO   RET2               NO
         TM    X'61'(6),X'A0' Q.BLDL FOR MANAGED LIBRARY        75035
         BNO   RET2                NO ,NO INTEREST              75035
         CLI   X'13A'(13),1        Q.DOUBLE CHECK               75035
         BNE   RET2                NO ,PLAY SAFE                75035
SETSAVE  MVC   2(64,13),0(8)      PUT DATA TO WORKAREA START
         LA    8,2(,13)           TELL IGC018
         STM   2,7,80(13)         WORK REGS
         L     R4,LINKADDR    ADDRESS OUR TABLE
         LA    R4,0(,R4)      DENOTE FIRST PASS
GETSLOT  L     R3,TABSLOTF    SCAN LIMIT ,LOW ADDR
         L     R2,TABSTART+4  ENTRY SIZE
         LCR   R2,R2          COMPLEMENT FOR   'BXH'
GETSLOTX L     R0,LASTSLOT    SCAN LIMIT ,HIGH ADDR
GETSLOTY SR    R5,R5          ZERO FOR    'CH'
         LR    R1,R0          INISH FOR
TESTSLOT CH    R5,RECENT    Q.SLOT AVAIL
         BE    MAYBE2         YES, PERHAPS
BXH      BXH   R1,R2,TESTSLOT NO, TRY NEXT SLOT
         LTR   R4,R4        Q.IST OR 2ND PASS
         BP    SORTCNTL       1ST
         B     SORTDO         2ND
MAYBE2   L     R3,DATA        1ST WORD OF MODULE NAME
         LTR   R3,R3        Q.VIRGIN ENTRY
         BNZ   MAYBE2A        NO, CONTINUE
         LR    R5,R3          YES, SET UP VALUE TO GRAB IT
         BCTR  R5,0
         CS    R3,R5,DATA   Q.CAN WE GRAB IT
         BNE   GETSLOT        NO ,GET ANOTHER SLOT
         B     MAYBE2         YES , LOOP BACK TO USE IT
MAYBE2A  O     R3,HEX40       SETUP 'UNLOCKED' FORM
         LR    R5,R3          COPY
         X     R5,HEX40       'MANAGED' FORM
SER2     CS    R3,R5,DATA   Q.TRY TO MANAGE SLOT
         BE    MANAGED2       YES SUCCEEDED
         L     R3,TABSLOTF    NO ,FAILED , CONTINUE SEARCH
         SR    R5,R5          RESET REGS FIRST
         B     BXH            TREAT AS NOT FOUND
MANAGED2 AR    R2,R1              ADDR. PREDECESSOR ENTRY
         C     R2,TABSLOTF    Q.IS THIS IN UPDATE AREA
         BL    OUTRANGE       NO ,SO DONT UPDATE POINTER
         CS    R0,R2,LASTSLOT     UPDATE SCAN LIMIT
OUTRANGE SR    R5,R5              NEW USER COUNT FORMAT
         CH    R5,RECENT        Q.CHECK WE REALLY HAVE SLOT
         BE    USESLOT            YES, USE IT
UNSER2A  OI    DATA+3,X'40'       NO, RELEASE 'UNLOCK' SLOT
         B     GETSLOT            TRY AGAIN
         EJECT
USESLOT  L     R5,=X'00010000'
         ST    R5,RECENT          SET NEW USER COUNT
         L     R3,TABSIZE         SIZE OF SLOT
         SH    R3,H5              SUBTRACT HEADER
         IC    R2,X'139'(13)      CONCAT. NO.
         LA    R5,DATA(R3)        ADDR. LOCN. TO SAVE IT
         SH    R3,H4              DONT MOVE FIRST (LOCK) WORD
         EX    R3,DATAMOVE        MOVE DATA EXCEPT 1ST WORD
         STC   R2,0(,R5)          SAVE CONCAT. NO.
UNSER2B  MVC   DATA(4),0(8)       1ST WORD OF NAME,RELEASE LOCK
         B     RESTORE2           EXIT
DATAMOVE MVC   DATA+4(1),4(8) MOVE TTR DATA TO OUR ENTRY
SORTCNTL ICM   R4,8,=X'FF'        SET R4 NEG. FOR 2ND PASS
         MVC   LASTSLOT,TABEND    RESET SCAN LIMIT
         B     GETSLOT            TRY AGAIN
SORTDO   LA    R3,0(,R4)          CLEAR TOP BYTE
         ICM   R4,8,=X'40'        SET FOR 'LOCK'    'CS'
         LA    R1,LINKADDR        POINT TO CTL WORD
SER3     CS    R3,R4,0(R1)      Q.OURS AND 'UNLOCKED'
         BE    SORTIT             YES,AND LOCKED SO SORT IT
*                                 NO,GIVE UP QUIETLY
RESTORE2 LM    2,7,80(13)      WORKREGS
RET2     L     R9,X'74'(6)    RESTORE OUR BASE REGISTER
         MVC   8(3,7),8(8)        **BLDL** OVERWRITTEN INSTR.
         IC    4,11(8)            **BLDL**
         NR    4,14               **BLDL**
         BC    15,X'4DC'(12)      RETURN INLINE
         SPACE 8
SORTIT   LH    R1,SORTS           MAINTAIN
         LA    R1,1(,R1)          TABLE SORT
         STH   R1,SORTS           STATISTICS
         LA    R7,X'40'           BIT PATTERN FOR LOCKING
SORTLOCK LM    R1,R3,TABSTART     LOCK UNLOCKED SLOTS TO US
SORTX    L     R5,DATA            PICKUP LOCK WORD
         LTR   R5,R5               Q.ENTRY A VIRGIN             75035
         BZ    SORTFLGL            YES,ALREADY MANAGED FMT      75035
         OR    R5,R7              ENSURE 'UNLOCKED' FORM
         LR    R6,R5              SAVE IT
         XR    R6,R7              SET 'LOCKED' FORM
         CS    R5,R6,DATA       Q.TRY TO LOCK SLOT
         BNE   LOCKBXLE           NO, TRY NEXT
SORTFLGL DS    0H                                               75035
         OI    RECENT,X'80'       YES,SPECIAL FLAG FOR WE OWN
LOCKBXLE BXLE  R1,R2,SORTX        ENSURE ALL SLOTS TRIED FOR
         EJECT
         LH    R7,ENTRIES         NO. OF PAIRS IS NO. OF SLOTS-1
         LR    R6,R2              SIZE
         BCTR  R6,0               SIZE-1 FOR 'EX'
         L     R3,TABEND          LAST SLOT
OUTER    L     R1,TABSTART        SET START RANGE
         SR    R3,R2              DO 1 LESS PAIR EACH LOOP
INNER    L     R0,RECENT          COUNT -VE IF OURS             77082
         C     R0,RECENT(2)     Q.SHOULD WE SWAP                77082
         BH    LOOPIN             NO, DO NOT                    75035
         LTR   R0,R0            Q.YES, IS IT 'LOCKED' TO US
         BNM   LOOPIN             NO, CAN NOT
         LA    R5,0(R2,R1)        YES,ADDR 2ND SLOT
         EX    R6,SWAP1           JUXTAPOSE
         EX    R6,SWAP2           OUT OF ORDER
         EX    R6,SWAP1           SLOTS
LOOPIN   BXLE  R1,R2,INNER        DO FOR ALL PAIRS IN SCAN
HOUSEKEP TM    RECENT,X'80'     Q.IS THIS OUR SLOT, AS FINISHED
         BNO   RESETUSE           NO, SKIP UNLOCK CODE
         NI    RECENT,X'7F'       YES ,REMOVE OURBIT
         OI    DATA+3,X'40'       RELEASE LOCK
RESETUSE LH    R0,TOTAL           PREV. COUNT
         AH    R0,RECENT          NEW COUNT
         ST    R0,RECENT          UPDATE PREV. ,ZEROISE RECENT
         LTR   R7,R7            Q.SPECIAL CASE ,TOP SLOT
         BE    SORTDONE           YES, SPECIAL HOUSEKEEPING ENTRY
         BCT   R7,OUTER         Q.NO ,CARRY ON SCANS IF ANY
         L     R1,TABSTART        NONE LEFT, RESET TO ENTRY
         B     HOUSEKEP           UPDATE 1ST ENTRY IN TABLE
SWAP1    XC    0(1,R5),0(R1)
SWAP2    XC    0(1,R1),0(R5)
SORTDONE MVC   LASTSLOT,TABEND    RESET SCAN LIMIT
LINKFREE MVI   LINKADDR,0         YES UNSERIALISE SORT WISE
         LA    R4,0(,R4)      DENOTE FIRST SCAN
TABFREE  B     GETSLOT            GO GET SLOT FOR OUR APPLICATION
         DROP  R9
         EJECT
LINKADDR DC    A(LINKTAB)
         SPACE 2
PATCH1   BALR  9,0
         L     9,6(9)
         BR    9
ENTRY1   DC    A(NWB1)
         SPACE 2
PATCH2   BALR  9,0
         L     9,6(9)
         BR    9
ENTRY2   DC    A(NWB2)
         SPACE 4
$$$DISAD DS    0F
IGC018AD DC    V(IGC018YY)
H5       DC    H'5'
H4       DC    H'4'
HEX40    DC    X'00000040'
         LTORG
         EJECT
ENT1     EQU   100 10              SLOTS (TEST LIVE)            75035
ENT1X    EQU   37   3         FIXED PART(ENT1/2.71828)          75035
         SPACE 2
LINKTAB  DS    0D
$$$DIS04 EQU   *
TABSTAT1 DC    A(START1)
TABSIZE1 DC    F'64'
TABEND1  DC    A(LASTENT1)
LASTSLT1 DC    A(LASTENT1)
TABSLOT1 DC    A(START1+ENT1X*64)
SORT1    DC    H'0'
ENTRIES1 DC    AL2(ENT1)
HITS1    DC    H'0'
MISSES1  DC    H'0'
         DS    0F                      ENSURE ALIGNED           77082
START1   DC    ((ENT1-1)*64)X'0'
LASTENT1 DC    64X'0'
LINKEND  DS    0F
         EJECT
TABHEAD  DSECT
TABSTART DS    F
TABSIZE  DS    F
TABEND   DS    F
LASTSLOT DS    F
TABSLOTF DS    F
SORTS    DS    H
ENTRIES  DS    H
HITS     DS    H
MISSES   DS    H
         SPACE 3
ENTRY    DSECT
RECENT   DS    H
TOTAL    DS    H
DATA     DS     0F
         END
