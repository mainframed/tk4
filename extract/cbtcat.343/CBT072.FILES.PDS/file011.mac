IEE6303D CSECT
***********************************************************************
*
*        # COMMAND - ACTS AS A COMMAND ROUTER
*
*        COMMAND FORMAT :
*                        # X Y  OR  # XY
*        WHERE :
*               X = COMMAND
*               Y = OPERAND OF THE COMMAND
*
*        ON ENTRY:
*                 R2 = A(EXTENDED SAVE-AREA)
*
*        EXITS :
*               NORMAL   - IGCXX03D WHERE XX = CODE FOR VALID COMMAND
*               ABNORMAL - IGG0503D - ERROR MESSAGE MODULE
*
*               ALL EXITS ARE BY A XCTL MACRO
*
*        CSECT NAME  : IEE6303D
*        MODULE NAME : IGC6303D
*        ATTRIBUTES  : REENTRANT,REUSEABLE,REFRESHABLE
*
*
***********************************************************************
         SPACE 3
         LR    R12,R15
         USING IEE6303D,R12
         USING XSA,R2
         L     R0,SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVERET
         SR    R4,R4              ZERO R4
         IC    R4,XAN             SAVE VERB INDEX CODE
         L     R5,XAL             PARM LIST PTR
         LA    R5,0(,R5)          CLEAR HI BYTE
         LTR   R5,R5              PARM EXIST ?
         BZ    NOCOMD             NO...ERROR
         SR    R9,R9              LENGTH REG FOR EXECUTE INST
         LA    R7,TBLSTART        START OF COMMAND TABLE
         LA    R6,TBLEND          END OF COMMAND TABLE
         SPACE 3
CHKCMD   IC    R9,0(,R7)          LENGTH-1 OF COMMAND
         EX    R9,DCOMPARE        COMPARE COMMANDS
         BE    VALIDCOM           OK..
         LA    R7,8(R9,R7)        INCREMENT TO NEXT COMMAND
         CR    R7,R6              END-OF-TABLE ?
         BL    CHKCMD             NO..TRY NEXT COMMAND
         B     BADCOM             YES..COMMAND NOT FOUND
         EJECT
VALIDCOM L     R11,XAR            START OF BUFFER
         LH    R6,0(,R11)         LENGTH OF BUFFER
         AR    R11,R6             COMPUTE ENDING ADDRESS
         BCTR  R11,0              OF BUFFER
         LA    R9,1(,R9)          CORRECT ACTUAL LENGTH OF COMMAND
         AR    R5,R9              START OF PARM FIELD
         CR    R5,R11             PAST END OF BUFFER ?
         BH    NOPARM             YES...NO PARAMETERS ENTERED
         LA    R10,1              INCREMENT FOR BXLE
         SPACE 3
CHKPARM  CLI   0(R5),X'40'        BLANK ?
         BNE   SETPARM            NO...MUST BE PARAMETER
         BXLE  R5,R10,CHKPARM     TRY NEXT CHARACTER TILL END OF BUFFER
         SPACE 3
NOPARM   SR    R5,R5              ZERO REGISTER
         SPACE 1
SETPARM  ST    R5,XAL             SET PARM LIST POINTER
         STC   R4,XAN             RESTORE VERB INDEX CODE
         LA    R7,1(R9,R7)        POINT TO MODULE NAME CHAR
         LTR   R5,R5               ANY PARM
         BZ    SETXCTL             NO, CONTINUE
         CR    R5,R11              PAST END OF BUFFER
         BH    SETXCTL             YES, CONTINUE
         LR    R9,R5               SAVE PARM ADDR
         CLC   0(3,R5),=C',L='     ONLY SUB-PARM IS ,L=
         BNE   *+6                 NO, BYPASS NEXT
         SR    R9,R9               INDICATE NO SUBPARMS
         EJECT
SETPARM1 CLI   0(R5),C' '          PARM END
         BE    SETXCTL             YES, CONTINUE
         CLC   0(3,R5),=C',L='     DIRECTED OUTPUT
         BE    SETPARM2            YES, GO PROCESS
         BXLE  R5,R10,SETPARM1     CHECK NEXT BYTE
         B     SETXCTL             PARM END
         SPACE 3
SETPARM2 LA    R1,4(0,R5)          END OF L=XX
         CR    R1,R11              PAST END OF BUFFER
         BH    NOCOMD              YES, ERROR
         BCTR  R1,0                BACK UP 1
         TM    0(R1),X'F0'         NUMERIC
         BNO   NOCOMD              NO, ERROR
         TM    1(R1),X'F0'         NUMERIC
         BNO   NOCOMD              NO, ERROR
         SR    R4,R4
         IC    R4,0(0,R1)          CONVERT
         SLL   R4,28
         SRL   R4,28                  MCS
         MH    R4,=H'10'
         SR    R0,R0                     UCMID
         IC    R0,1(0,R1)
         SLL   R0,28                        TO
         SRL   R0,28
         AR    R4,R0                           BINARY
         STC   R4,XAU              SAVE CONVERTED UCMID
         ST    R9,XAL              SAVE PARM ADDR
         MVC   0(5,R5),=CL5' '     CLEAR  ,L=XX  PARM
         EJECT
SETXCTL  MVC   XAX(8),XCTLNAME    GET BASE MODULE NAME
         MVC   XAX+3(2),0(R7)     COMPLETE NAME
         MVC   WTOLIST(48),WTOOVLY
         BCTR  R7,0
         MVC   CMDID(1),0(R7)      COMMAND ISSUED
         L     R1,16               CVT
         L     R1,196(0,R1)        SMCA SYSTEM MANAGEMENT CONTROL AREA
         MVC   SYSID(4),16(R1)     SYSTEM ID
         TIME DEC
         ST    R1,WORKAREA         SAVE DATE
         UNPK  YEAR(3),WORKAREA+1(2)
         MVI   YEAR+2,C'.'
         UNPK  DAY(3),WORKAREA+2(2)
         OI    DAY+2,X'F0'
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         L     R0,SIZE
         L     R14,SAVERET
         LR    R1,R13
         FREEMAIN R,LV=(0),A=(1)
         L     R15,3(0,R7)        LOAD EP ADDR
         BR    R15
         SPACE 3
NOCOMD   MVI   XAE,X'0B'          PARM MISSING CODE
         B     SETERR             JOIN COMMON CODE
         SPACE 3
BADCOM   MVI   XAE,X'05'          INVALID COMMAND CODE
         L     R1,XAR             START OF INPUT STRING
         LH    R3,0(,R1)          LENGTH OF INPUT MINUS
         SH    R3,=H'2'           2 FOR '# ' PRIOR TO COMMAND
         CH    R3,=H'8'           INVALID COMMAND > 8 BYTES LONG ?
         BNH   SETEX              NO...OK AS IS
         LA    R3,8               YES...REDUCE TO 8 BYTES
         SPACE 3
SETEX    BCTR  R3,0               DECREMENT FOR EX INST
         EX    R3,GETVERB         PLACE IN XSA FOR ERROR MSG
         SPACE 3
SETERR   LA    R7,ERRMOD          SET FOR IGC0503D-ERROR MODULE
         B     SETXCTL            JOIN COMMON CODE
         EJECT
***********************************************************************
*
*        COMMAND TABLE
*
***********************************************************************
         CNOP  0,4
TBLSTART EQU   *                  START OF VALID COMMAND TABLE
*
         DC    AL1(L'DENQ-1)      LENGTH OF COMMAND - 1
DENQ     DC    C'DENQ '           DENQ COMMAND
         DC    C'SA'              CHAR FOR MODULE NAME
         DC    V(IEESA03D)        ADDR OF MODULE
         DC    AL1(L'N-1)
N        DC    C'N'               ABBREVIATION OF DENQ CMD
         DC    C'SA'              CHAR FOR MODULE NAME
         DC    V(IEESA03D)        ADDR OF MODULE
         DC    AL1(L'LINES-1)
LINES    DC    C'LINES'           COMMAND NAME
         DC    C'SC'              CHAR FOR MODULE NAME
         DC    V(IEESC03D)        ADDR OF MODULE
         DC    AL1(L'L-1)
L        DC    C'L'               ABBREVIATION OF LINES CMD
         DC    C'SC'              CHAR FOR MODULE NAME
         DC    V(IEESC03D)        ADDR OF MODULE
         DC    AL1(L'D-1)
D        DC    C'D'               DUMP/ALTER COMMAND
         DC    C'SD'              CHAR FOR MODULE NAME
         DC    V(IEESD03D)        ADDR OF MODULE
         DC    AL1(L'A-1)
A        DC    C'A'               DISPLAY ACTIVE COMMAND
         DC    C'SE'              CHAR FOR MODULE NAME
         DC    V(IEESE03D)        ADDR OF MODULE
         DC    AL1(L'F-1)
F        DC    C'F'               FRAME ALLOCATION DISPLAY
         DC    C'SF'              CHAR FOR MODULE NAME
         DC    V(IEESF03D)        ADDR OF MODULE
         DC    AL1(L'PFT-1)
PFT      DC    C'PFT  '           FRAME ALLOCATION DISPLAY
         DC    C'SF'              CHAR FOR MODULE NAME
         DC    V(IEESF03D)        ADDR OF MODULE
         DC    AL1(L'R-1)
R        DC    C'R'               DISPLAY UNIT REQUESTS
         DC    C'SG'              CHAR FOR MODULE NAME
         DC    V(IEESG03D)        ADDR OF MODULE
         DC    AL1(L'S-1)
S        DC    C'S'               LPA MEMBER DISPLAY
         DC    C'SH'              CHAR FOR MODULE NAME
         DC    V(IEESH03D)        ADDR OF MODULE
         DC    AL1(L'V-1)
V        DC    C'V'               VOLUME DISPLAY
         DC    C'SI'              CHAR FOR MODULE NAME
         DC    V(IEESI03D)        ADDR OF MODULE
         DC    AL1(L'I-1)
I        DC    C'I'               IPS ID, SWAP COUNTS, PAGE AND SWAP DS
         DC    C'SJ'              CHAR FOR MODULE NAME
         DC    V(IEESJ03D)        ADDR OF MODULE
         DC    AL1(L'B-1)
B        DC    C'B'               DUMMY ENTRY 'B'
         DC    C'SO'              CHAR FOR MODULE NAME
         DC    V(IEESO03D)        ADDR OF MODULE
         DC    AL1(L'C-1)
C        DC    C'C'               DISPLAY HEX & CALC
         DC    C'SK'              CHAR FOR MODULE NAME
         DC    V(IEESK03D)        ADDR OF MODULE
         DC    AL1(L'E-1)
E        DC    C'E'               DUMMY ENTRY 'E'
         DC    C'SP'              CHAR FOR MODULE NAME
         DC    V(IEESP03D)        ADDR OF MODULE
         DC    AL1(L'G-1)
G        DC    C'G'               DUMMY ENTRY 'G'
         DC    C'SV'              CHAR FOR MODULE NAME
         DC    V(IEESV03D)        ADDR OF MODULE
         DC    AL1(L'H-1)
H        DC    C'H'               DISPLAY HELP DATA SET
         DC    C'SM'              CHAR FOR MODULE NAME
         DC    V(IEESM03D)        ADDR OF MODULE
         DC    AL1(L'J-1)
J        DC    C'J'               DISPLAY ACTIVE REPLACEMENT
         DC    C'SL'              CHAR FOR MODULE NAME
         DC    V(IEESL03D)        ADDR OF MODULE
         DC    AL1(L'K-1)
K        DC    C'K'               DUMMY ENTRY 'K'
         DC    C'SQ'              CHAR FOR MODULE NAME
         DC    V(IEESQ03D)        ADDR OF MODULE
         DC    AL1(L'M-1)
M        DC    C'M'               DUMMY ENTRY 'M'
         DC    C'SR'              CHAR FOR MODULE NAME
         DC    V(IEESR03D)        ADDR OF MODULE
         DC    AL1(L'O-1)
O        DC    C'O'               DUMMY ENTRY 'O'
         DC    C'SS'              CHAR FOR MODULE NAME
         DC    V(IEESS03D)        ADDR OF MODULE
         DC    AL1(L'P-1)
P        DC    C'P'               DUMMY ENTRY 'P'
         DC    C'ST'              CHAR FOR MODULE NAME
         DC    V(IEEST03D)        ADDR OF MODULE
         DC    AL1(L'Q-1)
Q        DC    C'Q'               DUMMY ENTRY 'Q'
         DC    C'SU'              CHAR FOR MODULE NAME
         DC    V(IEESU03D)        ADDR OF MODULE
         DC    AL1(L'T-1)
T        DC    C'T'               TSO DISPLAY ACTIVE REPLACEMENT
         DC    C'SN'              CHAR FOR MODULE NAME
         DC    V(IEESN03D)        ADDR OF MODULE
         DC    AL1(L'U-1)
U        DC    C'U'               DUMMY ENTRY 'U'
         DC    C'SW'              CHAR FOR MODULE NAME
         DC    V(IEESW03D)        ADDR OF MODULE
         DC    AL1(L'W-1)
W        DC    C'W'               DUMMY ENTRY 'W'
         DC    C'SX'              CHAR FOR MODULE NAME
         DC    V(IEESX03D)        ADDR OF MODULE
         DC    AL1(L'X-1)
X        DC    C'X'               DUMMY ENTRY 'X'
         DC    C'SY'              CHAR FOR MODULE NAME
         DC    V(IEESY03D)        ADDR OF MODULE
         DC    AL1(L'Y-1)
Y        DC    C'Y'               DUMMY ENTRY 'Y'
         DC    C'SZ'              CHAR FOR MODULE NAME
         DC    V(IEESZ03D)        ADDR OF MODULE
         DC    AL1(L'Z-1)
Z        DC    C'Z'               DUMMY ENTRY 'Z'
         DC    C'S#'              CHAR FOR MODULE NAME
         DC    V(IEES#03D)        ADDR OF MODULE
*
TBLEND   EQU   *                  END OF COMMAND TABLE
         EJECT
***********************************************************************
*
*        CONSTSNTS, EQUATES, DSECTS, ETC.
*
***********************************************************************
         SPACE 3
         LTORG
         SPACE 3
DCOMPARE CLC   0(*-*,R5),1(R7)
GETVERB  MVC   XAV(*-*),4(R1)
XCTLNAME DC    CL8'IGCXX03D'
         CNOP  2,4
ERRMOD   DC    C'05'              FOR IGC0503D ERROR MODULE
         DC    V(IEE0503D)
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
WTOOVLY  DS    0CL48
         DC    X'00300100'
         DC    CL7'-----  ',CL10'CMD ROUTER',CL4' (# '
         DC    CL4'X)  ',CL4'XXXX',CL8'  YY.DDD',CL7'  -----'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVERET  DS    F
WORKAREA DS    F
         SPACE 3
WTOLIST  DS    0CL48
         DS    CL25
CMDID    DS    C
         DS    CL3
SYSID    DS    CL4
         DS    CL2
YEAR     DS    CL2
         DS    C
DAY      DS    CL3
         DS    CL7
WORKEND  DS    0D
         EJECT
DSXSA    DSECT
XSA      DS    0D
XAP      DS    F                  PTR TO XCTL NAME
XAD      DS    F                  DCB PTR
XAX      DS    D                  XCTL NAME
XAE      DS    0C                 ERROR CODE
XAR      DS    F                  PTR TO PARM LIST (REG1)
XAN      DS    0C                 VERB INDEX
XAL      DS    F                  PTR TO LIST POSITION
XAV      DS    D                  VERB
XAS      DS    D                  TEMP SAVE-AREA
XAU      DS    C                  UCM ENTRY INDICATOR
         DS    C                  RESERVED
XAJ      DS    H                  TJID ENTRY INDICATOR
         DS    F                  RESERVED
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         END
