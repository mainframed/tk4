MVSG     TITLE 'S H A R E D  D A S D  A N A L Y S I S  PROGRAM MVS...'
******************************************************************
*
*        THIS PGM IS FOR ANALYSIS OF GTF SIO AND IO RECORDS.
*        REDUCTION DETERMINES THE NO OF SIOS,SIO AND CC2,
*        SIO AND CC1 - EITHER DEV BUSY OR CONTROL UNIT BUSY,
*        IO INTERRUPTS TOTAL AND THOSE WITH JUST DEVICE END.
*
*        TIME IS ALSO ACCUMULATED FOR EACH IO DONE TO THE UNIT
*        ADDRESS. THIS NEW VERSION BREAKS DOWN ALT CHANNEL
*        INFORMATION FOR CHANNEL PROBLEMS. CONTENTION REPORTS
*        THEN CAN TELL YOU IF A BOTTLENECK EXISTS.
*        ALSO INCLUDED IS THE LONGEST TIME TAKEN TO DO SIO TO
*        SIO WITH CC ZERO. IN SOME CASES THIS IS THE FIRST ATTEMPT
*        I.E. SIO TO SIO CC ZERO IS THE SAME SIO. IN OTHER CASES
*        IT IS POSSIBLE TO DO SEVERAL SIOS UNTIL SIO WITH CC ZERO.
*        IT IS THIS INFORMATION THAT SHOWS POSSIBLE STRING CONT.
*
******************************************************************
         EJECT
GTFTEST  CSECT
         STM   14,12,12(13)
         BALR  12,0
         USING *,12,11
FRSTCSCT L     11,NXTCSCT
         ST    13,MYSAVE+4
         LA    13,MYSAVE
         L     R7,0(,1)
         LA    R7,0(,R7)
         CLC   0(2,R7),=H'5'
         BNE   OMITINT
         MVC   TEMP2(5),2(R7)
         B     CALCINT
OMITINT  EQU   *
         MVC   TEMP2(5),=C'03600'
         B     CALCINT
CALCINT  EQU   *
         MVC   TEMP1(8),=D'0'
         PACK  TEMP1+5(3),TEMP2(5)
         CVB   R8,TEMP1
         ST    R8,RPORTINT
         B     BEGIN
MYSAVE   DS    18F
NXTCSCT  DC    A(FRSTCSCT+4096)
BEGIN    EQU   *
GTFREG   EQU   2
DEVREG   EQU   3
PRTREG   EQU   4
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING GTFREC,GTFREG
         USING DEVBLK,DEVREG
         USING PRTDSCT,PRTREG
         USING CDTAB,R6
         MVI   TRTAB+X'F1',X'01'
         MVI   TRTAB+X'F2',X'02'
         MVI   TRTAB+X'F3',X'03'
         MVI   TRTAB+X'F4',X'04'
         MVI   TRTAB+X'F5',X'05'
         MVI   TRTAB+X'F6',X'06'
         MVI   TRTAB+X'F7',X'07'
         MVI   TRTAB+X'F8',X'08'
         MVI   TRTAB+X'F9',X'09'
         MVI   TRTAB+X'C1',X'0A'
         MVI   TRTAB+X'C2',X'0B'
         MVI   TRTAB+X'C3',X'0C'
         MVI   TRTAB+X'C4',X'0D'
         MVI   TRTAB+X'C5',X'0E'
         MVI   TRTAB+X'C6',X'0F'
         TITLE 'OPEN DATA DETS AND START INIT OF TIMES.'
         OPEN  (CRDCB,(INPUT))
*        B     AROUND          GO AROUND GTF INIT
         OPEN  (GTFDCB,(INPUT))
         OPEN  (PRTDCB,(OUTPUT))
         OPEN  (SNAPDCB,(OUTPUT))
INITTCVT DS    0H
         GET   GTFDCB,IOAREA
         LA    GTFREG,IOAREA
         CLI   GTFAID,X'00'
         BNE   INITTCVT
         MVC   GTFTCVT(4),GTFTIME
AROUND   EQU   *                   COME HERE FOR INIT TESTS
         GETMAIN EU,LV=6400,SP=1,A=CDTBLA,                             X
               BNDRY=PAGE          GET CORE FOR CARD INPUT TBL
         GETMAIN EU,LV=4000,SP=2,A=DEVTABA,                            X
               BNDRY=PAGE          GET CORE FOR DEVICE TABLE
         GETMAIN EU,LV=28000,SP=3,A=STATA,                             X
               BNDRY=PAGE     GET CORE FOR STATS
         L     R4,CDTBLA      LOAD CD TAB POINTER
         ST    R4,SNAPLIST    STORE IN SNAPLIST
         A     R4,=F'6400'    ADD LENGTH OF GETMAIN
         ST    R4,SNAPLIST+4  STORE END FOR SNAP
         L     R4,DEVTABA     LOAD DEV TAB POINTER
         ST    R4,SNAPLIST+8  STORE IN SNAPLIST
         LA    R4,4000(R4)    ADD LENGTH FOR DEVTAB
         ST    R4,SNAPLIST+12 STORE FOR SNAP
         L     R4,STATA       LOAD STATS GETMAINED AREA
         ST    R4,SNAPLIST+16 STORE FOR SNAP
         A     R4,=F'28000'   SET LENGTH
         ST    R4,SNAPLIST+20 STORE FOR SNAPLIST
         OI    SNAPLIST+20,X'80'
         L     R4,CDTBLA           LOAD R 4 WITH FIRST BYTE TO CLEAR
         LA    R6,1                LOAD R 6 WITH A LOW ADDRESS
         XR    R7,R7               CLEAR R7 FOR LENGTH AND PAD
         ICM   R7,8,=C' '          SET SP PAD FOR CLEAR
         L     R5,=F'6400'         SET LENGTH FOR MVCL
         MVCL  R4,R6               SET THIS AREA TO BLANKS
         L     R4,DEVTABA          LOAD R 4 WITH AREA TO CLEAR
         LA    R6,1                SET R 6 TO A LOW ADDR
         XR    R7,R7               SET LENGTH ZERO AND PAD
         LA    R5,4000             SET LENGTH FOR MVCL
         MVCL  R4,R6               CLEAR AREA TO ZEROES
         L     R4,STATA            SET FOR AREA TO CLEAR
         LA    R6,1                SET R 6 TO A LOW ADDRESS
         XR    R7,R7               SET PAD ZERO AND LENGTH ZERO
         L     R5,=F'28000'        SET LENGTH FOR MVCL
         MVCL  R4,R6               CLEAR AREA TO ZEROES
         L     R7,DEVTABA          LOAD R7 WITH DEVTAB ENTRIES
         L     R6,CDTBLA           LOAD R6 WITH CARD TABLE ENTRIES
         L     R3,STATA            LOAD R3 WITH STAT TAB ENTRIES
         XR    R8,R8
         TITLE 'INPUT CARD ROUTINE. EACH DEVICE ON A SEPERATE CARD'
GETCRD   EQU   *
         GET   CRDCB,CRDIN
         LA    R8,1(,R8)
         C     R8,=F'500'
         BH    ABND101
         MVC   CHAR+1(3),CRDIN
         BAL   14,TRANADDR
         MVC   UNITP,CRDIN         MOVE IN PRIMARY ADDRESS
         ST    R7,ADEVTABA         STORE ADDR OF ASSOC DEVTAB ENTRY
         ST    R3,4(,R7)           STORE ADDR OF STAT ENTRIES
         MVC   1(3,R3),CRDIN       MOVE IN ADDRESS OF UNIT
         ST    R6,8(,R7)           ST CDTAB ENTRY IN DEVTAB
         MVC   DEVTTME(8),=PL8'0'
         MVC   DEVPTME(8),=PL8'0'
         A     R7,=F'12'
         LA    R5,CRDIN                POINT TO CARD INPUT
TESTNEXT LA    R5,4(R5)                POINT TO NEXT ENTRY
         CLI   0(R5),C' '
         BE    GETCDRA
         MVC   CHAR+1(3),0(R5)
         BAL   14,TRANADDR
         OI    0(R7),X'40'
         LA    R8,1(R8)
         C     R8,=F'500'
         BH    ABND101
         ST    R7,ADEVTABB         ST SECON ADDR IN CDTABL
         MVC   UNITS(3),0(R5)   MOVE IN SECOND ADDR EBCDIC
         LA    R3,DEVLGTH(R3)      UP R3 FOR NEXT DEV STAT ENTRY
         MVC   1(3,R3),0(R5)       ST ADDRESS OF UNIT IN STAT
         MVC   DEVTTME(8),=PL8'0'    ZERO COUNTER
         MVC   DEVPTME(8),=PL8'0'    ZERO COUNTER
         ST    R6,8(,R7)           ST CDTAB PRT IN DEVTAB
         ST    R3,4(R7)
         LA    R7,12(R7)
         B     TESTNEXT
TRANADDR DS    0H
         TR    CHAR+1(3),TRTAB
         L     R9,CHAR
         OC    3(1,R7),CHAR+3
         SRL   R9,4
         STC   R9,CHAR+3
         OC    3(1,R7),CHAR+3
         SRL   R9,12
         STC   R9,CHAR+3
         OC    2(1,R7),CHAR+3
         BR    14
GETCDRA  EQU   *
         CLI   UNITS+2,X'40'  SECOND ADDR IN CDTAB
         BNE   GTTTT          YES DONT ZERO OUT THE SECOND ENTRY
         MVC   ADEVTABB(4),=F'0'   CLEAR SECOND ADDR PRT IN CRTAB
GTTTT    EQU   *
         MVC   SIOFIRST(8),=PL8'0'   ZERO HOLDER
         MVC   SIOLAST(8),=PL8'0'   ZERO OLD STORE
         MVC   SIOLONG(8),=PL8'0'   ZERO OLD STORE
         LA    R6,CDTABLG(R6)      SET UP FOR NEXT REAL DEVICE
         LA    R3,DEVLGTH(R3)      GO TO NEXT STAT ENTRY
         B     GETCRD              READ ANOTHER CARD
ENDCRD   EQU   *
         S     R7,=F'12'
         OI    0(R7),X'80'
         S     R6,=F'40'           SUBTRACT FOR LAST ENTRY
         OI    0(R6),X'80'      SET LAST CDTAB ENRTY
         TITLE 'GTF INPUT ROUTINE. MAIN INPUT FROM GTF TAPE.'
GETGTFRC EQU   *
         GET   GTFDCB,IOAREA
         LA    GTFREG,IOAREA
         CLI   GTFAID,X'00'
         BE    GETGTFRC
         CLC   GTFEID(2),=X'5100'
         BE    SIORTN
         CLC   GTFEID(2),=X'5200'
         BE    IORTN
         B     GETGTFRC
SIORTN   EQU   *
         BAL   14,TIMEINIT
         MVC   TEMP1(8),GTFTIME
         LM    4,5,TEMP1
         MVC   TEMP1(4),GTFTCVT
         A     4,TEMP1
         SRDL  4,12
         D     4,=F'60000000'
         LR    7,4
         XR    4,4
         D     4,=F'1440'
         MH    4,=H'60'
         XR    6,6
         D     6,=F'1000000'
         AR    4,7
         C     4,ENDTIME
         BL    NORPORT
         SNAP  DCB=SNAPDCB,LIST=SNAPLIST
         BAL   14,PRTANAL
         MVI   TIMESW,X'00'
         BAL   14,TIMEINIT
         TITLE 'PROCESS SIO ENTRIES FOR EACH RECORD'
NORPORT  MVC   DEVID(2),SIODEVAD
         BAL   14,FINDDEV
         TM    DEVFLAG,X'20'  FIRST SIO TIME STORED
         BO    NOSTORE        YES DISREGARD GTF TIME..
         MVC   SIOFIRST(8),GTFTIME   PUT TIME IN CDTAB ENTRY
         OI    DEVFLAG,X'20'  SET FLAG FOR STORED
NOSTORE  EQU   *
         L     R7,DEVSIO
         A     R7,=F'1'
         ST    R7,DEVSIO
         NI    GTFCC,X'30'
         CLI   GTFCC,X'00'             I/O OPERATION INITIATED?
         BE    SIOOK
         CLI   GTFCC,X'10'             CSW STORED?
         BE    CHKCSW
         CLI   GTFCC,X'20'             CHANNEL OR SUBCHANNEL BUSY?
         BE    SIOERR5
         CLI   GTFCC,X'30'             NOT OPERATIONAL?
         BE    SIOERR
         B     GETGTFRC
SIOERR5  EQU   *                   COME HERE FOR CHAN BUSY
         L     R7,DEVSIO2          LOAD CURRENT CC2 CNTR
         LA    R7,1(,R7)           ADD ONE TO CNTR
         ST    R7,DEVSIO2          STORE UPDATED
         B     GETGTFRC            GET ANOTHER GTF RECORD
SIOOK    EQU   *
         MVC   DEVSIOTL(8),GTFTIME
         MVC   SIOLAST(8),GTFTIME  STORE SIO CC ZERO TIME...
         OI    DEVFLAG,X'40'
         B     STSIOTME
CHKCSW   EQU   *
         TM    SIOSTAT,X'50'          CONTROL UNIT BUSY?
         BO    CUBUSY
         TM    SIOSTAT,X'10'          DEVICE BUSY?
         BO    DEVBUSY
         B     GETGTFRC
CUBUSY   EQU   *
         L     R7,DEVSIOCB
         A     R7,=F'1'
         ST    R7,DEVSIOCB
         B     STSIOTME
DEVBUSY  EQU   *
         L     R7,DEVSIODB
         A     R7,=F'1'
         ST    R7,DEVSIODB
         B     STSIOTME
STSIOTME EQU   *
         TM    DEVFLAG,X'80'
         BO    GETGTFRC
         MVC   DEVSIOTF(8),GTFTIME
         OI    DEVFLAG,X'80'
         B     GETGTFRC
SIOERR   EQU   *
         B     GETGTFRC
         TITLE 'PROCESS IO INTERRUPTS....'
IORTN    EQU   *
         BAL   14,TIMEINIT
         MVC   DEVID(2),IODEVAD
         BAL   14,FINDDEV
         L     R7,DEVIO
         A     R7,=F'1'
         ST    R7,DEVIO
         TM    GTFCSW+4,X'08'          CHANNEL END?
         BO    CHLEND
         TM    GTFCSW+4,X'04'          DEVICE END?
         BO    DVCEND
         TM    GTFCSW+4,X'20'          CONTROL UNIT END?
         BE    GETGTFRC
         B     GETGTFRC
CHLEND   EQU   *
         BAL   14,CALCTTME
         BAL   14,CALCPTME
         TM    DEVFLAG,X'80'  IS DEV FLAG ON FOR DATA
         BZ    RETHIGH        NOPE DONT DO ZERO SP
         MVC   MICBIN(8),SIOLAST   GET CC=0 TIME
         BAL   R14,MICCVT          CONVERT TIME
         MVC   NEWTIME(16),MICDEC  SAVE THIS TIME
         MVC   MICBIN(8),SIOFIRST   GET FIRST NON ZERO CC
         BAL   R14,MICCVT     CONVERT FIRST SIO TIME
         MVC   OLDTIME(16),MICDEC  SAVE THIS TIME
         SP    NEWTIME(16),OLDTIME(16)   DETERMINE TIME LOSS
         CP    SIOLONG(8),NEWTIME+8  IS THIS LONGER
         BH    RETHIGH             NO RETURN
         MVC   SIOLONG(8),NEWTIME+8    SAVE LONGEST TIME
RETHIGH  EQU   *
         B     INITDEV
DVCEND   EQU   *
         L     R7,DEVIODE
         A     R7,=F'1'
         ST    R7,DEVIODE
         B     GETGTFRC
CALCTTME EQU   *
         ST    14,RETADDR
         TM    DEVFLAG,X'80'
         BZ    CALCTRET
         MVC   MICBIN(8),GTFTIME
         BAL   14,MICCVT
         MVC   NEWTIME(16),MICDEC
         MVC   MICBIN(8),DEVSIOTF
         BAL   14,MICCVT
         MVC   OLDTIME(16),MICDEC
         SP    NEWTIME(16),OLDTIME(16)
         AP    DEVTTME(8),NEWTIME+8(8)
CALCTRET EQU   *
         L     14,RETADDR
         BR    14
CALCPTME EQU   *
         ST    14,RETADDR
         TM    DEVFLAG,X'80'
         BZ    CALCPRET
         TM    DEVFLAG,X'40'
         BZ    CALCPRET
         MVC   MICBIN(8),GTFTIME
         BAL   14,MICCVT
         MVC   NEWTIME(16),MICDEC
         MVC   MICBIN(8),DEVSIOTL
         BAL   14,MICCVT
         MVC   OLDTIME(16),MICDEC
         SP    NEWTIME(16),OLDTIME(16)
         AP    DEVPTME(8),NEWTIME+8(8)
CALCPRET EQU   *
         L     14,RETADDR
         BR    14
         TITLE 'MISC ROUTINES USED FOR TIMES,DEV INIT,ETC.'
INITDEV  EQU   *
         MVC   SIOFIRST(8),=PL8'0'    CLEAR HOLD AREAS
         MVC   SIOLAST(8),=PL8'0'
         L     R7,ADEVTABA         LOAD PRIM DEVTAB
         L     R3,4(,R7)           LOAD R3 WITH STAT PTR
         MVC   DEVSIOTF(8),=D'0'   CLEAR AREAS
         MVC   DEVSIOTL(8),=D'0'
         MVI   DEVFLAG,0
         L     R7,ADEVTABB         LOAD SECD DEVTAB
         LTR   R7,R7          IS THERE ANOTHER DEVTAB
         BZ    GETGTFRC       NOPE. GET ANOTHER INPUT RECORD
         L     R3,4(,R7)           LOAD R3 WITH STAT PTR
         MVC   DEVSIOTF(8),=D'0'   CLEAR AREAS
         MVC   DEVSIOTL(8),=D'0'
         MVI   DEVFLAG,0
         B     GETGTFRC
FINDDEV  EQU   *
         L     R7,DEVTABA           LOAD ADDRESS OF DEVTAB....
LOOP1    CLC   2(2,R7),DEVID
         BE    GOTDEV
         TM    0(R7),X'80'
         BO    DRUMTEST      CHANGE FOR DRUMS....
         A     R7,=F'12'
         B     LOOP1
GOTDEV   L     DEVREG,4(,R7)
         L     R6,8(,R7)           LOAD CDTAB ENTRY FOR DEVICE
         BR    14
MICCVT   EQU   *
         LM    4,5,MICBIN
         SRDL  4,12
         MVC   TEMP1(16),=PL16'0'
         MVC   TEMP2(16),=PL16'0'
         LA    R7,MICTAB
         L     R8,=F'0'
LOOP2    LR    R9,5
         N     R9,=XL4'000000FF'
         CVD   R9,TEMP1+8
         MP    TEMP1(16),0(8,R7)
         AP    TEMP2(16),TEMP1(16)
         A     R8,=F'1'
         C     R8,=F'7'
         BNL   CVTDONE
         A     R7,=F'8'
         SRDL  4,8
         B     LOOP2
CVTDONE  EQU   *
         MVC   TEMP1(4),GTFTCVT
         A     4,TEMP1
         SRDL  4,12
         D     4,=F'60000000'      R4=MICS, R5=MINS
         LR    7,4                 SAVE MICS
         XR    4,4                 CLEAR FOR DIVIDE
         D     4,=F'1440'          R4=MINS, R5=DAYS
         MH    4,=H'60'            R4=SECS
         XR    6,6                 CLEAR FOR DIVIDE
         D     6,=F'1000000'       R6=MICS, R7=SECS
         AR    4,7
         ST    4,STRTTIME
         A     4,RPORTINT
         ST    4,ENDTIME
         MVI   TIMESW,X'FF'
         BR    14
SECCVT   EQU   *
         MVC   TIMEOUT(8),=C'  .  .  '
         XR    4,4
         L     5,TIMEIN
         D     4,=F'3600'
         CVD   5,TEMP1
         UNPK  TEMP2(3),TEMP1+6(2)
         OI    TEMP2+2,X'F0'
         MVC   TIMEOUT(2),TEMP2+1
         LR    5,4
         XR    4,4
         D     4,=F'60'
         CVD   5,TEMP1
         UNPK  TEMP2(3),TEMP1+6(2)
         OI    TEMP2+2,X'F0'
         MVC   TIMEOUT+3(2),TEMP2+1
         CVD   4,TEMP1
         UNPK  TEMP2(3),TEMP1+6(2)
         OI    TEMP2+2,X'F0'
         MVC   TIMEOUT+6(2),TEMP2+1
         BR    14
         SPACE 5
DRUMTEST EQU   *                   COME HERE TO CORRECT DRUM PLBM
*        THE TEST HERE ANDS OFF THE ADDRESS PART
*        OF THE DEVID IN GTF RECORDS. THIS PUTS
*        THE DRUMS ADDRESSES INTO 1 BUCKET..
*        OHTERWISE INCORRCT DATA FOR DEVICES
*
         NI    DEVID+1,X'F8'       AND OFF LOW ORDER ADDRESS
         L     R7,DEVTABA          NOW REDO THE LOOP FROM ABOVE
LOPP2    CLC   2(2,R7),DEVID       IS THIS THE DEVICE
         BE    GOTDEV              YEP. SET REGS AND RETURN
         TM    0(R7),X'80'         IS IT END OF DEVICES...
         BO    GETGTFRC            YES. NOW DISREGARD RECORD
         A     R7,=F'12'           CONTINUE LOOP FOR DEVICE
         B     LOPP2               CONTINUE.......
*        THIS CHANGE MADE TO MVS DASDMVS APRIL 12,1976
         TITLE '       PRINT ROUTINES'
PRTANAL  EQU   *
         ST    14,PRTRET
         MVC   TIMEIN(4),STRTTIME
         BAL   14,SECCVT
         MVC   HEAD2+14(8),TIMEOUT
         MVC   TIMEIN(4),ENDTIME
         BAL   14,SECCVT
         MVC   HEAD2+117(8),TIMEOUT
         BAL   14,PUTHEAD
         LA    PRTREG,PRTREC
         L     R6,CDTBLA      LOAD CDTAB POINTER FOR LISTING
CLRLINE  EQU   *
         L     R7,ADEVTABA         LOAD R7 WITH DEVTAB FIRST DEVICE
         L     DEVREG,4(,R7)       LOAD R3 WITH STATS FOR UNIT
         SP    TOTSIO,TOTSIO       CLEAR AREAS FOR TOTALS
         SP    TOTSDB,TOTSDB
         SP    TOTSCUB,TOTSCUB
         SP    TOTIO,TOTIO
         SP    TOTIODE,TOTIODE
         SP    TOTSDBT,TOTSDBT
         SP    TOTPDBT,TOTPDBT
         SP    TOTSIO2,TOTSIO2
CLRLINE1 DS    0H
         BAL   14,CLEAR
         MVC   PRTADDR(3),DEVADDR
         L     R8,DEVSIO
         CVD   R8,TEMP1
         AP    TOTSIO(4),TEMP1+4(4)       ADD TO TOTALS
         MVC   PRTSIO(9),MASK1
         ED    PRTSIO(9),TEMP1+4
         L     R8,DEVSIODB
         CVD   R8,TEMP1
         AP    TOTSDB(4),TEMP1+4(4)       ADD TO TOTALS
         MVC   PRTSIODB(9),MASK1
         ED    PRTSIODB(9),TEMP1+4
         L     R8,DEVSIOCB
         CVD   R8,TEMP1
         AP    TOTSCUB(4),TEMP1+4(4)       ADD TO TOTALS
         MVC   PRTSIOCB(9),MASK1
         ED    PRTSIOCB(9),TEMP1+4
         L     R8,DEVSIO2          LOAD SIO CC 2 COUNTER
         CVD   R8,TEMP1            CONVERT TO DEC
         AP    TOTSIO2(4),TEMP1+4(4)  ADD TO TOTALS
         MVC   PRTSIO2(9),MASK1    MOVE IN PRT MASK
         ED    PRTSIO2(9),TEMP1+4  EDIT FOR PRINT
         L     R8,DEVIO
         CVD   R8,TEMP1
         AP    TOTIO(4),TEMP1+4(4)       ADD TO TOTALS
         MVC   PRTIO(9),MASK1
         ED    PRTIO(9),TEMP1+4
         L     R8,DEVIODE
         CVD   R8,TEMP1
         AP    TOTIODE(4),TEMP1+4(4)       ADD TO TOTALS
         MVC   PRTIODE(9),MASK1
         ED    PRTIODE(9),TEMP1+4
         MVC   TEMP1(8),DEVTTME
         AP    TOTSDBT(8),TEMP1(8) ADD TO TOTAL SIM TIME
         AP    TEMP1(8),=PL8'50000'
         DP    TEMP1(8),=PL4'100000'
         MVC   PRTTTME(10),MASK2
         ED    PRTTTME(10),TEMP1
         MVC   TEMP1(8),DEVPTME
         AP    TOTPDBT(8),TEMP1(8) ADD TO TOTAL PRO TIME
         AP    TEMP1(8),=PL8'50000'
         DP    TEMP1(8),=PL4'100000'
         MVC   PRTPTME(10),MASK2
         ED    PRTPTME(10),TEMP1
         CLC   DEVTTME(8),=PL8'50000'
         BL    NOTIME
         MVC   TEMP1(8),DEVTTME
         SP    TEMP1(8),DEVPTME
         AP    TEMP1(8),=PL8'50000'
         DP    TEMP1(8),=PL4'100000'
         MVC   TEMP1+4(4),TEMP1
         MVC   TEMP1(4),=F'0'
         MVC   TEMP2(8),DEVTTME
         AP    TEMP2(8),=PL8'50000'
         DP    TEMP2(8),=PL4'100000'
         MP    TEMP1(8),=PL3'1000'
         DP    TEMP1(8),TEMP2(4)
         MVC   PRTPRCNT(5),MASK3
         ED    PRTPRCNT(5),TEMP1+2
         MVI   PRTPRCNT+5,X'6C'
         MVC   PRTAVGS(8),MASK4
         MVC   TEMP1(8),DEVTTME
         CLC   DEVIO,=F'0'
         BE    EDPCNT
         L     R8,DEVIO
         CVD   R8,TEMP2
         DP    TEMP1(8),TEMP2+4(4)
         AP    TEMP1(4),=PL4'50'
         ED    PRTAVGS(8),TEMP1
         MVC   TEMP1(8),DEVPTME
         MVC   PRTAVGP(8),MASK4
         DP    TEMP1(8),TEMP2+4(4)
         AP    TEMP1(4),=PL4'50'
         ED    PRTAVGP(8),TEMP1
         MVC   PRTABUSY(8),MASK4
         MVC   TEMP1(8),DEVPTME
         L     R8,RPORTINT
         CVD   R8,TEMP2
         DP    TEMP1(8),TEMP2+5(3)
         AP    TEMP1(5),=PL2'500'
         ZAP   TEMP2(6),TEMP1(5)
         DP    TEMP2(6),=PL3'1000'
         ED    PRTABUSY(8),TEMP2
EDPCNT   EQU   *
         PUT   PRTDCB,PRTREC
         L     R8,LINECTR
         LA    R8,1(R8)
         C     R8,=F'66'
         BH    NEEDHEAD
         ST    R8,LINECTR
         B     CHKNXT
NOTIME   EQU   *
         MVC   TEMP1(4),=PL4'0'
         B     EDPCNT
NEEDHEAD EQU   *
         BAL   14,PUTHEAD
         MVC   LINECTR(4),=F'0'
         B     CHKNXT
CHKNXT   EQU   *
         TM    SW,SECOND           IS THIS SECOND TIME SECOND UNIT
         BO    TOTALIT             YES. MAKE TOTAL LINE
         MVC   PRTADDR(3),TOTAL
         L     R7,ADEVTABB         LOAD SECOND DEVTAB ENTRY
         LTR   R7,R7               DOES SECOND EXIST
         BZ    E1PCNT              NO. TOTAL UNIT AND CONTINUE
         L     DEVREG,4(,R7)       LOAD STAT FOR THAT UNIT
         OI    SW,SECOND           TURN ON SECOND UNIT SW
         B     CLRLINE1            PUT OUT SECOND LINE
PRTDONE  EQU   *
         MVC   LINECTR(4),=F'0'
         L     14,PRTRET
         BR    14
PUTHEAD  EQU   *
         ST    14,HEADRET
         PUT   PRTDCB,HEAD1
         PUT   PRTDCB,PRTDUM
         PUT   PRTDCB,HEAD2
         PUT   PRTDCB,PRTDUM
         PUT   PRTDCB,HEAD3
         PUT   PRTDCB,HEAD4
         PUT   PRTDCB,HEAD5
         PUT   PRTDCB,PRTDUM
         PUT   PRTDCB,PRTDUM
         L     14,HEADRET
         BR    14
CLEAR    EQU   *
         MVI   PRTREC,X'40'
         MVC   PRTREC+1(132),PRTREC
         BR    14
         TITLE 'TOTAL ROUTINE FOR EACH DEVICE DONE HERE'
TOTALIT  EQU   *              COME HERE TO TOTAL DEVICE
         BAL   14,CLEAR
         MVC   PRTADDR(3),TOTAL
         MVC   PRTSIO(9),MASK1
         ED    PRTSIO(9),TOTSIO
         MVC   PRTSIODB(9),MASK1
         ED    PRTSIODB(9),TOTSDB
         MVC   PRTSIOCB(9),MASK1
         ED    PRTSIOCB(9),TOTSCUB
         MVC   PRTSIO2(9),MASK1    MOVE IN PRT MASK
         ED    PRTSIO2(9),TOTSIO2  EDIT FOR PRINT
         MVC   PRTIO(9),MASK1
         ED    PRTIO(9),TOTIO
         MVC   PRTIODE(9),MASK1
         ED    PRTIODE(9),TOTIODE
         MVC   TEMP1(8),TOTSDBT
         AP    TEMP1(8),=PL8'50000'
         DP    TEMP1(8),=PL4'100000'
         MVC   PRTTTME(10),MASK2
         ED    PRTTTME(10),TEMP1
         MVC   TEMP1(8),TOTPDBT
         AP    TEMP1(8),=PL8'50000'
         DP    TEMP1(8),=PL4'100000'
         MVC   PRTPTME(10),MASK2
         ED    PRTPTME(10),TEMP1
         CLC   TOTSDBT(8),=PL8'50000'
         BL    N1TIME
         MVC   TEMP1(8),TOTSDBT
         SP    TEMP1(8),TOTPDBT
         AP    TEMP1(8),=PL8'50000'
         DP    TEMP1(8),=PL4'100000'
         MVC   TEMP1+4(4),TEMP1
         MVC   TEMP1(4),=F'0'
         MVC   TEMP2(8),TOTSDBT
         AP    TEMP2(8),=PL8'50000'
         DP    TEMP2(8),=PL4'100000'
         MP    TEMP1(8),=PL3'1000'
         DP    TEMP1(8),TEMP2(4)
         MVC   PRTPRCNT(5),MASK3
         ED    PRTPRCNT(5),TEMP1+2
         MVI   PRTPRCNT+5,X'6C'
         MVC   PRTAVGS(8),MASK4
         MVC   TEMP1(8),TOTSDBT
         CP    TOTIO(4),=PL4'0'
         BE    E1PCNT
         MVC   TEMP2(4),=F'0'     MOVE IN ZEROES
         MVC   TEMP2+4(4),TOTIO
         DP    TEMP1(8),TEMP2+4(4)
         AP    TEMP1(4),=PL4'50'
         ED    PRTAVGS(8),TEMP1
         MVC   TEMP1(8),TOTPDBT
         MVC   PRTAVGP(8),MASK4
         DP    TEMP1(8),TEMP2+4(4)
         AP    TEMP1(4),=PL4'50'
         ED    PRTAVGP(8),TEMP1
         MVC   PRTABUSY(8),MASK4
         MVC   TEMP1(8),TOTPDBT
         L     R8,RPORTINT
         CVD   R8,TEMP2
         DP    TEMP1(8),TEMP2+5(3)
         AP    TEMP1(5),=PL2'500'
         ZAP   TEMP2(6),TEMP1(5)
         DP    TEMP2(6),=PL3'1000'
         ED    PRTABUSY(8),TEMP2
E1PCNT   EQU   *
         MVC   PRTLONG(8),MASK4 MOVE IN MASK FOR EDIT
         MVC   TEMP2(8),SIOLONG   MOVE DATA TO TEMP STOR
         DP    TEMP2(8),=PL4'100'   DIVIDE BY 100 TO SCALE
         ED    PRTLONG(8),TEMP2    EDIT INTO PRINT LINE
         PUT   PRTDCB,PRTREC      PUT OUT INFO  LINE
         PUT   PRTDCB,PRTDUM      PUT OUT DUMMY LINE
         L     R8,LINECTR
         LA    R8,2(R8)
         C     R8,=F'66'
         BH    N1EDHEAD
         ST    R8,LINECTR
         B     C1KNXT
N1TIME   EQU   *
         MVC   TEMP1(4),=PL4'0'
         B     E1PCNT
N1EDHEAD EQU   *
         BAL   14,PUTHEAD
         MVC   LINECTR(4),=F'0'
         B     C1KNXT
         TITLE 'CLEAR UP EACH ENTRY AND CONTINUE'
C1KNXT   EQU   *
*        CLEAR EACH DEVSTAT AREAS AND CDTAB AREAS
         NI    SW,X'FF'-SECOND     CLEAR THE BIT
         MVC   SIOFIRST(8),=PL8'0'   CLEAR THE SAVE AREAS
         MVC   SIOLAST(8),=PL8'0'
         MVC   SIOLONG(8),=PL8'0'
         L     R7,ADEVTABA        LOAD DEVTAB UNIT A
         L     DEVREG,4(,R7)      LOAD DEVSTAT POINTER
         BAL   R15,CLRDEV         CLEAR AREAS
         L     R7,ADEVTABB        LOAD SECON ADCON FOR UNIT B
         LTR   R7,R7              TEST FOR THE SECOND UNIT
         BZ    LETGO2             NO SECOND UNIT CONTINE
         L     DEVREG,4(,R7)      LOAD STAT ADCON
         BAL   R15,CLRDEV         CLEAR THE DEVICE
LETGO2   TM    0(R6),X'80'    IS IT END OF CDTAB
         BO    PRTDONE        RETURN TO MAINLINE
         LA    R6,40(,R6)     GO TO NEXT ENTRY
         B     CLRLINE        GO GO
         SPACE 5
CLRDEV   EQU   *              COME HERE TO CLEAR DEVSTAT
         MVI   DEVFLAG,0      CLEAR DEVFLAG
*        XC    DEVSIO(L'DEVLGTH-DEVSIO,DEVREG),DEVSIO(DEVREG)
         XC    4(56,DEVREG),4(DEVREG)
         MVC   DEVTTME(8),=PL8'0' CLEAR DEVTIME
         MVC   DEVPTME(8),=PL8'0'
         BR    R15                RETURN
         TITLE '    END OF PROGRAM CLOSE AND EXIT'
ENDPGM   EQU   *
         SNAP  DCB=SNAPDCB,LIST=SNAPLIST
         BAL   14,PRTANAL
         CLOSE (CRDCB)
         CLOSE (GTFDCB)
         CLOSE (PRTDCB)
         CLOSE (SNAPDCB)
         L     13,MYSAVE+4
         LM    14,12,12(13)
         XR    15,15
         BR    14
ABND101  ABEND 101,DUMP
         TITLE 'CONSTANTS AND DATA AREAS FOR '
PRTRET   DS    A
HEADRET  DS    A
RETADDR  DC    A(0)
GTFTCVT  DC    F'0'
TIMEIN   DS    F
TIMEOUT  DS    8C
RPORTINT DS    F
STRTTIME DS    F
ENDTIME  DS    F
TIMESW   DC    X'00'
TEMP1    DS    2D
TEMP2    DS    2D
OLDTIME  DS    2D
NEWTIME  DS    2D
MICBIN   DS    D
MICDEC   DS    2D
MICTAB   DS    0D
         DC    XL8'000000000000001C'
         DC    XL8'000000000000256C'
         DC    XL8'000000000065536C'
         DC    XL8'000000016777216C'
         DC    XL8'000004294967296C'
         DC    XL8'001099511627776C'
         DC    XL8'281474976710656C'
DEVID    DS    CL2
CHAR     DC    F'0'
TRTAB    DC    256X'00'
TOTSIO   DC    PL4'0'   SIO    TOTAL COUNTERS
TOTSIO2  DC    PL4'0'   SIO  2  TOTAL COUNTERS
TOTSDB   DC    PL4'0'   SIO DB  TOTAL COUNTERS
TOTSCUB  DC    PL4'0'   SIO CU  TOTAL COUNTERS
TOTIO    DC    PL4'0'    IO     TOTAL COUNTERS
TOTIODE  DC    PL4'0'    IO  DE TOTAL COUNTERS
TOTSDBT  DC    PL8'0'    SIM DEVICE BUSY TOTAL
TOTPDBT  DC    PL8'0'    PRO DEVICE BUSY TOTAL
DEVTABA  DC    F'0'                FULL WORD FOR GETMAIN RET ADDR
CDTBLA   DC    F'0'                FULL WORD FOR GETMAIN RET ADDR
STATA    DC    F'0'                FULL WORD FOR GETMAIN RET ADDR
SW       DC    X'00'               SWITCH FOR SEVERAL THINGS
TOTAL    DC    CL3'TOT'
SECOND   EQU   2                   SET FRO SECOND UNIT IN PRINT
SNAPLIST DC    3D'0'
LINECTR  DC    F'0'
PRTREC   DS    133C
PRTDUM   DC    133C' '
HEAD1    DC    CL133'1                  S H A R E D   D A S D   C O N TX
                E N T I O N   A N A L Y S I S'
HEAD2    DC    CL133' START TIME =                                     X
                                                                       X
               END TIME =         '
HEAD3    DC    C' DEV'
         DC    CL6' '
         DC    C'TOTAL'
         DC    CL5' '
         DC    C'TOTAL'
         DC    CL5' '
         DC    C'TOTAL'
         DC    CL5' '
         DC    C'TOTAL'
         DC    CL5' '
         DC    C'TOTAL'
         DC    CL5' '
         DC    C'TOTAL'
         DC    CL7' '
         DC    C'TOTAL'
         DC    CL7' '
         DC    C'TOTAL'
         DC    CL3' '
         DC    CL5' '
HEAD4    DC    CL11' '
         DC    C'SIO'
         DC    CL7' '
         DC    C'SIO'
         DC    CL7' '
         DC    C'SIO'
         DC    CL7' '
         DC    C'SIO'
         DC    CL8' '
         DC    C'IO'
         DC    CL8' '
         DC    C'IO'
         DC    CL9' '
         DC    C'SIM'
         DC    CL8' '
         DC    C'PROD'
         DC    CL5' '
         DC    C'CONT'
         DC    CL5' '
         DC    C'SIM'
         DC    CL4' '
         DC    C'PROD'
         DC    CL4' '
         DC    C'BUSY'
         DC    CL5' '
         DC    C'SIO'
         DC    CL6' '
HEAD5    DC    CL21' '
         DC    C'CC2'
         DC    CL6' '
         DC    C'D.B.'
         DC    CL6' '
         DC    C'C.U.'
         DC    CL18' '
         DC    C'DE'
         DC    CL8' '
         DC    C'DB TME'
         DC    CL6' '
         DC    C'DB TME'
         DC    CL9' '
         DC    C'I/O'
         DC    CL9' '
         DC    C'I/O'
         DC    CL6' '
         DC    C'PROD'
         DC    CL16' '
MASK1    DC    XL9'40202020206B202120'
MASK2    DC    XL10'402020206B2020214B20'
MASK3    DC    XL5'4020214B20'
MASK4    DC    XL8'40206B2020214B20'
CRDIN    DS    CL80
PATCH    DC    S(*)                ESTABLISH ADDR TO PATCH AREA
AREA     DC    50F'0'              REAL PATCH AREA
         TITLE 'DCBS FOR ALL INPUT OUTPUT FILES'
CRDCB    DCB   DSORG=PS,DDNAME=SYSIN,MACRF=GM,EODAD=ENDCRD
GTFDCB   DCB   DSORG=PS,DDNAME=DDGTF,MACRF=GM,EODAD=ENDPGM,            X
               RECFM=VBS
PRTDCB   DCB   DSORG=PS,DDNAME=SYSPRINT,RECFM=FBA,MACRF=PM,            X
               BLKSIZE=133,LRECL=133
SNAPDCB  DCB   DSORG=PS,DDNAME=SNAP,RECFM=VBA,                         X
               MACRF=(W),BLKSIZE=882,LRECL=125
IOAREA   DS    CL1024
         TITLE 'DSECTS FOR INPUT RECORDS FROM GTF TAPE'
GTFREC   DSECT
GTFLTH   DS    H
         DS    H
GTFAID   DS    1C
GTFFID   DS    1C
GTFTIME  DS    8C
GTFEID   DS    2C
GTFASCB  DS    4C
GTFCPUID DS    2C
GTFDATA  DS    0C
SIODATA  DS    0CL43
SIOJOBNM DS    CL8
SIOCCWAD DS    CL8
SIOPURGR DS    CL4
SIOCC    DS    CL1
SIODEVAD DS    CL2
SIOCAW   DS    CL4
SIOSTAT  DS    CL2
SIOSEEK  DS    CL8
SIOFLASG DS    CL6
         ORG   SIODATA
IODATA   DS    0CL53
IOJOBNM  DS    CL8
IOPSW    DS    CL8
IORQETCB DS    CL4
IOCSW    DS    CL8
IOCCWAD  DS    CL8
IOSENSE  DS    CL2
IODEVAD  DS    CL2
IOPURGE  DS    CL4
IOFLAGS  DS    CL9
GTFCSW   EQU   IOCSW
GTFCC    EQU   SIOCC
         TITLE 'DSECT OF STAT AREA FOR EACH DEVICE.'
DEVBLK   DSECT
DEVFLAG  DS    1X        CONTROL FLAGS
DEVFLGB0 EQU   X'80'     FIRST SIO TIME STORED
DEVFLGB1 EQU   X'40'     LAST SIO TIME STORED
DEVFLAG2 EQU   X'20'     FIRST SIO TIME FOR DEVICE STORED..
DEVADDR  DS    3C        DEVICE UCB ADDRESS
DEVSIO   DS    F         TOTAL SIO INTERRUPTS
DEVSIODB DS    F         TOTAL SIO INTERRUPTS WITH DEVICE BUSY
DEVSIOCB DS    F         TOTAL SIO INTERRUPTS WITH C. U. BUSY
DEVIO    DS    F         TOTAL I/O INTERRUPTS
DEVIODE  DS    F         TOTAL I/O INTERRUPTS WITH DEVICE END ONLY
DEVTTME  DS    D         TOTAL SIMULATED DEVICE BUSY TIME
DEVPTME  DS    D         TOTAL PRODUCTIVE DEVICE BUSY TIME
DEVSIOTF DS    D         FIRST SIO TIME TEMP STORAGE
DEVSIOTL DS    D         LAST SIO TIME TEMP STORAGE
DEVSIO2  DS    F                   TOTAL SIO CC 2 CHANNEL BUSY
DEVLGTH  EQU   *-DEVBLK            LENGTH OF DSECT....
         TITLE 'DSECT FOR PRINT LINE OF STATS  '
PRTDSCT  DSECT
         DS    1C
PRTADDR  DS    3C
         DS    1C
PRTSIO   DS    10C
PRTSIO2  DS    10C
PRTSIODB DS    10C
PRTSIOCB DS    10C
PRTIO    DS    10C
PRTIODE  DS    10C
         DS    3C
PRTTTME  DS    10C
         DS    1C
PRTPTME  DS    10C
         DS    3C
PRTPRCNT DS    6C
PRTAVGS  DS    8C
PRTAVGP  DS    8C
PRTABUSY DS    8C
PRTLONG  DS    8C
         TITLE 'DSECT FOR DCTAB ENTRIES. BUILT BY CARD INPUT'
CDTAB    DSECT                     DSECT FOR USE WITH INPUT ENTRIES
         DS    CL1
UNITP    DS    CL3                 PRIMARY UNIT ADDRESS
         DS    CL1
UNITS    DS    CL3                 SECONDARY UNIT ADDRESS
ADEVTABA DS    F                   ADDR OF DEVTAB ENTRY FOR PRIME UNIT
ADEVTABB DS    F                   ADDR OF DEVTAB ENTRY FOR SECOND UNIT
SIOFIRST DS    D                   FIRST SIO ATTEMP TIME
SIOLAST  DS    D                   SIO  SUCCESSFUL CC ZERO TIME
SIOLONG  DS    D              LONGEST SIO FOR COMPLETE DEVICE
CDTABLG  EQU   *-CDTAB        LENGTH OF DSECT ENTRY
         END
