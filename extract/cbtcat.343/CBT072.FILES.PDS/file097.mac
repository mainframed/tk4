*          DATA SET ANBMONR    AT LEVEL 001 AS OF 18/02/76
MONR     CSECT
         B     ENTRY-MONR(15)
         DC    X'06',CL7'MONR'
ENTRY    EQU   *
*
**  ROUTINE LOGS SRM CONTROL BLOCKS - ICT,CCT,MCT,RMPT,RMCA
**  LINKAGE IS MONR(INPUT,MONRT,TOD)
*
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW10     EQU   10
RW15     EQU   15
*
         STM   14,12,12(13)            SAVE REGISTERS
         BALR  RB2,0
         USING *,RB2
         ST    13,SAVE+4
         LR    RW3,13
         LA    13,SAVE
         ST    13,8(RW3)
*
         LM    RW3,RW4,4(1)            PICK UP PARAMS
         MVC   ADRT(4),0(RW3)          ADDRESS OF MONRT
         MVC   TOD(4),0(RW4)           MOVE TOD
*
**  FIND START OF RMCT
*
         L     RW3,16                  ADDRESS OF CVT
         L     RW3,604(RW3)            ADDRESS OF RMCT
         LA    RW5,101                  NORMAL LENGTH OF DATA
         TM    FIRST,X'FF'             FIRST TIME IN?
         BNZ   NOTFIRST
         MVI   FIRST,X'FF'             SET FLAG
         LA    RW5,180                 FIRST TIME LENGTH
         L     RW4,4(RW3)              ADDRESS OF CCT
         MVC   CCC(76),0(RW4)          MOVE IN CONSTANTS
         L     RW4,8(RW3)              ADDRESS OF ICT
         MVC   ICC(64),0(RW4)          MOVE IN ICT CONSTANTS
         L     RW4,12(RW3)             ADDRESS OF MCT
         MVC   MCC(80),0(RW4)          MOVE MCT CONSTANTS
         L     RW4,16(RW3)             ADDRESS OF RMPT
         MVC   RMPT(96),0(RW4)         MOVE RMPT
NOTFIRST EQU   *
         ST    RW5,LENGTH              STORE DATA LENGTH
         L     RW4,4(RW3)              ADDRESS OF CCT
         MVC   CCT(52),76(RW4)         MOVE IN CCT VARIABLES
         L     RW4,8(RW3)              ADDRESS OF ICT
         MVC   ICT(24),64(RW4)         MOVE IN ICT VARIABLES
         L     RW4,12(RW3)             ADDRESS OF MCT
         MVC   MCT(72),80(RW4)         MOVE IN MCT VARIABLES
         L     RW4,20(RW3)             ADDRESS OF RMCA
         MVC   RMCA1(16),4(RW4)         WORKLOAD LEVELS
         MVC   RMCATOI(4),40(RW4)       EXPCT INT TIME
         MVC   RMCATAN(4),48(RW4)       LAST ANALYSIS TIME
         MVC   RMCAIST(8),88(RW4)       LAST SWAPIN
         MVC   RMCASAS(4),164(RW4)      SWAP SIZE
         SR    RW6,RW6
         ST    RW6,RMCASID             ZERO ASIDS
         MVC   RMCASID+4(16),RMCASID
         L     RW10,116(RW4)           END LIST
         L     RW4,112(RW4)            START LIST
         LA    RW5,RMCAJOB              POINT AT JOBNAMES
         LA    RW6,RMCASID              POINT AT ASIDS
         LA    RW7,10                   COUNTER
NEXT     EQU   *
         MVC   0(8,RW5),NULL           ZERO JOBNAME
         L     RW8,0(RW4)               OUCB ADDRESS
         LTR   RW8,RW8                  ENTRY?
         BZ    INC                      NO,BRANCH
         CLC   0(4,RW8),OUCB           CHECK BLOCK ID
         BNE   INC
         L     RW8,32(RW8)              ASCB ADDRESS
         CLC   0(4,RW8),ASCB           CHECK BLOCK ID
         BNE   INC
         LH    RW9,36(RW8)              ASID
         STH   RW9,0(RW6)
         L     RW9,172(RW8)             JOBNAME POINTER
         LTR   RW9,RW9                  EXIST?
         BNZ   JOB
         L     RW9,176(RW8)             OTHER POINTER
         LTR   RW9,RW9                  EXIST?
         BNZ   JOB
INC      EQU   *
         LA    RW9,NULL
JOB      EQU   *
         MVC   0(8,RW5),0(RW9)          MOVE IN JOBNAME
         CR    RW4,RW10                END OF LIST?
         BE    ZERASID
         LA    RW4,4(RW4)               OUCB POINTER
         LA    RW5,8(RW5)               JOBNAME POINTER
         LA    RW6,2(RW6)               ASID POINTER
         BCT   RW7,NEXT
ZERASID  EQU   *
         L     RW4,28(RW3)             ADDRESS OF RLCT
         MVC   RLCT(36),0(RW4)         MOVE IN RLCT
         MVC   BTOD(4),128(RW3)        MOVE IN SRM BASE TIME
         MVC   BSRM(4),132(RW3)        MOVE CURRENT SRM TIME
         LA    RW3,4                   NUMBER OF LCH'S SUPPORTED
         L     RW4,16                  CVT
         L     RW4,140(RW4)            LCH TABLE
         LA    RW5,LCHUSE
NXTLCH   EQU   *
         L     RW6,16(RW4)             TCH POINTER
         LTR   RW6,RW6                 ZERO?
         BNZ   GOTLCH
*
**  ZERO OUT FOR NON-CHANNEL
*
         SR    RW6,RW6
         ST    RW6,0(RW5)
         ST    RW6,4(RW5)
         B     NOLCH
GOTLCH   EQU   *
         MVC   0(2,RW5),0(RW6)
         MVC   2(2,RW5),24(RW4)        TOTAL I/O STARTED
         MVC   4(4,RW5),28(RW4)        LOGICALLY/PHYSICALLY BUSY
         LA    RW4,32(RW4)             NEXT LCH ENTRY
         LA    RW5,8(RW5)              NEXT SLOT
         BCT   RW3,NXTLCH
NOLCH    EQU   *
         L     RW3,16         CVT ADDRESS
         L     RW3,356(RW3)   PVT ADDRESS
         MVC   PVTAFC(6),2(RW3)
         MVC   PVTCSA(2),292(RW3)
         MVC   PVTPAGE(32),232(RW3)
*
**  WRITE RECORD TO LOG
*
         LA    RW1,ADARGS              POINT AT PARAM LIST
         L     RW15,ADRT               ADDRESS OF MONRT
         BALR  14,15                   LINK TO MONRT
*
**  RETURN
*
         L     13,4(13)
         LM    14,12,12(13)            RESTORE REGISTERS
         BR    14
*
**  STORAGE AND CONSTANTS
*
FIRST    DC    X'00'
SAVE     DS    18F
ADRT     DS    F
ADARGS   DC    A(NAME)
         DC    A(LENGTH)
         DC    A(TOD)
         DC    A(FLAG)
NAME     DC    CL8'MONR'
OUCB     DC    C'OUCB'
ASCB     DC    C'ASCB'
LENGTH   DS    F
NULL     DC    CL8' '
FLAG     DC    F'0'
TOD      DS    F
CCT      DS    13F
ICT      DS    6F
MCT      DS    18F
RMCA1    DS    4F
RMCATOI  DS    F
RMCATAN  DS    F
RMCAIST  DS    2F
RMCASAS  DS    F
RMCAJOB  DS    20F
RMCASID  DS    5F
LCHUSE   DS    8F
RLCT     DS    9F
BTOD     DS    F
BSRM     DS    F
PVTAFC   DS    F
PVTHRESH DS    H
PVTCSA   DS    H
PVTPAGE  DS    8F
CCC      DS    19F
ICC      DS    16F
MCC      DS    20F
RMPT     DS    24F
         END
