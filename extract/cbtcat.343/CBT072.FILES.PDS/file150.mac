*          DATA SET RECAP      AT LEVEL 001 AS OF 01/11/72
*  THIS PROGRAM WILL PRINT AND/OR PUNCH THE INDEXES,ALIASES,CVOL      *
*  POINTERS,AND DATA SETS IN ANY ONE SYSCATLG DATA SET. PUNCHING      *
*  IS IN THE FORM OF CONTROL CARDS FOR IEHPROGM TO RECONSTRUCT THE    *
*  CATALOG OR TO REORGANIZE IT. LISTING ALSO INCLUDES SOME STATIS-    *
*  TICAL INFORMATION ABOUT THE CATALOG.                               *
*****                                                             *****
*                                                               DWH MSS
*                                                               DWH MSS
*        THIS PROGRAM HAS BEEN MODIFIED TO HANDLE THE NEW CVOL PTR  MSS
*        THE OLD CVOL PTR(TYPE=03) IS NOW AND INDEX CONTROL PTR DWH MSS
*        AND IS SIMPLY FLAGGED BY A CONSOLE MESSAGE             DWH MSS
*        THERE HAS BEEN A FILE ADDED(DISK) TO CONTAIN ALL VOLUMEDWH MSS
*        SERIAL NUMBERS, DEVICE ID, AND DATA SET NAME           DWH MSS
*             A MOD HAS BEEN ADDED FOR CORRECT PRINTING OF GDG  DWH MSS
*        DATA SETS. CHANGES HAVE BEEN MADE FOR MORE THAN ONE PASS   MSS
*        THRU THE CODE FOR PUNCHED DATA SETS                    DWH MSS
*                                                               DWH MSS
*        CHANGES MADE 7/8/70  DWH                               DWH MSS
*
*
*        NEWEST MODIFICATIONS FOR MVS REL 3.0...........
*              1.  LOCATE BY TTR NOT SUPPORTED. CHANGED TO POINT/READ
*                  LOGIC. WILL RUN IN SVS OR MVS
*              2.  NODE LISTING WILL ALSO BE SUPPORTED AS IN MVS
*                  NODE UNDER LISTCVOL IS NOT AVAILABLE. NODE NAME
*                  WILL BE IN HEADING OF EACH PAGE.
*                  NODE LISTS CAN BE DONE BACK TO BACK AS LONG
*                  AS THEY COME FROM THE SAME CATALOG.
*                  NODE INPUT BEGINS IN CARD COLUMN 16 IN INPUT CARD.
*                  (DSNAME/CVOL/INDEX/ETC....)
*                  EXAMPLE..
*
*                  1     7        16
*                  PRINT 30B05    SYS1.
*
*                  CC 1 FUNCTION   CC 7 VOL SER (1-6)  CC 16 NODE
*              3.  FULL CATALOG LIST MUST BE DONE ONE AT A TIME.
*                  NODE LIST CAN PRECEDE AND FOLLOW FULL LIST.
*              4.  DISK DD CARD MUST BE USED AND POINT TO SYSCTLG
*                  ON VOL. EXAMPLE AS FOLLOWS......
*                    //DISK DD DSN=SYSCTLG,DISP=SHR,
*                          VOL=SER=SER,UNIT=UNIT....
*              5.  PUNCH IS STILL SUPPORTED FOR A FULL CATALOG ONLY.
*
*
*        CHANGES MADE AT MELLON BANK,N.A. SEPTEMBER 1975...
*
*
*
*
         EJECT                                                  DWH MSS
RECAPCS1 START 0
R01      EQU   1                            *EQUATE REGISTERS
R02      EQU   2
R03      EQU   3
R04      EQU   4
R05      EQU   5
R06      EQU   6
R07      EQU   7
R08      EQU   8
R09      EQU   9
R12      EQU   12
R14      EQU   14
R15      EQU   15
         EJECT
*
* SYMBOLIC REGISTER NOTATIONS(SPECIAL)
*
RETURNR  EQU   7    *RETURN ADDRESS LINKAGE REGISTER
INDEXPTR EQU   8    *BLOCK AREA INDEX POINTER
PCHPTR   EQU   9    *PUNCH LINE INDEX
QUEUEPTR EQU   12   *QUEUE TABLE INDEX
*
* USING STATEMENTS FOR DSECTS
*
         USING ENTDSECT,INDEXPTR
         USING QELEMENT,QUEUEPTR
         USING VOLINFO,R03
*
*
*
*
         USING *,15                         *ESTABLISH ADDRESSABILITY
RECAPPER DS    0H
         STM   14,12,12(13)                 *SAVE REGS
         DROP  15
         BALR  10,0                         *ESTABLISH REG 10 AS BASE
B0002    EQU   *
         USING *,10
         LR    11,13                        *SAVE SAVE AREA PTR
         LA    13,A0002                     *SET PTR TO NEW SAVE AREA
         ST    13,8(11)
         ST    11,4(13)
         LA    11,B0002+4095                *ESTABLISH REG 11 AS BASE
         USING B0002+4095,11
         B     C0002
A0002    DC    18F'+0'                      *SAVE AREA
C0002    EQU   *
         B     OPENFILE
         DC    C'RECAP '                    *PROGRAM NAME
         DC    C'V01L01'                    *VERSION AND LEVEL
OPENFILE OPEN  (READER,,PRINTER,(OUTPUT))   *OPEN  READER AND PRINTER
MVSOPN   OPEN  (DISK)              /*CATALOG*/                  MVS/3
*        OPEN  (SNAP,(OUTPUT))           /*OPEN SNAP DATASET*/
GETCARD  GET   READER,CARDHOLD              *READ CONTROL CARD
CHKFUNCT CLC   FUNCTION,=C'PRINT'           *PRINT ONLY
         BE    READVICE
         CLC   FUNCTION,=C'PUNCH'           *PUNCH ONLY
         BNE   NXTCHK
         MVI   PCHSW,C'1'                   *PUNCH SW ON
         B     OPENPCH
NXTCHK   CLC   FUNCTION,=C'PTPCH'           *PRINT AND PUNCH
         BNE   ERR01RTN                     *INVALID FUNCTION
         MVI   PTPCHSW,C'1'                 *SET SWITCH ON
OPENPCH  OPEN  (PUNCHER,(OUTPUT))           *OPEN PUNCH
READVICE BAL   RETURNR,NODELIST      /* MVS NODE LIST CHK */    MVS/3
         MVC   TTR,=X'000001'               *ADDR OF FIRT BLOCK
         BAL   RETURNR,READBLK         *READ FIRST BLOCK
         LA    INDEXPTR,BLKAREA+2           *POINT TO V.I.C.E.
         CLI   PCHSW,C'1'                   *CHECK PUNCH-ONLY
         BE    LOCNXTEN
         BAL   RETURNR,HEADING1             *PRINT PAGE HEADING
         MVC   FORMCTRL,SPACE3
         MVC   PRINTWRK(L'STATHEAD),STATHEAD
         BAL   RETURNR,PRINT                *PRINT STAT HEADING
         MVC   FORMCTRL,SPACE2
         MVC   PRINTWRK+9(6),VOLIDHDG
         MVC   PRINTWRK+19(17),BLKSUHDG
         MVC   PRINTWRK+39(17),BLKSLHDG
         BAL   RETURNR,PRINT                *PRINT STAT COL HEADINGS
         MVC   FORMCTRL,SPACE1
         MVC   PRINTWRK+9(9),VOLSERNO
         L     R04,16              LOAD CVT ADDRESS              77056
         L     R04,0(R04)           LOAD PTR TO NEW/OLD TCB      77056
         L     R04,4(R04)           LOAD OUT TCB ADDDRESS        77056
         L     R04,12(R04)          LOAD OUT TIOT ADDRESS        77056
         AH    R04,DISK+40         ADD OUR OFFSET IN DCB         77056
*        R04 WILL NOW HAVE UCB ADDRESS                           77056
         L     R04,16(R04)          LOAD PTR TO UCB              77056
         CLI   19(R04),11          IS IT 3350 (DEVTYPE+3)        77056
         BNE   VOL1               NO. CONTINUE                   77056
         LA    R05,36              SET NUMBER OF ENT /TRACK      77056
         B     ENDVOL             GO TO MAINLINE                 77056
VOL1     CLI   19(R04),9           IS IT A 3330 MOD 1            77056
         BNE   VOL2               NOPE LETS TRY ANOTHER          77056
         LA    R05,28              LOAD NO/TRK                   77056
         B     ENDVOL             CONTINUE MAINLINE              77056
VOL2     CLI   19(R04),13          IS IT A 3330-1 MOD11          77056
         BNE   VOL3               NO. CONTINUE LOOK              77056
         LA    R05,28              LOAD NO/TRK                   77056
         B     ENDVOL             CONTINUE MAINLINE              77056
VOL3     CLI   19(R04),8           IS IT A 2314                  77056
         BNE   VOL4               NO. CONTINUE                   77056
         LA    R05,20              LOAD NO./TRK                  77056
         B     ENDVOL             CONTINUE PROCESSING            77056
VOL4     CLI   19(R04),10          IS IT A 3340                  77056
         BE    VOL5               YES SET FOR 3340               77056
         LA    R05,10              ASSUME 2311                   77056
         B     ENDVOL             CONTINUE                       77056
VOL5     LA    R05,24               SET 3340 DEFAULT             77056
ENDVOL   STH   R05,DEVTYP          STORE FOR LATER               77056
         LH    R03,16(INDEXPTR)             *TT OF FIRST AVAILABLE
         MH    R03,DEVTYP                   *DEVTYPE PER TRK     77056
         SR    R04,R04                      *CLEAR REG
         IC    R04,18(INDEXPTR)             *R OF 1ST AVAILABLE BLK
         AR    R03,R04                      *TOTAL NO OF BLKS USED+1
         BCTR  R04,0                        *DECREMENT BY ONE
         CVD   R03,BLKSUSED
         OI    BLKSUSED+7,X'0F'             *CHANGE SIGN
         UNPK  PRINTWRK+24(5),BLKSUSED+5(3) *MOVE BLKS USED TO PRINT
         LH    R04,12(INDEXPTR)             *LAST TT OF CATALOG
         SR    R05,R05                      *CLEAR REG
         IC    R05,14(INDEXPTR)             *LAST R
         MH    R04,DEVTYP                   *DEVTYPE PER TRK
         AR    R04,R05                      *TOTAL NO OF BLKS IN CATLG
         SR    R04,R03                      *BLKS LEFT
         CVD   R04,BLKSLEFT
         OI    BLKSLEFT+7,X'0F'             *CHANGE SIGN
         UNPK  PRINTWRK+44(5),BLKSLEFT+5(3) *MOVE BLKS LEFT TO PRINT
         BAL   RETURNR,PRINT                *PRINT STATISTICS
         BAL   RETURNR,HEADING2             *PRINT ENTRY HEADINGS
         EJECT
*
* LOCATE NEXT ENTRY ROUTINE-INDEXPTR MUST BE SET AT FIRST BYTE OF LAST
*                           ENTRY EXAMINED-REGISTER 3 IS USED
*
LOCNXTEN SR    R03,R03                      *CLEAR REG
         IC    R03,ENTHWCT                  *NO OF HALFWORDS LEFT
         SLL   R03,1                        *MULTIPLY BY 2
         LA    INDEXPTR,12(R03,INDEXPTR)    *LOCATE NEXT ENTRY
         CLC   ENTNAME,LINKENT              *CHK FOR LINK ENTRY
         BNE   IXPTRCHK
         CLC   ENTTTR,=X'000000'            *CHK FOR END-OF-CHAIN
         BE    CHAINEND
         MVC   TTR,ENTTTR                   *ADDRESS OF NEXT BLK
         BAL   RETURNR,READBLK              *READ NEXT BLOCK IN CHAIN
         LA    INDEXPTR,BLKAREA+2           *SET INDEX POINTER
         CLC   ENTNAME,CNTRLNAM             *CHK FOR INDEX CTRL ENTRY
         BE    ERR02RTN                     *WRONG BLOCK
*
* DETERMINE TYPE OF ENTRY
*
IXPTRCHK CLI   ENTHWCT,X'00'                *CHK FOR INDEX POINTER
         BE    IXPTRRTN
DSCVOLCK CLC   ENTTTR,=X'000000'            *CHK FOR DS PTR OR CVOL PTR
         BNE   VCBPTCHK
         CLI   ENTHWCT,X'05'           *CHK FOR CVOL PTR        DWH MSS
         BE    CVOLPRTN
         B     DSPTRRTN
VCBPTCHK CLI   ENTHWCT,X'01'                *CHK FOR VOL CNTRL BLK PTR
         BE    VCBPTRTN
GENIXCHK CLI   ENTHWCT,X'02'                *CHK FOR GENERATION INDEX
         BE    GENIXRTN
ALIASCHK CLI   ENTHWCT,X'04'                *CHK FOR ALIAS
         BE    ALIASRTN
         CLI   ENTHWCT,X'07'                               AWW PPG
         BE    DSPTRRTN                                    AWW PPG
         CLI   ENTHWCT,X'0D'                               AWW PPG
         BE    DSPTRRTN                                    AWW PPG
         CLI   ENTHWCT,X'13'                               AWW PPG
         BE    DSPTRRTN                                    AWW PPG
         CLI   ENTHWCT,X'19'                               AWW PPG
         BE    DSPTRRTN                                    AWW PPG
         CLI   ENTHWCT,X'1F'                               AWW PPG
         BE    DSPTRRTN                                    AWW PPG
*                                                               DWH MSS
*        CHANGE FOR OLD CVOL PTR WHICH IS NOW AND INDEX CONTROL DWH MSS
*        ENTRY   TYPE=X'03'                                     DWH MSS
         CLI   ENTHWCT,X'03'      * CHK FOR INDEX CONTROL ENTRY DWH MSS
         BNE   ERR03RTN           INVALID ENTRY                 DWH MSS
         WTO   'INDEX CONTROL ENTRY NOTIFY SYSTEM PROGRAMMER'   DWH MSS
         B     LOCNXTEN           TRY AGAIN                     DWH MSS
*
* READ CATALOG BLOCK ROUTINE
*
READBLK  EQU   *                         /* SNAP RTN ADDITION */
READBLKB B     POINTR
         MVI   READBLKB+1,X'F0'          SET THE BRANCH
         GETMAIN  R,LV=4096              GET A PAGE.
         ST    R01,GETMAIN                STORE BEGINNING ADDR
         ST    R01,WORKGET                ST FOR ENTRIES
         LA    R01,4095(R01)               SET UP FOR END ADDR
         LA    R01,1(R01)
         ST    R01,GETMAINE               SET END OF PAGE
         MVI   GETMAINE,X'80'            SET END OF LIST
         SR    R15,R15                   SET R15 TO ZERO
         ST    R15,SNAPNO                SET SNAP NO TO ZERO
*
         STAE  SNAPNOW,CT,XCTL=NO,PURGE=NONE  SET UP THE STAE
*
         LTR   R15,R15                   TEST THE RTN CODE
         BNE   ABENDST                   NO. ABEND THE DUMMY
*
SNAPSET  EQU   *
         L     R01,SNAPNO                 LOAD CURRENT TOTAL OF ENTRIES
         LA    R01,1(R01)                  INCR FOR ENTRY
         C     R01,=F'1024'               IS TABLE FULL
         BL    SNAPITN                   NOPE CONTINUE
*
         SNAP  DCB=SNAP,ID=5,STORAGE=(WORKGET,GETMAINE)
         SNAP  DCB=SNAP,ID=6,LIST=GETMAIN
         LA    R01,1                      SET FOR # ENTRIES
         MVC   WORKGET(4),GETMAIN        RESET BEGINNING
SNAPITN  EQU   *
         L     R14,WORKGET               SET UP FOR MOVE
         MVC   0(4,R14),TTR              SAVE TTR IN TABLE
         LA    R14,4(R14)                SET UP FOR NEXT ENTRY
         ST    R14,WORKGET               STORE
         ST    R01,SNAPNO                 STORE # ENTRIES
         B     POINTR                    GO TO MAIN LINE
         PUSH  USING
SNAPNOW  EQU   *                         /* ABEND SNAP ROUTINE */
         BALR  R15,0                     SET UP BASE
         USING *,R15                     EST USING
         SNAP  DCB=SNAP,ID=7,STORAGE=(WORKGET,GETMAINE)
         SNAP  DCB=SNAP,ID=8,LIST=GETMAIN
         WTO   'PLS FORWARD THIS DUMP TO SP',                          X
               ROUTCDE=11
         LA    R15,0                     SET FOR ABEND TO CONTINUE
         BR    R14                       RETURN TO VS
         DROP  R15                       DROP USING
         POP   USING                     RESET CURRENT REGISTERS
ABENDST  ABEND 999                       ABEND A BAD STAE MACRO
POINTR   POINT DISK,TTR            POINT TO THE RECORD
         READ  READABLK,SF,MF=E    READ A RECORD INTO MEMORY
         CHECK READABLK             CHECK THE IO
         BR    RETURNR             RETURN TO VALLER
READABLK READ  READCTLG,SF,DISK,BLKAREA,256,MF=L
         C     R15,=F'0'           SUCESSSFUL READ               77074
         BCR   8,RETURNR           YES, GO BACK TO CALLER        77074
         C     R15,=F'4'           CHK FOR SER ERROR             77074
         BE    ERR08RTN            YES ABEND                     77074
         C     R15,=F'24'          CHK FOR IO ERR                77074
         BE    ERR04RTN            YES ABEND JOB                 77074
         B     ERR05RTN            INVALID CODE                  77074
NODELIST CLI   NODEL,C' '          NODE LIST WANTED             MVS/3
         BER   RETURNR             NO. GO BACK                  MVS/3
         MVI   NODELST,C'1'          SET SW TO LOOK             MVS/3
         LA    R14,0               SET UP R14 FOR EX MOVE       MVS/3
         LA    R15,NODEL           SET R15 ADDR OF INPUT        MVS/3
         LA    R08,44               SET MAX LENGTH FOR DSNAME   MVS/3
NODELOOP CLI   0(R15),C' '         TEST FOR A BLANK END         MVS/3
         BE    ENDNODE             EQ SET END OF CLC            MVS/3
         LA    R15,1(R15)          GO TO NEXT BYTE              MVS/3
         LA    R14,1(R14)          INCR COUNTER                 MVS/3
         BCT   R08,NODELOOP         LOOP THROUGH THE LIST       MVS/3
ENDNODE  BCTR  R14,0       DECREASE BY ONE                      MVS/3
         STC   R14,NODECOMP+1      SET CLC LENGTH               MVS/3
         BR    RETURNR             RETURN.....                  MVS/3
*
* PRINT PAGE HEADING ROUTINE
*
HEADING1 MVC   FORMCTRL,SKPTOCH1
         MVC   PRINTWRK+60-L'HDGLINE1/2(L'HDGLINE1),HDGLINE1
         ST    RETURNR,SAVERTN1             *SAVE RETURN ADDRESS
         B     NXTINSTN
SAVERTN1 DS    F                            *REGISTER SAVE AREA
NXTINSTN AP    PAGENUMP,=P'+1'              *ADD ONE TO PAGE COUNTER
         UNPK  PAGENUM,PAGENUMP
         OI    PAGENUM+3,X'F0'              *CLEAR SIGN
         MVC   PRINTWRK+109(9),PAGEID
         BAL   RETURNR,PRINT                *PRINT HEADING LINE 1
         MVC   PRINTWRK+56-L'HDGLINE2/2(L'HDGLINE2),HDGLINE2
         MVC   PRINTWRK+58+L'HDGLINE2/2(6),VOLSERNO
         MVC   FORMCTRL,SPACE1
         BAL   RETURNR,PRINT                *PRINT HEADING LINE 2
         L     RETURNR,SAVERTN1             *RESTORE RETURN ADDRESS
         BR    RETURNR                      *RETURN
*
* PRINT ROUTINE
*
PRINT    CLC   PRINTWRK+50(8),=C'DATA SET'  * IS THIS A DATA SETDWH MSS
         BNE   NORPT
         XC    CARD,CARD          * CLEAR CARD                  DWH MSS
         MVC   CARD(6),PRINTWRK+73          * MVE VOLID         DWH MSS
         MVC   CARD+7(6),PRINTWRK+63        * MVE DEVICE        DWH MSS
         MVC   CARD+15(44),PRINTWRK         * MVE NAME          DWH MSS
         MVC   AWWNAME(44),PRINTWRK                        AWW PPG
NORPT    EQU   *
         CLI   NODELST,C'1'        NODE LISTING WANTED          MVS/3
         BNE   PRINTM              NO. PRINT ALL RECORDS        MVS/3
*                                                               MVS/3
*        ELIMINATE THE HEADINGS THROUGH FUNNEL                  MVS/3
*                                                               MVS/3
         CLC   PRINTWRK+50(7),=C'TALOGED'                       MVS/3
         BE    PRINTM                                           MVS/3
         CLC   PRINTWRK+50(7),=C'SYSCTLG'                       MVS/3
         BE    PRINTM                                           MVS/3
         CLC   PRINTWRK+50(7),=C'       '                       MVS/3
         BE    PRINTM                                           MVS/3
         CLC   PRINTWRK+50(7),=C'S LEFT '                       MVS/3
         BE    PRINTM                                           MVS/3
         CLC   PRINTWRK+50(7),=C'NDEXES '                       MVS/3
         BE    PRINTM                                           MVS/3
         CLC   PRINTWRK+50(7),=C'NTRY TY'                       MVS/3
         BE    PRINTM                                           MVS/3
         EX    0,NODECOMP         EXEC THE CLC                  MVS/3
         BL    CHKLIMIT            IGNORE THIS ENTRY            MVS/3
         BH    RDCARD              HIGH DONE                    MVS/3
PRINTM   EQU   *                                                MVS/3
         PUT   PRINTER,PRINTREC    PUT OUT SYSPRINT LINE        MVS/3
         SR    R02,R02                      *CLEAR REG
         TRT   FORMCTRL,TABLE-64            *GET LINE COUNT
         CVD   R02,LINECT08
         AP    LINECTR,LINECT08+7(1)        *INCREMENT CTR
CHKLIMIT MVI   PRINTREC,C' '
         MVC   PRINTWRK,PRINTWRK-1          *CLEAR PRINT AREA
         CP    LINECTR,=P'60'               *CHK FOR LIMIT
         BL    PAGEOK
         ST    RETURNR,SAVERTN2             *SAVE RETURN ADDRESS
         B     FORWARD
SAVERTN2 DS    F                            *REGISTER SAVE AREA
FORWARD  ZAP   LINECTR,=P'0'                *CLEAR LINE COUNTER
         BAL   RETURNR,HEADING1             *PAGE HEADINGS
         BAL   RETURNR,HEADING2             *COLUMN HEADINGS
         L     RETURNR,SAVERTN2             *RESTORE RETURN ASDRESS
PAGEOK   BR    RETURNR                      *RETURN
*
* COLUMN HEADING ROUTINE
*
HEADING2 MVC   FORMCTRL,SPACE3
         MVC   PRINTWRK(L'ENTRYHDG),ENTRYHDG
         CLI   NODELST,C'1'   *NODE LIST                        MVS/3
         BNE   NONODEH        NO. DONT ADD TO HEADING           MVS/3
         MVC   PRINTWRK+73(5),=C'NODE='   ADD TO HEADING        MVS/3
         MVC   PRINTWRK+80(44),NODEL      MOVE NODE IN CARD TO LMVS/3
NONODEH  EQU   *              AROUND CODE BR                    MVS/3
         ST    RETURNR,SAVERTN3             *SAVE RETURN ADDRESS
         B     AROUND
SAVERTN3 DS    F                            *REG SAVE AREA
AROUND   BAL   RETURNR,PRINT
         MVC   FORMCTRL,SPACE2
         MVC   PRINTWRK+9(4),NAMEHDG
         MVC   PRINTWRK+49(10),TYPEHDG
         MVC   PRINTWRK+63(6),DEVICHDG
         MVC   PRINTWRK+73(6),VOLIDHDG
         MVC   PRINTWRK+82(6),SEQNOHDG
         MVC   PRINTWRK+91(8),ALIASHDG
         BAL   RETURNR,PRINT                *PRINT COLUMN HEADINGS
         BE    CHAINEND              YES END THE STUPID THING.
*        ABOVE ENTRY DONE FOR PLBM AT MB DEC 75
*        TTR WAS BEING SET TO ZERO AND CAUSING 001
*        ABENDS.
         MVI   CATLGSW,C'0'                 *TURN SWITCH OFF
         L     R03,NOQENTS
         AH    R03,=H'1'                    *ADD 1 TO NO IN QUEUE
         ST    R03,NOQENTS
         MH    R03,=H'14'                   *MULTIPLY BY ELEMENT LGTH
         LA    QUEUEPTR,QTABLE(R03)         *LOCATE 1ST AVAILABLE SLOT
         MVC   QELTTR,TTR                   *SAVE ADDRESS OF BLK
         LA    R03,BLKAREA
         MVC   QELNAME,ENTNAME              *SAVE INDEX NAME
         MVC   TTR,ENTTTR                   *TTR OF NEXT BLOCK TO READ
         SR    INDEXPTR,R03                 *DISP FROM END OF BLOCK
         STH   INDEXPTR,QELDISP             *SAVE DISP IN QUEUE TABLE
         BAL   RETURNR,READBLK              *READ NEXT BLOCK IN CHAIN
FRSTENCK LA    INDEXPTR,BLKAREA+2           *SET UP ENTRY PTR
         CLC   ENTNAME,CNTRLNAM             *CHK FOR INDEX CNTRL ENTRY
         BNE   ERR06RTN                     *NO INDEX CNTRL ENTRY
         BE    LOCNXTEN
*
* CVOL POINTER ROUTINE
*
CVOLPRTN CLI   PCHSW,C'1'                   *CHK FOR PUNCH ONLY
         MVI   CATLGSW,C'0'                 *TURN CATLG SWITCH OFF
         BE    PCHCVOLP
         MVC   PRINTWRK(8),ENTNAME
         MVC   PRINTWRK+50(8),=C'CVOL PTR'
         MVC   PRINTWRK+73(6),CVOLSRNO
         BAL   RETURNR,PRINT                *PRINT CVOL PTR ENTRY
         CLI   PTPCHSW,C'1'                 *CHK FOR PUNCHING
         BNE   NOPUNCH
PCHCVOLP MVC   OPCODE,=C'CONNECT'           *BUILD CONNECT CARD
         LA    PCHPTR,PARMS                 *SET UP PUNCH LINE PTR
         MVC   0(6,PCHPTR),=C'INDEX='       *INDEX PARAMETER
         LA    PCHPTR,6(PCHPTR)             *INCREMENT PTR
         LA    R03,ENTNAME
         LR    R06,PCHPTR
         BAL   RETURNR,MOVERTN              *MOVE NAME TO PUNCH AREA
         AR    PCHPTR,R05                   *INCREMENT PTR
         MVI   0(PCHPTR),C','               *COMMA
         LA    PCHPTR,1(PCHPTR)             *INCREMENT PTR
         MVC   0(4,PCHPTR),=C'VOL='         *VOL PARAMETER
         MVC   4(6,PCHPTR),=C'SYSDA='
         MVC   10(6,PCHPTR),CVOLSRNO
         MVI   16(PCHPTR),C','              *COMMA
         LA    PCHPTR,17(PCHPTR)            *INCREMENT PTR
         MVC   0(5,PCHPTR),=C'CVOL='        *CVOL PARAMETER
         MVC   5(6,PCHPTR),=C'SYSDA='
         MVC   11(6,PCHPTR),VOLSERNO
         MVI   SORTCODE,C'1'                *CONNECT CARDS 1ST
         BAL   RETURNR,COUNTRTN
         BAL   RETURNR,PUNCH                *PUNCH CONNECT CARD
NOPUNCH  B     LOCNXTEN                     *LOCATE NEXT ENTRY
         EJECT
*
*   SORT COUNTER ROUTINE
*
COUNTRTN UNPK  SORTCODE+1(7),SRTCOUNT       *SORT COUNT IN COLS 74-80
         OI    SORTCODE+7,X'F0'
         AP   SRTCOUNT,=P'1'                *ADD ONE TO COUNT
         BR    RETURNR
         EJECT
*
* NAME MOVE ROUTINE
*
MOVERTN  TRT   0(8,R03),TRANSTBL            *LOOK FOR DELIMITER
         BC    7,NOT8CHAR                   *BRANCH IF NOT 8 CHAR NAME
         L     R05,=F'7'                    *MACHINE LGTH FOR 8
         B     MOVENAME
NOT8CHAR LR    R05,R01
         SR    R05,R03                      *COMPUTE NAME LGTH
         BCTR  R05,0                        *ADJUST TO MACH LGTH
         B     MOVENAME
NAMEMOVE MVC   0(0,R06),0(R03)
MOVENAME EX    R05,NAMEMOVE                 *MOVE NAME
         AH    R05,=H'1'                    *READJUST LGTH TO ACTUAL
         BR    RETURNR
*
* PUNCH ROUTINE
*
PUNCH    PUT   PUNCHER,PUNCHREC             *PUNCH CARD OUT
         MVI   PUNCHREC,C' '
         MVC   PUNCHREC+1(79),PUNCHREC      *CLEAR PUNCH AREA
         BR    RETURNR
         EJECT
*
* ATIAS ROUTINE
*
ALIASRTN CLI   PCHSW,C'1'                   *CHK FOR PUNCH ONLY
         MVI   CATLGSW,C'0'                 *TURN CATLG SWITCH OFF
         BE    PCHALIAS                     *PUNCH ONLY
         MVC   PRINTWRK(8),ENTNAME
         MVC   PRINTWRK+51(5),=C'ALIAS'
         MVC   PRINTWRK+91(8),ALIASTRN
         BAL   RETURNR,PRINT                *PRINT ALIAS ENTRY
         CLI   PTPCHSW,C'1'                 *CHK FOR PUNCHING
         BNE   DONTPCH
PCHALIAS MVC   OPCODE,=C'BLDA   '           *BLDA CARD
         LA    PCHPTR,PARMS                 *SET PTR TO PARM FIELD
         MVC   0(6,PCHPTR),=C'INDEX='       *INDEX PARAMETER
         LA    PCHPTR,6(PCHPTR)             *INCREMENT PTR
         LR    R06,PCHPTR
         LA    R03,ALIASTRN
         BAL   RETURNR,MOVERTN              *MOVE TRUE NAME
         AR    PCHPTR,R05                   *INCREMENT PTR
         MVI   0(PCHPTR),C','               *COMMA
         MVC   1(5,PCHPTR),=C'CVOL='        *CVOL PARAMETER
         MVC   6(6,PCHPTR),=C'SYSDA='
         MVC   12(6,PCHPTR),VOLSERNO
         LA    PCHPTR,18(PCHPTR)            *INCREMENT PTR
         MVI   0(PCHPTR),C','               *COMMA
         MVC   1(6,PCHPTR),=C'ALIAS='       *ALIAS PARAMETER
         LA    PCHPTR,7(PCHPTR)
         LR    R06,PCHPTR
         LA    R03,ENTNAME
         BAL   RETURNR,MOVERTN              *MOVE ALIAS NAME
         MVI   SORTCODE,C'5'                *BLDA CARDS 5TH
         BAL   RETURNR,COUNTRTN
         BAL   RETURNR,PUNCH                *PUNCH BLDA CARD
DONTPCH  B     LOCNXTEN                     XLOCATE NEXT ENTRY
*
* DATA SET POINTER ROUTINE
*
DSPTRRTN BAL   RETURNR,DSPRTRTN             *INITIAL PRINT ROUTINE
         LA    R03,14(INDEXPTR)             *ADDR OF FIRST VOL ENTRY
         LH    R04,DSPVOLCT                 *LOAD NO OF VOLUMES
         BAL   RETURNR,PRINTVOL             *PRINT VOL INFO
         CLI   PTPCHSW,C'1'                 *CHK FOR PUNCHING
         BE    PCHDSRTN                     *PUNCH ROUTINE
         B     LOCNXTEN                     *LOCATE NEXT ENTRY
*
* VOLUME CONTROL BLOCK POINTER ROUTINE
*
VCBPTRTN MVI   VCBSW,C'1'                   *SET SW ON
         BAL   RETURNR,DSPRTRTN             *INITIAL PRINT ROUTINE
         MVC   SAVETTR,TTR                  *SAVE BLOCK ADDR
         MVC   TTR,ENTTTR                   *ADDRESS OF VCB
         BAL   RETURNR,READBLK              *READ NEXT BLK
         LA    R03,BLKAREA                  *SET POINTER
         LH    R04,0(R03)                   *LOAD VOL COUNT
         LA    R03,2(R03)                   *INCREMENT PTR
         BAL   RETURNR,PRINTVOL             *PRINT VOL INFO
         CLI   PTPCHSW,C'1'                 *CHK FOR PUNCHING
         BE    PCHDSRTN                     *PUNCH ROUTINE
         MVC   TTR,SAVETTR                  *ADDRESS OF VCB PTR
         BAL   RETURNR,READBLK              *READ VCB PTR BLOCK
         MVI   VCBSW,C'0'
         B     LOCNXTEN                     *LOCATE NEXT ENTRY
         EJECT
*
* BUILD INDEX NAME ROUTINE
*
BLDNAME  LA    QUEUEPTR,QTABLE+14           *SET PTR ON 1ST ENTRY
         ST    RETURNR,SAVERTN4             *SAVE RETURN ADDR
         B     AHEAD
SAVERTN4 DS    F
AHEAD    L     R04,NOQENTS                  *SET LOOP COUNTER
         LA    R06,NAMEAREA                 *SET POINTER TO BUILD AREA
LOOPER   LA    R03,QELNAME                  *SET UP PTR TO NAME
         BAL   RETURNR,MOVERTN              *MOVE NAME TO BUILD AREA
         AR    R06,R05                      *INCREMENT PTR TO NAME AREA
         MVI   0(R06),C'.'                  *PERIOD
         LA    R06,1(R06)                   *INCREMENT PTR TO NAME AREA
         LA    QUEUEPTR,14(QUEUEPTR)        *INCREMENT QUEUE PTR
         BCT   R04,LOOPER
         LA    R04,NAMEAREA
         SR    R06,R04                      *COMPUTE LENGTH OF INDEX
         STH   R06,NAMELGTH
         L     RETURNR,SAVERTN4             *RESTORE RETURN ADDRESS
         BR    RETURNR                      *RETURN
*
* DSNAME INITIAL PRINT ROUTINE
*
DSPRTRTN ST    RETURNR,SAVERTN8
         B     OVERONE
SAVERTN8 DS    F
OVERONE  CLC   NOQENTS,=X'00000000'         *CHK FOR EMPTY QUEUE
         BE    CHKPCHSW
         CLI   CATLGSW,C'1'                 *CHK FOR NAME ALREADY BUILT
         BE    CHKPCHSW
         MVI   CATLGSW,C'1'                 *SET SW ON
         BAL   RETURNR,BLDNAME              *BUILD INDEX NAME
CHKPCHSW CLI   PCHSW,C'1'                   *CHK FOR PUNCH ONLY
         BE    PCHDSRTN
         MVC   PRINTWRK+50(8),=C'DATA SET'
         LA    R06,PRINTWRK                 *SET PTR TO PRINT AREA
         CLC   NAMELGTH,=X'0000'            *CHK FOR NO INDEX
         BE    MOVDSNAM
         LH    R05,NAMELGTH
         BCTR  R05,0                        *ADJUST TO MACH LGTH
         LA    R03,NAMEAREA
         EX    R05,NAMEMOVE                 *MOVE INDEX TO PRINT
         AH    R05,=H'1'                    *READ LGTH TO ACTUAL
         AR    R06,R05                      *INCREMENT PTR
MOVDSNAM LR    R03,INDEXPTR
         BAL   RETURNR,MOVERTN              *MOVE DSNAME TO PRINT
         CLI   0(INDEXPTR),C'G'             *CHK FOR GDG NAME
         BNE   NOTGDG
         CLI   5(INDEXPTR),C'V'
         BNE   NOTGDG
         TM    6(INDEXPTR),X'F0'
         BC    14,NOTGDG
         TM    7(INDEXPTR),X'F0'
         BC    14,NOTGDG
         TM    1(INDEXPTR),X'F0'                                DWH MSS
         BC    1,NOTGDG                                         DWH MSS
         XC    1(4,R06),=X'FFFFFFFF'        *CHANGE GEN. NO. TO TRUE
NOTGDG   L     RETURNR,SAVERTN8             *LOAD RETURN ADDRESS
         BR    RETURNR
*
* PRINT DATA SET VOLUME INFORMATION ROUTINE
*
PRINTVOL ST    RETURNR,SAVERTN5             *SAVE RETURN ADDRESS
         B     PAST
SAVERTN5 DS    F
PAST     CH    R04,=H'20'                   *CHK FOR MORE THAN 1 BLOCK
         BNH   ONEBLOCK
         LH    R05,=H'20'                   *LOAD LOOP COUNTER
         SH    R04,=H'20'                   *DECREMENT VOL COUNT
         B     STARTPRT
ONEBLOCK LR    R05,R04                      *LOAD LOOP COUNTER
         SR    R04,R04                      *CLEAR VOL COUNT REGISTER
STARTPRT LA    R01,DEVCDTBL                 *PTR TO DEVICE CODE TABLE
CHKNEXT  CLC   0(4,R01),VOLDEVCD            *COMPARE CODE TO TABLE
         BE    FIND                         *BRANCH IF EQUAL
         LH    R02,4(R01)                   *GET LGTH OF VAR TBL PART
         LA    R01,6(R02,R01)               *LOCATE NEXT ENTRY
         CLC   0(4,R01),LSTENTRY            *CHK FOR END OF TABLE
         BNE   CHKNEXT
         UNPK  PRINTWRK+62(9),VOLDEVCD(5)
         MVI   PRINTWRK+70,C' '
         B     MOVSERNO
FIND     B     AROUNDIT
MOVDEVCD MVC   PRINTWRK+63(0),6(R01)
AROUNDIT LH    R02,4(R01)                   *LENGTH OF DEVICE TYPE
         BCTR  R02,0                        *ADJUST TO MACH LGTH
         EX    R02,MOVDEVCD                 *MOVE DEVICE TYPE TO PRINT
MOVSERNO MVC   PRINTWRK+73(6),VOLSRNUM      *VOLUME SERIAL NO
         LH    R01,VOLSEQNO                 *SEQUENCE NO
         CVD   R01,SEQNUMCV
         OI    SEQNUMCV+7,X'0F'             *CLEAR SIGN TO F
         UNPK  PRINTWRK+82(6),SEQNUMCV+4(4)
         BAL   RETURNR,PRINT                *PRINT VOLUME INFO
         AH    R03,=H'12'                   *INCREMENT PTR
         BCT   R05,AWWSTPRT                                AWW PPG
         C     R04,=F'0'                    *CHK FOR MOVE VOL CTRL BLKS
         BE    FINISHED
         MVC   TTR,10(R03)                  *ADDRESS OF NEXT BLK
         BAL   RETURNR,READBLK              *READ NEXT BLK
         LA    R03,BLKAREA+2                *ADDRESS OF 1ST ENTRY
         B     PAST                         *REPEAT
FINISHED L     RETURNR,SAVERTN5             *RESTORE RETURN ADDRESS
         BR    RETURNR                      *RETURN
AWWSTPRT MVC   PRINTWRK+50(8),=C'DATA SET'                 AWW PPG
         MVC   PRINTWRK(44),AWWNAME                        AWW PPG
         B     STARTPRT                                    AWW PPG
AWWNAME  DC    44C' '                                      AWW PPG
*
* DATA SET CATLG CARD PUNCH ROUTINE
*
PCHDSRTN MVC   OPCODE,=C'CATLG  '           *CATLG CARD
         LA    PCHPTR,PARMS                 *SET POINTER
         MVC   0(7,PCHPTR),=C'DSNAME='
         LA    PCHPTR,7(PCHPTR)             *INCREMENT PTR
         LR    R06,PCHPTR
         CLC   NAMELGTH,=X'0000'            *CHK FOR NO INDEX
         BE    DSNAMMOV
         LH    R05,NAMELGTH                 *INDEX LGTH
         BCTR  R05,0                        *ADJUST TO MACH LGTH
         LA    R03,NAMEAREA
         EX    R05,NAMEMOVE                 *MOVE INDEX TO PUNCH
         AH    R05,=H'1'                    *READJUST LGTH TO ACTUAL
         AR    R06,R05                      *INCREMENT PTR
DSNAMMOV CLI   VCBSW,C'0'                   *CHK FOR NO VCB
         BE    BLOCKOK
         CLI   PCHSW,C'1'                   *CHK FOR PUNCH SWITCH ON
         BE    BLOCKOK
         MVC   TTR,SAVETTR                  *ADDRESS OF VCB PTR
         BAL   RETURNR,READBLK              *READ VCB PTR BLK
BLOCKOK  LA    RETURNR,RETURNAD             *SET UP RETURN ADDRESS
         ST    RETURNR,SAVERTN8
         B     MOVDSNAM                     *MOVE DSNAME
RETURNAD LA    PCHPTR,0(R05,R06)            *INCREMENT PTR
         MVI   0(PCHPTR),C','               *COMMA
         LA    PCHPTR,1(PCHPTR)             *INCREMENT PTR
         LA    R06,COL72
         SR    R06,PCHPTR                   *CHK FOR ROOM IN CARD
         CH    R06,=H'18'
         BNL   ROOM
         BAL   RETURNR,NEWCARD
ROOM     MVC   0(5,PCHPTR),=C'CVOL='        *CVOL PARAMETER
         MVC   5(6,PCHPTR),=C'SYSDA='
         MVC   11(6,PCHPTR),VOLSERNO
         MVI   17(PCHPTR),C','              *COMMA
         LA    PCHPTR,18(PCHPTR)            *INCREMENT PTR
         CLI   VCBSW,C'1'                   *CHK FOR VOL CTRL BLK
         BNE   DSPTR
         MVC   SAVETTR,TTR                  *SAVE PRESENT BLOCK ADDR
         MVC   TTR,ENTTTR                   *ADDRESS OF VCB
         BAL   RETURNR,READBLK              *READ NEXT BLOCK
         LA    R03,BLKAREA                  *SET PTR
         B     ACOUPLE
DSPTR    LA    R03,12(INDEXPTR)             *SET PTR
ACOUPLE  LH    R04,0(R03)                   *LOAD VOL COUNT
         LA    R03,2(R03)                   *INCREMENT PTR
         BAL   RETURNR,PUNCHVOL             *PUNCH VOL INFO
         CLI   VCBSW,C'0'                   *CHK FOR VCB
         BE    THEEND
         MVC   TTR,SAVETTR                  *ADDRESS OF VCB PTR
         BAL   RETURNR,READBLK              *READ VCB PTR BLOCK
         MVI   VCBSW,C'0'                   *TURN OFF VCB SWITCH
THEEND   B     LOCNXTEN                     *LOCATE NEXT ENTRY
*
* NEWCARD ROUTINE
*
NEWCARD  ST    RETURNR,SAVERTN9             *SAVE RETURN ADDRESS
         B     ON
SAVERTN9 DS    F
ON       MVI   COL72,C'C'                   *CONT CHAR IN COL 72
         MVI   SORTCODE,C'4'                *CATLG CARDS ARE 4TH
         BAL   RETURNR,COUNTRTN
         BAL   RETURNR,PUNCH                *PUNCH CARD
         LA    PCHPTR,PARMCONT              *SET PTR TO COL 16
         LA    R06,56                       *56 SPACES IN CARD
         L     RETURNR,SAVERTN9             *LOAD RETURN ADDRESS
         BR    RETURNR
         EJECT
         LTORG
         EJECT
*
* PUNCH DATA SET VOLUME INFORMATION ROUTINE
*
PUNCHVOL ST    RETURNR,SAVERTN6             *SAVE RETURN ADDRESS
         B     ALINE
SAVERTN6 DS    F
ALINE    CH    R04,=H'20'                   *CHK FOR MORE THAN 1 BLK
         BNH   UNOBLOCK
         LH    R05,=H'20'                   *LOAD LOOP COUNTER
         SH    R04,=H'20'                   *DECREMENT VOL COUNT
         B     STARTPCH
UNOBLOCK LR    R05,R04                      *LOAD LOOP COUNTER
         SR    R04,R04                      *CLEAR VOL CT REGISTER
STARTPCH LA    R06,COL72
         SR    R06,PCHPTR
         CH    R06,=H'25'                   *CHK FOR ROOM IN CARD
         BNL   WILLFIT
         BAL   RETURNR,NEWCARD              *START ANOTHER CARD
WILLFIT  MVC   0(4,PCHPTR),=C'VOL='
         LA    PCHPTR,4(PCHPTR)             *INCREMENT PTR
         LA    R01,DEVCDTBL                 *SET TABLE PTR
LOOP     CLC   0(4,R01),VOLDEVCD            *CHK DEV CODE AGAINST TABLE
         BE    HIT
         LH    R02,4(R01)                   *LGTH OF VAR PORTION
         LA    R01,6(R02,R01)               *LOCATE NXT TABLE ELEMENT
         CLC   0(4,R01),LSTENTRY            *CHK FOR END OF TABLE
         BE    NOHIT                        *NO DEVICE IN TABLE FOR CD
         B     LOOP
NOHIT    MVI   NOHITSW,C'1'                 *SET SWITCH ON
         LA    R02,6                        *LOAD 6 INTO REG 2
         B     LVSPACE
HIT      B     UNOLINE
DEVCDMOV MVC   0(0,PCHPTR),6(R01)
UNOLINE LH     R02,4(R01)                   *LGTH OF DEV CODE NAME
         BCTR  R02,0                        *ADJUST TO MACH LGTH
         EX    R02,DEVCDMOV                 *MOVE DEV CODE TO PUNCH
         AH    R02,=H'1'                    *READJUST LGTH
LVSPACE  LA    PCHPTR,0(R02,PCHPTR)         *INCREMENT PTR
         SR    R06,R02                      *DECREMENT SPACE CTR
         SH    R06,=H'4'                    *ADJUST SPACE COUNTER
         MVC   0(2,PCHPTR),=C'=('
         LA    PCHPTR,2(PCHPTR)             *INCREMENT PTR
         SH    R06,=H'2'                    *ADJUST SPACE COUNTER
NEXTSET  MVC   0(6,PCHPTR),VOLSRNUM         *MOVE VOL SER TO PUNCH AREA
         LA    PCHPTR,6(PCHPTR)             *INCREMENT PTR
         SH    R06,=H'7'                    *ADJUST SPACE COUNTER
         MVI   0(PCHPTR),C','               *COMMA
         LH    R02,VOLSEQNO                 *LOAD SEQ NO
         CH    R02,=H'0'                    *CHK FOR ZERO (DISK)
         BE    NOSEQNUM
         CVD   R02,SEQNUMCV
         OI    SEQNUMCV+7,X'0F'
         UNPK  1(4,PCHPTR),SEQNUMCV+5(3)    *MOVE SEQ NO TO CARD
         MVI   5(PCHPTR),C','               *COMMA
         LA    PCHPTR,6(PCHPTR)             *INCREMENT PTR
         SH    R06,=H'7'                    *ADJUST SPACE COUNTER
         B     INCPTR
NOSEQNUM LA    PCHPTR,1(PCHPTR)             *INCREMENT PTR
INCPTR   LA    R03,12(R03)                  *INDEX PTR
         BCT   R05,CHKROOM
         B     BLKEND
CHKROOM  CH    R06,=H'13'                   *CHK SPACE LEFT
         BNL   NEXTSET
         BAL   RETURNR,NEWCARD
         B     NEXTSET
BLKEND   CH    R04,=H'0'                    *CHK FOR FINISH
         BE    FINI
         MVC   TTR,10(R03)                  *ADDRESS OF NEXT VCB
         BAL   RETURNR,READBLK              *READ NEXT BLOCK
FINI     BCTR  PCHPTR,0                     *DECREMENT PTR
         MVI   0(PCHPTR),C')'               *CLOSE PARENS
         MVI   SORTCODE,C'4'                *CATLG CARDS ARE 4TH
         BAL   RETURNR,COUNTRTN
         CLI   NOHITSW,C'1'                 *CHK SWITCH ON
         BNE   LOADRTRN                     *BRANCH IF NOT ON
         MVC   FORMCTRL,SPACE2
         MVC   PRINTWRK(30),=C'DEVICE TYPE NOT FOUND IN TABLE'
         BAL   RETURNR,PRINT
         MVC   PRINTWRK(26),=C'SPACE PROVIDED IN CARD NO.'
         MVC   PRINTWRK+27(8),SORTCODE
         MVC   PRINTWRK+36(27),=C'FOR 6 CHARACTER DEVICE NAME'
         BAL   RETURNR,PRINT
         MVI   NOHITSW,C'0'                 *SET SWITCH OFF
         BAL   RETURNR,PRINT                *BLANK LINE
         MVC   RTNCODE,=X'00000004'         *RETURN CODE OF 4
LOADRTRN BAL   RETURNR,PUNCH
         L     RETURNR,SAVERTN6             *LOAD RETURN ADDRESS
         BR    RETURNR
         EJECT
*
* GENERATION INDEX ROUTINE
*
GENIXRTN L     R03,NOQENTS
         AH    R03,=H'1'                    *ADD 1 TO NO IN QUEUE
         ST    R03,NOQENTS
         MH    R03,=H'14'
         LA    QUEUEPTR,QTABLE(R03)         *LOCATE AVAILABLE SLOT
         MVC   QELTTR,TTR                   *SAVE BLOCK ADDR
         LA    R03,BLKAREA                  *SET BLOCK PTR
         LR    R04,INDEXPTR
         SR    R04,R03                      *COMPUTE DISPLACEMENT
         STH   R04,QELDISP                  *SAVE DISPLACEMENT IN BLK
         MVC   QELNAME,ENTNAME              *SAVE INDEX NAME
         CLI   CATLGSW,C'1'                 *CHK CATLG SWITCH ON
         BE    ADDNAME
         CLC   NOQENTS,=F'1'                *CHK FOR MORE NAMES
         BNH   ADDNAME
         BAL   RETURNR,BLDNAME              *BUILD INDEX NAME
         B     CHKPUNCH
ADDNAME  LH    R04,NAMELGTH
         LA    R06,NAMEAREA
         LA    R06,0(R04,R06)               *SET PTR TO POS IN NAMEAREA
         LR    R03,INDEXPTR
         BAL   RETURNR,MOVERTN              *MOVE INDEX NAME TO AREA
         AR    R06,R05
         MVI   0(R06),C'.'                  *PERIOD
         LA    R04,1(R05,R04)               *UPDATE LGTH
         STH   R04,NAMELGTH
CHKPUNCH CLI   PCHSW,C'1'                   *CHK FOR PUNCHING
         BE    PCHBLDG
         CLI   PTPCHSW,C'1'
         BNE   PRTGDG
PCHBLDG  MVC   OPCODE,=C'BLDG   '           *BUILD BLDG CARD
         LA    PCHPTR,PARMS                 *SET POINTER
         MVC   0(6,PCHPTR),=C'INDEX='       *INDEX PARAMETER
         LA    R03,NAMEAREA                 *POINTER TO NAME
         LH    R05,NAMELGTH                 *LGTH OF NAME
         LA    R06,6(PCHPTR)                *POINTER TO PUNCH AREA
         SH    R05,=H'2'                    *ADJUS TO MACH LGTH
         EX    R05,NAMEMOVE                 *MOVE NAME TO PCH
         AH    R05,=H'1'                    *READJUST LGTH
         LA    PCHPTR,0(R05,R06)            *SET POINTER
         MVI   0(PCHPTR),C','               *COMMA
         LA    PCHPTR,1(PCHPTR)             *INCKEMENT PTR
         LA    R06,COL72
         SR    R06,PCHPTR                   *COMPUTE SPACE LEFT IN CARD
         CH    R06,=H'43'                   *CHK TO SEE IF ENOUGH
         BNL   SPACEOK
         MVI   SORTCODE,C'3'                *BLDG CARDS ARE 3RD
         BAL   RETURNR,COUNTRTN
         MVI   COL72,C'C'
         BAL   RETURNR,PUNCH                *PUNCH FIRST CARD OF BLDG
         LA    PCHPTR,PARMCONT
SPACEOK  MVC   0(5,PCHPTR),=C'CVOL='        *CVOL PARAMETER
         MVC   5(6,PCHPTR),=C'SYSDA='
         MVC   11(6,PCHPTR),VOLSERNO
         MVI   17(PCHPTR),C','              *COMMA
         LA    PCHPTR,18(PCHPTR)            *INCREMENT PTR
         MVC   0(8,PCHPTR),=C'ENTRIES='     *ENTIES PARAMETER
         LA    PCHPTR,8(PCHPTR)             *INCREMENT PTR
         SR    R03,R03
         IC    R03,GIMXENTS                 *MAX NO OF ENTRIES
         CVD   R03,SEQNUMCV
         UNPK  0(3,PCHPTR),SEQNUMCV+6(2)
         OI    2(PCHPTR),X'F0'              *F SIGN
         CLI   GIFLAGS,X'00'                *CHK FOR NO MORE PARMS
         BE    THATSALL
         MVI   3(PCHPTR),C','               *COMMA
         LA    PCHPTR,4(PCHPTR)             *INCREMENT PTR
         CLI   GIFLAGS,X'01'
         BE    EMPTY
         CLI   GIFLAGS,X'02'
         BE    DELETE
         CLI   GIFLAGS,X'03'
         BE    EMPTY
         B     ERR07RTN                     *INVALID FLAGS
EMPTY    MVC   0(5,PCHPTR),=C'EMPTY'        *EMPTY PARAMETER
         CLI   GIFLAGS,X'03'
         BNE   THATSALL
         MVI   5(PCHPTR),C','               *COMMA
         LA    PCHPTR,6(PCHPTR)             *INCREMENT PTR
DELETE   MVC   0(6,PCHPTR),=C'DELETE'       *DELETE PARAMETER
THATSALL MVI   SORTCODE,C'3'                *BLDG CARDS ARE 3RD
         BAL   RETURNR,COUNTRTN
         BAL   RETURNR,PUNCH                *PUNCH BLDG CARD
         CLI   PCHSW,C'1'                   *CHK PUNCH SWITCH ON
         BNE   PRTGDG
READNXT  MVI   CATLGSW,C'1'                 *SET SW ON
         CLC   GICURENT,=X'0000'            *CHK FOR ENTRIES
         BNE   SOMEENTS
         B     ADJUSTQ                      *LOCATE NEXT ENTRY
SOMEENTS MVC   TTR,ENTTTR                   *ADDRESS OF DATA SETS
         BAL   RETURNR,READBLK              *READ NEXT BLOCK
         B     FRSTENCK
PRTGDG   LA    R03,NAMEAREA
         LA    R06,PRINTWRK
         LH    R05,NAMELGTH
         BCTR  R05,0
         EX    R05,NAMEMOVE                 *MOVE INDEX NAME
         MVC   PRINTWRK+50(7),=C'GEN.IX.'
         MVC   PRINTWRK+62(8),=C'MAX.NO.='
         SR    R02,R02                      *CLEAR REG
         IC    R02,GIMXENTS                 *MAX NO OF ENTIES
         CVD   R02,SEQNUMCV
         UNPK  PRINTWRK+70(3),SEQNUMCV+6(2)
         OI    PRINTWRK+72,X'F0'
         MVC   PRINTWRK+80(6),=C'FLAGS='
         IC    R02,GIFLAGS                  *EMPTY-DELETE FLAGS
         CVD   R02,SEQNUMCV
         UNPK  PRINTWRK+86(2),SEQNUMCV+6(2)
         OI    PRINTWRK+87,X'F0'
         BAL   RETURNR,PRINT                *PRINT GDG INDEX
         B     READNXT
         EJECT
*
* END OF CHAIN ROUTINE
*
CHAINEND CLC   NOQENTS,=X'00000000'         *CHK FOR FINISH
         BNE   CKCTLGSW
RDCARD   GET   READER,CARDHOLD              *READ ANY EXTRA CTL CARDS
*                                                               DWH MSS
*        CHANGES TO TRY MULTIPLE PASS                           DWH MSS
         MVI   PRINTWRK,C' '  MOVE BLANK TO FIRST LINE          MVS/3
         MVC   PRINTWRK+1(L'PRINTWRK-1),PRINTWRK    CLEAR THE REMVS/3
         LA    R14,QTABLE              LA FOR CLEAR             MVS/3
         LA    R15,322                 SET UP LENGTH            MVS/3
         LA    R08,1                   SET R2 OF MVCL TO 1      MVS/3
         SLR   R09,R09                 CLEAR R9 FOR PAD         MVS/3
         MVCL  R14,R08                  CLEAR QTABLE            MVS/3
         XC    NOQENTS,NOQENTS         CLEAR NOQENTS            MVS/3
         MVI   NOHITSW,C'0'            TURN NOHITSW OFF         MVS/3
         ZAP   SRTCOUNT,=P'0'          ZERO SRTCOUNTER          MVS/3
         MVI   PCHSW,C'0'         * TURN PUNCH SW OFF           DWH MSS
         MVI   PTPCHSW,C'0'       * TURN PTPCH SW OFF           DWH MSS
         MVI   NODELST,C'0'        *RESET NODE LIST POST        MVS/3
RESET    MVI   CATLGSW,C'0'                 *TURN CATLG SWITCH OFF
         MVI   VCBSW,C'0'                   *TURN VCB SWITCH OFF
         ZAP   LINECTR,=P'0'                *ZERO LINE COUNTER
         MVC   NAMEAREA,BLANKS44            *CLEAR NAME AREA
         MVC   NAMELGTH,=X'0000'            *ZERO NAME LENGTH
         ZAP   PAGENUMP,=P'0'               *ZERO PAGE COUNTER
         B     CHKFUNCT
CKCTLGSW CLI   CATLGSW,C'1'                 *CHK CATLG SWITCH ON
         BE    ADJUSTQ
         BAL   RETURNR,BLDNAME              *BUILD INDEX NAME
         CLI   PCHSW,C'1'                   *CHK FOR PUNCHING
         BE    PCHINDX
         LH    R05,NAMELGTH                 *LOAD NAME LENGTH
         BCTR  R05,0                        *ADJUST TO MACH LGTH
         LA    R03,NAMEAREA
         LA    R06,PRINTWRK
         EX    R05,NAMEMOVE                 *MOVE INDEX NAME TO PRINT
         MVC   PRINTWRK+51(5),=C'INDEX'
         BAL   RETURNR,PRINT
         CLI   PTPCHSW,C'1'                 *CHK FOR PUNCHING
         BNE   ADJUSTQ
PCHINDX  MVC   OPCODE,=C'BLDX   '           *BUILD BLDX CARD
         LA    PCHPTR,PARMS                 *SET PTR
         MVC   0(6,PCHPTR),=C'INDEX='       *INDEX PARAMETER
         LA    PCHPTR,6(PCHPTR)             *INCREMENT PTR
         LR    R06,PCHPTR
         LA    R03,NAMEAREA
         LH    R05,NAMELGTH
         SH    R05,=H'2'
         EX    R05,NAMEMOVE                 *MOVE INDEX NAME TO PUNCH
         AH    R05,=H'1'
         LA    PCHPTR,0(R05,PCHPTR)         *INCREMENT PTR
         LA    R06,COL72
         SR    R06,PCHPTR                   *COMPUTE SPACE LEFT
         MVI   0(PCHPTR),C','               *COMMA
         CH    R06,=H'18'                   *CHK FOR SPACE IN CARD
         BNL   ROOMOK
         MVI   COL72,C'C'                   *CONT CHAR IN COL 72
         MVI   SORTCODE,C'2'                *BLDX CARDS ARE 2ND
         BAL   RETURNR,COUNTRTN
         BAL   RETURNR,PUNCH                *PUNCH FIRST CARD
         LA    PCHPTR,PARMCONT-1            *SET PTR FOR CONT CARD
ROOMOK   MVC   1(5,PCHPTR),=C'CVOL='        *CVOL PARAMETER
         MVC   6(6,PCHPTR),=C'SYSDA='
         MVC   12(6,PCHPTR),VOLSERNO
         MVI   SORTCODE,C'2'                *BLDX CARDS ARE 2ND
         BAL   RETURNR,COUNTRTN
         BAL   RETURNR,PUNCH
ADJUSTQ  LH    R05,NAMELGTH
         BCTR  R05,0
         LA    R04,NAMEAREA-1
         LA    R06,NAMEAREA(R05)            *LOAD POINTER
         AH    R05,=H'1'
ONCEMORE BCTR  R06,0                        *DECREMENT 1
         CLI   0(R06),C'.'                  *LOOK FOR PERIOD
         BE    AHIT
         CR    R04,R06
         BE    AHIT
         B     ONCEMORE
AHIT     LA    R04,NAMEAREA(R05)
         BCTR  R04,0
         SR    R04,R06                      *COMPUTE LGTH OF LAST NAME
         SR    R05,R04
         STH   R05,NAMELGTH
         BCTR  R04,0
         EX    R04,MOVBLNKS                 *BLANK LAST NAME
         B     OVERIT
MOVBLNKS MVC   1(0,R06),BLANKS44
OVERIT   L     R03,NOQENTS
         LR    R05,R03
         MH    R05,=H'14'
         LA    QUEUEPTR,QTABLE(R05)         *LOCATE LAST QUEUE ENTRY
         MVC   TTR,QELTTR
         LH    R04,QELDISP
         SH    R03,=H'1'                    *SUBTRACT 1 FROM NO IN Q
         ST    R03,NOQENTS
         BAL   RETURNR,READBLK              *READ BLOCK OF LAST Q ENTRY
         LA    INDEXPTR,BLKAREA(R04)        *SET UP POINTER
         B     LOCNXTEN                     *LOCATE NEXT ENTRY
         EJECT
*
* INVALID FUNCTION CODE
*
ERR01RTN MVC   PRINTWRK(21),=C'INVALID FUNCTION CODE'
         BAL   RETURNR,PRINT
         MVC   PRINTWRK(80),CARDHOLD
         BAL   RETURNR,PRINT
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
*
* WRONG BLOCK ROUTINE
*
ERR02RTN MVC   PRINTWRK(28),=C'ERROR IN READING INDEX BLOCK'
         BAL   RETURNR,PRINT
         MVC   PRINTWRK(33),=C'FOUND INDEX CONTROL ENTRY AT ADDR'
         UNPK  PRINTWRK+34(7),TTR(4)
         CLI   PRINTWRK+39,X'FA'
         BNE   OK2
         MVI   PRINTWRK+39,X'C1'
OK2      MVI   PRINTWRK+40,C' '
         BAL   RETURNR,PRINT
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
*
* INVALID ENTRY ROUTINE
*
ERR03RTN MVC   PRINTWRK(24),=C'INVALID ENTRY TYPE FOUND'
         MVC   PRINTWRK+25(7),=C'AT ADDR'
         UNPK  PRINTWRK+33(7),TTR(4)
         CLI   PRINTWRK+38,X'FA'
         BNE   OK3
         MVI   PRINTWRK+38,X'C1'
OK3      MVI   PRINTWRK+39,C' '
         BAL   RETURNR,PRINT
         B     LOCNXTEN
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
*
* I/O ERROR ROUTINE
*
ERR04RTN MVC   PRINTWRK(26),=C'I/O ERROR IN READING BLOCK'
         BAL   RETURNR,PRINT
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
*
* INVALID CONDITION CODE FROM READ ROUTINE
*
ERR05RTN MVC   PRINTWRK(31),=C'INVALID CODE RETURNED FROM READ'
         BAL   RETURNR,PRINT
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
*
* NO INDEX CONTROL ENTRY ROUTINE
*
ERR06RTN MVC   PRINTWRK(36),=C'NO INDEX CONTROL ENTRY FOUND AT ADDR'
         UNPK  PRINTWRK+37(7),TTR(4)
         CLI   PRINTWRK+42,X'FA'
         BNE   OK6
         MVI   PRINTWRK+42,X'C1'
OK6      MVI   PRINTWRK+43,C' '
         BAL   RETURNR,PRINT
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
*
* INVALID GENERATION INDEX FLAGS ROUTINE
*
ERR07RTN MVC   PRINTWRK(29),=C'INVALID FLAGS IN GEN. AT ADDR'
         UNPK  PRINTWRK+30(7),TTR(4)
         CLI   PRINTWRK+35,X'FA'
         BNE   OK7
         MVI   PRINTWRK+35,X'C1'
OK7      MVI   PRINTWRK+36,C' '
         BAL   RETURNR,PRINT
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
*
* VOLUME SERIAL NO. ERROR OR NO SYSCTLG ON VOLUME
*
ERR08RTN MVC   PRINTWRK(74),=C'VOLUME SERIAL NUMBER IN ERROR OR NO SYSCC
               TLG DATA SET RESIDES ON VOLUME NO.'
         MVC   PRINTWRK+75(6),VOLSERNO
         BAL   RETURNR,PRINT
         MVC   RTNCODE,=X'00000008'         *RETURN CODE OF 8
         B     EOFRDR
         EJECT
*
* END OF FILE ROUTINE
*
EOFRDR   CLOSE READER                       *CLOSE CARD READER
         CLI   PCHSW,C'1'                   *CHK PUNCH SWITCH ON
         BE    CLOSEPCH
         CLI   PTPCHSW,C'1'                 *CHK PTPCH SWITCH ON
         BNE   CLOSEPRT
CLOSEPCH CLOSE PUNCHER                      *CLOSE PUNCH
CLOSEPRT CLOSE PRINTER                      *CLOSE PRINTER
         CLOSE DISK                    CLOSE SYSCTLG         /* MVS/3
*        SNAP  DCB=SNAP,ID=10,STORAGE=(WORKGET,GETMAINE)
*        SNAP  DCB=SNAP,ID=11,LIST=GETMAIN
         L     R01,GETMAIN
*        FREEMAIN  R,LV=4096,A=(1)
*        CLOSE SNAP
         L     13,4(13)                     *LOAD SAVE AREA PTR
         L     15,RTNCODE                   *LOAD RETURN CODE
         ST    15,16(13)                    *STORE INTO SAVE AREA
         LM    14,12,12(13)                 *RESTOR REGS
         BR    14                           *RETURN
         EJECT
         LTORG
         EJECT
*
* DSECTS
*
ENTDSECT DSECT
ENTNAME  DS    CL8
ENTTTR   DS    CL3
ENTHWCT  DS    CL1
*
*
GIFLAGS  DS    CL1
GIMXENTS DS    CL1
GICURENT DS    CL2
*
*
         ORG   GIFLAGS
ALIASTRN DS    CL8
*
*
         ORG   GIFLAGS
         DS    CL4                                              DWH MSS
CVOLSRNO DS    CL6
*
*
         ORG   GIFLAGS
DSPVOLCT DS    CL2
*
*
QELEMENT DSECT                              *OVERLAY FOR QUEUE ELEMENTS
QELTTR   DS    CL3
FILLER   DS    CL1
QELDISP  DS    CL2
QELNAME  DS    CL8
*
*
VOLINFO  DSECT                              *OVERLAY OF VOLUME INFO
VOLDEVCD DS    CL4
VOLSRNUM DS    CL6
VOLSEQNO DS    CL2
*
*
RECAPCS1 CSECT
         EJECT
*
* WORK AREAS
*
         DS    0D
CARDHOLD DS    0CL80                        *CARD READ-IN AREA
FUNCTION DS    CL5
         DS    CL1
VOLSERNO DS    CL6
         DS    CL3
NODEL    DS    CL44                NODE LIST POINTER FOR MVS
         DS    CL21
*
*
         ORG   *-1
COL71    DS    CL1
         ORG
*
*
PRINTREC DS    0CL133                       *PRINT AREA
FORMCTRL DS    CL1
PRINTWRK DC    CL132' '
*
*
CARD     DC    CL80'0'                                          DWH MSS
*                                                               DWH MSS
*                                                               DWH MSS
INDEXBLK CAMLST BLOCK,TTR,VOLSERNO,BLKAREA  *CAMLIST MACRO     77074
         DS    0F                                 /*  MVS - 3 */MVS/3
TTR      DC    CL3'0'                  TTR POINTR FOR POINT MVS/MVS/3
         DC    X'00'                   FILLER  FOR TTD          MVS/3
BLKAREA  DS    0D
         DS    265C                         *INDEX BLOCK FROM LOCATE
*
*
         DS    0D
QTABLE   DS    322C                         *INDEX NAME AND LOC QUEUE
NOQENTS  DC    F'0'
*
NAMEAREA DC    CL44' '
NAMELGTH DC    H'0'
*
BLKSUSED DS    D
BLKSLEFT DS    D
SEQNUMCV DS    D
LINECT08 DS    D
RTNCODE  DC    F'0'
SAVETTR  DS    CL3
LINECTR  DC    PL2'0'
SRTCOUNT DC    PL4'0'
         EJECT
*
* CONSTANTS
*
STATHEAD DC    C'SYSCTLG STATISTICS'
VOLIDHDG DC    C'VOL.ID'
BLKSUHDG DC    C'INDEX BLOCKS USED'
BLKSLHDG DC    C'INDEX BLOCKS LEFT'
ENTRYHDG DC    C'CATALOGED DATA SETS, ALIASES, CVOL POINTERS, AND INDEXC
               ES'
NAMEHDG  DC    C'NAME'
TYPEHDG  DC    C'ENTRY TYPE'
DEVICHDG DC    C'DEVICE'
SEQNOHDG DC    C'SEQ.NO'
ALIASHDG DC    C'ALIAS OF'
PAGEID   DS    0CL10
         DC    C'PAGE '
PAGENUM  DS    CL4
PAGENUMP DC    PL3'0'
CNTRLNAM DC    X'0000000000000001'
LINKENT  DC    8X'FF'
HDGLINE1 DC    C'RECAP LISTING OF CATALOGED DATA SETS, POINTERS AND INDC
               EXES'
HDGLINE2 DC    C'FROM SYSCTLG ON VOLUME'
SKPTOCH1 DC    C'1'
SPACE1   DC    C' '
SPACE2   DC    C'0'
SPACE3   DC    C'-'
BLANKS44 DC    44C' '
*        THESE CONSTANTS MUST STAY IN THIS ORDER FOR SNAPS.....
WORKGET  DC    F'0'                      CURRENT ADDR OF TTR ENTRY
SNAPNO   DC    F'0'                      # ENTRIES IN TABLE
GETMAIN  DC    F'0'                      GETMAINED AREA ADDR
GETMAINE DC    F'0'                      END OF GETMAIN AREA
*        END OF CONSTANTS FOR SNAPS....
*
* DEVICE CODE NAME TABLE
*
         DS    0H
DEVCDTBL DC    X'30008001',H'4',C'2400'
         DC    X'3050200D',H'6',C'3330-1'                       MB
         DC    X'3070200D',H'6',C'3330-1'     SHARED 3330-1     MVS/3
         DC    X'34008003',H'4',C'3420'                    AWW PPG
         DC    X'30502009',H'4',C'3330'                    AWW PPG
         DC    X'30702009',H'4',C'3330'     SHARED 3330 MOD 1   MVS/3
         DC    X'30808001',H'6',C'2400-1'
         DC    X'30C08001',H'6',C'2400-2'
         DC    X'34008001',H'6',C'2400-3'
         DC    X'34208001',H'6',C'2400-4'
         DC    X'30C02008',H'4',C'2314'
         DC    X'00058000',H'4',C'3420'
         DC    X'34208003',H'6',C'9TR800'
         DC    X'30002005',H'4',C'2321'
         DC    X'30502006',H'6',C'2305M1'                       MB
         DC    X'30502007',H'6',C'2305M2'                       MB
         DC    X'32008003',H'4',C'3420'        3420 EQU IN MVS SMVS/3
         DC    X'3050200B',H'4',C'3350'        3350 DISK STORAGEMVS/3
         DC    X'3070200B',H'4',C'3350'        3350 DISK STORAGEMVS/3
         DC    X'30782009',H'6',C'3330V '      3330V DISK MSS   MVS/3
         DC    X'30582009',H'6',C'3330V '      3330V DISK MSS   MVS/3
LSTENTRY DC    X'99999999'
*
TRANSTBL DC    64X'00'
         DC    X'FF'
         DC    191X'00'
*   FORMS CONTROL CHARACTER TABLE
TABLE    DC    X'01'                        *HEX 40
         DC    31X'00'
         DC    X'03'                        *HEX 60
         DC    143X'00'
         DC    X'0201'                      *HEX F0 AND F1
*
* SWITCHES
*
PCHSW    DC    C'0'
PTPCHSW  DC    C'0'
CATLGSW  DC    C'0'
VCBSW    DC    C'0'
NOHITSW  DC    C'0'
NODELST  DC    C'0'                SW TO SAY NODE LIST WANTED   MVS/3
NODECOMP CLC   PRINTWRK(0),NODEL   EX INST FOR COMPARE  /MVS/   MVS/3
DEVTYP   DC    H'0'                NO OF ENT/TRK                 77056
         EJECT
*
*   DATA CONTROL BLOCKS
*
READER   DCB   DSORG=PS,                                               C
               EODAD=EOFRDR,                                           C
               MACRF=GM,                                               C
               DDNAME=SYSIN
         EJECT
*
PRINTER  DCB   DSORG=PS,                                               C
               MACRF=PM,                                               C
               LRECL=133,                                              C
               DDNAME=SYSPRINT,BLKSIZE=133,RECFM=FA             MVS/3
         EJECT
*
PUNCHER  DCB   DSORG=PS,                                               C
               MACRF=PM,                                               C
               LRECL=80,                                               *
               DDNAME=SYSPUNCH
*
         EJECT
SNAP     DCB   DDNAME=SNAP,DSORG=PS,RECFM=VBA,MACRF=(W),               X
               BLKSIZE=1632,LRECL=125       /* SNAP DATASET */
         EJECT
DISK     DCB   DSORG=PS,                                               *
               MACRF=(RP),DDNAME=DISK                           MVS/3
         END   RECAPPER
