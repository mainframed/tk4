./      CHANGE NAME=$QSE
*  QSERESTB  *            *            *            *          MBJ20026
*      RESOURCE TABLE FOR THIS SYSTEM  * (RESERVED) *          MBJ20026
         DS    BL1                 RESERVED.                   MBJ20026
QSERESTB DS    BL3                 RESOURCE ROUTING TABLE.     MBJ20026
./  CHANGE NAME=$JQE
*  JQEDNAME  *            *            *            *          MBJ20026
*                                                   *          MBJ20026
*                  CNTL/DEP JOBNAME                 *          MBJ20026
*            *            *            *            *          MBJ20026
*****************************************************          MBJ20026
*            *            *            *            *          MBJ20026
*                                                   *          MBJ20026
*             CNTL/DEP JOBNAME (CONTINUED)          *          MBJ20026
*            *            *            *            *          MBJ20026
*****************************************************          MBJ20026
*  JQERESRT  *            *            *  JQEFLAG3  *          MBJ20026
*                                                   *          MBJ20026
*           RESOURCE ROUTING           *   FLAGS    *          MBJ20026
*            *            *            *            *          MBJ20026
*****************************************************          MBJ20026
JQEDNAME DS    CL8                 NAME FROM CNTL/BEF/AFT.     MBJ20026
JQERESRT DS    BL3                 RESOURCE ROUTING.           MBJ20026
JQEFLAG3 DS    BL1                 MORE JOBQ FLAGS             MBJ20026
QUEDNAME EQU   JQEDNAME-JQEDSECT   JOB DEP/CNTL NAME           MBJ20026
QUERESRT EQU   JQERESRT-JQEDSECT   RESOURCE ROUTING.           MBJ20026
QUEFLAG3 EQU   JQEFLAG3-JQEDSECT   MORE JOBQ FLAGS             MBJ20026
         SPACE 2                                               MBJ20026
*                                  JQEFLAG3                    MBJ20026
         SPACE 2                                               MBJ20026
QUEAFT   EQU   X'08'               AFTER CARD USED             MBJ20026
QUEEXC   EQU   X'04'               CNTL,EXC   USED             MBJ20026
QUECNTL  EQU   X'02'               CNTL,SHR   USED             MBJ20026
QUEBEF   EQU   X'01'               BEFORE CARD USED            MBJ20026
./      CHANGE NAME=$RDRWORK
RQUEDNAM DS    CL8                 DEPENDENT/CNTL NAME         MBJ20026
RQUERESR DS    BL3                 RESOURCE ROUTING BITS       MBJ20026
RQUEFLG3 DS    BL1                 MORE JQE FLAGS              MBJ20026
./       CHANGE NAME=HASPCOMM
         BAL   R0,COFJRES          ADD RESOURCE ROUTINGS       MBJ20026
         BAL   R0,COFJRES          ADD RESOURCE ROUTINGS       MBJ20026
************************************************************** MBJ20026
*        ADD RESOURCE ROUTINGS TO JOB MESSAGE.               * MBJ20026
************************************************************** MBJ20026
COFJRES  STM   WA,WC,COMWREGS      SAVE REGISTERS WA,WB,WC     MBJ20026
         LA    R15,COFJOB+L'COMMAND-9  MAXIMUM MESSAGE LENGTH  MBJ20026
         SR    R14,R14             CLEAR R14                   MBJ20026
         IC    R14,COFLNGTH        GET MESSAGE LENGTH          MBJ20026
         LA    R14,COFJOB+1(R14)   AND POINT TO MESSAGE END    MBJ20026
         LA    WA,$RESTAB          GET RESOURCE TABLE          MBJ20026
         SR    WB,WB               CLEAR WB                    MBJ20026
         ICM   WB,14,JQERESRT      GET JOBS RESOURCE ROUTINGS  MBJ20026
         BZ    COFJRES6            IF NONE, MAKE QUICK EXIT    MBJ20026
COFJRES1 DS    0H        CC INDICATES RESOURCE STATUS          MBJ20026
         BP    COFJRES4            HIGH BIT 0-NOT ROUTED TO IT MBJ20026
         BZ    COFJRES5            ALL BITS 0-NO MORE ROUTINGS MBJ20026
         MVC   0(8,R14),0(WA)      MOVE RESOURCE NAME INTO MSG MBJ20026
         LA    R14,7(,R14)         POINT TO LAST CHAR OF NAME  MBJ20026
COFJRES2 CLI   0(R14),C' '         Q. BLANK                    MBJ20026
         BNE   COFJRES3            A. NO-POLISH IT UP          MBJ20026
         BCT   R14,COFJRES2        ELSE BACK UP AND TRY AGAIN  MBJ20026
COFJRES3 DS    0H                  SEPARATE RESOURCES BY COMMA MBJ20026
         LA    R14,2(,R14)         AND BUMP TO NEXT AREA       MBJ20026
         CR    R14,R15             Q. NEAR END OF MESSAGE AREA MBJ20026
         BNH   COFJRES4            A. NO-CONTINUE BUILDING     MBJ20026
         LA    R15,COFJOB          GET MSG START               MBJ20026
         SR    R14,R15             CALCULATE MSG LENGTH        MBJ20026
         STC   R14,COFLNGTH        AND STORE FOR WTOER         MBJ20026
         LR    R15,R0              SAVE RETURN ADDRESS IN R15  MBJ20026
         BAL   R14,COFJWTO         AND ISSUE MESSAGE           MBJ20026
*  NOTE- ONE BAL TO COFJWTO MAY RESULT IN TWO WTO'S            MBJ20026
*        WHERE SUBSEQUENT LINES BEGIN AT COFQUE.               MBJ20026
         LR    R0,R15              RESTORE RETURN ADDRESS      MBJ20026
         LA    R15,COFJOB+L'COMMAND-9  MAXIMUM MESSAGE LENGTH  MBJ20026
         LA    R14,COFQUE          START OF SUBSEQUENT MSGS    MBJ20026
COFJRES4 LA    WA,$RESLEN(,WA)     BUMP TO NEXT RESOURCE NAME  MBJ20026
         SLL   WB,1                AND JOB'S NEXT ROUTING BIT  MBJ20026
         LTR   WB,WB               SET CONDITION CODE.         MBJ20026
         B     COFJRES1            GO CHECK THIS RESOURCE      MBJ20026
COFJRES5 LA    R15,COFQUE          GET REAL START OF 2ND MSGS  MBJ20026
         CR    R14,R15             Q. 2ND LINE WITH NO NAMES   MBJ20026
         BE    COFJRES6            A. YES-NO WTO               MBJ20026
         BCTR  R14,0               BACK UP ONE                 MBJ20026
         MVI   0(R14),C' '         AND PUT BLANK-SHOULD BE ',' MBJ20026
         LA    R15,COFJOB-1        GET MESSAGE START-1         MBJ20026
         SR    R14,R15             AND CALCULATE LENGTH        MBJ20026
         STC   R14,COFLNGTH        STORE FOR WTO               MBJ20026
COFJRES6 LR    R15,R0              GET RETURN ADDRESS          MBJ20026
         LM    WA,WC,COMWREGS      RESTORE WORK REGS WA,WB,WC  MBJ20026
         BR    R15                 AND RETURN TO CALLER        MBJ20026
         SPACE 2                                               MBJ20026
         TITLE 'HASP COMMAND PROCESSOR - SYSTEM RESOURCE TABLE' BJ20026
         $RESTABL                 GENERATE JES2 RESOURCE TABLE MBJ20026
         $COMTAB DC,HASPCRES,REJECT=COMRMT  DISPLAY CONFLICTS  MBJ20026
         $COMTAB DR,HASPCRES,REJECT=COMRMT  DISPLAY RESOURCES  MBJ20026
COMTBLQ  $COMTAB QA,HASPCRES,REJECT=COMRMT+COMS  ADD RESOURCE  MBJ20026
         $COMTAB QD,HASPCRES,REJECT=COMRMT+COMS  DROP RESOURCE MBJ20026
         $COMTAB QJ,HASPCJB2,REJECT=COMRMT  INFO JOB JCL FILE. MBJ20026
         DC    C'Q',AL3(COMTBLQ)   ENTRY FOR 'Q' VERBS         MBJ20026
HASPCJB2 $COMGRUP AJ,AS,AT,CJ,CS,CT,DJ,DS,DT,EJ,HJ,HS,HT,IJ,LJ,LS,LT, 6C
         EJECT                                                 MBJ20026
************************************************************** MBJ20026
*                                                            * MBJ20026
*        DISPLAY JOBCARD AND JES CONTROL CARDS FOR A JOB     * MBJ20026
*        $QJX    $QJX-X   $QJX,X,X,X,X                       * MBJ20026
*                                                            * MBJ20026
************************************************************** MBJ20026
CQJ      DS    0H                                              MBJ20026
         STM   WA,WE,COMREGSV+4    SAVE REGISTERS.             MBJ20026
         MVI   COMMAND,X'40'       CLEAR FIRST CHACACTER       MBJ20026
         MVC   COMMAND+1(L'COMMAND-1),COMMAND CLEAR REST.      MBJ20026
         MVC   COMMAND(3),=C'JOB'  SHOW JOB.                   MBJ20026
         LH    R0,JQEJOBNO         GET JOB NO.                 MBJ20026
         $CFCVE VALUE=(R0)         CONVERT IT.                 MBJ20026
         MVC   COMMAND+3(5),COMDWORK MOVE TO MESSAGE.          MBJ20026
         MVC   COMMAND+10(8),JQEJNAME MOVE IN JOBNAME          MBJ20026
         TM    JQEFLAGS,QUEBUSY    IS IT ACTIVE SOMEWHERE      MBJ20026
         BNZ   CLCNOTXQ            YES, CANNOT $QJ IT.         MBJ20026
         TM    JQETYPE,$XEQ        IS IT IN EXECUTION Q?       MBJ20026
         BZ    CLCNOTXQ            NO, CANNOT $QJ IT           MBJ20026
         CLC   JQEJOBNO,=H'10000'  JOB IN NORMAL BATCH RANGE   MBJ20026
         BNL   CLCNOTXQ            NO, CANNOT $QJ STC,TSU JOB  MBJ20026
*    AT THIS POINT WE HAVE FINALLY AGREED WE HAVE A VALID JQE. MBJ20026
         MVI   CLCJOBSW,0          SET TO NO JOBCARD FOUND     MBJ20026
         L     WA,=A(CRESAREA)     POINT TO RESIDENT WORK AREA MBJ20026
         ST    WA,COMREGSV         SAVE FOR LATER.             MBJ20026
         LA    WB,4095             LENGTH TO CLEAR.            MBJ20026
         LA    WC,=C' '            SET TO MOVE SPACES.         MBJ20026
         LA    WD,1                LENGTH OF FROM FIELD.       MBJ20026
         ICM   WD,8,=C' '          FILL CHARACTER.             MBJ20026
         MVCL  WA,WC               SPACE OUT CRESAREA.         MBJ20026
         MVC   COMMAND+8(1),JQETYPE MOVE IN JOBCLASS           MBJ20026
         OI    COMMAND+8,X'80'     MAKE PRINTABLE              MBJ20026
         MVI   COMMAND+18,C' '     BLANK ANOTHER CHARACTER     MBJ20026
         LR    WA,R1               SAVE JQE ADDRESS.           MBJ20026
         LA    WB,PCEDADCT         POINT TO DA DCT.            MBJ20026
         $GETBUF CLCNOBUF          GET A BUFFER FOR I/O        MBJ20026
         LR    WC,R1               SAVE BUFFER ADDRESS         MBJ20026
         USING BUFDSECT,WC         ADDRESS BUFFER.             MBJ20026
         MVI   PCEDEVTP,PCEDARD    INDICATE READ REQUEST       MBJ20026
         ST    WC,PCEBUFAD         SET BUFFER ADDRESS          MBJ20026
         L     R15,QUETRAK(WA)     GET JCT TRACK ADDRESS       MBJ20026
         ST    R15,PCESEEK         SET TRACK ADDRESS           MBJ20026
         LA    R15,IOBCCW1         START OF CCW'S              MBJ20026
         ST    R15,IOBSTART        STORE IN BUFFER             MBJ20026
         $EXCP (WB)                READ IN THE JCT             MBJ20026
CLCEXCP1 $WAIT IO                  WAIT FOR COMPLETION         MBJ20026
         TM    BUFECBCC,X'7F'      TEST COMPLETION             MBJ20026
         BZ    CLCEXCP1            NOT COMPLETE, GO WAIT.      MBJ20026
         BM    CLCIOERR            IO ERROR INDICATED          MBJ20026
*    AT THIS POINT WE HAVE SUCCESSFULLY READ THE JCT.          MBJ20026
         LR    JCT,WC              PUT JCT ADDR IN JCT REG.    MBJ20026
         L     R0,$JOBQPTR         GET ADDR OF HASP JOBQ       MBJ20026
         AL    R0,JCTJQE           ADD JQE OFFSET FROM JCT     MBJ20026
         CLR   R0,WA               TEST JCT VALIDITY.          MBJ20026
         BNE   CLCIOERR            BR IF INVALID.              MBJ20026
*    AT THIS POINT WE HAVE A VALID JCT FOR THE JOB.            MBJ20026
         L     R15,JCTIOT          GET FIRST IOT TRAK ADDR     MBJ20026
         ST    R15,PCESEEK         SET FOR READ.               MBJ20026
         $EXCP (WB)                READ IN IOT                 MBJ20026
CLCEXCP2 $WAIT IO                  WAIT FOR COMPLETION         MBJ20026
         TM    BUFECBCC,X'7F'      TEST COMPLETION.            MBJ20026
         BZ    CLCEXCP2            NOT COMPLETE GO WAIT AGAIN. MBJ20026
         BM    CLCIOERR            IO ERROR INDICATED.         MBJ20026
*    AT THIS POINT WE HAVE SUCCESSFULLY READ THE IOT.          MBJ20026
*    NOW ATTEMPT TO GET INPUT PDDB S.                          MBJ20026
         LA    JCT,IOTPDBOJ-IOTDSECT(WC) POINT TO JCL PDDB     MBJ20026
         L     R15,PDBMTTR-PDBDSECT(JCT) POINT TO JCL TRACK    MBJ20026
CLCEXCPL ST    R15,PCESEEK         SET FOR READ                MBJ20026
         $EXCP (WB)                READ THE PDDB.              MBJ20026
CLCWAIT  $WAIT IO                  WAIT FOR POST.              MBJ20026
         TM    BUFECBCC,X'7F'      GOOD COMPLETION.            MBJ20026
         BZ    CLCWAIT             NOT COMPLETE GO WAIT.       MBJ20026
         BM    CLCIOERR            IO ERROR INDICATED          MBJ20026
*   AT THIS POINT WE HAVE A PDDB FOR THE JOB.                  MBJ20026
         LA    WD,BUFSTART+10      POINT TO DATA.              MBJ20026
         SR    WE,WE               CLEAR FOR WORK              MBJ20026
CLCGETCD IC    WE,0(,WD)           GET LENGTH                  MBJ20026
         CLI   CLCJOBSW,1          JOB CARD FOUND?             MBJ20026
         BNE   CLCJOBCD            NO, GO DO IT.               MBJ20026
         CLC   3(3,WD),=C'//*'     IT JES CONTROL CARD.        MBJ20026
         BNE   CLCNEXTC            NO, IGNORE IT.              MBJ20026
         LA    R15,CLCTABEN        NO. OF ENTRIES IN TABLE     MBJ20026
         LA    R14,CLCTABST        BEGINNING OF TABLE.         MBJ20026
*    AT THIS POINT WE HAVE A JES CONTROL CARD. SEARCH TABLE.   MBJ20026
CLCLOOP  CLC   6(4,WD),0(R14)      HIT?                        MBJ20026
         BE    CLCJESCD            YES, GO PROCESS             MBJ20026
         LA    R14,7(,R14)         BUMP POINTER                MBJ20026
         BCT   R15,CLCLOOP         TRY AGAIN.                  MBJ20026
CLCNEXTC LA    WD,3(WD,WE)         POINT TO NEXT CARD          MBJ20026
         CLI   0(WD),X'FF'         END OF BUFFER               MBJ20026
         BE    CLCNEXTB            YES, GET NEXT ONE.          MBJ20026
         CLI   1(WD),X'FF'         TEST NEXT BYTE FOR END.     MBJ20026
         BE    CLCNEXTB            YES, GET NEXT ONE.          MBJ20026
         B     CLCGETCD            DO NEXT CARD.               MBJ20026
CLCJOBCD DS    0H                                              MBJ20026
         MVI   CLCJOBSW,1          SHOW JOB CARD DONE.         MBJ20026
CLCJESCD L     R15,COMREGSV        PICKUP OUTPUT POINTER       MBJ20026
         BCTR  WE,0                DOWN ONE FOR COMPARE.       MBJ20026
         EX    WE,CQJMVCRD         MOVE CARD FOR DESIRED LENGTH BJ20026
         LA    WE,1(WE)            BACK UP ONE.                MBJ20026
         LA    R15,70(R15)         POINT TO NEXT SLOT.         MBJ20026
         ST    R15,COMREGSV        SAVE IT.                    MBJ20026
         B     CLCNEXTC            GO GET NEXT CARD.           MBJ20026
CLCNEXTB DS    0H                                              MBJ20026
         ICM   R15,15,HDBNXTRK     NEXT BUFFER ADDRESS         MBJ20026
         BNZ   CLCEXCPL            END OF CHAIN IF ZERO        MBJ20026
         L     R15,COMREGSV        GET SAVE POINTER.           MBJ20026
         MVI   0(R15),X'FF'        SHOW END OF CARDS.          MBJ20026
         L     WD,=A(CRESAREA)     POINT TO BEGINNING OF CARDS MBJ20026
         ST    WA,COMREGSV         SAVE Q POINTER.             MBJ20026
*    AT THIS POINT WE HAVE PROCESSED ALL CARDS AND ARE READY   MBJ20026
*    TO PRINT THEM TO OPERATOR.                                MBJ20026
CLCPRTCD MVC   COMMAND+10(70),0(WD) MOVE IN A CARD             MBJ20026
         LA    R0,80               MESSAGE LENGTH              MBJ20026
         $CWTO L=(R0)              WTO IT.                     MBJ20026
         LA    WD,70(WD)           POINT TO NEXT CARD          MBJ20026
         CLI   0(WD),X'FF'         END OF CARDS.               MBJ20026
         BNE   CLCPRTCD            NO, GO PRINT OTHER CARD     MBJ20026
*                                                              MBJ20026
CLCCLEAN $FREEBUF (WC)             FREE THE BUFFER.            MBJ20026
CQJREST  LM    R1,WE,COMREGSV      RESTORE Q POINTER & REGS 2-6MBJ20026
         B     CAJNEXT             GO GET NEXT JOB.            MBJ20026
         SPACE 2                                               MBJ20026
CLCNOBUF DS    0H                                              MBJ20026
         $CRET MSG=CLCBUFNO,L=L'CLCBUFNO RETURN TO BUFFER      MBJ20026
         SPACE 1                                               MBJ20026
CLCIOERR DS    0H                                              MBJ20026
         ST    WA,COMREGSV         SAVE Q POINTER.             MBJ20026
         $IOERROR (WC)             IO ERROR MESSAGE.           MBJ20026
         MVC   COMMAND+5(L'CLCERROR),CLCERROR MOVE ERROR MSG.  MBJ20026
         LA    R0,L'CLCERROR+6     SET LENGTH                  MBJ20026
         $CWTO L=(R0)              WTO IT.                     MBJ20026
         B     CLCCLEAN            GO CLEAN UP FOR NEXT JOB    MBJ20026
         SPACE 1                                               MBJ20026
CLCNOTXQ DS    0H                                              MBJ20026
         ST    R1,COMREGSV         SAVE Q POINTER.             MBJ20026
         MVC   COMMAND+19(L'CLCNOTEX),CLCNOTEX ERROR MESSAGE   MBJ20026
         LA    R0,L'CLCNOTEX+19    SET LENGTH                  MBJ20026
         $CWTO L=(R0)              WTO IT.                     MBJ20026
         B     CQJREST             GO CLEANUP AND LEAVE.       MBJ20026
         SPACE 3                                               MBJ20026
CLCJOBSW EQU   COMEWORK,1          JOB CARD SWITCH.            MBJ20026
CLCTABST DS    0H                  TABLE START.                MBJ20026
         DC    CL7'ROUTE'          POSSIBLE                    MBJ20026
         DC    CL7'MESSAGE'          CURRENT                   MBJ20026
         DC    CL7'OUTPUT'             JES2                    MBJ20026
         DC    CL7'JOBPARM'              SHRSPL                MBJ20026
         DC    CL7'CNTL'                   CONTROL             MBJ20026
         DC    CL7'BEFORE'                   CARD              MBJ20026
         DC    CL7'AFTER'                      TABLE.          MBJ20026
CLCTABEN EQU   (*-CLCTABST)/7      NO. OF ENTRIES IN TABLE.    MBJ20026
CLCERROR DC    C'I/O ERROR, $QJ ABORTED *' ERROR MESSAGE       MBJ20026
CLCBUFNO DC    C'NO BUFFER AVAILABLE, TRY $QJ AGAIN' NOBUF MSG MBJ20026
CLCNOTEX DC    C' IS TSU, STC, OR NOT EXEC.'                   MBJ20026
*                                                              MBJ20026
*                                                              MBJ20026
*     *********   EXECUTE ONLY.     $$$$$$$$$$$$$$             MBJ20026
CQJMVCRD MVC   0(*-*,R15),3(WD)    MOVE IN CARD.               MBJ20026
         DROP  WC                  GIVE UP ADDRESS.            MBJ20026
         EJECT                                                 MBJ20026
         L     R1,SJBASCBP-SJBDSECT(R1) GET ASCB ADDRESS       MBJ20042
         L     R1,ASCBOUCB-ASCB(R1) GET OUCB ADDRESS.          MBJ20042
         SR    R0,R0               CLEAR FOR INSERT.           MBJ20042
         IC    R0,24(R1)           INSERT OUCBNPG (PERF GROUP) MBJ20042
         L     R1,COMEWORK         SAVE COMEWORK CONTENTS.     MBJ20042
         $CFCVE VALUE=(R0)         CONVERT IT TO EBCDIC        MBJ20042
         MVC   CDIGROUP-CDIM+COMMAND,COMDWORK+2 MOVE GROUP NO. MBJ20042
         ST    R1,COMEWORK         RESTORE IT'S CONTENTS.      MBJ20042
CDINMID  DC    8C'*'               JOBID.                      MBJ20042
CDIPERFG DC    CL10' PG=*** C='    PERF GROUP NO, CLASS PREFIX MBJ20042
CDIGROUP EQU   CDIPERFG+4,3        AREA FOR PERFORM NUMBER.    MBJ20042
         TITLE 'HASP COMMAND PROCESSOR - HASPCRES - RESOURCE CONTROL   C
               COMMANDS'
HASPCRES $COMGRUP DC,DR,QA,QD  RESOURCE TABLE COMMANDS         MBJ20026
************************************************************** MBJ20026
*                                                            * MBJ20026
*        RESOURCE TABLE SUBPROCESSOR                         * MBJ20026
*                                                            * MBJ20026
* FORM 1-$QX RES,SID                                         * MBJ20026
*              = HASP VERB CODE (RESOURCE Q)                 * MBJ20026
*        X     = OPERATION (A-ATTACH,D=DETACH                * MBJ20026
*        RES   = RESOURCE (REQ'D) - ANY VALID JES2 RESOURCE  * MBJ20026
*        SID   = SYSTEM ID (OPTIONAL) - ANY VALID CPU ID     * MBJ20026
*                   IN FORM 68#X                             * MBJ20026
*        DEFAULTS                                            * MBJ20026
*              SID=SYSTEM IN WHICH COMMAND WAS ISSUED        * MBJ20026
*                                                            * MBJ20026
* FORM 2-$DX SID                                             * MBJ20026
*        $D    = HASP VERB CODE (DISPLAY)                    * MBJ20026
*        X     = OPERATION (R-RESOURCE,C=CONFLICT)           * MBJ20026
*              = 'C'  DISPLAY JOBS WITH RESOURCE ROUTINGS    * MBJ20026
*                     INCOMPATIBLE WITH ANY CPU.             * MBJ20026
*              = 'R'  DISPLAY THE RESOURCES ATTACHED TO      * MBJ20026
*                     THE SPECIFIED CPU.                     * MBJ20026
*        SID   = SAME AS FORM1.(ONLY FOR $DR), OR 'ALL'      * MBJ20026
*        DEFAULTS                                            * MBJ20026
*              SID=ALL ACTIVE CPUS                           * MBJ20026
*                                                            * MBJ20026
*        NOTES - $Q COMMAND EXIT NORMALLY WITH A BRANCH      * MBJ20026
*                EXIT TO $DR.AND $DC                         * MBJ20026
*                                                            * MBJ20026
************************************************************** MBJ20026
         SPACE 1                                               MBJ20026
CQA      NULL  ,         $QA RESOURCE                          MBJ20026
         OI    CQFLAG,CQATTACH     SET ATTACH RESOURCE FLAG    MBJ20026
         B     CQPROC              AND PROCESS                 MBJ20026
CQD      NULL  ,         $QD RESOURCE                          MBJ20026
         OI    CQFLAG,CQDETACH     SET DETACH RESOURCE FLAG    MBJ20026
CQPROC   NULL  ,         COMMON $RESTAB UPDATE                 MBJ20026
         BXH   WD,WE,CQ0OPER       Q. ANY OPERANDS             MBJ20026
         LM    WA,WB,0(WD)         A. YES GET THEM IN REGS     MBJ20026
         LR    WC,WB                  WC=2ND OPER              MBJ20026
         SR    WC,WA                  WC=LENGTH 1ST OPER + 1   MBJ20026
         BCTR  WC,0                   WC=LENGTH 1ST OPER       MBJ20026
         BCTR  WC,0                   WC=VALUE FOR MVC         MBJ20026
         LA    R1,7                MAXIMUM LENGTH              MBJ20026
         NR    WC,R1               GUARANTEE LENGTH LE 8       MBJ20026
         MVI   CQRESNAM,C' '       CLEAR RESOURCE              MBJ20026
         MVC   CQRESNAM+1(7),CQRESNAM  NAME TO BLANKS          MBJ20026
         EX    WC,CQRESMVC         MOVE RESOURCE IN            MBJ20026
*    NOW HAVE FIRST OPERAND (RESOURCE NAME) AT CQRESNAM        MBJ20026
         MVC   CQCPUID(4),$SID     SET DEFAULT SYSTEM ID       MBJ20026
         L     WA,=V($QSE1)        GET FIRST QSE               MBJ20026
         BXH   WD,WE,CQ1OPER       Q. ANOTHER OPERAND          MBJ20026
*                                  A. NO - USE DEFAULT         MBJ20026
         CLC   0(3,WB),$SID        Q. FORM 68#X                MBJ20026
         BNE   CQINVCPU            A. NO - ERROR - BRANCH      MBJ20026
         MVC   CQCPUID(4),0(WB)    A. YES- USE AS CPU ID       MBJ20026
         SPACE 1                                               MBJ20026
CQ1OPER  SR    WC,WC               CLEAR WC                    MBJ20026
         IC    WC,CQCPUID+3        GET 68#.IN EBCDIC           MBJ20026
         LA    WB,7                GET MAXIMUM CPU             MBJ20026
         NR    WC,WB               STRIP DOWN TO BINARY CPU    MBJ20026
         BCTR  WC,0                RELATIVE CPU #              MBJ20026
         MH    WC,CQQSELEN         OFFSET TO QSE               MBJ20026
CQQSELEN EQU   CDRQSELN            =H'QSELNGTH'                MBJ20026
         ALR   WC,WA               ADD TO GET REAL POINTER     MBJ20026
         USING QSEDSECT,WC         AND ESTABLISH ADDRESSABILITYMBJ20026
         CLC   CQCPUID(4),QSESID   Q. IS CPU ACTIVE            MBJ20026
         BNE   CQINVCPU            A. NO - ERROR - BRANCH      MBJ20026
         SPACE 1                                               MBJ20026
         LA    R15,$NOLEFT         GET # OF RESOURCES          MBJ20026
         L     R1,CQHIBIT          GET X'80000000' IN R1       MBJ20026
         LA    WA,$RESTAB          GET RESOURCE TABLE          MBJ20026
CQRESLOP CLC   CQRESNAM(8),0(WA)   Q. MATCH TABLE ENTRY        MBJ20026
         BE    CQRESMAT            A. YES-BRANCH OUT OF LOOP   MBJ20026
         SRL   R1,1                A. NO -MOVE BIT OVER 1      MBJ20026
         LA    WA,$RESLEN(,WA)        POINT TO NEXT TABLE ENTRYMBJ20026
         BCT   R15,CQRESLOP           AND LOOP THROUGH TABLE   MBJ20026
         B     CQINVRES            NOT FOUND-INVALID RESOURCE  MBJ20026
         SPACE 1                                               MBJ20026
CQRESMAT LR    WD,R1               SAVE RESOURCE ID            MBJ20026
         L     WE,QSERESTB         GET CURRENT RESOURCES       MBJ20026
         TM    CQFLAG,CQATTACH     Q. RESOURCE BEING ATTACHED  MBJ20026
         BO    CQADDRES            A. YES - BRANCH             MBJ20026
         NR    WD,WE               Q. RESOURCE ALREADY DETACHEDMBJ20026
         BZ    CQEXIT              A. YES - DONT ALTER CKPT    MBJ20026
         $QSUSE  ,                    GET CKPT RECORD          MBJ20026
         X     R1,QSERESTB         TURN OFF RESOURCE BIT       MBJ20026
         STCM  R1,8+4+2,QSERESTB   AND PUT BACK IN QSE         MBJ20026
         B     CQEXIT              AND EXIT FROM $QD           MBJ20026
         SPACE 1                                               MBJ20026
CQADDRES NR    WD,WE               Q. RESOURCE ALREADY ATTACHEDMBJ20026
         BNZ   CQEXIT              A. YES-DONT ALTER CKPT      MBJ20026
         $QSUSE  ,                    GET CKPT RECORD          MBJ20026
         O     R1,QSERESTB            TURN ON RESOURCE BIT     MBJ20026
         STCM  R1,8+4+2,QSERESTB      AND REPLACE IN QSE       MBJ20026
         B     CQEXIT              EXIT FROM $QA               MBJ20026
         DROP  WC                  DROP QSE ADDRESSABILITY     MBJ20026
         SPACE 1                                               MBJ20026
CQ0OPER  $CRET MSG='NO OPERANDS'                               MBJ20026
         SPACE 1                                               MBJ20026
CQINVCPU $CRET MSG='INVALID SYSTEM ID'                         MBJ20026
         SPACE 1                                               MBJ20026
CQINVRES $CRET MSG='INVALID RESOURCE'                          MBJ20026
         SPACE 1                                               MBJ20026
CQEXIT   $POST $HASPECF,JOB POST CPUS-NEW SYSTEM CONFIGURATION MBJ20026
         OI    CQFLAG,CQCMD        TELL $DR THAT WERE COMING   MBJ20026
         B     CDRENTRY            BRANCH ENTER $DR            MBJ20026
         SPACE 1                                               MBJ20026
*                                                              MBJ20026
* LOCAL DEFINITIONS FOR $QA AND $QD.                           MBJ20026
*                                                              MBJ20026
*     EXECUTE INSTRUCTIONS                                     MBJ20026
CQRESMVC MVC   CQRESNAM(0),0(WA)   VARIABLE RESOURCE NAME MOVE MBJ20026
*                                                              MBJ20026
*     FLAGS & FIELDS                                           MBJ20026
CQCPUID  EQU   COMEWORK,4          CPU ID                      MBJ20026
CQRESNAM EQU   COMDWORK,8          RESOURCE NAME               MBJ20026
CQFLAG   EQU   COMNULOP,1          FLAG BYTE                   MBJ20026
CQATTACH EQU   128                 BIT0                        MBJ20026
CQDETACH EQU   64                  BIT1                        MBJ20026
CQCMD    EQU   32                  BIT2                        MBJ20026
         DS    0F                                              MBJ20026
CQHIBIT  DC    X'80000000'                                     MBJ20026
         EJECT                                                 MBJ20026
************************************************************** MBJ20026
*                                                            * MBJ20026
*        DISPLAY RESOURCES ATTACHED TO EACH CPU.             * MBJ20026
         BNE   CDRINVCP            A. NO - INVALID CPU         MBJ20026
         MVC   CDRCPULO(4),0(WD)   A. YES- SET UP FOR DISPLAY  MBJ20026
CDRENTRY MVC   CDRCPUHI(4),CDRCPULO   SET HIGH=LOW             MBJ20026
         CLI   CDRCPULO+3,C'0'     Q. ALL SPECIFIED            MBJ20026
         BNE   CDRSETUP            A. NO - START DISPLAY       MBJ20026
         MVI   CDRCPULO+3,C'1'        ALL - LOW=1              MBJ20026
         MVI   CDRCPUHI+3,C'8'        ALL - HIGH=8             MBJ20026
CDRSETUP L     WD,=V($QSE1)        GET FIRST QSE               MBJ20026
         MVI   COMMAND,C' '             CLEAR                  MBJ20026
         MVC   COMMAND+1(133),COMMAND   BUFFER                 MBJ20026
         USING QSEDSECT,WD                                     MBJ20026
         SR    WA,WA               CLEAR WA                    MBJ20026
         LA    WE,7                GET MASK FOR CPUID          MBJ20026
         IC    WA,CDRCPUHI+3       GET CPU ID (HIGH)           MBJ20026
         BCTR  WA,0                MAKE IT RELATIVE TO 0       MBJ20026
         NR    WA,WE               MASK OFF ZONE               MBJ20026
         MH    WA,CDRQSELN         CONVERT TO OFFSET           MBJ20026
         LA    WF,0(WA,WD)         WF=HIGH QSE                 MBJ20026
         SR    WA,WA               CLEAR WA                    MBJ20026
         IC    WA,CDRCPULO+3       GET CPU ID (LOW)            MBJ20026
         BCTR  WA,0                MAKE RELATIVE TO 0          MBJ20026
         NR    WA,WE               MASK OFF ZONE               MBJ20026
         MH    WA,CDRQSELN         CONVERT TO OFFSET           MBJ20026
         LA    WD,0(WA,WD)         WD=LOW QSE                  MBJ20026
         LA    WE,QSELNGTH         WD/WE/WF SETUP FOR BXLE LOOPMBJ20026
CDRQSELN EQU   *-2       USE LAST HALF OF INST FOR HALFWORD    MBJ20026
*                        MULTIPLIER IN ABOVE INSTRUCTIONS.     MBJ20026
         LA    WA,$RESTAB          GET RESOURCE TABLE FIRST    MBJ20026
         LA    WB,$NOLEFT*$RESLEN(,WA)  AND LAST ENTRIES       MBJ20026
         LR    WC,WA               SAVE TABLE START            MBJ20026
CDRLOOP0 MVI   COMMAND+5,C'-'                                  MBJ20026
         LA    R15,COMMAND+7       FIRST RESOURCE IN MSG       MBJ20026
         MVC   COMMAND(4),QSESID   MOVE CPUID IN               MBJ20026
         CLC   QSESID(3),$SID      Q. ACTIVE CPU               MBJ20026
         BNE   CDRNEQSE            A. NO - NEXT QSE            MBJ20026
         SPACE 1                                               MBJ20026
         SR    R1,R1               CLEAR R1                    MBJ20026
         ICM   R1,8+4+2,QSERESTB   GET RESOURCE FLAGS          MBJ20026
CDRBRC   BP    CDRNORES            THIS RESOURCE NOT ATTACHED  MBJ20026
         BZ    CDRWRITE            NO MORE RESOURCES           MBJ20026
         MVC   0(8,R15),0(WA)      MOVE RESOURCE NAME          MBJ20026
         LA    R15,$RESLEN+1(,R15) NEXT OUTPUT AREA            MBJ20026
CDRNORES LA    WA,$RESLEN(,WA)     NEXT RESOURCE               MBJ20026
         SLL   R1,1                CHECK NEXT BIT              MBJ20026
         LTR   R1,R1               SET CONDITION CODE.         MBJ20026
         B     CDRBRC              BRANCH TO CHECKER           MBJ20026
CDRWRITE LA    R1,COMMAND          ADDRESS OF COMMAND          MBJ20026
         SR    R15,R1              CALCULATE MSG LENGTH        MBJ20026
         LR    R0,R15              LENGTH IN 0                 MBJ20026
         $CWTO L=(R0)              REPLY TO OPERATOR           MBJ20026
CDRNEQSE LR    WA,WC               RESTORE $RESTAB START       MBJ20026
         BXLE  WD,WE,CDRLOOP0      LOOP UNTIL NO MORE QSE'S    MBJ20026
         TM    CDRFLAG,CQCMD       Q. DID WE COME FROM $Q CMD  MBJ20026
         BO    CDC                 A. YES - GO ON TO $DC       MBJ20026
         $CRET ,                   A. NO - RETURN              MBJ20026
CDRINVCP $CRET MSG='INVALID CPU ID'                            MBJ20026
         DROP  WD                                              MBJ20026
         SPACE 3                                               MBJ20026
*                                                              MBJ20026
*  LOCAL VALUES FOR $DR - DISPLAY RESOURCES                    MBJ20026
*                                                              MBJ20026
*                                                              MBJ20026
CDRCPULO EQU   COMEWORK,4          SID OF START CPU FOR $DR    MBJ20026
CDRCPUHI EQU   COMDWORK,4          SID OF END CPU FOR $DR      MBJ20026
CDRFLAG  EQU   COMNULOP,1          FLAG BYTE                   MBJ20026
         EJECT                                                 MBJ20026
************************************************************** MBJ20026
*                                                            * MBJ20026
*        SCAN JOBQ FOR JOBS WHOSE RESOURCE ROUTINGS          * MBJ20026
*        CANNOT BE FULFILLED BY ANY CPU.                     * MBJ20026
*                                                            * MBJ20026
************************************************************** MBJ20026
CDC      NULL  ,                                               MBJ20026
         MVI   CDCFLAG,0           SET FLAGS OFF               MBJ20026
         $CFJSCAN PROCESS=CDCUSE,NEXT=CDCJQE   SCAN JOB QUEUE  MBJ20026
         TM    CDCFLAG,CDCCONF     Q. DID ANY CONFLICTS EXIT   MBJ20026
         BO    CDC2RET             A. YES-BRANCH               MBJ20026
CDC1RET  $CRET MSG='$ NO CONFLICTS EXIST'                      MBJ20026
CDC2RET  $CRET MSG='$ END OF CONFLICTS'                        MBJ20026
         SPACE 1                                               MBJ20026
CDCUSE   TM    JQEFLAGS,QUEBUSY    Q. IS JOB EXECUTING         MBJ20026
         BNZ   CDCJQE              A. YES-NEXT JQE             MBJ20026
         TM    JQETYPE,$XEQ        Q. AWAITING XEQ             MBJ20026
         BZ    CDCJQE              A. NO-NEXT JQE              MBJ20026
         CLI   JQETYPE,$XEQ        JOB IN CONVERTER?           MBJ20026
         BE    CDCJQE              YES, GET NEXT JQE.          MBJ20026
         SR    WC,WC               CLEAR FOR COMPARE.          MBJ20026
         CLM   WC,14,JQERESRT      ANY RESOURCES FOR JOB?      MBJ20026
         BE    CDCJQE              IF EQUAL, NO RESOURCES.     MBJ20026
         L     WA,=V($QSE1)        GET FIRST QSE               MBJ20026
         TM    JQEFLAG2,QUESYSAF   Q. CPU ROUTINGS             MBJ20026
         BZ    CDCNOCPU            A. NO-SET SPECIAL AFFINITY  MBJ20026
         IC    WC,JQEFLAG2         GET AFFINITIES              MBJ20026
         N     WC,=A(X'0000007F')  TURN OFF QUEINDAF           MBJ20026
         B     CDCLOOP2            BRANCH TO CHECKER           MBJ20026
CDCNOCPU LA    WC,255-QUEINDAF     SET AFFINITY TO ALL         MBJ20026
         SPACE 1                                               MBJ20026
         USING QSEDSECT,WB                                     MBJ20026
CDCLOOP2 LR    WB,WA               GET FIRST QSE               MBJ20026
         B     CDCLOOP0            BYPASS SHIFT ON FIRST PASS  MBJ20026
CDCLOOP1 SRL   WC,1                SHIFT IMPORTANT BIT DOWN    MBJ20026
CDCLOOP0 STC   WC,CDCTESTC         STORE FOR COMPARE.          MBJ20026
         TM    CDCTESTC,X'01'      IS A CPU BIT ON.            MBJ20026
         BZ    CDCQSE              IF ZERO, NO CPU BYPASS TEST MBJ20026
         BAL   WF,CDCTEST          ELSE, GO CHECK RESOURCES    MBJ20026
CDCQSE   TM    QSEFLAGS,QSELAST    CONFLICT EXISTS-WAS IT LAST MBJ20026
         BO    CDCONMSG            IF LAST, NO CPU FOR ROUTING MBJ20026
         LA    WB,QSELNGTH(,WB)    ELSE, GET NEXT QSE          MBJ20026
         B     CDCLOOP1            AND CHECK IT.               MBJ20026
CDCTEST  NULL  ,                   R1=JQE, WB=QSE              MBJ20026
         L     WD,JQERESRT         GET JOBS ROUTING            MBJ20026
         SRL   WD,8                  INTO BYTES 1-3            MBJ20026
         L     WE,QSERESTB         GET CPUS RESOURCES          MBJ20026
         SRL   WE,8                  INTO BYTES 1-3            MBJ20026
         NR    WE,WD               CLEAR UNIMPORTANT BITS      MBJ20026
         CR    WE,WD               Q. ROUTING=RESOURCES        MBJ20026
         BE    CDCJQE              A. YES-NEXT JQE             MBJ20026
         BR    WF                    ELSE RETURN               MBJ20026
         SPACE 1                                               MBJ20026
CDCONMSG OI    CDCFLAG,CDCCONF     TURN CONFLICT BIT ON        MBJ20026
         $CFJMSG ,                 ISSUE JOB MSG               MBJ20026
         B     CDCJQE              NEXT JQE                    MBJ20026
*                                                              MBJ20026
*  LOCAL DATA AREAS FOR $DC                                    MBJ20026
*                                                              MBJ20026
CDCFLAG  EQU   COMNULOP,1          FLAG BYTE                   MBJ20026
CDCTESTC EQU   COMNULOP+1,1        CPU TEST BYTE.              MBJ20026
CDCCONF  EQU   X'80'               CONFLICT BIT                MBJ20026
         LTORG ,                                               MBJ20026
         LTORG                                                 MBJ20026
CRESAREA DS    CL4096              WORKAREA FOR $LC            MBJ20026
./       CHANGE NAME=HASPNUC
*        $PATCHSP 64               NO PATCH SPACE DESIRED      MBJ20026
         SPACE 1                                               MBJ20026
* * * NOW CHECK FOR RESOURCE ROUTING IN XEQ JOB.               MBJ20026
         TM    JQETYPE,$XEQ        IS IT XEQ JOB               MBJ20026
         BNO   $QGETRES            NO, DON'T BOTHER            MBJ20026
         CLI   JQETYPE,$XEQ        IS IT FOR CONVERSION        MBJ20026
         BE    $QGETRES            YES, DON'T BOTHER           MBJ20026
         L     R15,$AQSE           GET THIS SYSTEMS QSE        MBJ20026
         LA    R15,0(R15)          HIGH ORDER OFF              MBJ20026
         MVC   QRESORCE,QSERESTB-QSEDSECT(R15) MOVE FOR AND    MBJ20026
         NC    QRESORCE,JQERESRT   TURN OFF UNREQ RESOURCES    MBJ20026
         CLC   QRESORCE,JQERESRT   DOES SYSTEM HAVE REQ RESOR  MBJ20026
         BNE   QNEXT               NO, IGNORE JOB.             MBJ20026
         SPACE 1                                               MBJ20026
* * * NOW CHECK FOR BEFORE, AFTER, CNTL SPECIFICATIONS.        MBJ20026
         STM   WA,WC,PCEWA         SAVE R2,3,4 FOR WORK        MBJ20026
         LA    WA,$JQTYPES*2       SET NUMBER OF QUEUES        MBJ20026
         LA    WB,$JQTYPES-7       # OF QUES TO SRCH.          MBJ20026
QGET001  DS    0H                  SCAN A JOBQ                 MBJ20026
         LA    WC,$JQHEADS-2-(JQECHAIN-JQEDSECT)(WA) PT TO QUE MBJ20026
QDEPSRCH DS    0H                  PICK UP NEXT JOB            MBJ20026
         LH    WC,JQECHAIN-JQEDSECT(WC) GET NEXT JQE           MBJ20026
         N     WC,=A(X'0000FFFF')  INSURE OFFSET POSITIVE      MBJ20026
         BZ    QGET003             END QUE, SEARCH FOR MORE    MBJ20026
         SLL   WC,2                COMPUTE ACTUAL OFFSET *4    MBJ20026
         AL    WC,$JOBQPTR         COMPUTE JQE ADDRESS         MBJ20026
         TM    JQETYPE-JQEDSECT(WC),$XEQ IS IT XEQ Q?          MBJ20026
         BNO   QDEPSRCH            NO, GO GET NEXT             MBJ20026
         CLI   JQETYPE-JQEDSECT(WC),$XEQ IS IT IN CONVERTER    MBJ20026
         BE    QDEPSRCH            YES, GO GET NEXT            MBJ20026
*       WE NOW HAVE A JQE IN THE EXECUTION QUE.                MBJ20026
         CLR   R1,WC               IS IT OUR JQE?              MBJ20026
         BE    QDEPSRCH            GO GET NEXT JQE.
         TM    JQEFLAG3,QUECNTL    DID SELECTED JOB USE CNTL   MBJ20026
         BZ    QAFTER              NO, GO SEE IF AFTER CARD    MBJ20026
         TM    QUEFLAG3(WC),QUECNTL DID OTHER JOB USE CNTL     MBJ20026
         BZ    QBEFORE             NO, GO SEE IF BEFORE CARD   MBJ20026
*       BOTH JOBS USED CNTL CARD NOW SEE IF SAME NAME          MBJ20026
         CLC   JQEDNAME(8),QUEDNAME(WC) SAME NAME?             MBJ20026
         BNE   QDEPSRCH            NO, GO GET ANOTHER JQE      MBJ20026
         TM    QUEFLAGS(WC),QUEBUSY IS OTHER GUY ACTIVE?       MBJ20026
         BZ    QDEPSRCH            NO, CHECK FURTHER.          MBJ20026
         TM    JQEFLAG3,QUEEXC     DID SELECTED JQE HAVE EXC   MBJ20026
         BO    QRESTORE            YES, GO SELECT ANOTHER JQE  MBJ20026
         TM    QUEFLAG3(WC),QUEEXC DID OTHER JOB USE EXC?      MBJ20026
         BO    QRESTORE            YES GO SELECT ANOTHER JQE.  MBJ20026
         B     QDEPSRCH            NO, GO CHECK ANOTHER JQE.   MBJ20026
         SPACE 1                                               MBJ20026
QAFTER   DS    0H                                              MBJ20026
         TM    JQEFLAG3,QUEAFT     SELECTED JOB AFTER          MBJ20026
         BZ    QBEFORE             NO, GO CHECK FOR BEFORE     MBJ20026
         CLC   JQEDNAME(8),QUEJNAME(WC) OT JQE NAME IN SEL JQE MBJ20026
         BE    QRESTORE            YES GO GET NEW JQE          MBJ20026
QBEFORE  DS    0H                                              MBJ20026
         TM    QUEFLAG3(WC),QUEBEF OTHER JOB USE BEFORE        MBJ20026
         BZ    QDEPSRCH            NO, GO GET ANOTHE JQE       MBJ20026
         CLC   JQEJNAME(8),QUEDNAME(WC) SEL NAME IN OTHER JQE  MBJ20026
         BNE   QDEPSRCH            NO, GO GET NEXT JQE.        MBJ20026
         SPACE 1                                               MBJ20026
*    HERE WE HAVE DETERMINED THAT SELECTED JOB IS INELIGIBLE   MBJ20026
*    TO STARTED YET BECAUSE OF ONE OF THE PREVIOUS CHECKS.     MBJ20026
*    WILL NOW RETURN TO ATTEMPT NEW SELECTION.                 MBJ20026
         SPACE 1                                               MBJ20026
QRESTORE DS    0H                                              MBJ20026
         LM    WA,WC,PCEWA         RESTORE WA-WC               MBJ20026
         B     QNEXT               GO SELECT NEW JOB.          MBJ20026
         SPACE 1                                               MBJ20026
QGET003  BCT   WB,QGET004          CONTINUE IF NOT ZERO        MBJ20026
         LM    WA,WC,PCEWA         RESTORE WA-WC               MBJ20026
         B     $QGETRES            SELECTED A JOB, CONTINUE.   MBJ20026
QGET004  DS    0H                                              MBJ20026
         BCTR  WA,0                COMPUTE NEXT LOWER          MBJ20026
         BCTR  WA,0                QUEUE OFFSET.               MBJ20026
         B     QGET001             GO TRY NEXT Q               MBJ20026
         SPACE 1                                               MBJ20026
$QGETRES DS    0H                                              MBJ20026
QRESORCE DC    BL3'0'              RESOURE COMPARE FIELD       MBJ20026
./       CHANGE NAME=HASPRDR
         DC    AL1(4),AL3(RCNTCARD),CL8'CNTL'     HASP CNTL    MBJ20026
         DC    AL1(6),AL3(RBEFCARD),CL8'BEFORE'   HASP BEFORE  MBJ20026
         DC    AL1(5),AL3(RAFTCARD),CL8'AFTER'    HASP AFTER   MBJ20026
*                                                              MBJ20026
*                                                              MBJ20026
*                                                              MBJ20026
*                                                              MBJ20026
*                             HASP  BEFORE, AFTER, CNTL CARD   MBJ20026
*                                           PROCESSING ROUTINE MBJ20026
*                                                              MBJ20026
         SPACE 3                                               MBJ20026
RBEFCARD NULL  ,                                               MBJ20026
         LTR   RBO,RBO             TEST FOR JOB                MBJ20026
         BZ    RILLCCRD            IGNORE BEFORE CARD IF NOT   MBJ20026
         MVI   RCARDLRC,LRC1INUL   INDICATE SUPERFLUOUS CARD   MBJ20026
         BAL   RL1,RPUT            ADD CARD TO OUTPUT FILE     MBJ20026
         TM    RDRSW,RJCLSW        TEST STATUS                 MBJ20026
         BO    *+12                BRANCH IF JCL FILE          MBJ20026
         BAL   RL1,RPUT            TERMINATE DATASET           MBJ20026
         OI    RDRSW,RJCLSW        SET JCL SWITCH              MBJ20026
         MVC   JCTWORK(79),0(RPI)  SAVE CARD IMAGE             MBJ20026
         OI    RQUEFLG3,QUEBEF     SET TO BEFORE CARD          MBJ20026
         NI    RQUEFLG3,255-QUEEXC-QUECNTL-QUEAFT NO CNTL/AFT  MBJ20026
         LA    R15,8(,RPI)         START OF SCAN               MBJ20026
RMVENAM  LA    R14,50              MAX POSITIONS TO SEARCH     MBJ20026
RFINDCAR CLI   0(R15),C'A'         FIND START OF JOBNAME       MBJ20026
         BL    RFINDCON            NOT IN GOOD RANGE           MBJ20026
         CLI   0(R15),C'Z'         TRY OTHER SIDE              MBJ20026
         BNH   RPUTNAM             THIS IS GOOD                MBJ20026
RFINDCON LA    R15,1(,R15)         POINT TO NEXT POSITION      MBJ20026
         BCT   R14,RFINDCAR        LOOP TILL END               MBJ20026
         NI    RQUEFLG3,255-QUEBEF-QUEAFT SET OFF FLAGS        MBJ20026
         B     RILLBADC            ILLEGAL BEFORE/AFTER CARD   MBJ20026
RPUTNAM  MVC   RQUEDNAM(8),0(R15)  PUT NAME IN                 MBJ20026
RENDALL  B     RMSSHIFT            GO TERMINATE NORMALLY       MBJ20026
         SPACE 3                                               MBJ20026
RAFTCARD NULL  ,                                               MBJ20026
         LTR   RBO,RBO             TEST FOR JOB                MBJ20026
         BZ    RILLCCRD            IGNORE AFTER CARD IF NOT    MBJ20026
         MVI   RCARDLRC,LRC1INUL   INDICATE SUPERFLUOUS CARD   MBJ20026
         BAL   RL1,RPUT            ADD CARD TO OUTPUT FILE     MBJ20026
         TM    RDRSW,RJCLSW        TEST STATUS                 MBJ20026
         BO    *+12                BRANCH IF JCL FILE          MBJ20026
         BAL   RL1,RPUT            TERMINATE DATASET           MBJ20026
         OI    RDRSW,RJCLSW        SET JCL SWITCH              MBJ20026
         MVC   JCTWORK(79),0(RPI)  SAVE CARD IMAGE             MBJ20026
         OI    RQUEFLG3,QUEAFT     SET TO AFTER CARD           MBJ20026
         NI    RQUEFLG3,255-QUEEXC-QUECNTL-QUEBEF NO CNTL/BEF  MBJ20026
         LA    R15,7(,RPI)         SET START OF SCAN           MBJ20026
         B     RMVENAM             TREAT LIKE BEFORE           MBJ20026
         SPACE 3                                               MBJ20026
RCNTCARD NULL  ,                                               MBJ20026
         LTR   RBO,RBO             TEST FOR JOB                MBJ20026
         BZ    RILLCCRD            IGNORE CNTL CARD IF NOT     MBJ20026
         MVI   RCARDLRC,LRC1INUL   INDICATE SUPERFLUOUS CARD   MBJ20026
         BAL   RL1,RPUT            ADD CARD TO OUTPUT FILE     MBJ20026
         TM    RDRSW,RJCLSW        TEST STATUS                 MBJ20026
         BO    *+12                BRANCH IF JCL FILE          MBJ20026
         BAL   RL1,RPUT            TERMINATE DATASET           MBJ20026
         OI    RDRSW,RJCLSW        SET JCL SWITCH              MBJ20026
         MVC   JCTWORK(79),0(RPI)  SAVE CARD IMAGE             MBJ20026
         OI    RQUEFLG3,QUECNTL    SET TO CNTL CARD            MBJ20026
         NI    RQUEFLG3,255-QUEEXC-QUEBEF-QUEAFT NO EX/AFT/BEF MBJ20026
         LA    R15,6(,RPI)         SET START OF SCAN           MBJ20026
         LA    R0,30               MAX FOR SEARCH              MBJ20026
RCTLFIND CLI   0(R15),C' '         NON BLANK                   MBJ20026
         BNE   RCTLGOT             YEP, GOT IT.                MBJ20026
         LA    R15,1(,R15)         BUMP TO NEXT POSITION       MBJ20026
         BCT   R0,RCTLFIND         CONTINUE                    MBJ20026
         B     RILLBADC            ERROR IF HERE               MBJ20026
RCTLGOT  LA    R0,8                MAX FOR SEARCH              MBJ20026
         LR    R14,R15             SAVE STARTING POINT         MBJ20026
REXCFIND CLI   1(R14),C','         FIND , EXC  ,SHR            MBJ20026
         BE    REXCGOT             GOT IT                      MBJ20026
         LA    R14,1(,R14)         POINT TO NEXT POSITION      MBJ20026
         BCT   R0,REXCFIND         CONTINUE                    MBJ20026
         B     RPUTNAM             NO SPEC IF HERE, MOVE 8.    MBJ20026
REXCGOT  CLC   2(3,R14),=C'EXC'    EXCLUSIVE CONT DESIRED      MBJ20026
         BNE   RTRYSHR             NO TRY SHR                  MBJ20026
         OI    RQUEFLG3,QUEEXC     SET TO EXCLUSIVE            MBJ20026
REXCPUT  SR    R14,R15             GET LENGTH-1                MBJ20026
         MVI   RQUEDNAM,C' '       BLANK OUT NAME              MBJ20026
         MVC   RQUEDNAM+1(7),RQUEDNAM BLANK REST               MBJ20026
         STC   R14,*+L'*+1         SAVE LENGTH FOR MVC         MBJ20026
         MVC   RQUEDNAM(*-*),0(R15) MOVE IN CNTL NAME          MBJ20026
         B     RENDALL             GO END NORMALLY             MBJ20026
RTRYSHR  CLC   2(3,R14),=C'SHR'    IS IT SHR                   MBJ20026
         BE    REXCPUT             YES, MOVE NAME AND EXIT     MBJ20026
         SPACE 3                                               MBJ20026
RILLBADC NULL  ,                                               MBJ20026
         MVI   0(RPI),C'0'         FORCE DOUBLE SPACE          MBJ20026
         MVI   1(RPI),C'*'         FILL OUT CARD IMAGE         MBJ20026
         MVC   2(78,RPI),1(RPI)    WITH ASTERISKS              MBJ20026
         MVC   (81-46)/2(46,RPI),RSHRVLID+4 MV IN ERROR MSG    MBJ20026
         MVI   RCARDLRC,LRC1CCTL+LRC1TASA ASA CONT CHAR        MBJ20026
         BAL   RL1,RPUT            ADD TO ADD TO OUTPUT FILE   MBJ20026
         $WTO  RSHRVLID,L'RSHRVLID,JOB=YES,  ISSUE ERR         MBJ20026C
               ROUTE=$LOG+$UR,CLASS=$TRIVIA,PRI=$ST    MSG     MBJ20026
         BAL   RL1,RJOBKILL        KILL JOB                    MBJ20026
         LA    RL2,RFLTEST         ADDRESS OF EXIT             MBJ20026
         $RETURN                   AND RETURN                  MBJ20026
         EJECT                                                 MBJ20026
         LR    R1,RPI         USE R1 FOR SCANNING              MBJ20026
         LA    RL1,5          SET SCAN LENGTH                  MBJ20026
RROUTSCN CLC   8(6,R1),RPRINT       IS IT PRINT                MBJ20026
         BE    RPROUTE        YES                              MBJ20026
         CLC   8(6,R1),RPUNCH       IS IS PUNCH                MBJ20026
         BE    RPUNCHY        YES                              MBJ20026
         CLC   8(3,R1),=C'XEQ'     IS IT SHRSPL XEQ            MBJ20026
         BE    RXEQCARD            YES, GO PROCESS             MBJ20026
         LA    R1,1(,R1)      POINT TO NEXT POSITION           MBJ20026
         BCT   RL1,RROUTSCN   TRY AGAIN                        MBJ20026
         B     RILLROUT       ILLEGAL ROUTE CARD               MBJ20026
RPUNCHY  LA    RW,JCTPUOUT    SET FOR PUNCH ROUTING            MBJ20026
RPROUTE  LA    R1,14(,R1)                                      MBJ20026
         LA    RL1,5          SET SCAN LENGTH                  MBJ20026
MRDESTSN CLI   0(R1),C' '     LOOK FOR SPACE                   MBJ20026
         BNE   *+16           NOT EQUAL - GO CHECK DEST        MBJ20026
         LA    R1,1(,R1)      TO NEXT POSITION                 MBJ20026
         BCT   RL1,MRDESTSN   TRY AGAIN                        MBJ20026
         B     RILLROUT       IF NOT YET FOUND, ILLEGAL        MBJ20026
         CLC   0(3,R1),=CL3'CPU'   IS IT CPU                   MBJ20026
         BE    RILLROUT            YES, OLD SHRSPL CRD, REJECT MBJ20026
ROUTEXIT LA    RL2,RJCLCARD   LOAD ADDRESS OF EXIT             MBJ20026
RXEQCARD NULL  ,                                               MBJ20026
         LA    WA,12(,R1)          SET FOR THIRD OPERAND       MBJ20026
         LA    R1,64               MAXIMUM FOR SEARCH          MBJ20026
RRCOMP3  CLI   0(WA),C' '          CHARACTER YET               MBJ20026
         BNE   RXEQOP3             YES DO THIRD OPERAND        MBJ20026
         LA    WA,1(WA)            STEP TO NEXT                MBJ20026
         BCT   R1,RRCOMP3          LOOP TILL END               MBJ20026
         B     RILLROUT            ERROR IF END OF CARD        MBJ20026
RXEQOP3  CLC   0(3,WA),=C'CPU'     CPU ROUTING?                MBJ20026
         BNE   ROUTHERE            NO TRY HERE                 MBJ20026
         IC    WE,3(WA)            GET DIGIT                   MBJ20026
         SLL   WE,28               GET RID OF ZONES            MBJ20026
         SRL   WE,28               PUT BACK                    MBJ20026
         STC   WE,RCPUCOMP+1       STORE FOR COMPARE           MBJ20026
         L     R15,RQSE1           POINT TO FIRST QSE          MBJ20026
         USING QSEDSECT,R15        ADDRESS IT                  MBJ20026
RCPUCOMP CLI   QSESIBSY,*-*        IS IT THIS QSE?             MBJ20026
         BE    ROUTCPU             YES CONTINUE                MBJ20026
         TM    QSEFLAGS,QSELAST    IS IT LAST QSE?             MBJ20026
         BO    RILLROUT            YES, ERROR                  MBJ20026
         LA    R15,QSELNGTH(,R15)  NO, GO TO NEXT QSE          MBJ20026
         B     RCPUCOMP            GO TRY COMPARE AGAIN        MBJ20026
ROUTCPU  DS    0H                                              MBJ20026
         MVI   RDRSIAFF,0          CLEAR ANY CPU ROUTING.      MBJ20026
         OC    RDRSIAFF,QSESIAFF   SET CPU AFFINITY            MBJ20026
         B     ROUTEXIT            GO EXIT.                    MBJ20026
ROUTHERE CLC   0(4,WA),=C'HERE'    IS IT ROUTHERE              MBJ20026
         BNE   ROUTRES             NO, GO TRY RESOURCES.       MBJ20026
         LA    R15,$SIDAFF-(QSESIAFF-QSEDSECT) POINT TO QSE    MBJ20026
         MVI   RDRSIAFF,0          CLEAR ANY CPU ROUTING.      MBJ20026
         OC    RDRSIAFF,QSESIAFF   SET CPU AFFINITY            MBJ20026
         B     ROUTEXIT            GO EXIT                     MBJ20026
ROUTRES  LA    WE,1                SET ONE BIT ON IN MASK      MBJ20026
         SLL   WE,24-1             SHIFT OVER MAXRES-1         MBJ20026
         LA    R15,$RESTAB         POINT TO RESOURCE TABLE     MBJ20026
         LA    R1,24               MAXIMUM RESOURCES           MBJ20026
RRCOMP4  CLC   0($RESLEN,WA),0(R15) CMPARE FOR RESOURCE EQUAL  MBJ20026
         BE    RFINDRES            BR IF HIT.                  MBJ20026
         LA    R15,$RESLEN(,R15)   GO TO NEXT TABLE ENTRY      MBJ20026
         SRL   WE,1                SHIFT MASK 1 BIT            MBJ20026
         BCT   R1,RRCOMP4          GO TRY AGAIN.               MBJ20026
         B     RILLROUT            NO HIT, ERROR               MBJ20026
RFINDRES LA    R15,24/8            NO. OF BYTES IN MASK        MBJ20026
         LA    R1,RQUERESR+(24/8)-1 ADDR OF LAST BYTE OF -     MBJ20026
*                                   RESOURCE MASK IN JQE       MBJ20026
RRESTOP  STC   WE,RSAVE1           SAVE A BYTE FOR OR'ING      MBJ20026
         OC    0(1,R1),RSAVE1      OR IN A BYTE OF RESOURCE    MBJ20026
         SRL   WE,8                SHIFT TO NEXT BYTE OF MASK  MBJ20026
         BCTR  R1,0                BACKUP RQUERER POINTER      MBJ20026
         BCT   R15,RRESTOP         GO PUT IN NEXT BYTE         MBJ20026
         MVC   RMESSAGE(L'ROUTRESM),ROUTRESM MOVE IN MSG.      MBJ20026
         MVC   RMESSAGE+25($RESLEN),0(WA) MOVE IN RESOURCE     MBJ20026
         $WTO  RMESSAGE,L'ROUTRESM,JOB=YES,   PUT OUT          MBJ20026C
               ROUTE=$LOG+$MAIN,CLASS=$ACTION,PRI=$ST THE MSG  MBJ20026
         B     ROUTEXIT            GO EXIT.                    MBJ20026
RSETHMSG $MSG  102,'*  H E L D  FOR THE FOLLOWING  ----'       MBJ20026
ROUTRESM $MSG  118,'-- RESOURCE ROUTING -- ******** '          MBJ20026
RSHRVLID $MSG  114,'-- INVALID /*BEFORE AFTER OR CNTL SPECIFICATION ' 6
         $RESTABL                  GENERATE RESOURCE TABLE.    MBJ20026
         SPACE 3                                               MBJ20026
         MVC   RQUEDNAM(8),=CL8' ' BLANK OUT NAME              MBJ20026
         XC    RQUERESR(4),RQUERESR CLEAR REMAINING            MBJ20026
         MVC   JQEDNAME(12),RQUEDNAM PUT IN ANY RESOURCE STUFF MBJ20026
./       ADD   NAME=$RESTABL
         MACRO -- $RESTABL -- EXECUTION RESOURCE TABLE.        MBJ20026
         $RESTABL &DUMMY
         DC    CL8'3525'           3525 PUNCH/INTERPRET        MBJ20026
         DC    CL8'EMPTY1'                                     MBJ20026
         DC    CL8'EMPTY2'                                     MBJ20026
         DC    CL8'GIS'            GIS PACKS.                  MBJ20026
         DC    CL8'TSO'            TIME SHARING SYSTEM.        MBJ20026
         DC    CL8'INQUIRY'        ONLINE INQUIRY SYSTEM.      MBJ20026
         DC    CL8'TRIMS'          TRUST IMS DATABASE.         MBJ20026
         DC    CL8'EMPTY3'                                     MBJ20026
         DC    CL8'EMPTY4'                                     MBJ20026
         DC    CL8'CHECKWTR'       UTILITY CHECK WRITER.       MBJ20026
         DC    CL8'EMPTY5'                                     MBJ20026
$NOLEFT  EQU   (*-$RESTAB)/$RESLEN NUMBER OF ENTRIES.          MBJ20026
         MEND                                                  MBJ20026
