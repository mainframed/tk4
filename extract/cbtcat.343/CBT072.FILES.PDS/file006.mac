*          DATA SET CBT348     AT LEVEL 001 AS OF 09/12/76
         TITLE 'CBT348 -   FREEALL COMMAND PROCESSOR FOR MVS'
CBT348   CSECT
         SAVE  (14,12)
         BALR  R12,0
         USING FREEALL,R12
FREEALL  LR    R8,R1             LOAD CPPL ADDR
         GETMAIN R,LV=LDYNAMIC
         LR    R11,R1
         USING WORKAREA,R11
         LA    R9,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R9,8(,R13)
         LR    R7,R13            SAVE REG 13 FOR A WHILE
         LR    R13,R9
         MVC   DAPB(DAPB18L),DAPB18C   INITIALIZE DAPB
         L     R1,4(0,R8)        UPT ADDR
         L     R2,12(0,R8)       ECT ADDR
         MVC   17(3,R7),1(R2)    MOVE PREVIOUS RETURN CODE
         MVI   16(R7),X'00'      TO SAVE AREA
         LA    R3,ECB            ECB ADDR
         L     R4,8(0,R8)        PSCB ADDR
         LA    R5,DAPB           PARAMETER BLOCK ADDR
         STM   R1,R5,DAPL        SET DYNAMIC ALLOCATION PARM LIST
         SR    R7,R7
         ST    R7,ECB            CLEAR ECB
         EJECT
         L     R8,16             CVT
         L     R8,0(0,R8)        TCB WORDS
         L     R8,4(0,R8)        CURRENT TCB
         L     R8,12(0,R8)       TIOT
         LA    R8,24(0,R8)       DD ENTRIES
         SR    R9,R9
TIOTLOOP IC    R9,0(0,R8)        DD ENTRY LENGTH
         LTR   R9,R9             LENGTH EQUAL ZERO
         BZ    RETURN            YES, END OF TIOT
         CLI   4(R8),C' '        CONCATENATED DDNAME
         BE    TIOTNEXT          YES, DAIR WONT FREE
         CLC   18(2,R8),=H'0'    DD ALLOCATED
         BNE   FREE              YES, GO FREE IT
TIOTNEXT AR    R8,R9             NEXT ENTRY
         B     TIOTLOOP          GO CHECK NEXT
         EJECT
FREE     BAL   R6,RESERVED       GO SEE IF THIS NAME RESERVED
         B     TIOTNEXT          RESERVED, DO NOT FREE
         MVC   DAPB+12(8),4(R8)  MOVE IN DDNAME
         LA    R1,DAPL           LOAD PARM LIST ADDR
         LINK  EP=IKJDAIR        GO TO DYNAMIC ALLOCATION
         LTR   R15,R15           TEST RETURN CODE
         BZ    TIOTNEXT          GOOD RETURN
         MVC   FREENAME(8),4(R8) MOVE DDNAME TO MSG
         LA    R10,FREEMSGN-FREEMSG   LOAD MSG LENGTH
*        TPUT  FREEMSG,(10),WAIT,HOLD
         B     TIOTNEXT          GO GET NEXT
         SPACE 3
RETURN   LR    R1,R13            DYNAMIC AREA ADDRESS IN R1
         LA    R0,LDYNAMIC       SIZE OF AREA
         L     R13,4(,R13)       PREV SAVE AREA
         LA    R1,0(,R1)         CLEAR HI ORDER
         SVC   10                FREEMAIN
         LM    14,12,12(13)      RESTORE REGS
         MVI   12(13),X'FF'
         BR    14                RETURN
         EJECT
RESERVED LA    R3,RESTABLE       RESERVED NAME TABLE ADDR
RESCHK   CLC   0(8,R3),4(R8)     DDNAME IN TABLE
         BCR   8,R6              YES, DO NOT FREE
         LA    R3,8(0,R3)        NEXT TABLE ENTRY
         CLI   0(R3),X'00'       ZERO ENTRY
         BNE   RESCHK            NO, GO CHECK NEXT
         B     4(R6)             YES, END OF TABLE, GO FREE DD
         SPACE 3
RESTABLE DS    0CL240
         DC    CL8'SYSPROC'
         DC    CL8'STEPLIB'
         DC    CL8'SYSPRINT'
         DC    CL8'SYSIN'
         DC    CL8'SYSEDIT'
         DC    CL8'SYSUT1'
         DC    CL8'SYSUT2'
         DC    CL8'SYSUT3'
         DC    CL8'SYSUT4'
         DC    CL8'SYSHELP'
         DC    CL8'SYSUDUMP'
         DC    CL8'SYSABEND'
         DC    CL8'FT05F001'
         DC    CL8'FT06F001'
         DC    CL8'PANDD1'
         DC    CL8'T'
         DC    CL8'Z$1'
         DC    CL8'Z$2'
         DC    CL8'Z$3'
         DC    11XL8'00'
RESTABND EQU   *
         EJECT
         LTORG
FREEMSG  DC    C'FREEALL UNABLE TO FREE '
FREENAME DC    CL8' '
         DC    C', CONTINUING'
FREEMSGN EQU   *
         SPACE 3
DAPB18C  DS    0F
         DC    X'0018'           DA18CD
         DC    H'0'              DA18FLG
         DC    H'0'              DA18DARC
         DC    H'0'              DA18CTRC
         DC    A(0)              DA18PDSN
DA18DDN  DC    CL8' '            DA18DDN
         DC    CL8' '            DA18MNM
         DC    CL2' '            DA18SCLS
         DC    X'00'             DA18DSP2
         DC    X'10'             DA18CTL - RLSE PERM ALLOC
         DC    CL8' '            DA18JBNM
DAPB18L  EQU   *-DAPB18C         LENGTH
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
DAPB     DS    0F
         ORG   DAPB+DAPB18L
         DS    0D
DAPL     DS    5F
ECB      DS    F
         DS    0D
LDYNAMIC EQU   *-WORKAREA
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
