*          DATA SET SMP005     AT LEVEL 001 AS OF 09/12/74
 TITLE 'S M P X C D S   ---   C D S   C R O S S   R E F E R E N C E R'
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                                                              *
*   SMPXCDS BUILDS A SEQUENTIAL DATA SET FOR SUBSEQUENT SOR-   *
*      TING, FROM THE SMP CDS MODULE AND / OR PTF ENTRIES,     *
*      DEPENDING ON WHETHER ITS PARAMETER IS MOD, PTF, OR      *
*      ALL (OR ABSENT).  THE SORTED FILE IS USED BY SMPXREF    *
*      TO PRINT CROSS - REFERENCE LISTINGS, AS FOLLOWS :       *
*                                                              *
*     (1) FOR EACH LOAD MODULE, ALL ITS COMPONENT MODULES      *
*     (2) FOR EACH MACRO, THE PTFS THAT HAVE UPDATED IT        *
*     (3) FOR EACH MODULE, THE PTFS OF EACH TYPE AGAIST IT     *
*     (4) FOR EACH SUPERSEDING PTF, THE PTFS IT SUPERSEDED     *
*                                                              *
*      THE FORMAT OF THE INTERMEDIATE FILE IS AS FOLLOWS :     *
*                                                              *
*     OFFSET/LENGTH/FIELD DESCRIPTION FOR EACH TYPE OF ENTRY   *
*                                                              *
*       0   /  8   / FOR MODULE ENTRIES, THE MODULE NAME.      *
*                    FOR PTF ENTRIES, THE PTF NAME.            *
*       8   /  8   / FOR MODULE ENTRIES, A LOAD MODULE NAME.   *
*                    FOR PTF ENTRIES, A MODULE OR SUPER NAME.  *
*       9   /  1   / SORT CODE : 01 = LOAD MOD,  04 = SUPER,   *
*                                02 = MACRO PTF, 03 = MOD PTF  *
*       A   /  1   / PTF TYPE  : 00 = MACRO,     02 = S/ZAP,   *
*                                03 = MODULE,    04 = X/ZAP    *
*       B   /  1   / STATUS : BIT 0 = APPLIED, BIT 2 = FORCE,  *
*                             BIT 1 = ACCEPTED,BIT 3 = SUPED   *
*       C   /  3   / PTF RECEIVED DATE (YYYDDF)                *
*                                                              *
*  SMPXCDS WAS DEVELOPED BY W.B.GOULD OF CALSPAN CORP. AND     *
*     MODIFIED BY J.V.DOODY OF MARINE MIDLAND SERVICES CORP.   *
*     TO HANDLE X/ZAPS AND TO USE DIFFERENT DDNAMES FOR TSO.   *
*                                                              *
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
         SPACE 2
         SPACE 2
SMPXCDS  CSECT
         EJECT
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    10,SAVEAREA
         ST    10,8(13)
         LR    13,10
         L     1,0(1)
         NC    0(2,1),0(1)
         BNZ   PARMPRES
         SPACE 1
DEFOPTS  DS    0H
         OI    SWITCHES,LISTPTF+LISTMOD
         B     OPENDCBS
PARMPRES DS    0H
         CLC   0(2,1),=H'3'
         BL    DEFOPTS
         CLC   2(3,1),=C'PTF'
         BNE   CHKMOD
         OI    SWITCHES,LISTPTF
         B     OPENDCBS
CHKMOD   DS    0H
         CLC   2(3,1),=C'MOD'
         BNE   DEFOPTS
         OI    SWITCHES,LISTMOD
         SPACE
OPENDCBS DS    0H
         OPEN  (DIRECTRY,INPUT,SMPCDS,INPUT,SORTIN,OUTPUT)
         EJECT
READBLK  DS    0H
         READ  DIRREAD,SF,DIRECTRY,DIRECTA
         CHECK DIRREAD
         LA    11,DIRECTA
         USING DIRECTA,11
         LH    9,DIRECTA
         AR    9,11
         BCTR  9,0
         LA    11,2(,11)
         EJECT
LOOPDIR  DS    0H
         CLC   DIRECTA(8),=8X'FF'
         BE    DONEDIR
         CLI   DIRECTA,DLIB
         BE    NXTMEMBR
         CLI   DIRECTA,SYSTEM
         BE    NXTMEMBR
         CLI   DIRECTA,EOF
         BE    NXTMEMBR
         CLI   DIRECTA,PTF
         BE    PTFENTRY
         TM    DIRECTA,MOD
         BO    MODENTRY
         SPACE 1
NXTMEMBR DS    0H
         IC    8,DIRECTA+11
         N     8,=F'31'
         SLL   8,1
         LA    8,12(,8)
         BXLE  11,8,LOOPDIR
         B     READBLK
         SPACE 1
DONEDIR  DS    0H
         CLOSE (DIRECTRY,,SMPCDS,,SORTIN)
         SPACE 1
EOJ      DS    0H
         L     13,SAVEAREA+4
         RETURN (14,12),T,RC=0
         SPACE 1
         SPACE 1
         EJECT
PTFENTRY DS    0H
         TM    SWITCHES,LISTPTF
         BZ    NXTMEMBR
         IC    7,DIRECTA+11
         N     7,=F'31'
         SLL   7,1
         S     7,=F'13'
         MVC   LEFTHALF(8),DIRECTA
         MVC   DATE(4),DIRECTA+12
         LA    6,DIRECTA+7
         TM    DIRECTA+12,X'10'        SUPERSEDED PTF
         BZ    NXTPTFMD                NO ...  CONTINUE AS USUAL
         MVC   RGHTHALF(8),9(6)        SUPERSEDING PTF NAME
         MVC   RGHTHALF+8(2),=X'0404'  FLAGS
         PUT   SORTIN,ENTRY
         B     NXTMEMBR
         SPACE 1
NXTPTFMD DS    0H
         LA    6,9(6)
         CLI   0(6),X'FF'
         BE    NXTMEMBR
         MVC   RGHTHALF(9),0(6)
         CLI   RGHTHALF+8,X'00'
         BE    MACROP
         MVI   RGHTHALF+8,X'01'
         SPACE 1
MACROP   DS    0H
         OI    RGHTHALF+8,X'02'
         MVC   RGHTHALF+9(1),8(6)
         PUT   SORTIN,ENTRY
         SPACE
PTFDLOOP DS    0H
         S     7,=F'9'
         BNM   NXTPTFMD
         EJECT
PTFDATA  DS    0H
         TM    DIRECTA+11,X'80'
         BO    NXTMEMBR
         LA    3,9(7)
         LCR   3,3
         LA    5,CDSDATA(3)
         MVC   0(9,5),9(6)
         MVC   TTR(3),DIRECTA+8
         POINT SMPCDS,TTR
         LA    4,80(7)
         SPACE 1
PTFREAD  DS    0H
         READ  CDSREAD,SF,SMPCDS,CDSDATA,MF=E
         CHECK CDSREAD
         SPACE 1
NXTMDPTF DS    0H
         CLI   0(5),X'FF'
         BE    NXTMEMBR
         MVC   RGHTHALF(9),0(5)
         CLI   RGHTHALF+8,X'00'
         BE    MACROP1
         MVI   RGHTHALF+8,X'01'
         SPACE
MACROP1  DS    0H
         OI    RGHTHALF+8,X'02'
         MVC   RGHTHALF+9(1),8(5)
         PUT   SORTIN,ENTRY
         EJECT
PTFRLOOP DS    0H
         LA    5,9(5)
         S     4,=F'9'
         BP    NXTMDPTF
         LA    3,9(4)
         LCR   3,3
         LA    3,CDSDATA(3)
         MVC   0(9,3),0(5)
         LR    5,3
         LA    4,80(4)
         B     PTFREAD
         EJECT
MODENTRY DS    0H
         TM    SWITCHES,LISTMOD
         BZ    NXTMEMBR
         IC    7,DIRECTA+11
         N     7,=F'31'
         SLL   7,1
         S     7,=F'17'
         MVC   LEFTHALF(8),DIRECTA
         MVC   DATE(4),=PL4'0'
         LA    6,DIRECTA+13
         SPACE 1
NXTMODMD DS    0H
         LA    6,8(6)
         CLI   0(6),X'FF'
         BE    NXTMEMBR
         MVC   RGHTHALF(8),0(6)
         MVI   RGHTHALF+8,X'01'
         MVI   RGHTHALF+9,X'00'
         PUT   SORTIN,ENTRY
         S     7,=F'8'
         BP    NXTMODMD
         SPACE 1
MODDATA  DS    0H
         TM    DIRECTA+11,X'80' IF ALIAS BIT IS ON
         BO    NXTMEMBR         THERE IS NO EXTENSION
         LA    3,8(7)
         LCR   3,3
         LA    5,CDSDATA(3)
         MVC   0(8,5),8(6)
         LA    4,80(7)
         MVC   TTR(3),DIRECTA+8
         POINT SMPCDS,TTR
         EJECT
MODREAD  DS    0H
         READ  CDSREAD,SF,SMPCDS,CDSDATA,MF=E
         CHECK CDSREAD
         SPACE
NXTMDMOD DS    0H
         CLI   0(5),X'FF'
         BE    NXTMEMBR
         MVC   RGHTHALF(8),0(5)
         MVI   RGHTHALF+8,X'01'
         MVI   RGHTHALF+9,X'00'
         PUT   SORTIN,ENTRY
         LA    5,8(5)
         S     4,=F'8'
         BP    NXTMDMOD
         LA    3,8(4)
         LCR   3,3
         LA    3,CDSDATA(3)
         MVC   0(8,3),0(5)
         LR    5,3
         LA    4,80(4)
         B     MODREAD
         EJECT
*   SYNCHRONOUS EXIT ROUTINE FOR I/O ERRORS ON CONTROL DATA SET   *
SYNAD    SYNADAF ACSMETH=BPAM
         WTO   MF=(E,(1))
         SYNADRLS
         B     EOJ
         EJECT
*   DECLARE ALL CONSTANTS AND CONTROL BLOCKS USED BY SMPXCDS   *
         READ  CDSREAD,SF,SMPCDS,CDSDATA,MF=L
         DS  0F
SAVEAREA DS    18F
TTR      DC    F'0'
ENTRY    DS    0H
LEFTHALF DS    8X
RGHTHALF DS    10X
DATE     DS    4X
SWITCHES DC    X'00'
LISTPTF  EQU   X'80'
LISTMOD  EQU   X'40'
DLIB     EQU   X'F0'
SYSTEM   EQU   X'F1'
EOF      EQU   X'F2'
PTF      EQU   X'F9'
MOD      EQU   X'C0'
DUMMY    DS    CL10
CDSDATA  DS    CL80
DUMMY1   DS    CL10
         LTORG
         EJECT
DIRECTRY DCB  DSORG=PS,DDNAME=SMPCDS,MACRF=(R),RECFM=F,BLKSIZE=256
         EJECT
SORTIN   DCB   DSORG=PS,DDNAME=SORTIN,MACRF=(PM)
         EJECT
SMPCDS   DCB   DSORG=PO,DDNAME=SMPCDS,MACRF=(R,W),EODAD=NXTMEMBR,      X
               SYNAD=SYNAD
         EJECT
DIRECTA  DS    CL256
         DS    CL10
         SPACE 2
         END   SMPXCDS
