LPAREA   CSECT
         SAVE  (14,12)
         LR    R3,R15
         USING LPAREA,R3
         ST    R13,SAVE+4
         LA    R13,SAVE
         L     R6,76
         MVC   FIND+5(3),361(R6)   LOWER BOUNDARY OF PLPA
         L     R2,ADCON1
         L     R5,ADCON4
         UNPK  0(15,R5),0(8,R2)
         UNPK  14(15,R5),7(8,R2)
         TR    0(29,R5),TDPTABLE-240
         OC    MLPAWORK(30),FIFTYBLK
         MVC   MLPADR(6),MLPAWORK+10
         LA    R1,MSGMLPA
         WTO   MF=(E,(1))
WTOR1    EQU   *
         MVC   REQ1(8),BLANK8
         WTOR  'ENTER MODULE NAME OR END',REQ1,8,ECB1
         WAIT  ECB=ECB1
         MVC   ECB1(4),=F'0'
         OC    REQ1(8),HEXFORTY
         CLC   REQ1(8),=C'END     '
         BE    GETOUT
         L     R4,76   A(CVT) INTO R4
         L     R4,188(R4)  ADDRESS OF POINTER TO MOST RECENT
**                         CDE FOR LPA IS NOW PUT INTO R4
         L     R4,0(R4)     ADDRESS OF CDE INTO R4
CDECHECK LA    R5,REQ1
         CLC   8(8,R4),0(R5)   CHECK FOR MODULE NAME
         BE    PICK
END      L     R4,0(R4)   NEXT CDE ADDRESS INTO R4
         C     R4,=F'0'   IS IT END OF CDE CHAIN
         BE    THRU1
         B     CDECHECK
PICK     MVC   NAME(8),0(R5)
         MVC   FIND+4(4),16(R4)
PREWTO   EQU   *
         L     R2,ADCON1
         L     R5,ADCON2
         UNPK  0(15,R5),0(8,R2)
         UNPK  14(15,R5),7(8,R2)
         TR    0(29,R5),TDPTABLE-240
         OC    CONSLOUT+4(20),FIFTYBLK
         MVC   MODLNAME(8),NAME
         MVC   MODLADDR(6),CONSLOUT+14
         LA    R1,MESSAGE
         WTO   MF=(E,(1))
         B     WTOR1
THRU     EQU   *
         WTO   'REQUESTED LOAD MODULE NOT FOUND'
         B     WTOR1
THRU1    L     R4,76   A(CVT) INTO R4
         L     R4,360(R4)   A(FIRST LPDE)
LPDECHK  LA    R5,REQ1
**FOLLOWING LOGIC IS TAKEN DIRECTLY FROM AMBLIST,
** CSECT HMBLKLPA, WHICH CONSTRUCTS THE LPA PORTION OF NUCREF
CHKFF    CLC   8(8,R4),FFEIGHT
         BE    THRU
         CLC   8(8,R4),0(R5)
         BE    PICKL
         AH    R4,=H'40'
         B     CHKFF
PICKL    TM    28(R4),X'04'    IS THIS A MINOR LPDE
         BZ    PICK1           NO
         MVC   NAME(8),0(R5)
         B     PICK2
PICK1    MVC   NAME(8),0(R5)
PICK2    MVC   FIND+4(4),16(R4)    E.P.
         B     PREWTO
GETOUT   L     R13,SAVE+4
         RETURN (14,12),RC=0
         EJECT
         LTORG
SAVE     DS    18F
MESSAGE  DC    AL2(45)
         DC    X'0000'
MSGBODY  DC    C'LOAD MODULE '
MODLNAME DC    C'NNNNNNNN '
         DC    C'E.P. IS AT '
MODLADDR DC    C'XXXXXX'
         DC    8C' '
HEXFORTY DC    8X'40'
ECB1     DC    F'0'
REQ1     DC    8C' '
NAME     DC    8C' '
ADR      DC    4C' '
ADCON1   DC    A(FIND)
ADCON2   DC    A(CONSLOUT+4)
ADCON3   DC    A(CONSLOUT)
CONSLOUT DC    AL2(20)
         DC    X'0000'
         DC    20X'40'
FILLER1  DC    70F'0'
TDPTABLE DC    C'0123456789ABCDEF'
         DS    0F
FIND     DC    8X'00'
FIFTYBLK DC    50X'40'
FFEIGHT  DC    X'FFFFFFFFFFFFFFFF'
LOCATION DC    3C' '
BLANK8   DC    8C' '
MLPAWORK DC    30X'40'
ADCON4   DC    A(MLPAWORK)
MSGMLPA  DC    AL2(37)
         DC    X'0000'
         DC    C'M.L.P.A. WOULD START BELOW '
MLPADR   DC    C'XXXXXX'
         DC    10C'0'
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
