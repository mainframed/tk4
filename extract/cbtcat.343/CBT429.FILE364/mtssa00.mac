     TITLE 'CSECT: TSSACTV'
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*        TSSACTV - ACTIVATE TRANSIENT SUBSYSTEM                       *
*                                                                     *
*                                                                     *
*        HOOK SUBSYSTEM SSVT TO SSCVT                                 *
*                                                                     *
*                                                                     *
*        THIS PROGRAM MAY BE USED AS AN INITIALIZATION FOR            *
*        FOR ALL SIMPLE SUBSYSTEMS.  BY SIMPLE IS MEANT A             *
*        SUBSYTEM WHOSE INTIALIZATION ROUTINE REQUIRES NOTHING        *
*        OTHER THAN THE LINKING OF THE SSVT AND SSCVT.  SUCH          *
*        A SUBSYSTEM MUST CONTAIN AN LPA RESIDENT MODULE WHOSE        *
*        NAME IS THE SAME AS THE SUBSYSTEM NAME AND WHOSE ENTRY       *
*        POINT IS THE SUBSYSTEM'S COMPLETED SSVT.  SUCH A SUB-        *
*        SYSTEM SHOULD BE TRANSIENT, I.E., NOT EXECUTE IN ITS         *
*        OWN ADDRESS SPACE.                                           *
*                                                                     *
*                                                                     *
*                                                                     *
*        MODULE ENTERED IN MASTER KEY ZERO                            *
*                                                                     *
*        UPON ENTRY REG. 1 = A(A(SSCVT))                              *
*                                                                     *
*                                                                     *
*                                                                     *
*        POTENTIAL CALLERS:                                           *
*                                                                     *
*        1. DIRECTLY BY MASTER SCHEDULER TO INITIALIZE SIMPLE         *
*           TRANSIENT SUBSYSTEMS DURING IPL.                          *
*                                                                     *
*        2. BY MODIFY ROUTINES OR PROGRAMS (MTSS) THAT EMULATE        *
*           THE MASTER SCHEDULER IN CALLING SUBSYSTEM INITIALI-       *
*           ZATION ROUTINES.                                          *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        PROGRAM INITIALIZATION                                       *
*                                                                     *
*               R9  = SSCT BASE                                       *
*               R12 = BASE = MODULE ADDRESSIBILITY                    *
*               R13 = SAVE AREA AND WORKAREA BASE                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
TSSACTV  AMODE 31
TSSACTV  RMODE 24
TSSACTV  CSECT
         STM   R14,R12,12(R13)           SAVE REGISTERS
         LR    R12,R15                   LOAD BASE
         USING TSSACTV,R12               DECLARE BASE
         B     TSSA0000                  BRANCH AROUND CSECT ID
         DC    C'TSSACTV &SYSDATE'
TSSA0000 DS    0H
         LR    R5,R1                     SAVE PTR TO PARM ADDRESS
         LA    R3,TSSAL                  SET LENGTH OF WORK AREA
         GETMAIN R,LV=(3)                GET A WORKAREA
         LR    R2,R1                     A(AREA) FOR MVCL
         SR    R14,R14                   NO SOURCE
         SR    R15,R15                   NO SOURCE LENGTH
         MVCL  R2,R14                    ZERO THE AREA
         ST    R13,4(,R1)                CHAIN THE
         ST    R1,8(,R13)                   SAVE AREAS
         LR    R13,R1                    NEW SAVE AREA
         USING TSSA,R13                  BASE FOR WORK AREA
         MVC   TSAID,=C'TSSA'            LABEL WORKAREA
         ST    R5,TSAPRMAD               SAVE R1 ENTRY PARM (TRACE)
         L     R9,0(,R5)                 GET A(SSCT)
         USING SSCT,R9                   SSCT ADDRESSIBILITY
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        LOCATE SSVT AND POINT SSCT TO IT                             *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         MVI   TSALMOD,X'40'             CLEAR THE LOAD MODULE NAME
         MVC   TSALMOD+1(L'TSALMOD-1),TSALMOD   TO BLANKS THEN SET TO
         MVC   TSALMOD(L'SSCTSNAM),SSCTSNAM     SUBSYSTEM NAME
         LA    R0,TSALMOD                A(ENTRY POINT NAME)
         LA    R2,TSSAE010               A(ERROR ROUTINE)
         LOAD  EPLOC=(0),ERRET=(2)       FIND A(SUBSYSTEM SSVT)...
         ST    R0,SSCTSSVT               ...PUT INTO SSCT
         ST    R0,TSAEPAD                ...PUT INTO WKAREA (TRACE)
         SPACE 5
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        PUT OUT INITIALIZATION MESSAGE                               *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         MVC   TSAMSG(M01L),M01          MSG TO WORKAREA
         MVC   TSAMSG+M01P1(L'SSCTSNAM),SSCTSNAM   SUBSYS NAME TO MSG
         WTO   MF=(E,TSAMSG)             PUT OUT MESSAGE
         SLR   R15,R15                   RETURN CODE = ZERO
         B     TSSARTN0                  RETURN TO CALLER
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        ERROR LOAD MODULE NOT FOUND                                  *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
TSSAE010 DS    0H
         MVC   TSAMSG(E01L),E01          MSG TO WORKAREA
         MVC   TSAMSG+E01P1(L'SSCTSNAM),SSCTSNAM   SUBSYS NAME TO MSG
         MVC   TSAMSG+E01P2(L'TSALMOD),TSALMOD     EP TO MESSAGE
         WTO   MF=(E,TSAMSG)             PUT OUT MESSAGE
         LA    R15,4                     RETURN CODE = 4 = ERROR
         B     TSSARTN0                  RETURN TO CALLER
         SPACE 5
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        RETURN TO CALLER                                             *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         DROP R9                         DROP SSCT ADDRESSIBILITY
         SPACE
TSSARTN0 DS    0H
         L     R9,TSASAVE+4              GET A(CALLER'S SAVE)
         LR    R10,R15                   SAVE RETURN CODE
         FREEMAIN RU,LV=TSSAL,A=(13)     FREE WORKING STORAGE
         LR    R15,R10                   SET RETURN CODE
         LR    R13,R9                    CALLER'S SAVE AREA PTR
         RETURN (14,12),RC=(15)
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        DATA AREAS                                                   *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
M01      WTO  'MTSS01I - SUBSYSTEM XXXX IS NOW ACTIVE',ROUTCDE=(2,11), X
               DESC=4,MF=L
M01L     EQU  *-M01
M01P1    EQU  4+20
         SPACE 3
E01      WTO  'MTSS09E - SUBSYSTEM XXXX NOT INITIALIZED SSVT MODULE, XXX
               XXXXXX, NOT FOUND',ROUTCDE=(2,11),DESC=4,MF=L
E01L     EQU  *-E01
E01P1    EQU  4+20
E01P2    EQU  4+54
         SPACE 3
         LTORG
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*        DSECT FOR PROGRAM WORK AREAS                                 *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
TSSA     DSECT
TSASAVE  DS    18F                       REGISTER SAVE AREA
TSAID    DS    CL8                       WORKAREA LABEL
TSAPRMAD DS    F                         A(CALLER'S PARM)
TSALMOD  DS    CL8                       LOAD MODULE EP
TSAEPAD  DS    F                         EP ADDRESS
TSAMSG   DS    XL100                     MESSAGE WORKAREA
TSSAL    EQU   *-TSSA
         SPACE 5
*---------------------------------------------------------------------*
*                                                                     *
*        DSECT FOR SSCT                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         PRINT NOGEN
         IEFJSCVT
         EJECT
*---------------------------------------------------------------------*
*        MISCELLANEOUS EQUATES                                        *
*---------------------------------------------------------------------*
         SPACE
R0       EQU       0
R1       EQU       1
R2       EQU       2
R3       EQU       3
R4       EQU       4
R5       EQU       5
R6       EQU       6
R7       EQU       7
R8       EQU       8
R9       EQU       9
R10      EQU      10
R11      EQU      11
R12      EQU      12
R13      EQU      13
R14      EQU      14
R15      EQU      15
         END
