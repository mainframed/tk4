JOBNAME TITLE 'SYNTEX STC INTRECEPT FOR ADDING JOB INFO       H. DEAN'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                   PREVIOUS SAVE AREA ADDRESS
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BASEREG  EQU   R12
         EJECT
JSEL     DSECT                     LOGON SCHEDULING ENTERANACE LIST
         DS    F                   FILLER
JSELJSOL DS    F
JSELCSCB DS    F                   CSCB POINTER
JSELJCLS DS    F                   JCL POINTER
JSELJZXL DS    F
JSELASCB DS    F                   ASCB POINTER
         ORG   JSELASCB
JSELEOL  DS    XL1                 END OF LIST INDICATOR
         SPACE 3
JCLS     DSECT                     JOB CONTROL LANGUAGE SET
JCLSHDR  DS    0F
JCLSJCLS DS    F                   NEXT JCL POINTER OR ZERO
         DS    F                   FILLER
JCLSCARD DS    0CL80
JCLSLASH DS    CL2                 // JCL INDICATOR
JCLSLABL DS    CL8                 JOB NAME
         DS    CL1                 FILLER
JCLSJOB  DS    CL3                 JOB
         DS    CL1                 FILLER
JCLSBODY DS    CL56                BODY OF JCL STATEMENT
JCLSCNTN DS    CL1                 CONTINUATION INDICATOR
JCLSSEQ  DS    CL8                 SEQUENCE NUMBER
         ORG   JCLSJOB
JCLSEXEC DS    CL5                 EXECUTE CARD
         ORG
JCLSACCT EQU   JCLSCARD+16
         EJECT
R13DSECT DSECT
R13SAVE  DS    18F                 SAVE AREA
R13DCB   DS    CL96                DCB FOR JOBNAME
R13DECB  DS    0F                  DECB FOR READ
R13ECB   DS    F                   ECB FOR READ
R13TYP1  DS    X
R13TYP2  DS    X
R13LNTH  DS    AL2(0)              LENGTH OF READ
R13DCBP  DS    A                   POINTER TO THE DCB
R13AREA  DS    A                   POINTER TO THE IOAREA
R13RPW   DS    A                   RECORD POINTER WORD
R13IO    DS    CL80                IOAREA
R13OPEN  OPEN  (R13DCB,INPUT),MF=L
R13ERROR DS    C                   ERROR INDICATOR FROM EXIT ROUTINES
R13TYPE  DS    C                   TYPE OF CARD READ
R13CARD  DS    C                   TYPE OF CURRENT CARD
R13FLAG  DS    C                                            *HMD 04/81*
R13LEN   EQU   *-R13DSECT
         EJECT
JFCBDSCT DSECT
         IEFJFCBN   LIST=YES                                *HMD 04/81*
         EJECT
         DCBD DSORG=PS
         EJECT
TIOTDSCT DSECT
         IEFTIOT1
         EJECT
         CVT  DSECT=YES,LIST=YES
         EJECT
         IKJTCB     LIST=YES                                *HMD 04/81*
         EJECT
         IRAOUCB
         EJECT
         IHAASCB
         EJECT
JOBNAME CSECT
         USING *,R15
         B     BEGIN
         DC    CL8'JOBNAME'        NAME OF MODULE
         DC    C'&SYSTIME &SYSDATE' DATE/TIME OF ASSEMBLY
BEGIN    DS    0H
         LR    R10,R1              SAVE PARM POINTER
         DROP  R15
         USING JOBNAME,BASEREG
         LR    BASEREG,R15
         LR    R11,R13             SAVE ORIGINAL R13
         GETMAIN R,LV=R13LEN       NEW SAVE AREA
         USING R13DSECT,R13
         LR    R13,R1              SAVE OLD SAVE AREA ADDRESS
         LR    R4,R10              JSEL ADDRESS
         L     R1,16               POINT TO CVT
         USING CVT,R1
         L     R1,CVTTCBP          TCB POINTER
         L     R1,4(R1)            ADDRESS OF CURRENT TCB
         USING TCB,R1
         L     R1,TCBTIO           POINT TO TIOT
         USING TIOT1,R1
         LA    R7,TIOELNGH         FIRST TIOT ENTRY
         USING TIOELNGH,R7
         SH    R1,=H'24'           POINT TO LAST ENTRY USED
         L     R2,0(R1)            SAVE LAST CURRENTLY USED ENTRY
TIOTENT  DS    0H
         CLI   TIOEDDNM,C' '       CONCATENATED DATASET
         BE    TIOTNEXT
         CLC   TIOEDDNM,=CL8'JOBNAME' LOOK FOR JOBNAME DD CARD
         BE    FINDJOB
TIOTNEXT DS    0H
         SR    R1,R1               CLEAR
         IC    R1,TIOELNGH         LENGTH OF THIS ENTRY
         LA    R7,0(R1,R7)         POINT TO NEXT ENTRY
         CLI   TIOELNGH,X'00'      IS THIS THE LAST ENTRY
         BE    RETURN
         CR    R7,R2               LAST ENTRY
         BE    RETURN
         B     TIOTENT
         EJECT
FINDJOB  DS    0H
         MVC   R13DCB,JOBDCB       INSURE REENTRENCY
         MVI   R13CARD,0           CLEAR FLAG BYTE          *HMD 04/81*
         MVI   R13FLAG,C' '        CLEAR FLAG BYTE          *HMD 04/81*
         MVI   R13OPEN,X'80'       END OF LIST
         OPEN  (R13DCB,INPUT),MF=(E,R13OPEN)
         TM    R13DCB+DCBOFLGS-IHADCB,DCBOFOPN IS DATASET OPEN
         BNO   RETURN              ..NO BYPASS
         USING JSEL,R4
         ICM   R4,7,JSELJCLS+1     JCL FOR LOGON
         USING JCLS,R4
         BZ    RETURN              WAS JCL BUILT
         FIND  R13DCB,JCLSLABL,D   USE JOB NAME IN FIND
         LTR   R15,R15             HAS NAME BEEN FOUND
         BNZ   RETURN
         SPACE 3
FINDJOB1 DS    0H
         TRT   JCLSBODY,CONTINUE   IS THERE A COMMA OR SPACE ON CARD
         CLM   R2,1,COMMA          IS IT A COMMA
         LR    R2,R1               SAVE ADDRESS OF FIRST SPACE
         BNE   READJOB
         ICM   R4,7,1(R4)          NEXT CARD
         BZ    RETURN
         CLC   JCLSEXEC,EXEC       IS THIS AN EXEC CARD
         BE    RETURN              ..YES. THIS IS A ERROR
         B     FINDJOB1
         SPACE 3
READJOB  DS    0H
         MVI   R13ERROR,X'40'
         READ  R13DECB,SF,R13DCB,R13IO,80,MF=E
         CHECK R13DECB
         CLI   R13ERROR,X'40'      WAS THERE AN ERROR ON READ
         BNE   RETURN
*                                                           *HMD 04/81*
         CLI   R13FLAG,C'X'        DO WE NEED ACCT SCAN?    *HMD 04/81*
         BE    RJCLB               NO, FORGET ROUTINE       *HMD 04/81*
LOOPINIT LA    R5,R13IO+3          SCAN FOR ACCT CARD       *HMD 04/81*
         LA    R6,1                INCREMENT                *HMD 04/81*
         LA    R7,R13IO+71         END OF SCAN              *HMD 04/81*
BXLEOP   CLI   0(R5),C' '          FIND BEGINNING OF OPERAND*HMD 04/81*
         BNE   GOTIT               WE GOT AN OPERAND        *HMD 04/81*
         BXLE  R5,R6,BXLEOP        CONTINUE SCAN            *HMD 04/81*
         B     RETURN              NOTHING ON CARD          *HMD 04/81*
GOTIT    CLI   0(R5),C'('          BEGINNING OF ACCT FIELD  *HMD 04/81*
         MVI   R13FLAG,C'X'        SAY WE'VE BEEN HERE      *HMD 04/81*
         BNE   RJCLB               ASSUME IT'S A KEYWORD    *HMD 04/81*
         SR    R7,R5               LENGTH OF TRT            *HMD 04/81*
         BNP   RETURN              MUST BE ERROR            *HMD 04/81*
         SR    R7,R6               DOWN 1 FOR EXECUTE       *HMD 04/81*
         BNP   RETURN              AVOID ERRORS             *HMD 04/81*
         SR    R2,R2               CLEAR FUNCTION REGISTER  *HMD 04/81*
         SR    R1,R1               CLEAR ADDRESS REGISTER   *HMD 04/81*
         EX    R7,ACCTTRT          LOOK FOR FIRST BLANK     *HMD 04/81*
         BZ    RETURN              ERROR; NO SPACES         *HMD 04/81*
         SR    R1,R5               R1 = LENGTH OF ACCT DATA *HMD 04/81*
         BNP   RETURN              ERROR OF SOME KIND       *HMD 04/81*
         CH    R1,MAXLEN           LENGTH GT MAX?           *HMD 04/81*
         BH    RETURN              ERROR; RETURN TO CALLER  *HMD 04/81*
         SR    R1,R6               DOWN 1 FOR MVC           *HMD 04/81*
         BNP   RETURN              NOPE, MUST BE AN ERROR   *HMD 04/81*
         EX    R1,ACCTMOVE         MOVE ACCT DATA           *HMD 04/81*
         LA    R1,JCLSBODY+1(R1)   FIND END OF FIELD        *HMD 04/81*
         LA    R2,L'RESTCARD(R1)   REPOSITION FOR COMMA     *HMD 04/81*
         MVC   0(L'RESTCARD,R1),RESTCARD                    *HMD 04/81*
*                                  MOVE IN MSGLEVEL=1 CARD  *HMD 04/81*
         B     READJOB             GET NEXT CARD            *HMD 04/81*
*                                                           *HMD 04/81*
RJCLB    BAL   R14,JCLBUILD        GET ADDITIONAL STORAGE
         CLC   JCLC,R13IO          IS THIS AN ACCOUNT CARD
         MVI   R13TYPE,C'A'        NON CONTINUATION TYPE JCL
         BNE   JCLINSRT
         MVI   R13TYPE,C'J'        CONTINUATION TYPE JCL
         CLI   R13CARD,X'00'       IS COMMA IN PLACE
         BNE   JCLINSRT
         MVI   R13CARD,C'J'
         MVI   0(R2),C','          MOVE A COMMA
         B     JCLINSRT
         EJECT
JCLINSRT DS    0H
         CLI   R13TYPE,C'J'        IS THIS A JCL CONTINUATION
         BNE   JCLINST1
         CLC   R13TYPE,R13CARD     ARE WE STILL PROCESSING JCL CONT
         BE    JCLINST2
         B     READJOB
JCLINST1 DS    0H
         MVI   R13TYPE,C'A'        INDICATE END OF CONTINUATION
JCLINST2 DS    0H
         MVC   JCLSCARD,R13IO      MOVE JCL STATEMENT
** REMOVED RESTRICTION THAT '//*ACCT' CARD SIGNIFIES THE    *HMD 04/81*
** LAST CARD                                                *HMD 04/81*
** 04/10/81 - HOWARD M. DEAN                                *HMD 04/81*
         B     READJOB
         SPACE 3
JCLBUILD DS    0H      R4 CONTAINS THE ADDR OF JCLSJCLS
         GETMAIN R,LV=88,SP=253
         MVC   JCLSJCLS-JCLS(4,R1),JCLSJCLS  EXEC CARD TO //*ACCT CARD
         ST    R1,JCLSJCLS         CHAIN //*ACCT CARD TO JOB CARD
         LR    R4,R1               ESTABLISH NEW JCL CARD ADDRESS
         BR    R14                 RETURN TO CALLING ROUTINE
         SPACE 3
         EJECT
RETURN   DS    0H
         TM    R13DCB+DCBOFLGS-IHADCB,DCBOFOPN IS DATA SET OPEN
         BNO   RETURN1
         CLOSE R13DCB,MF=(E,R13OPEN)
RETURN1  DS    0H
         LR    R1,R13               SET ADDRESS FOR FREEMAIN
         FREEMAIN R,LV=R13LEN,A=(1) FREE ACQUIRED SAVE AREA
         LR    R1,R10               PARM PASSED FROM IEEVJCL
         LR    R13,R11              RESTORE R13
         XCTL  EP=IEESB605          RESTORE REGISTERS
         EJECT
ABNDEXIT DS    0H
         MVI   3(R1),4              SET IGNORE ABEND
         MVI   R13ERROR,C'A'        INDICATE ABEND EXIT TAKEN
         BR    R14                  RETURN TO SYSTEM
         SPACE 3
SYNAD    DS    0H
         MVI   R13ERROR,C'S'        INDICATE SYNAD EXIT TAKEN
         BR    R14                  RETURN TO SYSTEM
         EJECT
***********************************************************************
* * * *        CONSTANTS                                        * * * *
***********************************************************************
MAXLEN   DC    H'45'               MAXIMUM LENGTH OF ACCTG  *HMD 04/81*
H5       DC    H'5'                BUILD JCL REQUEST
H1       DC    H'1'                MIN LENGTH OF ACCT FIELD AFTER SCAN
JCLC     DC    CL3'// '            ADD THIS CONTIUATION CARD
ACCT     DC    CL7'//*ACCT'        ACCOUNT CARD
JOB      DC    CL3'JOB'            JOBCARD
EXEC     DC    CL4'EXEC'           EXECUTE CARD
RESTCARD DC    C',MSGLEVEL=1'      REST OF CARD             *HMD 04/81*
ACCTMOVE MVC   JCLSBODY(0),0(R5)   MOVE IN ACCT DATA        *HMD 04/81*
ACCTTRT  TRT   0(0,R5),CHKCARD     LOOK FOR SPACE           *HMD 04/81*
         EJECT
JOBDCB   DCB   DDNAME=JOBNAME,RECFM=F,BLKSIZE=80,MACRF=R,SYNAD=SYNAD,  *
               DSORG=PO,EXLST=EXLST,EODAD=RETURN
         EJECT
EXLST    DC    XL1'91',AL3(ABNDEXIT) ABEND EXIT ROUTINE
CONTINUE DC    256XL1'00'          TRANSLATE AND TEST TABLE
         ORG   CONTINUE+X'40'
         DC    XL1'40'
         ORG   CONTINUE+C','
COMMA    DC    CL1','
         ORG   ,                                            *HMD 04/81*
CHKCARD  DC    256XL1'00'          TRT TABLE                *HMD 04/81*
         ORG   CHKCARD+X'40'       CHECK FOR SPACES         *HMD 04/81*
         DC    X'01'               NON-ZERO VALUE           *HMD 04/81*
         ORG   ,                                            *HMD 04/81*
         EJECT
***********************************************************************
* * * *        LITERALS                                         * * * *
***********************************************************************
         SPACE 3
         LTORG
         SPACE 3
         END  JOBNAME
