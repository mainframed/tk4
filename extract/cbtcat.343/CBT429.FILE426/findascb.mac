FIND     TITLE 'FIND AN ASCB BY ADDRESS SPACE NAME'         *HMD 10/80*
***********************************************************************
*                                                                     *
*                                                                     *
*                              FINDASCB                               *
*                                                                     *
*                                                                     *
* ABSTRACT: FIND AN ASCB BY ADDRESS SPACE NAME.                       *
*                                                                     *
*                                                                     *
* FUNCTION: FINDASCB INDEXES THROUGH THE ADDRESS SPACE VECTOR TABLE   *
*           (ASVT) LOOKING FOR AN ASCB WHOSE INITIATED JOB NAME       *
*           (ASCBJBNI) OR STARTED JOB NAME (ASCBJBNS) EQUALS THE      *
*           INPUT ADDRESS SPACE NAME.                                 *
*                                                                     *
*                                                                     *
* INPUT: R1 -> A(CL8'ADDRESS SPACE NAME')                             *
*              A(F'TO RECEIVE THE ADDRESS OF THE ASCB IF FOUND')      *
*                                                                     *
*                                                                     *
* OUTPUT: 2ND INPUT PARM -> REQUESTED ASCB                            *
*                                                                     *
*                                                                     *
* RETURN CODES: R15 = 0 => ASCB FOUND.                                *
*                     4 => ASCB NOT FOUND.                            *
*                                                                     *
*                                                                     *
* DATA AREAS REFERENCED: CVT                                          *
*                        ASVT                                         *
*                        ASCB                                         *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              RETURN                                                 *
*              REGS (USER)                                            *
*              CVT                                                    *
*              IHAASVT                                                *
*              IHAASCB                                                *
*                                                                     *
*                                                                     *
* ATTRIBUTE: RENT                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
FINDASCB CSECT                FIND AN ASCB BY NAME
         SAVE  (1,7),,FINDASCB_&SYSDATE_&SYSTIME SAVE CALLER'S REGS
         USING FINDASCB,R15   CSECT ADDRESSABILITY
         LM    RJOBNM,RETAD,0(R1) A(INPUT JOB NAME),A(RETURN ADDR)
         L     R1,16          A(CVT)
         USING CVT,R1         ADDRESSABILITY
         L     R1,CVTASVT     A(ASVT)
         USING ASVT,R1        ADDRESSABILITY
         L     RMAXU,ASVTMAXU MAXIMUM NUMBER OF ASCB'S
         LA    RENTRY,ASVTENTY A(A(1ST ASCB))
GETASCB  L     R1,0(RENTRY) A(ASCB OR < 0 => NO ASCB)
         LTR   R1,R1          ASCB SLOT IN USE?
         BM    NEXTASCB       NO => GET ANOTHER ASCB
         USING ASCB,R1        ADDRESSABILITY
         LM    RJOBI,RJOBS,ASCBJBNI A(INIT JOB NAME),A(START JOB NAME)
         CLC   0(8,RJOBI),0(RJOBNM) FOUND THE ASCB?
         BE    GOTASCB        YES => GIVE IT TO OUR CALLER
         CLC   0(8,RJOBS),0(RJOBNM) FOUND THE ASCB?
         BE    GOTASCB        YES => RETURN IT TO OUR CALLER
NEXTASCB LA    RENTRY,4(RENTRY) A(A(NEXT ASCB))
         BCT   RMAXU,GETASCB  CHECKED ALL ASCB'S?
         XC    0(4,RETAD),0(RETAD) YES => TELL THE CALLER
         RETURN (1,7),RC=4    WE COULDN'T FIND AN ASCB
GOTASCB  ST    R1,0(RETAD)    RETURN THE CALLER THE
         RETURN (1,7),RC=0    ADDRESS OF THE ASCB
         EJECT
         REGS  ,
RJOBNM   EQU   R2             A(INPUT JOB NAME)
RETAD    EQU   R3             A(WORD TO RECEIVE A(ASCB))
RMAXU    EQU   R4             MAXIMUM NUMBER OF ASCB'S
RENTRY   EQU   R5             A(A(ASCB))
RJOBI    EQU   R6             A(INITIATED JOB NAME)
RJOBS    EQU   R7             A(STARTED JOB NAME)
         EJECT
         CVT   DSECT=YES,LIST=YES
         EJECT                                              *HMD 12/79*
         IHAASVT
         EJECT                                              *HMD 12/79*
         IHAASCB
         END
