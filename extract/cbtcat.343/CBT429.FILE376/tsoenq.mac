//C4ADMDMA JOB C4ADM000,MSGCLASS=X,CLASS=A,REGION=4M,NOTIFY=C4ADMDM
//*
//*
//ASMHCL  PROC
//C      EXEC  PGM=IEV90,PARM=OBJECT,REGION=5M
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(10,5)),DSN=&SYSUT1
//SYSPUNCH DD  SYSOUT=B,DCB=(BLKSIZE=800),SPACE=(CYL,(5,5,0))
//SYSPRINT DD SYSOUT=X,DCB=(BLKSIZE=3509),UNIT=(,SEP=(SYSUT1,SYSPUNCH))
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(5,5,0)),            *
//             DCB=(BLKSIZE=400),DSN=&&LOADSET
//L    EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL',REGION=2M,COND=(8,LT,C)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD DD DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(2,1,2)),DSN=&GOSET(GO)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=X,DCB=(RECFM=FB,BLKSIZE=3509)
//  PEND
//*
//  EXEC ASMHCL,
//    PARM.L='XREF,LET,LIST,AMODE=24,RMODE=24'
//SYSLIB   DD   DSN=SYS1.MACLIB,DISP=SHR
//         DD   DSN=SYS1.MODGEN,DISP=SHR
         MACRO
&NAME    SAVEALL &RENT,&REG=3,&EQU=YES,&ID=NO
         GBLB  &SAVRENT
         GBLB  &REGSW
         GBLB  &TIME
         GBLC  &SAVNDX
&SAVNDX  SETC  '&SYSNDX'
&NAME    CSECT
         AIF   ('&SAVRENT' EQ '1').NOTFREE
         AIF   ('&REGSW' EQ '1').NOEQU
         AIF   ('&EQU' NE 'YES').NOEQU
&REGSW   SETB  1
         SPACE
***********************************************************************
********           DEFINITION OF GENERAL REGISTERS             ********
***********************************************************************
R0       EQU   0    GENERAL REGISTER ZERO
R1       EQU   1    GENERAL REGISTER ONE
R2       EQU   2    GENERAL REGISTER TWO
R3       EQU   3    GENERAL REGISTER THREE
R4       EQU   4    GENERAL REGISTER FOUR
R5       EQU   5    GENERAL REGISTER FIVE
R6       EQU   6    GENERAL REGISTER SIX
R7       EQU   7    GENERAL REGISTER SEVEN
R8       EQU   8    GENERAL REGISTER EIGHT
R9       EQU   9    GENERAL REGISTER NINE
R10      EQU   10   GENERAL REGISTER TEN
R11      EQU   11   GENERAL REGISTER ELEVEN
R12      EQU   12   GENERAL REGISTER TWELVE
R13      EQU   13   GENERAL REGISTER THIRTEEN
R14      EQU   14   GENERAL REGISTER FOURTEEN
R15      EQU   15   GENERAL REGISTER FIFTEEN
         SPACE
RA       EQU   10   GENERAL REGISTER TEN
RB       EQU   11   GENERAL REGISTER ELEVEN
RC       EQU   12   GENERAL REGISTER TWELVE
RD       EQU   13   GENERAL REGISTER THIRTEEN
RE       EQU   14   GENERAL REGISTER FOURTEEN
RF       EQU   15   GENERAL REGISTER FIFTEEN
***********************************************************************
********             END OF REGISTER DEFINITIONS               ********
***********************************************************************
         SPACE
.NOEQU   ANOP
         SAVE  (14,12)
         AIF   ('&REG' EQ '').MESS
         BALR  &REG(1),0
         AIF   (N'&REG EQ 1).A
         AIF   (N'&REG EQ 2).B
         AIF   (N'&REG EQ 3).C
         AIF   (N'&REG EQ 4).D
         AIF   (N'&REG EQ 5).E
         MNOTE *****,'EXCESSIVE BASE REGISTERS -- 5 ASSUMED *****'
         AGO   .E
.MESS    MNOTE 12,'***** NO BASE REGISTER SPECIFIED *****'
         MEXIT
.NOTFREE MNOTE 12,'***** ENDALL NOT ISSUED PRIOR TO NEW SAVEALL *****'
         MEXIT
.E       USING *,&REG(1),&REG(2),&REG(3),&REG(4),&REG(5)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         L     &REG(5),=A(Q&SYSNDX+16384)
         AGO   .END
.D       USING *,&REG(1),&REG(2),&REG(3),&REG(4)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         AGO   .END
.C       USING *,&REG(1),&REG(2),&REG(3)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         AGO   .END
.B       USING *,&REG(1),&REG(2)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTER
         AGO   .END
.A       USING *,&REG(1)
.END     ANOP
         AIF   ('&RENT' NE 'RENT').NORENT
&SAVRENT SETB  1
         GETMAIN R,LV=4096         GET STORAGE FOR SAVE AREA
         ST    13,4(1)             SET UP FORWARD CHAIN
         ST    1,8(13)             SET UP BACKWARD CHAIN
         L     0,20(0,13)          RESET REGISTER 0
         L     1,24(0,13)          RESET REGISTER 1
         L     13,8(0,13)          LOAD SAVE AREA ADDRESS
         B     J&SYSNDX           BRANCH AROUND LTORG
         AGO   .RENT
.NORENT  ANOP
&TIME    SETB  1
         ST    13,SAVE&SYSNDX+4    SET UP FORWARD CHAIN
         LA    1,SAVE&SYSNDX
         ST    1,8(0,13)           SET UP BACKWARD CHAIN
         L     1,24(0,13)          RESET REGISTER 1
         LA    13,SAVE&SYSNDX
         B     J&SYSNDX           BRANCH AROUND BASE REGISTER CONST
SAVE&SYSNDX DC 18F'0'
.RENT    ANOP
         LTORG
         AIF   ('&ID' NE 'YES').NOID
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
.NOID    ANOP
J&SYSNDX DS    0H
         MEND
         MACRO
&NAME    ENDALL &CODE=
         GBLB  &TIME
         GBLB  &SAVRENT
         GBLC  &SAVNDX
         LCLA  &LEN
         LCLC  &REG
         AIF   ('&CODE' GT '4095').INVRET
&NAME    DS    0H
         AIF   (K'&CODE LE 0).CHKRENT
         AIF   ('&CODE'(1,1) NE '(').CHKRENT
         AIF   ('&CODE' EQ '(13)' OR '&CODE' EQ '(14)').INVREG
         AIF   ('&CODE' EQ '(0)' OR '&CODE' EQ '(1)').INVREG
         AIF   ('&CODE' NE '(15)').CHKRENT
         LR    2,15               SAVE REG 15 RETURN CODE
.CHKRENT AIF   (NOT &SAVRENT).NOTRENT
&SAVRENT SETB  0
         LR    1,13               LOAD SAVE AREA ADDRESS
         L     13,4(0,13)         RE-LOAD REGISTER 13
         FREEMAIN R,LV=4096,A=(1) FREE SAVE AREA STORAGE
         AGO   .RENT
.NOTRENT ANOP
         AIF   (NOT &TIME).NOSAVE
&TIME    SETB  0
         L     13,SAVE&SAVNDX+4   RE-LOAD REGISTER 13
.RENT    ANOP
         AIF   ('&CODE' EQ '').COND1
         AIF   ('&CODE' NE '(15)').CHKREG
         LR    15,2               GET RETURN CODE BACK FROM R2
         AGO   .ENDIT
.CHKREG  AIF   ('&CODE'(1,1) NE '(').NOTREG
&LEN     SETA  K'&CODE-2
&REG     SETC  '&CODE'(2,&LEN)
         LR    15,&REG            GET RETURN CODE
         AGO   .ENDIT
.NOTREG  ANOP
         LA    15,&CODE           LOAD CODE TO REG 15
         AGO   .ENDIT
.INVRET  MNOTE 12,'***** RETURN CODE PASSED GREATER THAN 4095 *****'
         MEXIT
.NOSAVE  MNOTE 12,'***** SAVEALL NOT ISSUED PRIOR TO ENDALL *****'
         MEXIT
.INVREG  MNOTE 12,'***** INVALID REGISTER USED FOR RETURN CODE "&CODE"'
         MEXIT
.COND1   LA    15,0               LOAD ZERO (NO CODE) TO REG 15
.ENDIT   ANOP
         RETURN     (14,12),T,RC=(15)
         MEND
*          DATA SET TSO115SP3  AT LEVEL 053 AS OF 11/18/81
*********************************************************************
*                                                                   *
* FUNCTION - DISPLAY DATASET ENQUE CONFLICTS (TSO CP) ON SP1.3      *
*            SYSTEM.                                                *
*                                                                   *
* COMMAND SYNTAX - TSOENQ                                           *
*                                                                   *
* NOTE - THIS CODE HAS NOT BEEN TESTED WITH A GRS RING.             *
*        SCOPE=GLOBAL WILL HAVE TO BE SPECIFIED ON THE GQSCAN MACRO *
*        AND THE GQSCAN HAS TO RUN AUTHORIZED KEY ZERO.             *
*                                                                   *
* NOTE - THE 'SYSNAME' IN IEASYS00 SHOULD SPECIFY YOUR SMFID TO     *
*        GET THE CORRECT SYSTEM NAME IN THE 'SYSTEM' FIELD,         *
*        OTHERWISE (NONA) NONAME WILL BE DISPLAYED.                 *
*                                                                   *
* NOTE - FOR A SYSTEM WITH MANY DATASETS IN USE, THE "SCANAREA"     *
*        MIGHT HAVE TO BE MADE LARGER.  IF IT IS THE CORRESPONING   *
*        FIELD ON THE "AREA" PARAMETER OF THE "GQSCAN" MACRO MUST   *
*        BE CHANGED.                                                *
*                                                                   *
*********************************************************************
*
TSO115   SAVEALL REG=(2,12)            BASE REGS
         EJECT
         LR    R3,R1                   LOAD REG.
         USING SMCABASE,R4             SET ADDRESSABILITY
         L     R9,16                   GET ADDRESS OF CVT
         L     R4,196(R9)
         LA    R8,TITLE                SET ADDRESS OF TITLE LINE
         MVC   37(4,R8),SMCASID        MOVE IN SYSTEM ID
         SPACE
         TIME  DEC                     R0: HHMMSSTH
GETTIME  ST    R0,WORKB                TIME
         UNPK  55(7,R8),WORKB(4)
         MVC   61(2,R8),59(R8)         XXSS
         MVC   59(1,R8),58(R8)         MXSS
         MVC   58(1,R8),57(R8)         HHXM MXSS
         MVI   57(R8),C'.'
         MVI   60(R8),C'.'             HH.MM.SS
GETDATE  ST    R1,WORKA                DATE
         UNPK  46(7,R8),WORKA(4)
         MVC   46(2,R8),48(R8)         YY
         MVC   49(3,R8),50(R8)         DDD
         MVI   48(R8),C'.'             YY.DDDX
         MVI   52(R8),C' '             YY.DDD
         TPUT  CLEAR,L'CLEAR,FULLSCR   CLEAR SCREEN
         TPUT  TITLE,L'TITLE           ADDR PRINT TITLE AND TIME
         LR    R5,R13                  SAVE R13 FOR ENDALL
         LA    R8,SCANAREA             ADDR OF RETURNED DATA
         LA    R9,SYSDSN               ADDR OF SYSDSN MAJOR NAME
         LA    R13,SCANSAVE            ADDR GQSCAN SAVE AREA
         EJECT
SCAN     GQSCAN AREA=((R8),8192),      ADDR AND LENGTH OF INFO RETURNEDX
               REQLIM=MAX,             MAX NUM OF OWNERS TO BE RETURNEDX
               SCOPE=ALL,                                              X
               SYSNAME=0,WAITCNT=1
         LR    R13,R5                  RELOAD REG 13 FOR ENDALL
         LA    R7,BRTABLE              ADDRESS OF BRANCH TABLE
         B     0(R15,R7)               BRANCH TO APPROPRIATE ROUTINE
BRTABLE  B     CODE00                  ALLS OK
         B     CODE04                  NO RESOURCES MATCHED REQUEST
         B     CODE08                  GQSCAN AREA TOO SMALL
         B     CODE0C                  ENCOUNTERED ERROR DATA AREA NG
         B     CODE10                  INVALID SYSNAME
         SPACE 2
CODE04   TPUT  CODE04M,L'CODE04M       DSNAME NOT FOUND
         B     RETURN                  EXIT
         SPACE
CODE08   TPUT  CODE08M,L'CODE08M       GQSCAN AREA TOO SMALL
         B     RETURN                  EXIT
         SPACE
CODE0C   TPUT  CODE0CM,L'CODE0CM       ERROR IN GQSCAN
         B     RETURN                  EXIT
         SPACE
CODE10   TPUT  CODE10M,L'CODE10M       INVALID SYSNAME
         B     RETURN                  EXIT
         EJECT
CODE00   ST    R0,RIBLNGTH             STORE RIB AND RIBE LENGTH
         LR    R7,R1                   SAVE NUMBER OF RIBS
         LA    R10,SCANAREA            ADDRESS OF GQSCAN AREA (1ST RIB)
         USING RIB,R10                 RESOURCE INFORMATION BLOCK
RIBLOOP  LR    R11,R10                 SAVE RIB POINTER
         AH    R11,RIBLNGTH            POINT TO RIB VARIABLE SECTION
         USING RIBVAR,R11              RESOURCE INFORMATION BLOCK
         SR    R8,R8                   CLEAR REG
         MVC   DSN(44),=44C' '         CLEAR BEFORE USING
         LH    R8,RIBVLEN              LENGTH OF VARIABLE SECTION
         BCTR  R8,0                    MINUS ONE FOR EX INSTRUCTION
         EX    R8,MVCDSN               MOVE DSN TO PRINT LINE
         B     SKIP                    BYPASS NEXT INSTRUCTION
MVCDSN   MVC   DSN(1),RIBRNAME         MOVE DSN TO PRINT LINE
         DROP  R11
SKIP     LR    R11,R10                 SAVE RIB POINTER
         AH    R11,RIBLNGTH            POINT TO RIB VARIABLE SECTION
         AH    R11,RIBVLEN             POINT TO FIRST RIBE
         USING RIBE,R11                RESOURCE INFORMATION BLOCK EXT
         L     R9,RIBNRIBE             LOAD THE # OF RIBE'S
         CLC   RIBNTWE,ANYENQ          ANY WAITING FOR EXCLUSIVE ?
         BNE   TPUTHDRS                  YES...PROCESS
         CLC   RIBNTWS,ANYENQ          ANY WAITING FOR SHARE ?
         BE    BUMPRIB                   NO....BUMP TO NEXT RIB
         EJECT
TPUTHDRS TPUT  BLANK,L'BLANK           PRINT BLANK
         TPUT  DSNLINE,L'DSNLINE       PRINT DATASET NAME
         TPUT  BLANK,L'BLANK           PRINT BLANK
         TPUT  FLAGHD,L'FLAGHD         PRINT FLAG HEADER
         TPUT  ULINE,L'ULINE           UNDER LINE IT
         TPUT  BLANK,L'BLANK           PRINT BLANK
         EJECT
SEARCH   MVI   SWITCH,C'1'             SET SWITCH THAT THERE WAS ENQ
         MVC   JOBNAME(8),RIBEJBNM     JOBNAME OF REQUESTOR
         MVC   SYSTEM(4),RIBESYSN      SYSTEM NAME OF REQUESTOR
         SPACE
TEST1    TM    RIBERFLG,RIBETYPE       IS THIS EXCL OR SHARED?
         BO    SHARED                     YES..
         MVC   EXCLFLAG(4),EXCL           NO...TELL USER
         B     TEST2                      NEXT TEST
SHARED   MVC   EXCLFLAG(4),SHR            TELL USER
         SPACE
TEST2    TM    RIBERFLG,RIBEMC         IS MUST COMPLETE?
         BO    MUSTC                      YES..
         MVI   MCFLAG,C'N'                NO...TELL USER
         B     TEST3                      NEXT TEST
MUSTC    MVI   MCFLAG,C'Y'                TELL USER
         SPACE
TEST3    TM    RIBERFLG,RIBERESV       IS THIS RESERVE REQUEST?
         BO    RESERVE                    YES..
         MVI   RESVFLAG,C'N'              NO...TELL USER
         B     TEST4                      NEXT TEST
RESERVE  MVI   RESVFLAG,C'Y'              TELL USER
         SPACE
TEST4    TM    RIBERFLG,RIBERESC       IS THIS RESERVE REQUEST GLOBAL?
         BO    GLOBAL                     YES..
         MVI   GLOBFLAG,C'N'              NO...TELL USER
         B     TEST5                      NEXT TEST
GLOBAL   MVI   GLOBFLAG,C'Y'              TELL USER
         SPACE
TEST5    TM    RIBESFLG,RIBESTAT       IS THIS GUY THE OWNER?
         BO    OWNER                      YES..
         MVI   OWNFLAG,C'N'               NO..TELL USER
         MVC   WAITFLAG(9),WAITING        TELL USER WAITING FOR DSET'S
         B     TPUTIT                     GO PRINT IT
OWNER    MVI   OWNFLAG,C'Y'               TELL USER
         SPACE
TPUTIT   TPUT  JOBLINE,L'JOBLINE       PRINT IT
         MVC   JOBLINE(72),=72C' '     CLEAR FOR NEXT LINE
         SPACE 2
BUMPRIBE AH    R11,RIBELENG            POINT TO NEXT RIBE
         BCT   R9,SEARCH               AND DO IT FOR NEXT USER
BUMPRIB  SR    R3,R3                   CLEAR THE REG
         L     R4,RIBNRIBE             NUMBER OF RIBE'S
BUMPLOOP AH    R3,RIBELENG             ADD RIBE LENGTH
         BCT   R4,BUMPLOOP             AND ADD TIMES NUMBER OF RIBE'S
         AH    R3,RIBLNGTH             ADD LENGTH OF RIB
         AH    R3,RIBVLEN              ADD LENGTH OF VARIABLE SECTION
         AR    R10,R3                  POINT TO NEXT RIB
         BCT   R7,RIBLOOP              AND LOOP UNTIL NO MORE RIBS
         SPACE
         CLI   SWITCH,C'1'             WAS THERE AN ENQ ?
         BE    RETURN                     YES...EXIT
         TPUT  BLANK,L'BLANK              NO... PRINT BLANK
         TPUT  NOENQ,L'NOENQ                    TELL USER
         EJECT
RETURN   ENDALL
         EJECT
         DS   0F
RIBLNGTH DC    H'0'                    RIB LENGTH
RIBELENG DC    H'0'                    RIBE LENGTH
ANYENQ   DC    F'0'                    NO ENQ'S
         SPACE
         DS   0F
SCANSAVE DS    18F                     18 WORD SAVEAREA REQ FOR GQSCAN
WORKA    DS    2F'0'                   DATE/TIME WORKAREA
WORKB    DS    2F'0'                   DATE/TIME WORKAREA
SWITCH   DS    C'0'                    ENQ SWITCH
WAITING  DC    CL9'*WAITING*'          WAITING FLAG
EXCL     DC    CL4'EXCL'               EXCL FLAG
SHR      DC    CL4'SHR '               SHR FLAG
SYSDSN   DC    CL8'SYSDSN  '           QNAME FOR GQSCAN
         EJECT
DSNLINE  DS   0CL72                    DATASET NAME LINE
         DC    CL14'RESOURCE NAME='
DSN      DS    CL44
DSNNULL  DC    CL14' '
DSNLINEL EQU   *-DSNLINE
         SPACE
ULINE    DS   0CL72                    UNDERLINE
         DC    72C'-'
ULINEL   EQU   *-ULINE
         SPACE
JOBLINE  DS   0CL72                    INFO LINE
JOBNAME  DS    CL8
         DC    C' '
WAITFLAG DS    CL9
         DC    C' '
EXCLFLAG DS    CL4
         DC    CL4' '
SYSTEM   DS    CL4
         DC    CL4' '
         DC    CL6' '
MCFLAG   DS    CL1
         DC    CL6' '
         DC    CL2' '
         DC    CL3' '
RESVFLAG DS    CL1
         DC    CL3' '
         DC    CL2' '
         DC    CL2' '
GLOBFLAG DS    CL1
         DC    CL3' '
         DC    CL2' '
         DC    CL2' '
OWNFLAG  DS    CL1
         DC    CL2' '
JOBLINEL EQU   *-JOBLINE
         SPACE
FLAGHD   DS   0CL72                    FLAG HEADER
         DC    CL14'JOBNAME/USERID'
         DC    CL5' '
         DC    CL4'DISP'
         DC    CL2' '
         DC    CL8' SYSTEM '
         DC    CL2' '
         DC    CL13'MUST COMPLETE'
         DC    CL2' '
         DC    CL7'RESERVE'
         DC    CL2' '
         DC    CL6'GLOBAL'
         DC    CL2' '
         DC    CL5'OWNER'
FLAGHDL  EQU   *-FLAGHD
         SPACE
CODE04M  DS   0CL72                    DSNAME NOT FOUND
         DC    CL72'ERROR ENCOUNTERED CODE 04 - RETRY COMMAND'
CODE04ML EQU   *-CODE04M
         SPACE
CODE08M  DS   0CL72                    GQSCAN AREA TOO SMALL
         DC    CL72'ERROR ENCOUNTERED CODE 08 - RETRY COMMAND'
CODE08ML EQU   *-CODE08M
         SPACE
CODE0CM  DS   0CL72                    GQSCAN SCREWED UP
         DC    CL72'ERROR ENCOUNTERED CODE 0C - RETRY COMMAND'
CODE0CML EQU   *-CODE0CM
         SPACE
CODE10M  DS   0CL72                    INVALID SYSNAME SPECIFIED
         DC    CL72'ERROR ENCOUNTERED CODE 10 - RETRY COMMAND'
CODE10ML EQU   *-CODE10M
         SPACE
NOENQ    DS   0CL72
         DC    CL72'NO ENQUEUES EXIST      '
NOENQL   EQU   *-NOENQ
         SPACE
CLEAR    DC    X'C1115D7E1140403C4040001DC813'  CLEAR SCREEN
TITLE    DC    C'   EDWA ENQUEUE CONFLICT DISPLAY     XSID     YY.DDD  X
                HH.MM.SS'
BLANK    DC    CL72' '
         LTORG
         DS   0F
SCANAREA DS    8192X                SCAN INFO AREA
         EJECT
         IEESMCA
         EJECT
         ISGRIB
         END
//L.SYSLMOD  DD   DSN=C4ADMDM.PROD.LKED(TSOENQ),DISP=SHR
//*
