//XODMNLIP JOB MSGCLASS=X,CLASS=B,NOTIFY=XSDMN
//*
//ASMHCL  PROC
//C      EXEC  PGM=IEV90,PARM=OBJECT,REGION=5M
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(10,5)),DSN=&SYSUT1
//SYSPUNCH DD  SYSOUT=B,DCB=(BLKSIZE=800),SPACE=(CYL,(5,5,0))
//SYSPRINT DD SYSOUT=X,DCB=(BLKSIZE=3509),UNIT=(,SEP=(SYSUT1,SYSPUNCH))
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(5,5,0)),            *
//             DCB=(BLKSIZE=400),DSN=&&LOADSET
//L    EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL',REGION=2M,COND=(8,LT,C)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD DD DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(2,1,2)),DSN=&GOSET(GO)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=X,DCB=(RECFM=FB,BLKSIZE=3509)
//  PEND
//*
//  EXEC ASMHCL
//C.SYSIN DD *
         TITLE '   L A S T I P L   '
***********************************************************************
*                                                                     *
*         'LASTIPL' TSO COMMAND                                       *
*                                                                     *
***********************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
*        DATE WRITTEN. JANUARY 18 1982.
*        DATE UPDATED. MARCH 2 1982.
*        ATTRIBUTES. RE-ENTRANT.
*        DESCRIPTION.
*         THIS TSO COMMAND DISPLAYS THE DATE AND TIME OF THE LAST IPL
*         ON THE TERMINAL.
*         DEPENDENCY - WORKS ONLY ON MVS/SE2 OR MVS/SP (IN OTHER WORDS,
*         MVS SYSTEMS IN WHICH SMF IS RECORDED ON VSAM DATA SETS).
         SPACE
LASTIPL  CSECT
         USING *,R10
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'LASTIPL'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(0),AL3(@DATAL)
@PROLOG  STM   14,12,12(R13)
         LR    R10,R15
         LR    R2,R1
         USING CPPL,R2
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         LR    R9,R1
         USING @DATA,R9
         ST    R13,4(,R9)
         ST    R9,8(,R13)
         LR    R13,R9
         SPACE
         MVC   PUTPTPB(12),MODEL
         SPACE
         LA    R15,PUTIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         LA    R1,ECB
         ST    R1,IOPLECB
         SR    R0,R0
         ST    R0,ECB
         DROP  R15                 IOPL
*----------------------------------------------------------------------
*
*        SCAN THE UCBS TO FIND THE IPL VOLUME
*
         MVC   IPLVOL,BLANKS       CLEAR VOLUME NAME
         MVC   IPLUNIT,BLANKS      CLEAR UNIT ADDRESS
         XC    SCNWORK,SCNWORK     CLEAR 100-BYTE IOSVSUCB WORK AREA
         MVI   SCNTYPE,UCB3DACC    SET DEVICE TYPE OF DASD
         XC    SCNADDR,SCNADDR     CLEAR UCB ADDRESS RETURN AREA
         LA    R0,SCNWORK          BUILD IOSVSUCB PARMLIST
         ST    R0,SCNPRM1          ..
         LA    R0,SCNTYPE          ..
         ST    R0,SCNPRM2          ..
         LA    R0,SCNADDR          ..
         ST    R0,SCNPRM3          ..
         OI    SCNPRM3,X'80'       SET END-OF-LIST INDICATOR
*
*CBLOOP  LA    R1,SCNPRML          GET ADDRESS OF IOSVSUCB PARMLIST
*        L     R15,CVTPTR          GET CVT PTR
*        L     R15,CVTUCBSC-CVT(,R15)  GET UCB SCAN SERVICE RTN ADDRESS
*        BALR  R14,R15             GO GET A UCB ADDRESS
*        LTR   R15,R15             DID WE GET A UCB ADDRESS?...
*        BNZ   UCBEND              ...NO, WE'VE SEEN THEM ALL
UCBLOOP  UCBSCAN COPY,WORKAREA=SCNWORK,UCBAREA=UCBAREA,RANGE=ALL,      1
               DYNAMIC=YES,DEVCLASS=DASD,RETCODE=RET,RSNCODE=RSN
         L     R2,RET
         LTR   R2,R2
         BNZ   UCBEND
         LA    R1,UCBAREA          GET THE UCB ADDRESS
         USING UCBCMSEG,R1         MAKE IT ADDRESSABLE
         TM    UCBSTAT,UCBSYSR     IS THIS THE SYSRES?...
         BZ    UCBLOOP             ...NO, KEEP HUNTING
         MVC   IPLVOL,UCBVOLI      COPY THE VOLUME SERIAL NUMBER
         MVC   IPLUNIT,UCBNAME     COPY THE UNIT ADDRESS
         DROP  R1                  DROP UCB ADDRESSABILITY
UCBEND   DS    0H
*----------------------------------------------------------------------
         L     R15,16              CVTPTR
         L     R2,196(,R15)        CVTSMCA
         LA    R3,340(,R2)         SMCAIDTE   IPL DATE
         LA    R4,336(,R2)         SMCAITME   IPL TIME
         MVC   JULDATE,JULPATT
         ED    JULDATE,1(R3)
         BAL   R8,DATECONV
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(MESSLEN-1),MESSAGE
         MVC   MESSAGE(15),=C'LAST IPL WAS ON'
         LA    R15,MESSAGE+16
         MVC   MESSAGE+16(9),PARM3+20  DAY OF WEEK
         LA    R15,MESSAGE+25
BACK1    CLI   0(R15),C' '
         BNE   *+8
         BCT   R15,BACK1
         LA    R15,2(,R15)
         MVC   0(20,R15),PARM3     MONTH DAY YEAR
         LA    R15,20(,R15)
BACK2    CLI   0(R15),C' '
         BNE   *+8
         BCT   R15,BACK2
         LA    R15,2(,R15)
         MVI   0(R15),C'('
         MVC   1(6,R15),JULDATE+1  JULIAN DATE
         MVI   7(R15),C')'
         LA    R15,9(,R15)
         MVC   0(2,R15),=C'AT'
         LA    R15,3(,R15)         POINT TO WHERE HH.MM WILL GO
         L     R1,0(,R4)
         SLR   R0,R0
         D     R0,=F'360000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  0(2,R15),DOUBLE+6(2)
         LR    R1,R0
         SLR   R0,R0
         D     R0,=F'6000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  3(2,R15),DOUBLE+6(2)
         MVI   2(R15),C'.'
         LA    R15,5(,R15)
         SPACE
*----------------------------------------------------------------------
         CLI   IPLVOL,C' '         DID WE FIND THE SYSRES?...
         BE    NOSYSRES            ...NO
*                              1234567890123456789
         MVC   0(20,R15),=CL20' FROM VVVVVV (XXX) '
         MVC   6(6,R15),IPLVOL
         MVC   14(3,R15),IPLUNIT
         LA    R15,20(,R15)        ACCOUNT FOR NEW MSG TEXT
NOSYSRES DS    0H
*----------------------------------------------------------------------
         SPACE
         LA    R1,MESSAGE
         LA    R0,4(,R15)
         SR    R0,R1               COMPUTE LENGTH PLUS 4
         SPACE
         SLL   R0,16
         ST    R0,PUTSEG
         SPACE
         PUTLINE PARM=PUTPTPB,OUTPUT=(PUTSEG,DATA),MF=(E,PUTIOPL)
         SPACE
         SLR   R15,R15
EXIT     LR    R1,R13
         L     R0,@SIZE
         L     R13,4(,R13)
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2
         LM    0,12,20(R13)
         L     R14,12(,R13)
         BR    R14
         SPACE
***********************************************************************
*                                                                     *
*         DATE CONVERSION                                             *
*                                                                     *
***********************************************************************
         SPACE
DATECONV LA    R1,3
         ST    R1,PARM2
         MVC   PARM1(4),0(R3)
         LA    R1,16+128
         ST    R1,PARM4
         LA    R1,PARMPTRS
         LA    R14,PARM1
         ST    R14,0(,R1)
         LA    R14,PARM2
         ST    R14,4(,R1)
         LA    R14,PARM3
         ST    R14,8(,R1)
         LA    R14,PARM4
         ST    R14,12(,R1)
         LA    R14,PARM5
         ST    R14,16(,R1)
         L     R15,=V(JULSUB)
*        BALR  R14,R15
         BR    R8
         SPACE
***********************************************************************
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         SPACE
MODEL    PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
MODELL   EQU   *-MODEL
         SPACE
JULPATT  DC    X'4020204B202020'
         DC    0D'0'
         SPACE
BLANKS   DC    CL16' '
         SPACE
***********************************************************************
*                                                                     *
*        DSECT                                                        *
*                                                                     *
***********************************************************************
         SPACE
@DATA    DSECT
SAVEAREA DS    18F
DOUBLE   DS    D
PUTIOPL  DS    4F
PUTPTPB  DS    3F
ECB      DS    F
PUTSEG   DS    F
MESSAGE  DS    C'LAST IPL WAS ON '
         DS    C'WEDNESDAY '
         DS    C'SEPTEMBER 10 1981 '
         DS    C'(YY.DDD) '
         DS    C'AT '
         DS    C'HH.MM'
MESSLEN  EQU   *-MESSAGE
         DS    0D
PARMPTRS DS    5F
PARM1    DS    F
PARM2    DS    F
PARM4    DS    F
PARM3    DS    CL32
PARM5    DS    32D
JULDATE  DS    CL7
*----------------------------------------------------------------------
IPLVOL   DS    CL6                 VOLSER OF SYSRES
IPLUNIT  DS    CL3                 UNIT ADDRESS OF SYSRES
SCNPRML  DS    0A                  IOSVSUCB PARMLIST
SCNPRM1  DS    A                   PTR TO 100-BYTE WORK AREA
SCNPRM2  DS    A                   PTR TO DEVICE CLASS FOR SEARCH
SCNPRM3  DS    A                   PTR TO UCB ADDRESS RETURN AREA
         DS    0D                  ALIGN ON DOUBLEWORD BOUNDARY
SCNWORK  DC    XL100'00'           IOSVSUCB WORKAREA
UCBAREA  DC    XL50'00'
SCNADDR  DS    A                   RETURN AREA FOR UCB ADDRESS
SCNTYPE  DS    X                   DEVICE CLASS (X'20' FOR DASD)
RET      DC    F'0'
RSN      DC    F'0'
*----------------------------------------------------------------------
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
         IKJCPPL
         SPACE
         IKJIOPL
         SPACE
         PRINT NOGEN
         DSECT
         IEFUCBOB
         SPACE
         CVT   DSECT=YES
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//*.SYSLIB  DD DISP=SHR,DSN=XSDMN.PROD.LKED
//L.SYSLMOD DD DISP=SHR,DSN=TEST.PGM.LOAD
//L.SYSIN DD *
 NAME LASTIPL(R)
