//XSDMNZZZ JOB MSGCLASS=X,CLASS=Q,NOTIFY=XSDMN
//*
//ASMHCL  PROC
//C      EXEC  PGM=IEV90,PARM=OBJECT,REGION=5M
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(10,5)),DSN=&SYSUT1
//SYSPUNCH DD  SYSOUT=X,DCB=(BLKSIZE=800),SPACE=(CYL,(5,5,0))
//SYSPRINT DD SYSOUT=X,DCB=(BLKSIZE=3509),UNIT=(,SEP=(SYSUT1,SYSPUNCH))
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(5,5,0)),            *
//             DCB=(BLKSIZE=400),DSN=&&LOADSET
//L    EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL',REGION=5M,COND=(8,LT,C)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD DD DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(2,1,2)),DSN=&GOSET(GO)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=X,DCB=(RECFM=FB,BLKSIZE=3509)
//  PEND
//*
//   EXEC ASMHCL
//C.SYSIN DD *
         TITLE '   D E L A Y  '
************************************************************
*                                                          *
*        'DELAY' TSO COMMAND CP                            *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY. JOHNS GRAY.
*        INSTALLATION. PRC COMPUTER CENTER INC, MCLEAN VA.
*        ATTRIBUTES. RE-ENTRANT.
*        COMMENTS AND CHANGES BY BILL GODFREY.
         SPACE
DELAY    CSECT
         USING *,R11
         B     @BEGIN-*(R15)
         DC    AL1(11),CL11'DELAY'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    C' SOFTWARE BY PRC COMPUTER CENTER, '
         DC    C'PLANNING RESEARCH CORPORATION, MCLEAN VA'
         DC    C' - BILL GODFREY '
@BEGIN   STM   R14,R12,12(R13)
         LR    R11,R15
         LR    R2,R1
         GETMAIN R,LV=@DATAL
         ST    13,4(,1)
         ST    1,8(,13)
         LR    13,1
         LA    R9,72(,R1)          R9 POINTS TO TIMEXITW
         USING TIMEXITW,R9
         SPACE
         LR    R1,R2
         USING CPPL,R1
         SPACE
         L     R2,CPPLCBUF
         USING CBUF,R2
         LH    R3,CBUFLNG
         LH    R4,CBUFOFF
         LA    R5,4(,4)
         SR    R3,R5
         BP    LL010
LE010    LA    R0,L'MSG0
         LA    R1,MSG0
         SVC   93
         LA    R15,8
         B     LLEXIT
LL010    CH    R3,=H'5'
         BNL   LE010
         LA    R4,CBUFDATA(R4)
         SLR   R5,R5
LP010    LA    R6,12
         LA    R7,TBL+11
LP020    CLC   0(1,R4),0(R7)
         BE    LL020
         BCTR  R7,0
         BCT   R6,LP020
         LA    R0,L'MSG1
         LA    R1,MSG1
         SVC   93
         LA    R15,12
         B     LLEXIT
LL020    CLI   0(R4),C'H'          LAST DIGIT H
         BE    LL031               YES, HUNDREDTHS OF SECONDS
         CLI   0(R4),C' '
         BE    LL030
         MH    R5,=H'10'
         AR    R5,R6
         BCTR  R5,R0
         LA    R4,1(,R4)
         BCT   R3,LP010
LL030    MH    R5,=H'100'
LL031    ST    R5,BINTVL
         MVC   TIMEXITW(TIMEXITL),TIMEXIT
         XC    ECB,ECB
         SPACE
         STIMER REAL,TIMEXITW,BINTVL=BINTVL
         SPACE
         WAIT  1,ECB=ECB,LONG=YES
         SPACE
LLEXIT   LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN R,A=(1),LV=@DATAL
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14
         DROP  R11
         SPACE
TBL      DC    CL11'0123456789 H'
MSG0     DC    C'LENGTH ERROR -'
MSG1     DC    C'NON-NUMERIC INTERVAL -'
         LTORG
         SPACE
         DC    0F'0'
         USING *,R11
TIMEXIT  STM   14,12,12(R13)
         LR    R11,R15
         LR    R9,R15              R9 POINTS TO TIMEXITW
         SPACE
         POST  ECB,0
         SPACE
         LM    14,12,12(R13)
         SLR   R15,R15
         BR    R14
TIMEXITL EQU   *-TIMEXIT
         SPACE
@DATA    DSECT
         DS    18F
TIMEXITW DS    0F,(TIMEXITL)X
ECB      DS    F
BINTVL   DS    F
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
CBUF     DSECT
CBUFLNG  DS    AL2
CBUFOFF  DS    AL2
CBUFDATA DS    0C
         SPACE
         IKJCPPL
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//L.SYSLMOD DD DISP=SHR,DSN=TEST.PGM.LOAD
//L.SYSIN DD *
 NAME DELAY(R)
