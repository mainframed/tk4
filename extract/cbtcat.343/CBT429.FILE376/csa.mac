//ED115DMM JOB MSGCLASS=X,CLASS=Q,NOTIFY=XSDMN
//  EXEC ASMHCL,PARM.L=('AMODE=31','RMODE=24')
//C.SYSLIB DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//SYSIN DD *
CSA      CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,R15
         STM   R14,R12,12(R13)
         LA    R12,SAVE
         ST    R12,8(R13)
         ST    R13,SAVE+4
         LR    R13,R12
         LR    R12,R15
         USING CSA,R12
         DROP  R15
         L     R9,16              CVT
         USING CVT,R9
         L     R5,CVTGDA          GDA
         MVC   OUT(80),SPACES
         LR    R7,R5              GDA
         USING GDA,R7
* CSA
         L     R4,GDACSASZ
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         ST    R4,COMMON
         MVC   OUT1+14(8),EDIT8
         ED    OUT1+14(8),WORK+4
*
         LA    R4,0
         L     R5,GDA_CSA_ALLOC
         M     R4,=F'100'
         LA    R4,0
         D     R4,GDACSASZ
         A     R5,=F'1'
         CVD   R5,WORK
         OI    WORK+7,X'0F'
         UNPK  OUT1+26(2),WORK+6(2)
*ECSA
         L     R4,GDAECSAS
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         MVC   OUT1+54(8),EDIT8
         ED    OUT1+54(8),WORK+4
*
         LA    R4,0
         L     R5,GDA_ECSA_ALLOC
         M     R4,=F'100'
         LA    R4,0
         D     R4,GDAECSAS
         A     R5,=F'1'
         CVD   R5,WORK
         OI    WORK+7,X'0F'
         UNPK  OUT1+66(2),WORK+6(2)
* SQA
         L     R4,GDASQASZ
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         ST    R4,CW
         MVC   OUT2+14(8),EDIT8
         ED    OUT2+14(8),WORK+4
*
         LA    R4,0
         L     R5,GDA_SQA_ALLOC
         M     R4,=F'100'
         LA    R4,0
         D     R4,GDASQASZ
         A     R5,=F'1'
         CVD   R5,WORK
         OI    WORK+7,X'0F'
         UNPK  OUT2+26(2),WORK+6(2)
         L     R4,COMMON
         A     R4,CW
         ST    R4,COMMON
*ESQA
         L     R4,GDAESQAS
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         MVC   OUT2+54(8),EDIT8
         ED    OUT2+54(8),WORK+4
*
         LA    R4,0
         L     R5,GDA_ESQA_ALLOC
         M     R4,=F'100'
         LA    R4,0
         D     R4,GDAESQAS
         A     R5,=F'1'
         CVD   R5,WORK
         OI    WORK+7,X'0F'
         UNPK  OUT2+66(2),WORK+6(2)
*CSA FREE
         L     R4,GDACSARE
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         MVC   OUT4+54(8),EDIT8
         ED    OUT4+54(8),WORK+4
*CSA CVT SQA
         L     R4,GDACSACV
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         MVC   OUT5+54(8),EDIT8
         ED    OUT5+54(8),WORK+4
* V=R
         L     R4,GDAVRSZ
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         MVC   OUTV+14(8),EDIT8
         ED    OUTV+14(8),WORK+4
*
         L     R3,CVTSMEXT
         USING CVTVSTGX,R3
* MLPA
         L     R2,CVTMLPAS
         L     R4,CVTMLPAE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         ST    R4,CW
         CVD   R4,WORK
         MVC   OUT8+14(8),EDIT8
         ED    OUT8+14(8),WORK+4
         L     R4,COMMON
         A     R4,CW
         ST    R4,COMMON
*EMLPA
         L     R2,CVTEMLPS
         L     R4,CVTEMLPE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         CVD   R4,WORK
         MVC   OUT8+54(8),EDIT8
         ED    OUT8+54(8),WORK+4
* FLPA
         L     R2,CVTFLPAS
         L     R4,CVTFLPAE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         ST    R4,CW
         CVD   R4,WORK
         MVC   OUT9+14(8),EDIT8
         ED    OUT9+14(8),WORK+4
         L     R4,COMMON
         A     R4,CW
         ST    R4,COMMON
*EFLPA
         L     R2,CVTEFLPS
         L     R4,CVTEFLPE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         CVD   R4,WORK
         MVC   OUT9+54(8),EDIT8
         ED    OUT9+54(8),WORK+4
* PLPA
         L     R2,CVTPLPAS
         L     R4,CVTPLPAE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         ST    R4,CW
         CVD   R4,WORK
         MVC   OUT10+14(8),EDIT8
         ED    OUT10+14(8),WORK+4
         L     R4,COMMON
         A     R4,CW
         ST    R4,COMMON
*EPLPA
         L     R2,CVTEPLPS
         L     R4,CVTEPLPE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         CVD   R4,WORK
         MVC   OUT10+54(8),EDIT8
         ED    OUT10+54(8),WORK+4
* RW NUC
         L     R2,CVTRWNS
         L     R4,CVTRWNE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         ST    R4,CW
         CVD   R4,WORK
         MVC   OUT11+14(8),EDIT8
         ED    OUT11+14(8),WORK+4
         L     R4,COMMON
         A     R4,CW
         ST    R4,COMMON
*ERW NUC
         L     R2,CVTERWNS
         L     R4,CVTERWNE
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         CVD   R4,WORK
         MVC   OUT11+54(8),EDIT8
         ED    OUT11+54(8),WORK+4
* RO NUC
         L     R2,CVTRONS
*        L     R4,CVTRONE
         L     R4,=X'00FFFFFF'
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         ST    R4,CW
         CVD   R4,WORK
         MVC   OUT12+14(8),EDIT8
         ED    OUT12+14(8),WORK+4
         L     R4,COMMON
         A     R4,CW
         ST    R4,COMMON
*ERO NUC
*        L     R2,CVTRONS
         L     R4,CVTRONE
         L     R2,=X'00FFFFFF'
         SR    R4,R2
         SRL   R4,10              TO GET K
         A     R4,=F'1'
         CVD   R4,WORK
         MVC   OUT12+54(8),EDIT8
         ED    OUT12+54(8),WORK+4
* PVT
         L     R4,GDAPVTSZ
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         MVC   OUTP+14(8),EDIT8
         ED    OUTP+14(8),WORK+4
* EPVT
         L     R4,GDAEPVTS
         SRL   R4,10              TO GET K
         CVD   R4,WORK
         MVC   OUTP+54(8),EDIT8
         ED    OUTP+54(8),WORK+4
*
ENDJC    DS    0H
         L     R4,COMMON
         CVD   R4,WORK
         MVC   OUT14+14(8),EDIT8
         ED    OUT14+14(8),WORK+4
***********************************
         LA    1,LABEL15                  GO AMODE 24
         BSM   0,1
LABEL15  DS    0H
***********************************
         OPEN  (SYSPRINT,OUTPUT)
         PUT   SYSPRINT,OUT1
         PUT   SYSPRINT,OUT2
         PUT   SYSPRINT,OUT3
         PUT   SYSPRINT,OUT4
         PUT   SYSPRINT,OUT5
         PUT   SYSPRINT,OUT7
         PUT   SYSPRINT,OUT8
         PUT   SYSPRINT,OUT9
         PUT   SYSPRINT,OUT10
         PUT   SYSPRINT,OUT11
         PUT   SYSPRINT,OUT12
         PUT   SYSPRINT,OUT13
         PUT   SYSPRINT,OUT14
         PUT   SYSPRINT,OUTPS
         PUT   SYSPRINT,OUTP
         PUT   SYSPRINT,OUTV
         CLOSE (SYSPRINT)
***********************************
         L     1,LABEL13                  GO AMODE 31
         BSM   0,1
LABEL13  DC    A(LABEL14+X'80000000')
LABEL14  DS    0H
***********************************
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
SYSPRINT DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT,LRECL=80,RECFM=FB,  X
               BLKSIZE=9040
SAVE     DC    18F'0'
         LTORG
SPACES   DC    CL80' '
ZEROS    DC    XL80'00'
OUT      DS    CL80
*UT1     DC    CL40'0123456789012345678901234567890123456789'
*        DC    CL40'                                        '
OUT1     DC    CL40'          CSA ???????? K (?? %)         '
         DC    CL40'         ECSA ???????? K (?? %)         '
OUT2     DC    CL40'          SQA ???????? K (?? %)         '
         DC    CL40'         ESQA ???????? K (?? %)         '
OUT3     DC    CL40'                                        '
         DC    CL40'                                        '
OUT4     DC    CL40'                                        '
         DC    CL40' FREE CSA/SQA ???????? K                '
OUT5     DC    CL40'                                        '
         DC    CL40'  CSA CVT SQA ???????? K                '
OUTV     DC    CL40'          V=R ???????? K                '
         DC    CL40'                                        '
OUT7     DC    CL40'                                        '
         DC    CL40'                                        '
OUT8     DC    CL40'  MLPA        ???????? K                '
         DC    CL40' EMLPA        ???????? K                '
OUT9     DC    CL40'  FLPA        ???????? K                '
         DC    CL40' EFLPA        ???????? K                '
OUT10    DC    CL40'  PLPA        ???????? K                '
OUT10    DC    CL40' EPLPA        ???????? K                '
OUT11    DC    CL40'  R/W NUCLEUS ???????? K                '
         DC    CL40' ER/W NUCLEUS ???????? K                '
OUT12    DC    CL40'  R/O NUCLEUS ???????? K                '
         DC    CL40' ER/O NUCLEUS ???????? K                '
OUT13    DC    CL40'                                        '
         DC    CL40'                                        '
OUT14    DC    CL40' COMMON TOTAL ???????? K                '
         DC    CL40'                                        '
OUTPS    DC    CL40'                                        '
         DC    CL40'                                        '
OUTP     DC    CL40'          PVT ???????? K                '
         DC    CL40'         EPVT ???????? K                '
WORK     DS    D
COMMON   DC    F'0'
CW       DC    F'0'
EDIT8    DC    X'4020202020202121'
         CVT DSECT=YES,LIST=NO
         IHAGDA
         END
//L.SYSLMOD DD DSN=TEST.PGM.LOAD(CSA),DISP=SHR
//*
//* EXEC PGM=CSA
//*TEPLIB DD DISP=SHR,DSN=ED115DM.LOAD
//*YSPRINT DD SYSOUT=*
