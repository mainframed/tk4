//C4SYSDMA JOB MSGCLASS=X,CLASS=Q,NOTIFY=XSDMN
//*
//*  DM
//ASMHCL  PROC
//C      EXEC  PGM=IEV90,PARM=OBJECT,REGION=5M
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(10,5)),DSN=&SYSUT1
//SYSPUNCH DD  SYSOUT=B,DCB=(BLKSIZE=800),SPACE=(CYL,(5,5,0))
//SYSPRINT DD SYSOUT=X,DCB=(BLKSIZE=3509),UNIT=(,SEP=(SYSUT1,SYSPUNCH))
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(5,5,0)),            *
//             DCB=(BLKSIZE=400),DSN=&&LOADSET
//L    EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL',REGION=2M,COND=(8,LT,C)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD DD DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(2,1,2)),DSN=&GOSET(GO)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=X,DCB=(RECFM=FB,BLKSIZE=3509)
//  PEND
//*
// EXEC ASMHCL
//*YSLIB DD DISP=SHR,DSN=C4ADMDM.FILE435.PDS
//SYSLIB DD DISP=SHR,DSN=SYS1.MACLIB
//       DD DISP=SHR,DSN=SYS1.MODGEN
//SYSPRINT DD SYSOUT=*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
CATDISP  CSECT
         USING *,R15
         STM   R14,R12,12(R13)
         LA    R12,SAVE
         ST    R12,8(R13)
         ST    R13,SAVE+4
         LR    R13,R12
         LR    R12,R15
         USING CATDISP,R12
         DROP  R15
         LA    R2,16                     -> CVT PTR
         L     R2,0(R2)                  -> CVT ITSELF
         LA    R3,256                    OFFSET TO AMCBS PTR
         L     R2,0(R2,R3)               -> AMCBS
         L     R2,20(R2)                 -> CAXWA CHAIN
         LTR   R2,R2                     CAXWA CHAIN PTR ZEROS
         BZ    NOCATS                    YES, WRITE MSG AND LEAVE
CAXWA    EQU   *
         MVC   CATDSN,52(R2)             MOVE CAT NAME TO MSG
TPUT     TPUT  CATOPEN,59
         L     R2,4(R2)                  -> NEXT CAXWA
         LTR   R2,R2                     IS THERE ONE ?
         BNZ   CAXWA                     YES - CONTINUE
         B     NOMORE
NOCATS   EQU   *
         TPUT  NOCAT,20
NOMORE   EQU   *
ENDJC    DS    0H
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
NOCAT    DC    CL20'NO OPEN CATALOGS'
CATOPEN  DC    C'OPEN CATALOG = '
CATDSN   DC    CL44' '
SAVE     DC    18F'0'
         END
//L.SYSLMOD DD DISP=SHR,DSN=TEST.PGM.LOAD(CATDISP)
