         MACRO - FCB - GENERATE 3202/3211 FORMS CONTROL BLOCK IMAGE SAC
&LABEL   FCB   &P=3211,&J=JES,&LPI=6,&LPP=66,&I=1,&CHAN=           DSAC
.***************************************************************** DSAC
         LCLA  &A(180),&LP,&N,&WK,&CC,&LAST,&K,&K1,&LI,&F1         DSAC
         LCLB  &Q,&C,&PASS,&CCH(12),&P1,&P2,&F3                    DSAC
         LCLC  &CH,&BLANK,&B,&B1,&PH1,&PH2                         DSAC
.*                                                                 DSAC
.***************** VERIFY DEVICE/SYS OPERANDS ******************** DSAC
.*                                                                 DSAC
         AIF   ('&J' NE 'JES' AND '&J' NE 'NJES').MACBAD0          DSAC
         AIF   ('&P' NE '3203' AND '&P' NE '3211').MACBAD1         DSAC
&P1      SETB  ('&P' EQ '3203')                                    DSAC
&P2      SETB  ('&J' EQ 'NJES')                                    DSAC
.*                                                                 DSAC
.*                                    (DEFAULT)                    DSAC
.*             &P1=1 3203            &P1=0 3211                    DSAC
.*             &P2=1 NON-JES IMAGE   &P2=0 JES IMAGE               DSAC
.*                                                                 DSAC
.***************** VERIFY CHANNEL/LINE OPERANDS ****************** DSAC
.*                                                                 DSAC
.APN1    ANOP                                                      DSAC
&LAST    SETA  N'&CHAN             GET NR OF OPERANDS             V03.1
         AIF   (&LAST LT 1).MACBAD ERROR IF LT 1                   DSAC
.*                                                                 DSAC
.********************** VERIFY LINES-PER-INCH OPERAND ************ DSAC
.*                                                                 DSAC
.OP1     AIF   (T'&LPI NE 'N').OP1BAD                             V03.1
         AIF   (&LPI NE 6 AND &LPI NE 8).OP1BAD                   V03.1
&LI      SETA  &LPI                                                DSAC
         AGO   .OP2                                                DSAC
.*                                                                V03.1
.********************* SET LINES PER PAGE ************************ DSAC
.*                                                                 DSAC
.OP2     AIF   (T'&LPP NE 'N').OP2BAD         OP2 MUST BE NUMERIC V03.1
         AIF   (&LPP LT 2 OR &LPP GT 180).OP2BAD                  V03.1
&LP      SETA  &LPP                SET NR OF LINES PER PAGE       V03.1
.*                                                                V03.1
.*********************** VERIFY INDEX FACTOR ********************* DSAC
.*                                                                 DSAC
.OP3    ANOP                                                       DSAC
         AIF   (T'&I NE 'N').MACBAD3           FACTOR NOT NUMERIC  DSAC
         AIF   (&I LT 0 OR &I GT 32).MACBAD                        DSAC
&F1      SETA  &I+128                                              DSAC
&F3      SETB  ('&P' EQ '3211')    INDEX BYTE IF 3211              DSAC
.*                                                                 DSAC
.******************** PROCESS CARRIAGE CHANNELS ****************** DSAC
.*                                                                 DSAC
&N       SETA  0                   SET OPERAND CTR TO 0            DSAC
.OPN     ANOP                      START LOOP                     V03.1
&N       SETA  &N+1                ADD 1 TO OPERAND CTR           V03.1
         AIF   (&N GT &LAST).ENSCAN  B IF NO MORE OPERANDS         DSAC
         AIF   (T'&CHAN(&N) NE 'N').OPH     B IF NOT NUMERIC OPD  V03.1
         AIF   (&N EQ 4).OP3BAD                                    DSAC
         AIF   (NOT &C).OPN        IGNORE OP IF INVALID &CC       V03.1
&WK      SETA  &CHAN(&N)           SET WORK TO OPERAND VALUE      V03.1
.OPN1    AIF   (&WK LT 1 OR &WK GT &LP).OPLBAD  B IF BAD LINE NR  V03.1
         AIF   (&A(&WK) NE 0 AND &A(&WK) NE &CC).DUPBAD  B IF DUP V03.1
&A(&WK)  SETA  &CC                 SET &WK-TH LINE TO &CC         V03.1
         AGO   .OPN                LOOP                           V03.1
.ENSCAN  ANOP                                                      DSAC
         AIF   (&P2).ENDSCAN                                       DSAC
         AIF   (&A(1) GT 1).OP5BAD                                 DSAC
         AIF   (&A(1) EQ 1).ENDSCAN                                DSAC
&A(1)    SETA  1                                                   DSAC
         AGO   .ENDSCAN                                            DSAC
.*                                                                V03.1
.************* STEP 3A - DECODE HYPHENATED OPERAND ************** V03.1
.*                                                                V03.1
.OPH     ANOP                      NON-NUMERIC OPERAND            V03.1
&K       SETA  K'&CHAN(&N)         SET OPERAND CHARACTER COUNT    V03.1
&K1      SETA  0                   SET CHAR CT TO ZERO            V03.1
&WK      SETA  0                   SET WORK TO ZERO               V03.1
&PASS    SETB  0                   SHOW FIRST PASS                V03.1
.**************************************************************** V03.1
.*             1ST PASS - FIND CARRIAGE CHANNEL NUMBER            V03.1
.*             2ND PASS - FIND FIRST LINE NUMBER                  V03.1
.**************************************************************** V03.1
.OPH1    ANOP                      START LOOP                     V03.1
&K1      SETA  &K1+1               ADD 1 TO CHAR CTR              V03.1
         AIF   (&K1 GT &K).OPH3    B IF END OF STRING             V03.1
&CH      SETC  '&CHAN(&N)'(&K1,1)     GET A CHARACTER             V03.1
         AIF   ('&CH' LT '0' OR '&CH' GT '9').OPH2  B IF NON-NUM  V03.1
&WK      SETA  &WK*10+&CH          COMPILE DECIMAL NUMBER         V03.1
         AGO   .OPH1               LOOP                           V03.1
.**************************************************************** V03.1
.*             NON-NUMERIC CHARACTER FOUND.                       V03.1
.*             IF FIRST PASS, EXPECT HYPHEN.                      V03.1
.*             IF SECOND PASS, ERROR.                             V03.1
.**************************************************************** V03.1
.OPH2    AIF   (&PASS OR '&CH' NE '-').HYPBAD  B IF ERROR         V03.1
&CC      SETA  &WK                 SET CARRIAGE CHANNEL           V03.1
&WK      SETA  0                   ZERO WORK                      V03.1
&PASS    SETB  1                   SHOW SECOND PASS               V03.1
         AGO   .OPH1               RETURN TO LOOP                 V03.1
.**************************************************************** V03.1
.*             END OF CHARACTER STRING.                           V03.1
.*             IF FIRST PASS, LOGIC ERROR.                        V03.1
.*             IF SECOND PASS, VALIDATE &CC.                      V03.1
.**************************************************************** V03.1
.OPH3    AIF   (NOT &PASS).LOGBAD  ERROR IF FIRST PASS            V03.1
         AIF   (&CC LT 1 OR &CC GT 12).OPCBAD  B IF CC BAD        V03.1
&CCH(&CC) SETB 1                   SHOW CARRIAGE CHANNEL USED     V03.1
&C       SETB  1                   SHOW VALID &CC                 V03.1
         AGO   .OPN1               GO CHECK VALID LINE NR         V03.1
.*                                                                V03.1
.************* OPERAND SCAN IS ENDED.  TEST ITS SUCCESS ********* V03.1
.*                                                                V03.1
.ENDSCAN ANOP                                                     V03.1
         AIF   (&Q).QUITBAD        EXIT IF QUIT SWITCH            V03.1
         AIF   (K'&LABEL EQ 0).LABBAD  B IF LABEL NOT SPECIFIED   V03.1
         AIF   (K'&LABEL LT 5 OR K'&LABEL GT 8).LABBAD1            DSAC
&CH      SETC  '&LABEL'(K'&LABEL,1)  GET LAST CHARACTER OF LABEL  V03.1
         AIF   ('&LABEL'(1,4) EQ 'FCB2').END0                      DSAC
         AIF   ('&LABEL'(1,4) EQ 'FCB1').END2                      DSAC
         AGO   .LABBAD1                                            DSAC
.END0    ANOP                                                      DSAC
         AIF   (K'&LABEL NE 5).END2                                DSAC
         AIF   ('&CH' EQ 'C').LABBAD4   FCB2C IS NOT VALID LABEL   DSAC
         AIF   ('&CH' EQ 'U').LABBAD4   FCB2U IS NOT VALID LABEL   DSAC
         AIF   ('&CH' EQ '6').LABBAD4   FCB26 IS NOT VALID LABEL   DSAC
         AIF   ('&CH' EQ '8').LABBAD4   FCB28 IS NOT VALID LABEL   DSAC
.END2    ANOP                                                     V03.1
&N       SETA  K'&LABEL-4                                          DSAC
&CH      SETC  '&LABEL'(5,&N)                                      DSAC
.END4    ANOP                                                      DSAC
.*                                                                V03.1
.****** INSURE ALL CHANNELS ARE PUNCHED TO PREVENT RUNAWAY ****** V03.1
.*                                                                V03.1
&PASS    SETB  0                                                   DSAC
&N       SETA  0                   ZERO CHANNEL COUNTER           V03.1
&C       SETB  0                   ZERO FIRST-TIME MSG SWITCH     V03.1
&K       SETA  0                   ZERO TOP-DOWN LINE COUNTER     V03.1
&K1      SETA  &LP+1               SET BOTTOM-UP LINE CTR TO MAX+1V03.1
.END11   ANOP                      START LOOP                     V03.1
&N       SETA  &N+1                ADD ONE TO CHANNEL COUNTER     V03.1
         AIF   (&N GT 12).END19    B IF DONE WITH ALL CHANNELS    V03.1
         AIF   (&CCH(&N)).END11    B IF CHANNEL IS ASSIGNED       V03.1
         AIF   (&C).END12          B IF NOT FIRST TIME            V03.1
         SPACE 1                                                  V03.1
         MNOTE *,'THE FOLLOWING ASSIGNMENTS OF UNSPECIFIED '      V03.1
         MNOTE *,'CARRIAGE CHANNELS PREVENT RUN-AWAY FORMS ---'   V03.1
&C       SETB  1                   SHOW FIRST-TIME MSG WRITTEN    V03.1
.END12   AIF   (&P1 OR &PASS).END13 NO CHAN AT END-OF-SHEET FOR 3203
         AIF   (&N EQ 9 OR &N EQ 12).END14  B IF CH 9 OR 12        DSAC
.END13   ANOP                      UNASSIGNED CHANNEL FOUND       V03.1
&K       SETA  &K+1                ADD ONE TO LINE COUNTER        V03.1
         AIF   (&K GT &LP).END17   B IF ALL LINES HAVE CHANNELS   V03.1
         AIF   (&A(&K) NE 0).END13 B IF THIS LINE HAS A CHANNEL   V03.1
&WK      SETA  &K                  SET LINE NUMBER AND            V03.1
         AGO   .END15              GO ASSIGN CHANNEL TO IT        V03.1
.END14   ANOP                      UNASSIGNED CHANNEL 9 OR 12      DSAC
&PASS    SETB  1                                                   DSAC
&K1      SETA  &K1-1               LOWER LINE COUNTER BY ONE       DSAC
         AIF   (&K1 LT 1).END17    B IF ALL LINES HAVE CHANNELS    DSAC
         AIF   (&A(&K1) NE 0).END14  B IF THIS LINE HAS A CHANNEL  DSAC
&WK      SETA  &K1                 SET LINE NUMBER                 DSAC
.END15   ANOP                      ASSIGN CHANNEL TO LINE         V03.1
&BLANK   SETC  ''                  ASSUME CHANNEL 10 OR GREATER   V03.1
         AIF   (&N GT 9).END16     B IF SO                        V03.1
&BLANK   SETC  ' '                 ADD A BLANK FOR CHAN 9 OR LESS V03.1
.END16   MNOTE *,'   CARRIAGE CHANNEL &BLANK.&N IN LINE &WK'      V03.1
&A(&WK)  SETA  &N                  ASSIGN CHANNEL TO LINE         V03.1
&CCH(&N) SETB  1                   SHOW CHANNEL ASSIGNED          V03.1
         AGO   .END11              B TO START OF LOOP             V03.1
.END17   AIF   (&CCH(1)).END18     GO IF CHANNEL 1 IS ASSIGNED    V03.1
         MNOTE 8,'ERROR - CARRIAGE CHANNEL 1 IS NOT SPECIFIED AND' 03.1
         MNOTE *,'CANNOT BE INSERTED.  CHANNEL 1 PUNCH IS'        V03.1
         MNOTE *,'REQUIRED FOR 3211 CARRIAGE-RESTORE FUNCTION.'   V03.1
         AGO   .QUITBAD                                           V03.1
.END18   MNOTE 0,'WARNING - UNSPECIFIED CARRIAGE CHANNELS'        V03.1
         MNOTE 0,'STARTING WITH CHANNEL &N CAN CAUSE'             V03.1
         MNOTE 0,'RUN-AWAY FORMS.'                                V03.1
.END19   ANOP                                                     V03.1
.*                                                                V03.1
.************* CREATE FORMS CONTROL BUFFER OVERLAY ************** V03.1
.*                                                                V03.1
*******************************************************************DSAC
*                                                                 *DSAC
         MNOTE *,'&P FORMS CONTROL BUFFER LOAD FOR $T PRTN,C=&CH' *DSAC
*                                                                 *DSAC
*******************************************************************DSAC
*                                                                *V03.1
&LP      SETA  &LP+&F3     BUMP LENGTH WITH INDEX BYTE              FRB
&CH      SETC  '&LABEL'(1,4).'&CH'    BUILD CSECT NAME             DSAC
&CH      CSECT                GENERATE FORMS CONTROL BUFFER        DSAC
         AIF   ('&CH'(5,4) EQ 'STD1' OR '&CH'(5,4) EQ 'STD2').AR   DSAC
         DC    X'00'               NOT DEFAULT IMAGE               DSAC
         AGO   .AS                                                 DSAC
.AR      ANOP                                                      DSAC
         DC    X'80'               DEFAULT IMAGE                   DSAC
.AS      ANOP                                                      DSAC
         DC    AL1(&LP)            IMAGE LENGTH                    DSAC
&LP      SETA  &LP-&F3                                             DSAC
         AIF   (&P1).DOX1    BRANCH IF 3203 ELSE PUT OUT           DSAC
         DC    AL1(&F1)            3211 INDEX BYTE                  FRB
.DOX1    ANOP                                                       FRB
&PASS    SETB  (&LI EQ 8)          &PASS=1 FOR 8 LINES/INCH       V03.1
&A(1)    SETA  &A(1)+16*&PASS      SET UP FIRST BYTE              V03.1
         AIF   (NOT &P1).DOX3      NO CHAN AT END-OF-SHEET FOR 3203 SAC
         AIF   (&A(&LP) GT 0).OP4BAD                               DSAC
.DOX3    ANOP                                                      DSAC
&A(&LP)  SETA  &A(&LP)+16          SET UP LAST BYTE               V03.1
&N       SETA  0                   SET CUR BYTE PTR TO ZERO       V03.1
&K       SETA  0                   SET PREV NON-0 BYTE PTR TO 0   V03.1
         AIF   (&P2).DO                                            DSAC
         MNOTE *,'    JES2 REQUIRES CHANNEL 1 IN LINE 1'           DSAC
.DO      ANOP                      BUILD THE FCB                  V03.1
&N       SETA  &N+1                POINT TO THE NEXT BYTE         V03.1
         AIF   (&A(&N) EQ 0).DO    IF IT'S ZERO, LOOP.            V03.1
&K       SETA  &N-&K-1             COMPUTE NR OF ZERO BYTES       V03.1
         AIF   (&K EQ 0).DO1       SKIP DOING THEM IF NONE        V03.1
         DC    &K.AL1(0)                                          V03.1
.DO1     ANOP                                                     V03.1
.*                                                                V03.1
.************* SELECT PROPERLY-COMMENTED DC STATEMENT *********** V03.1
.*                                                                V03.1
&WK      SETA  &A(&N)              SET WORK TO BYTE'S VALUE       V03.1
         AIF   (&WK EQ 16).DO3     B IF ZERO-DIGIT ENTRY          V03.1
&B       SETC  ''                  SET TWO LCLC VARIABLES         V03.1
&B1      SETC  ''                  TO NULL STRINGS                V03.1
         AIF   (&WK GT 9).DO2      B IF ADCON IS TWO DIGITS       V03.1
&B       SETC  ' '                 ELSE ADD BLANK TO COMMENT      V03.1
.DO2     ANOP                                                     V03.1
&WK      SETA  &WK-&WK/16*16       GET CHANNEL NUMBER             V03.1
         AIF   (&WK GT 9).DO2A     B IF CHANNEL NR IS 2 DIGITS    V03.1
&B1      SETC  ' '                 ELSE ADD BLANK TO COMMENT      V03.1
.* ONE- OR TWO-DIGIT ENTRY                                        V03.1
.DO2A    DC    AL1(&A(&N)),0C'      &B.CHAN &B1.&WK IS AT LINE &N' DSAC
         AGO   .DO4                                               V03.1
.* NO-DIGIT ENTRY                                                 V03.1
.DO3     DC    AL1(&A(&N))                                        V03.1
.DO4     AIF   (&N EQ &LP).END     B IF LAST BYTE PROCESSED       V03.1
&K       SETA  &N                  SET K TO LAST NON-ZERO BYTE    V03.1
         AGO   .DO                 GO LOOP                        V03.1
.*                                                                V03.1
.*                                                                V03.1
.**************************************************************** V03.1
.*                                                                V03.1
.*                                                                V03.1
.*             DIAGNOSTIC ERROR MESSAGES                          V03.1
.*                                                                V03.1
.*                                                                V03.1
.**************************************************************** V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OP1BAD  MNOTE 8,'ERROR - INVALID OPERAND - &LPI -'                DSAC
         MNOTE *,'    OPERAND MUST BE EITHER 6 OR 8.'              DSAC
&Q       SETB  1                                                  V03.1
         AGO   .OP2                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OP2BAD  MNOTE 8,'ERROR - INVALID OPERAND - &LPP -'                DSAC
         MNOTE *,'    LPP OPERAND MUST BE GREATER THAN 1'          DSAC
         MNOTE *,'    AND NOT GREATER THAN 180.'                  V03.1
&Q       SETB  1                                                  V03.1
         AGO   .OP3                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OP3BAD  MNOTE 8,'ERROR - INVALID OPERAND - &CHAN(4) -'            DSAC
         MNOTE *,'    CHAN OPERAND MUST HAVE A CARRIAGE CHANNEL'   DSAC
         MNOTE *,'    NUMBER, A HYPHEN, AND A LINE NUMBER.'       V03.1
&Q       SETB  1                                                  V03.1
         AGO   .OPN                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OP4BAD  MNOTE 8,'ERROR - INVALID CHANNEL ASSIGNMENT -'            DSAC
         MNOTE *,'    END-OF-SHEET CANNOT BE COMBINED'             DSAC
         MNOTE *,'    WITH A CARRIAGE CHANNEL FOR A 3203.'         DSAC
         AGO   .QUITBAD                                           V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OP5BAD  MNOTE 8,'ERROR - JES2 REQUIRES CHANNEL 1'               DSAC
         MNOTE *,'    TO BE ASSIGNED TO LINE 1.'                   DSAC
         AGO   .QUITBAD                                            DSAC
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OPLBAD  AIF   (T'&CHAN(&N) NE 'N').OPLBADA                       V03.1
         MNOTE 8,'ERROR - INVALID OPERAND &N - &CHAN(&N) -'        DSAC
         AGO   .OPLBADB                                           V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OPLBADA MNOTE 8,'ERROR - INVALID SUBOPERAND &WK'                  DSAC
         MNOTE *,'    OF OPERAND &N - &CHAN(&N)'                   DSAC
.OPLBADB MNOTE *,'    LINE NUMBER MUST BE GREATER THAN 1 AND '    V03.1
         MNOTE *,'    NOT GREATER THAN &LP..'                     V03.1
&Q       SETB  1                                                  V03.1
         AGO   .OPN                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.DUPBAD  MNOTE 8,'ERROR - DUPLICATE SPECIFICATION - OPERAND'       DSAC
         MNOTE *,'    &N ASSIGNS CARRIAGE CHANNEL &CC TO'          DSAC
         MNOTE *,'    LINE &WK, WHICH WAS PREVIOUSLY'              DSAC
         MNOTE *,'    ASSIGNED CHANNEL &A(&WK)..'                  DSAC
&Q       SETB  1                                                  V03.1
         AGO   .OPN                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.HYPBAD  MNOTE 8,'ERROR - INVALID OPERAND &N - &CHAN(&N) -'        DSAC
         AIF   (&PASS).HYPBADA                                    V03.1
         MNOTE *,'    UNRECOGNIZABLE CHARACTER ''&CH'''           V03.1
&Q       SETB  1                                                  V03.1
         AGO   .OPN                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.HYPBADA MNOTE 8,'ERROR - UNRECOGNIZABLE CHARACTER'                DSAC
         MNOTE *,'    ''&CH'' IN LINE NUMBER.'                     DSAC
.HYPBADB ANOP                                                     V03.1
&Q       SETB  1                                                  V03.1
         AGO   .OPN                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.OPCBAD  MNOTE 8,'ERROR - INVALID OPERAND &N - &CHAN(&N) -'        DSAC
         MNOTE *,'    CARRIAGE CHANNEL MUST BE GREATER THAN 0'    V03.1
         MNOTE *,'    AND NOT GREATER THAN 12.'                   V03.1
&C       SETB  0                   SHOW INVALID &CC               V03.1
&Q       SETB  1                                                  V03.1
         AGO   .OPN                                               V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.LOGBAD  MNOTE 8,'ERROR - INTERNAL LOGIC ERROR AT OPERAND &N'      DSAC
         AGO   .QUITBAD                                           V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.MACBAD  MNOTE 8,'ERROR - AT LEAST ONE CARRIAGE CONTROL PUNCH'     DSAC
         MNOTE *,'    MUST BE SPECIFIIED.'                         DSAC
         AIF   (N'&CHAN EQ 2).OP1                                 V03.1
         AGO   .QUITBAD                                           V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.MACBAD0 MNOTE 8,'ERROR - INCORRECT PARAMETER - '                  DSAC
         MNOTE *,'    MUST BE ''JES'' OR ''NJES''.'                DSAC
         AGO   .QUITBAD                                            DSAC
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.MACBAD1 MNOTE 8,'ERROR - INCORRECT PARAMETER - '                  DSAC
         MNOTE *,'    MUST BE ''3203'' OR ''3211''.'               DSAC
         AGO   .QUITBAD                                           V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.MACBAD3 MNOTE 8,'ERROR - NON-NUMERIC INDEX PARAMETER'             DSAC
         AGO   .QUITBAD                                            DSAC
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.LABBAD  MNOTE 1,'WARNING - LABEL MISSING, DEFAULTS TO ''X''.'    V03.1
&CH      SETC  'X'                                                V03.1
         AGO   .END4                                              V03.1
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.LABBAD1 MNOTE 8,'ERROR - INVALID LABEL SPECIFIED'                 DSAC
&LABEL   CSECT                                                     DSAC
         AGO   .QUITBAD                                            DSAC
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.LABBAD2 MNOTE 8,'ERROR - LABEL CHARACTER IS NOT A LETTER OR A'   V03.1
         MNOTE *,'    NUMBER.  HASP UCS/FCB LOOKUP'                DSAC
         MNOTE *,'    ALGORITHIM REQUIRES THAT BITS 0, 1'          DSAC
         MNOTE *,'    BE ON IN AN FCB LABEL CHARACTER.'            DSAC
         AGO   .QUITBAD                                            DSAC
.*                                                                V03.1
.*                                                                V03.1
.*                                                                V03.1
.LABBAD4 MNOTE 8,'ERROR - LABEL VALUE &CH IS INVALID'              DSAC
         AGO   .QUITBAD                                            DSAC
.*                                                                 DSAC
.*                                                                 DSAC
.*                                                                 DSAC
.QUITBAD MNOTE *,'    FCB IMAGE WILL NOT BE GENERATED.'            DSAC
         MEXIT                                                     DSAC
.*                                                                 DSAC
.*                                                                 DSAC
.*                                                                 DSAC
.END     ANOP                                                     V03.1
         AIF   (NOT &P1).ENDIT                                     DSAC
         MNOTE *,'    3203 CANNOT COMBINE END-OF-SHEET'            DSAC
         MNOTE *,'    WITH A CHANNEL PUNCH.'                       DSAC
.ENDIT   ANOP                                                      DSAC
         END
         MEND                                                     V03.1
