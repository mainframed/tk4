         MACRO
&LABEL   REQUESTR
         GBLA  &FLAG(32),&NRES
         GBLB  &IND1,&IND2
         GBLC  &NAME(32)
         LCLA  &N,&N1,&LRS,&LRS1,&FLG(32),&G,&LRES,&NRES1
         LCLC  &C1,&C2,&C3,&NX(32),&NY(32),&EMP,&EMP1
.********************************************************************
.* REF: LOCL.USERSRC(DLASIP00)                                      *
.*      LOCL.USERSRC(IEFDB401)                                      *
.*                                                                  *
.*                     &FLAG IS THE RESOURCE FLAG                   *
.*                     &NRES IS THE NUMBER OF RESOURCES INCLUDED    *
.*                     &IND1 IS A RESOURCE MACRO HAS STARTED FLAG   *
.*                     &IND2 IS A RESOURCE MACRO HAS COMPLETED FLAG *
.*                     &NAME IS THE NAME OF THE RESOURCE            *
.*                                                                  *
.* CHANGE LOG:                                                      *
.*      3 DEC 85 - FIRST WRITTEN, M C CRUZAN.                       *
.*     11 DEC 85 - CHANGED "ALL" TO "*", M C CRUZAN.                *
.*     18 DEC 85 - ADDED DOCUMENTATION, M C CRUZAN.                 *
.*                                                                  *
.********************************************************************
         AIF   (N'&SYSLIST GT 32).TOMANY
         AIF   (K'&LABEL LT 3 OR K'&LABEL GT 8).LABBAD
         AIF   (&IND1 AND NOT &IND2).NOGOOD
         AIF   (&NRES EQ 0).NORESO
&NRES1   SETA  N'&SYSLIST       NUMBER OF REQUESTERS
&C1      SETC  '&LABEL'(1,3)    REQUESTR NAME FOR ADCON
&C2      SETC  '&LABEL'(1,4)    REQUESTR NAME
&C3      SETC  '&LABEL'(1,3).'END'  TABEL END MARKER
&LABEL   DS    0D      ALLIGN THE TABLE
.*
&N       SETA  0       REQUESTER COUNTER
.*
.*
.LOOP1   ANOP  THIS LOOPS THRU THE LIST OF REQUESTERS
&N       SETA  &N+1    INCREMENT TO NEXT REQUESTER
         AIF   (&N GT N'&SYSLIST).ENDSCAN1 END OF REQUESTER LIST
&N1      SETA  1       START WITH SECOND IN REQUEST LIST
.*                     THE FIRST IS THE REQUESTER
.*
.*
.LOOP2   ANOP  THIS LOOPS THRU THE LIST OF REQUESTS
&N1      SETA  &N1+1   INCREMENT TO NEXT REQUEST
         AIF   ('&SYSLIST(&N,&N1)' EQ '*').MARK  GETEM ALL
         AIF   (&N1 GT N'&SYSLIST(&N)).LOOPX END OF REQUEST LIST
&G       SETA  0       RESOURCE LIST
.*
.*
.LOOP3   ANOP  THIS LOOPS THRU THE LIST OF RESOURCES
&G       SETA  &G+1    INCREMENT TO NEXT RESOURCE
         AIF   (&G GT &NRES).LOOP2 RESOURCE NOT FOUND ON LIST
         AIF   ('&SYSLIST(&N,&N1)' NE '&NAME(&G)').LOOP3
&FLG(&N) SETA  &FLG(&N)+&FLAG(&G) ACCUMULATE TO REQUESTERS FLAG BYTE
         AGO   .LOOP2  GO TO NEXT REQUEST
.*
.MARK    ANOP
&NX(&N)  SETC  '&C1'.'&N'
         DC    A(&NX(&N)),X'FFFFFFFF'
         AGO   .LOOP1  GET NEXT REQUESTR
.*
.LOOPX   ANOP
&NX(&N)  SETC  '&C1'.'&N'
         DC    A(&NX(&N)),A(&FLG(&N))
         AGO   .LOOP1  GET NEXT REQUESTER
.*
.ENDSCAN1 ANOP
&EMP1    SETC  '*-'.'&LABEL'
&C3      EQU   &EMP1
&N       SETA  0       SET COUNTER TO ZERO
.OPN2    ANOP          START LOOP ONE
&N       SETA  &N+1    INCREMENT COUNTER
         AIF   (&N GT &NRES1).NDSCAN2 IF NO MORE OPERANDS
&LRES    SETA  K'&SYSLIST(&N,1)-1
&NX(&N)  DC    AL1(&LRES),C'&SYSLIST(&N,1)'
         AGO   .OPN2
.NDSCAN2 ANOP
&IND1    SETB  0   RESET FLAGS TO SHOW
&IND2    SETB  0   A GOOD COMPLETION
         AGO   .ENDIT
.*
.TOMANY  MNOTE 8,'RESOURCE LIST TOO LONG - CANNOT EXCEED 32'
         AGO   .ENDIT
.LABBAD  MNOTE 8,'ERROR IN &LABEL'
         MNOTE 0,'LABEL MUST BE AT LEAST 3 CHARACTERS LONG'
         AGO   .ENDIT
.NOGOOD  MNOTE 8,'ERROR IN PREVIOUS MACRO - SET NOGO'
         MNOTE 0,'CHECK PRIOR MACRO EXPANSION'
         AGO   .ENDIT
.NORESO  MNOTE 8,'NO RESOURCE LIST FOUND - A VALID RESOURCE'
         MNOTE 0,'LIST MUST IMMEDIATELY PRECEED REQUESTR LIST'
         AGO   .ENDIT
.ENDIT   ANOP
         MEND
