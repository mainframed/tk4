         MACRO
         NIPDPMLB
.*/********************************************************************
.*/*                                                                  *
.*/* THIS IS TO BE USED FOR READING SYS1.PARMLIB DURING IPL.          *
.*/*                                                                  *
.*/* BLDL IS DONE ONLY THE FIRST TIME THROUGH.                        *
.*/*                                                                  *
.*/* THIS CODE REQUIRES THE IEAPPNIP MAPPING MACRO.                   *
.*/*                                                                  *
.*/* INPUT:                                                           *
.*/*    ENTRY BY BALR 14,15 TO NIPDPMLB,                              *
.*/*    BASED ADDRESSING TO NVT AND CVT,                              *
.*/*    REG14 IS RETURN REGISTER.                                     *
.*/*                                                                  *
.*/* OUTPUT:                                                          *
.*/*    REG15 HAS RETURN CODE; 0 IS GOOD BLDL OR READ AFTER GOOD BLDL *
.*/*                           4 IS NO BLDL,                          *
.*/*                           8 IS BAD READ AFTER GOOD BLDL,         *
.*/*    REG1 POINTER TO CARD IMAGE IF REG15 IS ZERO,                  *
.*/*    REG1 IS ZEROED IF REG15 IS NON-ZERO (BAD READ),               *
.*/*    REG11 IS DESTROYED.                                           *
.*/*                                                                  *
.*/* CHANGE LOG:                                                      *
.*/*    09 JAN 86 - FIRST WRITTEN - M CRUZAN.                         *
.*/*                                                                  *
.*/********************************************************************
NIPDPMLB DS    0H                                               DLA-MCC
         L     R11,NVTPARMA     GET PARMLIB PARAMETER LIST      DLA-MCC
         USING NIPPAREA,R11         AND ADDRESS IT              DLA-MCC
         STM   R0,R15,SAVEREGS  SAVE OTHER REGS                 DLA-MCC
*                                                               DLA-MCC
         NOP   NIPTXTRD      **** ONE TIME NO-OP INSTRUCTION ***DLA-MCC
         OI    *-3,X'F0'        THRU HERE ONLY THE FIRST TIME   DLA-MCC
*                                                               DLA-MCC
         LA    R1,NIPPADCB      GET PARMLIB DCB                 DLA-MCC
         SH    R1,=H'44'            AND MAKE LEGITIMATE         DLA-MCC
         LA    R0,NIPPABDL      GET BLDL WORK AREA              DLA-MCC
         BLDL  (1),(0)              AND ISSUE                   DLA-MCC
         LTR   R15,R15                                          DLA-MCC
         BZ    NIPDGBDL         GO IF MEMBER WAS FOUND          DLA-MCC
         LM    R0,R15,SAVEREGS  RESTORE CALLERS REGS            DLA-MCC
         LA    R15,4            SHOW BAD RETURN CODE            DLA-MCC
         BR    R14              AND RETURN TO CALLER            DLA-MCC
NIPDGBDL DS    0H                                               DLA-MCC
         ST    R15,NIPPARC      SAVE RETURN CODE                DLA-MCC
         SLR   R0,R0            ZERO REG0                       DLA-MCC
         ICM   R0,M7,NIPPATTR   MOVE TTR TO REG0                DLA-MCC
         SLL   R0,8                 AND SHIFT LEFT ONE BYTE     DLA-MCC
         LA    R1,NIPPADEB      GET PARMLIB DEB                 DLA-MCC
         L     R15,CVTPCNVT     GET TTR CONVERT ROUTINE         DLA-MCC
         STM   R2,R13,SAVEAREA  SAVE REGS                       DLA-MCC
         LA    R2,NIPDMMBC      GET MY WORK AREA                DLA-MCC
         LR    R6,R12           SAVE BASE AROUND CALL           DLA-MCC
         BALR  R14,R15              AND GO CONVERT              DLA-MCC
         LR    R12,R6           RESTORE BASE AFTER CALL         DLA-MCC
         LM    R2,R13,SAVEAREA      AND RESTORE REGS            DLA-MCC
         MVC   NIOBSEEK,NIPDMMBC MBBCCHHR TO PARMLIB IOB        DLA-MCC
         MVC   NIPPASID,NIPDCCHH CCHHR TO SEARCH-ID             DLA-MCC
NIPTXTRD DS    0H                                               DLA-MCC
         XC    NIOBFLG1(4),NIOBFLG1 PREPARE                     DLA-MCC
         XC    NIOBFLG3(8),NIOBFLG3         IOB FOR             DLA-MCC
         XC    NIOBERCT,NIOBERCT                   NEW          DLA-MCC
         MVC   NIOBSEEK+3(L'NIPPASID),NIPPASID        READ      DLA-MCC
         LA    R1,NIPPAIOB                                      DLA-MCC
         EXCP  (1)                                              DLA-MCC
         LA    R1,NIPPAECB      WAIT FOR THE POSTMAN            DLA-MCC
         WAIT  ECB=(1)                                          DLA-MCC
         CLI   NIPPAECC,X'7F'   WAS IT A GOOD TRIP?             DLA-MCC
         BE    NIPDGDRD         YES                             DLA-MCC
         LM    R0,R15,SAVEREGS  RESTORE CALLERS REGS            DLA-MCC
         SLR   R1,R1            ZERO REG1 TO SHOW BAD READ      DLA-MCC
         LA    R15,8            SHOW BAD RETURN CODE            DLA-MCC
         BR    R14              AND RETURN TO CALLER            DLA-MCC
NIPDGDRD DS    0H                                               DLA-MCC
         LM    R0,R15,SAVEREGS  RESTORE CALLERS REGS            DLA-MCC
         LA    R1,NIPPARCD      POINT TO TEXT RECORD            DLA-MCC
         LA    R15,0            SHOW GOOD RETURN                DLA-MCC
         BR    R14              AND RETURN TO CALLER            DLA-MCC
         SPACE 3                                                DLA-MCC
SAVEREGS DS    16F    REG SAVE AREA AROUND PARMLIB READS        DLA-MCC
SAVEAREA DS    12F    REG SAVE AREA AROUND TTR CONVERTION       DLA-MCC
NIPDMMBC DS    0XL8   WORKAREA FOR CVTPCNVT ROUTINE             DLA-MCC
         DS    XL3     : MBB                                    DLA-MCC
NIPDCCHH DS    XL5     :    CCHHR                               DLA-MCC
         MEND
