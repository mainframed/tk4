//NTA0991A JOB (0991,TAA),'* PLIBSMF0 *',MSGCLASS=X,CLASS=6,
//  NOTIFY=NTA0991,MSGLEVEL=(1,1)
//IEFUTL1 EXEC PGM=IEV90,
// PARM='XREF,LIST,LINECOUNT(55)),DECK'
//SYSPRINT DD SYSOUT=*
//SYSPUNCH DD DISP=SHR,DSN=LOCL.USEROBJ(PLIBSMF0)
//SYSLIB   DD DISP=SHR,DSN=SYS1.SMPMTS
//         DD DISP=SHR,DSN=LOCL.USERSRC
//         DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.HASPSRC,UNIT=3350,VOL=SER=DLB85A
//         DD DISP=SHR,DSN=SYS1.AMODGEN,UNIT=3350,VOL=SER=DLB85A
//         DD DISP=SHR,DSN=SYS1.AGENLIB,UNIT=3350,VOL=SER=DLB85A
//         DD DISP=SHR,DSN=SYS1.ATSOMAC,UNIT=3350,VOL=SER=DLB85A
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=1
//SYSUT2   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=1
//SYSUT3   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=1
//SYSIN    DD *
         TITLE 'PLIBSMF0 - LINKAGE AND HOUSEKEEPING'
PLIBSMF0 CSECT
         SPACE 3
         EJECT
R0       EQU   0               LINKAGE REGISTER
R1       EQU   1               LINKAGE REGISTER
R2       EQU   2               WORK
R3       EQU   3               POINTER TO ASCB
R4       EQU   4               POINTER TO SMCA
R5       EQU   5               POINTER TO TIOT
R6       EQU   6               POINTER TO TSB
R7       EQU   7               RETURN CODE SETUP REGISTER
R8       EQU   8               WORK
R9       EQU   9               WORK - ADDRESS CVT, SMCA
R10      EQU   10              SMF RECORD GETMAINED WORK AREA
R11      EQU   11              SAVE REG1 AROUND CALLS
R12      EQU   12              STANDARD BASE REGISTER
R13      EQU   13              SAVE AREA POINTER
R14      EQU   14              RETURN REGISTER
R15      EQU   15              LINKAGE REGISTER/RETURN CODE
         EJECT
         USING PLIBSMF0,R15    ENTRY ADDRESSABILITY
         B     DAIR00          BRANCH AROUND ID
         DC    C'PLIBSMF0.&SYSDATE..&SYSTIME' IDENTIFIER
DAIR00   DS    0H
         STM   R0,R14,SAVEAREA
         DROP  R15             DROP ENTRY BASE REGISTER
         LR    R12,R15         LOAD BASE REGISTER
         USING PLIBSMF0,R12    ESTABLISH ADDRESSABILITY
         SPACE 3
         L     R4,16
       USING CVT,R4
         L     R4,CVTSMCA
       DROP  R4
       USING SMCABASE,R4             R4 = SMCA
         L     R3,548
       USING ASCB,R3                 R3 = ASCB
         L     R5,ASCBTNEW
       USING TCB,R5
         L     R5,TCBTIO
       DROP  R5
       USING TIOT1,R5                R5 = TIOT
         ICM   R6,R15,ASCBTSB
       USING TSB,R6                  R6 = TSB
         BNZ ALLOC1
         EX    0,*      FAIL BATCH CALLERS WITH ABENDS0C3
         TITLE 'PLIBSMF0 - CHECK USERS REQUEST'
ALLOC1   DS    0H
         LA    R0,WTOLEN       GET WTO LENGTH
         BAL   R1,*+4          INDICATE GETMAIN
         SVC   10              GETMAIN FOR WTO
         LR    R9,R1           SHIFT TO REG9
         LA    R7,WTOLEN
         BCTR  R7,0
         EX    7,MVCWTO        MOVE WTO LIST PARAMETERS
         TIME  DEC
         SRL   R0,4            SHIFT TO 0HHMMSST
         O     R0,=F'15'       OR TO 0HHMMSSF
         ST    R0,60(R9)       STORE TIMESTAMP
         UNPK  68(7,R9),60(4,R9) UNPACK HHMMSS
         MVC   35(2,R9),69(R9)     MOVE IN HOURS
         MVC   38(2,R9),71(R9)       MOVE IN MINUTES
         MVC   41(2,R9),73(R9)         MOVE IN SECONDS
         ST    R1,60(R9)       STORE DATESTAMP
         UNPK  44(5,R9),61(3,R9) AND UNPACK YYDDD
         MVC   59(16,R9),=C'ACCESSING FTTSO '
       MODESET KEY=ZERO,MODE=SUP
         MVC   21(8,R9),TSBTRMID MOVE IN LOGICAL TERMINAL NAME
       MODESET KEY=NZERO,MODE=PROB
         MVC   30(4,R9),SMCASID MOVE SYSID TO MSG
         MVC   13(7,R9),TIOCNJOB MOVE IN JOB NAME
         WTO   MF=(E,(9))
         LA    R0,WTOLEN       GET WTO WORKAREA LENGTH
         LR    R1,R9           AND POINT TO IT
         SVC   10              AND FREE IT
         EJECT
         USING SMFFDREC,R10    ADDRESS GETMAINED AREA
         LA    R0,SMFLENTH     LENGTH FOR SMF RECORD WORK AREA
         BAL   R1,*+4          INDICATE GETMAIN
         SVC   10              ISSUE GETMAIN FOR SMF RECORD WORK AREA
         LR    R10,R1          SHIFT WORK AREA TO REG10
         LR    R1,R11          RESTORE REG1
         XC    SMFFDREC,SMFFDREC AND CLEAR IT
         LA    R2,SMFLENTH     GET SMF REC LENGTH
         STH   R2,SMFFDLEN     MOVE IN LENGTH
         MVI   SMFFDFLG,B'00000010' SHOW VS2 SYSTEM
         MVI   SMFFDRTY,SMFFDTYP INSTALL USER SMF RECORD TYPE
         MVC   SMFFDSID,SMCASID MOVE IN THE SYSTEM ID
         MVC   SMFFDUID,TIOCNJOB MOVE IN USERID
         MVI   SMFFDUID+7,BLANK EIGTH CHARACTER IS ALWAYS BLANK
       MODESET KEY=ZERO,MODE=SUP
         MVC   SMFFDTID,TSBTRMID INSTALL LOGICAL TERMINAL NAME
       MODESET KEY=NZERO,MODE=PROB
         MVC   SMFFDALC,X'0000'
         MVC   SMFFDDSN,=CL44' '   NO DATASET NAME
         MVC   SMFFDMNM,=CL8'FTTSO' USE CALLER FOR MEMBER NAME
         TITLE 'PLIBSMF0 - PASS LINK THROUGH'
PASS1    DS    0H              FORCE CERTAIN ALLOCATION PARAMETERS
       MODESET KEY=ZERO        GET INTO KEY ZERO
         TIME  BIN,ZONE=LT     GET DATE/TIMESTAMP FOR SMF RECORD
         STM   R0,R1,SMFFDTME  AND INSTALL IN SMF RECORD
        SMFWTM (R10)           WRITE AUDIT TRAIL
         LR    R7,R15          SAVE RETURN CODE
       MODESET KEY=NZERO       RESTORE CALLER KEY
         LA    R0,SMFLENTH     GET SMF WORK AREA LENGTH
         LR    R1,R10          LOAD RECORD ADDRESS FOR FREEMAIN
         DROP  R10
         SVC   10              AND FREE IT
         LTR   R7,R7           VERIFY THAT AUDIT RECORD IS WRITTEN
         BZ    EXIT            IF SO, PROCEED
         EX    0,*             NO WRITEE, NO RINGEE
         TITLE 'PLIBSMF0 - EXIT CODE'
EXIT     DS    0H
         L     R15,PLISTART
         LM    R0,R14,SAVEAREA
         BR    R15
         TITLE 'PLIBSMF0 - CONSTANTS/EQUATES'
         LTORG
WTOLIST  WTO   'IND0001I UUUUUUU,TTTTTTTT,SSSS,HH:MM:SS,YYDDD,        ,C
               ACCESSING FTTSO UPLOAD/DOWNLOAD FACILITY',              C
               ROUTCDE=(9),DESC=(4),MF=L
WTOLEN   EQU   *-WTOLIST
         SPACE 3
MVCWTO   MVC   0(0,R9),WTOLIST
         SPACE 3
         DS    0F
         DC    X'FFFFFFFF'
PLISTART DC    V(PLISTART)    TOP OF PLI CODE
SAVEAREA DS    15F
         EJECT
SMFFDTYP EQU   253               SMF USER RECORD TYPE
BLANK    EQU   X'40'
SMFLENTH EQU   SMFFDEND-SMFFDLEN EQU TO REC LENGTH
         TITLE 'PLIBSMF0 - DSECTS AND DOCUMENTATION'
SMFFDREC DSECT UNUSED PARTS FOR OFFSET COMPATIBILITY WITH TYPE 14/15
SMFFDLEN DS    XL2             RECORD LENGTH (RDW)
SMFFDSEG DS    XL2             SEGMENT DESCRIPTOR FOR VBS
SMFFDFLG DS    XL1             FLAG FOR VS2=02
SMFFDRTY DS    XL1             RECORD TYPE IS 253
SMFFDTME DS    XL4             FROM TIME MACRO
SMFFDDTE DS    XL4             FROM TIME MACRO
SMFFDSID DS    XL4             SYSTEM ID FROM SMCA
         DS    XL8             NOT USED
SMFFDTID DS    XL8             USER LOGICAL TERMINAL ID FTOM TSB
SMFFDUID DS    XL8             USER ID FROM PSCB
SMFFDALC DS    XL2             ALLOCATION TYPE FROM DAPB
         DS    XL24            NOT USED
SMFFDDSN DS    XL44            DSNAME FROM DAPB
SMFFDMNM DS    XL8             MEMBER NAME FROM DAPB
SMFFDEND EQU   *
         PRINT NOGEN
         EJECT
         IEFTIOT1
         EJECT
         IKJTCB
         EJECT
         IEESMCA
         EJECT
         IHAASCB
         EJECT
         IKJTSB
         EJECT
         CVT   DSECT=YES
         END
/*
//IEWL EXEC PGM=IEWL,
// PARM='LIST,XREF,LET'
//SYSPRINT DD SYSOUT=*
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,1),DCB=BUFNO=1
//USEROBJ  DD DISP=SHR,DSN=LOCL.USEROBJ
//SYSLMOD  DD DISP=SHR,DSN=LOCL.LINKLIB
//SYSLIN   DD *
   INSERT  PLIBSMF0
     ORDER PLIBSMF0
     ORDER PLISTART
     ORDER PLIMAIN
     ORDER FIL
     ORDER SYSIN
     ORDER SYSPINT
     ORDER ***MAIN2
     ORDER IELCGOC
     ORDER IELCGMV
     ORDER IELCGCL
     ORDER ***MAIN1
     ORDER NORSUB
     ORDER NORALC
     ORDER IBMBCAC1
     ORDER IBMBCCC1
     ORDER IBMBCH01
     ORDER IBMBCW01
     ORDER IBMBEOC1
     ORDER IBMBMXS1
     ORDER IBMBPIR1
     ORDER IBMBSED1
     ORDER IBMBCGT1
     ORDER IBMBERR1
     ORDER IBMBOCL1
     ORDER IBMBRIO1
     ORDER IBMBSAO1
     ORDER IBMBSIO1
     ORDER IBMBSLO1
     ORDER IBMBSPL1
     ORDER IBMBSXC1
     ORDER IBMBEEF1
     ORDER IBMBEER1
   INCLUDE USEROBJ(PLIBSMF0)
   INCLUDE SYSLMOD(FTTSO1)
   SETCODE AC(1)
   ALIAS   XMIT
   NAME    FTTSO(R)
/*
//
