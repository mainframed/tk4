         MACRO
&LABEL   RESOURCE
         GBLA  &FLAG(32),&NRES
         GBLB  &IND1,&IND2
         GBLC  &NAME(32)
         LCLA  &ACON(32),&N,&CON,&LRES
         LCLC  &C1,&C2,&C3,&NX(32),&NY(32),&EMP,&EMP1
.********************************************************************
.*  REF: LOCL.USERSRC(DLASIP00)                                     *
.*       LOCL.USERSRC(IEFDB401)                                     *
.*                                                                  *
.*                     &FLAG IS THE RESOURCE FLAG                   *
.*                     &NRES IS THE NUMBER OF RESOURCES INCLUDED    *
.*                     &IND1 IS A RESOURCE MACRO HAS STARTED FLAG   *
.*                     &IND2 IS A RESOURCE MACRO HAS COMPLETED FLAG *
.*                     &NAME IS THE NAME OF THE RESOURCE            *
.*                                                                  *
.*   CHANGE LOG:                                                    *
.*       2 DEC 85 - FIRST WRITTEN, M C CRUZAN.                      *
.*      18 DEC 85 - ADDED DOCUMENTATION, M C CRUZAN.                *
.*                                                                  *
.********************************************************************
         AIF   (&IND1 AND NOT &IND2).NOGOOD
&IND1    SETB  1       SHOW RESOURCE MACRO HAS BEEN ENTERED
&IND2    SETB  1       SHOW RESOURCE MACRO HAS BEEN ENTERED
&NRES    SETA  N'&SYSLIST  NUMBER OF RESOURCES
&N       SETA  0       SET COUNTER TO ZERO
.CLR     ANOP
&N       SETA  &N+1    INCREMENT COUNTER FOR CLEARING LOOP
&FLAG(&N) SETA 0       NULL THE GLOBAL
&NAME(&N) SETC ''      NULL THE GLOBAL
         AIF   (&N EQ &NRES).CLRD
         AGO   .CLR
.CLRD    ANOP
         AIF   (N'&SYSLIST GT 32).TOMANY TOO MANY RESOURCES
         AIF   (K'&LABEL LT 3 OR K'&LABEL GT 8).LABBAD      EXIT
&C1      SETC  '&LABEL'(1,3)    ADCON LABEL
&C2      SETC  '&LABEL'(1,4)    RESOURCE NAME LABEL
&C3      SETC  '&LABEL'(1,3).'END'
&LABEL   DS    0D      ALLIGN THE TABLE
&N       SETA  0       SET COUNTER TO ZERO
&CON     SETA  1
.OPN1    ANOP          START LOOP ONE
&N       SETA  &N+1    INCREMENT COUNTER
         AIF   (&N GT &NRES).NDSCAN1 IF NO MORE OPERANDS
         AIF   (K'&SYSLIST(&N) GT 44).BADOP RESOURCE NAME TOO LONG
&NAME(&N) SETC '&SYSLIST(&N)'
&FLAG(&N) SETA &CON
&NX(&N)  SETC  '&C1'.'&N'
         DC    A(&NX(&N)),A(&FLAG(&N))
&CON     SETA  &CON*2  INCREMENT FLAG
         AGO   .OPN1   LOOP BACK
.NDSCAN1 ANOP
&EMP     SETC  '*-'.'&LABEL'
&C3      EQU   &EMP
&N       SETA  0       SET COUNTER TO ZERO
&CON     SETA  1
.OPN2    ANOP          START LOOP ONE
&N       SETA  &N+1    INCREMENT COUNTER
         AIF   (&N GT &NRES).NDSCAN2 IF NO MORE OPERANDS
&LRES    SETA  K'&SYSLIST(&N)
&LRES    SETA  &LRES-1
&NX(&N)  DC    AL1(&LRES),C'&SYSLIST(&N)'
         AGO   .OPN2
.NDSCAN2 ANOP
         AGO   .ENDIT
.TOMANY  MNOTE 8,'RESOURCE LIST TOO LONG - CANNOT EXCEED 32'
&IND2    SETB  0
         AGO   .ENDIT
.LABBAD  MNOTE 8,'ERROR IN &LABEL'
         MNOTE 0,'LABEL MUST BE AT LEAST 3 CHARACTERS LONG'
&IND2    SETB  0
         AGO   .ENDIT
.NOGOOD  MNOTE 8,'ERROR IN PREVIOUS MACRO - SET NOGO'
         MNOTE 0,'CHECK PRIOR MACRO EXPANSION'
&IND2    SETB  0
         AGO   .ENDIT
.BADOP   MNOTE 8,'ERROR IN &SYSLIST(&N)'
         MNOTE 0,'OPERAND TOO LONG - CANNOT EXCEED 44'
&IND2    SETB  0
         AGO   .ENDIT
.ENDIT   ANOP
         MEND
