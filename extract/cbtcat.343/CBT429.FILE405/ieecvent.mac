         MACRO
      IEECVENT &MSG=,&ACT=NULL,&DO=
.*********************************************************************
.*    THIS MACRO BUILDS THE IEECVXIT DRIVER TABLE.                   *
.*    THE ENTRIES ARE MAPPED BY IEECVUSR.                            *
.*                                                                   *
.*        MSG=  REQUIRED, GENERIC TEXT OF MSG REQUIRING AUTOREPLY.   *
.*        ACT=  OPTIONAL, NULL    : DUMMY ENTRY.                     *
.*                      , SUPPRESS: SUPPRESS THIS MESSAGE ENTRY      *
.*                      , ROLL    : SWITCH TO ROLL                   *
.*                      , NOROLL  : SWITCH TO NOROLL                 *
.*                      , NOROLLD : SWITCH TO NOROLL WITH DELETE     *
.*                      , REPLY   : REPLY TO THIS MESSAGE            *
.*                      , ACTION  : TAKE SOME ACTION                 *
.*                      , END     : TERMINAL ENTRY                   *
.*        DO=   OPTIONAL, FULL TEXT OF MESSAGE TO ISSUE              *
.* DO= REQUIRED IF ACT=REPLY OR ACTION.                              *
.* DO= MUST BE NULL IF ACT=NULL, SUPPRESS ROLL, NOROLL OR NOROLLD.   *
.*                                                                   *
.* CHANGE LOG:                                                       *
.*        13 JAN 86 - FIRST WRITTEN - M CRUZAN                       *
.*                                                                   *
.*********************************************************************
         GBLB  &IND
         LCLA  &L1,&L2,&FLAGS,&L3
         LCLB  &DO1
         LCLC  &C1,&C2,&C3,&C4,&C5
.*
         AIF   (&IND).ERROR4   PREMATURE END DETECTED
.*
         AIF   ('&ACT' EQ 'END').AS1    NOT REQUIRED IF LAST ONE
.*
         AIF   ('&MSG' EQ '').ERROR1       /* REQUIRED PARAMETER
&C1      SETC  '&MSG'             SET SUBJECT MESSAGE
&L3      SETA  (K'&C1-2)             AND LENGTH LESS QUOTES
&C3      SETC  ' *** AUTOREPLY ***'''
&C4      SETC  ''''.'R XX,'
         AIF   ('&ACT' EQ 'NULL').NUL      /* A DUMMY ENTRY
         AIF   ('&ACT' EQ 'SUPPRESS').SUP  /* SUPPRESS THIS MESSAGE
         AIF   ('&ACT' EQ 'REPLY').REP     /* REPLY TO THIS MESSAGE
         AIF   ('&ACT' EQ 'ROLL').ROL      /* SWITCH ROLL/NO-ROLL
         AIF   ('&ACT' EQ 'NOROLLD').NROLD /* SWITCH ROLL/NO-ROLL
         AIF   ('&ACT' EQ 'NOROLL').NROL   /* SWITCH ROLL/NO-ROLL
         AIF   ('&ACT' EQ 'ACTION').ACT    /* START SOMETHING ELSE
.*
.NUL     ANOP
&FLAGS   SETA  0               FLAG FOR NULL
         AIF   ('&DO' NE '').ERROR2  NO TEXT THIS FUNCTION
         AGO   .PUTIT
.*
.SUP     ANOP
&FLAGS   SETA  64              FLAG FOR SUPPRESS
         AIF   ('&DO' NE '').ERROR2  NO TEXT THIS FUNCTION
         AGO   .PUTIT
.*
.REP     ANOP
&FLAGS   SETA  16              FLAG FOR REPLY
         AIF   ('&DO' EQ '').ERROR3  TEXT TO REPLY WITH IS REQUIRED
&DO1     SETB  1               SHOW ADDITIONAL TEXT
         AGO   .PUTIT
.*
.ROL     ANOP
&FLAGS   SETA  32              FLAG FOR ROLL SWITCH
         AIF   ('&DO' NE '').ERROR2  NO TEXT THIS FUNCTION
         AGO   .PUTIT
.*
.NROL    ANOP
&FLAGS   SETA  8               FLAG FOR NON-ROLL SWITCH
         AIF   ('&DO' NE '').ERROR2  NO TEXT THIS FUNCTION
         AGO   .PUTIT
.*
.NROLD   ANOP
&FLAGS   SETA  4               FLAG FOR NON-ROLL DELETE SWITCH
         AIF   ('&DO' NE '').ERROR2  NO TEXT THIS FUNCTION
         AGO   .PUTIT
.*
.ACT     ANOP
&FLAGS   SETA  128             FLAG FOR ACTION INITIATE
         AIF   ('&DO' EQ '').ERROR3  TEXT IS REQUIRED
&DO1     SETB  1
         AGO   .PUTIT
.*
.PUTIT   ANOP
         AIF   (NOT &DO1).NOD   WHEN TEXT IS NOT REQUIRED
&C2      SETC  '&DO'(1,K'&DO-1) ELSE CALCULATE
&L2      SETA  (K'&C2+K'&C3-2)       THE TEXT LENGTH
&C2      SETC  '&C2.&C3'                 AND CONCAT AUTOREPLY MSG
         AIF   ('&ACT' NE 'REPLY').NOD
&C2      SETC  '&C2'(2,K'&C2-1)
&C2      SETC  '&C4.&C2'
&L2      SETA  &L2+5           INCREMENT FOR MSG NUMBER
.NOD     ANOP
&L1      SETA  (4+&L2+&L3)          CALCULATE ENTRY LENGTH
         DC    AL1(&L1)             ENTRY LENGTH
         DC    AL1(&FLAGS)          ENTRY FLAGS
         DC    AL1(&L3)             SUBJECT MSG LENGTH
         DC    AL1(&L2)             TEXT MSG LENGTH
         DC    CL&L3&MSG
         AIF   (NOT &DO1).NODO   WHEN TEXT IS NOT REQUIRED
         DC    CL&L2&C2
         AIF   ('&ACT' NE 'REPLY').NODO
.NODO    ANOP
         AGO   .END1       EXIT
.*
.AS1     ANOP
         AIF   (&IND).ERROR4
&IND     SETB  1
         DC    AL1(0,0,0,0)  LAST ENTRY INDICATOR
         AGO   .END1
.*
.ERROR1  ANOP
         MNOTE 8,'MISSING REQUIRED PARAMETER - MSG='
         MEXIT
.*
.ERROR2  ANOP
         MNOTE 8,'ILLEGAL PARAMETER - DO='
         MEXIT
.*
.ERROR3  ANOP
         MNOTE 8,'MISSING REQUIRED PARAMETER - DO='
         MEXIT
.*
.ERROR4  ANOP
         MNOTE 8,'ADDITIONAL ENTRIES AFTER END DETECTED'
         MEXIT
.*
.END1    ANOP
         SPACE 2
         MEND
