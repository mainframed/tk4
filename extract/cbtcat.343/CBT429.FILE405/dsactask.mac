//NTS0991S JOB (0991,TAA),'* DSACTASK *',MSGLEVEL=(1,1),
// MSGCLASS=X,NOTIFY=NTS0991,CLASS=6
//IEFUTL1 EXEC PGM=IEV90,
// PARM='XREF(SHORT),LIST,LINECOUNT(55)),DECK'
//SYSPRINT DD SYSOUT=*
//SYSPUNCH DD DISP=SHR,DSN=LOCL.USEROBJ(DSACTASK)
//SYSLIB   DD DISP=SHR,DSN=SYS1.SMPMTS
//         DD DISP=SHR,DSN=LOCL.USERSRC
//         DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.AMODGEN,UNIT=3350,VOL=SER=DLB85A
//         DD DISP=SHR,DSN=SYS1.AGENLIB,UNIT=3350,VOL=SER=DLB85A
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=1
//SYSUT2   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=1
//SYSUT3   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=1
//SYSIN    DD *
         TITLE 'DSACTASK - DYNABLDL/AUTORPLY MANAGER'
DSACTASK CSECT
**********************************************************************
*                                                                    *
* DSACTASK IS A STARTED TASK THAT PROVIDES INSTALLATION MANAGEMENT   *
* FOR DYNABLDL AND THE AUTOREPLIES FACILITY.                         *
*                                                                    *
* RELATED CODE AND HOOKS:                                            *
*    IEAVNP99 - USER NIP EXIT INITIALIZES THE CVTVERID.              *
*    IEECVXIT - MCS USER EXIT DOES AUTO REPLIES.                     *
*    DLACMD00 - TABLE IN CSA CONTAINS CONSOLE MESSAGES TO LOOK FOR   *
*               AND FLAGS, RESPONSES FOR PROCESSING.                 *
*    IEECVENT - USER MACRO TO BUILD DLACMD00.                        *
*    DSACCVTV - USER MACRO TO MAP CVTVERID.                          *
*    IHANVT   - USER MACRO TO MAP NIP VECTOR TABLE.                  *
*    IHAIQE   - USER MACRO TO MAP IQE.                               *
*    IEAPPNIP - USER MACRO TO MAP NIP WORK AREA AND P-LIST.          *
*    IEECVUSR - USER MACRO TO MAP DLACMD00.                          *
*    IEAVVWTO - SUPERZAPPED TO PASS ALL MESSAGES TO IEECVXIT.        *
*    IEAVNIPM - SUPERZAPPED TO PROVIDE TABLE ENTRY FOR IEAVNP99.     *
*                                                                    *
* PROCESSING:                                                        *
*    DSACTASK FIRST LOCATES THE USER CVT PREFIX WHICH IS INITIALIZED *
*    BY THE NIP EXIT ROUTINE IEAVNP99 TO VERIFY THAT THE OPERATOR    *
*    REPLIED "YES" TO THE INITIALIZATION REQUEST MESSAGE ISSUED AT   *
*    IPL TIME.  IF "NO", NP99AUTO FLAG IS ZERO AND DSACTASK TERMIN-  *
*    ATES.                                                           *
*    IF "YES", DSACTASK VERIFIES THAT HE IS RUNNING AS A STARTED     *
*    TASK AND NOT BATCH OR TSO.  IF ALL IS OK, AN ESTAE ENVIRONMENT  *
*    IS CREATED.                                                     *
*    DSACTASK THEN LOCATES THE AUTOREPLY MESSAGE TABLE DLACMD00 IN   *
*    THE LINKLIST.                                                   *
*    THE MODULE IS NEXT LOADED INTO CSA VIA THE GLOBAL=(YES,P) OPTION*
*    OF THE LOAD MACRO AND ANCHORS IT IN THE USER CVT PREFIX AND SETS*
*    THE "TABLE LOADED" FLAG, NP99TBLD.                              *
*    LAST, THE CIB CHAIN IS PROCESSED FOR MODIFIES.                  *
*                                                                    *
* PROCESSING OPTIONS:                                                *
*    P CMD1               TERMINATE DSACTASK                         *
*    F CMD1,STOP          TERMINATE DSACTASK                         *
*    F CMD1,DISPLAY       SHOW DYNABLDL STATUS                       *
*    F CMD1,BLDL          REFRESH BLDL LIST (START/STOP DYNABLDL)    *
*    F CMD1,MOD=........  REFRESH SINGLE MODULE IN DYNAMIC BLDL      *
*    F CMD1,AUTOREPLY     REFRESH AUTOREPLY TABLE IN CSA             *
*    F CMD1,AUTOREPLY=YY  REPLACE DLACMDXX WITH DLACMDYY             *
*                                                                    *
* CHANGE LOG:                                                        *
*    17 DEC 85 - FIRST WRITTEN - M CRUZAN.                           *
*    07 FEB 86 - MODIFIED TO REFRESH DLACMD00 ON OPERATOR MODIFY     *
*                COMMAND - M CRUZAN.                                 *
*    10 MAR 86 - MODIFIED TO ACCEPT ALTERNATE TABLES - M CRUZAN.     *
*                                                                    *
*                         MARV CRUZAN                                *
*                         DEFENSE LOGISTICS AGENCY                   *
*                         DLA SYSTEMS AUTOMATION OFFICE              *
*                         DSAC-TSG                                   *
*                         P O BOX P1605                              *
*                         COLUMBUS, OH  43216-5002                   *
*                         (614) 238-9409 (AV) 850-9409               *
*                                                                    *
**********************************************************************
         EJECT
         USING DSACTASK,R15    ENTRY ADDRESSABILITY
R0       EQU   0               LINKAGE REGISTER
R1       EQU   1               LINKAGE REGISTER
M1       EQU   1               ICM MASK
R2       EQU   2               WORK
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7               CVTVER BASE
M7       EQU   7               ICM MASK
R8       EQU   8               ECB
R9       EQU   9               CIB
R10      EQU   10              TOKEN
R11      EQU   11              USER
R12      EQU   12              STANDARD BASE REGISTER
R13      EQU   13              SAVE AREA POINTER
R14      EQU   14              RETURN REGISTER
R15      EQU   15              LINKAGE REGISTER/RETURN CODE
M15      EQU   15              ICM MASK
         B     START           BRANCH AROUND ID
         DC    C'DSACTASK.&SYSDATE.&SYSTIME' IDENTIFIER
START    DS    0H
         STM   R14,R12,12(R13) SAVE CALLERS REGS
         DROP  R15             DROP ENTRY BASE REGISTER
         LR    R12,R15         LOAD BASE REGISTER
         USING DSACTASK,R12    ESTABLISH ADDRESSABILITY
         LA    R0,72           LOAD LENGTH FOR SAVE AREA
         BAL   R1,*+4          INDICATE GETMAIN
         SVC   10              ISSUE GETMAIN FOR MY SAVE AREA
         ST    R13,4(R1)       STORE BACKWARD POINTER
         ST    R1,8(R13)       STORE FORWARD POINTER
         LR    R13,R1          POINT TO MY SAVE AREA
         L     R1,4(R13)       RESTORE REG 1
         L     R1,24(R1)       FOR PARAMETER LIST
         ICM   R1,M15,0(R1)    LOOK FOR PARM
         ST    R1,PARMAREA     SAVE IT
         LA    R1,2(,R1)       BUMP PAST LENGTH
         LH    R2,0(,R1)       GET PARM LENGTH
         ST    R2,PARMLGTH     SAVE IT, TOO
         LTR   R2,R2           WAS IT REALLY THERE?
         BZ    NOPARM          NO
         MVC   DLANAME+6(2),0(R1) UPDATE DLACMD MODULE
         MVC   NEWNAME+6(2),0(R1)
NOPARM   DS    0H
         XR    R15,R15         ZERO REG15 FOR RETURN CODE
         TITLE 'DSACTASK - INITIALIZATION CODE'
**********************************************************************
*                                                                    *
* ALLOW ONLY AS A STARTED TASK.                                      *
*                                                                    *
**********************************************************************
         L     R2,548          PSAAOLD
         USING ASCB,R2
         ICM   R3,M15,ASCBJBNI BATCH JOB?
         BNZ   EXITM           - NOT ALLOWED
         ICM   R3,M15,ASCBTSB  TSO?
         BNZ   EXITM           - NOT ALLOWED
       MODESET KEY=ZERO,MODE=SUP
*     SYSEVENT DONTSWAP
         SPACE 3
**********************************************************************
*                                                                    *
* LOCATE USER PREFIX TO THE CVT.                                     *
* MODIFY COMMAND TABLE NAME IF OPERATOR PROVIDED AN OVERRIDE AT IPL. *
*                                                                    *
**********************************************************************
         L     R7,16            FIND CVT
         S     R7,=A(CVTMAP-CVTFIX) BACK UP TO PREFIX
         USING CVTFIX,R7        AND ADDRESS
         LA    R7,CVTVERID      FIND USER PREFIX
         DROP  R7
         USING CVTVER,R7        AND ADDRESS IT
         TM    DSACFLG1,NP99AUCH COMMAND MODULE CHANGE?
         BZ    GOESTAE          NO -
         MVC   DLANAME+6(2),DSACTCMD+2 FROM OPER OVERRIDE
         MVC   NEWNAME+6(2),DSACTCMD+2 FROM OPER OVERRIDE
         XC    DSACTCMD,DSACTCMD CLEAN UP
         NI    DSACFLG1,255-NP99AUCH RESET CHANGE FLAG
         TITLE 'DSACTASK - ESTABLISH ERROR HANDLING'
**********************************************************************
*                                                                    *
* ESTABLISH AN ESTAE.                                                *
*                                                                    *
**********************************************************************
GOESTAE  DS    0H
         L     R2,EXITERR      GET ESTAE ROUTINE ADDRESS
         LA    R3,ERRADDR      GET USER PARAMETER LIST
         ESTAE (2),              /* ADDRESS TO GET CONTROL             X
               PARAM=(3),        /* PARAMETER LIST ADDRESS             X
               PURGE=HALT,       /* NO OUTSTANDING I/O                 X
               ASYNCH=YES,       /* MAY BE REQUIRED IN FUTURE          X
               TERM=YES,         /* IF CANCELLED BY OPERATOR           X
               BRANCH=NO,        /* NO BRANCH ENTRY                    X
               RECORD=NO,        /* NO LOGREC RECORD NEEDED            X
               XCTL=YES,         /* MAY BE REQUIRED IN FUTURE          X
               TOKEN=ESTOKEN     /* TAKE CARE OF MY OWN
         LTR   R15,R15          SUCESSFUL?
         BZ    CVTVER1          YES - GO SET UP
         WTO   'DLA1999I DSACTASK UNABLE TO ESTABLISH ERROR RECOVERY', X
               ROUTCDE=(2,11)
         EJECT
**********************************************************************
*                                                                    *
* LOCATE CVTVER AND VERIFY AUTOREPLY IS REQUESTED.                   *
*                                                                    *
**********************************************************************
CVTVER1  DS    0H
         TM    DSACFLG1,NP99AUTO AM I NEEDED?
         BNO   EXTRACT         AUTOREPLIES DISABLED
         BAL   R11,LOADCOMD
         B     EXTRACT
         TITLE 'DSACTASK - FIND THE CIB'
**********************************************************************
*                                                                    *
* BEGIN THE CIB PROCESSING.                                          *
*                                                                    *
**********************************************************************
EXTRACT  DS    0H
       EXTRACT COMMADDR,FIELDS=COMM
         L     R1,COMMADDR      ANSWER AREA
         L     R2,4(,R1)        ADDRESS OF CIB
         LA    R3,4(,R1)        ADDRESS OF ADDRESS OF CIB
         LTR   R2,R2            IS THERE A CIB?
         BZ    NOSTART
         USING CIB,R2
         CLI   CIBVERB,CIBSTART IS IT A START?
         BNE   NOSTART
         DROP  R2
         QEDIT ORIGIN=(3),BLOCK=(2)
NOSTART  QEDIT ORIGIN=(3),CIBCTR=2
         LA    R15,0               BITS 0-2 MUST BE ZERO FOR
         BAL   R11,DYNABLDL
         WTO   'DLA1001I DSACTASK INITIALIZED',ROUTCDE=(2,11)
         EJECT
**********************************************************************
*                                                                    *
* DETERMINE THE STATUS OF DLACMD00.                                  *
*                                                                    *
**********************************************************************
         TM    DSACFLG1,NP99TBLD   IS TABLE LOADED?
         BZ    CIBWAIT             NO - DONT SHOW READY
         OI    DSACFLG1,NP99TBIU   ELSE SHOW AVAILABLE
         SPACE 3
CIBWAIT  DS    0H
         L     R1,COMMADDR         POINT TO COMM AREA
         L     R1,0(,R1)           POINT TO COMM ECB
         WAIT  ECB=(1)
**********************************************************************
*                                                                    *
* WAIT HERE FOR MODIFY/STOP                                          *
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
*                                                                    *
* OPERATOR HAS ISSUED A MODIFY COMMAND - PARSE IT.                   *
*                                                                    *
**********************************************************************
         L     R1,COMMADDR         POINT TO COMM AREA
         L     R2,4(,R1)           POINT TO CIB
         USING CIB,R2
         LA    R3,4(,R1)           POINT TO CIB POINTER
         CLI   CIBVERB,CIBSTOP     STOP
         BE    EXIT1               YES, SAME AS DELETE
         MVC   REPLY(12),=CL12' '
         LH    R1,CIBDATLN         GET LENGTH OF OPERAND
         CH    R1,=H'12'           TOO LONG
         BNH   NOTOLONG            NO
         LH    R1,=H'12'           YES, TRUNCATE IT
NOTOLONG DS    0H
         LTR   R1,R1               NULL
         BZ    COMPROC0            YES, SAME AS BLANKS
         BCTR  R1,0                LENGTH MINUS 1 FOR EX
         EX    R1,MVCHAR           MOVE OPERAND TO REPLY
         B     COMPROC0
MVCHAR   MVC   REPLY(0),CIBDATA        MOVE OPERAND TO REPLY
         TITLE 'DSACTASK - COMMAND ROUTER'
COMPROC0 DS    0H                  FREE THE CIB HERE
         QEDIT ORIGIN=(3),BLOCK=(2)
         CLC   =C'STOP    ',REPLY  STOP REQUESTED?
         BNE   COMPROC1
         B     EXIT1               PROCESS THE STOP COMMAND
         SPACE
COMPROC1 DS    0H
         CLC   =C'BLDL    ',REPLY  DYNAMIC BLDL FACILITY?
         BNE   COMPROC2
         BAL   R11,DYNABLDL        SERVICE THE DYNAMIC BLDL FUNCTION
         B     CIBWAIT             WAIT FOR THE NEXT ONE
         SPACE
COMPROC2 DS    0H
         CLC   =C'DISPLAY ',REPLY  TASK STATUS?
         BNE   COMPROC3
         BAL   R11,DISPLAY         SHOW WHAT WE ARE DOING
         B     CIBWAIT             WAIT FOR THE NEXT ONE
         SPACE
COMPROC3 DS    0H
         CLC   =C'MOD=',REPLY      IS IT A MODULE ENTRY REPLACE?
         BNE   COMPROC4            NO - CHECK FOR NEXT ONE
         BAL   R11,MODREP          ELSE SERVICE THIS REPLACE REQUEST
         B     CIBWAIT             AND WAIT FOR NEXT ONE
         SPACE
COMPROC4 DS    0H
         CLC   =C'AUTOREPLY',REPLY REFRESH THE AUTORPLY TABLE?
         BNE   COMPROC5            NO - CHECK FOR NEXT ONE
         BAL   R11,MODFRESH        ELSE SERVICE THIS REFRESH REQUEST
         B     CIBWAIT             AND WAIT FOR NEXT ONE
         SPACE
COMPROC5 DS    0H
*********************************************************************
*                                                                   *
*        SAMPLE CODE FOR ADDING A COMMAND ROUTER                    *
*                                                                   *
*        CLC   =C'COMMAND ',REPLY  IS IT THIS COMMAND?              *
*        BNE   COMPROCX            NO - CHECK FOR NEXT ONE          *
*        BAL   R11,COMMAND         ELSE SERVICE THIS REQUEST        *
*        B     CIBWAIT             AND WAIT FOR NEXT ONE            *
*COMPROCX DS    0H                                                  *
*                                                                   *
*********************************************************************
         SPACE
BADCMD   DS    0H                  FALL THROUGH IF NO HITS
         MVC   WTOERROR+13(12),REPLY TELL THE SENDER
         LA    R0,0                CLEAR REG ZERO
         ICM   R0,M1,CIBCONID      GET CONSOLE ID FOR MESSAGE
         WTO   MF=(E,WTOERROR)
         B     CIBWAIT
         TITLE 'DSACTASK - TASK STATUS DISPLAY'
*********************************************************************
*                                                                   *
* TELL OPERATOR IF DYNABLDL IS ACTIVE/INACTIVE.                     *
*                                                                   *
*********************************************************************
DISPLAY  DS    0H
         LA    R0,0                CLEAR REG ZERO
         IC    R0,CIBCONID         GET CONSOLE ID FOR MESSAGE
         TM    OPFLAGS,DBACTIVE    IS DYNABLDL ACTIVE?
         BO    DISPACT             YES - GO TELL REQUESTER
         WTO   'DLA1004I DYNABLDL INACTIVE',                           X
               DESC=(5,8,9),MCSFLAG=(REG0,RESP)
         BR    R11                 RETURN TO CALLER
DISPACT  DS    0H
         WTO   'DLA1004I DYNABLDL ACTIVE',                             X
               DESC=(5,8,9),MCSFLAG=(REG0,RESP)
         BR    R11                 RETURN TO CALLER
         TITLE 'DSACTASK - DLACMD00 PROCESSOR'
*********************************************************************
*                                                                   *
* AUTOREPLY TABLE REFRESHED FROM HERE.                              *
*                                                                   *
*********************************************************************
MODFRESH DS    0H
         ST    R11,SAVERTN         SAVE CALLERS RETURN
         CLI   REPLY+9,C'='        AN OPERAND?
         BNE   NOPERAND            NO
         MVC   NEWNAME+6(2),REPLY+10 ELSE MOVE IT IN
NOPERAND DS    0H
         TM    DSACFLG1,NP99AUTO   WAS AUTOREPLY AUTHORIZED?
         BO    MODFLOAD            YES -
         WTO   'DLA1006I REFRESH DENIED - AUTOREPLY NOT AUTHORIZED',   X
               ROUTCDE=(2,11)
         B     MODFEXIT
MODFLOAD DS    0H
         TM    OPFLAGS,DBACTIVE    IS DYNABLDL ACTIVE
         BZ    MODNLOAD
         MVC   MBLDLNME,NEWNAME      INSERT TABLE NAME
         SR    R0,R0               SHOW SYSTEM ISSUED
         LA    R1,MBLDL            ISSUE S DYNABLDL,P=DLACMD00
         MGCR  (1)
         MVC   MBLDLNME,=C'XXXXXXXX' AND RESET DUMMY NAME
MODNLOAD DS    0H
         BAL   R11,DELECODE        DELETE THE COMMAND TABLE
         BAL   R11,LOADCOMD        AND GET THE NEW ONE
         OI    DSACFLG1,NP99TBIU   SHOW AVAILABLE FOR USE
MODFEXIT DS    0H
         L     R11,SAVERTN         GET OUR ROADMAP
         BR    R11                 AND RETURN TO CALLER
         TITLE 'DSACTASK - DYNABLDL PROCESSOR'
*********************************************************************
*                                                                   *
* COMMANDS FOR CONTROLING DYNABLDL ISSUED HERE.                     *
*                                                                   *
*********************************************************************
DYNABLDL DS    0H
         TM    OPFLAGS,DBACTIVE    IS DYNABLDL ACTIVE
         BO    REPORT              YES - MAKE A REPORT
STARTIT  DS    0H
         SR    R0,R0               SHOW SYSTEM ISSUED
         LA    R1,SBLDL            ISSUE S DYNABLDL,P=START
         MGCR  (1)
         LA    R0,0                CLEAR REG ZERO
         IC    R0,CIBCONID         GET CONSOLE ID FOR MESSAGE
         WTO   'DLA1005I DYNABLDL STARTED',                            X
               DESC=(5,8,9),MCSFLAG=(REG0,RESP)
         OI    OPFLAGS,DBACTIVE    SHOW DYNABLDL ACTIVE
         BR    R11                 RETURN TO CALLER
REPORT   DS    0H
         SR    R0,R0               SHOW SYSTEM ISSUED
         LA    R1,RBLDL            ISSUE S DYNABLDL,P=REPORT
         MGCR  (1)
         BR    R11                 AND RETURN TO CALLER
MODREP   DS    0H
         MVC   MBLDLNME,REPLY+4    MOVE NEW NAME IN
         SR    R0,R0               SHOW SYSTEM ISSUED
         LA    R1,MBLDL            ISSUE S DYNABLDL,P=MODULENAME
         MGCR  (1)
         MVC   MBLDLNME,=C'XXXXXXXX' AND RESET DUMMY NAME
         BR    R11                 AND RETURN TO CALLER
         TITLE 'DSACTASK - LOCATE DLACMD00 AND UPDATE CVTVERID'
**********************************************************************
*                                                                    *
* FIND THE CMD PROCESSING TABLE AND INSTALL IT.                      *
*                                                                    *
**********************************************************************
LOADCOMD DS    0H
         MVC   DLANAME,NEWNAME  REFRESH NAME ALWAYS
         LA    R2,DLALIST       GET LIST FOR BLDL
         BLDL  0,(2)            AND LOOK IN LINK LIST
         LTR   R15,R15          FOUND IT?
         BZ    CMDLOCAT         - YES
NOCOMMND DS    0H
         WTO   'DLA1007E AUTOREPLY MODULE NOT FOUND - AUTOREPLY IS INACX
               TIVE',ROUTCDE=(2,11),DESC=(2)
         BR    R11              ELSE TELL THE WORLD AND CONTINUE
CMDLOCAT DS    0H
         LOAD  EPLOC=NEWNAME,GLOBAL=(YES,P) PUT IN PAGEABLE CSA
         LTR   R15,R15          GOOD LOAD?
         BNZ   NOCOMMND          NO - SAY SO
         MVC   DLANAME,NEWNAME  ALWAYS REFRESH IT
         ST    R0,DSACTCMD      AND SAVE DLACMD00 ENTRY POINT
         OI    DSACFLG1,NP99TBLD MARK AS LOADED
         WTO   'DLA1008I CVTVERID UPDATED BY DSACTASK',ROUTCDE=(2,11)
         BR    R11              RETURN TO CALLER
         TITLE 'DSACTASK - DLACMD00 DELETE CODE'
*********************************************************************
*                                                                   *
* DLACMD00 DELETED HERE.                                            *
*                                                                   *
*********************************************************************
DELECODE DS    0H
         NI    DSACFLG1,255-NP99TBIU SHOW TABLE NOT AVAILABLE
         TM    DSACFLG1,NP99TBLD WAS DLACMD00 LOADED
         BZR   R11              NO CLEANUP TO BE DONE
        DELETE EPLOC=DLANAME    ELSE DELETE IT
         LTR   R15,R15          SUCCESSFUL?
         BNZ   NODELEMD          NOT QUITE -
         XC    DSACTCMD,DSACTCMD CLEAR E/P POINTER
         NI    DSACFLG1,255-NP99TBLD  AND FLAG
         BR    R11              RETURN TO CALLER
NODELEMD DS    0H
         WTO   'DLA1007E AUTOREPLY MODULE NOT DELETED',ROUTCDE=(2,11)
         BR    R11              RETURN TO CALLER
         TITLE 'DSACTASK - EXIT CODE'
*********************************************************************
*                                                                   *
* DSACTASK EXIT CODE HERE.                                          *
*                                                                   *
*********************************************************************
EXIT1    DS    0H
         NI    DSACFLG1,255-NP99TBIU SHOW TABLE NOT AVAILABLE
         WTO   'DLA1003I DSACTASK TERMINATING',ROUTCDE=(2,11)
         TM    OPFLAGS,DBACTIVE    IS DYNABLDL ACTIVE?
         BZ    BLDLEND             NO
         LA    R1,PBLDL            ELSE NEED TO STOP IT
         MGCR  (1)
         NI    OPFLAGS,DBINACTI    SHOW DYNABLDL INACTIVE
BLDLEND  DS    0H
         ESTAE 0,TOKEN=ESTOKEN CANCEL ERROR RECOVERY
         BAL   R11,DELECODE       DELETE THE COMMAND LIST
DELGOOD  DS    0H
         B     EXIT
EXIT     DS    0H              NORMAL EXIT
         DROP  R7
*     SYSEVENT OKSWAP
       MODESET KEY=NZERO,MODE=PROB
EXITM    DS    0H              EXIT HERE IF ILLEGAL ENTRY
         LR    R1,R13          POINT TO MY SAVE AREA
         L     R13,4(R13)      POINT TO CALLERS SAVE AREA
         ST    R15,16(R13)     INSTALL RETURN CODE IN SAVE AREA
         LA    R0,72           LOAD LENGTH
         SVC   10              ISSUE FREEMAIN
         LM    R14,R12,12(R13) RESTORE REGISTERS
         BR    R14             AND RETURN TO CALLER
         LTORG
PARMAREA DC    F'0'            PARAMETER LIST POINTER
PARMLGTH DC    F'0'            PARAMETER LIST LENGTH
SAVERTN  DC    F'0'            LINKAGE REGISTER SAVE AREA
DLALIST  DC    AL2(1,58)       BLDL LIST ENTRY
DLANAME  DC    CL8'DLACMD00'   BLDL LIST ENTRY
         DC    XL46'00'        BLDL LIST ENTRY
         SPACE
NEWNAME  DC    CL8'DLACMD00'   BLDL LIST ENTRY
         SPACE
ESTOKEN  DC    A(0)            ESTAE TOKEN
OPFLAGS  DC    A(0)
DBACTIVE EQU   X'80'           DYNABLDL INDICATOR 1
DBINACTI EQU   X'7F'
*                                   1 = ACTIVE
*                                   0 = INACTIVE
DBLOADED EQU   X'40'           DYNABLDL INDICATOR 2
DBUNLOAD EQU   X'BF'
*                                   1 = DYNABLDL LOADED
*                                   0 = DYNABLDL NOT LOADED
OPFLAG20 EQU   X'20'           RESERVED
OPFLAG10 EQU   X'10'           RESERVED
OPFLAG08 EQU   X'08'           RESERVED
OPFLAG04 EQU   X'04'           RESERVED
OPFLAG02 EQU   X'02'           RESERVED
OPFLAG01 EQU   X'01'           RESERVED
         SPACE 3
EXITERR  DC    V(DLAESTAE)     ESTAE ENTRY
ERRADDR  DC    A(OPFLAGS),A(PBLDL),X'80',AL3(DLANAME)
COMMADDR DC    A(0)            COMM AREA ADDRESS FOR EXTRACT
ECBADDR  DC    A(0)            CIB ECB ADDRESS
CIBADDR  DC    A(0)            CIB ADDRESS
TOKADDR  DC    A(0)            TOKEN ADDRESS
         SPACE 3
REPLY    DC    CL12' '
WTOERROR WTO   'DLA1002I XXXXXXXXXXXX - INVALID COMMAND',              X
               MCSFLAG=(REG0),MF=L
         SPACE 3
         DS    0F
SBLDL    DC    X'00'
         DC    AL1(SBLDLEND-SBLDL)
         DC    X'0000'
         DC    C'S DYNABLDL,PARM=START ** DSACTASK **'
SBLDLEND DS    0C
         SPACE 3
         DS    0F
RBLDL    DC    X'00'
         DC    AL1(RBLDLEND-RBLDL)
         DC    X'0000'
         DC    C'S DYNABLDL,PARM=REPORT  ** DSACTASK **'
RBLDLEND DS    0C
         SPACE 3
         DS    0F
PBLDL    DC    X'00'
         DC    AL1(PBLDLEND-PBLDL)
         DC    X'0000'
         DC    C'S DYNABLDL,PARM=STOP  ** DSACTASK **'
PBLDLEND DS    0C
         SPACE 3
         DS    0F
MBLDL    DC    X'00'
         DC    AL1(MBLDLEND-MBLDL)
         DC    X'0000'
         DC    C'S DYNABLDL,PARM='
MBLDLNME DC    C'XXXXXXXX'
         DC    C' ** DSACTASK **'
MBLDLEND DS    0C
         EJECT
CIB      DSECT
         IEZCIB
         EJECT
         IHAASCB
         CVT   DSECT=YES,PREFIX=YES,LIST=YES
         DSACCVTV
         EJECT
DLAESTAE CSECT
         USING DLAESTAE,R15
         STM   R14,R12,12(R13) SAVE RTMS REGS
         DROP  R15
         LR    R12,R15         SHIFT BASE REGISTER
         USING DLAESTAE,R12
       GETMAIN R,LV=72
         ST    R13,4(,R1)      AND CHAIN
         ST    R1,8(,R13)         SAVE AREA POINTERS
         LR    R13,R1          MY SAVE AREA
         L     R1,4(,R13)
         L     R1,24(,R1)      RESTORE REG 1
         XR    R15,R15         CLEAR REG 15 FOR RETURN TO RTM
         L     R1,0(,R1)       FIRST WORD OF SDWA POINTS TO PARM LIST
         LM    R8,R10,0(R1)    PARAMETER LIST
         WTO   'DLA1998I DSACTASK ESTAE ENTERED',ROUTCDE=(2,11)
         TM    0(R8),X'80'     DYNABLDL ACTIVE?
         BZ    TERM2
         SR    R0,R0           SHOW SYSTEM
         LA    R1,0(,R9)       POINT TO MGCR LIST
         MGCR  (1)
TERM2    DS    0H
         L     R7,16           GET CVT
         S     R7,=A(CVTMAP-CVTFIX) BACK UP TO PREFIX
         USING CVTFIX,R7       AND ADDRESS IT
         LA    R7,CVTVERID     FIND USER PREFIX
         DROP  R7
         USING CVTVER,R7       AND ADDRESS IT
         NI    DSACFLG1,255-NP99TBIU SHOW TABLE NOT AVAILABLE
         TM    DSACFLG1,NP99TBLD WAS TABLE LOADED
         BZ    TERM3           NO - FINISH UP
         LA    R10,0(,R10)     ELSE STRIP VL FLAG
        DELETE EPLOC=(10)       AND DELETE IT
         XC    DSACTCMD,DSACTCMD CLEAR E/P POINTER
         NI    DSACFLG1,255-NP99TBLD  AND FLAG
         LTR   R15,R15         GOOD DELETE?
         BZ    TERM3           YES
         WTO   'DLA1007E AUTOREPLY MODULE NOT DELETED',ROUTCDE=(2,11)
TERM3    DS    0H
         LR    R1,R13
         L     R13,4(,R1)
      FREEMAIN R,LV=72,A=(1)
         SETRP WKAREA=(2),REGS=(14,12),RC=0
         LTORG
       IHASDWA DSECT=YES
         END
/*
//*
//IEWL EXEC PGM=IEWL,
// PARM='LIST,NCAL,XREF,LET'
//SYSPRINT DD SYSOUT=*
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,1),DCB=BUFNO=1
//USEROBJ  DD DISP=SHR,DSN=LOCL.USEROBJ
//SYSLMOD  DD DISP=SHR,DSN=LOCL.LINKLIB
//SYSLIN   DD *
   INCLUDE USEROBJ(DSACTASK)
   SETCODE AC(1)
 NAME  DSACTASK(R)
/*
//
