         MACRO
&USRNAME CASE  &REL=,&MASK=                                   33001.
         COPY  STRCGBLS
         LCLA  &CASECTR           CASE_COUNTER                33011.
         LCLA  &COMPLB#           COMP_LABEL_NO               33013.
         LCLA  &I                 I                           33015.
         LCLB  &CLABREQ           CASE_LABEL_REQD             33017.
         LCLB  &XRANGA            INDEX_RANGE_ASSURED         33019.
         LCLB  &EQUAL             EQUAL_TEST_OUTSTANDING      33022.
         LCLB  &RANGE             RANGE_TEST_OUTSTANDING      33026.
         LCLB  &MISCFND           MISC_FOUND                  33031.
         LCLB  &MULTOF4           MULTIPLESOF4                33032.
         LCLB  &UNEXP             UNEXPECTED_OPERANDS_FOUND   33034.
         LCLC  &CFORMAT           CASE_FORMAT                 33038.
         LCLC  &MAMAPFX           MAMA_BLOCK_PREFIX           33040.
         LCLC  &XADDR             INDEX_ADDR                  33041.
         LCLC  &LABEL             LABEL                       33042.
         LCLC  &COMPLAB           COMP_LABEL                  33043.
         LCLC  &NXTCASE           NEXT_CASE                   33044.
         LCLC  &XLENG             INDEX_LENGTH                33046.
         LCLC  &X                 X                           33161.
         LCLA  &MOM               MOM                         33162.
         LCLC  &INSERT            INSERT                      33276.
         LCLA  &BASE              BASE                        33363.
         LCLA  &OP                OP                          33364.
         LCLA  &LIMIT             LIMIT                       33365.
         LCLA  &GUESS             GUESS                       33440.
         LCLA  &OPCOUNT           OP_COUNT                    33496.
         STRCTRAC  CASE
.*       CALL  CASE_POSITION_CHECK                            33050.
&STRCERR SETB  0                                              33134.
         AIF   ('&STRCTYP(&STRCNST)' EQ 'DOCASE').M155        33135.
         MNOTE 8,' STRC3301 "CASE" NOT IMMEDIATE DAUGHTER OF "DOCASE"'
&STRCERR SETB  1                                              33138.
         AIF   (&STRCNST LE 1).M155                           33139.
         AIF   ('&STRCTYP(&STRCNST-1)' NE 'DOCASE').M145      33140.
         MNOTE 8,' STRC3302 ASSUMING "BLEND" OMITTED--INSERTED'
         BLEND
&STRCERR SETB  0                                              33144.
         AGO   .M155                                          33143.
.M145    AIF   (&STRCNST LE 2).M155                           33146.
         AIF   ('&STRCTYP(&STRCNST-2)' NE 'DOCASE').M155      33147.
         MNOTE 8,' STRC3303 ASSUMING TWO "BLENDS" OMITTED--INSERTED'
         BLEND
         BLEND
&STRCERR SETB  0                                              33152.
.M155    ANOP
.*       CORP  CASE_POSITION_CHECK                            33156.
         AIF   (NOT &STRCERR).M058                            33053.
         MNOTE 8,' STRC3304 "CASE" TREATED AS "BLOCK" MACRO'  33055.
&USRNAME BLOCK
         MEXIT
.M058    ANOP
&USRNAME STRCPUSH  TYPEVAL=CASE                               33059.
         AIF   (&STRCERR).FIN                                 33063.
         AIF   ('&REL&MASK' EQ '').M070                       33067.
         MNOTE 8,' STRC3310 REL= OR MASK= NOT IN PARENTHESES--IGNORED'
.M070    ANOP
.*       CALL  CASE_GET_DOCASE_INFO                           33071.
&MOM     SETA  &STRCNST-1                                     33164.
&CASECTR SETA  &STRCOP1(&MOM)                                 33165.
         AIF   ('&SYSLIST(1)' EQ 'MISC').M169                 33166.
&CASECTR SETA  &CASECTR+1                                     33168.
.M169    ANOP
&XADDR   SETC  '&STRCOP2(&MOM)'                               33170.
&CFORMAT SETC  '&STRCOP3(&MOM)'                               33171.
&XLENG   SETC  '&STRCOP4(&MOM)'                               33172.
&X       SETC  '&STRCINF(&MOM)'                               33173.
&CLABREQ SETB  ('&X'(1,1) EQ '1')                             33174.
&MISCFND SETB  ('&X'(2,1) EQ '1')                             33175.
&MULTOF4 SETB  ('&X'(3,1) EQ '1')                             33176.
&UNEXP   SETB  ('&X'(4,1) EQ '1')                             33177.
&XRANGA  SETB  ('&X'(5,1) EQ '1')                             33178.
&MAMAPFX SETC  '$&STRCBL#(&MOM)'                              33179.
.*       CORP  CASE_GET_DOCASE_INFO                           33180.
         AIF   ('&USRNAME' EQ '').M081                        33075.
&USRNAME DS    0H
.M081    AIF   ('&SYSLIST(1)' NE 'MISC').M086                 33082.
.*       CALL  CASE_MISC_PROCESS                              33084.
         AIF   (&CLABREQ).M546                                33541.
         B     &MAMAPFX.C&CASECTR
.M546    AIF   (NOT &MISCFND).M551                            33547.
         MNOTE 8,' STRC3311 MULTIPLE MISC CASES IN DOCASE--THIS BLOCK'
         MNOTE 0,'          IS DEAD CODE'                     33550.
         AGO   .M557                                          33551.
.M551    ANOP
&LABEL   SETC  '&MAMAPFX.MSC'                                 33552.
&MISCFND SETB  1                                              33556.
.M557    AIF   (NOT &XRANGA).M562                             33558.
         MNOTE 8,' STRC3308 "DOCASE ...,ONLY" INVALID WITH MISC'
&XRANGA  SETB  0                                              33561.
.M562    ANOP
.*       CORP  CASE_MISC_PROCESS                              33563.
         AGO   .M117                                          33086.
.M086    AIF   ('&CFORMAT' EQ 'SIMPLE').M103                  33087.
         AIF   ('&CFORMAT' EQ 'CONDTEST').M103                33087.
.* THESE BRANCHES HAVE BEEN REVERSED FROM SOURCE;  SEE COMMENT
.* ON LINE 33103. OF SOURCE.
         AIF   (&STRCGNL GT &STRCGNX).M117                    33089.
.*       CALL  CASE_PROCESS_COMPARE_OPERANDS                  33091.
&I       SETA  1                                              33191.
&COMPLB# SETA  &STRCCL#(&STRCGNL)                             33192.
&EQUAL   SETB  0                                              33194.
&RANGE   SETB  0                                              33194.
.M195    AIF   (&I GT N'&SYSLIST).M241                        33195.
         AIF   ('&CFORMAT' EQ 'SPARSE').M204                  33197.
         AIF   ('&CFORMAT' EQ 'CHARCOMP').M204                33197.
         AIF   (N'&SYSLIST(&I) GT 1).M200                     33197.
         AIF   (T'&SYSLIST(&I) NE 'N').M204                   33197.
         AIF   (&SYSLIST(&I) GE 0 AND &SYSLIST(&I) LE 255).M239
         AGO   .M204                                          33197.
.M200    AIF   (T'&SYSLIST(&I,1) NE 'N').M204                 33200.
         AIF   (&SYSLIST(&I,1) LT 0 OR &SYSLIST(&I,1) GT 255).M204
         AIF   (T'&SYSLIST(&I,2) NE 'N').M204                 33202.
         AIF   (&SYSLIST(&I,2) GE 0 AND &SYSLIST(&I,2) LE 255).M239
.M204    AIF   (NOT &EQUAL).M212                              33205.
&LABEL   SETC  '&STRCPFX.BEG'                                 33207.
         BE    &LABEL
&EQUAL   SETB  0                                              33211.
         AGO   .M228                                          33212.
.M212    AIF   (NOT &RANGE).M220                              33213.
&LABEL   SETC  '&STRCPFX.BEG'                                 33215.
         BNH   &LABEL
&RANGE   SETB  0                                              33219.
         AGO   .M228                                          33220.
.M220    AIF   (NOT &CLABREQ).M225                            33221.
&COMPLAB SETC  '&MAMAPFX.C&COMPLB#'                           33223.
.M225    ANOP
&UNEXP   SETB  1                                              33226.
.M228    ANOP
.*       CALL  CASE_GEN_COMPARE                               33229.
         AIF   ('&XADDR' NE '').M282                          33278.
&COMPLAB LA    0,&SYSLIST(&I,1)
         CR    1,0
         AGO   .M304                                          33282.
.M282    AIF   ('&CFORMAT' NE 'CHARCOMP').M300                33283.
&INSERT  SETC  ''                                             33285.
         AIF   ('&SYSLIST(&I,1)'(1,1) EQ '=').M297            33286.
         AIF   ('&SYSLIST(&I,1)'(1,1) NE '''').M291           33288.
&INSERT  SETC  '=C'                                           33290.
         AGO   .M297                                          33291.
.M291    AIF    ('&SYSLIST(&I,1)'(K'&SYSLIST(&I,1),1) NE '''').M297
&INSERT  SETC  '='                                            33294.
.M297    ANOP
&COMPLAB CLC   &XADDR.(&XLENG),&INSERT&SYSLIST(&I,1)
         AGO   .M304                                          33300.
.M300    ANOP
&COMPLAB CLI   &XADDR,&SYSLIST(&I,1)
.M304    AIF   ('&COMPLAB' EQ '').M310                        33305.
&COMPLAB SETC  ''                                             33307.
&COMPLB# SETA  &COMPLB#+1                                     33308.
.M310    AIF   (N'&SYSLIST(&I) GT 1).M313                     33310.
&EQUAL   SETB  1                                              33312.
         AGO   .M353                                          33313.
.M313    AIF   (N'&SYSLIST(&I) LE 2).M318                     33314.
         MNOTE 8,' STRC3312 &SYSLIST(&I) INVALID--'           33316.
         MNOTE 0,'                ONLY FIRST TWO SUBOPERANDS PROCESSED'
.M318    ANOP
&RANGE   SETB  1                                              33319.
&COMPLAB SETC  '&MAMAPFX.C&COMPLB#'                           33321.
         BL    &COMPLAB
         AIF   ('&XADDR' NE '').M329                          33325.
         LA    0,&SYSLIST(&I,2)
         CR    1,0
         AGO   .M353                                          33329.
.M329    AIF   ('&CFORMAT' NE 'CHARCOMP').M349                33330.
&INSERT  SETC  ''                                             33332.
         AIF   ('&SYSLIST(&I,2)'(1,1) EQ '=').M346            33335.
         AIF   ('&SYSLIST(&I,2)'(1,1) NE '''').M340           33337.
&INSERT  SETC  '=C'                                           33339.
         AGO   .M346                                          33322
.M340    AIF   ('&SYSLIST(&I,2)'(K'&SYSLIST(&I,2),1) NE '''').M346
&INSERT  SETC  '='                                            33343.
.M346    CLC   &XADDR.(&XLENG),&INSERT&SYSLIST(&I,2)
         AGO   .M353                                          33349.
.M349    CLI   &XADDR,&SYSLIST(&I,2)
.M353    ANOP
.*       CORP  CASE_GEN_COMPARE                               33354.
.M239    ANOP
&I       SETA  &I+1                                           33240.
         AGO   .M195                                          33241.
.M241    AIF   (NOT &EQUAL).M250                              33245.
         BNE   &MAMAPFX.C&COMPLB#
         AGO   .M257                                          33250.
.M250    AIF   (NOT &RANGE).M257                              33251.
         BH    &COMPLAB
.M257    ANOP
&STRCCL#(&STRCGNL)  SETA  &COMPLB#                            33258.
.*       CORP  CASE_PROCESS_COMPARE_OPERANDS                  33260.
         AIF   ('&CFORMAT' NE 'GENERAL').M117                 33096.
.*       CALL  CASE_PROCESS_VECTOR_OPERANDS                   33098.
&BASE    SETA  ((&STRCGNL-1)*256)+1                           33367.
         AIF   (N'&SYSLIST LE 0).M426                         33369.
&I       SETA  1                                              33371.
.M372    AIF   (&I GT N'&SYSLIST).M424                        33372.
         AIF   (T'&SYSLIST(&I,1) NE 'N').M422                 33374.
&OP      SETA  &SYSLIST(&I,1)                                 33376.
         AIF   (&OP GT 255 OR &OP LT 0).M422                  33377.
  AIF  (T'&SYSLIST(&I,2) NE 'O' AND T'&SYSLIST(&I,2) NE 'N').M422
         AIF   (N'&SYSLIST(&I) LE 2).M385                     33381.
         MNOTE 8,' STRC3312 &SYSLIST(&I) INVALID--'           33383.
         MNOTE 0,'                ONLY FIRST TWO SUBOPERANDS PROCESSED'
.M385    AIF   (T'&SYSLIST(&I,2) NE 'N').M397                 33386.
&LIMIT   SETA  &SYSLIST(&I,2)                                 33388.
         AIF   (&LIMIT GT 255 OR &LIMIT LT 0).M422            33389.
         AIF   (&LIMIT GE &OP).M399                           33391.
         MNOTE 8,' STRC3305 &SYSLIST(&I) INVALID--&OP ASSUMED'
&LIMIT   SETA  &OP                                            33395.
         AGO   .M399                                          33397.
.M397    ANOP
&LIMIT   SETA  &OP                                            33398.
.M399    ANOP
         AIF   (&LIMIT LE &STRCMCV(&STRCGNL)).M403            33400.
&STRCMCV(&STRCGNL)  SETA  &LIMIT                              33402.
.M403    ANOP
.M404    AIF   (&OP GT &LIMIT).M417                           33404.
&STRCOCC(&BASE+&OP) SETB  1                                   33406.
&MAMAPFX.G&OP  DS  0H
         AIF   (&OP EQ &CASECTR).M411                         33408.
&UNEXP   SETB  1                                              33410.
.M411    AIF   (&OP/4*4 EQ &OP).M415                          33412.
&MULTOF4 SETB  0                                              33414.
.M415    ANOP
&OP      SETA  &OP+1                                          33416.
         AGO   .M404                                          33417.
.M417    ANOP
.M422    ANOP
&I       SETA  &I+1                                           33423.
         AGO   .M372                                          33424.
.M424    AGO   .M431                                          33426.
.M426    ANOP
.*       CALL  CASE_ASSUMED_VECTOR_CASE                       33427.
&GUESS   SETA  &STRCMCV(&STRCGNL)+1                           33442.
         AIF   (&GUESS GT 0).M448                             33444.
&GUESS   SETA  1                                              33446.
.M448    ANOP
&STRCMCV(&STRCGNL)  SETA  &GUESS                              33448.
&STRCOCC(&BASE+&GUESS)  SETB  1                               33449.
&MAMAPFX.G&GUESS  DS  0H
         AIF   (NOT &UNEXP).M456                              33451.
         MNOTE 4,' STRC3306 EARLIER UNEXPECTED OPERAND IMPLIES THIS'
         MNOTE 0,'          TO BE CASE &GUESS'                33454.
.M456    ANOP
.*       CORP  CASE_ASSUMED_VECTOR_CASE                       33457.
.M431    ANOP
.*       CORP  CASE_PROCESS_VECTOR_OPERANDS                   33432.
         AGO   .M117                                          33103.
.M103    ANOP
.*       CALL  CASE_SET_NAMES                                 33104.
         AIF   (NOT &CLABREQ).M468                            33465.
&LABEL   SETC  '&MAMAPFX.C&CASECTR'                           33467.
.M468    ANOP
&I       SETA  &CASECTR+1                                     33469.
&NXTCASE SETC  '&MAMAPFX.C&I'                                 33470.
.*       CORP  CASE_SET_NAMES                                 33471.
         AIF   ('&CFORMAT' NE 'SIMPLE').M112                  33108.
.*       CALL  CASE_BCT_GEN                                   33110.
&LABEL   BCT   1,&NXTCASE
&LABEL   SETC  ''                                             33477.
         AIF   (T'&SYSLIST(1) EQ 'O').M491                    33478.
         AIF   (T'&SYSLIST(1) NE 'N').M487                    33480.
         AIF   (&SYSLIST(1) EQ &CASECTR).M491                 33482.
         MNOTE 8,' STRC3307 OPERAND INVALID VALUE ON SIMPLE CASE &CASEC*
               TR'                                            33485.
         AGO   .M491                                          33487.
.M487    MNOTE 8,' STRC3309 OPERAND MUST BE SELF-DEFINING TERM OR'
         MNOTE 0,'          OMITTED ON SIMPLE CASE &CASECTR'  33488.
.M491    ANOP
.*       CORP  CASE_BCT_GEN                                   33492.
         AGO   .M117                                          33112.
.M112    ANOP
.*       CALL  CASE_CONDTEST_GEN                              33113.
&STRCUBL SETC  '&NXTCASE'                                     33499.
&STRCUFL SETC  '&STRCPFX.BEG'                                 33500.
&STRCUFC SETB  1                                              33501.
&STRCFLU SETB  0                                              33502.
&STRCFRX SETA  1                                              33503.
&STRCLSX SETA  N'&SYSLIST                                     33504.
&STRCUID SETC  'T'                                            33506.
&LABEL   STRCCEP  &SYSLIST(1),&SYSLIST(2),&SYSLIST(3),&SYSLIST(4),     *
               &SYSLIST(5),&SYSLIST(6),&SYSLIST(7),&SYSLIST(8),        *
               &SYSLIST(9),&SYSLIST(10),&SYSLIST(11),&SYSLIST(12),     *
               &SYSLIST(13),&SYSLIST(14),&SYSLIST(15),&SYSLIST(16),    *
               &SYSLIST(17),&SYSLIST(18),&SYSLIST(19),&SYSLIST(20),    *
               &SYSLIST(21),&SYSLIST(22),&SYSLIST(23),&SYSLIST(24),    *
               &SYSLIST(25),&SYSLIST(26),&SYSLIST(27),&SYSLIST(28),    *
               &SYSLIST(29),&SYSLIST(30),&SYSLIST(31),&SYSLIST(32),    *
               &SYSLIST(33),&SYSLIST(34),&SYSLIST(35),&SYSLIST(36),    *
               &SYSLIST(37),&SYSLIST(38),&SYSLIST(39),&SYSLIST(40),    *
               &SYSLIST(41),&SYSLIST(42),&SYSLIST(43),&SYSLIST(44),    *
               &SYSLIST(45),&SYSLIST(46),&SYSLIST(47),&SYSLIST(48),    *
               &SYSLIST(49),&SYSLIST(50)                      33507.
         AIF   (&STRCFLU).M529                                33527.
&LABEL   SETC  ''                                             33531.
         AGO   .M532                                          33532.
.M529    ANOP
&LABEL   SETC  '&STRCPFX.BEG'                                 33529.
.M532    ANOP
.*       CORP  CASE_CONDTEST_GEN                              33533.
.M117    ANOP
.*       CALL  CASE_TRACE_COUNTERS                            33118.
         AIF   (NOT (&STRCDBC OR &STRCDBN)).M601              33568.
         AIF   (NOT &STRCDBC).M587                            33570.
&LABEL   LH    1,&STRCPFX.CTR
&LABEL   SETC  ''                                             33574.
         LA    1,1(1)
         STH   1,&STRCPFX.CTR
         AIF   ('&SYSLIST(1)' EQ 'MISC' OR &CASECTR GT 255).M579
&STRCHXI SETA  &CASECTR                                       33581.
         STRCXHEX
         MNOTE *,' STRC3313 CASE DEBUG ID=X''&STRCHXO'''      33583.
         MVI   &MAMAPFX.LSC,X'&STRCHXO' CASE NUMBER FOR TRACING
         AGO   .M586                                          33586.
.M579    MVI   &MAMAPFX.LSC,X'FF'
.M586    ANOP
.M587    ANOP
&LABEL   B     &STRCPFX.GO
&LABEL   SETC  '&STRCPFX.GO'                                  33589.
         AIF   (NOT &STRCDBN).M596                            33592.
         DC    C'&STRCNAM(&STRCNST)',0H'0'
.M596    AIF   (NOT &STRCDBC).M601                            33597.
&STRCPFX.CTR  DC  H'0' CASE COUNT
.M601    ANOP
.*       CORP  CASE_TRACE_COU NTER                            33602.
.*       CALL  CASE_UPDATE_INFO                               33121.
&MOM     SETA  &STRCNST-1                                     33609.
&STRCINF(&MOM) SETC  '1&MISCFND&MULTOF4&UNEXP&XRANGA'         33611.
&STRCOP1(&MOM)  SETC  '&CASECTR'                              33613.
.*       CORP  CASE_UPDATE_INFO                               33616.
         AIF   ('&LABEL' EQ '').M126                          33123.
&LABEL   DS    0H
.M126    ANOP
.FIN     MEND
