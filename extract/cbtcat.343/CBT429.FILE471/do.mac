         MACRO
&USRNAME DO    &REL=,&MASK=                                   21001.
         COPY  STRCGBLS
         LCLA  &WHX               WHILE_INDEX                 21031.
         LCLA  &WHXEND            WHILE_END_INDEX             21034.
         LCLA  &UNX               UNTIL_INDEX                 21035.
         LCLA  &UNXEND            UNTIL_END_INDEX             21035.
         LCLA  &OPF               OPERAND_FORMAT              21036.
         LCLA  &I                 I                           21038.
         LCLA  &LB                LB                          21039.
         LCLA  &LASTOP            LASTOP                      21041.
         LCLB  &WHPRES            WHILE_PRESENT               21045.
         LCLB  &UNPRES            UNTIL_PRESENT               21045.
         LCLB  &LBLABRQ           LB_LABEL_REQ                21048.
         LCLB  &WHCTEST           WHILE_COND_TEST             21050.
         LCLB  &UNCTEST           UNTIL_COND_TEST             21050.
         LCLB  &CONDREQ           THIS_CONDITIONAL_REQD       21052.
         LCLC  &LBOPID            LB_OPCODE_ID                21057.
         LCLC  &LBOP1             LB_OPERAND1                 21059.
         LCLC  &LBOP2             LB_OPERAND2                 21059.
         LCLC  &LBLOGIC           LB_LOGIC_OP                 21061.
         LCLC  &LBTYPE            LOOPING_BRANCH_TYPE         21064.
         LCLC  &LABEL             LABEL                       21067.
         LCLC  &FIRSTID           FIRST_ID                    21069.
         LCLC  &MAINOP            MAIN_OP                     21071.
         LCLA  &OPCOUNT           OPERAND_COUNT               21385.
         LCLA  &PASS              PASS                        21587.
         LCLC  &B8,&B10,&B11      B8, B10, B11                21776.
         LCLC  &X                 MACRO TEMPORARY
         STRCTRAC  DO                                         21075.
&USRNAME STRCPUSH  TYPEVAL=DO                                 21077.
         AIF   (&STRCERR).FIN                                 21081.
         AIF   ('&REL&MASK' EQ '').M088                       21085.
         MNOTE 8,' STRC2113 REL= OR MASK= NOT IN PARENTHESES--IGNORED'
.M088    ANOP
&LABEL   SETC  '&USRNAME'                                     21089.
.* CALL DO_SCAN_OPERANDS                                      21090.
.* CALL DO_FIND_KEYWORDS_AND_PRESENCE                         21137.
&WHX     SETA  0                                              21166.
&UNX     SETA  0                                              21166.
         AIF   ('&SYSLIST(1)' EQ 'WHILE' OR '&SYSLIST(1)' EQ 'UNTIL'   *
               OR '&SYSLIST(1)' EQ 'FOREVER'                           *
               OR '&SYSLIST(1)' EQ '').M171                   21167.
         MNOTE 8,' STRC2108 FIRST OPERAND MUST BE "WHILE", "UNTIL",'
         MNOTE 0,'          "FOREVER", OR OMITTED'            21169.
.M171    ANOP
&I       SETA  1                                              21173.
&LASTOP  SETA  N'&SYSLIST                                     21174.
.M175    AIF   (&I GT &LASTOP).M202                           21175.
         AIF   ('&SYSLIST(&I)' NE 'WHILE').M186               21177.
         AIF   (&WHX NE 0).M182                               21179.
&WHX     SETA  &I                                             21181.
         AGO   .M185                                          21182.
.M182    MNOTE 8,' STRC2101 OPERANDS AFTER SECOND "WHILE" IGNORED'
&LASTOP  SETA  &I-1                                           21184.
.M185    AGO   .M197                                          21185.
.M186    AIF   ('&SYSLIST(&I)' NE 'UNTIL').M196               21187.
         AIF   (&UNX NE 0).M192                               21189.
&UNX     SETA  &I                                             21191.
         AGO   .M195                                          21192.
.M192    MNOTE 8,' STRC2102 OPERANDS AFTER SECOND "UNTIL" IGNORED'
&LASTOP  SETA  &I-1                                           21194.
.M195    ANOP
.M196    ANOP
.M197    ANOP
&I       SETA  &I+1                                           21198.
         AGO   .M175                                          21199.
.M202    AIF   (&WHX LE 1 OR &UNX LE 1).M205                  21202.
         MNOTE 8,' STRC2114 SUPERFLUOUS LOOPING GROUP IGNORED'
.M205    ANOP
&UNPRES  SETB  (&UNX GT 0)                                    21214.
&WHPRES SETB (&WHX GT 0 OR &UNX GT 1 OR (NOT &UNPRES AND &LASTOP GT 0))
.* CORP DO_FIND_KEYWORDS_AND_PRESENCE                         21220.
.* CALL DO_FIND_END_INDEXES_AND_MAIN_OP                       21142.
&MAINOP  SETC  ''                                             21230.
&WHXEND  SETA  &LASTOP                                        21231.
&UNXEND  SETA  &LASTOP                                        21231.
         AIF   (&LASTOP NE 1).M235                            21232.
         AIF   ('&SYSLIST(1)' NE 'FOREVER').M235              21232.
&WHPRES  SETB  0                                              21234.
&UNPRES  SETB  0                                              21234.
         AGO   .M299                                          21235.
.M235    AIF   (NOT &WHPRES).M279                             21236.
         AIF   (NOT &UNPRES).M273                             21238.
&I       SETA  &WHX-1                                         21240.
         AIF   (&UNX GE &WHX).M248                            21241.
&UNXEND  SETA  &I-1                                           21243.
         AIF ('&SYSLIST(&I)' EQ 'AND' OR '&SYSLIST(&I)' EQ 'OR').M247
&UNXEND  SETA  &I                                             21246.
.M247    AGO   .M255                                          21247.
.M248    ANOP
&I       SETA  &UNX-1                                         21249.
&WHXEND  SETA  &I-1                                           21250.
         AIF ('&SYSLIST(&I)' EQ 'AND' OR '&SYSLIST(&I)' EQ 'OR').M254
&WHXEND  SETA  &I                                             21253.
.M254    ANOP
.M255    AIF   (&WHX NE &WHXEND).M264                         21256.
         MNOTE 8,' STRC2109 WHILE TEST IS VOID--IGNORED'      21262.
&WHPRES  SETB  0                                              21263.
         AGO   .M272                                          21264.
.M264    AIF   (&UNX NE &UNXEND).M272                         21265.
         MNOTE 8,' STRC2111 UNTIL TEST IS VOID--IGNORED'      21269.
&UNPRES  SETB  0                                              21270.
.M272    AGO   .M278                                          21272.
.M273    AIF   (&WHX NE &WHXEND).M278                         21274.
&WHPRES  SETB  0                                              21276.
.M278    AGO   .M284                                          21278.
.M279    AIF   (NOT (&UNPRES AND &UNX EQ &UNXEND)).M284       21280.
&UNPRES  SETB  0                                              21282.
.M284    AIF   (NOT (&WHPRES AND &UNPRES)).M298               21285.
&MAINOP  SETC  'AND'                                          21287.
         AIF   ('&SYSLIST(&I)' NE 'OR').M291                  21288.
&MAINOP  SETC  'OR'                                           21290.
         AGO   .M298                                          21291.
.M291    AIF   ('&SYSLIST(&I)' EQ 'AND').M298                 21292.
         MNOTE 8,' STRC2110 LOGIC OPERATOR BETWEEN "WHILE" AND "UNTIL"'
         MNOTE 0,'          OMITTED--"AND" ASSUMED'           21294.
.M298    ANOP
.M299    ANOP
.* CORP DO_FIND_END_INDEXES_AND_MAIN_OP                       21300.
.* CALL DO_LOOPING_BRANCH_AND_FIRST_OPERAND                   21147.
&LB      SETA  0                                              21309.
&LBTYPE  SETC  'NONE'                                         21310.
         AIF   (NOT &UNPRES).M326                             21311.
&I       SETA  &UNX+1                                         21313.
&UNX     SETA  &I                                             21313.
&X       SETC  '&SYSLIST(&I,1)'                               21316.
       AIF ('&X' NE 'BCT' AND '&X' NE 'BXLE' AND '&X' NE 'BXH').M323
&LBTYPE  SETC  'UNTIL'                                        21318.
&UNCTEST SETB  (&UNXEND GT &I)                                21319.
&LB      SETA  &I                                             21320.
         AGO   .M325                                          21323.
.M323    ANOP
&UNCTEST SETB  1                                              21324.
.M325    AGO   .M330                                          21326.
.M326    ANOP
&UNX     SETA  0                                              21327.
&UNXEND  SETA  0                                              21327.
&UNCTEST SETB  0                                              21328.
.M330    AIF   (NOT &WHPRES).M365                             21331.
&I       SETA  &WHX+1                                         21333.
&WHX     SETA  &I                                             21333.
&X       SETC  '&SYSLIST(&I,1)'                               21336.
       AIF ('&X' NE 'BCT' AND '&X' NE 'BXLE' AND '&X' NE 'BXH').M362
         AIF   ('&LBTYPE' NE 'NONE').M355                     21338.
&LBTYPE  SETC  'WHILE'                                        21340.
&WHCTEST SETB  (&WHXEND GT &I)                                21341.
&LB      SETA  &I                                             21342.
         AIF   ('&X' NE 'BCT').M349                           21345.
         MNOTE 4,' STRC2103 WARNING--"WHILE,(BCT,..." WILL LOOP ONE'
         MNOTE 0,'          LESS TIME THAN VALUE IN REGISTER' 21347.
.M349    AIF   ('&MAINOP' NE 'OR').M354                       21350.
         MNOTE 4,' STRC2104 WARNING--LOOPING BRANCH MAY NOT BE'
         MNOTE 0,'          EXECUTED ON EVERY ITERATION'      21352.
.M354    AGO   .M361                                          21354.
.M355    MNOTE 8,' STRC2105 TWO LOOPING BRANCHES INVALID IN "DO"--'
         MNOTE 0,'          "WHILE" IGNORED'                  21357.
&WHPRES  SETB  0                                              21358.
&WHCTEST SETB  0                                              21358.
&MAINOP  SETC  ''                                             21359.
&WHX     SETA  0                                              21360.
&WHXEND  SETA  0                                              21360.
.M361    AGO   .M364                                          21361.
.M362    ANOP
&WHCTEST SETB  1                                              21363.
.M364    AGO   .M368                                          21365.
.M365    ANOP
&WHX     SETA  0                                              21366.
&WHXEND  SETA  0                                              21366.
&WHCTEST SETB  0                                              21367.
.M368    ANOP
.* CALL DO_LOOPING_BRANCH_PROCESS                             21369.
&LBOPID  SETC  ''                                             21387.
&LBOP1   SETC  ''                                             21387.
&LBOP2   SETC  ''                                             21387.
&LBLOGIC SETC  ''                                             21387.
         AIF   (&LB EQ 0).M432                                21389.
&LBOPID  SETC  '&SYSLIST(&LB,1)'                              21391.
         AIF   ('&LBOPID' NE 'BCT').M395                      21392.
&OPCOUNT SETA  2                                              21394.
         AGO   .M397                                          21395.
.M395    ANOP
&OPCOUNT SETA  3                                              21396.
.M397    AIF   (N'&SYSLIST(&LB) NE 1).M401                    21398.
         MNOTE 8,' STRC2112 PARENTHESES OMITTED AROUND &SYSLIST(&LB)'
         AGO   .M432                                          21401.
.M401    ANOP
&LBOP1   SETC  '&SYSLIST(&LB,2)'                              21402.
&LBOP2   SETC  '&SYSLIST(&LB,3)'                              21403.
         AIF   (N'&SYSLIST(&LB) EQ &OPCOUNT).M407             21404.
         MNOTE 8,' STRC2106 INVALID NUMBER OF OPERANDS FOR &LBOPID'
.M407    ANOP
&LB      SETA  &LB+1                                          21408.
         AIF   ('&LBTYPE' EQ 'WHILE' AND &WHCTEST).M413       21411.
         AIF   (NOT ('&LBTYPE' EQ 'UNTIL' AND &UNCTEST)).M432
.M413    ANOP
&LBLOGIC SETC  '&SYSLIST(&LB)'                                21414.
         AIF   ('&LBLOGIC' NE 'AND' AND '&LBLOGIC' NE 'OR').M418
&LB      SETA  &LB+1                                          21417.
         AGO   .M422                                          21418.
.M418    MNOTE 8,' STRC2107 &LBLOGIC INVALID AFTER LOOPING BRANCH--'
         MNOTE 0,'          "AND" INSERTED'                   21419.
&LBLOGIC SETC  'AND'                                          21421.
.M422    AIF   ('&LBTYPE' NE 'WHILE').M426                    21423.
&WHX     SETA  &LB                                            21425.
         AGO   .M432                                          21426.
.M426    ANOP
&UNX     SETA  &LB                                            21427.
.M432    ANOP
&LBLABRQ SETB  ('&LBTYPE' EQ 'WHILE')                         21433.
.* CORP DO_LOOPING_BRANCH_PROCESS                             21434.
.* CORP DO_LOOPING_BRANCH_AND_FIRST_OPERAND                   21372.
.* CALL DO_SET_FORMAT                                         21153.
.*   SOME CODE FROM DO_INFO_SAVE HAS BEEN MOVED HERE TO SIMPLIFY
.*   SETTING &B8, &B10, AND &B11.
&B8      SETC  'W'                                            21780.
&B10     SETC  '0'                                            21781.
&B11     SETC  '0'                                            21781.
         AIF   (NOT &WHPRES).M535                             21440.
         AIF   ('&LBTYPE' NE 'WHILE').M489                    21442.
&B8      SETC  'U'                                            21789.
         AIF   (NOT &WHCTEST).M472                            21444.
         AIF   ('&LBLOGIC' NE 'AND').M459                     21446.
&B10     SETC  'W'                                            21805.
&B11     SETC  'N'                                            21818.
         AIF   (NOT &UNPRES).M456                             21448.
         AIF   ('&MAINOP' NE 'AND').M453                      21450.
&OPF     SETA  15                                             21452.
         AGO   .ENDSET
.M453    ANOP
&OPF     SETA  16                                             21454.
         AGO   .ENDSET
.M456    ANOP
&OPF     SETA  13                                             21457.
&B8      SETC  'L'                                            21793.
         AGO   .ENDSET
.M459    ANOP
&B10     SETC  'B'                                            21808.
&B11     SETC  'W'                                            21821.
         AIF   (NOT &UNPRES).M468                             21460.
         AIF   ('&MAINOP' NE 'AND').M465                      21462.
&OPF     SETA  17                                             21464.
         AGO   .ENDSET
.M465    ANOP
&OPF     SETA  18                                             21466.
         AGO   .ENDSET
.M468    ANOP
&OPF     SETA  14                                             21469.
&B8      SETC  'L'                                            21793.
         AGO   .ENDSET
.M472    ANOP
&B10     SETC  'B'                                            21808.
&B11     SETC  'N'                                            21818.
         AIF   (NOT &UNPRES).M481                             21473.
         AIF   ('&MAINOP' NE 'AND').M478                      21475.
&OPF     SETA  11                                             21477.
         AGO   .ENDSET
.M478    ANOP
&OPF     SETA  12                                             21479.
         AGO   .ENDSET
.M481    ANOP
&OPF     SETA  10                                             21482.
&B8      SETC  'L'                                            21793.
         AGO   .ENDSET
.M489    AIF   ('&LBTYPE' NE 'UNTIL').M518                    21490.
&B8      SETC  'L'                                            21793.
         AIF   (NOT &UNCTEST).M510                            21492.
         AIF   ('&LBLOGIC' NE 'AND').M502                     21494.
&B11     SETC  'U'                                            21811.
         AIF   ('&MAINOP' NE 'AND').M499                      21496.
&OPF     SETA  7                                              21498.
&B10     SETC  'W'                                            21818.
         AGO   .ENDSET
.M499    ANOP
&OPF     SETA  9                                              21500.
&B10     SETC  'B'                                            21821.
         AGO   .ENDSET
.M502    ANOP
&B10     SETC  'U'                                            21824.
         AIF   ('&MAINOP' NE 'AND').M506                      21503.
&OPF     SETA  6                                              21505.
&B11     SETC  'N'                                            21805.
         AGO   .ENDSET
.M506    ANOP
&OPF     SETA  8                                              21507.
&B11     SETC  'W'                                            21808.
         AGO   .ENDSET
.M510    AIF   ('&MAINOP' NE 'AND').M514                      21511.
&OPF     SETA  4                                              21513.
&B10     SETC  'W'                                            21805.
&B11     SETC  'N'                                            21818.
         AGO   .ENDSET
.M514    ANOP
&OPF     SETA  5                                              21515.
&B10     SETC  'B'                                            21808.
&B11     SETC  'W'                                            21821.
         AGO   .ENDSET
.M518    AIF   (NOT &UNPRES).M527                             21519.
&B8      SETC  'U'                                            21789.
         AIF   ('&MAINOP' NE 'AND').M524                      21521.
&OPF     SETA  2                                              21523.
         AGO   .ENDSET
.M524    ANOP
&OPF     SETA  3                                              21525.
         AGO   .ENDSET
.M527    ANOP
&OPF     SETA  1                                              21528.
         AGO   .ENDSET
.M535    AIF   (NOT &UNPRES).M554                             21536.
         AIF   ('&LBTYPE' NE 'UNTIL').M551                    21538.
&B8      SETC  'L'                                            21793.
         AIF   (NOT &UNCTEST).M548                            21540.
         AIF   ('&LBLOGIC' NE 'AND').M545                     21542.
&OPF     SETA  22                                             21544.
&B10     SETC  'B'                                            21811.
&B11     SETC  'U'                                            21818.
         AGO   .ENDSET
.M545    ANOP
&OPF     SETA  21                                             21546.
&B10     SETC  'U'                                            21808.
&B11     SETC  'N'                                            21824.
         AGO   .ENDSET
.M548    ANOP
&OPF     SETA  19                                             21549.
&B10     SETC  'B'                                            21808.
&B11     SETC  'N'                                            21818.
         AGO   .ENDSET
.M551    ANOP
&OPF     SETA  20                                             21552.
&B8      SETC  'U'                                            21789.
         AGO   .ENDSET
.M554    ANOP
&OPF     SETA  0                                              21555.
&B8      SETC  'B'                                            21796.
.ENDSET  ANOP
.* CORP DO_SET_FORMAT                                         21558.
.* CORP DO_SCAN_OPERANDS                                      21158.
   AIF   (&OPF EQ 0 OR &OPF EQ 10 OR &OPF EQ 12 OR &OPF EQ 19).M098
&STRCEND(&STRCNST)  SETB  1                                   21097.
.M098    AIF   (NOT &STRCDBC).M104                            21099.
&LABEL   SR    1,1
&LABEL   SETC  ''                                             21102.
         STH   1,&STRCPFX.DOL
.M104    AIF   (&OPF EQ 0).M113                               21105.
.* CALL DO_BRANCH_FOR_LOOP_ENTRY                              21107.
         AIF   (&OPF GT 9).M571                               21566.
         AIF   (&OPF LT 2).M581                               21566.
         AIF   (&OPF GT 3 AND &OPF LT 6).M581                 21566.
&LABEL   B     &STRCPFX.W1
         AGO   .CLEARLB
.M571    AIF   (&OPF GT 18).M576                              21571.
&LABEL   B     &STRCPFX.LPB
         AGO   .CLEARLB
.M576    AIF   (&OPF EQ 19).M581                              21576.
&LABEL   B     &STRCPFX.BEG
.CLEARLB ANOP
&LABEL   SETC  ''
.M581    ANOP
.* CORP DO_BRANCH_FOR_LOOP_ENTRY                              21582.
.* CALL DO_GENERATE_ALL_CONDITIONAL_TESTS                     21110.
&PASS    SETA  1                                              21589.
.M590    AIF   (&PASS GT 2).M634                              21590.
         AIF   (&PASS NE 1).M595                              21592.
.* CALL DO_UNTIL_PREPROCESS                                   21594.
&CONDREQ SETB  (&UNCTEST)                                     21643.
         AIF   (NOT &UNCTEST).M672                            21644.
&STRCFRX SETA  &UNX                                           21646.
&STRCLSX SETA  &UNXEND                                        21647.
&STRCUBL SETC  '&STRCPFX.END'                                 21649.
&STRCUFL SETC  '&STRCPFX.W1'                                  21650.
&STRCUFC SETB  0                                              21652.
&STRCUID SETC  'U'                                            21653.
&FIRSTID SETC  '&STRCPFX.U1'                                  21654.
         AIF   (&OPF NE 3 AND &OPF NE 8 AND &OPF NE 9).M661   21657.
&STRCUFC SETB  1                                              21658.
&STRCUBL SETC  '&STRCPFX.BEG'                                 21659.
         AGO   .M671                                          21660.
.M661    AIF   (&OPF NE 11 AND &OPF NE 15 AND &OPF NE 17).M664
&STRCUFL SETC  '&STRCPFX.LPB'                                 21662.
         AGO   .M671                                          21663.
.M664    AIF   (&OPF NE 12 AND &OPF NE 16 AND &OPF NE 18).M668
&STRCUBL SETC  '&STRCPFX.LPB'                                 21665.
&STRCUFL SETC  '&STRCPFX.BEG'                                 21666.
         AGO   .M671                                          21667.
.M668    AIF   (&OPF LT 20).M671                              21664 .
&STRCUFL SETC  '&STRCPFX.BEG'                                 21669.
.M671    ANOP
.M672    ANOP
.* CORP DO_UNTIL_PREPROCESS                                   21673.
         AGO   .M597                                          21595.
.M595    ANOP
.* CALL DO_WHILE_PREPROCESS                                   21596.
&CONDREQ SETB  (&WHCTEST)                                     21680.
         AIF   (NOT &WHCTEST).M692                            21681.
&STRCFRX SETA  &WHX                                           21683.
&STRCLSX SETA  &WHXEND                                        21684.
&STRCUBL SETC  '&STRCPFX.END'                                 21685.
&STRCUFL SETC  '&STRCPFX.BEG'                                 21687.
&STRCUFC SETB  1                                              21689.
&STRCUID SETC  'W'                                            21690.
&FIRSTID SETC  '&STRCPFX.W1'                                  21691.
.M692    ANOP
.* CORP DO_WHILE_PREPROCESS                                   21693.
.M597    ANOP
         AIF   (NOT &STRCDDM).M608                            DEBUGMACS
         MNOTE *,' STRC2199 CONDREQ=&CONDREQ,FRX=&STRCFRX,LSX=&STRCLSX,*
               UBL=&STRCUBL,UFL=&STRCUFL'                     DEBUGMACS
.M608    AIF   (NOT &CONDREQ).M632                            21608.
.* CALL DO_GENERATE_CONDITIONAL_SET                           21610.
&STRCFLU SETB  0                                              21704.
         AIF   ('&LABEL' EQ '').M718                          21705.
         AIF   ('&FIRSTID' NE '').M712                        21707.
&FIRSTID SETC  '&LABEL'                                       21711.
         AGO   .M716                                          21712.
.M712    ANOP
&LABEL   DS    0H
.M716    ANOP
&LABEL   SETC  ''                                             21717.
.M718    ANOP
&FIRSTID STRCCEP  &SYSLIST(1),&SYSLIST(2),&SYSLIST(3),&SYSLIST(4),     *
               &SYSLIST(5),&SYSLIST(6),&SYSLIST(7),&SYSLIST(8),        *
               &SYSLIST(9),&SYSLIST(10),&SYSLIST(11),&SYSLIST(12),     *
               &SYSLIST(13),&SYSLIST(14),&SYSLIST(15),&SYSLIST(16),    *
               &SYSLIST(17),&SYSLIST(18),&SYSLIST(19),&SYSLIST(20),    *
               &SYSLIST(21),&SYSLIST(22),&SYSLIST(23),&SYSLIST(24),    *
               &SYSLIST(25),&SYSLIST(26),&SYSLIST(27),&SYSLIST(28),    *
               &SYSLIST(29),&SYSLIST(30),&SYSLIST(31),&SYSLIST(32),    *
               &SYSLIST(33),&SYSLIST(34),&SYSLIST(35),&SYSLIST(36),    *
               &SYSLIST(37),&SYSLIST(38),&SYSLIST(39),&SYSLIST(40),    *
               &SYSLIST(41),&SYSLIST(42),&SYSLIST(43),&SYSLIST(44),    *
               &SYSLIST(45),&SYSLIST(46),&SYSLIST(47),&SYSLIST(48),    *
               &SYSLIST(49),&SYSLIST(50)                      21719.
.* CORP DO_GENERATE_CONDITIONAL_SET                           21736.
         AIF   (&PASS NE 1).M626                              21619.
.* CALL DO_UNTIL_POSTPROCESS                                  21622.
         AIF   (&OPF EQ 11).M748                              21747.
         AIF   (&OPF LT 15 OR &OPF GT 18).M751                21747.
.M748    B     &STRCUFL
.M751    ANOP
.* CORP DO_UNTIL_POSTPROCESS                                  21752.
.M626    AIF   (NOT &STRCFLU).M631                            21627.
&LABEL   SETC  '&STRCUFL'                                     21629.
.M631    ANOP
.M632    ANOP
&PASS    SETA  &PASS+1                                        21633.
         AGO   .M590                                          21634.
.M634    ANOP
.* CORP DO_GENERATE_ALL_CONDITIONAL_TESTS                     21636.
.M113    ANOP
.* CALL DO_LABEL_BLOCK                                        21114.
         AIF   (&OPF EQ 0 OR &OPF EQ 3 OR &OPF EQ 5).M760     21759.
         AIF   (&OPF LT 8 OR &OPF EQ 13 OR &OPF EQ 15).M770   21759.
.M760    AIF   ('&LABEL' EQ '&STRCPFX.BEG').M770              21760.
         AIF   ('&LABEL' EQ '').M767                          21763.
&LABEL   DS    0H
.M767    ANOP
&LABEL   SETC  '&STRCPFX.BEG'                                 21767.
.M770    ANOP
.* CORP DO_LABEL_BLOCK                                        21771.
.* CALL DO_INFO_SAVE                                          21116.
.*   LINES 21780. THROUGH 21827. SETTING &B8, &B10, AND &B11 DEPENDING
.*   ON &OPF MOVED TO DO_SET_FORMAT.
&STRCINF(&STRCNST)  SETC  '&B8&B10&B11&LBLABRQ.0000'          21828.
&STRCOP1(&STRCNST)  SETC  '&LBOP1'                            21835.
&STRCOP2(&STRCNST)  SETC  '&LBOP2'                            21836.
&STRCOP3(&STRCNST)  SETC  '&LBOPID'                           21837.
.* CORP DO_INFO_SAVE                                          21838.
.* CALL DO_TRACE_COUNTERS                                     21119.
         AIF   (NOT (&STRCDBC OR &STRCDBN)).M869              21844.
         AIF   (NOT &STRCDBC).M855                            21846.
&LABEL   LH    1,&STRCPFX.DOL
&LABEL   SETC  ''                                             21849.
         LA    1,1(1)
         STH   1,&STRCPFX.DOL
         LH    1,&STRCPFX.DTR
         LA    1,1(1)
         STH   1,&STRCPFX.DTR
.M855    ANOP
&LABEL   B     &STRCPFX.GO
&LABEL   SETC  '&STRCPFX.GO'                                  21858.
         AIF   (NOT &STRCDBN).M864                            21859.
         DC    C'&STRCNAM(&STRCNST)',0H'0'
.M864    AIF   (NOT &STRCDBC).M868                            21864.
&STRCPFX.DOL  DC  H'0' CURRENT LOOP COUNT
&STRCPFX.DTR  DC  H'0' OVERALL LOOP COUNT
.M868    ANOP
.M869    ANOP
.* CORP DO_TRACE_COUNTERS                                     21870.
         AIF   ('&LABEL' EQ '').M124                          21121.
&LABEL   DS    0H
.M124    ANOP
.FIN     MEND
