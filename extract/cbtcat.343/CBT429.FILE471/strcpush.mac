         MACRO
&NAMEVAL STRCPUSH  &TYPEVAL=,&OP1VAL=,&OP2VAL=,&OP3VAL=,      93001.   *
               &OP4VAL=,&INFVAL=,&ENDVAL=0                    93001.
         COPY  STRCGBLS
         STRCTRAC  STRCPUSH                                   93013.
&STRCNSL SETA  100                                            93015.
&STRCGNX SETA  9                                              39016.1
&STRCERR SETB  0                                              93021.
&STRCNST SETA  &STRCNST+1                                     93022.
         AIF   (&STRCNST LE &STRCNSL).M028                    93023.
         MNOTE 12,' STRC9301 BLOCK NESTING LIMIT OF &STRCNSL EXCEEDED'
         MNOTE 0,'           MACROS MUST BE MODIFIED'         93025.
&STRCERR SETB  1                                              93027.
         AGO   .M059                                          93028.
.M028    ANOP
&STRCLB# SETA  &STRCLB#+1                                     93029.
&STRCBL#(&STRCNST)  SETA  &STRCLB#                            93031.
&STRCXIT(&STRCNST)  SETB  0                                   93032.
&STRCTYP(&STRCNST)  SETC  '&TYPEVAL'                          93033.
&STRCOP1(&STRCNST)  SETC  '&OP1VAL'                           93034.
&STRCOP2(&STRCNST)  SETC  '&OP2VAL'                           93035.
&STRCOP3(&STRCNST)  SETC  '&OP3VAL'                           93036.
&STRCOP4(&STRCNST)  SETC  '&OP4VAL'                           93037.
&STRCINF(&STRCNST)  SETC  '&INFVAL'                           93038.
&STRCEND(&STRCNST)  SETB  ('&ENDVAL' EQ '1')                  93039.
&STRCNAM(&STRCNST)  SETC  '&NAMEVAL'                          93040.
         AIF   ('&NAMEVAL' NE '').M044                        93041.
&STRCNAM(&STRCNST)  SETC  'BLK&STRCLB#'                       93043.
.M044    ANOP
&STRCPFX SETC  '$&STRCLB#'                                    93045.
         AIF   (&STRCNST LE 1).M52                            93046.
         AIF   ('&STRCTYP(&STRCNST-1)' NE 'DOCASE' OR                  *
               '&TYPEVAL' EQ 'CASE').M52                      93046.
         MNOTE 8,' STRC9302 NON-CASE BLOCK IMMEDIATELY SURROUNDED'
         MNOTE 0,'                BY DOCASE INVALID'
.M52     AIF   (NOT &STRCDLB).M058                            93053.
         MNOTE *,'****************************************************'
         MNOTE *,' STRC9302 START OF BLOCK &STRCLB#.(&STRCNAM(&STRCNST)*
               ) AT DEPTH &STRCNST'                           93056.
.M058    ANOP
.M059    ANOP
         MEND
