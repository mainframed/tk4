         MACRO
&USRNAME PROC  &LINKAGE=,&ID=,&BASE=,&WORK=,&SAVE=,&DEBUG=,   81001.   *
               &EXIT=,&DSECT=
         COPY  STRCGBLS
         LCLB  &PROC1             FIRST_PROC                  81010.
         LCLB  &FRKNOWN           FIRST_VALUE_KNOWN           81012.
         LCLB  &OSLINK            OS_LINKAGE                  81014.
         LCLB  &SPECPFX           SPECIAL_PREFIX              81016.
         LCLB  &USING13           USING13                     81018.
         LCLB  &MULTIBS           MULTIBASE                   81019.
         LCLB  &WORKUSD           WORKREG_USED                81021.
         LCLC  &COMMA2            COMMA2                      81024.
         LCLC  &MULT              MULT                        81024.
         LCLC  &FIRST,&LAST       FIRST, LAST                 81027.
         LCLC  &LABEL             LABEL                       81028.
         LCLC  &LOCPTR            LOCAL_POINTER               81029.
         LCLC  &OSPTR             OS_POINTER                  81029.
         LCLC  &PVDEBUG           PREVIOUS_DEBUG_VECTOR       81031.
         LCLC  &IDBYTE            PROC_ID_BYTE                81033.
         LCLC  &SVLENG            SAVE_LENGTH                 81035.
         LCLC  &SVTYPE            SAVE_TYPE                   81037.
         LCLC  &SAVEREG           SAVEREG                     81039.
         LCLC  &WORKREG           WORKREG                     81041.
         LCLA  &OFFSET            OFFSET                      81043.
         LCLA  &OFFSET0           OFFSET_TO_GPR0              81045.
         LCLA  &SAF,&SAL          SAF, SAL                    81046.
         LCLB  &GLOBAL            GLOBAL                      81173.
         LCLB  &STVALUE           SAVETRACE_VALUE             81176.
         LCLB  &STCHECK           SAVETRACE_CHECK             81178.
         LCLA  &I                 I      81165. 81401. 81518. 81948.
.*                                       81890. 81595.1       81966.
         LCLC  &SECT              SECT                        81350.
         LCLC  &QUOTE             QUOTE                       81351.
         LCLC  &TARGET            TARGET                      81352.
         LCLA  &LENGTH            LENGTH                      81354.
         LCLB  &INLINE            INLINE_SAVEAREA             81614.
         LCLC  &BASEREG           BASEREG                     81617.
         LCLC  &X                 X                           81763.
         LCLC  &FWDPTR            FWD_PTR                     81836.
         LCLB  &PCTWVEC           PCT_GENNED_WITH_VECTOR      81867.
         LCLC  &SPOT              COUNT_SPOT                  81869.
         LCLA  &J                 J            81595.1 81890. 81966.
         LCLB  &BIT               MACRO TEMPORARY
         STRCTRAC  PROC                                       81051.
&USRNAME STRCPUSH  TYPEVAL=PROC                               81053.
         AIF   (&STRCERR).FIN                                 81057.
&LABEL   SETC  '&USRNAME'                                     81061.
.*       CALL  PROC_SCAN_OPTIONS                              81063.
&OSLINK  SETB  ('&LINKAGE(1)' NE '')                          81114.
         AIF   ('&LINKAGE(1)' EQ 'OS' OR '&LINKAGE(1)' EQ '').M119
         MNOTE 8,' STRC8101 LINKAGE=&LINKAGE(1) INVALID--"OS" ASSUMED'
.M119    AIF   ('&LINKAGE(2)' EQ 'CSECT' OR '&LINKAGE(2)' EQ '').M123
         MNOTE 8,' STRC8102 SECOND LINKAGE OPERAND IGNORED'   81122.
.M123    ANOP
&WORKREG SETC  '&WORK'                                        81124.
         AIF   ('&WORK' NE 'NONE' AND '&WORK' NE '').M135     81125.
         AIF   (&OSLINK).M131                                 81129.
&WORKREG SETC  '1'                                            81133.
         AGO   .M135                                          81134.
.M131    ANOP
&WORKREG SETC  '2'                                            81131.
.M135    AIF   ('&EXIT' EQ '').M1363                          81136.
&STRCXSV SETC  '&EXIT'                                        81136.2
.M1363   ANOP
&PROC1   SETB  (NOT &STRCNFP)                                 81138.
&STRCNFP SETB  1                                              81139.
.*       CALL  PROC_DEBUG_SET                                 81144.
&GLOBAL  SETB  0                                              81182.
&PVDEBUG SETC '&STRCDBN&STRCDPN&STRCDLB&STRCDBC&STRCDPC&STRCDPT&STRCDCV*
               &STRCDST'                                      81183.
&I       SETA  1                                              81193.
.M194    AIF   (&I GT N'&DEBUG).M307                          81194.
&X       SETC  '&DEBUG(&I)'                                   81196.
         AIF   ('&X' NE 'GLOBAL' AND '&X' NE 'GBL').M201      81198.
&GLOBAL  SETB  1                                              81199.
         AGO   .M305                                          81200.
.M201    AIF   ('&X'(1,1) EQ 'N').SETOFF                      81201.
         AIF   ('&X' NE 'BLOCKNAM' AND '&X' NE 'BN').M210     81201.
&STRCDBN SETB  1                                              81202.
         AGO   .M305                                          81206.
.M210    AIF   ('&X' NE 'PROCNAME' AND '&X' NE 'PN').M219     81210.
&STRCDPN SETB  1                                              81211.
         AGO   .M305                                          81215.
.M219    AIF   ('&X' NE 'LISTBLOC' AND '&X' NE 'LB').M227     81219.
&STRCDLB SETB  1                                              81220.
         AGO   .M305                                          81223.
.M227    AIF   ('&X' NE 'BLOCKCOU' AND '&X' NE 'BC').M235     81227.
&STRCDBC SETB  1                                              81228.
         AGO   .M305                                          81231.
.M235    AIF   ('&X' NE 'PROCCOUN' AND '&X' NE 'PC').M244     81235.
&STRCDPC SETB  1                                              81236.
         AGO   .M305                                          81243.
.M244    AIF   ('&X' NE 'PROCTRAC' AND '&X' NE 'PT').M253     81244.
&STRCDPT SETB  1                                              81245.
         AGO   .M305                                          81249.
.M253    AIF   ('&X' NE 'CORPVALU' AND '&X' NE 'CV').M262     81253.
&STRCDCV SETB  1                                              81254.
         AGO   .M305                                          81258.
.M262    AIF   ('&X' NE 'SAVETRAC' AND '&X' NE 'ST').M274     81262.
&STVALUE SETB  1                                              81266.
&STCHECK SETB  1                                              81267.
         AGO   .M305                                          81269.
.M274    AIF   ('&X' NE 'ALL').M286                           81274.
&STRCDBN SETB  1                                       "ALL"  81275.
&STRCDPN SETB  1                                              81275.
&STRCDLB SETB  1                                              81275.
&STRCDBC SETB  1                                              81275.
&STRCDPC SETB  1                                              81275.
&STRCDPT SETB  1                                              81275.
&STRCDCV SETB  1                                              81275.
&STVALUE SETB  1                                              81275.
&STCHECK SETB  1                                              81280.
         AGO   .M305                                          81281.
.M286    AIF   ('&X' NE 'MACRONAM' AND '&X' NE 'MN').M294     81286.
&STRCDMN SETB  1                                              81287.
         AGO   .M305                                          81290.
.M294    AIF   ('&X' NE 'DEBUGMAC' AND '&X' NE 'DM').M302     81294.
&STRCDDM SETB  1                                              81295.
         AGO   .M305                                          81298.
.SETOFF  AIF   ('&X' NE 'NOBLOCKN' AND '&X' NE 'NBN').M216    81207.
&STRCDBN SETB  0                                              81208.
         AGO   .M305                                          81209.
.M216    AIF   ('&X' NE 'NOPROCNA' AND '&X' NE 'NPN').M224    81216.
&STRCDPN SETB  0                                              81217.
         AGO   .M305                                          81218.
.M224    AIF   ('&X' NE 'NOLISTBL' AND '&X' NE 'NLB').M232    81224.
&STRCDLB SETB  0                                              81225.
         AGO   .M305                                          81226.
.M232    AIF   ('&X' NE 'NOBLOCKC' AND '&X' NE 'NBC').M240    81232.
&STRCDBC SETB  0                                              81233.
         AGO   .M305                                          81234.
.M240    AIF   ('&X' NE 'NOPROCCO' AND '&X' NE 'NPC').M250    81240.
&STRCDPC SETB  0                                              81241.
         AGO   .M305                                          81242.
.M250    AIF   ('&X' NE 'NOPROCTR' AND '&X' NE 'NPT').M259    81250.
&STRCDPT SETB  0                                              81251.
         AGO   .M305                                          81252.
.M259    AIF   ('&X' NE 'NOCORPVA' AND '&X' NE 'NCV').M270    81259.
&STRCDCV SETB  0                                              81260.
         AGO   .M305                                          81261.
.M270    AIF   ('&X' NE 'NOSAVETR' AND '&X' NE 'NST').M274N N 81270.
&STVALUE SETB  0                                              81271.
&STCHECK SETB  1                                              81272.
         AGO   .M305                                          81273.
.M274N   AIF   ('&X' NE 'NONE').M291                  "NONE"  81274.
&STRCDBN SETB  0                                      "NONE"  81275.
&STRCDPN SETB  0                                              81275.
&STRCDLB SETB  0                                              81275.
&STRCDBC SETB  0                                              81275.
&STRCDPC SETB  0                                              81275.
&STRCDPT SETB  0                                              81275.
&STRCDCV SETB  0                                              81275.
&STVALUE SETB  0                                              81275.
&STCHECK SETB  1                                              81280.
         AGO   .M305                                          81281.
.M291    AIF   ('&X' NE 'NOMACRON' AND '&X' NE 'NMN').M299    81291.
&STRCDMN SETB  0                                              81292.
         AGO   .M305                                          81293.
.M299    AIF   ('&X' NE 'NODEBUGM' AND '&X' NE 'NDM').M302    81299.
&STRCDDM SETB  0                                              81300.
         AGO   .M305                                          81301.
.M302    MNOTE 8,' STRC8104 DEBUG=&DEBUG(&I) INVALID--IGNORED'
.M305    ANOP
&I       SETA  &I+1                                           81306.
         AGO   .M194                                          81307.
.M307    AIF   (NOT &STCHECK).M335                            81310.
         AIF   (&STVALUE).M313                                81312.
&STRCDST SETB  0                                              81333.
         AGO   .M335                                          81334.
.M313    AIF   (NOT &PROC1).M324                              81314.
         AIF   (NOT &OSLINK).M321                             81316.
&STRCSFP SETB  1                                              81318.
&STRCDST SETB  1                                              81319.
         MNOTE 4,' STRC8103 WARNING--SAVETRACE REQUIRES "FINAL" MACRO'
         AGO   .M335                                          81321.
.M321    MNOTE 8,' STRC8106 SAVETRACE REQUIRES FIRST PROC TO BE'
         MNOTE 0,'          LINKAGE=OS'                       81322.
         AGO   .M335                                          81324.
.M324    AIF   (&STRCSFP).M326                                81325.
         MNOTE 8,' STRC8105 SAVETRACE MUST BE SPECIFIED ON FIRST PROC'
         AGO   .M335                                          81330.
.M326    ANOP
&STRCDST SETB  1                                              81327.
.M335    AIF   (NOT &GLOBAL).M340                             81336.
&PVDEBUG SETC  ''                                             81338.
.M340    ANOP
.*       CORP  PROC_DEBUG_SET                                 81341.
&SPECPFX SETB  ((&STRCDST OR &STRCDPT OR &STRCDPC) AND &STRCPCT LT 254)
         AIF   (NOT &SPECPFX).M162                            81151.
&STRCPCT SETA  &STRCPCT+1                                     81153.
&STRCHXI SETA  &STRCPCT                                       81155.
         STRCXHEX
&IDBYTE  SETC  '&STRCHXO'                                     81157.
&STRCPFX SETC  '$P&IDBYTE'                                    81158.
         MNOTE *,' STRC8108 PROC &STRCNAM(&STRCNST), DEBUG ID=X''&IDBYT*
               E'''                                           81160.
         AGO   .M164                                          81162.
.M162    ANOP
&IDBYTE  SETC  '00'                                           81163.
.M164    ANOP
.*       CORP  PROC_SCAN_OPTIONS                              81165.
.*       CALL  PROC_HEADER                                    81068.
         AIF   ('&LINKAGE(2)' NE 'CSECT').M361                81356.
&LABEL   CSECT
&LABEL   SETC  ''                                             81359.
         USING *,15
.M361    ANOP
         AIF   (NOT (((&OSLINK OR '&ID' NE '') AND                     *
               '&ID' NE 'NONE') OR &STRCDPN)).M413            81362.
&TARGET  SETC  '&STRCPFX.AA'                                  81365.
&LABEL   B     &TARGET
&LABEL   SETC  '&TARGET'                                      81368.
         AIF ('&ID' NE '' AND '&ID' NE '*' AND '&ID' NE 'NONE').M393
         AIF   ('&USRNAME' NE '').M380                        81371.
         AIF   (&OSLINK).M375                                 81373.
&SECT    SETC  '&STRCNAM(&STRCNST)'                           81377.
         AGO   .M378                                          81377.
.M375    ANOP
&SECT    SETC  '$PRIVATE'                                     81375.
.M378    ANOP
&LENGTH  SETA  8                                              81379.
         AGO   .M383                                          81380.
.M380    ANOP
&SECT    SETC  '&USRNAME'                                     81381.
&LENGTH  SETA  K'&USRNAME                                     81382.
.M383    AIF   (&OSLINK).M385                                 81384.
&LENGTH  SETA  ((&LENGTH+1)/2)*2                              81390.
         DC    CL&LENGTH'&SECT'
         AGO   .M392                                          81392.
.M385    ANOP
&LENGTH  SETA  ((&LENGTH/2)*2)+1                              81386.
         DC    AL1(&LENGTH),CL&LENGTH'&SECT'
.M392    AGO   .M412                                          81393.
.M393    ANOP
&QUOTE   SETC  ''''                                           81394.
&LENGTH  SETA  0                                              81395.
         AIF   ('&ID'(1,1) NE '''').M400                      81396.
&QUOTE   SETC  ''                                             81398.
&LENGTH  SETA  0-2                                            81399.
.M400    AIF   (&OSLINK).M403                                 81402.
&LENGTH  SETA  ((K'&ID+1)/2)*2+&LENGTH                        81408.
         DC    CL&LENGTH&QUOTE&ID&QUOTE
         AGO   .M411                                          81411.
.M403    ANOP
&LENGTH  SETA  ((K'&ID/2)*2)+1+&LENGTH                        81403.
         DC    AL1(&LENGTH),CL&LENGTH&QUOTE&ID&QUOTE
.M411    ANOP
.M412    ANOP
.M413    ANOP
.*       CORP  PROC_HEADER                                    81414.
.*       CALL  PROC_REG_SAVE                                  81071.
&FIRST   SETC  ''                                             81424.
&LAST    SETC  ''                                             81424.
&OFFSET  SETA  0                                              81425.
&OFFSET0 SETA  0                                              81425.
&SAF     SETA  0                                              81425.
&SAL     SETA  0                                              81425.
         AIF   ('&SAVE' NE 'NONE').M436                       81427.
         AIF   (NOT &STRCDST).M474                            81429.
&SVTYPE  SETC  'FULL'                                         81431.
&SVLENG  SETC  '15'                                           81432.
&LABEL   STM   14,12,&STRCPFX.SV+12
&LABEL   SETC  ''                                             81434.
         AGO   .M474                                          81436.
.M436    ANOP
.*       CALL  PROC_SET_SAVE_INFO                             81437.
&FRKNOWN SETB  1                                              81487.
&MULT    SETC  'M'                                            81488.
&COMMA2  SETC  ','                                            81489.
         AIF   (T'&SAVE(1) NE 'O').M496                       81491.
&FIRST   SETC  '14'                                           81493.
&SAF     SETA  14                                             81493.
&LAST    SETC  '12'                                           81494.
&SAL     SETA  12                                             81494.
         AGO   .M527                                          81496.
.M496    AIF   (T'&SAVE(1) NE 'N').M503                       81497.
&SAF     SETA  &SAVE(1)                                       81499.
&FIRST   SETC  '&SAF'                                         81500.
         AGO   .M507                                          81503.
.M503    ANOP
&FIRST   SETC  '&SAVE(1)'                                     81504.
&SAF     SETA  14                                             81505.
&FRKNOWN SETB  0                                              81506.
.M507    AIF   (T'&SAVE(2) NE 'O').M517                       81508.
&LAST    SETC  ''                                             81510.
&MULT    SETC  ''                                             81510.
&COMMA2  SETC  ''                                             81510.
         AIF   (&FRKNOWN).M512                                81511.
&SAL     SETA  12                                             81515.
         AGO   .M516                                          81516.
.M512    ANOP
&SAL     SETA  &SAF                                           81513.
.M516    AGO   .M526                                          81517.
.M517    AIF   (T'&SAVE(2) NE 'N').M522                       81518.
&SAL     SETA  &SAVE(2)                                       81520.
&LAST    SETC  '&SAL'                                         81521.
         AGO   .M526                                          81522.
.M522    ANOP
&LAST    SETC  '&SAVE(2)'                                     81523.
&SAL     SETA  12                                             81524.
.M526    ANOP
.M527    ANOP
.*       CORP  PROC_SET_SAVE_INFO                             81528.
.*       CALL  PROC_DECIDE_SAVE_TYPE                          81446.
         AIF   (NOT &OSLINK).M559                             81542.
&OSPTR   SETC  '(13)'                                         81544.
&LOCPTR  SETC  ''                                             81545.
         AIF   ('&SAVE(3)' NE 'NONE').M549                    81546.
&SVTYPE  SETC  'NONE'                                         81548.
         AGO   .M598                                          81549.
.M549    ANOP
&SVTYPE  SETC  'OSSAVE'                                       81550.
&OFFSET0 SETA  5                                              81551.
&OFFSET  SETA  5                                              81551.
         AIF   ('&SAVE(4)' NE '').M555                        81552.
&SVLENG  SETC  '18'                                           81554.
         AGO   .M598                                          81555.
.M555    ANOP
&SVLENG  SETC  '&SAVE(4)'                                     81556.
         AGO   .M598                                          81559.
.M559    ANOP
&OSPTR   SETC  ''                                             81560.
&LOCPTR  SETC  '&STRCPFX.SV+'                                 81561.
         AIF   (NOT &STRCDST).M568                            81562.
&SVTYPE  SETC  'FULL'                                         81564.
&SVLENG  SETC  '15'                                           81565.
&OFFSET  SETA  5                                              81567.
&OFFSET0 SETA  5                                              81567.
         AGO   .M598                                          81568.
.M568    ANOP
&I       SETA  &SAL-((&SAL+2)/16*16)-&SAF+((&SAF+2)/16*16)+1  81569.
&SVLENG  SETC  '&I'                                           81570.
         AIF   (&SAF NE 14).M582                              81572.
         AIF   (&STRCDPC).M576                                81574.
&SVTYPE  SETC  'NORML'                                        81579.
&OFFSET  SETA  2                                              81580.
&OFFSET0 SETA  2                                              81580.
         AGO   .M598                                          81581.
.M576    ANOP
&SVTYPE  SETC  'NORMLHDR'                                     81576.
&OFFSET  SETA  3                                              81577.
&OFFSET0 SETA  3                                              81577.
         AGO   .M598                                          81578.
.M582    AIF   (&STRCDPC).M584                                81583.
&SVTYPE  SETC  'TRUNC'                                        81588.
&OFFSET  SETA  0                                              81589.
         AGO   .M590                                          81590.
.M584    ANOP
&SVTYPE  SETC  'TRUNCHDR'                                     81585.
&OFFSET  SETA  1                                              81586.
.M590    ANOP
&OFFSET0 SETA  &OFFSET-&SAF                                   81591.
         AIF   (&SAF LE 13).M598                              81592.
&OFFSET0 SETA  &OFFSET0+16                                    81594.
.M598    AIF   (NOT &FRKNOWN OR '&SVTYPE'(1,5) EQ 'TRUNC').M608
&OFFSET  SETA  &OFFSET0+&SAF                                  81603.
         AIF   (&SAF LE 13).M608                              81604.
&OFFSET  SETA  &OFFSET-16                                     81606.
.M608    ANOP
.*       CORP  PROC_DECIDE_SAVE_TYPE                          81609.
         AIF   ('&SVTYPE' NE 'FULL').M461                     81457.
&LABEL   STM   14,12,&STRCPFX.SV+12
         AGO   .M472                                          81461.
.M461    AIF   (&FRKNOWN).M463                                81462.
&LABEL   ST&MULT  &FIRST,&LAST&COMMA2&LOCPTR.(&FIRST+&OFFSET-((&FIRST+2:
               )/16*16))*4&OSPTR
         AGO   .M471                                          81471.
.M463    ANOP
&I       SETA  &OFFSET*4                                      81464.
&LABEL   ST&MULT  &FIRST,&LAST&COMMA2&LOCPTR&I&OSPTR
.M471    ANOP
.M472    ANOP
&LABEL   SETC  ''                                             81473.
.M474    ANOP
.*       CORP  PROC_REG_SAVE                                  81475.
.*       CALL  PROC_ESTABLISH_BASE                            81074.
     AIF  ('&BASE' EQ 'NONE' OR NOT (&OSLINK OR '&BASE' NE '')).M696
&BASEREG SETC  '&BASE(1)'                                     81623.
&INLINE  SETB  ('&SAVE' NE 'NONE' AND '&SAVE(3)' EQ '')       81624.
         AIF   ('&BASEREG' NE '13' OR &INLINE OR &OSLINK).M630
         MNOTE 8,' STRC8109 REGISTER 13 INVALID--IGNORED'     81627.
&BASEREG SETC  ''                                             81628.
.M630    AIF   ('&BASEREG' NE '').M647                        81630.
         AIF   (NOT (&INLINE AND &OSLINK)).M644               81632.
&BASEREG SETC  '&WORKREG'                                     81634.
&USING13 SETB  1                                              81637.
         AIF   ('&WORK' NE 'NONE' OR &WORKUSD).M642           81638.
&LABEL   LR    0,&WORKREG
&LABEL   SETC  ''                                             81641.
.M642    ANOP
&WORKUSD SETB  1                                              81643.
         AGO   .M647                                          81644.
.M644    ANOP
&BASEREG SETC  '12'                                           81645.
.M647    ANOP
&J       SETA  0                                              81648.
&I       SETA  2                                              81649.
.M650    AIF   (&I GT N'&BASE).M662                           81650.
         AIF   ('&BASE(&I)' NE '13').M655                     81652.
         MNOTE 8,' STRC8109 REGISTER 13 INVALID--IGNORED'     81654.
         AGO   .M659                                          81655.
.M655    ANOP
&LABEL   L     &BASE(&I),&STRCPFX.MBR+&J
&LABEL   SETC  ''                                             81658.
.M659    ANOP
&I       SETA  &I+1                                           81660.
&J       SETA  &J+4                                           81661.
         AGO   .M650                                          81662.
.M662    AIF   (NOT &INLINE).M675                             81664.
         AIF   (NOT &OSLINK).M671                             81666.
         CNOP  0,4
.M671    ANOP
&TARGET  SETC  '&STRCPFX.BB'                                  81672.
&LABEL   BAL   &BASEREG,&TARGET
&LABEL   SETC  '&TARGET'                                      81674.
         AGO   .M678                                          81675.
.M675    ANOP
&LABEL   BALR  &BASEREG,0
&LABEL   SETC  ''                                             81677.
.M678    AIF   (&USING13).M682                                81679.
         USING *,&BASEREG
.M682    AIF   (N'&BASE LE 1).M696                            81683.
&STRCPFX.MBP  EQU  *
&MULTIBS SETB  1                                              81686.
&J       SETA  4096                                           81687.
&I       SETA  2                                              81688.
.M689    AIF   (&I GT N'&BASE).M696                           81689.
         USING *+&J,&BASE(&I)
&I       SETA  &I+1                                           81692.
&J       SETA  &J+4096                                        81693.
         AGO   .M689                                          81694.
.M696    ANOP
.*       CORP  PROC_ESTABLISH_BASE                            81697.
.*       CALL  PROC_GEN_SAVE_AREA                             81078.
         AIF   ('&SVTYPE' NE 'OSSAVE').M709                   81703.
.*       CALL  PROC_GEN_OSSAVE_AREA                           81705.
.*       CALL  PROC_DEFINE_NEW_OSSAVE                         81725.
&X       SETC  ''                                             81765.
&SAVEREG SETC  '&WORKREG'                                     81766.
         AIF   ('&SAVE(3)' NE 'DYNAM').M789                   81767.
&LABEL   LA    0,(&SVLENG)*4
&LABEL   SETC  ''                                             81770.
         AIF   ('&WORK' EQ 'NONE').M774                       81771.
         LR    &WORKREG,1
.M774    ANOP
*        GETMAIN  R,LV=(0)
         GETMAIN  R,LV=(0)                                    81775.
         LR    0,&WORKREG
         LR    &WORKREG,1
         AIF   ('&WORK' NE 'NONE').M785                       81778.
         L     1,24(13)
         AIF   ('&FIRST' EQ '14' AND '&LAST' EQ '12').M787    81781.
         MNOTE 4,' STRC8107 REG 1 MUST BE AMONG THOSE SAVED'  81783.
         AGO   .M787                                          81785.
.M785    LR    1,0
.M787    ANOP
&WORKUSD SETB  1                                              81788.
         AGO   .M829                                          81789.
.M789    AIF   ('&SAVE(3)' NE '').M819                        81790.
         AIF   ('&LABEL' NE '').M802                          81792.
         AIF   ('&WORK' NE 'NONE' OR &WORKUSD).M797           81794.
         LR    0,&WORKREG
.M797    ANOP
&WORKUSD SETB  1                                              81798.
         CNOP  0,4
&LABEL   SETC  '&STRCPFX.CC'                                  81800.
         BAL   &WORKREG,&LABEL
         AGO   .M808                                          81802.
.M802    ANOP
&X       SETC  '&STRCPFX.ISV'                                 81803.
         AIF   (&USING13).M808                                81804.
&SAVEREG SETC  '&BASEREG'                                     81806.
.M808    AIF   (&STRCDPT AND &PROC1).M810                     81809.
&X       DC    (&SVLENG)F'0'
         AGO   .M816                                          81816.
.M810    ANOP
&X       DC    A($TRACE),(&SVLENG-1)F'0'
.M816    ANOP                                                 81816.
.*       CALL  PROC_MULTIBASE_GEN (COPY A)                    81817.
         AIF   (NOT &MULTIBS).M989A                           81969.
         AIF   ('&LABEL' NE '').M975AA                        81971.
&LABEL   SETC  '&STRCPFX.FF'                                  81973.
         B     &LABEL
.M975AA  ANOP
&I       SETA  2                                              81976.
&J       SETA  4096                                           81977.
&X       SETC  '&STRCPFX.MBR'                                 81978.
.M979A   AIF   (&I GT N'&BASE).M986A                          81979.
&X       DC    A(&STRCPFX.MBP+&J)
&X       SETC  ''                                             81983.
&I       SETA  &I+1                                           81984.
&J       SETA  &J+4096                                        81985.
         AGO   .M979A                                         81986.
.M986A   ANOP
&MULTIBS SETB  0                                              81988.
.M989A   ANOP
.*       CORP  PROC_MULTIBASE_GEN (COPY A)                    81990.
         AGO   .M829                                          81819.
.M819    AIF   ('&WORK' NE 'NONE' OR &WORKUSD).M824           81820.
&LABEL   LR    0,&WORKREG
&LABEL   SETC  ''                                             81823.
.M824    ANOP
&WORKUSD SETB  1                                              81825.
&LABEL   LA    &WORKREG,&SAVE(3)
&LABEL   SETC  ''                                             81827.
.M829    ANOP
.*       CORP  PROC_DEFINE_NEW_OSSAVE                         81831.
         AIF   (NOT (&STRCDST AND &PROC1)).M737               81728.
&LABEL   ST    13,$FIRSTSV+4
&LABEL   SETC  ''                                             81732.
         MVC   8(4,13),=A($FIRSTSV)
         L     13,=A($LASTSAV)
&STRCPSP SETC  '$FIRSTSV'                                     81735.
&STRCPSA SETC  '0'                                            81736.
.M737    ANOP
&LABEL   ST    &SAVEREG,8(13)
&LABEL   SETC  ''                                             81739.
         ST    13,4(&SAVEREG)
         LR    13,&SAVEREG
         AIF   (NOT &USING13).M745                            81742.
         USING &STRCPFX.ISV,13
.M745    AIF   (NOT (&STRCDPT AND &PROC1 AND '&SAVE(3)' NE '')).M756
         AIF   ('&WORK' NE 'NONE' OR &WORKUSD).M752           81749.
         LR    0,&WORKREG
.M752    LA    &WORKREG,$TRACE
         ST    &WORKREG,0(13)
&WORKUSD SETB  1                                              81755.
.M756    ANOP
.*       CORP  PROC_GEN_OSSAVE_AREA                           81757.
         AGO   .M717                                          81709.
.M709    AIF   (('&SAVE' EQ 'NONE' OR '&SVTYPE' EQ 'NONE') AND         *
               NOT &STRCDST).M717                             81710.
.*       CALL  PROC_GEN_LOCAL_SAVEAREA                        81713.
         AIF   ('&LABEL' NE '').M842                          81838.
&LABEL   SETC  '&STRCPFX.DD'                                  81840.
         B     &LABEL
.M842    ANOP
&STRCPFX.SV  DS  0F
         AIF   ('&SVTYPE' NE 'FULL' AND '&SVTYPE' NE 'NORMLHDR' AND    *
               '&SVTYPE' NE 'TRUNCHDR').M848                  81844.
         DC    X'FF&IDBYTE.0000' FLAG (FF=FINISHED,FE=ENTERED), ID, COU*
               NT
.M848    AIF   ('&SVTYPE' NE 'FULL').M860                     81849.
&FWDPTR  SETC  '&STRCPFX.NXT'                                 81851.
&STRCPSP EQU   &STRCPFX.SV
         DC    A(&STRCPSA,&FWDPTR)
&STRCPSP SETC  '&FWDPTR'                                      81856.
&STRCPSA SETC  '&STRCPFX.SV'                                  81858.
.M860    DC    (&SVLENG)F'0'
.*       CALL  PROC_MULTIBASE_GEN (COPY B)                    81862.
         AIF   (NOT &MULTIBS).M989B                           81969.
         AIF   ('&LABEL' NE '').M975BB                        81971.
&LABEL   SETC  '&STRCPFX.FF'                                  81973.
         B     &LABEL
.M975BB  ANOP
&I       SETA  2                                              81976.
&J       SETA  4096                                           81977.
&X       SETC  '&STRCPFX.MBR'                                 81978.
.M979B   AIF   (&I GT N'&BASE).M986B                          81979.
&X       DC    A(&STRCPFX.MBP+&J)
&X       SETC  ''                                             81983.
&I       SETA  &I+1                                           81984.
&J       SETA  &J+4096                                        81985.
         AGO   .M979B                                         81986.
.M986B   ANOP
&MULTIBS SETB  0                                              81988.
.M989B   ANOP
.*       CORP  PROC_MULTIBASE_GEN (COPY B)                    81990.
.*       CORP  PROC_GEN_LOCAL_SAVEAREA                        81863.
.M717    ANOP
.*       CORP  PROC_GEN_SAVEAREA                              81718.
.*       CALL  PROC_MULTIBASE_GEN (COPY C)                    81082.
         AIF   (NOT &MULTIBS).M989C                           81969.
         AIF   ('&LABEL' NE '').M975CC                        81971.
&LABEL   SETC  '&STRCPFX.FF'                                  81973.
         B     &LABEL
.M975CC  ANOP
&I       SETA  2                                              81976.
&J       SETA  4096                                           81977.
&X       SETC  '&STRCPFX.MBR'                                 81978.
.M979C   AIF   (&I GT N'&BASE).M986C                          81979.
&X       DC    A(&STRCPFX.MBP+&J)
&X       SETC  ''                                             81983.
&I       SETA  &I+1                                           81984.
&J       SETA  &J+4096                                        81985.
         AGO   .M979C                                         81986.
.M986C   ANOP
&MULTIBS SETB  0                                              81988.
.M989C   ANOP
.*       CORP  PROC_MULTIBASE_GEN (COPY C)                    81990.
.*       CALL  PROC_DEBUG_STUFF                               81084.
         AIF   (NOT &STRCDPT).M907                            81871.
         AIF   (NOT &STRCTVG).M886                            81873.
         AIF   ('&WORK' NE 'NONE' OR &WORKUSD).M879           81875.
&LABEL   LR    0,&WORKREG
&LABEL   SETC  ''                                             81878.
.M879    ANOP
&LABEL   L     &WORKREG,=A($TRACE)
         MVC   0(256,&WORKREG),1(&WORKREG)
         MVI   256(&WORKREG),X'&IDBYTE'
&WORKUSD SETB  1                                              81885.
         AGO   .M905                                          81886.
.M886    AIF   ('&LABEL' NE '').M891                          81887.
&LABEL   SETC  '&STRCPFX.EE'                                  81889.
         B     &LABEL
.M891    DC    C'$TRACE'
$TRACE   DC    258X'FF'
&STRCTVG SETB  1                                              81894.
         AIF   (NOT ((&STRCDPC OR &STRCDBC) AND &PROC1)).M902
&STRCPFX.PCT  DC  H'0' PROC COUNT
&PCTWVEC SETB  1                                              81901.
.M902    ANOP
&LABEL   MVC   $TRACE(256),$TRACE+1
         MVI   $TRACE+256,X'&IDBYTE'
.M905    ANOP
&LABEL   SETC  ''                                             81906.
.M907    AIF   (NOT (&STRCDPC OR &STRCDBC)).M938              81908.
         AIF   (NOT &OSLINK AND                                        *
               ('&SAVE' NE 'NONE' OR &STRCDST)).M923          81910.
         AIF   (&PCTWVEC).M921                                81912.
         AIF   ('&LABEL' NE '').M918                          81914.
         NOP   0
&STRCPFX.PCT  EQU  *-2 PROC COUNT
         AGO   .M921                                          81918.
.M918    ANOP
&STRCPFX.PCT  DC  H'0' PROC COUNT
.M921    ANOP
&SPOT    SETC  'PCT'                                          81922.
         AGO   .M925                                          81923.
.M923    ANOP
&SPOT    SETC  'SV+2'                                         81924.
.M925    AIF   ('&WORK' NE 'NONE' OR &WORKUSD).M930
&LABEL   LR    0,&WORKREG
&LABEL   SETC  ''                                             81929.
.M930    ANOP
&LABEL   LH    &WORKREG,&STRCPFX&SPOT
&LABEL   SETC  ''                                             81933.
         LA    &WORKREG,1(&WORKREG)
         STH   &WORKREG,&STRCPFX&SPOT
&WORKUSD SETB  1                                              81937.
.M938    AIF   (&OSLINK OR NOT &STRCDST).M943                 81939.
&LABEL   MVI   &STRCPFX.SV,X'FE'
&LABEL   SETC  ''                                             81942.
.M943    ANOP
.*       CORP  PROC_DEBUG_STUFF                               81944.
         AIF   (NOT ('&WORK' EQ 'NONE' AND &WORKUSD)).M091    81086.
&LABEL   LR    &WORKREG,0
&LABEL   SETC  ''                                             81090.
.M091    AIF   ('&LABEL' EQ '').M095                          81092.
&LABEL   DS    0H
.M095    AIF   ('&LINKAGE(2)' NE 'CSECT').M100                81096.
         DROP  15
.M100    ANOP
.*       CALL  PROC_INFO_SAVE                                 81101.
&STRCOP1(&STRCNST)  SETC  '&FIRST'                            81951.
&STRCOP2(&STRCNST)  SETC  '&LAST'                             81952.
&STRCOP3(&STRCNST)  SETC  '&SVLENG'                           81953.
&STRCOP4(&STRCNST)  SETC  '&PVDEBUG'                          81954.
&I       SETA  &OFFSET0+50                                    81955.
&BIT     SETB  ('&SAVE(3)' EQ 'DYNAM')                        81957.
&STRCINF(&STRCNST)  SETC  '&I&OSLINK&BIT&FRKNOWN&IDBYTE&SPECPFX'
         AIF   (NOT &STRCDDM).M960                            DEBUGMACS
         MNOTE *,' STRC8199 PROC SAVES OP1=&FIRST,OP2=&LAST,OP3=&SVLENG*
               ,OP4=&PVDEBUG,INF=&STRCINF(&STRCNST)'          DEBUGMACS
.M960    ANOP
.*       CORP  PROC_INFO_SAVE                                 81960.
.FIN     ANOP
         AIF   ('&DSECT' EQ '').EXITPRO
         USING &DSECT,13
.EXITPRO MEND
