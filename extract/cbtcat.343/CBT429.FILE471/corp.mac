         MACRO
&USRNAME CORP  &PROCNAM,&RETURN=,&LINK=14,&RESTORE=,&RC=      83001.
         COPY  STRCGBLS
         LCLA  &FRSAREG           FIRST_SAVEAREA_REG          83013.
         LCLA  &OFFSET0           OFFSET_TO_GPR0              83016.
         LCLB  &ANYSAVD           ANY_REGS_SAVED              83019.
         LCLB  &DYNAMSA           DYNAMIC_SAVEAREA            83021.
         LCLB  &BCKREQD           BCK_AREA_REQD               83023.
         LCLB  &FRKNOWN           FIRST_VALUE_KNOWN           83024.
         LCLB  &FRRESTK           FIRST_RESTORE_VALUE_KNOWN   83026.
         LCLB  &OSLINK            OS_LINKAGE                  83028.
         LCLC  &LABEL             LABEL                       83031.
         LCLC  &OFF0STR           GPR0_OFFSET_STRING          83032.
         LCLC  &FIRSTSV           FIRST_REG_SAVED             83033.
         LCLC  &LASTSV            LAST_REG_SAVED              83033.
         LCLC  &MULT              MULT                        83035.
         LCLC  &COMMA2            COMMA2                      83035.
         LCLC  &LOCPTR            LOCAL_POINTER               83037.
         LCLC  &OSPTR             OS_POINTER                  83037.
         LCLC  &RSTAREA           RESTORE_AREA                83037.
         LCLC  &RCREG             RC_REG                      83040.
         LCLC  &SVLENG            SAVE_LENGTH                 83042.
         LCLC  &REST1,&REST2      REST1,REST2                 83043.
         LCLC  &IDBYTE            PROC_ID_BYTE                83044.
         LCLC  &PVDEBUG           PREVIOUS_DEBUG_VECTOR       83046.
         LCLC  &X                 X                           83150.
         LCLC  &CBOFF             CRP_BCK_OFFSET              83225.
         LCLC  &SVOFF             SV_OFFSET                   83227.
         LCLA  &OFFSET            OFFSET               83321. 83391.
         LCLA  &I                 I                    83321. 83391.
         LCLC  &LSTAREA           LAST_AREA                   83439.
         LCLC  &FWDPTR            FWD_PTR                     83441.
         LCLC  &TARGET            TARGET                      83442.
         STRCTRAC  CORP                                       83050.
         AIF   (&STRCNST LE &STRCNSL).M056                    83052.
         STRCPOP
         MEXIT
.M056    STRCVERF  PROC,&PROCNAM                              83057.
         AIF   (&STRCERR).FIN                                 83069.
&LABEL   SETC  '&USRNAME'                                     83071.
.*       CALL  CORP_GET_PROC_INFO                             83074.
&FIRSTSV SETC  '&STRCOP1(&STRCNST)'                           83152.
&LASTSV  SETC  '&STRCOP2(&STRCNST)'                           83153.
&X       SETC  '&STRCINF(&STRCNST)'(1,2)                      83154.
&OFFSET0 SETA  &X-50                                          83155.
         AIF   (&OFFSET0 GE 0).M160                           83156.
&OFF0STR SETC  '-&OFFSET0'                                    83158.
         AGO   .M162                                          83160.
.M160    ANOP
&OFF0STR SETC  '+&OFFSET0'                                    83161.
.M162    ANOP
&X       SETC  '&STRCINF(&STRCNST)'                           83154.
&OSLINK  SETB  ('&X'(3,1) EQ '1')                             83163.
&DYNAMSA SETB  ('&X'(4,1) EQ '1')                             83164.
&FRKNOWN SETB  ('&X'(5,1) EQ '1')                             83165.
&SVLENG  SETC  '&STRCOP3(&STRCNST)'                           83166.
&PVDEBUG SETC  '&STRCOP4(&STRCNST)'                           83167.
&ANYSAVD SETB  ('&FIRSTSV' NE '')                             83168.
&IDBYTE  SETC  '&X'(6,2)                                      83169.
         AIF   ('&X'(8,1) EQ '0').M173                        83170.
&STRCPFX SETC  '$P&IDBYTE'                                    83172.
         AGO   .M175                                          83173.
.M173    ANOP
&STRCPFX SETC  '$&STRCBL#(&STRCNST)'                          83174.
.M175    AIF   (&FRKNOWN).M177                                83176.
&FRSAREG SETA  14                                             83180.
         AGO   .M181                                          83181.
.M177    ANOP
&FRSAREG SETA  &FIRSTSV                                       83178.
.M181    ANOP
.*       CORP  CORP_GET_PROC_INFO                             83182.
.*       CALL  CORP_SET_RESTORE_RANGE                         83079.
&REST1   SETC  '&FIRSTSV'                                     83191.
&REST2   SETC  '&LASTSV'                                      83192.
&MULT    SETC  'M'                                            83193.
&COMMA2  SETC  ','                                            83194.
&FRRESTK SETB  (&FRKNOWN)                                     83195.
         AIF   (&ANYSAVD).M203                                83198.
         AIF   ('&RESTORE' EQ '').M211                        83200.
         MNOTE 8,' STRC8301 NO REGISTERS SAVED--RESTORE IGNORED'
         AGO   .M211                                          83203.
.M203    AIF   ('&RESTORE' EQ '').M211                        83204.
&REST1   SETC  '&RESTORE(1)'                                  83206.
&REST2   SETC  '&RESTORE(2)'                                  83207.
&FRRESTK SETB  (T'&RESTORE(1) EQ 'N')                         83208.
.M211    AIF   ('&REST2' NE '').M215                          83212.
&MULT    SETC  ''                                             83214.
&COMMA2  SETC  ''                                             83214.
.M215    ANOP
&RSTAREA SETC  'SV'                                           83216.
.*       CORP  CORP_SET_RESTORE_RANGE                         83217.
.*       CALL  CORP_GEN_EXIT_LABEL                            83082.
         AIF   (NOT &STRCXIT(&STRCNST)).M507                  83492.
         AIF   ('&LABEL' EQ '').M497                          83494.
&LABEL   DS    0H
.M497    ANOP
&LABEL   SETC  '&STRCPFX.XIT'                                 83498.
         AIF   ('&STRCXSV' NE '').M502                        83499.
&STRCXSV SETC  '0'                                            83501.
.M502    MNOTE &STRCXSV,' STRC8303 ONE OR MORE EXIT''S REFERENCE THIS P*
               OINT'                                          83503.
         MNOTE &STRCXSV,'          VIA &LABEL'                83505.
&STRCXIT(&STRCNST)  SETB  0                                   83505.
.M507    ANOP
.*       CORP  CORP_GEN_EXIT_LABEL                            83508.
         AIF   (NOT &OSLINK).M095                             83085.
.*       CALL  CORP_RESTORE_REG13                             83087.
         AIF   (NOT &DYNAMSA).M268                            83264.
&LABEL   LR    1,13
&LABEL   SETC  ''                                             83267.
.M268    AIF   (&STRCSFP AND '&IDBYTE' EQ '01').M271          83269.
&LABEL   L     13,4(13)
         AGO   .M274                                          83274.
.M271    ANOP
&LABEL   L     13,$FIRSTSV+4
.M274    ANOP
&LABEL   SETC  ''                                             83275.
.*       CORP  CORP_RESTORE_REG13                             83276.
         AIF   (NOT &DYNAMSA).M100                            83090.
.*       CALL  CORP_FREE_DYNAM_SAVEAREA                       83092.
&LABEL   LA    0,(&SVLENG)*4
&LABEL   SETC  ''                                             83384.
*        FREEMAIN  R,LV=(0),A=(1)
         FREEMAIN  R,LV=(0),A=(1)                             83385.
.*       CORP  CORP_FREE_DYNAM_SAVEAREA                       83386.
         AGO   .M100                                          83095.
.M095    ANOP
.*       CALL  CORP_DEBUGGING_STORES                          83096.
         AIF   (NOT &STRCDCV).M256                            83230.
         AIF   (NOT &STRCDST).M236                            83232.
&SVOFF   SETC  '+12'                                          83234.
&CBOFF   SETC  '+12'                                          83235.
         AGO   .M244                                          83236.
.M236    ANOP
&CBOFF   SETC  ''                                             83237.
         AIF   (&STRCDPC).M239                                83238.
&SVOFF   SETC  ''                                             83242.
         AGO   .M243                                          83243.
.M239    ANOP
&SVOFF   SETC  '+4'                                           83240.
.M243    ANOP
.M244    ANOP
&LABEL   STM   14,12,&STRCPFX.CRP&CBOFF
&LABEL   SETC  ''                                             83247.
    AIF  ('&RETURN' EQ '' AND ('&RC' EQ '' OR '&RC' EQ 'NONE')).M255
&BCKREQD SETB  1                                              83250.
         MVC   &STRCPFX.BCK&CBOFF.(&SVLENG*4),&STRCPFX.SV&SVOFF
&RSTAREA SETC  'BCK'                                          83254.
.M255    ANOP
.M256    ANOP
.*       CORP  CORP_DEBUGGING_STORES                          83257.
.M100    ANOP
.*       CALL  CORP_SET_RETURN_CODE                           83101.
&RCREG   SETC  ''                                             83283.
         AIF   ('&RC' NE '').M293                             83284.
         AIF   (NOT &OSLINK).M311                             83286.
&LABEL   SR    15,15
&LABEL   SETC  ''                                             83290.
&RCREG   SETC  '15'                                           83291.
         AGO   .M311                                          83293.
.M293    AIF   ('&RC'(1,1) NE '(').M298                       83294.
&RCREG   SETC  '&RC(1)'                                       83296.
         AGO   .M311                                          83298.
.M298    AIF   ('&RC' EQ 'NONE').M311                         83299.
         AIF   ('&RC' NE '0').M304                            83301.
&LABEL   SR    15,15
         AGO   .M306                                          83304.
.M304    ANOP
&LABEL   LA    15,&RC
.M306    ANOP
&LABEL   SETC  ''                                             83307.
&RCREG   SETC  '15'                                           83308.
.M311    ANOP
.*       CORP  CORP_SET_RETURN_CODE                           83312.
.*       CALL  CORP_SAVE_RETURNING_REGS                       83105.
         AIF   (NOT &ANYSAVD).M378                            83323.
         AIF   (&OSLINK).M326                                 83325.
&OSPTR   SETC  ''                                             83330.
&LOCPTR  SETC  '&STRCPFX&RSTAREA+'                            83331.
         AGO   .M332                                          83332.
.M326    ANOP
&OSPTR   SETC  '(13)'                                         83327.
&LOCPTR  SETC  ''                                             83328.
.M332    AIF   ('&RCREG' EQ '').M348                          83333.
         AIF   (&FRSAREG GE 14).M342                          83335.
         AIF   ('&RCREG' EQ '15').M348                        83337.
&LABEL   LR    15,&RCREG
&LABEL   SETC  ''                                             83340.
         AGO   .M348                                          83342.
.M342    ANOP
&OFFSET  SETA  (&OFFSET0-1)*4                                 83343.
&LABEL   ST    &RCREG,&LOCPTR&OFFSET&OSPTR
&LABEL   SETC  ''                                             83346.
.M348    AIF   ('&FRSAREG' EQ '14' OR N'&RETURN EQ 0).M353    83349.
         MNOTE 4,' STRC8302 WARNING--NO CHECK MADE TO INSURE RETURNING'
         MNOTE 0,'          REGISTERS ARE AMONG THOSE SAVED IN'
         MNOTE 0,'          TRUNCATED SAVE AREA'              83352.
.M353    ANOP
&I       SETA  1                                              83354.
.M355    AIF   (&I GT N'&RETURN).M378                         83355.
         AIF   (T'&RETURN(&I) NE 'N').M362                    83357.
&OFFSET  SETA  (&OFFSET0+&RETURN(&I)-((&RETURN(&I)+2)/16*16))*4
&LABEL   ST    &RETURN(&I),&LOCPTR&OFFSET&OSPTR
         AGO   .M373                                          83362.
.M362    AIF   (&FRSAREG GE 14).M368                          83363.
&LABEL   ST    &RETURN(&I),&LOCPTR.(&RETURN(&I)&OFF0STR)*4&OSPTR
         AGO   .M373                                          83368.
.M368    ANOP
&LABEL   ST    &RETURN(&I),&LOCPTR.(&RETURN(&I)&OFF0STR-((&RETURN(&I)+2:
               )/16*16))*4&OSPTR
.M373    ANOP
&I       SETA  &I+1                                           83374.
&LABEL   SETC  ''                                             83375.
         AGO   .M355                                          83376.
.M378    ANOP
.*       CORP  CORP_SAVE_RETURNING_REGS                       83379.
         AIF   (&OSLINK OR NOT &STRCDST).M116                 83112.
&LABEL   MVI   &STRCPFX.SV,X'FF'
&LABEL   SETC  ''                                             83115.
.M116    ANOP
.*       CALL  CORP_RESTORE_REGISTERS                         83117.
         AIF   (NOT &ANYSAVD).M415                            83393.
         AIF   (NOT &FRRESTK).M401                            83395.
&I       SETA  &REST1                                         83397.
&OFFSET  SETA  (&OFFSET0+&I-((&I+2)/16*16))*4                 83398.
&LABEL   L&MULT  &REST1,&REST2&COMMA2&LOCPTR&OFFSET&OSPTR
         AGO   .M413                                          83401.
.M401    AIF   (&FRSAREG GE 14).M407                          83402.
&LABEL   L&MULT  &REST1,&REST2&COMMA2&LOCPTR.(&REST1&OFF0STR)*4&OSPTR
         AGO   .M413                                          83407.
.M407    ANOP
&LABEL   L&MULT  &REST1,&REST2&COMMA2&LOCPTR.(&REST1&OFF0STR-((&REST1+2:
               )/16*16))*4&OSPTR
.M413    ANOP
&LABEL   SETC  ''                                             83414.
.M415    ANOP
.*       CORP  CORP_RESTORE_REGISTERS                         83416.
         AIF   (NOT &OSLINK).M123                             83119.
&LABEL   MVI   12(13),X'FF'
&LABEL   SETC  ''                                             83122.
.M123    AIF   ('&LINK' EQ 'NONE').M129                       83125.
&LABEL   BR    &LINK
&LABEL   SETC  ''                                             83128.
.M129    ANOP
.*       CALL  CORP_GEN_CRP_BCK_AREAS                         83130.
         AIF   (&OSLINK OR NOT &STRCDCV).M485                 83444.
         AIF   ('&LINK' NE 'NONE').M451                       83446.
&TARGET  SETC  '&STRCPFX.FIN'                                 83448.
&LABEL   B     &TARGET
&LABEL   SETC  '&TARGET'                                      83450.
.M451    ANOP
&LSTAREA SETC  '&STRCPFX.CRP'                                 83452.
&LSTAREA DS    0F
         AIF   (NOT &STRCDST).M467                            83454.
         DC    X'FC&IDBYTE.0000'
         AIF   (&BCKREQD).M458                                83457.
&FWDPTR  SETC  '&STRCPFX.FWD'                                 83461.
         AGO   .M462                                          83462.
.M458    ANOP
&FWDPTR  SETC  '&STRCPFX.BCK'                                 83459.
.M462    ANOP
&STRCPSP EQU   &STRCPFX.CRP
         DC    A(&STRCPSA,&FWDPTR)
.M467    DC    15F'0'
         AIF   (NOT &BCKREQD).M482                            83469.
&LSTAREA SETC  '&STRCPFX.BCK'                                 83471.
&LSTAREA DS    0F
         AIF   (NOT &STRCDST).M480                            83474.
         DC    X'FB&IDBYTE.0000'
&FWDPTR  SETC  '&STRCPFX.FWD'                                 83478.
         DC    A(&STRCPFX.CRP,&FWDPTR)
.M480    DC    (&SVLENG)F'0'
.M482    ANOP
&STRCPSP SETC  '&FWDPTR'                                      83483.
&STRCPSA SETC  '&LSTAREA'                                     83484.
.M485    ANOP
.*       CORP  CORP_GEN_CRP_BCK_AREAS                         83486.
         AIF   ('&LABEL' EQ '').M136                          83132.
&LABEL   DS    0H
&LABEL   SETC  ''                                             83135.
.M136    STRCPOP
.*       CALL  CORP_RESTORE_DEBUG_ENVIRONMENT                 83140.
         AIF   ('&PVDEBUG' EQ '').M433                        83423.
&STRCDBN SETB  ('&PVDEBUG'(1,1) EQ '1')                       83425.
&STRCDPN SETB  ('&PVDEBUG'(2,1) EQ '1')                       83426.
&STRCDLB SETB  ('&PVDEBUG'(3,1) EQ '1')                       83427.
&STRCDBC SETB  ('&PVDEBUG'(4,1) EQ '1')                       83428.
&STRCDPC SETB  ('&PVDEBUG'(5,1) EQ '1')                       83429.
&STRCDPT SETB  ('&PVDEBUG'(6,1) EQ '1')                       83430.
&STRCDCV SETB  ('&PVDEBUG'(7,1) EQ '1')                       83431.
&STRCDST SETB  ('&PVDEBUG'(8,1) EQ '1')                       83432.
.M433    ANOP
.*       CORP  CORP_RESTORE_DEBUG_ENVIRONMENT                 83434.
.FIN     MEND
