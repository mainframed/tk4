         MACRO
&LABEL   CVTOD &OPTION
         AIF   ('&OPTION' EQ  '').GENALL
         AIF   ('&OPTION' EQ  'DATA').DATONLY
.GENALL  ANOP
************************************************************
************************************************************
** CVTOD - CONVERT TOD TO INTERNAL/EXTERNAL FORMAT        **
**   ENTRY:  1  - ADDR OF TOD TO CONVERT                 **
**                                                        **
**                                                        **
**   EXIT:   1  - ADDRESS OF OUTPUT DATA                 **
**           0  - LEN OF OUTPUT                          **
**                                                        **
**   LABEL:    OUTDAY = DAY OF WEEK    LEN=3              **
**             OUTJ   = JULIAN DATE    LEN=6              **
**             OUTDM  = GREGORIAN DATE LEN=8              **
**             OUTTH  = TIME OF DAY    LEN=5              **
**                                                        **
**    OUTPUT FORMAT: 'DAY YY.DDD MM/DD/YY HH:MM -'        **
**                                                        **
**     ASSUMES ZERO VALUE FOR TOD CLOCK IS ----           **
**                   '01/01/00 (000) 00:00:00.000000'     **
**                                                        **
**  THIS  CODE  IS FROM UCLA PROGRAM DUMPINFO ACQUIRED    **
**  VIA  THE CBT TAPE FILE 149 01/24/84....               **
**  AND  MODIFIED BY L. DYCK AS A MACRO                   **
**                                                        **
************************************************************
************************************************************
         SPACE 3
&LABEL   STM   0,15,CVSAVE              SAVE CALLERS REGS
         MVI   OUT,C' '                 INIT OUTPUT AREA TO BLANK
         MVC   OUT+1(OUTLEN-1),OUT
         LM    2,3,0(1)                 GET ORIG TOD VALUE
         SRDL  2,12                     ALIGN MICROSECONDS
         D     2,=A(60*1000000)         MIN TO R3, USEC TO R2
         CVD   2,DWD                    USEC TO DECIMAL
         SR    2,2                      CLEAR FOR DIVIDE
         D     2,=A(60)                 HOUR TO R3, MIN TO R2
         CVD   2,DWD                    MINUTES TO DEC
         UNPK  OUTTM(2),DWD+6(2)        MIN TO OUTPUT AREA
         OI    OUTTM+1,C'0'             FIX ZONE
         SR    2,2                      CLEAR FOR DIVIDE
         D     2,=A(24)                 DAYS TO R3, HOURS TO R2
*************************
*   CALC DAY OF WEEK    *
*************************
         SR    9,9
         LR    9,3                      TOTAL # DAYS IN R9
         SR    8,8                      CLEAR R8 FOR DIVIDE
         D     8,=F'7'                  DIVIDE BY 7 TO GET REMAINDER
         LA    6,DAYTBL                 ADDR OF DAY TABLE IN R6
         LR    9,8                      MOVE REMAINDER FOR MULTIPLY
         M     8,=F'3'                  MULTIPLY REMAINDER BY 3
         AR    6,9                      ADD TO TABLE ADDR
         MVC   OUTDAY(3),0(6)           MOVE DAY INTO OUTPUT
**********************************
*   CALC YEAR, HOURS & MINUTES   *
**********************************
         CVD   2,DWD                    HOURS TO DEC
         UNPK  OUTTH(2),DWD+6(2)         HOURS TO OUTPUT AREA
         OI    OUTTH+1,C'0'              FIX ZONE
         MVI   OUTTH+2,C':'             COLON BETWEEN HOUR:MIN
         C     3,=A(365)                AFTER 1900?
         BNH   *+8                      BIF NOT AFTER 1900
         A     3,=A(1)                  ELSE PRETEND 1900 LEAP YEAR
         SLL   3,2                      DAYS * 4
         SR    2,2                      CLEAR FOR DIVIDE
         D     2,=A((365*4)+1)          YEAR TO R3, DAY TO R2
         CVD   3,DWD                    YEAR TO DEC
         UNPK  OUTJ(2),DWD+6(2)         YEAR TO JULIAN DATE OUTPUT
         UNPK  OUTDY(2),DWD+6(2)        YEAR TO OUTPUT AREA
         OI    OUTJ+1,C'0'              FIX ZONE
         OI    OUTDY+1,X'F0'            FIX ZONE
         SRL   2,2                      DAYS * 4 TO DAYS
         A     2,=A(1)                  + 1 TO GET DAYS ORIG 1
         CVD   2,DWD                    JULIAN DAY TO DEC
         UNPK  OUTJ+3(3),DWD+6(2)       JULIAN DAY TO OUTPUT AREA
         OI    OUTJ+5,X'F0'             FIX ZONE
         MVI   OUTJ+2,C'.'              . IN JULIAN DATE
*****************************************************
*****************************************************
**                                                 **
**       CONVERT JULIAN TO GREGORIAN DATE          **
**                                                 **
**       R2 - JULIAN DAY IN BIN                    **
**       R3 - YEAR IN BIN                          **
**                                                 **
*****************************************************
*****************************************************
GRDATE   DS    0H
         LA    5,CVTOD#T1-2             ASSUME NON-LEAP YEAR
         N     3,=F'3'                  TEST FOR LEAP YEAR
         BNZ   CVTOD$NL                 BIF NOT LEAP YEAR
         LA    5,CVTOD#T2-2             ELSE FEB HAS 29 DAYS
CVTOD$NL LR    4,5                      COPY TABLE ADDR
CVTOD$L1 LA    4,2(,4)                  POINT TO NEXT ENTRY
         SH    2,0(,4)                  DAYS LEFT AFTER MONTH
         BP    CVTOD$L1                 BIF AFTER THIS MONTH
         AH    2,0(,4)                  GET DAY IN MONTH
         CVD   2,DWD                    DAY IN MONTH TO DEC
         UNPK  OUTDD(2),DWD+6(2)        DAY TO OUTPUT AREA
         OI    OUTDD+1,C'0'             FIX ZONE
         MVI   OUTDD+2,C'/'             SLASH AFTER DAY
         SR    4,5                      GET 2*MONTH ORIG 0
         SRL   4,1                      GET MONTH
         CVD   4,DWD                    MONTH TO DEC
         UNPK  OUTDM(2),DWD+6(2)        MONTH TO OUTPUT AREA
         OI    OUTDM+1,C'0'             FIX ZONE
         MVI   OUTDM+2,C'/'             SLASH AFTER MONTH
         LA    1,OUT                    -> OUTPUT AREA
         LA    0,OUTLEN
         STM   0,1,CVSAVE
         LM    0,15,CVSAVE
         B     CVTODX                  BRANCH AROUND DATA
         SPACE 1
*---            DAY TABLE
DAYTBL   DC    CL3'MON'
         DC    CL3'TUE'
         DC    CL3'WED'
         DC    CL3'THR'
         DC    CL3'FRI'
         DC    CL3'SAT'
         DC    CL3'SUN'
         SPACE
*---            NON - LEAP YEAR TABLE OF DAYS / MONTH
CVTOD#T1 DC    H'31'       1 -          JAN
         DC    H'28'       2 -          FEB
         DC    H'31'       3 -          MARCH
         DC    H'30'       4 -          APRIL
         DC    H'31'       5 -          MAY
         DC    H'30'       6 -          JUNE
         DC    H'31'       7 -          JULY
         DC    H'31'       8 -          AUG
         DC    H'30'       9 -          SEPT
         DC    H'31'      10 -          OCT
         DC    H'30'      11 -          NOV
         DC    H'31'      12 -          DEC
         SPACE 1
*---              LEAP YEAR TABLE OF DAYS / MONTH
CVTOD#T2 DC    H'31'       1 -          JAN
         DC    H'29'       2 -          FEB
         DC    H'31'       3 -          MARCH
         DC    H'30'       4 -          APRIL
         DC    H'31'       5 -          MAY
         DC    H'30'       6 -          JUNE
         DC    H'31'       7 -          JULY
         DC    H'31'       8 -          AUG
         DC    H'30'       9 -          SEPT
         DC    H'31'      10 -          OCT
         DC    H'30'      11 -          NOV
         DC    H'31'      12 -          DEC
         DC    H'32767'   13 -          13TH MONTH (LARGE AND LAST)
         AIF   ('&OPTION' EQ  'NOGEN').NOGEN
.DATONLY ANOP
CVSAVE   DS    16F                     REGISTER SAVE AREA
DWD      DS    D
OUT      DS    0F
OUTDAY   DC    C'DAY '                          DAY OF WEEK
OUTJ     DC    C'YY.DDD '                       JULIAN DATE
OUTDM    DC    C'MM/'                           MONTH
OUTDD    DC    C'DD/'                           DAY
OUTDY    DC    C'YY '                           YEAR
OUTTH    DC    C'HH:'                           HOURS
OUTTM    DC    C'MM'                            MINUTES
OUTLEN   EQU   *-OUT                            AREA LENGTH
.NOGEN   ANOP
         AIF   ('&OPTION' EQ  'DATA').EXIT
CVTODX   DS    0H
.EXIT    ANOP
         MEND
