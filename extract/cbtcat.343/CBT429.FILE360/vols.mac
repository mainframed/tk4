//ASUPTASM JOB (9169,0000),'G.B.HAMPTON',MSGCLASS=Q,
//  REGION=32M,
//             CLASS=A,NOTIFY=&SYSUID
/*ROUTE PRINT LOCAL
/*JOBPARM S=*
//*
//*  FROM CBT MVS MODS TAPE FILE 360 VERSION 04-21-85 VERSION # 247
//*
//HLASMCL  EXEC HLASMCL
//C.SYSLIB DD  DISP=SHR,DSN=SYS1.MACLIB
//         DD  DISP=SHR,DSN=SYS1.MODGEN
//         DD  DISP=SHR,DSN=SYSCB1.CBT.FILE360.PDS                 #10
//C.SYSIN  DD  *
         TITLE 'WSRCC - VOLS TSO C.P.  --  VERSION 6'
***********************************************************************
*  THIS PROGRAM (COMMAND) WILL LIST THE AVAILABLE FREE SPACE ON ALL   *
*  OR SELECTED ONLINE DISK PACKS.  AS IT IS HERE, IT WILL NOT WORK    *
*  UNDER MVS/XA.  THIS CP WAS ORIGIONALLY FROM CAMBRIDGE SYSTEMS      *
*  GROUP AND WAS DISTRIBUTED IN AN "AS IS" FORM WITH A VERY EARLY     *
*  VERSION OF THE ASM2 PACKAGE.  THIS CP CONTAINED NO COPYRITE NOTICE *
*  IN ANY FORM .  I HAVE RECEIVED PERMISSION FROM CSG TO PUT THIS ON  *
*  THE CBT TAPE.  - J.MARTIN  02/15/85                                *
***********************************************************************
*--------------MODIFICATION.LOG---------------------------------------*
*  PROGRAMMER  T  DATE  T  SUMMARY OF CHANGES MADE TO THIS PROGRAM    *
*--------------+--------+---------------------------------------------*
* TOM JARVIS   T01/21/80T 1. CHANGED CONTROL UNIT NAME CHECK SO THAT  *
*              T        T    ALL DISK VOLUMES ARE LISTED.             *
* JOE MARTIN   T03/29/82T 2. ADDED THE UNIT ATTR DISPLAY FOR EACH     *
*              T        T    VOLUME. DISPLAY IS STOR, PRIV OR PUBL.   *
*              T        T    ADDED THE BEGIN AND FINISH MACROS.       *
*              T        T    MOVED MOST OF WORKAREA TO DSECT.         *
*              T        T                                             *
* JOE MARTIN   T05/20/82T 3. ADDED SOME SELECTION TO THE VOLS COMMAND.*
*              T        T    FORMAT IS "TYPE(....) (DEVT AND ATTR)    *
*              T        T    WHERE THE "...." IS DEVICE TYPE OR       *
*              T        T    OR DEVICE ATTRIBUTE. EXAMPLE:            *
*              T        T    VOLS TYPE(3350) -- SELECT ONLY 3350'S    *
*              T        T    VOLS TYPE(PRIV) -- SELECT PRIVATE VOLS.  *
* JOE MARTIN   T11/02/82T 4. ADDED NUMBER OF OPEN DCB'S TO THE        *
*              T        T    OUTPUT DISPLAY FOR THE VOLS COMMAND.     *
* JOE MARTIN   T03/21/83T 5. ADDED SELECTION FOR VOLSER OR UCB ADDR   *
*              T        T    ADDED PUTLINE SUPPORT REPLACING TPUT.    *
*              T        T    FIXED PROBLEM WITH UCB TABLE LOOKUP RTN. *
* JOE MARTIN   T03/25/83T 6. ADDED CLEAR KEYWORD TO CLEAR THE SCREEN  *
*              T        T    WHEN RUNNING UNDER VTAM.                 *
*              T        T                                             *
* M. KARLIN    T05/07/90T 7. USE CVT UCB SCAN ROUTINE                 *
* S. GOLOB     T08/14/92T 8. TAUGHT VOLS ABOUT 3390'S AND "UNTAUGHT"  *
*              T        T    HIM ABOUT 2314'S.                        *
* G. GORAB     T08/21/95T 9. CHANGED UCB LOOKUP ROUTINE FOR MVS 5.1   *
*              T        T    USING UCBSCAN FROM S. GOLOB/R. BLUMLEIN  *
*              T        T    CHANGES ARE MARKED @GTG                  *
* G.B.HAMPTON  T03/26/98T10. CHANGED UCB TO USE UCBSCAN MACRO TO GET  *
*              T        T    PRINTABLE UCBNAME TO BE ABLE TO SUPPORT  *
*              T        T    4 CHARACTER NAMES                        *
*              T        T    ALSO REMOVED R11 AS BASE FOR UCBDEV      *
*              T        T      IT DID NOT LOOK LIKE ITS NEEDED AND    *
*              T        T      IT PRODUCED A WARNING FROM HI-LVL ASM  *
*              T        T    NOTE: 3 DIGIT UCB SYSTEMS (BELOW SP4)    *
*              T        T          RETURN A SPACE AS 1ST CHAR OF 4    *
*---------------------------------------------------------------------*
VOLS     BEGIN SAVE=(SAVEAREA,@SL,17),AUTHOR=CSG,VER=009       SBG  #2
         LR    R11,R1                  POINT AT CPPL.               #3
         USING CPPL,R11
         ST    R11,VOLCPPL             SAVE CPPL ADDRESS            #3
         LA    R7,VIOPL                POINT AT IOPL                #5
         USING IOPL,R7                 SET UP ADDRESSIBILITY - IOPL #5
         LA    R9,VOLPPL               POINT TO PPL                 #3
         USING PPL,R9                  SET UP ADDRESSABILITY TO PPL #3
         L     R10,CPPLPSCB            POINT AT PSCB.               #3
         ST    R10,VOLPSCB             STORE BUFFER ADDRESS IN PPL  #3
         L     R10,CPPLCBUF            COMMAND BUFFER               #3
         ST    R10,PPLCBUF             STORE BUFFER ADDRESS IN PPL  #3
         L     R10,CPPLUPT             USER PROFILE TABLE           #3
         ST    R10,PPLUPT              STORE UPT IN PPL             #3
         ST    R10,IOPLUPT             STORE UPT IN IOPL            #5
         L     R10,CPPLECT             ENVIRONMENT CONTROL TABLE    #3
         ST    R10,PPLECT              STORE ECT IN PPL             #3
         ST    R10,IOPLECT             STORE ECT IN IOPL            #5
         L     R10,VOLPCLP             PARSE CONTROL LIST           #3
         ST    R10,PPLPCL              STORE PCL IN PPL             #3
         LA    R10,VOLECB              EVENT CONTROL BLOCK          #3
         ST    R10,PPLECB              STORE ECB IN PPL             #3
         ST    R10,IOPLECB             STORE ECB IN IOPL            #5
         LA    R10,VOLANS              ANSWER AREA                  #3
         ST    R10,PPLANS              STORE ANSWER ADDRESS IN PPL  #3
         LA    R10,VOLUWA              USER WORK AREA               #3
         ST    R10,PPLUWA              STORE WORK ADDRESS IN PPL    #3
         DROP  R7,R9,R11                                            #5
         MVI   LINEFLG,C'N'            INITIALIZE DEVICE SELECT FLG #5
         LR    R1,R9                   POINTER TO PPL IN R1         #3
         CALLTSSR EP=IKJPARS           CALL PARSER                  #3
         LTR   R15,R15                 CHECK FOR ZERO PARSE RETURN  #3
         BNZ   FINISH                  GO AWAY UPSET                #3
         L     R8,VOLANS               LOAD ANSWER AREA FOR USING   #3
         USING PCLDSECT,R8             SET UP ADDRESSABILITY TO ANS #3
         MVI   SCRNFLG,C'N'            SET NOT-SCREEN FLAG          #6
         SR    R7,R7                   CLEAR R7 FOR POINTER         #6
         LH    R7,VOLCLEAR             GET POINTER VALUE            #6
         LTR   R7,R7                   IF PARM NOT PRESENT          #6
         BZ    CHKVOLTP                   THEN CONTINUE             #6
         GTSIZE ,                      GET SCREEN DISPLAY SIZE      #6
         LTR   R0,R0                   IF DISPLAY TERMINAL          #6
         BZ    CHKVOLTP                   THEN CONTINUE             #6
         MVI   SCRNFLG,C'Y'            SET SCREEN FLAG              #6
         BCTR  R0,0                    REDUCE FOR THE "***"         #6
         ST    R0,MAXLINES             SAVE SCREEN DISPLAY SIZE     #6
***      STFSMODE ON,INITIAL=YES       CLEAR SCREEN                 #6
***      STFSMODE OFF                  CLEAR SCREEN                 #6
CHKVOLTP EQU   *
         L     R7,VOLTYPE              POINT AT PARAMETER           #3
         LH    R6,VOLTYPE+4            GET LENGTH OF PARAMETER      #3
         BCTR  R6,0                    REDUCE R6 FOR EX OF CLC      #3
         EX    R6,CLC#ALL              CHECK FOR "ALL"              #3
         BE    SET#ALL                 YES = GO SETUP FOR "ALL"     #3
         EX    R6,CLC#PRIV             CHECK FOR "PRIVATE"          #3
         BE    SET#PRIV                YES = GO SETUP FOR "PRIVATE" #3
         EX    R6,CLC#PUBL             CHECK FOR "PUBLIC"           #3
         BE    SET#PUBL                YES = GO SETUP FOR "PUBLIC"  #3
         EX    R6,CLC#STOR             CHECK FOR "STORAGE"          #3
         BE    SET#STOR                YES = GO SETUP FOR "STORAGE" #3
         EX    R6,CLC#3380             CHECK FOR "3380"             #3
         BE    SET#3380                YES = GO SETUP FOR "3380"    #3
         EX    R6,CLC#3350             CHECK FOR "3350"             #3
         BE    SET#3350                YES = GO SETUP FOR "3350"    #3
         EX    R6,CLC#3330             CHECK FOR "3330"             #3
         BE    SET#3330                YES = GO SETUP FOR "3330"    #3
         EX    R6,CLC#3311             CHECK FOR "3311"             #3
         BE    SET#3311                YES = GO SETUP FOR "3311"    #3
         EX    R6,CLC#3390             CHECK FOR "3390"             #3
         BE    SET#3390                YES = GO SETUP FOR "3390"    #3
         PUTLINE PARM=LINEOUT,OUTPUT=(PBP1,DATA),MF=(E,VIOPL)       #5
         PUTLINE PARM=LINEOUT,OUTPUT=(PBP2,DATA),MF=(E,VIOPL)       #5
*        TPUT  BADPARM1,LBPRM1         TELL USER OF BAD PARM VALUE  #3
*        TPUT  BADPARM2,LBPRM2         TELL USER OF BAD PARM VALUE  #3
         LA    R15,8                   SET RETRUN CODE              #3
         B     FINISH                  GO AWAY WITH NO VOLS OUTPUT  #3
         SPACE
CLC#ALL  CLC   ADALL(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#PRIV CLC   APRIV(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#PUBL CLC   APUBL(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#STOR CLC   ASTOR(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#3380 CLC   D3380(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#3350 CLC   D3350(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#3330 CLC   D3330(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#3311 CLC   D3311(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
CLC#3390 CLC   D3390(0),0(R7)          EXECUTED COMPARE OF PARM.    #3
         SPACE
SET#ALL  EQU   *                                                    #3
         MVI   SELFLAGS,ALLDEVT        SET SELECTION BITS           #3
         OI    SELFLAGS,ALLATTR        SET SELECTION BITS           #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#PRIV EQU   *                                                    #3
         MVI   SELFLAGS,ATTRPRIV       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLDEVT        SET BITS FOR DEVICE TYPES    #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#PUBL EQU   *                                                    #3
         MVI   SELFLAGS,ATTRPUBL       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLDEVT        SET BITS FOR DEVICE TYPES    #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#STOR EQU   *                                                    #3
         MVI   SELFLAGS,ATTRSTOR       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLDEVT        SET BITS FOR DEVICE TYPES    #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#3380 EQU   *                                                    #3
         MVI   SELFLAGS,DEVT3380       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLATTR        SET BITS FOR ALL ATTRIBS     #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#3350 EQU   *                                                    #3
         MVI   SELFLAGS,DEVT3350       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLATTR        SET BITS FOR ALL ATTRIBS     #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#3330 EQU   *                                                    #3
         MVI   SELFLAGS,DEVT3330       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLATTR        SET BITS FOR ALL ATTRIBS     #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#3311 EQU   *                                                    #3
         MVI   SELFLAGS,DEVT3311       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLATTR        SET BITS FOR ALL ATTRIBS     #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
SET#3390 EQU   *                                                    #3
         MVI   SELFLAGS,DEVT3390       SET SELECTION BIT            #3
         OI    SELFLAGS,ALLATTR        SET BITS FOR ALL ATTRIBS     #3
         B     TPUTH1                  CONTINUE PROCESSING          #3
         EJECT
TPUTH1   EQU   *
         MVI   SERFLG,C'N'         SET VOLSER FLAG                  #5
         L     R6,VOLNAME              POINT AT PARM                #5
         LTR   R6,R6                   IF PARM ADDRESS IS ZERO      #5
         BZ    TPUTH2                     THEN SKIP MOVE OF VOLSER  #5
         LH    R7,VOLNAME+4            GET LENGTH OF PARM           #5
         LTR   R7,R7                   IF LENGTH IS ZERO            #5
         BZ    TPTH2                      THEN SKIP BCTR            #5
         BCTR  R7,0                    REDUCE FOR EX OF CLC         #5
TPTH2    EQU   *
         STH   R7,SERLEN               SAVE SERIAL NUMBER LENGTH    #5
         MVI   SERFLG,C'Y'         SET VOLSER FLAG                  #5
         EX    R7,SERMOVE              MOVE VOLSER TO HOLD AREA     #5
TPUTH2   EQU   *
         MVI   UCBFLG,C'N'             SET UCBADR FLAG              #5
         L     R6,VOLADDR              POINT AT PARM                #5
         LTR   R6,R6                   IF PARM ADDRESS IS ZERO      #5
         BZ    TPUTH3                     THEN SKIP MOVE OF UCBADR  #5
         LH    R7,VOLADDR+4            GET LENGTH OF PARM           #5
         LTR   R7,R7                   IF LENGTH IS ZERO            #5
         BZ    TPTH3                      THEN SKIP BCTR            #5
         BCTR  R7,0                    REDUCE FOR EX OF CLC         #5
TPTH3    EQU   *
         STH   R7,UCBLEN               SAVE UCB ADDRESS LENGTH      #5
         MVI   UCBFLG,C'Y'             SET UCBADR FLAG              #5
         EX    R7,UCBMOVE              MOVE UCBADR TO HOLD AREA     #5
         B     TPUTH3                  SKIP THE EXECUTED STUFF      #5
         SPACE
SERMOVE  MVC   SERVAL(0),0(R6)         MOVE IN VOL SERIAL           #5
UCBMOVE  MVC   UCBVAL(0),0(R6)         MOVE IN UCB ADDRESS          #5
         SPACE
TPUTH3   EQU   *
         SR    R9,R9                   CLEAR OUT LINE COUNTER       #6
         PUTLINE PARM=LINEOUT,OUTPUT=(PLH1,DATA),MF=(E,VIOPL)       #5
         PUTLINE PARM=LINEOUT,OUTPUT=(PLH2,DATA),MF=(E,VIOPL)       #5
         LA    R9,2(0,R9)              INCRIMENT THE LINE COUNTER   #6
*        L     R4,16             @ CVT                          @MK
*        L     R2,40(R4)         @ UCB LOOKUP TABLE             @MK
*CBLOOP  EQU   *                                                @MK
*        LH    R3,0(R2)          GET UCB ADDR.                  @MK
*        SLL   R3,16               MAKE SURE THE UCB ADDR IS    @MK
*        SRL   R3,16               CLEAN IN THE HIGH ORDER BITS @MK
*        LA    R2,2(R2)          BUMP TO NEXT...                @MK
*        LTR   R3,R3               CHECK FOR NULL               @MK
*        BZ    UCBLOOP           GET NEXT IF NULL               @MK #5
*        CL    R3,=X'0000FFFF'   END OF TABLE?                  @MK
*        BE    DONE              YES...                         @MK
************************************************************
*        SET UCB SCAN                                           @MK
************************************************************    @MK
         XC    WKAREA,WKAREA       CLEAR WKAREA           @MK
         LA    R1,WKAREA             SET UP PARM LIST         @MK
         ST    R1,PARMWA                                        @MK
         LA    R1,DEVCLASS                                      @MK
         ST    R1,PARMDEVT                                      @MK
         MVI   DEVCLASS,UCB3DACC       SEARCH DIRECT ACCESS ONLY@MK
         LA    R1,ADDRUCB                                       @MK
         ST    R1,PARMUCB                                       @MK
         OI    PARMUCB,X'80'           INDICATE END OF PARMLIST @MK
************************************                            @MK
*        SCAN UCB                                               @MK
************************************                            @MK
*UCBLOOP  LA    R1,PARMLIST                                     @GTG
*         L     R5,CVTPTR                                       @GTG
*         USING CVT,R5                                          @GTG
*         L     R15,CVTUCBSC        SCAN SERVICE ADDRESS        @GTG
*         DROP  R5                                              @GTG
*         BALR  R14,R15                                         @GTG
*         LTR   R15,R15                                         @GTG
*         BNZ   DONE                NO MORE UCBS                @GTG
*        USING UCBOB,R3                                         @GTG
*        L     R3,ADDRUCB                                       @GTG
*        TM    18(R3),X'20'      DISK?                          @GTG
*        BZ    UCBLOOP            NO                            @GTG
*        TM    3(R3),X'80'       ONLINE?                        @GTG
*        BZ    UCBLOOP            NO                            @GTG
*        TM    6(R3),X'40'       READY? (CHECK NOT READY FLAG)  @GTG
*        BNZ   UCBLOOP            NO    WAS BCR 7,R4            @GTG
RESCANLP DS    0H                                               @GTG
         IOCINFO IOCTOKEN=TOKEN,MF=(E,IOCINFP)                  @GTG
         XC    SCANWORK,SCANWORK                                @GTG
UCBLOOP  DS    0H                                               @GTG
         UCBSCAN UCBAREA=UCBCOPY,WORKAREA=SCANWORK,DYNAMIC=YES,        C
               DEVCLASS=DASD,IOCTOKEN=TOKEN,MF=(E,UCBSLST),     @GTG#10X
               DEVNCHAR=DEVNAME,RANGE=ALL                          #10
         LTR   R15,R15           WAS A UCB RETURNED?            @GTG
         BNZ   SCANDONE          NO, EITHER A CONFIGURATION     @GTG
*                                CHANGE HAS OCCURRED OR NO MORE @GTG
*                                UCB'S.                         @GTG
*                                                               @GTG
*        PROCESS UCB                                            @GTG
*                                                               @GTG
*                                                               @GTG
         USING UCBCMSEG,R3       UCB COMMON SEGMENT DSECT       @GTG
         LA    R3,UCBCOPY        ADDRESSABILITY TO UCBCOPY      @GTG
         TM    UCBSTAT,UCBONLI   IS VOLUME ONLINE?              @GTG
         BO    MOVEUCB           YES, ONLINE. MOVE IT.          @GTG
*        TM    UCBSTAT,UCBALOC   IS VOLUME ALLOCATED?           @GTG
*        BO    MOVEUCB           YES, ALLOCATED. MOVE IT.       @GTG
         B     UCBLOOP           GET ANOTHER UCB                @GTG
*        MVC   LINE(LNGT),BLANKS   MOVE IN BLANKS TO OUTPUT LINE@GTG
MOVEUCB  MVC   LINE(LNGT),BLANKS   MOVE IN BLANKS TO OUTPUT LINE@GTG
         MVC   ADDRT,DEVNAME       MAKE COPY OF CHARACTER UNIT NAME#10
         TR    ADDRT,TRTAB         TR ALPHA CHARACTERS A-F TO FA-FF#10
         CLC   ADDRTL,ADDRT        COMPARE UNIT NAMES               #1
         BNL   UCBLOOP             GET NEXT UCB                     #5
CHKVOL   EQU   *
         CLI   SERFLG,C'N'         IF VOL COMPARE NOT NEEDED        #5
         BE    CHKUCB                 THEN CHECK ADDRESS WANTED     #5
         LH    R6,SERLEN           LOAD LENGTH OF SERIAL#-1         #5
         EX    R6,SERCLC           COMPARE SERIAL NUMBER TO UCB     #5
         BNE   UCBLOOP             TRY NEXT VOLSER                  #5
CHKUCB   EQU   *
         CLI   UCBFLG,C'N'         IF UCB COMPARE NOT NEEDED        #5
         BE    SAVE01                 THEN CONTINUE LIKE BEFORE     #5
         LH    R6,UCBLEN           LOAD LENGTH OF UCB ADDRESS       #5
         CLI   DEVNAME,C' '        3 CHAR UCBS (MUST BE BELOW SP5) #10
         BE    CHKUCB3             SKIP OVER 4 DIGIT CHECK         #10
         EX    R6,UCBCLC           COMPARE UCB ACCRESS TO UCB       #5
         BE    SAVE01              LOOKS GOOD                      #10
         C     R6,=F'3'            4 DIGITS SPECIFED?              #10
         BNL   UCBLOOP             IF SO, THAT'S IT!               #10
         CLI   DEVNAME,C'0'        1ST CHAR NOT ZERO               #10
         BNE   UCBLOOP             UCB ALREADY COMPARED            #10
CHKUCB3  DS    0H                                                  #10
         EX    R6,UCBCLC1          LETS CHECK IN CASE JUST 3 CHAR  #10
         BNE   UCBLOOP             TRY NEXT VOLSER                  #5
         B     SAVE01                  CONTINUE AS USUAL            #5
         SPACE
SERCLC   CLC   SERVAL(0),28(R3)        COMPARE REQUESTED VOL - UCB  #5
UCBCLC   CLC   UCBVAL(0),DEVNAME       COMPARE REQUESTED ADR - UCB #10
UCBCLC1  CLC   UCBVAL(0),DEVNAME+1     COMPARE REQUESTED ADR - UCB #10
         SPACE
SAVE01   EQU   *
*DEL*    USING UCBDEV,R11                                       @GTG#10
*DEL*    LA    R11,UCBCOPY+24                                   @GTG#10
         MVC   VOLSER(6),UCBVOLI   MOVE THE VOLSER TO OUTPUT AREA
         MVC   ADDR(4),DEVNAME     MOVE THE UNIT ADDRESS TO AREA   #10
         TR    ADDR(1),TRTABF0     CHANGE A ZERO TO A BLANK!       #10
         MVC   ADDRTL,ADDRT        KEEP TRANSLATED UNIT NAME        #1
         EJECT
T3380    EQU   *
         CLI   19(R3),X'0E'        IS THIS A 3380
         BNE   T3350               NO GO TRY 3350
         TM    SELFLAGS,DEVT3380   CHECK FOR 3380 SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   DEVT(4),=C'3380'    MOVE THE IN DEVICE TYPE
         B     GOTDEV              GO SETUP FOR OUTPUT
T3350    EQU   *
         CLI   19(R3),X'0B'        IS THIS A 3350
         BNE   T3311               NO GO TRY 3330-1
         TM    SELFLAGS,DEVT3350   CHECK FOR 3350 SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   DEVT(4),=C'3350'    MOVE THE IN DEVICE TYPE
         B     GOTDEV              GO SETUP FOR OUTPUT
T3311    EQU   *
         CLI   19(R3),X'0D'        IS THIS A 3330-1
         BNE   T3330               NO GO TRY 3330
         TM    SELFLAGS,DEVT3311   CHECK FOR 3311 SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   DEVT(4),=C'3311'    MOVE THE IN DEVICE TYPE
         B     GOTDEV              GO SETUP FOR OUTPUT
T3330    EQU   *
         CLI   19(R3),X'09'        IS THIS A 3330
         BNE   T3390               NO GO TRY 3390
         TM    SELFLAGS,DEVT3330   CHECK FOR 3330 SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   DEVT(4),=C'3330'    MOVE THE IN DEVICE TYPE
         B     GOTDEV              GO SETUP FOR OUTPUT
T3390    EQU   *
         CLI   19(R3),X'0F'        IS THIS A 3390
         BNE   UNKN                NO GO MOVE IN "UNKN"
         TM    SELFLAGS,DEVT3390   CHECK FOR 3390 SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   DEVT(4),=C'3390'    MOVE THE IN DEVICE TYPE
         B     GOTDEV              GO SETUP FOR OUTPUT
UNKN     EQU   *
         MVC   DEVT(4),=C'UNKN'    FOUND SOMETHING THAT WE DONT KNOW
GOTDEV   EQU   *
         LR    R0,R3               GOT A GOOD ONE.
         LA    R1,OUTPUT           SETUP FOR LSPCE SVC
         SVC   78                  ISSUE LSPCE
         MVC   ALLC(3),=C'YES'     SET ALLOCATED MSG
         TM    3(R3),X'08'         TEST IF ALLOCATED
         BO    CHKPRIV             BRANCH IF ALLOCATED              #2
         MVC   ALLC(3),=C'NO '     NOT ALLOCATED
CHKPRIV  TM    UCBSTAB,UCBBPRV     IS THIS VOL PRIVATE          @GTG
         BZ    CHKPUBL             NO -- TRY PUBLIC                 #2
         TM    SELFLAGS,ATTRPRIV   CHECK FOR PRIV SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   ATTR(4),=C'PRIV'    MOVE IN PUBLIC LITERAL           #2
         B     MOVESPCE            GO DO SPACE MOVES                #2
CHKPUBL  TM    UCBSTAB,UCBBPUB     IS THIS VOL PUBLIC           @GTG
         BZ    CHKSTOR             NO -- TRY STORAGE                #2
         TM    SELFLAGS,ATTRPUBL   CHECK FOR PUBL SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   ATTR(4),=C'PUBL'    MOVE IN PRIVATE LITERAL          #2
         B     MOVESPCE            GO DO SPACE MOVES                #2
CHKSTOR  TM    UCBSTAB,UCBBSTR     IS THIS VOL STORAGE          @GTG
         BZ    CHKFAIL             NO -- INDICATE UNKN              #2
         TM    SELFLAGS,ATTRSTOR   CHECK FOR STOR SELECTION         #3
         BZ    UCBLOOP             DO NOT CONTINUE IF NO SELECTION  #3
         MVC   ATTR(4),=C'STOR'    MOVE IN PRIVATE LITERAL          #2
         B     MOVESPCE            GO DO SPACE MOVES                #2
CHKFAIL  MVC   ATTR(4),=C'UNKN'    MOVE IN UNKNOWN LITERAL          #2
MOVESPCE MVC   CYL(4),FCYL         MOVE IN FREE CYLS
         MVC   TRK(4),FTRK         MOVE IN FREE TRKS
         MVC   XTNS(4),NXTN        MOVE IN NUM EXTENTS
         MVC   XCYL(4),OCYL        MOVE IN CONTIG CYLS
         MVC   XTRK(4),OTRK        MOVE IN CONTIG TRKS
         MVI   PLUS,C'+'           MOVE IN THE "+"                  #2
         SR    R5,R5                   CLEAR R5 FOR ICM             #4
         MVC   DCBS,=CL6'     0'       MOVE IN INITIAL DCB COUNT    #4
         ICM   R5,B'0001',35(R3)       INSERT THE DCB COUNT         #4
         BZ    DONEDCB                 GO AWAY                      #4
         SLL   R5,25                   SHIFT LEFT TO REMOVE "MOUNT" #4
         SRL   R5,25                   SHIFT RIGHT TO RESTORE COUNT #4
         CVD   R5,DCBCNT               MAKE DCBCOUNT DECIMAL        #4
         MVC   DCBS(6),DCBEDT          MOVE IN EDIT FIELD           #4
         ED    DCBS(6),DCBCNT+5        PUT IN THE EDITED DCB COUNT  #4
         SPACE
DONEDCB  EQU   *                                                    #4
         CLI   SCRNFLG,C'N'            IF THIS IS NOT-SCREEN MODE   #6
         BE    SHOWLINE                   THEN CONTINUE THIS CODE   #6
         C     R9,MAXLINES             IF LINES EXCEEDED            #6
         BL    SHOWLINE                   THEN DISPLAY HEADING      #6
         SR    R9,R9                   CLEAR OUT LINE COUNTER       #6
         PUTLINE PARM=LINEOUT,OUTPUT=(PLH1,DATA),MF=(E,VIOPL)       #5
         PUTLINE PARM=LINEOUT,OUTPUT=(PLH2,DATA),MF=(E,VIOPL)       #5
         LA    R9,2(0,R9)              INCRIMENT THE LINE COUNTER   #6
SHOWLINE MVC   PLNE(4),PLNELN          MOVE LINE LENGTH FOR PUTLINE #5
         PUTLINE PARM=LINEOUT,OUTPUT=(PLNE,DATA),MF=(E,VIOPL)       #5
         LA    R9,1(0,R9)              INCRIMENT THE LINE COUNTER   #6
         MVI   LINEFLG,C'Y'        SET THE DISPLAYED FLAG           #5
         B     UCBLOOP             GO DO NEXT UCB ENTRY
SCANDONE DS    0H                                               @GTG
         LA    R3,12                                            @GTG
         LA    R4,4                                             @GTG
         CR    R15,R3              HAS CONFIGURATION CHANGED?   @GTG
         BE    RESCANLP            YES, START OVER              @GTG
         CR    R15,R4              SCAN SUCCESSFUL?             @GTG
         BNE   FINISH              NO, EXIT WITH R15            @GTG
         SPACE
DONE     EQU   *
         CLI   LINEFLG,C'Y'            IF SOME DEVICES DISPLAYED    #5
         BE    DONE1                      THEN SKIP NODEVICE MSG    #5
         PUTLINE PARM=LINEOUT,OUTPUT=(PLBS,DATA),MF=(E,VIOPL)       #5
         PUTLINE PARM=LINEOUT,OUTPUT=(PLE2,DATA),MF=(E,VIOPL)       #5
         SPACE
DONE1    EQU   *
         PUTLINE PARM=LINEOUT,OUTPUT=(PLBS,DATA),MF=(E,VIOPL)       #5
         PUTLINE PARM=LINEOUT,OUTPUT=(PLE1,DATA),MF=(E,VIOPL)       #5
*        TPUT  BLANKS,1            PUT OUT BLANK LINE               #2
*        TPUT  END1,L'END1         PUT OUT END MESSAGE              #2
         LA    R15,0               SETUP RETURN CODE                #3
         SPACE
FINISH   FINISH RC=(R15)                                            #3
         EJECT
PLH1     DC    AL2(LHDR1+4),AL2(0)     LENGTH FOR PUTLINE           #5
HDR1     DC    C'            UNIT TOT FR TOT FR  TOT   LRG. FREE   IN '
         DC    C'  UNIT  OPEN'                                      #4
LHDR1    EQU   *-HDR1                                               #2
PLH2     DC    AL2(LHDR2+4),AL2(0)     LENGTH FOR PUTLINE           #5
HDR2     DC    C'VOLSER DEVT ADDR   CYL    TRK   XTNS  CYLS+TRKS   USE'
         DC    C'  ATTR  DCBS'                                      #4
LHDR2    EQU   *-HDR2                                               #2
PLE1     DC    AL2(LEND1+4),AL2(0)     LENGTH FOR PUTLINE           #5
END1     DC    C'**PROCESSING COMPLETED**  ---  VOLS REL.01 MOD.09' #6
LEND1    EQU   *-END1                                               #5
PLE2     DC    AL2(LEND2+4),AL2(0)     LENGTH FOR PUTLINE           #5
END2     DC    C'  0 DEVICES SELECTED '                             #4
LEND2    EQU   *-END2                                               #5
PLBS     DC    AL2(5),AL2(0)           LENGTH FOR PUTLINE           #5
BLANKS   DC    CL80' '             BLANKS FOR ASSORTED THINGS       #2
PLNELN   DC    AL2(LNGT+4),AL2(0)      LENGTH FOR PUTLINE OF LINE   #5
DCBEDT   DC    XL06'402020202120'  EDIT PATTERN FOR OPEN DCB COUNT  #4
PBP1     DC    AL2(LBPRM1+4),AL2(0)    LENGTH FOR PUTLINE           #5
BADPARM1 DC    C'INVALID SELECTION KEYWORD - '                      #3
LBPRM1   EQU   *-BADPARM1          SETUP LENGTH FOR TPUT            #3
PBP2     DC    AL2(LBPRM2+4),AL2(0)    LENGTH FOR PUTLINE           #5
BADPARM2 DC    C'  VALID ARE: ALL PRIVATE PUBLIC STORAGE '          #3
         DC    C'3380 3350 3330 3311 AND 3390'                      #3
LBPRM2   EQU   *-BADPARM2          SETUP LENGTH FOR TPUT            #3
ADALL    DC    CL8'ALL     '           SELECT ALL VOLUMES           #3
APRIV    DC    CL8'PRIVATE '           ATTRIB IS PRIVATE            #3
APUBL    DC    CL8'PUBLIC  '           ATTRIB IS PUBLIC             #3
ASTOR    DC    CL8'STORAGE '           ATTRIB IS STORAGE            #3
D3380    DC    CL8'3380    '           DEVICE IS 3380               #3
D3350    DC    CL8'3350    '           DEVICE IS 3350               #3
D3330    DC    CL8'3330    '           DEVICE IS 3330               #3
D3311    DC    CL8'3311    '           DEVICE IS 3311               #3
D3390    DC    CL8'3390    '           DEVICE IS 3390               #3
ALLDEVT  EQU   X'F1'                   SETUP FOR DEVICE TYPES       #3
ALLATTR  EQU   X'0E'                   SETUP FOR ALL ATTRIBS        #3
DEVT3390 EQU   X'01'               SETUP DEVICE TYPE 3390           #3
DEVT3380 EQU   X'80'               SETUP DEVICE TYPE 3380           #3
DEVT3350 EQU   X'40'               SETUP DEVICE TYPE 3350           #3
DEVT3330 EQU   X'20'               SETUP DEVICE TYPE 3330           #3
DEVT3311 EQU   X'10'               SETUP DEVICE TYPE 3311           #3
ATTRPRIV EQU   X'08'               SETUP FOR PRIVATE DEVICES        #3
ATTRPUBL EQU   X'04'               SETUP FOR PUBLIC  DEVICES        #3
ATTRSTOR EQU   X'02'               SETUP FOR STORAGE DEIVCES        #3
VOLPCLP  DC    V(PCLCSECT)         POINTER TO PCLCSECT FOR PARSE    #3
         SPACE
         LTORG
         EJECT ,         MOVE TABLES HERE                          #10
TRTAB    DC    256X'00'            TRANSLATE TABLE                 1.
         ORG   TRTAB+C' '                                          #10
         DC    C' '                                                #10
         ORG   TRTAB+C'A'                                          1.
         DC    X'FAFBFCFDFEFF'                                     1.
         ORG   TRTAB+C'0'                                          1.
         DC    X'F0F1F2F3F4F5F6F7F8F9'                             1.
         ORG
TRTABF0  DS    CL256                                               #10
         ORG   TRTABF0                                             #10
         DC    256X'00'                                            #10
         ORG   TRTABF0+C' '                                        #10
         DC    C' '                                                #10
         ORG   TRTABF0+C'A'                                        #10
         DC    C'ABCDEF'                                           #10
         ORG   TRTABF0+C'0'                                        #10
         DC    C' '                                                #10
         DC    C'123456789'                                        #10
         ORG   ,                                                   #10
         EJECT
WORKAREA DSECT                                                      #2
SAVEAREA DS    18F                     SAVEAREA FOR REGISTERS       #2
TOKEN    DS    CL48                                             @GTG
UCBCOPY  DS    CL48                                             @GTG
SCANWORK DS    CL100                                            @GTG
SELFLAGS DS    F                       SELECTION FLAGS              #3
VOLCPPL  DS    F                       ADDRESS OF CPPL              #3
VOLPSCB  DS    F                       ADDRESS OF PSCB              #5
VOLECB   DS    F                       ECB FOR PARSE TO USE         #3
VOLANS   DS    F                       ANSWER AREA FOR PARSE        #3
VOLUWA   DS    4F                      USER WORK AREA FOR PARSE     #3
VOLPPL   DS    7F                      PPL FOR USE WITH PARSE       #3
VIOPL    DS    4F                      IOPL FOR PUTLINE SUPPORT     #5
MAXLINES DS    F                       MAX SCREEN SIZE              #6
SCRNFLG  DS    CL1                     DISPLAY SCREEN FLAG          #6
SERLEN   DS    H                       LENGTH OF VOLSER PARM        #5
UCBLEN   DS    H                       LENGTH OF UCBADR PARM        #5
SERFLG   DS    CL1                     FLAG USED TO SELECT VOLSER   #5
UCBFLG   DS    CL1                     FLAG USED TO SELECT UCBADR   #5
SERVAL   DS    CL6                     VALUE OF THE VOLSER PARM     #5
UCBVAL   DS    CL4                     VALUE OF THE UCBADR PARM     #5
LINEFLG  DS    CL1                     DEVICE SELECTED FLAG         #5
TIMEDATE DS    D                       AREA FOR THE TIME AND DATE   #3
DCBCNT   DS    D                       NUMBER OF OPEN DCBS          #4
OUTPUT   DS    0CL30                   LSPACE OUTPUT AREA           #2
         DS    CL6                                                  #2
FCYL     DS    CL4                                                  #2
         DS    CL1                                                  #2
FTRK     DS    CL4                                                  #2
         DS    CL1                                                  #2
NXTN     DS    CL4                                                  #2
         DS    CL1                                                  #2
OCYL     DS    CL4                                                  #2
         DS    CL1                                                  #2
OTRK     DS    CL4                                                  #2
         SPACE 1
         DS    0D                  ALIGNMENT                        #2
PLNE     DS    AL2(LNGT+4),AL2(0)      LENGTH FOR PUTLINE           #5
LINE     EQU   *                                                    #2
VOLSER   DS    CL7' '              VOLUME SERIAL NUMBER             #2
DEVT     DS    CL5' '              DEVICE TYPE                     #10
ADDR     DS    CL6' '              DEVICE ADDRESS                  #10
CYL      DS    CL7' '              FREE CYLS                        #2
TRK      DS    CL7' '              FREE TRKS                        #2
XTNS     DS    CL6' '              EXTENTS                          #2
XCYL     DS    CL4' '              CONTIG CYLS                      #2
PLUS     DS    CL1'+'                                               #2
XTRK     DS    CL7' '              CONTIG TRKS                      #2
ALLC     DS    CL5' '              ALLOCATED FLAG                   #2
ATTR     DS    CL4' '              VOLUME ATTRIBUTES PUBL/PRIV/STOR #2
DCBS     DS    CL6' '              NUMBER OF OPEN DCBS              #4
LNGT     EQU   *-LINE                                               #2
         SPACE 1
ADDRT    DS    CL4                 FIELD TO TRANSLATE UNIT NAME IN #10
ADDRTL   DS    CL4                 FIELD TO HOLD TRANS UNIT NAME   #10
DEVNAME  DS    CL4                 HOLD UCBNAME HERE               #10
LINEOUT  PUTLINE MF=L              LIST FORMAT OF PUTLINE           #5
************* @MK @MK @MK *******************************
*     UCB SCAN AREAS                                            @MK
PARMLIST DS    3F         PARMLIST MAPPING                      @MK
         ORG   PARMLIST                                         @MK
PARMWA   DS    F          ADDRESS OF 100-BYTE WKAREA          @MK
PARMDEVT DS    F          ADDR OF BYTE CONTAINING DEVTYPE TO SEARCH
PARMUCB  DS    F          ADDRESS OF WORD TO CONTAIN UCB ADDR   @MK
         SPACE                                                , @MK
DEVCLASS DS    CL1        BYTE CONTAINING DEVICE CLASS TO BE SEARCHED
ADDRUCB  DS    F          UCB ADDRESSES PLACED HERE BY UCB SCAN @MK
         DS    0D         ALIGN WKAREA ON D-WORD BOUNDARY     @MK
WKAREA   DS    CL100      WKAREA                              @MK
DEVNUM   DS    F          UCBNAME                               @MK
@SL      EQU   ((*-SAVEAREA+8/16)*16) LENGTH OF WORKAREA            #2
         EJECT
UCBLIS   UCBSCAN MF=(L,UCBSLST)                               @GTG
IOCINF   IOCINFO MF=(L,IOCINFP)                               @GTG
VOLS     CSECT
         PRINT ON,NOGEN                                             #3
PCLCSECT IKJPARM DSECT=PCLDSECT                                     #3
         SPACE
VOLCLEAR IKJKEYWD ,                                                 #6
         IKJNAME 'CLEAR'                                            #6
         SPACE
VOLPTYPE IKJKEYWD DEFAULT='TYPE'                                    #3
         IKJNAME  'TYPE',SUBFLD=$VOLTYPE                            #3
         IKJNAME  'ATTRIBUTE',SUBFLD=$VOLTYPE                       #3
         IKJNAME  'DEVICETYPE',ALIAS='DEVT',SUBFLD=$VOLTYPE         #3
         SPACE
VOLPNAME IKJKEYWD ,                                                 #5
         IKJNAME  'SERIAL',SUBFLD=$VOLNAME                          #5
         IKJNAME  'VOLSER',SUBFLD=$VOLNAME                          #5
         SPACE
VOLPADDR IKJKEYWD ,                                                 #5
         IKJNAME  'ADDRESS',SUBFLD=$VOLADDR                         #5
         IKJNAME  'UCBNAME',SUBFLD=$VOLADDR                         #5
         SPACE
$VOLTYPE IKJSUBF
VOLTYPE  IKJIDENT 'SELECTION VALUE',UPPERCASE,MAXLNTH=8,            #3 X
               FIRST=ALPHANUM,OTHER=ALPHANUM,                       #3 X
               DEFAULT='ALL',HELP='DASD DEVICE SELECTION VALUE'     #3
         SPACE
$VOLNAME IKJSUBF
VOLNAME  IKJIDENT 'SELECTION VALUE',UPPERCASE,MAXLNTH=6,            #5 X
               FIRST=ALPHANUM,OTHER=ALPHANUM,                       #5 X
               PROMPT='VOLUME SERIAL NUMBER - 6 DIGIT MAX',         #5 X
               HELP='DASD VOLSER SELECTION VALUE'                   #5
         SPACE
$VOLADDR IKJSUBF
VOLADDR  IKJIDENT 'SELECTION VALUE',UPPERCASE,MAXLNTH=4,            #5 X
               FIRST=ALPHANUM,OTHER=ALPHANUM,                       #5 X
               PROMPT='UCB ADDRESS - 4 DIGIT MAX',                  #5 X
               HELP='DASD ADDRESS SELECTION VALUE'                  #5
         SPACE
         IKJENDP
         SPACE
         IKJPPL
         SPACE
         IKJCPPL
         SPACE
         IKJIOPL
         SPACE
         DSECT
*        IEFUCBOB                 UCB MACRO                     @GTG
         IEFUCBOB DEVCLAS=DA      UCB MACRO                     @GTG
         CVT   DSECT=YES,PREFIX=YES
         END
/*
//L.SYSLMOD DD DSN=U014706.LOAD(VOLS),DISP=SHR
/*
