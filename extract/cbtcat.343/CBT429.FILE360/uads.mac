*---------------------------------------------------------------------
*
*        THIS TSO COMMAND PROCESSOR WILL ALLOW THOSE USERS WITH
*        OPERATOR ABILITY TO DYNAMICALLY CHANGE THEIR UADS ENTRIES
*        IN THE PSCB.  ONCE CHANGED, IT WILL REMAIN IN EFFECT
*        ACCROSS A RELOGON (UNLESS USING ACF2 IN NOUADS MODE).
*
*        THOSE ENTRIES THAT MAY BE CHANGED AND THEIR ASSOCIATED
*        KEYWORDS ARE LISTED BELOW:
*
*        ID(????)/RESET    CHANGE CURRENT USER-ID TO ???? OR RESET
*                          BACK TO REAL ID. THIS OPERAND IS ALLOWED
*                          ONLY FOR USERS WITH ACCOUNT AUTHORITY,
*                          SINCE ONE MUST INSURE THAT PRIOR TO
*                          LOGOFF THE NEW ID MUST BE RESET.  IF NOT
*                          RESET, THE NEW ID SET WILL BE UPDATED
*                          WITH THE UADS ENTRY FOR THE ACTIVE USER.
*        LISTID/LID        LIST THE CURRENT UADS ID FROM PSCB.
*
*        DEST(????)/NODEST CHANGE THE CURRENT DEFAULT DESTINATION
*                          TO ???? OR NULLIFY IT.
*        LISTDEST/LDEST    LIST CURRENT DEFAULT DESTINATION.
*
*        UNIT(????)/NOUNIT CHANGE THE CURRENT DEFAULT DYNAMIC
*                          ALLOCATION UNITNAME TO ???? OR NULLIFY IT.
*        LISTUNIT/LUNIT    LIST CURRENT DEFAULT UNIT ASSIGNMENT.
*
*        JCL/NOJCL         ALLOW OR DISALLOW BATCH JOB SUBMISSIONS.
*
*        MOUNT/NOMOUNT     ALLOW OR DISALLOW FOREGROUND TAPE/DISK
*                          MOUNT CAPABILITIES.
*
*        PASS/NOPASS       ALLOW OR DISALLOW DATASET PASSWORD CHECKING
*
*---------------------------------------------------------------------
         SPACE
*--------------MODIFICATION.LOG---------------------------------------*
*                                                                     *
*  PROGRAMMER  ×  DATE  ×  SUMMARY OF CHANGES MADE TO THIS PROGRAM    *
*--------------+--------+---------------------------------------------*
* J. MARTIN    ×01/05/82× ADDED LIST KEYWORDS TO "ID" "DEST" AND      *
*              ×        × "UNIT" FIELDS.                              *
*              ×        ×                                             *
*---------------------------------------------------------------------*
         EJECT
         SPACE
         PRINT ON,NOGEN
         SPACE
UADS     BEGIN
         SPACE
*        REGISTER USAGE:
*
*        R0-R8 =====> WORK REGISTERS
*
*        R9    =====> BASE FOR THE PSCB
*        R10   =====> BASE FOR THE PARSE PDE'S
*        R11   =====> BASE FOR THE CPPL
*        R12   =====> BASE FOR UADS (THIS ROUTINE)
         EJECT
*---------------------------------------------------------------------*
*
*        RETRIEVE INITIALIZATION PARMS
*        LINK TO PARSE TO RETRIEVE COMMAND DATA
*
*---------------------------------------------------------------------*
         SPACE
ENTRY010 DS    0H
         LR    R11,R1             SAVE CPPL POINTER
         USING CPPL,R11           PROVIDE ADDRESSABILITY
         MVC   PPLUPT,CPPLUPT     COPY UPT ADDRESS
         MVC   PPLECT,CPPLECT     COPY ECT ADDRESS
         MVC   PPLCBUF,CPPLCBUF   COPY ADDRESS OF COMMAND BUF
         L     R9,CPPLPSCB        ADDR OF PSCB
         USING PSCB,R9
         LA    R1,PPLUPT          PROVIDE LIST FOR PARSE
         LINK  EP=IKJPARS         PARSE COMMAND
         AUTH  ON                 GET  AUTHORIZATION
         MODESET KEY=ZERO
         L     R10,PPLANSR        GET POINTER TO ANSWER AREA
         USING IKJPARMD,R10       PROVIDE ADDRESSABILITY
         SPACE
EXIT010  EQU   *
         B     ENTRY020                GO PROCESS USER-ID OPERANDS
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS USER-ID OPERANDS
*
*---------------------------------------------------------------------*
         SPACE
ENTRY020 DS    0H
         LH    R15,USERID         GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT020            NOT SPECIFIED, SKIP IT
         B     BBB020             RESET SPECIFIED, GO TO IT
         B     AAA020             PROCESS NEW USERID
         B     EEE020             PROCESS LISTID
         B     EEE020             PROCESS LISTID
         SPACE
AAA020   EQU   *
         L     R1,UID             ADDRESS OF NEW USERID
         LH    R3,UID+4           GET SIZE OF ID
         B     DDD020             TO COMMON ROUTINE
         SPACE
BBB020   EQU   *
         L     R3,16              ADDR OF CVT
         L     R3,0(R3)           ADDR OF OLD/NEW PTRS
         L     R3,12(R3)          ADDR OF CURRENT ASCB
         L     R1,176(R3)         ADDR OF REAL USER-ID
         LR    R3,R1
         MVC   PSCBUSER,BLANKS
         LA    R15,7              MAX SIZE OF USERID
         SPACE
LOOP020  EQU   *
         CLI   0(R3),C' '         END OF ID ?
         BE    CCC020             YES - OK
         CLI   0(R3),X'00'        END OF ID ?
         BE    CCC020             YES - OK
         LA    R3,1(R3)           BUMP
         BCT   R15,LOOP020        CHECK ALL BYTES
         SPACE
CCC020   EQU   *
         SR    R3,R1              LENGTH OF USERID
         SPACE
DDD020   EQU   *
         TM    PSCBATR1,PSCBACCT  DOES USER HAVE ACCOUNT AUTHORITY?
         BZ    ERR020             NO, INFORM USER
         STC   R3,PSCBUSRL        STORE LENGTH
         BCTR  R3,R0              DECR FOR EX
         EX    R3,MVCID1          MOVE NEW USERID INTO PSCB
         B     EXIT020            EXIT FROM USER-ID ROUTINE
EEE020   EQU   *
         MVC   MSG0202+20(8),BLANKS    CLEAR THE USERID FIELD
         SR    R3,R3                   CLEAR LENGTH OF OLD USERID
         IC    R3,PSCBUSRL             GET LENGTH OF USERID FIELD
         BCTR  R3,0                    REDUCE FOR EX MVC
         EX    R3,MVCID2               MOVE IN USERID TO MESSAGE
         TPUT  MSG0202,60              MESSAGE TO USER
         B     EXIT020                 EXIT FROM LIST USER-ID RTN
         SPACE
ERR020   EQU   *
         TPUT  MSG0201,60              ISSUE ERROR MESSAGE
         B     EXIT020                 EXIT FROM USER-ID ROUTINE
         SPACE
EXIT020  EQU   *
         B     ENTRY030                GO DO NEXT CHECK
         SPACE
MVCID1   MVC   PSCBUSER(0),0(R1)
MVCID2   MVC   MSG0202+20(0),PSCBUSER
MSG0201  DC    CL60'USER-ID CHANGE NOT AUTHORIZED'
MSG0202  DC    CL60'CURRENT USERID IS -          '
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS UNIT OPERANDS
*
*---------------------------------------------------------------------*
         SPACE
ENTRY030 DS    0H
         LH    R15,UNIT           GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT030            NOT SPECIFIED, SKIP IT
         B     BBB030             NOUNIT SPECIFIED, GO TO IT
         B     AAA030             UNIT SPECIFIED -- GO FOR IT
         B     CCC030             LISTUNIT SPECIFIED
         B     CCC030             LUNIT SPECIFIED
         SPACE
AAA030   EQU   *
         L     R1,UNITN           UNIT SPECIFIED, DO IT
         LH    R2,UNITN+4         GET SIZE OF NAME
         BCTR  R2,R0              DECR FOR EX
         EX    R2,MVCUNIT         MOVE UNIT
         B     EXIT030                 EXIT FROM UNIT
         SPACE
BBB030   EQU   *
         MVC   PSCBGPNM,BLANKS         CLEAR OUT UNIT NAME
         B     EXIT030                 EXIT FROM NOUNIT
         SPACE
CCC030   EQU   *
         MVC   MSG0301+29(8),PSCBGPNM  MOVEIN THE UNIT
         TPUT  MSG0301,60              ISSUE MESSAGE TO USER
         B     EXIT030                 EXIT FROM LISTUNIT
         SPACE
EXIT030  DS    0H
         B     ENTRY040                GO CHECK DEST OPERANDS
         SPACE
MVCUNIT  MVC   PSCBGPNM(0),0(R1)
MSG0301  DC    CL60'CURRENT ALLOCATION UNIT IS -          '
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS DEST OPERANDS
*
*---------------------------------------------------------------------*
ENTRY040 DS    0H
         LH    R15,DEST           GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT040            NOT SPECIFIED, SKIP IT
         B     BBB040             NODEST SPECIFIED, GO TO IT
         B     AAA040             DEST SPECIFIED -- GO FOR IT
         B     CCC040             LISTDEST SPECIFIED
         B     CCC040             LDEST SPECIFIED
         SPACE
AAA040   EQU   *
         MVC   PSCBDEST,BLANKS
         L     R1,DESTN           DEST SPECIFIED, DO IT
         LH    R2,DESTN+4         GET SIZE OF DEST NAME
         BCTR  R2,R0              DECR FOR EX
         EX    R2,MVCDEST         MOVE DEST
         B     EXIT040
         SPACE
BBB040   EQU   *
         MVC   PSCBDEST,BLANKS
         B     EXIT040                 EXIT FROM LIST
         SPACE
CCC040   EQU   *
         MVC   MSG0401+32(8),PSCBDEST  MOVE IN DESTINATION
         TPUT  MSG0401,60              ISSUE MESSAGE TO USER
         B     EXIT040                 EXIT FROM LIST
         SPACE
EXIT040  DS    0H
         B     ENTRY050
         SPACE
MVCDEST  MVC   PSCBDEST(0),0(R1)
MSG0401  DC    CL60'CURRENT OUTPUT DESTINATION IS -          '
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS JCL OPERANDS
*
*---------------------------------------------------------------------*
         SPACE
ENTRY050 DS    0H
         LH    R15,JCL            GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT050            NOT SPECIFIED, SKIP IT        0
         B     AAA050             NOJCL SPECIFIED, GO TO IT      1
         OI    PSCBATR1,PSCBJCL   ALLOW JCL                      2
         B     EXIT050
         SPACE
AAA050   EQU   *
         NI    PSCBATR1,X'FF'-PSCBJCL  RESET JCL
         SPACE
EXIT050  EQU   *
         B     ENTRY060
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS MOUNT OPERANDS
*
*---------------------------------------------------------------------*
         SPACE
ENTRY060 DS    0H
         LH    R15,MOUNT          GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT060            NOT SPECIFIED, SKIP IT          0
         B     AAA060             NOMOUNT SPECIFIED, GO TO IT        1
         OI    PSCBATR1,PSCBVMNT  ALLOW MOUNT                     2
         B     EXIT060
         SPACE
AAA060   EQU   *
         NI    PSCBATR1,X'FF'-PSCBVMNT  RESET MOUNT
         SPACE
EXIT060  DS    0H
         B     ENTRY070
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS PASSWORD OPERANDS
*
*---------------------------------------------------------------------*
         SPACE
ENTRY070 DS    0H
         L     R1,16          GET CVT POINTER
         L     R1,0(R1)       OLD/NEW POINTER
         L     R1,4(R1)       OUR TCB POINTER
         L     R2,180(R1)     JSCB POINTER
         USING IEZJSCB,R2
         OI    JSCBSWT1,JSCBPASS  NO PASSWORD CHECKING
         LH    R15,PASS           GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT070            NOT SPECIFIED, SKIP IT          0
         B     AAA070             NOPASS SPECIFIED, GO TO IT      1
         NI    JSCBSWT1,X'FF'-JSCBPASS  NO PASSWORD CHECKING      2
         B     EXIT070
         SPACE
AAA070   EQU   *
         OI    JSCBSWT1,JSCBPASS  ALLOW PASSWORD CHECKING
         SPACE
EXIT070  DS    0H
         B     RETURN
         DROP  R2
         EJECT
*---------------------------------------------------------------------*
         SPACE
RETURN   DS    0H
         MODESET KEY=ZERO
         AUTH  OFF                GET  RID OF AUTHORIZATION
         L     R15,RC             RESTORE RETURN CODE
         FINISH RC=(R15)
         SPACE 2
*---------------------------------------------------------------------*
BLANKS   DC    81C' '             BLANK FOLLOWED BY 80 BYTE BUFFER
ECB      DC    F'0'               DUMB ECB FOR PARSE
PPLANSR  DC    F'0'               PARSE ANSWER AREA POINTER
RC       DC    F'0'               RETURN CODE SAVE AREA
*---------------------------------------------------------------------*
PPLUPT   DC    F'0'               USER PROFILE TABLE POINTER
PPLECT   DC    F'0'               ENVIRONMENT CONTROL TABLE
PPLECB   DC    A(ECB)             ECB POINTER
PPLPCL   DC    A(IKJPCL)          ADDRESS OF IKJPARM
PPLANS   DC    A(PPLANSR)         PLACE TO PUT ANSWER
PPLCBUF  DC    F'0'               POINTER TO COMMAND BUFFER
*---------------------------------------------------------------------*
         SPACE 2
         LTORG
         EJECT
IKJPCL   IKJPARM
USERID   IKJKEYWD
         IKJNAME 'RESET'
         IKJNAME 'ID',SUBFLD=IDSUBF
         IKJNAME 'LISTID'
         IKJNAME 'LID'
UNIT     IKJKEYWD
         IKJNAME 'NOUNIT'
         IKJNAME 'UNIT',SUBFLD=UNITSUBF
         IKJNAME 'LISTUNIT'
         IKJNAME 'LUNIT'
DEST     IKJKEYWD
         IKJNAME 'NODEST'
         IKJNAME 'DEST',SUBFLD=DESTSUBF
         IKJNAME 'LISTDEST'
         IKJNAME 'LDEST'
JCL      IKJKEYWD
         IKJNAME 'NOJCL'
         IKJNAME 'JCL'
MOUNT    IKJKEYWD
         IKJNAME 'NOMOUNT'
         IKJNAME 'MOUNT'
PASS     IKJKEYWD
         IKJNAME 'NOPASS'
         IKJNAME 'PASS'
IDSUBF   IKJSUBF
UID      IKJPOSIT  USERID,PROMPT='NEW USER-ID TO BE USED'
UNITSUBF IKJSUBF
UNITN    IKJIDENT  'UNITNAME',MAXLNTH=8,FIRST=ALPHANUM,OTHER=ALPHANUM, X
               PROMPT='UNIT NAME FOR DEFAULT DEVICE ALLOCATIONS'
DESTSUBF IKJSUBF
DESTN    IKJIDENT  'DEST',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,        X
               PROMPT='DESTID FOR DEFAULT SYSOUT/JOB ROUTING'
         IKJENDP
         SPACE
         PRINT OFF
         IKJCPPL
         IKJPSCB
         IEZJSCB
         SPACE
         END   UADS
