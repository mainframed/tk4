IEFU84 TITLE 'WSRCC MODIFIED SMF RECORD EXIT'
         PRINT GEN
*/* REFER:  IPO1.SAMPLIB(IEFU84)
*/* COMPID: SYSTEM
*/* DOC     THIS IS A SAMPLE SMF EXIT ROUTINE TO SUPRESS WRITING
*/*         SELECTED SMF RECORD TYPES.
*/*
*/* NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE
*/*
*/* THIS MODULE IS SIMPLY LINKEDITTED INTO LPALIB RENT.  FOR TESTING
*/* VIA MLPA, MAKE CERTAIN THAT IT IS NOT IN IEAFIX##.
*/*
*/* NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE NOTE
*/*
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFU84                                    *
*                                                                     *
*             DESCRIPTIVE NAME = WSRCC MODIFIED SMF RECORD EXIT.      *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             FUNCTION =                                              *
*             WSRCC HAS ALSO ADDED SUPPORT FOR A USER CVT (WCVT).     *
*             AN AREA IS GETMAINED WHEN U84 IS ENTERED                *
*             FOR THE TYPE 0 IPL SMF RECORD.  THIS AREA IS IN THE     *
*             SYSTEM SUBPOOL 245 - FIXED, NOT FETCH PROTECETED,       *
*             AND STORE PROTECTED.                                    *
*             MODIFICATIONS DONE BY TOM FOTH, JOE MARTIN              *
*             AND JAN HELD.                                           *
*             W.S.R.C.C.   03/31/81  SYSTEMS SOFTWARE UNIT.           *
*                                                                     *
*             DEPENDENCIES = NONE                                     *
*                                                                     *
*             RESTRICTIONS = NONE                                     *
*                                                                     *
*                                                                     *
*             MODULE TYPE = EXECUTABLE
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                ATTRIBUTES = PROTECTION KEY 0, REENTRANT, ENABLED    *
*                                                                     *
*                                                                     *
*             ENTRY POINTS = IEFU84 (ONLY ENTRY POINT)                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A 4 BYTE ADDRESS OF THE    *
*                RECORD DESCRIPTOR WORD (RDW) OF THE SMF RECORD TO    *
*                BE PROCESSED BY THE EXIT.                            *
*                                                                     *
*             OUTPUT= REGISTER 15 MUST CONTAIN ONE OF THE FOLLOWING:  *
*                0 - WRITE THE SMF RECORD (KEEP)                      *
*                4 - DO NOT WRITE THE SMF RECORD (DELETE)             *
*                                                                     *
*             EXTERNAL REFERENCES = NONE                              *
*                                                                     *
*             EXITS - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14  *
*                OUTPUT = NONE                                        *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXITS - ERROR = NONE                                    *
*                OUTPUT = NONE                                        *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             CHARACTER CODE DEPENDENCY = NONE                        *
*                                                                     *
*             NOTES = MACROS USED                                     *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
*--------------MODIFICATION.LOG---------------------------------------*
*  PROGRAMMER  ×  DATE  ×  SUMMARY OF CHANGES MADE TO THIS PROGRAM    *
*--------------+--------+---------------------------------------------*
* JOE MARTIN   ×02/01/81× #1. ADDED USERCVT GETMAIN AND STORE OF      *
*              ×        ×     ADDRESS INTO CVTUSER ON ENTRY WITH      *
*              ×        ×     RECORD TYPE "0".                        *
* JAN HELD     ×10/21/81× #2. ADDED CHECK FOR PREVIOUS STORE OF       *
*              ×        ×     USERCVT ADDRESS INTO CVTUSER PRIOR TO   *
*              ×        ×     GETMAIN AND STORE DESCRIBED IN #1.      *
* JAN HELD     ×10/22/82× #3. MODIFIED THE PROGRAM TO BE ABLE TO      *
*              ×        ×     RUN IN LOCKED OR SRB MODE.              *
* JOE MARTIN   ×11/17/81× #4. REMOVED TIME SVC FROM CODE - IT CAUSED  *
*              ×        ×     SMF TO ABEND BECAUSE OF BEING IN LOCKED *
*              ×        ×     MODE ON ENTRY.  USING THE DATE AND TIME *
*              ×        ×     FROM THE TYPE 0 RECORD INSTEAD.         *
* HELD, MARTIN,×  12/81 × #5. MISC. CHANGES TO ALLOW EXIT TO FUNCTION *
* FOTH         ×        ×     UNDER MVS/SP1 @ 8107.  INCLUDED IS      *
*              ×        ×     CONDITIONALLY GETTING THE SALLOC AND    *
*              ×        ×     LOCAL LOCK IF AN IPL SMF RECORD (TYPE 0)*
*              ×        ×     IS PROCESSED AND A GETMAIN FOR THE LOCAL*
*              ×        ×     CVT IS NECESSARY                        *
* TOM FOTH     ×09/15/82× #6. GENERAL CLEANUP AND ENHANCEMENT.  MOST  *
*              ×        ×     DISPLACEMENTS ARE NOW RESOLVED VIA      *
*              ×        ×     LABELS IN MAPPING DSECTS.  FURTHER, THE *
*              ×        ×     BINARY TIME OF DAY IS CONVERTED TO UN-  *
*              ×        ×     SIGNED PACKED (HHMMSSTH).  FINALLY, NEW *
*              ×        ×     FIELDS (WCVTTLEN, WCVTMLEN, WCVTULEN)   *
*              ×        ×     ARE IMPLEMENTED (FOR TOTAL LENGTH,      *
*              ×        ×     MAPPED LENGTH, AND USED LENGTH.         *
*              ×        ×                                             *
* JOE MARTIN   ×01/03/84× #7. ADDED SUPPORT FOR WCVT LAST MODIFIED    *
*              ×        ×     TIME AND DATE.                          *
*              ×        ×                                             *
*              ×        ×                                             *
*---------------------------------------------------------------------*
         EJECT
IEFU84   CSECT
R00      EQU   0                   USED BY SETLOCK FOR SAVING REGS
R0       EQU   0                   USED BY SETLOCK FOR SAVING REGS
R01      EQU   1                   WORK REGISTER-AT ENTRY ADDR SMF REC
R1       EQU   1                   WORK REGISTER-AT ENTRY ADDR SMF REC
R02      EQU   2                   WORK REGISTER-CLR, TIME RTN
R2       EQU   2                   WORK REGISTER-CLR, TIME RTN
R03      EQU   3                   WORK REGISTER-CLR, TIME RTN
R3       EQU   3                   WORK REGISTER-CLR, TIME RTN
R04      EQU   4                   WORK REGISTER-CLR, TIME RTN
R4       EQU   4                   WORK REGISTER-CLR, TIME RTN
R05      EQU   5                   WORK REGISTER-CLR, TIME RTN
R5       EQU   5                   WORK REGISTER-CLR, TIME RTN
R06      EQU   6                   WORK REGISTER-TIME RTN
R6       EQU   6                   WORK REGISTER-TIME RTN
R07      EQU   7                   GETS ADDRESS OF THE CVT
R7       EQU   7                   GETS ADDRESS OF THE CVT
R08      EQU   8                   POINTS TO USER CVT (WCVT)
R8       EQU   8                   POINTS TO USER CVT (WCVT)
R09      EQU   9                   BASE REGISTER
R9       EQU   9                   BASE REGISTER
R10      EQU   10                  POINTER TO THE SMF RECORD
R11      EQU   11                  LOCKS OBTAINED REGISTER
R12      EQU   12                  NOT USED
R13      EQU   13                  SAVEAREA REG
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  ENTRY ADDRESS & TEMPORARY BASE
         USING *,R15               TEMPORARY BASE FOR
         B     START               BRANCHING AROUND
         DC    CL8'IEFU84  '       MODULE IDENTIFIER
         DC    CL8'*WSRCC**'
         DC    CL8'&SYSDATE'
         DC    CL8' @ &SYSTIME'
START    EQU   *
         STM   R14,R12,12(R13)     SAVE CALLERS REGISTERS
         LR    R09,R15             SET UP ADDRSSABILITY IN R09
         DROP  R15                 DROP R15 FOR ADDRESSABILITY
         USING IEFU84,R09
*
*  CHECK FOR TYPE 0 SMF RECORD
*
         L     R10,0(0,R01)        GET ADDRESS OF SMF RECORD
         USING SMFRCD0,R10
         XR    R03,R03             CLEAR WORKREG
         ICM   R03,X'01',SMF0RTY   GET SMF RECORD TYPE FROM RECORD
         BNZ   RESTREGS            IF NOT TYPE 0, GO AWAY
*
*   CHECK USER CVT FOR ZEROES
*
         L     R07,CVTPTR          POINT AT CVT
         USING CVT,R07             ESTAB. ADDR. FOR CVT
         L     R03,CVTUSER         GET CVTUSER FOR CHECK
         LTR   R03,R03             CHECK FOR PREVIOUS WCVT USER
         BNZ   RESTREGS            IF NOT ZEROES, GO AWAY
*
*   OBTAIN GLOBAL LOCK
*
LOCKUP   SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=USE,              +
               RELATED=(UNLOCK,'GET SALLOC')                        #5.
         C     R15,=F'8'           WAS RETURN CODE 8
         BNE   LOCKOK              NO LOCK WAS OKAY
         SDUMP HDR='WSRCC ERROR, SETLOCK FAILED IN IEFU84',BRANCH=YES, +
               ASID=0
         B     RESTREGS            RETURN WITH NO USER CVT
LOCKOK   LR    R11,R15             SAVE RETURN CODE IN R11
*
*   GET SPACE FOR USERCVT AND FILL WITH GOODIES
*
         GETMAIN RC,LV=WCVTGMSZ,SP=245,BRANCH=(YES,GLOBAL),BNDRY=PAGE
         C     R15,=F'8'
         BNE   GOTMAIN
         SDUMP HDR='WSRCC ERROR, GETMAIN FAILED IN IEFU84',BRANCH=YES, +
               ASID=0
         B     RLSELOCK
GOTMAIN  LR    R08,R01             R08 IS ADDRESS OF WCVT
         USING WCVT,R08            MAP IT
         ST    R08,CVTUSER         STORE WCVT ADDRESS IN CVTUSER
         LR    R02,R08             POINT TO THE WCVT FOR CLEARING
         LA    R03,WCVTGMSZ        LENGTH TO CLEAR
         SR    R04,R04             SOURCE IS INCONSEQUENTIAL
         SR    R05,R05             LENGTH IS ZERO
         MVCL  R02,R04             CLEAR THE AREA
         MVC   WCVTID,WSRCCCVT     MOVE LITERAL INTO WCVT
         MVC   WCVTDATE,SMF0DTE    MOVE IN DATE
         MVC   WCVTSID,SMF0SID     MOVE IN THE SYSTEM ID
         LA    R02,WCVTLEN         GET LENGTH OF MAPPED CVT
         ST    R02,WCVTMLEN        SAVE IT (MAPPED LENGTH)
         ST    R02,WCVTULEN        SAVE IT (USED LENGTH)
         LA    R02,WCVTGMSZ        GET LENGTH OF GETMAIN
         ST    R02,WCVTTLEN        SAVE IT
*
* FOLLOWING CODE CONVERTS BINARY HUNDREDTHS OF SECONDS TO
* UNSIGNED PACKED HHMMSSTH FORMAT.
*
         ICM    R04,B'1111',SMF0TME GET THE TIME
         SRDA   R04,32         MOVE TO R5 W/SIGN EXTENSION
         D      R04,=F'360000' DIVIDE BY 100THS OF MINUTES
         LR     R06,R04        SAVE THE REMAINDER
         M      R04,=F'10000000' SHIFT LEFT
         LR     R02,R05        SAVE IT
         LR     R04,R06        RESTORE REMIANDER TO R4
         SRDA   R04,32         MOVE TO R5 W/SIGN EXTENSION
         D      R04,=F'6000'   DIVIDE BY 100THS SECONDS
         LR     R06,R04        SAVE THE REMAINDER
         M      R04,=F'100000' SHIFT LEFT
         AR     R02,R05        ADD TO RUNNING SUM
         LR     R05,R06        RESTORE THE REMAINDER
         M      R04,=F'10'     SHIFT LEFT
         AR     R02,R05        ADD TO RUNNING SUM
         CVD    R02,WCVTRS01   SAVE TIME
*
* WCVTRS01+3  NOW CONTAINS PACKED UNSIGNED TIME
*
         MVC   WCVTTIME,WCVTRS01+3 MOVE PASCKED TIME TO CVT
         XC    WCVTRS01(8),WCVTRS01 CLEAR TEMP STORAGE
         MVC   WCVTMDTM(8),WCVTTIME MOVE IPL TIME/DATE TO MOD T/D   #7.
*
*   RELEASE LOCAL LOCK, IF NECESSARY, RESTORE REGS, AND RETURN
*
RLSELOCK LTR   R11,R11             Q. WERE ANY LOCKS GOTTEN
         BNZ   RESTREGS            A. NO, NONE TO RELEASE
UNLOCK   SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,                         +
               RELATED=(LOCKUP,'RLSE LOCK')                         #5.
RESTREGS L     R14,12(R13)         RESTORE RETURN ADDRESS
         SR    R15,R15             CLEAR RETURN CODE (ALWAYS RET ZERO)
         LM    R00,R12,20(R13)     RESTORE REGS ZERO THRU 12
         BR    R14                 RETURN
WSRCCCVT DC    CL8'WSRCCCVT'       LITERAL FOR USER CVT (WCVT)
         PRINT NOGEN
         WSRCCCVT
         CVT   PREFIX=NO,DSECT=YES
         IHAPSA DSECT=YES
         IHAWSAVT DSECT=YES,CLASS=GLOBAL
SMFDSCT  DSECT
         IFASMFR 0
         END
