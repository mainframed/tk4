CRSD     TITLE '--- C R S D - CURRENT REGION STATISTICS DISPLAY ---'
CURESTA  START 0
        PRINT  OFF
         GBLC  &PRS,&PRF
         AIF   ('&SYSPARM' EQ 'FULL').SF
         AIF   ('&SYSPARM' EQ 'SHORT').SS
&PRS     SETC  'NOGEN'
&PRF     SETC  'NOGEN'
         AGO   .ST
.SS      ANOP
&PRS     SETC  'GEN'
&PRF     SETC  'NOGEN'
         AGO   .ST
.SF      ANOP
&PRS     SETC  'GEN'
&PRF     SETC  'GEN'
.ST     PRINT  ON
         SPACE 1
* PURPOSE :    DISPLAY (TSO COMMAND) OR LOG (OTHER WAYS OF EXECUTION)
* ---------    THE CURRENT REGION STATISTICS.
*              NAME :       CURESTA
*              ATTRIBUTES : RENT
*
* SYNTAX WHEN USED AS TSO COMMAND (OR SUBCOMMAND) :
* -------------------------------------------------
*                  COMMAND   OPERAND(S)
*                  --------  -----------------------------------------
*                  CURESTA   ID(...) / I(...)    NOLOG / NL
*                                                LOG / L
*              OPTIONAL (KEYWORDS) :
*              ID(...) = IDENTIFY THIS DISPLAY WITH THE GIVEN NUMBER.
*                    I.E. : ID(11)
*                    ALIAS : I(...)
*                    MAXIMUM RANGE FOR ID NUMBER : 0-255.
*              NOLOG = NORMAL USE OF "PUTLINE" TO DISPLAY THE MESSAGES
*                    (THIS IS THE DEFAULT).
*                    ALIAS : NL
*              LOG = FORCE MESSAGES DISPLAY TO GO ON THE JOB LOG (WITH
*                    COPY IN THE MESSAGES LOG) WHEN IN TSO BACKGROUND.
*                    WHEN RUNNING IN TSO FOREGROUND SESSION IT HAS NO
*                    EFFECT (THEY ARE ALWAYS SENDED TO THE TERMINAL).
*                    ALIAS : L
*
* OTHER WAYS OF EXECUTION :
* -------------------------
*              1. AS PROGRAM : //...  EXEC PGM=CURESTA
*                         OR : //...  EXEC PGM=CURESTA,
*                              //          PARM=INNN OR IDNNN
*                      WHERE "NNN" IS THE ID NUMBER.
*              2. AS ROUTINE : XR   R1,R1
*                              CALL CURESTA
*                              ...
*                         OR : XR   R1,R1
*                              CALL CURESTA
*                              B    *+L'*+2
*                              DC   AL1(255),AL1(NNN)
*                              ...
*                              WHERE "NNN" IS THE ID NUMBER.
*          NOTES : THE BRANCH AFTER THE CALL MAY BE A BRANCH REGISTER
*                  OPERATION (BR) ALSO.
*                  THE MESSAGES ARE ALWAYS OUTPUTED ON LOG (UNLESS
*                  WHEN EXECUTED IN TSO FOREGROUND SESSION).
         EJECT
* DEBUGING AIDS : A DEBUG MODE HAS BEEN INCLUDED TO ALLOW AN EASIER
* --------------- WAY OF SEARCH AND TEST FACILITIES (I.E., IF YOU
*                 PLAN TO DO SOME PRIVATE MODIFICATIONS).
*
*        1. WHEN USED AS TSO COMMAND (OR SUBCOMMAND) :
*                  COMMAND   OPERAND(S)
*                  --------  -----------------------------------------
*                  CURESTA   NODEBUG / ND
*                            DEBUG / D
*           OPTIONAL (KEYWORDS) :
*              NODEBUG = NORMAL DISPLAY MESSAGES TAKES PLACE (THIS IS
*                      THE DEFAULT).
*                      ALIAS : ND
*              DEBUG = DISPLAY OF SENSITIVE "LDA" FIELDS.
*                      ALIAS : D
*
*        2. WHEN EXECUTED AS PROGRAM :
*                      //...  EXEC PGM=CURESTA,
*                      //          PARM=D OR DEBUG
*                 OR : //...  EXEC PGM=CURESTA,
*                      //          PARM='INNN,D' OR 'IDNNN,DEBUG'
*                      WHERE "NNN" IS THE ID NUMBER.
*
*        3. WHEN CALLED AS ROUTINE IN A PROGRAM (I.E. ASSEMBLER) :
*                      XR   R1,R1         OR : XR   R1,R1
*                      CALL CURESTA            CALL CURESTA
*                      B    *+L'*+2            B    *+L'*+6
*                      DC   AL1(1),CL1'D'      DC   AL1(5),CL5'DEBUG'
*                      ...                     ...
*                 OR : XR   R1,R1         OR : XR   R1,R1
*                      CALL CURESTA            CALL CURESTA
*                      B    *+L'*+4            B    *+L'*+8
*                      DC   AL1(1),CL1'D'      DC   AL1(5),CL5'DEBUG'
*                      DC   AL1(255),AL1(NNN)  DC   AL1(255),AL1(NNN)
*                      ...                     ...
*                      WHERE "NNN" IS THE ID NUMBER.
*           NOTE : THE BRANCH AFTER THE CALL MAY BE A BRANCH REGISTER
*                  OPERATION (BR) ALSO.
*
* SUPPLIED ASSEMBLER MACRO : ...  $CRS  A,B,C
* --------------------------            (ALL OPERANDS ARE OPTIONAL)
*        WHERE A : IS THE ID NUMBER "NNN" (OR OMITED).
*              B : IS "D" OR "DEBUG" KEYWORD (OR OMITED).
*              C : IS THE BRANCH LABEL OR REGISTER (OR OMITED).
         EJECT
* ORIGIN : EXTRACTED FROM CBT 94 DEC TAPE, FILE 437, MEMBER "STORUSXA"
* --------     (MVS/XA VERSION). REWORKED BY :
*                      MOINIL P.A.
*                      COMPUTING CENTRE (TP 361)
*                      J.R.C. - ISPRA ESTABLISHMENT
*                      21020 ISPRA (VA), ITALY
         SPACE 1
        PRINT  &PRS
        $DEFREG
        $XENT  BASE=R12,LV=WKLEN,TYPE=RENT
        PRINT  GEN
         LR    R10,R13
         USING WKAREA,R10
         LR    R8,R1               RETAIN ENTRY REG. 1
         ST    R14,SVR14           SAVE ENTRY REG. 14
         MVI   SWITCH,0
         BAS   R9,SNDRTNX
        PRINT  &PRS
        $TEW$EC NOTTSO=ANAL,NOTCMD=ANAL,MSG=PUTMSG,PUT=PUTLNE,TIOT=YES
        $TEW$PP PARM=PARMLST,REG=R3,ERR=BEGIN
        PRINT  GEN
         CLI   KWID+1,0            ID SPECIFIED ?
         BE    TSNXT               NO
         TM    KSID+6,X'80'        VALUE PRESENT ?
         BZ    TSNXT               NO
         LH    R14,KSID+4          VALUE LENGTH
         LTR   R14,R14
         BNP   TSNXT               INVALID
         L     R1,KSID             VALUE POINTER
         BCT   R14,*+L'*+6
         PACK  DWK,0(0,R1)         <<EXECUTED>>
         EX    R14,*-6
         CVB   R0,DWK              VALUE IN BINARY NOW
         CH    R0,=H'255'
         BH    TSNXT
         STC   R0,TREQID
         OI    SWITCH,TRQRID
TSNXT    CLC   KWLOG(2),=H'2'      TSO COMMAND OR SUB-COMMAND
         BE    *+L'*+4
         OI    SWITCH,USEPLR
         CLC   KWDBG(2),=H'2'      DEBUG OPTION
         BNE   *+L'*+4
         OI    SWITCH,TRQRUN
         DROP  R3
        IKJRLSA #TSANSW            RELEASE PDL'S SPACE
         OI    SWITCH,TRQDTR
         B     BEGIN
ANAL     LTR   R8,R8               JCL EXEC PARM. FIELD ?
         BZ    TCALL               NO
         USING PSA,R0
         L     R1,PSATOLD          -> TCB
         DROP  R0
         USING TCB,R1
         L     R2,TCBFSA           -> FIRST SAVE AREA OF PROBLEM
         DROP  R1
         CL    R8,24(R2)           IS ENTRY REG. 1 ?
         BNE   TCALL               NO, NOT JCL EXEC PGM=
         OI    SWITCH,TRQDTR
         L     R2,0(R8)            -> PARM. FIELD ADDRESS
         XR    R1,R1
         ICM   R1,B'0011',0(R2)
         BZ    BEGIN               NO PARM. FIELD
         LA    R3,1(R1,R2)
         LA    R4,2(R2)
         LA    R2,1
SCPRM    CLI   0(R4),C'D'
         BE    TSDBG
         CLI   0(R4),C'I'
         BE    TSIDN
SCPRMT   CLI   0(R4),C','
         BNE   ERPARM
SCPRML   BXLE  R4,R2,SCPRM
         B     BEGIN
TSDBG    BXLE  R4,R2,*+L'*+4       CHECK D OR DEBUG
         B     TSDBGX
         CLI   0(R4),C','
         BE    TSDBGX
         LA    R0,4
         LA    R1,=CL4'EBUG'
TSDBGL   CLC   0(1,R4),0(R1)
         BNE   ERPARM
         BCT   R0,*+L'*+4
         B     *+L'*+12
         LA    R1,1(R1)
         BXLE  R4,R2,TSDBGL
         B     ERPARM
         BXLE  R4,R2,*+L'*+4
         B     TSDBGX
         CLI   0(R4),C','
         BNE   ERPARM
TSDBGX   OI    SWITCH,TRQRUN
         CLR   R4,R3
         BNH   SCPRML
         B     BEGIN
TSIDN    BXLE  R4,R2,*+L'*+4       CHECK INNN OR IDNNN
         B     ERPARM
         CLI   0(R4),C'D'
         BNE   *+L'*+8
         BXLE  R4,R2,*+L'*+4
         B     ERPARM
         CLI   0(R4),C'0'
         BL    ERPARM
         CLI   0(R4),C'9'
         BH    ERPARM
         XR    R1,R1
         LR    R14,R1
         LA    R15,15
TSIDNL   IC    R14,0(R4)
         NR    R14,R15
         LTR   R1,R1
         BNP   *+L'*+4
         MH    R1,=H'10'
         AR    R1,R14
         BXLE  R4,R2,*+L'*+4
         B     TSIDNX
         CLI   0(R4),C'0'
         BL    TSIDNX
         CLI   0(R4),C'9'
         BNH   TSIDNL
TSIDNX   CH    R1,=H'255'
         BH    ERPARM
         STC   R1,TREQID
         OI    SWITCH,TRQRID
         CLR   R4,R3
         BNH   SCPRMT
         B     BEGIN
ERPARM   MVC   MSGBUF+2(35),=CL35'<<= JCL-EXEC ... PARM=... ERROR =>>'
         LA    R0,37
         BAS   R9,SNDRTN
         B     LEAVE
TCALL    L     R14,SVR14           GET CALL-RETURN ADDRESS
         MVC   DWK(2),0(R14)
         NI    DWK+1,X'F0'
         LA    R1,2(R14)
         CLC   DWK(2),=XL2'07F0'   CHECK IF FOLLOWED BY BR
         BE    *+L'*+14
         LA    R1,2(R1)
         CLC   DWK(2),=XL2'47F0'   CHECK IF FOLLOWED BY B
         BNE   BEGIN
TCALLL   CLI   0(R1),X'01'         THEN FOLLOWED BY D
         BNE   TCALLN
         CLI   1(R1),C'D'
         BNE   BEGIN
         LA    R1,2(R1)
         B     TCALLD
TCALLN   CLI   0(R1),X'05'         OR FOLLOWED BY DEBUG
         BNE   TCALLI
         CLC   1(5,R1),=CL5'DEBUG'
         BNE   BEGIN
         LA    R1,6(R1)
TCALLD   OI    SWITCH,TRQRUN
         B     TCALLL
TCALLI   CLI   0(R1),X'FF'         OR FOLLOWED BY ID
         BNE   BEGIN
         MVC   TREQID,1(R1)
         LA    R1,2(R1)
         OI    SWITCH,TRQRID
         B     TCALLL
BEGIN    MVC   MSGBUF(31),=CL31'CURRENT REGION STATISTICS FOR :'
         TM    SWITCH,TRQRUN
         BZ    *+L'*+6
         MVC   MSGBUF+15(10),=CL10'DEBUG REQ.'
         L     R1,#TSTIOT          TIOT POINTER
         USING TIODSECT,R1
         MVC   MSGBUF+32(L'TIOCNJOB),TIOCNJOB MOVE NAMES
         MVC   MSGBUF+L'TIOCNJOB+33(L'TIOCSTEP/2),TIOCSTEP
         MVC   MSGBUF+L'TIOCNJOB+(L'TIOCSTEP/2)+34(L'TIOCSTEP/2),TIOCSTX
               EP+(L'TIOCSTEP/2)
         LA    R0,35+L'TIOCNJOB+L'TIOCSTEP
         TM    SWITCH,TRQRID
         BZ    TRQR14
         MVC   MSGBUF+L'TIOCNJOB+L'TIOCSTEP+35(9),=CL9'/ ID ==>>'
         XR    R1,R1
         IC    R1,TREQID
         CVD   R1,DWK
         UNPK  MSGBUF+L'TIOCNJOB+L'TIOCSTEP+45(3),DWK+L'DWK-2(2)
         OI    MSGBUF+L'TIOCNJOB+L'TIOCSTEP+47,C'0'
         AH    R0,=H'13'
         B     NOTTRQ
TRQR14   TM    SWITCH,TRQDTR
         BO    NOTTRQ
         MVC   MSGBUF+L'TIOCNJOB+L'TIOCSTEP+35(7),=CL7'/ R14 ='
         LA    R1,MSGBUF+L'TIOCNJOB+L'TIOCSTEP+43
         LA    R2,SVR14
         BAS   R9,HEXADD
         AH    R0,=H'17'
         DROP  R1
NOTTRQ   BAS   R9,SNDRTN
        $TSWXA 31,EXPAND=ONLY
         USING PSA,R0
         L     R1,PSAAOLD          -> ASCB
         DROP  R0
         USING ASCB,R1
         L     R8,ASCBLDA          -> LDA
         DROP  R1
         USING LDA,R8
         MVC   XDASTRTA,LDASTRTA
         MVC   XDASIZA,LDASIZA
         MVC   XDAESTRA,LDAESTRA
         MVC   XDAESIZA,LDAESIZA
         MVC   XDASTRTS,LDASTRTS
         MVC   XDASIZS,LDASIZS
         MVC   XDAESTRS,LDAESTRS
         MVC   XDAESIZS,LDAESIZS
         MVC   XDASTRTR,LDASTRTR
         MVC   XDASIZR,LDASIZR
         MVC   XDAESTRR,LDAESTRR
         MVC   XDAESIZR,LDAESIZR
         MVC   XDACRGTP,LDACRGTP
         MVC   XDAERGTP,LDAERGTP
         MVC   XDAREGRQ,LDAREGRQ
         MVC   XDALIMIT,LDALIMIT
         MVC   XDAVVRG,LDAVVRG
         MVC   XDAELIM,LDAELIM
         MVC   XDAEVVRG,LDAEVVRG
         MVC   XDASMFL,LDASMFL
         MVC   XDASMFR,LDASMFR
         MVC   XDASMFEL,LDASMFEL
         MVC   XDASMFER,LDASMFER
         DROP  R8
        $TSWXA 24,EXPAND=ONLY
         TM    SWITCH,TRQRUN
         BZ    PROCEED
         MVC   MSGBUF(8),=CL8'LDASTRTA'
         LA    R1,MSGBUF+10
         LA    R2,XDASTRTA
         BAS   R9,HEXADD
         L     R3,XDASTRTA
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF+1(7),=CL7'LDASIZA'
         LA    R1,MSGBUF+10
         LA    R2,XDASIZA
         BAS   R9,HEXADD
         L     R3,XDASIZA
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAESTRA'
         LA    R1,MSGBUF+10
         LA    R2,XDAESTRA
         BAS   R9,HEXADD
         L     R3,XDAESTRA
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAESIZA'
         LA    R1,MSGBUF+10
         LA    R2,XDAESIZA
         BAS   R9,HEXADD
         L     R3,XDAESIZA
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDASTRTS'
         LA    R1,MSGBUF+10
         LA    R2,XDASTRTS
         BAS   R9,HEXADD
         L     R3,XDASTRTS
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF+1(7),=CL7'LDASIZS'
         LA    R1,MSGBUF+10
         LA    R2,XDASIZS
         BAS   R9,HEXADD
         L     R3,XDASIZS
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAESTRS'
         LA    R1,MSGBUF+10
         LA    R2,XDAESTRS
         BAS   R9,HEXADD
         L     R3,XDAESTRS
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAESIZS'
         LA    R1,MSGBUF+10
         LA    R2,XDAESIZS
         BAS   R9,HEXADD
         L     R3,XDAESIZS
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDASTRTR'
         LA    R1,MSGBUF+10
         LA    R2,XDASTRTR
         BAS   R9,HEXADD
         L     R3,XDASTRTR
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF+1(7),=CL7'LDASIZR'
         LA    R1,MSGBUF+10
         LA    R2,XDASIZR
         BAS   R9,HEXADD
         L     R3,XDASIZR
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAESTRR'
         LA    R1,MSGBUF+10
         LA    R2,XDAESTRR
         BAS   R9,HEXADD
         L     R3,XDAESTRR
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAESIZR'
         LA    R1,MSGBUF+10
         LA    R2,XDAESIZR
         BAS   R9,HEXADD
         L     R3,XDAESIZR
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDACRGTP'
         LA    R1,MSGBUF+10
         LA    R2,XDACRGTP
         BAS   R9,HEXADD
         L     R3,XDACRGTP
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAERGTP'
         LA    R1,MSGBUF+10
         LA    R2,XDAERGTP
         BAS   R9,HEXADD
         L     R3,XDAERGTP
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAREGRQ'
         LA    R1,MSGBUF+10
         LA    R2,XDAREGRQ
         BAS   R9,HEXADD
         L     R3,XDAREGRQ
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDALIMIT'
         LA    R1,MSGBUF+10
         LA    R2,XDALIMIT
         BAS   R9,HEXADD
         L     R3,XDALIMIT
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF+1(7),=CL7'LDAVVRG'
         LA    R1,MSGBUF+10
         LA    R2,XDAVVRG
         BAS   R9,HEXADD
         L     R3,XDAVVRG
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF+1(7),=CL7'LDAELIM'
         LA    R1,MSGBUF+10
         LA    R2,XDAELIM
         BAS   R9,HEXADD
         L     R3,XDAELIM
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDAEVVRG'
         LA    R1,MSGBUF+10
         LA    R2,XDAEVVRG
         BAS   R9,HEXADD
         L     R3,XDAEVVRG
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF+1(7),=CL7'LDASMFL'
         LA    R1,MSGBUF+10
         LA    R2,XDASMFL
         BAS   R9,HEXADD
         L     R3,XDASMFL
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF+1(7),=CL7'LDASMFR'
         LA    R1,MSGBUF+10
         LA    R2,XDASMFR
         BAS   R9,HEXADD
         L     R3,XDASMFR
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDASMFEL'
         LA    R1,MSGBUF+10
         LA    R2,XDASMFEL
         BAS   R9,HEXADD
         L     R3,XDASMFEL
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         MVC   MSGBUF(8),=CL8'LDASMFER'
         LA    R1,MSGBUF+10
         LA    R2,XDASMFER
         BAS   R9,HEXADD
         L     R3,XDASMFER
         BAS   R9,DMKRTNT
         BAS   R9,SNDRTNT
         B     LEAVE
PROCEED  MVC   MSGBUF+11(20),=CL20'SYSTEM REGION SIZE :'
         L     R3,XDASTRTA
         BAS   R9,DMKRTNP
         BAS   R9,SNDRTNP
         MVC   MSGBUF+6(25),=CL25'SYSTEM REGION SIZE USED :'
         L     R3,XDASTRTS
         BAS   R9,DMKRTNP
         BAS   R9,SNDRTNP
         MVC   MSGBUF+8(23),=CL23'REQUESTED REGION SIZE :'
         L     R3,XDAREGRQ
         BAS   R9,DMKRTNP
         LA    R0,51
         BAS   R9,SNDRTN
         MVC   MSGBUF+39(6),=CL6'< 16 M'
         MVC   MSGBUF+61(12),=CL12'> 16 M (EXT)'
         BAS   R9,SNDRTNP
         MVI   MSGBUF+32,C'-'
         MVC   MSGBUF+33(18),MSGBUF+32
         MVI   MSGBUF+54,C'-'
         MVC   MSGBUF+55(18),MSGBUF+54
         BAS   R9,SNDRTNP
         MVC   MSGBUF+12(19),=CL19'CURRENT SIZE USED :'
         L     R3,XDACRGTP         CURRENT HIGH ADDRESS OF PVT
         S     R3,XDASTRTA
         BAS   R9,DMKRTNP
         L     R3,XDAERGTP         CURRENT HIGH ADDRESS OF PVT EXT.
         S     R3,XDAESTRA
         BAS   R9,DMKRTNE
         BAS   R9,SNDRTNP
         MVC   MSGBUF+7(24),=CL24'MAX. PRIVATE AREA SIZE :'
         L     R3,XDASIZA
         BAS   R9,DMKRTNP
         L     R3,XDAESIZA
         BAS   R9,DMKRTNE
         BAS   R9,SNDRTNP
         MVC   MSGBUF+7(24),=CL24'V=V REGION LIMIT VALUE :'
         L     R3,XDALIMIT
         BAS   R9,DMKRTNP
         L     R3,XDAELIM
         BAS   R9,DMKRTNE
         BAS   R9,SNDRTNP
         MVC   MSGBUF+8(23),=CL23'V=V REGION HIGH VALUE :'
         L     R3,XDAVVRG
         BAS   R9,DMKRTNP
         L     R3,XDAEVVRG
         BAS   R9,DMKRTNE
         BAS   R9,SNDRTNP
         L     R3,XDASMFL          SMF IEALIMIT
         LTR   R3,R3
         BNP   *+L'*+4
         BAS   R9,DMKRTNP
         L     R3,XDASMFEL
         LTR   R3,R3
         BNP   *+L'*+4
         BAS   R9,DMKRTNE
         CLI   MSGBUF+50,C' '
         BNE   *+L'*+8
         CLI   MSGBUF+72,C' '
         BE    *+L'*+10
         MVC   MSGBUF+10(21),=CL21'V=V SMF LIMIT VALUE :'
         BAS   R9,SNDRTNP
         L     R3,XDASMFR
         LTR   R3,R3
         BNP   *+L'*+4
         BAS   R9,DMKRTNP
         L     R3,XDASMFER
         LTR   R3,R3
         BNP   *+L'*+4
         BAS   R9,DMKRTNE
         CLI   MSGBUF+50,C' '
         BNE   *+L'*+8
         CLI   MSGBUF+72,C' '
         BE    *+L'*+10
         MVC   MSGBUF+11(20),=CL20'V=V SMF HIGH VALUE :'
         BAS   R9,SNDRTNP
        PRINT  &PRS
LEAVE   $XRET  CC=0,LV=WKLEN,TYPE=RENT
        PRINT  GEN
         SPACE 1
*- - - - HEX. ADDRESS EDIT (R9 = LINK / R1 = A.OUTPUT / R2 = A.ADDRESS)
HEXADD   UNPK  0(9,R1),0(5,R2)
         TR    0(8,R1),TRTHEX-C'0'
         MVI   8(R1),C' '
         BR    R9
         SPACE 1
*- - - - DO M AND K ROUTINE (R9 = LINK / R1 = A.OUTPUT / R3 = VALUE)
DMKRTNT  LA    R1,MSGBUF+20
         B     DMKRTN
DMKRTNP  LA    R1,MSGBUF+31
         B     DMKRTN
DMKRTNE  LA    R1,MSGBUF+53
DMKRTN   LTR   R0,R3               SAVE FOR COMPUTATION IN K
         BNPR  R9
         M     R2,=F'10'           MULTIPLY BY 10
         CL    R3,=A(X'00100000')  OVER 1 MEG
         BL    *+L'*+8             NO, LESS
         D     R2,=A(X'00100000')  YES, DIVIDE BY 1 MEG
         B     *+L'*+2
         XR    R3,R3
         CVD   R3,DWK              CONVERT
         MVC   0(7,R1),=XL7'40202020214B20' FORMAT SIZE IN M
         ED    0(7,R1),DWK+5       MOVE TO OUTPUT
         MVC   7(3,R1),=CL3'M -'
         SRA   R0,10               SIZE IN K
         CVD   R0,DWK              CONVERT
         MVC   11(8,R1),=XL8'4020202020202120' FORMAT SIZE IN K
         ED    11(8,R1),DWK+4      MOVE TO OUTPUT
         MVI   19(R1),C'K'
         BR    R9
         SPACE 1
*- - - - MESSAGE LINE ROUTINE (R9 = LINK / R0 = MSG LENGTH)
SNDRTNT  LA    R0,40
         B     SNDRTN
SNDRTNP  LA    R0,73
SNDRTN   TM    SWITCH,USEPLR
         BZ    *+L'*+12
         LA    R1,MSGBUF
         BAS   R14,PUTLNE
         B     SNDRTNX
         LA    R1,4
         AR    R1,R0
         STCM  R1,B'0011',MSGWTO
         MVC   MSGWTO+2(L'DRCDS1),DRCDS1
         LA    R1,MSGBUF
         AR    R1,R0
         MVC   0(L'DRCDS2,R1),DRCDS2
         LA    R1,MSGWTO
         BAS   R14,PUTMSG
SNDRTNX  MVI   MSGBUF,C' '
         MVC   MSGBUF+1(L'MSGBUF-1),MSGBUF
         BR    R9
         SPACE 1
*- - - - CONSTANTS
         SPACE 1
SKWTO    WTO   '  ',ROUTCDE=11,DESC=7,MF=L
DRCDS1   EQU   SKWTO+2,2
DRCDS2   EQU   *-4,4
TRTHEX   DC    CL16'0123456789ABCDEF'
         SPACE 1
*- - - - LITERAL POOL
         SPACE 1
        LTORG
         SPACE 1
        PRINT  &PRS
PARMLST  IKJPARM  DSECT=IKJPARMD
KWID     IKJKEYWD
         IKJNAME  'ID',SUBFLD=SFID,ALIAS=('I')
KWLOG    IKJKEYWD DEFAULT='NOLOG'
         IKJNAME  'NOLOG',ALIAS=('NL')
         IKJNAME  'LOG',ALIAS=('L')
KWDBG    IKJKEYWD DEFAULT='NODEBUG'
         IKJNAME  'NODEBUG',ALIAS=('ND')
         IKJNAME  'DEBUG',ALIAS=('D')
SFID     IKJSUBF
KSID     IKJIDENT 'ID(0-255)',FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=3
         IKJENDP
        PRINT  GEN
         SPACE 1
*- - - - WORK-AREAS
         SPACE 1
WKAREA   DSECT
         DS    18F                 1ST SAVE AREA
DWK      DS    D
XDASTRTA DS    F                   SENSITIVE "LDA" FIELDS
XDASIZA  DS    F
XDAESTRA DS    F
XDAESIZA DS    F
XDASTRTS DS    F
XDASIZS  DS    F
XDAESTRS DS    F
XDAESIZS DS    F
XDASTRTR DS    F
XDASIZR  DS    F
XDAESTRR DS    F
XDAESIZR DS    F
XDACRGTP DS    F
XDAERGTP DS    F
XDAREGRQ DS    F
XDALIMIT DS    F
XDAVVRG  DS    F
XDAELIM  DS    F
XDAEVVRG DS    F
XDASMFL  DS    F
XDASMFR  DS    F
XDASMFEL DS    F
XDASMFER DS    F
SVR14    DS    F
MSGWTO   DS    F
MSGBUF   DS    CL84
TREQID   DS    AL1
SWITCH   DS    XL1
USEPLR   EQU   X'80'               USE PUTLNE ROUTINE
TRQRUN   EQU   X'40'               DEBUG RUN
TRQRID   EQU   X'20'               DISPLAY ID
TRQDTR   EQU   X'10'               DON'T TRACE R14
        PRINT  &PRS
        $TEW$WA
        PRINT  GEN
WKLEN    EQU   (((*-WKAREA)+7)/8)*8
         SPACE 1
        PRINT  &PRS
        $TEW$DS
        PRINT  &PRF
        IHAPSA DSECT=YES,LIST=YES
        IKJTCB LIST=YES
        IHAASCB
        IHALDA
        PRINT  GEN
         SPACE 1
         END
