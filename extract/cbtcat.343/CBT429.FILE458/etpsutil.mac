UTIL     TITLE '--- E T P S -- ETPSUTIL - UTILITY ROUTINES ---'
         PRINT OFF
         COPY  ETPS$O1             "ETPS" SYSPARM SELECTION
         COPY  ETPSMACS            "ETPS" MACROS
         COPY  ETPSDEFS            "ETPS" DEFINITIONS
         PRINT ON
ETPSUTIL START 0
         SPACE 1
ETPSUTIL AMODE 24
ETPSUTIL RMODE 24
        MIDENT
         SPACE 1
*------- ROUTINES ADDRESSES VECTOR
         SPACE 1
         DC    A(ETPSDYNA,ETPSFDTE)
         EJECT
*------- DYNAMIC ALLOCATION ROUTINE
*        AT ENTRY : R1 = PARM.LIST ADDRESS = A(DYNWORKP)
*                        DYNWORKP = A(DYNWORKS)
*                                   A(DYNRCODE+X'80000000')
*        AT EXIT : R15 = 0 + R0 = 0  - ALL OK
*                  R15 = 15 + R0 = POINTER ON ERROR IN DYNAMIC
*                                  ALLOCATION T.U. LIST
*                  R15 = NON-ZERO RETURN CODE OF SVC 99 + R0 =
*                        ERROR/INFORMATION REASON CODES
*                  AND "DYNRCODE" +0 = R0 REASON CODES
*                                 +4 = R15 RETURN CODE
         CNOP  0,4
         PRINT GEN
         USING *,R12
ETPSDYNA B     32(,R15)
         DC    CL28'  ETPSDYNA &SYSDATE &SYSTIME'
         PRINT &PRF
         STM   R14,R12,12(R13)     SAVE INPUT REGISTERS
         LR    R12,R15             SET PROGRAM BASE REGISTER
         L     R11,0(R1)           PICK UP PARAMETER LIST ADDRESS
         USING PARMLIST,R11
         LR    R2,R13
         LA    R13,SAVEAREA
         USING SAVEAREA,R13
         LR    R0,R13
         L     R1,=A(DYNWRKSZ)
         LA    R14,*
         XR    R15,R15
         MVCL  R0,R14              CLEAR WORK AREA TO BINARY ZEROS
         ST    R2,SAVEAREA+4       STORE FORWARD POINTER
         ST    R13,8(R2)           STORE BACKWARD POINTER
         LA    R10,DYNWORK
         USING S99RB,R10
         ST    R10,REQBKPTR        POINT TO REQUEST BLOCK
         OI    REQBKPTR,S99RBPND
         LA    R1,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK
         STC   R1,S99RBLN
         CLC   DSNAME,=XL44'0'     CHECK FOR UNALLOCATION REQUEST
         BNE   *+L'*+8
         MVI   S99VERB,S99VRBUN    REQUEST UNALLOCATION
         B     *+L'*+8
         MVI   S99VERB,S99VRBAL    REQUEST ALLOCATION
         OI    S99FLG11,S99NOCNV+S99NOMNT
         LA    R8,S99RBEND         PICK UP ADDRESS FOR TEXT POINTERS
         ST    R8,S99TXTPP
         L     R9,=A(4*40)         RESERVE SPACE FOR 40 TEXT POINTERS
         ALR   R9,R8               POINT TO FIRST TEXT UNIT
         USING S99TUNIT,R9
*- - - - DDNAME
         LA    R7,DDNAME-PARMLIST  INITIALIZE 'OFFSET' REGISTER
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BNE   *+L'*+8
         LA    R1,DUNDDNAM
         B     *+L'*+4
         LA    R1,DALDDNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         LA    R14,DDNAME          FIND OUT ACTUAL LENGTH OF DDNAME
         LA    R15,L'DDNAME
         BAL   R1,RR1
         LTR   R15,R15
         BNP   LL1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DDNAME
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BE    MM1                 GO DO REQUEST
*- - - - DSNAME
         LA    R7,DSNAME-PARMLIST  OFFSET
         CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION
         BNE   AA1
         LA    R1,DALDUMMY         S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY           CLEAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUENT         NEXT TEXT UNIT
         B     AA10
AA1      LA    R14,DSNAME
         LA    R15,L'DSNAME
         BAL   R1,RR1              GET DS-NAME LENGTH
         LTR   R15,R15
         BNP   AA2
         LA    R1,DALDSNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DS-NAME
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
*- - - - DSMEMBER
AA2      LA    R7,DSMEMBER-PARMLIST
         LA    R14,DSMEMBER
         LA    R15,L'DSMEMBER
         BAL   R1,RR1              GET DSMEMBER LENGTH
         LTR   R15,R15
         BNP   AA3
         LA    R1,DALMEMBR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSMEMBER
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
*- - - - PASSWORD
AA3      LA    R7,PASSWORD-PARMLIST
         LA    R14,PASSWORD
         LA    R15,L'PASSWORD
         BAL   R1,RR1              GET PASSWORD LENGTH
         LTR   R15,R15
         BNP   AA4
         LA    R1,DALPASSW
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE PASSWORD
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
*- - - - DSSTATUS
AA4      LA    R7,DSSTATUS-PARMLIST
         LA    R14,DSSTATUS
         LA    R15,L'DSSTATUS
         BAL   R1,RR1              GET DSSTATUS LENGTH
         LTR   R15,R15
         BNP   AA5
         LA    R1,DALSTATS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSSTATUS,=CL8'OLD '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'01'
         B     AA4A
         CLC   DSSTATUS,=CL8'MOD '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     AA4A
         CLC   DSSTATUS,=CL8'NEW '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'04'
         B     AA4A
         CLC   DSSTATUS,=CL8'SHR '
         BNE   LL1
         MVI   S99TUPAR,X'08'
AA4A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSNDISP
AA5      LA    R7,DSNDISP-PARMLIST
         LA    R14,DSNDISP
         LA    R15,L'DSNDISP
         BAL   R1,RR1              GET DSNDISP LENGTH
         LTR   R15,R15
         BNP   AA6
         LA    R1,DALNDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSNDISP,=CL8'UNCATLG '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'01'
         B     AA5A
         CLC   DSNDISP,=CL8'CATLG '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     AA5A
         CLC   DSNDISP,=CL8'DELETE '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'04'
         B     AA5A
         CLC   DSNDISP,=CL8'KEEP '
         BNE   LL1
         MVI   S99TUPAR,X'08'      ASSUME KEEP
AA5A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSADISP
AA6      LA    R7,DSADISP-PARMLIST
         LA    R14,DSADISP
         LA    R15,L'DSADISP
         BAL   R1,RR1              GET DSADISP LENGTH
         LTR   R15,R15
         BNP   AA7
         LA    R1,DALCDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSADISP,=CL8'UNCATLG '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'01'
         B     AA6A
         CLC   DSADISP,=CL8'CATLG '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     AA6A
         CLC   DSADISP,=CL8'DELETE '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'04'
         B     AA6A
         CLC   DSADISP,=CL8'KEEP '
         BNE   LL1
         MVI   S99TUPAR,X'08'      ASSUME KEEP
AA6A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSUNIT
AA7      LA    R7,DSUNIT-PARMLIST
         LA    R14,DSUNIT
         LA    R15,L'DSUNIT
         BAL   R1,RR1              GET DSUNIT LENGTH
         LTR   R15,R15
         BNP   AA8
         LA    R1,DALUNIT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSUNIT
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
*- - - - $RESERV1
*- - - - DSVOLSER
AA8      LA    R7,DSVOLSER-PARMLIST
         CLC   DSVOLSER,=XL44'0'
         BNE   AA8A
         LA    R1,DALRTVOL         SET UP TO GET VOL-SER RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,6
         MVC   S99TUPAR(6),=CL24' '
         LA    R1,S99TUPAR
         ST    R1,WKRTNVOL         SAVE ADDRESS OF PARAMETER FIELD
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+6       NEXT TEXT UNIT
         B     AA9
AA8A     LA    R14,DSVOLSER
         LA    R15,L'DSVOLSER
         BAL   R1,RR1              GET VOL-SER LENGTH
         LTR   R15,R15
         BNP   AA9
         LA    R1,DALVLSER
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE VOL-SER
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
*- - - - $RESERV2
*- - - - DSVOLREF
AA9      LA    R7,DSVOLREF-PARMLIST
         LA    R14,DSVOLREF
         LA    R15,L'DSVOLREF
         BAL   R1,RR1              GET DSVOLREF LENGTH
         LTR   R15,R15
         BNP   AA10
         CLC   DSVOLSER,=CL24' '   CHECK TO SEE THAT DSVOLREF DOES NOT
         BE    AA9A                  CONFLICT WITH DSVOLSER
         CLC   DSVOLSER,=XL44'0'
         BNE   LL1
AA9A     LA    R1,DALVLRDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLREF
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
*- - - - DSFREE
AA10     LA    R7,DSFREE-PARMLIST
         LA    R14,DSFREE
         LA    R15,L'DSFREE
         BAL   R1,RR1              GET DSFREE LENGTH
         LTR   R15,R15
         BNP   AA11
         CLC   DSFREE,=CL8'END   '
         BE    AA11
         CLC   DSFREE,=CL8'CLOSE '
         BNE   LL1
         LA    R1,DALCLOSE         S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY           CLEAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUENT         NEXT TEXT UNIT
AA11     CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION
         BE    CC1                 GO DO DCB PARAMETERS
*- - - - DSLABEL
         LA    R7,DSLABEL-PARMLIST
         LA    R14,DSLABEL
         LA    R15,L'DSLABEL
         BAL   R1,RR1              GET DSLABEL LENGTH
         LTR   R15,R15
         BNP   AA12
         LA    R1,DALLABEL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSLABEL,=CL4'SL  '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     AA11A
         CLC   DSLABEL,=CL4'SUL '
         BNE   LL1
         MVI   S99TUPAR,X'08'
AA11A    ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSINOUT
AA12     LA    R7,DSINOUT-PARMLIST
         LA    R14,DSINOUT
         LA    R15,L'DSINOUT
         BAL   R1,RR1              GET DSINOUT LENGTH
         LTR   R15,R15
         BNP   AA13
         LA    R1,DALINOUT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSINOUT,=CL4'IN '
         BNE   *+L'*+8
         MVI   S99TUPAR,VLB        SET "VL" BIT
         B     AA12A
         CLC   DSINOUT,=CL4'OUT '
         BNE   LL1
         MVI   S99TUPAR,X'40'
AA12A    ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - $RESERV3
AA13     LA    R7,$RESERV3-PARMLIST
         CLC   $RESERV3,=CL24' '
         BNE   LL1
         CLC   DSSTATUS,=CL24' '   CHECK FOR NEW ALLOCATION
         BE    BB1
         CLC   DSSTATUS,=CL8'NEW '
         BNE   CC1                 GO DO DCB PARAMETERS
*- - - - DSPWDLBL
BB1      LA    R7,DSPWDLBL-PARMLIST
         LA    R14,DSPWDLBL
         LA    R15,L'DSPWDLBL
         BAL   R1,RR1              GET DSPWDLBL LENGTH
         LTR   R15,R15
         BNP   BB2
         LA    R1,DALPASPR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSPWDLBL,=CL8'PASSWORD'
         BNE   *+L'*+8
         MVI   S99TUPAR,X'10'
         B     BB1A
         CLC   DSPWDLBL,=CL8'NOPWREAD'
         BNE   LL1
         MVI   S99TUPAR,X'30'
BB1A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSDATE
BB2      LA    R7,DSDATE-PARMLIST
         LA    R14,DSDATE
         LA    R15,L'DSDATE
         BAL   R1,RR1              GET DSDATE LENGTH
         LTR   R15,R15
         BNP   BB3
         CLC   DSDATE(6),=CL6'EXPDT=' CHECK FOR DATE TYPE
         BNE   BB2A
         SH    R15,=H'6'
         CH    R15,=H'5'           CHECK FOR VALID LENGTH
         BNE   LL1
         LA    R14,6(0,R14)        BUMP POINTER FOR MVCPARM
         LA    R1,DALEXPDT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE EXPDT DATE
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
         B     BB3
BB2A     CLC   DSDATE(6),=CL6'RETPD='
         BNE   LL1                 ERROR
         SH    R15,=H'6'
         BNP   LL1
         CH    R15,=H'4'
         BH    LL1
         LA    R14,6(0,R14)        BUMP POINTER TO START OF NUMBER
         BAL   R1,RR2              EDIT AND CONVERT RETPD
         LA    R1,DALRETPD
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR        PLACE RETPD VALUE IN TEXT UNIT
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT
*- - - - DSALLOC
BB3      LA    R7,DSALLOC-PARMLIST
         LA    R14,DSALLOC
         LA    R15,L'DSALLOC
         BAL   R1,RR1              GET DSALLOC LENGTH
         LTR   R15,R15
         BNP   LL1                 ERROR
         CLC   DSALLOC,=CL5'TRK '  CHECK FOR TRACK ALLOCATION
         BNE   BB3A
         LA    R1,DALTRK           S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY           CLEAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUENT         NEXT TEXT UNIT
         B     BB4
BB3A     CLC   DSALLOC,=CL5'CYL '  CHECK FOR CYLINDER ALLOCATION
         BNE   BB3B
         LA    R1,DALCYL           S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY           CLEAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUENT         NEXT TEXT UNIT
         B     BB4
BB3B     BAL   R1,RR2              CHECK FOR AVERAGE BLOCK ALLOCATION
         LA    R1,DALBLKLN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT
*- - - - DSPRI
BB4      LA    R7,DSPRI-PARMLIST
         LA    R14,DSPRI
         LA    R15,L'DSPRI
         BAL   R1,RR1              GET DSPRI LENGTH
         LTR   R15,R15
         BNP   LL1
         BAL   R1,RR2              CHECK FOR PRIMARY QUANTITY
         LA    R1,DALPRIME
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT
*- - - - DSSEC
         LA    R7,DSSEC-PARMLIST
         LA    R14,DSSEC
         LA    R15,L'DSSEC
         BAL   R1,RR1              GET DSSEC LENGTH
         LTR   R15,R15
         BNP   BB5
         BAL   R1,RR2              CHECK FOR SECONDARY QUANTITY
         LA    R1,DALSECND
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT
*- - - - DSDIR
BB5      LA    R7,DSDIR-PARMLIST
         LA    R14,DSDIR
         LA    R15,L'DSDIR
         BAL   R1,RR1              GET DSDIR LENGTH
         LTR   R15,R15
         BNP   BB6
         BAL   R1,RR2              CHECK FOR DIRECTORY QUANTITY
         LA    R1,DALDIR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT
*- - - - DSRLSE
BB6      LA    R7,DSRLSE-PARMLIST
         LA    R14,DSRLSE
         LA    R15,L'DSRLSE
         BAL   R1,RR1              GET DSRLSE LENGTH
         LTR   R15,R15
         BNP   BB7
         CLC   DSRLSE,=CL8'RLSE '
         BNE   LL1
         LA    R1,DALRLSE          S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY           CLEAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUENT         NEXT TEXT UNIT
*- - - - DSCONTIG
BB7      LA    R7,DSCONTIG-PARMLIST
         LA    R14,DSCONTIG
         LA    R15,L'DSCONTIG
         BAL   R1,RR1              GET DSCONTIG LENGTH
         LTR   R15,R15
         BNP   BB8
         LA    R1,DALSPFRM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSCONTIG,=CL8'CONTIG '
         BNE   LL1
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSROUND
BB8      LA    R7,DSROUND-PARMLIST
         LA    R14,DSROUND
         LA    R15,L'DSROUND
         BAL   R1,RR1              GET DSROUND LENGTH
         LTR   R15,R15
         BNP   BB9
         CLC   DSROUND,=CL8'ROUND '
         BNE   LL1
         LA    R1,DALROUND         S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY           CLEAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUENT         NEXT TEXT UNIT
*- - - - $RESERV4
BB9      LA    R7,$RESERV4-PARMLIST
         CLC   $RESERV4,=CL24' '
         BNE   LL1
*- - - - DSBLKSI
CC1      CLC   DSSTATUS(3),=CL3'NEW' IF ALLOCATION IS NOT NEW
         BNE   MM1
         LA    R7,DSBLKSI-PARMLIST
         LA    R14,DSBLKSI
         LA    R15,L'DSBLKSI
         BAL   R1,RR1              GET DSBLKSI LENGTH
         LTR   R15,R15
         BNP   CC2
         BAL   R1,RR2              CHECK FOR BLKSIZE VALUE
         CH    R15,=H'32767'       CHECK FOR MAXIMUM BLOCKSIZE
         BH    LL1                 ERROR
         LA    R1,DALBLKSZ
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT
*- - - - DSORG
CC2      LA    R7,DSORG-PARMLIST
         CLC   DSORG,=XL44'0'
         BNE   CC2A
         LA    R1,DALRTORG         SET UP TO HAVE DSORG RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         MVC   S99TUPAR(2),=XL44'0'
         LA    R1,S99TUPAR
         ST    R1,WKRTDSRG         SAVE ADDRESS OF PARAMETER FIELD
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT
         B     CC3
CC2A     LA    R14,DSORG
         LA    R15,L'DSORG
         BAL   R1,RR1              GET DSORG LENGTH
         LTR   R15,R15
         BNP   CC3
         LA    R1,DALDSORG
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSORG,=CL8'VSAM '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(0,DS1ORGAM)
         B     CC2B
         CLC   DSORG,=CL8'PO   '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DS1DSGPO,0)
         B     CC2B
         CLC   DSORG,=CL8'POU  '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DS1DSGPO+DS1DSGU,0)
         B     CC2B
         CLC   DSORG,=CL8'DA   '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DS1DSGDA,0)
         B     CC2B
         CLC   DSORG,=CL8'DAU  '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DS1DSGDA+DS1DSGU,0)
         B     CC2B
         CLC   DSORG,=CL8'PS   '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DS1DSGPS,0)
         B     CC2B
         CLC   DSORG,=CL8'PSU  '
         BNE   LL1                 ERROR
         MVC   S99TUPAR(2),=AL1(DS1DSGPS+DS1DSGU,0)
CC2B     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT
*- - - - DSKEYLEN
CC3      LA    R7,DSKEYLEN-PARMLIST
         LA    R14,DSKEYLEN
         LA    R15,L'DSKEYLEN
         BAL   R1,RR1              GET DSKEYLEN LENGTH
         LTR   R15,R15
         BNP   CC4
         BAL   R1,RR2              CHECK FOR KEYLENGTH QUANTITY
         CH    R15,=H'255'         CHECK FOR MAX KEYLENGTH
         BH    LL1                 ERROR
         LA    R1,DALKYLEN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         STC   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSLRECL
CC4      LA    R7,DSLRECL-PARMLIST
         LA    R14,DSLRECL
         LA    R15,L'DSLRECL
         BAL   R1,RR1              GET DSLRECL LENGTH
         LTR   R15,R15
         BNP   CC5
         LA    R1,DALLRECL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSLRECL,=CL5'X '    CHECK FOR SPANNED RECORDS
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL2(32768)
         B     CC4A
         BAL   R1,RR2              CHECK FOR LRECL QUANTITY
         CH    R15,=H'32767'
         BH    LL1
         STH   R15,S99TUPAR
CC4A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT
*- - - - DSRECFM1 THRU DSRECFM8
CC5      LA    R7,DSRECFM1-PARMLIST
         CLC   DSRECFM,=CL24' '    CHECK FOR NO INFORMATION
         BE    CC6
         LA    R1,DALRECFM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSRECFM1,C' '
         BE    CC5A
         CLI   DSRECFM1,C'V'
         BNE   *+L'*+8
         OI    S99TUPAR,DS1RECFV
         B     CC5A
         CLI   DSRECFM1,C'F'
         BNE   *+L'*+8
         OI    S99TUPAR,DS1RECFF
         B     CC5A
         CLI   DSRECFM1,C'U'
         BNE   LL1                 ERROR
         OI    S99TUPAR,DS1RECFU
CC5A     LA    R7,DSRECFM2-PARMLIST
         CLI   DSRECFM2,C' '
         BE    CC5B
         CLI   DSRECFM2,C'B'
         BNE   LL1                 ERROR
         OI    S99TUPAR,DS1RECFB
CC5B     LA    R7,DSRECFM3-PARMLIST
         CLI   DSRECFM3,C' '
         BE    CC5C
         CLI   DSRECFM3,C'S'
         BNE   LL1                 ERROR
         OI    S99TUPAR,DS1RECFS
CC5C     LA    R7,DSRECFM4-PARMLIST
         CLI   DSRECFM4,C' '
         BE    CC5D
         CLI   DSRECFM4,C'T'
         BNE   LL1                 ERROR
         OI    S99TUPAR,DS1RECFT
CC5D     LA    R7,DSRECFM5-PARMLIST
         CLI   DSRECFM5,C' '
         BE    CC5E
         CLI   DSRECFM5,C'M'
         BNE   *+L'*+8
         OI    S99TUPAR,DS1RECMC
         B     CC5E
         CLI   DSRECFM5,C'A'
         BNE   LL1                 ERROR
         OI    S99TUPAR,DS1RECFA
CC5E     LA    R7,DSRECFM6-PARMLIST
         CLI   DSRECFM6,C' '
         BNE   LL1                 ERROR
         LA    R7,DSRECFM7-PARMLIST
         CLI   DSRECFM7,C' '
         BNE   LL1                 ERROR
         LA    R7,DSRECFM8-PARMLIST
         CLI   DSRECFM8,C' '
         BNE   LL1                 ERROR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT
*- - - - DSDCBDS
CC6      LA    R7,DSDCBDS-PARMLIST
         LA    R14,DSDCBDS
         LA    R15,L'DSDCBDS
         BAL   R1,RR1              GET DSDCBDS LENGTH
         LTR   R15,R15
         BNP   CC7
         LA    R1,DALDCBDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCT   R15,MVCPARM+L'MVCPARM
MVCPARM  MVC   S99TUPAR(*-*),0(R14) <<EXECUTED>>
         EX    R15,MVCPARM
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT
*- - - - $RESERV5
CC7      LA    R7,$RESERV5-PARMLIST
         CLC   $RESERV5,=CL24' '
         BE    MM1
LL1      LR    R0,R7               SET UP RETURN CODES ----------------
         LA    R15,15              R15 = 15, R0 = DISPL. (PARMLIST) <==
         B     ZZ1
MM1      SH    R8,=H'4'            BACK UP TO LAST USED TEXT POINTER
         OI    0(R8),S99TUPLN      INDICATE END OF LIST
         LA    R1,REQBKPTR
        DYNALLOC
         L     R0,S99RSC           PICK UP REASON CODES
         LTR   R15,R15             CHECK FOR ERRORS
         BNZ   ZZ1
         L     R1,WKRTNVOL         CHECK FOR VOL-SER RETURN REQUEST
         LTR   R1,R1
         BZ    MM1A
         MVC   DSVOLSER,0(R1)      GIVE CALLER INFO
MM1A     L     R1,WKRTDSRG         CHECK FOR DSORG RETURN REQUEST
         LTR   R1,R1
         BZ    ZZ1
         CLC   0(2,R1),=AL1(0,DS1ORGAM)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'VSAM '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGPO,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'PO   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGPO+DS1DSGU,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'POU  '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGDA,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'DA   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGDA+DS1DSGU,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'DAU  '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGPS,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'PS   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGPS+DS1DSGU,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'PSU  '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGIS,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'IS   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DS1DSGIS+DS1DSGU,0)
         BNE   ZZ1
         MVC   DSORG,=CL8'ISU  '
ZZ1      LR    R1,R13              PREPARE FOR FREEMAIN
         L     R13,SAVEAREA+4
         LTR   R11,R11             CHECK TO SEE IF PARM.LIST IS ONLY
         BNP   ZZ1A                  ENTRY IN CALLER'S ADDRESS LIST
         L     R2,24(0,R13)        GET R1 ENTRY REGISTER
         L     R2,4(0,R2)          GET SECOND ADDRESS IN PARM.LIST
         ST    R0,0(0,R2)          PUT REGISTER INFORMATION IN LIST
         ST    R15,4(0,R2)          " "
ZZ1A     L     R14,12(R13)         PRESERVE R15
         LM    R1,R12,24(R13)      RESTORE CALLING REGISTERS
         BR    R14                 NORMAL RETURN
*- - - - ROUTINE TO DETERMINE LENGTH OF NON-BLANK CHARACTERS IN FIELD
RR1      LTR   R15,R15             AT ENTRY, R1 CONTAINS THE RETURN
         BNPR  R1                  ADDRESS, R14 CONTAINS FIELD ADDRESS,
         ALR   R15,R14             R15 CONTAINS LENGTH OF FIELD.
RR1A     BCTR  R15,0               AT EXIT, R15 WILL BE ALTERED TO
         CLI   0(R15),C' '         CONTAIN LENGTH OF NON-BLANK
         BNE   RR1B                CHARACTERS IN FIELD.
         CLR   R15,R14
         BH    RR1A
         XR    R15,R15
         BR    R1
RR1B     SLR   R15,R14
         LA    R15,1(0,R15)
         BR    R1
*- - - - ROUTINE TO EDIT EBCDIC NUMERIC DATA AND, IF VALID, TO CONVERT
RR2      LTR   R15,R15             IT TO BINARY
         BNPR  R1                  AT ENTRY, R1 CONTAINS THE RETURN
         ST    R15,DBLWORD         ADDRESS, R14 CONTAINS DATA ADDRESS,
         ALR   R15,R14             R15 CONTAINS LENGTH OF DATA.
RR2A     BCTR  R15,0               AT NORMAL EXIT, R15 WILL BE ALTERED
         CLI   0(R15),C'0'         TO CONTAIN THE BINARY EQUIVALENT OF
         BL    LL1                 THE NUMBER.
         CLI   0(R15),C'9'         AN ABNORMAL EXIT WILL BE TAKEN TO
         BH    LL1                 THE COMMON ERROR ROUTINE IF DATA IS
         CLR   R15,R14             NOT NUMERIC.
         BH    RR2A
         L     R15,DBLWORD
         BCT   R15,*+L'*+6
         PACK  DBLWORD,0(*-*,R14)  <<EXECUTED>>
         EX    R15,*-6
         CVB   R15,DBLWORD
         BR    R1
         PRINT &PRS
        LTORG
         PRINT &PRF
         DROP  R9,R10,R11,R12,R13
         EJECT
*------- FORMATS DATE AND TIME ROUTINE
*        AT ENTRY : R1  = POINTER ON WORK-AREA (SEE "FDTWA" DSECT)
*                   R13 = CALLER SAVE AREA
*                   R14 = CALLER RETURN ADDRESS
*                   R15 = ENTRY ADDRESS
*        AT EXIT : R15 = RETURN CODE +0 - OK
*                                    +4 - ERROR
         CNOP  0,4
         PRINT GEN
         USING *,R12
ETPSFDTE B     32(,R15)
         DC    CL28'  ETPSFDTE &SYSDATE &SYSTIME'
         PRINT &PRF
         STM   R14,R12,12(R13)     SAVE CALLER REGISTER
         LR    R12,R15             SET BASE REGISTER
         ST    R13,4(R1)           STORE BACKWARD POINTER
         ST    R1,8(R13)           STORE FORWARD POINTER
         LR    R13,R1
         USING FDTWA,R13
        TIME   ,
         LA    R15,X'0F'           R0 = HHMMSSTH
         SRL   R0,4                R0 = 0HHMMSST
         OR    R0,R15              R0 = 0HHMMSSF
         ST    R0,FDTWK+4
         UNPK  FTM,FDTWK+4(4)
         XR    R0,R0               R1 = 0CYYDDDF
         ST    R0,FDTWK
         SLDL  R0,16               R0 = 00000CYY
         SRL   R1,16               R1 = 0000DDDF
         SLL   R0,4                R0 = 0000CYY0
         OR    R0,R15              R0 = 0000CYYF
         ST    R0,FDTWK+4
         AP    FDTWK,=PL3'+1900'   ADD BASE CENTURY
         OI    FDTWK+L'FDTWK-1,X'0F'
         UNPK  FXYEAR(L'FXYEAR),FDTWK+5(3) SET YEAR IN PLACE
         CVB   R0,FDTWK            R0 = YEAR IN BINARY
         ST    R1,FDTWK+4
         OI    FDTWK+L'FDTWK-1,X'0F'
         UNPK  FXDDD(L'FXDDD),FDTWK+6(2) SET SERIAL DAY IN PLACE
         CVB   R1,FDTWK            R1 = SERIAL DAY IN BINARY
         LR    R4,R1
         CH    R1,=Y(31+28)        DAY PAST FEBRUARY?
         BNH   FDTS3               NO
         LR    R15,R0              LOOK TO SEE
         XR    R14,R14               IF WE ARE
         D     R14,=F'100'             ON A
         LTR   R14,R14                   CENTURY?
         BNZ   FDTS1               NO
         LR    R14,R15             YES, LOOK IF
         SRL   R14,2                 THE CENTURY
         SLL   R14,2                   MUST BE
         CLR   R14,R15                   A LEAP YEAR?
         BE    FDTS3               YES
         B     FDTS2               ELSE ADJUST DAYS
FDTS1    LR    R15,R0              LOOK TO SEE
         SRL   R15,2                 IF FEBRUARY
         SLL   R15,2                   OF THIS YEAR
         CLR   R15,R0                    MUST BE 29 DAYS?
         BE    FDTS3               YES
FDTS2    LA    R1,1(R1)            NO, ADJUST DAYS
FDTS3    LA    R2,12               NOW COMPUTE MONTH
         XR    R3,R3
FDTS4    IC    R3,TMONTH-1(R2)
         SR    R1,R3
         BNP   FDTS5
         BCT   R2,FDTS4
         LA    R15,4 ------------- WRONG YYYYDDD
         B     FDTS99
FDTS5    AR    R1,R3               R1 IS THE DAY IN THE MONTH
         CVD   R1,FDTWK
         OI    FDTWK+7,X'0F'
         UNPK  FXDD(L'FXDD),FDTWK+6(2)
         LA    R3,13
         SR    R3,R2               R3 IS THE MONTH
         CVD   R3,FDTWK
         OI    FDTWK+7,X'0F'
         UNPK  FXMM(L'FXMM),FDTWK+6(2)
         LR    R2,R3
         SLA   R2,1
         AR    R2,R3
         LA    R3,MONTHT-L'MONTHT(R2)
         MVC   FXMONTH(L'FXMONTH),0(R3)
         LR    R3,R0               R3 = YEAR IN BINARY
         LR    R1,R3
         XR    R0,R0
         BCTR  R1,0
         D     R0,=F'100'
         LR    R2,R1
         SRL   R1,2
         SR    R2,R1
         LR    R1,R3
         LR    R0,R1
         BCTR  R1,0
         SRL   R1,2
         AR    R1,R0
         AR    R1,R4               R4 = SERIAL DAY IN BINARY
         SR    R1,R2
         XR    R0,R0
         D     R0,=F'7'
         A     R0,=F'6'            ADJUST SO SUNDAY IS INDEX 0
         CL    R0,=F'7'
         BL    *+L'*+4
         S     R0,=F'7'            ONE DIGIT DAY INDEX
         LR    R2,R0
         SLA   R2,1
         AR    R2,R0
         LA    R2,DAYT(R2)
         MVC   FXDAY(L'FXDAY),0(R2)
         XR    R15,R15             RC = OK
FDTS99   L     R13,4(R13)          GET CALLER SAVE-AREA
         ST    R15,16(R13)         SET RETURN CODE
         LM    R14,R12,12(R13)     RESTORE ALL REGISTERS
         BR    R14                 RETURN TO CALLER
         SPACE 1
TMONTH   DC    AL1(31,30,31,30,31,31,30,31,30,31,29,31)
MONTHT   DC    CL3'JAN',C'FEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'
DAYT     DC    CL3'SUN',C'MONTUEWEDTHUFRISAT'
         PRINT &PRS
        LTORG
         DROP  R12,R13
         EJECT
        PARMLIST
         SPACE 1
        FDATE
         PRINT &PRF
        IECSDSL1 (1)
        IEFZB4D0
        IEFZB4D2
         PRINT GEN
         END
