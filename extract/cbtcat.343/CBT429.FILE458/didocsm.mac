         MACRO
         IEECRDCM    &A=,&KEY=,&DSECT=                         @G860P2G
         SPACE 1                                               @G64UPSS
***********************************************************************
*                                                              @G64UPSS
*          IEECRDCM - RESIDENT DISPLAY CONTROL MODULE          @G64UPSS
*                                                              @G64UPSS
* COPYRIGHT  -                                                     @D1C
*               THIS PRODUCT CONTAINS RESTRICTED MATERIALS OF IBM.
*               5740-XC6 COPYRIGHT IBM CORPORATION 1980, 1984.
*
* STATUS     - OS/VS2 JBB2125                                      @D1C
*                                                              @G64UPSS
* INVOCATION - SPECIFY: IEECRDCM A=,KEY=,DSECT=                @G860P2G
*              WHERE:                                          @G64UPSS
*
*                A=     NUMBER OF DISPLAY AREAS AND THE        @G64UPSS
*                       LENGTHS OF EACH.                       @G64UPSS
*                       EXAMPLE: A=(6,4) WHERE TWO AREAS ARE   @G64UPSS
*                       BEING DEFINED. THE FIRST CONSISTS OF   @G64UPSS
*                       6 LINES; THE SECOND OF 4 LINES.        @G64UPSS
*
*                KEY=   THE NUMBER OF PROGRAM FUNCTION KEYS    @G860P2G
*                       DEFINED FOR THIS DEVICE.               @G860P2G
*
*                DSECT= Y ONLY ONE SACB WILL BE GENERATED.     @G64UPSS
*                       ENTRY STATEMENTS WILL NOT BE           @G64UPSS
*                       GENERATED. DO NOT SPECIFY DSECT= IF    @G64UPSS
*                       DSECT=Y IS NOT WANTED.                 @G64UPSS
*                                                              @G64UPSS
* FUNCTION   - THIS MACRO IS INVOKED AT SYSGEN TO BUILD AN     @G64UPSS
*              RDCM WITH SACBS IN THE NUCLEUS. THE MAPPING     @G64UPSS
*              MACRO FOR THIS CONTROL BLOCK IS IEERDCM.        @G64UPSS
*                                                              @G64UPSS
* CHANGE-ACTIVITY -                                            @G64UPSS
*              G64DPSS, G64UPSS, G811P2G, ZMD0033,             @ZMD0033
* $L1=XMCTASK,  JBB1326,  801031,  PD2I:  ADDED DCMAASID
* $L2=CONCOLOR, JBB1326,  801205,  PDSS:  BASE COLOR SUPPORT
* $P1=PMM0107,  JBB1326,  810402,  PD2I:  BASE ASCB
*              G860P2G - SUPPORT A PFK AREA FOR EACH CONSOLE   @G860P2G
*              ZMC0190 - DEFINE FIELD DCMMSGSV                 @ZMC0190
*              G860PSS - EMBEDDED LABEL LINE SUPPORT           @G860PSS
* $T1=OZ72482,  JBB2110,  830601,  PD2L:  SUPPORT 3279-2C CONSOLE
* $D1=DCR43,    JBB2125,  821225,  PDSS:  3290 SUPPORT
*
***********************************************************************
.*                                                             @G811P2G
.* OPERATION = THIS MACRO DEFINES THE RDCM FIELDS FOR A        @G860P2G
.*             CONSOLE.  IT ALSO BUILDS AN SACB FOR EACH       @G860P2G
.*             DISPLAY AREA DEFINED.                           @G811P2G
.*                                                             @G811P2G
.* MODULE TYPE = MACRO                                         @G811P2G
.*                                                             @G811P2G
.*   PROCESSOR = OS/VS2 ASSEMBLER                              @G811P2G
.*                                                             @G811P2G
.*   MODULE SIZE = N/A                                         @G811P2G
.*                                                             @G811P2G
.*   ATTRIBUTES = N/A                                          @G811P2G
.*                                                             @G811P2G
.* ENTRY POINT = N/A                                           @G811P2G
.*                                                             @G811P2G
.* INPUT = SEE 'INVOCATION' ABOVE FOR A DESCRIPTION OF THE     @G811P2G
.*         INPUT PARAMETERS.                                   @G811P2G
.*                                                             @G811P2G
.* OUTPUT = AN RDCM IS BUILT.  ONE SACB IS BUILT FOR EACH      @G811P2G
.*          DISPLAY AREA DEFINED BY INPUT.                     @G811P2G
.*                                                             @G811P2G
.* EXIT - NORMAL = N/A                                         @G811P2G
.*                                                             @G811P2G
.* EXIT - ERROR = N/A                                          @G811P2G
.*                                                             @G811P2G
.* EXTERNAL REFERENCES = NONE                                  @G811P2G
.*                                                             @G811P2G
.* TABLES = &TABLE   - A TABLE OF SCREEN AREA ID'S             @G811P2G
.*                                                             @G811P2G
.* VARIABLES = THE FOLLOWING LOCAL VARIABLES ARE USED:         @G811P2G
.*                                                             @G811P2G
.*       NAME     FUNCTION                                     @G811P2G
.*       ----     --------                                     @G811P2G
.*       &C       THE ID OF THE AREA CURRENTLY BEING DEFINED   @G811P2G
.*       &D       THE LENGTH OF THE AREA CURRENTLY BEING       @G811P2G
.*                DEFINED                                      @G811P2G
.*       &E       INDEX INTO AREA DEFINITION PARAMETER (&A)    @G811P2G
.*       &F       THE ID OF THE TOPMOST AREA ON THE CONSOLE    @G811P2G
.*       &N       INDEX INTO THE AREA ID TABLE (&TABLE)        @G811P2G
.*       &PFKLEN  THE LENGTH OF THE PFK BUFFER                 @G860P2G
.*       &S       THE TOTAL NUMBER OF SCREEN AREAS DEFINED     @G811P2G
.*                FOR THIS DEVICE                              @G811P2G
.*                                                             @G811P2G
.* MACROS = NONE                                               @G811P2G
.*                                                             @G811P2G
.* MESSAGES = NONE                                             @G860P2G
.*                                                             @G811P2G
.* COMPLETION CODES = N/A                                      @G811P2G
.*                                                             @G811P2G
.**********************************************************************
.*C (.NOTOPAR, .ZZZ)                                           @G811P2G
.*A (.DDD)                                                     @G811P2G
.*C (.FFF)                                                     @ZMD0033
.*A (DCMBCOLR,DCMECOLR) BASE COLOR SUPPORT                         @L2A
.*C (DCMAASCB)                                                     @P1A
.*A (DCMRVRID, DCMRSP21, DCMRVERN, DCMPFKEY, DCMPFKWK)         @G860P2G
.*A (DCMPFKIN, DCMPFKBD, DCMRCBID, DCMPFKSZ, DCMPFKBF)         @G860P2G
.*A (DCMRSV02, &PFKLEN)                                        @G860P2G
.*D (DCMRMS, DCMADRMS, AFTER DCMSUBAD)                         @G860P2G
.*A (DCMMSGSV)                                                 @ZMC0190
.*D (DCMAPAD)                                                  @G860PSS
.*A (DCMAELLF, DCMADLF, DCMANLAB)                              @G860PSS
.* COMMENT AND PROLOGUE CHANGES ONLY                               @T1A
.*A (DCMFAIL)                                                      @D1A
.*A (DCMSPRPQ,DCMEXTDS,DCM14IAD)                                   @D1A
.*A (DCMIMGLT)                                                     @D1A
         EJECT ,                                               @G811P2G
         LCLA  &N,&S,&E,&PFKLEN                                @G860P2G
         LCLC  &TABLE,&C,&D,&F                                 @G860P2G
.**********************************************************************
.*                                                            @G811P2G*
.*       SET VARIABLES TO GENERATE ONE AREA IF THE RDCM IS    @G811P2G*
.*       BEING MAPPED                                         @G811P2G*
.*                                                            @G811P2G*
.**********************************************************************
         AIF   ('&DSECT' EQ '').NODEC
&S       SETA   1
&E       SETA   1
&D       SETC   '1'
         AGO   .POS
.NODEC   ANOP
.**********************************************************************
.*                                                            @G811P2G*
.*       SET AREA PARAMETERS IF THE RDCM IS BEING CREATED     @G811P2G*
.*       AND AREAS ARE DEFINED                                @G811P2G*
.*                                                            @G811P2G*
.**********************************************************************
         AIF   ('&A'  EQ  '').NEG
&S       SETA    N'&A
&E       SETA  1
&D       SETC  '&A(&E)'
         AGO   .POS
.NEG     ANOP
&S       SETA  0
.POS     ANOP
&N       SETA  1
&F       SETC  '0'
&TABLE   SETC  'ABCDEFGHIJK'
         AIF   (&S  EQ  0).NUN
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID
&F       SETC  '&TABLE'(&S,1)
.NUN     ANOP
.**********************************************************************
.*                                                            @G811P2G*
.*       DEFINE THE FIRST PORTION OF THE RDCM                 @G811P2G*
.*                                                            @G811P2G*
.**********************************************************************
DCMADTRN DC    F'0'                POINTER TO PAGEABLE DCM
DCMRSP21 EQU   1                   OS/VS2 HBB2102              @G860P2G
DCMRSP22 EQU   2                   OS/VS2 JBB2220                  -EU-
DCMRVRID EQU   DCMRSP22            CURRENT VERSION                 -EU-
DCMRVERN DC    AL1(DCMRVRID)       CONTROL BLOCK VERSION       @G860P2G
         AIF   (T'&KEY EQ 'O').NOPFKS PFK'S SPECIFIED?         @G860P2G
         AIF   (&KEY EQ 0).NOPFKS  SPECIFIED AS ZERO?          @G860P2G
DCMRFLGS DC    X'80'               FLAGS - PFK'S PRESENT       @G860P2G
         AGO   .SETFLGS            FLAGS ARE SET               @G860P2G
.NOPFKS  ANOP                                                  @G860P2G
DCMRFLGS DC    X'00'               FLAGS
.SETFLGS ANOP                                                  @G860P2G
DCMPFKEY EQU   X'80'               CONSOLE HAS PFK'S           @G860P2G
DCMPFKWK EQU   X'40'               PFK'S ARE OPERATIONAL       @G860P2G
DCMPFKIN EQU   X'20'               PFK INIT. IN PROCESS        @G860P2G
DCMDOM   EQU   X'10'               DOM MUST BE TRIED
DCMNIPP  EQU   X'08'               CONSOLE WAS USED BY NIP     @G64UPSS
DCMPFKBD EQU   X'04'               PFK DEFS ARE NOT FROM DCMLIB AND    *
                                   CAN'T BE SAVED WHEN CHANGED @G860P2G
DCMFAIL  EQU   X'02'               MODEL TDCM COULD NOT BE USED    @D1A
DCMLEN   DC    H'0'                LENGTH OF TRANS DCM
DCMADKP  DC    A(0)                ADDRESS OF ROUTED K COMMAND
*                                       PARAMETER LIST
         AIF   ('&F' EQ '0').NOTOPAR                           @G811P2G
DCMTOPAR DC    C'&F'               TOP DISPLAY AREA DEFINED
         AGO   .TOPDONE                                        @G811P2G
.NOTOPAR ANOP  ,                                               @G811P2G
DCMTOPAR DC    X'00'
.TOPDONE ANOP  ,                                               @G811P2G
DCMTOPDS DC    X'00'               TOP DISPLAY ON SCREEN
DCMPREVM DC    X'00'               CONSOLE'S PREVIOUS OPER. MODE   -EU-
DCMPREFC EQU   X'08'               PREVIOUS MODE WAS FC            -EU-
DCMPREMS EQU   X'04'               PREVIOUS MODE WAS MS            -EU-
DCMPRESD EQU   X'02'               PREVIOUS MODE WAS SD            -EU-
DCMDEVTY DC    X'00'               DEVICE TYPE FLAGS
DCMTY60  EQU   X'80'               USABLE FOR SD              MC X01958
DCMTY50  EQU   X'40'               UNUSABLE FOR SD            MC X01958
DCMBCOLR EQU   X'20'               USABLE FOR BASE COLOR           @L2A
DCMECOLR EQU   X'10'               USABLE FOR EXTENDED COLOR       @L2A
DCMSPRPQ EQU   X'08'               DEVICE SUPPORTS READ PARTITION      X
                                   QUERY FUNCTION                  @D1A
DCMEXTDS EQU   X'04'               DEVICE SUPPORTS EXTENDED DATA       X
                                   STREAM I/O AND 14 BIT ADDRESSES ON  X
                                   OUTBOUND DATA                   @D1A
DCM14IAD EQU   X'02'               DEVICE SUPPORTS 14 BIT ADDRESSES ON X
                                   INBOUND DATA                    @D1A
DCMIMGLT EQU   X'01'               DEVICE HAS AN IMAGE LIMIT       @D1A
         AIF   (&S EQ 0).NONE      IF NO SDS AREAS REQUESTED
DCMADSDS DC    A(DCMACB)           POINTER TO FIRST SDS SUPPORT AREA
         AGO   .MCH                                            @G811P2G
.NONE    ANOP
DCMADSDS DC    F'0'                POINTER TO FIRST SDS SUPPORT AREA
.MCH     ANOP                                                  @G811P2G
DCMRCBID DC    C'RDCM'             CONTROL BLOCK ID            @G860P2G
.YYY     ANOP
.**********************************************************************
.*                                                            @G811P2G*
.*       DEFINE THE REMAINDER OF THE RDCM                     @G811P2G*
.*                                                            @G811P2G*
.**********************************************************************
DCMWLAST DC    F'0'                PT CON Q ENTRY LAST OUT (O-O-L)
DCMRMSAL DC    H'0'                NUMBER LINES IN MSG AREA
DCMDOMKY DC    H'0'                CONSOLE DOM ELEMENT        MC XM5812
DCMRSV04 DC    F'0'                RESERVED                        -EU-
DCMADPFK DC    F'0'                POINTER TO RESIDENT PFK AREA
DCMINTVL DC    H'0'                INTERVAL FOR THIS DCM
DCMTMCTR DC    H'0'                TIME COUNTER FOR THIS DCM
DCMR2FLG DC    X'00'               TIMER FLAGS
DCMRXSFL EQU   X'80'               FULL SCREEN FLAG
DCMRXUNV EQU   X'40'               UNVIEWABLE MESSAGE DISPLAYED
DCMRXTMR EQU   X'20'               TIMER FLAG
DCMRXRLL EQU   X'10'               READY TO ROLL
DCMRXDEL EQU   X'08'               PENDING DELETE REQUEST
DCMRXTIM EQU   X'02'               TIMER ELAPSED FOR THIS DISPLAY
DCMRXDCM EQU   X'01'               T DCM IS IN CORE
DCMR3FLG DC    X'00'               MISC FLAGS
DCMSTSWT EQU   X'80'               CHANGING STATUS OF OUTPUT ONLY CON
DCMKVIP  EQU   X'40'               ENTRY FOR K VARY COMMAND
DCMCLPR  EQU   X'20'               CLOSE IN PROCESS
DCMRXSCN EQU   X'10'               ASY ERROR MESSAGE ON SCREEN
DCMR3PKA EQU   X'08'               DA I/O COMPLETE FLAG
DCMRXHMT EQU   X'04'               FULL SCREEN SIMULATED?     MC YM4102
DCMOPEN  EQU   X'02'                                               -EU-
DCMIFVLD EQU   X'01'                                               -EU-
         AIF   (T'&KEY EQ 'O').NOKEYS   PFK'S SPECIFIED?       @G860P2G
         AIF   (&KEY EQ 0).NOKEYS SPECIFIED AS ZERO?           @G860P2G
&PFKLEN  SETA  (&KEY*110)+19       LENGTH OF PFK AREA IS 110 BYTES     *
                                   PER KEY + 18-BYTE HEADER + ONE-BYTE *
                                   STOPPER                     @G860P2G
&PFKLEN  SETA  (&PFKLEN+7)/8*8     ROUND UP TO DOUBLEWORD      @G860P2G
.NOKEYS  ANOP                                                  @G860P2G
DCMPFKSZ DC    H'&PFKLEN'          LENGTH OF PFK BUFFER        @G860P2G
DCMRQDEL DC    XL8'40'             BUFFER FOR PENDING DELETE REQUESTS
DCMMSGSV DC    F'0'                LINE COUNT SAVE AREA FOR            *
                                   IEECVFTP                    @ZMC0190
DCMPFKBF DC    F'0'                PFK BUFFER ADDRESS          @G860P2G
DCMRSV01 DC    F'0'                RESERVED                    @G860PSS
DCMRSV02 DC    F'0'                RESERVED                    @G860P2G
         AIF   (&S EQ 0).END       IF NO SDS AREAS, DEPART
         EJECT
***********************************************************************
* SCREEN AREA CONTROL BLOCK (SACB)                             @G811P2G
***********************************************************************
         SPACE 1                                               @G811P2G
DCMACB   EQU   *                   SCREEN AREA CONTROL BLOCK
         AIF   (&N EQ &S).ZERO     BRANCH IF LAST AREA
DCMACBNX DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB
         AGO   .AID
.ZERO    ANOP
DCMACBNX DC    A(0)                POINTER TO NEXT SACB
.AID     ANOP
DCMAID   DC    C'&C'               AREA ID
DCMASACB DC    X'80'               SACB FLAGS
DCMAUSE  EQU   X'80'               AREA PRESENTLY DEFINED     MB Y02958
DCMAGM   EQU   X'40'               GETMAINED SACB
DCMALN   DC    FL2'&D'                  LENGTH OF AREA
DCMATOP  DC    X'00'               TOP ROW OF AREA
DCMACLR  EQU   *                   REINITIALIZED PORTION      MB Y02958
DCMAROW  DC    X'00'               ROW TO BE WRITTEN NEXT
DCMAFR   DC    H'00'               FRAME ON SCREEN
DCMAMJWQ DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR
DCMAMIN  DC    A(0)                POINTER TO MINOR WQE
DCMATIME DC    F'0'                TIME CONTROL LINE WAS WRITTEN
DCMANLAB DC    FL1'0'              NUMBER OF LABEL LINES FOUND @G860PSS
DCMARES1 DC    XL1'00'             RESERVED                    @G860PSS
DCMAFLG1 DC    X'00'               AREA FLAGS1
DCMADISP EQU   X'40'               DISPLAY IN AREA
DCMADEND EQU   X'20'               END OF DISPLAY ON SCREEN
DCMAFRPR EQU   X'10'               FRAMING IN PROGRESS
DCMAFULL EQU   X'08'               FRAME FULL
DCMABL   EQU   X'04'               BLANKING TO BE DONE
DCMAELLF EQU   X'02'               EMBEDDED LABEL LINE FOUND   @G860PSS
DCMADLF  EQU   X'01'               DATA LINE FOUND WHILE WRITING       X
                                   DISPLAY                     @G860PSS
DCMAFLG2 DC    X'00'               AREA FLAGS 2
DCMALMIN EQU   X'80'               SAVED POINTER TO LAST MINOR OUTPUT
DCMAWCON EQU   X'40'               WRITE CONTROL LINE
DCMARCON EQU   X'20'               REWRITE CONTROL LINE
DCMAMJFR EQU   X'10'               MAJOR WQE HAS BEEN FOUND
DCMADFLG DC    X'00'               DYNAMIC DISPLAY FLAGS      MB Y02958
DCMADD   EQU   X'80'               TRACK IN AREA              MB Y02958
DCMAHOLD EQU   X'40'               TRACK IN HOLD MODE         MB Y02958
DCMACSIB EQU   X'20'               TRACK WITH CONTINUATION LINE IN     *
                                   SCREEN IMAGE BUFFER        MB Y02958
DCMATRCK DC    X'00'               TRACK REQUEST INDICATORS   MB Y02958
DCMATJOB EQU   X'80'               TRACK JOBS                 MB Y02958
DCMATJBL EQU   X'40'               TRACK JOBS,LIST            MB Y02958
DCMATTS  EQU   X'08'               TRACK TS                   MB Y02958
DCMATTSL EQU   X'04'               TRACK TS,LIST              MB Y02958
DCMATA   EQU   X'88'               TRACK A                    MB Y02958
DCMATAL  EQU   X'44'               TRACK A,LIST               MB Y02958
DCMRSV05 DC    H'0'                RESERVED                        -EU-
DCMATECB DC    F'0'                TRACK CANCEL ECB ADDRESS   MB Y02958
DCMAASCB DC    F'0'                TRACK ASCB POINTER              @P1C
*                                  NOTE: WHEN THE SACB IS
*                                       REINITIALIZED, FIELDS
*                                       DCMAROW THROUGH THE END
*                                       (INCLUDING THE RESERVED
*                                       FIELD) ARE ZERO'D
DCMACBND EQU   *                   END OF SACB
DCMCLRLN EQU   *-DCMACLR           LENGTH FOR REINITIALIZING  MB Y02958
DCMACBID DC    F'0'                ACRONYM = SACB                  -EU-
DCMRSV03 DC    F'0'                RESERVED                        -EU-
DCMACBSZ EQU   *-DCMACB            SIZE OF SACB IN BYTES
.**********************************************************************
.*                                                            @G811P2G*
.*       BUILD THE SCREEN AREA CONTROL BLOCKS (SACB'S) - ONE  @G811P2G*
.*       PER AREA DEFINED                                     @G811P2G*
.*                                                            @G811P2G*
.**********************************************************************
.NEXT    AIF   (&N EQ &S).END      BRANCH IF LAST AREA IS DONE
&N       SETA  &N+1
&E       SETA  &E+1
&C       SETC  '&TABLE'(&N,1)           COMPUTE AREA ID
&D       SETC  '&A(&E)'
         SPACE
***********************************************************************
* SCREEN AREA CONTROL BLOCK (SACB)                             @G811P2G
***********************************************************************
         SPACE 1                                               @G811P2G
         AIF   (&N EQ &S).Z        BRANCH IF LAST AREA
         DC    A(*+DCMACBSZ+DCMPRSZ) POINTER TO NEXT SACB
         AGO   .ID
.Z       ANOP
         DC    A(0)                POINTER TO NEXT SACB
.ID      ANOP
         DC    C'&C'               AREA ID
         DC    X'80'               SACB FLAGS
         DC    FL2'&D'             LENGTH OF AREA
         DC    X'00'               TOP ROW OF AREA
         DC    X'00'               ROW TO BE WRITTEN NEXT
         DC    H'00'               FRAME ON SCREEN
         DC    A(0)                POINTER TO CON Q ENTRY FOR MAJOR
         DC    A(0)                POINTER TO MINOR WQE
         DC    F'0'                TIME CONTROL LINE WAS WRITTEN
         DC    FL1'0'              NUMBER OF LABEL LINES FOUND @G860PSS
         DC    XL1'00'             RESERVED                    @G860PSS
         DC    X'00'               AREA FLAGS1
         DC    X'00'               AREA FLAGS2
         DC    X'00'               DISPLAY FLAGS
         DC    X'00'               TRACK REQUEST INDICATORS   MB Y02958
         DC    H'0'                RESERVED                        -EU-
         DC    F'0'                CANCEL TRACK ECB ADDRESS   MB Y01958
         DC    F'0'                TRACK ASCB POINTER              @P1C
         DC    F'0'                ACRONYM = SACB                  -EU-
         DC    F'0'                RESERVED                        -EU-
         AGO   .NEXT               RETURN TO GENERATE NEXT AREA
.END     MEND
