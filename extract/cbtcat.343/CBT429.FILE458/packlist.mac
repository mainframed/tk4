PCKL     TITLE 'PACKLIST - BUILDER PROGRAM.'
PACKLIST START 0
         SPACE 1
*        DATA SET CBT931 - AT LEVEL 001 AS OF 02/25/79
*                          PACKLIST VERSION 1.0 (WDPSC)
         SPACE 1
*        NOT WRITTEN AT STATE OF WASHINGTON, BUT SUBSTANTIALLY ALTERED
*        ON INPUT SIDE TO USE OUTPUT FROM TRACE TABLE SAMPLE ROUTINE.
         SPACE 1
* PURPOSE :    PACKLIST IS THE MODULE THAT BUILDS THE IEAPAK00 MEMBER
*              OF SYS1.PARMLIB. THE DATA IS OBTAINED BY USING THE
*              PSWSAMP ROUTINE. THE CONSTRUCTION IS BASED ON FREQUENCY
*              OF INTERUPTS IN A GIVEN LPA MODULE. ASSOCIATION BY
*              FREQUENCY IS POSSIBLE, BUT IS NOT ENFORCED.
*
*              THE INTERUPTS IN AND TO LPA ARE COUNTED. THEN THE COUNTS
*              FOR EACH MODULE ARE DIVIDED BY THE LENGTH OF THAT
*              MODULE. THIS INDEX VALUE IS SORTED INTO DESCENDING
*              ORDER. THE PACK LIST IS THEN BUILT, TAKING PAGE
*              BOUNDARIES INTO ACCOUNT. WHEN THIS IS FINISHED, THE
*              INDIVIDUAL RECORDS ARE REVERSED IN ORDER TO PLACE
*              THE MOST ACTIVE CLOSEST TO THE LPA PAGE DIRECTORY ON
*              THE LPA PAGING DATA-SET.
*
*              ALL MEMBERS OF LPALIB SHOULD BE INCLUDED IN THE
*              PACKLIST AT THE FRONT OF IEAPAK00. IF A MEMBER IS NOT
*              INCLUDED IN THE PACK LIST, IT GOES TO THE SPOT DESIRED
*              FOR THE MOST ACTIVE LPA MEMBERS.
         SPACE 1
* INVOKING JCL :
* --------------
*
*        //PACKLIST EXEC PGM=PACKLIST
*        //SYSLPA     DD DSN=... DATA-SET CONTAINING LISTLPA
*                                OUTPUT / AMBLIST OF PSWSAMP
*        //SYSDIR     DD DSN=... DATA-SET CONTAINING LISTPDS
*                                OUTPUT / IEHLIST OF PSWSAMP
*        //SYSPSW     DD DSN=... DATA-SET CONTAINING SAMPLES
*                                GATHERED BY PSWSAMP
*   OR : //SYSPSW     DD DUMMY   IF PSWSAMP HAS RUN WITH PARM=NULL
*        //SYSPUNCH   DD DSN=... OUTPUT DATA-SET FOR IEAPAK00 MODEL
*        //SYSPRINT   DD SYSOUT=A       REPORT ON ACTIVITY
*        //SYSLIST    DD SYSOUT=A       OPTIONAL (SEE PARM. FIELD)
         SPACE 2
* CBT ORIGIN : EXTRACTED FROM CBT TAPE FEB 85, FILE 270.
* ------------ ADAPTED BY : MOINIL P.A.
*                           COMPUTING CENTRE
*                           J.R.C. - ISPRA ESTABLISHMENT
*                           21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG ,                                                  -EU-
*        MISCELLANEOUS DEFINITIONS                                 -EU-
*        -------------------------                                 -EU-
         SPACE 1                                                   -EU-
NLPA     EQU   3000                EST. NO. OF LPA MODULES ENTRIES -EU-
PLNSZ    EQU   131                 POOL LINE SIZE                  -EU-
         SPACE 1                                                   -EU-
SZG      EQU   (((NLPA*TBL)+7)/8)*8     GETMAIN SIZE               -EU-
POOLSZ   EQU   (((61*PLNSZ)+7)/8)*8     GETMAIN SIZE               -EU-
TMVSXA   EQU   X'01'               MVS/XA SYSTEM TYPE              -EU-
SETNOP   EQU   X'0F'               SET NO-OPERATION                -EU-
SETBR    EQU   X'F0'               SET BRANCH                      -EU-
ALL      EQU   X'FF'               SWITCH ALL VALUES ON            -EU-
         SPACE 1                                                   -EU-
*------- SEE IHAPDS MACRO FOR DEFINITION BELOW.                    -EU-
PDS2PAGA EQU   X'20'     PAGE ALIGNMENT REQUIRED FOR LOAD MODULE   -EU-
         EJECT
        $XENT  BASE=(R11,R12)                                      -EU-
         EJECT ,                                                   -EU-
*        GET PARM.FIELD IF ANY SUPPLIED                            -EU-
*        ------------------------------                            -EU-
*        THE OPTIONS, SPECIFIED IN ANY ORDER, MAY BE :             -EU-
*              D - ABEND WITH DUMP ON ERROR DETECTED               -EU-
*                  (NEED SYSUDUMP-DD)                              -EU-
*              L - LIST LISTLPA/LISTPDS OUTPUT (NEED SYSLIST-DD)   -EU-
*              P - LIST IN PLACE OF PUNCH IEAPAK00                 -EU-
*                  (NEED SYSLIST-DD)                               -EU-
*              S - SNAP REQUEST (NEED SYSSNAP-DD)                  -EU-
*              T - USE 62 CHARACTERS LINE PRINT LENGTH             -EU-
*                  TO TSO VIEWING (SYSPRINT-DD)                    -EU-
         SPACE 1                                                   -EU-
         L     R2,0(R1)            GET PARM DATA AREA              -EU-
         LH    R3,0(R2)            DATA LENGTH                     -EU-
         LTR   R3,R3                                               -EU-
         BNP   IT100               BR IF NO PARM                   -EU-
         LA    R1,2(R2)                                            -EU-
         LA    R3,1(R2,R3)                                         -EU-
         LA    R2,1                                                -EU-
SC100    CLI   0(R1),C'S'          SNAP REQUESTED ?                -EU-
         BNE   SC110               NO                              -EU-
         NI    SWSNAP+1,SETNOP     YES, SYSSNAP-DD ASSUMED         -EU-
         B     SC200                                               -EU-
SC110    CLI   0(R1),C'D'          DUMP REQUESTED ?                -EU-
         BNE   SC120               NO                              -EU-
         OI    ABEND+1,SETBR       YES, SYSUDUMP-DD ASSUMED        -EU-
         B     SC200                                               -EU-
SC120    CLI   0(R1),C'T'          TSO REQUESTED ?                 -EU-
         BNE   SC130               NO                              -EU-
         NI    SWPRNT+1,SETNOP     YES, USE 62 LENGTH              -EU-
         B     SC200                                               -EU-
SC130    CLI   0(R1),C'L'          LIST LISTLPA/LISTPDS REQUESTED ?-EU-
         BNE   SC140               NO                              -EU-
         NI    SWLIST+1,SETNOP     YES, SYSLIST-DD ASSUMED         -EU-
         B     SC200                                               -EU-
SC140    CLI   0(R1),C'P'          LIST IEAPAK00 REQUESTED ?       -EU-
         BNE   SC200               NO                              -EU-
         OI    SWITCH,SWPNP        YES, SYSLIST-DD ASSUMED         -EU-
SC200    BXLE  R1,R2,SC100         LOOP IF ANY MORE...             -EU-
         EJECT
*        READ AMBLIST (LISTLPA) INFO FROM PSWSAMP                  -EU-
*        ----------------------------------------                  -EU-
         SPACE 1                                                   -EU-
IT100    XR    R9,R9               SET R.C. = 0                    -EU-
        OPEN   (SYSLPA,(INPUT))    INPUT IS AMBLIST OUTPUT
         LA    R1,101              SET USER ABEND 101 ****************
         TM    SYSLPA+(DCBOFLGS-IHADCB),DCBOFOPN  OPEN SUCCESSFUL ?
         BZ    ABEND               NO
         XR    R2,R2               JUST IN CASE...                 -EU-
         SPACE 1                                                   -EU-
IT110   GET    SYSLPA,PI           SEARCH FOR NUMERIC SECTION... GET
         BAS   R8,SWLIST                                           -EU-
         CLI   CC,C'1'             TITLE PAGE ?
         BNE   IT110               NO... GET NEXT PRINT IMAGE
         CLC   CC+15(L'LPMNUM),LPMNUM   START OF NUMERICAL LISTING ?
         BE    IT120               YES                             -EU-
         CLC   CC+1(L'LPMNEW),LPMNEW    NEW NUMERICAL LISTING ?    -EU-
         BNE   IT110               NO... MUST STILL BE IN ALPHABETIC
         OI    SWITCH,SWNEW        YES... NEW AMBLIST FORMAT       -EU-
IT120   GETMAIN LU,LA=TBSZG,A=TAB  GET THE TABLE WORK AREA         -EU-
         L     R2,TAB              POINT TO IT
         USING TBS,R2              TABLE DSECT
         L     R1,TBSZ             COMPUTE LAST TABLE ENTRY ADDR.  -EU-
         ALR   R1,R2                                               -EU-
         SL    R1,TAB+4                                            -EU-
         ST    R1,TAB+8                                            -EU-
         SPACE 1                                                   -EU-
IT130   GET    SYSLPA,PI           READ PRINT IMAGE
         BAS   R8,SWLIST                                           -EU-
         CLI   CC,C' '             DETAIL LINE ?
         BNE   IT130               NO... IGNORE
         LA    R1,S1               POINT TO FIRST SEGMENT
         BAS   R14,IT200           ADD TO TABLE
         LA    R1,S2               POINT TO SECOND SEGMENT
         BAS   R14,IT200           CONVERT
         B     IT130               GET NEXT RECORD
         SPACE 1
         USING LPAE,R1             SEGMENT DSECT
IT200    CLC   LL+1(4),=CL10' '    BLANK LENGTH (NOT MODULE NAME) ?-EU-
         BER   R14                 YES... IGNORE
         CLI   LN,C'A'             AMBLIST INVALID NAME ?    -ERROR-EU-
         BLR   R14                 YES... IGNORE             -ERROR-EU-
         CL    R2,TAB+8            TABLE OVERFLOW ?                -EU-
         BNH   IT210               NO, OK                          -EU-
         LA    R1,301              SET USER ABEND 301 *************-EU-
         B     ABEND                                               -EU-
IT210    XC    TN(TBL),TN          CLEAR                           -EU-
         MVC   TN,LN               SAVE MODULE NAME
         TM    SWITCH,SWNEW        NEW AMBLIST FORMAT ?            -EU-
         BO    IT220               YES                             -EU-
         TR    LX,TOBIN            CONVERT START AND LENGTH TO BIN
         PACK  TS(L'TS+1),LS(L'LS+1)    SAVE START ADDRESS
         PACK  TL(L'TL+1),LL(L'LL+1)    LENGTH
         B     IT230                                               -EU-
IT220    CLI   LXN,C' '            TWO BLANKS AFTER NAME ?         -EU-
         BNE   *+L'*+4             NO, OK                          -EU-
         LA    R1,1(R1)            YES, ADJUST LINE POINTER        -EU-
         TR    LXN,TOBIN           CONVERT START AND LENGTH TO BIN -EU-
         PACK  TS(L'TS+1),LSN(L'LSN+1)  SAVE START ADDRESS         -EU-
         PACK  TL(L'TL+1),LLN(L'LLN+1)  LENGTH                     -EU-
         DROP  R1                                                  -EU-
IT230    NOP   IT240               ONE TIME SWITCH
         OI    IT230+1,SETBR       CHANGE TO BRANCH
         MVC   LPALOW,TS           SAVE LPA LOW ADDR
         CLC   LPALOW,LPAUP        DOWN 16M LINE ?                 -EU-
         BL    IT250               YES                             -EU-
         OI    IT240+1,SETBR       NO... ONLY UP 16M LINE          -EU-
IT240    NOP   IT250               16M LINE SWITCH                 -EU-
         CLC   TS,LPAUP            DOWN 16M LINE ?                 -EU-
         BL    IT250               YES                             -EU-
         OI    IT240+1,SETBR       CHANGE TO BRANCH                -EU-
         MVC   LPALOWX,TS          SAVE X-LPA LOW ADDR             -EU-
         SL    R2,TAB+4            POINT TO PREVIOUS TABLE ENTRY   -EU-
         ST    R2,TAB16M           16M LINE TABLE POINTER          -EU-
         L     R1,TS               LAST MOD START ADDR             -EU-
         AL    R1,TL               ADD LENGTH OF MOD               -EU-
         ST    R1,LPAHI            SAVE LPA HIGH ADDR              -EU-
         AL    R2,TAB+4            AGAIN TO CURRENT TABLE ENTRY    -EU-
IT250    AL    R2,TAB+4            POINT TO NEXT TABLE ENTRY
         BR    R14                 RETURN
         SPACE 1                                                   -EU-
SWLIST   BR    R8                                                  -EU-
        $PRINT PI,,DDLIST                                          -EU-
         LTR   R15,R15                                             -EU-
         BZR   R8                                                  -EU-
         LA    R1,310(R15)         SET USER ABEND 31X *************-EU-
         B     ABEND                                               -EU-
         SPACE 1
IT900    CL    R2,TAB              TABLE EMPTY ?                   -EU-
         BH    IT910               NO... OK, CONTINUE              -EU-
         LA    R1,302              SET USER ABEND 302 *************-EU-
         B     ABEND                                               -EU-
IT910    SL    R2,TAB+4            SET END OF TABLE
         ST    R2,TAB+8            EOD(SYSLPA)... SAVE TABLE END
         L     R1,TS               LAST MOD START ADDR
         AL    R1,TL               ADD LENGTH OF MOD
         OC    LPAHI,LPAHI         ALREADY STORED ?                -EU-
         BZ    IT920               NO... ONLY ONE LPA AREA         -EU-
         ST    R1,LPAHIX           SAVE X-LPA HIGH ADDR            -EU-
         OI    SWITCH,SW16M        INDICATE UP 16M ALSO            -EU-
         B     IT990                                               -EU-
IT920    ST    R1,LPAHI            SAVE LPA HIGH ADDR
         DROP  R2                                                  -EU-
         SPACE 1
IT990   CLOSE  SYSLPA              DON'T NEED THIS ANYMORE
         EJECT
*        READ IEHLIST (LISTPDS) INFO FROM SAMPLER RUN              -EU-
*        --------------------------------------------              -EU-
         SPACE 1                                                   -EU-
        OPEN   (SYSDIR,(INPUT))    INPUT IS IEHLIST OUTPUT         -EU-
         LA    R1,102              SET USER ABEND 102 *************-EU-
         TM    SYSDIR+(DCBOFLGS-IHADCB),DCBOFOPN OPEN SUCCESSFUL ? -EU-
         BZ    ABEND               NO                              -EU-
         LA    R4,CC+1             IEHLIST PRINT SEGMENT           -EU-
         USING IEHL,R4                                             -EU-
         SPACE 1                                                   -EU-
DR100   GET    SYSDIR,PI           GET                             -EU-
         BAS   R8,SWLIST                                           -EU-
         CLI   CC,C'1'             TITLE PAGE ?                    -EU-
         BE    DR100               YES... GET NEXT PRINT IMAGE     -EU-
         CLC   MARGIN,=CL10' '     DETAIL LINE ?                   -EU-
         BNE   DR100               NO... GET NEXT PRINT IMAGE      -EU-
         CLC   MNAME(9),=CL9' MEMBERS ' SUBTITLE LINE ?            -EU-
         BE    DR100               YES... GET NEXT PRINT IMAGE     -EU-
         LM    R1,R3,TAB           SEARCH TABLE                    -EU-
         USING TBS,R1              TABLE DSECT                     -EU-
DR200    CLC   MNAME,TN            THIS MODULE NAME ?              -EU-
         BE    DR300               YES                             -EU-
         BXLE  R1,R2,DR200         CONTINUE SEARCH                 -EU-
         B     DR100               NOT FOUND... GET NEXT           -EU-
DR300    MVC   DWD(4),MATT1        GET MODULE ATTRIBUTES           -EU-
         MVC   DWD+4(2),MATT2                                      -EU-
         TR    DWD(6),TOBIN        CONVERT TO BINARY               -EU-
         PACK  WORK,DWD(7)                                         -EU-
         MVC   TMATTR1(3),WORK     SET MODULE ATTRIBUTES           -EU-
         B     DR100               GET NEXT PRINT IMAGE            -EU-
         DROP  R1,R4                                               -EU-
         SPACE 1                                                   -EU-
DR900   CLOSE  SYSDIR              DON'T NEED THIS ANYMORE         -EU-
         EJECT
*        READ SAMPLES GATHERED BY PSWSAMP                          -EU-
*        --------------------------------                          -EU-
         SPACE 1                                                   -EU-
        OPEN   (SYSPSW,(INPUT))    PROCESS COUNT DATA
         LA    R1,201              SET USER ABEND 201 ****************
         TM    SYSPSW+(DCBOFLGS-IHADCB),DCBOFOPN  OPEN SUCCESSFULL ?
         BZ    ABEND               NO
         SPACE 1                                                   -EU-
RS100   GET    SYSPSW
         LA    R2,32               TTE LENGTH
         SLR   R1,R2               COMPUTE SAMPLES SCANNING POINTERS
         LH    R3,SYSPSW+(DCBBLKSI-IHADCB)
         AR    R3,R1
RS120    BXH   R1,R2,RS100         SCAN SAMPLES (TRACE TABLE)
         CLC   0(4,R1),=F'-1'      PSW SAMPLER INFO ?              -EU-
         BNE   RS130               BR IF NOT                       -EU-
         MVC   PSI(PSIL),4(R1)     YES... PICK IT                  -EU-
         B     RS120                                               -EU-
RS130    CLI   2(R1),0             REAL TRACE TABLE ENTRY ?        -EU-
         BE    RS120               BR IF NOT                       -EU-
         AP    TTETOT,=PL1'+1'     BUMP COUNTER                    -EU-
         TM    2(R1),X'F0'         CONTROL TYPE CODE ?             -EU-
         BZ    RS120               BR IF NONE OF TYPE CODE
         AP    TTEUSE,=PL1'+1'     BUMP COUNTER                    -EU-
         L     R4,4(,R1)           GET ADDRESS FROM PSW            -EU-
         CL    R4,LPALOW           IN LPA ?                        -EU-
         BNL   RS150               YES... MAY BE
RS140    AP    ASCTR,=PL1'+1'      BUMP A.S. COUNTER               -EU-
         B     RS120               GET ANOTHER TRACE RECORD
RS150    CL    R4,LPAHI            ABOVE LPA ?                     -EU-
         BL    RS200               NO... IT IS IN LPA              -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVATED ?            -EU-
         BZ    RS140               NO...                           -EU-
         CL    R4,LPALOWX          IN X-LPA ?                      -EU-
         BL    RS140               NO...                           -EU-
         CL    R4,LPAHIX           ABOVE X-LPA ?                   -EU-
         BNL   RS140               YES...                          -EU-
RS200    LM    R5,R7,TAB           GET TABLE LIMITS
         USING TBS,R5              TABLE DSECT                     -EU-
RS250    CL    R4,TS               EXCEED MODULE START ?           -EU-
         BL    RS260               YES                             -EU-
         BXLE  R5,R6,RS250         CONTINUE SEARCH                 -EU-
         B     RS270               IF HERE IT IS LAST MODULE       -EU-
RS260    CL    R5,TAB              FIRST MODULE START ?            -EU-
         BE    RS140               YES...                          -EU-
RS270    SLR   R5,R6
         L     R0,TF                                               -EU-
         A     R0,=F'1'            BUMP COUNT COUNT BY ONE         -EU-
         ST    R0,TF               STORE RESULT                    -EU-
         AP    LPACTR,=PL1'+1'     BUMP LPA COUNTER
         B     RS120               GET NEXT RECORD
         DROP  R5                                                  -EU-
         SPACE 1
RS900   CLOSE  SYSPSW
         SPACE 1
         LM    R1,R3,TAB           GET TABLE LIMITS
         USING TBS,R1              TABLE DSECT                     -EU-
         SR    R1,R2
RS910    BXH   R1,R2,ST100         POINT TO NEXT ENTRY
         L     R5,TF               GET NUMBER OF FAULTS
         LTR   R5,R5                                               -EU-
         BNP   RS910                                               -EU-
         M     R4,=F'100000'       ALLOW 5 DECIMAL PLACES
         OC    TL,TL               JUST TO                         -EU-
         BZ    RS920                 BYPASS                        -EU-
         CL    R4,TL                   EVENTUAL                    -EU-
         BL    RS930                     OVERFLOW                  -EU-
RS920    MVC   TFL,=F'99999999'    SET MAXIMUM AS ANSWER           -EU-
         B     RS910               DO FOR NEXT ENTRY
RS930    D     R4,TL               CALC FAULTS / LENGTH
         LTR   R5,R5               JUST TO                         -EU-
         BNP   RS920                 BYPASS                        -EU-
         CL    R5,=F'99999999'         EVENTUAL                    -EU-
         BH    RS920                     TOO BIG VALUE             -EU-
         ST    R5,TFL              SAVE ANSWER
         B     RS910               DO FOR NEXT ENTRY
         DROP  R1                                                  -EU-
         EJECT
*        ORDER TABLE BY DESCENDING FAULTS/BYTE (TFL)
*        -------------------------------------------
         SPACE 1
ST100    MVC   STT(3*4),TAB        SET TABLE BOUNDARIES TO SORT    -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVATED ?            -EU-
         BZ    ST110               NO...                           -EU-
         MVC   STT+8(4),TAB16M     YES... PROCESS DOWN 16M LINE    -EU-
         BAS   R8,ST200            SORT                            -EU-
         L     R1,TAB16M           PROCESS UP 16M LINE             -EU-
         AL    R1,TAB+4                                            -EU-
         ST    R1,STT                                              -EU-
         MVC   STT+8(4),TAB+8                                      -EU-
ST110    BAS   R8,ST200            SORT                            -EU-
         B     PL100                                               -EU-
         SPACE 1                                                   -EU-
ST200    LM    R1,R3,STT           SET UP TO BEGIN THE SORT
         MVI   SORTTEST,0          HAS-SOMETHING-BEEN-MOVED BYTE
         USING TBS,R1              TABLE DSECT                     -EU-
ST210    LR    R4,R1
         BXH   R1,R2,ST230         INCREMENT FOR NEXT ENTRY
         CLC   TFL,TFL-TBS(R4)     COMPARE ON FAULTS PER BYTE
         BH    ST220               EXCHANGE THE ENTRIES
         BNE   ST210               LOW
         CLC   TL,TL-TBS(R4)       EQUAL, COMPARE ON LENGTH
         BNH   ST210
ST220    TS    SORTTEST       SET THE HAS-SOMETHING-BEEN-MOVED BYTE
         XC    0(TBL,R1),0(R4)     EXCHANGE THE ENTRIES
         XC    0(TBL,R4),0(R1)
         XC    0(TBL,R1),0(R4)
         B     ST210
ST230    SR    R3,R2               SHRINK THE TABLE FOR A TIME
         ST    R3,STT+8
         TS    SORTTEST
         BNZ   ST200
         BR    R8                  RETURN TO CALLER                -EU-
         DROP  R1                                                  -EU-
         EJECT
*        COMPUTE PACK LIST (PAGE BOUNDARIES)                       -EU-
*        -----------------------------------                       -EU-
         SPACE 1
PL100    TM    SWPRNT+1,SETBR                                      -EU-
         BO    *+L'*+10                                            -EU-
         MVC   SYSPRINT+(DCBLRECL-IHADCB)(L'DCBLRECL),=AL2(62)     -EU-
         B     PL110                                               -EU-
         L     R0,=A(POOLSZ)                                       -EU-
         LR    R5,R0                                               -EU-
        GETMAIN R,LV=(0)           GET THE POOL OF LINES           -EU-
         ST    R1,APOOL                                            -EU-
         LR    R4,R1               INITIALIZE IT                   -EU-
         LA    R6,*                                                -EU-
         XR    R7,R7                                               -EU-
         ICM   R7,B'1000',=CL10' '                                 -EU-
         MVCL  R4,R6                                               -EU-
         MVI   0(R1),C'1'                                          -EU-
PL110   OPEN   (SYSPRINT,(OUTPUT))
         LA    R1,202              SET USER ABEND 202 ****************
         TM    SYSPRINT+(DCBOFLGS-IHADCB),DCBOFOPN     OPEN OK ?
         BZ    ABEND               NO
         LM    R5,R7,TAB           RESET TABLE LIMITS
         TM    SWITCH,SW16M        16M LINE ACTIVATED ?            -EU-
         BZ    *+L'*+4             NO...                           -EU-
         L     R7,TAB16M           YES... PROCESS DOWN 16M LINE    -EU-
         USING TBS,R5              TABLE DSECT                     -EU-
         SR    R5,R6
         LR    R3,R5
         XR    R2,R2               START PAGE-GROUPS BACKWARD CHAIN-EU-
         XR    R4,R4               START PAGE-MODULES FORWARD CHAIN-EU-
         B     PL350                                               -EU-
         SPACE 1
PL150    LR    R5,R3
         NI    SWITCH,ALL-SWPPB
PL160    BXH   R5,R6,PL200
         CLI   TF,X'FF'            USED ENTRY ?
         BE    PL160               YES
         LTR   R4,R4               FIRST PAGE-MODULE ?             -EU-
         BZ    *+L'*+8             YES                             -EU-
         TM    TMATTR3,PDS2PAGA    NO, PAGE ALIGNMENT NEEDED ?     -EU-
         BO    PL170               YES                             -EU-
         LR    R3,R5               SAVE CURR LOC
         L     R1,TL
         LTR   R4,R4               FIRST PAGE-MODULE ?             -EU-
         BNZ   *+L'*+4             NO                              -EU-
         N     R1,=F'4095'
         A     R1,PAGELEN
         C     R1,=F'4096'         OVER A PAGE ?
         BNH   NOTOVER
PL170    SR    R5,R6               TRY TO FIND ENTRY WHICH WILL FIT
         LR    R3,R5
         OI    SWITCH,SWPPB
PL180    BXH   R5,R6,PL250         TRY TO FIND A FIT               -EU-
         CLI   TF,X'FF'            ALREADY USED FLAG ?
         BE    PL180               YES... DO NOT USE
         TM    TMATTR3,PDS2PAGA    NO, PAGE ALIGNMENT NEEDED ?     -EU-
         BO    PL180               YES... DO NOT USE               -EU-
         L     R1,PAGELEN          CURRENT PAGELEN
         A     R1,TL               LENGTH OF MOD
         C     R1,=F'4096'         FIT ?
         BH    PL180               NO
         B     NOTOVER             YES
PL200    CL    R7,TAB16M           DOWN 16M LINE TABLE LIMIT ?     -EU-
         BNE   PL900               NO... END OF PROCESS            -EU-
         L     R7,TAB+8            YES... PROCESS UP 16M LINE NOW  -EU-
         NI    SWTCH+1,SETNOP                                      -EU-
PL250    XR    R4,R4                                               -EU-
         UNPK  PGBSZ,TOTSZ(L'TOTSZ+1)   EDIT TOTAL PAGE(S) SPACE   -EU-
         TR    PGBSZ,TOHEX-C'0'                                    -EU-
         MVI   PGBSZ+L'PGBSZ-1,C' '                                -EU-
         LA    R0,PGB                                              -EU-
         BAS   R8,SWPRNT                                           -EU-
         SP    CTR,=PL1'+1'
         L     R1,TOTSZ            COMPUTE NEW LPA TOTAL           -EU-
         A     R1,=F'4095'                                         -EU-
         SRL   R1,12                                               -EU-
         SLL   R1,12                                               -EU-
SWTOT    NOP   *+L'*+12                                            -EU-
         A     R1,TOTLSZ                                           -EU-
         ST    R1,TOTLSZ                                           -EU-
         B     *+L'*+8                                             -EU-
         A     R1,TOTLSZX                                          -EU-
         ST    R1,TOTLSZX                                          -EU-
         XC    TOTSZ,TOTSZ                                         -EU-
         XC    PAGELEN,PAGELEN                                     -EU-
SWTCH    B     PL150                                               -EU-
         OI    SWTCH+1,SETBR                                       -EU-
         OI    SWTOT+1,SETBR                                       -EU-
         OI    SWTLS+1,SETBR                                       -EU-
         SP    CTR,CTR             FORCE NEW PAGE                  -EU-
         B     PL150
NOTOVER  ST    R1,PAGELEN          CURRENT PAGE LENGTH
         LTR   R4,R4               FIRST PAGE-MODULE ?             -EU-
         BNZ   *+L'*+10            NO                              -EU-
         ST    R2,TPGBC            SET BACKWARD CHAIN              -EU-
         LR    R2,R5                                               -EU-
         B     *+L'*+4                                             -EU-
         ST    R5,TPMFC-TBS(R4)    SET FORWARD CHAIN               -EU-
         LR    R4,R5                                               -EU-
         MVC   PN,TN               PRINT MODULE NAME
         UNPK  PS,TS(L'TS+1)       START ADDRESS
         UNPK  PL,TL(L'TL+1)       LENGTH
         TR    PT,TOHEX-C'0'
         MVI   PS+L'PS-1,C' '      BLANK GARBAGE
         MVI   PL+L'PL-1,C' '      BLANK GARBAGE
         MVC   PF(18),=XL18'40202020204B202120402021206B20202020'  -EU-
         L     R1,TF               GET NUMBER FAULTS
         MVI   TF,X'FF'
         CVD   R1,DWD              IN DECIMAL
         ED    PF,DWD+4            READABLE
         L     R1,TFL              GET FACTOR
         CVD   R1,DWD              IN DECIMAL
         SRP   DWD+3(5),64-1,5                                     -EU-
         ED    PFL,DWD+4           READABLE
         L     R0,TOTSZ            COMPUTE TOTAL PAGE(S) SPACE     -EU-
         A     R0,TL                                               -EU-
         ST    R0,TOTSZ                                            -EU-
         DROP  R5                                                  -EU-
         SP    CTR,=PL1'+1'
         BP    PL400
PL350    ZAP   CTR,=PL2'+55'       LINES PER PAGE
         MVC   T1PGN,=XL4'40202120'                                -EU-
         ED    T1PGN,PGN           PAGE NUMBER                     -EU-
         LA    R0,T1               TITLES                          -EU-
         BAS   R8,SWPRNT                                           -EU-
         LA    R0,T2                                               -EU-
         BAS   R8,SWPRNT                                           -EU-
         AP    PGN,=PL1'+1'                                        -EU-
         LTR   R2,R2               INITIAL TIME ?                  -EU-
         BZ    PL150               YES                             -EU-
PL400    LA    R0,P                PRINT DETAIL LINE               -EU-
         BAS   R8,SWPRNT                                           -EU-
         L     R1,PAGELEN
         N     R1,=F'4095'
         BZ    PL250
         ST    R1,PAGELEN
         TM    SWITCH,SWPPB
         BO    PL180
         B     PL150               LOOK FOR NEXT ENTRY
         SPACE 1                                                   -EU-
SWPRNT   B     PL800               SYSPRINT PROCESS ROUTINE        -EU-
         LTR   R0,R0                                               -EU-
         BNPR  R8                                                  -EU-
        PUT    SYSPRINT,(0)                                        -EU-
         BR    R8                                                  -EU-
PL800    STM   R2,R3,SVREGS        WORK REGISTERS                  -EU-
         LTR   R2,R0                                               -EU-
         BNP   PL880               BR IF LAST CALL                 -EU-
         OC    CURPT,CURPT         PROCESSING STARTED ?            -EU-
         BNZ   PLCNT               YES                             -EU-
         CLI   0(R2),C'1'          NEW PAGE ?                      -EU-
         BNER  R8                  NO... FLUSH                     -EU-
         B     PLSTR                                               -EU-
PLCNT    CLI   0(R2),C'1'          NEW PAGE ?                      -EU-
         BNE   PL830               NO                              -EU-
         TM    SWITCH,SWPRS        PAGE RIGHT SIDE PROCESSED ?     -EU-
         BO    *+L'*+8             YES                             -EU-
         OI    SWITCH,SWPRS        NO... START IT NOW              -EU-
         B     PLSTR                                               -EU-
         NI    SWITCH,ALL-SWPRS    RESET TO PAGE LEFT SIDE         -EU-
         L     R3,APOOL                                            -EU-
PL820   PUT    SYSPRINT,(R3)                                       -EU-
         MVC   2(PLNSZ-2,R3),1(R3)                                 -EU-
         AH    R3,=Y(PLNSZ)                                        -EU-
         CL    R3,LSTPT                                            -EU-
         BNH   PL820               PRINT AN ENTIRE PAGE            -EU-
         XC    LSTPT,LSTPT                                         -EU-
PLSTR    MVC   CURPT,APOOL                                         -EU-
PL830    L     R1,CURPT            BUILD PAGE                      -EU-
         CLI   0(R2),C'0'          DOUBLE SPACE ?                  -EU-
         BNE   *+L'*+4             NO                              -EU-
         AH    R1,=Y(PLNSZ)                                        -EU-
         TM    SWITCH,SWPRS        RIGHT SIDE PROCESSING ?         -EU-
         BO    *+L'*+10            YES                             -EU-
         MVC   8(61,R1),1(R2)      MOVE TO LEFT SIDE               -EU-
         B     *+L'*+14                                            -EU-
         MVC   70(61,R1),1(R2)     MOVE TO RIGHT SIDE              -EU-
         CL    R1,LSTPT                                            -EU-
         BNH   *+L'*+4                                             -EU-
         ST    R1,LSTPT            SET PRINT POINTER               -EU-
         AH    R1,=Y(PLNSZ)                                        -EU-
         ST    R1,CURPT            SET CURRENT POINTER             -EU-
PL870    LM    R2,R3,SVREGS                                        -EU-
         BR    R8                                                  -EU-
PL880    NI    SWITCH,ALL-SWPRS    RESET ALL                       -EU-
         XC    CURPT,CURPT                                         -EU-
         OC    LSTPT,LSTPT         ANYTHING TO PRINT ?             -EU-
         BZ    PL870               NO                              -EU-
         L     R3,APOOL                                            -EU-
PL890   PUT    SYSPRINT,(R3)                                       -EU-
         MVC   2(PLNSZ-2,R3),1(R3)                                 -EU-
         AH    R3,=Y(PLNSZ)                                        -EU-
         CL    R3,LSTPT                                            -EU-
         BNH   PL890               PRINT LAST PAGE                 -EU-
         XC    LSTPT,LSTPT                                         -EU-
         B     PL870                                               -EU-
         EJECT
*        REPORT FINAL STATISTICS.                                  -EU-
*        ------------------------                                  -EU-
         SPACE 1
PL900    OC    PAGELEN,PAGELEN     ANYTHING COMPUTED ?             -EU-
         BZ    PL920               NO                              -EU-
         UNPK  PGBSZ,TOTSZ(L'TOTSZ+1)   EDIT TOTAL PAGE(S) SPACE   -EU-
         TR    PGBSZ,TOHEX-C'0'                                    -EU-
         MVI   PGBSZ+L'PGBSZ-1,C' '                                -EU-
         LA    R0,PGB                                              -EU-
         BAS   R8,SWPRNT                                           -EU-
         L     R1,TOTSZ            COMPUTE NEW LPA TOTAL           -EU-
         A     R1,=F'4095'                                         -EU-
         SRL   R1,12                                               -EU-
         SLL   R1,12                                               -EU-
SWTLS    NOP   *+L'*+12                                            -EU-
         A     R1,TOTLSZ                                           -EU-
         ST    R1,TOTLSZ                                           -EU-
         B     *+L'*+8                                             -EU-
         A     R1,TOTLSZX                                          -EU-
         ST    R1,TOTLSZX                                          -EU-
PL920    MVC   T3PGN,=XL4'40202120'                                -EU-
         ED    T3PGN,PGN           PAGE NUMBER                     -EU-
         LA    R0,T3               TITLE                           -EU-
         BAS   R8,SWPRNT                                           -EU-
         LA    R0,T4                                               -EU-
         BAS   R8,SWPRNT                                           -EU-
         CP    LPACTR,=PL1'+0'                                     -EU-
         BNE   *+L'*+10                                            -EU-
         CP    ASCTR,=PL1'+0'                                      -EU-
         BE    NTSMP                                               -EU-
         ED    TLPACTR,LPACTR      SHOW LPA FAULTS
         ED    TASCCTR,ASCTR       ADDRESS SPACE FAULTS
         LA    R0,TSMP             PRINT INFO ABOUT SAMPLES        -EU-
         BAS   R8,SWPRNT                                           -EU-
NTSMP    TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    *+L'*+10            NO                              -EU-
         MVI   LPASEP+1,C'+'                                       -EU-
         MVC   LPASIZX(L'LPASIZE+6),LPASIZE                        -EU-
         L     R1,LPAHI            INFO ABOUT CURRENT LPA          -EU-
         S     R1,LPALOW                                           -EU-
         A     R1,=F'4095'                                         -EU-
         SRL   R1,12                                               -EU-
         SLL   R1,12                                               -EU-
         CVD   R1,DWD                                              -EU-
         ED    LPASIZE,DWD+3                                       -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    PL930               NO                              -EU-
         ST    R1,WORK                                             -EU-
         L     R1,LPAHIX                                           -EU-
         S     R1,LPALOWX                                          -EU-
         A     R1,=F'4095'                                         -EU-
         SRL   R1,12                                               -EU-
         SLL   R1,12                                               -EU-
         CVD   R1,DWD                                              -EU-
         ED    LPASIZX,DWD+3                                       -EU-
         A     R1,WORK                                             -EU-
         ST    R1,WORK                                             -EU-
PL930    LA    R0,TLSZ             PRINT INFO                      -EU-
         BAS   R8,SWPRNT                                           -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    PL950               NO                              -EU-
         L     R1,WORK             GET TOTAL LPA SIZE              -EU-
         CVD   R1,DWD                                              -EU-
         ED    LPASZT,DWD+3                                        -EU-
         LA    R0,TLSZT            PRINT INFO                      -EU-
         BAS   R8,SWPRNT                                           -EU-
PL950    TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    *+L'*+10            NO                              -EU-
         MVI   TLBDSEPT+1,C'/'                                     -EU-
         MVC   TLBDLXT,TLBDLT                                      -EU-
         LA    R0,TLBDT            PRINT BOUNDARIES TITLE          -EU-
         BAS   R8,SWPRNT                                           -EU-
         UNPK  TLBDLL,LPALOW(L'LPALOW+1)                           -EU-
         UNPK  TLBDLH,LPAHI(L'LPAHI+1)                             -EU-
         TR    TLBDLL(L'TLBDLL+L'TLBDLH),TOHEX-C'0'                -EU-
         MVI   TLBDLL+L'TLBDLL-1,C' '                              -EU-
         MVI   TLBDLH+L'TLBDLH-1,C' '                              -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    *+L'*+26            NO                              -EU-
         UNPK  TLBDLXL,LPALOWX(L'LPALOWX+1)                        -EU-
         UNPK  TLBDLXH,LPAHIX(L'LPAHIX+1)                          -EU-
         TR    TLBDLXL(L'TLBDLXL+L'TLBDLXH),TOHEX-C'0'             -EU-
         MVI   TLBDLXL+L'TLBDLXL-1,C' '                            -EU-
         MVI   TLBDLXH+L'TLBDLXH-1,C' '                            -EU-
         LA    R0,TLBD             PRINT BOUNDARIES                -EU-
         BAS   R8,SWPRNT                                           -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    *+L'*+10            NO                              -EU-
         MVI   NEWLSEP+1,C'+'                                      -EU-
         MVC   NEWLPSX(L'NEWLPSZ+6),NEWLPSZ                        -EU-
         L     R1,TOTLSZ           INFO ABOUT NEW LPA SIZE         -EU-
         CVD   R1,DWD                                              -EU-
         ED    NEWLPSZ,DWD+3                                       -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    *+L'*+14            NO                              -EU-
         L     R1,TOTLSZX          INFO ABOUT NEW LPA SIZE         -EU-
         CVD   R1,DWD                                              -EU-
         ED    NEWLPSX,DWD+3                                       -EU-
         LA    R0,PSZ              PRINT INFO                      -EU-
         BAS   R8,SWPRNT                                           -EU-
         TM    SWITCH,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    PL960               NO                              -EU-
         L     R1,TOTLSZ           GET TOTAL DOWN 16M LINE         -EU-
         A     R1,TOTLSZX          ADD TOTAL UP 16M LINE           -EU-
         CVD   R1,DWD                                              -EU-
         ED    TOTLPSZ,DWD+3                                       -EU-
         LA    R0,PSZT             PRINT INFO                      -EU-
         BAS   R8,SWPRNT                                           -EU-
PL960    XR    R0,R0               INFO ABOUT WORK AREA USE        -EU-
         L     R1,TAB+8                                            -EU-
         SL    R1,TAB                                              -EU-
         AL    R1,TAB+4                                            -EU-
         ST    R1,TABSNPL                                          -EU-
         D     R0,TAB+4                                            -EU-
         CVD   R1,DWD                                              -EU-
         ED    TABSIZE,DWD+5                                       -EU-
         LA    R0,TSZ              PRINT INFO                      -EU-
         BAS   R8,SWPRNT                                           -EU-
         CP    TTETOT,=PL1'+0'                                     -EU-
         BNE   *+L'*+10                                            -EU-
         CP    TTEUSE,=PL1'+0'                                     -EU-
         BE    NTRU                                                -EU-
         ED    TOTTTE,TTETOT                                       -EU-
         ED    USETTE,TTEUSE                                       -EU-
         LA    R0,TRU              PRINT SAMPLER USE INFO          -EU-
         BAS   R8,SWPRNT                                           -EU-
NTRU     UNPK  DWD(5),DAY          DAY OF RUN                      -EU-
        $GDATE PDAY,,,DWD                                          -EU-
         LTR   R15,R15                                             -EU-
         BNZ   NOPSI                                               -EU-
         LA    R0,PSWI1                                            -EU-
         BAS   R8,SWPRNT                                           -EU-
         MVC   PSID,SID            SYSTEM ID                       -EU-
         TM    STY,TMVSXA          SYSTEM TYPE                     -EU-
         BZ    *+L'*+6                                             -EU-
         MVC   PSTY,=CL4'XA) '                                     -EU-
         LA    R0,PSWI2                                            -EU-
         BAS   R8,SWPRNT                                           -EU-
         LA    R1,PSTME                                            -EU-
         L     R5,STRTME           START TIME                      -EU-
         LA    R14,PL980                                           -EU-
PL970    CL    R5,=F'100'          EDIT HH.MM.SS                   -EU-
         BLR   R14                                                 -EU-
         XR    R4,R4                                               -EU-
         D     R4,=F'100'                                          -EU-
         CL    R5,=F'60'                                           -EU-
         BNL   *+L'*+8                                             -EU-
         LR    R4,R5                                               -EU-
         XR    R5,R5                                               -EU-
         B     *+L'*+12                                            -EU-
         XR    R4,R4                                               -EU-
         D     R4,=F'60'                                           -EU-
         LTR   R4,R4                                               -EU-
         BZ    *+L'*+18                                            -EU-
         CVD   R4,DWD                                              -EU-
         UNPK  DWD(3),DWD+6(2)                                     -EU-
         OC    6(2,R1),DWD+1                                       -EU-
         XR    R4,R4                                               -EU-
         CL    R5,=F'60'                                           -EU-
         BNL   *+L'*+8                                             -EU-
         LR    R4,R5                                               -EU-
         XR    R5,R5                                               -EU-
         B     *+L'*+10                                            -EU-
         D     R4,=F'60'                                           -EU-
         LTR   R4,R4                                               -EU-
         BZ    *+L'*+16                                            -EU-
         CVD   R4,DWD                                              -EU-
         UNPK  DWD(3),DWD+6(2)                                     -EU-
         OC    3(2,R1),DWD+1                                       -EU-
         LTR   R5,R5                                               -EU-
         BZR   R14                                                 -EU-
         CVD   R5,DWD                                              -EU-
         UNPK  DWD(3),DWD+6(2)                                     -EU-
         OC    0(2,R1),DWD+1                                       -EU-
         BR    R14                                                 -EU-
PL980    LA    R1,PETME                                            -EU-
         L     R5,ENDTME           END TIME                        -EU-
         BAS   R14,PL970                                           -EU-
         LA    R0,PSWI3                                            -EU-
         BAS   R8,SWPRNT                                           -EU-
         TM    STY,TMVSXA          SYSTEM TYPE                     -EU-
         BZ    PLNTTES                                             -EU-
         CP    COUNTER,=PL1'+0'                                    -EU-
         BE    NONTTES                                             -EU-
         ED    PUEXTS,COUNTER                                      -EU-
         LA    R0,PSWI4B                                           -EU-
         B     PLPSWI4                                             -EU-
PLNTTES  LH    R1,NTTESTT          NO. OF TTE'S                    -EU-
         LTR   R1,R1                                               -EU-
         BZ    NONTTES                                             -EU-
         CVD   R1,DWD                                              -EU-
         ED    PNTTES,DWD+5                                        -EU-
         LA    R0,PSWI4A                                           -EU-
PLPSWI4  BAS   R8,SWPRNT                                           -EU-
NONTTES  L     R5,WTIME            LAST WAIT TIME                  -EU-
         CL    R5,=F'100000'                                       -EU-
         BNL   *+L'*+6                                             -EU-
         XR    R5,R5                                               -EU-
         B     *+L'*+6                                             -EU-
         XR    R4,R4                                               -EU-
         D     R4,=F'100000'                                       -EU-
         CVD   R5,DWD                                              -EU-
         ED    PLTME,DWD+6                                         -EU-
         LA    R0,PSWI5                                            -EU-
         BAS   R8,SWPRNT                                           -EU-
         L     R5,MDMTME           WAIT TIME AVERAGE               -EU-
         CL    R5,=F'100000'                                       -EU-
         BNL   *+L'*+6                                             -EU-
         XR    R5,R5                                               -EU-
         B     *+L'*+6                                             -EU-
         XR    R4,R4                                               -EU-
         D     R4,=F'100000'                                       -EU-
         CVD   R5,DWD                                              -EU-
         ED    PMTME,DWD+6                                         -EU-
         LA    R0,PSWI6                                            -EU-
         BAS   R8,SWPRNT                                           -EU-
NOPSI    XR    R0,R0                                               -EU-
         BAS   R8,SWPRNT                                           -EU-
         SPACE 1                                                   -EU-
        CLOSE  SYSPRINT
         TM    SWPRNT+1,SETBR                                      -EU-
         BZ    SWSNAP                                              -EU-
         L     R0,=A(POOLSZ)                                       -EU-
         L     R1,APOOL                                            -EU-
        FREEMAIN R,A=(1),LV=(0)    FREE THE POOL OF LINES          -EU-
         EJECT
*        DUMP TABLE IF REQUESTED (DEBUG AID)                       -EU-
*        -----------------------------------                       -EU-
         SPACE 1                                                   -EU-
SWSNAP   B     SM100               SNAP SWITCH                     -EU-
         L     R3,TAB                                              -EU-
        $SNAP  (R3),TABSNPL,DDSNAP                                 -EU-
         LTR   R15,R15                                             -EU-
         BZ    SM100                                               -EU-
         LA    R1,400(R15)         SET USER ABEND 4XX *************-EU-
         B     ABEND                                               -EU-
         EJECT
*        OUTPUT IEAPAK00 MEMBER                                    -EU-
*        ----------------------                                    -EU-
         SPACE 1                                                   -EU-
SM100    TM    SWITCH,SWPNP                                        -EU-
         BO    SM110                                               -EU-
        OPEN   (SYSPUNCH,(OUTPUT))
         LA    R1,203              SET USER ABEND 203 ****************
         TM    SYSPUNCH+(DCBOFLGS-IHADCB),DCBOFOPN  SUCCESSFULL OPEN ?
         BZ    ABEND               NO
SM110    LA    R6,CS+1             CARD IMAGE POINTER
         USING TBS,R4              TABLE DSECT                     -EU-
         LR    R4,R2               STARTING PAGE-GROUP             -EU-
         SPACE 1                                                   -EU-
SM120    MVC   0(8,R6),TN          MOVE MODULE NAME
SM130    LA    R6,1(,R6)           NEXT CHAR (KNOW FIRST WASN'T BLANK)
         CLI   0(R6),C' '          BLANK ?
         BNE   SM130               NO... CONTINUE SEARCH
         L     R3,TPMFC            YES... GET FORWARD CHAIN        -EU-
         LTR   R4,R3               ANYONE ?                        -EU-
         BZ    SM200               NO... END OF PAGE-GROUP         -EU-
         MVI   0(R6),C','          YES... COMMA AFTER NAME
         BAS   R8,SM500            GO TO CONTROL CARD STORE        -EU-
         B     SM120               GO MOVE ANOTHER                 -EU-
SM200    MVI   0(R6),C')'          MOVE IN CLOSE PAREN
         LA    R6,1(R6)            POINT PAST PAREN
         L     R2,TPGBC-TBS(R2)    GET BACKWARD CHAIN              -EU-
         LTR   R4,R2               ANYONE ?                        -EU-
         BZ    SM900               NO... END OF PROCESS            -EU-
         MVI   0(R6),C','          YES... COMMA AFTER GROUP
         BAS   R8,SM500            GO TO CONTROL CARD STORE        -EU-
         MVI   0(R6),C'('          MOVE IN OPEN PAREN
         LA    R6,1(R6)            POINT PAST PAREN
         B     SM120               GO MOVE ANOTHER                 -EU-
         DROP  R4                                                  -EU-
         SPACE 1
SM500    LA    R6,1(,R6)           POINT PAST LAST CHAR INSERTED
         CL    R6,=A(CS+60)        CARD FILLED ?                   -EU-
         BLR   R8                  NO... RETURN
         BAS   R7,SM600            PUNCH OR LIST CARD              -EU-
         MVI   CS,C' '             BLANK CARD IMAGE
         MVC   CS+1(L'CS-1),CS
         LA    R6,CS               RESET CARD POINTER
         BR    R8                  RETURN
         SPACE 1                                                   -EU-
SM600    AP    CSCTR,=PL1'+1'      PUNCH OR LIST ROUTINE           -EU-
         UNPK  CSN,CSCTR                                           -EU-
         OI    CSN+L'CSN-1,C'0'                                    -EU-
         TM    SWITCH,SWPNP                                        -EU-
         BO    SM610                                               -EU-
        PUT    SYSPUNCH,CS         PUNCH CARD
         BR    R7                                                  -EU-
SM610    CLC   CSN+L'CSN-2(2),=CL2'01'                             -EU-
         BE    SM620                                               -EU-
         CLC   CSN+L'CSN-2(2),=CL2'51'                             -EU-
         BNE   SM630                                               -EU-
SM620    MVI   CSL,C'1'                                            -EU-
SM630   $PRINT CSL,,DDLIST         LIST CARD                       -EU-
         LTR   R15,R15                                             -EU-
         BZ    SM640                                               -EU-
         LA    R1,210(R15)         SET USER ABEND 21X *************-EU-
         B     ABEND                                               -EU-
SM640    MVI   CSL,C' '                                            -EU-
         BR    R7                                                  -EU-
         SPACE 1
SM900    BAS   R7,SM600            PUNCH OR LIST LAST CARD         -EU-
         TM    SWITCH,SWPNP                                        -EU-
         BO    EXIT                                                -EU-
        CLOSE  SYSPUNCH
         EJECT
*        NORMAL TERMINATION                                        -EU-
*        ------------------                                        -EU-
         SPACE 1
EXIT    $PCLOSE ,                                                  -EU-
         OC    TAB,TAB                                             -EU-
         BZ    RETURN                                              -EU-
        FREEMAIN LU,LA=TBSZG,A=TAB                                 -EU-
         SPACE 1
RETURN  $XRET  CC=(R9)                                             -EU-
         SPACE 2
*        ABNORMAL TERMINATION                                      -EU-
*        --------------------                                      -EU-
         SPACE 1                                                   -EU-
ABEND    NOP   ABENDD                                              -EU-
         LR    R9,R1               PASS R.C.                       -EU-
         TM    SYSLPA+(DCBOFLGS-IHADCB),DCBOFOPN  OPENED ?         -EU-
         BZ    ABEND1              NO                              -EU-
        CLOSE  SYSLPA                                              -EU-
ABEND1   TM    SYSDIR+(DCBOFLGS-IHADCB),DCBOFOPN  OPENED ?         -EU-
         BZ    ABEND2              NO                              -EU-
        CLOSE  SYSDIR                                              -EU-
ABEND2   TM    SYSPSW+(DCBOFLGS-IHADCB),DCBOFOPN  OPENED ?         -EU-
         BZ    ABEND3              NO                              -EU-
        CLOSE  SYSPSW                                              -EU-
ABEND3   TM    SYSPRINT+(DCBOFLGS-IHADCB),DCBOFOPN  OPENED ?       -EU-
         BZ    ABEND4              NO                              -EU-
        CLOSE  SYSPRINT                                            -EU-
ABEND4   TM    SYSPUNCH+(DCBOFLGS-IHADCB),DCBOFOPN  OPENED ?       -EU-
         BZ    EXIT                NO                              -EU-
        CLOSE  SYSPUNCH                                            -EU-
         B     EXIT                                                -EU-
ABENDD   ABEND (1),DUMP,STEP,USER
         EJECT
*        CONSTANTS AND WORK-AREAS
*        ------------------------
         SPACE 1
DWD      DC    D'0'
WORK     DC    F'0'                                                -EU-
SVREGS   DC    2F'0'                                               -EU-
TBSZG    DC    0F'0',XL1'80',AL3(SZG)                              -EU-
TBSZ     DC    A(NLPA*TBL)                                         -EU-
TAB      DC    A(*-*,TBL,*-*)
STT      DC    A(*-*,*-*,*-*)                                      -EU-
TAB16M   DC    A(*-*)              16M LINE TABLE POINTER          -EU-
TABSNPL  DC    A(*-*)                                              -EU-
APOOL    DC    A(*-*)                                              -EU-
CURPT    DC    A(*-*)                                              -EU-
LSTPT    DC    A(*-*)                                              -EU-
PAGELEN  DC    F'0'
TOTSZ    DC    F'0'                                                -EU-
TOTLSZ   DC    F'0'                                                -EU-
TOTLSZX  DC    F'0'                                                -EU-
PSI      DS    0F  - - - - - - - - PSW SAMPLER INFO                -EU-
STY      DC    AL1(0)                ...                           -EU-
DAY      DC    XL3'0'                ...                           -EU-
SID      DC    CL4' '                ...                           -EU-
STRTME   DC    F'0'                  ...                           -EU-
ENDTME   DC    F'0'                  ...                           -EU-
MDMTME   DC    F'0'                  ...                           -EU-
WTIME    DC    F'0'                  ...                           -EU-
NTTESTT  DC    0H'0'                 ...                           -EU-
COUNTER  DC    PL4'+0'               ...                           -EU-
PSIL     EQU   *-PSI   - - - - - - - ---                           -EU-
LPALOW   DC    0F'0',X'7FFFFFFF'
LPAHI    DC    0F'0',X'00000000'
LPALOWX  DC    0F'0',X'7FFFFFFF'                                   -EU-
LPAHIX   DC    0F'0',X'00000000'                                   -EU-
LPAUP    DC    A(16*1024*1024)     16M LINE                        -EU-
TOBIN    DC    256X'0'
         ORG   TOBIN+C'A'               POSITION TO 'A'
         DC    X'0A0B0C0D0E0F'          TRANSLATE A-F
         ORG   TOBIN+C'0'               POSITION AT '0'
         DC    X'00010203040506070809'  TRANSLATE 0-9
         ORG
DDLIST   DC    CL8'SYSLIST'                                        -EU-
DDSNAP   DC    CL8'SYSSNAP'                                        -EU-
TOHEX    DC    CL16'0123456789ABCDEF'
SORTTEST DC    H'0'
TTETOT   DC    PL5'+0'                                             -EU-
TTEUSE   DC    PL5'+0'                                             -EU-
ASCTR    DC    PL4'+0'
LPACTR   DC    PL4'+0'
CSCTR    DC    PL3'+0'                                             -EU-
CTR      DC    PL2'+0'
PGN      DC    PL2'+1'
SWITCH   DC    X'0'
SWPPB    EQU   X'01'               PROCESS PAGE BOUNDARIES         -EU-
SWSRT    EQU   X'02'               SORT TABLE IN TWO PARTS (16M)   -EU-
SWPNP    EQU   X'08'               LIST IN PLACE OF PUNCH IEAPAK00 -EU-
SWPRS    EQU   X'10'               PROCESS RIGHT SIDE OF PAGE      -EU-
SW16M    EQU   X'40'               PROCESS UP 16M LINE ALSO        -EU-
SWNEW    EQU   X'80'               NEW FORMAT AMBLIST (LISTLPA)    -EU-
         SPACE 1
         DS    0F                                                  -EU-
P        DS    0CL62               PRINT LINE - SINGLE SPACE
         DC    C' '                SINGLE SPACE
PN       DC    CL8' '              MODULE NAME
         DC    CL1' '
PT       DS    0CL18               TRANSLATE TO HEX
PS       DC    CL9' '                   MODULE START ADDRESS
PL       DC    CL9' '                   LENGTH OF MODULE
         DS    0CL18               MOVE EDIT MASK HERE
PF       DC    CL9' '                   NUMBER OF FAULTS
PFL      DC    CL9' '                   FACTOR
         DC    CL16' '
         SPACE 1
T1       DS    0CL62
         DC    CL47'1 NAME     START    LENGTH    COUNTS   C/L     '
         DC    CL6'  PAGE'                                         -EU-
T1PGN    DC    CL4' ',CL5'. '                                      -EU-
         SPACE 1
T2       DS    0CL62
         DC    CL47' -------- -------- --------  -------- -------- '
         DC    CL15' '
         SPACE 1                                                   -EU-
T3       DS    0CL62                                               -EU-
         DC    CL36'1 P A C K L I S T  -  P S W S A M P ',CL11' '  -EU-
         DC    CL6'  PAGE'                                         -EU-
T3PGN    DC    CL4' ',CL5'. '                                      -EU-
         SPACE 1                                                   -EU-
T4       DS    0CL62                                               -EU-
         DC    CL36'  --------------------------------- '          -EU-
         DC    CL26' '                                             -EU-
         SPACE 1
PGB      DS    0CL62
         DC    19C' '                                              -EU-
         DC    CL30'---------------- TOTAL SIZE :'                 -EU-
PGBSZ    DC    CL9' ',CL4' '                                       -EU-
         SPACE 1
TSMP     DS    0CL62
         DC    CL2'0 ',CL12'LPA FAULTS :'                          -EU-
TLPACTR  DC    XL10'40204B2020204B202120'
         DC    CL16' / A.S. FAULTS :'                              -EU-
TASCCTR  DC    XL10'40204B2020204B202120',CL12' '
         SPACE 1
TLSZ     DS    0CL62
         DC    CL2'0 ',CL10'LPA-SIZE :'
LPASIZE  DC    XL12'402020204B2020204B202120',CL6' BYTES'
LPASEP   DC    CL3' '                                              -EU-
LPASIZX  DC    CL12' ',CL6' ',CL10' '                              -EU-
         SPACE 1                                                   -EU-
TLSZT    DS    0CL62                                               -EU-
         DC    CL13' ',CL12'TOTAL SIZE :'                          -EU-
LPASZT   DC    XL12'402020204B2020204B202120',CL25' BYTES'         -EU-
         SPACE 1                                                   -EU-
TLBDT    DS    0CL62                                               -EU-
         DC    CL2' ',CL13'BOUNDARIES :'                           -EU-
TLBDLT   DC    CL17'  LOW   -  HIGH  '                             -EU-
TLBDSEPT DC    CL3' '                                              -EU-
TLBDLXT  DC    CL17' ',CL10' '                                     -EU-
         SPACE 1                                                   -EU-
TLBD     DS    0CL62                                               -EU-
         DC    CL15' '                                             -EU-
TLBDLL   DC    CL9' '                                              -EU-
TLBDLH   DC    CL9' ',CL2' '                                       -EU-
TLBDLXL  DC    CL9' '                                              -EU-
TLBDLXH  DC    CL9' ',CL9' '                                       -EU-
         SPACE 1                                                   -EU-
PSZ      DS    0CL62                                               -EU-
         DC    CL2'0 ',CL14'NEW LPA-SIZE :'                        -EU-
NEWLPSZ  DC    XL12'402020204B2020204B202120',CL6' BYTES'          -EU-
NEWLSEP  DC    CL3' '                                              -EU-
NEWLPSX  DC    CL12' ',CL6' ',CL7' '                               -EU-
         SPACE 1                                                   -EU-
PSZT     DS    0CL62                                               -EU-
         DC    CL17' ',CL12'TOTAL SIZE :'                          -EU-
TOTLPSZ  DC    XL12'402020204B2020204B202120',CL21' BYTES'         -EU-
         SPACE 1                                                   -EU-
TSZ      DS    0CL62                                               -EU-
         DC    CL2'0 ',CL10'TAB-SIZE :'                            -EU-
TABSIZE  DC    XL6'402020202120',CL44' (NLPA VALUE)'               -EU-
         SPACE 1                                                   -EU-
TRU      DS    0CL62                                               -EU-
         DC    CL2'0 ',CL9'TOT-TTE :'                              -EU-
TOTTTE   DC    XL12'402020204B2020204B202120'                      -EU-
         DC    CL12' / USE-TTE :'                                  -EU-
USETTE   DC    XL12'402020204B2020204B202120',CL15' '              -EU-
         SPACE 1                                                   -EU-
PSWI1    DS    0CL62                                               -EU-
         DC    CL2'0 ',CL23'PSWSAMP - DAY OF RUN :'                -EU-
PDAY     DC    CL20' ',CL17' '                                     -EU-
         SPACE 1                                                   -EU-
PSWI2    DS    0CL62                                               -EU-
         DC    CL12' ',CL17'RUNNING SYSTEM :'                      -EU-
PSID     DC    CL4' ',CL6' (MVS/'                                  -EU-
PSTY     DC    CL4'370)',CL19' '                                   -EU-
         SPACE 1                                                   -EU-
PSWI3    DS    0CL62                                               -EU-
         DC    CL12'0 ',CL8'START :'                               -EU-
PSTME    DC    CL8'00.00.00',CL10' / STOP :'                       -EU-
PETME    DC    CL8'00.00.00',CL16' '                               -EU-
         SPACE 1                                                   -EU-
PSWI4A   DS    0CL62                                               -EU-
         DC    CL12'0 ',CL31'DYNAMIC TRACE TABLE HAS HANDLED'      -EU-
PNTTES   DC    XL6'402020202120',CL6' TTE''S',CL7' '               -EU-
PSWI4B   DS    0CL62                                               -EU-
         DC    CL12'0 ',CL30'UNKNOWN EX-TTE''S ENCOUNTERED :'      -EU-
PUEXTS   DC    XL8'4020202020202120',CL12' (IN ''TBUF'')'          -EU-
         SPACE 1                                                   -EU-
PSWI5    DS    0CL62                                               -EU-
         DC    CL17'0 ',CL16'LAST WAIT TIME :'                     -EU-
PLTME    DC    XL5'4021206B20',CL24' SECONDS'                      -EU-
         SPACE 1                                                   -EU-
PSWI6    DS    0CL62                                               -EU-
         DC    CL17' ',CL19'WAIT TIME AVERAGE :'                   -EU-
PMTME    DC    XL5'4021206B20',CL21' SECONDS'                      -EU-
         SPACE 1
         DC    A(L'CSL+L'CS+L'CSN+4)                               -EU-
CSL      DC    CL10' '                                             -EU-
CS       DC    CL72'( '                                            -EU-
CSN      DC    CL4' ',CL4'0000'                                    -EU-
         SPACE 1
         DC    A(L'PI)                                             -EU-
PI       DS    0CL121              PRINT IMAGE FROM OUTPUT HERE
CC       DC    CL1' '              PRINTER CARRIAGE CONTROL CHARACTER
S1       DC    CL60' '             SEGMENT ONE
S2       DC    CL60' '             SEGMENT TWO
         SPACE 1
LPMNUM   DC    C'LINK PACK MAP - NUMERICALLY BY'                   -EU-
LPMNEW   DC    C'PAGEABLE LINK PACK AREA MAP -  NUMERICALLY BY'    -EU-
         EJECT ,
        LTORG  ,
         SPACE 2
         PRINT NOGEN
SYSLPA   DCB   DDNAME=SYSLPA,DSORG=PS,MACRF=GM,EODAD=IT900
SYSDIR   DCB   DDNAME=SYSDIR,DSORG=PS,MACRF=GM,EODAD=DR900
SYSPSW   DCB   DDNAME=SYSPSW,DSORG=PS,MACRF=GL,RECFM=F,EODAD=RS900
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FA,LRECL=131
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM,LRECL=80
         EJECT
TBS      DSECT                  -- TABLE ENTRY FOR LPA MODULE
TN       DS    CL8                 NAME
TS       DS    CL4                 START ADDRESS
TL       DS    CL4                 LENGTH
TF       DS    CL4                 NUMBER OF FAULTS
TFL      DS    F                   FAULTS DIVIDED BY LENGTH
TPGBC    DS    A                   PAGE-GROUPS BACKWARD CHAIN      -EU-
TPMFC    DS    A                   PAGE-MODULES FORWARD CHAIN      -EU-
TMATTR1  DS    X                   MODULE ATTR. BYTE 1 (PDS2ATR1)  -EU-
TMATTR2  DS    X                   MODULE ATTR. BYTE 2 (PDS2ATR2)  -EU-
TMATTR3  DS    X                   MODULE ATTR. BYTE 3 (PDS2FTB1)  -EU-
         DS    X                   - NOT USED -                    -EU-
TBL      EQU   *-TBS               LENGTH OF TABLE ENTRY
         SPACE 1
LPAE     DSECT                  -- LPA LIST PRINT SEGMENT
LN       DS    CL8                 MODULE NAME
         DS    C
LX       DS    0CL17               TRANSLATE NEXT 17 BYTES
LS       DS    CL6                      START ADDRESS
         DS    CL4
LL       DS    CL6                      LENGTH
         DS    CL4                      ... ETC ...
         ORG   LX                                                  -EU-
LXN      DS    0CL19               TRANSLATE NEXT 19 BYTES         -EU-
LSN      DS    CL8                      START ADDRESS              -EU-
         DS    CL2                                                 -EU-
LLN      DS    CL8                      LENGTH                     -EU-
         DS    CL2                      ... ETC ...                -EU-
         SPACE 1                                                   -EU-
IEHL     DSECT                  -- IEHLIST PRINT SEGMENT           -EU-
MARGIN   DS    CL10                                                -EU-
MNAME    DS    CL8                 MODULE NAME                     -EU-
         DS    CL2,CL8             TTRC                            -EU-
         DS    CL2,CL10            VARIABLE USER DATA ...          -EU-
         DS    CL1,CL6             ...                             -EU-
MATT1    DS    CL4                 MODULE ATTRIBUTES               -EU-
         DS    CL1,CL10            ...                             -EU-
         DS    CL1,CL6             ...                             -EU-
MATT2    DS    CL2                 MODULE ATTRIBUTES               -EU-
         DS    CL2                 ... ETC ...                     -EU-
         SPACE 1
        DCBD   DSORG=PS,DEVD=TA
         SPACE 2
         END
