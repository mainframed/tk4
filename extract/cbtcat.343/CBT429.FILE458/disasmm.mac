         MACRO
&NAME    SVLNK &REG1,&REG2,&REG3,&IND,&LV=
         GBLC  &EQU
         GBLC  &GM
         GBLC  &CSECT
         LCLC  &REGA
         AIF   (T'&NAME EQ 'O').NOCSECT
         AIF   ('&NAME' EQ '*').NOCSECT
&CSECT   SETC  '&NAME'
&CSECT   CSECT                         NAME OF PROGRAM
         AGO   .RST
.NOCSECT ANOP
&CSECT   SETC  '*'
         CSECT                         BEGINNING OF PROGRAM
.RST     AIF   ('&EQU' EQ 'ON').NRQ
&EQU     SETC  'ON'
*
*** REGISTER EQUATES ***
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.NRQ     ANOP
*
*******************  PROGRAM INITIALIZATION  *************************
*
         AIF   (T'&REG1 EQ 'O').NOREG
&REGA    SETC  '&REG1'
         AIF   (T'&REG2 EQ 'O').ONEREG
         USING &CSECT,&REGA,&REG2          ESTABLISH ADDRESSABILITY
         AGO   .BYUSE
.NOREG   ANOP
&REGA    SETC  'R3'
.ONEREG  USING &CSECT,&REGA              ESTABLISH ADDRESSABILITY
.BYUSE   STM   R14,R12,12(R13)         STORE REGS IN HIGH SAVE AREA
         LR    &REGA,R15                  INITIALIZE BASE REG
         AIF   ('&CSECT' EQ '*').NOMN                              -EU-
         B     *+L'*+22                                            -EU-
         DC    CL8'&CSECT'             MODULE IDENTIFIER.          -EU-
         DC    CL8'&SYSDATE'           ASSEMBLY DATE.              -EU-
         DC    C'-',CL5'&SYSTIME'      ASSEMBLY TIME.              -EU-
.NOMN    AIF   (T'&REG2 EQ 'O').NOLA
         LA    &REG2,4095(&REGA)       INITIALIZE THE SECOND
         LA    &REG2,1(&REG2)                BASE REGISTER
.NOLA    AIF   (T'&REG3 EQ 'O').NOLNK
         L     &REG3,0(R1)                GET PARAMETER LIST ADDR
.NOLNK   AIF (T'&IND EQ 'O').GETIT
         CNOP  0,4
         BAS   R15,SAVEAREA+72         BRANCH AROUND SAVE AREA
SAVEAREA DC    18F'0'                  REGISTER SAVE AREA
         ST    R15,8(R13)
         ST    R13,SAVEAREA+4
         LR    R13,R15
         AGO   .NOSAVE
.GETIT   AIF   (T'&LV EQ 'O').GMD
&GM      SETC  '&LV'
         AGO   .DGM
.GMD     ANOP
&GM      SETC  '72'
.DGM     ANOP
*
*** GET MAIN STORAGE FOR SAVE AREA ***
*
         AIF   ('&GM' LT '4096').LA
         GETMAIN R,LV=&GM              GET CORE FOR SAVE AREA
         AGO   .EGM
.LA      LA    R0,&GM                  GET &GM BYTES
         GETMAIN R,LV=(0)
.EGM     ANOP
*
*** SET UP SAVE AREA POINTERS ***
*
         ST    R1,8(R13)               STORE LOW SAVE POINTER
         ST    R13,4(R1)               STORE HIGH SAVE POINTER
         LR    R13,R1                  INITIALIZE SAVE POINTER
         AIF   (T'&REG3  NE 'O').EXIT
         L     R1,4(R13)               GET POINTER TO RESTORE PARA REG
         L     R1,24(R1)               RESTORE PARAMETER REGISTER
.EXIT    ANOP                          END INITIAL., BEGIN THIS PROG.
*
*********************  END INITIALIZATION  ***************************
*
         MEND
         MACRO
&NAME    SVBCK &DUMMY                                              -EU-
         GBLC  &GM                                                 -EU-
*
*******************  PROGRAM TERMINATION  ****************************
*
&NAME    LR    R1,R13              SAVE AREA TO FREE               -EU-
         L     R13,4(R13)          GET POINTER TO CALLER'S S.A.    -EU-
*
*** FREE MAIN STORAGE OF SAVE AREA ***
*
         AIF   ('&GM' LT '4096').A0                                -EU-
        FREEMAIN R,LV=&GM,A=(1)    FREE CORE OF SAVE AREA          -EU-
         AGO   .A1                                                 -EU-
.A0      LA    R0,&GM              GET NUMBER OF BYTES             -EU-
        FREEMAIN R,LV=(0),A=(1)    FREE CORE OF SAVE AREA          -EU-
.A1      LM    R14,R12,12(R13)     RESTORE CALLER'S REGS           -EU-
         XR    R15,R15             CLEAR RETURN CODE               -EU-
         BR    R14                 RETURN TO CALLER                -EU-
*
*********************  END TERMINATION  ******************************
*
         MEND
         MACRO
         DEFCOM &DSECT=NO                                          -EU-
         SPACE 1
****************************************************************
*                 *** DATA AND WORK AREAS ***                  *
****************************************************************
*                                                              *
* FOLLOWING FIELDS: COMMPARM THRU COMMEND ARE COMMON AREAS     *
* SHARED BY THIS, AND CALLED SUB-PROGRAMS. ALL CHANGES MUST    *
* BE COORDINATED WITH ALL OTHER PROGRAMS.                      *
*                                                              *
****************************************************************
         SPACE 1
         AIF   ('&DSECT' EQ 'YES').GDSCT                           -EU-
COMMPARM DS    0D -------------------> COMMON AREAS
DBLWD    DC    D'0'                    DOUBLEWORD WORK AREA
PUNCHDCB DC    A(SYSPUNCH)             @ SYSPUNCH DCB
PRINTDCB DC    A(SYSPRINT)             @ SYSPRINT DCB
INDCB    DC    A(SYSIN)                @ SYSIN DCB
CSECT    DC    CL8' '                  SPECIFIED CSECT NAME
ESDID    DC    X'0001'                 ESD ID OF SPECIFIED CSECT
ENDLBLNM DC    CL8' '                  SYMBOL FOR END STMT BEGIN POINTE
LINECT   DC    PL2'0'                  PRINT LINE COUNTER
START    DC    F'0'                    LKED ASSIGNED START ADDR OF CSEC
END      DC    F'0'                    CSECT END ADDRESS
LENGTH   DC    F'0'                    LENGTH OF SPECIFIED CSECT
LBLTBL   DC    F'0'                    @ LABEL TABLE
CURRLBL  DC    F'0'                    CURRENT LABEL ENTRY ADDR
ENDLBL   DC    F'0'                    @ END OF LABEL TBL
SVCOPAD  DC    A(SVCOP)                @ SVC NAME TBL              -EU-
DBLOPAD  DC    A(DBLOP)                @ TWO-BYTE OP-CODE TBL      -EU-
SGLOPAD  DC    A(SGLOP)                @ SINGLE-BYTE OP-CODE TBL   -EU-
SVCOPL   DC    AL2(L'SVCOP)            SVC NAME TBL LEN            -EU-
DBLOPL   DC    AL2(L'DBLOP)            TWO-BYTE OP-COD TBL LEN     -EU-
SGLOPL   DC    AL2(L'SGLOP)            SINGLE-BYTE OP-COD TBL LEN  -EU-
LBLLGTH  DC    AL2(L'LABEL)            LENGTH OF LABEL ENTRY
TXTSTRT  DC    F'0'                    @ TEXT STORAGE AREA
TXTCURR  DC    F'0'                    @ CURRENT TEXT LOC
TXTEND   DC    F'0'                    @ END OF TEXT AREA
DTBLGTH  DC    A(256*L'DTL+8)          DSECT TABLE STORAGE LENGTH  -EU-
DTBSTRT  DC    F'0'                    DSECT TABLE START ADDR
DTBCURR  DC    F'0'                    DSECT TABLE CURRENT ENTRY ADDR
DTBEND   DC    F'0'                    DSECT TABLE END ADDR
USGLGTH  DC    A(256*L'USING+8)        USING TABLE STORAGE LENGTH  -EU-
USGSTRT  DC    F'0'                    USING TABLE START ADDR
USGCUR   DC    F'0'                    USING TABLE CURRENT ADDR
USGEND   DC    F'0'                    USING TABLE END ADDR
DATONLY  DC    F'0'                    DATA ONLY TABLE ADDR
DATOCUR  DC    F'0'                    CURRENT DATA ONLY ADDR
DATOEND  DC    F'0'                    END OF DATA ONLY TABLE
FLPTASM  DC    X'00'                   FLOATING POINT INDICATOR
PRIVASM  DC    X'00'                   PRIVILEGED INDICATOR
BREMASM  DC    X'00'                   BR EXT. MNEMONICS INDICATOR -EU-
USERR    DC    X'00'                   ERROR INDIC FOR DISASM1
NBRLBLS  DC    H'0'                    NBR LABELS FROM DISASM1
CARDNO   DC    PL4'0'                  OUTPUT CARD NBR
         SPACE 1
WORKREC  DS    0CL80                   DISASSEMBLY WORK AREA
NAME     DC    CL8' '                  NAME
         DC    CL1' '
MNEMONIC DC    CL5' '                  INSTRUCTION MNEMONIC
         DC    CL1' '
OPNDS    DC    CL27' '                 1ST OPERAND
         DC    CL1' '
COMMENT  DC    CL28' '                 COMMENT
COL72    DC    CL1' '
SEQNBR   DC    CL8' '                  CARD SEQ NBR
         SPACE 1
PRTLINE  DS    0CL121                  PRINT LINE
PCC      DC    C'1'                    CARRIAGE CONTROL
PRT      DC    CL120' '                PRINT DATA
         SPACE 1
         DS    0F
BLDLIST  DS    0CL62                   BLDL LIST
         DC    H'1'                    ONE ENTRY
         DC    H'58'                   LENGTH OF ENTRY
MEMBER   DC    CL8' '                  MEMBER NAME
TTRMOD   DC    XL3'000000'             TTR OF MODULE
CCAT     DC    XL1'00'                 CONCATENATION NUMBER
         DC    XL1'00'
ALIASIND DC    XL1'00'                 ALIAS AND MISC INDICATOR
*                           80=ALIAS
TTR1TXT  DC    XL3'000000'             TTR OF 1ST TXT RECORD
         DC    XL1'00'
TTRNS    DC    XL3'000000'             TTR OF NOTE OR SCATTER LIST
NNOTE    DC    XL1'00'                 NBR ENTRIES IN NOTE LIST
ATTR1A   DC    XL1'00'                 MODULE ATTRIBUTES 1, BYTE 1
*                           80=RENT
*                           40=REUS
*                           20=OVERLAY
*                           10=UNDER TEST
*                           08=ONLY LOADABLE
*                           04=SCATTER FORMAT
*                           02=EXECUTABLE
*                           01=ONE TXT, NO RLD RECORDS
ATTR1B   DC    XL1'00'                 ATTRIBUTES 1, BYTE 2
*                           80=CANNOT BE REPROCESSED BY LKED E
*                           40=ORIGIN OF 1ST TXT RECORD IS ZERO
*                           20=ASSIGNED ENTRY POINT ADDR IS ZERO
*                           10=CONTAINS NO RLD RECORD
*                           08=CANNOT BE REPROCESSED BY LKED
*                           04=CONTAINS TESTRAN SYMBOLS
*                           02=CREATED BY LKED
*                           01=REFR
TOTVIRT  DC    XL3'000000'             TOTAL VIRTUAL STRG REQRD FOR MOD
LENG1    DC    XL2'0000'               LENGTH OF 1ST TEXT RECORD
LKEPA    DC    XL3'000000'             ASSIGNED ENTRY POINT ADDR
ATTR2    DC    XL1'00'                 ATTRIBUTES 2
*                           80=PROCESSED BY OS/VS LKED
*                           20=PAGE ALIGNMENT REQUIRED FOR MODULE
*                           10=SSI PRESENT
         DC    XL2'0000'
SCTRLEN  DC    XL2'0000'               SCATTER LIST LENGTH
TTLEN    DC    XL2'0000'               TRANSLATION TABLE LENGTH
SCESDID  DC    XL2'0000'               CESD NBR FOR 1ST TXT RECD
SCEPESD  DC    XL2'0000'               CESD NBR FOR ENTRY POINT
ALEPA    DC    XL3'000000'             ENTRY POINT OF THE MEMBER NAME
ALMEM    DC    CL8' '                  REAL MEMBER NAME FOR ALIAS
SSI      DC    XL4'00000000'           SSI BYTES
AUTHLEN  DC    XL1'00'                 AUTH CODE LENGTH
AUTHCOD  DC    XL1'00'                 AUTH CODE
         AGO   .EXIT                                               -EU-
.GDSCT   ANOP  ,                                                   -EU-
COMMPARM DSECT , --------------------> COMMON AREAS
DBLWD    DS    D                       DOUBLEWORD WORK AREA
PUNCHDCB DS    F                       @ SYSPUNCH DCB
PRINTDCB DS    F                       @ SYSPRINT DCB
INDCB    DS    F                       @ SYSIN DCB
CSECT    DS    CL8                     SPECIFIED CSECT NAME
ESDID    DS    XL2                     ESD ID OF SPECIFIED CSECT
ENDLBLNM DS    CL8                     SYMBOL FOR END STMT BEGIN POINTE
LINECT   DS    PL2                     PRINT LINE COUNTER
START    DS    A                       LKED ASSIGNED START ADDR OF CSEC
END      DS    A                       CSECT END ADDRESS
LENGTH   DS    F                       LENGTH OF SPECIFIED CSECT
LBLTBL   DS    F                       @ LABEL TABLE
CURRLBL  DS    F                       CURRENT LABEL ENTRY ADDR
ENDLBL   DS    F                       @ END OF LABEL TBL
SVCOPAD  DS    A                       @ SVC NAME TBL              -EU-
DBLOPAD  DS    A                       @ TWO-BYTE OP-CODE TBL      -EU-
SGLOPAD  DS    A                       @ SINGLE-BYTE OP-CODE TBL   -EU-
SVCOPL   DS    AL2                     SVC NAME TBL LEN            -EU-
DBLOPL   DS    AL2                     TWO-BYTE OP-COD TBL LEN     -EU-
SGLOPL   DS    AL2                     SINGLE-BYTE OP-COD TBL LEN  -EU-
LBLLGTH  DS    AL2                     LENGTH OF LABEL ENTRY
TXTSTRT  DS    F                       @ TEXT STORAGE AREA
TXTCURR  DS    F                       @ CURRENT TEXT LOC
TXTEND   DS    F                       @ END OF TEXT AREA
DTBLGTH  DS    F                       DSECT TABLE STORAGE LENGTH  -EU-
DTBSTRT  DS    F                       @ DSECT TBL START
DTBCURR  DS    F                       @ CURRENT DSECT TBL ENTRY
DTBEND   DS    F                       @ DSECT TBL END
USGLGTH  DS    F                       USING TABLE STORAGE LENGTH  -EU-
USGSTRT  DS    F                       @ USING TBL START
USGCUR   DS    F                       @ CURRENT USING TBL ENTRY
USGEND   DS    F                       @ USING TBL END
DATONLY  DS    F                       @ DATA ONLY TABLE
DATOCUR  DS    F                       @ CURRENT DATA ONLY ENTRY
DATOEND  DS    F                       @ END OF DATA ONLY TABLE
FLPTASM  DS    XL1                     FLOATING POINT INDICATOR
PRIVASM  DS    XL1                     PRIVILEGED INDICATOR
BREMASM  DS    XL1                     BR EXT. MNEMONICS INDICATOR -EU-
USERR    DS    XL1                     ERROR INDIC FOR DISASM1
NBRLBLS  DS    H                       NBR LABELS FROM DISASM1
CARDNO   DS    PL4                     CURRENT OUTPUT CARD NBR
         SPACE 1
WORKREC  DS    0CL80                   DISASSEMBLY WORK AREA
NAME     DS    CL8                     NAME
         DS    CL1
MNEMONIC DS    CL5                     INSTRUCTION MNEMONIC
         DS    CL1
OPNDS    DS    CL27                    1ST OPERAND
         DS    CL1
COMMENT  DS    CL28                    COMMENT
COL72    DS    CL1                     CONTINUATION COLUMN
SEQNBR   DS    CL8                     CARD SEQUENCE NBR
         SPACE 1
PRTLINE  DS    0CL121                  PRINT LINE
PCC      DS    CL1                     CARRIAGE CONTROL
PRT      DS    CL120                   PRINT DATA
         SPACE 1
         DS    0F
BLDLIST  DS    0CL62                   BLDL LIST
         DS    H                       NUMBER OF ENTRY (ONE)
         DS    H                       LENGTH OF ENTRY
MEMBER   DS    CL8                     MEMBER NAME
TTRMOD   DS    XL3                     TTR OF MODULE
CCAT     DS    XL1                     CONCATENATION NUMBER
         DS    XL1
ALIASIND DS    XL1                     ALIAS AND MISC INDICATOR
*                           80=ALIAS
TTR1TXT  DS    XL3                     TTR OF 1ST TXT RECORD
         DS    XL1
TTRNS    DS    XL3                     TTR OF NOTE OR SCATTER LIST
NNOTE    DS    XL1                     NBR ENTRIES IN NOTE LIST
ATTR1A   DS    XL1                     MODULE ATTRIBUTES 1, BYTE 1
*                           80=RENT
*                           40=REUS
*                           20=OVERLAY
*                           10=UNDER TEST
*                           08=ONLY LOADABLE
*                           04=SCATTER FORMAT
*                           02=EXECUTABLE
*                           01=ONE TXT, NO RLD RECORDS
ATTR1B   DS    XL1                     ATTRIBUTES 1, BYTE 2
*                           80=CANNOT BE REPROCESSED BY LKED E
*                           40=ORIGIN OF 1ST TXT RECORD IS ZERO
*                           20=ASSIGNED ENTRY POINT ADDR IS ZERO
*                           10=CONTAINS NO RLD RECORD
*                           08=CANNOT BE REPROCESSED BY LKED
*                           04=CONTAINS TESTRAN SYMBOLS
*                           02=CREATED BY LKED
*                           01=REFR
TOTVIRT  DS    XL3                     TOTAL VIRTUAL STRG REQRD FOR MOD
LENG1    DS    XL2                     LENGTH OF 1ST TEXT RECORD
LKEPA    DS    XL3                     ASSIGNED ENTRY POINT ADDR
ATTR2    DS    XL1                     ATTRIBUTES 2
*                           80=PROCESSED BY OS/VS LKED
*                           20=PAGE ALIGNMENT REQUIRED FOR MODULE
*                           10=SSI PRESENT
         DS    XL2
SCTRLEN  DS    XL2                     SCATTER LIST LENGTH
TTLEN    DS    XL2                     TRANSLATION TABLE LENGTH
SCESDID  DS    XL2                     CESD NBR FOR 1ST TXT RECD
SCEPESD  DS    XL2                     CESD NBR FOR ENTRY POINT
ALEPA    DS    XL3                     ENTRY POINT OF THE MEMBER NAME
ALMEM    DS    CL8                     REAL MEMBER NAME FOR ALIAS
SSI      DS    XL4                     SSI BYTES
AUTHLEN  DS    XL1                     AUTH CODE LENGTH
AUTHCOD  DS    XL1                     AUTH CODE
.EXIT    SPACE 1                                                   -EU-
COMMEND  EQU   * <-------------------- END OF COMMON AREAS
         MEND
         MACRO
         DEFCDS &DUMMY                                             -EU-
LABELD   DSECT ,                       LABEL TABLE ENTRY
LABEL    DS    0CL13                   13-BYTE ENTRIES
LBLADR   DS    XL3                     RELATIVE ADDR IN TEXT
LBLTYP   DS    CL1                     TYPE: L=LABEL, A=ADCON, V=VCON,
LBLNAME  DS    CL8                     NAME (SYMBOL)
LBLLEN   DS    XL1                     LENGTH IF A, V, OR W
         SPACE 2
USINGD   DSECT ,                       USING TABLE ENTRY
USING    DS    0CL11
USBGN    DS    XL3                     OFFSET TO BEGINNING OF RANGE
USEND    DS    XL3                     OFFSET TO END OF RANGE
USREG    DS    XL1                     BASE REGISTER USED
USTYPE   DS    XL1                     TYPE:P=PGM,D=DSECT
USVALU   DS    XL3                     BASE REG VALUE
         SPACE 2                                                   -EU-
DTDS     DSECT ,                       DSECT TABLE ENTRY           -EU-
DTL      DS    0CL14                                               -EU-
DTNAME   DS    CL8                     DSECT NAME                  -EU-
DTLGTH   DS    XL3                     DSECT FIELD TABLE LENGTH    -EU-
DTADDR   DS    XL3                     DSECT FIELD TABLE ADDRESS   -EU-
         SPACE 2
DSECTD   DSECT ,                       DSECT FIELD TABLE ENTRY
DSECT    DS    0CL13
DSOFSET  DS    XL3                     OFFSET TO 1ST BYTE OF FIELD
DSLBTYP  DS    CL1                     LABEL TYPE = L
DSNAME   DS    CL8                     FIELD NAME
DSLENG   DS    XL1                     FIELD LENGTH
         SPACE 2                                                   -EU-
         PRINT NOGEN                                               -EU-
         DCBD  DEVD=DA,DSORG=PS                                    -EU-
         MEND
         MACRO
         DEFINS &DSECT=NO                                          -EU-
         PRINT GEN                                                 -EU-
         EJECT ,                                                   -EU-
******************************************************************
*                                                                *
* INSTRUCTION DISASSEMBLY WORK-AREA.                             *
*                                                                *
******************************************************************
         SPACE 1
         AIF   ('&DSECT' NE 'YES').GENDT                           -EU-
INSTWA   DSECT ,                                                   -EU-
.GENDT   ANOP                                                      -EU-
INSTENT  DS    0CL10          CURRENT INSTRUCTION ENTRY
INAME    DC    CL5' '              INSTR NAME (MNEMONIC)
ITYPE    DC    XL1'0'              INSTRUCTION TYPE
RR       EQU   0                       RR FORMAT
RX       EQU   4                       RX FORMAT
S        EQU   8                       S FORMAT
SI       EQU   12                      SI FORMAT
RS       EQU   16                      RS FORMAT
SS1      EQU   20                      SS FORMAT, 1 LENGTH
SS2      EQU   24                      SS FORMAT, 2 LENGTHS
TWO      EQU   28                      TWO BYTE OP-CODE
CONDBR   EQU   32                      CONDITIONAL BRANCH
SVC      EQU   36                      SUPERVISOR CALL
SSR      EQU   40                      SS FORMAT, 2 REGISTERS      -EU-
RRE      EQU   44                      RRE FORMAT, 2 REGISTERS     -EU-
SSE      EQU   48                      SSE FORMAT                  -EU-
ICLASS   DC    XL1'0'              INSTRUCTION CLASS
PRIV     EQU   X'01'                   PRIVILEGED INSTRUCTION
IOP      EQU   X'02'                   IGNORE OPERAND              -EU-
FLTPT    EQU   X'10'                   FLOATING POINT INSTR        -EU-
FLSHT    EQU   X'20'                   SHORT PREC FLT PT INSTR     -EU-
FLEXR    EQU   X'40'                   EXTENDED FLT PT INSTR       -EU-
IEDT     DC    XL1'0'              INSTRUCTION EDITS
S1       EQU   X'01'                   1ST OPND MUST HAVE BASE/INDEX
S2       EQU   X'02'                   2ND OPND MUST HAVE BASE
E2       EQU   X'04'                   2ND OPND ON HALFWORD BOUND
E4       EQU   X'08'                   2ND OPND ON FULLWORD BOUND
E8       EQU   X'10'                   2ND OPND ON DBL WORD BOUND
EPR      EQU   X'20'                   EVEN-ODD REGISTER PAIR
E1R      EQU   X'40'                   RRE FORMAT, 1 REGISTER      -EU-
ICCSET   DC    XL1'0'              TYPE CONDITION CODE SET
ARITH    EQU   X'01'                   ARITHMETIC TYPE
CPR      EQU   X'02'                   COMPARE TYPE
ZRO8     EQU   X'04'                   BC 8 MAY BE BZ
INLNG    DC    XL1'0'              INSTRUCTION LENGTH
         MEND                                                      -EU-
