* HERE FOLLOWS THE ASSEMBLY SYSPARM(...) OPTIONS PROCESS WHICH ALLOWS
*        YOU TO SELECT :
*        NONE OR OMITTED = NO LIST OF MACROS DEVELOPMENT
*        SHORT = REDUCED LIST OF MACROS DEVELOPMENT
*        FULL = LIST ALL MACROS DEVELOPMENT
*        HOW TO DEFINE OPTIONS : I.E - SYSPARM(FULL)
*        IF YOU WANT ADD ANOTHER SELECTION, YOU CAN EASILY DO IT
*        ADJUSTING BELOW THE STATEMENTS MARKED "<===", DEFINE THE NEW
*        OPT(N) DEFAULT, AND SUPPLY THE NEW OPTION CODING JUST BEFORE
*        THE COMMENT CARD MARKED "---> ADD OPTION(S)".
         GBLC  &PRS,&PRF                                      <===
         LCLA  &CN,&CT,&LN,&MX,&SB,&ST
         LCLC  &OPT(1)                                        <===
&MX      SETA  1                                              <===
&OPT(1)  SETC  'NONE'
&CN      SETA  K'&SYSPARM
         AIF   (K'&SYSPARM EQ 0).SP99
&CT      SETA  1
&SB      SETA  1
&ST      SETA  &CT
.SP1     AIF   ('&SYSPARM'(&CT,1) EQ ',').SP2
&CT      SETA  &CT+1
         AIF   (&CT LE &CN).SP1
.SP2     ANOP
&LN      SETA  &CT-&ST
         AIF   (&LN EQ 0).SP3
&OPT(&SB) SETC '&SYSPARM'(&ST,&LN)
.SP3     ANOP
&SB      SETA  &SB+1
         AIF   (&SB GT &MX).SP99
&CT      SETA  &CT+1
         AIF   (&CT GT &CN).SP99
&ST      SETA  &CT
         AGO   .SP1
.SP99    AIF   ('&OPT(1)' NE 'FULL').SL1
&PRS     SETC  'GEN'
&PRF     SETC  'GEN'
         AGO   .SL99
.SL1     AIF   ('&OPT(1)' NE 'SHORT').SL2
&PRS     SETC  'GEN'
&PRF     SETC  'NOGEN'
         AGO   .SL99
.SL2     ANOP
&PRS     SETC  'NOGEN'
&PRF     SETC  'NOGEN'
* ---> ADD OPTION(S)
.SL99    ANOP
