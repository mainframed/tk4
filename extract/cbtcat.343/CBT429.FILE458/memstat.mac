MMST     TITLE 'M E M S T A T - MEMBER STATUS TSO COMMAND.'
MEMSTAT  START 0
         SPACE 1
*        DATA SET 761KKMSTAT AT LEVEL 001 AS OF 10/13/80
*        DATA SET SW2MEMSTAT AT LEVEL 001 AS OF 07/01/74
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
* PURPOSE :    THE 'MEMSTAT' COMMAND WELL GO OUT TO A LIBRARY AND     *
* ---------    LOOK FOR A PARTICULAR MEMBER.                          *
* SYNTAX :     MEMSTAT LIBRARY(MEMBER) LIST                           *
* --------                             NOLIST                         *
* ORIGIN :     EXTRACTED FROM CBT TAPE 89 FEB, FILE 270.              *
* --------     COMMAND NAME : MEMSTAT (06/25/82)                      *
*              SOURCE : KERMIT KISER                                  *
*              AGENCY : WDPSC                                         *
*              UPDATED BY MOINIL P.A.                                 *
*                         COMPUTING CENTRE (TP 361)                   *
*                         J.R.C. - ISPRA ESTABLISHMENT                *
*                         21020 ISPRA (VA), ITALY                     *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
MEMSTAT AMODE  24
MEMSTAT RMODE  24
         SPACE 1
        $DEFREG
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
* REGISTERS USE :                                                     *
* ---------------                                                     *
*        R0  = WORK                                                   *
*        R1  = PARM. / WORK                                           *
*        R2  = WORK                                                   *
*        R3  = WORK                                                   *
*        R4  = DA08 / DA18 POINTER                                    *
*        R5  = PDE'S POINTER                                          *
*        R6  = DCB POINTER                                            *
*        R7  = DAPL POINTER                                           *
*        R8  = PPL POINTER                                            *
*        R9  = RETURN CODE                                            *
*        R10 = CPPL POINTER                                           *
*        R11 = WORK-AREAS POINTER                                     *
*        R12 = BASE REGISTER                                          *
*        R13 = SAVE AREA POINTER                                      *
*        R14 = LINK / WORK                                            *
*        R15 = CALL / WORK                                            *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        $XENT  BASE=R12,LV=WORKLEN,TYPE=RENT
         LR    R11,R13             SET WORK-AREA REGISTER
         LA    R2,18*4(R11)
         L     R3,=A(WORKLEN-(18*4))
         LR    R4,R2
         XR    R5,R5
         MVCL  R2,R4
         USING WORKAREA,R11
        $TEW$EC MSG=PUTMSG,ERR=SETERR,PUT=PUTLNE
         LR    R10,R2
         USING CPPL,R10
         L     R1,CPPLUPT
         USING UPT,R1
         MVC   USID(L'UPTPREFX+L'UPTPREFL),UPTPREFX
         DROP  R1
        $TEW$PP PARM=PARMLST,REG=5
         MVC   HOLDDSN,=CL44' '
         L     R14,DSNPTR
         LH    R1,DSNLEN
         BCTR  R1,0
         LA    R15,HOLDDSN
         EX    R1,MOVIT
         TM    DSNFLG,X'40'
         BZ    APPENDID
         TM    MEMFLG,X'80'
         BO    ALLOCIT
         B     NOMEM
MOVIT    MVC   0(*-*,R15),0(R14)   <<EXECUTED>>
APPENDID MVC   HOLDDSN(L'USID),USID
         XR    R1,R1
         IC    R1,USIDL
         LA    R0,C'.'
         STC   R0,HOLDDSN(R1)
         LH    R15,DSNLEN
         LA    R15,1(R15,R1)
         STH   R15,DSNLEN
         LA    R15,HOLDDSN+1(R1)
         L     R14,DSNPTR
         LH    R1,DSNLEN
         BCTR  R1,0
         EX    R1,MOVIT
ALLOCIT STAX   DEFER=YES           DEFER ALL ATTENTIONS
         LH    R1,DSNLEN
         STH   R1,HOLDDSNL
         LA    R4,DA08LIST
         USING DAPB08,R4
         MVC   DA08CD,=XL2'0008'
         MVI   DA08DSP1,DA08SHR
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         MVI   DA08DSO,DCBDSGPO
         MVC   DA08DDN(L'DA08DDN),=CL44' '
         MVC   DA08UNIT(L'DA08UNIT),=CL44' '
         MVC   DA08SER(L'DA08SER),=CL44' '
         MVC   DA08MNM(L'DA08MNM),=CL44' '
         MVC   DA08PSWD(L'DA08PSWD),=CL44' '
         LA    R1,HOLDDSNL
         ST    R1,DA08PDSN
         LA    R7,DAPLIST
         USING DAPL,R7
         LA    R1,DAPLECBS
         ST    R1,DAPLECB
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         ST    R4,DAPLDAPB
         BAS   R14,XDAIR
         B     DAIRERR             RETURN +0
         MVC   OPNLIST(DIRLGTH),DIROPN    +4
         LA    R6,LIBDIR
         USING IHADCB,R6
         MVC   DCBDDNAM,DA08DDN
         DROP  R4
         MVC   HOLDMEM(L'HOLDMEM),=CL44' '
         L     R14,MEMPTR
         LH    R1,MEMLEN
         BCTR  R1,0
         LA    R15,HOLDMEM
         EX    R1,MOVIT
        OPEN   ((R6)),MF=(E,OPNLIST)
         TM    DCBOFLGS,DCBOFOPN
         BZ    OPNERR
        FIND   (R6),HOLDMEM,D
         LTR   R15,R15
         BNZ   NOTFOUND
         TM    KEYNUM,X'01'
         BZ    SETRC
         MVC   DIAG(21),=CL21'MEMBER          FOUND'
         MVC   DIAG+7(L'HOLDMEM),HOLDMEM
         LA    R1,DIAG
         LA    R0,21
         BAS   R14,PUTLNE
SETRC    XR    R9,R9               MEMBER FOUND --------------------- 0
         B     ENDIT
NOTFOUND LA    R9,4                MEMBER NOT FOUND ----------------- 4
         TM    KEYNUM,X'01'
         BZ    ENDIT
         MVC   DIAG(31),=CL31'MEMBER          NOT IN DATA-SET'
         MVC   DIAG+7(L'HOLDMEM),HOLDMEM
         LA    R1,DIAG
         LA    R0,31
         BAS   R14,PUTLNE
ENDIT   CLOSE  ((R6)),MF=(E,CLSLIST)
LEAVE    MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA    R4,DA18LIST
         USING DAPB18,R4
         MVC   DA18CD,=XL2'0018'
         MVI   DA18DPS2,DA18KEEP
         MVC   DA18DDN(L'DA18DDN),=CL44' '
         MVC   DA18MNM(L'DA18MNM),=CL44' '
         MVC   DA18SCLS(L'DA18SCLS),=CL44' '
         MVC   DA18JBNM(L'DA18JBNM),=CL44' '
         LA    R1,HOLDDSNL
         ST    R1,DA18PDSN
         DROP  R4
         ST    R4,DAPLDAPB
         BAS   R14,XDAIR
         NOP   0                   RETURN +0
        STAX   DEFER=NO                   +4 RESTORE ALL ATTENTIONS
QUIT    IKJRLSA #TSANSW
        $XRET  CC=(R9),LV=WORKLEN,TYPE=RENT
NOMEM    LA    R1,MNME
         LA    R0,L'MNME
MSGERR   BAS   R14,PUTLNE
         B     SETERR
DAIRERR  LA    R1,MLNF
         LA    R0,L'MLNF
         BAS   R14,PUTLNE
        STAX   DEFER=NO            RESTORE ALL ATTENTIONS
         B     SETERR
OPNERR   LA    R1,MLOE
         LA    R0,L'MLOE
         BAS   R14,PUTLNE
         EX    0,SETERR
         B     LEAVE
SETERR   LA    R9,8                ERROR ---------------------------- 8
         B     QUIT
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        XDAIR - IKJDAIR ROUTINE                                      *
*              R14 : LINK REGISTER                                    *
*                    RETURN = +0 - IKJDAIR ERROR                      *
*                             +4 - IKJDAIR OK                         *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
XDAIR    STM   R0,R15,SAVEREGX     SAVE CALLER'S REGS
         XC    DAPLECBS,DAPLECBS
        CALLTSSR EP=IKJDAIR,MF=(E,(R7))
         LTR   R15,R15             TEST RETURN CODE
         BNZ   *+L'*+8             BRANCH IF ERROR
         LM    R0,R15,SAVEREGX     OK, RESTORE CALLER'S REGS
         B     4(R14)              NORMAL RETURN IS +4
         ST    R15,RETC            STORE RETURN CODE
         LA    R2,DFPLIST
         USING DFDSECTD,R2
         ST    R7,DFDAPLP
         LA    R1,RETC
         ST    R1,DFRCP
         LA    R1,ZERO
         ST    R1,DFJEFF02
         LA    R1,DASW
         ST    R1,DFIDP
         DROP  R2
         LR    R1,R2               PARM. ADDRESS
        LINK   EP=IKJEFF18         ERROR MESSAGE ROUTINE
         LTR   R15,R15             TEST RETURN CODE
         BZ    XDAIRX              OK, GO BACK
         MVC   DIAG(29),=CL29'DAIRFAIL ERROR. RETURN CODE :'
         CVD   R15,WORKD
         MVC   DIAG+29(6),=XL6'402020202120'
         ED    DIAG+29(6),WORKD+5
         LA    R1,DIAG
         LA    R0,35
         BAS   R14,PUTLNE
XDAIRX   LM    R0,R15,SAVEREGX     RESTORE CALLER'S REGS
         BR    R14                 ERROR RETURN IS +0
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        CONTROL BLOCKS INITIALIZATION DATA                           *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        PRINT  NOGEN
DIROPN  OPEN   (*-*),MF=L
DIRCLS  CLOSE  (*-*),MF=L
DIRDCB  DCB    DSORG=PO,DDNAME=XXXXXXXX,MACRF=R,EODAD=SETRC,           X
               RECFM=F,BLKSIZE=256,KEYLEN=0
DIRLGTH  EQU   *-DIROPN
        PRINT  GEN
         SPACE 1
ZERO     DC    F'0'
DASW     DC    AL1(0,DFDAIR)
MNME     DC    C'NO MEMBER ENTERED'
MLNF     DC    C'LIBRARY NOT FOUND'
MLOE     DC    C'LIBRARY OPEN ERROR'
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        LITERAL POOL                                                 *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        LTORG
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        OPERANDS PARM. LIST                                          *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        PRINT  NOGEN
PARMLST IKJPARM
DSNME   IKJPOSIT DSNAME,LIST,PROMPT='ENTER LIBRARY(MEMBER) TO CHECK',  X
               HELP='LIBRARY(MEMBER)'
LOPTN   IKJKEYWD DEFAULT='LIST'
        IKJNAME 'LIST'
        IKJNAME 'NOLIST'
        PRINT  GEN
DSNPTR   EQU   DSNME,4             PDE'S ENTRIES DEFINITIONS
DSNLEN   EQU   DSNME+4,2
DSNFLG   EQU   DSNME+6,1
MEMPTR   EQU   DSNME+8,4
MEMLEN   EQU   DSNME+12,2
MEMFLG   EQU   DSNME+14,1
KEYNUM   EQU   LOPTN+1,1
        PRINT  NOGEN
        IKJENDP
        PRINT  GEN
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        WORK-AREA                                                    *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
WORKAREA DSECT
         DS    18F                 SAVE AREA
WORKD    DS    D
SAVEREGX DS    16F            XDAIR - SAVE REGISTERS 0-15
        $TEW$WA
         SPACE 1
DAPLIST  DS    0F                  DAIR PARM. LIST
         DS    (DAPLLGTH)X'0'
DAPLECBS DS    F
         SPACE 1
DA08LIST DS    0F                  DAIR ALLOC. PARM. LISTS
         DS    (DA08LGTH)X'0'
DA18LIST DS    0F
         DS    (DA18LGTH)X'0'
         SPACE 1
DFPLIST  DS    0F                  DAIR FAILURE PARAMETER LIST
         DS    (DFLEN)X'0'
RETC     DS    F
         SPACE 1
OPNLIST OPEN   (*-*),MF=L
CLSLIST CLOSE  (*-*),MF=L
LIBDIR  DCB    DSORG=PO,DDNAME=XXXXXXXX,MACRF=R,EODAD=SETRC,           X
               RECFM=F,BLKSIZE=256,KEYLEN=0
         SPACE 1
HOLDDSNL DS    H
HOLDDSN  DS    CL44
HOLDMEM  DS    CL8
USID     DS    CL7,XL1
USIDL    EQU   USID+L'USID,1
DIAG     DS    CL60
         SPACE 1
WORKLEN  EQU   (((*-WORKAREA)+7)/8)*8
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        DSECTS CONTROL BLOCKS                                        *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        IKJUPT
        IKJDAPL
DAPLLGTH EQU   *-DAPL
        IKJDAP08
DA08LGTH EQU   *-DAPB08
        IKJDAP18
DA18LGTH EQU   *-DAPB18
        IKJEFFDF DFDSECT=YES
        DCBD   DEVD=DA,DSORG=PO
        PRINT  GEN
         SPACE 1
         END
