PSWS     TITLE 'PACKLIST - SAMPLE TAKER.'
PSWSAMP START 0
         SPACE 1
*        DATA SET CBT932 - AT LEVEL 001 AS OF 02/25/79
*                          PSWSAMP VERSION 1.0 (WDPSC)
         SPACE 1
* PURPOSE OF ORIGINAL VERSION :
* -----------------------------
*
*              IS THE SAMPLE TAKER FOR PACKLIST. THE TECHNIQUE IS
*              TO MOVE THE TRACE TABLE TO AN OUTPUT RECORD. IT DOES
*              NOT MATTER THAT THE MOVE LONG OF THE TABLE CAN BE
*              INTERUPTED. THE VALUES TAKEN AT ANY POINT ARE A SAMPLE,
*              WHETHER INTERUPTED OR NOT.
*              THE SAMPLE WILL NOT BEGIN UNLESS THE FIRST ENTRY OF THE
*              TABLE IS NOW DIFFERENT THAN THE PREVIOUS SAMPLE. THE
*              SAMPLE RATE IS APPROXIMATELY ONCE EVERY 2.5 SECONDS
*              (THE FIELD HOW LONG CAN BE ADJUSTED).
*              THE TECHNIQUE FOR OVERALL SAMPLING AND END USE IS
*              COVERED IN THE PACKLIST PROGRAM.
*
*              PSWSAMP WILL RUN FOR THE TIME INTERVAL SET WITHIN THIS
*              PROGRAM (UNLESS CHANGED BY THE USE OF PARM=... IN THE
*              EXEC) AND THEN WILL AUTOMATICALLY TERMINATE. IT CAN
*              ALSO BE STOPPED BY REPLYING TO THE WTOR ISSUED.
         EJECT ,                                                   -EU-
* STRATEGY CHANGES DESCRIPTION :                                   -EU-
* ------------------------------                                   -EU-
*                                                                  -EU-
*              ORIGINALLY, THIS SAMPLER TAKER WAS COPYING THE      -EU-
*              ENTIRE TRACE TABLE, BUT THE PACKLIST PROGRAM        -EU-
*              REALLY USE ONLY 1/10 OF ALL ENTRIES, ONLY THOSE     -EU-
*              WHICH FAULTS INTO THE LPA BOUNDARIES. SO, THE       -EU-
*              NUMBER OF OUTPUT RECORDS WILL BE CONSIDERABLY       -EU-
*              REDUCED BY SELECTING THE USEFULL TRACE TABLE        -EU-
*              ENTRIES ONLY.                                       -EU-
         SPACE 1                                                   -EU-
*     N O T E  THAT THE SIZE (BLKSIZE) OF THE OUTPUT RECORD        -EU-
*              DEPENDS UPON THE CHOICE OF THE VALUE SPECIFIED      -EU-
*              TO THE PARAMETER HEREAFTER :                        -EU-
         SPACE 1                                                   -EU-
NTTE     EQU   355            NUMBER OF TRACE TABLE ENTRIES        -EU-
         SPACE 1                                                   -EU-
*              AN ENTRY OF THE TRACE TABLE IS 32 BYTES LONG.       -EU-
*              HERE FOLLOWS, AS EXAMPLE, SOME VALUES TO SET AN     -EU-
*              ADJUSTED DATA-SET BLOCK-SIZE CONSIDERING THE DISK   -EU-
*              TYPE ON WHICH IT RESIDES :                          -EU-
*                   400 - 3330 DISK (1 REC/TRK)                    -EU-
*                   295 - 3350 DISK (2 REC/TRK)                    -EU-
*                   355 - 3380 DISK (4 REC/TRK)                    -EU-
         SPACE 1                                                   -EU-
*              THE ORIGINAL PENDING WTOR USED TO STOP THE PROGRAM  -EU-
*              HAS BEEN REPLACED BY THE MAIN CONSOLE OPERATOR STOP -EU-
*              COMMAND, AND THE TIME INTERVAL LIMIT MAY BE CHANGED -EU-
*              AT ANY MOMENT THROUGH THE MODIFY COMMAND.           -EU-
*              NOW, THE PROGRAM TRIES TO DYNAMICALLY ADJUST THE    -EU-
*              SAMPLE RATE STARTING WITH ONCE EVERY 2.4 SECONDS.   -EU-
         SPACE 1                                                   -EU-
*              THE AMBLIST SERVICE AIDS AND THE IEHLIST UTILITY    -EU-
*              PROGRAMS ARE ALSO INTERNALLY INVOKED TO PRODUCE     -EU-
*              THE LISTLPA AND LISTPDS OF THE CURRENTLY RUNNING    -EU-
*              SYSTEM, AVOIDING POSSIBLE PROBLEMS WHEN EXECUTING   -EU-
*              THE PACKLIST PROGRAM LATER.                         -EU-
         SPACE 1                                                   -EU-
*              NOW, AT THE START TIME, WHEN THE TRACE IS FOUND     -EU-
*              INACTIVE, THE PROGRAM TRIES TO START IT FOR HIS     -EU-
*              PURPOSES. IF SUCCESSFULLY STARTED, THEN IT STOPS    -EU-
*              THE TRACE AUTOMATICALLY AT THE END.                 -EU-
         EJECT ,                                                   -EU-
* INVOKING JCL :
* --------------
*
*   //SAMPLER  EXEC PGM=PSWSAMP,PARM=NNNN                          -EU-
*   //SYSDIR     DD DSN=SYS1.LPALIB,DISP=SHR ---> MVS/370          -EU-
*   OR :                                                           -EU-
*   //SYSDIR0    DD DSN=SYS1.LPALIB,DISP=SHR ---> MVS/XA           -EU-
*   //SYSDIR1    DD DSN=OTHER.LPALIB,DISP=SHR ---> MVS/XA          -EU-
*   //SYSPSW     DD ... OUTPUT SAMPLER DATA-SET ...                -EU-
*   OR OMIT IT IF PARM=NULL IS SPECIFIED.                          -EU-
*   //SYSPRINT   DD ... OUTPUT LISTLPA DATA-SET / AMBLIST ...      -EU-
*   //SYSIN      DD ... (LISTLPA STATEMENT / AMBLIST) ...          -EU-
*   //SYSDOU     DD ... OUTPUT LISTPDS DATA-SET(S) / IEHLIST ...   -EU-
*   //SYSDIN     DD ... (LISTPDS STATEMENT(S) / IEHLIST) ...       -EU-
*                                                                  -EU-
*        WHERE NNNN (1 TO 4 NUMERIC DIGITS) IS THE TIME INTERVAL   -EU-
*                   LIMIT EXPRESSED IN MINUTES (MAXIMUM IS 1200).  -EU-
*        IF PARM=NULL IS SPECIFIED, THEN ONLY LISTLPA (AMBLIST)    -EU-
*                   AND LISTPDS (IEHLIST) ARE EXECUTED.            -EU-
*        IF PARM=... IS OMITTED, THE DEFAULT IS 15 MINUTES.        -EU-
*
* CBT ORIGIN : EXTRACTED FROM CBT TAPE FEB 85, FILE 270.
* ------------ ADAPTED BY : MOINIL P.A.
*                           COMPUTING CENTRE
*                           J.R.C. - ISPRA ESTABLISHMENT
*                           21020 ISPRA (VA), ITALY
         SPACE 2                                                   -EU-
PSWSAMP AMODE  24                                                  -EU-
PSWSAMP RMODE  24                                                  -EU-
         SPACE 2                                                   -EU-
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *  -EU-
*        AUTHORITY LEVELS DEFINITIONS.                          *  -EU-
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *  -EU-
         SPACE 1                                                   -EU-
LV15     EQU   15                  FULLY AUTHORIZED                -EU-
         SPACE 1                                                   -EU-
        $MDL@IX ,                                                  -EU-
         SPACE 2                                                   -EU-
*        MISCELLANEOUS DEFINITIONS                                 -EU-
*        -------------------------                                 -EU-
         SPACE 1                                                   -EU-
TMVSXA   EQU   X'01'          MVS/XA SYSTEM TYPE                   -EU-
EL       EQU   X'80'          END OF LIST INDICATOR                -EU-
SETNOP   EQU   X'0F'          SET NO-OPERATION                     -EU-
SETBR    EQU   X'F0'          SET BRANCH                           -EU-
         EJECT ,                                                   -EU-
        $DEFREG ,                                                  -EU-
        $XENT  BASE=(R11,R12)                                      -EU-
         XR    R10,R10             C.C. = 0                        -EU-
         L     R8,0(R1)            GET PARM DATA AREA ADDRESS      -EU-
         EJECT
*        VERIFY CALLER AUTHORITY                                   -EU-
*        -----------------------                                   -EU-
         SPACE 1                                                   -EU-
        EXTRACT ATIOT,'S',FIELDS=(TIOT,TSO)                        -EU-
         L     R1,ATSO             TSO FLAG ADDRESS                -EU-
         TM    0(R1),X'80'         ARE WE RUNNING IN TSO ?         -EU-
         BZ    NOTTSO              BRANCH IF NOT                   -EU-
        TPUT   NOTAUT,L'NOTAUT                                     -EU-
         B     RETURN              REJECTED IF TSO ADDRESS SPACE   -EU-
NOTTSO  $EACM  REQAUT                                              -EU-
         CLI   AUTH,LV15           AUTHORIZED ?                    -EU-
         BNL   CHPRM               YES                             -EU-
        WTO    ' PSWSAMP - INSUFFICIENT AUTHORITY.',               -EU-X
               ROUTCDE=(2,11),DESC=(6)                             -EU-
         B     RETURN              REJECTED BECAUSE UNAUTHORIZED   -EU-
         EJECT
*        HANDLE PARM=... IF ANY VALID                              -EU-
*        ----------------------------                              -EU-
*        (TIME INTERVAL LIMIT EXPRESSED IN MINUTES OR 'NULL')      -EU-
         SPACE 1                                                   -EU-
CHPRM    LH    R1,0(R8)            PARM DATA COUNT                 -EU-
         LTR   R1,R1                                               -EU-
         BNP   SETUP               NO PARM SPECIFIED               -EU-
         CH    R1,PRMLG                                            -EU-
         BH    SETUP               INVALID, IGNORE IT              -EU-
         LA    R2,VALUE+L'VALUE                                    -EU-
         SLR   R2,R1                                               -EU-
         BCT   R1,*+L'*+6                                          -EU-
         MVC   0(*-*,R2),2(R8)     GET PARM DATA                   -EU-
         EX    R1,*-6                                              -EU-
         CLC   VALUE,=CL4'NULL'    NO SAMPLER REQUESTED ?          -EU-
         BNE   *+L'*+8             NO, GO TEST FOR A TIME VALUE    -EU-
         L     R4,CVTPTR           CVT POINTER                     -EU-
         B     GETSID                                              -EU-
         CLC   VALUE,LOWV                                          -EU-
         BL    SETUP               INVALID, IGNORE IT              -EU-
         CLC   VALUE,HIGHV                                         -EU-
         BH    SETUP               INVALID, IGNORE IT              -EU-
         PACK  WORKD,VALUE                                         -EU-
         CVB   R3,WORKD                                            -EU-
         M     R2,=F'6000'                                         -EU-
         ST    R3,MAXTIME          SET TIME INTERVAL LIMIT         -EU-
         B     EDTME                                               -EU-
SETUP    L     R3,MAXTIME                                          -EU-
         XR    R2,R2                                               -EU-
         D     R2,=F'6000'                                         -EU-
         CVD   R3,WORKD            EDIT VALUE IN MESSAGE           -EU-
EDTME    MVC   STMEL+40(L'EDMSK),EDMSK                             -EU-
         ED    STMEL+40(L'EDMSK),WORKD+L'WORKD-(L'EDMSK/2)         -EU-
         EJECT
*        TEST TRACE STATUS
*        -----------------
         SPACE 1                                                   -EU-
         L     R15,=A(XTTR)        GO TEST STATUS                  -EU-
         BASR  R9,R15                                              -EU-
         B     TRCOK                                               -EU-
         B     ERR9                                                -EU-
        $SSCMD STRCMD              TRY TO START TRACE              -EU-
         LTR   R15,R15                                             -EU-
         BZ    TRSTD                                               -EU-
         STC   R15,SRC+L'SRC+29                                    -EU-
         SRL   R15,4                                               -EU-
         STC   R15,SRC+L'SRC+28                                    -EU-
         NC    SRC+L'SRC+28(3),MASK                                -EU-
         TR    SRC+L'SRC+28(3),TRTAB                               -EU-
SRC     WTO    ' PSWSAMP - SSCMD R.C. =     (TRACE ST).',          -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,103             C.C. = 103 ******************** -EU-
         B     LEAVE                                               -EU-
         SPACE 1                                                   -EU-
TRSTD    OI    SWPROC,SWTST        SET COMMAND SENDED              -EU-
TRCOK    L     R4,CVTPTR           CVT POINTER                     -EU-
         USING CVT,R4                                              -EU-
         EJECT ,                                                   -EU-
*        GET SYSTEM ID                                             -EU-
*        -------------                                             -EU-
         SPACE 1                                                   -EU-
GETSID   L     R1,CVTSMCA          SMF CONTROL AREA ADDRESS        -EU-
         USING SMCABASE,R1                                         -EU-
         MVC   SID,SMCASID         SYSTEM ID (SID)                 -EU-
         DROP  R1,R4                                               -EU-
         SPACE 1                                                   -EU-
*        EXECUTE AMBLIST (LISTLPA)                                 -EU-
*        -------------------------                                 -EU-
         SPACE 1                                                   -EU-
        LINK   EP=AMBLIST                                          -EU-
         LTR   R15,R15                                             -EU-
         BZ    XLPDS                                               -EU-
         STC   R15,ARC+L'ARC+31                                    -EU-
         SRL   R15,4                                               -EU-
         STC   R15,ARC+L'ARC+30                                    -EU-
         NC    ARC+L'ARC+30(2),MASK                                -EU-
         TR    ARC+L'ARC+30(2),TRTAB                               -EU-
ARC     WTO    ' PSWSAMP - AMBLIST R.C. =   .',                    -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,100             C.C. = 100 ******************** -EU-
         B     LEAVE                                               -EU-
         EJECT ,                                                   -EU-
*        EXECUTE IEHLIST (LISTPDS)                                 -EU-
*        -------------------------                                 -EU-
         SPACE 1                                                   -EU-
XLPDS   LINK   EP=IEHLIST,PARAM=(OPTLIST,DDNMELST),VL=1            -EU-
         LTR   R15,R15                                             -EU-
         BZ    RLOPEN                                              -EU-
         STC   R15,IRC+L'IRC+31                                    -EU-
         SRL   R15,4                                               -EU-
         STC   R15,IRC+L'IRC+30                                    -EU-
         NC    IRC+L'IRC+30(2),MASK                                -EU-
         TR    IRC+L'IRC+30(2),TRTAB                               -EU-
IRC     WTO    ' PSWSAMP - IEHLIST R.C. =   .',                    -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,101             C.C. = 101 ******************** -EU-
         B     LEAVE                                               -EU-
         EJECT ,                                                   -EU-
*        GET LPA BOUNDARIES                                        -EU-
*        ------------------                                        -EU-
         SPACE 1                                                   -EU-
RLOPEN  OPEN   (SYSLPA,(INPUT))    INPUT IS AMBLIST OUTPUT         -EU-
         TM    SYSLPA+(DCBOFLGS-IHADCB),DCBOFOPN  OK OPEN ?        -EU-
         BO    RLSRCH              YES                             -EU-
        WTO    ' PSWSAMP - AMBLIST OUTPUT OPEN ERROR.',            -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,102             C.C. = 102 ******************** -EU-
         B     LEAVE                                               -EU-
         SPACE 1                                                   -EU-
RLSRCH  GET    SYSLPA,PI           SEARCH FOR NUMERIC SESSION...   -EU-
         CLI   CC,C'1'             TITLE PAGE ?                    -EU-
         BNE   RLSRCH              NO... GET NEXT PRINT IMAGE      -EU-
         CLC   CC+15(L'LPMNUM),LPMNUM   START OF NUMERICAL LIST ?  -EU-
         BE    RLFNUM              YES                             -EU-
         CLC   CC+1(L'LPMNEW),LPMNEW    NEW NUMERICAL LIST ?       -EU-
         BNE   RLSRCH              NO... MUST STILL BE IN ALPHA    -EU-
         OI    SWPROC,SWNEW        YES... NEW AMBLIST FORMAT       -EU-
         SPACE 1                                                   -EU-
RLFNUM  GET    SYSLPA,PI           READ PRINT IMAGE                -EU-
         CLI   CC,C' '             DETAIL LINE ?                   -EU-
         BNE   RLFNUM              NO... IGNORE                    -EU-
         LA    R1,S1               POINT TO FIRST SEGMENT          -EU-
         BAS   R14,RLANAL          ANALYZE IT                      -EU-
         LA    R1,S2               POINT TO SECOND SEGMENT         -EU-
         BAS   R14,RLANAL          ANALYZE IT                      -EU-
         B     RLFNUM              GET NEXT RECORD                 -EU-
         SPACE 1                                                   -EU-
         USING LPAE,R1             SEGMENT DSECT                   -EU-
RLANAL   CLC   LL+1(4),=CL4' '     BLANK LENGTH (NOT MODULE) ?     -EU-
         BER   R14                 YES... IGNORE                   -EU-
         CLI   LN,C'A'             AMBLIST INVALID NAME ?    -ERROR-EU-
         BLR   R14                 YES... IGNORE             -ERROR-EU-
         TM    SWPROC,SWNEW        NEW AMBLIST FORMAT ?            -EU-
         BO    RLANAL1             YES                             -EU-
         TR    LX,TOBIN            CONVERT START AND LENGTH TO BIN -EU-
         PACK  TS(L'TS+1),LS(L'LS+1)    SAVE START ADDRESS         -EU-
         PACK  TL(L'TL+1),LL(L'LL+1)    LENGTH                     -EU-
         B     RLANAL2                                             -EU-
RLANAL1  CLI   LXN,C' '            TWO BLANKS AFTER NAME ?         -EU-
         BNE   *+L'*+4             NO, OK                          -EU-
         LA    R1,1(R1)            YES, ADJUST LINE POINTER        -EU-
         TR    LXN,TOBIN           CONVERT START AND LENGTH TO BIN -EU-
         PACK  TS(L'TS+1),LSN(L'LSN+1)  SAVE START ADDRESS         -EU-
         PACK  TL(L'TL+1),LLN(L'LLN+1)  LENGTH                     -EU-
         DROP  R1                                                  -EU-
RLANAL2  NOP   RLANAL3             ONE TIME SWITCH                 -EU-
         OI    RLANAL2+1,SETBR     CHANGE TO BRANCH                -EU-
         MVC   LPALOW,TS           SAVE LPA LOW ADDR               -EU-
         CLC   LPALOW,LPAUP        DOWN 16M LINE ?                 -EU-
         BL    RLANAL4             YES                             -EU-
         OI    RLANAL3+1,SETBR     NO... ONLY UP 16M LINE          -EU-
RLANAL3  NOP   RLANAL4             16M LINE SWITCH                 -EU-
         CLC   TS,LPAUP            DOWN 16M LINE ?                 -EU-
         BL    RLANAL4             YES                             -EU-
         OI    RLANAL3+1,SETBR     CHANGE TO BRANCH                -EU-
         MVC   LPALOWX,TS          SAVE X-LPA LOW ADDR             -EU-
         MVC   LPAHI,TE            SAVE LPA HIGH ADDR              -EU-
RLANAL4  L     R0,TS               COMPUTE MODULE END ADDRESS      -EU-
         AL    R0,TL                                               -EU-
         ST    R0,TE                                               -EU-
         BR    R14                 RETURN                          -EU-
         SPACE 1                                                   -EU-
RLEND    OC    LPAHI,LPAHI         ALREADY STORED ?                -EU-
         BZ    RLONLY              NO... ONLY ONE LPA AREA         -EU-
         MVC   LPAHIX,TE           SAVE X-LPA HIGH ADDR            -EU-
         OI    SWPROC,SW16M        INDICATE UP 16M ALSO            -EU-
         B     RLCLOSE                                             -EU-
RLONLY   MVC   LPAHI,TE            SAVE LPA HIGH ADDR              -EU-
RLCLOSE CLOSE  SYSLPA              DON'T NEED THIS ANYMORE         -EU-
         CLC   VALUE,=CL4'NULL'    NO SAMPLER REQUESTED ?          -EU-
         BE    RETURN              YES, END                        -EU-
         EJECT ,                                                   -EU-
*        TEST TRACE STATUS AGAIN IF NEEDED                         -EU-
*        ---------------------------------                         -EU-
         SPACE 1                                                   -EU-
         TM    SWPROC,SWTST        COMMAND HAS BEEN SENDED?        -EU-
         BZ    OKTRC               NO                              -EU-
         L     R15,=A(XTTR)        GO TEST STATUS                  -EU-
         BASR  R9,R15                                              -EU-
         B     OKTRC                                               -EU-
         NOP   0                                                   -EU-
         NI    SWPROC,255-SWTST    COMMAND HAS NOT RUN             -EU-
         B     ERR9                                                -EU-
         SPACE 1
*        PREPARE TRACE TABLE COPY                                  -EU-
*        ------------------------                                  -EU-
         SPACE 1
OKTRC   ZEROKEY ,                                                  -EU-
        ESAR   R2                  GET SECONDARY ASID              -EU-
         ST    R2,SASID            KEEP IT                         -EU-
        AXRES  AXLIST=AXPL         RESERVE AX                      -EU-
         L     R2,TRASID           GET TRACE ASID                  -EU-
        AXEXT  ASID=(2)            EXTRACT AX OF TRACE IN R0       -EU-
        AXSET  AX=(0)              SET AX TO ALLOW TRACE AS 2ND    -EU-
        RESETKEY ,                                                 -EU-
         L     R5,TRSIZE           TRACE BUFFER LENGTH             -EU-
         ST    R5,MOVLONG+4        LENGTH                          -EU-
        GETMAIN R,LV=(5)           WORK AREA                       -EU-
         ST    R1,MOVLONG+8        WORK AREA ADDRESS               -EU-
         ST    R1,SCAN             SET SCAN REGISTERS VALUES       -EU-
         L     R14,=A(SNAP1)                                       -EU-
         ST    R1,0(R14)                                           -EU-
         AR    R1,R5                                               -EU-
         LR    R2,R1                                               -EU-
         SH    R1,=H'4'            (LESS EBCDIC 'TBUF')            -EU-
         ST    R1,SCAN+8                                           -EU-
         BCTR  R2,0                                                -EU-
         O     R2,=A(X'80000000')                                  -EU-
         ST    R2,4(R14)                                           -EU-
         EJECT ,                                                   -EU-
*        OPEN OUTPUT DATA-SET
*        --------------------
         SPACE 1                                                   -EU-
         LA    R2,OUTS                                             -EU-
         USING IHADCB,R2                                           -EU-
         LH    R0,BLKSZ                                            -EU-
         STH   R0,DCBLRECL         LRECL
         STH   R0,DCBBLKSI         BLKSIZE
         SLL   R0,1                                                -EU-
        GETMAIN R,LV=(0)           BUFFER AREA                     -EU-
         ST    R1,BUFSTR           MAIN BUFFER AREA                -EU-
         LR    R3,R1               SET STARTING BUFFER AREA        -EU-
         LH    R5,BLKSZ                                            -EU-
         AR    R1,R5                                               -EU-
         ST    R1,BUFSTRA          ALTERNATE BUFFER AREA           -EU-
         LR    R4,R3               CLEAR BUFFER                    -EU-
         LA    R6,*                                                -EU-
         XR    R7,R7                                               -EU-
         MVCL  R4,R6                                               -EU-
         LR    R8,R3               SET CURRENT BUFFER POINTER      -EU-
        OPEN   (OUTS,(OUTPUT))     OUTPUT FILE                     -EU-
         TM    DCBOFLGS,DCBOFOPN   OPEN SUCCESSFULL ?              -EU-
         BZ    ERR2                NO                              -EU-
         DROP  R2                                                  -EU-
         L     R2,BUFSTRA          BUFFER AREA END +1              -EU-
         EJECT
*        MESSAGE TO OPERATOR SO PROGRAM CAN BE ENDED
*        -------------------------------------------
         SPACE 1
         L     R1,ATIOT                                            -EU-
         USING TIODSECT,R1                                         -EU-
         MVC   EUMSP(L'EUMSP),TIOCNJOB  SET JOB-NAME               -EU-
         DROP  R1                                                  -EU-
        $SETOPC EUMS               ESTABLISH OPERATOR COMM.        -EU-
         LTR   R15,R15             SUCCESSFULL ?                   -EU-
         BNZ   ERR3                NO                              -EU-
         STM   R0,R1,MSGLL         SAVE LENGTH AND MSG-ID'S LIST   -EU-
         SPACE 1
*        DISPLAY TIME INTERVAL LIMIT TO RUN                        -EU-
*        ----------------------------------                        -EU-
         SPACE 1
STMEL   WTO    ' PSWSAMP - TIME INTERVAL LIMIT :       MIN.',      -EU-X
               ROUTCDE=(2),DESC=(2)                                -EU-
         ST    R1,MSGDEL                                           -EU-
         OI    MSGDEL,EL                                           -EU-
         SPACE 1
*        CONTROL CONSTANTS
*        -----------------
         SPACE 1
        TIME   BIN                 TIME CHECK
         ST    R0,STRTME           STARTING TIME OF DAY            -EU-
         STCM  R1,B'0111',DAY      DAY (00YYDDDF)                  -EU-
         AL    R0,MAXTIME          MAX IS TIME TO RUN
         ST    R0,TIME             SAVE FOR REFERENCE
         EJECT ,                                                   -EU-
*        MAIN PROCESSING - COPY SAMPLES                            -EU-
*        ------------------------------                            -EU-
         SPACE 1                                                   -EU-
ANAL    $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE               -EU-
         LA    R5,4                                                -EU-
         XR    R7,R7                                               -EU-
         USING PSA,R0                                              -EU-
         L     R4,PSATBVTV         GET CURRENT TBVT                -EU-
         ST    R4,CURTBVT          SAVE IT FOR CONTROL             -EU-
         DROP  R0                                                  -EU-
        ZEROKEY ,                                                  -EU-
         L     R1,TRASID           GET TRACE ASID                  -EU-
        SSAR   R1                  SET TRACE AS SECONDARY          -EU-
         USING TBVT,R4                                             -EU-
         MVCP  GADDR(R5),TBVTBWRD,R7    GET PREVIOUS TBVT          -EU-
         L     R4,GADDR                                            -EU-
         MVCP  GADDR(R5),TBVTBUFV,R7    GET PREVIOUS TBVT BUFFER   -EU-
         DROP  R4                                                  -EU-
         L     R4,GADDR                                            -EU-
         LM    R5,R6,MOVLONG+4     LENGTH + WORK AREA              -EU-
MOVEBUF  MVCP  0(R5,R6),0(R4),R7   MOVE DATA FROM TRACE BUFFER     -EU-
         BZ    ENDMOVE                                             -EU-
         AL    R4,=F'256'          BUMP 'FROM' ADDRESS             -EU-
         AL    R6,=F'256'          BUMP 'TO' ADDRESS               -EU-
         SL    R5,=F'256'          DECREMENT THE LENGTH            -EU-
         B     MOVEBUF             GO BACK AND GET MORE            -EU-
ENDMOVE  L     R1,SASID            RESTORE PREVIOUS SECONDARY      -EU-
        SSAR   R1                  SHOULD BE AS PRIMARY            -EU-
        RESETKEY ,                                                 -EU-
        $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
         L     R15,=A(SNAP)        TRACE IF NEEDED                 -EU-
         LA    R0,1                SNAP ID NUMBER - - - - - ID = 1 -EU-
         BASR  R9,R15                                              -EU-
         LM    R5,R7,SCAN          SCAN TRACE TABLE ENTRIES        -EU-
LOOP     L     R15,=A(XATT)        TTE'S ANALYSIS                  -EU-
         BASR  R9,R15                                              -EU-
         LTR   R1,R1               TTE OK ?                        -EU-
         BZ    NXTE                BRANCH IF NO                    -EU-
         CL    R1,LPALOW           IN LPA ?                        -EU-
         BL    TSUP                BRANCH IF NOT                   -EU-
         CL    R1,LPAHI            IN LPA ?                        -EU-
         BNH   PICK                BRANCH IF YES                   -EU-
TSUP     TM    SWPROC,SW16M        16M LINE ACTIVE ?               -EU-
         BZ    NXTE                BRANCH IF NOT                   -EU-
         CL    R1,LPALOWX          IN X-LPA ?                      -EU-
         BL    NXTE                BRANCH IF NOT                   -EU-
         CL    R1,LPAHIX           IN X-LPA ?                      -EU-
         BH    NXTE                BRANCH IF NOT                   -EU-
PICK     MVC   0(32,R8),NEWTTE     SET NEW XA-TTE FORMAT           -EU-
         LA    R8,32(,R8)          BUMP OUTPUT POINTER             -EU-
         CLR   R8,R2               BUFFER FILLED ?                 -EU-
         BL    NXTE                BRANCH IF NOT                   -EU-
         BAS   R14,CHCK            VERIFY PREVIOUS RECORD OUTPUTED -EU-
         BAS   R14,WRTE            EACH COPY IS SEPARATE RECORD    -EU-
         TM    SWITCH,ALTBUF       SELECT BUFFER                   -EU-
         BO    FLIP                                                -EU-
         L     R3,BUFSTRA          ALTERNATE BUFFER                -EU-
         B     FLOP                                                -EU-
FLIP     L     R3,BUFSTR           MAIN BUFFER                     -EU-
FLOP     XI    SWITCH,ALTBUF                                       -EU-
         LR    R8,R3               CLEAR BUFFER                    -EU-
         LH    R9,BLKSZ                                            -EU-
         LR    R2,R3                                               -EU-
         ALR   R2,R9                                               -EU-
         LA    R14,*                                               -EU-
         XR    R15,R15                                             -EU-
         MVCL  R8,R14                                              -EU-
         LR    R8,R3               RESET CURRENT BUFFER POINTER    -EU-
NXTE     BXLE  R5,R6,LOOP          NEXT ENTRY                      -EU-
         EJECT ,                                                   -EU-
*        MAIN PROCESSING - CONTROL                                 -EU-
*        -------------------------                                 -EU-
         SPACE 1                                                   -EU-
         LA    R1,L'MTXT                                           -EU-
         STH   R1,MTXTL                                            -EU-
        $OPCOM ,MTXTL              LOOK TO OPERATOR                -EU-
         BCT   R15,GOBACK          BR IF OPERATOR SAY SOMETHING    -EU-
CTRLT   TIME   BIN                 TIME CHECK
         ST    R0,ENDTME           ENDING TIME OF DAY IF STOP      -EU-
         CL    R0,TIME             HAS MAX TIME BEEN REACHED ?
         BH    GOHOME              YES, THEN STOP
CTNUE    XR    R14,R14                                             -EU-
         L     R15,WTIME                                           -EU-
         SLDL  R14,12                                              -EU-
         STM   R14,R15,HOWLONG                                     -EU-
WHEN    STIMER WAIT,MICVL=HOWLONG  WAIT A FEW SECONDS
         USING PSA,R0                                              -EU-
         CLC   PSATBVTV,CURTBVT    HAS CURRENT TBVT CHANGED ?      -EU-
         DROP  R0                                                  -EU-
         BNE   DYNAD               YES (1ST ENTRY TOD VALUE CHANGED)
         OI    SWITCH,SWWAIT       NO, WAIT SOME MORE
         XR    R14,R14                                             -EU-
         L     R15,WTLOW                                           -EU-
         SLDL  R14,12                                              -EU-
         STM   R14,R15,HOWLONG                                     -EU-
         B     WHEN                                                -EU-
         EJECT ,                                                   -EU-
*        MAIN PROCESSING - WAIT ADJUSTMENT                         -EU-
*        ---------------------------------                         -EU-
         SPACE 1                                                   -EU-
DYNAD    TM    SWITCH,SWWAIT       HAVE WE WAIT SOME MORE ?        -EU-
         BO    DYNAD3              YES                             -EU-
         CLC   WTIME,WTLOW         HAVE WE REACH THE LOWER LIMIT ? -EU-
         BNH   ANAL                YES, DON'T ADJUST ANY MORE      -EU-
         TM    SWITCH,WTON+WTOFF   ADJUSTMENT SHOULD BE DONE ?     -EU-
         BO    DYNAD1              NO                              -EU-
         L     R15,WTIME           TRY TO DECREMENT WAIT TIME      -EU-
         S     R15,WTADJ                                           -EU-
         ST    R15,WTIME                                           -EU-
         OI    SWITCH,WTOFF                                        -EU-
         NI    SWITCH,255-WTON                                     -EU-
         B     ANAL                                                -EU-
DYNAD1   CP    WTCTR,WTMAX                                         -EU-
         BNL   DYNAD2                                              -EU-
         AP    WTCTR,=P'1'         STILL INTERVAL                  -EU-
         B     ANAL                                                -EU-
DYNAD2   ZAP   WTCTR,=P'0'         STILL INTERVAL ENDED            -EU-
         NI    SWITCH,255-WTON-WTOFF                               -EU-
         L     R15,WTIME           COMPUTE MEDIUM WAIT TIME        -EU-
SWMF     NOP   *+L'*+8                                             -EU-
         OI    SWMF+1,X'F0'        CLOSE THIS WAY                  -EU-
         B     *+L'*+8                                             -EU-
         A     R15,MDMTME                                          -EU-
         SRL   R15,1                                               -EU-
         ST    R15,MDMTME                                          -EU-
         B     ANAL                                                -EU-
DYNAD3   CLC   WTIME,WTHIGH        HAVE WE REACH THE HIGH LIMIT ?  -EU-
         BNL   DYNAD6              YES, DON'T ADJUST ANY MORE      -EU-
         TM    SWITCH,WTOFF                                        -EU-
         BZ    DYNAD4                                              -EU-
         TM    SWITCH,WTON                                         -EU-
         BO    DYNAD4                                              -EU-
         ZAP   WTCTR,=P'0'         RESET STILL INTERVAL            -EU-
         B     DYNAD5                                              -EU-
DYNAD4   NI    SWITCH,255-WTOFF                                    -EU-
DYNAD5   OI    SWITCH,WTON                                         -EU-
         L     R15,WTIME           TRY TO INCREMENT WAIT TIME      -EU-
         A     R15,WTADJ                                           -EU-
         ST    R15,WTIME                                           -EU-
DYNAD6   NI    SWITCH,255-SWWAIT                                   -EU-
         B     ANAL                                                -EU-
         EJECT ,                                                   -EU-
*        CHANGE TIME LIMIT - MODIFY COMMAND                        -EU-
*        ----------------------------------                        -EU-
*        (NEW TIME INTERVAL LIMIT EXPRESSED IN MINUTES)            -EU-
         SPACE 1                                                   -EU-
MODIFY   BCT   R15,ERR5            BR IF NOT OPERATOR MODIFY       -EU-
         LH    R1,MTXTL            NEW TIME LIMIT SPECIFICATION    -EU-
         LTR   R1,R1                                               -EU-
         BNP   CTRLT               NO VALUE SPECIFIED              -EU-
         CH    R1,PRMLG                                            -EU-
         BH    CTRLT               INVALID, IGNORE IT              -EU-
         MVC   VALUE,LOWV                                          -EU-
         LA    R14,VALUE+L'VALUE                                   -EU-
         SLR   R14,R1                                              -EU-
         BCT   R1,*+L'*+6                                          -EU-
         MVC   0(*-*,R14),MTXT     GET NEW VALUE                   -EU-
         EX    R1,*-6                                              -EU-
         CLC   VALUE,LOWV                                          -EU-
         BL    CTRLT               INVALID, IGNORE IT              -EU-
         CLC   VALUE,HIGHV                                         -EU-
         BH    CTRLT               INVALID, IGNORE IT              -EU-
         PACK  WORKD,VALUE                                         -EU-
         CVB   R15,WORKD                                           -EU-
         M     R14,=F'6000'                                        -EU-
         ST    R15,MAXTIME         NEW TIME INTERVAL LIMIT         -EU-
        TIME   BIN                                                 -EU-
         AL    R0,MAXTIME                                          -EU-
         ST    R0,TIME             NEW MAX IS TIME TO RUN          -EU-
         MVC   EXPMS+18(L'EDMSK),EDMSK                             -EU-
         ED    EXPMS+18(L'EDMSK),WORKD+L'WORKD-(L'EDMSK/2)         -EU-
         LA    R1,MSGDEL                                           -EU-
        DOM    MSGLIST=(1)         DELETE PREVIOUS MSG LIMIT       -EU-
EXPMS   WTO    ' PSWSAMP -       MIN. NEW TIME INT. LIMIT SET.',   -EU-X
               ROUTCDE=(2),DESC=(2)                                -EU-
         ST    R1,MSGDEL                                           -EU-
         OI    MSGDEL,EL                                           -EU-
         B     CTNUE                                               -EU-
         EJECT ,                                                   -EU-
*        PROGRAM TERMINATION                                       -EU-
*        -------------------                                       -EU-
         SPACE 1                                                   -EU-
GOBACK   BCT   R15,MODIFY          BR IF NOT OPERATOR STOP         -EU-
         XC    MSGLA,MSGLA         MSG-ID'S LIST IS DELETED        -EU-
GOHOME   BAS   R14,CHCK                                            -EU-
         MVC   0(4,R8),=F'-1'      PSW SAMPLER INFO                -EU-
         MVC   4(PSIL,R8),PSI                                      -EU-
         MVC   4+PSIL(4,R8),WTIME                                  -EU-
         MVC   8+PSIL(4,R8),COUNTER                                -EU-
         BAS   R14,WRTE            LAST RECORD TO OUTPUT FILE      -EU-
         BAS   R14,CHCK                                            -EU-
CLOUTS  CLOSE  (OUTS)              CLOSE THE OUTPUT FILE           -EU-
FREE     LM    R5,R6,MOVLONG+4                                     -EU-
        FREEMAIN R,A=(R6),LV=(R5)  FREE WORK AREA                  -EU-
        ZEROKEY ,                                                  -EU-
        AXFRE  AXLIST=AXPL         FREE AX                         -EU-
        RESETKEY ,                                                 -EU-
         LH    R0,BLKSZ                                            -EU-
         SLL   R0,1                                                -EU-
         L     R1,BUFSTR                                           -EU-
        FREEMAIN R,A=(1),LV=(0)    FREE BUFFER AREA                -EU-
EXIT     OC    MSGDEL,MSGDEL                                       -EU-
         BZ    NODEL                                               -EU-
         LA    R1,MSGDEL                                           -EU-
        DOM    MSGLIST=(1)         DELETE MSG LIMIT                -EU-
NODEL    L     R1,MSGLA                                            -EU-
         LTR   R1,R1                                               -EU-
         BZ    LEAVE                                               -EU-
        DOM    MSGLIST=(1)         DELETE INITIAL MSG-ID'S LIST    -EU-
         LM    R0,R1,MSGLL                                         -EU-
        FREEMAIN R,LV=(0),A=(1)    FREE AREA                       -EU-
         EJECT ,                                                   -EU-
*        EVERY THING DONE (QUIT)                                   -EU-
*        -----------------------                                   -EU-
         SPACE 1                                                   -EU-
LEAVE   WTO    ' --- PSW SAMPLER ACTIVITY STOPPED ---',            -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
RETURN   L     R15,=A(SNAPCL)                                      -EU-
         BASR  R9,R15                                              -EU-
         TM    SWPROC,SWTST        COMMAND HAS BEEN SENDED?        -EU-
         BZ    QUIT                NO                              -EU-
        $SSCMD STPCMD              STOP TRACE                      -EU-
         LTR   R15,R15                                             -EU-
         BZ    QUIT                                                -EU-
         STC   R15,PRC+L'PRC+29                                    -EU-
         SRL   R15,4                                               -EU-
         STC   R15,PRC+L'PRC+28                                    -EU-
         NC    PRC+L'PRC+28(3),MASK                                -EU-
         TR    PRC+L'PRC+28(3),TRTAB                               -EU-
PRC     WTO    ' PSWSAMP - SSCMD R.C. =     (TRACE ST,OFF).',      -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
QUIT    $XRET  CC=(R10)                                            -EU-
         EJECT ,                                                   -EU-
*        ABNORMAL TERMINATION                                      -EU-
*        --------------------                                      -EU-
         SPACE 1                                                   -EU-
        PRINT  NOGEN                                               -EU-
ERR0    $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
        WTO    ' PSWSAMP - TRACE SERVICES UNAVAILABLE.',           -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
ERR0X    LA    R10,4               C.C. = 4 ********************** -EU-
         B     EXIT                                                -EU-
ERR1    $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
        WTO    ' PSWSAMP - NO TRACE TABLE.',                       -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
ERR1X    LA    R10,8               C.C. = 8 ********************** -EU-
         B     EXIT                                                -EU-
ERR2    WTO    ' PSWSAMP - OPEN OUTPUT FAILED.',                   -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,12              C.C. = 12 ********************* -EU-
         B     FREE                                                -EU-
ERR3    WTO    ' PSWSAMP - OP.COM. NOT ESTABLISHED.',              -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,16              C.C. = 16 ********************* -EU-
         B     CLOUTS                                              -EU-
ERR4    WTO    ' PSWSAMP - OUTPUT I/O ERROR.',                     -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,20              C.C. = 20 ********************* -EU-
         B     CLOUTS                                              -EU-
ERR5    WTO    ' PSWSAMP - OP.COM. FAILED.',                       -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         LA    R10,24              C.C. = 24 ********************* -EU-
         B     GOHOME                                              -EU-
ERR6    $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
        WTO    ' PSWSAMP - NO TRACE OPTION BLOCK.',                -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         B     ERR1X                                               -EU-
ERR7    $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
        WTO    ' PSWSAMP - TRACE PENDING ACTION.',                 -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         B     ERR0X                                               -EU-
ERR8    $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
        WTO    ' PSWSAMP - TRACE MUST BE : AS=ON BR=OFF EX=ON.',   -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         B     ERR0X                                               -EU-
ERR9    WTO    ' PSWSAMP - UNABLE TO ACTIVATE THE TRACE.',         -EU-X
               ROUTCDE=(2,11),DESC=(4)                             -EU-
         B     ERR0X                                               -EU-
        PRINT  GEN                                                 -EU-
         EJECT
*        I/O ROUTINES (R14 - LINK REGISTER)                        -EU-
*        ------------                                              -EU-
         SPACE 1                                                   -EU-
WRTE     ST    R14,SVRET                                           -EU-
        WRITE  OUTSD,SF,,(R3),,MF=E                                -EU-
         OI    SWITCH,WAITIO       SET I/O STARTED                 -EU-
         L     R14,SVRET                                           -EU-
         BR    R14                 RETURN                          -EU-
         SPACE 1                                                   -EU-
CHCK     TM    SWITCH,WAITIO       PREVIOUS I/O ACTIVE ?           -EU-
         BZR   R14                 NO, RETURN                      -EU-
         ST    R14,SVRET                                           -EU-
         NI    SWITCH,255-WAITIO                                   -EU-
        CHECK  OUTSD                                               -EU-
         TM    SWITCH,ERRIO        HOW COMPLETED ?                 -EU-
         BO    ERR4                WITH ERROR                      -EU-
         L     R14,SVRET                                           -EU-
         BR    R14                 RETURN                          -EU-
         EJECT ,                                                   -EU-
*        SYNAD ROUTINE                                             -EU-
*        -------------                                             -EU-
         SPACE 1                                                   -EU-
         USING *,R15                                               -EU-
OUTERR   ST    R10,SVREG                                           -EU-
         LR    R10,R15                                             -EU-
         DROP  R15                                                 -EU-
         USING OUTERR,R10                                          -EU-
        SYNADAF ACSMETH=BSAM                                       -EU-
         CLI   69(R1),C'*'         UNSIGNIFICANT MESSAGE ?         -EU-
         BE    SYNAD4              YES                             -EU-
         CLI   8(R1),C' '          BINARY INFORMATIONS ?           -EU-
         BE    SYNAD2              NO                              -EU-
         UNPK  56(7,R1),9(4,R1)                                    -EU-
         NC    56(6,R1),MASK                                       -EU-
         TR    56(6,R1),TRTAB                                      -EU-
         MVI   62(R1),C','                                         -EU-
         UNPK  63(5,R1),12(3,R1)                                   -EU-
         NC    63(4,R1),MASK                                       -EU-
         TR    63(4,R1),TRTAB                                      -EU-
         MVI   67(R1),C','                                         -EU-
         B     SYNAD3                                              -EU-
SYNAD2   MVC   56(12,R1),8(R1)     MOVE BLANKS                     -EU-
SYNAD3   LA    R1,52(R1)           SET PARM ADDRESS FOR WTO        -EU-
         MVC   0(4,R1),LWTO                                        -EU-
         MVC   76(4,R1),RWTO                                       -EU-
        WTO    MF=(E,(1))                                          -EU-
SYNAD4  SYNADRLS ,                                                 -EU-
         OI    SWITCH,ERRIO        SET ERROR OCCURED               -EU-
         L     R10,SVREG                                           -EU-
         BR    R14                 RETURN                          -EU-
         DROP  R10                                                 -EU-
         EJECT
*        CONSTANTS AND WORK-AREAS
*        ------------------------
         SPACE 1
WORKD    DC    D'0'                                                -EU-
MOVLONG  DC    4F'0'                                               -EU-
TE       DC    F'0'                                                -EU-
TS       DC    F'0'                                                -EU-
TL       DC    F'0'                                                -EU-
SVRET    DC    F'0'                                                -EU-
SVREG    DC    F'0'                                                -EU-
MSGDEL   DC    F'0'                                                -EU-
MSGLL    DC    F'0'                LENGTH OF AREA AND              -EU-
MSGLA    DC    F'0'                  MSG-ID'S LIST                 -EU-
TIME     DC    F'0'
PSI      DS    0F  - - - - - - - - PSW SAMPLER INFO                -EU-
         DC    AL1(TMVSXA)           ...                           -EU-
DAY      DC    XL3'0'                ...                           -EU-
SID      DC    CL4' '                ...                           -EU-
STRTME   DC    F'0'                  ...                           -EU-
ENDTME   DC    F'0'                  ...                           -EU-
MDMTME   DC    F'0'                  ...                           -EU-
PSIL     EQU   *-PSI   - - - - - - - ---                           -EU-
SCAN     DC    A(*-*,32,*-*)                                       -EU-
LPALOW   DC    A(*-*)                                              -EU-
LPAHI    DC    A(*-*)                                              -EU-
LPALOWX  DC    A(*-*)                                              -EU-
LPAHIX   DC    A(*-*)                                              -EU-
LPAUP    DC    A(16*1024*1024)     16M LINE                        -EU-
ATIOT    DC    A(*-*)                                              -EU-
ATSO     DC    A(*-*)                                              -EU-
BUFSTR   DC    A(*-*)                                              -EU-
BUFSTRA  DC    A(*-*)                                              -EU-
MAXTIME  DC    A(15*60*100)        IN 0.01 SEC.                    -EU-
TOBIN    DC    256X'0'                                             -EU-
         ORG   TOBIN+C'A'               POSITION TO 'A'            -EU-
         DC    X'0A0B0C0D0E0F'          TRANSLATE A-F              -EU-
         ORG   TOBIN+C'0'               POSITION TO '0'            -EU-
         DC    X'00010203040506070809'  TRANSLATE TO 0-9           -EU-
         ORG   ,                                                   -EU-
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUTH)           -EU-
BLKSZ    DC    0H'0',AL2(NTTE*32)                                  -EU-
PRMLG    DC    0H'0',AL2(L'VALUE)                                  -EU-
LWTO     DC    AL2(76),XL2'8000'   WTO - TEXT LENGTH, MCS FLAGS    -EU-
RWTO     DC    XL2'1000',XL2'4020' WTO - DESC=4, ROUTCDE=(2,11)    -EU-
MTXTL    DC    0H'0',AL2(L'MTXT)   OPERATOR                        -EU-
MTXT     DC    CL40' '               COMMUNICATION                 -EU-
NTTESTT  DC    H'0'                                                -EU-
AUTH     DC    XL1'0'                                              -EU-
SWITCH   DC    XL1'0'                                              -EU-
ALTBUF   EQU   X'01'          SWITCH - I/O ALTERNATE BUFFER USE    -EU-
WAITIO   EQU   X'02'          SWITCH - CHECK I/O MUST BE ISSUED    -EU-
ERRIO    EQU   X'04'          SWITCH - OUTPUT I/O ERROR OCCURED    -EU-
SWWAIT   EQU   X'10'          SWITCH - WAITING SOME MORE           -EU-
WTOFF    EQU   X'20'          SWITCH - NO WAIT OCCURS              -EU-
WTON     EQU   X'40'          SWITCH - WAIT OCCURS                 -EU-
OFFBYTES DC    X'02FB'             TRACE OFF                       -EU-
VALUE    DC    C'0000'                                             -EU-
LOWV     DC    C'0001'             1 MINUTE                        -EU-
HIGHV    DC    C'1200'             20 HOURS                        -EU-
TRTAB    DC    C'0123456789ABCDEF'                                 -EU-
EDMSK    DC    X'402020202120'                                     -EU-
MASK     DC    X'0F0F0F0F0F0F'                                     -EU-
SWPROC   DC    XL1'0'                                              -EU-
SWNEW    EQU   X'01'          SWPROC - NEW FORMAT AMBLIST (LISTLPA)-EU-
SW16M    EQU   X'02'          SWPROC - PROCESS UP 16M LINE ALSO    -EU-
SWTST    EQU   X'10'          SWPROC - TRACE ST COMMAND SENDED     -EU-
NOTAUT   DC    C' PSWSAMP - NOT AUTHORIZED IN TSO.'                -EU-
LPMNUM   DC    C'LINK PACK MAP - NUMERICALLY BY'                   -EU-
LPMNEW   DC    C'PAGEABLE LINK PACK AREA MAP -  NUMERICALLY BY'    -EU-
         SPACE 1                                                   -EU-
         DC    AL1(L'STRCMD)                                       -EU-
STRCMD   DC    C'TRACE ST'                                         -EU-
         DC    AL1(L'STPCMD)                                       -EU-
STPCMD   DC    C'TRACE ST,OFF'                                     -EU-
         SPACE 1                                                   -EU-
EUMS     DC    AL1(L'EUMSM)        OP.COMM. PARM. LIST - - - -> S  -EU-
EUMSM    DC    C' --- PSW SAMPLER ACTIVITY STARTED ---'         E  -EU-
         DC    AL1(L'EUMSS+L'EUMSP)                             T  -EU-
EUMSS    DC    C' PSWSAMP - TO INTERRUPT USE COMMAND : P '      O  -EU-
EUMSP    DC    CL8' '                                           P  -EU-
         DC    AL1(0)              END OF PARM. LIST            C  -EU-
         SPACE 1                                                   -EU-
        CNOP   2,4                                                 -EU-
OPTLIST  DC    H'0',H'0'                                           -EU-
DDNMELST DC    AL2(DDNMELSL)                                       -EU-
         DC    4XL8'0'                                             -EU-
         DC    CL8'SYSDIN'                                         -EU-
         DC    CL8'SYSDOU'                                         -EU-
         DC    XL8'0'                                              -EU-
DDNMELSL EQU   (*-DDNMELST)-L'DDNMELST                             -EU-
         SPACE 1                                                   -EU-
TRSIZE   DC    A(4*1024)                                           -EU-
TRASID   DC    F'0'                                                -EU-
SASID    DC    F'0'                                                -EU-
CURTBVT  DC    F'0'                                                -EU-
GADDR    DC    F'0'                                                -EU-
AXPL     DC    H'1',H'0'           AX PARM LIST                    -EU-
NEWTTE   DS    0XL32               NEW XA-TTE FORMAT               -EU-
         DC    H'0',X'70',X'0'       ...                           -EU-
NEWPSWA  DC    A(*-*)                ...                           -EU-
NEWDATA  DC    6F'0' - - - - - - - - ---                           -EU-
COUNTER  DC    PL4'+0'             UNKNOWN EX TTE'S                -EU-
         EJECT
*        WAIT TIME MECCHANISM VALUES (MICSEC)                      -EU-
*        ------------------------------------                      -EU-
         SPACE 1                                                   -EU-
HOWLONG  DC    D'0'                BIT 51 = 1 MICSEC.              -EU-
WTIME    DC    A(24*100000)         2.4 SEC                        -EU-
WTLOW    DC    A(10*100000)         1.0 SEC                        -EU-
WTHIGH   DC    A(100*100000)       10.0 SEC                        -EU-
WTADJ    DC    A(2*100000)          0.2 SEC                        -EU-
WTCTR    DC    PL2'0'                                              -EU-
WTMAX    DC    PL2'20'             STILL INTERVAL                  -EU-
         SPACE 1                                                   -EU-
PI       DS    0CL121              PRINT IMAGE FOR SYSLPA HERE     -EU-
CC       DC    CL1' '              PRINTER CARRIAGE CONTROL CHAR.  -EU-
S1       DC    CL60' '             SEGMENT ONE                     -EU-
S2       DC    CL60' '             SEGMENT TWO                     -EU-
         SPACE 2                                                   -EU-
        LTORG  ,                                                   -EU-
         SPACE 2
         PRINT NOGEN                                               -EU-
SYSLPA  DCB    DSORG=PS,MACRF=GM,EODAD=RLEND,DDNAME=SYSPRINT       -EU-
OUTS    DCB    DSORG=PS,DEVD=DA,MACRF=W,RECFM=F,                   -EU-X
               SYNAD=OUTERR,DDNAME=SYSPSW                          -EU-
         SPACE 1                                                   -EU-
         PRINT GEN                                                 -EU-
        WRITE  OUTSD,SF,OUTS,,'S',MF=L                             -EU-
         EJECT ,                                                   -EU
*        TEST TRACE ON AND GET TABLE POINTER
*        -----------------------------------
         SPACE 1                                                   -EU-
* XTTR ENTRY : TEST TRACE STATUS.                                  -EU-
*              R9  = LINK REGISTER.                                -EU-
*              R10 = LOCAL BASE REGISTER.                          -EU-
*              R11 = PSWSAMP FIRST BASE REGISTER.                  -EU-
*              R12 = PSWSAMP SECOND BASE REGISTER.                 -EU-
*              R15 = ADDRESS OF XTTR ROUTINE AT ENTRY.             -EU-
* XTTR EXIT  : R15 = RETURN CODE :                                 -EU-
*                    0 = TRACE STATUS OK.                          -EU-
*                    4 = TRACE INACTIVE BUT SOMETHING PENDING.     -EU-
*                    8 = TRACE INACTIVE AND NOTHING PENDING.       -EU-
         SPACE 1                                                   -EU-
         CNOP  0,4                                                 -EU-
         USING *,R10                                               -EU-
XTTR     STM   R14,R12,12(R13)     ENTRY                           -EU-
         LR    R10,R15             SET LOCAL BASE                  -EU-
        $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE               -EU-
         USING PSA,R0                                              -EU-
         L     R2,PSATRVT          TRACE VECTOR TABLE POINTER      -EU-
         DROP  R0                                                  -EU-
         USING TRVT,R2                                             -EU-
         CLC   TRVTID,=CL4'TRVT'   IDENTIFIER ?                    -EU-
         BNE   ERR1                NO                              -EU-
         L     R1,TRVTTOB          TRACE OPTION BLOCK ADDRESS      -EU-
         DROP  R2                                                  -EU-
         USING TOB,R1                                              -EU-
         CLC   TOBID,=CL4'TOB '    IDENTIFIER ?                    -EU-
         BNE   ERR6                NO                              -EU-
         TM    TOBTRFG1,TOBSVACT   TRACE SERVICES AVAILABLE ?      -EU-
         BZ    ERR0                NO                              -EU-
         TM    TOBTRFG1,TOBSTACT   TRACE ACTIVE ?                  -EU-
         BO    XTTR2               YES                             -EU-
         CLC   TOBTRFG1(3),=AL1(TOBSVACT,0,0) SOMETHING PENDING ?  -EU-
         BE    XTTR1               NO                              -EU-
         LA    R15,4               YES, RC = 4                     -EU-
         B     XTTR3               RETURN                          -EU-
XTTR1    LA    R15,8               RC = 8                          -EU-
         B     XTTR3               RETURN                          -EU-
XTTR2    TM    TOBTRFG2,TOBPNDEA+TOBPNDOF    PENDING ACTION ?      -EU-
         BNZ   ERR7                YES                             -EU-
         TM    TOBTRFG3,TOBTAST    PENDING ACTION ?                -EU-
         BNZ   ERR7                YES                             -EU-
         TM    TOBTROPT,TOBTRBR    BR TRACE OPTION ?               -EU-
         BO    ERR8                YES                             -EU-
         TM    TOBTROB4,TOBTRASD+TOBTREXP    AS+EX TRACE OPTIONS ? -EU-
         BNO   ERR8                NO                              -EU-
         MVC   TRASID+2(2),TOBASID GET TRACE A.S. ID               -EU-
         DROP  R1                                                  -EU-
         XR    R15,R15             RC = 0                          -EU-
XTTR3   $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
         L     R14,12(R13)         EXIT                            -EU-
         LM    R0,R12,20(R13)                                      -EU-
         B     0(R9,R15)           RETURN                          -EU-
         DROP  R10                                                 -EU-
         SPACE 2                                                   -EU-
        LTORG  ,                                                   -EU-
         EJECT ,                                                   -EU-
*        MVS/XA TTE'S ANALYSIS                                     -EU-
*        ---------------------                                     -EU-
         SPACE 1                                                   -EU-
* XATT ENTRY : TRACE BUFFER SCANNING.                              -EU-
*              R5  = CURRENT TTE POINTER.                          -EU-
*              R7  = END OF TRACE BUFFER POINTER.                  -EU-
*              R9  = LINK REGISTER.                                -EU-
*              R10 = LOCAL BASE REGISTER.                          -EU-
*              R11 = PSWSAMP FIRST BASE REGISTER.                  -EU-
*              R12 = PSWSAMP SECOND BASE REGISTER.                 -EU-
*              R15 = ADDRESS OF XATT ROUTINE AT ENTRY.             -EU-
* AT EXIT :    R1  = FAULT ADDRESS FOR CONTROL OR ZERO (END).      -EU-
*              R6  = TTE LENGTH.                                   -EU-
*       NOTE - THE CURRENT TTE ENTRY, IF ACCEPTED, HAS BEEN        -EU-
*              BUILDED TO LOOK LIKE AN MVS/370 TTE (LENGTH=32)     -EU-
*              AND PLACED IN FIELD NAMED 'NEWTTE'.                 -EU-
         SPACE 1                                                   -EU-
         CNOP  0,4                                                 -EU-
         USING *,R10                                               -EU-
XATT     STM   R14,R12,12(R13)     ENTRY                           -EU-
         LR    R10,R15             SET LOCAL BASE                  -EU-
         XC    NEWDATA(6*4),NEWDATA                                -EU-
         USING TTE,R5                                              -EU-
         CLI   TTEPCTYP,TTETPC     PC TTE ?                        -EU-
         BE    XAPCPT              YES                             -EU-
         CLI   TTEPTTYP,TTETPT     PT TTE ?                        -EU-
         BE    XAPCPT              YES                             -EU-
         CLI   TTESSTYP,TTETSSAR   SSAR TTE ?                      -EU-
         BE    XASSAR              YES                             -EU-
         XR    R6,R6                                               -EU-
         IC    R6,TTETYPE          GET TTE TYPE                    -EU-
         LR    R2,R6                                               -EU-
         N     R6,=A(TTEMEX)                                       -EU-
         CL    R6,=A(TTETEX)       EXPLICIT TTE ?                  -EU-
         BE    XAEX                YES                             -EU-
         L     R6,=F'4096'         SET R6 TO FORCE END             -EU-
         B     XARESET                                             -EU-
XAPCPT   MVC   NEWDATA(8),TTEPC OR TTEPT                           -EU-
         LA    R6,L'TTEPC OR L' TTEPT                              -EU-
         MVC   NEWPSWA,TTEPCADR OR TTEPTADR                        -EU-
         TM    NEWPSWA,TTEPCAMD OR TTEPTAMD                        -EU-
         BO    *+L'*+8                                             -EU-
         MVI   NEWPSWA,0                                           -EU-
         B     *+L'*+4                                             -EU-
         NI    NEWPSWA,255-TTEPCAMD OR TTEPTAMD                    -EU-
         B     XALOAD                                              -EU-
XASSAR   LA    R6,L'TTESSAR        BYPASS IT                       -EU-
         B     XARESET                                             -EU-
XAEX     N     R2,=A(TTEREGS)                                      -EU-
         LA    R2,1(,R2)                                           -EU-
         SLL   R2,2                                                -EU-
         LA    R6,L'TTEXP(R2)                                      -EU-
         MVC   NEWDATA(L'TTEXP),TTEXP                              -EU-
         LM    R1,R3,XAEXSCN                                       -EU-
         CLC   0(L'TTEXPTYP,R1),TTEXPTYP                           -EU-
         BE    2(R1)                                               -EU-
         BXLE  R1,R2,*-10                                          -EU-
         AP    COUNTER,=PL1'+1'    UNKNOWN EX TTE                  -EU-
XARESET  XR    R1,R1                                               -EU-
         B     XAEXIT                                              -EU-
XAXEXT   MVC   NEWDATA+L'TTEXP(L'TTE003PW),TTE003PW                -EU-
         MVC   NEWPSWA,TTE003P2                                    -EU-
         B     XAXX                                                -EU-
XAXSVC   MVC   NEWDATA+L'TTEXP(L'TTE005PW),TTE005PW                -EU-
         MVC   NEWPSWA,TTE005P2                                    -EU-
         B     XAXX                                                -EU-
XAXPGM   MVC   NEWDATA+L'TTEXP(L'TTE007PW),TTE007PW                -EU-
         MVC   NEWPSWA,TTE007P2                                    -EU-
         B     XAXX                                                -EU-
XAXSPER  MVC   NEWDATA+L'TTEXP(L'TTE009PW),TTE009PW                -EU-
         MVC   NEWPSWA,TTE009P2                                    -EU-
         B     XAXX                                                -EU-
XAXIO    MVC   NEWDATA+L'TTEXP(L'TTE00BPW),TTE00BPW                -EU-
         MVC   NEWPSWA,TTE00BP2                                    -EU-
         B     XAXX                                                -EU-
XAXDSP   MVC   NEWDATA+L'TTEXP(L'TTE00FPW),TTE00FPW                -EU-
         MVC   NEWPSWA,TTE00FP2                                    -EU-
         B     XAXX                                                -EU-
XAXMCH   MVC   NEWDATA+L'TTEXP(L'TTE013PW),TTE013PW                -EU-
         MVC   NEWPSWA,TTE013P2                                    -EU-
         B     XAXX                                                -EU-
XAXRST   MVC   NEWDATA+L'TTEXP(L'TTE015PW),TTE015PW                -EU-
         MVC   NEWPSWA,TTE015P2                                    -EU-
         B     XAXX                                                -EU-
XAXSUSP  MVC   NEWDATA+L'TTEXP(L'TTE019RT),TTE019RT                -EU-
         MVC   NEWDATA+L'TTEXP+L'TTE019RT(L'TTE019SI),TTE019SI     -EU-
         MVC   NEWPSWA,TTE019RT                                    -EU-
         B     XAXX                                                -EU-
XAXUSR0  MVC   NEWDATA+L'TTEXP(L'TTE07FAD),TTE07FAD                -EU-
         MVC   NEWDATA+L'TTEXP+L'TTE07FAD(L'TTE07FCI),TTE07FCI     -EU-
         MVC   NEWPSWA,TTE07FAD                                    -EU-
         B     XAXX                                                -EU-
XAXEMS   MVC   NEWDATA+L'TTEXP(L'TTE103PW),TTE103PW                -EU-
         MVC   NEWPSWA,TTE103P2                                    -EU-
         B     XAXX                                                -EU-
XAXSVCR  MVC   NEWDATA+L'TTEXP(L'TTE105PW),TTE105PW                -EU-
         MVC   NEWPSWA,TTE105P2                                    -EU-
         B     XAXX                                                -EU-
XAXSRB   MVC   NEWDATA+L'TTEXP(L'TTE10FPW),TTE10FPW                -EU-
         MVC   NEWPSWA,TTE10FP2                                    -EU-
         B     XAXX                                                -EU-
XAXSS    MVC   NEWDATA+L'TTEXP(L'TTE203PW),TTE203PW                -EU-
         MVC   NEWPSWA,TTE203P2                                    -EU-
         B     XAXX                                                -EU-
XAXSSRB  MVC   NEWDATA+L'TTEXP(L'TTE20FPW),TTE20FPW                -EU-
         MVC   NEWPSWA,TTE20FP2                                    -EU-
         B     XAXX                                                -EU-
XAXCALL  MVC   NEWDATA+L'TTEXP(L'TTE303PW),TTE303PW                -EU-
         MVC   NEWPSWA,TTE303P2                                    -EU-
         B     XAXX                                                -EU-
XAXCLKC  MVC   NEWDATA+L'TTEXP(L'TTE403PW),TTE403PW                -EU-
         MVC   NEWPSWA,TTE403P2                                    -EU-
         B     XAXX                                                -EU-
XAXSVCE  MVC   NEWDATA+L'TTEXP(L'TTEF05PW),TTEF05PW                -EU-
         MVC   NEWPSWA,TTEF05P2                                    -EU-
XAXX     TM    NEWPSWA,X'80'                                       -EU-
         BO    *+L'*+8                                             -EU-
         MVI   NEWPSWA,0                                           -EU-
         B     *+L'*+4                                             -EU-
         NI    NEWPSWA,X'7F'                                       -EU-
XALOAD   L     R1,NEWPSWA                                          -EU-
XAEXIT   ST    R1,20+R1*4(R13)     SET R1                          -EU-
         ST    R6,20+R6*4(R13)     SET R6                          -EU-
         LM    R14,R12,12(R13)     EXIT                            -EU-
         BR    R9                  RETURN                          -EU-
         DROP  R5,R10                                              -EU-
         SPACE 1                                                   -EU-
XAEXSCN  DC    A(XAEXTAB,L'XAEXTAB,XAEXEND)                        -EU-
         SPACE 1                                                   -EU-
XAEXTAB  DS    0XL6                EX TTE ID TABLE                 -EU-
         DC    AL2(TTETSSCH)       001 - SKIP                      -EU-
         B     XARESET                                             -EU-
         DC    AL2(TTETEXT)        003                             -EU-
         B     XAXEXT                                              -EU-
         DC    AL2(TTETSVC)        005                             -EU-
         B     XAXSVC                                              -EU-
         DC    AL2(TTETPGM)        007                             -EU-
         B     XAXPGM                                              -EU-
         DC    AL2(TTETSPER)       009                             -EU-
         B     XAXSPER                                             -EU-
         DC    AL2(TTETIO)         00B                             -EU-
         B     XAXIO                                               -EU-
         DC    AL2(TTETDSP)        00F                             -EU-
         B     XAXDSP                                              -EU-
         DC    AL2(TTETMCH)        013                             -EU-
         B     XAXMCH                                              -EU-
         DC    AL2(TTETRST)        015                             -EU-
         B     XAXRST                                              -EU-
         DC    AL2(TTETACR)        017 - SKIP                      -EU-
         B     XARESET                                             -EU-
         DC    AL2(TTETSUSP)       019                             -EU-
         B     XAXSUSP                                             -EU-
         DC    AL2(TTETALTR)       01B - SKIP                      -EU-
         B     XARESET                                             -EU-
         DC    AL2(TTETUSR0)       07F                             -EU-
         B     XAXUSR0                                             -EU-
         DC    AL2(TTETMSCH)       101 - SKIP                      -EU-
         B     XARESET                                             -EU-
         DC    AL2(TTETEMS)        103                             -EU-
         B     XAXEMS                                              -EU-
         DC    AL2(TTETSVCR)       105                             -EU-
         B     XAXSVCR                                             -EU-
         DC    AL2(TTETSRB)        10F                             -EU-
         B     XAXSRB                                              -EU-
         DC    AL2(TTETHSCH)       201 - SKIP                      -EU-
         B     XARESET                                             -EU-
         DC    AL2(TTETSS)         203                             -EU-
         B     XAXSS                                               -EU-
         DC    AL2(TTETSSRB)       20F                             -EU-
         B     XAXSSRB                                             -EU-
         DC    AL2(TTETCSCH)       301 - SKIP                      -EU-
         B     XARESET                                             -EU-
         DC    AL2(TTETCALL)       303                             -EU-
         B     XAXCALL                                             -EU-
         DC    AL2(TTETRSCH)       401 - SKIP                      -EU-
         B     XARESET                                             -EU-
         DC    AL2(TTETCLKC)       403                             -EU-
         B     XAXCLKC                                             -EU-
         DC    AL2(TTETSVCE)       F05                             -EU-
         B     XAXSVCE                                             -EU-
         DC    AL2(TTETWAIT)       F0F - SKIP                      -EU-
         B     XARESET                                             -EU-
XAEXEND  EQU   *-L'XAEXTAB         END OF TABLE                    -EU-
         SPACE 1                                                   -EU-
         SPACE 1                                                   -EU-
        LTORG  ,                                                   -EU-
         EJECT ,                                                   -EU-
*        SNAP DUMP PROCESS                                         -EU-
*        -----------------                                         -EU-
         SPACE 1                                                   -EU-
* SNAP ENTRY : TO TAKE A SNAP DUMP.                                -EU-
*              R0  = SNAP ID NUMBER (0-255).                       -EU-
*              R9  = LINK REGISTER.                                -EU-
*              R10 = LOCAL BASE REGISTER.                          -EU-
*              R11 = PSWSAMP FIRST BASE REGISTER.                  -EU-
*              R12 = PSWSAMP SECOND BASE REGISTER.                 -EU-
*              R15 = ADDRESS OF SNAP ROUTINE AT ENTRY.             -EU-
*              NOTE - THE FOLLOWING ALLOCATION IS NEEDED TO        -EU-
*                     OBTAIN THE SNAP DUMPS :                      -EU-
*                        ALLOC F(SYSSNAP) SYSOUT(A)                -EU-
*                     OR :                                         -EU-
*                        ALLOC F(SYSSNAP) DA(...) OLD              -EU-
         SPACE 1                                                   -EU-
         CNOP  0,4                                                 -EU-
         USING *,R15                                               -EU-
SNAP     TM    SNAPSW,SNAPFNA      FUNCTION INACTIVE ?             -EU-
         BOR   R9                  YES, BYPASS                     -EU-
         STM   R14,R12,12(R13)     ENTRY                           -EU-
         LR    R10,R15             SET LOCAL BASE                  -EU-
         DROP  R15                                                 -EU-
         USING SNAP,R10                                            -EU-
         LR    R1,R13                                              -EU-
         LA    R13,SNAPSV                                          -EU-
         ST    R1,4(R13)                                           -EU-
         ST    R13,8(R1)                                           -EU-
         LR    R4,R0               SAVE SNAP ID NUMBER             -EU-
         LA    R2,SNAPDCB                                          -EU-
         USING IHADCB,R2                                           -EU-
         TM    SNAPSW,SNAPOPN      DATA-SET ALREADY OPENED ?       -EU-
         BO    SNO                 YES                             -EU-
         L     R3,ATIOT                                            -EU-
         USING TIODSECT,R3                                         -EU-
         XR    R1,R1                                               -EU-
SNS      CLC   TIOELNGH(4),=F'0'                                   -EU-
         BE    SNN                                                 -EU-
         CLC   TIOEDDNM(L'TIOEDDNM),DCBDDNAM                       -EU-
         BE    SNF                                                 -EU-
         IC    R1,TIOELNGH                                         -EU-
         ALR   R3,R1                                               -EU-
         B     SNS                                                 -EU-
         DROP  R3                                                  -EU-
SNF     OPEN   (SNAPDCB,OUTPUT)                                    -EU-
         TM    DCBOFLGS,DCBOFOPN   SUCCESSFULLY OPENED ?           -EU-
         BZ    SNN                 NO                              -EU-
         DROP  R2                                                  -EU-
         OI    SNAPSW,SNAPOPN      INDICATE DATA-SET OPENED        -EU-
SNO      CH    R4,SNAPMAX          KNOWN ID NUMBER ?               -EU-
         BNL   SNE                 NO, BYPASS                      -EU-
         LR    R5,R4                                               -EU-
         MH    R5,SNAPTLE                                          -EU-
         LA    R5,SNAPTB(R5)                                       -EU-
         LA    R6,8(R5)                                            -EU-
         TM    0(R6),X'80'         UNUSED ELEMENT ?                -EU-
         BZ    SNE                 YES, BYPASS                     -EU-
        SNAP   TCB='S',ID=(R4),LIST=(R5),STRHDR=(R6),              -EU-1
               MF=(E,SNAPLIST)                                     -EU-
         LTR   R15,R15             ALL OK ?                        -EU-
         BZ    SNE                 YES                             -EU-
SNN      OI    SNAPSW,SNAPFNA      INACTIVE FUNCTION               -EU-
SNE      L     R13,4(R13)          EXIT                            -EU-
         LM    R14,R12,12(R13)                                     -EU-
         BR    R9                                                  -EU-
         DROP  R10                                                 -EU-
         SPACE 1                                                   -EU-
* SNAPCL ENTRY : TO CLOSE THE SNAP FILE IF OPENED.                 -EU-
*              R9  = LINK REGISTER.                                -EU-
*              R10 = LOCAL BASE REGISTER.                          -EU-
*              R11 = PSWSAMP FIRST BASE REGISTER.                  -EU-
*              R12 = PSWSAMP SECOND BASE REGISTER.                 -EU-
*              R10 = ADDRESS OF SNAPCL ROUTINE AT ENTRY.           -EU-
         SPACE 1                                                   -EU-
         CNOP  0,4                                                 -EU-
         USING *,R15                                               -EU-
SNAPCL   TM    SNAPSW,SNAPOPN      DATA-SET IS OPENED ?            -EU-
         BZR   R9                  NO                              -EU-
         STM   R14,R12,12(R13)     ENTRY                           -EU-
         LR    R10,R15             SET LOCAL BASE                  -EU-
         DROP  R15                                                 -EU-
         USING SNAPCL,R10                                          -EU-
         LR    R1,R13                                              -EU-
         LA    R13,SNAPSV                                          -EU-
         ST    R1,4(R13)                                           -EU-
         ST    R13,8(R1)                                           -EU-
        CLOSE  (SNAPDCB)                                           -EU-
         L     R13,4(R13)          EXIT                            -EU-
         LM    R14,R12,12(R13)                                     -EU-
         BR    R9                                                  -EU-
         DROP  R10                                                 -EU-
         SPACE 1                                                   -EU-
         PRINT NOGEN                                               -EU-
SNAPDCB  DCB   DSORG=PS,RECFM=VBA,MACRF=(W),                       -EU-1
               BLKSIZE=1632,LRECL=125,DDNAME=SYSSNAP               -EU-
SNAPLIST SNAP  DCB=SNAPDCB,MF=L                                    -EU-
         PRINT GEN                                                 -EU-
         SPACE 1                                                   -EU-
SNAPSV   DC    18F'0'                                              -EU-
         SPACE 1                                                   -EU-
SNAPTB   DC    3F'0'               0 = NOT USED                    -EU-
SNAPTBL  EQU   *-SNAPTB            ELEMENT LENGTH                  -EU-
SNAP1    DC    A(*-*),XL1'80',AL3(*-*),XL1'80',AL3(SHD1)           -EU-
SNAPTBM  EQU   (*-SNAPTB)/SNAPTBL  NUMBER OF ELEMENTS              -EU-
         SPACE 1                                                   -EU-
SNAPTLE  DC    Y(SNAPTBL)          ELEMENT LENGTH                  -EU-
SNAPMAX  DC    Y(SNAPTBM)          NUMBER OF ELEMENTS (MAX ID)     -EU-
SNAPSW   DC    XL1'0'                                              -EU-
SNAPFNA  EQU   X'01'               SNAP FUNCTION NOT ACTIVE.       -EU-
SNAPOPN  EQU   X'02'               SNAP DATA-SET IS OPENED.        -EU-
SHD1     DC    AL1(L'SHD1T)                                        -EU-
SHD1T    DC    C'P S W S A M P - DUMP OF TRACE BUFFER.'            -EU-
         SPACE 1                                                   -EU-
        LTORG  ,                                                   -EU-
         EJECT ,                                                   -EU-
LPAE     DSECT             -- LPA LIST PRINT SEGMENT               -EU-
LN       DS    CL8            MODULE NAME                          -EU-
         DS    C                                                   -EU-
LX       DS    0CL17          TRANSLATE NEXT 17 BYTES              -EU-
LS       DS    CL6                 START ADDRESS                   -EU-
         DS    CL4                                                 -EU-
LL       DS    CL6                 LENGTH                          -EU-
         DS    CL2                 ... ETC ...                     -EU-
         ORG   LX                                                  -EU-
LXN      DS    0CL19          TRANSLATE NEXT 19 BYTES              -EU-
LSN      DS    CL8                 START ADDRESS                   -EU-
         DS    CL2                                                 -EU-
LLN      DS    CL8                 LENGTH                          -EU-
         DS    CL2                 ... ETC ...                     -EU-
         SPACE 2                                                   -EU-
TRACEHDR DSECT             -- TRACE TABLE HEADER
NEXT     DS    F              NEXT ENTRY ADDRESS
FIRST    DS    F              FIRST ENTRY ADDRESS
LAST     DS    F              LAST ENTRY ADDRESS
SPLENGTH DS    F              SUBPOOL & LENGTH OF TABLE
         EJECT ,                                                   -EU-
*       IHATOB - A DSECT SHOULD BE SUBSTITUTED WHEN AVAILABLE.     -EU-
         SPACE 1                                                   -EU-
TOB      DSECT                SYSTEM TRACE OPTION BLOCK            -EU-
TOBID    DS    CL4,CL17       TOB EBCDIC IDENTIFIER                -EU-
TOBTRFG1 DS    XL1            TRACE STATUS FLAGS                   -EU-
TOBSVACT EQU   X'80'          TRACE SERVICES AVAILABLE             -EU-
TOBSTACT EQU   X'40'          TRACE ACTIVE                         -EU-
TOBTRFG2 DS    XL1            TRACE PENDING FLAGS                  -EU-
TOBPNDEA EQU   X'80'          TRACE ENV. ALTERATION PENDING        -EU-
TOBPNDOF EQU   X'40'          TRACE OFF PENDING                    -EU-
TOBTRFG3 DS    XL1            MISCELLANEOUS TRACE FLAGS            -EU-
TOBTAST  EQU   X'80'          TRACE A.S. TERMINATION IN PROGRESS   -EU-
TOBTROPT DS    0XL4,XL3       TRACE OPTIONS REQ. IN CR12 FORMAT    -EU-
TOBTRBR  EQU   X'80'          BRANCH TRACE OPTION                  -EU-
TOBTROB4 DS    XL1,XL4        LAST BYTE OF TRACE OPTIONS REQ.      -EU-
TOBTRASD EQU   X'02'          ASID TRACE OPTION                    -EU-
TOBTREXP EQU   X'01'          EXPLICIT TRACE OPTION                -EU-
TOBASCB  DS    A,XL2          TRACE A.S. ASCB ADDRESS              -EU-
TOBASID  DS    H              TRACE A.S. ASID                      -EU-
         SPACE 2                                                   -EU-
*       IHATBVT - A DSECT SHOULD BE SUBSTITUTED WHEN AVAILABLE.    -EU-
         SPACE 1                                                   -EU-
TBVT     DSECT                SYSTEM TRACE BUFFER VECTOR TABLE     -EU-
TBVTID   DS    CL4,XL12       TBVT EBCDIC IDENTIFIER               -EU-
TBVTFWRD DS    0D             TBVT FORWARD QUEUE POINTERS          -EU-
TBVTNXTR DS    A              REAL ADDRESS OF NEXT TBVT            -EU-
TBVTNXTV DS    A              VIRTUAL ADDRESS OF NEXT TBVT         -EU-
TBVTBWRD DS    A              VIRTUAL ADDRESS OF PREVIOUS TBVT     -EU-
TBVTBUFV DS    A              VIRTUAL ADDRESS OF 4K BUFFER         -EU-
         SPACE 2                                                   -EU-
         PRINT NOGEN                                               -EU-
        CVT    DSECT=YES,LIST=YES                                  -EU-
        IHAPSA ,                                                   -EU-
        IHATRVT ,                                                  -EU-
        IHATTE AS=YES,EX=YES                                       -EU-
        IEESMCA ,                                                  -EU-
TIODSECT DSECT ,                                                   -EU-
        IEFTIOT1 ,                                                 -EU-
        DCBD   DSORG=PS,DEVD=DA                                    -EU-
         SPACE 2                                                   -EU-
         END
