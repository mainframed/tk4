MESS     TITLE '--- E T P S -- ETPSMESS - MESSAGES SENDER ---'
         PRINT OFF
         COPY  ETPS$O1             "ETPS" SYSPARM SELECTION
         MACRO
&NAME    @MSG  &NO,&FLGS,&R=N
         LCLC  &F,&W
         AIF   ('&NO' NE '').A
         MNOTE 8,' MESSAGE NUMBER MISSING ************************** '
         AGO   .END
.A       AIF   ('&FLGS' EQ '').B
&F       SETC  '&FLGS'
         AGO   .C
.B       ANOP
&F       SETC  '0'
.C       AIF   ('&R'(1,1) EQ 'Y').D
&W       SETC  '0'
         AGO   .E
.D       ANOP
&W       SETC  'RMS&NO'
.E       ANOP
&NAME    DC    AL2(&NO),AL1(&F,L'MSG&NO-1),AL4(MSG&NO,&W)
.END     MEND
         MACRO
&NAME    @PHN  &NO,&NM
&NAME    DC    CL2'&NO',CL4'&NM'
.END     MEND
         COPY  ETPSMACS            "ETPS" MACROS
         COPY  ETPSDEFS            "ETPS" DEFINITIONS
         PRINT ON
ETPSMESS START 0
         SPACE 1
ETPSMESS AMODE 24
ETPSMESS RMODE 24
        MIDENT
         EJECT
*---------------------------- AT ENTRY : R0 MUST POINT TO "MYSAVE"
         SPACE 1
         PRINT GEN
         USING *,R12
         B     32(,R15)
         DC    CL28'  ETPSMESS &SYSDATE &SYSTIME'
         PRINT &PRF
         STM   R14,R12,12(R13)     SAVE INPUT REGISTERS
         LR    R12,R15             SET BASE REGISTER
         LR    R9,R0               SET "MYSAVE" ADDRESSABILITY
         USING MYSAVE,R9
         L     R11,=A(SPMSGWA-MYSAVE) GET LOCAL SA-WA
         AR    R11,R0
         LR    R0,R11
         L     R1,=A(MWKLG)
         LA    R14,*
         XR    R15,R15
         MVCL  R0,R14              CLEAR ALL TO BINARY ZEROS
         ST    R13,4(R11)          STORE BACKWARD POINTER
         ST    R11,8(R13)          STORE FORWARD POINTER
         LR    R13,R11
         USING MWKA,R11
         LA    R10,SPLITWRK        SET DSECT BASE OF SPLIT-SCREEN
         USING SPLTAREA,R10
         MVC   MWKID(L'MWKID+L'MWKPH),=CL18'ETPS.... - .... : '
         XR    R2,R2
         ICM   R2,B'0011',MESSNO   GET MESSAGE NUMBER
         BZ    *+L'*+8             INVALID NUMBER
         CH    R2,=H'9999'
         BNH   *+L'*+8             VALID NUMBER
         L     R3,=A(MSGTINV)
         B     MSTCKT
         L     R3,=A(MSGTBS)
SCTBMS   CLC   MSGTBSN(L'MSGTBSN,R3),=H'0' SEARCH MESSAGE IN TABLE
         BE    MSTFND              UNKOWN NUMBER
         CLM   R2,B'0011',MSGTBSN(R3) IS IT THIS NUMBER?
         BE    MSTFND              YES
         LA    R3,L'MSGTBS(R3)     NO, POINT TO NEXT
         B     SCTBMS
MSTFND   LR    R0,R2
         BAL   R14,DSCRC
         MVC   MWKIN(L'MWKIN),MWKWC-L'MWKIN+7
MSTCKT   TM    MSGTBSF(R3),ALTT    HOW TEST TERMINAL ACCESS TYPE?
         BZ    STDWAY              BY STANDARD WAY
         MVC   MWKXT(XTRCTL),XTRCT ELSE BY EXTRACT WAY
         XC    MWKTSO,MWKTSO
        EXTRACT MWKTSO,'S',FIELDS=(TSO),MF=(E,MWKXT)
         L     R1,MWKTSO
         TM    0(R1),X'80'         TSO FOREGROUND RUNNING?
         BO    TSOENV              YES
SYSENV   LA    R6,MSGS1            NO
         B     SETMSG
STDWAY   CLI   TUBEDDNM,C'T'       TSO TERMINAL?
         BNE   SYSENV              NO
TSOENV   LA    R6,MSGS2            YES
SETMSG   MVC   MWKPX(L'MSGI),MSGI(R6) SET WTO-PARM-LIST PREFIX
         XR    R4,R4               MOVE MESSAGE
         IC    R4,MSGTBSL(R3)
         TM    MSGTBSF(R3),NPHN    PHASE NAME SUPPRESS?
         BO    SPCLMS              YES
         L     R2,=A(MSGTBN)       ELSE SET IT
SCTBNL   CLC   0(2,R2),=AL2(0)
         BE    STPHNM+L'STPHNM     END OF TABLE, MISSING
         CLC   MWKIN(2),MSGTBNO(R2)
         BE    STPHNM              THIS ONE
         LA    R2,L'MSGTBN(R2)
         B     SCTBNL
STPHNM   MVC   MWKPN(L'MWKPN),MSGTBNM(R2)
         LA    R1,L'MWKPX+L'MWKID+L'MWKPH+1(R4)
         STH   R1,MWKPX
         ICM   R5,B'1111',MSGTBST(R3)
         EX    R4,MVFTXT
         LA    R1,MWKMSG+L'MWKPX+L'MWKID+L'MWKPH+1(R4)
         B     STWPLS
SPCLMS   LA    R1,L'MWKPX+L'MWKID+1(R4)
         STH   R1,MWKPX
         ICM   R5,B'1111',MSGTBST(R3)
         EX    R4,MVFPHN
         LA    R1,MWKMSG+L'MWKPX+L'MWKID+1(R4)
STWPLS   MVC   0(L'MSGD,R1),MSGD(R6) SET WTO-PARM-LIST SUFFIX
         ICM   R8,B'1111',MSGTBSR(R3) IS THERE A MESSAGE ROUTINE?
         BZ    SNDMSG              NO
         BR    R8                  YES, GO DO IT
SNDMSG  WTO    MF=(E,MWKMSG)       SEND MESSAGE
         L     R13,4(,R13)         PICK UP CALLING SAVE-AREA
         LM    R14,R12,12(R13)     RESTORE CALLING REGISTERS
         XR    R15,R15             SET RC=0
         BR    R14                 RETURN
MVFTXT   MVC   MWKTX(*-*),0(R5)    <<EXECUTED>>
MVFPHN   MVC   MWKPH(*-*),0(R5)    <<EXECUTED>>
         SPACE 1
*- - - - DISPLAY RC ROUTINE
*              R14 = LINK REGISTER
*              AT EXIT : DISPLAY RC IN "MWKWC" FIELD (LENGTH=7)
DSPRC    LH    R0,MESSRC           GET RC IN R0
DSCRC    CVD   R0,MWKDW            CONVERT R0
DSERC    UNPK  MWKWC(7),MWKDW+L'MWKDW-4(4) DISPLAY RC
         OI    MWKWC+6,C'0'
         BR    R14                 RETURN
*- - - - EDIT RC ROUTINE
*              R14 = LINK REGISTER
*              AT ENTRY : R0 = RC
*              AT EXIT : EDITED RC IN "MWKWC" FIELD
EDTRC    CVD   R0,MWKDW
         MVC   MWKWC,=XL8'4020202020202120'
         ED    MWKWC,MWKDW+L'MWKDW-4
         BR    R14                 RETURN
*- - - - HEXADECIMAL DISPLAY RC ROUTINE
*              R14 = LINK REGISTER
*              R1-R15 = WORK REGISTERS
*              AT EXIT : HEX RC IN "MWKWC" FIELD
HEXRC    L     R0,DYNRCODE         GET RC IN R0
HXDRC    LA    R1,MWKWC+L'MWKWC-1  HEX DISPLAY IT
         LA    R15,L'MWKWC
         STC   R0,0(R1)
         NI    0(R1),X'0F'
         SRL   R0,4
         BCTR  R1,0
         BCT   R15,*-14
         TR    MWKWC,HEXTB
         BR    R14                 RETURN
         EJECT
XTRCT   EXTRACT ,'S',,MF=L
XTRCTL   EQU   *-XTRCT
MSGS1   WTO    '  ',ROUTCDE=2,DESC=3,MF=L
MSGS2   WTO    '  ',ROUTCDE=11,DESC=7,MF=L
MSGI     EQU   0,4
MSGD     EQU   6,4
HEXTB    DC    CL16'0123456789ABCDEF'
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
*- - - - R12 BASE REGISTER HIGH LIMIT  - - - - - - - - - - - - - <===*
         EJECT
*------- MESSAGE ROUTINES
         SPACE 1
         USING *,R8
RMSINV   XR    R0,R0
         ICM   R0,B'0011',MESSNO
         BAL   R14,DSCRC
         MVC   MWKPH+MSGINVN(L'MSGINVN),MWKWC-L'MSGINVN+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS0099  ICM   R15,B'1111',MESSPL  SAVED R0 (MODULE NAME POINTER)
         BZ    *+L'*+6             NONE, SKIP
         MVC   MWKPH+MSG0099N(L'MSG0099N),0(R15)
         BAL   R14,DSPRC
         MVC   MWKPH+MSG0099R(L'MSG0099R),MWKWC-L'MSG0099R+7
         L     R0,MESSPL+4         SAVED R1 (ABEND CODE)
         BAL   R14,HXDRC
         MVC   MWKPH+MSG0099C(L'MSG0099C),MWKWC+L'MWKWC-L'MSG0099C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1003  BAL   R14,DSPRC
         MVC   MWKTX+MSG1003R(L'MSG1003R),MWKWC-L'MSG1003R+7
         ICM   R0,B'1111',ERRINFRC
         BAL   R14,HXDRC
         MVC   MWKTX+MSG1003C(L'MSG1003C),MWKWC+L'MWKWC-L'MSG1003C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1005  BAL   R14,DSPRC
         MVC   MWKTX+MSG1005R(L'MSG1005R),MWKWC-L'MSG1005R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1007  BAL   R14,DSPRC
         MVC   MWKTX+MSG1007R(L'MSG1007R),MWKWC-L'MSG1007R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1010  BAL   R14,DSPRC
         MVC   MWKTX+MSG1010R(L'MSG1010R),MWKWC-L'MSG1010R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1011  BAL   R14,DSPRC
         MVC   MWKTX+MSG1011R(L'MSG1011R),MWKWC-L'MSG1011R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1101  BAL   R14,DSPRC
         MVC   MWKTX+MSG1101R(L'MSG1101R),MWKWC-L'MSG1101R+7
         ICM   R0,B'1111',ERRINFRC
         BAL   R14,HXDRC
         MVC   MWKTX+MSG1101C(L'MSG1101C),MWKWC+L'MWKWC-L'MSG1101C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1102  BAL   R14,DSPRC
         MVC   MWKTX+MSG1102R(L'MSG1102R),MWKWC-L'MSG1102R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1201  XR    R0,R0
         IC    R0,RECB
         BAL   R14,HXDRC
         MVC   MWKTX+MSG1201C(L'MSG1201C),MWKWC+L'MWKWC-L'MSG1201C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1202  XR    R0,R0
         IC    R0,RECB
         BAL   R14,HXDRC
         MVC   MWKTX+MSG1202C(L'MSG1202C),MWKWC+L'MWKWC-L'MSG1202C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1205  CLC   CCW1(1),WRITECC     WAS WRITE OPERATION?
         BE    SNDMSG              YES
         MVI   MWKTX+MSG1205O,C'R' NO, SAY READ
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1206  CLC   CCW1(1),WRITECC     WAS WRITE OPERATION?
         BE    SNDMSG              YES
         MVI   MWKTX+MSG1206O,C'R' NO, SAY READ
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1207  BAL   R14,DSPRC
         MVC   MWKTX+MSG1207R(L'MSG1207R),MWKWC-L'MSG1207R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1208  BAL   R14,DSPRC
         MVC   MWKTX+MSG1208R(L'MSG1208R),MWKWC-L'MSG1208R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1301  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG1301R(L'MSG1301R),MWKWC-L'MSG1301R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG1301C(L'MSG1301C),MWKWC+L'MWKWC-L'MSG1301C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1303  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG1303R(L'MSG1303R),MWKWC-L'MSG1303R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG1303C(L'MSG1303C),MWKWC+L'MWKWC-L'MSG1303C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1305  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG1305R(L'MSG1305R),MWKWC-L'MSG1305R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG1305C(L'MSG1305C),MWKWC+L'MWKWC-L'MSG1305C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1306  LA    R0,NLDCB+1
         S     R0,MESSPL           COMPUTE FILE PROCESS NUMBER
         STC   R0,MWKTX+MSG1306N
         OI    MWKTX+MSG1306N,C'0'
         L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG1306R(L'MSG1306R),MWKWC-L'MSG1306R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG1306C(L'MSG1306C),MWKWC+L'MWKWC-L'MSG1306C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1307  BAL   R14,DSPRC
         MVC   MWKTX+MSG1307R(L'MSG1307R),MWKWC-L'MSG1307R+7
         L     R0,MESSPL           SAVED R0 (ERR-INF/CODE)
         BAL   R14,HXDRC
         MVC   MWKTX+MSG1307C(L'MSG1307C),MWKWC+L'MWKWC-L'MSG1307C
         ICM   R15,B'1111',MESSPL+4
         BZ    SNDMSG
         XR    R14,R14
         IC    R14,0(R15)
         LTR   R14,R14
         BNP   SNDMSG
         EX    R14,*+L'*+4
         B     SNDMSG
         MVC   MWKTX+MSG1307D(*-*),1(R15)
         DROP  R8
         USING *,R8
RMS1308  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG1308R(L'MSG1308R),MWKWC-L'MSG1308R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG1308C(L'MSG1308C),MWKWC+L'MWKWC-L'MSG1308C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1401  BAL   R14,DSPRC
         MVC   MWKTX+MSG1401R(L'MSG1401R),MWKWC-L'MSG1401R+7
         L     R0,MESSPL           SAVED R0 (ERROR CODE OR INFO)
         BAL   R14,HXDRC
         MVC   MWKTX+MSG1401C(L'MSG1401C),MWKWC+L'MWKWC-L'MSG1401C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1501  BAL   R14,DSPRC
         MVC   MWKTX+MSG1501R(L'MSG1501R),MWKWC-L'MSG1501R+7
         L     R0,MESSPL           SAVED R0 (ERROR CODE OR INFO)
         BAL   R14,HXDRC
         MVC   MWKTX+MSG1501C(L'MSG1501C),MWKWC+L'MWKWC-L'MSG1501C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS1601  BAL   R14,DSPRC
         MVC   MWKTX+MSG1601R(L'MSG1601R),MWKWC-L'MSG1601R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2003  BAL   R14,DSPRC
         MVC   MWKTX+MSG2003R(L'MSG2003R),MWKWC-L'MSG2003R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2003C(L'MSG2003C),MWKWC+L'MWKWC-L'MSG2003C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2004  L     R0,MESSPL           MAX. CARDS
         BAL   R14,EDTRC
         MVC   MWKTX+MSG2004L(L'MSG2004L),MWKWC+L'MWKWC-L'MSG2004L
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2005  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG2005R(L'MSG2005R),MWKWC-L'MSG2005R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2005C(L'MSG2005C),MWKWC+L'MWKWC-L'MSG2005C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2006  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG2006R(L'MSG2006R),MWKWC-L'MSG2006R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2006C(L'MSG2006C),MWKWC+L'MWKWC-L'MSG2006C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2007  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG2007R(L'MSG2007R),MWKWC-L'MSG2007R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2007C(L'MSG2007C),MWKWC+L'MWKWC-L'MSG2007C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2008  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG2008R(L'MSG2008R),MWKWC-L'MSG2008R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2008C(L'MSG2008C),MWKWC+L'MWKWC-L'MSG2008C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2009  BAL   R14,DSPRC
         MVC   MWKTX+MSG2009R(L'MSG2009R),MWKWC-L'MSG2009R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2010  BAL   R14,DSPRC
         MVC   MWKTX+MSG2010R(L'MSG2010R),MWKWC-L'MSG2010R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2011  BAL   R14,DSPRC
         MVC   MWKTX+MSG2011R(L'MSG2011R),MWKWC-L'MSG2011R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2012  BAL   R14,DSPRC
         MVC   MWKTX+MSG2012R(L'MSG2012R),MWKWC-L'MSG2012R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2013  BAL   R14,DSPRC
         MVC   MWKTX+MSG2013R(L'MSG2013R),MWKWC-L'MSG2013R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2015  BAL   R14,DSPRC
         MVC   MWKTX+MSG2015R(L'MSG2015R),MWKWC-L'MSG2015R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2015C(L'MSG2015C),MWKWC+L'MWKWC-L'MSG2015C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2016  BAL   R14,DSPRC
         MVC   MWKTX+MSG2016R(L'MSG2016R),MWKWC-L'MSG2016R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2016C(L'MSG2016C),MWKWC+L'MWKWC-L'MSG2016C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2017  BAL   R14,DSPRC
         MVC   MWKTX+MSG2017R(L'MSG2017R),MWKWC-L'MSG2017R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2017C(L'MSG2017C),MWKWC+L'MWKWC-L'MSG2017C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2018  BAL   R14,DSPRC
         MVC   MWKTX+MSG2018R(L'MSG2018R),MWKWC-L'MSG2018R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2018C(L'MSG2018C),MWKWC+L'MWKWC-L'MSG2018C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2019  BAL   R14,DSPRC
         MVC   MWKTX+MSG2019R(L'MSG2019R),MWKWC-L'MSG2019R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2019C(L'MSG2019C),MWKWC+L'MWKWC-L'MSG2019C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2020  BAL   R14,DSPRC
         MVC   MWKTX+MSG2020R(L'MSG2020R),MWKWC-L'MSG2020R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2020C(L'MSG2020C),MWKWC+L'MWKWC-L'MSG2020C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2021  BAL   R14,DSPRC
         MVC   MWKTX+MSG2021R(L'MSG2021R),MWKWC-L'MSG2021R+7
         L     R0,MESSPL           REASON CODE
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2021C(L'MSG2021C),MWKWC+L'MWKWC-L'MSG2021C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS2022  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG2022R(L'MSG2022R),MWKWC-L'MSG2022R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG2022C(L'MSG2022C),MWKWC+L'MWKWC-L'MSG2022C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3001  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG3001R(L'MSG3001R),MWKWC-L'MSG3001R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG3001C(L'MSG3001C),MWKWC+L'MWKWC-L'MSG3001C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3002  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG3002R(L'MSG3002R),MWKWC-L'MSG3002R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG3002C(L'MSG3002C),MWKWC+L'MWKWC-L'MSG3002C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3003  XR    R0,R0
         IC    R0,RECB
         BAL   R14,HXDRC
         MVC   MWKTX+MSG3003C(L'MSG3003C),MWKWC+L'MWKWC-L'MSG3003C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3004  XR    R0,R0
         IC    R0,RECB
         BAL   R14,HXDRC
         MVC   MWKTX+MSG3004C(L'MSG3004C),MWKWC+L'MWKWC-L'MSG3004C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3005  BAL   R14,DSPRC
         MVC   MWKTX+MSG3005R(L'MSG3005R),MWKWC-L'MSG3005R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3006  BAL   R14,DSPRC
         MVC   MWKTX+MSG3006R(L'MSG3006R),MWKWC-L'MSG3006R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3008  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG3008R(L'MSG3008R),MWKWC-L'MSG3008R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG3008C(L'MSG3008C),MWKWC+L'MWKWC-L'MSG3008C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3009  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG3009R(L'MSG3009R),MWKWC-L'MSG3009R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG3009C(L'MSG3009C),MWKWC+L'MWKWC-L'MSG3009C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3010  BAL   R14,DSPRC
         MVC   MWKTX+MSG3010R(L'MSG3010R),MWKWC-L'MSG3010R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS3011  BAL   R14,DSPRC
         MVC   MWKTX+MSG3011R(L'MSG3011R),MWKWC-L'MSG3011R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS4001  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG4001R(L'MSG4001R),MWKWC-L'MSG4001R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG4001C(L'MSG4001C),MWKWC+L'MWKWC-L'MSG4001C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS4002  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG4002R(L'MSG4002R),MWKWC-L'MSG4002R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG4002C(L'MSG4002C),MWKWC+L'MWKWC-L'MSG4002C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS4003  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG4003R(L'MSG4003R),MWKWC-L'MSG4003R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG4003C(L'MSG4003C),MWKWC+L'MWKWC-L'MSG4003C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS4004  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG4004R(L'MSG4004R),MWKWC-L'MSG4004R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG4004C(L'MSG4004C),MWKWC+L'MWKWC-L'MSG4004C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS4005  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG4005R(L'MSG4005R),MWKWC-L'MSG4005R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG4005C(L'MSG4005C),MWKWC+L'MWKWC-L'MSG4005C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS4006  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG4006R(L'MSG4006R),MWKWC-L'MSG4006R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG4006C(L'MSG4006C),MWKWC+L'MWKWC-L'MSG4006C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS4007  BAL   R14,DSPRC
         MVC   MWKTX+MSG4007R(L'MSG4007R),MWKWC-L'MSG4007R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5001  BAL   R14,DSPRC
         MVC   MWKTX+MSG5001R(L'MSG5001R),MWKWC-L'MSG5001R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5002  L     R0,MESSPL           MAX. MEMBERS TABLE
         BAL   R14,EDTRC
         MVC   MWKTX+MSG5002L(L'MSG5002L),MWKWC+L'MWKWC-L'MSG5002L
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5003  BAL   R14,DSPRC
         MVC   MWKTX+MSG5003R(L'MSG5003R),MWKWC-L'MSG5003R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5004  BAL   R14,DSPRC
         MVC   MWKTX+MSG5004R(L'MSG5004R),MWKWC-L'MSG5004R+7
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5005  L     R0,MESSPL           ECB CONTENTS
         BAL   R14,HXDRC
         MVC   MWKTX+MSG5005E(L'MSG5005E),MWKWC+L'MWKWC-L'MSG5005E
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5008  L     R0,MESSPL           MAX. MEMBERS TABLE
         BAL   R14,EDTRC
         MVC   MWKTX+MSG5008L(L'MSG5008L),MWKWC+L'MWKWC-L'MSG5008L
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5009  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG5009R(L'MSG5009R),MWKWC-L'MSG5009R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG5009C(L'MSG5009C),MWKWC+L'MWKWC-L'MSG5009C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5010  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG5010R(L'MSG5010R),MWKWC-L'MSG5010R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG5010C(L'MSG5010C),MWKWC+L'MWKWC-L'MSG5010C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5011  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG5011R(L'MSG5011R),MWKWC-L'MSG5011R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG5011C(L'MSG5011C),MWKWC+L'MWKWC-L'MSG5011C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5012  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG5012R(L'MSG5012R),MWKWC-L'MSG5012R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG5012C(L'MSG5012C),MWKWC+L'MWKWC-L'MSG5012C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5013  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG5013R(L'MSG5013R),MWKWC-L'MSG5013R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG5013C(L'MSG5013C),MWKWC+L'MWKWC-L'MSG5013C
         B     SNDMSG
         DROP  R8
         USING *,R8
RMS5014  L     R0,DYNRCODE+4
         BAL   R14,DSCRC
         MVC   MWKTX+MSG5014R(L'MSG5014R),MWKWC-L'MSG5014R+7
         BAL   R14,HEXRC
         MVC   MWKTX+MSG5014C(L'MSG5014C),MWKWC+L'MWKWC-L'MSG5014C
         B     SNDMSG
         DROP  R8
         SPACE 1
         DROP  R9,R10,R11,R12
         EJECT
*------- MESSAGES TABLE SELECTION
         SPACE 1
MSGTBS   EQU   *,12                TABLE (START/LENGTH)
MSGTBSN  EQU   0,2                      MESSAGE NUMBER
MSGTBSF  EQU   2,1                      MESSAGE FLAGS BYTE
ALTT     EQU   X'80'                    ALTERNATE TEST FLAG
NPHN     EQU   X'40'                    NO PHASE NAME
MSGTBSL  EQU   3,1                      MESSAGE LENGTH
MSGTBST  EQU   4,4                      MESSAGE TEXT POINTER
MSGTBSR  EQU   8,4                      MESSAGE ROUTINE OR ZERO (NONE)
         @MSG  0099,NPHN,R=Y
         @MSG  1001,ALTT
         @MSG  1002,ALTT
         @MSG  1003,,R=Y
         @MSG  1004
         @MSG  1005,,R=Y
         @MSG  1006
         @MSG  1007,,R=Y
         @MSG  1008
         @MSG  1009
         @MSG  1010,,R=Y
         @MSG  1011,,R=Y
         @MSG  1012
         @MSG  1013
         @MSG  1101,,R=Y
         @MSG  1102,,R=Y
         @MSG  1201,,R=Y
         @MSG  1202,,R=Y
         @MSG  1203
         @MSG  1204
         @MSG  1205,,R=Y
         @MSG  1206,,R=Y
         @MSG  1207,,R=Y
         @MSG  1208,,R=Y
         @MSG  1301,,R=Y
         @MSG  1302
         @MSG  1303,,R=Y
         @MSG  1304
         @MSG  1305,,R=Y
         @MSG  1306,,R=Y
         @MSG  1307,,R=Y
         @MSG  1308,,R=Y
         @MSG  1309
         @MSG  1401,,R=Y
         @MSG  1501,,R=Y
         @MSG  1601,,R=Y
         @MSG  2001
         @MSG  2002
         @MSG  2003,,R=Y
         @MSG  2004,,R=Y
         @MSG  2005,,R=Y
         @MSG  2006,,R=Y
         @MSG  2007,,R=Y
         @MSG  2008,,R=Y
         @MSG  2009,,R=Y
         @MSG  2010,,R=Y
         @MSG  2011,,R=Y
         @MSG  2012,,R=Y
         @MSG  2013,,R=Y
         @MSG  2014
         @MSG  2015,,R=Y
         @MSG  2016,,R=Y
         @MSG  2017,,R=Y
         @MSG  2018,,R=Y
         @MSG  2019,,R=Y
         @MSG  2020,,R=Y
         @MSG  2021,,R=Y
         @MSG  2022,,R=Y
         @MSG  3001,,R=Y
         @MSG  3002,,R=Y
         @MSG  3003,,R=Y
         @MSG  3004,,R=Y
         @MSG  3005,,R=Y
         @MSG  3006,,R=Y
         @MSG  3007
         @MSG  3008,,R=Y
         @MSG  3009,,R=Y
         @MSG  3010,,R=Y
         @MSG  3011,,R=Y
         @MSG  4001,,R=Y
         @MSG  4002,,R=Y
         @MSG  4003,,R=Y
         @MSG  4004,,R=Y
         @MSG  4005,,R=Y
         @MSG  4006,,R=Y
         @MSG  4007,,R=Y
         @MSG  5001,,R=Y
         @MSG  5002,,R=Y
         @MSG  5003,,R=Y
         @MSG  5004,,R=Y
         @MSG  5005,,R=Y
         @MSG  5006
         @MSG  5007
         @MSG  5008,,R=Y
         @MSG  5009,,R=Y
         @MSG  5010,,R=Y
         @MSG  5011,,R=Y
         @MSG  5012,,R=Y
         @MSG  5013,,R=Y
         @MSG  5014,,R=Y
*- - - - END OF TABLE (LET'S STAY HERE AS IT IS)
         DC    AL2(0),AL1(ALTT+NPHN,L'MSGUNK-1),AL4(MSGUNK,0)
MSGTINV  DC    AL2(0),AL1(ALTT+NPHN,L'MSGINV-1),AL4(MSGINV,RMSINV)
         SPACE 1
*------- MESSAGES TABLE PHASE NAMES
         SPACE 1
MSGTBN   EQU   *,6                 TABLE (START/LENGTH)
MSGTBNO  EQU   0,2                      MESSAGE NUMBER
MSGTBNM  EQU   2,4                      MESSAGE FLAGS BYTE
         @PHN  10,INIT
         @PHN  11,EXIT
         @PHN  12,COMM
         @PHN  13,LIBM
         @PHN  14,COMP
         @PHN  15,IDCA
         @PHN  16,BROW
         @PHN  20,EDIT
         @PHN  30,ZAP
         @PHN  40,IZAP
         @PHN  50,DSST
         DC    AL2(0)              END OF TABLE
         EJECT
*------- MESSAGES LIST (SEE "MWKA" DSECT FOR LENGTH)
         SPACE 1
MSGINV   DC    C'INVALID MESSAGE NUMBER (.....)'
MSGINVN  EQU   24,5
MSGUNK   DC    C'UNKNOWN MESSAGE (UNDEFINED NUMBER)'
MSG0099  DC    C'LOAD "........" FAILED / RC = .. / ABEND CODE = ......X
               .. '
MSG0099N EQU   6,8
MSG0099R EQU   30,2
MSG0099C EQU   48,8
MSG1001  DC    C'INVALID PARM LENGTH'
MSG1002  DC    C'INVALID PARM FIELD'
MSG1003  DC    C'TUBE ALLOCATION (RC = .../........)'
MSG1003R EQU   22,3
MSG1003C EQU   26,8
MSG1004  DC    C'COULD NOT OPEN TUBE'
MSG1005  DC    C'GTSIZE FAILED (RC = ...)'
MSG1005R EQU   20,3
MSG1006  DC    C'NOT DISPLAY TERMINAL'
MSG1007  DC    C'STFSMODE ON,INITIAL=YES (RC = ...)'
MSG1007R EQU   30,3
MSG1008  DC    C'NOT TSO (NO TSB)'
MSG1009  DC    C'CANNOT CHANGE USER-ID'
MSG1010  DC    C'CHANGE GROUP-ID (RACINIT RC = ...)'
MSG1010R EQU   30,3
MSG1011  DC    C'EXCP ATT. GETMAIN FAILED (RC = ...)'
MSG1011R EQU   31,3
MSG1012  DC    C'EXCP ATT. DUMMY ROUTINE NOT FOUND'
MSG1013  DC    C'EXCP ATT. FREE/DUMMY ENTRY NOT FOUND'
MSG1101  DC    C'FREE TUBE (RC = .../........)'
MSG1101R EQU   16,3
MSG1101C EQU   20,8
MSG1102  DC    C'STFSMODE OFF (RC = ...)'
MSG1102R EQU   19,3
MSG1201  DC    C'BAD ECB CODE ON WRITE (..)'
MSG1201C EQU   23,2
MSG1202  DC    C'BAD ECB CODE ON READ (..)'
MSG1202C EQU   22,2
MSG1203  DC    C'INVALID SBA CODE'
MSG1204  DC    C'INCOMPLETE SBA CODE'
MSG1205  DC    C'PERMANENT I/O ERROR (W)'
MSG1205O EQU   21,1
MSG1206  DC    C'I/O INTERCEPTED (W)'
MSG1206O EQU   17,1
MSG1207  DC    C'BAD WRITE RETURN CODE FROM TPUT (RC = ...)'
MSG1207R EQU   38,3
MSG1208  DC    C'BAD READ RETURN CODE FROM TGET (RC = ...)'
MSG1208R EQU   37,3
MSG1301  DC    C'ALLOC "LIB" FAILED (RC = .../........)'
MSG1301R EQU   25,3
MSG1301C EQU   29,8
MSG1302  DC    C'OPEN "LIB" FAILED'
MSG1303  DC    C'ALLOC "LIB(MEM)" FAILED (RC = .../........)'
MSG1303R EQU   30,3
MSG1303C EQU   34,8
MSG1304  DC    C'OPEN "LIB(MEM)" FAILED'
MSG1305  DC    C'FREE "LIB(MEM)" FAILED (RC = .../........)'
MSG1305R EQU   29,3
MSG1305C EQU   33,8
MSG1306  DC    C'FREE FILE (N = .) FAILED (RC = .../........)'
MSG1306N EQU   15,1
MSG1306R EQU   31,3
MSG1306C EQU   35,8
MSG1307  DC    C'COPY MEMBER FAILED (RC = .../......../................X
               ....)'
MSG1307R EQU   25,3
MSG1307C EQU   29,8
MSG1307D EQU   38,20
MSG1308  DC    C'FREE "LIB" FAILED (RC = .../........)'
MSG1308R EQU   24,3
MSG1308C EQU   28,8
MSG1309  DC    C'END-OF-FILE READ ON "LIB" DIRECTORY'
MSG1401  DC    C'COMPRESS FAILED (RC = .../R0=........)'
MSG1401R EQU   22,3
MSG1401C EQU   29,8
MSG1501  DC    C'IDCAMS FAILED (RC = .../R0=........)'
MSG1501R EQU   20,3
MSG1501C EQU   27,8
MSG1601  DC    C'BROWSE GETMAIN FAILED (RC = ...)'
MSG1601R EQU   28,3
MSG2001  DC    C'OPEN INPUT FAILED'
MSG2002  DC    C'OPEN OUTPUT FAILED'
MSG2003  DC    C'SUBMIT "OPEN" FAILED (RC = .../CODE = ..)'
MSG2003R EQU   27,3
MSG2003C EQU   38,2
MSG2004  DC    C'CANNOT SUPPORT OVER ....... CARDS'
MSG2004L EQU   20,7
MSG2005  DC    C'ALLOC "DIR" FAILED (RC = .../........)'
MSG2005R EQU   25,3
MSG2005C EQU   29,8
MSG2006  DC    C'FREE "DIR" FAILED (RC = .../........)'
MSG2006R EQU   24,3
MSG2006C EQU   28,8
MSG2007  DC    C'ALLOC "CPY" FAILED (RC = .../........)'
MSG2007R EQU   25,3
MSG2007C EQU   29,8
MSG2008  DC    C'FREE "CPY" FAILED (RC = .../........)'
MSG2008R EQU   24,3
MSG2008C EQU   28,8
MSG2009  DC    C'SPF STATISTICS FAILED (RC = ...)'
MSG2009R EQU   28,3
MSG2010  DC    C'"CPY" GETMAIN FAILED (RC = ...)'
MSG2010R EQU   27,3
MSG2011  DC    C'INITIAL DATA GETMAIN FAILED (RC = ...)'
MSG2011R EQU   34,3
MSG2012  DC    C'NEXT DATA GETMAIN FAILED (RC = ...)'
MSG2012R EQU   31,3
MSG2013  DC    C'COPY DATA GETMAIN FAILED (RC = ...)'
MSG2013R EQU   31,3
MSG2014  DC    C'END-OF-FILE READ ON "CPY" DIRECTORY'
MSG2015  DC    C'SUBMIT "GENCB-ACB" FAILED (RC = .../........)'
MSG2015R EQU   32,3
MSG2015C EQU   36,8
MSG2016  DC    C'SUBMIT "GENCB-RPL" FAILED (RC = .../........)'
MSG2016R EQU   32,3
MSG2016C EQU   36,8
MSG2017  DC    C'SUBMIT "MODCB" FAILED (RC = .../........)'
MSG2017R EQU   28,3
MSG2017C EQU   32,8
MSG2018  DC    C'SUBMIT "PUT" FAILED (RC = .../........)'
MSG2018R EQU   26,3
MSG2018C EQU   30,8
MSG2019  DC    C'SUBMIT "ENDREQ" FAILED (RC = .../........)'
MSG2019R EQU   29,3
MSG2019C EQU   33,8
MSG2020  DC    C'SUBMIT "CLOSE" FAILED (RC = .../CODE = ..)'
MSG2020R EQU   28,3
MSG2020C EQU   39,2
MSG2021  DC    C'SUBMIT "ALLOC" FAILED (RC = .../........)'
MSG2021R EQU   28,3
MSG2021C EQU   32,8
MSG2022  DC    C'SUBMIT "FREE" FAILED (RC = .../........)'
MSG2022R EQU   27,3
MSG2022C EQU   31,8
MSG3001  DC    C'ALLOC "ZAP" FAILED (RC = .../........)'
MSG3001R EQU   25,3
MSG3001C EQU   29,8
MSG3002  DC    C'FREE "ZAP" FAILED (RC = .../........)'
MSG3002R EQU   24,3
MSG3002C EQU   28,8
MSG3003  DC    C'BAD ECB CODE ON WRITE (..)'
MSG3003C EQU   23,2
MSG3004  DC    C'BAD ECB CODE ON READ (..)'
MSG3004C EQU   22,2
MSG3005  DC    C'BAD WRITE RETURN CODE FROM TPUT (RC = ...)'
MSG3005R EQU   38,3
MSG3006  DC    C'BAD READ RETURN CODE FROM TGET (RC = ...)'
MSG3006R EQU   37,3
MSG3007  DC    C'RESPONSE IS TOO LONG ...'
MSG3008  DC    C'ALLOC "LOG" FAILED (RC = .../........)'
MSG3008R EQU   25,3
MSG3008C EQU   29,8
MSG3009  DC    C'FREE "LOG" FAILED (RC = .../........)'
MSG3009R EQU   24,3
MSG3009C EQU   28,8
MSG3010  DC    C'X-ZAP GETMAIN FAILED (RC = ...)'
MSG3010R EQU   27,3
MSG3011  DC    C'GETMAIN BUFFER FAILED (RC = ...)'
MSG3011R EQU   28,3
MSG4001  DC    C'ALLOC "LIB" FAILED (RC = .../........)'
MSG4001R EQU   25,3
MSG4001C EQU   29,8
MSG4002  DC    C'FREE "LIB" FAILED (RC = .../........)'
MSG4002R EQU   24,3
MSG4002C EQU   28,8
MSG4003  DC    C'ALLOC "PRT" FAILED (RC = .../........)'
MSG4003R EQU   25,3
MSG4003C EQU   29,8
MSG4004  DC    C'FREE "PRT" FAILED (RC = .../........)'
MSG4004R EQU   24,3
MSG4004C EQU   28,8
MSG4005  DC    C'IN-ZAP ALLOC "PRT" FAILED (RC = .../........)'
MSG4005R EQU   32,3
MSG4005C EQU   36,8
MSG4006  DC    C'IN-ZAP FREE "PRT" FAILED (RC = .../........)'
MSG4006R EQU   31,3
MSG4006C EQU   35,8
MSG4007  DC    C'IN-ZAP GETMAIN FAILED (RC = ...)'
MSG4007R EQU   28,3
MSG5001  DC    C'STATDS GETMAIN FAILED (RC = ...)'
MSG5001R EQU   28,3
MSG5002  DC    C'STATDS CANNOT SUPPORT OVER ....... REAL-MEMBERS'
MSG5002L EQU   27,7
MSG5003  DC    C'STATDS RDJFCB FAILED (RC = ...)'
MSG5003R EQU   27,3
MSG5004  DC    C'STATDS OBTAIN FAILED (RC = ...)'
MSG5004R EQU   27,3
MSG5005  DC    C'STATDS READ FAILED (ECB=........)'
MSG5005E EQU   24,8
MSG5006  DC    C'STATDS OUTPUT FILE OPEN FAILED'
MSG5007  DC    C'STATDS TRACE FILE OPEN FAILED'
MSG5008  DC    C'STATDS CANNOT SUPPORT OVER ....... GAS-MEMBERS'
MSG5008L EQU   27,7
MSG5009  DC    C'ALLOC "LIB" FAILED (RC = .../........)'
MSG5009R EQU   25,3
MSG5009C EQU   29,8
MSG5010  DC    C'ALLOC "PRT" FAILED (RC = .../........)'
MSG5010R EQU   25,3
MSG5010C EQU   29,8
MSG5011  DC    C'ALLOC "TRC" FAILED (RC = .../........)'
MSG5011R EQU   25,3
MSG5011C EQU   29,8
MSG5012  DC    C'FREE "LIB" FAILED (RC = .../........)'
MSG5012R EQU   24,3
MSG5012C EQU   28,8
MSG5013  DC    C'FREE "PRT" FAILED (RC = .../........)'
MSG5013R EQU   24,3
MSG5013C EQU   28,8
MSG5014  DC    C'FREE "TRC" FAILED (RC = .../........)'
MSG5014R EQU   24,3
MSG5014C EQU   28,8
         EJECT
*------- ETPSMESS - WORK-AREA DESCRIPTION ----------------------------*
MWKA     DSECT ,                   LOCAL SA-WA
         DS    18F                 SAVE-AREA
MWKDW    DS    D                   WORK DOUBLE-WORD
MWKWC    DS    CL8                 WORK FIELD
MWKXT   EXTRACT ,'S',,MF=L         AREA FOR LIST FORM OF EXTRACT
MWKTSO   DS    A                   TSO FLAG ADDRESS
MWKMSG   DS    0XL88               MESSAGE WORK-AREA
MWKPX    DS    XL4                 WTO-PARM-LIST PREFIX
MWKID    DS    XL11                MESSAGE ID ('ETPS.... - ')
MWKIN    EQU   MWKID+4,4           MESSAGE ID NUMBER
MWKPH    DS    XL7                 MESSAGE PHASE ('.... : ')
MWKPN    EQU   MWKPH+0,4           MESSAGE PHASE NAME
MWKTX    DS    XL66                MESSAGE TEXT / WTO-PARM-LIST SUFFIX
MWKLG    EQU   (((*-MWKA)+7)/8)*8
MWKLGT   EQU   (STBIGWA-SPMSGWA)-MWKLG
         DS    0XL(MWKLGT)         "MWKLG" VALIDITY CHECK
         PRINT &PRS
        SPLTAREA
         SPACE 1
        MYSAVE
         PRINT &PRF
        IEFZB4D0
        IEFZB4D2
        DCBD   DSORG=(PS,PO),DEVD=DA
        IEZIOB
         PRINT GEN
         END
