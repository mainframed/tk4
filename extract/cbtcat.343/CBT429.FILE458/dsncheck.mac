DSNC     TITLE 'DSNCHECK -- CHECK FOR DSN OR MEMBER NAME.'
DSNCHECK START 0
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                          D S N C H E C K                            *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*
* FUNCTION :   THIS TSO COMMAND PROCESSOR CHECKS THE SYSTEM CATALOG
*              FOR THE EXISTENCE OF A NAME OR ALIAS.
*              IF A MEMBER NAME IS SPECIFIED, "DSNCHECK" ATTEMPTS TO
*              ALLOCATE THE DATA-SET, OPEN IT, AND CHECK FOR A MEMBER
*              NAME WITH BLDL.
*
* SYNTAX :     DSNCHECK  DSNAME  LIST
*              DSNC              NOLIST
*
* ATTRIBUTES : RENT
* LKED OPTIONS : RENT, REUS, ALIAS(DSNC)
* RETURN CODES :   0 - SUCCESSFULL
*                  4 - MEMBER NOT FOUND
*                  8 - CATALOG ERROR
*                 12 - NOT PARTITIONED DATA-SET (ONLY IF MEMBER)
*                 16 - PARSE ERROR
*                 20 - ALLOCATION ERROR
*                 24 - OPEN ERROR
*                 28 - CLOSE ERROR
*                 32 - DEALLOCATION ERROR
*                 36 - RUN ERROR
*
* AUTHOR :     CHUCK HOFFMAN, CONSULTANT
*              USER SERVICES CENTER
*              BUREAU OF SYSTEMS OPERATIONS
*              OFFICE OF MANAGEMENT INFORMATION SYSTEMS
*              EXECUTIVE OFFICE FOR ADMINISTRATION AND FINANCE
*              COMMONWEALTH OF MASSACHUSETTS (PHEW)
*              ONE ASHBURTON PLACE
*              BOSTON, MA 02108
* ORIGIN :     EXTRACTED FROM TAPE CBT 89 FEB, FILE 300.
*
* MAINTENANCE HISTORY :
*           1. ADD FEATURE TO ALLOCATE, OPEN, AND BLDL IF A  CAH83351A
*              MEMBER NAME IS SPECIFIED IN THE DSNAME.       CAH83351A
*           2. MODIFY REPORTING TO ACCOUNT FOR MEMBER NAMES. CAH83351A
*           3. CHANGE DEFAULT FROM NOLIST TO LIST.           CAH83351A
*           4. ADAPTED BY MOINIL P.A.                             -EU-
         EJECT
DSNCHECK AMODE 24
DSNCHECK RMODE 24
         SPACE 1
        $DEFREG
* REGISTER USAGE :
*              R0-R1 - LINKAGE CONVENTIONS, MACROS
*              R2-R5 - WORK
*                 R6 - (UNUSED)
*                 R7 - CPPL POINTER
*                 R8 - COMPLETION CODE
*                 R9 - BASE OF DSECT IKJPARMD
*                R10 - BASE OF DSECT WKAREA
*                R11 - (UNUSED)
*                R12 - BASE OF CSECT CHECKDSN
*                R13 - SAVE AREA POINTER
*                R14 - LINKAGE CONVENTIONS, RETURN ADDRESS
*                R15 - LINKAGE CONVENTIONS, RETURN CODE
         EJECT
        $XENT  BASE=R12,LV=WKLEN,TYPE=RENT
         LR    R10,R13             GET WORK-AREA ADDRESS
         USING WKAREA,R10          ADDRESSABILITY OF WORK-AREA
         LA    R2,18*4(R10)
         L     R3,=A(WKLEN-(18*4))
         LR    R4,R2
         XR    R5,R5
         MVCL  R2,R4
         LR    R7,R1               GET ENTRY REGISTER
        $TEW$EC REG=R7,MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
         USING CPPL,R7
         XR    R8,R8               SET COMPLETION CODE -------------- 0
        $TEW$PP PARM=PARMLST,REG=R9,ERR=PARSERR
         SPACE 1
*------- GET DATA-SET NAME
         SPACE 1
         MVI   DSNARG,C' '         CLEAR DSN + TRAILING SPACE
         MVC   DSNARG+1(L'DSNARG-1),DSNARG
         LA    R2,PDSN             R2 POINTER TO PDE FOR DSN
         LH    R3,4(0,R2)          R3 IS LENGTH OF DSN
         L     R2,0(,R2)           R2 POINTS TO DSN
         STH   R3,DSNARGLN         SAVE IT
         BCT   R3,*+L'*+6          R3-1 FOR EXEC
         MVC   DSNARG(*-*),0(R2)   MOVE DSN TO STORAGE
         EX    R3,*-6              EXECUTE THE MOVE
         SPACE 1
*------- INITIALIZE CIR PARM LIST
         SPACE 1
         MVC   CIRPARM(CIRDATAL),CIRDATA CONSTANTS TO PARM LIST
         LA    R1,CIRWORK          ADDR OF WORK AREA
         ST    R1,CIRWA            INTO PARM LIST
         LA    R1,CIRRSA           ADDR OF S.A
         ST    R1,CIRSAVE          INTO PARM LIST
         LA    R1,DSNARG           ADDR OF DSN
         ST    R1,CIRSRCH          INTO PARM LIST
         MVC   AREALN,=Y(VDATAL)   SET LENGTH OF WORK AREA
         SPACE 1
*------- GET INFORMATION FROM CATALOG
         SPACE 1
        CALLTSSR EP=IKJEHCIR,MF=(E,CIRPARM)
         LTR   R15,R15             AND TEST COMPLETION
         BZ    *+L'*+12            BRANCH IF GOOD
         ST    R15,ERRCDE          SAVE RETURN CODE
         LA    R8,8                SET RETURN CODE ------------------ 8
         B     MALIST
         SPACE 1
*------- GET MEMBER NAME INFORMATION
         SPACE 1
         TM    PDSN+14,X'80'       ANY MEMBER NAME ?
         BZ    MALIST              NO
         LA    R1,DAWRBP           LINK CONTROL BLOCKS
         USING S99RBP,R1
         LA    R2,DAWRB
         ST    R2,S99RBPTR
         OI    S99RBPTR,S99RBPND
         DROP  R1
         USING S99RB,R2
         MVI   S99RBLN,S99RBEND-S99RB
         MVI   S99VERB,S99VRBAL
         MVI   S99FLG11,S99ONCNV+S99NOMNT
         MVI   S99FLG12,0
         LA    R1,DAWTUPL
         ST    R1,S99TXTPP
         DROP  R2
         USING S99TUNIT,R1
         LA    R1,DAWTUN02
         LA    R2,DALDSNAM         DSNAME
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=Y(L'DAWDSNAM)
         MVI   S99TUPAR,C' '
         MVC   S99TUPAR+1(L'DAWDSNAM-1),S99TUPAR
         ST    R1,DAWTUP02
         LA    R1,DAWTUN04
         LA    R2,DALSTATS         DATA-SET STATUS
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'1'
         MVI   S99TUPAR,X'08'      STATUS SHR
         ST    R1,DAWTUP04
         LA    R1,DAWTUN55
         LA    R2,DALRTDDN         RETURN DD-NAME
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'8'
         MVC   S99TUPAR(8),=CL8' '
         ST    R1,DAWTUP55
         LA    R1,DAWTUN57
         LA    R2,DALRTORG         RETURN DS ORGANIZATION
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'2'
         ST    R1,DAWTUP57
         OI    DAWTUP57,S99TUPLN
         DROP  R1
         LA    R2,DAUDFPRM
         USING DFDSECTD,R2
         MVC   DFS99RBP,DAWRBP     PTR TO RB
         LA    R1,DAUS99RC
         ST    R1,DFRCP            PTR TO S99RC
         LA    R1,DAUF02EP
         ST    R1,DFJEFF02         PTR TO EP
         LA    R1,DAUID
         ST    R1,DFIDP            PTR TO SW+ID
         ST    R7,DFCPPLP          PTR TO CPPL
         DROP  R2
         MVC   DAUID,=XL2'0032'
         L     R2,PDSN             R2 POINTS TO DSN
         LH    R3,PDSN+4           R3 IS DSN LENGTH
         BCT   R3,*+L'*+6          R3-1 FOR EXEC
         MVC   DAWDSNAM(*-*),0(R2) MOVE DSN TO T.U.
         EX    R3,*-6              EXECUTE MOVE
         LA    R1,DAWRBP           DO DYNAMIC ALLOCATION
        DYNALLOC ,
         LTR   R15,R15             TEST RC
         BZ    DAWOK               BRANCH IF GOOD
         ST    R15,DAUS99RC        SAVE RC FROM SVC 99
         LA    R1,DAUDFPRM         POINT TO IKJEFF18 PARM
        LINK   EP=IKJEFF18         GO TO IKJEFF18 (DAIRFAIL)
         LA    R8,20               SET RETURN CODE ----------------- 20
MEABORT  OI    SWITCH,SWAB         TURN ON ABORT SW
         B     MALIST              GO TO ERROR
DAWOK    TM    DAWRTORG,DCBDSGPO   IS DSORG PO/POU ?
         BO    *+L'*+8             BRANCH IF YES
         LA    R8,12               SET RETURN CODE ----------------- 12
         B     MEABORT             GO TO ABORT
         MVC   INDCB(DCBMODLL),DCBMODEL INITIALIZE DCB
         LA    R2,INDCB            R3 POINTS TO IN DCB
         USING IHADCB,R2           ADDRESSABILITY
         MVC   DCBDDNAM,DAWRTDDN   FILL IN DDNAME
         MVC   OPENLIST(4),OPENLI  OPTIONS TO PARM LIST
         STCM  R2,B'0111',OPENLIST+1 INTO PARM LIST
        OPEN  MF=(E,OPENLIST)      OPEN THE DATA-SET
         TM    DCBOFLGS,DCBOFOPN   OPEN SUCCESSFULL ?
         BO    *+L'*+12            BRANCH IF YES
         ST    R15,ERRCDE          SAVE RETURN CODE
         LA    R8,24               SET RETURN CODE ----------------- 24
         B     MEABORT             GO TO ABORT
         DROP  R2                  DROP ADDRESSABILITY
         MVC   BLDLLIST(BLDLDATL),BLDLDAT INIT BLDL LIST
         L     R2,PDSN+8           R2 POINT TO MEM NAME
         LH    R3,PDSN+12          R3 IS MEM NAME LEN
         BCT   R3,*+L'*+6          R3-1 FOR EXEC
         MVC   BLDLLIST+4(*-*),0(R2) NAME TO BLDL LIST
         EX    R3,*-6              EXECUTE MOVE
        BLDL   INDCB,BLDLLIST      DO BLDL
         LR    R3,R15              SAVE BLDL RETURN CODE
         LA    R2,INDCB            ADDRESS OF INPUT DCB
         MVC   CLOSLIST(4),CLOSLI  INITIALIZE PARM LIST
        CLOSE  ((R2)),MF=(E,CLOSLIST) CLOSE THE DATA-SET
         LR    R4,R15              SAVE CLOSE RETURN CODE
         LA    R1,DUWRBP           LINK CONTROL BLOCKS
         USING S99RBP,R1
         LA    R2,DUWRB
         ST    R2,S99RBPTR
         OI    S99RBPTR,S99RBPND
         DROP  R1
         USING S99RB,R2
         MVI   S99RBLN,S99RBEND-S99RB
         MVI   S99VERB,S99VRBUN
         MVI   S99FLG11,S99ONCNV+S99NOMNT
         MVI   S99FLG12,0
         XC    S99ERROR,S99ERROR
         XC    S99INFO,S99INFO
         LA    R1,DUWTUPL
         ST    R1,S99TXTPP
         XC    S99S99X,S99S99X     (OLD S99RSV01 FIELD)            -EU-
         XC    S99FLAG2,S99FLAG2
         DROP  R2
         USING S99TUNIT,R1
         LA    R1,DUWTUN01
         LA    R2,DUNDDNAM         DDNAME
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'8'
         MVC   S99TUPAR(8),=CL8' '
         ST    R1,DUWTUP01
         OI    DUWTUP01,S99TUPLN
         DROP  R1
         LA    R2,DAUDFPRM
         USING DFDSECTD,R2
         MVC   DFS99RBP,DUWRBP     PTR TO RB
         LA    R1,DAUS99RC
         ST    R1,DFRCP            PTR TO S99RC
         LA    R1,DAUF02EP
         ST    R1,DFJEFF02         PTR TO EP
         LA    R1,DAUID
         ST    R1,DFIDP            PTR TO SW+ID
         ST    R7,DFCPPLP          PTR TO CPPL
         XC    DFBUFP,DFBUFP       ZERO BUFFER ADDRESS
         DROP  R2
         XC    DAUF02EP,DAUF02EP
         MVC   DAUID,=XL2'0032'
         MVC   DUWDDNAM(8),DAWRTDDN SET DDNAME
         LA    R1,DUWRBP           DO DYNAMIC UNALLOCATION
        DYNALLOC ,
         LTR   R3,R3               TEST BLDL RETURN CODE
         BZ    *+L'*+12            BRANCH IF GOOD
         ST    R3,ERRCDE           SAVE RETURN CODE
         LA    R8,4                SET RETURN CODE ------------------ 4
         B     MALIST              GO TO ERROR
         LTR   R4,R4               TEST CLOSE RETURN CODE
         BZ    *+L'*+12            BRANCH IF GOOD
         ST    R4,ERRCDE           SAVE RETURN CODE
         LA    R8,28               SET RETURN CODE ----------------- 28
         B     MALIST              GO TO ERROR
         LTR   R15,R15             TEST RC
         BZ    MALIST              BRANCH IF GOOD
         ST    R15,DAUS99RC        SAVE RC FROM SVC 99
         LA    R1,DAUDFPRM         POINT TO IKJEFF18 PARM
        LINK   EP=IKJEFF18         GO TO IKJEFF18 (DAIRFAIL)
         LA    R8,32               SET RETURN CODE ----------------- 32
         SPACE 1
*------- LIST DSN AND VOLUME, OR ERROR MESSAGE
         SPACE 1
MALIST   CLC   KLIST(2),=H'1'      IF 'LIST' OPERAND NOT USED
         BNE   LEAVE               GO TO END
         MVI   MSGAREA,C' '        CLEAR MESSAGE AREA
         MVC   MSGAREA+1(L'MSGAREA-1),MSGAREA
         LH    R2,DSNARGLN         R2 IS LENGTH OF DSNARG
         B     *+L'*+6             BRANCH AROUND MOVE
         MVC   MSGAREA(*-*),DSNARG MOVE DSN PLUS TRAILING BLANK
         EX    R2,*-6              EXECUTE THE MOVE
         LA    R2,MSGAREA+1(R2)    R2 POINTS TO NEXT PLACE IN MSG
         CH    R8,=H'8'            IS CIR R/C ZERO ?
         BE    NOTFND              NO, THUS CATALOG FAILS
         SPACE 1
*------- CONSTRUCT 'CATALOGED' MESSAGE
         SPACE 1
         MVC   0(3,R2),=CL3'ON '   'ON ' TO MSG
         LA    R2,3(,R2)           INCR POINTER
         MVC   0(6,R2),VOLSER      VOLSER TO MSG LINE
         LA    R2,6(,R2)           INCR POINTER
         TM    PDSN+14,X'80'       IF NO MEMBER NAME
         BZ    SNDMSG              GO TO END
         MVC   0(2,R2),=CL2', '    ', ' TO MSG
         LA    R2,2(,R2)           INCR POINTER
         L     R1,PDSN+8           R1 POINTS TO MEMNAME
         LH    R3,PDSN+12          R3 IS LEN OF MEMNAME
         BCT   R3,*+L'*+6          R3-1 FOR EXEC
         MVC   0(*-*,R2),0(R1)     MEMNAME TO LINE
         EX    R3,*-6              EXECUTE THE MOVE
         LA    R2,1(R3,R2)         INCR POINTER
         TM    SWITCH,SWAB         IS MEMBER SEARCH ABORTED ?
         BZ    *+L'*+10            BRANCH IF NOT
         MVC   0(12,R2),=CL12' NOT CHECKED'
         B     SNDMSG
         CH    R8,=H'4'            WAS BLDL BAD ?
         BNE   *+L'*+10            BRANCH IF NOT
         MVC   0(10,R2),=CL10' NOT FOUND'
         B     SNDMSG
         MVC   0(12,R2),=CL12' IS A MEMBER'
         B     SNDMSG
         SPACE 1
*------- CONSTRUCT 'NOT FOUND' MESSAGE
         SPACE 1
NOTFND   MVC   0(20,R2),=CL20'NOT FOUND IN CATALOG'
SNDMSG   LA    R0,L'MSGAREA        MESSAGE AREA LENGTH
         LA    R1,MSGAREA          POINT TO MESSAGE AREA
         BAS   R14,PUTLNE          WRITE THE MESSAGE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     *+L'*+4             +0 - OK
         B     LEAVE               +4 - ERROR
         LTR   R8,R8               EVERY THING GOOD ?
         BZ    LEAVE               BRANCH IF YES
         SPACE 1
*------- CONSTRUCT DIAGNOSE MESSAGE
         SPACE 1
         MVI   MSGAREA,C' '        CLEAR MESSAGE AREA
         MVC   MSGAREA+1(L'MSGAREA-1),MSGAREA
         MVC   MSGAREA(15),=CL15'===> DIAGNOSE :'
         CH    R8,=H'4'
         BNE   TRC8
         CLC   ERRCDE,=F'4'         IS BLDL MEMBER NOT FOUND ?
         BE    LEAVE                BRANCH IF YES
         MVC   MSGAREA+16(4),=CL4'BLDL'
         LA    R2,MSGAREA+21
         B     SNDDIAG
TRC8     CH    R8,=H'8'
         BNE   TRC24
         CLC   ERRCDE,=F'4'         IS IKJEHCIR NOT FOUND ?
         BE    LEAVE                BRANCH IF YES
         MVC   MSGAREA+16(8),=CL8'IKJEHCIR'
         LA    R2,MSGAREA+25
         B     SNDDIAG
TRC24    CH    R8,=H'24'
         BNE   TRC28
         MVC   MSGAREA+16(4),=CL4'OPEN'
         LA    R2,MSGAREA+21
         B     SNDDIAG
TRC28    CH    R8,=H'28'
         BNE   LEAVE               OTHER, DIAGNOSE ALREADY DONE
         MVC   MSGAREA+16(5),=CL5'CLOSE'
         LA    R2,MSGAREA+22
SNDDIAG  MVC   0(12,R2),=CL12'- R15 (RC) ='
         UNPK  13(9,R2),ERRCDE(5)
         MVI   21(R2),C' '
         LA    R0,L'MSGAREA        MESSAGE AREA LENGTH
         LA    R1,MSGAREA          POINT TO MESSAGE AREA
         BAS   R14,PUTLNE          WRITE THE MESSAGE
         B     LEAVE
         SPACE 1
*------- TERMINATION
         SPACE 1
PARSERR  LA    R8,16               YES, SET RETURN CODE ------------ 16
LEAVE   IKJRLSA #TSANSW            RELEASE SPACE
         B     QUIT
SETRC    LA    R8,36               SET RETRUN CODE ----------------- 36
QUIT    $XRET  CC=(R8),LV=WKLEN,TYPE=RENT
         EJECT
*------- DATA CONSTANTS
         SPACE 1
         PRINT NOGEN
DCBMODEL DCB   DSORG=PO,MACRF=R,DDNAME=XXXXXXXX
DCBMODLL EQU   *-DCBMODEL
OPENLI  OPEN   (,(INPUT)),MF=L
CLOSLI  CLOSE  (,),MF=L
         PRINT GEN
         SPACE 1
CIRDATA  DS    0D                  CONSTANTS FOR CIR PARM
         DC    XL1'04'             OPTIONS = GET VOLUME
         DS    XL2'0'              RESERVED
         DC    XL1'0'              LOCATE RETURN CODE
         DC    A(*-*)              ADDRESS OF SEARCH ARGUMENT
         DC    A(*-*)              ADDR OF VOLID OF CVOL
         DC    A(*-*)              ADDR OF WORK AREA
         DC    A(*-*)              ADDR OF SAVE AREA
         DC    A(*-*)              ADDR OF PASSWORD
CIRDATAL EQU   *-CIRDATA           LENGTH OF PARM BLOCK
         SPACE 1
BLDLDAT  DS    0F
BLDLFF   DC    H'1'                1 ENTRY
BLDLLL   DC    H'12'               SHORT FORMAT
BLDLNAME DC    CL8' '              NAME
BLDLTTRK DC    XL4'0'              TT, R, K
BLDLDATL EQU   *-BLDLDAT
         SPACE 1
        LTORG
         SPACE 1
         PRINT NOGEN
PARMLST  IKJPARM
PDSN     IKJPOSIT DSNAME,USID,PROMPT='NAME OF DATA-SET TO BE LOCATED'
KLIST    IKJKEYWD DEFAULT='LIST'
         IKJNAME  'LIST'
         IKJNAME  'NOLIST'
         IKJENDP
         PRINT GEN
         EJECT
*------- DATA WORK-AREA
         SPACE 1
WKAREA   DSECT
         DS    18F                 REGISTER SAVE AREA
CIRRSA   DS    18F                 CIR REGISTER SAVE AREA
         SPACE 1
CIRPARM  DS    0D
CIROPT   DS    XL1                 OPTIONS = GET VOLUME
         DS    XL2                 RESERVED
CIRLOCRC DS    XL1                 LOCATE RETURN CODE
CIRSRCH  DS    A                   ADDRESS OF SEARCH ARGUMENT
CIRCVOL  DS    A                   ADDR OF VOLID OF CVOL
CIRWA    DS    A                   ADDR OF WORK AREA
CIRSAVE  DS    A                   ADDR OF SAVE AREA
CIRPSWD  DS    A                   ADDR OF PASSWORD
         SPACE 1
DAWRBP   DS    F                   REQUEST BLOCKS FOR SVC 99
DAWRB    DS    0F,((((S99RBEND-S99RB)+3)/4)*4)X
DAWTUPL  DS    0F                  TEXT UNIT POINTER LIST
DAWTUP02 DS    F
DAWTUP04 DS    F
DAWTUP55 DS    F
DAWTUP57 DS    F
DAWTUN02 DS    3H                  TEXT UNITS
DAWDSNAM DS    CL44                DATA-SET NAME
DAWTUN04 DS    3H
DAWSTATS DS    XL1                 STATUS
DAWTUN55 DS    3H
DAWRTDDN DS    CL8                 RETURNED DD-NAME
DAWTUN57 DS    3H
DAWRTORG DS    XL2                 RETURNED DSORG
         SPACE 1
DAUDFPRM DS    0F,(DFLEN)X         PARM BLOCK FOR IKJEFF18
DAUS99RC DS    F                   AREA FOR SVC 99 R/C
DAUF02EP DS    F                   AREA FOR EP OF IKJEFF18
DAUID    DS    XL2                 DAIRFAIL SWITCHES & CALLER ID
         SPACE 1
DUWRBP   DS    F                   REQUEST BLOCKS FOR SVC 99
DUWRB    DS    0F,((((S99RBEND-S99RB)+3)/4)*4)X
DUWTUPL  DS    0F                  TEXT UNIT POINTER LIST
DUWTUP01 DS    F
DUWTUN01 DS    3H                  TEXT UNITS
DUWDDNAM DS    CL8                 DD-NAME
         SPACE 1
INDCB    DS    0F,(DCBMODLL)X
OPENLIST DS    F                   OPEN PARM LIST
CLOSLIST DS    F                   CLOSE PARM LIST
         SPACE 1
        $TEW$WA
         SPACE 1
BLDLLIST DS    0F,(BLDLDATL)X      BLDL ENTRY LIST
         SPACE 1
ERRCDE   DS    F                   ERROR RETURN CODE (R15)
MSGAREA  DS    CL80                OUTPUT MESSAGE LINE
         SPACE 1
DSNARGLN DS    H                   LENGTH OF DSN
DSNARG   DS    CL45                DSN PLUS TRAILING SPACE
         SPACE 1
CIRWORK  DS    0F                  CIR WORK AREA
AREALN   DS    H                   LENGTH OF AREA
         DS    H                   LENGTH OF RETURNED DATA
VDATA    DS    0H                  RETURNED VARIABLE DATA
         DS    XL1                 NUMBER OF VOLUMES
VOLDATA  DS    0XL12               RETURNED VOLUME INFORMATION
         DS    XL4                 DEVICE TYPE
VOLSER   DS    CL6                 VOLUME SERIAL NUMBER
         DS    XL2                 FILE SEQUENCE NUMBER (TAPE)
         DS    19XL(L'VOLDATA)
         DS    XL1                 RESERVED (CONTAINS X'FF')
         DS    XL9                 (UNUSED)
VDATAL   EQU   *-VDATA
         SPACE 1
SWITCH   DS    XL1                 SWITCHES
SWAB     EQU   X'80'               NO CHECK MEM NAME
         SPACE 1
WKLEN    EQU   (((*-WKAREA)+7)/8)*8     LENGTH OF WKAREA DATA AREA
         SPACE 1
*------- MAPPING MACROS
         SPACE 1
         PRINT NOGEN
        $TEW$DS
        IEFZB4D0 ,                 MAPS SVC-99 BLOCKS
        IEFZB4D2
        IKJEFFDF DFDSECT=YES,DFDSEC2=YES MAPS IKJEFF18 BLK
        DCBD   DSORG=PS,DEVD=DA    MAPS THE DCB FOR DA
         SPACE 1
         END
