COMP     TITLE '--- E T P S -- ETPSCOMP - COMPRESS LIBRARY ---'
         PRINT OFF
         COPY  ETPS$O1             "ETPS" SYSPARM SELECTION
         COPY  ETPSMACS            "ETPS" MACROS
         COPY  ETPSDEFS            "ETPS" DEFINITIONS
         PRINT ON
ETPSCOMP START 0
         SPACE 1
ETPSCOMP AMODE 24
ETPSCOMP RMODE 24
        MIDENT
         EJECT
*------- AT ENTRY, R1 = A(PARM.LIST) -> (DS-NAME,
*                                        VOL-SER,
*                                        PASSWORD)
         SPACE 1
         PRINT GEN
         USING *,R12
         USING SAVE2,R13      ************************** R13 ==> SAVE2
         USING SPLTAREA,R10   AT ENTRY : R10 MUST POINT TO "SPLTAREA"
         B     32(,R15)
         DC    CL28'  ETPSCOMP &SYSDATE &SYSTIME'
         PRINT &PRF
         STM   R14,R12,12(R13)     SAVE INPUT REGISTERS
         LR    R12,R15             SET PROGRAM BASE REGISTER
         LR    R3,R10              SPLIT-SCREEN DSECT
         SH    R3,=Y(SPLITWRK-SAVE2) POINT TO SAVE2
         ST    R13,4(,R3)          STORE BACKWARD POINTER
         ST    R3,8(R13)           STORE FORWARD POINTER
         LR    R13,R3              SET DSECT BASE
         LM    R3,R5,0(R1)         GET PARAMETERS ADDRESSES
         L     R9,=A(WKLEN)
        GETMAIN RC,LV=(R9)
         LTR   R15,R15             HOW COMPLETE?
         BZ    *+L'*+10            OK
         LR    R5,R15              ERROR, RETAIN GETMAIN RC
         LA    R4,98               SET GETMAIN ERROR RETURN CODE
         B     GOBACK
         LR    R0,R1
         LR    R1,R9
         LR    R9,R0
         LA    R14,*
         XR    R15,R15
         MVCL  R0,R14              CLEAR ALL TO BINARY ZEROS
         USING WKAREA,R9
         MVC   INDDIN(SKLTUL),SKLTU
         MVC   INDS(L'INDS),0(R3)  R3 = INPUT DSN
         MVC   INV(L'INV),0(R4)    R4 = INPUT VOLSER
         MVC   INP(L'INP),0(R5)    R5 = INPUT PASSWORD
         MVC   DDIN,N#IN           SET ALL NEEDED DDNAMES
         MVC   INDD,N#UT1
         MVC   DDUT3,N#UT3
         MVC   DDUT4,N#UT4
         L     R1,ACOPDCB          SET DDNAME FOR SYSPRINT
         MVC   DDOUT(L'DDOUT),DCBDDNAM-IHADCB(R1)
         LA    R1,N#IN+L'N#IN-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   STSFX(L'STSFX),0(R1)
         XR    R3,R3               AUTH-SWITCH USE
         LR    R4,R3               DYNALLOC RC / R15
         LR    R5,R3               DYNALLOC REASON-INFO CODES / R0
         LR    R6,R3               IEBCOPY RC / R15
         SPACE 1
         MVC   RB99TU(SKFLEN),SKF  SET FREE LIST
         LA    R0,INDDUT1          FREE ANY SYSUT1
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
         LA    R0,INDDUT3          FREE ANY SYSUT3
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
         LA    R0,INDDUT4          FREE ANY SYSUT4
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
         LA    R0,INDDIN           FREE ANY SYSIN
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
         LA    R0,OUDDNAME         FREE ANY SYSPRINT
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
         SPACE 1
         MVC   RBLBTU(SKLLEN),SKL ALLOCATE LIBRARY AS SYSUT1
         LA    R0,INDDUT1
         ST    R0,RBLBTU
         LA    R0,INDSNAME
         ST    R0,INNADDR
         OI    INV,C' '
         CLI   INV,C' '
         BE    *+L'*+8
         LA    R0,INVOLSER
         ST    R0,INVADDR
         OI    INP,C' '
         CLI   INP,C' '
         BE    *+L'*+8
         LA    R0,INPASS
         ST    R0,INPADDR
         LA    R0,RTNDSORG
         O     R0,INDSORG
         ST    R0,INDSORG
         XC    DSORG,DSORG
         LA    R1,RBLBTU
         BAL   R8,DYNALL
         B     ERRUT1              +0 - ERROR RETURN
         CLC   DSORG(L'DSORG),=AL1(DCBDSGPO,0)
         BE    *+L'*+8
         LA    R4,99               NOT A PARTITIONED
         B     ERRUT1
         MVC   RBLBTU(SKUT3L),SKUT3 ALLOCATE SYSUT3
         LA    R0,INDDUT3
         ST    R0,RBLBTU
         LA    R1,RBLBTU
         BAL   R8,DYNALL
         B     ERRUT3              +0 - ERROR RETURN
         MVC   RBLBTU(SKUT4L),SKUT4 ALLOCATE SYSUT4
         LA    R0,INDDUT4
         ST    R0,RBLBTU
         LA    R1,RBLBTU
         BAL   R8,DYNALL
         B     ERRUT4              +0 - ERROR RETURN
         MVC   RBLBTU(SKINL),SKIN  ALLOCATE SYSIN (NULLFILE)
         LA    R0,INDDIN
         ST    R0,RBLBTU
         LA    R1,RBLBTU
         BAL   R8,DYNALL
         B     ERRIN               +0 - ERROR RETURN
         MVC   RBOUTU(SKPLEN),SKP  ALLOCATE SYSPRINT
         LA    R0,OUDDNAME
         ST    R0,RBOUTU
         LA    R0,OUDSNAME
         ST    R0,OUDSNM
         LA    R0,OURTDS
         O     R0,OURDSNM
         ST    R0,OURDSNM
         LA    R1,RBOUTU
         BAL   R8,DYNALL
         B     ERROUT              +0 - ERROR RETURN
        TESTAUTH FCTN=1            AUTHORIZATION STATUS
         LTR   R3,R15
         BZ    DOLINK
         LA    R0,1                SET AUTH ON
         SVC   235
DOLINK   MVC   PARM1(L'PARM1SK+OPTL),PARM1SK
         MVC   PARM2,=AL2(DDAL)
         MVC   PIN,DDIN
         MVC   PPRT,DDOUT
         MVC   PUT1,INDD
         MVC   PUT2,INDD
         MVC   PUT3,DDUT3
         MVC   PUT4,DDUT4
         LA    R0,PARM1
         ST    R0,PARMLIST
         LA    R0,PARM2
         ST    R0,PARMLIST+4
         OI    PARMLIST+4,VLB      SET "VL" BIT
         LA    R1,PARMLIST
        LINK   EP=IEBCOPY
         LR    R6,R15              PICK UP RETURN CODE
         XR    R5,R5
         LTR   R3,R3
         BZ    ALLDONE
         SR    R0,0                SET AUTH OFF
         SVC   235
         SPACE 1
ALLDONE  LTR   R6,R6               IEBCOPY DONE SUCESSFULL?
         BNZ   ERROUT              NO, SKIP FREE (DELETE LATER)
         MVC   RB99TU(SKFLEN),SKF  SET FREE LIST
         LA    R0,OUDDNAME         FREE SYSPRINT
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
ERROUT   MVC   RB99TU(SKFLEN),SKF  SET FREE LIST
         LA    R0,INDDIN           FREE SYSIN
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
ERRIN    MVC   RB99TU(SKFLEN),SKF  SET FREE LIST
         LA    R0,INDDUT4          FREE SYSUT4
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
ERRUT4   MVC   RB99TU(SKFLEN),SKF  SET FREE LIST
         LA    R0,INDDUT3          FREE SYSUT3
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
ERRUT3   MVC   RB99TU(SKFLEN),SKF  SET FREE LIST
         LA    R0,INDDUT1          FREE SYSUT1
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
         SPACE 1
ERRUT1   LTR   R6,R6               IEBCOPY DONE SUCCESSFULL?
         BZ    LEAVE               YES
         LR    R4,R6               NO, BROWSE SYSPRINT
         L     R3,ACOPDCB
         LA    R2,OURDS
         LA    R6,=CL8' '
         ICM   R15,B'1111',ARBROWSE
         BNZ   CALLBR
        LOAD   EP=ETPSBROW,ERRET=XLDERR
         ST    R0,ARBROWSE
         LR    R15,R0
*- - - - ETPSBROW PARM.LIST : DCB, DS-NAME, MEMBER-NAME
CALLBR  CALL   (15),((R3),(R2),(R6)),VL,MF=(E,PARMVL)
QUIT     MVC   RB99TU(SKFLEN),SKF  SET FREELIST
         LA    R0,OUDDNAME         FREE SYSPRINT NOW
         ST    R0,RB99TU
         LA    R1,RB99TU
         BAL   R8,UNALL
LEAVE    LR    R1,R9
         L     R0,=A(WKLEN)
        FREEMAIN R,LV=(0),A=(1)
GOBACK   LA    R1,OKMSG
         LTR   R5,R5
         BZ    SETMSG
         ST    R4,MESSPL           ERROR
        XMESS  1401,R15
         LA    R1,NPOMSG
         CH    R4,=H'99'
         BE    SETMSG
         LA    R1,GEMSG
         CH    R4,=H'98'
         BE    SETMSG
         LA    R1,BADMSG
SETMSG   ST    R1,MSGADD
         L     R13,4(R13)          PICK UP CALLING SAVE-AREA
         LM    R14,R12,12(R13)     RESTORE CALLING REGISTERS
         XR    R15,R15             SET RC=0
         BR    R14                 RETURN
XLDERR   STM   R0,R1,MESSPL        BROWSE IS UNAVAILABLE
        XMESS  99,R15
         B     QUIT
         SPACE 1
UNALL    LA    R0,S99VRBUN         UNALL ROUTINE --------------------*
         B     DYNALL+L'DYNALL
DYNALL   LA    R0,S99VRBAL         DYNALL ROUTINE -------------------*
         MVC   RB99P(SKRLEN),SKR
         STC   R0,RB99VR
         ST    R1,RB99TUP
         LA    R0,RB99
         O     R0,RB99P
         ST    R0,RB99P
         LA    R1,RB99P
        DYNALLOC
         CLI   RB99VR,S99VRBAL     DYNALL?
         BNER  R8                  NO, ALWAYS RETURN
         L     R5,RB99RC           YES, CHECK ANY ERROR
         LTR   R4,R15
         BNZR  R8                  ERROR, RETURN +0
         LTR   R5,R5
         BNZR  R8                  ERROR, RETURN +0
         B     4(R8)               NORMAL, RETURN +4
         EJECT
SKR      DC    0F'0',AL1(S99RBPND),AL3(*-*)
SKRP     DC    AL1(S99RBEND-S99RB)
         DC    AL1(*-*)            VERB CODE
         DC    AL1(0,0)            FLAGS
         DC    2XL2'0'             ERROR-INFORMATION REASON CODES
         DC    A(*-*)
SKRL     EQU   (S99RBEND-S99RB)-(*-SKRP)
         DC    (SKRL)X'0'
SKRLEN   EQU   *-SKR
         SPACE 1
SKF      DC    A(*-*)
         DC    AL1(S99TUPLN),AL3(UNCOND)
SKFLEN   EQU   *-SKF
         SPACE 1
SKL      DC    A(*-*)
         DC    A(*-*)
         DC    A(*-*)
         DC    A(*-*)
         DC    A(DISPSHR)
         DC    A(DISPKEEP)
         DC    AL1(S99TUPLN),AL3(*-*)
SKLLEN   EQU   *-SKL
         SPACE 1
SKP      DC    A(*-*)
         DC    A(*-*)
         DC    A(UNIT)
         AIF   ('&CSPUN' NE 'TRK').SK1
         DC    A(TRKS)
         AGO   .SK2
.SK1     DC    A(CYLS)
.SK2     DC    A(PRIMRY)
         DC    A(SECNRY)
         DC    A(RECFM)
         DC    A(BLKSZ)
         DC    A(LRECL)
         DC    A(DISPNEW)
         DC    A(DISPDEL)
         DC    A(DISPCDEL)
         DC    AL1(S99TUPLN),AL3(*-*)
SKPLEN   EQU   *-SKP
         SPACE 1
SKUT3    DC    A(*-*)
         DC    A(CYLS)
         DC    A(PRIMRY)
         DC    A(SECNRY)
         DC    AL1(S99TUPLN),AL3(DISPNEW)
SKUT3L   EQU   *-SKUT3
         SPACE 1
SKUT4    DC    A(*-*)
         DC    A(CYLS)
         DC    A(PRIMRY)
         DC    A(SECNRY)
         DC    A(KEYLEN)
         DC    AL1(S99TUPLN),AL3(DISPNEW)
SKUT4L   EQU   *-SKUT4
         SPACE 1
SKIN     DC    A(*-*)
         DC    AL1(S99TUPLN),AL3(DUMMY)
SKINL    EQU   *-SKIN
         SPACE 1
SKLTU    DC    AL2(DALDDNAM),AL2(1),AL2(8)   OR (DUNDDNAM)
         DC    CL8' '
         DC    AL2(DALDDNAM),AL2(1),AL2(8)   OR (DUNDDNAM)
         DC    CL8' '
         DC    AL2(DALDDNAM),AL2(1),AL2(8)  OR (DUNDDNAM)
         DC    CL8' '
         DC    AL2(DALDDNAM),AL2(1),AL2(8)  OR (DUNDDNAM)
         DC    CL8' '
         DC    AL2(DALDSNAM),AL2(1),AL2(44)
         DC    CL44' '
         DC    AL2(DALVLSER),AL2(1),AL2(6)
         DC    CL6' '
         DC    AL2(DALPASSW),AL2(1),AL2(8)
         DC    CL8' '
         DC    AL2(DALRTORG),AL2(1),AL2(2)
         DC    XL2'0'
         DC    AL2(DALDDNAM),AL2(1),AL2(8) OR (DUNDDNAM)
         DC    CL8' '
         DC    AL2(DALDSNAM),AL2(1),AL2(44)
         DC    CL44'&&&&LIST '
         DC    AL2(DALRTDSN),AL2(1),AL2(44)
         DC    CL44' '
SKLTUL   EQU   *-SKLTU
         SPACE 1
UNIT     DC    AL2(DALUNIT),AL2(1),AL2(8),CL8'&CUNME'
PRIMRY   DC    AL2(DALPRIME),AL2(1),AL2(3),AL3(&CPRIQ)
SECNRY   DC    AL2(DALSECND),AL2(1),AL2(3),AL3(&CSECQ)
BLKSZ    DC    AL2(DALBLKSZ),AL2(1),AL2(2),AL2(&CBLSZ)
LRECL    DC    AL2(DALLRECL),AL2(1),AL2(2),AL2(&CLRCL)
RECFM    DC    AL2(DALRECFM),AL2(1),AL2(1)
        SETRF  &CRCFM
KEYLEN   DC    AL2(DALKYLEN),AL2(1),AL2(1),AL1(8)
DISPNEW  DC    AL2(DALSTATS),AL2(1),AL2(1),XL1'04'
DISPSHR  DC    AL2(DALSTATS),AL2(1),AL2(1),XL1'08'
DISPDEL  DC    AL2(DALNDISP),AL2(1),AL2(1),XL1'04'
DISPKEEP DC    AL2(DALNDISP),AL2(1),AL2(1),XL1'08'
DISPCDEL DC    AL2(DALCDISP),AL2(1),AL2(1),XL1'04'
CYLS     DC    AL2(DALCYL),AL2(0)
         AIF   ('&CSPUN' NE 'TRK').SK3
TRKS     DC    AL2(DALTRK),AL2(0)
.SK3     ANOP
DUMMY    DC    AL2(DALDUMMY),AL2(0)
UNCOND   DC    AL2(DUNUNALC),AL2(0)
         SPACE 1
PARM1SK  DC    AL2(OPTL)
OPT      DC    C'SIZE=100K'
OPTL     EQU   *-OPT
         SPACE 1
OKMSG    MSG   'COMPRESS SUCCESSFUL'
NPOMSG   MSG   'NOT PARTITIONED'
GEMSG    MSG   'GETMAIN ERROR'
BADMSG   MSG   'COMPRESS FAILED'
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         DROP  R9,R10,R12,R13
         EJECT
*------- LOCAL WORK-AREA --------------------------------------------*
WKAREA   DSECT
RB99P    DS    A
RB99     DS    AL1
RB99VR   DS    AL1                 VERB CODE
         DS    AL2                 FLAGS
RB99RC   DS    XL4                 ERROR-INFORMATION REASON CODES
RB99TUP  DS    A
RB99L    EQU   (S99RBEND-S99RB)-(*-RB99)
         DS    (RB99L)X
RB99TU   DS    13A
         ORG   RB99TU
RBLBTU   DS    A
INNADDR  DS    A
INVADDR  DS    A
INPADDR  DS    A
         DS    2A
INDSORG  DS    A
         ORG   RB99TU
RBOUTU   DS    A
OUDSNM   DS    A
         DS    10A
OURDSNM  DS    A
         ORG   ,                   "SKLTU" MOVED BELOW
INDDIN   DS    3AL2
DDIN     DS    CL8
INDDUT1  DS    3AL2
INDD     DS    CL8
INDDUT3  DS    3AL2
DDUT3    DS    CL8
INDDUT4  DS    3AL2
DDUT4    DS    CL8
INDSNAME DS    3AL2
INDS     DS    CL44
INVOLSER DS    3AL2
INV      DS    CL6
INPASS   DS    3AL2
INP      DS    CL8
RTNDSORG DS    3AL2
DSORG    DS    XL2
OUDDNAME DS    3AL2
DDOUT    DS    CL8
OUDSNAME DS    3AL2
OUDS     DS    CL44
STSFX    EQU   OUDS+6,1
OURTDS   DS    3AL2
OURDS    DS    CL44
PARMLIST DS    2F                  IEBCOPY PARM.LIST
         DS    H
PARM1    DS    H
         DS    CL8
         DS    H
PARM2    DS    H
         DS    4XL8
PIN      DS    XL8                  5 - SYSIN
PPRT     DS    XL8                  6 - SYSPRINT
         DS    XL8
PUT1     DS    XL8                  8 - SYSUT1
PUT2     DS    XL8                  9 - SYSUT2
PUT3     DS    XL8                 10 - SYSUT3
PUT4     DS    XL8                 11 - SYSUT4
DDAL     EQU   (*-PARM2)-L'PARM2
WKLEN    EQU   (((*-WKAREA)+7)/8)*8
         SPACE 1
         PRINT &PRS
        SPLTAREA
         SPACE 1
        MYSAVE
         PRINT &PRF
        IEFZB4D0
        IEFZB4D2
        DCBD   DSORG=(PS,PO),DEVD=DA
        IEZIOB
         PRINT GEN
         END
