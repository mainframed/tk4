DSNT     TITLE 'DSNTAB -- DSNAME(S) OF A DDNAME.'
DSNTAB   START  0
         SPACE 1
* ORIGIN :     EXTRACTED FROM TAPE CBT 89 FEB, FILE 270.
*              DATA-SET 761KKDSNTB AT LEVEL 001 AS OF 08/30/82
*              WRITTEN BY KERMIT KISER
*              WASHINGTON STATE DP SERVICE CENTER (WDPSC)
*              ADAPTED BY MOINIL P.A.
*              ATTRIBUTES : RE-ENTERABLE
* PURPOSE :    SUBROUTINE TO PASS BACK LIST OF ALL DSNAMES CONCATENATED
*              TO A GIVEN DDNAME. THIS IS A SUBROUTINE WHICH ACCEPTS A
*              A DDNAME AND PASSES PASSES BACK TO THE INVOKER A LIST OF
*              DSNAMES CONCATENATED TO THAT DDNAME. THE MAXIMUM NUMBER
*              OF DATA-SETS CONCATENATED TO A GIVEN DDNAME WHICH CAN
*              BE PASSED BACK BY THE ROUTINE DEPENDS ON THE SIZE OF THE
*              PASSED ARRAY TABLE. THE ROUTINE WILL DETERMINE WHETHER
*              THE DDNAME PASSED IS ALLOCATED. IT IT IS ALLOCATED, THEN
*              IT PASS BACK ALL OF THE ASSOCIATED DSNAMES (AND THEIR
*              LENGTHS). IF THE DDNAME IS ALLOCATED AND THERE IS NO
*              OVERFLOW OF THE CONCATENATED DATA-SETS NAMES TABLE, AT
*              END, THE TABLE IS LOADED WITH ALL THE DSNAMES WHICH ARE
*              ASSOCIATED WITH THE DDNAME AND THE NUMBER OF SIGNIFICANT
*              ENTRIES IN THAT TABLE IS STORED. IF THE DDNAME IS NOT
*              ALLOCATED, THE ROUTINE EXITS WITH RC=4, AND IF THERE
*              IS AN OVERFLOW OF THE DATA-SETS NAMES TABLE, IT EXITS
*              WITH RC=8.
*
* TO CALL THIS ROUTINE FROM A COBOL PROGRAM, DO THE FOLLOWING :
*              1. PLACE THE FOLLOWING DATA AREAS IN WORKING STORAGE :
*                 01  THE-DDNAME                        PIC X(8).
*                 01  DSNAME-TABLE.
*                     05  DSNAME-ELEMENT OCCURS N1 TIMES.
*                         10  FILLER                    PIC X(2).
*                         10  THE-DSNAME                PIC X(44).
*                 01  TABLSIZE      VALUE +N2           PIC S9(4) COMP.
*                 01  NO-OF-SIGNIF-DSNAMES              PIC S9(4) COMP.
*                 WHERE : N1 IS THE MAXIMUM DESIRED NUMBER OF DATA-SETS
*                         NAMES, AND N2 IS THE MULTIPLICATION RESULT
*                         OF N1 BY THE LENGTH OF A TABLE ENTRY.
*              2. PLACE THE FOLLOWING CODE IN THE PROCEDURE DIVISION :
*                   MOVE YOUR DDNAME TO THE-DDNAME.
*                   CALL 'DSNTAB' USING  THE-DDNAME
*                                        DSNAME-TABLE
*                                        TABLSIZE
*                                        NO-OF-SIGNIF-DSNAMES.
*              AFTER THE CALL, THE LIST OF DSNAMES WILL BE FOUND IN
*              DSNAME-TABLE AND THE NUMBER OF SIGNIFICANT DATA-SET
*              NAMES WILL BE FOUND IN NO-OF-SIGNIF-DSNAMES.
         EJECT
* POSSIBLE RETURN CODES :
*    VALUE     MEANING
*    -----     ------------------------------------------------
*      0       SUCCESSFUL COMPLETION
*      4       DDNAME NOT FOUND
*      8       CONCATENATED DATA-SETS NAMES TABLE OVERFLOW
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=WKLEN,TYPE=RENT
         LR    R10,R13
         USING WKAREA,R10
         LA    R2,18*4(R10)
         L     R3,=A(WKLEN-(18*4))
         LR    R4,R2
         XR    R5,R5
         MVCL  R2,R4
         L     R2,0(,R1)           R2 -> DDNAME PASSED
         XR    R4,R4               R4 = 0 (RC)
         XR    R5,R5               R5 = 0 (DSN COUNT)
         L     R6,4(,R1)           R6 -> DSN TABLE
         USING DSNTDS,R6
         L     R7,8(,R1)           R7 -> SIZE OF TABLE
         LH    R7,0(,R7)           R7 = LENGTH OF TABLE
         L     R8,12(,R1)          R8 -> ENTRY COUNT PARM
         MVC   EXTR(LEXTR),EXTRP
         XC    ATIOT,ATIOT
        EXTRACT ATIOT,'S',FIELDS=(TIOT),MF=(E,EXTR)
         SPACE 1
*------- FIND THE DDNAME IN THE TIOT
         SPACE 1
         L     R3,ATIOT            R3 -> TIOT
         USING TIODSECT,R3
MTCHL    CLC   TIOELNGH(4),=XL4'0' END OF TIOT ?
         BNE   *+L'*+8             NO
         LA    R4,4                YES, R4 = 4 (RC)
         B     EXIT
         CLC   TIOEDDNM(L'TIOEDDNM),0(R2) DDNAME MATCH ?
         BE    *+L'*+8             YES
         BAL   R14,NEXTDD          R3 -> NEXT DD ENTRY
         B     MTCHL
         SPACE 1
*------- MOVE THE DSN IN THE TABLE
         SPACE 1
MOVEL    XR    R1,R1
         ICM   R1,B'0111',TIOEJFCB R1 -> JFCB
         CH    R7,=Y(DSNTDSEL)     ROOM IN TABLE FOR THIS NAME ?
         BNL   *+L'*+8             YES -> DO IT
         LA    R4,8                NO -> SAY TABLE SIZE ERROR (RC)
         B     EXIT
         USING ZB502,R1
         MVC   DSNTDSN,JFCBDSNM    MOVE THE DSN INTO THE TABLE
         DROP  R1
         SPACE 1
*------- FIND THE LENGTH OF THE DSN
         SPACE 1
         LA    R0,44
         XR    R1,R1               R1 = 0 INIT DSN LEN
         LA    R2,DSNTDSN          R2 -> DSN
DSL      CLI   0(R2),C' '          IS IT BLANK ?
         BE    DSEND               YES
         LA    R1,1(,R1)           R1 + 1 -> NEXT CHAR
         LA    R2,1(,R2)           R2 -> NEXT DSN CHAR TO BE CHECKED
         BCT   R0,DSL              CHECK NEXT IF NO BLANK YET
DSEND    STH   R1,DSNTDSL          PUT THE DSN LENGTH IN THE TABLE
         SH    R7,=Y(DSNTDSEL)     UPDATE LENGTH REMAINING IN TABLE
         LA    R5,1(,R5)           R5 + 1 = DSN COUNT IN TABLE
         BAL   R14,NEXTDD          R3 -> NEXT DD-ENTRY IN TIOT
         LA    R6,DSNTDSEL(,R6)    R6 -> NEXT SLOT IN NAME TABLE
         CLC   TIOELNGH(4),=XL4'0' END OF TIOT ?
         BE    EXIT                YES
         CLC   TIOEDDNM(L'TIOEDDNM),=CL8' ' BLANK DD-NAME ?
         BE    MOVEL               YES -> MUST BE CONCATENATED
EXIT     STH   R5,0(R8)            PASS THE COUNT OF ENTRIES
        $XRET  CC=(R4),LV=WKLEN,TYPE=RENT
         SPACE 1
*------- SUBROUTINE TO MOVE R3 TO THE NEXT DD-ENTRY IN THE TIOT
         SPACE 1
NEXTDD   XR    R0,R0
         IC    R0,TIOELNGH        R0 = LENGTH OF CURRENT DD-ENTRY
         ALR   R3,R0              R3 -> NEXT DD-ENTRY IN TIOT
         BR    R14                RETURN
         DROP  R3
         EJECT
*------- DATA CONSTANTS
         SPACE 1
EXTRP   EXTRACT *-*,'S',MF=L
         SPACE 1
        LTORG
         SPACE 1
WKAREA   DSECT
         DS    18F            SAVE AREA
EXTR    EXTRACT *-*,'S',MF=L
LEXTR    EQU   *-EXTR
ATIOT    DS    A
         SPACE 1
WKLEN    EQU   (((*-WKAREA)+7)/8)*8
         SPACE 1
*------- DSNAME(S) TABLE DSECT
         SPACE 1
DSNTDS   DSECT
DSNTDSL  DS    H
DSNTDSN  DS    CL44
DSNTDSEL EQU   *-DSNTDS
         SPACE 1
         SPACE 1
        PRINT  NOGEN
TIODSECT DSECT
        IEFTIOT1
        IEFZB502 , - - - - - - - -+
        IEFJFCBN ,                +- SWA CONTROL BLOCK
         SPACE 2
         END
