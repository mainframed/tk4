PDFI     TITLE 'PDFINIT - DYNAMIC ALLOCATION COMMAND FOR ISPF/PDF.'
PDFINIT  START 0
         SPACE 1
* PURPOSE :    THIS COMMAND WILL ALLOCATE THE PDF PROFILE DATA-SET AT
* ---------    LOGON TIME OR MAY BE INVOKED PRIOR TO INVOKING ISPF/PDF.
*              THIS IS CONSIDERABLY FASTER THAN USING A CLIST TO
*              ALLOCATE. THE PDF DATA-SET IS ALLOCATED AS OLD AND IF
*              UNABLE TO ALLOCATE, TRIES TO ALLOCATE AND CATALOG A NEW
*              PROFILE DATA-SET.
*
*              CURRENT ALLOCATION IS AS :                          -EU-
*              ATTR FB3120 RECFM(F B) LRECL(80) BLKSIZE(3120)
*              ALLOC F(ISPPROF) DA('USERID.SPF.PROF') -
*                    UNIT(DISK) VOL(USER01) TRA SP(1 1) -
*                    DSORG(PO) DIR(3) US(FB3120)
*
*              ALLOCATION DEFAULTS CAN BE CHANGED IN THE DC'S AS
*              DESIRED. - I.E. : DSNAME, VOLSER, UNIT, ETC...
*              CODE COULD BE ADDED TO TELL THE USER WHY THE ALLOCATION
*              FAILED - BE MY GUEST. WE JUST FILTER OUT THE OBVIOUS
*              ERROR AND RESPOND BACK WITH THE RETURN CODE, ERROR CODE
*              AND INFO CODE IF A REAL ERROR OCCURS.
*
*              THIS MODULE MAY BE ASSEMBLED AND LINKED INTO A LINKLIST
*              LIBRARY OR INTO LPALIB.
*
*              THE INVOCATION AT LOGON TIME IS BY USING THE PARM FIELD
*              ON THE EXEC STATEMENT IN THE LOGON PROCEDURE.
*
* ALLOCATION CHANGE (BY MOINIL P.A.) :                             -EU-
*              THE ALLOCATION HAS BEEN CHANGED FROM OLD TO SHR     -EU-
*              TO PERMIT THE USE OF OUR SYSTEM SECTOR SHORT        -EU-
*              COMMUNICATION FEATURE TO RUN.                       -EU-
*
* CBT ORIGIN : EXTRACTED FROM CBT TAPE FEB 85, FILE 361.
* ------------ ADAPTED BY : MOINIL P.A.
*                           COMPUTING CENTRE
*                           J.R.C. - ISPRA ESTABLISHMENT
*                           21020 ISPRA (VA), ITALY
         EJECT
PDFINIT AMODE  24                                                  -EU-
PDFINIT RMODE  24                                                  -EU-
         SPACE 1                                                   -EU-
        $DEFREG ,                                                  -EU-
         EJECT
        $XENT  BASE=R12,LV=AREALEN,TYPE=RENT                       -EU-
         LR    R10,R13        SAVE POINTER TO GOTTEN AREA
         USING WORKAREA,R10   SET ADDRESSABILITY TO SAVE/WORK AREA
         XC    DW(L'DW),DW    CLEAR GOTTEN AREAS TO ZERO           -EU-
         XC    RBS99(RBS99L),RBS99                                 -EU-
         XC    RBIP1(RBILSTL),RBIP1                                -EU-
         XC    RBOP1(RBOLSTL),RBOP1                                -EU-
         XC    RBAP1(RBALSTL),RBAP1                                -EU-
         MVI   ERRMSG,C' '    SET MESSAGE AREA TO BLANKS           -EU-
         MVC   ERRMSG+1(ERRMSGL-1),ERRMSG                          -EU-
         MVC   ERRMSGAB,=CL3'***'                                  -EU-
         MVC   ERRMSGAE,=CL3'***'                                  -EU-
         XC    WK(L'WK+L'WKDSNL+L'WKDSN),WK                        -EU-
         EJECT                                                     -EU-
*        BUILD PROFILE DATA-SET NAME.                              -EU-
         SPACE 1                                                   -EU-
         L     R1,CVTPTR      LOAD CVT ADDR
         USING CVT,R1                                              -EU-
         L     R1,CVTTCBP     LOAD TCB/ASCB PTR
         DROP  R1                                                  -EU-
         L     R1,12(R1)      LOAD CURRENT ASCB ADDRESS
         USING ASCB,R1                                             -EU-
         L     R1,ASCBJBNS    LOAD USERID ADDRESS
         DROP  R1                                                  -EU-
         LA    R2,6(,R1)      GET THE TSO USERID TO USE AS PREFIX  -EU-
         LA    R3,6                                                -EU-
         CLI   0(R2),C' '                                          -EU-
         BNE   *+L'*+6                                             -EU-
         BCTR  R2,0                                                -EU-
         BCT   R3,*-10                                             -EU-
         LA    R4,WKDSN                                            -EU-
         B     *+L'*+6                                             -EU-
         MVC   0(*-*,R4),0(R1)     <<EXECUTED>>                    -EU-
         EX    R3,*-6         MOVE USERID INTO NAME                -EU-
         LA    R4,1(R3,R4)                                         -EU-
         MVC   0(L'DSNAME,R4),DSNAME    MOVE SKELETON NAME         -EU-
         LA    R3,L'DSNAME+1(,R3)                                  -EU-
         STCM  R3,B'0011',WKDSNL                                   -EU-
         EJECT
*        BUILD SVC99 REQUEST BLOCKS IN GOTTEN AREA
*        TO RETRIEVE THE PROFILE DDNAME EXISTANCY.                 -EU-
         SPACE 1
         LA    R7,RBS99       BUILD S99 RB PTR
         ST    R7,RBPS99
         OI    RBPS99,S99RBPND
         USING S99RB,R7
         MVI   S99RBLN,S99RBEND-S99RB   BUILD S99 RB
         MVI   S99VERB,S99VRBIN
         LA    R6,RBIP1
         ST    R6,S99TXTPP
         LA    R5,RBITU1      BUILD RBITU1 - DDNAME
         ST    R5,RBIP1
         USING S99TUNIT,R5
         MVC   S99TUKEY,=AL2(DINDDNAM)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'DDNAME)
         MVC   S99TUPAR(L'DDNAME),DDNAME
         LA    R5,RBITU5      BUILD RBITU5 - RETURN DSNAME         -EU-
         ST    R5,RBIP5                                            -EU-
         MVC   S99TUKEY,=AL2(DINRTDSN)                             -EU-
         MVC   S99TUNUM,=XL2'1'                                    -EU-
         MVC   S99TULNG,=AL2(44)                                   -EU-
         MVI   S99TUPAR,C' '                                       -EU-
         MVC   S99TUPAR+1(43),S99TUPAR                             -EU-
         OI    RBIP5,S99TUPLN FLAG END OF LIST
         DROP  R5
         LA    R1,RBPS99      LOAD PARAMETER POINTER FOR SVC99
        DYNALLOC ,            ISSUE SVC99
         LTR   R15,R15        CHECK RETURN CODE
         BNZ   ANRETRV                                             -EU-
         LA    R5,RBITU5      ANALYZE RBITU5 - RETURN DSNAME       -EU-
         USING S99TUNIT,R5                                         -EU-
         CLC   S99TULNG,WKDSNL                                     -EU-
         BNE   PREDEFN                                             -EU-
         XR    R1,R1                                               -EU-
         ICM   R1,B'0011',WKDSNL                                   -EU-
         BCT   R1,*+L'*+6                                          -EU-
         CLC   S99TUPAR(*-*),WKDSN <<EXECUTED>>                    -EU-
         EX    R1,*-6                                              -EU-
         BNE   PREDEFN                                             -EU-
         BAS   R8,PERMDS      PERM. ALL.                           -EU-
         B     RETURN                                              -EU-
         EJECT                                                     -EU-
*        AN OTHER PROFILE DATA-SET IS IN USE.                      -EU-
         SPACE 1                                                   -EU-
PREDEFN  MVI   WARNING,C' '   SET MESSAGE AREA TO BLANKS           -EU-
         MVC   WARNING+1(WARNINGL-1),WARNING                       -EU-
         MVC   WARNINGA,=CL3'---'                                  -EU-
         MVC   WARNINGT,=CL23'ISPF PROFILE DATA-SET :'             -EU-
         XR    R1,R1                                               -EU-
         ICM   R1,B'0011',S99TULNG                                 -EU-
         BCT   R1,*+L'*+6                                          -EU-
         MVC   WARNINGN(*-*),S99TUPAR   <<EXECUTED>>               -EU-
         EX    R1,*-6                                              -EU-
         DROP  R5                                                  -EU-
         BAS   R8,PERMDS      PERM. ALL.                           -EU-
         LA    R1,WARNING                                          -EU-
         LA    R0,WARNINGL                                         -EU-
         B     TPUT                                                -EU-
ANRETRV  CLC   S99ERROR,=XL2'0438' SEE IF DDNAME NOT FOUND
         BE    ALLOCSHR       ... OK, NO ERROR AFTER ALL
         BAS   R8,ERRMSGPR    GO TELL USER OF ERROR
         DC    CL8'AL.INF :'                                       -EU-
         EJECT
*        BUILD SVC99 REQUEST BLOCKS IN GOTTEN AREA
*        TO ALLOCATE THE PROFILE DATA-SET AS 'SHR' WITH            -EU-
*        PERMANENTLY ALLOCATED ATTRIBUTE (AGAINST FREE ALL).       -EU-
         SPACE 1
ALLOCSHR XC    S99ERROR(L'S99ERROR+L'S99INFO),S99ERROR             -EU-
         MVI   S99VERB,S99VRBAL    BUILD S99 RB
         MVI   S99FLG11,S99NOCNV
         LA    R6,RBOP1
         ST    R6,S99TXTPP
         LA    R5,RBOTU1      BUILD RBOTU1 - DDNAME
         ST    R5,RBOP1
         USING S99TUNIT,R5
         MVC   S99TUKEY,=AL2(DALDDNAM)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'DDNAME)
         MVC   S99TUPAR(L'DDNAME),DDNAME
         LA    R5,RBOTU2      BUILD RBOTU2 - DSNAME
         ST    R5,RBOP2
         MVC   S99TUKEY,=AL2(DALDSNAM)
         MVC   S99TUNUM,=XL2'1'
         XR    R1,R1                                               -EU-
         ICM   R1,B'0011',WKDSNL                                   -EU-
         STCM  R1,B'0011',S99TULNG                                 -EU-
         BCT   R1,*+L'*+6                                          -EU-
         MVC   S99TUPAR(*-*),WKDSN <<EXECUTED>>                    -EU-
         EX    R1,*-6                                              -EU-
         LA    R5,RBOTU4      BUILD RBOTU4 - STATUS
         ST    R5,RBOP4
         MVC   S99TUKEY,=AL2(DALSTATS)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'1'
         MVI   S99TUPAR,DA08SHR    SET FOR SHR
         LA    R5,RBOTU5      BUILD RBOTU5 - DISPOSITION
         ST    R5,RBOP5
         MVC   S99TUKEY,=AL2(DALNDISP)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'1'
         MVI   S99TUPAR,DA08KEEP   SET FOR KEEP
         LA    R5,RBOTU52     BUILD RBOTU52 - PERM. ALLOC. ATTR.
         ST    R5,RBOP52
         MVC   S99TUKEY,=AL2(DALPERMA)
         MVC   S99TUNUM(L'S99TUNUM),=XL4'0'
         OI    RBOP52,S99TUPLN     FLAG END OF LIST
         DROP  R5
         EJECT
*        TRY TO ALLOCATE THE PROFILE DATA-SET AS SHR.              -EU-
         SPACE 1
         LA    R1,RBPS99      LOAD PARAMETER POINTER FOR SVC99
        DYNALLOC ,            ISSUE SVC99
         LTR   R15,R15        CHECK SVC99 RETURN CODE
         BNZ   ANALSHR        GO ANALYZE IT                        -EU-
         BAS   R8,PERMDS      PERM. ALL.                           -EU-
         B     RETURN         WE ALLOCATED IT AS SHR - LETS GO HOME
ANALSHR  CLC   S99ERROR,=XL2'1708' SEE IF DSNAME NOT FOUND
         BE    ALLOCNEW       ... OK, NO ERROR AFTER ALL
         BAS   R8,ERRMSGPR    GO TELL USER OF ERROR
         DC    CL8'AL.SHR :'                                       -EU-
         EJECT
*        OK - MUST NOT HAVE ONE SO LETS BUILD A NEW PROFILE
*        DATA-SET. BUILD MORE SVC99 REQUEST BLOCKS IN GOTTEN
*        AREA TO ALLOCATE THE PROFILE DATA-SET AS 'NEW,CATLG'
*        WITH PERMANENTLY ALLOCATED ATTRIBUTE (AGAINST FREE ALL).  -EU-
         SPACE 1
ALLOCNEW XC    S99ERROR(L'S99ERROR+L'S99INFO),S99ERROR             -EU-
         LA    R6,RBAP1
         ST    R6,S99TXTPP
         LA    R5,RBATU1      BUILD RBATU1 - DDNAME
         ST    R5,RBAP1
         USING S99TUNIT,R5
         MVC   S99TUKEY,=AL2(DALDDNAM)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'DDNAME)
         MVC   S99TUPAR(L'DDNAME),DDNAME
         LA    R5,RBATU2      BUILD RBATU2 - DSNAME
         ST    R5,RBAP2
         MVC   S99TUKEY,=AL2(DALDSNAM)
         MVC   S99TUNUM,=XL2'1'
         XR    R1,R1                                               -EU-
         ICM   R1,B'0011',WKDSNL                                   -EU-
         STCM  R1,B'0011',S99TULNG                                 -EU-
         BCT   R1,*+L'*+6                                          -EU-
         MVC   S99TUPAR(*-*),WKDSN <<EXECUTED>>                    -EU-
         EX    R1,*-6                                              -EU-
         LA    R5,RBATU4      BUILD RBATU4 - STATUS
         ST    R5,RBAP4
         MVC   S99TUKEY,=AL2(DALSTATS)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'1'
         MVI   S99TUPAR,DA08NEW    SET FOR NEW
         LA    R5,RBATU5      BUILD RBATU5 - DISPOSITION
         ST    R5,RBAP5
         MVC   S99TUKEY,=AL2(DALNDISP)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'1'
         MVI   S99TUPAR,DA08CAT    SET FOR CATLG
         LA    R5,RBATU7      BUILD RBATU7 - TRACK ALLOCATION
         ST    R5,RBAP7
         MVC   S99TUKEY,=AL2(DALTRK)
         MVC   S99TUNUM(L'S99TUNUM),=XL4'0'
         LA    R5,RBATUA      BUILD RBATUA - PRIMARY SPACE
         ST    R5,RBAPA
         MVC   S99TUKEY,=AL2(DALPRIME)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'3'
         MVC   S99TUPAR(3),TRKS    SET FOR # TRACK
         LA    R5,RBATUB      BUILD RBATUB - SECONDARY SPACE
         ST    R5,RBAPB
         MVC   S99TUKEY,=AL2(DALSECND)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'3'
         MVC   S99TUPAR(3),TRKS    SET FOR # TRACK
         LA    R5,RBATUC      BUILD RBATUC - DIRECTORY BLKS
         ST    R5,RBAPC
         MVC   S99TUKEY,=AL2(DALDIR)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'3'
         MVC   S99TUPAR(3),DIRBLKS SET FOR # DIR BLK
         LA    R5,RBATU10     BUILD RBATU10 - VOLSER
         ST    R5,RBAP10
         MVC   S99TUKEY,=AL2(DALVLSER)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'6'
         MVC   S99TUPAR(6),VOLSER  MOVE IN VOLSER
         LA    R5,RBATU15     BUILD RBATU15 - UNIT
         ST    R5,RBAP15
         MVC   S99TUKEY,=AL2(DALUNIT)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'UNIT)
         MVC   S99TUPAR(L'UNIT),UNIT    MOVE IN UNIT
         LA    R5,RBATU30     BUILD RBATU30 - BLKSIZE
         ST    R5,RBAP30
         MVC   S99TUKEY,=AL2(DALBLKSZ)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'2'
         MVC   S99TUPAR(2),BLKSIZE MOVE IN BLKSIZE
         LA    R5,RBATU3C     BUILD RBATU3C - DSORG
         ST    R5,RBAP3C
         MVC   S99TUKEY,=AL2(DALDSORG)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'2'
         MVC   S99TUPAR(2),=AL1(DCBDSGPO,0)  MOVE IN DSORG=PO
         LA    R5,RBATU42     BUILD RBATU42 - LRECL
         ST    R5,RBAP42
         MVC   S99TUKEY,=AL2(DALLRECL)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'2'
         MVC   S99TUPAR(2),LRECL   MOVE IN LRECL
         LA    R5,RBATU49     BUILD RBATU49 - RECFM
         ST    R5,RBAP49
         MVC   S99TUKEY,=AL2(DALRECFM)
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=XL2'1'
         MVI   S99TUPAR,DCBRECF+DCBRECBR     MOVE IN RECFM=FB
         LA    R5,RBATU52     BUILD RBATU52 - PERM. ALLOC. ATTR.
         ST    R5,RBAP52
         MVC   S99TUKEY,=AL2(DALPERMA)
         MVC   S99TUNUM(L'S99TUNUM),=XL4'0'
         OI    RBAP52,S99TUPLN     FLAG END OF LIST
         DROP  R5
         EJECT
*        TRY TO ALLOCATE THE PROFILE DATA-SET AS NEW.              -EU-
         SPACE 1
         LA    R1,RBPS99      LOAD PARAMETER POINTER FOR SVC99
        DYNALLOC ,            ISSUE SVC99
         LTR   R15,R15        CHECK SVC99 RETURN CODE
         BZ    RETURN0        0 - WE ALLOCATED OK
         BAS   R8,ERRMSGPR    GO TELL USER OF ERROR
         DC    CL8'AL.NEW :'                                       -EU-
         LA    R1,MSGBAD      UNABLE TO ALLOCATE - TELL THE USER
         LA    R0,L'MSGBAD
         B     TPUT
         SPACE 2
RETURN0  BAS   R8,PERMDS      PERM. ALL.                           -EU-
         LA    R1,MSGGOOD     TELL THE USER WE CREATED A NEW ONE
         LA    R0,L'MSGGOOD
         SPACE 1
TPUT    TPUT   (1),(0)        WRITE OUT RESULTS OF NEW ALLOCATION
         SPACE 1
RETURN  $XRET  CC=0                                                -EU-
         EJECT
*        MARK DATA-SET PERMANENTLY ALLOCATED SUBROUTINE.           -EU-
*              R8 : LINK REGISTER.                                 -EU-
         SPACE 1                                                   -EU-
PERMDS   L     R1,CVTPTR      LOAD CVT ADDRESS                     -EU-
         USING CVT,R1                                              -EU-
         L     R1,CVTTCBP     LOAD TCB/TCB PTR                     -EU-
         DROP  R1                                                  -EU-
         L     R1,4(R1)       LOAD CURRENT TCB ADDRESS             -EU-
         USING TCB,R1                                              -EU-
         L     R1,TCBJSCB     JOB STEP CONTROL BLOCK ADDRESS       -EU-
         DROP  R1                                                  -EU-
         USING IEZJSCB,R1                                          -EU-
         L     R2,JSCDSABQ    LOAD ADDRESS OF QDB FOR DSAB CHAIN   -EU-
         DROP  R1                                                  -EU-
         CLC   8(4,R2),=XL4'0'     NO DATA-SETS ?                  -EU-
         BER   R8             IF DON'T HAVE ANY WE CAN'T PERM.ALL. -EU-
         L     R2,12(R2)      LOAD ADDRESS OF FIRST DSAB           -EU-
         USING DSAB,R2                                             -EU-
INLOOP   TM    DSABFLG1,DSABDALC   DATA-SET DYN. ALL. ?            -EU-
         BZ    NOPERM         NO, GO LOOK AT NEXT ONE              -EU-
         L     R3,DSABTIOT    POINT TO TIOT                        -EU-
         USING TIOELNGH,R3                                         -EU-
         CLC   TIOEDDNM(L'TIOEDDNM),DDNAME   PROFILE DD-NAME ?     -EU-
         BE    SETPERM        YES                                  -EU-
         DROP  R3                                                  -EU-
NOPERM   L     R2,DSABFCHN    NO, GET PTR TO NEXT DSAB ENTRY       -EU-
         LTR   R2,R2          END OF CHAIN ?                       -EU-
         BNZ   INLOOP         NO, KEEP LOOKING                     -EU-
         BR    R8             NOT FOUND OR ALREADY DONE            -EU-
SETPERM STAX   DEFER=YES      NO ATTN WHILE WE ARE AUTHORIZED      -EU-
        ZEROKEY ,             ENTER AUTHORIZED                     -EU-
         NI    DSABFLG1,255-DSABDALC    INDICATE NO DYN. ALL.      -EU-
        RESETKEY ,            RE-ENTER TO PROBLEM STATE            -EU-
        STAX   DEFER=NO       ACCEPT ATTN                          -EU-
         BR    R8             RETURN                               -EU-
         DROP  R2                                                  -EU-
         EJECT
*        ERROR MESSAGE DISPLAY ROUTINE.                            -EU-
*              R7 : S99 REQUEST BLOCK ADDRESS                      -EU-
*              R8 : LINK REGISTER, AT +0 - MESSAGE ID (8 CHAR)     -EU-
*                                  RETURN TO +8                    -EU-
         SPACE 1                                                   -EU-
ERRMSGPR ST    R15,DW         GET RETURN CODE AND MAKE READABLE
         UNPK  WK,DW(5)
         NC    WK(8),=XL8'0F0F0F0F0F0F0F0F'
         TR    WK(8),=CL16'0123456789ABCDEF'
         MVC   ERRMSGR1,WK
         LH    R15,S99ERROR   GET ERROR CODE AND MAKE READABLE
         ST    R15,DW
         UNPK  WK,DW(5)
         NC    WK(8),=XL8'0F0F0F0F0F0F0F0F'
         TR    WK(8),=CL16'0123456789ABCDEF'
         MVC   ERRMSGE1,WK+4
         LH    R15,S99INFO    GET INFO CODE AND MAKE READABLE
         ST    R15,DW
         UNPK  WK,DW(5)
         NC    WK(8),=XL8'0F0F0F0F0F0F0F0F'
         TR    WK(8),=CL16'0123456789ABCDEF'
         MVC   ERRMSGI1,WK+4
         MVC   ERRMSGRC,=C'RC='
         MVC   ERRMSGEC,=C'ERROR CODE='
         MVC   ERRMSGIC,=C'INFO CODE='
         MVC   ERRMSGID,0(R8)                                      -EU-
         LA    R1,ERRMSG
         LA    R0,ERRMSGL
        TPUT   (1),(0)
         B     8(R8)          BACK TO CALLER                       -EU-
         DROP  R7
         EJECT
*        CURRENT ALLOCATION IS AS :                                -EU-
*              ATTR FB3120 RECFM(F B) LRECL(80) BLKSIZE(3120)
*              ALLOC F(ISPPROF) DA('USERID.SPF.PROF') -
*                    UNIT(DISK) VOL(USER01) TRA SP(1 1) -
*                    DSORG(PO) DIR(3) US(FB3120)
         SPACE 1
DDNAME   DC    C'ISPPROF',C' '          DDNAME  - MUST BE ISPPROF
DSNAME   DC    C'.SPF.PROF'             DSNAME  - NAME OF PROFILE DS
TRKS     DC    AL3(1)                   TRKS    - # OF TRKS TO ALLOC
DIRBLKS  DC    AL3(3)                   DIRBLKS - # OF DIRECTORY BLKS
UNIT     DC    C'DISK'                  UNIT    - UNIT TO ALLOC
VOLSER   DC    C'USER01'                VOLSER  - VOLSER TO ALLOC
LRECL    DC    AL2(80)                  LRECL   - MUST BE 80 FOR ISPF
BLKSIZE  DC    AL2(3120)                BLKSIZE -
         SPACE 2
MSGGOOD  DC    C' *** ISPF PROFILE DATA-SET HAS BEEN CREATED ***'
MSGBAD   DC    C' *** UNABLE TO ALLOCATE ISPF PROFILE DATA-SET ***'
         EJECT
        LTORG  ,              LITERAL POOL.                        -EU-
         EJECT
WORKAREA DSECT                DSECT FOR GOTTEN AREA
         SPACE 1
         DS    18F            REGISTER SAVE AREA
         SPACE 1
DW       DS    D
         SPACE 1
RBPS99   DS    F              POINTER TO SVC99 REQUEST BLOCKS
RBS99    DS    5F             SVC 99 REQUEST BLOCK
RBS99L   EQU   *-RBS99
         SPACE 1
RBIP1    DS    F              POINTER TO SVC99 TEXT UNIT 01
RBIP5    DS    F              POINTER TO SVC99 TEXT UNIT 05
RBITU1   DS    XL14                SVC99 TEXT UNIT 01 - DDNAME
RBITU5   DS    XL6,CL44            SVC99 TEXT UNIT 05 - RETURN DSNAME
RBILSTL  EQU   *-RBIP1
         SPACE 1
RBOP1    DS    F              POINTER TO SVC99 TEXT UNIT 01
RBOP2    DS    F              POINTER TO SVC99 TEXT UNIT 02
RBOP4    DS    F              POINTER TO SVC99 TEXT UNIT 04
RBOP5    DS    F              POINTER TO SVC99 TEXT UNIT 05
RBOP52   DS    F              POINTER TO SVC99 TEXT UNIT 52
RBOTU1   DS    XL14                SVC99 TEXT UNIT 01 - DDNAME
RBOTU2   DS    XL(13+L'DSNAME)     SVC99 TEXT UNIT 02 - DSNAME
RBOTU4   DS    XL7                 SVC99 TEXT UNIT 04 - DS STATUS
RBOTU5   DS    XL7                 SVC99 TEXT UNIT 05 - DS DISPOSITION
RBOTU52  DS    XL4                 SVC99 TEXT UNIT 52 - PERM.ALL.ATTR.
RBOLSTL  EQU   *-RBOP1
         SPACE 1
RBAP1    DS    F              POINTER TO SVC99 TEXT UNIT 01
RBAP2    DS    F              POINTER TO SVC99 TEXT UNIT 02
RBAP4    DS    F              POINTER TO SVC99 TEXT UNIT 04
RBAP5    DS    F              POINTER TO SVC99 TEXT UNIT 05
RBAP7    DS    F              POINTER TO SVC99 TEXT UNIT 07
RBAPA    DS    F              POINTER TO SVC99 TEXT UNIT 0A
RBAPB    DS    F              POINTER TO SVC99 TEXT UNIT 0B
RBAPC    DS    F              POINTER TO SVC99 TEXT UNIT 0C
RBAP10   DS    F              POINTER TO SVC99 TEXT UNIT 10
RBAP15   DS    F              POINTER TO SVC99 TEXT UNIT 15
RBAP30   DS    F              POINTER TO SVC99 TEXT UNIT 30
RBAP3C   DS    F              POINTER TO SVC99 TEXT UNIT 3C
RBAP42   DS    F              POINTER TO SVC99 TEXT UNIT 42
RBAP49   DS    F              POINTER TO SVC99 TEXT UNIT 49
RBAP52   DS    F              POINTER TO SVC99 TEXT UNIT 52
RBATU1   DS    XL14                SVC99 TEXT UNIT 01 - DDNAME
RBATU2   DS    XL(13+L'DSNAME)     SVC99 TEXT UNIT 02 - DSNAME
RBATU4   DS    XL7                 SVC99 TEXT UNIT 04 - DS STATUS
RBATU5   DS    XL7                 SVC99 TEXT UNIT 05 - DS DISPOSITION
RBATU7   DS    XL4                 SVC99 TEXT UNIT 07 - TRACK ALLOC
RBATUA   DS    XL9                 SVC99 TEXT UNIT 0A - PRIMARY SPACE
RBATUB   DS    XL9                 SVC99 TEXT UNIT 0B - SECONDARY SPACE
RBATUC   DS    XL9                 SVC99 TEXT UNIT 0C - DIRECTORY BLKS
RBATU10  DS    XL12                SVC99 TEXT UNIT 10 - VOL SER
RBATU15  DS    XL(6+L'UNIT)        SVC99 TEXT UNIT 15 - UNIT
RBATU30  DS    XL8                 SVC99 TEXT UNIT 30 - BLKSIZE
RBATU3C  DS    XL8                 SVC99 TEXT UNIT 3C - DSORG
RBATU42  DS    XL8                 SVC99 TEXT UNIT 42 - LRECL
RBATU49  DS    XL7                 SVC99 TEXT UNIT 49 - RECFM
RBATU52  DS    XL4                 SVC99 TEXT UNIT 52 - PERM.ALL.ATTR.
RBALSTL  EQU   *-RBAP1
         SPACE 1
ERRMSG   DS    0C
         DS    CL1
ERRMSGAB DS    CL3
         DS    CL1
ERRMSGID DS    CL8
         DS    CL1
ERRMSGRC DS    CL3
ERRMSGR1 DS    CL8
         DS    CL1
ERRMSGEC DS    CL11
ERRMSGE1 DS    CL4
         DS    CL1
ERRMSGIC DS    CL10
ERRMSGI1 DS    CL4
         DS    CL1
ERRMSGAE DS    CL3
ERRMSGL  EQU   *-ERRMSG
         SPACE 1
WARNING  DS    0C
         DS    CL1
WARNINGA DS    CL3
         DS    CL1
WARNINGT DS    CL23
         DS    CL1
WARNINGN DS    CL44
WARNINGL EQU   *-WARNING
         SPACE 1
WK       DS    CL9
WKDSNL   DS    XL2                                                 -EU-
WKDSN    DS    CL(L'DSNAME+7)                                      -EU-
         SPACE 1
AREALEN  EQU   (((*-WORKAREA)+7)/8)*8                              -EU-
         EJECT
         PRINT NOGEN                                               -EU-
         SPACE 1                                                   -EU-
        CVT    DSECT=YES,LIST=YES                                  -EU-
        IHAASCB ,                                                  -EU-
        IKJTCB ,                                                   -EU-
        IEZJSCB ,                                                  -EU-
        IEFZB4D0
        IEFZB4D2
        IKJDAP08
DSAB    IHADSAB ,                                                  -EU-
TIOT     DSECT  ,                                                  -EU-
        IEFTIOT1 ,                                                 -EU-
        DCBD   DEVD=DA,DSORG=PO                                    -EU-
         SPACE 2
         END
