OACN     TITLE 'O A C N O W  --  FORMATS DATE AND TIME.'
*        O A C N O W
*        -----------
*
* AUTHOR : BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
* -------- INSTALLATION : AIR FORCE DATA SERVICES CENTER, PENTAGON.
*          DATE WRITTEN : OCTOBER 1 1981.
*          ATTRIBUTES : REENTRANT.
*
* DESCRIPTION : THIS SUBPROGRAM FORMATS THE DATE AND TIME, THEN
* ------------- RETURNS THE ADDRESS OF THE RESULTS IN REGISTER 1.
*
*          IT IS CALLED BY THE 'ZAP' TSO COMMAND FROM UCLA.
*          THIS VERSION OF 'OACNOW' WAS WRITTEN AT AFDSC/PENTAGON
*          AFTER WE INSTALLED THE ZAP COMMAND AND DISCOVERED THAT
*          'OACNOW' WAS MISSING. THIS IS A VERSION OF THE 'JULSUB'
*          SUBPROGRAM MODIFIED TO CONFORM TO 'OACNOW' PROTOCOL.
*
*          INPUT : NONE (CALLER SET R1 TO ZERO, BUT WE IGNORE IT).
*          OUTPUT : REGISTER 1 POINTS TO
*                       +0  HH:MM:SS
*                      +14  MM/DD/YY
*                      +24  CL9'WEEKDAY'                      '
*                      +34  MMM(M) (D)D, 19YY
*                   REGISTER 0 CONTAINS LENGTH OF DATA POINTED TO
*                   BY REGISTER 1.
*                   CALLER SHOULD ISSUE FREEMAIN R,LV=(0),A=(1).
         EJECT
         MACRO
         STAMP
         LCLC  &ASMTIME
&ASMTIME SETC  '&SYSTIME'(1,5)
         DC    CL16' &SYSDATE &ASMTIME '
         MEND
         SPACE 2
OACNOW   START 0
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         USING *,R12
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'OACNOW'
         STAMP
@SIZE    DC    A(@DATAL)           LENGTH OF WORKAREA
@PROLOG  STM   R14,R12,12(R13)
         LR    R12,R15
         L     R0,@SIZE
         GETMAIN R,LV=(0)          GET SAVEAREA ADDRESS
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING @DATA,R13
         SPACE 1
         LA    R0,RESULTL          GET LENGTH OF RESULTS
         GETMAIN R,LV=(0)          GET AREA FOR RESULTS
         ST    R1,RESULTA
         XC    DOUBLE,DOUBLE
         SLR   R14,R14             ZERO R14
         ST    R14,FRSTLAST
         ST    R14,TIMEB
         TIME  BIN
         ST    R0,TIMEB            SAVE TIME
         SLR   R0,R0
         STM   R0,R1,DOUBLE        SAVE DATE
         CVB   R1,DOUBLE
         ST    R1,JB
         L     R14,DOUBLE+4        SAVE 00YYDDDF
         LH    R1,DOUBLE+4         GET 00YY
         SLL   R1,16               GET 00YY0000
         SRL   R1,12               GET 00000YY0
         ST    R1,DOUBLE+4         STORE BACK 00000YY0
         OI    DOUBLE+7,X'0F'      SET SIGN
         CVB   R1,DOUBLE
         ST    R1,YYYYB
         CH    R1,=H'99'
         BH    *+L'*+4
         AH    R1,=H'1900'
         ST    R1,CENTB
         UNPK  YYYYC,DOUBLE+5(3)   SAVE YYYY CHAR
         MVC   YYC,YYYYC+2
         ST    R14,DOUBLE+4        RESTORE 00YYDDDF
         XC    DOUBLE(6),DOUBLE    00YYDDDF TO 0000DDDF
         OI    DOUBLE+7,X'0F'      SET SIGN
         UNPK  DDDC,DOUBLE+4(4)    SAVE DDD CHAR
         CVB   R1,DOUBLE           CONVERT DDD TO BINARY
         ST    R1,DDDB             SAVE DDD BINARY
         BAS   R9,TODD
         BAS   R9,TOMON
         BAS   R9,TOTIME
         BAS   R9,OUTPUT
         B     EXIT0
         SPACE 1
*        CONVERT JULIAN DATE TO MONTH-DAY-YEAR.
*
*              INPUT : YYYYB AND DDDB (ALSO YYC).
*              OUTPUT : MMB AND DDB, MMC AND DDC, FRSTLAST.
         SPACE 1
TODD     MVC   DPM(26),DAYSPMON
         TM    YYYYB+3,B'00000011' IS YEAR A MULTIPLE OF 4?
         BNZ   DPM365              NO, BRANCH
         CLC   YYC,=C'00'          DIVISIBLE BY 100?
         BNE   DPM366              NO, BRANCH
         L     R15,CENTB
         SLR   R14,R14
         D     R14,=F'400'
         LTR   R14,R14             EVENLY DIVISIBLE BY 400?
         BNZ   DPM365              NO, NOT A LEAP YEAR
DPM366   MVI   DPM+3,29            CHANGE FEB TO 29 DAYS
DPM365   LA    R14,2               INDEX FOR DPM
         LR    R0,R14              INDEX INCREMENT
         L     R1,DDDB             DAY OF YEAR
DPMLOOP  CH    R1,DPM-2(R14)       DAY LESS THAN OR EQ ENTRY?
         BE    DPMEND              EQUAL, BRANCH
         BL    DPMWIN              LESS, BRANCH
         SH    R1,DPM-2(R14)       NO, SUBTRACT ENTRY FROM DAY
         ALR   R14,R0              ADD 2 TO INDEX
         B     DPMLOOP             LOOP
DPMEND   MVI   FRSTLAST+3,2        LAST DAY OF MONTH
         B     DPMCOMM
DPMWIN   CH    R1,=H'1'            FIRST OF MONTH?
         BNE   DPMCOMM             NO, BRANCH
         MVI   FRSTLAST+3,1        FIRST DAY OF MONTH
DPMCOMM  SRL   R14,1               HALVE INDEX TO GET MM
         ST    R1,DDB              SAVE DD BINARY
         CVD   R1,DOUBLE           CONVERT TO PACKED
         OI    DOUBLE+7,X'0F'      SET SIGN
         UNPK  DDC,DOUBLE+6(2)     SAVE DD CHAR
         ST    R14,MMB             SAVE MM BINARY
         CVD   R14,DOUBLE          CONVERT TO PACKED
         OI    DOUBLE+7,X'0F'      SET SIGN
         UNPK  MMC,DOUBLE+6(2)     SAVE MM CHAR
         MVI   SLASH1,C'/'
         MVI   SLASH2,C'/'
         BR    R9
         SPACE 1
*        CONVERT MONTH NUMBER TO MONTH NAME.
*
*               INPUT : MMB (ALSO DDC AND YYC FOR MOVE).
*               OUTPUT : MONTH AND ALPHA.
         SPACE 1
TOMON    L     R15,MMB
         MH    R15,=H'9'           MULT BY 9
         LA    R15,ALPHAMON-9(R15) ADDRESS OF ENTRY
         MVC   MONTH,0(R15)        SAVE PROSE MONTH
         MVC   ALPHA+2(3),0(R15)   SAVE ALPHA MONTH
         MVC   ALPHA(2),DDC
         MVC   ALPHA+5(2),YYC
         MVI   ALPHA+7,C' '
         BR    R9
         SPACE 1
*        CONVERT TIMEB TO TIMEC.
*
*              DIVIDE BY 10,10,10,6,10,6,10,6
         SPACE 1
TOTIME   L     R15,TIMEB
         LA    R6,10               INITIAL DIVISOR
         LA    R7,12               SET DIVISOR MASK
         LA    R8,32               SHIFT COUNT
DECLOOP  XR    R14,R14
         DR    R14,R6
         LR    R0,R14              SAVE REMAINDER
         SRDL  R0,4                SHIFT INTO R1
         SH    R8,=H'4'            DECREMENT SHIFT COUNT
         LTR   R15,R15             QUOTIENT ZERO?
         BZ    TOTIMX              YES, BRANCH
         CL    R8,=F'24'           IF COUNT LT 24
         BNL   DECLOOP             NO, BRANCH
         XR    R6,R7               10 TO 6 TO 10 ETC
         B     DECLOOP
TOTIMX   SRL   R1,0(R8)            SHIFT REMAINING BITS
         SRL   R1,4                GET 0HHMMSST
         ST    R1,DOUBLE
         OI    DOUBLE+3,X'0F'
         MVC   DOUBLE+6(10),=X'402120204B20204B2020'
         ED    DOUBLE+6(10),DOUBLE
         MVC   TIMEC,DOUBLE+8
         BR    R9
         EJECT
OUTPUT   L     R1,RESULTA
         MVI   0(R1),C' '          FILL WITH BLANKS
         MVC   1(RESULTL-1,R1),0(R1)
         MVC   00(08,R1),TIMEC     HH.MM.SS
         MVC   14(08,R1),MMC       MM/DD/YY
         MVC   24(09,R1),WEEKC     THURSDAY
         MVC   34(03,R1),MONTH     OCT
         BR    R9
         SPACE 1
EXIT0    L     R2,RESULTA          GET ADDRESS OF RESULTS
         L     R0,@SIZE            SET UP R0 FOR FREEMAIN
         LR    R1,R13              SET UP R1 FOR FREEMAIN
         L     R13,4(,R13)
         ST    R2,24(,R13)         LM WILL PUT A(RESULTS) IN R1
         LA    R2,RESULTL          GET LENGTH OF RESULTS
         ST    R2,20(,R13)         LM WILL PUT L'RESULTS IN R0
         FREEMAIN R,LV=(0),A=(1)
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
         EJECT
*        CONSTANTS.
*        ----------
         SPACE 1
DAYSPMON DC    H'31,28,31,30,31,30,31,31,30,31,30,31,999'
ALPHAMON DC    CL36'JANUARY  FEBRUARY MARCH    APRIL    '
         DC    CL36'MAY      JUNE     JULY     AUGUST   '
         DC    CL36'SEPTEMBEROCTOBER  NOVEMBER DECEMBER '
         DC    CL09'UNKNOWN  '
WEEKDAYS DC    CL36'SATURDAY SUNDAY   MONDAY   TUESDAY  '
         DC    CL27'WEDNESDAYTHURSDAY FRIDAY   '
WEEKBLNK EQU   WEEKDAYS+15         3 BLANKS
         SPACE 2
         LTORG
         EJECT
*        DSECTS.
*        -------
         SPACE 1
@DATA    DSECT
         DS    18F
DOUBLE   DS    D
WORK     DS    8C
SC       DS    0CL8
MMC      DS    CL2
SLASH1   DS    C
DDC      DS    CL2
SLASH2   DS    C
YYC      DS    CL2
YYYYC    DS    CL4
DDB      DS    F
MMB      DS    F
YYYYB    DS    F
CENTB    DS    F
DDDB     DS    F
JB       DS    F
SB       DS    F
WEEKB    DS    F
FRSTLAST DS    F
TIMEB    DS    F
TIMEC    DS    CL8
JC       DS    0CL5
         DS    CL2
DDDC     DS    CL3
MONTH    DS    CL9
ALPHA    DS    0CL8
         DS    CL2
MON      DS    CL3
         DS    CL2
         DS    CL1                 BLANK FILLER LAST BYTE OF ALPHA
WEEKC    DS    CL12
PROSE    DS    CL20
DPM      DS    13H
RESULTA  DS    F
RESULTL  EQU   72
         DS    0D
@DATAL   EQU   (((*-@DATA)+7)/8)*8
         SPACE 2
         END
