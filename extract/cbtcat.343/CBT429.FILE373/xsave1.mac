         MACRO
&LABEL   XSAVE1 &SVAREA=,&REEN=,&OPT=,&REG=
.**********************************************************************
.* MACRO    = XSAVE                                                   *
.* AUTHOR   = MARK SCHERFLING                                         *
.* DATE     = 01/16/81                                                *
.* CHANGE   = 05/12/83  RIC FORD                                      *
.*            CHANGED "BAL 15,12(15)" TO "BC 15,12(,15)"              *
.*                                                                    *
.*                                                                    *
.* FUNCTION = THIS MARCO SETS UP STANDARD LINKAGE IN SAVING THE       *
.*      CALLING ROUTINE'S REGISTERS AND LINKING IN THE NEW SAVE AREA  *
.*                                                                    *
.* PARAMETERS =                                                       *
.*      SVAREA  = LABEL OF THE OF A SAVE AREA SPECIFIED BY THE USER   *
.*                IF OMITTED, A SAVE AREA IS GENERATED.               *
.*      REG     = IF THE SVAREA OR REEN PARAMETER IS USED, A REGISTER *
.*                IS REQUIRED TO SET UP A BASE REGISTER.              *
.*                IF A SAVE AREA IS SETUP, R13 IS USED AS BASE        *
.*                                                                    *
.*      REEN    = NUMBER OF BYTES TO BE ALLOCATED.                    *
.*                THIS AREA MUST INCLUDE THE 18 FULLWORDS FOR A LOCAL *
.*                PROGRAM WORK AREA ALONG WITH LOCAL WORK STORAGE     *
.*      OPT     = CSECT  - GENERATE A CSECT STATMENT WITH THE LABEL   *
.*                         PROVIDED                                   *
.**********************************************************************
.*
         LCLC  &BREG
&BREG    SETC  '15'               DEFAULT BASE REGISTER IS R15
.*
         AIF   ('&OPT' NE 'CSECT').NOCSECT
&LABEL   CSECT
         B     12(0,15)        BRANCH OVER TRACE NAME
         DC    CL8'&LABEL'        MODULE NAME
         AGO   .OVLBL
.NOCSECT ANOP
&LABEL   CNOP  0,4
.OVLBL   AIF   ('&REG' EQ '').NOREG
&BREG    SETC  '&REG'
.NOREG   ANOP
         STM   14,12,12(13)       SAVE REGISTERS
         BALR  &BREG,0            GET CURRENT ADDR IN 15
         USING *,&BREG            MAKE TEMP REGISTER
         AIF   ('&REEN' EQ '').NOREEN
         LA    0,&REEN            LOAD BYTE COUNT
         GETMAIN R,LV=(0)         GETMAIN AS SAVE AREA AND WORK BUFFER
         LR    14,1               MOVE STORAGE AREA ADDR
         L     1,24(,13)          RELOAD R1 (MAY HAVE A PARM LIST)
         AGO   .SVPTRS
.NOREEN  AIF   ('&SVAREA' EQ '').MKSAVE
         LA    14,&SVAREA         LOAD USER SAVE AREA
         AGO   .SVPTRS
.MKSAVE  CNOP  0,4                MAKE SURE RIGHT ALIGNMENT
         BAL   14,*+76            BRANCH OVER SAVE AREA
         USING *,13               SET UP ADDRESSABILTY
         DS    18F                REGISTER SAVE AREA
         DROP  &BREG
.SVPTRS  ANOP
         ST    14,8(,13)          SAVE FORWARD PTR
         ST    13,4(,14)          SAVE BACKWARD PTR
         LR    13,14              MAKE SV CURRENT
         MEND
