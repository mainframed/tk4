         MACRO
&NAME    EXODUS &REGS=,&RC=0,&LV=,&SP=0
.*
         GBLA  &ESAVEC(2),&ESAVEL,&ESAVEB(2)
         GBLB  &ESAVES(3)
         GBLC  &ESAVEX,&ESAVEA
         LCLB  &D,&F
         LCLC  &IDL
.*
.*       EXODUS (EXTENDED RETURN) FUNCTION
.*             REWRITTEN 09-01-74, MACRO LEVEL 3
.*
.*       THIS MACRO SUPPORTS THE FOLLOWING MODES: REENTRANT AND
.*       NONREENTRANT TYPE IV SAVE AREAS.                      08-01-74
.*
.*       NOTE: THIS MACRO USES THE GENESIS RECURSIVE CALL FACILITY TO
.*             GENERATE THE LM OR L INSTRUCTION.  THIS SAVES THE
.*             DUPLICITY OF MACRO CODE.  THE IBM RETURN MACRO IS NOT
.*             USED SO THAT EFFICIENT CODE MAY BE GENERATED.
.*
         AIF   ('&REGS' EQ '').I3  IF THERE ARE NO OVERRIDE REGS
         AIF   (N'&REGS LE 2).I1   IF RIGHT NO. PARMS
         IHBERMAC 1012,REGS        ELSE FLAG
&F       SETB  1                   SET FAIL FLAG
         AGO   .I3                 CONTINUE
.I1      ANOP
         AIF   (T'&REGS(1) EQ 'N').I2  TEST FOR DIGITS
         IHBERMAC 1001,REGS,&REGS(1)
&F       SETB  1                   SET FIAL FLAG
.I2      ANOP
         AIF   (N'&REGS EQ 1).I3
         AIF   (T'&REGS(2) EQ 'N').I3
         IHBERMAC 1001,REGS,&REGS(2)
&F       SETB  1
.*
.I3      ANOP
         AIF   ('&RC' NE '').I4
         IHBERMAC 1014,RC
&F       SETB  1
         AGO   .I6
.I4      ANOP
         AIF   ('&RC'(1,1) EQ '(').I6
         AIF   (T'&RC NE 'N').I5
         AIF   (&RC LE 4095).I6
.I5      ANOP
         IHBERMAC 1001,RC,&RC
&F       SETB  1
.*
.I6      ANOP
         AIF   (NOT &F).G0
         MEXIT
.*
.*
.G0      ANOP
         AIF   ('&NAME' EQ '').G1
&NAME    DS    0H
.*
.G1      ANOP
         AIF   (NOT &ESAVES(1) AND ('&LV' EQ '' OR '&LV' EQ '0')).G2
         LA    1,0(,13)
.*
.G2      ANOP
         L     13,4(,13)
.*
         AIF   ('&RC'(1,1) NE '(').G3
         ST    &RC(1),16(,13)
.*
.G3      ANOP
         AIF   (NOT &ESAVES(1) AND ('&LV' EQ '' OR '&LV' EQ '0')).G8
         AIF   ('&LV' NE '').G5
         AIF   (&ESAVES(2)).G4
         LA    0,&ESAVEL
         AGO   .G7
.G4      ANOP
         ICM   0,15,&ESAVEL.(&ESAVEX)
         AGO   .G7
.G5      ANOP
         AIF   (T'&LV NE 'N' OR T'&SP NE 'N').G6
         AIF   (&LV GT 4095 OR &SP NE 0).G6
         LA    0,&LV
         AGO   .G7
.G6      ANOP
         ICM   0,15,IHB&SYSNDX
&D       SETB  1
.G7      ANOP
         SVC   10
.*
.G8      ANOP
         AIF   (T'&REGS EQ 'O').G9
         GENESIS REGS=&REGS,TYPE=(RECURSIVE,L)
         AGO   .G11
.*
.G9      ANOP
         AIF   (&ESAVES(3)).G10
         LM    14,12,12(13)
         AGO   .G11
.*
.G10     ANOP
         AIF ('&ESAVEA' NE 'GEN').G10B
&ESAVEC(1) SETA &ESAVEB(1)
&ESAVEC(2) SETA &ESAVEB(2)
.G10B     ANOP
         AIF   (&ESAVEC(1) GT 99).G11
         AIF   (&ESAVEC(2) GT 15).G10A
         GENESIS REGS=(&ESAVEC(1),&ESAVEC(2)),TYPE=(RECURSIVE,L)
         AGO   .G11
.G10A    ANOP
         GENESIS REGS=&ESAVEC(1),TYPE=(RECURSIVE,L)
.*
.G11     ANOP
         AIF   ('&RC'(1,1) EQ '(').G13
         AIF   (&RC EQ 0).G12
         LA    15,&RC
         AGO   .G13
.G12     ANOP
         SLR   15,15
.*
.G13     ANOP
         BR    14
         AIF   (NOT &D).G14
&IDL     SETC  'IHB&SYSNDX'
&IDL     DC    AL1(&SP),AL3(&LV)
.G14     ANOP
*
         MEND
