          TITLE '*----NSP015----*  JCL XREF -- PROGRAM NAME CROSSREFERE*
               NCE'
***********************************************************************
*                                                                     *
*     'PGM'  CROSS REFERENCE PROGRAM  -  'NSP015'                     *
*                                                                     *
*                                                                     *
*     THIS PROGRAM SORTS AND PRINTS OUTPUT FROM 'NSP011'.             *
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
         GENESIS CSECT=NSP015,ID=NSP015,EQU=Y,BASE=R13
*                                                                     *
BLANK    EQU   C' '                     EQUATE
CHR0     EQU   C'0'
CHRI     EQU   C'I'
CHRO     EQU   C'O'
*                                                                     *
HEX00    EQU   X'00'
HEXFF    EQU   X'FF'
*                                                                     *
A10      EQU   *
         LA    R1,SORTLST2              SORT BY PROGRAM NAME
         LINK  EP=SORT
*                                                                     *
         LOAD  EP=NSP016                LOAD DATE ROUTINE
         LR    R15,R0                   AND CALL
         CALL  (15),(DATEWK),VL
         MVC   H1DATE,DATE3             MOVE CURRENT DATE TO HEADER
*                                                                     *
         OPEN  (INFILE,(INPUT),PRTFILE,(OUTPUT))
         MVC   PRTREC,PRTREC-1
         BAL   R10,HEADER10             PRINT HEADERS
A20      EQU   *
         GET   INFILE,XREC              GET INPUT RECORD
         CLC   PROGRAM,XPGM             DID PROGRAM CHANGE
         BE    A100                     NO
         MVC   PROGRAM,XPGM             SAVE NEW PROGRAM
         CLI   FIRSTSW,HEX00            IS THIS 1ST TIME THRU
         BE    A30
         PUT   PRTFILE,HDR3             PUT SEPARATOR LINE
         L     R1,LINECTR
         A     R1,=F'1'                 BUMP LINE COUNTER
         ST    R1,LINECTR
         C     R1,LINEMAX1              IS IT PAGE OVERFLOW
         BNH   A30                      NO
         BAL   R10,HEADER10             PRINT HEADERS
A30      EQU   *
         MVI   FIRSTSW,HEXFF
         MVI   PCC,BLANK                SET TO SINGLE SPACE
         MVC   PPGM,XPGM                MOVE PROGRAM TO PRINT
         B     A102
A100     EQU   *
         CLC   PROC,XPROC                DONT PRINT IF PROC AND STEP
         BNE   A102                         ARE THE SAME
         CLC   STEP,XSTEP
         BNE   A102
         B     A20
A102     EQU   *
         MVC   PROC,XPROC
         MVC   STEP,XSTEP
         L     R1,LINECTR
         C     R1,LINEMAX2              IS IT PAGE OVERFLOW
         BNH   A110                     NO
         BAL   R10,HEADER10             YES - PRINT HEADERS
         MVC   PPGM,XPGM
         MVC   PCONT,=C' * CONT'        CONTINUE ON NEXT PAGE
A110     EQU   *
         MVC   PPROC,XPROC              MOVE PROC TO PRINT
         MVC   PSTEP,XSTEP              MOVE STEP TO PRINT
*                                                                     *
         PUT   PRTFILE,PRTREC           PRINT RECORD
         MVC   PRTREC,PRTREC-1          CLEAR
         L     R1,LINECTR
         A     R1,=F'1'                 BUMP LINE COUNTER
         ST    R1,LINECTR
         B     A20
*                                                                     *
         EJECT
HEADER10 EQU   *                        HEADER ROUTINE
         L     R1,PAGECTR
         A     R1,=F'1'                 BUMP PAGE COUNTER
         ST    R1,PAGECTR
         CVD   R1,DWRD                  MOVE PAGE NO.
         MVC   H1PAGE,MASK
         ED    H1PAGE,DWRD+6
         PUT   PRTFILE,HDR1             PRINT HEADER 1
         PUT   PRTFILE,HDR2             PRINT HEADER 2
         PUT   PRTFILE,PRTREC           PRINT BLANK LINE
         SR    R1,R1
         ST    R1,LINECTR
         BR    R10
*                                                                     *
INEOF    EQU   *                        END OF FILE ROUTINE
         MVI   H3CC,BLANK
         PUT   PRTFILE,HDR3
         CLOSE (INFILE,,PRTFILE)
         EXODUS RC=0
*                                                                     *
         EJECT
INDCB    DS    0F
INFILE   DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=GM,EODAD=INEOF
*                                                                     *
PRTDCB   DS    0F
PRTFILE  DCB   DDNAME=SYSPRNT3,DSORG=PS,MACRF=PM,                      X
               RECFM=FBA,LRECL=133,BLKSIZE=3990
*                                                                     *
         EJECT
LINECTR  DC    F'0'                     LINE COUNTER
LINEMAX1 DC    F'49'                    MAX LINES PER PAGE
LINEMAX2 DC    F'54'                    MAX LINES PER PAGE
PAGECTR  DC    F'0'                     PAGE COUNTER
DWRD     DS    D
MASK     DC    X'40202021'
FIRSTSW  DC    X'00'
*                                                                     *
PROGRAM  DC    XL8'0000000000000000'
PROC     DC    CL8' '
STEP     DC    CL8' '
*                                                                     *
DATEWK   DS    0CL55                    DATE AREA
         DS    CL19
DATE3    DS    CL8                      MO/DA/YR
         DS    CL28
*                                                                     *
         EJECT
         DS    0F                       SORT PARMS
SORTLST2 DC    X'80',AL3(SORTPRM2)
         DC    H'0'
SORTPRM2 DC    AL2(PARMEND2-PARMBEG2)
PARMBEG2 EQU   *
         DC    A(SORT2,SORTEND2,REC2,RECEND2,0,0)
SORTSIZ2 DC    F'100000'
SORTMSG2 DC    X'FF00'
         DC    C'AP'
PARMEND2 EQU   *
*                                                                     *
SORT2    DS    0H
*                              PGM       PROC      STEP               *
         DC    C' SORT FIELDS=(54,8,CH,A,46,8,CH,A,62,8,CH,A),'
         DC    C'SIZE=E40000 '
SORTEND2 EQU   *-1
*                                                                     *
REC2     DS    0H
         DC    C' RECORD LENGTH=135,TYPE=F '
RECEND2  EQU   *-1
*                                                                     *
         EJECT
XREC     DS    0CL135                   INPUT RECORD
XDSN     DS    CL44                     DSNAME
XIO      DS    CL1                      INPUT/OUTPUT
XPROC    DS    CL8                      PROC
XPGM     DS    CL8                      PROGRAM
XSTEP    DS    CL8                      STEP
XRELGEN  DS    CL4                      RELATIVE GEN NO.
XDISP    DS    0CL9                     DISP
XDISP1   DS    CL3
XDISP2   DS    CL3
XDISP3   DS    CL3
XUNIT    DS    CL8                      UNIT
XEXPDT   DS    CL5
XSPACE   DS    CL25                     SPACE
XDCB     DS    0CL15
XDSORG   DS    CL2                      DSORG
XRECFM   DS    CL3                      RECFM
XLRECL   DS    CL5                      LRECL
XBLKSIZE DS    CL5                      BLKSIZE
         DS    CL15                     FILLER
*                                                                     *
         DC    C' '
PRTREC   DS    0CL133                   PRINT RECORD
PCC      DS    CL1
         DS    CL5
PPGM     DS    CL8                      PROGRAM
PCONT    DS    CL7
         DS    CL8
PPROC    DS    CL8                      PROC
         DS    CL7
PSTEP    DS    CL8                      STEP
         DS    CL81
*                                                                     *
HDR1     DS    0CL133                   HEADER 1
         DC    CL21'1  NSP015            '
         DC    CL17' '
         DC    CL41'P G M   CROSS REFERENCE LISTING'
         DC    CL9'RUN DATE'
H1DATE   DC    CL8' '
         DC    CL11'    PAGE'
H1PAGE   DC    CL4' '
         DC    CL22' '
*                                                                     *
HDR2     DS    0CL133                   HEADER 2
         DC    CL29'0    PROGRAM  NAME'
         DC    CL15'PROC'
         DC    CL5'STEP'
         DC    CL84' '
*                                                                     *
HDR3     DS    0CL133                   SEPARATOR LINE
H3CC     DC    C' '
         DC    CL15' '
         DC    40C'-'
         DC    CL77' '
*                                                                     *
         LTORG
         END
