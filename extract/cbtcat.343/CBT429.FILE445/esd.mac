ESD      TITLE 'ESD'
***********************************************************************
*                                                                     *
*  process ESD card from input                                        *
*                                                                     *
*  called by PASS1                                                    *
*                                                                     *
*  parms @ r14:                                                       *
*    +0 - @ input card                                                *
*    +4 - @ print area                                                *
*    +8 - return to here                                              *
*                                                                     *
*  this csect passes input ESD cards to the appropriate handling      *
*  routine.                                                           *
*                                                                     *
*  History:                                                           *
*    pass weak extrns to ESD2, like normal extrns         CFK may 1999*
*    add the input ESD card to the ILLEGAL ESD CARD msg   CFK may 1999*
*    pass Private code to ESD1 like SD                    CFK jun 1999*
*                                                                     *
***********************************************************************
ESD      CSECT
         BALR  13,0                   establish base reg
         USING *,13                   tell assembler
         EXTRN SCNE                   in ESD2 csect
         EXTRN PRINT                  csect
         EXTRN ESD0                   csect
         EXTRN ESD1                   csect
         EXTRN ESD2                   csect
         EXTRN ESD5                   csect
         EXTRN PDOFF                  flag to not punch in PRINTA csect
         ST    13,ESD13               save r13
         L     1,0(14)                get @ card
         MVC   ESC+2(2),10(1)         copy length of ESD data
         L     4,ASCNE                @ scne in esd2 csect
         MVC   2(2,4),14(1)           copy ESD identifier of 1st item
         CLC   0(4,4),C01             =x'00000001' ?
         BNE   COM3                   br if not
         MVI   3(4),X'02'             else set to x'02'
COM3     LM    3,5,ESDB               get BXLE parms
         L     2,4(14)                get @ Print Area
COME     CLC   24(1,1),0(3)       SELECT
         BE    BESD               ESD
         BXLE  3,4,COME           SUBROUTINE
ESD3     MVC   0(21,2),ESDI       ERROR MESSAGE '**  illegal esd card'
         MVC   21(80,2),0(1)          add ESD card to error messsage
         ST    14,ESD14               save r14
         MVC   EPA(4),4(14)           pass @ PA to PRINT
         L     14,APDOFF              get @ PDOFF flag in PRINTA csect
         OI    0(14),X'40'            flag not to punch this line
         L     14,EPRINT              get @ PRINT csect
         CNOP  2,4
         BALR  14,14                  call PRINT to output message
EPA      DC    F'0'                   @ message
         L     14,ESD14               reload r14
         B     8(14)                  and exit
BESD     S     3,ESDB                 calc offset into table
         LA    3,AESD(3)              @ ESDx csect vcon
         L     3,0(3)                 get @ ESDx csect
         CNOP  2,4
         BALR  11,3               BRANCH TO SUBR
         L     13,8(11)               reload r13
         B     AS13                   br round save area
ESD13    DC    F'0'                   save area for r13
AS13     L     1,0(14)                reload @ input card
         LA    1,16(1)                adjust for @ next ESD data item
         ST    1,0(14)                and save it
         L     3,ESC                  get len of ESD data
         S     3,SI6                  subtract len of an ESD entry
         ST    3,ESC                  save length
         BP    COM3                   br if more entries to process
         B     8(14)                  exit
         DC    C'IMM='
ASCNE    DC    A(SCNE)                @ SCNE in ESD2 csect
EPRINT   DC    A(PRINT)               @ PRINT csect
APDOFF   DC    A(PDOFF)               @ flag to not punch cards
AESD     DC    A(ESD5)                @ ESD5 csect, for CM entries
         DC    A(ESD2)                @ ESD2 csect      ER
         DC    A(ESD2)                @ ESD2 csect      WX
         DC    A(ESD1)                @ ESD1 csect      LD
         DC    A(ESD0)                @ ESD1 csect      PC
         DC    A(ESD0)                @ ESD0 csect      SD
         DC    A(ESD5)                @ ESD5 csect      ?
         DC    A(ESD2)                @ ESD2 csect      ?
         DC    A(ESD1)                @ ESD1 csect      ?
         DC    A(ESD0)                @ ESD0 csect      ?
ESDB     DC    A(N02)                 BXLE parms, @ list to search
         DC    F'4'                     length of each entry
         DC    A(N00)                   @ last entry
ESD14    DC    F'0'
N02      DC    X'05000000'            CM type ESD entry, pass to ESD5
         DC    X'02000000'            ER, External Reference     ESD2
         DC    X'0A000000'            WX, Weak eXternal ref.     ESD2
         DC    X'01000000'            LD, Label                  ESD1
         DC    X'04000000'            PC, Private Code           ESD0
         DC    X'00000000'            SD, Section (Csect)        ESD0
         DC    X'F5000000'                                       ESD5
         DC    X'F2000000'                                       ESD2
         DC    X'F1000000'                                       ESD1
N00      DC    X'F0000000'                                       ESD0
SI6      DC    F'16'
ESC      DC    F'0'                   length of ESD data
C01      DC    F'1'
ESDI     DC    C'**  ILLEGAL ESD CARD:'
         DS    0F
         END
