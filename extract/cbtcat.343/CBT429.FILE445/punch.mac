PUN      TITLE 'STANDARDIZED PUNCH WITH DISK OUTPUT       AUG. 14,1967'
***********************************************************************
*                                                                     *
* history:                                                            *
*   changed DCB's to recfm FB instead of recfm F          CFK may 1994*
*                                                                     *
***********************************************************************
PUNCH    CSECT
         ENTRY PUDCB,DADCB        used by PASS5 to close these files
         EXTRN COTR               csect
         BALR  10,0               setup base reg
         USING *,10               tell assembler
         LA    13,SAVE
         STM   14,12,12(13)       *************
         ST    13,SVPU+4          *   S/360    *
         LR    11,13              *  LINKAGE   *
         LA    13,SVPU            *            *
         ST    13,8(11)           **************
         L     1,0(14)
         LR    2,1                SAVE DATA ADR
         TM    DADCB+48,X'10'     ARE DATA SETS OPEN?
         BO    OKPU               BRANCH IF YES
         OPEN  (PUDCB,(OUTPUT),DADCB,(OUTPUT))
         TM    DADCB+48,X'10'     WAS DISK OPEN SUCCESSFUL?
         BO    TPDS               BRANCH IF YES
         ABEND 1,DUMP             DUMP IF UNABLE TO OPEN DISK
TPDS     TM    PUDCB+48,X'10'     WAS PUNCH OPEN SUCCESSFUL?
         BO    OKPU               BRANCH IF YES
         ABEND 3,DUMP             DUMP IF UNABLE TO OPEN PUNCH
OKPU     CLI   9(2),C' '          IS COL 10 BLANK
         MVI   LT,X'00'
         BNE   MV
         CLI   10(2),C' '         IS COL 11 BLANK
         MVI   LT,X'01'
         BE    RTN                SKIP CARD IF YES
         LA    2,1(2)             NO,MOVE IMAGE LEFT ONE COL
MV       MVC   PUNO(76),0(2)      MOVE DATA TO PUNCH AREA
         L     3,PCCT
         LA    3,1(3)             INCREMENT CARD COUNT
         ST    3,PCCT
         CVD   3,DW               OBTAIN DECIMAL COUNT
         UNPK  PUNO+76(4),DW+5(3) PLACE UNPACKED COUNT IN COL 77-80
         OI    PUNO+79,X'F0'      REPLACE SIGN BY ZONE (F)
         L     5,PCOTR            get @ control card image in COTR
         CLI   LT,X'00'
         BE    PUP
         CLI   2(5),C'P'          TEST FOR PUNCH INDICATOR
         BNE   WRDA
PUP      PUT   PUDCB,PUNO         PUNCH A CARD
WRDA     PUT   DADCB,PUNO         CARD TO DISK FOR LATER ASM STEP
         CLC   ENDC(3),PUNO+9     IS THIS THE END CARD
         BE    ENDCD
RTN      L     13,SVPU+4          **********
         LM    14,12,12(13)       * RETURN *
         B     4(14)
ENDCD    MVC   PUNO(3),DELM       PLACE DELIMETER ON DISK
         MVC   PUNO+3(77),PUNO+2  CLEAR REST OF DELIMETER CARD
         B     WRDA
DW       DC    D'0'               UNPACKED CARD COUNT
PCOTR    DC    A(COTR)            ADR OF CONTROL CARD IMAGE
PCCT     DC    F'0'               PUNCH CARD COUNT
PUNO     DS    20F                PUNCH AREA
SVPU     DC    18F'0'             PUNCH SAVE AREA
SAVE     DS    18F
LT       DC    X'00'
ENDC     DC    CL3'END'
DELM     DC    CL3'/* '
         EJECT
PUDCB    DCB   DDNAME=PUDD,DSORG=PS,MACRF=(PM),RECFM=FB,               X
               LRECL=80
DADCB    DCB   DDNAME=DADD,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80
         END
