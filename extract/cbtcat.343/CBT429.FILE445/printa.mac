PRTA     TITLE 'STANDARDIZED PRINT WITH ERROR ROUTINE     AUG. 14,1967'
***********************************************************************
*                                                                     *
*  Standard print csect                                               *
*                                                                     *
*  Input @ R1 - 133 byte area to print                                *
*       r1+54 - 80 byte card image passed to PUNCH if PDOFF is zero.  *
*                                                                     *
*  called by PRINT and READA                                          *
*                                                                     *
* history:                                                            *
*   add opt to bypass call to PUNCH for this record only. CFK may 1999*
*   change header to ESA/370                              CFK jun 1999*
*   change header to ESA/390                              CFK dec 1999*
*                                                                     *
***********************************************************************
PRINTA   CSECT
         ENTRY LCT                printer line count, ref'ed by PASS4
*                                 and COUNT
         ENTRY PDOFF              flag to bypass punch and disk
*                                 ref'ed by ESD, READA and COUNT
         ENTRY PRDCB              used by PASS5 to close this DCB
         EXTRN PUNCH              csect
         STM   14,12,12(13)       **************
         BALR  12,0               *            *
         USING *,12               *  STANDARD  *
         ST    13,SVPR+4          *   S/360    *
         LR    11,13              *  LINKAGE   *
         LA    13,SVPR            *            *
         ST    13,8(11)           **************
         L     3,0(1)             LOAD DATA SOURCE ADR
         TM    PRDCB+48,X'10'     IS PRINT DATA SET OPEN?
         BO    OKPR               BRANCH IF YES
         OPEN  (PRDCB,(OUTPUT))   NO, OPEN DATA SET
         TM    PRDCB+48,X'10'     WAS PRINT OPEN SUCCESSFUL?
         BO    PG1                BRANCH IF YES
         ABEND 2,DUMP             DUMP IF UNABLE TO OPEN PRINT
PG1      DS    0H
         PUT   PRDCB,MSG          PRINT HEADER MESSAGE
OKPR     L     5,LCT              )
         LA    5,1(5)             ) INCREMENT LINE COUNT
         ST    5,LCT              )
         C     5,FFT              IS THIS LAST LINE OF PAGE
         BL    PRNT               BRANCH IF NO
         LA    5,1                RE-SET LINE
         ST    5,LCT              COUNT TO ONE
         L     5,PCT              INCREMENT
         LA    5,1(5)             PAGE COUNT
         ST    5,PCT              BY ONE
         CVD   5,DPCT             GET DECIMAL PAGE COUNT
         UNPK  PAGE+4(3),DPCT+5(3)
         CLI   PAGE+4,C'0'        REMOVE
         BNE   NOBLK
         MVI   PAGE+4,C' '        LEADING
         CLI   PAGE+5,C'0'
         BNE   NOBLK              ZEROES
         MVI   PAGE+5,C' '
NOBLK    OI    PAGE+6,C'0'        REMOVE DECIMAL SIGN (ZONE)
         PUT   PRDCB,HEAD         PRINT PAGE HEADING
         MVI   0(3),C'0'          SKIP LINE AFTER HEADING
PRNT     PUT   PRDCB,0(3)         PRINT A LINE FROM CALLING PROGRAM
         CLI   PDOFF,X'00'        SHOULD PUNCH & DISK BE BYPASSED?
         BNE   ENDPR              BRANCH IF YES
         LA    1,54(3)            LOAD PUNCH DATA ADR
         ST    1,PLL              put where PUNCH expects it
         L     14,PPUN            get @ PUNCH csect
         CNOP  2,4
         BALR  14,14              ***** PUNCH *****
PLL      DC    F'0'               @ card to punch
ENDPR    MVI   0(3),C' '          CLEAR DATA SOURCE LOCATIONS
         MVC   1(134,3),0(3)      IN CALLING PROGRAM
         NI    PDOFF,X'7F'        clear don't punch this line flag bit
         L     13,SVPR+4          **********
         LM    14,12,12(13)       * RETURN *
         BR    14                 **********
PRERR    MVC   0(44,3),PRMSG      *** PRINTER ERROR ROUTINE ***
         MVC   45(87,3),44(3)     CLEAR PRINT POS BEYOND 45
         B     OKPR               PRINT ERROR MESSAGE & CONTINUE
PPUN     DC    A(PUNCH)           ADR OF PUNCH ROUTINE
T13      DC    F'0'
LCT      DC    F'0'               PRINTER LINE COUNT
FFT      DC    F'60'              MAX LINE COUNT PER PAGE
PDOFF    DC    F'0'               PUNCH & DISK BYPASSED IF not zeroes.
PDOFFPRM EQU   X'80'                don't punch until this cleared flag
PDOFFTMP EQU   X'40'                don't punch this line flag bit
PCT      DC    F'0'               PAGE COUNT
DPCT     DC    D'0'               DECIMAL PAGE COUNT
SVPR     DC    18F'0'             PRINT-READ SAVE AREA
PRMSG    DC    CL45' *** PRINTER ERROR *** (CHECK PREVIOUS LINE) '
HEAD     DC    CL48'1   LOC  OBJECT CODE          ADDR1     ADDR2 '
         DC    CL55' STMT          SOURCE STATEMENT   DEC99 '
VER      DC    CL19'TIMING  TOTAL TIME '
PAGE     DC    CL10'PAGE '
MSG      DC    CL132'1 ESA/390 DISASSEMBLER PROCESSING MESSAGES'
         EJECT
PRDCB    DCB   DDNAME=PRDD,DSORG=PS,MACRF=(PM),RECFM=FA,EROPT=ACC,SYNADX
               =PRERR,BLKSIZE=132,LRECL=132
         END
