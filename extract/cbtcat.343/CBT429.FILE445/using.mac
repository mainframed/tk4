USIN     TITLE 'USING '
***********************************************************************
*                                                                     *
*        keep track of what USINGs are active at any time             *
*                                                                     *
*  called by PASS4, ADDR, SPPR and RR                                 *
*                                                                     *
*  input addressed by R14                                             *
*    +0  hex flag, 00 - drop, 01 or 02 - insert, 03 - retrieve        *
*    +2  1 byte address part of instruction                           *
*    +3  1 byte address part of instruction                           *
*    +4  3 byte current loc (?) - if insert                           *
*    +7  1 byte instruction format from AXN table                     *
*    +8  callers r13                                                  *
*    +12 return to here                                               *
*                                                                     *
*  output addressed by R14                                            *
*    +0  1 byte from IC                                               *
*    +1  3 bytes from IC (loc of using?)                              *
*                                                                     *
***********************************************************************
USING    CSECT
         BALR  13,0               setup base reg
         USING *,13               tell assembler
         L     3,0(14)            get input
         SLL   3,16               ) mask out
         SRL   3,28               ) reg no.
         SLL   3,3                mul by 8
         A     3,AUS              calc table loc
         C     3,AUS              IS REGISTER ZERO ?
         BE    RETR2              yes - can't be a base reg
         TM    0(14),X'03'        NO - what sort of call is it ?
         BZ    DROP               drop call
         BO    RETR               retrieve
         MVC   1(4,3),4(14)       STORE IC IN TABLE
         MVI   0(3),X'01'         flag this entry in use
         B     RETR               and exit
DROP     MVC   0(8,3),DUZ         DROP REGISTER by clearing entry
RETR     MVC   1(3,14),1(3)       pass back location of using
         MVC   0(1,14),4(3)       pass back instruction format from AXN
RETR3    L     13,8(14)           reload callers base reg
         BAL   14,12(14)          RETURN
RETR2    MVC   0(4,14),DUZ        REGISTER IS ZERO
         B     RETR3              go to return
         DC    C'IMM='
DUZ      DC    D'0'               constant zero
AUS      DC    A(USTB)
USTB     DC    16D'0'             table of usings
* each entry is as follows
*   1 byte flag, x'01' if using active for this register, else x'00'
*   3 bytes instruction counter+2 when using set up
*   1 byte instruction format from AXN table
*   3 bytes padding.
         END
