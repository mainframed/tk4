PR       TITLE 'PR  '
***********************************************************************
*                                                                     *
*        format object code in listing                                *
*                                                                     *
*        as xx xx             - 2 byte instructions                   *
*           xx xx x xxx       - 4 byte instructions                   *
*           xx xx x xxx x xxx - 6 byte instructions                   *
*                                                                     *
*  input: addresed by R14                                             *
*    +0  - @ output area                                              *
*    +4  - instruction to format                                      *
*    +12 - instruction length                                         *
*    +16 - callers R13 (callers base reg)                             *
*    +20 - return to here                                             *
*                                                                     *
***********************************************************************
PR       CSECT
         BALR  13,0               setup base reg
         USING *,13               tell assembler
         EXTRN CXN
         ST    14,PR14            save @ parms
         MVC   PAX(4),4(14)       pass value to convert to hex
         ST    13,PAX+8           put r13 where cxn expects it
         L     14,PACXN           get @ cxn csect
         CNOP  6,8
         BALR  14,14              call cxn
PAX      DC    D'0'               o/p in hex
         DC    F'0'               cxn reloads r13 from here
         L     14,PR14            reload @ parms
         L     3,0(14)            get @ output area
         MVC   9(2,3),PAX         output first byte
         MVC   12(2,3),PAX+2      output second byte
         L     4,12(14)           get instruction length
         S     4,PTWO             sub 2 (bytes processed so far)
         BZ    PRET               if RR instr exit
         MVC   15(1,3),PAX+4      RX, RS, SI, AND SS
         MVC   17(3,3),PAX+5      INSTRUCTIONS
         S     4,PTWO             finished formatting instr ?
         BZ    PRET               if so exit
         MVC   PAX2(4),8(14)      SS INSTRUCTIONS - unpack next 2 bytes
         ST    13,PAX2+8          put r13 where cxn expects it
         L     14,PACXN           get @ cxn csect
         CNOP  6,8
         BALR  14,14              call cxn
PAX2     DC    D'0'               hex o/p from cxn
         DC    F'0'               cxn reloads r13 frrm here
         L     14,PR14            reload @ parms
         L     3,0(14)            reload @ output
         MVC   21(1,3),PAX2       base reg
         MVC   23(3,3),PAX2+1     displacement
PRET     L     13,16(14)          RETURN
         BAL   14,20(14)
         DC    C'IMM='
PR14     DC    F'0'               save area for r14
PTWO     DC    F'2'               constant 2
PACXN    DC    A(CXN)             @ cxn csect
         END
