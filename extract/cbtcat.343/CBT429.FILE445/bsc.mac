BSC      TITLE 'BSC '
***********************************************************************
*                                                                     *
*     Binary search routine                                           *
*                                                                     *
*  Called by PASS3, PASS4, SAVA, SPPR                                 *
*                                                                     *
*  Input: 12 bytes pointed to by R14                                  *
*    +0 - input byte x'00'                                            *
*    +1 - 3 byte string to search for                                 *
*    +4 - output only                                                 *
*    +8 - Callers R13                                                 *
*  Output: overlays input                                             *
*    C' ' if entry not found                                          *
*    8 bytes of data if found                                         *
*  Return is to R14+12                                                *
*  Uses R3-R7, these are NOT restored at exit.                        *
*    PASS3 uses the value placed in R3                                *
***********************************************************************
BSC      CSECT
         BALR  13,0
         USING *,13
         ENTRY NAVBS              reffed by PASS3
         L     3,NAVBS
         L     7,NUMX
         L     5,NUMX
         SR    4,4
         L     6,NUMX
         S     6,BONE             - 1
         BM    PSPB2
         SLL   6,2
         AR    3,6
         CLC   1(3,14),0(3)
         BH    PSPB2
         BE    FND2
SRB      AR    7,4                BINARY SEARCH
SRB2     SRA   7,1
         SLL   7,2
         L     3,NAVBS
         AR    3,7
         CLC   1(3,14),0(3)
         BE    FND2
         BL    HIGH2
         SRL   7,2
         CR    4,7
         BE    PSPB2
         LR    4,7
         AR    7,5
         B     SRB2
HIGH2    SRL   7,2
         CR    5,7
         BE    PSPB2
         LR    5,7
         B     SRB
FND2     S     3,NAVBS
         SLL   3,1
         A     3,BCDVE
         MVC   0(8,14),0(3)
BRET     L     13,8(14)
         B     12(14)             RETURN
PSPB2    MVI   0(14),C' '         ADDRESS NOT IN TABLE
         B     BRET
         DC    C'IMM='
BONE     DC    F'1'
NAVBS    DC    F'0'               ) Input filled
NUMX     DC    F'0'               )  in by
BCDVE    DC    F'0'               )   PASS3
         END
