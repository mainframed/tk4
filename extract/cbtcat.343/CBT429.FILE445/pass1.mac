PASS1    TITLE 'PASS1'
PASS1    CSECT
         BALR  15,0               establish base reg
         USING *,15               tell assembler
         ENTRY LCF                ref'ed by TXT
         ENTRY PA                 Print Area used by most csects
         ENTRY PAB                ref'ed by PASS4, RR, FTXTA and SPPR
         ENTRY CARD               input card put here by READA
         EXTRN READ               in PRINT csect
         EXTRN PRINT              csect
         EXTRN ESD                csect
         EXTRN TXT                csect
         EXTRN RLD                csect
         EXTRN END                in TXT csect
         EXTRN PASS4              csect
         EXTRN COTR               csect
INIT     MVC   PB+1(135),PB       INITIALIZE PROG
         L     3,ACOTR            get @ COTR csect
         MVI   0(3),X'00'         ) clear
         MVC   1(14,3),0(3)       )  COTR
SKIP     MVI   LCF,X'00'          clear END found flag
READP    CLI   LCF,X'FF'          has TXT found an END card
         BE    BPASS              br. if so
NRP      L     14,AREAD           get @ READ in PRINT csect
         CNOP  2,4
         BALR  14,14              call READ
         DC    A(CARD)            @ input buffer
         CLI   RFG,X'FF'          1st time through here ?
         BE    NR                 br if not 1st time
         L     14,ACOTR           get @ COTR csect
         MVI   0(14),X'00'        put x'00' into COTR
         CLI   CARD,C'S'          'S' card ?
         BE    NPRT               br. if yes
         CLI   CARD,C'P'          'P' card ?
         BNE   NP                 br. if not
         MVI   0(14),X'01'        yes, put x'01' into COTR
         MVI   RFG,X'FF'          flag not 1st time through
         B     NPRT               and get next card
NP       CLI   CARD,C'R'          'R' card ?
         BNE   NR                 no - process as text card
         MVI   0(14),X'02'        yes, put x'02' into COTR
         MVI   RFG,X'FF'          flag not 1st time through
NPRT     MVC   1(14,14),CARD+1    copy rest of card to COTR
         B     NRP                read next card
NR       LM    3,5,TERC           get BXLE parms
         MVI   RFG,X'FF'          flag not 1st time through
COM      CLC   CARD(4),0(3)       DETERMINE TYPE
         BE    TERE               OF CARD
         BXLE  3,4,COM            try next entry
TEER     MVC   PA(25),TERM        PRINT ERROR
         MVC   PA+30(80),CARD
         L     14,APRINT          MESSAGE
         CNOP  2,4
         BALR  14,14              call PRINT
         DC    A(PA)              @ print area
         B     READP              and get next card
* an END card has been found
BPASS    L     3,APASS            CALL SECOND
         BALR  14,3               PASS (PASS4 csect)
         B     INIT               and process next csect
TERE     S     3,TERC             CALL ROUTINE TO
         LA    3,TXTA(3)
         L     3,0(3)             HANDLE CARD
         MVC   ACE(4),ACARD
         CNOP  2,4                TYPE
         BALR  14,3
ACE      DC    F'0'               @ input card
         DC    A(PA)              @ print buffer
         B     READP              and get next card
         DC    C'IMM='
ACARD    DC    A(CARD)            @ input card
TERC     DC    A(TXTC)            BXLE parms to search TXTC table
         DC    F'4'                 length of table entry
         DC    A(ENDC)              @ last entry
TXTC     DC    X'02'              CARD TYPES, table scanned by BXLE
         DC    C'TXT'             text
         DC    X'02'
         DC    C'ESD'             external symbol directory
         DC    X'02'
         DC    C'RLD'             relocation directory
ENDC     DC    X'02'
         DC    C'END'             end of csect
TXTA     DC    A(TXT)             HANDLING
         DC    A(ESD)             ROUTINES
         DC    A(RLD)
         DC    A(END)               (in TXT csect)
APASS    DC    A(PASS4)           @ PASS4 csect
AREAD    DC    A(READ)            @ READ in PRINT csect
APRINT   DC    A(PRINT)           @ PRINT csect
ACOTR    DC    A(COTR)            @ COTR csect
RFG      DC    F'0'               flag to ind. 1st time through
TERM     DC    C'**  NON-RECOGNIZABLE CARD'
LCF      DC    X'00'              entry point, set by END when called
CARD     DS    CL80               entry point, input card read by READ
         DC    C'READ ERROR OCCURED. DISASSEMLE ERROR MAY OCCUR '
PB       DC    C' '
PA       DS    CL135              entry point, common print area
PAB      DC    34C'    '          entry point, second print area
         DS    0F
         END
