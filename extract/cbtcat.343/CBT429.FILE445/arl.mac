ARL      TITLE 'Format ADCONs in the listing'
***********************************************************************
*                                                                     *
*  called by PASS4, FTXTA and SPPR                                    *
*                                                                     *
*  parms @ r14:                                                       *
*   +0  - @ print area                                                *
*   +4  - unused by ARL                                               *
*   +6  - ADCON length                                                *
*   +8  - ADCON TEXT                                                  *
*   +12 - unused by ARL (next 4 bytes of TEXT)                        *
*   +16 - instruction counter                                         *
*   +19 - copy of AXNFLG1 (X'6E')                                     *
*   +20 - return to here                                              *
*                                                                     *
***********************************************************************
         FMTPARMS                 copy format routine parameters
ARL      CSECT
         BALR  13,0               setup base reg
         USING *,13               tell assembler
         ENTRY SBCZ               ref'ed by PASS4
         ENTRY COMF               not used by any csect
         EXTRN SAVA               csect
         EXTRN CXN                csect
         EXTRN NEXT               in ESD2 csect
         EXTRN LCOM               in ESD5 csect
         EXTRN COFST              in FTXTA csect
         MVI   COMF,X'00'
         MVC   CXNCA(4),8(14)     pass parms to CXN
         MVC   ASAVZ(3),9(14)     pass parms to SAVA
         ST    14,AAR14           save @ parms
         ST    13,CXNCA+8         put r13 where CXN expects it
         L     14,ARCXN           CONVERT CONST TO
         CNOP  6,8                BCD FORMAT
         BALR  14,14              call CXN
CXNCA    DC    D'0'
         DC    F'0'               our r13 reloaded from here by CXN
         L     14,AAR14           reload @ parms
         L     4,0(14)            get @ print area
         MVC   9(8,4),CXNCA       copy const to print area
         MVC   64(2,4),DCR        copy c'DC'
         MVC   70(2,4),DCR+2      copy c'A('
         CLC   FX+2(2),6(14)      is ADCON 4 bytes long ?
         BE    FIR                br. if yes
         MVI   71(4),C'L'         else add L
         OI    7(14),C'0'         make length printable
         MVC   72(1,4),7(14)      copy it to print area
         NI    7(14),X'0F'        restore length
         SR    3,3
         ST    3,ASAVZ            put in SAVA parms
         IC    3,7(14)            get ADCON length
         S     3,FX               subtract 4
         LPR   3,3
         LA    5,ASAVZ-1          get @ SAVA parms -1
         AR    5,3
         MVC   0(3,5),8(14)       copy ADCON TEXT
         LA    5,9(4)
         SR    3,3
         IC    3,7(14)            get ADCON length
         SLL   3,1                double it
         AR    5,3
         MVC   0(4,5),BLXX        copy spaces
         MVI   73(4),C'('
         LA    4,2(4)
FIR      CLI   7(14),X'04'        4 byte ADCON ?
         BNE   NFA                branch if not
         TM    8(14),X'FF'
         BZ    NFA
         TM    9(14),X'F0'
         BO    NFA
         MVC   70(2,4),XPR        copy X'
         MVC   72(2,4),CXNCA      copy const to print area
         MVI   74(4),X'7D'
         MVI   75(4),C','
         MVC   76(4,4),XPR+2      copy c'AL3('
         LA    4,8(4)
NFA      MVI   SBCZ,X'FF'
         CLC   12(2,14),14(14)    is it an EXTRN ?
         BE    LOCE               br if not
         L     3,ANEXT            YES, get @ NEXT in ESD2
         L     5,0(3)             get number of EXTRNs read
         LA    3,4(3)             get @ table of EXTRNS
CEX      CLC   12(2,14),8(3)      FIND BCD NAME
         BE    EEX                br if match
         LA    3,15(3)            else get @ next entry
         BCT   5,CEX              br if more to search
         MVC   90(13,4),MET       copy c'MISSING EXTRN   '
EEX      MVC   72(8,4),0(3)       copy EXTRN name
         MVC   SBCZ+1(8),0(3)
         CLI   72(4),C' '
         BNE   RBL
         MVI   COMF,X'10'
         OI    ASAVZ,X'10'        change SAVA parms
         B     LOCE
RBL      LA    5,7
         LA    3,79(4)
CEE      CLI   0(3),C' '
         BNE   INSS
         BCT   3,NER
NER      BCT   5,CEE
INSS     STC   5,SBCZ
         CLI   8(14),X'FF'
         BE    MINE
         TM    19(14),X'10'
         BO    MINE
         MVI   1(3),C'+'
         B     NMINE
MINE     MVI   1(3),C'-'
NMINE    LA    6,4
         L     5,8(14)
         SH    6,6(14)            subtract ADCON length
         LPR   6,6
         SLL   6,3
         STC   6,SOFT+3           *modify instruction*
SOFT     SRL   5,0
         SLL   5,8
         SRA   5,8
         LTR   5,5
         BZ    RPE
         LA    3,1(3)
LOC2     CVD   5,OFS
         UNPK  OF2(4),OFS+4(4)
         OI    OF2+3,C'0'
         MVC   1(4,3),OF2
         LA    3,4(3)
RPE      MVI   1(3),C')'
         BAL   14,20(14)          RETURN
LOCE     ST    4,TMA4
         MVI   ASAVZ+3,X'60'      ind. type of address to SAVA
         ST    13,ASAVZ+8         put r13 where SAVA expects it
         TM    ASAVZ,X'10'
         BO    LSA
         L     3,AOFST            get @ COFST in FTXTA
         L     3,0(3)
         SLL   3,8
         A     3,ASAVZ
         ST    3,ASAVZ
LSA      L     14,SAVZ            get @ SAVA csect
         CNOP  2,4
         BALR  14,14              call SAVA
ASAVZ    DC    F'0'               input addresss
         DC    F'0'
         DC    F'0'               r13 reloaded from here
         L     14,AAR14           reload @ parms
         L     4,TMA4
         MVC   72(8,4),ASAVZ      copy address label
         LA    5,7
         LA    3,79(4)
CEE2     CLI   0(3),C' '
         BNE   INSA
         BCT   3,NER2             subtract 1 from r3
NER2     BCT   5,CEE2
INSA     MVI   1(3),C')'
         BAL   14,20(14)          RETURN
         DC    C'IMM='
SAVZ     DC    A(SAVA)            @ SAVA csect
ARCXN    DC    A(CXN)             @ CXN csect
FX       DC    F'4'               constant
ANEXT    DC    A(NEXT)            @ NEXT in ESD2
AOFST    DC    A(COFST)           @ COFST in FTXTA
OFS      DC    D'0'
OF2      DC    F'0'
AAR14    DC    F'0'               r14 on entry saved here
DCR      DC    C'DCA('
BLXX     DC    C'    '            constant
TMA4     DC    F'0'
XPR      DC    X'E77D'            X'
         DC    C'AL3('
COML     DC    C'COM '
MET      DC    C'MISSING EXTRN   '
ALCOM    DC    A(LCOM)            @ LCOM in ESD5 csect
DWRD     DC    D'0'
SBCZ     DC    3F'0'              entry point
COMF     DC    X'00'              entry point
         END
