LPT     TITLE 'FLOATING POINT CONVERSION'
***********************************************************************
*                                                                     *
*  called by CONST                                                    *
*                                                                     *
*  parms @ r14:                                                       *
*    +0  - @ Print Area                                               *
*    +4  - input TEXT, 4 or 8 bytes                                   *
*    +12 - flag, x'00' or x'80'                                       *
*    +16 - callers r13                                                *
*    +20 - return to here                                             *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
FLPT     CSECT
         BALR  13,0                establish base reg
         USING *,13                tell assembler
         MVC   TM1(8),4(14)
         MVC   FSO(2),ZFP
         TM    TM1,X'80'
         BZ    PFP
         MVI   FSO,X'01'
         NI    TM1,X'7F'
PFP      LD    0,TN16
         LD    2,TM1
         SR    1,1
         CLC   TM1+1(3),ZFP
         BE    MZER
         LA    2,16
COMF     CLC   TM1(8),TN17
         BNL   DVFP
         CLC   TM1(8),TN1
         BH    NPMF
         MDR   2,0
         STD   2,TM1
         SR    1,2
         B     COMF
DVFP     DDR   2,0
         STD   2,TM1
         AR    1,2
         B     COMF
NPMF     LA    3,TN16
         LA    4,5
         MVC   TM2(8),TM1
COMF2    CLC   TM2(8),TN17
         BL    NMFP2
         AR    1,2
         MVC   TM2(8),TM1
NMFP2    MVC   TM1(8),TM2
         SRL   2,1
         LA    3,8(3)
         LD    0,0(3)
         LD    2,TM2
         MDR   2,0
         SR    1,2
         STD   2,TM2
         BCT   4,COMF2
         CLI   TM1,X'4F'
         BNE   NOF
         MVI   FSO+1,X'01'
NOF      MVI   TM1,X'00'
         LM    2,3,TM1
         TM    FSO+1,X'01'
         BZ    NSFP
         SLDL  2,4
         B     NFFP
NSFP     SR    4,4
         NI    TM2,X'0F'
         IC    4,TM2
         S     4,FFP               subtract 14
         LPR   4,4
         SLL   4,2
         STC   4,SLFP+3
SLFP     SRDL  2,0
NFFP     D     2,TNT
         CVD   2,DWFP
         UNPK  FDP+8(9),DWFP+3(5)
         OI    FDP+16,C'0'
         CVD   3,DWFP
         UNPK  FDP(8),DWFP+3(5)
         OI    FDP+7,C'0'
         LA    3,17
         LA    2,FDP
CLFP     CLI   0(2),C'0'
         BNE   NMFZ
         LA    2,1(2)
         BCT   3,CLFP
NMFZ     AR    1,3
         L     4,0(14)             get @ Print Area
         MVC   70(4,4),FQD         copy D'0.
         TM    12(14),X'80'
         BO    DPF
         S     3,EIGHF             subtract 8
         MVI   70(4),C'E'
DPF      CLI   FSO,X'01'
         BNE   DPF2
         MVI   72(4),C'-'
         MVC   73(2,4),FQD+2       copy 0.
         LA    4,1(4)
DPF2     EX    3,MVFZ
         AR    4,3
         MVI   73(4),C'E'
         MVI   74(4),C'+'
         LTR   1,1
         BNM   PEXP
         MVI   74(4),C'-'
PEXP     CVD   1,DWFP
         UNPK  75(2,4),DWFP+6(2)
         OI    76(4),C'0'
         MVI   77(4),X'7D'
         L     13,16(14)           reload callers r13
         BAL   14,20(14)           and return
MZER     L     4,0(14)
         MVC   70(5,4),ZERPT       copy E'0.'
         CLI   FSO,X'01'
         BNE   20(14)              return
         MVI   70(4),C'D'
         BAL   14,20(14)           return to caller
MVFZ     MVC   74(1,4),0(2)        EXecuted instruction
         DC    C'IMM='
TM1      DC    D'0'
TM2      DC    D'0'
TN17     DC    X'4F163457'
         DC    X'85D8A000'
TN16     DC    X'4E2386F2'
         DC    X'6FC10000'
         DC    X'475F5E10'
         DC    X'00000000'
         DC    X'44271000'
         DC    X'00000000'
         DC    X'42640000'
         DC    X'00000000'
         DC    X'41A00000'
         DC    X'00000000'
TN1      DC    X'41100000'
         DC    X'00000000'
DWFP     DC    D'0'
FDP      DC    D'0'
         DC    D'0'
         DC    F'0'
ZFP      DC    F'0'
FFP      DC    F'14'               constant
FSO      DC    F'0'
EIGHF    DC    F'8'                constant
TNT      DC    X'3B9ACA00'         constant
FQD      DC    X'C47DF04B'         D'0.
ZERPT    DC    X'C57DF0'           E'0
         DC    C'.'                .
         DC    X'7D'               '
         END
