CXN      TITLE 'CXN '
***********************************************************************
*                                                                     *
*        convert 4 byte hex number at 0(r14) to 8 bytes bcd starting  *
*        at the same address. Return is to R14+12.                    *
*        R13 is reloaded from R14+8 where the caller should save it.  *
*                                                                     *
*  called by PASS4, SI, FTXTA, SAVA, PR, PASS3, ESD0, ARL, CONST,     *
*            ADDR                                                     *
*                                                                     *
***********************************************************************
CXN      CSECT
         BALR  13,0                setup base reg
         USING *,13                tell assembler
         L     3,0(14)             load number to convert
         LA    5,0(14)             get @ output area
         LA    1,8                 no of nibbles to convert to bytes
SH       SR    2,2                 clear r2
         SLDL  2,4                 put next nibble into it
         A     2,AONE              calc adr of correct table entry
         MVC   0(1,5),0(2)         copy 1 byte to output area
         LA    5,1(5)              point at next output byte
         BCT   1,SH                if not finished loop
         L     13,8(14)            reload callers base register
         BAL   14,12(14)           RETURN
         DC    C'IMM='
AONE     DC    A(HX)               @ lookup table
HX       DC    C'0123456789ABCDEF' hex digit to bcd table
         END
