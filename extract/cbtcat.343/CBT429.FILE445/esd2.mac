ESD2     TITLE 'ESD2'
***********************************************************************
*                                                                     *
*  process ESD type 2 entry, External Reference                       *
*                                                                     *
*  called by ESD, ESD5                                                *
*                                                                     *
*  parms @ r14:                                                       *
*    +0 - @ input card adjusted to make this ESD entry 1st            *
*    +4 - @ print area                                                *
*  @r11 - return address                                              *
*  @r2  - @ print area                                                *
*                                                                     *
***********************************************************************
ESD2     CSECT
         BALR  13,0             load base reg
         USING *,13             tell assembler
         ENTRY SCNE             updated by ESD csect
         ENTRY NEXT             ref'ed by PASS4, ARL,
         EXTRN PRINT            csect
         L     3,NEXT           increment
         LA    3,1(3)             extern count
         C     3,TWED           more than 29 ?
         BL    ARESD            no - ok
         BHR   11               if more than 30 exit now
         L     1,4(14)          get @ print area
         MVC   ESDP2(4),4(14)   pass to PRINT csect
         MVC   0(36,1),ESDM2    msg '**  more than 29 extern refs'
         ST    14,ES214         save r14
         L     14,EPRT2         get @ print csect
         CNOP  2,4
         BALR  14,14            call PRINT
ESDP2    DC    F'0'             @ print area
         L     14,ES214         reload r14
ARESD    ST    3,NEXT           save new extern count
         LA    2,15             multiply
         MR    2,2                by 15
         A     3,AEXT           calc @ next slot in table
         L     1,0(14)          get @ input card
         MVC   8(2,3),SCNE+2    copy extern number
         L     4,SCNE           )
         LA    4,1(4)           ) increment scne
         ST    4,SCNE           )
         L     2,4(14)          get @ print area
         MVC   0(8,3),16(1)     copy referenced name
         MVC   12(3,3),29(1)    copy CM length if called via ESD5
         MVC   11(1,3),24(1)    copy ESD type (prob x'02')
         B     0(11)            return to caller
         DC    C'IMM='
TWED     DC    F'30'
ESDM2    DC    C'**  MORE THAN 29 EXTERN REFERENCES  '
EPRT2    DC    A(PRINT)         @ PRINT csect
AEXT     DC    A(EXTN-15)
SCNE     DC    F'0'             entry point, extern number
ES214    DC    F'0'             r14 saved here
NEXT     DC    F'0'             entry point, number of EXTRNs read
EXTN     DS    8CL1             referenced name
EXTS     DS    2CL1             reference sequence number
         DS    2CL1             1 unused byte, then ESD type, ER or CM
         DS    3CL1             CM length if called via ESD5
         DS    30CL15           space for 15 more entries
         DS    0F
         END
