MDMP     TITLE 'MNEMONIC DUMP PREPARATION                AUG.  1, 1967'
DISINF   CSECT
         STM   14,12,12(13)  SAVE REGISTERS FROM PREVIOUS PROGRAM
         BALR  12,0          ESTABLISH BASE FORE ADDRESSING PURPOSES
         USING *,12
         ST    RSA,SAVE+4    STORE POINTER TO SAVE AREA
         LR    11,RSA        HOLD SAVE ADR JUST USED
         LA    RSA,SAVE      PROVIDE NEW SAVE AREA
         ST    RSA,8(11)     GIVE CALLER THIS SAVE ADR
         LM    CAD,NBD,0(LP)
         CR    CAD,NBD       ARE LIMITS IN ORDER
         BNH   NX            BRANCH IF YES
         XR    CAD,NBD       INTER-
         XR    NBD,CAD       CHANGE
         XR    CAD,NBD       LIMITS
NX       SR    NBD,CAD       NUMBER OF BYTES TO BE DUMPED
         TM    DCB+48,X'10'  IS DATA SET OPEN
         BO    RIN           BRANCH IF YES
         OPEN  (DCB,(OUTPUT)) OPEN DATA SET
         TM    DCB+48,X'10'  WAS OPEN SUCCESSFUL
         BO    RIN           BRANCH IF YES
         ABEND 37,DUMP       DUMP IF UNABLE TO OPEN DATA SET
RIN      MVC   NC(4),NS      RESET CARD COUNT
         ST    CAD,ADDR      PLACE STARTING ADDR ON ESD CARD
         MVC   ESDSA(3),ADDR+1
         C     NBD,LCD       MORE THAN 16K?
         BL    LESS          BRANCH IF NO
         MVC   ESDLN(3),LCD+1 MAKE SECTION LENGTH 16K
         B     ESDIO
LESS     ST    NBD,TEMP      PLACE SECTION LENGTH ON ESD CARD
         MVC   ESDLN(3),TEMP+1
ESDIO    LA    DA,ESDC       PUNCH ESD CARD
         BAL   14,IO
         SR    MBC,MBC       ZERO MAX BYTE COUNT
         SR    BYC,BYC       ZERO BYTE COUNT
         MVC   NBY(2),T38    NUMBER OF BYTES ON TXT CARD=56
LOOP     ST    CAD,ADDR      ADDR OF FIRST BYTE
         MVC   TXT(56),0(CAD) MOVE DATA TO TXT AREA
         MVC   STA(3),ADDR+1 MOVE FIRST ADDRESS
         L     PCC,NC
         LA    PCC,1(PCC)    INCREMENT CARD COUNT
         ST    PCC,NC
         CVD   PCC,DWD       CONVERT TO DECIMAL
         UNPK  CN(3),DWD+6(2)
         OI    CN+2,C'0'     PLACE COUNT IN PUNCH AREA
         LA    MBC,56(MBC)   INCREMENT MAXIMUM BYTE COUNT
         LA    BYC,56(BYC)   INCREMENT BYTE COUNT
         CR    BYC,NBD       HOW MANY BYTES LEFT
         BL    CONT          BRANCH IF MORE THAN 56 BYTES LEFT
         BE    RTNE          BRANCH IF EXACTLY 56 BYTES LEFT
         LA    NBD,56(NBD)   LESS THAN 56 BYTES LEFT
         SR    NBD,BYC       FIND NUMBER OF BYTES LEFT
         MVI   TXT,C' '
         MVC   TXT+1(55),TXT BLANK TXT AREA
         ST    NBD,TEMP
         MVC   NBY(2),TEMP+2 SET NEW COUNT
         BCTR  NBD,0         DECREMENT NBD BY ONE
         EX    NBD,MOVE      MOVE REMAINING BYTES
RTNE     BAL   14,PUNCH      PUNCH TXT CARD
         B     RTNC
CONT     C     MBC,LCD       MORE THAN 16K
         BNE   GON           BRANCH IF NO
         S     NBD,LCD       INITIALIZE FOR NEXT 16K
         BAL   14,PUNCH      PUNCH TXT CARD
         BAL   14,ENDC       PUNCH END CARD
         LA    CAD,56(CAD)   INCREMENT CORE ADDRESS
         B     RIN           BEGIN NEXT SECTION
GON      BAL   14,PUNCH      PUNCH TXT CARD
         LA    CAD,56(CAD)   INCREMENT CORE ADDRESS
         CR    NBD,BYC       END OF DATA
         BNE   LOOP          CONTINUE PUNCHING IF NO
RTNC     BAL   14,ENDC       PUNCH END CARD
         CLOSE (DCB)         CLOSE DATA SET
RETF     L     RSA,SAVE+4    RETURN
         LM    14,12,12(13)
         BR    14
MOVE     MVC   TXT(1),0(CAD)
IO       STM   14,1,SLKG     SAVE LINKAGE REGS
         PUT   DCB,(0)       WRITE ONE LOGICAL RECORD
         LM    14,1,SLKG     RESTORE LINKAGE REGS
         BR    14
PUNCH    LA    DA,PCARD      PUNCH DATA IS TXT CARD
         B     IO            GO TO I/O ROUTINE
ENDC     L     PCC,NC
         LA    PCC,1(PCC)    INCREMENT CARD COUNT
         ST    PCC,NC
         CVD   PCC,DWD       CONVERT TO DECIMAL
         UNPK  ENDSQ(3),DWD+6(2)
         OI    ENDSQ+2,C'0'  FORMAT SEQ. NO. ON END CARD
         LA    DA,ECARD      PUNCH DATA IS END CARD
         B     IO            GO TO I/O ROUTINE
DA       EQU   0             DATA ADDRESS
LP       EQU   1             LIST POINTER
CAD      EQU   2             CORE ADDRESS
NBD      EQU   3             NUMBER OF BYTES DUMPED
PCC      EQU   4             PUNCH CARD COUNT
BYC      EQU   5             BYTE COUNT
MBC      EQU   6             MAXIMUM BYTE COUNT
RSA      EQU   13            SAVE AREA POINTER
ESDC     DC    X'02'         ESD CARD
         DC    CL9'ESD '
         DC    X'0010'       FIELD COUNT
         DC    CL2' '
         DC    X'0001'       ESD I.D. NO.
         DC    CL8'MAIN '    CSECT NAME
         DC    X'00'         ESD TYPE CODE
ESDSA    DC    X'000000'     STARTING ADDRESS
         DC    CL1' '
ESDLN    DC    X'000000'     SECTION LENGTH
         DC    CL45'  '
         DC    CL3'001'      SEQ. NO.
PCARD    DC    X'02'         TXT CARD
         DC    C'TXT '
STA      DC    X'000000'
         DC    C'  '
NBY      DC    X'0038'       BYTE COUNT
         DC    C'  '
         DC    X'0001'
TXT      DS    CL56          TXT INFO
         DC    5C' '
CN       DC    C'000'        CARD COUNT
ECARD    DC    X'02'         END CARD
         DC    CL76'END '
ENDSQ    DC    CL3'000'      SEQ. NO.
DWD      DC    D'0'
ADDR     DC    F'0'
LCD      DC    F'16352'      292 CARDS
NS       DC    F'1'          ONE
NC       DC    F'0'          CARD COUNT
TEMP     DC    F'0'          TEMPORARY
T38      DC    H'56'         56 BYTES ON FULL CARD
SLKG     DS    4F            LINKAGE REGS SAVE AREA
SAVE     DC    3F'0'         SAVE
         DS    15F           AREA
DCB      DCB   DDNAME=OUT,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80,BLKSIZEX
               =0,EROPT=ACC
         END
