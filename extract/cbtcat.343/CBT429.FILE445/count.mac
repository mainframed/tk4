COUNT    TITLE '*** C O U N T ***  (WITH PAGE HEADING)    AUG. 14,1967'
***********************************************************************
*                                                                     *
*   output instruction usage table at end of listing                  *
*                                                                     *
*  called by PASS5                                                    *
*                                                                     *
* history:                                                            *
*   disable punching when printing table                  cfk may 1999*
*                                                                     *
***********************************************************************
         AXNTAB                      Map AXN table
COUNT    CSECT
         EXTRN LCT                   in PRINTA csect
         EXTRN MEN                   @ AXN table
         EXTRN PRINT                 csect
         EXTRN PA                    in PASS1 csect
         EXTRN PDOFF                 in PRINTA csect
         BALR  13,0                  establish base reg
         USING *,13                  tell assembler
         ST    14,CNT14              save r14
         L     8,PAA                 get @ print o/p buffer in PASS1
         MVI   0(8),C' '             clear
         MVC   1(131,8),0(8)          print buffer
         L     2,AAMEN               get @ opcode name table in AXN
         USING AXNTAB,2              tell assembler
         L     9,APDOFF              get @ skip punch flag in PRINTA
         MVI   0(9),X'FF'            flag not to punch this
         SR    9,9                   clear total use this pass
         L     10,TOTAL              get grand total
CCLL     SR    6,6
         L     5,AXNTOTCT            ) add current
         A     5,AXNUSECT            )  use count
         ST    5,AXNTOTCT            )   to total count
         CR    6,5                   use count > 0 ?
         BE    CLLA                  no - skip printing
         SRL   5,1
         CVD   5,DWM                 make total use decimal
         UNPK  FPW(8),DWM(8)           and printable
         OI    FPW+7,C'0'            add sign
         MVC   2(4,8),AXNAME         copy mnemonic to output buffer
         MVC   28(5,8),FPW+3         copy total use to output buffer
         L     5,AXNUSECT            get use count
         AR    9,5                   add to total
         AR    10,5                  add to grand total
         ST    6,AXNUSECT            zero use count
         SRL   5,1
         CVD   5,DWM                 make use count decimal
         UNPK  FPW(8),DWM(8)           and printable
         OI    FPW+7,C'0'            add sign
         MVC   13(5,8),FPW+3         copy to output buffer
         L     14,CLCT               ) inc
         LA    14,1(14)              )  current
         ST    14,CLCT               )   line count
         C     14,MCLCT              >= max line count
         BL    GOCT                  br if not
* else force new page and o/p headers
         L     14,ALCT               get @ printer line count in PRINTA
         MVC   0(4,14),CLCT          set to 59
         MVC   CLCT(4),ZRCT
         L     14,APRINC             get @ print routine (PRINT csect)
         CNOP  2,4
         BALR  14,14
         DC    A(CTMSG+84)           @ header 1
         L     14,APRINC             get @ PRINT csect
         CNOP  2,4
         BALR  14,14
         DC    A(CTMSG)              @ header 2
GOCT     L     14,APRINC             get @ PRINT csect
         CNOP  2,4                   ) print
         BALR  14,14                 )  line
PAA      DC    A(PA)                 @ print o/p buffer in PASS1
CLLA     LA    2,AXNNEXT             @ next opcode table entry
         CLI   AXNAME,C' '           end of table ?
         BNE   CCLL                  no, process next opcode
*                       table finished, output totals
         MVC   0(8,8),TOT          c'0TOTALS'
         SRL   9,1
         CVD   9,DWM               make total decimal
         UNPK  FPW(8),DWM(8)       ) and
         OI    FPW+7,C'0'          )  printable
         MVC   13(5,8),FPW+3       copy to Print Area
         ST    10,TOTAL            save grand total
         SRL   10,1
         CVD   10,DWM              make it decimal
         UNPK  FPW(8),DWM(8)       ) and
         OI    FPW+7,C'0'          )  printable
         MVC   28(5,8),FPW+3       copy to Print Area
         L     14,APRINC           get @ PRINT csect
         CNOP  2,4
         BALR  14,14               call PRINT
         DC    A(PA)               @ print o/p buffer in PASS1
         L     9,APDOFF            get @ skip punch flag in PRINTA
         MVI   0(9),X'00'          re-enable punching
         L     14,CNT14            get return address
         BR    14                  and exit
         DC    C'IMM='
CLCT     DC    F'59'               current line on page counter
MCLCT    DC    F'57'               max lines per page
ZRCT     DC    F'0'
TOTAL    DC    F'0'                grand total
APDOFF   DC    A(PDOFF)            skip punch flag in PRINTA
AAMEN    DC    A(MEN)              opcode name table in AXN
DWM      DC    D'0'
FPW      DC    D'0'
APRINC   DC    A(PRINT)            @ print routine (PRINT csect)
CNT14    DC    F'0'
ALCT     DC    A(LCT)              @ LCT in PRINTA csect
TOT      DC    C'0TOTALS '
CTMSG    DC    CL132'0INSTR   CURRENT USAGE   TOTAL USAGE '
         DC    CL84'--- INSTRUCTION USAGE TABLE --- '
         END
