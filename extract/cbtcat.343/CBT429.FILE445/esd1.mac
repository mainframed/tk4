ESD1     TITLE 'ESD1 '
***********************************************************************
*                                                                     *
*  process ESD type 1 entry, Label Definition                         *
*                                                                     *
*  called by ESD                                                      *
*                                                                     *
*  parms @ r14:                                                       *
*    +0 - @ input card adjusted to make this ESD entry 1st            *
*    +4 - @ print area                                                *
*  @r11 - return address                                              *
*  @r2  - @ print area                                                *
*                                                                     *
*  This csect fills a table of entry points in the input module.      *
*  The table is at entry NENT, which is a fullword count of the       *
*  number of entry points followed by the entries.                    *
*                                                                     *
*  History:                                                           *
*    added check for table overflow                       CFK jun 1999*
*                                                                     *
***********************************************************************
ESD1     CSECT
         BALR  13,0                  establish base reg
         USING *,13                  tell assembler
         ENTRY NENT                  ref'ed by PASS4, PASS3
         EXTRN PRINT                 csect
         L     3,NENT                )
         LA    3,1(3)                ) increment nent
         CH    3,=AL2(MAXENTRY)      table full ?
         BL    OK                    branch if table not full
         MVC   ERRMENT,16(1)         else copy ignored entry point
         MVC   0(ERRMSGL,2),ERRMSG   copy message to print area
         ST    14,ES114              save our r14
         L     14,EPRT1              get @ PRINT csect
         BALR  14,14                 call PRINT
         DC    V(PA)                 @ Print Area
         L     14,ES114              reload r14
         BR    11                    and return to caller
OK       DS    0H
         ST    3,NENT                )
         LA    2,13                  get table entry length
         MR    2,2                   calc offset into table
         A     3,AENT                calc @ entry table slot
         L     1,0(14)               get @ input card
         L     2,4(14)               get @ print area
         MVC   0(8,3),16(1)          copy label name
         MVC   8(2,3),29(1)          copy ?
         MVC   10(3,3),25(1)         copy label address
         B     0(11)                 return to caller
         DC    C'IMM='
EPRT1    DC    A(PRINT)              @ print csect
AENT     DC    A(ENTN-13)            fiddled @ label tables
ES114    DC    F'0'
ERRMSG   DC    C' TWO MANY ENTRY POINTS, IGNORING '
ERRMENT  DC    CL8' '
ERRMSGL  EQU   *-ERRMSG
NENT     DC    F'0'                  entry point, number of labels
ENTN     DS    8CL1                  label name
ENTS     DS    2CL1                  label ?
ENTA     DS    3CL1                  label address
ENTLN    EQU   *-ENTN                length of one table entry
         DS    16CL13                space for 16 more labels
MAXENTRY EQU   (*-ENTN)/ENTLN        number of slots in table
         DS    0F
         END
