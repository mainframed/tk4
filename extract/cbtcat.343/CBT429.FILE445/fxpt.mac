FXPT     TITLE 'FXPT'
***********************************************************************
*                                                                     *
*  format fixed point constants in output                             *
*                                                                     *
*  called by CONST                                                    *
*                                                                     *
*  parms @ r14                                                        *
*    +0 - @ Print Area                                                *
*    +4 - output constant                                             *
*    +8 - callers r13                                                 *
*    +12 - return to here                                             *
*                                                                     *
***********************************************************************
FXPT     CSECT
         BALR  13,0                   establish base reg
         USING *,13                   tell assembler
         L     4,0(14)                get @ print area
         MVC   70(2,4),FQ             copy F'
         TM    4(14),X'80'            negative number ?
         BZ    NFN                    br if not
         MVI   72(4),C'-'             else add - sign
         LA    4,1(4)                 and increment output pointer
NFN      L     2,4(14)                get constant
         LPR   2,2                    make it positive
         CVD   2,DWFP2                make decimal
         UNPK  DWD2-2(10),DWFP2+2(6)  make it ebcdic
         OI    DWD2+7,C'0'            make sign printable
         LA    2,DWD2-2               get @ start of output area
         LA    3,9                    get max len - 1
FZL      CLI   0(2),C'0'              )
         BNE   NFZ                    ) skip over
         LA    2,1(2)                 ) leading zeroes
         BCT   3,FZL                  )
NFZ      EX    3,MVFZ2                move number
         AR    4,3                    calc @ end of number
         MVI   73(4),X'7D'            add final '
         L     13,8(14)               reload callers r13
         BAL   14,12(14)              return
MVFZ2    MVC   72(1,4),0(2)           EXecuted instruction
         DC    C'IMM='
DWFP2    DC    D'0'                   CVD work area
FQ       DC    X'C67D'                F'
         DC    F'0'                   extension of next field
DWD2     DC    D'0'                   ebcdic work area
         END
