SAVA     TITLE 'SAVA '
***********************************************************************
*                                                                     *
*  Keep track of label references                                     *
*                                                                     *
*  Called by ARL, PASS3, PASS4, ADDR, SPPR                            *
*                                                                     *
*  Input addressed by R14:                                            *
*    +0 - 4 bytes to put into table                                   *
*    +4 -                                                             *
*    +8 - callers r13                                                 *
*   +12 - return to here                                              *
*                                                                     *
*  Output addressed by R14:                                           *
*    +0 - 8 byte symbol                                               *
*                                                                     *
***********************************************************************
SAVA     CSECT
         BALR  13,0               establish base reg
         USING *,13               tell assembler
         ENTRY AST                ref'ed by PASS3
         ENTRY NAST               ref'ed by PASS3
         EXTRN CXN                csect
         EXTRN BSC                csect
         EXTRN PA                 in PASS1 csect
         EXTRN PRINT              csect
         ST    14,S14             save r14
         CLI   SVK,X'FF'          on to 3rd pass ?
         BE    SVXR               br. if yes, all inserts done
         L     3,NAST             get number of entries in use
         C     3,SV100            full ?
         BL    LASTV              no - OK
         BH    SVXR               yes - ignore this call
         L     3,SPAG             get @ PA in PASS1 csect
         MVI   0(3),C' '          ) clear
         MVC   1(132,3),0(3)      )  Print Area
         MVC   0(35,3),SEM        copy error message
         L     14,SVPRT           get @ PRINT csect
         CNOP  2,4
         BALR  14,14              call PRINT
SPAG     DC    A(PA)              @ PA in PASS1 csect
         L     14,S14             reload R14
         L     3,NAST             reload r3
LASTV    LA    3,1(3)             ) add 1 to
         ST    3,NAST             ) entry count
         L     4,AST              get @ 1st free entry
         MVC   0(4,4),0(14)       copy new entry
         LA    4,4(4)             ) bump last
         ST    4,AST              ) entry pointer
SVXR     MVC   SAX(4),0(14)       pass input to CXN
         CLI   SVK,X'FF'          PASS4 ?
         BE    RTSB               br. if yes
RSTV     ST    13,SAX+8           NO, put r13 where CXN expects it
         L     14,SCXN            FIND BCD EQUIV.
         CNOP  6,8
         BALR  14,14              call CXN
SAX      DC    D'0'
         DC    F'0'               our r13
         L     14,S14             reload r14
         L     3,0(14)            get input
         MVC   0(8,14),BLK        clear output to spaces
         SRL   3,28               ) make high
         LA    3,193(3)           )  4 bits
         STC   3,0(14)            )   into A-I
         MVC   1(5,14),SAX+1      output rest of symbol
         L     13,8(14)           RETURN
         BAL   14,12(14)
RTSB     ST    13,BCDA+8          put r13 where BSC expects it
         SR    3,3                ) set up
         ST    3,BCDA             )  search
         MVC   BCDA+1(4),SAX      )   parms
         L     14,ABSC            FIND BCD SYMBOL
         CNOP  2,4
         BALR  14,14              call BSC
BCDA     DC    F'0'
         DC    F'0'
         DC    F'0'               our r13 reloaded from here by BSC
         L     14,S14             reload R14
         CLI   BCDA,C' '          IS  HERE A SYMBOL
         BE    RSTV               br. to generate symbol if not
         MVC   0(8,14),BCDA       YES - return it
         L     13,8(14)           RETURN
         BAL   14,12(14)
         DC    C'IMM='
ABSC     DC    A(BSC)             @ BSC csect
SCXN     DC    A(CXN)             @ CXN csect
S14      DC    F'0'
BLK      DC    C'        '
SV100    DC    F'999'             max entries in table
SEM      DC    C'**  MORE THAN 1000 LABEL REFERENCES'
SVPRT    DC    A(PRINT)           @ PRINT csect
AST      DC    A(STT)             entry point, @ 1st free entry
SST      DC    A(STT)
SVK      DC    F'0'               set by PASS3
NAST     DC    F'0'               entry point, number of entries used
STT      DS    500F               table
         DS    500F
         END
