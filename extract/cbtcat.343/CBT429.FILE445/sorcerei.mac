         TITLE 'SORCERER - P.J.CHISLETT 31/10/72'
* THIS PROGRAM PRODUCED BY BRITISH STEEL CORPORATION CORBY
* REFER ANY PROBLEMS TO MALCOLM SISMEY TEL 053-66-2121 EXT 729
*
* MODIFIED BY C.KENDON TO CALL DELINKI AND READ SYSLIN INSTEAD
* OF CALLING DELINK0 AND READING SYSPUNCH
*
*     SORCERER CALLS DELINKI TO PRODUCE A FILE OF OBJECT MODULES.
*     THIS FILE IS COPIED TO A WORK FILE AN OBJECT MODULE AT A TIME.
*     EACH OBJECT MODULE IS INPUT INTO THE DISASSEMBLER PROGRAM.
*     AFTER DISASSEMBLY, CONTROL IS RETURNED TO SORCERER FOR THE
*     NEXT OBJECT MODULE TO BE COPIED.
         SPACE 5
SORCEREI CSECT
         ENTER
         LINK  EPLOC=DELINKI           LINK TO DELINKI
         SPACE 2
         OPEN  (OBJMODS)
         SPACE 4
NEXTOBJ  OPEN  (WORKDCB,(OUTPUT))
         LA    2,0                     INITIALIZE OBJ.MOD COUNT
GETREC   GET   OBJMODS
         LR    0,1
         PUT   WORKDCB                 COPY OBJMOD RECORD TI WORKFULE
         CLC   0(4,1),ENDC             END OF OBJ.ODULE/Q
         BNE   GETREC                  NO - COPY NEXT RECORD
         CLOSE (WORKDCB)
         SPACE 4
*    DISASSEMBLE OBJECT MODULE
         LINK  EP=DISAS
         LA    2,1(2)                  INCREMENT OBJ. MOD COUNT
         STH   2,*-6                   INSERT COUNT IN LINK ID.
         B     NEXTOBJ
         SPACE 5
EOJ      CLOSE (WORKDCB,,OBJMODS)
         L     13,4(13)
         RETURN (14,12),,RC=0
         EJECT
ENDC     DC    X'02',C'END'
DELINKI  DC    CL8'DELINKI'
OBJMODS  DCB   DDNAME=SYSLIN,DSORG=PS,MACRF=(GL),EODAD=EOJ
         SPACE 5
WORKDCB  DCB   DDNAME=RDDD,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80
         END
