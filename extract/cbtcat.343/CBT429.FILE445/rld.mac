RLD      TITLE 'RLD '
***********************************************************************
*                                                                     *
*  process RLD card from input                                        *
*                                                                     *
*  called by PASS1                                                    *
*                                                                     *
*  parms @ r14:                                                       *
*    +0 - @ input card                                                *
*    +4 - @ print area                                                *
*    +8 - return to here                                              *
*                                                                     *
***********************************************************************
RLD      CSECT
         BALR  13,0               load base reg
         USING *,13               tell assembler
         ENTRY NBR                ref'ed by CRLD
         EXTRN PA                 Print Area in PASS1 csect
         EXTRN PRINT              csect
         CLC   NBR(4),RL200       rld buffer full ?
         BL    SRLD               no - ok
         CLI   ORL,X'FF'          msg o/p before ?
         BE    8(14)              yes - exit
         ST    14,RL14            save r14
         L     3,LDPG             get @ print area
         MVC   0(27,3),RLDER      copy msg '** more than 250 rld cards'
         L     14,XRPT            get @ PRINT csect
         CNOP  2,4
         BALR  14,14              call PRINT
LDPG     DC    A(PA)              @ print area in pass1
         MVI   ORL,X'FF'          flag msg o/p
         L     14,RL14            reload @ parms
SRLD     L     3,NRLD             @ current loc in rld buffer
         L     1,0(14)            get @ input card
         MVC   MRLD+1(1),11(1)    set length to move (len of RLD data)
MRLD     MVC   0(1,3),16(1)       ** altered instruction **
         MVC   MRR+3(1),11(1)     get entry len
         A     3,MRR              calc new
         ST    3,NRLD               loc in buffer
         L     3,MRR              )
         A     3,NBR              ) update bytes used in rlda
         ST    3,NBR              )
         B     8(14)              exit
XRPT     DC    A(PRINT)           @ PRINT csect
RL14     DC    F'0'               r14 saved here during call to PRINT
RL200    DC    F'1940'            RLD table length
RLDER    DC    C'**  MORE THAN 250 RLD CARDS '
ORL      DC    X'00'
NRLD     DC    A(RLDA)            @ next free entry in rlda
NRLDN    DC    A(RLDA)
MRR      DC    F'0'
NBR      DC    F'0'               entry point ( bytes used in rlda)
RLDA     DS    250CL8             buffer to hold rld entries
         DS    0F
         END
