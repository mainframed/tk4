READ     TITLE 'STAND. READ WITH EOF && RDERR ROUTINES   AUG. 14, 1967'
***********************************************************************
*                                                                     *
* Standard read routine to read the next input card.                  *
*                                                                     *
* Input, @ r1                                                         *
*   +0 - @ 80 byte buffer to put the card in                          *
*                                                                     *
* Return is to where r14 points if a card was succesfully read.       *
*                                                                     *
* If an I/O error occurs an error message is issued and the next card *
* is read.                                                            *
*                                                                     *
* If end of file is reached it branches to EXIT in PASS5 csect.       *
*                                                                     *
* history:                                                            *
*   changed DCB from recfm F to recfm FB                              *
*                                                                     *
***********************************************************************
READA    CSECT
         ENTRY RDDCB              used by PASS5 to close this DCB
         EXTRN EXIT               in PASS5 csect
         EXTRN PA                 in PASS1 csect
         EXTRN PDOFF              in PRINTA csect
         EXTRN PRINTA             csect
         STM   14,12,12(13)       **************
         BALR  12,0               *            *
         USING *,12               *  STANDARD  *
         ST    13,SVRD+4          *   S/360    *
         LR    11,13              *  LINKAGE   *
         LA    13,SVRD            *            *
         ST    13,8(11)           **************
         L     5,0(1)             LOAD DATA DESTINATION ADR
         TM    RDDCB+48,X'10'     IS READ DATA SET OPEN?
         BO    OKRD               BRANCH IF YES
         OPEN  (RDDCB,(INPUT))    NO, OPEN DATA SET
         TM    RDDCB+48,X'10'     WAS OPEN SUCCESSFUL?
         BO    OKRD               BRANCH IF YES
         ABEND 4,DUMP             DUMP IF UNABLE TO OPEN READ
OKRD     GET   RDDCB,0(5)         READ A CARD INTO CALLING PROGRAM
         L     13,SVRD+4          **********
         LM    14,12,12(13)       * RETURN *
         BR    14                 **********
RDERR    L     1,APDF             *** READ ERROR ROUTINE ***
         MVI   0(1),X'FF'         SET PUNCH-DISK BYPASS FLAG
         L     1,APA              ERROR MESSAGE
         MVC   0(40,1),RDMSG      AND CARD IMAGE
         MVC   40(80,1),0(5)      TO PRINT AREA
         MVI   119(1),C' '        BLANK REMAINING
         MVC   120(11,1),119(1)   PRINT POSITIONS
         LA    1,APA              LOAD PRINT PARM
         L     15,APRN            PRINT ERROR MESSAGE
         LA    14,OKRD            THEN READ NEXT CARD
         BR    15                 OVER ERROR IMAGE
EOF      L     1,APA              *** END OF FILE ROUTINE ***
         MVC   0(32,1),EOFMG      EOF MSG TO PRINT AREA
         MVC   33(100,1),32(1)    CLEAR PRINT POS BEYOND 32
         MVC   54(31,1),EOFMG+1   EOF MSG TO CARD IMAGE
         LA    1,APA              LOAD PRINT PARM
         L     15,APRN
         L     14,AEXIT           PRINT EOF MESSAGE, THEN
         BR    15                 GO TO SYS RETURN ROUTINE
SVRD     DC    18F'0'             READ TO PRINT SAVE AREA
AEXIT    DC    A(EXIT)            ADR OF SYS RETURN ROUTINE
APA      DC    A(PA)              ADR OF PRINT AREA
APDF     DC    A(PDOFF)           PUNCH-DISK BYPASS FLAG ADR
APRN     DC    A(PRINTA)          ADR OF STANDARDIZED PRINT ROUTINE
RDMSG    DC    CL40' *** READ ERROR *** THIS CARD IGNORED : '
EOFMG    DC    CL33'0*** END OF FILE TERMINATION *** '
         EJECT
RDDCB    DCB   DDNAME=RDDD,DSORG=PS,MACRF=(GM),RECFM=FB,LRECL=80,      X
               EODAD=EOF,EROPT=ACC,SYNAD=RDERR
         END
