         MACRO
&NAME    INPUT1 &IOAREA=,&LRECL=
         GBLC  &PREF,&DCBDSW,&DCBDGEN,&WAREA
         GBLC  &GSECT,&WSECT       SECTION NAMES
         VERSION 2
&PREF    SETC  'UT1'
         AIF   ('&NAME' EQ '').SKIP01
&PREF    SETC  '&NAME'
.SKIP01  ANOP
&WAREA   SETC  '&IOAREA'
         AIF   ('&IOAREA' NE '').SKIP02
&WAREA   SETC  '&PREF.WORK'
.SKIP02  ANOP
         TITLE 'SYSUT1 INPUT ROUTINE'
         SPACE 2
***********************************************************************
* 'UT1INPUT' READS EACH INPUT RECORD, AND AT EOF SETS UT1EOFSW        *
***********************************************************************
         SPACE
&PREF.INPUT SECTION
         FIRST 1                   CHECK OPEN FIRST TIME ONLY
           IF    (TM,SYS&PREF+48,X'10',NO),THENDO OPENED?
             PERFORM &PREF.PREP    SEE IF WE CAN GET IT OPENED
             IF    (TM,SYS&PREF+48,X'10',NO),THENDO OPEN FAIL?
               ABEND 16,DUMP       MIGHT AS WELL QUIT NOW
             ENDO  , ** NOTE ** POSSIBLE TO ADD DYNALLOC CODE HERE TO
           ENDO  ,   ** NOTE ** . FORCE ALLOCATION FOR PRINTER FILE.
         ENDO
         IF    (CLI,&PREF.EOFSW,C'Y',NE),THENDO DO IF NOT EOF
           GET   SYS&PREF,&WAREA   READ NEXT INPUT RECORD
           AP    &PREF.COUNT,=P'1' BUMP INPUT COUNTER
         ELSE  ,                   ELSE IT IS EOF...
           FIRST 5                 FIVE WARNINGS IN PLENTY
             WTO  'READ PAST END-OF-FILE ON SYS&PREF' FLAG ERROR
           ENDO  ,
         ENDELSE ,
         RETN ,                    AND RETURN
&PREF._SET_EOF MVI &PREF.EOFSW,C'Y' SET EOFSW
         ENDSECT ,                 AND RETURN
         TITLE 'SYSUT1 FILE VERIFICATION ROUTINE AND DCB'
         SPACE 2
***********************************************************************
* SYSUT1 VERIFICATION ROUTINE                                         *
***********************************************************************
         SPACE
&PREF.PREP SECTION
         SLR   R15,R15             SET ZERO RETURN CODE
         RDJFCB MF=(E,&PREF.RJFCB)  CHECK IT OUT
         IF    (LTR,R15,R15,NZ),THENDO DO IF FAILED
           LA    R15,4            4 RETURN CODE
           RETN ,                 RETURN TO CALLER
         ENDO  ,
         IF    (CLI,&PREF.JNVOL,X'00',E),THENDO DO IF NO VOLS ON JCL
           LOCATE &PREF.LST        SEARCH THE CATALOG
           IF    (LTR,R15,R15,NZ),OR,          IF LOCATE FAILED        C
               (OC,&PREF.LNVOL,&PREF.LNVOL,Z),THENDO
             LA    R15,8          8 RETURN CODE
             RETN ,               RETURN TO CALLER
           ENDO  ,
           MVC   &PREF.VOL,&PREF.LVOL1 MOVE IN FIRST VOLUME FROM LOCATE
         ELSE    ,                 VOL IS ON JCL
           MVC   &PREF.VOL,&PREF.JVOLS MOVE IN FIRST VOLUME FROM JFCB
         ENDELSE ,
         OBTAIN &PREF.LST2         GET DSCB
         IF    (LTR,R15,R15,NZ),THENDO DO IF OBTAIN FAILED
           LA    R15,12            12 RETURN CODE
           RETN ,                  RETURN TO CALLER
         ENDO  ,
         MVC   &PREF.DSNM(44),&PREF.JDSN COPY DATA SET NAME
         IF    (CLI,&PREF.JMEM,C' ',NE),THENDO DO IF MEMBER PRESENT
           LA    R2,&PREF.DSNM+43  LAST CHAR OF DSNAME
           DO    UNTIL=(CLI,0(R2),C' ',NE) FIND LAST NON-BLANK
             BCTR  R2,0            DECREMENT POINTER
           ENDO  ,
           MVI   1(R2),C'('        FOR '(MEMBER)'
           LA    R3,&PREF.JMEM     1ST CHAR OF MEMBER
           DO    8                 MOVE NO MORE THAN 8
             IF    (CLI,0(R3),C' ',NE),THENDO DO IF NON-BLANK
               MVC   2(1,R2),0(R3) MOVE 1 CHARACTER
               I    R2             BUMP OUTPUT
               I    R3             BUMP INPUT
             ENDO  ,
           ENDO  ,
           MVI   2(R2),C')'        CLOSE PARENS
         ENDO  ,
         OPEN  ,TYPE=J,MF=(E,&PREF.RJFCB) OPEN THE SUCKER
         IF   (TM,SYS&PREF.+DCBOFLGS-IHADCB,DCBOFOPN,NO),THENDO FAILED?
           LA    R15,20            20 RETURN CODE
         ENDO  ,
         ENDSECT
&WSECT   LOCTR
&PREF.VOL DC   CL6' '              WILL HOLD VOL FROM JFCB OR CATALOG
&PREF.DSNM DC  CL54' '             DATA.SET.NAME(MEMBER)
         PUSH  PRINT
         PRINT NOGEN
SYS&PREF DCB   DDNAME=SYS&PREF,                                        C
               DSORG=PS,                                               C
               MACRF=GM,                                               C
               EXLST=&PREF.EXIT,                                       C
               EODAD=&PREF._SET_EOF
         POP   PRINT
&PREF.RJFCB RDJFCB SYS&PREF.,MF=L  LIST PORTION
&PREF.EXIT DC  0F'0',X'87',AL3(&PREF.JFCB) ONLY INTERESTED IN JFCB
&PREF.JFCB DC  0D'0'               JOB FILE CONTROL BLOCK
&PREF.JDSN DC  CL44' '             DATA SET NAME
&PREF.JMEM DC  CL8' ',65X'00'      MEMBER NAME
&PREF.JNVOL DC X'00'               # OF VOLUMES
&PREF.JVOLS DC 5XL6'00',28X'00'    UP TO 5 VOLSERS
&PREF.LST2 CAMLST SEARCH,&PREF.JDSN,&PREF.VOL,&PREF.DSCB FOR 'OBTAIN'
&PREF.DSCB DC  0D'0'               DATA SET CONTROL BLOCK
         DC    38X'00'             - LOADED BY 'OBTAIN' MACRO
&PREF.DSORG DC X'0000'
&PREF.RECFM DC X'00'
&PREF.OPTCD DC X'00'
&PREF.BLKL DC  H'0'
&PREF.LRECL DC H'0'
         DC    50X'00'
&PREF.LST CAMLST NAME,&PREF.JDSN,,&PREF.STBLK FOR 'LOCATE'
&PREF.STBLK DC 0D'0'               ORIGIN OF LOCATE INFO
&PREF.LNVOL DC H'0'                NUMBER OF VOLUMES
&PREF.DEVT1 DC XL4'00'             DEVTYPE FOR VOL1
&PREF.LVOL1 DC CL6' '              VOLSER FOR VOL1
         DC    19XL12'00'
         DC    10X'00'
&PREF.TTR DC   XL3'00'             TTR FOR DSCB
         DC    4X'00'
         DC    CL6' '
         AIF   ('&DCBDSW' EQ 'NO').SKIP50
&DCBDSW  SETC  'NO'
         PUSH  PRINT
         AIF   ('&DCBDGEN' EQ 'GEN').GENOK
&DCBDGEN SETC  'NOGEN'
.GENOK   PRINT &DCBDGEN
         DCBD  DSORG=PS
&SYSECT  CSECT
&WSECT   LOCTR
         POP   PRINT
.SKIP50  ANOP
         SPACE
&PREF.EOFSW DC C'N'                SET TO 'Y' AT &PREF.EOF
         SPACE
         AIF   ('&IOAREA' NE '').SKIP90
&PREF.WORK DS  CL&LRECL
.SKIP90  ANOP
&PREF.COUNT DC PL8'0'
         AIF   ('&WSECT' EQ '&SYSLOC').MEND
&SYSECT  CSECT
&GSECT   LOCTR
.MEND    MEND
