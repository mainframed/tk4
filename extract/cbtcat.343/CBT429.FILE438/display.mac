         MACRO
&N       DISPLAY &DUMMY &NAMED=NO  DISPLAY LIST OF VARIABLES
         COPY  CPYPAR$E            COPY UNIQUE PAR$E SYMBOLS
         GBLA  &$CTR               FOR UNIQUE LITERAL NAMES
         GBLC  &SUBCALL            WHICH TYPE?
         LCLC  &TEMP,&NAME
         LCLA  &I,&J,&K,&L
         LCLB  &NSW                NAMED SWITCH
&NSW     SETB  (0)                 ASSUME DEFAULT
.*       AIF   ('&NAMED'(1,1) NE 'Y').SKIP010
&NSW     SETB  (1)                 LET'S NAME THEM...
.SKIP010 ANOP                      COME FROM *-2
&I       SETA  1                   CYCLE THRU POSITIONAL PARAMETERS
&J       SETA  1                   DISPLACEMENT ON PRINT LINE
&NAME    SETC  '&N'                USED TO PREVENT DUPLICATE TAGS
.LOOP100 AIF   ('&SYSLIST(&I)' EQ '').OUTP900 FINISH UP IF ALL DONE
         AIF   ('&SYSLIST(&I)' NE '/').TEST105 NEW LINE CHARACTER?
&I       SETA  &I+1                BUMP TO NEXT POSITIONAL PARAMETER
         AGO   .OUTP900            GO FISH
.TEST105 AIF   ('&SYSLIST(&I)'(1,2) EQ 'C''').LIT250 GO IF LITERAL
         AIF   ('&SYSLIST(&I)'(1,1) EQ '(').REG300 GO IF GPR
         AIF   (T'&SYSLIST(&I) NE 'P').PROC200 FALL THROUGH IF PACKED
&K       SETA  2*L'&SYSLIST(&I)-1  FIRST GUESS AT OUTPUT LENGTH REQ'D
&L       SETA  &J+&K               CHECK FOR PRINT LINE OVERFLOW
         AIF   (&L GT 132).OUTP900 FINISHED IF NO ROOM
&L       SETA  &K                  CALCULATE # OF OUTPUT BYTES REQ'D
         AIF   (S'&SYSLIST(&I) EQ 0).TEST110 EXPLICIT DECIMAL POINT?
&K       SETA  &K+1                YES - ALLOW FOR '.'
&L       SETA  &L-S'&SYSLIST(&I)   REDUCE REMAINING DIGIT COUNT
.TEST110 AIF   (&L LT 4).PROC120   GO IF NO MORE COMMAS REQ'D
&K       SETA  &K+1                ALLOW FOR A COMMA
&L       SETA  &L-3                REDUCE REMAINING DIGIT COUNT BY 3
         AGO   .TEST110            . AND LOOP BACK
.PROC120 ANOP                      ** ALMOST ** READY TO FORMAT NUMBER
&K       SETA  &K+1                ALLOW FOR POSSIBLE MINUS SIGN
&K       SETA  &K+&L               CALCULATE TOTAL BYTES REQ'D
&L       SETA  &J+&K               CALCULATE NEW PRINT LINE SIZE
         AIF   (&L GT 132).OUTP900 ALL FINISHED IF TOO BIG
         DC    0C'OPND LENGTH ==> &K OPND ==> &SYSLIST(&I)'
&NAME    EM    OUTLINE+&J.(&K),&SYSLIST(&I),DB=- FORMAT NUMBER
         AGO   .INCR400            GO INCREMENT INDEXES
.PROC200 PAR$E &SYSLIST(&I)        SPLIT INTO COMPONENT PARTS
&K       SETA  &$PLEN1             STRAIGHTFORWARD BYTE COUNT
&L       SETA  &J+&K               CHECK LENGTH
         AIF   (&L GT 132).OUTP900 ALL FINISHED IF PRINT LINE TOO LONG
&NAME    MVC   OUTLINE+&J.(&K),&$PTAG1&$PEB1 MOVE DATA TO PRINT LINE
         AGO   .INCR400            GO INCREMENT INDEXES
.LIT250  $WA                       SET UP DATA AREA
&TEMP    SETC  '$LT&$CTR'          SET UP TAG FOR 'LITERAL'
&TEMP    DC    &SYSLIST(&I)        DEFINE LITERAL
         $IA                       ENTER INSTRUCTION ZONE
&K       SETA  L'&TEMP             CALCULATE LENGTH OF LITERAL
&NAME    MVC   OUTLINE+&J.(&K),&TEMP MOVE LITERAL TO PRINT LINE
&$CTR    SETA  &$CTR+1             CREATE UNIQUE COUNTER FOR NEXT LIT
         AGO   .INCR400            GO INCREMENT INDEXES
.REG300  ANOP
&K       SETA  K'&SYSLIST(&I,1)    CHARS IN REG NAME
&L       SETA  &J+&K+9
         AIF   (&L GT 132).OUTP900 GO IF NO ROOM AT THE INN
&NAME    LR    R1,&SYSLIST(&I,1)   LOAD FIRST (AND ONLY) PARAMETER
         &SUBCALL CVXWORD          GET (PRINTABLE) HEXADECIMAL
         MVC   OUTLINE+&J.(&K),=C'&SYSLIST(&I,1)'
&J       SETA  &J+&K+1             BUMP TO NEXT AVAIL LOC'N
&K       SETA  0                   CAN'T COUNT IT TWICE...
         STCM  R0,15,OUTLINE+&J    STORE 1ST 4 HEX CHARACTERS
         STCM  R1,15,OUTLINE+&J+4  NOW LAST 4
&J       SETA  &J+8                BUMP TO NEXT AVAIL LOC'N
.INCR400 ANOP                      INCREMENT INDEXES
&J       SETA  &J+&K+1             CALC NEW LOCATION ON PRINT LINE
&I       SETA  &I+1                NEW SUBSCRIPT FOR POSITIONAL PARMS
&NAME    SETC  ''                  PREVENT DUPLICATE TAGS
         AGO   .LOOP100            BACK TO CHECK FOR MORE POSITIONALS
.OUTP900 &SUBCALL PRINTRTN         PRINT THE LINE JUST FORMATTED
&J       SETA  1                   RESET PRINTLINE LOC'N (JUST IN CASE)
         AIF   ('&SYSLIST(&I)' NE '').LOOP100 BACK IF MORE TO DO
         MEND                      GUESS?
