         MACRO
&NAME    STOP  &DUMMY,&RC=         TERMINATE MAINLINE OF PROG
.* POSITIONAL PARAMETER '&DUMMY' ALLOWS A COMMENT FIELD WITHOUT
.* COMMA IF NO PARMS ARE SPECIFIED IN MACRO CALL STATEMENT.
         COPY  $MGBLDEF
         LCLC  &X,&Y               FOR NAME
         AIF   (&$MDGLVL EQ 0).SKIP03 GO IF NO DO-GROUPS TO END
         MNOTE 4,'&$MDGLVL DO-GROUP(S) ENDED'
         ENDO  DOGROUP=*ALL*
.SKIP03  ANOP
&X       SETC  '$STOP'             BLANK IF NECESSARY
         AIF   ('&NAME' EQ '').SKIP05 NAME PRESENT...
&NAME    DC    0H'0' .             GENERATE TAG
.SKIP05  ANOP
         AIF   ('&RC' EQ '').SKIP10
&X       SETRC &RC .               SET USERS RETURN CODE
&X       SETC  ''
.SKIP10  ANOP
&X       L     R13,4(,R13)         CHAIN BACK
         LM    R14,R15,12(R13)     RESTORE EVERYTHING BUT
         LM    R2,R12,28(R13)       ZERO AND ONE
         BR    R14
         DROP  &$USING(1)           DROP CURRENT BASE REG(S)
&$USING(1) SETC ''                  DON'T DROP THEM AGAIN!
         LTORG
&$MSECT(1) SETC 'DATAAREA'
DATAAREA CSECT                     DEFINE DATA AREA CSECT
         MEND
