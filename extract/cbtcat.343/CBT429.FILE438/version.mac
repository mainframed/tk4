         MACRO
         VERSION &VER
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* SOME OF THE STRUCTURED MACROS ARE INCOMPATIBLE WITH OTHERS.        *
.* VERSION 1 MACROS (PROC, PCALL, ENDP, GO AND STOP) CANNOT BE USED   *
.* IN THE SAME PROGRAM AS VERSION 2 (DIVISION, SECTION, ENDSECT,      *
.* PERFORM, AND GOBACK). THE 'VERSION' MACRO MAY BE USED IN OPEN CODE *
.* TO SET THE STAGE. IT IS ALREADY USED WITHIN THE MACROS TO PREVENT  *
.* CATASTROPHE.                                                       *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
         GBLA  &VERSION      FIRST CALL WILL SET THIS TO GLOBAL VERSION
         GBLB  &VERROR       SET ON IF MISMATCH
         GBLC  &SUBCALL      WHAT TO USE TO CALL A SUBROUTINE
&VERROR  SETB  (0)                 ASSUME NO ERROR
         AIF   (&VERSION EQ 0).SET 0 -> NOT YET SET: DO IT!
         AIF   (&VERSION EQ &VER).SET   EQ -> OK: RESET
         MNOTE 12,'SPECIFIED VERSION &VER INCOMPATIBLE WITH GLOBAL VERSC
               ION &VERSION'
&VERROR  SETB  (1)                 SHOW ERROR
         MEXIT
.SET     ANOP
&VERSION SETA  &VER                SET TO VERSION OF THIS MACRO
&SUBCALL SETC  'PCALL'             ASSUMPTION.
         AIF   (&VERSION EQ 1).FINIS CORRECT?
&SUBCALL SETC  'PERFORM'           FACT.
.FINIS   MEND
