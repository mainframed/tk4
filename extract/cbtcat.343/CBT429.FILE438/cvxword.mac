         MACRO
&NAME    CVXWORD &DBL,&FW          CONV 4 BYTES CHAR TO 8 BYTES HEX
.*                            GLOBAL SYMBOLS FOR FIRST OPERAND
         GBLC  &$PTAG1        DISPLACEMENT PORTION OF OPERAND
         GBLA  &$PLEN1      EXPLICIT LENGTH IF SPECIFIED ELSE IMPLICIT
         GBLC  &$PBASE1       EXPLICIT BASE IF SPECIFIED ELSE NULL
         GBLC  &$PEB1         NULL IF NO EXPLICIT BASE ELSE (&$PBASE1)
         GBLC  &$PELB1 (&$PLEN1) IF NO EXP BASE ELSE (&$PLEN1,&$PBASE1)
.*                            GLOBAL SYMBOLS FOR SECOND OPERAND
         GBLC  &$PTAG2        DISPLACEMENT PORTION OF OPERAND
         GBLA  &$PLEN2      EXPLICIT LENGTH IF SPECIFIED ELSE IMPLICIT
         GBLC  &$PBASE2       EXPLICIT BASE IF SPECIFIED ELSE NULL
         GBLC  &$PEB2         NULL IF NO EXPLICIT BASE ELSE (&$PBASE2)
         GBLC  &$PELB2 (&$PLEN2) IF NO EXP BASE ELSE (&$PLEN2,&$PBASE2)
         GBLA  &$MBLATH       DOCUMENTATION CONTROL
         LCLC  &X
         PAR$E &DBL
         AIF   (&$PLEN1 EQ 8).OK02
         MNOTE 4,'LENGTH CODE OF FIRST OPERAND (&$PLEN1) FORCED TO 8'
.OK02    ANOP
&NAME    MVC   CVXFW,&FW           COPY TO 'SAFE' AREA
         UNPK  CVXDBL(9),CVXFW(5)  EXPLODE INTO 1-BYTE 'NIBBLES'
         MVZ   CVXDBL,CVXZEROS     SET HIGH NIBBLES TO ZERO
         TR    CVXDBL,CVXTABLE     TR UNPRINTABLE GARBAGE TO PRINTABLE
&X       SETC  ''                  ASSUME NO EXPLICIT BASE REGISTER
         AIF   ('&$PBASE1' EQ '').SKIP10 WAS ASSUMPTION CORRECT?
&X       SETC  '&$PBASE1'          ADD BASE REG SPECIFICATION
.SKIP10  MVC   &$PTAG1.(8&X),CVXDBL COPY 8 BYTES TO CALLER'S RESULT
         AIF   (D'CVXFW).MEND      DEFINE ONLY ONCE, THANK YOU
         $WA                       ESTABLISH WORK AREA ADDRESSABILITY
CVXFW    DC    CL4' ',C' '         FOUR (PLUS 1 TO FLIP SIGN)
CVXDBL   DC    CL8' ',C' '         EIGHT (PLUS 1 TO FLIP SIGN)
CVXZEROS DC    XL8'0F0F0F0F0F0F0F0F' FOR ZERO ZONES
CVXTABLE DC    C'0123456789ABCDEF' MAGIC TABLE
         $IA                       BACK TO EXECUTABLE ADDRESSABILITY
.MEND    MEND
