         MACRO
&TYPE    SECONDEP &DUMMY,&PROGID=MAIN,&INST=,&DATA=,&BASE=,&LINK=
         COPY  $MGBLDEF
         GBLC  &GSECT,&WSECT       SECTION NAMES
         GBLC  &NSECT              SECTION NAME
         GBLC  &GSAVE              SAVE AREA NAME
         GBLC  &GPROG              SET TO CSECT NAME
         GBLC  &PARM               INTERNAL USE ONLY
         GBLC  &GBASE,&GLINK       DEFINE GLOBAL BASE AND LINK REGS
         GBLC  &GRETN,&GSKIP       FOR SECTION MACRO
         GBLB  &SEQSW              SEQUENCE SWITCH
         LCLC  &X                  FOR ENTRY STATEMENT
         VERSION 2
&X       SETC  '&SYSNDX'           SHORTHAND
         USING *,R15               TEMPORARY BASE REGISTER
         DROP  R12,R13             TEMPORARILY
         STM   R14,R12,12(R13)     SAVE CALLER'S REGS
         L     &GBASE,=A(&GSECT)   LOAD BASE REG
         USING &GSECT,&GBASE       REESTABLISH
         DROP  R15                 BACK TO STD BASE REG
         XC    16(4,R13),16(R13)   SET ZERO RETURN CODE
         L     R15,=A(&GSAVE)      SAVE
         ST    R15,8(,R13)         . AREA
         ST    R13,4(,R15)         . CHAINING
         LR    R13,R15             PRIME OUR SAVE POINTER
         USING &GSAVE,R13          REESTABLISH
&GRETN   SETC  'RETN&X'            UNIQUE TAG FOR EXIT
         DC    0C'&&GRETN SET TO RETN&X'
&GSKIP   SETC  'SKIP&X'            UNIQUE TAG FOR END OF SECTION
         DC    0C'&&GSKIP SET TO SKIP&X'
         B     STRT&X               . AND B TO SECTION PROPER
RETN&X   L     &GLINK,LINK&X       RESTORE LINK REGISTER
         B     8(,&GLINK)           AND RETURN
LINK&X   DC    A(&GSKIP-8)         'EXIT' ADDRESS
STRT&X   DC    0H'0'               START OF SECTION
         AIF   (D'R0).SKIP
         EQUATE
.MEND    MEND
