         MACRO
&NAME    PROC  &SAVE=LINK,&NBASE=1 SET &SAVE TO 'ALL' FOR R2 - R14
         COPY  $MGBLDEF
         GBLB  &VERROR             SET BY VERSION MACRO
         LCLA  &TEMP,&TEMP2,&TEMP3
         LCLC  &$MX
         VERSION 1
         AIF   (NOT &VERROR).OK02 TEST FOR VERSION ERROR
         MEXIT                     GIVE UP ALL HOPE IF YES
.OK02    AIF   ('&NAME' NE '').OK10 MUST HAVE NAME TO BE CALLABLE
         MNOTE 8,'UNNAMED PROC - GENERATION SUPPRESSED'
         MEXIT
.OK10    ANOP
&$MX     SETC  '&SYSNDX'           MAKE ASM LIST LOOK NICER
         AIF   ('&SYSECT' EQ 'DATAAREA').OK12
         B     $SKP&$MX            B AROUND LITERALS
         LTORG                     GENERATE LITERALS FOR PREV CSECT
$SKP&$MX DC    0H'0'
.OK12    AIF   ('&$USING(&$MPLVL)' EQ '').SKIP01
         DROP  &$USING(&$MPLVL)   DROP CURRENT BASE REG(S)
.SKIP01  ANOP
&$MPLVL  SETA  &$MPLVL+1           BUMP NESTING LEVEL
&$MSECT(&$MPLVL) SETC '&NAME'      REMEMBER PROC NAME
&$MPCSV(&$MPLVL) SETA  0           ASSUME REGS WILL BE SAVED
&$PROCNM(&$MPLVL) SETC  '&NAME'    SAVE THIS NAME FOR ENDP
         AIF   ('&NBASE' EQ '').SKIP00
         AIF   (&NBASE NE 1).OK20
.SKIP00  AIF   ('&SAVE' EQ 'YES' OR '&SAVE' EQ 'ALL').OK20
         AIF   ('&SAVE' EQ 'LINK').OK30
         AIF   ('&SAVE' EQ 'NO' OR '&SAVE' EQ 'NONE').OK40
         MNOTE 4,'''&SAVE'' INVALID - ''LINK'' ASSUMED'
         AGO   .OK30
.OK20    ANOP
&NAME    CSECT
         USING *,&$BASE(1)         ADDRESSABILITY
&$USING(&$MPLVL) SETC '&$BASE(1)'  INITIALIZE USING
         IF    (TM,$MCL&$MX,X'80',O),THENDO DO IF PROC ACTIVE
           ABEND &$MABEND,DUMP     REENTRANT NOT SUPPORTED
         ENDO  ,
         OI    $MCL&$MX,X'80'      FLAG PROC ACTIVE
         STM   R2,R14,$MSV&$MX     SAVE ALL USER REGISTERS
         AIF   ('&NBASE' EQ '').DONEBAS NONE SPECIFIED
         AIF   (&NBASE EQ 1).DONEBAS 1 IS DEFAULT
&TEMP    SETA  (&NBASE)            NUMBER OF BASE REGS FOR PROC
         AIF   (&TEMP LE &$MNBASE).OK22 CAN'T BE > THAN GO MACRO
         MNOTE 4,'MORE BASE REGS THAN SPECIFIED IN ''GO'' MACRO'
         MNOTE *,'&$MNBASE REGS USED'
&TEMP    SETA  (&$MNBASE)
.OK22    LA    &$BASE(&TEMP),2048   INCREMENT FOR BASE REG(S)
&TEMP2   SETA  2                    SECOND BASE
.BASELUP AIF   (&TEMP2 GE &TEMP).FINBASE
&TEMP3   SETA  &TEMP2-1             POINT TO PREV BASE
&X       SETC  '&$BASE(&TEMP2)'
&Y       SETC  '&$BASE(&TEMP3)'
&Z       SETC  '&$BASE(&TEMP)'
         LA    &X,2048(&Y,&Z)       INITIALIZE ANOTHER BASE
&$USING(&$MPLVL) SETC '&$USING(&$MPLVL),&$BASE(&TEMP2)' BUILD USING
&TEMP2   SETA  &TEMP2+1             NEXT BASE
         AGO   .BASELUP             LOOP BACK
.FINBASE ANOP                       DONE WITH BASE REG LOOP
&TEMP2   SETA  &TEMP2-1             BACK TO NEXT-TO-LAST
&X       SETC  '&$BASE(&TEMP)'
&Y       SETC  '&$BASE(&TEMP2)'
         LA    &X,2048(&Y,&X)       INITIALIZE LAST BASE
&$USING(&$MPLVL) SETC '&$USING(&$MPLVL),&$BASE(&TEMP)' FINISH USING
&X       SETC  '&$USING(&$MPLVL)'   SHORTHAND
         USING  &NAME,&X            ESTABLISH ADDRESSABILITY
.DONEBAS TRC   ,'PROC ''&NAME'' ENTERED'
         B     $MSP&$MX             . AND B TO PROC PROPER
         DC    CL8'&NAME.*******',CL10'-&SYSDATE ' FINGERPRINT
$MEX&$MX NI    $MCL&$MX,X'7F'      FLAG PROC INACTIVE
         TRC   ,'LEAVING PROC ''&NAME'''
         LM    R2,R14,$MSV&$MX     RESTORE ALL USER REGS
         L     &$BASE(1),4(,&$LINK) RESTORE BASE REGISTER
         B     8(,&$LINK)           AND RETURN
DATAAREA CSECT
$MSV&$MX DC    13F'0'              SAVE AREA FOR 2 - 14
$MCL&$MX DC    X'00'               SET TO X'80' WHEN EXECUTING
&NAME    CSECT
         AGO   .FINISH             SKIP TO STD CLEANUP
.OK30    ANOP
&NAME    CSECT
         USING *,&$BASE(1)         ADDRESSABILITY
&$USING(&$MPLVL) SETC '&$BASE(1)'  INITIALIZE USING
         IF    (TM,$MCL&$MX,X'80',O),THENDO DO IF PROC ACTIVE
           ABEND &$MABEND,DUMP     REENTRANT NOT SUPPORTED
         ENDO  ,
         OI    $MCL&$MX,X'80'      FLAG PROC ACTIVE
         ST    &$LINK,$MSV&$MX     SAVE LINK REGISTER
         TRC   ,'PROC ''&NAME'' ENTERED'
         B     $MSP&$MX             AND B TO PROC PROPER
         DC    CL8'&NAME.*******',CL10'-&SYSDATE ' FINGERPRINT
$MEX&$MX NI    $MCL&$MX,X'7F'      FLAG PROC INACTIVE
         TRC   ,'LEAVING PROC ''&NAME'''
         L     &$LINK,$MSV&$MX     RESET LINK REGISTER
         L     &$BASE(1),4(,&$LINK) RESTORE BASE REGISTER
         B     8(,&$LINK)           AND RETURN
DATAAREA CSECT
$MSV&$MX DC    F'0'                SAVE AREA FOR LINK REG
$MCL&$MX DC    X'00'               SET TO X'80' WHEN EXECUTING
&NAME    CSECT
         AGO   .FINISH             SKIP TO STD CLEANUP
.OK40    ANOP
&$MPCSV(&$MPLVL) SETA  1           SHOW NO REGS SAVED
&NAME    CSECT
         USING *,&$BASE(1)         ADDRESSABILITY
&$USING(&$MPLVL) SETC '&$BASE(1)'  INITIALIZE USING
         B     $MSP&$MX            B TO PROC PROPER
         DC    CL8'&NAME.*******',CL10'-&SYSDATE ' FINGERPRINT
$MEX&$MX L     &$BASE(1),4(,&$LINK) RESTORE BASE REGISTER
         B     8(,&$LINK)          RETURN
.FINISH  ANOP
&$MENAME(&$MPLVL) SETC  '$MEX'.'&$MX'
$MSP&$MX DC    0H'0'               START OF PROC
         MEND
