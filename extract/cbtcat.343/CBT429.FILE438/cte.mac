         MACRO
&N       CTE   &OP
.*                                                                   *
.*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* CTE - 'CREATE TABLE ENTRY'                                        *
.*                                                                   *
.* CREATE ONE OR MORE TABLE ENTRIES REPRESENTING STRINGS TO SEARCH   *
.* FOR DURING SCAN. A 'HIT' WILL TERMINATE THE SCAN ROUTINE AND      *
.* RETURN TO THE CALLING ROUTINE WITH A 'FOUND' CONDITION.           *
.*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                   *
         LCLA  &SUB,&K
         LCLC  &NAME
&NAME    SETC  '&N'                BLANKED OUT AFTER 1ST GEN
&SUB     SETA  1                   TO SCAN THROUGH &SYSLIST PARMS
         AIF   ('&N' NE '').LOOP100 NAME MEANS 1ST STMT OF TABLE DEF
         ORG   *-1                 NOT 1ST - BACK UP OVER X'FF'
.LOOP100 ANOP                      PROCESS STRING
&K       SETA  K'&SYSLIST(&SUB)-3  LENGTH FOR 'EX' OF 'CLC'
&NAME    DC    AL1(&K,0),PL4'0',C&SYSLIST(&SUB) TABLE ENTRY
&NAME    SETC  ''                  NULLIFY NAME FIELD
&SUB     SETA  &SUB+1              BUMP TO NEXT INDEX
         AIF   ('&SYSLIST(&SUB)' NE '').LOOP100 BACK IF NOT NULL
         DC    X'FF'               DEFINE 'END OF TABLE'
         MEND
