         MACRO
&NAME    RTTPRT &RESET=NO
         COPY  $MGBLDEF
         GBLA  &RTTTL,&RTTCL,&RTTTOTL
         GBLB  &RTTODD              CONTROLS ZEROING OF HIGH DIGIT
         GBLC  &RTTNAME,&SUBCALL
&NAME    ST    R10,R10&SYSNDX
         $WA                        SET LOCTR TO WORK AREA
R10&SYSNDX DC    F'0'               SAVE AREA FOR WORK REGISTER
         $IA                        SET LOCTR TO INSTRUCTION AREA
         LA    R10,&RTTNAME         ADDR OF FIRST TABLE ENTRY
         DO    UNTIL=(CL,R10,=A(&RTTNAME._END),NL) PRINT ENTIRE TABLE
           USING &RTTNAME._DSECT,R10 SET ADDRESSABILITY
&DIGITS    SETA  &RTTCL*2-1         NUMBER OF DIGITS TO PRINT
&COMMAS    SETA  (&DIGITS+2)/3-1    NUMBER OF COMMAS REQUIRED
&CHARS     SETA  &DIGITS+&COMMAS    NUMBER OF PRINT POSITIONS REQ'D
           AIF   (&RTTODD).NEXT010  SKIP IF ODD NUMBER OF DIGITS
           NI    &RTTNAME._COUNTER,X'0F' SET HIGH ORDER TO ZERO
.NEXT010   ANOP
           EM    OUTLINE+1(&CHARS),&RTTNAME._COUNTER FORMAT IT
           MVC   OUTLINE+2+&CHARS.(&RTTTL),&RTTNAME._TEXT MOVE IN TITLE
          &SUBCALL PRINTRTN         PRINT A LINE
           AIF   ('&RESET' NE 'Y' AND '&RESET' NE 'YES').SKIP020
           PACK  &RTTNAME._COUNTER,=Z'0' RESET TO ZERO
           DROP  R10                DROP ADDRESSABILITY
.SKIP020   I     R10,&RTTTOTL       INCREMENT ADDRESS TO NEXT ENTRY
         ENDO  ,                    END OF PRINT LOOP
         L     R10,R10&SYSNDX       RESTORE WORK REGISTER
         MEND
