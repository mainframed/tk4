         MACRO
&NAME    SYSUT2 &LRECL=80,&IOAREA=
         GBLC  &PREF,&DCBDSW,&DCBDGEN,&WAREA
&PREF    SETC  'UT2'
         AIF   ('&NAME' EQ '').SKIP01
&PREF    SETC  '&NAME'
.SKIP01  ANOP
&WAREA   SETC  '&IOAREA'
         AIF   ('&IOAREA' NE '').SKIP02
&WAREA   SETC  '&PREF.WORK'
.SKIP02  ANOP
         TITLE 'SYSUT2 OUTPUT ROUTINE'
         SPACE 2
***********************************************************************
* 'UT2OUTPT' WRITES EACH OUTPUT RECORD                                *
***********************************************************************
         SPACE
&PREF.OUTPT PROC ,
           PUT   SYS&PREF,&WAREA   WRITE RECORD
           AP    &PREF.COUNT,=P'1' BUMP OUTPUT COUNTER
         ENDP  ,                   AND RETURN
         TITLE 'SYSUT2 FILE VERIFICATION ROUTINE AND DCB'
         SPACE 2
***********************************************************************
* SYSUT2 VERIFICATION ROUTINE                                         *
***********************************************************************
         SPACE
&PREF.PREP PROC ,
         SLR   R15,R15             SET ZERO RETURN CODE
         RDJFCB MF=(E,&PREF.RJFCB)  CHECK IT OUT
         IF    (LTR,R15,R15,NZ),THENDO DO IF FAILED
           LA    R15,4            4 RETURN CODE
           RETN  ,                RETURN TO CALLER
         ENDO  ,
         MVC   &PREF.DSNM(44),&PREF.JDSN COPY DATA SET NAME
         IF    (CLI,&PREF.JMEM,C' ',NE),THENDO DO IF MEMBER PRESENT
           LA    R2,&PREF.DSNM+43  LAST CHAR OF DSNAME
           DO    UNTIL=(CLI,0(R2),C' ',NE) FIND LAST NON-BLANK
             BCTR  R2,0            DECREMENT POINTER
           ENDO  ,
           MVI   1(R2),C'('        FOR '(MEMBER)'
           LA    R3,&PREF.JMEM     1ST CHAR OF MEMBER
           DO    8                 MOVE NO MORE THAN 8
             IF    (CLI,0(R3),C' ',NE),THENDO DO IF NON-BLANK
               MVC   2(1,R2),0(R3) MOVE 1 CHARACTER
               I    R2             BUMP OUTPUT
               I    R3             BUMP INPUT
             ENDO  ,
           ENDO  ,
           MVI   2(R2),C')'        CLOSE PARENS
         ENDO  ,
         OPEN  (,OUTPUT),TYPE=J,MF=(E,&PREF.RJFCB) OPEN THE SUCKER
         IF   (TM,SYS&PREF.+DCBOFLGS-IHADCB,DCBOFOPN,NO),THENDO FAILED?
           LA    R15,20            20 RETURN CODE
         ENDO  ,
         ENDP  ,
DATAAREA CSECT
&PREF.VOL DC   CL6' '              WILL HOLD VOL FROM JFCB OR CATALOG
&PREF.DSNM DC  CL54' '             DATA.SET.NAME(MEMBER)
         PUSH  PRINT
         PRINT NOGEN
SYS&PREF DCB   DDNAME=SYS&PREF,                                        C
               LRECL=&LRECL,                                           C
               DSORG=PS,                                               C
               MACRF=PM,                                               C
               EXLST=&PREF.EXIT
         POP   PRINT
&PREF.RJFCB RDJFCB SYS&PREF.,MF=L  LIST PORTION
&PREF.EXIT DC  0F'0',X'87',AL3(&PREF.JFCB) ONLY INTERESTED IN JFCB
&PREF.JFCB DC  0D'0'               JOB FILE CONTROL BLOCK
&PREF.JDSN DC  CL44' '             DATA SET NAME
&PREF.JMEM DC  CL8' ',65X'00'      MEMBER NAME
&PREF.JNVOL DC X'00'               # OF VOLUMES
&PREF.JVOLS DC 5XL6'00',28X'00'    UP TO 5 VOLSERS
&PREF.LST2 CAMLST SEARCH,&PREF.JDSN,&PREF.VOL,&PREF.DSCB FOR 'OBTAIN'
&PREF.DSCB DC  0D'0'               DATA SET CONTROL BLOCK
         DC    38X'00'             - LOADED BY 'OBTAIN' MACRO
&PREF.DSORG DC X'0000'
&PREF.RECFM DC X'00'
&PREF.OPTCD DC X'00'
&PREF.BLKL DC  H'0'
&PREF.LRECL DC H'0'
         DC    50X'00'
&PREF.LST CAMLST NAME,&PREF.JDSN,,&PREF.STBLK FOR 'LOCATE'
&PREF.STBLK DC 0D'0'               ORIGIN OF LOCATE INFO
&PREF.LNVOL DC H'0'                NUMBER OF VOLUMES
&PREF.DEVT1 DC XL4'00'             DEVTYPE FOR VOL1
&PREF.LVOL1 DC CL6' '              VOLSER FOR VOL1
         DC    19XL12'00'
         DC    10X'00'
&PREF.TTR DC   XL3'00'             TTR FOR DSCB
         DC    4X'00'
         DC    CL6' '
         AIF   ('&DCBDSW' EQ 'NO').SKIP50
&DCBDSW  SETC  'NO'
         PUSH  PRINT
         AIF   ('&DCBDGEN' EQ 'GEN').GENOK
&DCBDGEN SETC  'NOGEN'
.GENOK   PRINT &DCBDGEN
         DCBD  DSORG=PS
DATAAREA CSECT
         POP   PRINT
.SKIP50  ANOP
         SPACE
&PREF.EOFSW DC C'N'                SET TO 'Y' AT &PREF.EOF
         SPACE
         AIF   ('&IOAREA' NE '').SKIP90
&PREF.WORK DC  CL&LRECL' '
.SKIP90  ANOP
&PREF.COUNT DC PL8'0'
.MEND    MEND
