         MACRO
&NAME    AN    &OP1,&OP2
.*                                                                   *
.*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* ADD NUMERIC ANY FORMAT TO ANY OTHER                               *
.*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                   *
         AIF   ('&OP1'(1,1) NE '(').TEST200 GO IF 1ST NOT GPR
         AIF   ('&OP2'(1,1) NE '(').TEST100 GO IF 2ND NOT GPR
&NAME    AR    &OP1(1),&OP2(1)    DO THE ADDITION
         AGO   .MEND999
.TEST100 AIF   (T'&OP2 NE 'P').TEST110
&NAME    ZAP   WS_PL8,&OP2        PLACE INTO 8-BYTE WORK AREA
         CVB   0,WS_PL8           CONVERT TO BINARY
         AR    &OP1(1),0          DO THE ADDITION
         AIF   (D'WS_PL8).MEND999
         $WA   ,
         DC    0D'0'
WS_PL8   DC    PL8'0'
         $IA   ,
         AGO   .MEND999
.TEST110 ANOP
&NAME    PACK  WS_PL8,&OP2        PLACE INTO 8-BYTE WORK AREA
         CVB   0,WS_PL8           CONVERT TO BINARY
         AR    &OP1(1),0          DO THE ADDITION
         AIF   (D'WS_PL8).MEND999
         $WA   ,
         DC    0D'0'
WS_PL8   DC    PL8'0'
         $IA   ,
         AGO   .MEND999
.TEST200 AIF   (T'&OP1 NE 'P').TEST300 GO IF 1ST NOP PACKED
         AIF   ('&OP2'(1,1) NE '(').TEST210 GO IF 2ND NOT GPR
&NAME    CVD   &OP2(1),WS_PL8
         AP    &OP1,WS_PL8
         AIF   (D'WS_PL8).MEND999
         $WA   ,
         DC    0D'0'
WS_PL8   DC    PL8'0'
         $IA   ,
         AGO   .MEND999
.TEST210 AIF   (T'&OP2 NE 'P').TEST220 GO IF 2ND NOT GPR
&NAME    AP    &OP1,&OP2
         AGO   .MEND999
.TEST220 ANOP
&NAME    PACK  WS_PL8,&OP2        PLACE INTO 8-BYTE WORK AREA
         AP    &OP1,WS_PL8        DO THE ADDITION
         AIF   (D'WS_PL8).MEND999
         $WA   ,
         DC    0D'0'
WS_PL8   DC    PL8'0'
         $IA   ,
         AGO   .MEND999
.TEST300 AIF   ('&OP2'(1,1) NE '(').TEST310
         AIF   (D'WS_PL8).TEST301
         $WA   ,
         DC    0D'0'
WS_PL8   DC    PL8'0'
         $IA   ,
         AGO   .MEND999
.TEST301 AIF   (D'WS_PL16A).CONT302
         $WA   ,
         DC    0D'0'
WS_PL16A DC    PL16'0'
         $IA   ,
.CONT302 ANOP
&NAME    CVD   &OP2(1),WS_PL8
         EPACK WS_PL16A,&OP1
         AP    WS_PL16A,WS_PL8
         UNPK  &OP1,WS_PL16A
         AGO   .MEND999
.TEST310 AIF   (T'&OP2 NE 'P').TEST320
         AIF   (D'WS_PL16A).CONT315
         $WA   ,
         DC    0D'0'
WS_PL16A DC    PL16'0'
         $IA   ,
.CONT315 ANOP
&NAME    EPACK WS_PL16A,&OP1
         AP    WS_PL16A,&OP2
         UNPK  &OP1,WS_PL16A
         AGO   .MEND999
.TEST320 AIF   (D'WS_PL16A).TEST325
         $WA   ,
         DC    0D'0'
WS_PL16A DC    PL16'0'
         $IA   ,
.TEST325 AIF   (D'WS_PL16B).CONT330
         $WA   ,
         DC    0D'0'
WS_PL16B DC    PL16'0'
         $IA   ,
.CONT330 ANOP
&NAME    EPACK WS_PL16A,&OP1
         EPACK WS_PL16B,&OP2
         AP    WS_PL16A,WS_PL16B
         UNPK  &OP1,WS_PL16A
.MEND999 MEND
