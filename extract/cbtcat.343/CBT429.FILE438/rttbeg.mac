         MACRO
         RTTBEG &TEXTLN=50,&MAXVAL=1234567
         GBLA  &RTTTL,&RTTCL,&RTTTOTL,&RTTDEF LENGTHS & OCCURANCES
         GBLB  &RTTODD             EVEN/ODD DIGIT COUNT FLAG
         GBLC  &RTTNAME            NAME OF TABLE
         AIF   (&RTTDEF EQ 0).NEXT010 GO IF FIRST EXAMPLE OF RTTBEG
         MNOTE 8,'ONLY ONE RUN-TIME TABLE ALLOWED - SORRY'
         MEXIT                     QUICK EXIT
.NEXT010 ANOP                      CONTINUE
&RTTDEF  SETA  1                   SHOW MACRO HAS BEEN CODED
&RTTNAME SETC  'RTT&SYSNDX'        CREATE TABLE NAME
&RTTNAME._DSECT DSECT              MAPPING DSECT
&RTTTL   SETA  &TEXTLN             SET TO TEXT LENGTH
&RTTODD  SETB  (1)                 ASSUME ODD NUMBER OF DIGITS
&X       SETA  K'&MAXVAL           NUMBER OF DIGITS
         AIF   (&X NE &X/2*2).NEXT030 GO IF ODD NUMBER OF DIGITS
&RTTODD  SETB  (0)                 SHOW EVEN NUMBER
.NEXT030 ANOP                      GET LENGTH FROM DIGIT COUNT:
&RTTCL   SETA  (K'&MAXVAL+1)/2     CALCULATE PACKED LENGTH
&RTTTOTL SETA  &RTTTL+&RTTCL       LENGTH OF EACH ENTRY
&RTTNAME._COUNTER DC PL&RTTCL'0'   COUNTER MAP ENTRY
&RTTNAME._TEXT DC  CL&RTTTL' '     TEXT MAP ENTRY
&SYSECT  CSECT                     BACK TO CSECT
&RTTNAME DC    0D'0'               BEGINNING OF TABLE ENTRIES
         MEND
