         MACRO
&NAME    RTTENT &TEXT=
         GBLA  &RTTTL,&RTTCL,&RTTDEF
         AIF   (&RTTDEF EQ 0).NEXT000  0 ==> RTTBEG NOT YET CODED
         AIF   (&RTTDEF EQ 1).NEXT010  1 ==> A-OK - RTTBEG CODED
         AIF   (&RTTDEF EQ 2).NEXT005  2 ==> RTTEND ALREADY CODED
         MNOTE 8,'INVALID VALUE &RTTDEF FOR &&RTTDEF - PROBABLE MACRO LX
               OGIC ERROR'
         MEXIT
.NEXT000 MNOTE 8,'RTTENT MACRO INVALID PRECEDING RTTBEG MACRO'
         MNOTE 0,'PLEASE CORRECT AND RE-ASSEMBLE'
         MEXIT
.NEXT005 MNOTE 8,'RTTENT MACRO INVALID FOLLOWING RTTEND MACRO'
         MNOTE 0,'PLEASE CORRECT AND RE-ASSEMBLE'
         MEXIT
.NEXT010 AIF   ('&NAME' NE '').NEXT015 MUST HAVE COUNTER NAME
         MNOTE 8,'NAME MISSING FROM RTT ENTRY STATEMENT: COUNTER GENERAC
               TION IMPOSSIBLE'
         MEXIT
.NEXT015 ANOP
&NAME    DC    PL&RTTCL'0'         COUNTER
         AIF   ('&TEXT' NE '').NEXT020
*        MNOTE 0,'TEXT FOR COUNTER ASSUMED TO BE NAME OF COUNTER'
         DC    CL&RTTTL'&NAME'     COUNTER NAME FOR TITLE
         MEXIT
.NEXT020 AIF   ('&TEXT'(1,1) EQ '''').NEXT030 GO IF QUOTED
         DC    CL&RTTTL'&TEXT'     TITLE FOR AMOUNT TO BE PRINTED
         MEXIT
.NEXT030 DC    CL&RTTTL&TEXT       TITLE FOR AMOUNT TO BE PRINTED
         MEND
