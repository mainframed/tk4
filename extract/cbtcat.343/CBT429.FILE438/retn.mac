         MACRO
&NAME    RETN &COND
         COPY  $MGBLDEF
         GBLA  &VERSION            VERSION CONTROL
         GBLC  &GRETN              RETRIEVE NAME OF EXIT ROUTINE
         LCLA  &A
         LCLC  &Y                  TO HOLD CONDITION
         AIF   (&VERSION EQ 2).SKIP010
         AIF   ('&SYSECT' NE '&$MSECT(1)').INPROC GO IF IN PROC
         L     R13,4(,R13)         CHAIN BACK
         LM    R14,R15,12(R13)     RESTORE EVERYTHING BUT
         LM    R2,R12,28(R13)       ZERO AND ONE
         BR    R14                 GOBACK
         MEXIT
.INPROC  ANOP
         TRACER C'RETN EXECUTED IN PROC ''&$MSECT(&$MPLVL)'''
&NAME    B     &$MENAME(&$MPLVL)   BRANCH TO COMMON EXIT ROUTINE
.MEND    MEXIT
.SKIP010 ANOP
         AIF   (K'&COND GT 2).SKIP020 CAN'T BE VALID CONDITION...
&Y       SETC  '&COND'             SET TO VALID(?) CONDITION
.SKIP020 ANOP
&NAME    B&Y   &GRETN              BRANCH TO COMMON EXIT ROUTINE
         MEND
