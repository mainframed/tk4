         MACRO
&NAME    AFTER &VAR
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* AFTER 1 WILL CAUSE FOLLOWING CODE TO BE EXECUTED ON EVERY PASS     *
.*            EXCEPT THE FIRST PASS                                   *
.* AFTER N - EVERY PASS FOLLOWING THE N-TH PASS                       *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         COPY  $MGBLDEF            BRING IN THE CLOWNS
&LCLV    SETC  '&VAR'              SET TO COUNT
         AIF   ('&VAR'(1,1) GE '0').OK30 GO IF SELF-DEFINING
         MNOTE '''&VAR'' NOT SELF-DEFINING: DO-GROUP WILL BE PROCESSED C
               WITH EACH PASS THROUGH THIS SECTION OF CODE'
&LCLV    SETC  '0'                 DO EVERY TIME
.OK30    ANOP
&$MDGLVL SETA  &$MDGLVL+1          INCREMENT LEVEL
         LEVELMSG
&$MDGSEQ(&$MDGLVL) SETC '&SYSNDX'
&$MIFELS(&$MDGLVL) SETC 'F'
&$MDGNAM(&$MDGLVL) SETC '&NAME'
&X       SETC  '$MDV&SYSNDX'       MAKE IT PRETTY
         AIF   ('&LCLV' NE '1').PACKED
&NAME    TM    &X,X'01'            HAVE WE BEEN HERE BEFORE?
         BO    *+12                SKIP OVER IS YES
         OI    &X,X'01'            ELSE LEAVE TRACKS
         B     $MDF&SYSNDX           AND BYPASS DO-GROUP
         AGO   .DEFINE
.PACKED  ANOP                      I GUESS WE'LL HAVE TO DO DECIMAL...
&NAME    SP    &X,=P'1'            DECREMENT
         BNM   $MDF&SYSNDX         B IF NOT YET TIME
         PACK  &X,=Z'0'            DO EVERY TIME FROM NOW ON
.DEFINE  $WA                       SET UP WORK AREA
         AIF   ('&LCLV' NE '1').PACKED2
&X       DC    X'00'               SET TO X'01' FIRST TIME THROUGH
         AGO   .INST
.PACKED2 ANOP
&X       DC    P'&LCLV'            FREQUENCY COUNTER
.INST    $IA                       BACK TO INSTRUCTION AREA
         LEVELMSG
         MEND
