         MACRO
&NAME    CVN   &TARGET,&SOURCE,&LENGTH,&LEADING=0
         SPACE 1
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
* MACRO CONVERTS FROM EDITTED NUMERIC (WITH SPACES, COMMAS, $, ETC)   *
* TO NUMERIC, RIGHT JUSTIFIED. ALL CHARACTERS EXCEPT 0 - 9 ARE        *
* REMOVED. IF LENGTH IS SPECIFIED (THIRD POSITIONAL PARAMETER), IT    *
* MUST BE A VARIABLE, AND IT WILL CONTAIN THE NUMBER OF VALID DIGITS  *
* IN THE OPERAND. IT CAN BE A GPR (EG (R4)), OR A PACKED DECIMAL      *
* VARIBLE (EG DATA_NAME).                                             *
* DEFAULT IS TO PAD TO THE LEFT WITH ZEROS. OPTIONALLY, YOU MAY       *
* REQUEST PADDING WITH SPACES BY SPECIFYING 'LEADING=B' (FOR BLANKS). *
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         SPACE 1
         GBLC  &$PTAG1,&$PTAG2
         GBLA  &$PLEN1,&$PLEN2,&$MBLATH
         GBLC  &$PBASE1,&$PBASE2,&$PEB1,&$PEB2,&$PELB1,&$PELB2
         LCLC  &TAG1,&EB1,&ELB1,&LEAD,&REG,&OPCODE,&OPND
         LCLA  &LEN1
&LEAD    SETC  '0'                 OPERATING ASSUMPTION
         AIF   ('&LEADING'(1,1) NE 'B').CONT00
&LEAD    SETC  ' '                 LEADING SPACES
.CONT00  PAR$E &TARGET,&SOURCE     BREAK OPNDS UP
         AIF   (&$MBLATH LT 6).SKIP05
 DC 0C' $PTAG - $PLEN - $PBASE - $PEB - $PELB ' CVN DIAGNOSTIC - DESC.
 DC 0C'&$PTAG1-&$PLEN1-&$PBASE1-&$PEB1-&$PELB1' CVN DIAGNOSTIC - TARGET
 DC 0C'&$PTAG2-&$PLEN2-&$PBASE2-&$PEB2-&$PELB2' CVN DIAGNOSTIC - SOURCE
.SKIP05  ANOP
&TAG1    SETC  '&$PTAG1'
&EB1     SETC  '&$PEB1'
&ELB1    SETC  '&$PELB1'
&LEN1    SETA  &$PLEN1
         AIF   (T'&TARGET NE 'P').CONT10 GO IF CHARACTER
&TAG1    SETC  '$PWK&SYSNDX'
&EB1     SETC  ''
&LEN1    SETA  2*&LEN1-1
&ELB1    SETC  '(&LEN1)'
         $WA
&TAG1    DC    CL&LEN1.'0000000000000000000000000000000'
         $IA
.CONT10  ANOP
&NAME    MVC   &TAG1&ELB1,=&LEN1.C'&LEAD'
         AIF   ('&LENGTH' EQ '').SKIP40
         AIF   ('&LENGTH'(1,1) NE '(').TEST20
&REG     SETC  '&LENGTH(1)'
         SLR   &REG,&REG
&OPCODE  SETC  'AH'
&OPND    SETC  '=H''1'''
         AGO   .SKIP40
.TEST20  AIF   (T'&LENGTH EQ 'P').CONT30
         MNOTE 8,'INVALID LENGTH COUNTER &LENGTH - MACRO TERMINATED'
         MEXIT
.CONT30  PACK  &LENGTH,=Z'0'
&OPCODE  SETC  'AP'
&OPND    SETC  '=P''1'''
&REG     SETC  '&LENGTH'
.SKIP40  LA    R0,&TAG1&EB1
         LA    R1,&$PTAG2&$PEB2
         LA    R14,&TAG1+&LEN1-1&EB1
         LA    R15,&$PTAG2+&$PLEN2-1&$PEB2
         DO    UNTIL=((CLR,R14,R0,L),OR,                               C
               (CLR,R15,R1,L))
           IF    (CLI,0(R15),X'EF',H),AND,                             C
               (CLI,0(R15),X'FA',L),THENDO
             MVC   0(1,R14),0(R15)
             BCTR  R14,0
             AIF   ('&OPCODE' EQ '').SKIP50
             &OPCODE &REG,&OPND
.SKIP50      ANOP
           ENDO  ,
           BCTR  R15,0
         ENDO  ,
         AIF   (T'&TARGET NE 'P').MEND
         EPACK &TARGET,&TAG1&ELB1
.MEND    MEND
