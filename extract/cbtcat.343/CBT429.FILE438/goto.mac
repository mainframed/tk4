         MACRO
&NAME    GOTO  &COND,&SECTION
         COPY  $MGBLDEF
         GBLC  &GBASE,&GLINK       SET BY 'SECTION' MACRO OR USER
         LCLC  &OPCD,&X            FOR CONDITIONAL BRANCH & &SYSNDX
         VERSION 2
&X       SETC  '&SYSNDX'
         AIF   (N'&SYSLIST EQ 1).SECOND GO IF ONLY ONE OPERAND
         DC    0AL4(REF&X)         ERROR IF MACRO PARM NOT SECTION NAME
         $AGOTO &SECTION,&X
&NAME    LA    &GLINK,*+10         LEAVE TRACKS...
         L     15,=A(&SECTION-16) LOAD 'GO TO' ADDRESS
&OPCD    SETC  'B&COND.R'          SET TO 'BER', 'BNER', ETC.
         &OPCD  15                 CONDITIONAL BRANCH
         MEXIT                     WE'RE ALL DONE, FOLKS
.SECOND  CNOP  2,4                 ALIGN 2ND HALFWORD OF WORD
.*'THE FOLLOWING INSTRUCTION WILL CAUSE AN ''UNDEFINED SYMBOL'''
.*'(CONT''D) ERROR IF &COND IS NOT THE NAME OF A SECTION'
         DC    0AL4(REF&X)         ERROR IF OBJECT NOT SECTION NAME
         $AGOTO &COND,&X
&NAME    L     15,*+6              LOAD 'GO TO' ADDRESS
         BASR  &GLINK,15           BRANCH AND LEAVE TRACKS
         DC    A(&COND-16)         'GO TO' ADDRESS
         MEND
