         MACRO
&NAME    EPACK &TARGET,&SOURCE
         GBLC  &$PTAG1,&$PTAG2
         GBLA  &$PLEN1,&$PLEN2
         GBLC  &$PBASE1,&$PBASE2
         GBLA  &$MBLATH
         LCLA  &LEN1,&LEN2,&DISP1,&DISP2
         AIF   (&$MBLATH LT 6).SKIP00
         DC    0C'&TARGET'         EPACK DIAGNOSTIC - &TARGET
         DC    0C'&SOURCE'         EPACK DIAGNOSTIC - &SOURCE
.SKIP00  ANOP
         PAR$E &TARGET,&SOURCE     GET VALUES FOR TARGET AND SOURCE
         AIF   (&$PLEN1 LT 17).TEST02
         MNOTE 12,'PARM #1 LENGTH &$PLEN1 INVALID - MACRO ABORTED'
         MEXIT
.TEST02  AIF   (&$PLEN2 LT 32).TEST04
         MNOTE 12,'PARM #2 LENGTH &$PLEN2 INVALID - MACRO ABORTED'
         MEXIT
.TEST04  AIF   (&$PLEN1 LT 16 OR &$PLEN2 LT 31).TEST10
.* BOTH OPERANDS ARE MAX LENGTHS - GENERATE THREE OVERLAPPING PACKS
&NAME    PACK  &$PTAG1.(2&$PBASE1),&$PTAG2.(3&$PBASE2)
         PACK  &$PTAG1+1(8&$PBASE1),&$PTAG2+2(15&$PBASE2)
         PACK  &$PTAG1+8(8&$PBASE1),&$PTAG2+16(15&$PBASE2)
         MEXIT
.TEST10  AIF   (&$PLEN2 GT 16).TEST20 WILL ONE PACK WORK? GO IF NO
&NAME    PACK  &TARGET,&SOURCE
         MEXIT
.TEST20  AIF   (&$PLEN1 LE 8).NEXT50 GO GEN 1 PACK WITH ADJUSTMENTS
.* TWO OVERLAPPING PACK INSTRUCTIONS ARE REQUIRED
&DISP2   SETA  0                   OPERATING ASSUMPTION
         AIF   (&$PLEN2 NE 31).CONT30 GO IF CORRECT
&DISP2   SETA  2                   SET TO DISPLACEMENT
.CONT30  ANOP
&LEN1    SETA  &$PLEN1-7
&LEN2    SETA  &$PLEN2-14-&DISP2
&NAME    PACK  &$PTAG1.(&LEN1&$PBASE1),&$PTAG2+&DISP2.(&LEN2&$PBASE2)
&DISP1   SETA  &$PLEN1-8
&DISP2   SETA  &$PLEN2-15
         PACK  &$PTAG1+&DISP1.(8&$PBASE1),&$PTAG2+&DISP2.(15&$PBASE2)
         MEXIT
.NEXT50  ANOP
&DISP2   SETA  &$PLEN2-16
&NAME    PACK  &TARGET,&$PTAG2+&DISP2.(16&$PBASE2)
         MEND
