         MACRO
&N       PRTTBLE &TABLE,&WREG=R3   DISPLAY SEARCH TABLE ON SYSPRINT
         GBLC  &SUBCALL            PCALL/PERFORM DEPENDING ON VER 1/2
&N       LA    &WREG,&TABLE        COPY BEGINNING OF TABLE TO REGISTER
         MVC   OUTLINE(51),=CL51'0DEFINITION OF SCAN TABLE ''&TABLE'''
         &SUBCALL PRINTRTN         PRINT 'HEADER' LINE
         MVC   OUTLINE(51),=CL51' FLAGS     COUNT SCAN STRING'
         &SUBCALL PRINTRTN         PRINT 'HEADER' LINE
         MVI   CCCHAR,C'0'         NEXT LINE SHOULD BE DOUBLE SPACED
         DO    UNTIL=(CLI,0(&WREG),255,E) STOP LOOP AT TABLE END
           ICM   R1,3,0(&WREG)     GET TWO BYTES INTO REG 1
           &SUBCALL CVXWORD        CONVERT ALL 4 BYTES TO HEX
           STCM  R1,15,OUTLINE+2   COPY THE TWO BYTES WE WANT
           ZAP   WS_PL4,2(4,&WREG) COPY COUNTER VALUE
           EM    OUTLINE+6(10),WS_PL4 EDIT TO CHAR FORM
           SLR   R1,R1             SET TO ZERO FOR MATH FUNCTION
           IC    R1,0(,&WREG)      SET LOW ORDER BYTE TO LENGTH
           EX    R1,*+4            COPY TEXT TO PRINT LINE
           MVC   OUTLINE+17(0),6(&WREG) LAZY WAY OF DOING 'EX'
           LA    &WREG,7(R1,&WREG) BUMP TO NEXT SEARCH STRING
           &SUBCALL PRINTRTN       PRINT THE LINE JUST FORMATTED
         ENDO  ,                   END OF PRINT LOOP
         MVI   CCCHAR,C'0'         SET TO DOUBLE SPACE FOR NEXT PRINT
         MEND
