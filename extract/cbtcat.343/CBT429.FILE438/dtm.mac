         MACRO
&NAME    DTM   &TIME,&DATE        ** ANOTHER ** DATE/TIME MACRO
         GBLC  &GSECT,&WSECT      SECTION NAMES
&NAME    STM   R0,R4,$DTMSAVE     SAVE WORK REGS
         AIF   ('&TIME' EQ 'DUMMY').SKIP1 TIME IS OPTIONAL
         ST    R0,$DTMWFWD        COPY TIME
         MVO   $DTMWPL4,$DTMWFWD(3) DROP FRACTION, AND GET GET A SIGN
         MVC   $DTMWCLA,=X'402120204B20204B2020' TIME MASK
         ED    $DTMWCLA,$DTMWPL4  PRINTABLE
         MVC   &TIME,$DTMWCLA+2   INTO RECORD AREA
.SKIP1   AIF   ('&DATE' EQ 'DUMMY').SKIP2 SO IS DATE
         ST    R1,$DTMWFWD        DATE
         MVO   $DTMWPL4,$DTMWFWD+1(1) SHIFT PAST SIGN
         MVC   $DTMBLDD+2(1),$DTMWFWD+1 ISOLATE YEAR
         CVB   R1,$DTMDBL         BINARY
         LR    R3,R1              COPY
         SRL   R1,2               DIV BY 4
         SLL   R1,2               MULT BY 4
         IF    (CLR,R3,R1,E),THENDO IF = THEN LEAP YEAR
           LA    R2,$DTMLMON      POINT TO LEAP
           LA    R4,$DTMLADJ      . TABLES
         ELSE ,
           LA    R2,$DTMSMON      ELSE POINT TO STD
           LA    R4,$DTMSADJ      . TABLES
         ENDELSE ,
         ZAP   $DTMWPL4,$DTMWFWD+2(2) DAY OF YEAR INTO WORK
         CVB   R1,$DTMDBL          BINARY
         LR    R0,R1               COPY
         BCTR  R1,0                SET TO RANGE 0 - 364
         IC    R3,0(R1,R2)         GET MONTH FOR THIS DAY
         CVD   R3,$DTMDBL          DECIMAL
         MVO   $DTMWXL2,$DTMDBL+6(2) ISOLATE MONTH
         MVC   $DTMBLDD(1),$DTMWXL2 $DTMBLDD IS NOW MMXXYY
         BCTR  R3,0                SET TO RANGE 0 - 11
         LR    R1,R3               COPY
         SLL   R3,1                DOUBLE FOR ADJ FACTOR
         MH    R1,=H'30'           APPROX DAY# FOR 1ST OF MONTH
         AH    R1,0(R3,R4)         EXACT DAY# FOR 1ST OF MONTH
         SR    R0,R1               GET DAY OF MONTH
         CVD   R0,$DTMDBL          DECIMAL
         MVO   $DTMWXL2,$DTMDBL+6(2) ISOLATE DAY
         MVC   $DTMBLDD+1(1),$DTMWXL2 $DTMBLDD IS NOW MMDDYY
         MVC   $DTMWCLA,=X'40212020612020612020' MAKE IT PRETTY
         MVO   $DTMWPL4,$DTMBLDD   SHIFT OVER PAST SIGN
         ED    $DTMWCLA,$DTMWPL4   DO THE EDIT
         MVC   &DATE,$DTMWCLA+2    INTO RECORD AREA
.SKIP2   LM    R0,R4,$DTMSAVE      RESTORE WORK REGS
         AIF   (D'$DTMSAVE).MEND   SKIP IF NOT FIRST TIME
         $WA
$DTMSAVE DC    5F'0'               FOR REGS 0 - 4
$DTMDBL  DC    0D'0',F'0'          ALIGNMENT AND FIRST WORD FOR CVB/CVD
$DTMWPL4 DC    PL4'0'              SECOND WORD FOR CVB/CVD
$DTMWFWD DC    F'0'                WORK AREA FULLWORD
$DTMWXL2 DC    XL2'0000'           WORK AREA HEX LENGTH 2
$DTMWCLA DC    CL10' '             WORK AREA CHAR LENGTH 10
$DTMBLDD DC    XL3'00'             BUILD AREA FOR DATE
$DTMSMON DC    31AL1(1),28AL1(2),31AL1(3),30AL1(4)    MONTH ID
         DC    31AL1(5),30AL1(6),31AL1(7),31AL1(8)    . FOR EACH
         DC    30AL1(9),31AL1(10),30AL1(11),31AL1(12) . DAY
$DTMSADJ DC    0H'0',AL2(0,1,65535,0,0,1,1,2,3,3,4,4) ADJUSTMENT
$DTMLMON DC    31AL1(1),29AL1(2),31AL1(3),30AL1(4)    MONTH ID
         DC    31AL1(5),30AL1(6),31AL1(7),31AL1(8)    . FOR LEAP
         DC    30AL1(9),31AL1(10),30AL1(11),31AL1(12) . YEAR DAYS
$DTMLADJ DC    0H'0',AL2(0,1,0,1,1,2,2,3,4,4,5,5)     ADJUSTMENT (LEAP)
         $IA
.MEND    MEND
