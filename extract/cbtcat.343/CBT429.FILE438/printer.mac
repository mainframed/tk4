         MACRO
&PROCN PRINTER &DDNAME=SYSPRINT,   DEFAULT DDNAME                      C
               &LRECL=133,         LOGICAL RECORD LENGTH               C
               &BLKSIZE=133,       JES2 TAKES CARE OF BLOCKING         C
               &OUTLINE=OUTLINE,   OUTPUT LINE NAME                    C
               &CCCHAR=CCCHAR,     CARRIAGE CONTROL                    C
               &HEADER1=HEADER1,   HEADER LINE NAME                    C
               &HEADER2=,          HEADER LINE NAME                    C
               &HEADER3=,          HEADER LINE NAME                    C
               &SPACES=SPACES,     TO BLANK OUT OUTLINE                C
               &LINECT=LINECT,     DEFAULT LINE COUNTER                C
               &PSIZE=60,          DEFAULT PAGE SIZE                   C
               &PCTR=PCTR,         PAGE COUNTER                        C
               &PFIELD=,           FIELD FOR PAGE NUMBER               C
               &DCBNAME=PRINTER    DEFAULT DCB NAME
         GBLC  &GSECT,&WSECT
         VERSION 2
         TITLE 'PRINT ROUTINE'
         SPACE
***********************************************************************
*        STANDARD PRINT ROUTINE PRINTS FROM '&OUTLINE', AND PRINTS    *
*        '&HEADER1' AT THE TOP OF EACH PAGE. IF '&HEADER2' OR         *
*        '&HEADER3' IS DEFINED, THEY ARE PRINTED AS APPROPRIATE.      *
***********************************************************************
         SPACE
&PROCN   SECTION
         SPACE
***********************************************************************
*        MAKE SURE THAT THE DCB HAS BEEN OPENED.                      *
***********************************************************************
         SPACE
         FIRST 1                   CHECK DCB FIRST CALL ONLY
           SPACE
           IF    (TM,&DCBNAME+48,X'10',NO),THENDO OPENED?
             OPEN  (&DCBNAME,OUTPUT) DO IF NECESSARY
             IF    (TM,&DCBNAME+48,X'10',NO),THENDO OPEN FAIL?
               ABEND 16,DUMP       MIGHT AS WELL QUIT NOW
             ENDO  , ** NOTE ** POSSIBLE TO ADD DYNALLOC CODE HERE TO
           ENDO  ,   ** NOTE ** . FORCE ALLOCATION FOR PRINTER FILE.
         ENDO  ,     ** NOTE ** . REQUESTS, ANYONE?
         SPACE
***********************************************************************
*        KEEP TRACK OF LINECT, AND DO OVERFLOW PROCESSING WHEN        *
*        REQUIRED.                                                    *
***********************************************************************
         SPACE
         IF    (CLI,&CCCHAR,C' ',E),THENDO IS IT SINGLE?
           AP   &LINECT,=P'1'      1 FOR SINGLE SPACING
         ELSEIF (CLI,&CCCHAR,C'0',E),THENDO IS IT DOUBLE?
           AP   &LINECT,=P'2'      BUMP FOR DOUBLE
         ELSEIF (CLI,&CCCHAR,C'-',E),THENDO IS IT TRIPLE?
           AP   &LINECT,=P'3'     BUMP FOR TRIPLE
         ELSEIF (CLI,&CCCHAR,C'1',E),THENDO NEW PAGE?
           PACK &LINECT,=Z'1'     'RESET' LINE COUNT
         ELSEIF (CLI,&CCCHAR,C'C',E),THENDO CHANNEL 12?
           PACK &LINECT,=Z'&PSIZE' FORCE OVERFLOW FOR NEXT CALL
         ELSEIF (CLI,&CCCHAR,C'+',NE),THENDO DO IF NOT SUPPRESS
           AP   &LINECT,=P'1'     ASSUME SINGLE
         ENDELSE ,
         CP   &LINECT,=P'&PSIZE'   FINISHED WITH PAGE?
         THENDO H                  OVERFLOW IF NECESSARY
          AIF   ('&PFIELD' EQ '').SKIP02
          AP    &PCTR,=P'1'        BUMP PAGE COUNTER
          IF    (CP,&PCTR,=P'10',L),THENDO
            EM2   0+&PFIELD.(1),0+&PCTR.(4)
          ELSEIF (CP,&PCTR,=P'100',L),THENDO
            EM2   0+&PFIELD.(2),0+&PCTR.(4)
          ELSEIF (CP,&PCTR,=P'1000',L),THENDO
            EM2   0+&PFIELD.(3),0+&PCTR.(4)
          ELSEIF (CP,&PCTR,=P'10000',L),THENDO
            EM2   0+&PFIELD.(5),0+&PCTR.(4)
          ELSEIF (CP,&PCTR,=P'100000',L),THENDO
            EM2   0+&PFIELD.(6),0+&PCTR.(4)
          ELSE  ,
            EM2   0+&PFIELD.(8),0+&PCTR.(4)
          ENDIF  ,
.SKIP02   ANOP
          PUT   &DCBNAME,&HEADER1  PUT OUT HEADER LINE
          PACK &LINECT,=Z'3'       ALLOW FOR HEADER & 1ST DOUBLE
          AIF   ('&HEADER2' EQ '').SKIP10 SKIP IF NOT NECESSARY
          IF   (CLC,&HEADER2,&SPACES,NE),THENDO DO IF 2ND IS PRESENT
           PUT   &DCBNAME,&HEADER2 PUT OUT 2ND HEADER LINE
           AP    &LINECT,=P'1'     BUMP LINE COUNT
          ENDO  ,
.SKIP10   AIF   ('&HEADER3' EQ '').SKIP20 SKIP IF NOT NECESSARY
          IF    (CLC,&HEADER3,&SPACES,NE),THENDO DO IF 3RD IS PRESENT
           PUT   &DCBNAME,&HEADER3 PUT OUT 3RD HEADER LINE
           AP    &LINECT,=P'1'    BUMP LINE COUNT
          ENDO  ,
.SKIP20   ANOP
          IF    (CLI,&CCCHAR,C' ',E),THENDO IF SINGLE THEN...
            MVI  &CCCHAR,C'0'      FORCE FIRST DATA TO BE DOUBLE
          ENDO  ,
         ENDO
         EJECT
***********************************************************************
*        ** FINALLY ** PRINT THE LINE IN QUESTION.                    *
***********************************************************************
         SPACE
         PUT   &DCBNAME,&OUTLINE   PUT OUT LINE
         SPACE
         MVC   &OUTLINE,&SPACES    BLANK INCL&CCCHAR
         SPACE
&WSECT   LOCTR
         SPACE
&LINECT  DC    PL2'&PSIZE'         FORCE OVERFLOW ON 1ST CALL
         SPACE
***********************************************************************
*        DEFINE PRINTER DCB                                           *
***********************************************************************
         SPACE
         PUSH  PRINT
         SPACE
         PRINT NOGEN               CREATE DCB WITH NOGEN
         SPACE
&DCBNAME DCB   DDNAME=&DDNAME,                                         X
               DSORG=PS,                                               X
               MACRF=PM,                                               X
               RECFM=FBA,                                              X
               LRECL=&LRECL,                                           X
               BLKSIZE=&BLKSIZE
         SPACE
         POP   PRINT
         SPACE
***********************************************************************
*        DEFINE HEADER AND DATA AREAS                                 *
***********************************************************************
         SPACE
&HEADER1 DC    CL&LRECL'1'         USER SHOULD MOVE IN DESIRED DATA
         SPACE
         AIF   ('&HEADER2' EQ '').SKIP30 SKIP IF NOT NECESSARY
&HEADER2 DC    CL&LRECL' '         USER SHOULD MOVE IN DESIRED DATA
         SPACE
.SKIP30  AIF   ('&HEADER3' EQ '').SKIP40 SKIP IF NOT NECESSARY
&HEADER3 DC    CL&LRECL' '         USER SHOULD MOVE IN DESIRED DATA
         SPACE
.SKIP40  ANOP
&OUTLINE DC    0CL&LRECL' '        TOTAL DEFINITION OF LINE
&CCCHAR  DC    C' ',CL(&LRECL-1)' ' CARRIAGE CONTROL, THEN DATA
         SPACE
         DC    C'0'                DOUBLE SPACE CTL CHAR
         SPACE
&SPACES  DC    CL&LRECL' '         GUESS?
&PCTR    DC    PL4'0'
         AIF   ('&GSECT' NE '&SYSLOC').SKIP50
&GSECT   LOCTR
.SKIP50  ANOP
         ENDSECT
         MEND
