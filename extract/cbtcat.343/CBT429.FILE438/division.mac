         MACRO
&TYPE    DIVISION &PROGID=,&INST=,&DATA=,&BASE=,&LINK=,                C
               &PARMS=YES,&INTRO=YES
         COPY  $MGBLDEF
         GBLC  &GSECT,&WSECT       SECTION NAMES
         GBLC  &NSECT              SECTION NAME
         GBLC  &GSAVE              SAVE AREA NAME
         GBLC  &GPROG              SET TO CSECT NAME
         GBLC  &PARM               INTERNAL USE ONLY
         GBLC  &GBASE,&GLINK       DEFINE GLOBAL BASE AND LINK REGS
         GBLC  &GRETN,&GSKIP       FOR SECTION MACRO
         GBLB  &SEQSW              SEQUENCE SWITCH
         LCLC  &X                  FOR ENTRY STATEMENT
         SETCSECT &PROGID
         SETREGS
         VERSION 2
&$GOTOM  SETA  4096
&$SECTM  SETA  4096
&X       SETC  '&SYSNDX'           SHORTHAND
         AIF   ('&TYPE' EQ 'DATA').DATA
         AIF   ('&TYPE' EQ 'PROCEDURE').DATA
         AIF   ('&TYPE' EQ 'IDENTIFICATION').DATA
         MNOTE 8,'INVALID DIVISION &TYPE - MACRO TERMINATED'
         MEXIT
.DATA    ANOP
&PARM    SETC  '&GSECT'
         SETDEF &INST,GSECT
&GSECT   SETC  '&PARM'
&PARM    SETC  '&WSECT'
         SETDEF &DATA,WSECT
&WSECT   SETC  '&PARM'
&PARM    SETC  '&GBASE'
         SETDEF &BASE,12
&GBASE   SETC  '&PARM'
&PARM    SETC  '&GLINK'
         SETDEF &LINK,14
&GLINK   SETC  '&PARM'
&PARM    SETC  '&GPROG'
         SETDEF &PROGID,MAIN
&GPROG   SETC  '&PARM'
         AIF   ('&TYPE' NE 'IDENTIFICATION').NOTID
         MNOTE *,'SOURCE LIST FOR &PROGID'
&DCBGEN  SETC  'NOGEN'                 ASSUME...
         MEXIT
.NOTID   ANOP
&NSECT   SETC  '&GSECT'
         AIF   (D'$REGSAVE).SKIP006 GO IF ALREADY IN CSECT
&GPROG   CSECT                     BEGINNING OF PROGRAM
&GSECT   LOCTR                     INSTRUCTIONS COME FIRST, THANK YOU
&WSECT   LOCTR                     WORKING STORAGE LOCATION COUNTER
&GSAVE   SETC  '$REGSAVE'          FOR UNIQUENESS
&GSAVE   DC    18F'0'              REGISTER SAVE AREA
PARMLEN  DC    H'0'                LENGTH OF PARM FIELD
PARMVAL  DC    CL253' '            VALUE OF PARM
         AIF   ('&TYPE' EQ 'DATA').MEND999
.SKIP006 AIF   ('&TYPE' EQ 'PROCEDURE').PROC
         AIF   ('&TYPE' NE 'DATA').MEND999
&WSECT   LOCTR
         MEXIT
.PROC    ANOP
$INTRO   WTO   '&GPROG EXECUTING ON 11/11/11 AT 00.00.00',MF=L,        C
               ROUTCDE=(11)
&TEMP    SETA  K'&GPROG
         ORG   $INTRO+18+&TEMP     LOCATION FOR DATE INFO
$EXDT    DC    CL8' '
         ORG   $INTRO+30+&TEMP     LOCATION FOR TIME INFO
$EXTM    DC    CL8' '
         ORG   ,
&GSECT   LOCTR                     BACK TO PROCDIV LOCTR
         USING &GSECT,&GBASE       SET UP BASE REG
         STM   14,12,12(13)        SAVE CALLER'S REGS
         LR    &GBASE,15           LOAD BASE REG
         XC    16(4,13),16(13)     SET ZERO RETURN CODE
         L     14,=A(&GSAVE)       SAVE
         ST    14,8(,13)           . AREA
         ST    13,4(,14)           . CHAINING
         LR    13,14               PRIME OUR SAVE POINTER
         USING &GSAVE,13           SET UP A FREE BASE REG
         AIF   ('&PARMS' EQ 'NO').SKIP010
         L     14,0(,1)            POINTER TO PASSED PARAMETERS
         IF    (LTR,14,14,M),THENDO DO IF ONLY ONE PARM
             LH    15,0(,14)       LENGTH OF PARAMETER FIELD
             LA    15,1(,15)       TO COVER LENGTH OF LENGTH
             EX    15,*+4          MOVE IN PARAMETER
             MVC   PARMLEN(0),0(14) ** EXECUTE **
         ENDO  ,
.SKIP010 ANOP
&GRETN   SETC  'RETN&X'            UNIQUE TAG FOR EXIT
         DC    0C'&&GRETN SET TO RETN&X'
&GSKIP   SETC  'SKIP&X'            UNIQUE TAG FOR END OF SECTION
         DC    0C'&&GSKIP SET TO SKIP&X'
         B     STRT&X               . AND B TO SECTION PROPER
         LTORG ,                   ENSURE ADDRESSABILITY
RETN&X   L     &GLINK,LINK&X       RESTORE LINK REGISTER
         B     8(,&GLINK)           AND RETURN
LINK&X   DC    A(&GSKIP-8)         'EXIT' ADDRESS
         $ASECT STRT&X             ADD TO THE LIST
STRT&X   DC    0H'0'               START OF SECTION
         AIF   ('&INTRO' EQ 'NO').SKIP030
         AIF   ('&INTRO' EQ 'YES').NEXT020
         MNOTE 4,'''&INTRO'' IS AN INVALID VALUE FOR ''INTRO'' - '
         MNOTE 0,'A VALUE OF ''YES'' IS FORCED'
.NEXT020 FIRST 1
           WTO   '&GPROG ASSEMBLED ON &SYSDATE AT &SYSTIME',           C
               ROUTCDE=(11)
           TIME  DEC               HEY, JOE, WHAT TIME IS IT?
           DTM   $EXTM,$EXDT       FORMAT FOR EXECUTING MESSAGE
           WTO   MF=(E,$INTRO),    SHOW DATE/TIME EXECUTING            C
               ROUTCDE=(11)
         ENDO  ,
         L     1,4(,13)            BACK CHAIN
         L     1,24(,1)            PREV VALUE FOR 1
.SKIP030 AIF   (D'R0).MEND999
         EQUATE
.MEND999 MEND
