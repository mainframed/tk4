         MACRO
&NAME    TRC   &CTR,&TEXT
         GBLC  &TRACE,&GSECT,&WSECT
         GBLA  &VERSION
         AIF   (&VERSION EQ 2).NEXT100
         VERSION 1
         AIF   ('&TRACE' EQ 'YES').NEXT010
         AIF   ('&NAME' EQ '').LAST999
&NAME    DC    0H'0'
         AGO   .LAST999
.NEXT010 ANOP
&Z       SETC  '&SYSNDX'
DATAAREA CSECT
$TRS&Z   DC    4F'0'               SAVE REGS 14 - 1
&Y       SETC  '&CTR'              MAKE ASSUMPTION
         AIF   ('&CTR' NE '').SKIP020 GO IF ASSUMPTION WAS RIGHT
&Y       SETC  '$MDC&SYSNDX'       MAKE IT PRETTY
.SKIP020 ANOP
         AIF   ('&TEXT' EQ '').SKIP030
&X       SETC  '$MDT&SYSNDX'       MAKE IT PRETTY
&X       DC    C&TEXT
&L       SETA  L'&X
.SKIP030 AIF   ('&CTR' NE '').SKIP040
&Y       DC    PL2'1'              COUNTER
.SKIP040 ANOP
         MNOTE '&SYSNDX '&TEXT''
&SYSECT  CSECT
&NAME    STM   R14,R1,$TRS&Z       SAVE THE REGS
         MVC   OUTLINE+1(4),=CL4'&SYSNDX' SEQUENCE STAMP
         MVC   OUTLINE+6(4),=X'40202120' EDIT MASK
         ED    OUTLINE+6(4),&Y     ITERATION COUNT
         AIF   ('&TEXT' EQ '').SKIP050
         MVC   OUTLINE+11(&L),&X   MOVE IN IDENTIFIER
.SKIP050 ANOP
         PCALL PRINTRTN            CALL ROUTINE TO PRINT
         AP    &Y,=P'1'            BUMP
         LM    R14,R1,$TRS&Z       RESTORE TO PRISTINE
         MEXIT
.NEXT100 VERSION 2
         AIF   ('&TRACE' EQ 'YES').NEXT110
         AIF   ('&NAME' EQ '').LAST999
&NAME    DC    0H'0'
         AGO   .LAST999
.NEXT110 ANOP
&Z       SETC  '&SYSNDX'
&WSECT   LOCTR
$TRS&Z   DC    4F'0'               SAVE REGS 14 - 1
&GSECT   LOCTR
&Y       SETC  '&CTR'              MAKE ASSUMPTION
         AIF   ('&CTR' NE '').SKIP120 GO IF ASSUMPTION WAS RIGHT
&Y       SETC  '$MDC&SYSNDX'       MAKE IT PRETTY
.SKIP120 ANOP
&WSECT   LOCTR
         AIF   ('&TEXT' EQ '').SKIP130
&X       SETC  '$MDT&SYSNDX'       MAKE IT PRETTY
&X       DC    C&TEXT
&L       SETA  L'&X
.SKIP130 AIF   ('&CTR' NE '').SKIP140
&Y       DC    PL2'1'              COUNTER
.SKIP140 ANOP
         MNOTE '&SYSNDX '&TEXT''
&GSECT   LOCTR
&NAME    STM   R14,R1,$TRS&Z       SAVE THE REGS
         MVC   OUTLINE+1(4),=CL4'&SYSNDX' SEQUENCE STAMP
         MVC   OUTLINE+6(4),=X'40202120' EDIT MASK
         ED    OUTLINE+6(4),&Y     ITERATION COUNT
         AIF   ('&TEXT' EQ '').SKIP150
         MVC   OUTLINE+11(&L),&X   MOVE IN IDENTIFIER
.SKIP150 ANOP
         PERFORM PRINTRTN          CALL ROUTINE TO PRINT
         AP    &Y,=P'1'            BUMP
         LM    R14,R1,$TRS&Z       RESTORE TO PRISTINE
.LAST999 MEND
