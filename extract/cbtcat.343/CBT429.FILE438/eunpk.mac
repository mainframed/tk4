         MACRO
&NAME    EUNPK &TARGET,&SOURCE
         GBLC  &$PTAG1,&$PTAG2
         GBLA  &$PLEN1,&$PLEN2
         GBLC  &$PBASE1,&$PBASE2
         LCLA  &LEN1,&LEN2,&DISP1,&DISP2
         PAR$E &TARGET,&SOURCE     GET VALUES FOR TARGET
         AIF   (&$PLEN1 LT 32).TEST02
         MNOTE 12,'PARM #1 LENGTH &$PLEN1 INVALID - MACRO ABORTED'
         MEXIT
.TEST02  AIF   (&$PLEN2 LT 17).TEST04
         MNOTE 12,'PARM #2 LENGTH &$PLEN2 INVALID - MACRO ABORTED'
         MEXIT
.TEST04  AIF   (&$PLEN1 LT 31 OR &$PLEN2 LT 16).TEST10
.* BOTH OPERANDS ARE MAX LENGTHS - GENERATE THREE OVERLAPPING UNPACKS
&NAME    UNPK  &$PTAG1.(3&$PBASE1),&$PTAG2.(2&$PBASE2)
         UNPK  &$PTAG1+2(15&$PBASE1),&$PTAG2+1(8&$PBASE2)
         UNPK  &$PTAG1+16(15&$PBASE1),&$PTAG2+8(8&$PBASE2)
         MEXIT
.TEST10  AIF   (&$PLEN1 GT 16).TEST20 TEST SOME MORE
&NAME    UNPK  &TARGET,&SOURCE
         MEXIT
.TEST20  AIF   (&$PLEN2 LE 8).NEXT50 GO GEN 1 UNPACK WITH ADJUSTMENTS
.* TWO OVERLAPPING UNPACK INSTRUCTIONS ARE REQUIRED
&DISP1   SETA  0                   OPERATING ASSUMPTION
         AIF   (&$PLEN1 NE 31).CONT30 GO IF CORRECT
&DISP1   SETA  2                   SET DISPLACEMENT
.CONT30  ANOP
&LEN1    SETA  &$PLEN1-14-&DISP1
&LEN2    SETA  &$PLEN2-7
&N       SETC  '&NAME'
         AIF   (&DISP1 EQ 0).SKIP40
&N       MVC   &$PTAG1.(&DISP1&$PBASE1),=15C'0'
&N       SETC  ''
.SKIP40  ANOP
&N       UNPK  &$PTAG1+&DISP1.(&LEN1&$PBASE1),&$PTAG2.(&LEN2&$PBASE2)
&DISP1   SETA  &$PLEN1-15
&DISP2   SETA  &$PLEN2-8
         UNPK  &$PTAG1+&DISP1.(15&$PBASE1),&$PTAG2+&DISP2.(8&$PBASE2)
         MEXIT
.NEXT50  ANOP
&DISP1   SETA  &$PLEN1-16
&NAME    MVC   &$PTAG1.(&DISP1&$PBASE1),=15C'0'
         UNPK  &$PTAG1+&DISP1.(16&$PBASE1),&SOURCE
         MEND
