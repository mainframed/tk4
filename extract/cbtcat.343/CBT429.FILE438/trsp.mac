         MACRO
&NAME    TRSP  &FIELD                 FIND NUMBER OF TRAILING SPACES
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* 'TRSP' WILL RETURN (IN REG 15) THE NUMBER OF TRAILING SPACES IN THE*
.* OPERAND FIELD. THE LENGTH OF THE FIELD MAY BE FROM 1 TO 256 BYTES. *
.* TO PROCESS THE FIELD USING THE LENGTH DEFINED FOR THE FIELD CODE:  *
.*                                                                    *
.*      TRSP  MYFIELD                                                 *
.*                                                                    *
.* WHERE 'MYFIELD' IS THE FIELD TO BE TESTED. IF YOU NEED TO SPECIFY A*
.* LENGTH OTHER THAN THE DEFAULT, THEN CODE THE LENGTH WITHIN         *
.* PARENTHESES IMMEDIATELY FOLLOWING THE OPERAND:                     *
.*                                                                    *
.*      TRSP  MYFIELD(30)                                             *
.*                                                                    *
.* IF YOU NEED TO CODE THE OPERAND USING BASE+DISPLACEMENT, THEN CODE:*
.*                                                                    *
.*      TRSP  12(30,R7)                                               *
.*                                                                    *
.* WHERE 12 IS THE DISPLACEMENT (ANY VALUE FROM 0 TO 4095) AND R7 IS  *
.* ANY GPR OTHER THAN R0. IN THIS FORM, THERE IS NO DEFAULT LENGTH, SO*
.* IT MUST BE EXPLICITLY CODED AS THE FIRST VALUE WITHIN THE          *
.* PARENTHESES.                                                       *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         GBLA  &$PLEN1                WILL BE SET TO LENGTH CODE
         GBLC  &$PEB1,&$PELB1,&$PTAG1 OPERAND FRAGMENTS
         LCLA  &Y                     LOCAL VARIABLE
         LCLC  &X                     LOCAL VARIABLE
&X       SETC  '&SYSNDX'              SET IT TO MACRO SEQUENCE NUMBER
         PAR$E &FIELD                 FERRET OUT LENGTH PARM
&Y       SETA  &$PLEN1                SET IT TO LENGTH OF USER'S FIELD
&NAME    MVC   $WK&X,REVERSE+256-&Y   COPY MASK TO LOCAL VARIABLE
         TR    $WK&X,&$PTAG1&$PEB1    COPY & REVERSE USER'S DATA
         LA    R1,$WK&X+&Y            DEFAULT POINTER
         TRT   $WK&X,NONBLANK         FIND FIRST (LAST) NON-BLANK
         SL    R1,=A($WK&X)           SET R1 TO # OF TRAILING SPACES
         LR    R15,R1                 COPY LENGTH TO STD RETURN REG
         $WA                          ESTABLISH WORKING-STORAGE
$WK&X    DC    CL&Y' '                LOCAL VARIABLE
         AIF   (D'REVERSE).SKIP100    SKIP DEFS IF ALREADY DEFINED
         DC    0D'0'                  ALIGNMENT FOR EFFICIENCY
REVERSE  DC    256AL1(REVERSE+255-*)  REVERSING TABLE
NONBLANK DC    64X'01',X'00',191X'01' NON-BLANK TABLE
.SKIP100 $IA                          RE-ESTABLISH EXECUTABLE ENV
         MEND
