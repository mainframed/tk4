         MACRO
&NAME    PCALL  &PROC,&VAR,&WHILE=,&UNTIL=
         COPY  $MGBLDEF
         LCLB  &DOLOOP             FLAG TO CREATE 'ENDO'
         LCLC  &N                  FOR NAME FIELD
         VERSION 1
&N       SETC  '&NAME'             SET TO USER SPECIFIED NAME
         AIF   (&$MPCSV(&$MPLVL) EQ 0).AOK IF REGS SAVED, THEN OK
         MNOTE 8,'''PCALL'' WITHIN PROC WITH NO REGS SAVED INVALID'
         MNOTE *,'PROGRAM LOOP PROBABLE'
.AOK     AIF   ('&SYSECT' NE 'DATAAREA').BOK
         MNOTE 8,'''PCALL'' WITHIN CODE IN CSECT ''DATAAREA'' CAN CAUSEC
                UNPREDICTABLE RESULTS - CONFLICT WITH BASE REG AND R13 C
                - MOVEMENT OF CODE TO ANOTHER CSECT IS ADVISED'
.BOK     AIF   ('&VAR' NE '').VAR10 GO IF VAR SPECIFIED
         AIF   ('&WHILE' NE '').WHILE20 GO IF WHILE SPECIFIED
         AIF   ('&UNTIL' NE '').UNTIL30 GO IF UNTIL SPECIFIED
         AGO   .COK
.VAR10   AIF   ('&WHILE' NE '').NOTOK
         AIF   ('&UNTIL' NE '').NOTOK
&N       DO    &VAR                CREATE DO-LOOP
&N       SETC  ''                  NULLIFY NAME FIELD
&DOLOOP  SETB  (1)                 SHOW 'ENDO' REQUIRED
         AGO   .COK                CONTINUE WITH COMMON CODE
.WHILE20 AIF   ('&UNTIL' NE '').NOTOK
&N       DO    WHILE=&WHILE        CREATE DO-LOOP
&N       SETC  ''                  NULLIFY NAME FIELD
&DOLOOP  SETB  (1)                 SHOW 'ENDO' REQUIRED
         AGO   .COK                CONTINUE WITH COMMON CODE
.UNTIL30 AIF   ('&UNTIL' EQ '').COK GO IF NO DO-LOOP TO CREATE
&N       DO    UNTIL=&UNTIL        CREATE DO-LOOP
&N       SETC  ''                  NULLIFY NAME FIELD
&DOLOOP  SETB  (1)                 SHOW 'ENDO' REQUIRED
         AGO   .COK                CONTINUE WITH COMMON CODE
.NOTOK   MNOTE 12,'MUTUALLY EXCLUSIVE PARAMETERS - MACRO ABORTED'
         MEXIT
.COK     AIF   ('&PROC'(1,1) NE '(').NOTREG
&N       VAL   &$BASE(1),&PROC     CALLER'S REG INTO BASE
         CNOP  2,4                 ALIGN ON HALFWORD
         BASR  &$LINK,&$BASE(1)    BRANCH TO PROC
         DC    A(0,&$MSECT(&$MPLVL)) LINKAGE ADDRESSES
         AGO   .CLEANUP
.NOTREG  ANOP
         CNOP  2,4                 ALIGN ON HALFWORD
&N       L     &$BASE(1),*+6       ADDRESSABILITY FOR PROC
         BASR  &$LINK,&$BASE(1)    BRANCH TO PROC
         DC    A(&PROC,&$MSECT(&$MPLVL)) LINKAGE ADDRESSES
.CLEANUP AIF   (NOT &DOLOOP).MEND  CREATE 'ENDO' ONLY IF REQUESTED
         ENDO  ,                   END OF PCALL LOOP
.MEND    MEND
