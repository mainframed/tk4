         MACRO
&NAME    PERFORM &SECTION          CALL A CO-ASSEMBLED SUBROUTINE
         COPY  $MGBLDEF
         GBLC  &GBASE,&GLINK       EXTRACT PRESET BASE AND LINK REGS
         GBLC  &NSECT              EXTRACT SECTION NAME
         LCLC  &N,&X               FOR NAME FIELD AND &SYSNDX
         VERSION 2
&N       SETC  '&NAME'             SET TO USER-SPECIFIED NAME
&X       SETC  '&SYSNDX'           FOR UNIQUENESS
         AIF   ('&SECTION'(1,1) NE '(').NOTREG GO IF NOT GPR
         CNOP  0,4                 ALIGN ON WORD BOUNDARY
&NAME    LR    15,&SECTION(1)      CALLER'S REG INTO 15
         SL    15,*+6              DECREMENT TO BASR/ST INSTRUCTIONS
         BASR  &GLINK,15           BRANCH TO SECTION
         DC    A(6,&NSECT)         DECREMENT AND LINKAGE ADDRESS
         AGO   .MEND
.NOTREG  CNOP  2,4                 ALIGN ON HALFWORD
         DC    0AL4(REF&X)         ERROR IF OBJECT NOT SECTION NAME
         $AGOTO &SECTION,&X        ADD THE REFERENCE TO THE TABLE
&NAME    L     15,*+6              SECTION ENTRY ADDRESS
         BASR  &GLINK,15           BRANCH AND LINK TO SECTION
         DC    A(&SECTION-6,&NSECT) LINKAGE ADDRESSES
         L     &GBASE,4(,&GLINK)   RESTORE BASE REGISTER
.MEND    MEND
