         MACRO
&NAME    FINAL &DUMMY
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* THIS MACRO SHOULD BE CODED FOLLOWING ALL PERFORM STATEMENTS AND    *
.* SECTION AND ENDSECT MACRO DEFINITIONS. IT IS REQUIRED TO GENERATE  *
.* LABELS REFERRED TO IN PERFORM STATEMENTS. MNOTE ERRORS WILL BE     *
.* GENERATED IF A 'PERFORM' OR A 'GOTO' MACRO HAS AS ITS OPERAND AN   *
.* INSTRUCTION THAT IS NOT A SECTION NAME. THESE TWO MACROS *MUST*    *
.* REFER ONLY TO LABELS THAT DEFINE SECTION MACROS. USING ASSEMBLER   *
.* BRANCH-ON-CONDITION INSTRUCTIONS (SUCH AS 'B', 'BNE', 'BC', ETC.), *
.* WITH SECTION NAMES WILL PRODUCE UNPREDICTABLE RESULTS.             *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
         COPY  $MGBLDEF
         GBLC  &GRETN,&GSAVE
         LCLC  &X,&LOCTR           SHORTHAND FOR 'REF&SYSNDX'
         LCLA  &I,&J               SUBSCRIPTS
&LOCTR   SETC  '&$MLOCTR'          SAVE CURRENT LOCTR NAME
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* THIS MACRO IS VALID ONLY FOR VERSION 2 OF THE MACRO DEFINITIONS    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
         VERSION 2
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* INITIALIZE SUBSCRIPTS                                              *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
&I       SETA  1                   INITIALIZE
&J       SETA  1                   .
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* INCREASE ASSEMBLER LOOP LIMITER                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
         ACTR  32767               .
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* ISSUE ENDSECT IF STILL WITHIN A SECTION                            *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
         AIF   ('&GRETN' EQ '').LOOP10
         ENDSECT ,
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* EXIT IF NO GOTO OR PERFORM MACROS                                  *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
.LOOP10  AIF   (&$GOTOC EQ 0).MEND
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* GO IF HIT ON NAMES                                                 *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
 AIF ('&$SECT(&I)' EQ '&$GOTO(&J)' AND '&$SECT(&I)' NE '').NEXT30
&I       SETA  &I+1                NO MATCH - BUMP POINTER
         AIF   (&I LE &$SECTC).LOOP10 BACK IF MORE TO SCAN
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* FALLING THROUGH TO HERE MEANS NO SECTION NAME WAS FOUND FOR GOTO OR
.* PERFORM, WHICH MEANS NO DEFINE CONSTANT WILL BE CREATED FOR
.* REF&SYSNDX, WHICH MEANS GOTO/PERFORM WILL GET UNDEFINED SYMBOL ERROR
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
         MNOTE           8,'GOTO/PERFORM OBJECT &$GOTO(&J) (SEARCH FOR X
               REF&$GREF(&J) IN LISTING)'
      MNOTE 0,'(CONT''D) IS NOT A SECTION NAME - UNPREDICTABLE RESULTS'
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* GO TO NEXT NAME                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
.NEXT20  ANOP
&I       SETA  1                   REINITIALIZE
&J       SETA  &J+1                BUMP
         AIF   (&J LE &$GOTOC).LOOP10 BACK IF MORE TO DO
         AGO   .MEND
.*                                                                    *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.* GETTING TO HERE IS A *VERY GOOD THING*. A GOTO OR A PERFORM        *
.* REFERRED TO A SECTION, SO GENERATE THE ADCON TO SATISFY THE        *
.* REFERENCE GENERATED IN THE GOTO/PERFORM MACRO.                     *
.**=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
.*                                                                    *
.NEXT30  ANOP
&X       SETC  'REF&$GREF(&J)'     NAME REFERRED TO IN GOTO/PERFORM
&X       DC    0C'&$GOTO(&J) '     SHOW NAME IS ACTUALLY SECTION NAME
         AGO   .NEXT20             CONTINUE WITH NEXT GOTO NAME
.MEND    ANOP
         PUSH  PRINT
         PRINT GEN
         $IA
INST_LEN EQU   *-&SYSECT           LENGTH OF INSTRUCTION PORTION
         $WA
DATA_LEN EQU   *-&GSAVE            LENGTH OF DATA PORTION
PROG_LEN EQU   (INST_LEN+DATA_LEN+7)/8*8 PROGRAM LENGTH
         POP   PRINT
         AIF   ('&LOCTR' EQ '&$MLOCTR').MEXIT SKIP UNNECESSARY CODE
&LOCTR   LOCTR ,                   RESET LOCATION COUNTER
.MEXIT   MEND
