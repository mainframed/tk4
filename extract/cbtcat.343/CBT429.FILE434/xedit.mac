PROC 1 DSN VOL()
/*                           */
/* AUTHOR: MARK ZELDEN       */
/*                           */
ISPEXEC CONTROL ERRORS RETURN
     /* DETERMINE ISPF LEVEL */
     ISPEXEC VGET ZENVIR
     SET XIVER3 = &SUBSTR(6:8,&ZENVIR)
     SET XIVER1 = &SUBSTR(1:1,&XIVER3)
     IF &XIVER1 >= 4 THEN DO
       ISPEXEC VGET ZPCFMCN PROFILE
       IF &STR(&ZPCFMCN) = &STR(/) THEN +
         SET &CONF = &STR(YES)
       ELSE +
         SET &CONF = &STR(NO)
     END
     SET &EDITOK = NOTOK
     DO WHILE &EDITOK = NOTOK
       ISPEXEC EDREC QUERY
       IF &LASTCC = 4 THEN DO
         ISPEXEC DISPLAY PANEL(ISREDM02)
         IF &LASTCC = 0 THEN DO
           IF &ZEDCMD =   THEN ISPEXEC EDREC PROCESS PASSWORD(&PSWD)
           IF &ZEDCMD = C THEN ISPEXEC EDREC CANCEL
           IF &ZEDCMD = D THEN ISPEXEC EDREC DEFER
         END
         ELSE DO
           EXIT CODE(0)
         END
       END
       ELSE SET &EDITOK = OK
     END
     SET &RC = 0
     IF &EDITOK = OK && &XIVER1 >= 4 THEN DO
       ISPEXEC EDIT DATASET(&DSN) VOLUME(&VOL) CONFIRM(&CONF)
       SET &RC = &LASTCC
       IF &RC = 4 THEN SET &RC = 0
     END
     IF &EDITOK = OK && &XIVER1 < 4 THEN DO
       ISPEXEC EDIT DATASET(&DSN) VOLUME(&VOL)
       SET &RC = &LASTCC
       IF &RC = 4 THEN SET &RC = 0
     END
     IF &RC ^= 0 THEN ISPEXEC SETMSG MSG(&ZERRMSG)
