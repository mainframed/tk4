PROC 1 DSN VOL()
/*                           */
/* AUTHOR: MARK ZELDEN       */
/*                           */
 CONTROL NOLIST NOPROMPT ASIS
 /* DETERMINE ISPF LEVEL */
 ISPEXEC VGET ZENVIR
 SET XIVER3 = &SUBSTR(6:8,&ZENVIR)
 SET XIVER1 = &SUBSTR(1:1,&XIVER3)
 IF &XIVER1 >= 4 THEN DO
   ISPEXEC VGET ZPCFMCN PROFILE
   IF &STR(&ZPCFMCN) = &STR(/) THEN +
     SET &CONF = &STR(YES)
   ELSE +
     SET &CONF = &STR(NO)
 END
   SET ARG  = &STR())
   SET MEMSTART = &SYSINDEX((,&DSN)
   IF &MEMSTART > 0 THEN DO
     SET MEMEND   = &SYSINDEX(&STR(&ARG),&DSN)
     SET XMEM     = &SUBSTR(&MEMSTART+1:&MEMEND-1,&DSN)
         IF  &SUBSTR(1:1,&DSN) = &STR(') THEN DO
           SET &DSN = &SUBSTR(1:&MEMSTART-1,&DSN)
           SET &DSN = &DSN&STR(')
         END
         ELSE +
           SET &DSN = &SUBSTR(1:&MEMSTART-1,&DSN)
   END
 ISPEXEC CONTROL ERRORS RETURN
       ISPEXEC LMINIT DATAID(XMEMD1) DATASET(&DSN) VOLUME(&VOL)
       SET &RC = &LASTCC
       IF &RC = 0 THEN DO
         ISPEXEC LMOPEN DATAID(&XMEMD1)
         SET &RC = &LASTCC
         IF &RC = 0 THEN DO
           SET &ZLLCMD =
           SET &ZLMEMBER =
           SET &ZLUDATA =
           ISPEXEC LMMDISP DATAID(&XMEMD1) COMMANDS(ANY) MEMBER(&XMEM)
           SET &RC = &LASTCC
           DO WHILE &RC = 0
             IF &STR(&ZLLCMD) = &STR(B) THEN DO
               ISPEXEC BROWSE DATAID(&XMEMD1) MEMBER(&ZLMEMBER)
               SET &CC = &LASTCC
               SET &UD = &STR(*BROWSED)
               IF &CC ^= 0 THEN ISPEXEC SETMSG MSG(&ZERRMSG)
               END
             ELSE IF &STR(&ZLLCMD) = &STR(/) && &XIVER1 ^<4 THEN DO
               ISPEXEC BROWSE DATAID(&XMEMD1) MEMBER(&ZLMEMBER)
               SET &CC = &LASTCC
               SET &UD = &STR(*BROWSED)
               IF &CC ^= 0 THEN ISPEXEC SETMSG MSG(&ZERRMSG)
               END
             ELSE IF &STR(&ZLLCMD) = &STR(V) && &XIVER1 ^<4 THEN DO
               ISPEXEC VIEW DATAID(&XMEMD1) MEMBER(&ZLMEMBER) +
                       CONFIRM(&CONF)
               SET &CC = &LASTCC
               SET &UD = &STR(*VIEWED)
               IF &CC ^= 0 THEN ISPEXEC SETMSG MSG(&ZERRMSG)
               END
             ELSE IF &STR(&ZLLCMD) = &STR(E) && &XIVER1 ^<4 THEN DO
               ISPEXEC EDIT DATAID(&XMEMD1) MEMBER(&ZLMEMBER) +
                       CONFIRM(&CONF)
               SET &CC = &LASTCC
               IF &CC = 4 THEN SET &CC = 0
               SET &UD = &STR(*EDITED)
               END
             ELSE IF &STR(&ZLLCMD) = &STR(E) && &XIVER1 <4 THEN DO
               ISPEXEC EDIT DATAID(&XMEMD1) MEMBER(&ZLMEMBER)
               SET &CC = &LASTCC
               IF &CC = 4 THEN SET &CC = 0
               SET &UD = &STR(*EDITED)
               END
             ELSE IF &STR(&ZLLCMD) = &STR(P) THEN DO
               ISPEXEC LMPRINT DATAID(&XMEMD1) MEMBER(&ZLMEMBER)
               SET &CC = &LASTCC
               SET &UD = &STR(*PRINTED)
               IF &CC ^= 0 THEN ISPEXEC SETMSG MSG(&ZERRMSG)
               END
             ELSE IF &STR(&ZLLCMD) = &STR(R) THEN DO
               ISPEXEC LMINIT DATAID(XMEMD2) DATASET(&DSN) +
                 ENQ(SHRW) VOLUME(&VOL)
               SET &RC = &LASTCC
               IF &RC = 0 THEN DO
                 ISPEXEC LMOPEN DATAID(&XMEMD2) OPTION(OUTPUT)
                 SET &CC = &LASTCC
                 IF &CC = 0 THEN DO
                   ISPEXEC LMMREN DATAID(&XMEMD2) MEMBER(&ZLMEMBER) +
                      NEWNAME(&ZLUDATA)
                   SET &CC = &LASTCC
                   IF &STR(&ZLUDATA) = THEN +
                     SET &ZERRMSG = &STR(ISRU003)
                   SET &UD = &STR(*RENAMED)
                   ISPEXEC LMCLOSE DATAID(&XMEMD2)
                   END
                 ISPEXEC LMFREE DATAID(&XMEMD2)
                 END
               END
             ELSE IF &STR(&ZLLCMD) = &STR(D) THEN DO
               ISPEXEC LMINIT DATAID(XMEMD2) DATASET(&DSN) +
                 ENQ(SHRW) VOLUME(&VOL)
               SET &RC = &LASTCC
               IF &RC = 0 THEN DO
                 ISPEXEC LMOPEN DATAID(&XMEMD2) OPTION(OUTPUT)
                 SET &CC = &LASTCC
                 IF &CC = 0 THEN DO
                   ISPEXEC LMMDEL DATAID(&XMEMD2) MEMBER(&ZLMEMBER)
                   SET &CC = &LASTCC
                   SET &UD = &STR(*DELETED)
                   ISPEXEC LMCLOSE DATAID(&XMEMD2)
                   END
                 ISPEXEC LMFREE DATAID(&XMEMD2)
                 END
               END
             ELSE DO
                 IF &STR(&ZCMD) NE &STR(&Z) THEN DO
                 SET &ZEDSMSG = &STR(INVALID COMMAND)
                 SET &ZEDLMSG = &STR(VALID MEMBER LIST COMMANDS ARE +
                                "SELECT", "SORT", "LOCATE", "RESET" +
                                AND "SAVE".)
                 SET &ZERRMSG = &STR(ISRZ001) /* MSG - WITH ALARM */
                 SET &CURLOC  = &STR(ZCMD)
                 END
                 ELSE DO
                 SET &ZEDSMSG = &STR(INVALID SELECTION CODE)
                 IF &XIVER1 < 4 THEN +
                   SET &ZEDLMSG = &STR(CODES ARE:  E (Edit), +
                                  B (Browse), +
                                  R (Rename), D (Delete), +
                                  P (Print).)
                   ELSE +
                   SET &ZEDLMSG = &STR(CODES ARE:  E (Edit), +
                                  B (Browse), V (View), +
                                  R (Rename), D (Delete), +
                                  P (Print).)
                 SET &ZERRMSG = &STR(ISRZ001) /* MSG - WITH ALARM */
                 SET &CURLOC  = &STR(ZLLCMD)
                 END
               SET &CC = 4
               END
             IF &CC = 0 THEN DO
               ISPEXEC LMMDISP DATAID(&XMEMD1) OPTION(PUT) +
                 MEMBER(&ZLMEMBER) ZLUDATA(&UD)
               ISPEXEC LMMDISP DATAID(&XMEMD1) OPTION(GET)
               SET &RC = &LASTCC
               IF &RC = 8 THEN DO
                 ISPEXEC LMMDISP DATAID(&XMEMD1) COMMANDS(ANY)
                 SET &RC = &LASTCC
                 END
             END
             ELSE DO
               ISPEXEC SETMSG MSG(&ZERRMSG)
               ISPEXEC LMMDISP DATAID(&XMEMD1) OPTION(PUT) +
                 MEMBER(&ZLMEMBER) ZLLCMD(&ZLLCMD) ZLUDATA(&ZLUDATA)
               ISPEXEC LMMDISP DATAID(&XMEMD1) COMMANDS(ANY) +
                 CURSOR(&CURLOC) TOP(&ZLMEMBER)
               SET &RC = &LASTCC
             END
             END
           IF &RC = 8 THEN SET &RC = 0
           ISPEXEC LMMDISP DATAID(&XMEMD1) OPTION(FREE)
           IF &ZERRSM = &STR(NO MEMBER LIST) THEN DO
             SET PTRNSTRT = &SYSINDEX(&STR(*),&XMEM)
             IF &PTRNSTRT > 0 THEN DO
               SET &ZEDSMSG = &STR(NO MATCHES)
               SET &ZEDLMSG = &STR(MEMBER NAME PATTERN &XMEM PRODUCED +
                              NO MATCHING MEMBERS.)
               SET &ZERRMSG = &STR(ISRZ001) /* MSG - WITH ALARM */
             END
             ELSE DO
               SET &ZEDSMSG = &STR(MEMBER NOT FOUND)
               SET &ZEDLMSG = &STR(MEMBER &XMEM WAS NOT FOUND IN THE +
                              PDS DIRECTORY.)
               SET &ZERRMSG = &STR(ISRZ001) /* MSG - WITH ALARM */
             END
           END
           IF &ZERRSM = &STR(DATA SET NOT PARTITIONED) THEN DO
             SET &ZEDSMSG = &STR(INVALID DSORG)
             SET &ZEDLMSG = &STR(&DSN ORGANIZATION MUST +
                            BE PARTITIONED.)
             SET &ZERRMSG = &STR(ISRZ001) /* MSG - WITH ALARM */
           END
           ISPEXEC LMCLOSE DATAID(&XMEMD1)
           END
         ISPEXEC LMFREE DATAID(&XMEMD1)
    END
    IF &RC ^= 0 THEN ISPEXEC SETMSG MSG(&ZERRMSG)
    END
    EXIT
