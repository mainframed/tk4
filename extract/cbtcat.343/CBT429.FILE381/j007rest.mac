         TITLE 'RESTORE JQE USER FIELDS FROM JCT USER HEADER'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS SMF EXIT PROGRAM WILL RESTORE USER FIELDS IN THE        *
*        JQE FROM THE USER NETWORK JOB HEADER.  THIS WILL BE          *
*        DONE WHEN THE JQE FIELDS HAVE NOT YET BEEN INITIALIZED       *
*        BUT THE NETWORK JOB HEADER HAS VALID VALUES.  THIS WILL      *
*        ONLY OCCUR WHEN RECEIVING A JOB FROM NJE OR ON A SPOOL       *
*        RELOAD CONDITION.  THIS WILL PRESERVE THE JQE USER           *
*        FIELDS UNDER THESE CONDITIONS.                               *
*                                                                     *
*        THIS SHOULD BE THE FIRST EXIT 7 ROUTINE SO THAT THE          *
*        JQE FIELDS CAN BE RESTORED PRIOR TO FURTHER MODIFICATION     *
*        BY ANY OTHER EXIT 7 ROUTINES.                                *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J 0 0 7 R E S T            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $CKPT        SYS1.HASPSRC                               *
*        04)  $ENTRY       SYS1.HASPSRC                               *
*        05)  $FCSUHDR     SYS1.HASPSRC (USER)                        *
*        06)  $HASPEQU     SYS1.HASPSRC                               *
*        07)  $HASPGBL     SYS1.HASPSRC                               *
*        08)  $HCT         SYS1.HASPSRC                               *
*        09)  $JCT         SYS1.HASPSRC                               *
*        10)  $JQE         SYS1.HASPSRC                               *
*        11)  $KIT         SYS1.HASPSRC                               *
*        12)  $MIT         SYS1.HASPSRC                               *
*        13)  $MODEND      SYS1.HASPSRC                               *
*        14)  $MODULE      SYS1.HASPSRC                               *
*        15)  $NHD         SYS1.HASPSRC                               *
*        16)  $NHDGET      SYS1.HASPSRC                               *
*        17)  $PADDR       SYS1.HASPSRC                               *
*        18)  $PCE         SYS1.HASPSRC                               *
*        19)  $QSUSE       SYS1.HASPSRC                               *
*        20)  $SAVE        SYS1.HASPSRC                               *
*        21)  $RETURN      SYS1.HASPSRC                               *
*        22)  IEESMCA      SYS1.AMODGEN                               *
*        23)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J007REST $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='RESTORE JQE USER FIELDS FROM JCT USER HEADER',   X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $KIT,               GENERATE HASP KIT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $NHD,               GENERATE HASP NDH DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $SMF,               GENERATE HASP SMF DSECT             X
               RPL,                GENERATE MVS  RPL DSECT             X
               SMCA                GENERATE MVS  SMCA DSECT
         EJECT
***********************************************************************
*                                                                     *
*        USER JOB HEADER                                              *
*                                                                     *
***********************************************************************
         SPACE 3
         $FCSUHDR
J007REST CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E007REST $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R10,R1              SET JCT ADDRESS
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         GET ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - A CODE PASSED TO YOUR ROUTINE BY JES2.                 *
*              0 - INDICATES THAT THE JCT HAS BEEN READ FROM SPOOL    *
*              4 - INDICATES THAT THE JCT WILL BE WRITTEN TO SPOOL    *
*        R1  - ADDRESS OF THE BUFFER THAT CONTAINS THE JCT            *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE PCE                                     *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         LTR   R0,R0               TEST ENTRY REASON CODE
         BZ    RETURN0             ZERO, READ ONLY, NOT INTERESTED
***********************************************************************
*        SEE IF JQE HAS USER FIELDS                                   *
***********************************************************************
         CLI   JQEXFLG1,C' '       SEE IF JOB TYPE ALREADY SET
         BH    RETURN0             YES, GET OUT
***********************************************************************
*        FIND OUR SPECIAL NJE USER HEADER                             *
***********************************************************************
         LA    R8,JCTNJHDR         POINT TO START OF NETWORK HEADERS
         USING FCSUHDR,R8          SET ADDRESSABILITY TO HEADERS
         LH    R5,0(R8)            LOAD LENGTH OF NETWORK HEADERS
         AR    R5,R8               GET END ADDRESS OF NETWORK HEADERS
         LA    R8,4(R8)            LOAD START ADDRESS OF FIRST HEADER
CKHD1    CR    R8,R5               SEE IF END OF HEADERS
         BNL   CKHD3               YES, GET OUT
         TM    FCSUHTYP,NTYPUSER   SEE IF USER HEADER
         BNO   CKHD2               NO, GO GET NEXT ONE
         CLC   FCSUHID,=CL4'FCSH'  SEE IF OUR HEADER
         BE    CKHD4               YES, GO CHECK IT
CKHD2    AH    R8,0(R8)            POINT TO NEXT HEADER
         B     CKHD1               LOOP
CKHD3    B     RETURN0             NO USER HEADER, JUST GET OUT
CKHD4    DS    0H
         EJECT
***********************************************************************
*                                                                     *
*        UPDATE THE USER FIELDS IN THE JCT USER HEADER WITH           *
*        USER FIELDS IN THE JQE IF THEY HAVE BEEN PREVIOUSLY          *
*        SET.  THIS WILL OCCUR FOR NJE JOBS AS WELL AS FOR            *
*        SPOOL OFFLOAD/RELOAD SITUATIONS.                             *
*                                                                     *
***********************************************************************
***********************************************************************
*        SEE IF NETWORK JOB HEADER HAS VALID INFORMATION              *
***********************************************************************
         CLI   FCSUHFL1,C' '       SEE IF JOB TYPE ALREADY SET
         BNH   RETURN0             NO, GET OUT
         $QSUSE ,                  GET CONTROL OF THE CHECKPOINT
         MVC   JQEXOWNR,FCSUHOWN   SET OWNERID
         MVC   JQEXTHRN,FCSUHTHN   SET THREAD NAME
         MVC   JQEXEXCN,FCSUHEXN   SET EXCLUDE NAME
         MVC   JQEXRLSN,FCSUHRLN   SET RELEASE NAME
         MVC   JQEXFLG1,FCSUHFL1   SET FLAG1 FIELD
         MVC   JQEXFLG2,FCSUHFL2   SET FLAG2 FIELD
         MVC   JQEXFLG3,FCSUHFL3   SET FLAG3 FIELD
         MVC   JQEXFLG4,FCSUHFL4   SET FLAG4 FIELD
         LR    R1,R7               SET JQE ADDRESS
         $CKPT ID=JQE,ADDR=(R1)    TELL JES2 ABOUT THE CHANGES
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
***********************************************************************
*                                                                     *
*                   CONSTANTS USED IN THIS EXIT                       *
*                                                                     *
***********************************************************************
         SPACE 3
         LTORG
         EJECT
         $MODEND
         END
