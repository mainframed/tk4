         TITLE 'WRITE SPECIAL END-OF-JOB MESSAGES'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS SMF EXIT PROGRAM WILL WRITE SPECIAL END-OF-JOB          *
*        MESSAGES FOR PRODUCTION JOBS WHICH HAVE ABENDED OR           *
*        HAVE FAILED WITH A JCL ERROR.  THE MESSAGE WILL BE           *
*        NON-ROLL DELETABLE.  THE ABEND MESSAGE COULD BE WRITTEN      *
*        FROM AN SMF EXIT, BUT THE JCL ERROR MESSAGE COULD NOT.       *
*        NOTE THAT THIS EXIT REQUIRES THAT THE JQE BE MODIFIED        *
*        TO ADD THE ABEND AND JCL ERROR BITS AND PROVIDE A BIT        *
*        TO INDICATE THAT THIS MESSAGE HAS BEEN WRITTEN.              *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J 0 0 7 E N D J            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $CKPT        SYS1.HASPSRC                               *
*        04)  $ENTRY       SYS1.HASPSRC                               *
*        05)  $FREEBUF     SYS1.HASPSRC                               *
*        06)  $GETBUF      SYS1.HASPSRC                               *
*        07)  $HASPEQU     SYS1.HASPSRC                               *
*        08)  $HASPGBL     SYS1.HASPSRC                               *
*        09)  $HCT         SYS1.HASPSRC                               *
*        10)  $JCT         SYS1.HASPSRC                               *
*        11)  $JQE         SYS1.HASPSRC                               *
*        12)  $KIT         SYS1.HASPSRC                               *
*        13)  $MIT         SYS1.HASPSRC                               *
*        14)  $MODEND      SYS1.HASPSRC                               *
*        15)  $MODULE      SYS1.HASPSRC                               *
*        16)  $MSG         SYS1.HASPSRC                               *
*        17)  $PADDR       SYS1.HASPSRC                               *
*        18)  $PCE         SYS1.HASPSRC                               *
*        19)  $QSUSE       SYS1.HASPSRC                               *
*        20)  $SAVE        SYS1.HASPSRC                               *
*        21)  $RETURN      SYS1.HASPSRC                               *
*        22)  $WTO         SYS1.HASPSRC                               *
*        23)  IEESMCA      SYS1.AMODGEN                               *
*        24)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J007ENDJ $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='WRITE SPECIAL END-OF-JOB MESSAGES',              X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $KIT,               GENERATE HASP KIT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               RPL                 GENERATE MVS  RPL DSECT
         TITLE 'WRITE SPECIAL END-OF-JOB MESSAGES'
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM WORK AREA                                            *
*                                                                     *
***********************************************************************
WORKAREA DSECT
         DS    CL(BUFSTART-BFPDSECT) ***** RESERVED BY JES2 *****
$WTOWRK  DS    CL130               $WTO WORK AREA
J007ENDJ CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E007ENDJ $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R10,R1              SET JCT ADDRESS
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         GET ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - A CODE PASSED TO YOUR ROUTINE BY JES2.                 *
*              0 - INDICATES THAT THE JCT HAS BEEN READ FROM SPOOL    *
*              4 - INDICATES THAT THE JCT WILL BE WRITTEN TO SPOOL    *
*        R1  - ADDRESS OF THE BUFFER THAT CONTAINS THE JCT            *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE PCE                                     *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         LTR   R0,R0               TEST ENTRY REASON CODE
         BZ    RETURN0             ZERO, READ ONLY, NOT INTERESTED
         TM    JQEXFLG2,JQEXENDJ   HAS END-OF-JOB MESSAGE BEEN DONE
         BO    RETURN0             YES, JUST GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        SEE IF JOB HAS JUST LEFT THE INPUT OR EXECUTION QUEUES.      *
*                                                                     *
***********************************************************************
         TM    JQETYPE,$XEQ+$INPUT+$XMIT+$RECEIVE SEE IF FINISHED
         BNZ   RETURN0             NO, JUST GET OUT
***********************************************************************
*                                                                     *
*        GET A WORK AREA FOR THE EXIT                                 *
*                                                                     *
***********************************************************************
         SPACE 3
         $GETBUF TYPE=HASP,WAIT=YES GET A WORK AREA
         LR    R9,R1               SAVE WORK AREA ADDRESS
         USING WORKAREA,R9         SET ADDRESSABILITY TO BUFFER
***********************************************************************
*                                                                     *
*        FORMAT THE JOB ENDED MESSAGE                                 *
*                                                                     *
***********************************************************************
         $QSUSE TYPE=WAIT          GET CONTROL OF THE SHARED QUEUES
         OI    JQEXFLG2,JQEXENDJ   SET END-OF-JOB MESSAGE PROCESS IND
         LR    R1,R7               SET JQE ADDRESS
         $CKPT ID=JQE,ADDR=(R1)    TELL JES2 ABOUT THE UPDATE
         MVC   $WTOWRK(L'MSG1),MSG1 INITIALIZE MSG1 WORK AREA
         TM    JQEXFLG3,JQEXABND+JQEXJCLE SEE IF NORMAL TERMINATION
         BZ    NORMAL              NO, GO ISSUE NORMAL MESSAGE
         MVC   $WTOWRK+2+6(10),=C'(ABNORMAL)' ASSUME ABEND
         TM    JQEXFLG3,JQEXABND   SEE IF ABENDED
         BO    CKPROD              YES, BRANCH
         MVC   $WTOWRK+2+6(10),=C'(JCLERROR)' ASSUME JCLERROR
CKPROD   CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION
         BNE   NORMAL              GO ISSUE NORMAL MESSAGE
         $QSUSE TYPE=WAIT          GET CONTROL OF THE SHARED QUEUES
         OI    JQEFLAG1,JQE1HLD1   TURN ON OPERATOR HOLD
         LR    R1,R7               SET JQE ADDRESS
         $CKPT ID=JQE,ADDR=(R1)    TELL JES2 ABOUT THE UPDATE
         MVC   $WTOWRK(2),MSG2     CHANGE THE MESSAGE NUMBER
NORMAL   LA    R1,$WTOWRK          LOAD ADDRESS OF MESSAGE
         LA    R0,L'MSG1           LOAD LENGTH OF MESSAGE
         $WTO  (R1),(R0),JOB=YES,CMB=NO,WAIT=NO,ROUTE=$MAIN,UCM=NO
         B     EXIT                GO FREE WORK AREA AND EXIT
         EJECT
***********************************************************************
*                                                                     *
*        FREE THE WORK AREA AND RETURN WITH THE APPROPRIATE           *
*        RETURN CODE.                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
EXIT     $FREEBUF (R9)             FREE THE BUFFER
         B     RETURN0             GET OUT
         DROP  R9                  DROP ADDRESSABILITY TO THE BUFFER
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
***********************************************************************
*                                                                     *
*                   CONSTANTS USED IN THIS EXIT                       *
*                                                                     *
***********************************************************************
         SPACE 3
         LTORG
         EJECT
MSG1     $MSG  995,'ENDED           '
MSG2     $MSG  996,'ENDED           '
         $MODEND
         END
