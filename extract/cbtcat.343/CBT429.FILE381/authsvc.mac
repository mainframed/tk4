IGC0024H TITLE 'SVC 248 - SET KEY ZERO SVC'
***********************************************************************
*                                                                     *
*        OPERATING SYSTEM - MVS                                       *
*                                                                     *
*        SVC TYPE         - TYPE 3 OR 4                               *
*                                                                     *
*        SIZE             - 200 BYTES                                 *
*                                                                     *
*        LINK EDIT ATTRIB - RENT, REUS, REFR                          *
*                                                                     *
*        IBM MACROS USED:                                             *
*              CVT          SYS1.AMODGEN                              *
*              FREEMAIN     SYS1.MACLIB                               *
*              GETMAIN      SYS1.MACLIB                               *
*              IEESMCA      SYS1.AMODGEN                              *
*              IEFAJCTB     SYS1.AMODGEN                              *
*              IEZJSCB      SYS1.AMODGEN                              *
*              IKJTCB       SYS1.AMODGEN                              *
*              IHARB        SYS1.AMODGEN                              *
*              SMFEWTM      SYS1.MACLIB                               *
*              TIME         SYS1.MACLIB                               *
*                                                                     *
*        USER MACROS USED:  NONE                                      *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST COMPUTER SERVICES                      *
*                        420 BRUSHY MOUNTAIN ROAD                     *
*                        WILKESBORO, N.C. 28697                       *
*                        (919) 651-5414                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        THIS SVC CAN BE CALLED TO SET AN UNATHORIZED PROGRAM         *
*        INTO PROTECT KEY ZERO OR TO RESET THE PROGRAM BACK TO        *
*        THE ORIGINAL PROTECT KEY.  UPON ENTRY REGISTER 0 WILL        *
*        POINT TO AN EIGHT (8) BYTE ENTRY GIVING THE PROGRAM          *
*        NAME OF THE CALLER.  THE SVC WILL WRITE A USER SMF           *
*        RECORD TELLING THE DATE, TIME, CPU, JOBNAME, PROGRAM         *
*        NAME, ETC. OF THE THE REQUEST.                               *
*        1)  REGISTER 1 = ZERO    - SET THE CALLER INTO KEY           *
*            ZERO.                                                    *
*        2)  REGISTER 1 = NONZERO - RESET THE PROTECT KEY AND         *
*            PROBLEM STATE TO THE VALUE IN THE TCB.                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                 R E G I S T E R     E Q U A T E S                   *
*                                                                     *
***********************************************************************
R0       EQU   0              ON SVC ENTRY - PARAMETER ADDRESS
R1       EQU   1              ON SVC ENTRY - PARAMETER ADDRESS
R2       EQU   2              USED FOR ADDR OF DCB
R3       EQU   3              ON SVC ENTRY - CVT ADDRESS
R4       EQU   4              ON SVC ENTRY - TCB ADDRESS
R5       EQU   5              ON SVC ENTRY - SVRB ADDRESS
R6       EQU   6              ON SVC ENTRY - ENTRY POINT ADDRESS
R7       EQU   7              ON SVC ENTRY - ASCB ADDRESS
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13             ON SVC ENTRY - SAME AS BEFORE SVC CALL
R14      EQU   14             ON SVC ENTRY - RETURN ADDRESS
R15      EQU   15
         EJECT
***********************************************************************
*                                                                     *
*        START OF PROGRAM                                             *
*                                                                     *
***********************************************************************
IGC0024H CSECT
         USING IGC0024H,R6    SET ADDRESSABILITY FOR SVC
         USING CVT,R3         SET ADDRESSABILITY TO CVT
         USING TCB,R4         SET ADDRESSABILITY TO TCB
         USING RBBASIC,R5     SET ADDRESSABILITY TO SVRB
         LR    R10,R0         SAVE REG 0
         LR    R11,R1         SAVE REG 1
         GETMAIN R,LV=DSALEN  GET STORAGE FOR WORKAREA
         LR    R12,R1         SAVE GETMAIN ADDRESS
         USING DSA,R12        SET ADDRESSABILITY TO SAVE AREA
***********************************************************************
*                                                                     *
*        GET THE FOLLOWING FIELDS OUT OF THE MVS CONTROL BLOCKS:      *
*                                                                     *
*        1)  JOBNAME                                                  *
*        2)  PROGRAMMER NAME                                          *
*        3)  FIRST 20 CHARACTERS OF THE JOB ACCOUNTING FIELD          *
*        4)  SMF CPUID                                                *
*                                                                     *
***********************************************************************
         MVC   JOBNAME,BLANKS BLANK JOBNAME
         MVC   PGMRNAME,BLANKS BLANK PROGRAMMER NAME
         MVC   ACCT,BLANKS    BLANK ACCOUNTING FIELD
         MVC   CPUID,BLANKS   BLANK THE CPUID
*
***********************************************************************
*        SAVE THE CPUID FROM THE SMCA                                 *
***********************************************************************
         L     R8,CVTSMCA     LOAD SMCA ADDRESS
         USING SMCABASE,R8    SET ADDRESSABILITY TO SMCA
         MVC   CPUID,SMCASID  MOVE CPUID TO SAVE
         DROP  R8             DROP ADDRESSABILITY TO SMCA
         L     R8,TCBJSCB     LOAD JSCB ADDRESS
         USING IEZJSCB,R8     SET ADDRESSABILITY TO JSCB
         L     R8,JSCBACT     LOAD ADDRESS OF ACTIVE JSCB
         L     R8,JSCBJCT     LOAD ADDRESS OF JCT PREFIX
         DROP  R8             DROP ADDRESSABILITY TO JSCB
         LA    R8,16(R8)      INCREMENT PAST JCT PREFIX
         USING INJMJCT,R8     SET ADDRESSABILITY TO JCT
***********************************************************************
*        SAVE THE JOBNAME FROM THE O/S JCT                            *
***********************************************************************
         MVC   JOBNAME,JCTJNAME SAVE JOBNAME
         ICM   R8,7,JCTACTAD  LOAD ADDRESS OF ACT PREFIX
         LA    R8,16(R8)      INCREMENT PAST ACT PREFIX
         DROP  R8             DROP ADDRESSABILITY TO JCT
         USING IEFAACTB,R8    SET ADDRESSABILITY TO ACT
***********************************************************************
*        SAVE THE PROGRAMMER NAME FROM THE ACT                        *
***********************************************************************
         MVC   PGMRNAME,ACTPRGNM SAVE PROGRAMMER NAME
         SLR   R3,R3          CLEAR REG FOR ICM
         ICM   R3,1,ACTJNFLD  LOAD NUMBER OF ACCOUNTING FIELDS
         BZ    NOACCT         ZERO, NONE, EXIT
         ICM   R3,1,ACTACCNT  LOAD LENGTH OF FIRST ACCOUNTING FIELD
         BZ    NOACCT         ZERO, NONE, EXIT
         CH    R3,H#20        SEE IF LENGTH AT 20 CHARACTERS
         BL    MOVEIT         NO, BRANCH
         LA    R3,20          SET LENGTH TO 20
***********************************************************************
*        SAVE THE FIRST 20 BYTES OF THE ACCOUNTING FIELD IN THE ACT   *
***********************************************************************
MOVEIT   BCTR  R3,0           MAKE MACHINE LENGTH
         EX    R3,MOVEACT     MOVE ACCOUNTING INFORMATION TO SAVE
         B     NOACCT         BYPASS EXECUTED INSTRUCTION
MOVEACT  MVC   ACCT(0),ACTACCNT+1 SAVE FIRST 20 CHARACTERS OF ACCT
         DROP  R8             DROP ADDRESSABILITY TO ACT
***********************************************************************
*        SAVE THE PROGRAM NAME AS POINTED TO BY REGISTER 0 UPON       *
*        ENTRY TO THE SVC.                                            *
***********************************************************************
NOACCT   MVC   PROGNAME,0(R10) SAVE PGMNAME FROM POINTER IN REG 0
         EJECT
***********************************************************************
*        BUILD AND WRITE THE USER SMF RECORD OF THE ACTIVITY          *
***********************************************************************
         LA    R1,LEN         LOAD LENGTH OF SMF RECORD
         STH   R1,SMFRDW      AND SAVE IT IN THE SMF RECORD
         XC    SMFILL,SMFILL  CLEAR FILLER AREA
         LA    R1,SMFTYPE     LOAD SMF RECORD NUMBER
         STH   R1,SMFRTYP     AND SAVE IT IN THE SMF RECORD
         TIME  BIN            GET TIME OF DAY
         STCM  R0,15,SMFT     SAVE BINARY TIME
         STCM  R1,15,SMFD     SAVE BINARY DATE
         MVC   SMFCPUID,CPUID   MOVE CPUID TO SMF RECORD
         MVC   SMFJOB,JOBNAME   MOVE JOBNAME TO SMF RECORD
         MVC   SMFACCT,ACCT     MOVE ACCT TO SMF RECORD
         MVC   SMFPGMR,PGMRNAME MOVE PROGRAMMER NAME TO SMF RECORD
         MVC   SMFPGMN,PROGNAME MOVE PROGRAM NAME TO SMF RECORD
         MVI   SMFOPTN,SMFKEY0 ASSUME SETTING TO KEY 0
         LTR   R11,R11        TEST REG 1 AS UPON ENTRY
         BZ    WRITEIT        IT WAS ZERO, BRANCH
         MVI   SMFOPTN,SMFKEYN0 REQUEST TO KEY NOT ZERO
WRITEIT  LA      R9,SMFRECD   LOAD ADDRESS OF SMF RECORD
         SMFEWTM (R9)         WRITE RECORD TO SMF
         EJECT
***********************************************************************
*        SET PROTECT KEY AS REQUESTED BY REG 1 ON ENTRY               *
***********************************************************************
SETKEY   L     R5,RBLINK      LOAD PREVIOUS RB ADDRESS FROM SVRB
         LTR   R11,R11        TEST PASSED VALUE IN REG 1
         BNZ   NZERO          NOT ZERO, TURN OFF PROTECT KEY ZERO
ZERO     NI    RBOPSWB2,X'0F' MAKE PROTECTION KEY ZERO
         B     RETURN         GET OUT
NZERO    MVZ   RBOPSWB2(1),TCBPKF RESET TO PROTECT KEY FROM TCB
         OI    RBOPSWB2,X'01' MAKE PROBLEM STATE
         SPACE 3
***********************************************************************
*        RETURN TO CALLER                                             *
***********************************************************************
RETURN   FREEMAIN R,LV=DSALEN,A=(12) FREE WORK AREA
         SLR   R15,R15        SET RETURN CODE TO 0
SVC3     SVC   3              RETURN
         EJECT
***********************************************************************
*        PROGRAM CONSTANTS                                            *
***********************************************************************
BLANKS   DC    CL20' '        CHARACTER STRING OF BLANKS
H#20     DC    H'20'          HALFWORD CONSTANT '20'
         EJECT
         CVT   DSECT=YES
         EJECT
         IKJTCB LIST=YES
         EJECT
         IHARB
         EJECT
         IEESMCA
         EJECT
         IEZJSCB
         EJECT
JCT      DSECT
         IEFAJCTB
         EJECT
***********************************************************************
*        PROGRAM DYNAMIC SAVE AREA                                    *
***********************************************************************
DSA      DSECT
JOBNAME  DS    CL8            JOBNAME FROM THE O/S JCT
PGMRNAME DS    CL20           PROGRAMMER NAME FROM THE ACT
ACCT     DS    CL20           FIRST ACCOUNTING FIELD FROM THE ACT
PROGNAME DS    CL8            CALLING PROGRAM NAME FROM PTR IN REG 0
CPUID    DS    CL4            SMF CPUID FROM THE SMCA
*
SMFRECD  DS    0F             USER SMF RECORD
SMFRDW   DS    AL2(LEN)       LENGTH OF RECORD
SMFILL   DS    H              FILLER (ZEROS)
SMFRTYP  DS    AL2(SMFTYPE)   SMF RECORD TYPE
SMFTYPE  EQU   244
SMFT     DS    XL4            TIME
SMFD     DS    XL4            DATE
SMFCPUID DS    CL4            SMF CPUID
SMFJOB   DS    CL8            JOBNAME
SMFACCT  DS    CL20           JOB ACCOUNTING FIELD
SMFPGMR  DS    CL20           PROGRAMMER NAME
SMFPGMN  DS    CL8            PROGRAM NAME
SMFOPTN  DS    X              ENTRY OPTION (ZERO/NOT ZERO)
SMFKEY0  EQU   X'00'
SMFKEYN0 EQU   X'80'
LEN      EQU   *-SMFRDW
DSALEN   EQU   *-DSA
         END   IGC0024H
