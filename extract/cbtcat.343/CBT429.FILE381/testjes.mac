         TITLE 'PROGRAM TO TEST PSEUDO JES2 EXIT6'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 1.3.4  LEVEL 8506                       *
*                      JES2 FOR SP 1.3.4                              *
*                                                                     *
*        THIS PROGRAM WAS WRITTEN TO TEST THE JES2 EXIT6 PROGRAM      *
*        WRITTEN TO ENFORCE INSTALLATION JCL STANDARDS AND TO         *
*        SET THE APPROPRIATE JOB CLASS BASED ON DEVICES USED.         *
*                                                                     *
*        THE INPUT TO THIS PROGRAM IS A SEQUENTIAL FILE OF            *
*        INTERNAL TEXT CREATED USING THE QUEUE COMMAND AVAILABLE      *
*        ON MOST OF THE MODS TAPES.  FILE 5 OF A JOB CAN BE           *
*        PROCESSED WITH ONE OF THE NEWER VERSIONS OF THE QUEUE        *
*        COMMAND AND THE SAVE COMMAND CAN BE USED TO SAVE THIS FILE   *
*        INTO A DATA SET.                                             *
*                                                                     *
*        THE OUTPUT FILE MUST BE A JES2 SYSOUT DATA SET SINCE THE     *
*        EXIT WILL DO A JES2 WRITE INTO THIS DATA SET WHENEVER        *
*        AN ERROR IS DETECTED OR THE STEP STATISTICS ARE TO BE        *
*        LISTED.                                                      *
*                                                                     *
*        THE REQUIRED JES2 CONTROL BLOCKS FOR THE EXIT ARE FAKED      *
*        AND CREATED WITH JUST ENOUGH INFORMATION FOR THE EXIT        *
*        TO FUNCTION.  THE CONTROL BLOCKS CREATED ARE THE JCT,        *
*        PCE, AND JQE.                                                *
*                                                                     *
*        THIS ROUTINE LOADS THE ENTRY NAMED 'EXIT6' FOR PROCESSING.   *
*        THIS NAME MUST MATCH THE NAME ON THE $ENTRY STATEMENT IN     *
*        THE EXIT CODE AND MUST BE AN ALIAS FOR OR THE ENTRY POINT    *
*        FOR THE MODULE BEING TESTED SO THAT THE JES2 PREFIX STUFF    *
*        CAN BE BYPASSED.                                             *
*                                                                     *
*        WHEN TESTING THIS JES2 EXIT, THE LOCAL VARIABLE &&TSO        *
*        IN THE EXIT SHOULD BE CHANGED TO 'YES' FROM 'NO' TO          *
*        CHANGE THE $$WTO INSTRUCTIONS TO WTO INSTRUCTIONS AND        *
*        BYPASS THE ESTAE MACROS SO THAT THE TEST COMMAND CAN         *
*        DETERMINE THE ADDRESS OF ANY ABENDS.                         *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST COMPUTER SERVICES                      *
*                        420 BRUSHY MOUNTAIN ROAD                     *
*                        WILKESBORO, N.C. 28697                       *
*                        (919) 651-5414                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      THIS PROGRAM MAY BE LINKED INTO A TEST LIBRARY WITH ANY        *
*      NAME BUT MUST HAVE AN ALIAS OF TESTJES TO UTILIZE THE          *
*      ACTUAL ROUTINE ENTRY POINT INSTEAD OF THE JES PROLOGUE         *
*      AT THE MAIN ENTRY POINT.                                       *
*                                                                     *
*      THE EXIT MAY BE LINKED INTO A TEST LIBRARY WITH ANY            *
*      NAME BUT MUST HAVE AN ALIAS OF EXIT6 TO UTILIZE THE            *
*      ACTUAL ROUTINE ENTRY POINT INSTEAD OF THE JES PROLOGUE         *
*      AT THE MAIN ENTRY POINT.  IT MUST BE LINKEDITED AS             *
*      REENTRANT FOR THIS TEST PROCEDURE TO WORK.                     *
*                                                                     *
*                                                                     *
*              CLIST USED TO TEST JES2 EXIT6                          *
*                                                                     *
*      PROC 1 DSN                                                     *
*      CONTROL NOMSG                                                  *
*      FREE F(INTEXT,JESMSG)                                          *
*      CONTROL MSG                                                    *
*      ALLOC F(INTEXT) DA(&DSN) SHR                                   *
*      ALLOC F(JESMSG) SYSOUT(A) HOLD                                 *
*      TEST USER.LIBRARY(TESTJES)                                     *
*      LOAD USER.LIBRARY(EXIT6)                                       *
*      Q EXIT6                                                        *
*                                                                     *
***********************************************************************
         COPY  $HASPGBL
TJ       $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               $HASPEQU,                                               X
               NEL,                                                    X
               DEB,                                                    X
               $BUFFER,                                                X
               $PCE,                                                   X
               $CNVWORK,                                               X
               $JCT,                                                   X
               $MIT,                                                   X
               $JQE
               SPACE 1
         DROP  R13
         USING PCEDSECT,R8
TESTJES  $ENTRY BASE=R12
         LR    R12,R15        SET BASE REGISTER
         STM   R14,R12,12(R13) SAVE REGISTERS
         LR    R12,R15        INITIALIZE BASE REG WITH ENTRY POINT
         LR    R15,R13        SAVE CALLING'S SAVE AREA ADDR
         LA    R13,NEWSAVE    LOAD ADDR OF NEW SAVE AREA
         ST    R13,8(R15)     STORE NEW SAVE ADDR IN OLD
         ST    R15,4(R13)     STORE OLD SAVE ADDR IN NEW
LJCT     EQU   JCTNJHDR-JCTSTART
LPCE     EQU   PCEWORK+JCLPCEWS-PSVID
LJQE     EQU   JQELNGTH
         LA    R0,LJCT        LOAD LENGTH OF JCT
         GETMAIN R,LV=(0)     GET CORE FOR JCT
         LR    R10,R1         SET JCT ADDRESS
         LA    R0,LPCE        LOAD LENGTH OF PCE
         GETMAIN R,LV=(0)     GET CORE FOR PCE
         LR    R8,R1          SAVE PCE ADDRESS
         LA    R0,LJQE        LOAD LENGTH OF JQE
         GETMAIN R,LV=(0)     GET CORE FOR JQE
         LR    R3,R1          SAVE JQE ADDRESS
         USING JQEDSECT,R3    SET ADDRESSABILITY TO JQE
         ST    R3,PCEJQE      SAVE JQE ADDRESS IN PCE
         XC    PCEUSER0,PCEUSER0 CLEAR USER FIELD
         XC    JCTCNVRC,JCTCNVRC SET CONVERTER RETURN CODE TO ZERO
         MVI   JCTJOBFL,JCTBATCH SET TO BATCH JOB
         MVI   JCTJCLAS,C'T'  SET CLASS TO 'T'
         MVI   JCTCLASS,C'T'  SET CLASS TO 'T'
         MVI   JQEJCLAS,C'T'  SET CLASS TO 'T'
         OPEN  (JESMSG,(OUTPUT))
         OPEN  INTEXT
         MVC   JPCEMSG(LACB),JESMSG INITIALIZE OUTPUT ACB
         LOAD  EP=EXIT6       LOAD EXIT ROUTINE
         ST    R0,EXITADDR    SAVE EPA ADDRESS
READ     GET   INTEXT
         ST    R1,PARM+4      SAVE TEXT ADDRESS
         SLR   R0,R0          CLEAR REG ZERO
         LA    R1,PARM        POINT TO PARMS
         L     R15,EXITADDR   LOAD EXIT ADDRESS
CALL     STM   R0,R13,SAVEREGS SAVE REGISTERS
         BALR  R14,R15        GO TO EXIT
         LM    R0,R13,SAVEREGS PROTECT OURSELVES
         LTR   R15,R15        TEST RETURN CODE
         BZ    READ           ZERO, CONTINUE
         MVC   PTBUF(LMSG1),MSG1 SET ERROR MSG
         BAL   R9,TPUTRTN     WRITE MESSAGE
         B     EOJ            GET OUT
         EJECT
EOF      LA    R0,4           SET RETURN CODE TO EOF
         LA    R1,PARM        POINT TO PARMS
         L     R15,EXITADDR   LOAD EXIT ADDRESS
         BALR  R14,R15        GO TO EXIT
         LTR   R15,R15        TEST RETURN CODE
         BZ    OK             ZERO, CONTINUE
         MVC   PTBUF(LMSG2),MSG2 SET ERROR MSG
         BAL   R9,TPUTRTN     WRITE MESSAGE
         B     EOJ            GET OUT
         EJECT
OK       MVC   M3+18(1),JCTJCLAS SET JOB CLASS
         MVC   PTBUF(LMSG3),MSG3 SET ERROR MSG
         BAL   R9,TPUTRTN     WRITE MESSAGE
         B     EOJ            GET OUT
         EJECT
EOJ      DELETE EP=EXIT6      DELETE EXIT ROUTINE
         LA    R0,LJCT        LOAD LENGTH OF JCT
         FREEMAIN R,LV=(0),A=(R10) FREE CORE FOR JCT
         LA    R0,LPCE        LOAD LENGTH OF PCE
         FREEMAIN R,LV=(0),A=(R8) FREE CORE FOR PCE
         LA    R0,LJQE        LOAD LENGTH OF JQE
         FREEMAIN R,LV=(0),A=(R3) FREE CORE FOR JQE
         LA    R15,0          SET REG 15 TO '0'
         L     R13,4(R13)     RESTORE ADDR OF ORIGINAL SAVE
         L     R14,12(R13)    RESTORE ORIGINAL REGISTERS
         LM    R0,R12,20(R13) EXCEPT REG 15
         BR    R14            RETURN
         EJECT
TPUTRTN  LH    R0,PTBUF       LOAD MSG LENGTH TO REG 0
         SH    R0,=H'2'       SUBTRACT 2 FROM LENGTH FOR MESSAGE PREFIX
         LA    R1,PTBUF+2     LOAD ADDR OF ERROR MSG TO REG 1
         TPUT  (R1),(R0)      WRITE ERROR MESSAGE
         BR    R9             RETURN
         EJECT
*
*  FOLLOWING IS THE LIST OF POSSIBLE ERROR MESSAGES THAT CAN BE
*  ISSUED BY THE PROGRAM
*
MSG1     DC    Y(LMSG1)
M1       DC    C'EXIT REQUESTED EARLY TERMINATION'
LMSG1    EQU   L'M1+2
MSG2     DC    Y(LMSG2)
M2       DC    C'EXIT CANCELLED JOB'
LMSG2    EQU   L'M2+2
MSG3     DC    Y(LMSG3)
M3       DC    C'JOB CLASS WILL BE X'
LMSG3    EQU   L'M3+2
         EJECT
PTBUF    DS    H,CL120        BUFFER FOR TPUT MESSAGE
NEWSAVE  DS    18F            NEW SAVE AREA FOR MAIN ROUTINE
SAVEREGS DS    18F            CYA FOR CALLING EXIT ROUTINE
WORK     DC    16F'0'         NEW SAVE AREA FOR MAIN ROUTINE
EXITADDR DS    F              ADDRESS OF JESEXIT6
PARM     DS    0F             JES EXIT PARM
         DC    A(WORK)        ADDRESS OF 16 WORK WORK AREA
         DC    A(0)
         EJECT
         PRINT NOGEN
INTEXT   DCB   DDNAME=INTEXT,DSORG=PS,MACRF=GL,EODAD=EOF
JESMSG   ACB   DDNAME=JESMSG,MACRF=(ADR,SEQ,OUT)
LACB     EQU   *-JESMSG
         PRINT GEN
         $MODEND
         END
