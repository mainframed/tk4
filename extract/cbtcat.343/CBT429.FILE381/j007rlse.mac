         TITLE 'PERFORM /*RELEASE REQUEST PROCESSING'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS JES2 EXIT PROGRAM WILL PERFORM RELEASE PROCESSING       *
*        AT JOB TERMINATION FOR JOBS WHICH REQUEST THAT A JOB         *
*        BE RELEASED FROM USER HOLD UPON SUCCESSFUL COMPLETION        *
*        OF PROCESSING.  ALL JOBS OF THE RELEASE NAME WILL BE         *
*        RELEASED FROM USER HOLD IF THE JQEXOWN NAMES MATCH.          *
*        EACH JOB THAT IS RELEASED WILL BE ACCOMPANIED BY A           *
*        MESSAGE TO THAT EFFECT.  IF NO JOB(S) CAN BE FOUND, A        *
*        MESSAGE FOR THAT ERROR WILL ALSO BE ISSUED.                  *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J 0 0 7 R L S E            *
*                                                                     *
*        01)  $#POST       SYS1.HASPSRC                               *
*        02)  $BUFFER      SYS1.HASPSRC                               *
*        03)  $CAT         SYS1.HASPSRC                               *
*        04)  $CKPT        SYS1.HASPSRC                               *
*        05)  $ENTRY       SYS1.HASPSRC                               *
*        06)  $FREEBUF     SYS1.HASPSRC                               *
*        07)  $GETBUF      SYS1.HASPSRC                               *
*        08)  $HASPEQU     SYS1.HASPSRC                               *
*        09)  $HASPGBL     SYS1.HASPSRC                               *
*        10)  $HCT         SYS1.HASPSRC                               *
*        11)  $JCT         SYS1.HASPSRC                               *
*        12)  $JQE         SYS1.HASPSRC                               *
*        13)  $KIT         SYS1.HASPSRC                               *
*        14)  $MIT         SYS1.HASPSRC                               *
*        15)  $MODEND      SYS1.HASPSRC                               *
*        16)  $MODULE      SYS1.HASPSRC                               *
*        17)  $MSG         SYS1.HASPSRC                               *
*        18)  $PADDR       SYS1.HASPSRC                               *
*        19)  $PCE         SYS1.HASPSRC                               *
*        20)  $POST        SYS1.HASPSRC                               *
*        21)  $SAVE        SYS1.HASPSRC                               *
*        22)  $RETURN      SYS1.HASPSRC                               *
*        23)  $WTO         SYS1.HASPSRC                               *
*        24)  IEESMCA      SYS1.AMODGEN                               *
*        25)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J007RLSE $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='PERFORM /*RELEASE REQUEST PROCESSING',           X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $KIT,               GENERATE HASP KIT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               WPL,                GENERATE MVS  WPL DSECT             X
               RPL                 GENERATE MVS  RPL DSECT
         TITLE 'PERFORM /*RELEASE REQUEST PROCESSING'
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM WORK AREA                                            *
*                                                                     *
***********************************************************************
WORKAREA DSECT
         DS    CL(BUFSTART-BFPDSECT) ***** RESERVED BY JES2 *****
DBL      DS    D                   DOUBLE WORK WORK AREA
RLSENAME DS    CL8                 RELEASE NAME
OWNERID  DS    CL8                 THE OWNER OF THE JOB
RLSEIND  DS    X                   MISCELLANEOUS RELEASE INDICATORS
RELEASED EQU   X'80'               AT LEASE ONE JOB HAS BEEN RELEASED
JQEADDR  DS    F                   ADDRESS OF CURRENT JQE
JQENBR   DS    F                   JQE NUMBER BEING REQUESTED
$WTOWRK  DS    CL130               $WTO WORK AREA
J007RLSE CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E007RLSE $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R10,R1              SET JCT ADDRESS
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         GET ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - A CODE PASSED TO YOUR ROUTINE BY JES2.                 *
*              0 - INDICATES THAT THE JCT HAS BEEN READ FROM SPOOL    *
*              4 - INDICATES THAT THE JCT WILL BE WRITTEN TO SPOOL    *
*        R1  - ADDRESS OF THE BUFFER THAT CONTAINS THE JCT            *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE PCE                                     *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         LTR   R0,R0               TEST ENTRY REASON CODE
         BZ    RETURN0             ZERO, READ ONLY, NOT INTERESTED
         TM    JQEXFLG2,JQEXRLSE   DOES JOB HAVE RELEASE REQUESTS
         BNO   RETURN0             NO, JUST GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        SEE IF JOB HAS JUST LEFT THE INPUT OR EXECUTION QUEUES.      *
*                                                                     *
***********************************************************************
         TM    JQETYPE,$XEQ+$INPUT+$XMIT+$RECEIVE SEE IF FINISHED
         BNZ   RETURN0             NO, JUST GET OUT
***********************************************************************
*                                                                     *
*        GET A WORK AREA FOR THE EXIT                                 *
*                                                                     *
***********************************************************************
         SPACE 3
         $GETBUF TYPE=HASP,WAIT=YES GET A WORK AREA
         LR    R9,R1               SAVE WORK AREA ADDRESS
         USING WORKAREA,R9         SET ADDRESSABILITY TO BUFFER
***********************************************************************
*                                                                     *
*        SAVE STUFF FROM OUR JQE TO TEST OTHER JQE'S                  *
*                                                                     *
***********************************************************************
         MVC   RLSENAME,JQEXRLSN   SAVE THE RELEASE NAME
         MVC   OWNERID,JQEXOWNR    SAVE THE JOB OWNERID
         XC    RLSEIND,RLSEIND     CLEAR THE MISC SWITCHES
         XC    JQENBR,JQENBR       CLEAR THE JQE NUMBER
         ST    R7,JQEADDR          SAVE ADDRESS OF CURRENT JQE
         TM    JQEXFLG3,JQEXABND+JQEXJCLE SEE IF ABEND OR JCL ERROR
         BNZ   FAILURE             YES, BRANCH
***********************************************************************
*                                                                     *
*        LOOK AT ALL OTHER JQE'S FOR THE FOLLOWING                    *
*                                                                     *
*        1)  THE JOBNAME MUST MATCH OUR RELEASE NAME                  *
*        2)  THE OWNERID MUST MATCH OUR OWNERID                       *
*        3)  THE JOB MUST BE ON USER HOLD                             *
*                                                                     *
***********************************************************************
JQELOOP  L     R1,JQENBR           LOAD LAST JQE NUMBER
         LA    R1,1(R1)            ADD 1 TO JQE NUMBER
         ST    R1,JQENBR           SAVE NEW JQE NUMBER
         CH    R1,=H'9999'         SEE IF IT EXCEEDS THE MAX
         BH    LASTJQE             YES, GET OUT
         $QLOC (R1)                GET THE NEXT JQE
         BZ    JQELOOP             ZERO, NOT FOUND, LOOP
         LR    R7,R1               SET THE JQE ADDRESS
         CLC   JQEJNAME,RLSENAME   SEE IF JOB NAME MATCHES
         BNE   JQELOOP             NO, LOOP
         CLC   JQEXOWNR,OWNERID    SEE IF JOB BELONGS TO THIS USER
         BNE   JQELOOP             NO, LOOP
         TM    JQEXFLG2,JQEXUHLD   SEE IF JOB ON USER HOLD
         BNO   JQELOOP             NO, LOOP
         $QSUSE TYPE=WAIT          GET CONTROL OF THE SHARED QUEUES
         NI    JQEXFLG2,255-JQEXUHLD TURN OFF USER HOLD BIT
         $POST $HASPECF,(JOB,CNVT,HOPE,PURGE),MASPOST=YES
         $#POST TYPE=JQE,ADDR=(R1) TELL OUTPUT PROCESSOR ABOUT CHANGES
         LR    R1,R7               SET JQE ADDRESS
         $CKPT ID=JQE,ADDR=(R1)    TELL JES2 TO CHECKPOINT THE CHGS
         MVC   $WTOWRK(L'MSG1),MSG1 INITIALIZE MSG1 WORK AREA
         MVC   $WTOWRK+2+4(8),JQEJNAME SET JOB NAME IN MESSAGE
         LH    R1,JQEJOBNO         LOAD JOB NUMBER
         CVD   R1,DBL              CONVERT TO PACKED
         OI    DBL+7,X'0F'         MAKE PRINTABLE
         UNPK  $WTOWRK+2+18(4),DBL UNPACK JOB NUMBER
         MVC   $WTOWRK+2+14(3),=C'TSU' ASSUME A TSO USER
         TM    JQEFLAG3,JQE3TSU    SEE IF A TSO USER
         BO    PUTMSG              YES, BRANCH
         MVC   $WTOWRK+2+14(3),=C'STC' ASSUME A STARTED TASK
         TM    JQEFLAG3,JQE3STC    SEE IF A STARTED TASK
         BO    PUTMSG              YES, BRANCH
         MVC   $WTOWRK+2+14(3),=C'JOB' ASSUME A BATCH JOB
PUTMSG   LA    R1,$WTOWRK          LOAD ADDRESS OF MESSAGE
         LA    R0,L'MSG1           LOAD LENGTH OF MESSAGE
         $WTO  (R1),(R0),JOB=YES,CMB=NO,WAIT=NO,ROUTE=$MAIN+$LOG,      X
               UCM=NO,CLASS=$NORMAL
         OI    RLSEIND,RELEASED    TURN ON JOB RELEASED INDICATOR
         B     JQELOOP             GO GET NEXT JQE
LASTJQE  TM    RLSEIND,RELEASED    SEE IF ANY JOB(S) RELEASED
         BO    EXIT                YES, DONE
         MVC   $WTOWRK(L'MSG2),MSG2 INITIALIZE MSG2 WORK AREA
         MVC   $WTOWRK+2+4(8),RLSENAME SET RELEASE NAME IN MESSAGE
         LA    R1,$WTOWRK          LOAD ADDRESS OF MESSAGE
         LA    R0,L'MSG2           LOAD LENGTH OF MESSAGE
         $WTO  (R1),(R0),JOB=YES,CMB=NO,WAIT=NO,ROUTE=$MAIN+$LOG,      X
               UCM=NO,CLASS=$NORMAL
         B     EXIT                DONE
FAILURE  MVC   $WTOWRK(L'MSG3),MSG3 INITIALIZE MSG2 WORK AREA
         MVC   $WTOWRK+2+4(8),RLSENAME SET RELEASE NAME IN MESSAGE
         LA    R1,$WTOWRK          LOAD ADDRESS OF MESSAGE
         LA    R0,L'MSG3           LOAD LENGTH OF MESSAGE
         $WTO  (R1),(R0),JOB=YES,CMB=NO,WAIT=NO,ROUTE=$MAIN+$LOG,      X
               UCM=NO,CLASS=$NORMAL
         B     EXIT                DONE
         EJECT
***********************************************************************
*                                                                     *
*        FREE THE WORK AREA AND RETURN WITH THE APPROPRIATE           *
*        RETURN CODE.                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
EXIT     $QSUSE TYPE=WAIT          GET CONTROL OF THE SHARED QUEUES
         L     R7,JQEADDR          LOAD OUR JQE ADDRESS
         NI    JQEXFLG2,255-JQEXRLSE TURN OFF RELEASE FLAG
         $FREEBUF (R9)             FREE THE BUFFER
         B     RETURN0             GET OUT
         DROP  R9                  DROP ADDRESSABILITY TO THE BUFFER
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
***********************************************************************
*                                                                     *
*                   CONSTANTS USED IN THIS EXIT                       *
*                                                                     *
***********************************************************************
         SPACE 3
         LTORG
         EJECT
MSG1     $MSG  707,'JOB XXXXXXXX (TTT NNNN) HAS BEEN RELEASED FROM USERX
                HOLD'
MSG2     $MSG  706,'JOB XXXXXXXX NOT RELEASED FROM USER HOLD BECAUSE ITX
                COULD NOT BE FOUND'
MSG3     $MSG  705,'JOB XXXXXXXX NOT RELEASED FROM USER HOLD BECAUSE OFX
                JOB FAILURE'
         $MODEND
         END
