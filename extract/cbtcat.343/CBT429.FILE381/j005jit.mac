         TITLE 'JES2 $TJIT COMMAND PROCESSOR'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED PROCESS THE $TJIT         *
*        COMMAND WHEN ENTERED.  THIS COMMAND IS IMPLEMENTED AS        *
*        AN EXIT5 EXIT TO JES2, AND AS SUCH MUST INTERCEPT ONLY       *
*        $TJIT COMMAND WHEN ENCOUNTERED.                              *
*                                                                     *
*                                                                     *
*        THE $TJIT COMMAND HAS THE FOLLOWING SYNTAX:                  *
*                                                                     *
*        $TJIT,                                                       *
*              STOP      STOP JOB INFORMATION TASK PROCESSING         *
*                        BY CLOSING AND FREEING THE VSAM FILE.        *
*                                                                     *
*              START     START JOB INFORMATION TASK PROCESSING        *
*                        BY ALLOCATING AND REOPENING THE VSAM         *
*                        FILE AND RESUMING NORMAL PROCESSING.         *
*                                                                     *
*              RESTART   STOP JOB INFORMATION TASK PROCESSING,        *
*                        CLOSE AND FREE THE VSAM FILE, REALLOCATE     *
*                        AND OPEN THE FILE AND RESUME NORMAL          *
*                        PROCESSING,                                  *
*                                                                     *
*              STATUS    DISPLAY THE STATUS OF THE JOB INFORMATION    *
*                        TASK.                                        *
*                                                                     *
*              DSN=DSN   MODIFY THE NAME OF THE VSAM FILE.  THIS      *
*                        MODIFICATION WILL NOT TAKE EFFECT UNTIL      *
*                        THE START OR RESTART OPTION IS REQUESTED.    *
*                                                                     *
*              LIST=KEY  LIST A RECORD FROM THE VSAM FILE WITH        *
*                        THE SPECIFIED KEY.  IF IMBEDDED BLANKS       *
*                        EXIST IN THE KEY, A '.' OR A '*' SHOULD      *
*                        BE USED INSTEAD OF THE BLANK.                *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        THE FOLLOWING RESPONSES WILL BE MADE BY THIS EXIT:           *
*                                                                     *
*        1)  THE HASP990 MESSAGE WILL BE ISSUED FOR EACH REQUEST      *
*                                                                     *
*        $HASP990 JIT START   PROCESSING SUCESSFULLY COMPLETED        *
*        $HASP990 JIT STOP    PROCESSING SUCESSFULLY COMPLETED        *
*        $HASP990 JIT RESTART PROCESSING SUCESSFULLY COMPLETED        *
*        $HASP990 JIT LIST    PROCESSING SUCESSFULLY COMPLETED        *
*        $HASP990 JIT DSNAME SUCESSFULLY MODIFIED                     *
*                                                                     *
*        2)  THE HASP990 MESSAGE WILL BE ISSUED FOR EACH STATUS       *
*            REQUEST.                                                 *
*                                                                     *
*        $HASP990 JIT DSN=DSNAME                                      *
*        $HASP990 JIT STOPPED/STARTED INIT ALLOC OPENED I/O           *
*        $HASP990 JIT WAIT/ACTIVE QUEUE=NNN                           *
*                                                                     *
*        3)  THE HASP990 MESSAGE WILL BE ISSUED FOR EACH LIST         *
*            REQUEST.                                                 *
*                                                                     *
*        $HASP990 JIT JOBNAME =JOBNAME                                *
*        $HASP990 JIT GROUPID =OUTPUT GROUP NAME                      *
*        $HASP990 JIT SEPID   =SEPARATOR ID                           *
*        $HASP990 JIT DUEOUT  =DUE OUT TIME                           *
*        $HASP990 JIT SYSCLASS=SYSOUT CLASS FOR STC                   *
*        $HASP990 JIT PGMRNAME=PROGRAMMER NAME FOR STC/TSU            *
*                                                                     *
*        4)  THE HASP999 MESSAGE WILL BE ISSUED WHEN AN INVALID       *
*            OPERAND KEYWORD OR VALUE IS DETECTED OR WHEN ONE         *
*            OF THE POSITIONAL KEYWORDS ARE NOT FIRST IN THE LIST.    *
*                                                                     *
*        $HASP999 ......... INVALID OPERAND                           *
*                                                                     *
*        5)  THE HASP999 MESSAGE WILL BE ISSUED WHEN THE L=CCA        *
*            OPERAND SPECIFIES AN INVALID CONSOLE OR DISPLAY          *
*            AREA.                                                    *
*                                                                     *
*        $HASP999 RESPONSE LOCATION L=CCA UNAVAILABLE                 *
*                                                                     *
***********************************************************************
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*              M A C R O S   U S E D   I N   J 0 0 5 J I T            *
*                                                                     *
*        01)  $AMODE       SYS1.HASPSRC                               *
*        02)  $BUFFER      SYS1.HASPSRC                               *
*        03)  $CAT         SYS1.HASPSRC                               *
*        04)  $CMB         SYS1.HASPSRC                               *
*        05)  $CWA         SYS1.HASPSRC                               *
*        06)  $COMWORK     SYS1.HASPSRC                               *
*        07)  $CWTO        SYS1.HASPSRC                               *
*        08)  $DTE         SYS1.HASPSRC                               *
*        09)  $ENTRY       SYS1.HASPSRC                               *
*        10)  $ERA         SYS1.HASPSRC                               *
*        11)  $GETBUF      SYS1.HASPSRC                               *
*        12)  $FCSDTE      SYS1.HASPSRC (USER)                        *
*        13)  $FCSJITB     SYS1.HASPSRC (USER)                        *
*        14)  $FCSUCT      SYS1.HASPSRC (USER)                        *
*        15)  $FREEBUF     SYS1.HASPSRC                               *
*        16)  $HASPEQU     SYS1.HASPSRC                               *
*        17)  $HASPGBL     SYS1.HASPSRC                               *
*        18)  $HCT         SYS1.HASPSRC                               *
*        19)  $JCT         SYS1.HASPSRC                               *
*        20)  $JOE         SYS1.HASPSRC                               *
*        21)  $JOT         SYS1.HASPSRC                               *
*        22)  $JQE         SYS1.HASPSRC                               *
*        23)  $MIT         SYS1.HASPSRC                               *
*        24)  $MODEND      SYS1.HASPSRC                               *
*        25)  $MODULE      SYS1.HASPSRC                               *
*        26)  $PADDR       SYS1.HASPSRC                               *
*        27)  $PCE         SYS1.HASPSRC                               *
*        28)  $RETURN      SYS1.HASPSRC                               *
*        29)  $SAVE        SYS1.HASPSRC                               *
*        30)  $STORE       SYS1.HASPSRC                               *
*        31)  $XECB        SYS1.HASPSRC                               *
*        32)  $WAIT        SYS1.HASPSRC                               *
*        33)  IAZRESPA     SYS1.AMODGEN                               *
*        34)  IFGRPL       SYS1.MACLIB                                *
*        35)  MODESET      SYS1.MACLIB                                *
*        36)  POST         SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       2K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J005JIT  $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='JES2 $TJIT COMMAND PROCESSOR',                   X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $CMB,               GENERATE HASP CMB DSECT             X
               $COMWORK,           GENERATE HASP COMWORK DSECT         X
               $CWA,               GENERATE HASP CWA DSECT             X
               $DTE,     DTE       GENERATE HASP DTE DSECT             X
               $ERA,     DTE       GENERATE HASP ERA DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP HCT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JOE,               GENERATE HASP JOE DSECT             X
               $JOT,               GENERATE HASP JOT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $XECB,    DTE       GENERATE HASP XECB DSECT            X
               CVT,                GENERATE MVS  CVT DSECT             X
               RESPA,              GENERATE MVS  RESPA  DSECT          X
               RPL,                GENERATE MVS  IFGRPL DSECT          X
               UCB                 GENERATE MVS  UCB DSECT
               SPACE 3
         EJECT
***********************************************************************
*                                                                     *
*        USER ADDITION OF THE DTE                                     *
*                                                                     *
***********************************************************************
DTE      DSECT
         ORG  DTEWORK
         DS   0CL2048              2048 BYTE EXTENSION
         $FCSDTE ,                 MAP THE COMMON DTE EXTENSION AREA
J005JIT  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
         $FCSUCT ,                 MAP THE UCT DSECT
J005JIT  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
         $FCSJITB ,                MAP THE JITB DSECT
J005JIT  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM WORK AREAS                                           *
*                                                                     *
***********************************************************************
         SPACE 3
$JITWORK DSECT                     COMMAND WORK AREA
         DS    CL(BUFSTART-BFPDSECT) **** RESERVED BY JES2 ****
JITBAREA DS    CL200               RESERVE 200 CHARACTERS FOR JITB
OPNDCTR  DS    H                   COUNT OF COMMAND OPERANDS
DSNAME   DS    CL44                REQUESTED NEW DSNAME
DBL1     DS    D                   DOUBLE WORD WORK AREA
WRKA     DS    CL10                EXIT WORK AREA
J005JIT  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         TITLE 'JES2 $TJIT COMMAND PROCESSOR'
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E005JIT  $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R5       - ADDRESS OF THE CURRENT OPERAND                    *
*        R6       - INCREMENT VALUE OF 4                              *
*        R7       - ADDRESS OF THE LAST OPERAND                       *
*        R11      - ADDRESS OF THE HCT                                *
*        R13      - ADDRESS OF THE HASPCOMM PCE                       *
*        R14      - RETURN ADDRESS                                    *
*        R15      - ENTRY ADDRESS                                     *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        SEE IF THIS IS A $TJIT COMMAND                               *
*                                                                     *
***********************************************************************
         CLC   COMVERB(4),=C'TJIT' SEE IF THIS IS THE $TJIT COMMAND
         BNE   EXIT0               NO, BRANCH TO COMMAND OK EXIT
         EJECT
***********************************************************************
*                                                                     *
*        GET A JES2 BUFFER FOR REENTRANCY AND                         *
*        CLEAR THE COMMAND WORK AREA                                  *
*                                                                     *
***********************************************************************
         SPACE 1
         $GETBUF TYPE=HASP,WAIT=YES GET A WORK AREA
         LR    R9,R1               SAVE WORK AREA ADDRESS
         USING $JITWORK,R9         SET ADDRESSABILITY TO WORK AREA
         LA    R14,$JITWORK+(BUFSTART-BFPDSECT) SET 'TO' ADDRESS
         LH    R15,$BUFSIZE        SET   'TO'   LENGTH
         SLR   R0,R0               CLEAR 'FROM' ADDRESS
         SLR   R1,R1               CLEAR 'FROM' LENGTH
         MVCL  R14,R0              CLEAR THE BUFFER
         CLI   COMVERB+4,C' '      SEE IF OTHER OPERANDS SPECIFIED
         BE    INSUFF              NO, INSUFFICIENT OPERANDS
         ICM   R10,15,$UCT         LOAD ADDRESS OF UCT
         BZ    NOJIT               ZERO, NONE, ERROR
         USING UCT,R10             SET ADDRESSABILITY TO UCT
         LA    R8,JITBAREA         LOAD ADDRESS OF JITB AREA
         USING JITB,R8             SET ADDRESSABILITY TO JITB
         MVC   JITBID,=C'JITB'     SET THE JITBID FIELD
         EJECT
***********************************************************************
*                                                                     *
*        EXAMINE THE COMMAND OPERANDS                                 *
*                                                                     *
***********************************************************************
CKOPND   BXH   R5,R6,CTJITOK       IF NO MORE GO PROCESS THEM
         L     R3,0(,R5)           POINT TO OPERAND
         LH    R15,OPNDCTR         LOAD OPERAND COUNTER
         LA    R15,1(R15)          ADD 1 TO COUNTER
         STH   R15,OPNDCTR         SAVE OPERAND COUNTER
         CLC   0(4,R3),=C'DSN='    SEE IF 'DSN=' OPERAND
         BE    OPDSN               YES, GO PROCESS IT
         CLC   OPNDCTR,=H'1'       IS THIS THE FIRST OPERAND
         BNE   BADOPRND            NO, INVALID OPERAND
         CLC   0(5,R3),=C'START'   SEE IF 'START' OPTION
         BE    OPSTART             YES, GO PROCESS IT
         CLC   0(4,R3),=C'STOP'    SEE IF 'STOP' OPTION
         BE    OPSTOP              YES, GO PROCESS IT
         CLC   0(7,R3),=C'RESTART' SEE IF 'RESTART' OPTION
         BE    OPREST              YES, GO PROCESS IT
         CLC   0(6,R3),=C'STATUS'  SEE IF 'STATUS' OPTION
         BE    OPSTAT              YES, GO PROCESS IT
         CLC   0(5,R3),=C'LIST='   SEE IF 'LIST' OPTION
         BE    OPLIST              YES, GO PROCESS IT
         B     BADOPRND            NOT VALID, ERROR
         EJECT
***********************************************************************
*                                                                     *
*        PROCESS THE START OPERAND                                    *
*                                                                     *
***********************************************************************
         SPACE 1
OPSTART  MVI   JITBFLG,JITBSTAR    TURN ON 'START' OPTION
         CLI   5(R3),C' '          FIVE CHARACTER OPERAND
         BE    CKOPND              YES, GO GET NEXT OPERAND
         CLI   5(R3),C','          FIVE CHARACTER OPERAND
         BNE   BADOPRND            NO, ERROR
         B     CKOPND              GO GET NEXT OPERAND
         SPACE 1
***********************************************************************
*                                                                     *
*        PROCESS THE STOP OPERAND                                     *
*                                                                     *
***********************************************************************
         SPACE 1
OPSTOP   MVI   JITBFLG,JITBSTOP    TURN ON 'STOP' OPTION
         CLI   4(R3),C' '          FOUR CHARACTER OPERAND
         BE    CKOPND              YES, GO GET NEXT OPERAND
         CLI   4(R3),C','          FOUR CHARACTER OPERAND
         BNE   BADOPRND            NO, ERROR
         B     CKOPND              GO GET NEXT OPERAND
         SPACE 1
***********************************************************************
*                                                                     *
*        PROCESS THE RESTART OPERAND                                  *
*                                                                     *
***********************************************************************
         SPACE 1
OPREST   MVI   JITBFLG,JITBREST    TURN ON 'RESTART' OPTION
         CLI   7(R3),C' '          SEVEN CHARACTER OPERAND
         BE    CKOPND              YES, GO GET NEXT OPERAND
         CLI   7(R3),C','          SEVEN CHARACTER OPERAND
         BNE   BADOPRND            NO, ERROR
         B     CKOPND              GO GET NEXT OPERAND
         SPACE 1
***********************************************************************
*                                                                     *
*        PROCESS THE STATUS OPERAND                                   *
*                                                                     *
***********************************************************************
         SPACE 1
OPSTAT   MVI   JITBFLG,JITBDISP    TURN ON 'STATUS' OPTION
         CLI   6(R3),C' '          SIX CHARACTER OPERAND
         BE    CKOPND              YES, GO GET NEXT OPERAND
         CLI   6(R3),C','          SIX CHARACTER OPERAND
         BNE   BADOPRND            NO, ERROR
         B     CKOPND              GO GET NEXT OPERAND
         SPACE 1
***********************************************************************
*                                                                     *
*        PROCESS THE LIST OPERAND                                     *
*                                                                     *
***********************************************************************
         SPACE 1
OPLIST   MVI   JITBKEY,C' '        BLANK ENTIRE
         MVC   JITBKEY+1(15),JITBKEY   FILE KEY FIELD
         MVI   JITBFLG,JITBREAD    TURN ON OPTION BIT
         LA    R15,8               SET LOOP COUNTER
OPLIST1  CLI   0(R3),C'='          FIND END OF KEYWORD
         BE    OPLIST2             YES, BRANCH
         LA    R3,1(R3)            INCREMENT TO NEXT CHAR
         BCT   R15,OPLIST1         LOOP
         B     BADOPRND            NO '=' ERROR
OPLIST2  LA    R3,1(R3)            INCREMENT PAST '='
         LR    R4,R3               SAVE START OF VALUE ADDRESS
         LA    R15,17              SET LOOP COUNTER
OPLIST3  CLI   0(R4),C','          PROPER END OF VALUE
         BE    OPLIST4             YES, BRANCH
         CLI   0(R4),C' '          PROPER END OF VALUE
         BE    OPLIST4             YES, BRANCH
         LA    R4,1(R4)            INCREMENT TO NEXT CHAR
         BCT   R15,OPLIST3         LOOP
         B     BADOPRND            OPERAND TOO LONG
OPLIST4  LR    R15,R4              SAVE END ADDRESS
         SR    R15,R3              CALCULATE LENGTH
         LTR   R15,R15             TEST LENGTH
         BZ    BADOPRND            ZERO, ERROR
         BCTR  R15,0               MAKE MACHINE LENGTH
         EX    R15,MVKEY           SAVE KEY
***********************************************************************
*        TRANSLATE PLACEHOLDER CHARACTERS IN KEY TO BLANKS            *
***********************************************************************
         LA    R15,16              SET LOOP COUNTER
         LA    R14,JITBKEY         POINT TO JITB KEY
OPLIST5  CLI   0(R14),C'*'         SEE IF PLACEHOLDER
         BE    OPLIST6             YES, BRANCH
         CLI   0(R14),C'.'         SEE IF PLACEHOLDER
         BNE   OPLIST7             NO,  BRANCH
OPLIST6  MVI   0(R14),C' '         MAKE IT A BLANK
OPLIST7  LA    R14,1(R14)          INCREMENT TO NEXT CHAR
         BCT   R15,OPLIST5         LOOP
         B     CKOPND              GO GET NEXT OPERAND
MVKEY    MVC   JITBKEY(0),0(R3)    **** EXECUTE ONLY ****
         SPACE 1
***********************************************************************
*                                                                     *
*        PROCESS THE DSN OPERAND                                      *
*                                                                     *
***********************************************************************
         SPACE 1
OPDSN    MVI   DSNAME,C' '         BLANK ENTIRE
         MVC   DSNAME+1(43),DSNAME     DSNAME FIELD
         LA    R15,8               SET LOOP COUNTER
OPDSN1   CLI   0(R3),C'='          FIND END OF KEYWORD
         BE    OPDSN2              YES, BRANCH
         LA    R3,1(R3)            INCREMENT TO NEXT CHAR
         BCT   R15,OPDSN1          LOOP
         B     BADOPRND            NO '=' ERROR
OPDSN2   LA    R3,1(R3)            INCREMENT PAST '='
         LR    R4,R3               SAVE START OF VALUE ADDRESS
         LA    R15,45              SET LOOP COUNTER
OPDSN3   CLI   0(R4),C','          PROPER END OF VALUE
         BE    OPDSN4              YES, BRANCH
         CLI   0(R4),C' '          PROPER END OF VALUE
         BE    OPDSN4              YES, BRANCH
         LA    R4,1(R4)            INCREMENT TO NEXT CHAR
         BCT   R15,OPDSN3          LOOP
         B     BADOPRND            OPERAND TOO LONG
OPDSN4   LR    R15,R4              SAVE END ADDRESS
         SR    R15,R3              CALCULATE LENGTH
         LTR   R15,R15             TEST LENGTH
         BZ    BADOPRND            ZERO, ERROR
         BCTR  R15,0               MAKE MACHINE LENGTH
         EX    R15,MVDSN           SAVE DSNAME
         B     CKOPND              GO GET NEXT OPERAND
MVDSN    MVC   DSNAME(0),0(R3)     **** EXECUTE ONLY ****
         EJECT
*****************************************************************
*                                                               *
*        CHECK FOR REDIRECTION OF RESPONSES                     *
*                                                               *
*****************************************************************
         SPACE 1
CTJITOK  TM    COMFLAG,CMBFLAGU    IS THIS A UCM
         BZ    PROCESS             NO, SKIP REDIRECTION
REDIR    TM    FBLFLG,FBLFLGC+FBLFLGR CONSOLE SET OR REMOTE SYSTEM
         BNZ   REDIR1              SKIP AUTOMATIC L=CCA
         LA    R1,2                LOAD REDIRECT OFFSET FOR $TJIT
*                                  SAME AS FOR $DF
         BCTR  R1,0                REDUCE BY 1
         SLL   R1,1                DOUBLE
         SLR   R2,R2               ZERO REGISTER FOR IC
         IC    R2,COMUCM           PICK UP CURRENT UCM ID
         L     R14,$COMEXTN        POINT TO CONSOLE WORK AREA
         USING CWA,R14             SET ADDRESSABILITY TO CWA
         CH    R2,CWAOCON          SEE IF EXCEEDS MAXIMUM
         BH    REDIR1              YES, ERROR, NO AUTOMATIC REDIRECTION
         MH    R2,CWALCON          MULTIPLY BY LENGTH OF EACH ENTRY
         AR    R1,R2               COMBINE OFFSETS
         LA    R1,CWARESP-CWACONL(R1) POINT TO CORRECT CWA ENTRY
         MVC   COMUCM,CWACON(R1)   INSERT NEW CONSOLE ID
         TM    FBLFLG,FBLFLGA      HAS AREA BEEN SET
         BO    REDIR1              YES, SKIP AUTOMATIC REDIRECTION
         MVC   COMUCMA,CWACONA(R1) SET NEW AREA
         DROP  R14                 DROP ADDRESSABILITY TO CWA
         EJECT
*****************************************************************
*                                                               *
*        ROUTINE TO VERIFY CONSOLE/AREA                         *
*                                                               *
*****************************************************************
         SPACE 1
REDIR1   TM    FBLFLG,FBLFLGR      SEE IF REMOTE SYSTEM UCM
         BO    REDIR3              YES, UCM AND AREA ARE OK
         L     R2,$COMEXTN         POINT TO CONSOLE WORK AREA
         USING CWA,R2              SET ADDRESSABILITY TO CWA
         MVC   XASCID(1),COMUCM    SET UCM ID
         MVC   XASDID(1),COMUCMA   SET AREA ID
         CLI   XASDID,0            SEE IF AREA ID SPECIFIED
         BNZ   REDIR2              YES, BRANCH
         MVI   XASDID,C'Z'         NO,  FORCE AREA Z
REDIR2   MVC   XAU(1),COMUCM       TELL OS SOURCE IS SAME AS OUTPUT
         MVI   XAN,X'22'           SET ID FOR VERIFIER
         MODESET EXTKEY=ZERO       GET KEY 0
         L     R15,CWA7603D        POINT TO OS VERIFICATION ROUTINE
         $AMODE 24,PUSHR=0,R=R14   GO TO AMODE 24
         BALR  R14,R15             ENTER IT
         $AMODE POPR=0,R=R14       RETURN TO ORIGINAL AMODE
         MODESET EXTKEY=HASP       RETURN TO KEY 1
         CH    R15,=H'4'           CHECK FOR SERIOUS ERROR
         BNH   REDIR3              NO, GO ISSUE ERROR MESSAGE
         SLR   R3,R3               CLEAR REGISTER FOR ICM
         ICM   R3,B'0011',COMUCM   PICK UP UCM AND AREA ID
         MVI   COMUCMA,0           FORCE NO DISPLAY AREA
         CH    R15,=H'12'          SEE IF CONSOLE DOWN
         BNE   *+16                NO, DO NOT SET LOGICAL CONSOLE
         MVC   COMFLAG(4),MOWTOL   DESTROY APPARENT SOURCE INFO
         MVC   COMOUT,MOWTOL+(COMOUT-COMFLAG) OMITTING CNTL FIELD
         $CWTO L=L'CMBMSG,MSGID=900 ECHO THE COMMAND
         MVC   COMMAND(MBVINVL),MBVINV SET ERROR MESSAGE TEXT
         STC   R3,MBVINVA          SET AREA
         SRL   R3,8                ALIGN CONSOLE ID
         CVD   R3,$DOUBLE          CONVERT TO PACKED
         MVC   WRKA(4),=X'F0212020' SET EDIT MASK
         ED    WRKA(4),$DOUBLE+6   EDIT CONSOLE ID TO AREA
         MVC   MBVINVC,WRKA+2      MOVE CONSOLE ID TO MESSAGE
         MVC   COMMAND+MBVINVL(8),COMACEID MOVE IDENTIFIER
         $CWTO L=MBVINVL,MSGID=999 WRITE ERROR MESSAGE TO CONSOLE
         B     EXIT8               GET OUT
         DROP  R2                  DROP ADDRESSABILITY TO CWA
REDIR3   CLI   COMUCMA,0           TEST FOR DISPLAY AREA
         BE    PROCESS             IF NOT, SKIP
         MVI   COMLINET,X'80'      SET CONTROL LINE
         $CWTO L=L'CMBMSG,MSGID=900 ECHO THE COMMAND
         MVI   COMLINET,X'20'      SET DATA LINE
         B     PROCESS             CONTINUE
         EJECT
*****************************************************************
*                                                               *
*       R E D I R E C T   R O U T I N E   C O N S T A N T S     *
*                                                               *
*****************************************************************
         SPACE 1
MOWTOL   $WTO  PRI=$HI,CLASS=$ALWAYS,JOB=NO,ROUTE=X'C000',MF=LX
MBVINV   DC    C'RESPONSE LOCATION L=CCA UNAVAILABLE '
MBVINVL  EQU   *-MBVINV
MBVINVC  EQU   COMMAND+MBVINVL-16,2
MBVINVA  EQU   MBVINVC+2,1
FBLFLG   EQU   COMEWORK+2          FLAG BYTE
COMJMBR  EQU   COMJRMT             SHARED SPOOL MEMBER NUMBER
FBLFLGR  EQU   1                   UCM COMMAND FROM REMOTE SYSTEM
FBLFLGC  EQU   2                   CONSOLE HAS BEEN SPECIFIED
FBLFLGA  EQU   4                   AREA HAS BEEN SPECIFIED
         EJECT
***********************************************************************
*                                                                     *
*        PROCESS THE REQUESTED OPERANDS                               *
*                                                                     *
***********************************************************************
PROCESS  DS    0H
CK1      CLI   DSNAME,C' '         SEE IF DSNAME REQUEST
         BNH   CK2                 NO, BRANCH
         MVC   UCTJITDS,DSNAME     RESET THE DSNAME IN THE UCT
         MVC   COMMAND(L'MSG3),MSG3 SET MESSAGE TEXT
         LA    R0,L'MSG3           SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE MESSAGE
CK2      TM    JITBFLG,JITBDISP    SEE IF DISPLAY STATUS OPTION
         BO    STATUS              YES, BRANCH
CK3      CLI   JITBFLG,0           SEE IF ANY OTHER OPTIONS SET
         BE    EXIT8               NO, GET OUT
         SPACE 1
***********************************************************************
*                                                                     *
*        CHAIN THE JITB FOR THE JIT SUBTASK                           *
*                                                                     *
***********************************************************************
         SPACE 1
         ICM   R4,B'1111',UCTJITDT LOAD JIT DTE ADDRESS
         BZ    NOJIT               NONE, BRANCH
         USING DTE,R4              SET ADDRESSABILITY TO DTE
         XC    JITBXECB(L'XECBECB),JITBXECB CLEAR THE XECB
         L     R1,DTEFQUE          LOAD THE WORK QUEUE ADDRESS
CSLOOP   ST    R1,JITBCH           PUSH HEAD QUEUE DOWN
         CS    R1,R8,DTEFQUE       ADD TO WORK QUEUE
         BNE   CSLOOP              CHANGED, TRY AGAIN
         POST  DTEWECB             POST SUBTASK
         $WAIT OPER,XECB=JITBXECB,INHIBIT=YES
         DROP  R4                  DROP ADDRESSABILITY TO DTE
         SPACE 1
***********************************************************************
*                                                                     *
*        DISPLAY THE RESULTS OF THE CALL TO THE JIT SUBTASK           *
*                                                                     *
***********************************************************************
         SPACE 1
RESULTS1 MVI   COMMAND,C' '        BLANK ENTIRE
         MVC   COMMAND+1(99),COMMAND     COMMAND AREA
         MVC   COMMAND(3),=C'JIT'  SET MESSAGE PREFIX
         MVC   COMMAND+12(10),=C'PROCESSING' SET MESSAGE TEXT
         MVC   COMMAND+23(6),=C'FAILED'  ASSUME THAT IT FAILED
         TM    JITBSTAT,JITBOK     SEE IF ACTUALLY SUCCESSFUL
         BNO   RESULTS2            NO, CONTINUE
         MVC   COMMAND+23(22),=C'SUCCESSFULLY COMPLETED'
RESULTS2 MVC   COMMAND+4(4),=C'LIST' ASSUME LIST REQUEST
         TM    JITBFLG,JITBREAD    SEE IF LIST REQUEST
         BO    RESULTS3            YES, BRANCH
         MVC   COMMAND+4(4),=C'STOP' ASSUME STOP REQUEST
         TM    JITBFLG,JITBSTOP    SEE IF STOP REQUEST
         BO    RESULTS3            YES, BRANCH
         MVC   COMMAND+4(5),=C'START' ASSUME START REQUEST
         TM    JITBFLG,JITBSTAR    SEE IF START REQUEST
         BO    RESULTS3            YES, BRANCH
         MVC   COMMAND+4(7),=C'RESTART' ASSUME RESTART REQUEST
         TM    JITBFLG,JITBREST    SEE IF RESTART REQUEST
         BO    RESULTS3            YES, BRANCH
         MVC   COMMAND+4(7),=C'UNKNOWN' UNKNOWN REQUEST
RESULTS3 LA    R0,50               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         TM    JITBFLG,JITBREAD    SEE IF LIST REQUEST
         BNO   EXIT8               NO, DONE
RESULTS4 MVC   COMMAND(4),=C'JIT ' SET LINE TITLE
         MVC   COMMAND+4(36),BLANKS BLANK DISPLAY LINE
         MVC   COMMAND+4(9),=C'JOBNAME =' SET FIELD TITLE
         MVC   COMMAND+13(08),JITBJOBN SET FIELD
         MVC   COMMAND+23(9),=C'GROUPID =' SET FIELD TITLE
         MVC   COMMAND+32(08),JITBGRP  SET FIELD
         LA    R0,40               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'PDIST   =' SET FIELD TITLE
         MVC   COMMAND+13(08),JITBPSID SET FIELD
         MVC   COMMAND+23(9),=C'SDIST   =' SET FIELD TITLE
         MVC   COMMAND+32(08),JITBSSID SET FIELD
         LA    R0,40               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'DUEOUT  =' SET FIELD TITLE
         MVC   COMMAND+13(08),BLANKS BLANK FIELD
         MVC   COMMAND+13(04),JITBDUEO SET FIELD
         MVC   COMMAND+23(9),=C'SYSCLASS=' SET FIELD TITLE
         MVC   COMMAND+32(08),BLANKS BLANK FIELD
         MVC   COMMAND+32(01),JITBCLAS SET FIELD
         LA    R0,33               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'DESCR   =' SET FIELD TITLE
         MVC   COMMAND+13(08),JITBDESC SET FIELD
         MVC   COMMAND+23(9),=C'UCC7FLG =' SET FIELD TITLE
         MVC   COMMAND+32(01),JITBU7FL SET FIELD
         LA    R0,33               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'NAME    =' SET MESSAGE PREFIX
         MVC   COMMAND+13(20),JITBPGMR SET MESSAGE KEY
         LA    R0,33               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'PHONE   =' SET MESSAGE PREFIX
         MVC   COMMAND+13(15),JITBPHON SET MESSAGE KEY
         LA    R0,28               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'LOCATION=' SET MESSAGE PREFIX
         MVC   COMMAND+13(15),JITBLOC  SET MESSAGE KEY
         LA    R0,28               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'DEPT    =' SET MESSAGE PREFIX
         MVC   COMMAND+13(20),JITBDEPT SET MESSAGE KEY
         LA    R0,33               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'MAILCODE=' SET MESSAGE PREFIX
         MVC   COMMAND+13(10),JITBMAIL SET MESSAGE KEY
         LA    R0,23               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'INST 1  =' SET MESSAGE PREFIX
         MVC   COMMAND+13(25),JITBINS1 SET MESSAGE KEY
         LA    R0,38               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'INST 2  =' SET MESSAGE PREFIX
         MVC   COMMAND+13(25),JITBINS2 SET MESSAGE KEY
         LA    R0,38               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'INST 3  =' SET MESSAGE PREFIX
         MVC   COMMAND+13(25),JITBINS3 SET MESSAGE KEY
         LA    R0,38               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         MVC   COMMAND+4(9),=C'INST 4  =' SET MESSAGE PREFIX
         MVC   COMMAND+13(25),JITBINS4 SET MESSAGE KEY
         LA    R0,38               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         B     EXIT8               GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        DISPLAY THE STATUS OF THE JIT SUBTASK                        *
*                                                                     *
***********************************************************************
STATUS   ICM   R4,B'1111',UCTJITDT LOAD JIT DTE ADDRESS
         BZ    NOJIT               NONE, BRANCH
         USING DTE,R4              SET ADDRESSABILITY TO DTE
         MVI   COMMAND,C' '        BLANK ENTIRE
         MVC   COMMAND+1(99),COMMAND     COMMAND AREA
         MVC   COMMAND(8),=C'JIT DSN=' SET MSG TITLE
         CLI   UCTJITDS,C' '       SEE IF DSNAME SET
         BNH   STATUS01            NO, BRANCH
         MVC   COMMAND+8(44),UCTJITDS MOVE DSNAME TO MESSAGE
STATUS01 LA    R0,52               SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE MESSAGE
         MVI   COMMAND,C' '        BLANK ENTIRE
         MVC   COMMAND+1(99),COMMAND     COMMAND AREA
         LA    R14,COMMAND         POINT TO COMMAND AREA
         SLR   R15,R15             ZERO THE LENGTH FIELD
         MVC   0(3,R14),=C'JIT'    SET MSG TITLE
         LA    R15,4(R15)          ADD 8 TO LENGTH
         LA    R14,4(R14)          INCREMENT TO FIRST STATUS LOCATION
         TM    DTEFLAGS,JALLOC+JOPEN  SEE IF JIT STARTED
         BO    STATUS02            YES, BRANCH
         MVC   0(7,R14),=C'STOPPED' SET STATUS TO STOPPED
         LA    R15,8(R15)          ADD 8 TO LENGTH
         LA    R14,8(R14)          INCREMENT TO NEXT STATUS LOCATION
         B     STATUS03            BRANCH
STATUS02 MVC   0(7,R14),=C'STARTED' SET STATUS TO STARTED
         LA    R15,8(R15)          ADD 8 TO LENGTH
         LA    R14,8(R14)          INCREMENT TO NEXT STATUS LOCATION
STATUS03 TM   DTEFLAGS,JINIT       SEE IF CONTROL BLOCKS INITIALIZED
         BNO   STATUS04            NO, BRANCH
         MVC   0(4,R14),=C'INIT'   SET STATUS TO INITIALIZED
         LA    R15,5(R15)          ADD 5 TO LENGTH
         LA    R14,5(R14)          INCREMENT TO NEXT STATUS LOCATION
STATUS04 TM   DTEFLAGS,JALLOC      SEE IF FILE ALLOCATED
         BNO   STATUS05            NO, BRANCH
         MVC   0(5,R14),=C'ALLOC'  SET STATUS TO ALLOCATED
         LA    R15,6(R15)          ADD 6 TO LENGTH
         LA    R14,6(R14)          INCREMENT TO NEXT STATUS LOCATION
STATUS05 TM   DTEFLAGS,JOPEN       SEE IF FILE OPENED
         BNO   STATUS06            NO, BRANCH
         MVC   0(6,R14),=C'OPENED'  SET STATUS TO OPENED
         LA    R15,7(R15)          ADD 7 TO LENGTH
         LA    R14,7(R14)          INCREMENT TO NEXT STATUS LOCATION
STATUS06 TM   DTEFLAGS,JACT        SEE IF ACTIVE I/O
         BNO   STATUS07            NO, BRANCH
         MVC   0(3,R14),=C'I/O'    SET STATUS TO I/O ACTIVE
         LA    R15,4(R15)          ADD 4 TO LENGTH
         LA    R14,4(R14)          INCREMENT TO NEXT STATUS LOCATION
STATUS07 TM   DTEWECB,X'80'        SEE IF SUBTASK WAITING ON ECB
         BNO   STATUS08            NO, BRANCH
         MVC   0(4,R14),=C'WAIT'   SET STATUS TO WAITING
         LA    R15,5(R15)          ADD 5 TO LENGTH
         LA    R14,5(R14)          INCREMENT TO NEXT STATUS LOCATION
         B     STATUS09            BRANCH
STATUS08 MVC   0(6,R14),=C'ACTIVE' SET STATUS TO ACTIVE
         LA    R15,7(R15)          ADD 7 TO LENGTH
         LA    R14,7(R14)          INCREMENT TO NEXT STATUS LOCATION
STATUS09 SLR   R2,R2               SET COUNTER TO ZERO
         L     R8,DTEFQUE          POINT TO FIRST JITB IN QUEUE
STATUS10 LTR   R8,R8               TEST JITB ADDRESS
         BZ    STATUS11            END OF QUEUE, BRANCH
         CLC   JITBID,=C'JITB'     MAKE SURE IT IS REALLY A JITB
         BNE   STATUS11            NO, JUST GET OUT
         LA    R2,1(R2)            ADD 1 TO COUNTER
         L     R8,JITBCH           LOAD ADDRESS OF NEXT JITB
         B     STATUS10            LOOP
STATUS11 MVC   0(6,R14),=C'QUEUE=' SET QUEUE DEPTH TITLE
         CVD   R2,DBL1             CONVERT QUEUE COUNT TO PACKED
         OI    DBL1+7,X'0F'        MAKE PRINTABLE
         UNPK  6(3,R14),DBL1+6(2)  UNPACK COUNT TO MESSAGE
         LA    R15,9(R15)          ADD 7 TO LENGTH
         LA    R14,9(R14)          INCREMENT TO NEXT STATUS LOCATION
         LR    R0,R15              SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE MESSAGE
         B     EXIT8               GET OUT
         DROP  R4                  DROP ADDRESSABILITY TO DTE
         EJECT
***********************************************************************
*                                                                     *
*        INVALID $TJIT OPERAND                                        *
*                                                                     *
***********************************************************************
         SPACE 1
BADOPRND L     R1,0(0,R5)          POINT TO INVALID OPERAND
         MVC   COMMAND(9),0(R1)    MOVE 9 CHARACTERS OF OPERAND TO MSG
         MVC   COMMAND+9(L'MSG2),MSG2 SET ERROR MESSAGE
         LA    R0,L'MSG2+9         SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         B     EXIT8               GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        JOB INFORMATION TASK NOT INITIALIZED                         *
*                                                                     *
***********************************************************************
         SPACE 1
NOJIT    MVC   COMMAND(L'MSG4),MSG4 SET ERROR MESSAGE
         LA    R0,L'MSG4           SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE THE MSG TO THE OPERATOR
         B     EXIT8               GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        INSUFFICIENT $TJIT OPERANDS                                  *
*                                                                     *
***********************************************************************
         SPACE 1
INSUFF   MVC   COMMAND(L'MSG1),MSG1 SET ERROR MESSAGE
         LA    R0,L'MSG1           SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=990    WRITE ERROR MESSAGE
         B     EXIT8               GET OUT
MSG1     DC    C'INSUFFICIENT OPERANDS'
MSG2     DC    C' INVALID OPERAND'
MSG3     DC    C'DSNAME SUCCESSFULLY MODIFIED'
MSG4     DC    C'JOB INFORMATION TASK NOT INITIALIZED'
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - TERMINATE WITHOUT ANY MESSAGE                      *
*        RC = 12 - TERMINATE ISSUING THE "OK" MESSAGE                 *
*        RC = 16 - TERMINATE USING A USER SUPPLIED MESSAGE            *
*                                                                     *
***********************************************************************
EXIT0    DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
EXIT8    DS    0H
         $FREEBUF (R9)             FREE THE WORK AREA
         LA    R15,8               SET RETURN CODE TO 8
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
         LTORG
         SPACE 3
BLANKS   DC    100C' '             STRING OF BLANKS
         EJECT
         $MODEND
         END
