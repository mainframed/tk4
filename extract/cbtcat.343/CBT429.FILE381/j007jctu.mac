         TITLE 'UPDATE JCT USER HEADER WITH JQE USER FIELDS'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS SMF EXIT PROGRAM WILL UPDATE USER FIELDS IN THE JCT     *
*        USER NETWORK JOB HEADER FROM THE USER FIELDS IN THE JQE.     *
*        THIS WILL PRESERVE THESE VALUES ACROSS NJE OR SPOOL          *
*        OFFLOAD/RELOAD PROCESSES.                                    *
*                                                                     *
*        THIS SHOULD BE THE LAST EXIT 7 ROUTINE TO INSURE THAT        *
*        ANY JQE UPDATES IN OTHER EXIT 7 ROUTINES WILL BE             *
*        REFLECTED IN THE JCT USER NETWORK JOB HEADER.                *
*                                                                     *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J 0 0 7 J C T U            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $ENTRY       SYS1.HASPSRC                               *
*        04)  $FCSUHDR     SYS1.HASPSRC (USER)                        *
*        05)  $HASPEQU     SYS1.HASPSRC                               *
*        06)  $HASPGBL     SYS1.HASPSRC                               *
*        07)  $HCT         SYS1.HASPSRC                               *
*        08)  $JCT         SYS1.HASPSRC                               *
*        09)  $JQE         SYS1.HASPSRC                               *
*        10)  $MIT         SYS1.HASPSRC                               *
*        11)  $MODEND      SYS1.HASPSRC                               *
*        12)  $MODULE      SYS1.HASPSRC                               *
*        13)  $NHD         SYS1.HASPSRC                               *
*        14)  $NHDGET      SYS1.HASPSRC                               *
*        15)  $PADDR       SYS1.HASPSRC                               *
*        16)  $PCE         SYS1.HASPSRC                               *
*        17)  $SAVE        SYS1.HASPSRC                               *
*        18)  $RETURN      SYS1.HASPSRC                               *
*        19)  IEESMCA      SYS1.AMODGEN                               *
*        20)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J007JCTU $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='UPDATE JCT USER HEADER WITH JQE USER FIELDS',    X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $NHD,               GENERATE HASP NDH DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $SMF,               GENERATE HASP SMF DSECT             X
               RPL,                GENERATE MVS  RPL DSECT             X
               SMCA                GENERATE MVS  SMCA DSECT
         EJECT
***********************************************************************
*                                                                     *
*        USER JOB HEADER                                              *
*                                                                     *
***********************************************************************
         SPACE 3
         $FCSUHDR
J007JCTU CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E007JCTU $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R10,R1              SET JCT ADDRESS
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         GET ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - A CODE PASSED TO YOUR ROUTINE BY JES2.                 *
*              0 - INDICATES THAT THE JCT HAS BEEN READ FROM SPOOL    *
*              4 - INDICATES THAT THE JCT WILL BE WRITTEN TO SPOOL    *
*        R1  - ADDRESS OF THE BUFFER THAT CONTAINS THE JCT            *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE PCE                                     *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         LTR   R0,R0               TEST ENTRY REASON CODE
         BZ    RETURN0             ZERO, READ ONLY, NOT INTERESTED
***********************************************************************
*        FIND OUR SPECIAL NJE USER HEADER                             *
***********************************************************************
         LA    R8,JCTNJHDR         POINT TO START OF NETWORK HEADERS
         USING FCSUHDR,R8          SET ADDRESSABILITY TO HEADERS
         LH    R5,0(R8)            LOAD LENGTH OF NETWORK HEADERS
         AR    R5,R8               GET END ADDRESS OF NETWORK HEADERS
         LA    R8,4(R8)            LOAD START ADDRESS OF FIRST HEADER
CKHD1    CR    R8,R5               SEE IF END OF HEADERS
         BNL   CKHD3               YES, GET OUT
         TM    FCSUHTYP,NTYPUSER   SEE IF USER HEADER
         BNO   CKHD2               NO, GO GET NEXT ONE
         CLC   FCSUHID,=CL4'FCSH'  SEE IF OUR HEADER
         BE    CKHD4               YES, GO CHECK IT
CKHD2    AH    R8,0(R8)            POINT TO NEXT HEADER
         B     CKHD1               LOOP
CKHD3    B     RETURN0             NO USER HEADER, JUST GET OUT
CKHD4    DS    0H
         EJECT
***********************************************************************
*                                                                     *
*        UPDATE THE USER FIELDS IN THE JCT USER HEADER WITH           *
*        USER FIELDS IN THE JQE.                                      *
*                                                                     *
***********************************************************************
         MVC   FCSUHOWN,JQEXOWNR   SET OWNERID
         MVC   FCSUHTHN,JQEXTHRN   SET THREAD NAME
         MVC   FCSUHEXN,JQEXEXCN   SET EXCLUDE NAME
         MVC   FCSUHRLN,JQEXRLSN   SET RELEASE NAME
         MVC   FCSUHFL1,JQEXFLG1   SET FLAG1 FIELD
         MVC   FCSUHFL2,JQEXFLG2   SET FLAG2 FIELD
         MVC   FCSUHFL3,JQEXFLG3   SET FLAG3 FIELD
         MVC   FCSUHFL4,JQEXFLG4   SET FLAG4 FIELD
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
***********************************************************************
*                                                                     *
*                   CONSTANTS USED IN THIS EXIT                       *
*                                                                     *
***********************************************************************
         SPACE 3
         LTORG
         EJECT
         $MODEND
         END
