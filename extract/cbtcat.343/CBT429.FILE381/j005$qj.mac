         TITLE 'JES2 $DJ COMMAND PROCESSOR'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED TO PROCESS THE $DJ        *
*        COMMAND WHEN ENTERED.  THIS COMMAND IS IMPLEMENTED AS        *
*        A TYPE 5 EXIT TO JES2, AND AS SUCH MUST INTERCEPT ONLY       *
*        THE $DJ COMMAND WHEN ENCOUNTERED.  THE IBM $DJ COMMAND       *
*        CAN BE EXECUTED BY EXECUTING THE $QJ COMMAND.                *
*                                                                     *
*        NOTE THAT AN EXIT 255 ROUTINE WILL BE CALLED TO PERFORM      *
*        THE ACTUAL DISPLAY OF THE REQUESTED JOBS.  THIS EXIT         *
*        WILL PROCESS THE COMMAND, PERFORM SOME PRELIMINARY           *
*        SELECTION PROCESSING, AND CALL THE EXIT WITH AN              *
*        APPROPRIATE PARM LIST.  THE PARM LIST IS MAPPED WITH         *
*        A USER MACRO $FCSFMTP.                                       *
*                                                                     *
*        THE $DJ COMMAND HAS THE FOLLOWING SYNTAX:                    *
*                                                                     *
*        $D                                                           *
*             'JOBNAME'                                               *
*             'JOBNAME                                                *
*             'JOBNAME*                                               *
*             JN-NN                                                   *
*             SN-NN                                                   *
*             TN-NN                                                   *
*                        SPOOL                                        *
*                        OWNERID                                      *
*                        RESOURCE                                     *
*                                                                     *
*        WHERE:                                                       *
*                                                                     *
*        'JOBNAME'     - LIST JOB INFORMATION FOR THIS JOBNAME        *
*                        WITHIN QUOTES                                *
*        'JOBNAME      - LIST JOB INFORMATION FOR THIS JOBNAME        *
*                        WITHIN UNPAIRED QUOTES                       *
*        'JOBNAME*     - LIST JOB INFORMATION FOR ALL JOBS            *
*                        BEGINNING WITH THESE CHARACTERS              *
*        JN-NN         - LIST JOB INFORMATION FOR THE SPECIFIED       *
*                        BATCH JOB(S)                                 *
*        SN-NN         - LIST JOB INFORMATION FOR THE SPECIFIED       *
*                        STARTED TASK(S)                              *
*        TN-NN         - LIST JOB INFORMATION FOR THE SPECIFIED       *
*                        TSO USER(S)                                  *
*        SPOOL         - DISPLAY THE SPOOL VOLUMES USED BY EACH       *
*                        OF THE REQUESTED JOB(S)                      *
*        OWNERID       - DISPLAY THE OWNERID OF EACH OF THE           *
*                        REQUESTED JOB(S)                             *
*        RESOURCE      - DISPLAY THE ESOTERIC RESOURCES ATTACHED      *
*                        TO EACH OF THE REQUESTED JOB(S)              *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        THE FOLLOWING RESPONSES WILL BE MADE BY THIS EXIT:           *
*                                                                     *
*        1)  THE HASP908 MESSAGE WILL BE ISSUED FOR EACH JOB          *
*            WHICH IS DISPLAYED.                                      *
*                                                                     *
*        $HASP908 JOBNAME  EXECUTING C PRIO XX SSSS                   *
*                                                                     *
*        $HASP908 JOBNAME  ON PRINTRXX PRIO XX SSSS                   *
*                                                                     *
*        $HASP908 JOBNAME  WAIT EXEC X PRIO XX SSSS                   *
*                                                                     *
*        2)  THE HASP900 MESSAGE WILL BE ISSUED WHEN NONE             *
*            OF THE REQUESTED JOBS CANNOT BE FOUND.                   *
*                                                                     *
*        $HASP900 JOB(S) NOT FOUND                                    *
*                                                                     *
*        3)  THE HASP999 MESSAGE WILL BE ISSUED FOR ALL               *
*            ERRORS.                                                  *
*                                                                     *
*        $HASP999 XXXXXXXXX INVALID OPERAND                           *
*                                                                     *
*        THE JOBNAME/JOB NUMBER CONTAINS INVALID SYNTAX.              *
*                                                                     *
*        $HASP999 LIST INCOMPLETE                                     *
*                                                                     *
*        DURING WTO PROCESSING A BUFFER SHORTAGE WAS DETECTED, SO     *
*        $DJ PROCESSING WILL BE TERMINATED TO PREVENT FURTHER         *
*        BUFFER SHORTAGES.                                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*              M A C R O S   U S E D   I N   J 0 0 5 $ D J            *
*                                                                     *
*        01)  $AMODE       SYS1.HASPSRC                               *
*        02)  $BUFFER      SYS1.HASPSRC                               *
*        03)  $CAT         SYS1.HASPSRC                               *
*        04)  $CMB         SYS1.HASPSRC                               *
*        05)  $COMWORK     SYS1.HASPSRC                               *
*        06)  $CWA         SYS1.HASPSRC                               *
*        07)  $CWTO        SYS1.HASPSRC                               *
*        08)  $DAS         SYS1.HASPSRC                               *
*        09)  $DCT         SYS1.HASPSRC                               *
*        10)  $ENTRY       SYS1.HASPSRC                               *
*        11)  $EXIT        SYS1.HASPSRC                               *
*        12)  $EXITPL      SYS1.HASPSRC                               *
*        13)  $FCSFMTP     SYS1.HASPSRC (USER)                        *
*        14)  $FREEBUF     SYS1.HASPSRC                               *
*        15)  $GETBUF      SYS1.HASPSRC                               *
*        16)  $HASPEQU     SYS1.HASPSRC                               *
*        17)  $HASPGBL     SYS1.HASPSRC                               *
*        18)  $HCT         SYS1.HASPSRC                               *
*        19)  $IOT         SYS1.HASPSRC                               *
*        20)  $JCT         SYS1.HASPSRC                               *
*        21)  $JOE         SYS1.HASPSRC                               *
*        22)  $JQE         SYS1.HASPSRC                               *
*        23)  $LCK         SYS1.HASPSRC                               *
*        24)  $MIT         SYS1.HASPSRC                               *
*        25)  $MODEND      SYS1.HASPSRC                               *
*        26)  $MODULE      SYS1.HASPSRC                               *
*        27)  $PADDR       SYS1.HASPSRC                               *
*        28)  $PCE         SYS1.HASPSRC                               *
*        29)  $QLOC        SYS1.HASPSRC                               *
*        20)  $QSE         SYS1.HASPSRC                               *
*        31)  $RETURN      SYS1.HASPSRC                               *
*        32)  $SAVE        SYS1.HASPSRC                               *
*        33)  $TAB         SYS1.HASPSRC                               *
*        34)  $XIT         SYS1.HASPSRC                               *
*        35)  IAZRESPA     SYS1.AMODGEN                               *
*        36)  IFGRPL       SYS1.MACLIB                                *
*        37)  MODESET      SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       4K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J005$QJ  $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='JES2 $DJ COMMAND PROCESSOR',                     X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $CMB,               GENERATE HASP CMB DSECT             X
               $COMWORK,           GENERATE HASP COMWORK DSECT         X
               $CWA,               GENERATE HASP CWA DSECT             X
               $DAS,               GENERATE HASP DAS DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $EXITPL,            GENERATE HASP EXITPL DSECT          X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP HCT DSECT             X
               $IOT,               GENERATE HASP IOT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JOE,               GENERATE HASP JOE DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $LCK,               GENERATE HASP LCK DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $PDDB,              GENERATE HASP PDDB DSECT            X
               $QSE,               GENERATE HASP QSE DSECT             X
               $TAB,               GENERATE HASP TAB DSECT             X
               $XIT,               GENERATE HASP XIT DSECT             X
               RESPA,              GENERATE MVS  RESPA DSECT           X
               RPL                 GENERATE MVS  IFGRPL DSECT
         EJECT
***********************************************************************
*                                                                     *
*        PROGRAM WORK AREAS                                           *
*                                                                     *
***********************************************************************
$DJWORK  DSECT
         DS    CL(BUFSTART-BFPDSECT) **** RESERVED BY JES2 ****
         $FCSFMTP ,                MAP THE EXIT 255 PARM LIST
DOUBLE   DS    D                   DOUBLE WORD WORK AREA
DBL      DS    D                   DOUBLE WORD WORK AREA
ROUTCDE  DS    F                   DESTINATION CODE FOR SETDEST ROUTINE
SAVEBAS  DS    CL18                MSG SAVE AREA FOR JOB NBR AND NAME
LTEXT    DS    H                   CURRENT LENGTH OF MESSAGE AREA
RC       DS    H                   $CWTO RETURN CODE
LOWJOB   DS    H                   LOW RANGE JOB REQUESTED
HIGHJOB  DS    H                   HIGH RANGE JOB REQUESTED
JOBNAME  DS    CL8                 JOBNAME REQUESTED
JOBNAMEL DS    H                   LENGTH OF JOBNAME REQUESTED
MISCSW1  DS    X                   MISCELLANEOUS SWITCHES
NAME     EQU   X'80'               A JOBNAME WAS ENTERED - NOT NUMBER
FOUND    EQU   X'40'               A JOB WAS FOUND FOR PROCESSING
SPOOLS   EQU   X'20'               SPOOL VOLUME INFORMATION REQUESTED
VOLHEAD  EQU   X'10'               SPOOL VOLUME HEADER WRITTEN IND
MISCSW2  DS    X                   MISCELLANEOUS SWITCHES
JOEFND   EQU   X'80'               AN ACTIVE JOE FOUND FOR THIS JOB
LASTYPE  DS    C                   LAST JOB TYPE PROCESSED
DEVNO    DS    H                   NUMBER OF ACTIVE DEVICES
LASTRNG  DS    F                   ADDRESS OF LAST RANGE ENTRY
RANGES   DS    H                   NUMBER OF JOB RANGES ENTERED
***********************************************************************
*                                                                     *
*       THE JOB RANGE ENTRY TABLE HAS A FORMAT AS FOLLOWS:            *
*                                                                     *
*       1 BYTE   JOB TYPE (J/S/T)                                     *
*       1 BYTE   FILLER                                               *
*       HALFWORD LOW  JOB NUMBER IN RANGE                             *
*       HALFWORD HIGH JOB NUMBER IN RANGE                             *
*                                                                     *
***********************************************************************
RANGETBL DS    51CL6               ROOM FOR 50 RANGES + END OF LIST
SAVEAFF  DS    16F                 REGISTER SAVE FOR SETAFF  ROUTINE
SAVESPL  DS    16F                 REGISTER SAVE FOR SETSPL  ROUTINE
SAVEBLD  DS    16F                 REGISTER SAVE FOR BLDACT  ROUTINE
SAVEPUT  DS    16F                 REGISTER SAVE FOR BLDPUT  ROUTINE
DEVTBL   DS    50XL4               ACTIVE DEVICE TABLE
         SPACE 3
J005$DJ  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         TITLE 'JES2 $DJ COMMAND PROCESSOR'
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E005$QJ  $ENTRY BASE=(R12)
         SPACE 1
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD FIRST BASE REGISTER
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R5       - ADDRESS OF THE CURRENT OPERAND                    *
*        R6       - INCREMENT VALUE OF 4                              *
*        R7       - ADDRESS OF THE LAST OPERAND                       *
*        R11      - ADDRESS OF THE HCT                                *
*        R13      - ADDRESS OF THE HASPCOMM PCE                       *
*        R14      - RETURN ADDRESS                                    *
*        R15      - ENTRY ADDRESS                                     *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        CONVERT $QJ, $QS, $QT, AND $Q" COMMANDS TO $D. AND           *
*        PASS IT ALONG TO THE IBM COMMAND PROCESSOR.                  *
*                                                                     *
***********************************************************************
         CLI   COMVERB,C'Q'        SEE IF THIS IS THE $DJ COMMAND
         BNE   CKDISPL             NO, GO SEE IF DISPLAY COMMAND
         CLI   COMVERB+1,C'J'      SEE IF $QJ
         BE    MODIFY              YES, GO CHANGE TO $DJ
         CLI   COMVERB+1,C'S'      SEE IF $QS
         BE    MODIFY              YES, GO CHANGE TO $DS
         CLI   COMVERB+1,C'T'      SEE IF $QS
         BE    MODIFY              YES, GO CHANGE TO $DT
         CLI   COMVERB+1,C''''     SEE IF $Q"
         BE    MODIFY              YES, GO CHANGE TO $DT
         B     EXIT0               NO,  GET OUT
MODIFY   MVI   COMVERB,C'D'        CHANGE TO DISPLAY COMMAND
         B     EXIT0               GET OUT
         SPACE 3
***********************************************************************
*                                                                     *
*        SEE IF THIS IS A $DJ COMMAND                                 *
*                                                                     *
***********************************************************************
CKDISPL  CLI   COMVERB,C'D'        SEE IF THIS IS THE $DJ COMMAND
         BNE   EXIT0               NO, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(8),=C'JOBCLASS' SEE IF DISPLAY JOBCLASS
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(6),=C'JOBDEF' SEE IF DISPLAY JOBDEF
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(7),=C'JOBPRTY' SEE IF DISPLAY JOBPRTY
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(8),=C'SPOOLDEF' SEE IF DISPLAY SPOOLDEF
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(6),=C'SMFDEF' SEE IF DISPLAY SMFDEF
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(8),=C'STCCLASS' SEE IF DISPLAY STCCLASS
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(5),=C'TPDEF' SEE IF DISPLAY TPDEF
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(8),=C'TSUCLASS' SEE IF DISPLAY TSUCLASS
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(5),=C'SPOOL' SEE IF DISPLAY SPOOL
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLC   COMVERB+1(3),=C'SPL'   SEE IF DISPLAY SPOOL
         BE    EXIT0               YES, BRANCH TO COMMAND OK EXIT
         CLI   COMVERB+1,C'J'      SEE IF BATCH JOBS REQUESTED
         BE    DJOK                YES, BRANCH
         CLI   COMVERB+1,C'T'      SEE IF TSO USERS REQUESTED
         BE    DJOK                YES, BRANCH
         CLI   COMVERB+1,C'S'      SEE IF STARTED TASKS REQUESTED
         BE    DJOK                YES, BRANCH
         CLI   COMVERB+1,C''''     SEE IF JOB NAME ENTERED
         BE    DJOK                YES, BRANCH
         B     EXIT0               NO, LET JES TAKE CARE OF IT
         EJECT
***********************************************************************
*                                                                     *
*        GET A JES2 BUFFER FOR REENTRANCY AND                         *
*        CLEAR THE COMMAND WORK AREA                                  *
*                                                                     *
***********************************************************************
         SPACE 1
DJOK     $GETBUF TYPE=HASP,WAIT=YES GET A WORK AREA
         LR    R8,R1               SAVE WORK AREA ADDRESS
         USING $DJWORK,R8          SET ADDRESSABILITY TO WORK AREA
         $GETBUF TYPE=HASP,WAIT=YES GET A WORK AREA FOR EXIT 255
         LR    R10,R1              SAVE WORK AREA ADDRESS FOR EXIT
         LA    R14,$DJWORK+(BUFSTART-BFPDSECT) SET 'TO' ADDRESS
         LH    R15,$BUFSIZE        SET   'TO'   LENGTH
         SLR   R0,R0               CLEAR 'FROM' ADDRESS
         SLR   R1,R1               CLEAR 'FROM' LENGTH
         MVCL  R14,R0              CLEAR THE BUFFER
*****************************************************************
*                                                               *
*        INITIALIZE PARM LIST FOR EXIT 225 ROUTINE              *
*                                                               *
*****************************************************************
         MVI   QBITS,QCNV+QXEQ+QOUT+QPPU+QXMT+QPUR ALL QUEUES
         MVI   QBITS+1,QACT+QDEV ACTIVE AND DEVICE ACTIVITY
         MVI   QMISC+L'SYSALL,SYSALL ALL SYSTEMS
         MVI   QFILT1,0            NO SPECIAL FILTERING
         MVI   QFILT2,0            NO SPECIAL FILTERING
         MVI   QCLASS,0            ZERO THE XEQ/CNV CLASS
         XC    OWNERID,OWNERID     NO OWNERID SELECTION
*****************************************************************
*                                                               *
*        CHECK FOR AUTOMATIC REDIRECTION OF RESPONSES           *
*                                                               *
*****************************************************************
         SPACE 1
         TM    COMFLAG,CMBFLAGU    IS THIS A UCM
         BZ    MSGGOOD             NO, SKIP REDIRECTION
         TM    FBLFLG,FBLFLGC+FBLFLGR CONSOLE SET OR REMOTE SYSTEM
         BNZ   MSGRPT              SKIP AUTOMATIC L=CCA
         LA    R1,4                LOAD REDIRECT OFFSET FOR $DJ
*                                  SAME AS FOR $DJ
         BCTR  R1,0                REDUCE BY 1
         SLL   R1,1                DOUBLE
         SLR   R2,R2               ZERO REGISTER FOR IC
         IC    R2,COMUCM           PICK UP CURRENT UCM ID
         L     R14,$COMEXTN        POINT TO CONSOLE WORK AREA
         USING CWA,R14             SET ADDRESSABILITY TO CWA
         CH    R2,CWAOCON          SEE IF EXCEEDS MAXIMUM
         BH    MSGRPT              YES, ERROR, NO AUTOMATIC REDIRECTION
         MH    R2,CWALCON          MULTIPLY BY LENGTH OF EACH ENTRY
         AR    R1,R2               COMBINE OFFSETS
         LA    R1,CWARESP-CWACONL(R1) POINT TO CORRECT CWA ENTRY
         MVC   COMUCM,CWACON(R1)   INSERT NEW CONSOLE ID
         TM    FBLFLG,FBLFLGA      HAS AREA BEEN SET
         BO    MSGRPT              YES, SKIP AUTOMATIC REDIRECTION
         MVC   COMUCMA,CWACONA(R1) SET NEW AREA
         DROP  R14                 DROP ADDRESSABILITY TO CWA
MSGRPT   BAL   R4,MSGVCCA          VERIFY CCA (NO RETURN IF BAD)
         CLI   COMUCMA,0           TEST FOR DISPLAY AREA
         BZ    MSGGOOD             IF NOT, SKIP
         MVI   COMLINET,X'80'      SET CONTROL LINE
         $CWTO L=L'CMBMSG,MSGID=908 ECHO THE COMMAND
         MVI   COMLINET,X'20'      SET DATA LINE
***********************************************************************
*                                                                     *
*        EXTRACT THE JOB/TSU/STC RANGE FROM THE COMMAND BUFFER        *
*                                                                     *
***********************************************************************
MSGGOOD  CLI   COMVERB+1,C''''     SEE IF JOBNAME ENTERED
         BNE   SETRANGE            NO, BRANCH
QQENT    OI    MISCSW1,NAME        SET NAME FLAG
         MVC   LOWJOB,=H'1'        SET LOW JOB NUMBER TO 1
         MVC   HIGHJOB,$NUMJBNO    SET HIGH JOB NUMBER
         B     SETNAME             AND GO PROCESS NAME
***********************************************************************
*        SEARCH FOR END OF FIRST JOB NUMBER IN RANGE                  *
***********************************************************************
SETRANGE MVC   LASTYPE,COMVERB+1   SAVE LAST JOB TYPE
         LA    R1,COMVERB+1        POINT TO START OF JOB NUMBER
         LA    R4,RANGETBL         POINT TO JOB RANGE TABLE
SETRNG01 LR    R14,R1              POINT TO OPERAND
         LR    R2,R1               SAVE START OF OPERAND ADDRESS
         SLR   R15,R15             SET LENGTH
         CLI   0(R14),C'0'         SEE IF NUMERIC
         BNL   SETRNG04            YES, MUST BE JOB NUMBER
         CLC   0(3,R14),=C'JOB'    SEE IF JOB
         BE    SETRNG03            YES, BRANCH
         CLC   0(3,R14),=C'STC'    SEE IF STC
         BE    SETRNG03            YES, BRANCH
         CLC   0(3,R14),=C'TSU'    SEE IF TSO
         BE    SETRNG03            YES, BRANCH
         CLI   0(R14),C'J'         SEE IF JOB
         BE    SETRNG02            YES, BRANCH
         CLI   0(R14),C'S'         SEE IF STC
         BE    SETRNG02            YES, BRANCH
         CLI   0(R14),C'T'         SEE IF TSO
         BNE   CKOPND1             NO, MUST BE A KEYWORD OPERAND
SETRNG02 MVC   LASTYPE,0(R1)       SAVE JOB TYPE
         CLI   1(R14),C'0'         SEE IF NEXT CHAR NUMERIC
         BL    CKOPND1             NO, MUST BE A KEYWORD OPERAND
         LA    R14,1(R14)          INCREMENT PAST JOB TYPE
         LR    R2,R14              SAVE START OF OPERAND ADDRESS
         B     SETRNG04            GO PROCESS NUMERIC PORTION
SETRNG03 MVC   LASTYPE,0(R1)       SAVE JOB TYPE
         CLI   3(R14),C'0'         SEE IF NEXT CHAR NUMERIC
         BL    CKOPND1             NO, MUST BE A KEYWORD OPERAND
         LA    R14,3(R14)          INCREMENT PAST JOB TYPE
         LR    R2,R14              SAVE START OF OPERAND ADDRESS
SETRNG04 CLI   0(R14),C' '         END OF OPERAND
         BE    SETRNG05            YES, BRANCH
         CLI   0(R14),C'-'         END OF OPERAND
         BE    SETRNG05            YES, BRANCH
         CLI   0(R14),C','         END OF OPERAND
         BE    SETRNG05            YES, BRANCH
         CLI   0(R14),C'0'         SEE IF NUMERIC
         BL    BADOPRND            NO, ERROR
         CLI   0(R14),C'9'         SEE IF NUMERIC
         BH    BADOPRND            NO, ERROR
         LA    R15,1(R15)          ADD 1 TO LENGTH
         LA    R14,1(R14)          INCREMENT TO NEXT CHAR OF JOB NBR
         B     SETRNG04            LOOP
SETRNG05 LTR   R15,R15             TEST LENGTH
         BZ    BADOPRND            ZERO, ERROR
         CH    R15,=H'6'           TEST LENGTH
         BH    BADOPRND            GREATER THAN 6, ERROR
         BCTR  R15,0               MAKE MACHINE LENGTH
         EX    R15,PKJOBNO         PACK JOB NUMBER
         CVB   R15,DOUBLE          CONVERT TO BINARY
         LTR   R15,R15             SEE IF JOB NUMBER IS ZERO
         BZ    BADOPRND            YES, ERROR
         CH    R15,=H'9999'        SEE IF EXCEEDS MAXIMUM JOB NUMBER
         BH    BADOPRND            YES, ERROR
         STH   R15,LOWJOB          SAVE AS LOW JOB NUMBER
         STH   R15,HIGHJOB         AND AS HIGH JOB NUMBER
         CLI   0(R14),C'-'         SEE IF JOB RANGE REQUESTED
         BNE   SETRNG08            NO, GO SEE IF OTHER JOB RANGES
         LA    R14,1(R14)          INCREMENT PAST '-'
         LR    R2,R14              SAVE START OF OPERAND ADDRESS
         XR    R15,R15             SET LENGTH
***********************************************************************
*        SEARCH FOR END OF SECOND JOB NUMBER IN RANGE                 *
***********************************************************************
SETRNG06 CLI   0(R14),C' '         END OF OPERAND
         BE    SETRNG07            YES, BRANCH
         CLI   0(R14),C','         END OF OPERAND
         BE    SETRNG07            YES, BRANCH
         CLI   0(R14),C'0'         SEE IF NUMERIC
         BL    BADOPRND            NO, ERROR
         CLI   0(R14),C'9'         SEE IF NUMERIC
         BH    BADOPRND            NO, ERROR
         LA    R15,1(R15)          ADD 1 TO LENGTH
         LA    R14,1(R14)          INCREMENT TO NEXT CHAR OF JOB NBR
         B     SETRNG06            LOOP
SETRNG07 LTR   R15,R15             TEST LENGTH
         BZ    BADOPRND            ZERO, ERROR
         CH    R15,=H'6'           TEST LENGTH
         BH    BADOPRND            GREATER THAN 6, ERROR
         BCTR  R15,0               MAKE MACHINE LENGTH
         EX    R15,PKJOBNO         PACK JOB NUMBER
         CVB   R15,DOUBLE          CONVERT TO BINARY
         LTR   R15,R15             SEE IF JOB NUMBER IS ZERO
         BZ    BADOPRND            YES, ERROR
         CH    R15,=H'9999'        SEE IF EXCEEDS MAXIMUM JOB NUMBER
         BH    BADOPRND            YES, ERROR
         STH   R15,HIGHJOB         AND AS HIGH JOB NUMBER
SETRNG08 LH    R15,RANGES          LOAD NUMBER OF RANGES
         LA    R15,1(R15)          ADD 1 TO NUMBER OF RANGES
         CH    R15,=H'50'          SEE IF MAX EXCEEDED
         BNH   SETRNG09            NO, CONTINUE
         MVC   COMMAND(L'MSG1),MSG1 SET MAXIMUM RANGE EXCEEDED MSG
         LA    R0,L'MSG1           SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=999    WRITE THE MSG TO THE OPERATOR
         B     EXIT8               GET OUT
SETRNG09 STH   R15,RANGES          SAVE NEW NUMBER OF RANGES
         MVC   0(1,R4),LASTYPE     SET JOB TYPE IN TABLE
         MVC   2(2,R4),LOWJOB      SET LOW JOB NUMBER IN TABLE
         MVC   4(2,R4),HIGHJOB     SET HIGH JOB NUMBER IN TABLE
         LA    R4,6(R4)            INCREMENT TO NEXT RANGE TABLE ENTRY
         MVI   0(R4),X'FF'         FLAG END OF LIST
         BXH   R5,R6,JQESCAN       IF NO MORE EXIT
         L     R1,0(R5)            POINT TO OPERAND
         B     SETRNG01            PROCESS THIS OPERAND
PKJOBNO  PACK  DOUBLE,0(0,R2)      *** EXECUTE ONLY ***
         SPACE 3
***********************************************************************
*        PROCESS JOB NAME                                             *
***********************************************************************
SETNAME  LA    R14,COMVERB+2       POINT TO START OF JOB NAME
         LA    R1,COMVERB+2        POINT TO OPERAND
         LR    R2,R1               SAVE START OF OPERAND ADDRESS
         XR    R15,R15             SET LENGTH TO ZERO
SETNAME1 CLI   0(R14),C' '         END OF JOBNAME
         BE    SETNAME2            YES, BRANCH
         CLI   0(R14),C','         END OF JOBNAME
         BE    SETNAME2            YES, BRANCH
         CLI   0(R14),C''''        END OF JOBNAME
         BE    SETNAME2            YES, BRANCH
         LA    R15,1(R15)          ADD 1 TO LENGTH COUNTER
         LA    R14,1(R14)          INCREMENT TO NEXT CHAR OF JOBNAME
         B     SETNAME1            LOOP
SETNAME2 LTR   R15,R15             TEST NAME LENGTH
         BZ    BADOPRND            ZERO, BAD OPERAND
         CH    R15,=H'8'           SEE IF LENGTH GT 8
         BH    BADOPRND            YES, BAD OPERAND
         MVC   JOBNAME,BLANKS      BLANK THE JOB NAME
         BCTR  R15,0               MAKE MACHINE LENGTH
         EX    R15,SAVEJOBN        SAVE JOB NAME
         MVC   JOBNAMEL,=H'8'      SET JOB NAME LENGTH TO 8
         LA    R14,COMVERB+2       POINT TO START OF JOB NAME
         XR    R15,R15             SET LENGTH TO ZERO
***********************************************************************
*        SET LENGTH TO OTHER THAN 8 FOR GENERIC JOBNAME SEARCH        *
***********************************************************************
SETNAME3 CLI   0(R14),C'*'         GENERIC JOB NAME
         BE    SETNAME4            YES, BRANCH
         CLI   0(R14),C' '         END OF JOBNAME
         BE    SETNAME5            YES, BRANCH
         CLI   0(R14),C','         END OF JOBNAME
         BE    SETNAME5            YES, BRANCH
         CLI   0(R14),C''''        END OF JOBNAME
         BE    SETNAME5            YES, BRANCH
         LA    R15,1(R15)          ADD 1 TO LENGTH COUNTER
         LA    R14,1(R14)          INCREMENT TO NEXT CHAR OF JOBNAME
         B     SETNAME3            LOOP
SETNAME4 LTR   R15,R15             TEST NAME LENGTH
         BZ    BADOPRND            ZERO, BAD OPERAND
         BCTR  R15,0               MAKE MACHINE LENGTH
         STH   R15,JOBNAMEL        SAVE JOB NAME LENGTH
SETNAME5 B     CKOPND              GO CHECK FOR OPERANDS
SAVEJOBN MVC   JOBNAME(0),0(R1)    *** EXECUTE ONLY ***
         EJECT
***********************************************************************
*                                                                     *
*        EXAMINE THE COMMAND OPERANDS                                 *
*                                                                     *
***********************************************************************
CKOPND   BXH   R5,R6,JQESCAN       IF NO MORE EXIT
         L     R1,0(R5)            POINT TO OPERAND
CKOPND1  CLC   0(5,R1),=C'SPOOL'   SEE IF SPOOL VOLUME INFO REQUESTED
         BE    DOSPOOLS            YES, BRANCH
CKOPND2  CLC   0(5,R1),=C'OWNER'   SEE IF OWNERID INFO REQUESTED
         BE    DOOWNER             YES, BRANCH
CKOPND3  CLC   0(8,R1),=C'RESOURCE' SEE IF RESOURCE INFO REQUESTED
         BE    DORES               YES, BRANCH
         B     BADOPRND            UNKNOWN OPERAND, ERROR
***********************************************************************
*                                                                     *
*        TURN ON THE SPOOL INFORMATION REQUESTED INDICATOR            *
*                                                                     *
***********************************************************************
         SPACE 1
DOSPOOLS OI    QMISC+L'SPOOL,SPOOL SPOOL VOLUME INFORMATION REQUESTED
         B     CKOPND              LOOP FOR NEXT OPERAND
***********************************************************************
*                                                                     *
*        TURN ON THE OWNERID INFORMATION REQUESTED INDICATOR          *
*                                                                     *
***********************************************************************
         SPACE 1
DOOWNER  OI    QMISC+L'OWNER,OWNER OWNERID INFORMATION REQUESTED
         B     CKOPND              LOOP FOR NEXT OPERAND
***********************************************************************
*                                                                     *
*        TURN ON THE RESOURCE INFORMATION REQUESTED INDICATOR         *
*                                                                     *
***********************************************************************
         SPACE 1
DORES    OI    QMISC+L'RESOURCE,RESOURCE RESOURCE INFO REQUESTED
         B     CKOPND              LOOP FOR NEXT OPERAND
         EJECT
***********************************************************************
*                                                                     *
*        SCAN THE JOB QUEUE FOR THE JOB LIST                          *
*                                                                     *
***********************************************************************
         SPACE 1
JQESCAN  TM    MISCSW1,NAME        SEE IF SEARCHING BY JOBNAME
         BO    JQESCAN1            YES, BRANCH
         LA    R1,RANGETBL         LOAD ADDRESS OF FIRST RANGE ENTRY
         ST    R1,LASTRNG          SAVE ADDRESS OF THIS RANGE ENTRY
         MVC   LASTYPE,0(R1)       SAVE JOB TYPE
         MVC   LOWJOB,2(R1)        SAVE LOW JOB
         MVC   HIGHJOB,4(R1)       SAVE HIGH JOB
JQESCAN1 LH    R1,LOWJOB           LOAD JOB NUMBER
         $QLOC (R1)                GET JQE FOR THIS JOB NUMBER
         BZ    NEXTJQE             GET NEXT JQE IF NOT FOUND
         LR    R9,R1               SAVE JQE ADDRESS
         USING JQE,R9              SET ADDRESSABILITY TO JQE
         TM    MISCSW1,NAME        SEE IF SEARCHING BY JOBNAME
         BZ    CKJOBNO             NO, BRANCH
         LH    R15,JOBNAMEL        LOAD JOB NAME LENGTH
         EX    R15,CLCJNAME        SEE IF THIS IS OUR JOB
         BE    JQEFND              YES, BRANCH
         B     NEXTJQE             NO, BYPASS THIS JOB
CLCJNAME CLC   JQEJNAME(0),JOBNAME *** EXECUTE ONLY ***
CKJOBNO  TM    JQEFLAG3,JQE3JOB    IS THIS A BATCH JOB
         BNZ   CKSTC               NO, SEE IF STARTED TASK
         CLI   LASTYPE,C'J'        ARE WE LOOKING FOR A BATCH JOB
         BE    JQEFND              YES, GO PROCESS IT
         B     NEXTJQE             NO, GET NEXT JQE
CKSTC    TM    JQEFLAG3,JQE3STC    IS THIS A STC
         BNO   CKTSU               NO, SEE IF A TSO USER
         CLI   LASTYPE,C'S'        ARE WE LOOKING FOR A STARTED TASK
         BE    JQEFND              YES, GO PROCESS IT
         B     NEXTJQE             NO, GET NEXT JQE
CKTSU    CLI   LASTYPE,C'T'        ARE WE LOOKING FOR A TSO USER
         BE    JQEFND              YES, GO PROCESS IT
NEXTJQE  LH    R1,LOWJOB           LOAD PREVIOUS JOB NUMBER
         LA    R1,1(R1)            ADD 1 TO JOB NUMBER
         STH   R1,LOWJOB           SAVE NEW JOB NUMBER
         CH    R1,HIGHJOB          SEE IF END OF RANGE
         BNH   JQESCAN1            NO, LOOP
         TM    MISCSW1,NAME        SEE IF SEARCHING BY JOBNAME
         BO    NEXTJQE1            YES, END OF PROCESSING
         L     R1,LASTRNG          LOAD ADDRESS OF LAST RANGE ENTRY
         LA    R1,6(R1)            INCREMENT TO NEXT ENTRY
         ST    R1,LASTRNG          AND SAVE ADDRESS OF THIS ENTRY
         CLI   0(R1),X'FF'         SEE IF END OF RANGES
         BE    NEXTJQE1            YES, END OF PROCESSING
         MVC   LASTYPE,0(R1)       SAVE JOB TYPE
         MVC   LOWJOB,2(R1)        SAVE LOW JOB
         MVC   HIGHJOB,4(R1)       SAVE HIGH JOB
         B     JQESCAN1            GO PROCESS THIS RANGE
NEXTJQE1 TM    MISCSW1,FOUND       SEE IF ANY JOBS FOUND
         BO    EXIT8               YES, GET OUT
         MVC   COMMAND(L'MSG2),MSG2 SET NO JOBS FOUND MESSAGE
         LA    R0,L'MSG2           SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=900    WRITE THE MSG TO THE OPERATOR
         B     EXIT8               GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        CALL EXIT 255 TO FORMAT THE JOB INFORMATION                  *
*                                                                     *
***********************************************************************
JQEFND   OI    MISCSW1,FOUND       TURN ON JOB FOUND INDICATOR
         LA    R1,QBITS            POINT TO EXIT PARM LIST
EXIT255  $EXIT 255,ENVIRON=JES2,MAXRC=8 CALL FORMAT ROUTINE
         CH    R15,=H'8'           TEST FOR CONSOLE BUFFER SHORTAGE
         BNE   NEXTJQE             NO, CONTINUE
         B     EXIT8               YES, GET OUT
         EJECT
***********************************************************************
*                                                                     *
*              SUBROUTINE TO VERIFY CONSOLE/AREA                      *
*                                                                     *
***********************************************************************
         SPACE 1
MSGVCCA  TM    FBLFLG,FBLFLGR      SEE IF REMOTE SYSTEM UCM
         BOR   R4                  YES, UCM AND AREA ARE OK
         L     R2,$COMEXTN         POINT TO CONSOLE WORK AREA
         USING CWA,R2              SET ADDRESSABILITY TO CWA
         MVC   XASCID(1),COMUCM    SET UCM ID
         MVC   XASDID(1),COMUCMA   SET AREA ID
         CLI   XASDID,0            SEE IF AREA ID SPECIFIED
         BNZ   MSGSSAOP            YES, BRANCH
         MVI   XASDID,C'Z'         NO,  FORCE AREA Z
MSGSSAOP MVC   XAU(1),COMUCM       TELL OS SOURCE IS SAME AS OUTPUT
         MVI   XAN,X'22'           SET ID FOR VERIFIER
         MODESET EXTKEY=ZERO       GET KEY 0
         L     R15,CWA7603D        POINT TO OS VERIFICATION ROUTINE
         $AMODE 24,PUSHR=0,R=R14   GO TO AMODE 24
         BALR  R14,R15             ENTER IT
         $AMODE POPR=0,R=R14       RETURN TO ORIGINAL AMODE
         MODESET EXTKEY=HASP       RETURN TO KEY 1
         CH    R15,=H'4'           CHECK FOR SERIOUS ERROR
         BNHR  R4                  NO, RETURN TO CALLER
         SLR   R3,R3               CLEAR REGISTER FOR ICM
         ICM   R3,B'0011',COMUCM   PICK UP UCM AND AREA ID
         MVI   COMUCMA,0           FORCE NO DISPLAY AREA
         CH    R15,=H'12'          SEE IF CONSOLE DOWN
         BNE   *+16                NO, DO NOT SET LOGICAL CONSOLE
         MVC   COMFLAG(4),MOWTOL   DESTROY APPARENT SOURCE INFO
         MVC   COMOUT,MOWTOL+(COMOUT-COMFLAG) OMITTING CNTL FIELD
         $CWTO L=L'CMBMSG,MSGID=908 ECHO THE COMMAND
         MVC   COMMAND(MBVINVL),MBVINV SET ERROR MESSAGE TEXT
         STC   R3,MBVINVA          SET AREA
         SRL   R3,8                ALIGN CONSOLE ID
         CVD   R3,$DOUBLE          CONVERT TO PACKED
         MVC   DBL(4),=X'F0212020' SET EDIT MASK
         ED    DBL(4),$DOUBLE+6    EDIT CONSOLE ID TO AREA
         MVC   MBVINVC,DBL+2       MOVE CONSOLE ID TO MESSAGE
         MVC   COMMAND+MBVINVL(8),COMACEID MOVE IDENTIFIER
         $CWTO L=MBVINVL,MSGID=999 WRITE ERROR MESSAGE TO CONSOLE
         B     EXIT8               GET OUT
         DROP  R2                  DROP ADDRESSABILITY TO CWA
         EJECT
**********************************************************************
*                                                                    *
*       R E D I R E C T   R O U T I N E   C O N S T A N T S          *
*                                                                    *
**********************************************************************
         SPACE 1
MOWTOL   $WTO  PRI=$HI,CLASS=$ALWAYS,JOB=NO,ROUTE=X'C000',MF=LX
MBVINV   DC    C'RESPONSE LOCATION L=CCA UNAVAILABLE '
MBVINVL  EQU   *-MBVINV
MBVINVC  EQU   COMMAND+MBVINVL-16,2
MBVINVA  EQU   MBVINVC+2,1
FBLFLG   EQU   COMEWORK+2          FLAG BYTE
COMJMBR  EQU   COMJRMT             SHARED SPOOL MEMBER NUMBER
FBLFLGR  EQU   1                   UCM COMMAND FROM REMOTE SYSTEM
FBLFLGC  EQU   2                   CONSOLE HAS BEEN SPECIFIED
FBLFLGA  EQU   4                   AREA HAS BEEN SPECIFIED
         EJECT
***********************************************************************
*        CONSTANTS FOR $DJ                                            *
***********************************************************************
         SPACE 3
BLANKS   DC    CL20' '             STRING OF 20 BLANKS
         SPACE 3
MSG1     DC    C'MORE THAN 50 JOBS OR RANGES SPECIFIED'
MSG2     DC    CL16'JOB(S) NOT FOUND'
MSG3     DC    C' INVALID OPERAND'
         EJECT
***********************************************************************
*                                                                     *
*        INVALID $DJ OPERAND                                          *
*                                                                     *
***********************************************************************
         SPACE 1
BADOPRND L     R1,0(0,R5)          POINT TO INVALID OPERAND
         MVC   COMMAND(9),0(R1)    MOVE 9 CHARACTERS OF OPERAND TO MSG
         MVC   COMMAND+9(L'MSG3),MSG3 SET ERROR MESSAGE
         LA    R0,L'MSG3+9         SET MESSAGE LENGTH
         $CWTO L=(R0),MSGID=999    WRITE THE MSG TO THE OPERATOR
         B     EXIT8               GET OUT
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - TERMINATE WITHOUT ANY MESSAGE                      *
*        RC = 12 - TERMINATE ISSUING THE "OK" MESSAGE                 *
*        RC = 16 - TERMINATE USING A USER SUPPLIED MESSAGE            *
*                                                                     *
***********************************************************************
EXIT0    LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
EXIT8    $FREEBUF (R8)             FREE THE WORK AREA
         $FREEBUF (R10)            FREE THE WORK AREA
         LA    R15,8               SET RETURN CODE TO 8
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
         LTORG
         EJECT
         $MODEND
         END
