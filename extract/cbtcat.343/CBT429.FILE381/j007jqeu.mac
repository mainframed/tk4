         TITLE 'UPDATE JQE USER FIELDS AND WRITE SMF30 RECORD'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS SMF EXIT PROGRAM WILL UPDATE USER FIELDS IN THE JQE     *
*        WITH INFORMATION FROM THE JCT.  THIS WILL INCLUDE            *
*        JCLERROR/ABEND INDICATORS, TEST/PRODUCTION INDICATOR,        *
*        SETUP REQUESTS FROM OTHER EXITS, AND OWNERID FIELD.          *
*        ALSO FOR NJE JOBS, AN SMF TYPE 30 SUBTYPE 1 RECORD WILL      *
*        BE WRITTEN TO PROVIDE ACCOUNTING INFORMATION FOR NJE         *
*        PRINT JOBS WHICH IS CURRENTLY BEING LOST SINCE THE WRITER    *
*        (TYPE6) AND THE PURGE (TYPE26) RECORDS DO NOT PROVIDE        *
*        JOB ACCOUNTING.  THIS RECORD WILL ONLY BE PRODUCED IF        *
*        THE APPROPRIATE NJE USER HEADER SECION IS PRESENT TO         *
*        PROVIDE THE JOB ACCOUNTING INFORMATION.                      *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J 0 0 7 J Q E U            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $CKPT        SYS1.HASPSRC                               *
*        04)  $ENTRY       SYS1.HASPSRC                               *
*        05)  $FCSUHDR     SYS1.HASPSRC (USER)                        *
*        06)  $GETSMFB     SYS1.HASPSRC                               *
*        07)  $HASPEQU     SYS1.HASPSRC                               *
*        08)  $HASPGBL     SYS1.HASPSRC                               *
*        09)  $HCT         SYS1.HASPSRC                               *
*        10)  $JCT         SYS1.HASPSRC                               *
*        11)  $JQE         SYS1.HASPSRC                               *
*        12)  $KIT         SYS1.HASPSRC                               *
*        13)  $MIT         SYS1.HASPSRC                               *
*        14)  $MODEND      SYS1.HASPSRC                               *
*        15)  $MODULE      SYS1.HASPSRC                               *
*        16)  $NHD         SYS1.HASPSRC                               *
*        17)  $NHDGET      SYS1.HASPSRC                               *
*        18)  $PADDR       SYS1.HASPSRC                               *
*        19)  $PCE         SYS1.HASPSRC                               *
*        20)  $QUESMFB     SYS1.HASPSRC                               *
*        21)  $QSUSE       SYS1.HASPSRC                               *
*        22)  $SAVE        SYS1.HASPSRC                               *
*        23)  $SMF         SYS1.HASPSRC                               *
*        24)  $RETURN      SYS1.HASPSRC                               *
*        25)  IEESMCA      SYS1.AMODGEN                               *
*        26)  IFASMFR      SYS1.AMODGEN                               *
*        27)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J007JQEU $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='UPDATE JQE USER FIELDS AND WRITE SMF30 RECORD',  X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $KIT,               GENERATE HASP KIT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $NHD,               GENERATE HASP NDH DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $SMF,               GENERATE HASP SMF DSECT             X
               RPL,                GENERATE MVS  RPL DSECT             X
               SMCA,               GENERATE MVS  SMCA DSECT            X
               SSOB                GENERATE MVS  SSOB DSECTS
         TITLE 'UPDATE JQE USER FIELDS AND WRITE SMF30 RECORD'
         IFASMFR 30
         EJECT
***********************************************************************
*                                                                     *
*        USER JOB HEADER                                              *
*                                                                     *
***********************************************************************
         SPACE 3
         $FCSUHDR
J007JQEU CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E007JQEU $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R10,R1              SET JCT ADDRESS
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         GET ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - A CODE PASSED TO YOUR ROUTINE BY JES2.                 *
*              0 - INDICATES THAT THE JCT HAS BEEN READ FROM SPOOL    *
*              4 - INDICATES THAT THE JCT WILL BE WRITTEN TO SPOOL    *
*        R1  - ADDRESS OF THE BUFFER THAT CONTAINS THE JCT            *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE PCE                                     *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         LTR   R0,R0               TEST ENTRY REASON CODE
         BZ    RETURN0             ZERO, READ ONLY, NOT INTERESTED
***********************************************************************
*        FIND OUR SPECIAL NJE USER HEADER                             *
***********************************************************************
         LA    R8,JCTNJHDR         POINT TO START OF NETWORK HEADERS
         USING FCSUHDR,R8          SET ADDRESSABILITY TO HEADERS
         LH    R5,0(R8)            LOAD LENGTH OF NETWORK HEADERS
         AR    R5,R8               GET END ADDRESS OF NETWORK HEADERS
         LA    R8,4(R8)            LOAD START ADDRESS OF FIRST HEADER
CKHD1    CR    R8,R5               SEE IF END OF HEADERS
         BNL   CKHD3               YES, GO ZERO THE HEADER ADDRESS
         TM    FCSUHTYP,NTYPUSER   SEE IF USER HEADER
         BNO   CKHD2               NO, GO GET NEXT ONE
         CLC   FCSUHID,=CL4'FCSH'  SEE IF OUR HEADER
         BE    CKHD4               YES, GO CHECK IT
CKHD2    AH    R8,0(R8)            POINT TO NEXT HEADER
         B     CKHD1               LOOP
CKHD3    SLR   R8,R8               CLEAR USER HEADER ADDRESS
CKHD4    DS    0H
         EJECT
***********************************************************************
*                                                                     *
*        UPDATE THE USER FIELDS IN THE JQE FROM INFORMATION           *
*        IN THE JCT                                                   *
*                                                                     *
***********************************************************************
CKBAD    CLI   JQEXFLG3,0          SEE IF ANY 'BAD' BITS ALREADY ON
         BNE   CKTP                YES, GO CHECK TEST/PRODUCTION
         CLI   JQETYPE,$INPUT      IS JOB PRE-CONVERTER/EXECUTION
         BE    CKTP                YES, NO JCLERROR OR ABEND YET
***********************************************************************
*        CHECK FOR JCL ERROR OR ABEND INDICATORS                      *
***********************************************************************
         CLC   JCTCNVRC,=F'0'      SEE IF CONVERTER RETURN CODE = 0
         BNE   JCLERR              NO, JCL ERROR
         CLI   JCTJTFLG,SSJTJFAL   SEE IF JCL ERROR DURING EXECUTION
         BE    JCLERR              YES, BRANCH
         TM    JCTJTFLG,SSJTABND   SEE IF ABENDED
         BO    ABEND               YES, BRANCH
         B     CKTP                GO CHECK TEST/PRODUCTION
JCLERR   BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         OI    JQEXFLG3,JQEXJCLE   TURN ON JCL ERROR INDICATOR
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
         B     CKTP                GO CHECK TEST/PRODUCTION
ABEND    BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         OI    JQEXFLG3,JQEXABND   TURN ON ABENDED INDICATOR
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
***********************************************************************
*        CHECK FOR TEST OR PRODUCTION STATUS                          *
***********************************************************************
CKTP     CLI   JQEXFLG1,0          SEE IF TEST/PRODUCTION FLAG SET
         BNE   CKHOLD              YES, GO CHECK FOR HOLD REQUEST
***********************************************************************
*        SEE IF WE HAVE OUR SPECIAL NJE USER HEADER                   *
***********************************************************************
         LTR   R8,R8               TEST USER HEADER ADDRESS
         BZ    CKHOLD              NO, GO CHECK FOR HOLD REQUEST
***********************************************************************
*        CHECK THE NJE USER HEADER FOR PRODUCTION JOB ACCOUNTING      *
***********************************************************************
CKTP1    BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         CLC   $OWNNODE,=H'1'      SEE IF JACKSONVILLE 9080
         BE    CKTP1J              YES, GO HANDLE IT DIFFERENTLY
         MVI   JQEXFLG1,JQEXTEST   DEFAULT TO TEST STATUS
         CLC   FCSUHACT(2),=C'F0'  SEE IF PRODUCTION ACCOUNTING
         BNE   CKTP2               NO, LEAVE IT TEST
         MVI   JQEXFLG1,JQEXPROD   TURN ON PRODUCTION INDICATOR
         B     CKTP2               GO SCHEDULE REWRITE OF JQE
CKTP1J   MVI   JQEXFLG1,JQEXTEST   DEFAULT TO TEST STATUS
         CLI   JCTJOBID,C'J'       SEE IF BATCH JOB
         BNE   CKTP2               NO, LEAVE IT TEST
         CLI   JCTJNAME+2,C'T'     SEE IF "T" IN 3RD POS JOBNAME
         BE    CKTP2               YES, LEAVE IT TEST
         MVI   JQEXFLG1,JQEXPROD   SET TO PRODUCTION STATUS
CKTP2    BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
***********************************************************************
*        CHECK FOR OPERATOR HOLD REQUEST FROM ANOTHER EXIT            *
***********************************************************************
CKHOLD   CLI   JCTUSER0,C'O'       SEE IF HOLD REQUEST
         BNE   CKOWN               NO, GO CHECK OWNERID
         BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         OI    JQEFLAG1,JQE1HLD1   TURN ON OPERATOR HOLD INDICATOR
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
         MVI   JCTUSER0,0          TURN OFF REQUEST
***********************************************************************
*                                                                     *
*        EXTRACT JOB OWNERID FROM RACF/VM/ACF2 LOGONID                *
*                                                                     *
*                              NOTE                                   *
*                                                                     *
*        THE RACFID IN THE JES2 RECORD CAN CHANGE DURING JOB          *
*        PROCESSING IF EARLY VERIFICATION IS REQUESTED.  THE          *
*        FIRST RACFID WILL BE THE SUBMITTER'S AND IT WILL             *
*        LATER BE CHANGED TO THE VALUE SPECIFIED IN THE               *
*        USER= PARAMETER ON THE JOB CARD.  IN EITHER CASE, WE         *
*        WANT THE LAST VALUE WHICH IS SET.                            *
*                                                                     *
*        IF THE OWNERID VALUE HAS BEEN SET FROM A THE OWNERID         *
*        VALUE ON A JOBPARM STATEMENT, THEN THE VALUE MUST            *
*        NEVER BE RESET.                                              *
*                                                                     *
***********************************************************************
CKOWN    TM    JQEXFLG3,JQEXOWNJ   SEE IF OWNERID SET BY JOBPARM
         BO    WRTSMF              YES, GO WRITE SMF TYPE 30 RECORD
***********************************************************************
*        SAVE JOBNAME AS OWNERID FOR STC OR TSU                       *
***********************************************************************
         CLI   JCTJOBID,C'J'       SEE IF A BATCH JOB
         BE    CKOWN01             YES, BRANCH
         CLI   JQEXOWNR,0          SEE IF OWNERID ALREADY SET
         BNE   CKOWN04             YES, GET OUT
         BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         MVC   JQEXOWNR,JCTJNAME   MOVE JOBNAME TO LOGONID FIELD
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
         B     CKOWN04             GET OUT
***********************************************************************
*        SEARCH THE GENERAL NETWORK JOB HEADER FOR VM                 *
***********************************************************************
CKOWN01  $NHDGET HEADER=JCTNJHDR,TYPE=NTYPGEN,MOD=NJHG$MOD,            X
               NOSEC=CKOWN02       GET NJE GENERAL SECTION
         LR    R6,R1               SAVE GENERAL HEADER ADDRESS
         USING NJHG,R6             SET ADDRESSABILITY TO GENERAL HDR
         CLI   NJHGXEQU,C' '       SEE IF VM LOGONID SPECIFIED
         BNH   CKOWN02             NO, NOT VM, GO CHECK FOR JES2
         CLC   JQEXOWNR,NJHGXEQU   SEE IF SAME OWNERID AS LAST
         BE    CKOWN04             YES, GET OUT
         BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         MVC   JQEXOWNR,NJHGXEQU   MOVE VM LOGONID FROM GENERAL HEADER
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
         B     CKOWN04             GET OUT
         DROP  R6                  DROP ADDRESSABILITY TO GENERAL HDR
***********************************************************************
*        SEARCH THE JES2 JOB HEADER                                   *
***********************************************************************
CKOWN02  $NHDGET HEADER=JCTNJHDR,TYPE=NTYPJES2,MOD=NJH2$MOD,           X
               NOSEC=CKOWN03       GET JES2 GENERAL HEADER
         LR    R6,R1               SAVE JES2 HEADER ADDRESS
         USING NJH2,R6             SET ADDRESSABILITY TO JES2 HEADER
         CLI   NJH2USR,C' '        SEE IF LOGONID SPECIFIED
         BNH   CKOWN03             NO, GO CHECK ANOTHER PLACE
         CLC   JQEXOWNR,NJH2USR    SEE IF SAME OWNERID AS LAST
         BE    CKOWN04             YES, GET OUT
         BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         MVC   JQEXOWNR,NJH2USR    YES, MOVE LOGONID TO JQE
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
         B     CKOWN04             GET OUT
         DROP  R6                  DROP ADDRESSABILITY TO JES2 HEADER
***********************************************************************
*        SEARCH FOR AN ACF2 USER HEADER                               *
***********************************************************************
CKOWN03  $NHDGET HEADER=JCTNJHDR,TYPE=ACFUHDR,MOD=ACFUMOD,             X
               NOSEC=CKOWN04       GET ACF2 HEADER
ACFUHDR  EQU   X'E0'               EQUATE FOR ACF2 HEADER TYPE ID
ACFUMOD  EQU   X'01'               EQUATE FOR ACF2 MODIFICATION ID
         LR    R6,R1               SAVE JES2 HEADER ADDRESS
         USING NJHU,R6             SET ADDRESSABILITY TO HEADERS
         CLC   NJHUCODE,=CL4'ACF2' SEE IF ACF2 HEADER
         BNE   CKOWN04             NO, NO WAY TO FIND THE OWNERID
         CLC   JQEXOWNR,88(R6)     SEE IF SAME OWNERID AS LAST
         BE    CKOWN04             YES, GET OUT
         BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         MVC   JQEXOWNR,88(R6)     YES, MOVE LOGONID FROM ACF2 HEADER
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
CKOWN04  DS    0H                  END OF FIND USERID ROUTINE
         DROP  R6                  DROP ADDRESSABILITY TO USER SECTION
         EJECT
***********************************************************************
*                                                                     *
*        WRITE AN SMF TYPE 30 RECORD IF THIS IS AN NJE JOB            *
*                                                                     *
***********************************************************************
WRTSMF   CLC   JCTXNODE,=X'0000'   SEE IF CONTAINS AN EXECUTION NODE
         BE    RETURN0             NO, JUST GET OUT
         CLC   JCTXNODE,$OWNNODE   SEE IF AN NJE JOB
         BE    RETURN0             NO, JUST GET OUT
***********************************************************************
*        SEE IF TYPE 30 RECORD ALREADY WRITTEN                        *
***********************************************************************
         TM    JQEXFLG2,JQEXSMF    SEE IF SMF TYPE30 RCD ALREADY DONE
         BO    RETURN0             YES, GET OUT
***********************************************************************
*        GET AN SMF BUFFER                                            *
***********************************************************************
         SPACE 3
         $GETSMFB WAIT=YES         GET AN SMF BUFFER
         LR    R9,R1               SAVE BUFFER ADDRESS
         USING SMF,R9              SET ADDRESSABILITY TO SMF BUFFER
         LA    R2,SMFRDW           POINT TO RECORD AREA IN BUFFER
         USING SMFRCD30,R2         SET ADDRESSABILITY TO SMF RECORD
***********************************************************************
*        BUILD THE SMF TYPE 30 RECORD IN THE WORK AREA                *
***********************************************************************
BASELEN  EQU   SMF30EOR+L'SMF30EOR-SMFRCD30
PSSLEN   EQU   SMF30PNM+L'SMF30PNM-SMF30PSS
IDLEN    EQU   SMF30TID+L'SMF30TID-SMF30ID
TYPE30LN EQU   BASELEN+PSSLEN+IDLEN
***********************************************************************
*        BUILD THE SMF TYPE 30 BASE SECTION                           *
***********************************************************************
         MVC   SMF30LEN,=AL2(TYPE30LN) SET BASE RECORD LENGTH
         MVI   SMF30FLG,X'02'      SET SYSTEM IDENTIFIER TO VS2
         MVI   SMF30RTY,30         SET RECORD TYPE TO TYPE 30
         TIME  BIN                 GET DATE AND TIME
         STCM  R0,15,SMF30TME      SAVE TIME IN RECORD
         STCM  R1,15,SMF30DTE      SAVE DATE IN RECORD
         MVC   SMF30SID,$SID       MOVE CPUID TO RECORD
         MVC   SMF30WID,=CL4'TSO ' SET SUBSYSTEM ID TO TSO
         CLI   JCTJOBID,C'T'       SEE IF A TSO USER
         BE    SSIDOK              YES, BRANCH
         MVC   SMF30WID,=CL4'STC ' SET SUBSYSTEM ID TO STC
         CLI   JCTJOBID,C'S'       SEE IF A STARTED TASK
         BE    SSIDOK              YES, BRANCH
         MVC   SMF30WID,=CL4'JES2' SET SUBSYSTEM ID TO JES2
SSIDOK   MVC   SMF30SOF,=A(BASELEN) SAVE OFFSET OF SUBSYSTEM SECTION
         MVC   SMF30SLN,=AL2(PSSLEN) SAVE LENGTH OF SUBSYSTEM SECTION
         MVC   SMF30SON,=H'1'      SAVE NUMBER OF IDENT SECTIONS
         MVC   SMF30IOF,=A(BASELEN+PSSLEN) SAVE OFFSET OF IDENT SECT
         MVC   SMF30ILN,=AL2(IDLEN) SAVE LENGTH OF IDENT SECTION
         MVC   SMF30ION,=H'1'      SAVE NUMBER OF IDENT SECTIONS
***********************************************************************
*        BUILD THE SMF TYPE 30 SUBSYSTEM SECTION                      *
***********************************************************************
         LA    R3,SMF30RHD+BASELEN POINT TO START OF SUBSYSTEM SECTION
         USING SMF30PSS,R3         SET ADDRESSABILITY TO SUBSYS SECTION
         MVC   SMF30TYP,=H'1'      SET TO SUBTYPE 1
         MVC   SMF30RVN,=C'01'     SET TO RECORD VERSION 1
         MVC   SMF30PNM,=CL8'BOGUSNJE' SET SUBSYSTEM NAME
         DROP  R3                  DROP ADDRESSABILITY TO SUBSYS SECT
***********************************************************************
*        BUILD THE SMF TYPE 30 IDENTIFICATION SECTION                 *
***********************************************************************
         LA    R3,SMF30RHD+BASELEN+PSSLEN POINT TO START OF IDENT SECTN
         USING SMF30ID,R3          SET ADDRESSABILITY TO IDENT SECTION
         MVC   SMF30JBN,JCTJNAME   SET JOB NAME
         MVC   SMF30PGM,=CL8' '    SET BLANK PGM NAME
         MVC   SMF30STM,=CL8'NJEPRINT' SET DUMMY STEP NAME
         MVC   SMF30UIF,JCTUSEID   SET USER ID FIELD
         MVC   SMF30JNM,JCTJOBID   SET JES JOB NUMBER
         MVI   SMF30CLS,C' '       SET JOB CLASS TO BLANK
         CLI   JCTJOBID,C'J'       SEE IF BATCH JOB
         BNE   NOTBATCH            NO, BRANCH
         MVC   SMF30CLS,JCTJCLAS   SET JOB CLASS
NOTBATCH MVC   SMF30JPT+1(1),JCTPRIO SET JES INPUT PRIORITY
         MVC   SMF30SIT,SMF30TME   SET STEP INIT TIME TO CURR TIME
         MVC   SMF30STD,SMF30DTE   SET STEP INIT DATE TO CURR DATE
         MVC   SMF30RST,JCTRDRON   SET READER START TIME
         MVC   SMF30RSD,JCTRDTON   SET READER START DATE
         MVC   SMF30RET,JCTRDROF   SET READER END TIME
         MVC   SMF30RED,JCTRDTOF   SET READER END DATE
         OI    SMF30RED+3,X'0F'    MAKE SURE DATE HAS A SIGN
         MVC   SMF30USR,JCTPNAME   SET PROGRAMMER NAME
         MVC   SMF30GRP,=CL8' '    SET BLANK RACF GROUP ID
         MVC   SMF30RUD,=CL8' '    SET BLANK RACF USER ID
***********************************************************************
*        TRY TO FIND OUT THE RACF/ACF2 USERID                         *
***********************************************************************
***********************************************************************
*        SAVE JOBNAME AS LOGONID FOR STC OR TSU                       *
***********************************************************************
FUID     CLI   JCTJOBID,C'J'       SEE IF A BATCH JOB
         BE    FUID01              YES, BRANCH
         MVC   SMF30RUD,JCTJNAME   NO,  MOVE JOBNAME TO LOGONID FIELD
         B     FUID04              GET OUT
***********************************************************************
*        SEARCH THE JES2 JOB HEADER                                   *
***********************************************************************
FUID01   $NHDGET HEADER=JCTNJHDR,TYPE=NTYPJES2,MOD=NJHO$MOD,           X
               NOSEC=FUID02        GET JES2 GENERAL HEADER
         LR    R6,R1               SAVE JES2 HEADER ADDRESS
         USING NJH2,R6             SET ADDRESSABILITY TO JES2 HEADER
         CLI   NJH2USR,C' '        SEE IF LOGONID SPECIFIED
         BNH   FUID02              NO, GO CHECK ANOTHER PLACE
         MVC   SMF30RUD,NJH2USR    YES, MOVE LOGONID TO SMF RECORD
         B     FUID04              GET OUT
         DROP  R6                  DROP ADDRESSABILITY TO JES2 HEADER
***********************************************************************
*        SEARCH THE GENERAL NETWORK JOB HEADER FOR VM                 *
***********************************************************************
FUID02   $NHDGET HEADER=JCTNJHDR,TYPE=NTYPGEN,MOD=NJHG$MOD,            X
               NOSEC=FUID03        GET NJE GENERAL SECTION
         LR    R6,R1               SAVE GENERAL HEADER ADDRESS
         USING NJHG,R6             SET ADDRESSABILITY TO GENERAL HDR
         CLI   NJHGXEQU,C' '       SEE IF VM LOGONID SPECIFIED
         BNH   FUID02A             NO, GO CHECK ANOTHER PLACE
         MVC   SMF30RUD,NJHGXEQU   MOVE VM LOGONID FROM GENERAL HEADER
         B     FUID04              GET OUT
FUID02A  CLI   NJHGUSID,C' '       SEE IF TSO LOGONID SPECIFIED
         BNH   FUID03              NO, GO CHECK ANOTHER PLACE
         MVC   SMF30RUD,NJHGUSID   MOVE TSO LOGONID FROM GENERAL HEADER
         B     FUID04              GET OUT
         DROP  R6                  DROP ADDRESSABILITY TO GENERAL HDR
***********************************************************************
*        SEARCH FOR AN ACF2 USER HEADER                               *
***********************************************************************
FUID03   $NHDGET HEADER=JCTNJHDR,TYPE=ACFUHDR,MOD=ACFUMOD,             X
               NOSEC=FUID04        GET ACF2 HEADER
         LR    R6,R1               SAVE JES2 HEADER ADDRESS
         USING NJHU,R6             SET ADDRESSABILITY TO ACF2 USER HDR
         CLC   NJHUCODE,=CL4'ACF2' SEE IF ACF2 HEADER
         BNE   FUID04              NO, NO WAY TO FIND THE OWNERID
         MVC   SMF30RUD,88(R6)     YES, MOVE LOGONID FROM ACF2 HEADER
FUID04   DS    0H                  END OF FIND USERID ROUTINE
         DROP  R3                  DROP ADDRESSABILITY TO ID SECTION
         DROP  R6                  DROP ADDRESSABILITY TO USER HDR RECD
***********************************************************************
*        BUILD THE SMF TYPE 30 ACCOUNTING SECTION                     *
***********************************************************************
         LTR   R8,R8               TEST USER HEADER ADDRESS
         BZ    WRITESMF            NONE, BRANCH
         LA    R3,SMF30RHD+BASELEN+PSSLEN+IDLEN POINT TO START OF ACCT
         USING SMF30ACS,R3         SET ADDRESSABILITY TO ACCT SECTION
         LH    R15,FCSUHACL        LOAD LENGTH OF ACCOUNTING FIELD
         LTR   R15,R15             TEST LENGTH
         BZ    WRITESMF            ZERO, NO ACCOUNTING FIELD
         STC   R15,SMF30ACL        SAVE LENGTH OF ACCOUNTING FIELD
         BCTR  R15,0               MAKE MACHINE LENGTH
         EX    R15,MVACCT          MOVE ACCT TO RECORD
***********************************************************************
*        UPDATE BASE SECTION FOR VARIABLE ACCOUNTING SECTION          *
***********************************************************************
         LH    R15,FCSUHACL        RELOAD LENGTH OF ACCOUNTING FIELD
         LA    R15,1(R15)          ADD 1 FOR LENGTH OF ACCT SECTION
         LH    R1,SMF30LEN         LOAD LENGTH OF RECORD
         AR    R1,R15              ADD LENGTH OF ACCOUNT SECTION
         STH   R1,SMF30LEN         SAVE NEW RECORD LENGTH
         MVC   SMF30AOF,=A(BASELEN+PSSLEN+IDLEN) LOAD OFFSET OF ACCT
         STH   R15,SMF30ALN        SAVE LENGTH OF ACCT SECTION
         MVC   SMF30AON,=H'1'      SAVE NUMBER OF ACCT SECTIONS
         B     WRITESMF            GO WRITE SMF RECORD
MVACCT   MVC   SMF30ACT(0),FCSUHACT **** EXECUTE ONLY ****
***********************************************************************
*        WRITE SMF TYPE 30 RECORD                                     *
***********************************************************************
WRITESMF $QUESMFB (R9)             WRITE THE SMF RECORD
         BAL   R4,GETCKPT          GET CONTROL OF CHECKPOINT
         OI    JQEXFLG2,JQEXSMF    TURN ON SMF RECORD WRITTEN INDICATOR
         BAL   R4,PUTCKPT          SCHEDULE REWRITE OF JQE
         B     RETURN0             GET OUT
         DROP  R9                  DROP ADDRESSABILITY TO SMF BUFFER
         EJECT
***********************************************************************
*                                                                     *
*        GET CONTROL OF THE JES2 CHECKPOINT SO THAT THE JQE CAN       *
*        BE UPDATED.                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
GETCKPT  $QSUSE TYPE=WAIT          GET CONTROL OF THE SHARED QUEUES
         BR    R4                  RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        TELL ABOUT THE CHANGE OF THE JQE SO THE JQE WILL BE          *
*        REWRITTEN AT THE END OF THIS CHECKPOINT CYCLE.               *
*                                                                     *
***********************************************************************
         SPACE 3
PUTCKPT  LR    R1,R7               SET JQE ADDRESS
         $CKPT ID=JQE,ADDR=(R1)    SCHEDULE REWRITE OF JQE
         BR    R4                  RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
***********************************************************************
*                                                                     *
*                   CONSTANTS USED IN THIS EXIT                       *
*                                                                     *
***********************************************************************
         SPACE 3
         LTORG
         EJECT
         $MODEND
         END
