         TITLE 'JES2 SMF RECORD EXIT'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 1.3.4  OR SP 1.3.6                      *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED EXAMINE AND/OR            *
*        MODIFY ALL JES2 SMF RECORDS.  THE SMF TYPE 57 RECORD         *
*        IS WRITTEN FOR EACH NJE SYSOUT TRANSMISSION WHICH            *
*        OCCURS.  THE ONLY PROBLEM IS THAT THE JOBNAME IS NOT         *
*        CONTAINED ANYWHERE IN THE RECORD.  THIS EXIT WILL PUT        *
*        THE JOBNAME INTO THE TYPE 57 RECORD IN THE NETACCT           *
*        FIELD WHICH IS AN EIGHT BYTE FIELD WHICH IS UNUSED           *
*        BY OUR INSTALLATION.                                         *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST COMPUTER SERVICES                      *
*                        420 BRUSHY MOUNTAIN ROAD                     *
*                        WILKESBORO, N.C. 28697                       *
*                        (919) 651-5414                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J E S X I T 1 7            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $ENTRY       SYS1.HASPSRC                               *
*        04)  $HASPEQU     SYS1.HASPSRC                               *
*        05)  $HASPGBL     SYS1.HASPSRC                               *
*        06)  $HCT         SYS1.HASPSRC                               *
*        07)  $JCT         SYS1.HASPSRC                               *
*        08)  $JQE         SYS1.HASPSRC                               *
*        09)  $MIT         SYS1.HASPSRC                               *
*        10)  $MODEND      SYS1.HASPSRC                               *
*        11)  $MODULE      SYS1.HASPSRC                               *
*        12)  $PCE         SYS1.HASPSRC                               *
*        13)  $SAVE        SYS1.HASPSRC                               *
*        14)  $SMF         SYS1.HASPSRC                               *
*        15)  $RETURN      SYS1.HASPSRC                               *
*        16)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
JESXIT21 $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='JES2 SMF RECORD EXIT',                           X
               RPL,                GENERATE MVS  RPL DSECT             X
               $CAT,               GENERATE HASP CAT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $SMF,               GENERATE HASP SMF BUFFER DSECT      X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $PCE                GENERATE HASP PCE DSECT
         TITLE 'JES2 SMF RECORD EXIT'
         PRINT OFF
         AIF   ('&VERSION' NE 'SP 1.3.6' AND                           X
               '&VERSION' NE 'SP 2.1.5').VEREND
         PRINT ON
***********************************************************************
*                                                                     *
*        EQUATED VARIABLES FOR SP 1.3.6 OR SP 2.1.5                   *
*                                                                     *
***********************************************************************
         SPACE 3
JQEDSECT EQU   JQE
SMFDSECT EQU   SMF
SMFHDRTY EQU   SMF57RTY
         SPACE 3
         USING PCE,R13             SET ADDRESSABILITY TO PCE
.VEREND  ANOP
         PRINT ON
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
EXIT21   $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - ZERO                                                   *
*        R1  - SMF BUFFER ADDRESS                                     *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE CALLER'S PCE                            *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         USING SMFDSECT,R1         SET ADDRESSABILITY TO SMF BUFFER
         CLI   SMFHDRTY,57         SEE IF TYPE 57 RECORD
         BNE   RETURN0             NO, JUST GET OUT
         MVC   SMF57ACN,=CL8' '    BLANK THE NETWORK ACCT FIELD
         ICM   R2,15,PCEJQE        LOAD JQE ADDRESS FROM PCE
         USING JQEDSECT,R2         SET ADDRESSABILITY TO JQE
         BZ    RETURN0             JQE ADDRESS ZERO, EXIT
         MVC   SMF57ACN,JQEJNAME   MOVE JOBNAME TO NETWORK ACCT FIELD
         B     RETURN0             GET OUT
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - TERMINATE NORMAL SMF QUEUE PROCESSING              *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
RETURN4  DS    0H
         LA    R15,4               SET RETURN CODE TO 4
         $RETURN RC=(R15)          RETURN TO JES2
RETURN8  DS    0H
         LA    R15,8               SET RETURN CODE TO 8
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
         $MODEND
         END
