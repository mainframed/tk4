J004$JEC TITLE 'FUNB MODIFICATION FOR DEPENDENT JOB CONTROL'
***********************************************************************
*                                                                     *
* MODULE NAME - J004$JEC                                              *
*                                                                     *
* DESCRIPTIVE NAME - DEPENDENT JOB CONTROL                            *
*                                                                     *
* JES2 LEVEL - SP 2.2.0                                               *
*                                                                     *
* FUNCTION - THIS EXIT SUPPORTS THE FUNB DEPENDENT JOB CONTROL        *
*            FACILITY.  JECL STATEMENTS ARE SCANNED AND SPECIFIC      *
*            OPERATIONS ARE PERFORMED ON EACH OF THE ONES LISTED      *
*            BELOW.  THE INFORMATION IS SET IN THE JQE.               *
*                                                                     *
*            /*SETUP    USED TO NAME THE TAPE OR DISK VOLUMES         *
*                       NEEDED BY THE JOB.                            *
*                                                                     *
*            /*THREAD   SPECIFIES A NAME THAT WILL BE USED TO SINGLE  *
*                       THREAD ALL OTHER JOBS SPECIFYING THIS NAME.   *
*                       THIS STATEMENT CAN OPTIONALLY PLACE A JOB ON  *
*                       USER HOLD PENDING RELEASE BY ANOTHER JOB.     *
*                                                                     *
*            /*EXCLUDE  SPECIFIES ONE JOB NAME THAT, IF CURRENTLY     *
*                       EXECUTING, WILL KEEP THIS JOB FROM            *
*                       EXECUTING.  THE REVERSE IS NOT TRUE           *
*                       UNLESS THOSE JOBS EXCLUDE THIS ONE AS WELL.   *
*                                                                     *
*            /*RELEASE  SPECIFIES ONE JOB NAME THAT WILL GET          *
*                       RELEASED FROM USER HOLD                       *
*                                                                     *
*            OPERATOR COMMANDS ALLOW THE JQE TO BE DISPLAYED, JOBS    *
*            TO BE SETUP AND RELEASED.                                *
*                                                                     *
*            QUEUE SCAN REPLACEMENT PERFORM THREAD/EXCLUDE PROCESSING.*
*                                                                     *
*                                                                     *
* EXIT ENVIRONMENT                                                    *
*                                                                     *
*    JES2 MAIN TASK                                                   *
*                                                                     *
* MODULE ATTRIBUTES                                                   *
*                                                                     *
*                                                                     *
* ENTRY POINTS = E004$JEC - PROCESS JECL STATEMENTS                   *
*                                                                     *
***********************************************************************
         SPACE
         COPY  $HASPGBL
J004$JEC $MODULE $HASPEQU,                                             X
               $PCE,               CONTAINS USING PCEDSECT,R13         X
               $CMB,                                                   X
               $DCT,                                                   X
               $PDDB,                                                  X
               $IOT,                                                   X
               $RDRWORK,                                               X
               $BUFFER,                                                X
               $JQE,                                                   X
               $JCT,                                                   X
               $CAT,                                                   X
               $JOE,                                                   X
               $NIT,                                                   X
               $TAB,                                                   X
               $MIT,                                                   X
               $KIT,                                                   X
               $HCT,                                                   X
               $PADDR,                                                 X
               RPL
         TITLE 'MAPPING OF JCTXWRK AREA'
FCSXWRK  DSECT                      FUNB USAGE OF JCTXWRK (80 BYTES)
FCSXSAVE DS   2F                    R0 & R1 SAVE AREA
FCSXPARM DS   0F                    SCANDLMT PARAMETER LIST
FCSXDLM  DS    F                      A(DELIMITER TABLE)
FCSXSTA  DS    F                      START OF FIELD
FCSXEND  DS    F                      END OF FIELD
FCSXFND  DS    F                      A(FOUND DELIMITER)
FCSXGM   DS    F                    ADDR. OF GETMAINED WORK AREA
FCSXDLMT DS    CL4                  DELIMITER WORK AREA
         SPACE
FCSLPARM DS   0F                    $LOCATE PARAMETER LIST
FCSLACCT DS    F
FCSLWORK DS    F
FCSLRC   DS    F
FCSLPID  DS    XL4
         TITLE 'ROUTINE E004$JEC UPDATE JQE FLAGS FROM JECL STATEMENTS'
E004$JEC $ENTRY BASE=R12,CSECT=YES
         $SAVE
*----------------------------------------------------------------------
*
* REGISTER USAGE IN E004$JEC
*
*  R0  -
*  R1  -
*  R2  - BUFFER CONTAINING STATEMENT; WORK
*  R3  - WORK AREA IN JCT (0 IF NO JCT EXIST)
*  R4  - WORK
*  R5  - PARAMETER PROCESSING
*  R7  -
*  R9  -
*  R10 - JCT
*  R11 - HCT
*  R12 - BASE
*  R13 - PCE
*  R14 - RETURN
*  R15 - ENTRY POINT; BALR; RETURN CODE
*
*----------------------------------------------------------------------
         SPACE 1
         LR    R12,R15             LOAD OUR BASE
         USING JCT,R10
         LTR   R0,R0               IS THIS FOR A JECL STATEMENT?
         BNZ   RETURN0               NO, RETURN TO JES
         L     R2,0(R1)            GET ADDRESS OF STATEMENT BUFFER
         L     R3,8(R1)              AND WORK AREA
         LTR   R3,R3               IF NO JCT EXISTS YET,
         BZ    RETURN0               DON'T PROCESS ANYTHING
         USING FCSXWRK,R3
         STM   R0,R1,FCSXSAVE      SAVE INPUT PARAMETERS
         SPACE
*======================================================================
* DETERMINE LENGTH OF JECL STATEMENT NAME
*======================================================================
         SPACE
         LA    R4,2(R2)            POINT PAST /*
         ST    R4,FCSXSTA            AND SET STARTING POINT
         LA    R4,70(R2)           POINT TO END OF CARD
         ST    R4,FCSXEND            AND SET STARTING POINT
         LA    R4,=X'0140'         SCAN FOR A BLANK
         ST    R4,FCSXDLM
         LA    R1,FCSXPARM         PRELOAD R1
         CALL  SCANDLMT             AND CALL SCAN ROUTINE
         L     R1,FCSXFND          GET ADDR OF FOUND DELIMITER
         LTR   R1,R1               IF NO BLANK FOUND,
         BZ    RETURN0               RETURN AND LET JES FLAG IT.
         S     R1,FCSXSTA          GET LENGTH OF JECL NAME
         BNP   RETURN0             IF 0 OR -, CAN IT!
         CH    R1,=H'8'            IF > 8,
         BH    RETURN0               RETURN AND LET JES FLAG IT.
         BCTR  R1,R0               DROP 1 FOR EXECUTED CLC
         SPACE
*======================================================================
* SEE IF THIS JECL STATEMENT IS TO BE PROCESSED BY US
*======================================================================
         SPACE
         L     R2,FCSXSTA          POINT TO JECL NAME
         LA    R4,JECLTBL          TABLE OF APPLICABLE JECL STMTS
         LA    R5,JECL#              AND # ENTRIES
CKJECL   EX    R1,CLCJECL          IF EQUAL
         BE    PRCJECL               GO CALL PROCESSING RTN.
         LA    R4,LJECLTBL(R4)     ELSE, INCREMENT TO NEXT ENTRY
         BCT   R5,CKJECL             AND TRY AGAIN.
         B     RETURN0             NO MATCH, RETURN TO JES.
         SPACE
PRCJECL  L     R15,8(R4)           GET PROCESSING ROUTINE
         LA    R1,FCSXWRK          GIVE IT THE WORK AREA ADDRESS
         BALR  R14,R15               AND CONTROL
         B     RETURN              ROUTINE WILL SET RC FOR US
         SPACE
RETURN0  SR    R15,R15             SET ZERO RETURN CODE
         SPACE
RETURN   $RETURN RC=(R15)          ISSUE RETURN
         SPACE
CLCJECL  CLC   0(1,R2),0(R4)
JECLTBL  DS   0F
         DC    CL8'SETUP',A(PRCSETP)
LJECLTBL EQU   *-JECLTBL
         DC    CL8'THREAD',A(PRCTHRD)
         DC    CL8'EXCLUDE',A(PRCEXCL)
         DC    CL8'RELEASE',A(PRCRLSE)
         DC    CL8'CHAIN',A(PRCCHN)
JECL#    EQU   (*-JECLTBL)/LJECLTBL
         LTORG
*
         TITLE 'ROUTINE PRCSETP - PROCESS SETUP JECL STATEMENT'
***********************************************************************
*                                                                     *
* ROUTINE: PRCSETP - SUBROUTINE TO PROCESS SETUP JECL STATEMENTS FOR  *
*                    DEPENDENT JOB CONTROL.                           *
*                                                                     *
* INPUT  R1    - FCSXWRK                                              *
*        R10   - JCT                                                  *
*        R11   - HCT                                                  *
*        R12   - BASE                                                 *
*        R13   - PCE                                                  *
*        R14   - RETURN                                               *
*        R15   - ENTRY                                                *
*                                                                     *
* OUTPUT                                                              *
*        R15   - RETURN CODE FOR JES2                                 *
*                                                                     *
***********************************************************************
         SPACE 1
PRCSETP  CSECT
         $SAVE
         USING PRCSETP,R12              SETUP
         LR    R12,R15                   BASE
         L     R9,PCEJQE                GET JQE ADDRESS
         USING JQE,R9                     AND SET UP ADDRESSABILITY
         TM    JQEXFLG2,JQEXWSET        IS SETUP ON?
         BC    1,RETSETP0               YEP..GET OUT OF TOWN PARTNER
         $QSUSE ,                       SYNC. CHECKPOINT AND WAIT
         OI    JQEXFLG2,JQEXWSET        INDICATE JOB SETUP
         SPACE
RETSETP0 LA    R15,8                    TELL JES TO COMMENT OUT CARD
         $RETURN RC=(R15)
         LTORG
         DROP  R3,R9
*
         TITLE 'ROUTINE PRCTHRD - PROCESS THREAD JECL STATEMENT'
***********************************************************************
*                                                                     *
* ROUTINE: PRCTHRD - SUBROUTINE TO PROCESS THREAD JECL STATEMENTS     *
*                    FOR DEPENDENT JOB CONTROL.                       *
*                                                                     *
* JECL FORMAT: FIXED FORMAT AS FOLLOWS.                               *
*                                                                     *
*              COLS  1- 8  /*THREAD                                   *
*                   11-14  HOLD OR BLANKS                             *
*                   16-23  THREAD NAME DELIMITED BY A BLANK           *
*                   24-71  UNREFERENCED                               *
*                                                                     *
* INPUT  R1    - FCSXWRK                                              *
*        R10   - JCT                                                  *
*        R11   - HCT                                                  *
*        R13   - PCE                                                  *
*        R14   - RETURN                                               *
*        R15   - ENTRY                                                *
*                                                                     *
* USAGE  R1    - WORK                                                 *
*        R2    - DEFAULT ERROR MESSAGE TO BE ISSUED                   *
*        R3    - FCSXWRK                                              *
*        R4    - WORK                                                 *
*        R9    - JQE                                                  *
*        R12   - BASE                                                 *
*                                                                     *
* OUTPUT                                                              *
*        R15   - RETURN CODE FOR JES2                                 *
*                                                                     *
***********************************************************************
         SPACE 1
PRCTHRD  CSECT
         $SAVE
         USING PRCTHRD,R12              SETUP
         LR    R12,R15                   BASE
         LR    R3,R1                    GET WORK AREA ADDRESS
         USING FCSXWRK,R3                 AND SET UP ADDRESSABILITY
         LA    R2,PRCITHRD              POINT TO DEFAULT INVALID MSG
         L     R9,PCEJQE
         USING JQE,R9                   SET UP ADDRESSABILITY TO JQE
         $QSUSE ,                       SYNCHRONIZE FOR SHARED QUEUES
         SPACE
PRCTHRDA L     R1,FCSXSAVE+4            POINT TO SAVED REG1
         L     R1,0(R1)                  AND RELOAD R1 (CARD IMAGE)
         XC    FCSXDLMT,FCSXDLMT        USE FCSXDLMT AS A SAVE
         CLC   9(6,R1),=C' HOLD '       IF COLS 10-15 = ' HOLD '
         BNE   PRCTHRDC
         OI    JQEXFLG2,JQEXUHLD        SET USER HOLD INDICATOR
         B     PRCTHRDE                   AND CHECK FOR A THREAD NAME
         SPACE
PRCTHRDC CLC   9(6,R1),$BLANKS          IF 'HOLD' OR BLANKS DONT EXIST
         BNE   RETTHRDC                   CAN THIS JOB!
         SPACE
PRCTHRDE LA    R1,15(R1)                POINT TO BEGINNING
         ST    R1,FCSXSTA                 OF THREAD NAME
         LA    R1,9(R1)                 POINT TO END
         ST    R1,FCSXEND                 OF SCAN
         LA    R1,=X'0140'              SCAN FOR A
         ST    R1,FCSXDLM                 BLANK
         LA    R1,FCSXPARM              PRELOAD PARM LIST ADDRESS
         CALL  SCANDLMT                 INVOKE SCAN
         SPACE
         L     R4,FCSXFND               GET A(DELIMITER)
         LTR   R4,R4                    WAS ONE FOUND?
         BZ    RETTHRDC                   NO, TELL JES TO CAN JOB
         S     R4,FCSXSTA               GET L' THREAD NAME
         BNP   RETTHRDC                 IF 0 OR -, CAN THIS ONE!
         CH    R4,=H'8'                 IF > 8
         BNH   PRCTHRDG                   TRUNCATE TO 8.
         LH    R4,=H'8'
PRCTHRDG BCTR  R4,R0                    DROP FOR EXECUTED MVC
         MVC   JQEXTHRN,$BLANKS         BLANK FIELD FIRST
         L     R5,FCSXSTA               POINT TO START OF SUPPLIED NAME
         EX    R4,MVCTHRD                 AND MOVE IT.
         OI    JQEXFLG2,JQEXTHRD        TURN ON THREAD BIT
         SPACE
RETTHRD8 SR    R15,R15                  SET RETURN CODE SO THAT
         LA    R15,8                      STATEMENT IS BYPASSED
         B     RETTHRD                    THEN RETURN
         SPACE
RETTHRDC SR    R15,R15                  SET RETURN CODE
         LA    R15,12                     TO 12.
         L     R1,FCSXWRK+4             RELOAD INITIAL PARAMETER VALUE
         L     R4,4(R1)                 A(EXIT FLAG)
         OI    0(R4),X'20'              INDICATE WE HAVE AN ERROR MSG
         L     R4,8(R1)                 LOAD A(JCTXWRK)
         MVC   0(80,R4),0(R2)           MOVE SUPPLIED MESSAGE
         MVC   0(1,R4),$CCOMCHR           AND INDICATE WHICH JES WE ARE
         B     RETTHRD
         SPACE
RETTHRD  $RETURN RC=(R15)               RETURN TO CALLER
         SPACE
MVCTHRD  MVC   JQEXTHRN(1),0(R5)
PRCITHRD DC    CL80'$HASPFCS INVALID /*THREAD STATEMENT'
         LTORG
         DROP  R3,R9
*
         TITLE 'ROUTINE PRCEXCL - PROCESS EXCLUDE JECL STATEMENT'
***********************************************************************
*                                                                     *
* ROUTINE: PRCEXCL - SUBROUTINE TO PROCESS EXCLUDE JECL STATEMENTS    *
*                    FOR DEPENDENT JOB CONTROL.                       *
*                                                                     *
* JECL FORMAT: FIXED FORMAT AS FOLLOWS.                               *
*                                                                     *
*              COLS  1- 8  /*EXCLUDE                                  *
*                    9 71  ONE OR MORE BLANKS FOLLOWED BY EXCLUDE     *
*                          NAMES SEPARATED BY COMMAS.                 *
*                                                                     *
* INPUT  R1    - FCSXWRK                                              *
*        R10   - JCT                                                  *
*        R11   - HCT                                                  *
*        R13   - PCE                                                  *
*        R14   - RETURN                                               *
*        R15   - ENTRY                                                *
*                                                                     *
* USAGE  R1    - WORK                                                 *
*        R2    - DEFAULT ERROR MESSAGE TO BE ISSUED                   *
*        R3    - FCSXWRK                                              *
*        R4    - WORK                                                 *
*        R9    - JQE                                                  *
*        R12   - BASE                                                 *
*                                                                     *
* OUTPUT                                                              *
*        R15   - RETURN CODE FOR JES2                                 *
*                                                                     *
***********************************************************************
         SPACE 1
PRCEXCL  CSECT
         $SAVE
         USING PRCEXCL,R12              SETUP
         LR    R12,R15                   BASE
         LR    R3,R1                    GET WORK AREA ADDRESS
         USING FCSXWRK,R3                 AND SET UP ADDRESSABILITY
         L     R9,PCEJQE                GET ADDRESS OF JQE
         USING JQE,R9                     AND SET UP ADDRESSABILITY
         L     R4,FCSXFND               SET NEW
         ST    R4,FCSXSTA                 STARTING ADDRESS
         LA    R4,=X'00'                SCAN FOR NON-BLANK
         ST    R4,FCSXDLM
         LA    R1,FCSXPARM              PRELOAD R1
         CALL  SCANDLMT                 POSITION AT FIRST EXCLUDE NAME
         L     R4,FCSXFND               GET FIRST NON-BLANK
         LTR   R4,R4                    FIND ONE?
         BZ    RETEXCLC                   NO, CAN THE JOB!
         ST    R4,FCSXSTA               SET STARTING ADDRESS
         LA    R4,=X'02406B'            SCAN FOR BLANK OR COMMA
         ST    R4,FCSXDLM
         XC    FCSXDLMT,FCSXDLMT        CLEAR RESULT INDICATORS
         SPACE
PRCEXCLB EQU   *
*======================================================================
*  FIND NEXT EXCLUDE NAME AND MOVE IT IN
*======================================================================
         CALL  SCANDLMT                 SCAN FOR NEXT DELIMITER
         L     R4,FCSXFND               GET DELIMITER ADDR.
         LTR   R4,R4                    IF ONE WAS FOUND,
         BNZ   PRCEXCLD                   CONTINUE
         L     R4,FCSXEND               ELSE, END OF CARD WAS REACHED
         LA    R4,1(R4)                   SET ADDRESS TO END OF CARD
         SPACE
PRCEXCLD L     R6,FCSXSTA               START OF EXCLUDE NAME
         SR    R4,R6                    GET LENGTH OF EXCLUDE NAME
         BNP   RETEXCLC                   CANCEL IF 0 OF NEGATIVE
         CH    R4,=H'8'                 IF > 8,
         BH    RETEXCLC                   TELL JES TO CAN THIS JOB
         BCTR  R4,R0                    ELSE DROP 1 FOR EXECUTED MOVE
         $QSUSE ,                       SYNC. CHECKPOINT AND WAIT
         MVC   JQEXEXCN,$BLANKS          BLANK OUT CURRENT JQE ENTRY
         EX    R4,PRCMEXCL                AND MOVE IN EXCLUDE NAME
         OI    JQEXFLG2,JQEXXCLU        TURN ON EXCLUDE BIT
         SPACE
*======================================================================
*  TEST FOR END OF CARD
*======================================================================
*        L     R4,FCSXFND               GET DELIMITER ADDRESS
*        LTR   R4,R4                    IF ZERO,
*        BZ    PRCEXCLF                   END OF CARD
*        CLI   0(R4),C' '               IF DELIMITER FOUND IS BLANK
*        BE    PRCEXCLF                   END OF CARD
*        SPACE
*        LA    R4,1(R4)                 POINT TO NEXT POSITION
*        C     R4,FCSXEND               IF > = END OF CARD,
*        BNL   PRCEXCLF                   WE'RE DONE
*        ST    R4,FCSXSTA               ELSE UPDATE STARTING POINT
*        B     PRCEXCLB                   AND CONTINUE
         SPACE
PRCEXCLF EQU   *
*======================================================================
*  RETURNS
*======================================================================
         SPACE
RETEXCL8 SR    R15,R15                  CLEAR RETURN CODE
         LA    R15,8                    SET FOR JES2 TO IGNORE
         B     RETEXCL
         SPACE
RETEXCLC L     R1,FCSXWRK+4             RELOAD ENTRY R1 VALUE
         L     R2,4(R1)                   ADDRESS RXITFLAG
         OI    0(R2),X'20'              INDICATE MESSAGE SUPPLIED
         L     R2,8(R1)                 RELOAD JCTXWRK
         MVC   0(80,R2),PRCIEXCL         MOVE IN MESSAGE
         MVC   0(1,R2),$CCOMCHR          INDICATE PRI/SEC SUBSYSTEM
         SR    R15,R15
         LA    R15,12
         SPACE
RETEXCL  $RETURN RC=(R15)               RETURN TO CALLER
         SPACE
PRCMEXCL MVC   JQEXEXCN(1),0(R6)            MOVE IT INTO PLACE
PRCIEXCL DC    CL80'$HASPFCS INVALID /*EXCLUDE STATEMENT'
         LTORG
         DROP  R3,R9
         TITLE 'ROUTINE PRCRLSE - PROCESS RELEASE JECL STATEMENT'
***********************************************************************
*                                                                     *
* ROUTINE: PRCRLSE - SUBROUTINE TO PROCESS RELEASE JECL STATEMENTS    *
*                    FOR DEPENDENT JOB CONTROL.                       *
*                                                                     *
* JECL FORMAT: FIXED FORMAT AS FOLLOWS.                               *
*                                                                     *
*              COLS  1- 8  /*RELEASE                                  *
*                    9 71  ONE OR MORE BLANKS FOLLOWED BY RELEASE     *
*                          NAMES SEPARATED BY COMMAS.                 *
*                                                                     *
* INPUT  R1    - FCSXWRK                                              *
*        R10   - JCT                                                  *
*        R11   - HCT                                                  *
*        R13   - PCE                                                  *
*        R14   - RETURN                                               *
*        R15   - ENTRY                                                *
*                                                                     *
* USAGE  R1    - WORK                                                 *
*        R2    - DEFAULT ERROR MESSAGE TO BE ISSUED                   *
*        R3    - FCSXWRK                                              *
*        R4    - WORK                                                 *
*        R9    - JQE                                                  *
*        R12   - BASE                                                 *
*                                                                     *
* OUTPUT                                                              *
*        R15   - RETURN CODE FOR JES2                                 *
*                                                                     *
***********************************************************************
         SPACE 1
PRCRLSE  CSECT
         $SAVE
         USING PRCRLSE,R12              SETUP
         LR    R12,R15                   BASE
         LR    R3,R1                    GET WORK AREA ADDRESS
         USING FCSXWRK,R3                 AND SET UP ADDRESSABILITY
         L     R9,PCEJQE                GET ADDRESS OF JQE
         USING JQE,R9                     AND SET UP ADDRESSABILITY
         L     R4,FCSXFND               SET NEW
         ST    R4,FCSXSTA                 STARTING ADDRESS
         LA    R4,=X'00'                SCAN FOR NON-BLANK
         ST    R4,FCSXDLM
         LA    R1,FCSXPARM              PRELOAD R1
         CALL  SCANDLMT                 POSITION AT FIRST RELEASE NAME
         L     R4,FCSXFND               GET FIRST NON-BLANK
         LTR   R4,R4                    FIND ONE?
         BZ    RETRLSEC                   NO, CAN THE JOB!
         ST    R4,FCSXSTA               SET STARTING ADDRESS
         LA    R4,=X'02406B'            SCAN FOR BLANK OR COMMA
         ST    R4,FCSXDLM
         XC    FCSXDLMT,FCSXDLMT        CLEAR RESULT INDICATORS
         SPACE
PRCRLSEB EQU   *
*======================================================================
*  FIND NEXT RELEASE NAME AND MOVE IT IN
*======================================================================
         CALL  SCANDLMT                 SCAN FOR NEXT DELIMITER
         L     R4,FCSXFND               GET DELIMITER ADDR.
         LTR   R4,R4                    IF ONE WAS FOUND,
         BNZ   PRCRLSED                   CONTINUE
         L     R4,FCSXEND               ELSE, END OF CARD WAS REACHED
         LA    R4,1(R4)                   SET ADDRESS TO END OF CARD
         SPACE
PRCRLSED L     R6,FCSXSTA               START OF RELEASE NAME
         SR    R4,R6                    GET LENGTH OF RELEASE NAME
         BNP   RETRLSEC                   CANCEL IF 0 OF NEGATIVE
         CH    R4,=H'8'                 IF > 8,
         BH    RETRLSEC                   TELL JES TO CAN THIS JOB
         BCTR  R4,R0                    ELSE DROP 1 FOR EXECUTED MOVE
         $QSUSE ,                       SYNC. CHECKPOINT AND WAIT
         MVC   JQEXRLSN,$BLANKS          BLANK OUT CURRENT JQE ENTRY
         EX    R4,PRCMRLSE                AND MOVE IN RELEASE NAME
         OI    JQEXFLG2,JQEXRLSE        TURN ON RELEASE BIT
         SPACE
*======================================================================
*  TEST FOR END OF CARD
*======================================================================
*        L     R4,FCSXFND               GET DELIMITER ADDRESS
*        LTR   R4,R4                    IF ZERO,
*        BZ    PRCRLSEF                   END OF CARD
*        CLI   0(R4),C' '               IF DELIMITER FOUND IS BLANK
*        BE    PRCRLSEF                   END OF CARD
*        SPACE
*        LA    R4,1(R4)                 POINT TO NEXT POSITION
*        C     R4,FCSXEND               IF > = END OF CARD,
*        BNL   PRCRLSEF                   WE'RE DONE
*        ST    R4,FCSXSTA               ELSE UPDATE STARTING POINT
*        B     PRCRLSEB                   AND CONTINUE
         SPACE
PRCRLSEF EQU   *
*======================================================================
*  RETURNS
*======================================================================
         SPACE
RETRLSE8 SR    R15,R15                  CLEAR RETURN CODE
         LA    R15,8                    SET FOR JES2 TO IGNORE
         B     RETRLSE
         SPACE
RETRLSEC L     R1,FCSXWRK+4             RELOAD ENTRY R1 VALUE
         L     R2,4(R1)                   ADDRESS RXITFLAG
         OI    0(R2),X'20'              INDICATE MESSAGE SUPPLIED
         L     R2,8(R1)                 RELOAD JCTXWRK
         MVC   0(80,R2),PRCIRLSE         MOVE IN MESSAGE
         MVC   0(1,R2),$CCOMCHR          INDICATE PRI/SEC SUBSYSTEM
         SR    R15,R15
         LA    R15,12
         SPACE
RETRLSE  $RETURN RC=(R15)               RETURN TO CALLER
         SPACE
PRCMRLSE MVC   JQEXRLSN(1),0(R6)            MOVE IT INTO PLACE
PRCIRLSE DC    CL80'$HASPFCS INVALID /*RELEASE STATEMENT'
         LTORG
         DROP  R3,R9
         TITLE 'ROUTINE PRCCHN - PROCESS RELEASE JECL STATEMENT'
***********************************************************************
*                                                                     *
* ROUTINE: PRCCHN - SUBROUTINE TO PROCESS CHAIN JECL STATEMENTS       *
*                    FOR DEPENDENT JOB CONTROL.                       *
*                                                                     *
* JECL FORMAT: FIXED FORMAT AS FOLLOWS.                               *
*                                                                     *
*              COLS  1- 8  /*CHAIN                                    *
*                    9 71  ONE OR MORE BLANKS FOLLOWED BY CHAIN       *
*                          NAMES SEPARATED BY COMMAS.                 *
*                                                                     *
* INPUT  R1    - FCSXWRK                                              *
*        R10   - JCT                                                  *
*        R11   - HCT                                                  *
*        R13   - PCE                                                  *
*        R14   - RETURN                                               *
*        R15   - ENTRY                                                *
*                                                                     *
* USAGE  R1    - WORK                                                 *
*        R2    - DEFAULT ERROR MESSAGE TO BE ISSUED                   *
*        R3    - FCSXWRK                                              *
*        R4    - WORK                                                 *
*        R9    - JQE                                                  *
*        R12   - BASE                                                 *
*                                                                     *
* OUTPUT                                                              *
*        R15   - RETURN CODE FOR JES2                                 *
*                                                                     *
***********************************************************************
         SPACE 1
PRCCHN   CSECT
         $SAVE
         USING PRCCHN,R12               SETUP
         LR    R12,R15                   BASE
*======================================================================
*  RETURNS
*======================================================================
         SPACE
         SR    R15,R15                  CLEAR RETURN CODE
         LA    R15,8                    SET FOR JES2 TO IGNORE
         SPACE
RETCHN   $RETURN RC=(R15)               RETURN TO CALLER
         SPACE
         LTORG
         TITLE 'J004$JEC COMMON ROUTINES; '
         SPACE 1
         TITLE 'SCANDLMT - SCAN A FIELD FOR ONE OR MORE DELIMITERS'
*----------------------------------------------------------------------
*
* USE THIS ROUTINE TO FIND A DELIMITER WITHIN A FIELD.  MORE THAN ONE
* DELIMITER CAN BE SCANNED FOR.  IF YOU WANT TO SCAN A FIELD FOR THE
* FIRST NON-BLANK, SET THE NUMBER OF DELIMITERS TO ZERO.
*
* R1 - PARAMETER LIST
*
*      +0 - DELIMITER LIST.  FIRST BYTE = BINARY # DELIMITERS IN LIST
*                            FOLLOWED BY DELIMITERS.  IE., TO SCAN A
*                            A FIELD FOR (,) YOU WOULD CODE X'034D6B5D'
*      +4 - BEGINNING OF FIELD TO BE SCANNED
*      +8 - END OF FIELD TO BE SCANNED.
*     +12 - ADDRESS OF FOUND DELIMITER
*
*----------------------------------------------------------------------
         SPACE
SCANDLMT CSECT
         USING SCANDLMT,R15
         $SAVE                     SAVE CALLERS REGISTERS
         L     R2,0(R1)            POINT TO DELIMITER LIST
         L     R7,4(R1)            GET FIELD STARTING ADDRESS
         LA    R8,1                INCREMENT
         L     R9,8(R1)            GET FIELD ENDING ADDRESS
         SR    R3,R3               (CLEAN UP R3)
         SPACE
DLMTLOOP IC    R3,0(R2)            # CHARACTERS TO SCAN FOR.
         LTR   R3,R3               IF 0,
         BZ    NONBLNK               SCAN FOR FIRST NON-BLANK
         LA    R4,1(R2)            POINT TO LIST OF DELIMITERS
         SPACE
CKDLMT   CLC   0(1,R7),0(R4)       FIND A HIT?
         BE    DONE                  YES, SET RETURN POINTERS
         LA    R4,1(R4)              NO, POINT TO NEXT DELIMITER
         BCT   R3,CKDLMT               AND CONTINUE
         SPACE
         BXLE  R7,R8,DLMTLOOP      POINT TO NEXT BUFFER ADDRESS
         SPACE
         SR    R7,R7               INDICATE NO DELIMITER FOUND
         B     DONE
         SPACE
NONBLNK  CLI   0(R7),C' '
         BNE   DONE
         BXLE  R7,R8,NONBLNK
         SR    R7,R7               INDICATE NO DELIMITER FOUND
         B     DONE
         SPACE
DONE     ST    R7,12(R1)           SET ADDRESS OF DELIMITER
         $RETURN RC=0
         LTORG
         $MODEND
         END
