         TITLE 'PRESERVE RACF USERID ACROSS NJE NODES'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS EXIT WILL PRESERVE THE RACF USERID IN THE USER          *
*        NJE JOB HEADER.  IT WILL RESTORE THIS FIELD WHENEVER         *
*        NECESSARY.  THIS REQUIRES THAT AN APPROPRIATE NJE            *
*        USER HEADER BE CREATED PRIOR TO ENTRY INTO THIS EXIT.        *
*        SUPPORT IS ALSO CODED FOR PROPAGATION OF THE VM USERID       *
*        AS WELL.                                                     *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                   JOHN V. HOOPER                                    *
*                   FIRST UNION NATIONAL BANK                         *
*                   301 S. COLLEGE ST                                 *
*                   DC-7  FCS-12                                      *
*                   CHARLOTTE, N.C.  28288                            *
*                   (704) 374-2427                                    *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J 0 0 7 R A C F            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $CKPT        SYS1.HASPSRC                               *
*        04)  $ENTRY       SYS1.HASPSRC                               *
*        05)  $FCSUHDR     SYS1.HASPSRC (USER)                        *
*        06)  $GETSMFB     SYS1.HASPSRC                               *
*        07)  $HASPEQU     SYS1.HASPSRC                               *
*        08)  $HASPGBL     SYS1.HASPSRC                               *
*        09)  $HCT         SYS1.HASPSRC                               *
*        10)  $JCT         SYS1.HASPSRC                               *
*        11)  $JQE         SYS1.HASPSRC                               *
*        12)  $KIT         SYS1.HASPSRC                               *
*        13)  $MIT         SYS1.HASPSRC                               *
*        14)  $MODEND      SYS1.HASPSRC                               *
*        15)  $MODULE      SYS1.HASPSRC                               *
*        16)  $NHD         SYS1.HASPSRC                               *
*        17)  $NHDGET      SYS1.HASPSRC                               *
*        18)  $PADDR       SYS1.HASPSRC                               *
*        19)  $PCE         SYS1.HASPSRC                               *
*        20)  $QSUSE       SYS1.HASPSRC                               *
*        21)  $SAVE        SYS1.HASPSRC                               *
*        22)  $SMF         SYS1.HASPSRC                               *
*        23)  $RETURN      SYS1.HASPSRC                               *
*        24)  IEESMCA      SYS1.AMODGEN                               *
*        25)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J007RACF $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='PRESERVE RACF USERID ACROSS NJE NODES',          X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $KIT,               GENERATE HASP KIT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $NHD,               GENERATE HASP NDH DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $SMF,               GENERATE HASP SMF DSECT             X
               RPL,                GENERATE MVS  RPL DSECT             X
               SMCA                GENERATE MVS  SMCA DSECT
         TITLE 'PRESERVE RACF USERID ACROSS NJE NODES'
         EJECT
***********************************************************************
*                                                                     *
*        USER JOB HEADER                                              *
*                                                                     *
***********************************************************************
         SPACE 3
         $FCSUHDR
J007RACF CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E007RACF $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R10,R1              SET JCT ADDRESS
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         SPACE 3
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - A CODE PASSED TO YOUR ROUTINE BY JES2.                 *
*              0 - INDICATES THAT THE JCT HAS BEEN READ FROM SPOOL    *
*              4 - INDICATES THAT THE JCT WILL BE WRITTEN TO SPOOL    *
*        R1  - ADDRESS OF THE BUFFER THAT CONTAINS THE JCT            *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE PCE                                     *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         SPACE 3
         LTR   R0,R0               TEST ENTRY REASON CODE
         BZ    RETURN0             ZERO, READ ONLY, NOT INTERESTED
         CLI   JCTJOBID,C'J'       SEE IF BATCH JOB
         BNE   RETURN0             NO, NOT INTERESTED
         CLI   JCTJCLAS,C'0'       SEE IF NUMERIC CLASS (XBM)
         BNL   RETURN0             YES, NOT INTERESTED
***********************************************************************
*        FIND OUR SPECIAL NJE USER HEADER                             *
***********************************************************************
         LA    R8,JCTNJHDR         POINT TO START OF NETWORK HEADERS
         USING FCSUHDR,R8          SET ADDRESSABILITY TO HEADERS
         LH    R5,0(R8)            LOAD LENGTH OF NETWORK HEADERS
         AR    R5,R8               GET END ADDRESS OF NETWORK HEADERS
         LA    R8,4(R8)            LOAD START ADDRESS OF FIRST HEADER
CKHD1    CR    R8,R5               SEE IF END OF HEADERS
         BNL   RETURN0             YES, CANNOT SAVE ANYTHING
         TM    FCSUHTYP,NTYPUSER   SEE IF USER HEADER
         BNO   CKHD2               NO, GO GET NEXT ONE
         CLC   FCSUHID,=CL4'FCSH'  SEE IF OUR HEADER
         BE    CKHD4               YES, GO CHECK IT
CKHD2    AH    R8,0(R8)            POINT TO NEXT HEADER
         B     CKHD1               LOOP
         SPACE 3
***********************************************************************
*        FIND THE GENERAL NJE HEADER                                  *
***********************************************************************
CKHD4    $NHDGET HEADER=JCTNJHDR,TYPE=NTYPGEN,MOD=NJHG$MOD,            X
               NOSEC=RETURN0       GET NJE GENERAL SECTION
         LR    R6,R1               SAVE GENERAL HEADER ADDRESS
         USING NJHG,R6             SET ADDRESSABILITY TO GENERAL HDR
         SPACE 3
***********************************************************************
*        FIND THE JES2 NJE HEADER                                     *
***********************************************************************
CKHD5    $NHDGET HEADER=JCTNJHDR,TYPE=NTYPJES2,MOD=NJH2$MOD,           X
               NOSEC=RETURN0       GET JES2 GENERAL HEADER
         LR    R7,R1               SAVE JES2 HEADER ADDRESS
         USING NJH2,R7             SET ADDRESSABILITY TO JES2 HEADER
         EJECT
***********************************************************************
*                                                                     *
*        SAVE THE VERIFIED USERID (IF ONE)                            *
*                                                                     *
***********************************************************************
SHDR     CLI   NJHGXEQU,C' '       SEE IF VM LOGONID SPECIFIED
         BNH   SHDR1               NO, NOT VM, GO PROCESS JES2
         CLC   NJHGUSID,NJHGXEQU   SEE IF JOB USERID = VM USERID
         BNE   SHDR1               NO, GO PROCESS JES2
         MVC   FCSUHUSR,NJHGXEQU   SAVE AS A VERIFIED USERID
         B     RHDR                GO RESTORE VERIFIED USERID
SHDR1    CLI   NJH2USR,C' '        SEE IF NJH2USR HAS BEEN SET
         BNH   SHDR2               NO, BRANCH
         TM    JCTFLAG2,JCT2RPAI   SEE IF RACF VERIFIED NJH2USR
         BNO   SHDR2               NO, BRANCH
         MVC   FCSUHUSR,NJH2USR    SAVE VERIFIED USER ID
         B     RHDR                GO RESTORE VERIFIED USERID
SHDR2    CLI   FCSUHUSR,C' '       SEE IF ALREADY HAVE A VERIFIED USER
         BH    RHDR                YES, GO RESTORE VERIFIED USERID
         MVC   FCSUHUSR,NJH2SUSR   SAVE SUBMITTERS USER ID
         EJECT
***********************************************************************
*                                                                     *
*        RESTORE THE VERIFIED USERID (IF REQUIRED)                    *
*                                                                     *
***********************************************************************
RHDR     CLI   NJH2SUSR,C' '       SEE IF NJH2SUSR HAS BEEN SET
         BH    RETURN0             YES, NO REASON TO RESTORE
         CLI   NJH2USR,C' '        SEE IF NJH2USR HAS BEEN SET
         BNH   RHDR1               NO, BRANCH
         TM    JCTFLAG2,JCT2RPAI   SEE IF RACF VERIFIED NJH2USR
         BO    RETURN0             YES, NO NEED TO RESTORE ANYTHING
RHDR1    CLI   NJH2SUSR,C' '       SEE IF NJH2SUSR HAS BEEN SET
         BH    RETURN0             YES, NO REASON TO RESTORE
RHDR2    CLI   FCSUHUSR,C' '       SEE IF ANYTHING TO RESTORE
         BNH   RETURN0             NO, RETURN TO JES2
         MVC   NJH2SUSR,FCSUHUSR   RESTORE SUBMITTERS ID FROM HEADER
         B     RETURN0             RETURN TO JES2
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*                                                                     *
***********************************************************************
RETURN0  LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
         SPACE 3
         DROP  R6                  DROP ADDRESSABILITY TO GENERAL HDR
         DROP  R7                  DROP ADDRESSABILITY TO JES2 HEADER
         DROP  R8                  DROP ADDRESSABILITY TO USER HEADER
         EJECT
***********************************************************************
*                                                                     *
*                   CONSTANTS USED IN THIS EXIT                       *
*                                                                     *
***********************************************************************
         SPACE 3
         LTORG
         EJECT
         $MODEND
         END
