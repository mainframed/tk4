JESTABLE TITLE 'BUILD JES2 JOBNAME/ROOM/PGMR NAME TABLE'
***********************************************************************
*                                                                     *
*        THIS PROGRAM WILL BUILD A JOBNAME/ROOM/PGMR NAME TABLE       *
*        IN CSA TO BE USED BY JES2 TO SET ROOM AND/OR PROGRAMMER      *
*        NAME VALUES FOR STARTED TASKS AND TSO LOGONS WHICH DO        *
*        NOT NORMALLY PROVIDE THIS INFORMATION TO JES2.  THIS         *
*        TABLE WILL BE USED BY THE PRINT/PUNCH SEPARATOR EXIT         *
*        (EXIT1) TO PRINT THE APPROPRIATE PROGRAMMER NAME AND         *
*        BIN NUMBER ON THE JOB SEPARATOR PAGE FOR OUTPUT FROM         *
*        STARTED TASKS AND TSO USERS.                                 *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
***********************************************************************
*                                                                     *
*        START OF PROGRAM                                             *
*                                                                     *
***********************************************************************
JESTABLE CSECT
         USING JESTABLE,R12        SET PROGRAM ADDRESSABILITY
         SAVE  (14,12)             SAVE REGISTERS
         LR    R12,R15             INITIALIZE BASE REG WITH ENTRY POINT
         LR    R15,R13             SAVE CALLING'S SAVE AREA ADDR
         LA    R13,NEWSAVE         LOAD ADDR OF NEW SAVE AREA
         ST    R13,8(R15)          STORE NEW SAVE ADDR IN OLD
         ST    R15,4(R13)          STORE OLD SAVE ADDR IN NEW
***********************************************************************
*        GET STORAGE FOR TABLE WORK AREA                              *
***********************************************************************
         L     R0,GETSIZE          LOAD SUBPOOL AND SIZE OF AREA
         GETMAIN R,LV=(0)          GET WORK AREA
         LR    R2,R1               SAVE GETMAINED ADDRESS
         ST    R2,GETMAIN          SAVE GETMAINED ADDRESS
         MVC   0(4,R2),GETSIZE     SAVE GETMAIN PARM IN TBL PREFIX
         L     R4,GETSIZE          LOAD SIZE OF WORK AREA
         LA    R2,8(R2)            INCREMENT PAST PREFIX
         LA    R4,0(R4)            CLEAR HIGH ORDER BYTE
         SH    R4,=H'8'            SUBTRACT 8 FOR LENGTH OF PREFIX
         SRDL  R4,32               SHIFT TO ODD REG OF PAIR
         D     R4,=F'32'           DIVIDE BY LENGTH OF EACH ENTRY
         ST    R5,TBLENTS          SAVE MAX NUMBER OF TABLE ENTRIES
         SLR   R3,R3               SET NUMBER OF TABLE ENTRIES TO ZERO
***********************************************************************
*        OPEN INPUT FILE                                              *
***********************************************************************
         OPEN  (SYSIN,(INPUT))     OPEN INPUT FILE
***********************************************************************
*        READ INPUT FILE                                              *
*                                                                     *
*        1)  MOVE FIRST 32 CHARACTERS IN INPUT RECORD TO TABLE        *
*        2)  KEEP COUNT OF ENTRIES                                    *
*        3)  AT END, SAVE ENTRY COUNT IN TABLE PREFIX                 *
*                                                                     *
***********************************************************************
READ     GET   SYSIN               READ INPUT FILE
         LTR   R5,R5               TEST NUMBER OF ENTRIES LEFT
         BZ    ERROR1              ZERO, ERROR
         MVC   0(32,R2),0(R1)      SAVE FIRST 32 CHARACTERS OF RECORD
         LA    R2,32(,R2)          INCREMENT TO NEXT TABLE ENTRY
         LA    R3,1(,R3)           ADD 1 TO ENTRY COUNT
         BCTR  R5,0                REDUCE ENTRIES LEFT COUNT
         B     READ                GO READ ANOTHER INPUT RECORD
EOF      L     R2,GETMAIN          LOAD START OF WORK AREA
         ST    R3,4(R2)            SAVE ENTRY COUNT IN PREFIX
***********************************************************************
*        SORT THE TABLE ACCORDING TO THE JOBNAME                      *
*        WHICH IS THE FIRST 8 CHARACTERS IN THE TABLE                 *
***********************************************************************
SORT     L     R2,GETMAIN          LOAD START OF TABLE
         ICM   R3,15,4(R2)         LOAD TABLE ENTRY COUNT
         BZ    ERROR2              ZERO, NO ENTRIES
         LA    R2,8(,R2)           POINT TO FIRST ENTRY IN TABLE
         LR    R4,R3               SAVE ENTRY COUNT
         BCTR  R4,0                MAKE COUNT RELATIVE TO ZERO
         MH    R4,=H'32'           MULTIPLY BY LENGTH OF EACH ENTRY
         AR    R4,R2               SET ADDRESS OF LAST ENTRY
         LR    R5,R2               SET FLOATING ENTRY
SORT1    CLC   0(08,R5),0(R2)      COMPARE THIS ENTRY TO LOW ENTRY
         BNL   SORT2               NOT NEW LOW, BRANCH
         MVC   SAVEENT(32),0(R2)   SAVE OLD LOW ENTRY
         MVC   0(32,R2),0(R5)      SWAP THIS ENTRY
         MVC   0(32,R5),SAVEENT        WITH THE OLD LOW ENTRY
SORT2    LA    R5,32(,R5)          INCREMENT TO NEXT TABLE ENTRY
         CR    R4,R5               SEE IF END OF TABLE
         BNL   SORT1               NO, LOOP
         LA    R2,32(,R2)          INCREMENT TO NEXT LOW TABLE ENTRY
         LR    R5,R2               SET FLOATING ENTRY ADDRESS
         CR    R4,R2               SEE IF END OF TABLE
         BH    SORT2               NO, LOOP
***********************************************************************
*        GET THE EXACT AMOUNT OF CSA STORAGE TO CONTAIN THE           *
*        TABLE WHICH WE HAVE JUST SORTED.                             *
***********************************************************************
ENDSORT  L     R2,GETMAIN          LOAD ADDRESS OF WORK AREA
         L     R3,4(R2)            LOAD NUMBER OF ENTRIES
         MH    R3,=H'32'           MULTIPLY BY LENGTH OF EACH ENTRY
         LA    R3,8(,R3)           ADD LENGTH OF PREFIX
         STCM  R3,7,CSASIZE+1      SAVE CSA LENGTH
         L     R0,CSASIZE          LOAD CSA GETMAIN PARM
GETCSA   GETMAIN R,LV=(0)          GET CSA TABLE AREA
         LR    R6,R1               SAVE CSA TABLE ADDRESS
***********************************************************************
*        MOVE OUR WORK TABLE TO CSA.  DO NOT FORGET TO SAVE THE       *
*        GETMAIN PARM IN THE PREFIX IN ORDER TO FREEMAIN THE AREA     *
*        IF THE TABLE IS RELOADED.                                    *
***********************************************************************
         LR    R2,R6               SET ADDRESS OF 'TO' AREA
         L     R3,CSASIZE          LOAD SIZE OF TABLE
         LA    R3,0(,R3)           SET SIZE OF 'TO' AREA
         L     R4,GETMAIN          SET ADDRESS OF 'FROM' AREA
         LR    R5,R3               SET SIZE OF 'FROM' AREA
         MVCL  R2,R4               MOVE OUR TABLE TO CSA
         MVC   0(4,R6),CSASIZE     MOVE GETMAIN PARM TO CSA TBL PREFIX
***********************************************************************
*        SEE IF THE CVT USER FIELD ALREADY HAS A POINTER TO A         *
*        PREVIOUSLY LOADED TABLE.  IF SO, SWAP OUR WORK NEW CSA       *
*        TABLE ADDRESS FOR THE OLD TABLE, WAIT A WHILE FOR ANY        *
*        JES2 PROCESSING ON THE OLD TABLE TO COMPLETE, AND THEN       *
*        FREEMAIN THE OLD CSA TABLE.  UNLESS YOU ARE X/A, THERE       *
*        IS NEVER ENOUGH CSA TO GO WASTE.                             *
***********************************************************************
         L     R2,16               LOAD CVT ADDRESS
         USING CVT,R2              SET ADDRESSABILITY TO CVT
         MODESET KEY=ZERO          GET INTO KEY ZERO
         L     R3,CVTUSER          LOAD ADDRESS (IF ANY) OF PREV TABLE
         ST    R6,CVTUSER          SAVE NEW CSA TABLE ADDRESS IN CVT
         MODESET KEY=NZERO         GET OUT OF KEY ZERO
         LTR   R3,R3               TEST OLD TABLE ADDRESS
         BZ    CLOSE               ZERO, FIRST TIME
         STIMER WAIT,BINTVL=ABIT   WAIT A BIT
         L     R0,0(R3)            LOAD FREEMAIN PARM IN PREFIX
         FREEMAIN R,LV=(0),A=(R3)  FREEMAIN OLD CSA TABLE AREA
CLOSE    CLOSE (SYSIN)             CLOSE INPUT FILES
         L     R13,4(R13)          RESTORE ORIG SAVE
         RETURN (14,12),RC=0       RETURN
         EJECT
***********************************************************************
*        ERROR ROUTINES                                               *
***********************************************************************
         SPACE 3
ERROR1   WTO   'BLDJ001E MAXIMUM NUMBER OF JOBNAME TABLE ENTRIES EXCEEDX
               ED - JOB ABORTED',ROUTCDE=2
         ABEND 001,DUMP
         SPACE 3
ERROR2   WTO   'BLDJ002E NO INPUT JOBNAME ENTRIES SUPPLIED - JOB ABORTEX
               D',ROUTCDE=2
         ABEND 001,DUMP
         EJECT
         LTORG
GETSIZE  DS    0F                  GETMAIN PARM FOR WORK AREA
         DC    AL1(0),AL3(40*1024) SUBPOOL 0 - 40K
CSASIZE  DS    0F                  GETMAIN PARM FOR CSA STORAGE
         DC    AL1(241),AL3(0)     SUBPOOL 241
GETMAIN  DS    F                   ADDRESS OF GETMAINED WORK AREA
TBLENTS  DS    F                   MAXIMUM TBL ENTRIES IN WORK AREA
SAVEENT  DS    CL32                WORK AREA FOR CORE SORT
NEWSAVE  DS    18F
ABIT     DC    A(1500)             STIMER INTERVAL (15.00 SECONDS)
         PRINT NOGEN
SYSIN    DCB   DDNAME=SYSIN,MACRF=GL,DSORG=PS,EODAD=EOF
         PRINT GEN
         EJECT
         CVT   DSECT=YES
         END
