J021$57  TITLE 'FCS NJE SMF EXIT'
***********************************************************************
*                                                                     *
* MODULE NAME - J021$57                                               *
*                                                                     *
* DESCRIPTIVE NAME - NETWORKING SMF EXIT                              *
*                                                                     *
* JES2 LEVEL - SP 2.2.0                                               *
*                                                                     *
*                                                                     *
* FUNCTION - THIS JES2 EXIT PROGRAM IS DESIGNED EXAMINE AND/OR        *
*            MODIFY ALL JES2 SMF RECORDS.  THE SMF TYPE 57 RECORD     *
*            IS WRITTEN FOR EACH NJE SYSOUT TRANSMISSION WHICH        *
*            OCCURS.  THE ONLY PROBLEM IS THAT THE JOBNAME IS NOT     *
*            CONTAINED ANYWHERE IN THE RECORD.  THIS EXIT WILL PUT    *
*            THE JOBNAME INTO THE TYPE 57 RECORD IN THE NETACCT       *
*            FIELD WHICH IS AN EIGHT BYTE FIELD WHICH IS UNUSED       *
*            BY OUR INSTALLATION.                                     *
*                                                                     *
* EXIT ENVIRONMENT                                                    *
*                                                                     *
*    JES2 MAIN TASK                                                   *
*                                                                     *
* MODULE ATTRIBUTES                                                   *
*                                                                     *
*    JES2 REENTRANT                                                   *
*                                                                     *
* ENTRY POINTS = E021$57 - PROCESS SMF 57 NJE RECORDS.                *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J021$57 $MODULE $CAT,                                                  X
               $CAT,               GENERATE HASP CAT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $SMF,               GENERATE HASP SMF BUFFER DSECT      X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               RPL
         TITLE 'JES2 NJE SMF 57 RECORD EXIT'
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - ZERO                                                   *
*        R1  - SMF BUFFER ADDRESS                                     *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE CALLER'S PCE                            *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
E021$57  $ENTRY BASE=R12
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         SPACE 1
         USING SMF,R1              SET ADDRESSABILITY TO SMF BUFFER
         CLI   SMF57RTY,57         SEE IF TYPE 57 RECORD
         BNE   RETURN0             NO, JUST GET OUT
         MVC   SMF57ACN,=CL8' '    BLANK THE NETWORK ACCT FIELD
         ICM   R2,15,PCEJQE        LOAD JQE ADDRESS FROM PCE
         USING JQE,R2              SET ADDRESSABILITY TO JQE
         BZ    RETURN0             JQE ADDRESS ZERO, EXIT
         MVC   SMF57ACN,JQEJNAME   MOVE JOBNAME TO NETWORK ACCT FIELD
         B     RETURN0             GET OUT
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - TERMINATE NORMAL SMF QUEUE PROCESSING              *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
RETURN4  DS    0H
         LA    R15,4               SET RETURN CODE TO 4
         $RETURN RC=(R15)          RETURN TO JES2
RETURN8  DS    0H
         LA    R15,8               SET RETURN CODE TO 8
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
         LTORG ,                   ROUTINE LITERAL POOL
         TITLE 'J021$57 END'
         $MODEND
         END
