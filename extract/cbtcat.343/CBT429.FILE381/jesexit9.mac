         TITLE 'JES2 OUTPUT EXCESSION EXIT'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 1.3.4  OR SP 1.3.6                      *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED EXAMINE ALL OCCURRENCES   *
*        OF OUTPUT EXCESSION.  IF THE JOB IS A TEST JOB, IT WILL      *
*        BE ALLOWED TO ABEND WITH A S722 ABEND.  IF NOT, THE JOB      *
*        WILL BE ALLOWED TO CONTINUE AFTER ISSUEING THE NORMAL        *
*        JES2 WARNING MESSAGE                                         *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST COMPUTER SERVICES                      *
*                        420 BRUSHY MOUNTAIN ROAD                     *
*                        WILKESBORO, N.C. 28697                       *
*                        (919) 651-5414                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J E S E X I T 9            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $ENTRY       SYS1.HASPSRC                               *
*        03)  $HASPEQU     SYS1.HASPSRC                               *
*        04)  $HASPGBL     SYS1.HASPSRC                               *
*        05)  $JCT         SYS1.HASPSRC                               *
*        06)  $MIT         SYS1.HASPSRC                               *
*        07)  $MODEND      SYS1.HASPSRC                               *
*        08)  $MODULE      SYS1.HASPSRC                               *
*        09)  $SCAT        SYS1.HASPSRC                               *
*        10)  $SVT         SYS1.HASPSRC                               *
*        11)  $XECB        SYS1.HASPSRC                               *
*        12)  IFGRPL       SYS1.MACLIB                                *
*        13)  RETURN       SYS1.MACLIB                                *
*        14)  SAVE         SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*        THIS MODULE MUST BE PLACED INTO SYS1.LPALIB OR INCLUDED      *
*        AS AN 'MLPA' OR 'FLPA' MEMBER INTO COMMONLY ADDRESSABLE      *
*        STORAGE.  FAILURE TO DO SO WILL NORMALLY RESULT IN S0C4      *
*        ABENDS AS HASPSSSM BRANCHES TO THE THE CORRECT ROUTINE       *
*        ADDRESS BUT IN THE USERS ADDRESS SPACE INSTEAD OF THE        *
*        JES2 ADDRESS SPACE.                                          *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
JESEXIT9 $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=USER,                                           X
               TITLE='JES2 OUTPUT EXCESSION EXIT',                     X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $JCT,               GENERATE HASP JCT DSECT             X
               $SCAT,              GENERATE HASP SCAT DSECT            X
               $SVT,               GENERATE HASP SVT DSECT             X
               $XECB,              GENERATE HASP XECB DSECT            X
               $MIT,               GENERATE HASP MIT DSECT             X
               RPL                 GENERATE MVS  RPL DSECT
         TITLE 'JES2 OUTPUT EXCESSION EXIT'
         PRINT OFF
         AIF   ('&VERSION' NE 'SP 1.3.6' AND                           X
               '&VERSION' NE 'SP 2.1.5').VEREND
         PRINT ON
***********************************************************************
*                                                                     *
*        EQUATED VARIABLES FOR SP 1.3.6 OR SP 2.1.5                   *
*                                                                     *
***********************************************************************
         SPACE 3
$SVDSECT EQU   SVT
         SPACE 3
         USING JCT,R10             SET ADDRESSABILITY TO JCT
.VEREND  ANOP
         PRINT ON
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
EXIT9    $ENTRY BASE=R12
         SPACE 1
         SAVE (14,12)              SAVE REGISTERS
         SPACE 1
         LR    R12,R15             LOAD BASE REGISTER
         LR    R10,R7              MOVE JCT ADDRESS TO PROPER REG
         USING $SVDSECT,R11        SET ADDRESSABILITY TO SSVT
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0       - A CODE INDICATING THE TYPE OF OUTPUT              *
*                   0 - INDICATES A PRINT OUTPUT RECORD               *
*                   4 - INDICATES A PUNCH OUTPUT RECORD               *
*        R1       - THE CURRENT RECORD COUNT                          *
*        R7       - ADDRESS OF THE JCT                                *
*        R11      - ADDRESS OF THE SSVT                               *
*        R13      - ADDRESS OF AN OS STYLE SAVE AREA                  *
*        R14      - RETURN ADDRESS                                    *
*        R15      - ENTRY ADDRESS                                     *
*                                                                     *
***********************************************************************
         CLI   JCTCLASS,C'0'       TEST CLASS
         BL    PROD                NOT NUMERIC, PRODUCTION
         SPACE 3
TEST     ICM   R0,15,ABEND         LOAD ABEND INDICATOR
         ST    R0,20(R13)          SET NEW REG0 IN SAVE AREA
         B     RETURN8             GET OUT
         SPACE 3
PROD     ICM   R0,15,EXTEND        LOAD EXTEND INDICATOR
         ST    R0,20(R13)          SET NEW REG0 IN SAVE AREA
         B     RETURN8             GET OUT
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - TAKE ACTION ABSED ON THE RETURN PROCESSING         *
*                  FLAGS IN R0                                        *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         RETURN (14,12),RC=(15)    RETURN TO JES2
RETURN4  DS    0H
         LA    R15,4               SET RETURN CODE TO 4
         RETURN (14,12),RC=(15)    RETURN TO JES2
RETURN8  DS    0H
         LA    R15,8               SET RETURN CODE TO 8
         RETURN (14,12),RC=(15)    RETURN TO JES2
         EJECT
         LTORG
         SPACE 3
EXTEND   DC    X'80',AL3(0)        USE OUTPOPT = '0' CONTINUE
ABEND    DC    X'80',AL3(1)        USE OUTPOPT = '1' ABEND W/O DUMP
         EJECT
         $MODEND
         END
