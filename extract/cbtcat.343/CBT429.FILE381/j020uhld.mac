         TITLE 'CHANGE TYPRUN=HOLD TO USERHOLD'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS JES2 EXIT WILL GAIN CONTROL AT END-OF-INPUT TO          *
*        ALLOW MODIFICATION OF MANY OF A JOB'S ATTRIBUTES.            *
*        THIS EXIT WILL SPECIFICALLY MODIFY THE PURPOSE OF            *
*        TYPRUN=HOLD TO USE A SPECIAL BIT IN THE JQE TO INDICATE      *
*        USERHOLD INSTEAD OF OPERATOR HOLD.  IT WILL DO SO BY         *
*        TURNING ON THE USERHOLD BIT IN THE JQE AND TURNING OFF       *
*        THE TYPRUN=HOLD BIT IN THE JCT WHICH WOULD LATER HAVE        *
*        RESULTED IN THE JQE1HLD1 BIT BEING TURNED ON IN THE JQE.     *
*                                                                     *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*            M A C R O S   U S E D   I N   J 0 2 0 U H L D            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $CKPT        SYS1.HASPSRC                               *
*        04)  $ENTRY       SYS1.HASPSRC                               *
*        05)  $HASPEQU     SYS1.HASPSRC                               *
*        06)  $HASPGBL     SYS1.HASPSRC                               *
*        07)  $HCT         SYS1.HASPSRC                               *
*        08)  $JCT         SYS1.HASPSRC                               *
*        09)  $JQE         SYS1.HASPSRC                               *
*        10)  $KIT         SYS1.HASPSRC                               *
*        11)  $MIT         SYS1.HASPSRC                               *
*        12)  $MODEND      SYS1.HASPSRC                               *
*        13)  $MODULE      SYS1.HASPSRC                               *
*        14)  $PADDR       SYS1.HASPSRC                               *
*        15)  $PCE         SYS1.HASPSRC                               *
*        16)  $QSUSE       SYS1.HASPSRC                               *
*        17)  $SAVE        SYS1.HASPSRC                               *
*        18)  $RETURN      SYS1.HASPSRC                               *
*        19)  IFGRPL       SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       1K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J020UHLD $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='CHANGE TYPRUN=HOLD TO USERHOLD',                 X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $DCT,               GENERATE HASP DCT DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP SVT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $KIT,               GENERATE HASP KIT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               RPL                 GENERATE MVS  RPL DSECT
         TITLE 'CHANGE TYPRUN=HOLD TO USERHOLD'
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E020UHLD $ENTRY BASE=R12
         SPACE 1
         $SAVE                     SAVE CALLERS REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0  - ZERO                                                   *
*        R10 - ADDRESS OF THE JCT                                     *
*        R11 - ADDRESS OF THE HCT                                     *
*        R13 - ADDRESS OF THE PCE                                     *
*        R14 - RETURN ADDRESS                                         *
*        R15 - ENTRY ADDRESS                                          *
*                                                                     *
***********************************************************************
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         L     R7,JCTJQE           LOAD OFFSET OF JQE FROM JCT
         AL    R7,$JOBQPTR         GET ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
***********************************************************************
*                                                                     *
*        SEE IF TYPRUN=HOLD HAS BEEN SPECIFIED                        *
*                                                                     *
***********************************************************************
         SPACE 3
CK1      TM    JCTJBOPT,JCTTHOLD   SEE IF TYPRUN=HOLD SPECIFIED
         BZ    RETURN0             NO, GET OUT
         CLC   JCTXEQND,$OWNNODE   SEE IF OUR JOB
         BNE   RETURN0             NO, LEAVE IT ALONE
         $QSUSE ,                  GET CONTROL OF THE CHECKPOINT
         OI    JQEXFLG2,JQEXUHLD   TURN ON USER HOLD INDICATOR
         LR    R1,R7               SET JQE ADDRESS
         $CKPT ID=JQE,ADDR=(R1)    TELL JES2 ABOUT THE CHANGE
         NI    JCTJBOPT,255-JCTTHOLD TURN OFF TYPRUN=HOLD IND
         B     RETURN0             GET OUT
         EJECT
***********************************************************************
*                                                                     *
*                    E X I T     R O U T I N E S                      *
*                                                                     *
*        RC = 00 - CONTINUE USING ANY ADDITIONAL EXITS                *
*        RC = 04 - CONTINUE WITHOUT USING ANY ADDITIONAL EXITS        *
*        RC = 08 - TERMINATE NORMAL PROCESSING AND PRINT THE OUTPUT   *
*        RC = 12 - TERMINATE NORMAL PROCESSING WITHOUT PRINTING       *
*                                                                     *
***********************************************************************
RETURN0  DS    0H
         LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
***********************************************************************
*                                                                     *
*                   CONSTANTS USED IN THIS EXIT                       *
*                                                                     *
***********************************************************************
         SPACE 3
         LTORG
         $MODEND
         END
