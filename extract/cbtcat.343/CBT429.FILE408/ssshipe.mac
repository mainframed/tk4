         MACRO
&NME     SSSHIPE &IID=,&IADR=,&IDA=,&SVC#=,&PRIO=,&MF=D
         LCLC  &LIID,&LIADR,&LIDA,&LSVC#,&LPRIO,&LBL,&R,&D
&LIID    SETC  '*'
         AIF   ('&IID' EQ '').GOTIID
&LIID    SETC  '&IID'
         AIF   ('&IID'(1,1) NE '''').GOTIID
&LIID    SETC  ' '
         AIF   (K'&IID LE 2).GOTIID
&LIID    SETC  '&IID'(2,K'&IID-2)
.GOTIID  ANOP
&LIADR   SETC  '*-*'
         AIF   ('&IADR' EQ '').GOTIADR
&LIADR   SETC  '&IADR'
.GOTIADR ANOP
&LIDA    SETC  '*-*'
         AIF   ('&IDA' EQ '').GOTIDA
&LIDA    SETC  '&IDA'
.GOTIDA  ANOP
&LSVC#   SETC  '*-*'
         AIF   ('&SVC#' EQ '').GOTSVC#
&LSVC#   SETC  '&SVC#'
.GOTSVC# ANOP
&LPRIO   SETC  '128'
         AIF   ('&PRIO' EQ '').GOTPRIO
&LPRIO   SETC  '&PRIO'
.GOTPRIO AIF   ('&MF(1)' EQ 'E').MFE
         AIF   ('&MF(1)' EQ 'L').MFL
         AIF   ('&MF(1)' EQ 'D').MFD
         MNOTE 8,'MF(1)=&MF(1) IS INVALID. MF(1)=D IS ASSUMED.'
.MFD     ANOP
&D       SETC  'DSECT'
         AIF   ('&MF(2)' EQ '').MFDOK3
         AIF   ('&MF(2)'(1,1) EQ 'Y').MFDOK3
&D       SETC  'DS'
         AIF   ('&MF(2)'(1,1) EQ 'N').MFDOK3
         MNOTE 8,'MF(2)=&MF(2) IS INVALID. MF(2)=N IS ASSUMED.'
.MFDOK3  ANOP
*************************************************************
*                                                           *
*        SSSHIPE -- INTERCEPT PARAMETER ENTRY. THIS AREA    *
*        DESCRIBES DATA ASSOCIATED WITH INDIVIDUAL          *
*        SUB-SCREEN INTERCEPT ROUTINES.                     *
*                                                           *
*************************************************************
         SPACE 1
         AIF   ('&NME' EQ ' ').MFDOK1
&NME     &D    0F
SSSHIPE  DS    0F                  MAP NAME
         AGO   .MFDOK2
.MFDOK1  ANOP
SSSHIPE  &D    0F
.MFDOK2  ANOP
SSSHIID  DC    CL8'&LIID'          IDENTIFIER ASSOCIATED WITH THIS
*                                  INTERCEPT.
SSSHIADR DC    A(&LIADR)           ADDRESS OF THE INTERCEPT ROUTINE. A
*                                  ZERO VALUE MEANS THAT THE INTERCEPT
*                                  DESCRIBED BY THIS IPE IS TO BE
*                                  DELETED.
SSSHIDA  DC    A(&LIDA)            VALUE TO BE MADE AVAILABLE TO THE
*                                  INTERCEPT ROUTINE.
SSSHSVC# DC    AL1(&LSVC#)         SVC WHICH THIS ROUTINE INTERCEPTS.
SSSHPRIO DC    AL1(&LPRIO)         PRIORITY AT WHICH THIS INTERCEPT
*                                  ROUTINE IS QUEUED.
SSSHCC   DC    FL1'0'              HIGHEST COMPLETION CODE ASSOCIATED
*                                  WITH THE ATTEMPT TO QUEUE OR DELETE
*                                  THIS INTERCEPT ROUTINE. MUST BE
*                                  PRE-ZEROED BY THE CALLER.
SSSHIEND DS    0F                  END OF THE IPE.
SSSHILEN EQU   SSSHIEND-SSSHIPE    LENGTH OF THE IPE.
         MEXIT
.MFL     ANOP
&NME     DS    0F
         DC    CL8'&LIID'          IDENTIFIER ASSOCIATED WITH THIS
*                                  INTERCEPT
         DC    A(&LIADR)           ADDRESS OF THE INTERCEPT ROUTINE. A
*                                  ZERO VALUE MEANS THAT THE INTERCEPT
*                                  DESCRIBED BY THIS IPE IS TO BE
*                                  DELETED.
         DC    A(&LIDA)            VALUE TO BE MADE AVAILABLE TO THE
*                                  INTERCEPT ROUTINE.
         DC    AL1(&LSVC#)         SVC WHICH THIS ROUTINE INTERCEPTS.
         DC    AL1(&LPRIO)         PRIORITY AT WHICH THIS INTERCEPT
*                                  ROUTINE IS QUEUED.
         DC    FL1'0'              HIGHEST COMPLETION CODE ASSOCIATED
*                                  WITH THE ATTEMPT TO QUEUE OR DELETE
*                                  THIS INTERCEPT ROUTINE. MUST BE
*                                  PRE-ZEROED BY THE CALLER.
         MEXIT
.MFE     ANOP
&LBL     SETC  '&NME'
&R       SETC  '1'
         AIF   ('&MF(2)' NE '').GETMF2
&LBL     LA    &R,SSSHIPE          --> IPE
&LBL     SETC  ''
         AGO   .GOTMF2
.GETMF2  AIF   ('&MF(2)'(1,1) EQ '(').MF2REG
&LBL     LA    &R,&MF(2)           --> IPE
&LBL     SETC  ''
         AGO   .GOTMF2
.MF2REG  ANOP
&R       SETC  '&MF(2)'(2,K'&MF(2)-2)
.GOTMF2  ANOP
&LBL     MVI   SSSHCC-SSSHIPE(&R),0 RESET THE COMPLETION CODE
&LBL     SETC  ''
         AIF   ('&IID' EQ '').NOIID
         AIF   (K'&LIID LE 8).LIIDOK
         MNOTE 4,'IID=&IID IS TOO LONG.'
.LIIDOK  ANOP
         MVC   SSSHIID-SSSHIPE(,&R),=CL8'&LIID' SET NEW IID
.NOIID   AIF   ('&IADR' EQ '').NOIADR
         AIF   ('&IADR' NE '&IADR(1)').IADRREG
         LA    0,&IADR             --> INTERCEPT
         ST    0,SSSHIADR-SSSHIPE(,&R) STORE
         AGO   .NOIADR
.IADRREG ST    &IADR(1),SSSHIADR-SSSHIPE(,&R) STORE DATA AREA ADDRESS
.NOIADR  AIF   ('&IDA' EQ '').NOIDA
         AIF   ('&IDA' NE '&IDA(1)').IDAREG
         LA    0,&IDA              --> DATA AREA
         ST    0,SSSHIDA-SSSHIPE(,&R) STORE
         AGO   .NOIDA
.IDAREG  ST    &IDA(1),SSSHIDA-SSSHIPE(,&R) STORE DATA AREA ADDRESS
.NOIDA   AIF   ('&SVC#' EQ '').NOSVC#
         AIF   ('&SVC#' NE '&SVC#(1)').SVC#REG
         MVI   SSSHSVC#-SSSHIPE(&R),&SVC# SET SVC NUMBER
         AGO   .NOSVC#
.SVC#REG STC   &SVC#(1),SSSHSVC#-SSSHIPE(,&R) STORE SVC NUMBER
.NOSVC#  AIF   ('&PRIO' EQ '').NOPRIO
         AIF   ('&PRIO' NE '&PRIO(1)').PRIOREG
         MVI   SSSHPRIO-SSSHIPE(&R),&PRIO SET QUEUING PRIORITY
         AGO   .NOPRIO
.PRIOREG STC   &PRIO(1),SSSHPRIO-SSSHIPE(,&R) STORE QUEUING PRIORITY
.NOPRIO  ANOP
         MEND
