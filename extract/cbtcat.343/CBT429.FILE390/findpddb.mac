FINDPDDB TITLE 'QUEUE COMMAND - FIND PDDB FOR A DSID'             ONL01
FINDPDDB QSTART ,                  START FINDPDDB CSECT ASSEMBLY  ONL01
         USING QCKPT,R10          BASE REG FOR CHECKPONT WORK AREA
         L     R10,QVCKPT         LOAD BASE REG
         USING QDISPLAY,R9        BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL          LOAD BASE REG
         USING WORK,R13           BASE REG FOR LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   CONVERT DATASET ID TO BINARY                                      *
*                                                                     *
***********************************************************************
         XC    CONVERT,CONVERT       CLEAR CONVERT WORK AREA
         PACK  CONVERT+5(3),DSID+1(3) PACK ASID
         CVB   R7,CONVERT         CONVERT DSID TO BINARY
***********************************************************************
*                                                                     *
*   FIND  PDDB  FOR  THIS  DATASET  ID                                *
*                                                                     *
***********************************************************************
NORMAL   MVI   SWITCH,0           INITIALIZE SWITCH
         USING PDB,R2             BASE REG FOR PDDB
         USING IOT,R3             BASE REG FOR IOT                UF046
         L     R5,QCIOTA          I/O AREA FOR READ IOT BLOCK     UF046
         L     R1,QCJCTA          ADDR OF JCT                     UF008
         USING JCT,R1             SET TEMP ADDRESSING             ONL16
         CLC   QCTRAK,JCTIOT      AT FIRST IOT?                   UF008
         BNE   *+8                NO, SKIP FLAG SET               UF008
         OI    SWITCH,X'02'       SET FLAG                        UF008
         DROP  R1                 DROP TEMP ADDRESSING            UF008
         LR    R3,R5              IOTSTART AREA                   UF046
         SH    R3,=AL2(IOTSTART-IOT)  ADJUST FOR PROPER STRT      UF046
         SPACE 1                                                  UF046
NEXTIOT  LR    R4,R3              BASE OF IOT
         CLC   IOTID,=CL4'IOT'    WAS AN IOT READ FROM SPOOL ?    ONL03
         BNE   BADIOT             ERROR IF NO                     ONL03
         CLC   QPJOBID,IOTJBKEY   IS THE IOT'S JOB KEY VALID ?    ONL03
         BNE   BADIOT             ERROR IF NO                     ONL03
         A     R4,IOTPDDBP        OFFSET BEYOND LAST PDDB
         LR    R2,R3              BASE OF IOT
         A     R2,IOTPDDB          OFFSET TO FIRST PDDB IN IOT    UF046
FINDDS   C     R7,PDBDSKEY        IS THIS THE DATASET?            WGH42
         BE    FOUNDDS            YES. CONTINUE
         LA    R2,PDBLENG(R2)     NO. LOOK AT NEXT PDDB
         CR    R2,R4              HAVE WE GONE PAST THE LAST PDDB
         BL    FINDDS             NO. TRY AGAIN
         L     R4,IOTIOTTR        DISK ADDR OF NEXT IOT
SPIN     LTR   R4,R4              IS THERE ANOTHER IOT?
         BZ    SPINIOT            NO. TRY THE SPIN IOT.
         BAL   R8,READ            READ THE IOT
         B     NEXTIOT            SEARCH THE NEXT IOT
         USING JCT,R1             BASE REG FOR JCT                ONL16
SPINIOT  TM    SWITCH,1           DID WE SEARCH THE SPINIOT ALREADY
         BO    CKIOT1             YES, SEE IF WE STARTED AT FRONT UF008
         OI    SWITCH,1           SET SWITCH
         L     R1,QCJCTA          LOAD BASE REG
         L     R4,JCTSPIOT        DISK ADDR OF SPIN IOT
         DROP  R1
         B     SPIN               SEARCH THE SPIN IOT CHAIN
FOUNDDS  L     R0,PDBRECCT        GET THE RECORD COUNT
         CVD   R0,CONVERT         CONVERT TO DECIMAL
         MVC   DSRECCT,ED8        MOVE EDIT PATTERN TO DISPLAY
         ED    DSRECCT,CONVERT+4  EDIT THE RECORD COUNT
         MVC   DSCLASS,PDBCLASS   MOVE PDBCLASS TO DISPLAY
STOP     QSTOP                    GO BACK TO CALLER
CKIOT1   TM    SWITCH,X'02'       DID WE START AT FIRST IOT?      UF008
         BO    STOP               YES, NOT FOUND                  UF008
         USING JCT,R1             SET TEMP ADDRESSING             ONL16
         L     R1,QCJCTA          POINT TO JCT                    UF008
         L     R4,JCTIOT          FIRST IOT ADDRESS               UF008
         DROP  R1                 DROP TEMP ADDRESSING            UF008
         BAL   R8,READ            READ THE IOT                    UF008
         OI    SWITCH,X'02'       SET STARTED AT FRONT            UF008
         B     NEXTIOT            AND TRY AGAIN                   UF008
***********************************************************************
*                                                                     *
*   READ A BLOCK FROM HASPACE                                         *
*                                                                     *
***********************************************************************
READ     ST    R4,QCTRAK          STORE DISK ADDR
         LR    R1,R5              IOAREA ADDRESS
         L     R15,=V(READSPC)    ADDR OF ROUTINE TO READ HASPACE
         BALR  R14,R15            GO TO IT
         BR    R8                 RETURN TO CALLER
         SPACE 3                                                  ONL03
BADIOT   QTILT '*** JOB HAS PURGED (OR IOT IS INVALID) ***'       ONL03
         SPACE 2                                                  ONL03
ED8      DC    X'4020202020202120'
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
FINDPDDB CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         $BUFFER ,                                                UF046
         IFGRPL
         $TAB
         $JCT
         $PDDB
         $IOT
         $HASPEQU
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         ORG   QDMSG
         DS    CL36                             WAS CL24 - WGH
DSID     DS    CL4
         DS    CL4
DSRECCT  DS    CL8
         DS    CL4
DSCLASS  DS    CL1
WORK     DSECT
         DS    CL72
SWITCH   DS    C
CONVERT  DS    D
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
