AUTH     TITLE 'AUTHORIZATION MODULE FOR THE QUEUE COMMAND'       ONL02
******************************************************************ONL02
*                                                                 ONL02
* MODULE NAME = AUTH                                              ONL02
*                                                                 ONL02
* DESCRIPTIVE NAME = AUTHORIZATION MODULE FOR THE QUEUE COMMAND   ONL02
*                                                                 ONL02
* AUTHOR = ROBERT M. JINKINS                                      ONL02
*          OAK RIDGE NATIONAL LABORATORY                          ONL02
*          BLDG. 4500-N  MS-259                                   ONL02
*          P.O. BOX X                                             ONL02
*          OAK RIDGE, TENNESSEE  37830                            ONL02
*                                                                 ONL02
*          PHONE: (615) 574-7208/5300                             ONL02
*                  FTS  624-7208/5300                             ONL02
*                                                                 ONL02
*          SHARE INSTALLATION CODE = OR                           ONL02
*                                                                 ONL02
* DATE WRITTEN = JUNE 83                                          ONL02
*                                                                 ONL02
* FUNCTION = TO PROVIDE A SECURITY FUNCTION WHICH CONTROLS THE    ONL02
*    USE THE TSO QUEUE COMMAND, ITS SUB-COMMANDS, AND THE JOBS    ONL02
*    OR DATASETS A USER MAY ACCESS, CHANGE, OR DELETE.  THE AUTH  ONL02
*    CSECT IS INVOKED AT VARIOUS POINTS DURING QUEUE COMMAND      ONL02
*    PROCESSING VIA A QAUTHCK MACRO.                              ONL02
*                                                                 ONL02
* DEFAULT OPERATION = THE DEFAULT AUTHORITY CHECKING FOR THE      ONL02
*    QUEUE COMMAND IS AS FOLLOWS:                                 ONL02
*                                                                 ONL02
*    (1) THE START-UP ENTRY FROM THE INIT CSECT GIVES JES2        ONL02
*        SYSTEM PROGRAMMER AUTHORITY TO ANYONE HAS OPERATOR       ONL02
*        AUTHORITY.  SINCE JES2 SYSTEM PROGRAMMER AUTHORITY HAS   ONL02
*        GREAT POWER, MANY USERS WILL PROBABLY WANT TO CHANGE     ONL02
*        THIS TO SUIT THE REQUIREMENTS OF THEIR INSTALLATION.     ONL02
*        THE SAME CONSIDERATIONS APPLY TO EXEC AUTHORITY (THE     ONL02
*        ABILITY TO ISSUE TSO COMMANDS WHILE IN QUEUE) BECAUSE    ONL02
*        THE QUEUECMN AREA COULD BE ALTERED BY USER PROGRAMS      ONL02
*        EXECUTING IN THAT ENVIRONMENT.                           ONL02
*                                                                 ONL02
*    (2) THE START-UP ENTRY FROM THE INIT CSECT ALSO ISSUES A     ONL02
*        TESTAUTH MACRO.  IF THE QUEUE COMMAND IS NOT APF         ONL02
*        AUTHORIZED, THEN THE AUTHORITY FOR USING THE SUB-SYSTEM  ONL02
*        INTERFACE COMMANDS IS TURNED OFF.                        ONL02
*                                                                 ONL02
*    (3) THE START-UP ENTRY FROM THE INIT CSECT ALSO SETS THE     ONL02
*        HELP CONTROL FLAGS FROM THE AUTHORIZATION FLAGS.  SEE    ONL02
*        THE HELP CSECT FOR MORE INFORMATION ON THE HELP CONTROL  ONL02
*        FLAGS.                                                   ONL02
*                                                                 ONL02
*    (4) THE ENTRY FROM THE PARSE ROUTINE IS USED TO DETERMINE    ONL02
*        WHO MAY USE A GIVEN QUEUE SUB-COMMAND.  A SUB-COMMAND    ONL02
*        IS ALLOWED TO PROCEED IF ALL SUB-COMMAND TYPE            ONL02
*        RESTRICTIONS ('TYPE=' CODED IN THE QSCE MACRO) HAVE      ONL02
*        CORRESPONDING AUTHORIZATION BITS SET ON IN THE           ONL02
*        AUTHORIZATION FLAGS (QAFLAGS).                           ONL02
*                                                                 ONL02
*    (5) THE ENTRY FROM THE FINDJOB ROUTINE IS ENTERED FOR ANY    ONL02
*        JOB WHOSE INPUT/OUTPUT IS TO BE ACCESSED BY A QUEUE      ONL02
*        COMMAND FUNCTION.  THE FUNCTION IS ALLOWED TO PROCEED    ONL02
*        IF THE JOBNAME BEGINS WITH THE USER'S USERID OR IF THE   ONL02
*        USER HAS OPERATOR OR JES2 SYSTEM PROGRAMMER AUTHORITY.   ONL02
*        ALTHOUGH THE ENTRY FROM THE FINDJOB ROUTINE MAY ALLOW    ONL02
*        THE FUNCTION TO PROCEED, OTHER ENTRIES MAY FUTHER        ONL02
*        RESTRICT THE ACCESS GRANTED BY THIS ENTRY.               ONL02
*                                                                 ONL02
*    (6) THE LIST COMMAND MAY NOT LIST DATASET ID'S LESS THAN     ONL02
*        101 UNLESS THE USER HAS JES2 SYSTEM PROGRAMMER           ONL02
*        AUTHORITY.                                               ONL02
*                                                                 ONL02
*    (7) THE DEL, REQ, AND CAN COMMANDS MAY NOT ACCESS A JOB      ONL02
*        WHOSE JOBNAME DOES NOT BEGIN WITH THE USER'S USERID      ONL02
*        UNLESS THEY HAVE JES2 SYSTEM PROGRAMMER AUTHORITY.       ONL02
*                                                                 ONL02
* OPTIONAL OPERATION = CONDITIONAL ASSEMBLY OPTIONS ALTER THE     ONL02
*    DEFAULT OPERATION OF THE QUEUE COMMAND AS SHOWN BELOW:       ONL02
*                                                                 ONL02
*    FCI01  --  IF THE &QACF2 OPTION IS SET, THEN THE ENTRY FROM  FCI01
*               FINDJOB ALLOWS THE FOLLOWING:                     FCI01
*                                                                 FCI01
*                (1) USER WITH OPER AUTH MAY LOOK AT ANY JOB.     FCI01
*                (2) USER MAY LOOK AT OWN JOBS (USERID MATCHES    FCI01
*                    LOGONID IN JOB'S JCT).                       FCI01
*                (3) ACFSVC ISSUED TO CHECK ALL OTHER ACCESSES.   FCI01
*                                                                 FCI01
*    RNB03  --  IF THE &QRACF OPTION IS SET, THEN:                RNB03
*                                                                 RNB03
*                (1) THE CHECKPOINT/SPOOL PRE-OPEN ENTRY CHANGES  RNB03
*                    THE USERID IN THE ACEE TO ALLOW THE DATASETS RNB03
*                    TO BE OPENED IF WE ARE APF AUTHORIZED, RACF  RNB03
*                    IS UP, AND &QRACUSR IS NOT NULL.  THE POST-- RNB03
*                    OPEN ENTRY FROM INIT RESTORES THE ORIGINAL   RNB03
*                    USERID.  ATTENTION INTERRUPTS ARE INHIBITED  RNB03
*                    DURING THIS INTERVAL.                        RNB03
*                                                                 RNB03
*                (2) THE ENTRY FROM READSPC ZEROS THE OLD AND     RNB03
*                    NEW PASSWORDS FIELDS WHEN JCT'S ARE READ     RNB03
*                    FROM SPOOL.                                  RNB03
*                                                                 RNB03
*                (3) THE ENTRY FROM PARSE IS USED TO PROCESSE     RNB03
*                    THE XP COMMAND.  A RACHECK MACRO IS ISSUED   RNB03
*                    TO DETERMINE IF THE USER SHOULD BE GIVEN     RNB03
*                    JES2 SYSTEM PROGRAMMER AUTHORITY.            RNB03
*                                                                 RNB03
*                (4) THE ENTRY FROM FINDJOB ONLY CHECKS FOR       RNB03
*                    ACCESSES MADE BY THE XD COMMAND AND USES A   RNB03
*                    RACHECK MACRO TO CONTROL AND LOG ITS USE.    RNB03
*                                                                 RNB03
*    RNB05  --  IF THE &QRNB OPTION IS SET, THEN:                 RNB05
*                                                                 RNB05
*                (1) THE START-UP ENTRY SETS THE AUTHORITY AND    RNB05
*                    HELP DISPLAY CONTROL FLAGS FOR THE RNB       RNB05
*                    ENVIRONMENT.                                 RNB05
*                                                                 RNB05
*                (2) THE LIST COMMAND CAN NEVER BE USED FOR       RNB05
*                    DSID'S LESS THAN 101.  THE XD COMMAND        RNB05
*                    MUST BE USED INSTEAD.                        RNB05
*                                                                 RNB05
*                (3) SPECIAL USERID/JOBNAME/NOTIFY CHECKING IS    RNB05
*                    USED TO CONTROL DATASET ACCRESS BY THE LIST  RNB05
*                    COMMAND (SEE COMMENTS IN THE CODE).          RNB05
*                                                                 RNB05
*    RNB06  --  IF THE &QRNB OPTION IS SET, THEN THE ENTRY FROM   RNB06
*               PARSE INHIBITS THE MODEL, TSO, AND EXEC COMMANDS. RNB06
*                                                                 RNB06
*    RNB08  --  IF THE &QRNB OPTION IS SET, THEN THE ENTRY FROM   RNB08
*               SYSOUT (THE DEL, REQ, AND CAN COMMANDS) IS USED   RNB08
*               TO PERFORM SPECIAL USERID/JOBNAME/NOTIFY/USER=    RNB08
*               CHECKING (SEE COMMENTS IN THE CODE).              RNB03
*                                                                 RNB08
*    ONL06  --  IF THE &QONL OPTION IS SET, JES2 SYSTEM           ONL06
*               PROGRAMMER AUTHORITY IS ONL GRANTED ONLY IF THE   ONL06
*               USER HAS OPERATOR AUTHORITY AND THEIR USERID      ONL06
*               IS CONTAINED IN A TABLE IN THIS ASSEMBLY.         ONL06
*                                                                 ONL06
* CHANGE ACTIVITY = NONE                                          ONL02
*                                                                 ONL02
******************************************************************ONL02
         EJECT ,                                                  ONL02
         GBLB  &QACF2              ACF2 OPTION                    FCI01
         GBLB  &QRACF              RACF OPTION                    RNB03
         GBLB  &QRNB               RNB OPTION                     RNB05
         GBLB  &QONL               ONL OPTION                     ONL06
         SPACE 1                                                  FCI01
         GBLC  &ACF2XIT,&ACF2DCT,&ACF2JCT,&OFFSET                 FCI01
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  DO ENTRY POINT HOUSEKEEPING FOR THE AUTH CSECT                 ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
AUTH     QSTART ,                  START AUTH CSECT ASSEMBLY      ONL02
         SPACE 2                                                  ONL02
*                                                                 ONL02
*  SETUP ADDRESSABILITY FOR COMMON SUB-AREAS AND SAVE/WORK AREA   ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
         USING WORK,R13            NOTE SAVE/WORK ADDRESSABILITY  ONL02
         SPACE 1                                                  ONL02
         L     R10,QVDAIR          ADDR OF DAIR WORK AREA         ONL02
         USING QDAIR,R10           NOTE DAIR WORK ADDRESSABILITY  ONL02
         SPACE 1                                                  ONL02
         L     R9,QVCKPT           ADDR OF CKPT WORK AREA         ONL02
         USING QCKPT,R9            NOTE CKPT WORK ADDRESSABILITY  ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  VALIDATE AUTHORIZATION REQUEST CODE AND ENTER PROCESSING RTN   ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         CLI   QACODE,MAXCODE      IS REQUEST CODE WITHIN RANGE ? ONL02
         BH    BOMB                ERROR IF NO                    ONL02
         SPACE 1                                                  ONL02
         SLR   R15,R15             ENTER                          ONL02
         IC    R15,QACODE           PROCESSING ROUTINE            ONL02
         SLL   R15,2                 PER                          ONL02
         B     VECTOR(R15)            AUTHORIZATION REQUEST CODE  ONL02
         SPACE 1                                                  ONL02
VECTOR   DS    0H                  AUTH REQUEST CODE VECTOR       ONL02
         B     Q#NULL           0  NULL REQUEST                   ONL02
         B     Q#START          1  QUEUE COMMAND START-UP         ONL02
         B     Q#END            2  QUEUE COMMAND TERMINATION      ONL02
         B     Q#ALLOC          3  DATASET ALLOCATION/UNALLOCATIONONL02
         B     Q#BEFOPN         4  BEFORE CKPT/SPOOL OPEN         ONL02
         B     Q#AFTOPN         5  AFTER CKPT/SPOOL OPEN          ONL02
         B     Q#SPOOL          6  READSPC RTN (AFTER SPOOL I/O)  ONL02
         B     Q#PARSE          7  QUEUE SUBCOMMAND AUTHORIZATION ONL02
         B     Q#FINDJ          8  FINDJOB RTN FOR JOB            ONL02
         B     Q#JCL            9  JCL CMD FOR JOB                ONL02
         B     Q#JLOG          10  JLOG CMD FOR JOB               ONL02
         B     Q#JMSG          11  JMSG CMD FOR JOB               ONL02
         B     Q#LIST          12  LIST CMD FOR JOB/DSID          ONL02
         B     Q#JHIST         13  JHIST CMD FOR JOB              ONL02
         B     Q#DD            14  DD CMD FOR JOB                 ONL02
         B     Q#PDDB          15  PDDB CMD FOR JOB               ONL02
         B     Q#SYSOUT        16  DEL/REQ/CAN CMDS FOR JOB       ONL02
         B     Q#SYSLOG        17  AUTHORIZATION TO LOOK AT SYSLOGONL02
         SPACE 1                                                  ONL02
MAXCODE  EQU   (*-4-VECTOR)/4      MAX SUPPORTED REQUEST CODE     ONL02
         SPACE 1                                                  ONL02
BOMB     DS    0H                  INVALID AUTH REQUEST CODE      ONL02
         WTO   'QUEUE COMMAND INTERNAL LOGIC ERROR IN AUTH RTN',  ONL02X
               ROUTCDE=11                                         ONL02
         SPACE 1                                                  ONL02
         ABEND 913                 ABEND U0913                    ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = NULL REQUEST                            ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#NULL   DS    0H                  NULL REQUEST                   ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = QUEUE COMMAND START-UP                  ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#START  DS    0H                  QUEUE COMMAND START-UP         ONL02
         SPACE 1                                                  ONL02
         AIF   (&QONL).ONL06S1                                    ONL06
         AIF   (&QRNB).RNB05S1                                    RNB05
*                                                                 ONL02
*  THE DEFAULT CODE FOR THIS ENTRY GIVES JES2 SYSTEM PROGRAMMER   ONL02
*  AUTHORITY AND THE ABILITY TO ISSUE TSO COMMANDS FROM QUEUE     ONL02
*  TO ANYONE WHO HAS OPERATOR AUTHORITY.                          ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
         OI    QAFLAGS,QAFXEXEC    GIVE ALL USERS TSO CMD AUTH    WGH
         TM    QAFLAGS,QAFXOPER    DOES USER HAVE OPER AUTHORITY? ONL02
         BZ    QSNOOPER            SKIP IF NO                     ONL02
         OI    QAFLAGS,QAFXSYSP+QAFXSLOG  GIVE USER UNLIMITED AUTH WGH
*        OI    QAFLAGS,QAFXSYSP+QAFXEXEC  GIVE USER UNLIMITED AUTHONL02
QSNOOPER DS    0H                                                 ONL02
         SPACE 2                                                  ONL02
         AGO   .ONL06S2                                           ONL06
.ONL06S1 ANOP  ,                                                  ONL06
*                                                                 ONL06
*  AT ONL, USERS ARE DIVIDED INTO THREE GROUPS:                   ONL06
*                                                                 ONL06
*    (1) ORDINARY USERS                                           ONL06
*    (2) USERS WITH THE TSO OPERATOR PRIVILEGE                    ONL06
*    (3) JES2 SYSTEM PROGRAMMERS                                  ONL06
*                                                                 ONL06
*  THESE GROUPS ARE GIVEN THE FOLLOWING QUEUE AUTHORIZATIONS:     ONL06
*                                                                 ONL06
*    AUTHORIZATION    ----GROUP----    DESCRIPTION                ONL06
*    -------------    (1)  (2)  (3)    -------------------------  ONL06
*      QAFXACTV             X    X     DISPLAY ACTIVE LISTS       ONL06
*      QAFXBLOG             X    X     DISPLAY BACKLOG LISTS      ONL06
*      QAFXSSSM        X    X    X     MAY USE CAN/DEL/REQ        ONL06
*      QAFXSLOG             X    X     MAY LOOK AT SYSLOG         ONL06
*      QAFXEXEC                  X     MAY ISSUE TSO CMDS IN QUE  ONL06
*      QAFXOPER             X    X     OPERATOR AUTHORITY         ONL06
*      QAFXSYSP                  X     JES2 SYSTEM PROGRAMMER     ONL06
*                                                                 ONL06
*  A USER IS GRANTED JES2 SYSTEM PROGRAMMER AUTHORITY ONLY        ONL06
*  IF THEY HAVE OPERATOR AUTHORITY AND THEIR USERID IS ALSO       ONL06
*  CONTAINED IN A TABLE BELOW (SEE SYMBOL SYSPTABL).              ONL06
*                                                                 ONL06
         SPACE 1                                                  ONL06
         NI    QAFLAGS,QAFXOPER    CLEAR ALL BUT OPER AUTHORITY   ONL06
         OI    QAFLAGS,QAFXSSSM    ASSUME GROUP 1 AUTHORITY       ONL06
         SPACE 1                                                  ONL06
         TM    QAFLAGS,QAFXOPER    DOES USER HAVE OPER AUTHORITY? ONL06
         BZ    QSNOOPER            SKIP IF NO                     ONL06
         SPACE 1                                                  ONL06
         OI    QAFLAGS,QAFXACTV+QAFXBLOG+QAFXSLOG ADD GROUP 2 AUTHONL06
         SPACE 1                                                  ONL06
         LA    R2,SYSPTABL         ADDR OF JES2 SYSTEM PGMR TABLE ONL06
         SPACE 1                                                  ONL06
QSNEXTSP DS    0H                  CHECK NEXT SYSTEM PGMR ENTRY   ONL06
         CLI   0(R2),X'FF'         END OF TABLE ?                 ONL06
         BE    QSNOSYSP            SKIP IF YES                    ONL06
         CLC   QLOGON,0(R2)        DOES USERID MATCH TABLE ENTRY? ONL06
         BE    QSSPHIT             SKIP IF YES                    ONL06
         LA    R2,8(,R2)           BUMP ADDR TO NEXT ENTRY        ONL06
         B     QSNEXTSP            LOOP TO CHECK NEXT ENTRY       ONL06
         SPACE 1                                                  ONL06
SYSPTABL DS    0CL8                USERID'S OF JES2 SYSTEM PGMR'S ONL06
         DC    X'FF'               NO SPECIAL USERS               WGH
         DC    CL8'SIC'                                           ONL06
         DC    CL8'TDC'                                           ONL06
         DC    X'FF'               FLAG END OF TABLE              ONL06
         SPACE 1                                                  ONL06
QSSPHIT  DS    0H                  USER ID FOUND IN SYS PGMR TABLEONL06
         OI    QAFLAGS,QAFXSYSP+QAFXEXEC  SET JES2 SYS-PGMR & EXECONL06
QSNOSYSP DS    0H                  CONT HERE IF NOT JES2 SYS PGMR ONL06
QSNOOPER DS    0H                  CONT HERE IF NO OPER AUTHORITY ONL06
         SPACE 1                                                  ONL06
.RNB05S1 AIF   (NOT &QRNB).RNB05S2                                RNB05
         SPACE 1                                                  RNB05
*                                                                 RNB05
*  AT RNB, THE AUTHORIZATION FLAGS ARE SET AS FOLLOWS:            RNB05
*                                                                 RNB05
         MVI   QAFLAGS,QAFXACTV+QAFXBLOG+QAFXSSSM+QAFXSLOG        RNB05
         SPACE 1                                                  RNB05
.RNB05S2 ANOP  ,                                                  RNB05
.ONL06S2 ANOP  ,                                                  ONL06
*                                                                 ONL02
*  THE DEFAULT CODE FOR THIS ENTRY TURNS OF AUTHORITY FOR THE     ONL02
*  SUB-SYSTEM INTERFACE COMMANDS IF THE QUEUE COMMAND IS NOT      ONL02
*  APF AUTHORIZED.                                                ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
         TESTAUTH FCTN=1           TEST FOR APF AUTHORIZATION     ONL02
         SPACE 1                                                  ONL02
         LTR   R15,R15             ARE WE APF AUTHORIZED?         ONL02
         BZ    QSNOTAPF            SKIP IF YES                    ONL02
         NI    QAFLAGS,255-QAFXSSSM  ELSE, TURN OFF SSSM AUTHORITYONL02
QSNOTAPF DS    0H                                                 ONL02
         SPACE 2                                                  ONL02
*                                                                 ONL02
*  THE DEFAULT CODE FOR THIS ENTRY SETS THE HELP CONTROL FLAGS    ONL02
*  FROM THE AUTHORITY FLAGS.                                      ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
         MVC   QAHELP,QAFLAGS      SET HELP FLAGS PER AUTHORITY   ONL02
         SPACE 1                                                  ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = QUEUE COMMAND TERMINATION               ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#END    DS    0H                  QUEUE COMMAND TERMINATION      ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = DATASET ALLOCATION/UNALLOCATION         ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#ALLOC  DS    0H                  DATASET ALLOCATION/UNALLOCATIONONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = PRE-OPEN FOR CHECKPOINT/SPOOL DATASETS  ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#BEFOPN DS    0H                  BEFORE CKPT/SPOOL OPEN         ONL02
         SPACE 1                                                  RNB03
         AIF   (NOT &QRACF).RNB03BO                               RNB03
         SPACE 1                                                  RNB03
         TESTAUTH FCTN=1           APF-AUTHORIZED?                RNB03
         LTR   R15,R15                                            RNB03
         BNZ   RNB03BO             /NO  - CAN'T CHG ACEEUSER      RNB03
*                                  /YES -                         RNB03
         RACSTAT ,                 IS RACF UP?                    RNB03
         LTR   R15,R15                                            RNB03
         BNZ   RNB03BO             /NO  - CAN'T CHG ACEEUSER      RNB03
*                                  /YES -                         RNB03
         CLI   QNEWUSR,0           IS THERE A NEW USERID TO USE?  RNB03
         BE    RNB03BO             /NO  - CAN'T CHG ACEEUSER      RNB03
*                                  /YES - CHANGE ACEEUSER TO      RNB03
*                                         ALLOW ACCESS TO THE     RNB03
*                                         SPOOL/CKPT DATA SETS    RNB03
         L     R2,CVTPTR           CVT                            RNB03
         USING CVT,R2              #####                          RNB03
         L     R2,CVTTCBP          TCB WORDS                      RNB03
         L     R2,12(,R2)          CURRENT ASCB                   RNB03
         USING ASCB,R2             #####                          RNB03
         L     R2,ASCBASXB         ASXB                           RNB03
         USING ASXB,R2             #####                          RNB03
         ICM   R2,15,ASXBSENV      ACEE                           RNB03
         BZ    RNB03BO             FORGET IT IF NO ACEE           RNB03
         USING ACEE,R2             #####                          RNB03
         CLC   =C'ACEE',ACEEACEE   REALLY AN ACEE?                RNB03
         BNE   RNB03BO             /NO  - FORGET IT               RNB03
         MVC   QUSRSAV,ACEEUSER    /YES - SAVE CURRENT USERID     RNB03
         STAX  DEFER=YES           DON'T ALLOW ATTENTION'S        RNB03
         MODESET KEY=ZERO          GET KEY ZERO TO UPDATE ACEE    RNB03
         MVC   ACEEUSER,QNEWUSR    SET NEW USERID                 RNB03
         MODESET KEY=NZERO         BACK TO USER KEY               RNB03
RNB03BO  DS    0H                                                 RNB03
         DROP  R2                  #####                          RNB03
         SPACE 1                                                  RNB03
.RNB03BO ANOP  ,                                                  RNB03
         SPACE 1                                                  RNB03
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = POST-OPEN FOR CHECKPOINT/SPOOL DATASETS ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#AFTOPN DS    0H                  AFTER CKPT/SPOOL OPEN          ONL02
         SPACE 1                                                  RNB03
         AIF   (NOT &QRACF).RNB03AO                               RNB03
         SPACE 1                                                  RNB03
         CLI   QUSRSAV,0           DID WE CHANGE ACEEUSER?        RNB03
         BE    RNB03AO             /NO  - SKIP THIS CODE          RNB03
*                                  /YES - PUT USERID BACK         RNB03
         L     R2,CVTPTR           CVT                            RNB03
         USING CVT,R2              #####                          RNB03
         L     R2,CVTTCBP          TCB WORDS                      RNB03
         L     R2,12(,R2)          CURRENT ASCB                   RNB03
         USING ASCB,R2             #####                          RNB03
         L     R2,ASCBASXB         ASXB                           RNB03
         USING ASXB,R2             #####                          RNB03
         ICM   R2,15,ASXBSENV      ACEE                           RNB03
         USING ACEE,R2             #####                          RNB03
         MODESET KEY=ZERO          GET KEY ZERO TO UPDATE ACEE    RNB03
         MVC   ACEEUSER,QUSRSAV    SET OLD USERID                 RNB03
         MODESET KEY=NZERO         BACK TO USER KEY               RNB03
         STAX  DEFER=NO            ALLOW ATTENTION INTERRUPTS     RNB03
RNB03AO  DS    0H                                                 RNB03
         DROP  R2                  #####                          RNB03
         SPACE 1                                                  RNB03
.RNB03AO ANOP  ,                                                  RNB03
         SPACE 1                                                  RNB03
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = READSPC ROUTINE (AFTER SPOOL I/O)       ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#SPOOL  DS    0H                  READSPC RTN (AFTER SPOOL I/O)  ONL02
         SPACE 1                                                  RNB03
         AIF   (NOT &QRACF).RNB03SR                               RNB03
*                                                                 RNB03
*  WIPE OUT THE OLD AND NEW PASSWORDS WHEN A JCT IS READ FROM     RNB03
*  THE SPOOL.                                                     RNB03
*                                                                 RNB03
         SPACE 1                                                  RNB03
         LR    R8,R1               COPY ADDR OF SPOOL BUFFER      RNB03
         SL    R8,=A(JCTSTART-JCT)  ADJUST FOR BUFFER PREFIX      ONL16
         USING JCT,R8              NOTE JCT ADDRESSABILITY        ONL16
         SPACE 1                                                  RNB03
         CLC   JCTID,=CL4'JCT'     DID WE READ A JCT?             RNB03
         BNE   RNB03SRO            /NO  - FORGET IT               RNB03
         CLC   JCTJNAME,JCTJMRJN   JOB NAME IN BOTH PLACES?       RNB03
         BNE   RNB03SRO            /NO  - FORGET IT               RNB03
         CLC   =C'JOB ',JCTJOBID   IS IT AN STC, A JOB, OR A TSU? RNB03
         BE    RNB03SRJ            IF SO, ASSUME THIS IS A JCT    RNB03
         CLC   =C'TSU ',JCTJOBID                                  RNB03
         BE    RNB03SRJ                                           RNB03
         CLC   =C'STC ',JCTJOBID                                  RNB03
         BNE   RNB03SRO                                           RNB03
         SPACE 1                                                  RNB03
RNB03SRJ DS    0H                  THIS MUST BE A JCT             RNB03
         XC    JCTPASS,JCTPASS     WIPE OUT THE OLD AND           RNB03
         XC    JCTNUPAS,JCTNUPAS    NEW PASSWORDS                 RNB03
RNB03SRO DS    0H                  CONT. HERE IF NOT A GOOD JCT   RNB03
         DROP  R8                  KILL JCT ADDRESSABILITY        RNB03
         SPACE 1                                                  RNB03
.RNB03SR ANOP  ,                                                  RNB03
         SPACE 1                                                  RNB03
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = QUEUE SUBCOMMAND AUTHORIZATION          ONL02
*                                                                 ONL02
*   THIS ENTRY IS USED TO CONTROL WHO CAN USE WHICH QUEUE         ONL02
*   SUB-COMMAND.  OTHER ENTRIES MAY FUTHER RESTRICT THE FUNCTION  ONL02
*   OF SOME SUB-COMMANDS.  INSTALLATIONS MAY TAYLOR THE FUNCTION  ONL02
*   OF THIS ENTRY BY ASSIGNING USER FLAGS IN THE PARSE TABLE      ONL02
*   (QSCE MACROS) AND BY INSERTING THEIR OWN CODE BELOW.          ONL02
*                                                                 ONL02
*   A SUB-COMMAND IS ALLOWED TO PROCEED IF ALL SUB-COMMAND TYPE   ONL02
*   RESTRICTIONS ('TYPE=' CODED IN THE QSCE MACRO) HAVE           ONL02
*   CORRESPONDING AUTHORIZATION BITS SET ON IN THE AUTHORIZATION  ONL02
*   FLAGS (QAFLAGS).                                              ONL02
*                                                                 ONL02
*   THE USER WILL GET THE HELP SCREEN IF THEY ENTER A COMMAND     ONL02
*   WHICH IS NOT ALLOWED.                                         ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#PARSE  DS    0H                  QUEUE SUBCOMMAND AUTHORIZATION ONL02
         SPACE 1                                                  ONL02
*  INSERT CODE HERE TO SUPERCEDE STANDARD CHECKING                ONL02
         SPACE 1                                                  ONL02
         ICM   R2,1,QASCFLGS       PICKUP SUB-COMMAND AUTH FLAGS  ONL02
         BZ    QPOK                SKIP IF NO RESTRICTIONS        ONL02
         EX    R2,QPTM             DOES USER HAVE THE AUTHORITY?  ONL02
         BO    QPOK                SKIP IF COMMAND IS ALLOWED     ONL02
         SPACE 1                                                  ONL02
*  INSERT CODE HERE TO ALLOW RESTRICTED SUB-COMMANDS TO PROCEED   ONL02
         SPACE 1                                                  ONL02
QPNOK    DS    0H                  COMMAND IS NOT ALLOWED         ONL02
         XC    QCODEH,QCODEH       CLEAR FUNCTION CODE            ONL02
         MVC   QSUBCMD,=V(HELP)    REDIRECT TO THE HELP ROUTINE   ONL02
         B     AUTHOK              PROCEED AS IF HELP WERE ENTEREDONL02
         SPACE 2                                                  ONL02
QPOK     DS    0H                  COMMAND IS ALLOWED             ONL02
         SPACE 1                                                  ONL02
*  INSERT CODE HERE TO IMPOSE ADDTIONAL RESTRICTIONS              ONL02
         AIF   (NOT &QRACF).RNB03P1                               RNB03
*                                                                 RNB03
*  ISSUE RACHECK MACRO AND SET JES2 SYSTEM PROGRAMMER AUTHORITY   RNB03
*  IF USER IS AUTHORIZED                                          RNB03
*                                                                 RNB03
         SPACE 1                                                  RNB03
         CLC   =C'XP',QSUBNAME     IS THIS THE XP COMMAND?        RNB03
         BNE   QPNOTXP             SKIP IF NO                     RNB03
         SPACE 1                                                  RNB03
         RACHECK ENTITY=QRACNMXP,MF=(E,QRACHECK)                  RNB03
         SPACE 1                                                  RNB03
         LTR   R15,R15             IS USER AUTHORIZED?            RNB03
         BNZ   QPNOK               IF NOT, DON'T ALLOW COMMAND    RNB03
         OI    QAFLAGS,QAFXSYSP    GIVE USER JES2 SYS PGMR AUTH   RNB03
         OI    QAHELP,QAHXSYSP     UPDATE HELP DISPLAY CNTL FLAGS RNB03
QPNOTXP  DS    0H                                                 RNB03
         SPACE 1                                                  RNB03
.RNB03P1 ANOP  ,                                                  RNB03
         AIF   (NOT &QRNB).RNB06P2                                RNB06
*                                                                 RNB06
*  AT RNB, CERTAIN COMMANDS ARE NOT ALLOWED                       RNB06
*                                                                 RNB06
         SPACE 1                                                  RNB06
         CLC   =C'MO',QSUBNAME     DO NOT                         RNB06
         BE    QPNOK                ALLOW                         RNB06
         CLC   =C'M ',QSUBNAME       THE MODEL,                   RNB06
         BE    QPNOK                  TSO, OR                     RNB06
         CLC   QSUBCMD,=V(CTSO)        EXEC                       RNB06
         BE    QPNOK                    COMMANDS                  RNB06
.RNB06P2 ANOP  ,                                                  RNB06
         SPACE 1                                                  ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
QPTM     TM    QAFLAGS,*-*         (EXECUTED) MATCH AUTH FLAGS    ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = FINDJOB ROUTINE FOR JOB AUTHORIZATION   ONL02
*                                                                 ONL02
*   THIS ENTRY IS THE FIRST LINE OF DEFENSE AT THE JOB LEVEL.  IT ONL02
*   IS ENTERED BEFORE A USER CAN LOOK AT A JOB'S INPUT/OUTPUT OR  ONL02
*   DO ANYTHING WITH A JOB.  OTHER ENTRIES IN THIS AUTHORIZATION  ONL02
*   MODULE MAY FUTHER REFINE A JOB LEVEL AUTHORITY GRANTED BY     ONL02
*   THIS ENTRY.                                                   ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#FINDJ  DS    0H                  FINDJOB RTN FOR JOB            ONL02
         SPACE 1                                                  ONL02
         AIF   (&QRACF).RNB03F1                                   RNB03
*                                                                 ONL02
*  THE DEFAULT CODE FOR THIS ENTRY IS TO ALLOW THE USER ACCESS    ONL02
*  TO ANY JOB WHOSE JOBNAME BEGINS WITH THEIR USERID.  USERS      ONL02
*  WITH OPERATOR AUTHORITY OR JES2 SYSTEM PROGRAMMER AUTHORITY    ONL02
*  HAVE ACCESS TO ALL JOBS IN THE SYSTEM.  THE ACCESS MAY BE      ONL02
*  RESTRICTED BY OTHER ENTRIES IN THIS MODULE.                    ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
         TM    QAFLAGS,QAFXOPSP    USER HAS OPER OR SYS PGMR AUTH?ONL02
         BNZ   AUTHOK              DONE IF YES, ALLOW ACCESS      ONL02
         SPACE 1                                                  ONL02
         L     R8,QCJCTA           ADDR OF JCT                    ONL02
         USING JCT,R8              NOTE JCT ADDRESSABILITY        ONL16
         SPACE 1                                                  ONL02
         ICM   R1,15,QLOGONLN      GET LENGTH OF USERID           ONL02
         BNP   FINDJNOK            ERROR IF NO LENGTH             ONL02
         BCTR  R1,0                LESS ONE FOR EXECUTE           ONL02
         EX    R1,FINDJCLC         JOBNAME BEGINS WITH USERID ?   ONL02
         BE    AUTHOK              DONE IF YES, ALLOW ACCESS      ONL02
         B     FINDJNOK            ELSE, GO TELL USER             ONL02
         SPACE 1                                                  ONL02
FINDJCLC CLC   JCTJNAME(*-*),QLOGON  EXECUTED - MATCH JOBNAME     ONL02
         SPACE 1                                                  ONL02
FINDJNOK DS    0H                  JOBNAME DOESN'T MATCH USERID   ONL02
         SPACE 1                                                  FCI01
         AIF   (NOT &QACF2).FCI01F1                               FCI01
*                                                                 FCI01
*  THIS CODE USES THE ACF2 SVC TO SEE IF THE USER MAY ACCESS A    FCI01
*  FICTITIOUS DSNAME OF THE FORM 'SYSOUT.LOGONID.JOBNAME'.  IF    FCI01
*  ACF2 WILL ALLOW THIS ACCESS, THEN THE QUEUE FUNCTION WILL      FCI01
*  PROCEED.                                                       FCI01
*                                                                 FCI01
         SPACE 1                                                  FCI01
         AIF   (NOT &QACF2).FCI01F2                               FCI01
         SPACE 1                                                  FCI01
         #ACFJES2 ,                SET ACF2 MVS/SP JES2 SPECS.    FCI01
         SPACE 1                                                  FCI01
         ICM   R7,15,&ACF2JCT      ACF2 AREA OFFSET IN JCT        FCI01
         BZ    ADSNNOK             DENY ACCESS IF NO ACF2 AREA    FCI01
         LA    R7,JCT(R7)          ADDR OF ACF2 AREA              ONL16
         USING #ACFJCTX,R7         NOTE ACF AREA 2ADDRESSABILITY  FCI01
         SPACE 1                                                  FCI01
         CLC   JCTACFJ,=C'ACFJ'    IS IT THE ACF2 AREA ?          FCI01
         BZ    ADSNNOK             IF NOT, DENY ACCESS            FCI01
         SPACE 1                                                  FCI01
.FCI01F2 ANOP  ,                                                  FCI01
         SPACE 1                                                  FCI01
         CLC   QLOGON,LIDLID       USERS LOGONID=JOBS ACF LOGONID?FCI01
         BE    AUTHOK              YES..CONTINUE FORTHWITH        FCI01
         SPACE 1                                                  FCI01
         ACFGACVT R6,NONE=ADSNNOK  GET THE ACF2 CVT               FCI01
         SPACE 1                                                  FCI01
         USING ACCVT,R6            NOTE ACF2 CVT ADDRESSABILITY   FCI01
         SPACE 1                                                  FCI01
         MVC   ADSNAME,=CL44'SYSOUT. '  INITIALIZE DSNAME TO USE  FCI01
         XC    ACFSPARM(ACFSPRML),ACFSPARM   CLEAR REQUEST BLOCK  FCI01
         MVI   ACFSPREQ,ACFSPRDS   DSNAME ACCESS ONLY             FCI01
         MVI   ACFSPID1,ACFSPIUR   DIS AM DE USER TALKING....     FCI01
         LA    R5,ADSNAME          GET DSNAME ADDRESS             FCI01
         ST    R5,ACFSPDSN         AND GIVE IT TO ACF PARM LIST   FCI01
         SPACE 1                                                  FCI01
*  GENERATE A DSN OF THE FORM 'SYSOUT.LOGONID.JOBNAME'            FCI01
         SPACE 1                                                  FCI01
         MVC   ADSNAME+7(8),LIDLID MOVE LID TO DSNAME             FCI01
         CLI   ADSNAME+7,C' '      IS THE LID BLANK?              FCI01
         BNE   ADSNCHK0            NO..NORMAL PROCESS             FCI01
         SPACE 1                                                  FCI01
*  GOT HERE BECAUSE LID IS ' ' (BLANK)..SUBSTITUTE 'SYSTEM'       FCI01
         MVC   ADSNAME+7(8),=CL8'SYSTEM'                          FCI01
         SPACE 1                                                  FCI01
ADSNCHK0 LA    R1,ADSNAME+7        GET ADDRESS                    FCI01
         LA    R5,8                LOAD COUNT                     FCI01
ADSNCHK  CLI   0(R1),C' '          LOOK FOR BLANK                 FCI01
         BE    ADSNCHK1            GOTIT..                        FCI01
         LA    R1,1(R1)            BUMP AND                       FCI01
         BCT   R5,ADSNCHK            GRIND                        FCI01
ADSNCHK1 MVI   0(R1),C'.'          MOVE IN PERIOD..               FCI01
         MVC   1(8,R1),JCTJNAME    MOVE IN JOBNAME                FCI01
         SPACE 1                                                  FCI01
         LA    R1,ACFSPARM         GET ADDRESS OF PARM FIELD      FCI01
         ACFSVC (1),TYPE=S,NONE=ADSNNOK,CVT=HAVE  INVOKE A C F 2  FCI01
         SPACE 1                                                  FCI01
         LTR   R15,R15             HOW DID YOU LIKE THEM APPLES?  FCI01
         BC    8,AUTHOK            ..OK BY YOU...CONTINUE..       FCI01
         SPACE 1                                                  FCI01
ADSNNOK  DS    0H                  ACF2 DOWN OR ACCESS NOT ALLOWEDFCI01
         QTILT '*** SORRY..NO ACF2 AUTHORITY TO LOOK AT THIS JOB' FCI01
         SPACE 1                                                  FCI01
         DROP  R6                  KILL ACF2 CVT ADDRESSABILITY   FCI01
         SPACE 1                                                  FCI01
         AIF   (NOT &QACF2).FCI01F1                               FCI01
         SPACE 1                                                  FCI01
         DROP  R7                  KILL ACF AREA 2ADDRESSABILITY  FCI01
         SPACE 1                                                  FCI01
.FCI01F1 ANOP  ,                                                  FCI01
         SPACE 1                                                  FCI01
         QTILT '*** JOBNAME MUST BEGIN WITH USERID ***'           ONL02
         SPACE 1                                                  ONL02
         DROP  R8                  KILL JCT ADDRESSABILITY        ONL02
         SPACE 1                                                  RNB03
         AGO   .RNB03F2                                           RNB03
.RNB03F1 ANOP  ,                                                  RNB03
*                                                                 RNB03
*  AT RNB, CHECKING FOR THE ENTRY FROM FINDJOB IS DONE ONLY FOR   RNB03
*  THE XD SUB-COMMAND:  RACHECK FOR APPL-QUEUEXDS AND PASS THE    RNB03
*  JOBNAME AS THE APPL FOR LOGGING                                RNB03
*                                                                 RNB03
         SPACE 1                                                  RNB03
         CLC   =C'XD',QSUBNAME     IS THIS AN XD COMMAND?         RNB03
         BNE   AUTHOK              DONE IF NO                     RNB03
         SPACE 1                                                  RNB03
         L     R2,QCJCTA           ADDR OF JCT                    RNB03
         USING JCT,R2              NOTE JCT ADDRESSABILITY        RNB03
         SPACE 1                                                  RNB03
         RACHECK ENTITY=QRACNMXD,APPL=JCTJNAME,MF=(E,QRACHECK)    RNB03
         SPACE 1                                                  RNB03
         LTR   R15,R15             IS ACCESS ALLOWED?             RNB03
         BZ    AUTHOK              DONE IF YES                    RNB03
         SPACE 1                                                  RNB03
         QTILT '*** XDS COMMAND NOT ALLOWED ***'                  RNB03
         SPACE 1                                                  RNB03
         DROP  R2                  KILL JCT ADDRESSABILITY        RNB03
         SPACE 1                                                  RNB03
.RNB03F2 ANOP  ,                                                  RNB03
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = JCL COMMAND FOR JOB AUTHORIZATION       ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#JCL    DS    0H                  JCL COMMAND FOR JOB            ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = JLOG COMMAND FOR JOB AUTHORIZATION      ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#JLOG   DS    0H                  JLOG COMMAND FOR JOB           ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = JMSG COMMAND FOR JOB AUTHORIZATION      ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#JMSG   DS    0H                  JMSG COMMAND FOR JOB           ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = LIST COMMAND FOR JOB/DSID AUTHORIZATION ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#LIST   DS    0H                  LIST CMD FOR JOB AND DSID      ONL02
         SPACE 1                                                  ONL02
         AIF   (&QRNB).RNB05L1                                    RNB05
*                                                                 ONL02
*  THE DEFAULT CODE DOES NOT ALLOW THE LIST COMMAND TO BE USED    ONL02
*  FOR A DATASET ID (DSID) LESS THAN 101 UNLESS THE USER HAS JES2 ONL02
*  SYSTEM PROGRAMMER AUTHORITY.                                   ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
         TM    QAFLAGS,QAFXSYSP    DOES USER HAVE SYS PGMR AUTH ? ONL02
         BO    DSIDOK              SKIP IF YES, ALLOW ANY DSID    ONL02
         CLC   QPDSID,=X'FFFF'     IS THE DSID INVALID?             SBG
         BE    DSIDBAD             GO MAKE SURE IT IS.              SBG
         CLC   QPDSID,=H'101'      IS THE DSID ID LESS THAN 101?  ONL02
         BNL   DSIDOK              SKIP IF YES                    ONL02
         SPACE 1                                                  ONL02
DSIDBAD  MVC   QPDSID,=X'FFFF'     INVALIDATE DSID                  SBG
         SPACE 1                                                  ONL02
         QTILT '*** DATASET ID INVALID ***'                       ONL02
         SPACE 1                                                  ONL02
DSIDOK   DS    0H                  THE DSID IS OK                 ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 1                                                  RNB05
         AGO   .RNB05L2                                           RNB05
.RNB05L1 ANOP  ,                                                  RNB05
*                                                                 RNB05
*  AT RNB, THE LIST COMMAND MAY NOT BE USED FOR A DATASET ID      RNB05
*  (DSID) LESS THAN 101.                                          RNB05
*                                                                 RNB05
         SPACE 1                                                  RNB05
         CLC   QPDSID,=X'FFFF'     IS THE DSID INVALID?             SBG
         BE    DSIDBAD1            GO MAKE SURE IT IS.              SBG
         CLC   QPDSID,=H'101'      IS THE DSID ID LESS THAN 101?  RNB05
         BNL   DSIDOK              SKIP IF YES                    RNB05
DSIDBAD1 MVC   QPDSID,=X'FFFF'     INVALIDATE DSID                  SBG
         QTILT '*** DATASET ID INVALID ***'                       RNB05
         SPACE 1                                                  RNB05
DSIDOK   DS    0H                  THE DSID IS OK                 RNB05
         SPACE 2                                                  RNB05
*                                                                 RNB05
*  AT RNB, THE JOBNAME MUST BEGIN WITH THE USER'S USERID.         RNB05
*  OTHERWISE:                                                     RNB05
*                                                                 RNB05
*   (1) "PJS" USERS CAN'T LIST ANYTHING.                          RNB05
*   (2) OTHERS CAN LIST IF THEIR USERID IS SPECIFIED IN A NOTIFY  RNB05
*       PARAMETER ON THE JOB STATEMENT.                           RNB05
*   (3) "TEC" USERS CAN LIST FROM ANY "TEC" JOBS OR JOBS WITH A   RNB05
*       NOTIFY FOR A "TEC" USERID.                                RNB05
*   (4) "TEC" USERS CAN LIST FROM ANY STARTED TASK.               RNB05
*                                                                 RNB05
         SPACE 1                                                  RNB05
         L     R8,QCJCTA           ADDR OF JCT                    RNB05
         USING JCT,R8              NOTE JCT ADDRESSABILITY        ONL16
         SPACE 1                                                  RNB05
         ICM   R1,15,QLOGONLN      GET LENGTH OF USERID           RNB05
         BNP   LISTNOK             ERROR IF NO LENGTH             RNB05
         BCTR  R1,0                LESS ONE FOR EXECUTE           RNB05
         EX    R1,LISTJCLC         JOBNAME BEGINS WITH USERID ?   RNB05
         BE    AUTHOK              DONE IF YES, ALLOW ACCESS      RNB05
         SPACE 1                                                  RNB05
         CLC   =C'PJS',QLOGON      IS THIS A PJS USER?            RNB05
         BE    LISTNOK             INVALID JOB IF SO              RNB05
         EX    R1,LISTNCLC         NOTIFY ID BEGINS WITH USERID ? RNB05
         BE    AUTHOK              GOOD JOB IF SO                 RNB05
         CLC   =C'TEC',QLOGON      IS THIS A TEC USERID?          RNB05
         BNE   LISTNOK             INVALID JOB IF NOT             RNB05
         CLC   =C'TEC',JCTJNAME    FOR A TEC USER, ALLOW LIST     RNB05
         BE    AUTHOK              FOR ANY TEC JOB OR ANY JOB     RNB05
         CLC   =C'TEC',JCTTSUID    WITH A TEC NOTIFY              RNB05
         BE    AUTHOK                                             RNB05
         TM    JCTJOBFL,JCTSTCJB   ALSO ALLOW IF AN STC           RNB05
         BNZ   AUTHOK                                             RNB05
         SPACE 1                                                  RNB05
LISTNOK  DS    0H                  USER MAY NOT LIST THIS DATASET RNB05
         MVC   QPDSID,=X'FFFF'     INVALIDATE DSID                  SBG
         QTILT '*** JOBNAME MUST BEGIN WITH USERID ***'           RNB05
         SPACE 1                                                  RNB05
LISTJCLC CLC   JCTJNAME(*-*),QLOGON  EXECUTED - MATCH JOBNAME     RNB05
LISTNCLC CLC   QLOGON(*-*),JCTTSUID  EXECUTED - MATCH NOTIFY ID   RNB05
         SPACE 1                                                  RNB05
         DROP  R8                  KILL JCT ADDRESSABILITY        RNB05
         SPACE 1                                                  RNB05
.RNB05L2 ANOP  ,                                                  RNB05
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = JHIST COMMAND FOR JOB AUTHORIZATION     ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#JHIST  DS    0H                  JHIST COMMAND FOR JOB          ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = DD COMMAND FOR JOB AUTHORIZATION        ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#DD     DS    0H                  DD COMMAND FOR JOB             ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = PDDB COMMAND FOR JOB AUTHORIZATION      ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#PDDB   DS    0H                  PDDB COMMAND FOR JOB           ONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = DEL, REQ, AND CAN COMMANDS FOR JOB AUTH ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#SYSOUT DS    0H                  DEL/REQ/CAN CMDS FOR JOB       ONL02
         SPACE 1                                                  ONL02
         AIF   (&QRNB).RNB08S1                                    RNB08
*                                                                 ONL02
*  THE DEFAULT CODE FOR THIS ENTRY IS TO ALLOW THE DEL, REQ, AND  ONL02
*  CAN COMMANDS TO BE USED ONLY ON JOBS WHOSE JOBNAME BEGINS WITH ONL02
*  THE USER'S USERID.  USERS WHO HAVE JES2 SYSTEM PROGRAMMER      ONL02
*  AUTHORITY MAY USE ANY JOBNAME.                                 ONL02
*                                                                 ONL02
         SPACE 1                                                  ONL02
         TM    QAFLAGS,QAFXSYSP    USER HAS JES2 SYSTEM PGMR AUTH?ONL02
         BO    AUTHOK              DONE IF YES, ALLOW ACCESS      ONL02
         SPACE 1                                                  ONL02
         L     R8,QCJCTA           ADDR OF JCT                    ONL02
         USING JCT,R8              NOTE JCT ADDRESSABILITY        ONL16
         SPACE 1                                                  ONL02
         ICM   R1,15,QLOGONLN      GET LENGTH OF USERID           ONL02
         BNP   SYSONOK             ERROR IF NO LENGTH             ONL02
         BCTR  R1,0                LESS ONE FOR EXECUTE           ONL02
         EX    R1,SYSOCLC          JOBNAME BEGINS WITH USERID ?   ONL02
         BE    AUTHOK              DONE IF YES, ALLOW ACCESS      ONL02
         B     SYSONOK             ELSE, GO TELL USER             ONL02
         SPACE 1                                                  ONL02
SYSOCLC  CLC   JCTJNAME(*-*),QLOGON  EXECUTED - MATCH JOBNAME     ONL02
         SPACE 1                                                  ONL02
SYSONOK  DS    0H                  JOBNAME DOESN'T MATCH USERID   ONL02
         QTILT '*** JOBNAME MUST BEGIN WITH USERID ***'           ONL02
         SPACE 1                                                  ONL02
         DROP  R8                  KILL JCT ADDRESSABILITY        ONL02
         SPACE 1                                                  RNB08
         AGO   .RNB08S2                                           RNB08
.RNB08S1 ANOP  ,                                                  RNB08
*                                                                 RNB08
*  AT RNB, ALLOW DEL/REQ/CAN IF JOBNAME STARTS WITH USERID OR     RNB08
*  NOTIFY IS FOR USERID, UNLESS JOB SUBMITTED FROM PJS.  ALLOW    RNB08
*  TEC USERS TO MANIPULATE OTHER TEC USER'S JOBS AND ALSO STC'S.  RNB08
*                                                                 RNB08
         SPACE 1                                                  RNB08
         L     R8,QCJCTA           ADDR OF JCT                    RNB08
         USING JCT,R8              NOTE JCT ADDRESSABILITY        ONL16
         SPACE 1                                                  RNB08
         ICM   R1,15,QLOGONLN      GET LENGTH OF USERID           RNB08
         BNP   SYSONOK             ERROR IF NO LENGTH             RNB08
         BCTR  R1,0                LESS ONE FOR EXECUTE           RNB08
         EX    R1,SYSOJCLC         JOBNAME BEGINS WITH USERID ?   RNB08
         BE    AUTHOK              DONE IF YES, ALLOW ACCESS      RNB08
         SPACE 1                                                  RNB08
         CLC   =C'PJS',QLOGON      IS THIS A PJS USER?            RNB08
         BE    SYSONOK             YES - INVALID                  RNB08
         BAL   R6,SYSPJSCK         ENSURE NOT PJS JOB FOR CAN/DEL RNB08
         SPACE 1                                                  RNB08
         L     R8,QCJCTA           RESTORE JCT ADDRESSABILITY     RNB08
         ICM   R1,15,QLOGONLN      GET LENGTH OF USERID           RNB08
         BCTR  R1,0                LESS ONE FOR EXECUTE           RNB08
         EX    R1,SYSONCLC         NOTIFY ID BEGINS WITH USERID ? RNB08
         BE    AUTHOK              YES - OK                       RNB08
         CLC   =C'TEC',QLOGON      IS THIS A TEC USER?            RNB08
         BNE   SYSONOK             NO  - INVALID                  RNB08
         TM    JCTJOBFL,JCTSTCJB   IS IT AN STC?                  RNB08
         BO    AUTHOK              /YES - OK FOR TEC USER         RNB08
         CLC   =C'TEC',JCTJNAME    IS JOBNAME FOR A TEC USER?     RNB08
         BE    AUTHOK              YES - OK                       RNB08
         CLC   =C'TEC',JCTTSUID    IS NOTIFY FOR A TEC USER?      RNB08
         BE    AUTHOK              YES - OK                       RNB08
         SPACE 1                                                  RNB08
SYSONOK  QTILT '*** JOBNAME MUST BEGIN WITH USERID ***'           RNB08
         SPACE 1                                                  RNB08
SYSOJCLC CLC   JCTJNAME(*-*),QLOGON  EXECUTED - MATCH JOBNAME     RNB08
SYSONCLC CLC   QLOGON(*-*),JCTTSUID  EXECUTED - MATCH NOTIFY ID   RNB08
         SPACE 1                                                  RNB08
         DROP  R8                  KILL JCT ADDRESSABILITY        RNB08
         SPACE 2                                                  RNB08
*                                                                 RNB08
*  SUBROUTINE TO TILT IF PJS JOB (ONLY FOR CANCEL OR DELETE)      RNB08
*                                                                 RNB08
         SPACE 1                                                  RNB08
         USING PDBDSECT,R1         NOTE PDDB ADDRESSABILITY       RNB08
         USING IOTDSECT,R3         NOTE IOT ADDRESSABILITY        RNB08
         SPACE 1                                                  RNB08
SYSPJSCK CLI   QCODEH+1,4          IS THIS A REQUEUE?             RNB08
         BER   R6                  /YES - OK FOR NOW              RNB08
*                                  /NO  - ENSURE NOT A PJS JOB    RNB08
         L     R3,QCIOTA           LOAD BASE REG                  RNB08
         SH    R3,=Y(IOTSTART-IOTDSECT) ADJUST FOR HEADER            CL
         LR    R5,R3               BASE OF IOT                    RNB08
         A     R5,IOTPDDBP         OFFSET BEYOND LAST PDDB        RNB08
         LR    R1,R3               BASE OF IOT                    RNB08
         A     R1,IOTPDDB          OFFSET TO FIRST PDDB IN IOT    RNB08
SYSPFDDS DS    0H                  SEARCH IOT FOR DSID = 5        RNB08
         CLC   IOTID,=CL4'IOT'     WAS AN IOT READ FROM SPOOL ?   RNB08
         BNE   SOBADIOT            ERROR IF NO                    RNB08
         CLC   QPJOBID,IOTJBKEY    IS THE IOT'S JOB KEY VALID ?   RNB08
         BNE   SOBADIOT            ERROR IF NO                    RNB08
         CLC   =F'5',PDBDSKEY      IS THIS THE DATASET?           WGH42
         BE    SYSPID5             YES. CONTINUE.                 RNB08
         LA    R1,PDBLENG(R1)      NO. LOOK AT NEXT PDDB.         RNB08
         CR    R1,R5               HAVE WE GONE PAST LAST PDDB?   RNB08
         BL    SYSPFDDS            NO. TRY AGAIN.                 RNB08
         B     SOBADTXR            ELSE BAD DD TABLE (INTERP. JCL)RNB08
SYSPID5  L     R5,PDBMTTR          DISK ADDR OF FIRST BLOCK       RNB08
         DROP  R1                  KILL PDDB ADDRESSABILITY       RNB08
         L     R7,QCBLKA           ADDR OF DATASET BLOCK IOAREA   RNB08
         MVC   QDMSG,QBLANK        BLANK OUT THE MESSAGE AREA     RNB08
         ST    R5,QCTRAK           STORE DISK ADDR                RNB08
         LR    R1,R7               IOAREA ADDRESS                 RNB08
         L     R15,=V(READSPC)     ADDR OF ROUTINE TO READ SPOOL  RNB08
         BALR  R14,R15             GO TO IT                       RNB08
         CLC   QPJOBID,4(R7)       IS RECORD'S JOB JEY VALID?     RNB08
         BNE   SOBADTXR            ERROR IF NO                    RNB08
         CLC   8(2,R7),=H'5'       IS RECORD'S DSID VALID?        RNB08
         BNE   SOBADTXR            ERROR IF NO                    RNB08
         LA    R5,10(R7)           ADDR OF FIRST RECORD IN BLOCK  RNB08
         SR    R7,R7               ZERO OUT REG                   RNB08
         IC    R7,0(R5)            INSERT LENGTH                  RNB08
         CLI   5(R5),1             IS THIS A JOB RECORD?          RNB08
         BNE   SOBADJTX            /NO  - INVALID INTERNAL TEXT   RNB08
         LA    R5,9(R5)            ADDR OF FIRST KEY              RNB08
         LR    R8,R7               REMAINING LENGTH OF RECORD     RNB08
         SR    R15,15              ZERO OUT R15                   RNB08
         SR    R14,R14             ZERO OUT R14                   RNB08
         SR    R1,R1               ZERO OUT R1                    RNB08
SYSPCKEY CLI   0(R5),X'A5'         IS THIS THE USER= PARM?        RNB08
         BE    SYSPGOTU            YES. PROCESS IT.               RNB08
         IC    R1,1(R5)            NUMBER OF SUBFIELDS            RNB08
         LA    R5,2(R5)            UPDATE LOCATION                RNB08
         SH    R8,=H'2'            REMAINING COUNT                RNB08
         SR    R8,R1               REMAINING COUNT                RNB08
         BNP   SOBADJTX            RECORD IS EXHAUSTED            RNB08
         LTR   R1,R1               ARE THERE ANY SUBFIELDS?       RNB08
         BZ    SYSPCKEY            NO. TRY NEXT FIELD.            RNB08
SYSPNXSF TM    0(R5),X'80'         IS THIS A SUB-SUB-FIELD        RNB08
         BZ    SYSPNOSS            NO. CONTINUE.                  RNB08
         NI    0(R5),X'7F'         CLEAR THE HEX 80 BIT           RNB08
         IC    R14,0(R5)           NUMBER OF SUB-SUB-FIELDS       RNB08
         LA    R5,1(R5)            UPDATE LOCATION                RNB08
         SH    R8,=H'1'            REMAINING COUNT                RNB08
         SR    R8,R14              REMAINING COUNT                RNB08
         BNP   SOBADJTX            RECORD IS EXHAUSTED            RNB08
         AR    R1,R14              INCREASE NUMBER OF SUBFIELDS   RNB08
         B     SYSPYESS            DECREMENT AND TRY AGAIN        RNB08
SYSPNOSS IC    R15,0(R5)           SUBFIELD LENGTH                RNB08
         LA    R5,1(R15,R5)        ADD TO LOCATION                RNB08
         SR    R8,R15              REMAINING COUNT                RNB08
         BNP   SOBADJTX            RECORD IS EXHAUSTED            RNB08
SYSPYESS BCT   R1,SYSPNXSF         DO NEXT SUBFIELD               RNB08
         B     SYSPCKEY            TRY NEXT FIELD                 RNB08
SYSPGOTU CLI   2(R5),7             IS USER ID LENGTH = 7?         RNB08
         BNER  R6                  /NO - NOT PJS JOB, PROCESS     RNB08
         CLI   2(R5),0             IS THE LENGTH ZERO?            RNB08
         BER   R6                  YES. SKIP THE FIELD.           RNB08
         CLC   =C'PROD',6(R5)      PJS JOB? (USER = ???PROD)      RNB08
         BE    SOPJSMSG            /YES - BAD                     RNB08
         BR    R6                  /NO  - GO PROCESS              RNB08
         SPACE 2                                                  RNB08
SOBADIOT QTILT '*** JOB HAS PURGED (OR IOT IS INVALID) ***'       RNB08
SOBADTXR QTILT '*** JOB HAS PURGED (OR DSID 5 SPOOL ERROR) ***'   RNB08
SOBADJTX QTILT '*** CANNOT PROCESS JOB - INVALID JOB STMT TEXT'   RNB08
SOPJSMSG QTILT '*** CANNOT CAN/DEL JOB SUBMITTED VIA PJS ***'     RNB08
         SPACE 1                                                  RNB08
         DROP  R3                  KILL IOT ADDRESSABILITY        RNB08
         SPACE 1                                                  RNB08
.RNB08S2 ANOP  ,                                                  RNB08
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  PROCESS REQUEST TYPE = AUTHORIZATION TO LOOK AT SYSLOG         ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
Q#SYSLOG DS    0H                  AUTHORIZATION TO LOOK AT SYSLOGONL02
         B     AUTHOK              ALLOW FUNCTION TO PROCEED      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  RETURN TO CALLER, REQUESTED FUNCTION MAY PROCEED               ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
AUTHOK   DS    0H                  REQUESTED FUNCTION MAY PROCEED ONL02
         QSTOP ,                   RETURN TO CALLER               ONL02
         SPACE 3                                                  ONL02
         LTORG ,                   DEFINE LITERAL ORIGIN          ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*  GENERATE CONTROL BLOCK DSECTS                                  ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       ONL02
AUTH     CSECT ,                   RESUME CSECT                   ONL02
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL02
         SPACE 1                                                  ONL02
         $BUFFER ,                                                ONL16
         IFGRPL
         $JQE  ,                                                  ONL02
         $JCT  ,                                                  ONL02
         $PDDB ,                                                  ONL02
         $TAB  ,                                                  ONL02
         $IOT  ,                                                  ONL02
         SPACE 1                                                  ONL02
WORK     DSECT ,                   SAVE AREA WORK DSECT           ONL02
         DS    18F                 OS SAVE AREA                   ONL02
WORKAREA DS    0D                  START OF DATA                  ONL02
         SPACE 1                                                  ONL02
         AIF   (NOT &QRACF).RNB03CB                               RNB03
         CVT   DSECT=YES                                          RNB03
         IHAASCB ,                                                RNB03
         IHAASXB ,                                                RNB03
         IHAACEE ,                                                RNB03
.RNB03CB ANOP  ,                                                  RNB03
         SPACE 1                                                  RNB03
         AIF   (NOT &QACF2).FCI01CB                               FCI01
WORK     DSECT ,                   CONTINUE SAVE AREA WORK DSECT  FCI01
         ORG   WORKAREA            ORG PAST OS SAVE AREA          FCI01
         ACDSV DSECT=NO            PARAMETER LIST FOR THE ACF2 SVCFCI01
ADSNAME  DS    CL44                DSNAME USED WITH THE ACF2 SVC  FCI01
AUTH     CSECT ,                   RESUME CSECT                   FCI01
         SPACE 1                                                  FCI01
         ACCVT ,                                                  FCI01
         ACUCB ,                                                  FCI01
         IHAPSA ,                                                 FCI01
         AIF   (NOT &QACF2).FCI01CB                               FCI01
         #ACFJCTX ,                GEN ACF2 JCT EXTENSION DSECT   FCI01
.FCI01CB ANOP  ,                                                  FCI01
         SPACE 1                                                  FCI01
         QCOMMON ,                 GENERATE QCOMMON DSECT         ONL02
         $HASPEQU
         SPACE 1                                                  ONL02
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL02
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    ONL02
         END   ,                                                  ONL02
