FINDJOB  TITLE 'QUEUE COMMAND - LOCATE JQE, JCT, AND IOT BY JOBNAME'
FINDJOB  QSTART ,                  START FINDJOB CSECT ASSEMBLY   ONL01
         GBLB   &QNERDC        NERDC OPTION                       UF035
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       LOCAL WORK AREA
         MVI   SLID#,0        CLEAR ID NUMBER                     UF035
         CLC   =C'SL',QSUBNAME SYSLOG COMMAND?                    UF035
         BE    SLIDNCK        YES, SKIP DSID CHECK                UF035
         LH    R1,QLNG1       LENGTH OF PARAMETER FIELD
         SH    R1,=H'1'       IS THE LENGTH ZERO?
         BM    TILT           YES. TILT.
SLIDNCK  DS    0H                                                 UF035
******************************************************************UF007
*                                                                 UF007
*   ALLOW JOBNAME = "*" TO SIGNIFY CURRENT JOB & SKIP CKPT READ   UF007
*                                                                 UF007
******************************************************************UF007
         CLI   QPARM1,C'*'    WANT CURRENT JOB AGAIN?             UF007
         BE    JOBAGAIN       YES, SKIP CKPT READ                 UF007
******************************************************************UF006
*                                                                 UF006
*   CALL - READ JES2 CHECKPOINT ROUTINE                           UF006
*                                                                 UF006
******************************************************************UF006
CALLCKPT L     R15,=V(CKPT)   ADDR OF CKPT ROUTINE                UF006
         BALR  R14,R15        GO TO IT                            UF006
         MVI   SLID#,0        CLEAR ID NUMBER                     UF035
******************************************************************UF035
*                                                                 UF035
*        SPECIAL PROCESSING IF THE COMMAND WAS SYSLOG.            UF035
*                                                                 UF035
******************************************************************UF035
         CLC   =C'SL',QSUBNAME SYSLOG COMMAND?                    UF035
         BNE   SLZ            NO, BACK TO NORMAL PROCESSING       UF035
         ICM   R1,3,QLNG1     WAS A PARAMETER SPECIFIED?          UF035
         BZ    SLTHIS         NO, WANT THIS SYSTEMS SYSLOG        UF035
         CLI   QPARM1,C'0'    NUMERIC JOB NUMBER?                 UF035
         BNL   SLZ            YES, MUST BE THE NUMBER - DO NORMAL UF035
         CLC   QPARM1,=CL8'SYSLOG' WANT THIS SYSTEMS SYSLOG?      UF035
         BE    SLTHIS         YES, DO IT                          UF035
SLIDPRE  LA    R1,QSYSID      POINT TO SYSID TABLE                UF035
         LR    R14,R1         POINT TO SYSID TABLE                UF035
         LA    R15,7          MAX NUMBER OF SYSTEMS               UF035
SLTESTID LA    R1,8(,R1)      BUMP TO NEXT ENTRY                  UF035
         CLC   QPARM1(4),0(R1) DOES PARM MATCH SYSTEM ID?         UF035
         BE    SLGOTID        YES, GET OUT OF LOOP                UF035
         BCT   R15,SLTESTID   LOOP FOR ALL ID'S                   UF035
         B     SLZ            NOT FOUND - DO NORMAL PROCESSING    UF035
SLGOTID  SR    R1,R14         OFFSET TO ENTRY                     UF035
         SRL   R1,3           DIVIDE BY 8                         UF035
         STC   R1,SLID#       SAVE FOR LATER TEST                 UF035
         MVC   QPARM1,=CL8'SYSLOG' SET JOBNAME TO MATCH ON        UF035
         MVI   QLNG1+L'QLNG1-1,8   SET LENGTH OF NAME             UF035
         B     SLZ            DONE FOR NOW                        UF035
SLTHIS   DS    0H             NEED TO RETURN THIS SYSTEMS SYSLOG  UF035
         L     R1,16          POINT TO CVT                        UF035
         ICM   R1,15,CVTSMCA-CVTMAP(R1) POINT TO SMF CONTROL AREA UF035
         BZ    SLZ            NONE, DO NORMAL PROCESSING          UF035
         MVC   QPARM1(4),SMCASID-SMCABASE(R1)  MOVE SID TO AREA   UF035
         AIF   (NOT &QNERDC).SLNNER  SKIP IF NOT NERDC            UF035
         OI    QPARM1+3,X'F0' CHANGE A->1, B->2                   UF035
.SLNNER  ANOP                                                     UF035
         B     SLIDPRE        AND GO TO COMMON CODE               UF035
SLZ      DS    0H                                                 UF035
***********************************************************************
*                                                                     *
*   DETERMINE IF SEARCH IS BY JOBNUMBER OR JOBNAME                    *
*                                                                     *
***********************************************************************
         LH    R1,QLNG1       LENGTH OF PARAMETER FIELD
         SH    R1,=H'1'       IS THE LENGTH ZERO?
         BM    TILT           YES. TILT.
         CLI   QPARM1,C'0'    IS THE FIRST CHARACTER NUMERIC?
         BL    JOBNAME        NO. SEARCH BY JOBNAME
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R1,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. TILT.
         EX    R1,PACK        PACK THE FIELD
         CVB   R5,CONVERT     CONVERT TO BINARY
***********************************************************************
*                                                                     *
*   LOCATE JQE BY JOBNAME                                             *
*                                                                     *
***********************************************************************
JOBNAME  L     R2,QCJQHEAD    LOAD ADDR OF JQT
         USING JQTDSECT,R2    BASE REG FOR JQT
         LA    R2,JQTOUT      FIRST JQE QUEUE
         LA    R1,JQTQMAX     MAXIMUM NUMBER OF QUEUES
         DROP  R2
NEXTJQT  L     R3,0(R2)       OFFSET TO FIRST JQE IN QUEUE        UF020
NEXTJQE  N     R3,=A(X'00FFFFFF')  TEST FOR END OF CHAIN          UF020
         BZ    ENDJQE         END OF QUEUE
         A     R3,QCJQTA      ADD BASE TO OFFSET
         USING JQE,R3         BASE REG FOR JQE
         CLI   QPARM1,C'0'    IS SEARCH BY JOBNUMBER?
         BL    CLCNAME        NO. SEARCH BY JOBNAME.
         CH    R5,JQEJOBNO    IS THIS THE RIGHT JOBNUMBER?
         BNE   NOMATCH        NO. TRY NEXT JQE.
         B     FOUND          YES. PROCESS IT.
CLCNAME  CLC   QPARM1,JQEJNAME IS THIS THE RIGHT JOBNAME?
         BE    FOUND          YES. PROCESS IT.
NOMATCH  L     R3,JQENEXT     NO. TRY NEXT ENTRY.                 UF020
         B     NEXTJQE        LOOP                                UF020
ENDJQE   LA    R2,4(R2)       TRY NEXT QUEUE                      UF020
         BCT   R1,NEXTJQT     LOOP IF NOT LAST QUEUE
TILT     QTILT '*** JOBNAME NOT FOUND OR INVALID ***'
         SPACE 1                                                  UF007
JOBAGAIN L     R3,QCJQEA      PRIME JQE ADDRESS                   UF007
         LTR   R3,R3          ANYONE HOME?                        UF034
         BNZ   FOUND          YES, CONTINUE                       UF034
         QTILT '*** YOU MUST SPECIFY A JOBNAME OR NUMBER BEFORE USING "+
               *" ***'                                            UF034
         SPACE 1                                                  ONL03
FOUND    ST    R3,QCJQEA      SAVE THE ADDRESS
         CLI   SLID#,0        LOOKING FOR A SPECIFIC SYSLOG?      UF035
         BE    FOUND1         NO, CONTINUE                        UF035
         IC    R14,SLID#      GET DESIRED ID                      UF035
         IC    R15,JQEFLAG1   GET SYSTEM ID OF THIS JOB           UF035
         N     R14,=A(X'0000000F') MASK OFF EXTRA STUFF           UF035
         N     R15,=A(X'0000000F') MASK OFF EXTRA STUFF           UF035
         CR    R14,R15        SEE IF IT IS THE ONE WE WANTED      UF035
         BNE   NOMATCH        NOPE, TRY AGAIN                     UF035
FOUND1   DS    0H                                                 UF035
         LR    R4,R3          SAVE THE ADDRESS FOR COMPARE        RNB22
         SPACE 2                                                  ONL03
******************************************************************ONL03
*                                                                 ONL03
*   READ THE JCT                                                  ONL03
*                                                                 ONL03
******************************************************************ONL03
         SPACE 1                                                  ONL03
         OC    JQETRAK,JQETRAK     IS THERE AN ADDRESS            UF046
         BNZ   JCTOK               YES, CONTINUE                  UF046
         CLC   =C'JQE',QSUBNAME    IS IT JQE COMMAND?             UF046
         BE    STOP                YES, ALLOW TO CONTINUE         UF046
         B     TILT                NO, ABORT                      UF046
JCTOK    DS    0H                                                 UF046
         MVC   QCTRAK,JQETRAK DISK ADDR OF JCT
         L     R1,QCJCTA           ADDRESS OF JCT                 UF046
         AL    R1,=A(JCTSTART-JCT)  ADJUST FOR BUFFER PREFIX      ONL16
         XC    0(4,R1),0(R1)       CLEAR START OF AREA            UF046
         NI    QFLAG1,255-QFLG1SDS CLEAR THE SYSTEM DS JOB FLAG   ONL14
         SPACE 1                                                  ONL14
         TM    JQEFLAG3,JQE3SYSD   JQETRAK POINT TO IOT?          UF046
         BNO   READJCT             NO, GO READ JCT                   CL
         SPACE 1                                                  ONL14
         OI    QFLAG1,QFLG1SDS     SHOW IT'S A SYSTEM DS JOB      ONL14
         SPACE 1                                                  ONL14
         MVC   QPJOBID(2),=C'RM'   SET                               CL
         MVC   QPJOBID+2(2),JQEJOBNO JOB KEY                         CL
         B     CKAUTH              SKIP JCT READ                  ONL02
         DROP  R3
READJCT  L     R3,QCJCTA      ADDR OF IOAREA FOR JCT                 CL
         LR    R1,R3          PARM FOR READSPC
         AL    R1,=A(JCTSTART-JCT)  ADJUST FOR BUFFER PREFIX      ONL16
         L     R15,=V(READSPC) ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
         USING JCT,R3         BASE REG FOR JCT                    ONL16
         USING JQE,R4         BASE FOR JQE                        RNB22
         SPACE 2                                                  ONL03
******************************************************************ONL03
*                                                                 ONL03
*   ENSURE THAT THE CORRECT JCT WAS READ                          ONL03
*                                                                 ONL03
******************************************************************ONL03
         SPACE 1                                                  ONL03
         CLC   JCTID,=CL4'JCT'     DID WE READ A JCT ?            ONL03
         BNE   BADJCT              TILT IF NO                     ONL03
         CLC   JCTJNAME,JQEJNAME   DOES JOBNAME MATCH ?           ONL03
         BNE   BADJCT              TILT IF NO                     ONL03
         SPACE 1                                                  ONL03
         LA    R1,JQE              ADDR OF JQE                    ONL03
         SL    R1,QCJQTA           COMPUTE THE JQE OFFSET         ONL03
         CL    R1,JCTJQE           DOES OFFSET MATCH THE JCT'S ?  ONL03
         BNE   BADJCT              TILT IF NO                     ONL03
         SPACE 1                                                  ONL03
         CLI   QPARM1,C'*'         WAS CURRENT JOB REQUESTED ?    ONL03
         BNE   NEWJOB              SKIP IF NO                     ONL03
         CLC   QPJOBID,JCTJBKEY    DOES JOB KEY MATCH ?           ONL03
         BE    VALIDJCT            SKIP IF YES                    ONL03
         SPACE 1                                                  ONL03
BADJCT   QTILT '*** JOB HAS PURGED (OR JCT IS INVALID) ***'       ONL03
         SPACE 1                                                  ONL03
NEWJOB   DS    0H                  A NEW JOB WAS REQUESTED        ONL03
         MVC   QPJOBID,JCTJBKEY    SAVE JOB KEY FOR FUTURE CHECKS ONL03
         MVC   QPDSID,=X'FFFF'     NULLIFY DATASET ID               SBG
         MVC   QOLDDSID,=X'FFFF'   NULLIFY PREVIOUS DSID            SBG
         SPACE 1                                                  ONL03
VALIDJCT DS    0H                  THE CORRECT JCT WAS READ       ONL03
         SPACE 2                                                  ONL03
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR JOB                           ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
CKAUTH   DS    0H                  SEE IF USER MAY LOOK AT JOB    ONL02
         QAUTHCK QAFINDJ           PERFORM JOB AUTHORIZATION      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL03
*                                                                 ONL03
*   READ JOB'S ALLOCATION IOT                                     ONL03
*                                                                 ONL03
*    NOTE:  QUEUE SUB-COMMANDS MUST VALIDATE THIS IOT BEFORE USE. ONL03
*                                                                 ONL03
******************************************************************ONL03
         SPACE 1                                                  ONL03
         TM    QFLAG1,QFLG1SDS     IS THIS A SYSTEM DS JOB?       ONL03
         BO    GOTMTTR             SKIP IF YES                    ONL03
         MVC   QCTRAK,JCTIOT       GET MTTR FOR IOT               ONL03
GOTMTTR  DS    0H                                                 ONL03
         SPACE 1                                                  ONL03
         DROP  R3                  KILL JCT ADDRESSABILITY        ONL03
         SPACE 1                                                  ONL03
         L     R1,QCIOTA      ADDR OF IOAREA FOR IOT
         L     R15,=V(READSPC) ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
         SPACE 2                                                  ONL03
******************************************************************ONL03
*                                                                 ONL03
*   RETURN TO CALLER                                              ONL03
*                                                                 ONL03
******************************************************************ONL03
         SPACE 1                                                  ONL03
STOP     QSTOP
         SPACE 2                                                  ONL03
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                  ONL03
MVZ      MVZ   QFZONES(1),QPARM1 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM1(1) CONVERT TO BINARY
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
FINDJOB  CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
        $BUFFER ,                                                 ONL16
         IFGRPL
         $JQE
         $JCT
TABLNGTH EQU   0                                                  UF046
         $IOT  ,                                                  UF046
         $JQT
         CVT   DSECT=YES                                          UF035
         IEESMCA ,                                                UF035
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
WORK     DSECT
         DS    72C
CONVERT  DS    D
SLID#    DS    X                   SYSTEM ID OF DESIRED SYSLOG    UF035
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
