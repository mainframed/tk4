PARSE    TITLE 'QUEUE COMMAND - COMMAND LINE PARSE ROUTINES'      ONL01
PARSE    QSTART ,                  START PARSE CSECT ASSEMBLY     ONL01
***********************************************************************
* RNB CHANGES:                                                        *
*    (1) ADDED COMMAND ABBREVIATIONS: RNB06                           *
*                JM FOR JMSG                                          *
*                JL FOR JLOG                                          *
*                JC FOR JCL                                           *
*                SL FOR SLOG                                          *
*                FT FOR FTIM                                          *
*                DE FOR DEL                                           *
*                RE FOR REQ                                           *
*                                                                     *
***********************************************************************
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     LOAD BASE REG
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   PARSE SUBCOMMAND NAME                                             *
*                                                                     *
***********************************************************************
         LH    R2,QDRLNG      LENGTH OF REPLY LINE
         OC    QDREPLY,QBLANK TRANSLATE TO UPPER CASE
         CLC   QDREPLY,QBLANK IS THE ENTIRE REPLY BLANK?
         BE    LOOKUP         YES. DO LOOKUP.
         MVC   QDTLINE,QDREPLY MOVE COMMAND LINE TO HEADING
         MVC   FIELD,QBLANK   BLANK THE WORK FIELD
         MVC   OFFSET(4),=F'0' ZERO THE OFFSET AND LENGTH
         MVC   QOFF0(12),OFFSET INITIALIZE FIRST FIELD
         MVC   QOFF1(48),QOFF0 INITIALIZE NEXT FOUR FIELDS
         LA    R6,QOFF4+12    ADDR PAST LAST FIELD
         LA    R5,QOFF0       ADDR OF FIRST SET OF FIELDS
         LA    R3,QDREPLY     FIRST BYTE OF REPLY LINE
ENCORE   LA    R4,FIELD       FIRST BYTE OF WORK FIELD
BLANK    CLI   0(R3),C' '     IS THIS BYTE BLANK?
         BNE   FIRST          NO. START OF FIELD.
         LA    R3,1(R3)       YES. SKIP IT.
         BCT   R2,BLANK       TRY NEXT BYTE
         B     EMPTY          END OF REPLY LINE.
FIRST    LH    R1,QDRLNG      REPLY LENGTH
         SR    R1,R2          COMPUTE OFFSET TO START OF FIELD
         STH   R1,OFFSET      STORE OFFSET
         LR    R1,R2          SAVE COUNT OF REMAINING BYTES
         B     CHAR           CONTINUE
LOOP     CLI   0(R3),C' '     IS THIS BYTE BLANK?
         BE    LAST           YES. END OF FIELD.
CHAR     MVC   0(1,R4),0(R3)  MOVE BYTE TO SUBNAME
         LA    R3,1(R3)       INCREMENT
         LA    R4,1(R4)       INCREMENT
         BCT   R2,LOOP        TRY NEXT BYTE
LAST     SR    R1,R2          COMPUTE FIELD LENGTH
         CH    R1,=H'8'       IS LENGTH GREATER THAN 8?
         BNH   STORE          NO. USE IT.
         LA    R1,8           YES. USE LENGTH OF EIGHT.
STORE    STH   R1,LENGTH      STORE FIELD LENGTH
EMPTY    MVC   0(12,R5),OFFSET MOVE FIELD TO QCOMMON
         LTR   R2,R2          IS THE REMAINING LENGTH ZERO?
         BZ    LOOKUP         YES. DO THE TABLE LOOKUP.
         MVC   FIELD,QBLANK   BLANK THE WORK FIELD
         MVC   OFFSET(4),=F'0' ZERO OUT OFFSET AND LENGTH
         LA    R5,12(R5)      INCREMENT TO NEXT FIELD
         CR    R5,R6          WAS THAT THE LAST FIELD?
         BL    ENCORE         NO. PROCESS NEXT FIELD.
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM A TABLE LOOK UP FOR THE QUEUE SUB-COMMAND             ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
LOOKUP   DS    0H                  FIND QUE SUB-COMMAND IN TABLE  ONL02
         CLI   QSUBNAME,C'0'       IS THIS A NUMBER               WGH
         BL    LOOKUP2             NO..                           WGH
         MVC   QPARM1,QSUBNAME     YES.. MOVE IT TO PARM1         WGH
         MVC   QLNG1,QLNG0               AND LENGTH               WGH
         MVC   QSUBNAME(4),=C'LIST'  CREATE LIST CMD              WGH
         LA    R2,4                SET LENGTH                     WGH
         STH   R2,QLNG0            STORE IT                       WGH
LOOKUP2  LA    R2,TABLE            ADDR OF SUB-COMMAND TABLE      ONL02
         SPACE 1                                                  ONL02
NEXT     CLC   0(4,R2),QSUBNAME    COMPARE FIRST 4 CHARACTERS     UF014
         BE    FOUND               THIS IS THE ONE
         LA    R2,12(R2)           NEXT ENTRY                     UF014
         CLI   0(R2),X'FF'         IS THIS THE END OF TABLE?
         BNE   NEXT                NO. TRY NEXT ENTRY.
         SPACE 1                                                  ONL02
FOUND    DS    0H                  SUB-CMD FOUND OR END OF TABLE  ONL02
         MVC   QSUBCMD,4(R2)       SET SUB-COMMAND ADDR FROM TABLEONL02
         MVC   QCODEH,8(R2)        SET FUNCTION CODE FROM TABLE   ONL02
         MVC   QASCFLGS,10(R2)     SET TYPE FLAGS FROM TABLE      ONL02
         MVC   QASCUSER,11(R2)     SET USER FLAGS FROM TABLE      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AN AUTHORIZATION CHECK AND RETURN TO PROCESS COMMAND  ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QAUTHCK QAPARSE           PERFORM AUTHORIZATION CHECK    ONL02
         SPACE 1                                                  ONL02
         QSTOP ,                   RETURN TO PROCESS COMMAND      ONL02
         EJECT ,                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   TABLE OF QUEUE SUB-COMMANDS                                   ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
TABLE    DS    0F                  QUE SUB-COMMAND COMMAND TABLE  ONL02
         QSCE  STAT,SEARCH                 STATUS                 ONL02
         QSCE  ST,SEARCH                   STATUS                 ONL02
         QSCE  DD,DDNAME                   LIST DSID'S FOR A JOB  ONL02
         QSCE  LIST,LIST                   LIST SYSIN/SYSOUT FILE ONL02
         QSCE  L,LIST                      LIST SYSIN/SYSOUT FILE ONL02
         QSCE  FIND,REPOS,FNC=4            FIND                   ONL02
         QSCE  FALL,REPOS,FNC=4            FIND ALL               ONL02
         QSCE  FA,REPOS,FNC=4              FIND ALL               ONL02
         QSCE  F,REPOS,FNC=4               FIND                   ONL02
         QSCE  FTIM,REPOS,FNC=8            FTIME (SYSLOG)         ONL02
         QSCE  FT,REPOS,FNC=8              FTIME (SYSLOG)         ONL02
         QSCE  COL,REPOS,FNC=12            COLUMN                 ONL02
         QSCE  CO,REPOS,FNC=12             COLUMN                 ONL02
         QSCE  C,REPOS,FNC=12              COLUMN                 ONL02
         QSCE  '@',REPOS,FNC=16            REPOS TO RECORD NUMBER ONL02
         QSCE  MD,REPOS,FNC=16             REPOS TO RECORD NUMBER ONL02
         QSCE  '+',REPOS,FNC=20            DOWN                   ONL02
         QSCE  D,REPOS,FNC=20              DOWN                   ONL02
         QSCE  PF,REPOS,FNC=20             PAGE FORWARD           ONL02
         QSCE  HF,REPOS,FNC=20             HALF PAGE FORWARD      ONL02
         QSCE  '-',REPOS,FNC=24            UP                     ONL02
         QSCE  UP,REPOS,FNC=24             UP                     ONL02
         QSCE  PB,REPOS,FNC=24             PAGE BACK              ONL02
         QSCE  HB,REPOS,FNC=24             HALF BACK              ONL02
         QSCE  TOP,REPOS,FNC=28            TOP                    ONL02
         QSCE  T,REPOS,FNC=28              TOP                    ONL02
         QSCE  BOTT,REPOS,FNC=32           BOTTOM                 ONL02
         QSCE  BOT,REPOS,FNC=32            BOTTOM                 ONL02
         QSCE  B,REPOS,FNC=32              BOTTOM                 ONL02
         QSCE  MODE,REPOS,FNC=36           MODEL                  ONL02
         QSCE  MO,REPOS,FNC=36             MODEL                  ONL02
         QSCE  M,REPOS,FNC=36              MODEL                  ONL02
         QSCE  SAVE,SAVE,FNC=4             SAVE CURRENT FILE      ONL02
         QSCE  SPIN,CSPIN,FNC=4            SPIN CURRENT FILE      ONL02
         QSCE  JCL,JCL                     LIST JCL               ONL02
         QSCE  JC,JCL                      LIST JCL               ONL02
         QSCE  JLOG,JLOG                   LIST JOBLOG            ONL02
         QSCE  JL,JLOG                     LIST JOBLOG            ONL02
         QSCE  JMSG,JMSG                   LIST SYSMSGS           ONL02
         QSCE  JM,JMSG                     LIST SYSMSGS           ONL02
         QSCE  JH,CJHIST                   LIST JOB HISTORY       ONL02
         QSCE  JHIS,CJHIST                 LIST JOB HISTORY       ONL02
         QSCE  PFK,CPFK                    PFK LIST/CHANGE CMD    ONL02
         QSCE  PDDB,CPDDB                  LIST PDDB'S FOR JOB    ONL02
         QSCE  DA,SEARCH,FNC=4,TYPE=QASXACTV  DISP ALL IN EXEC    ONL02
         QSCE  DC,ACTIVE,TYPE=QASXACTV     DISP CPU BATCH/STC/TSO ONL02
         QSCE  DI,SEARCH,FNC=8,            DISP ALL IN INPUT CLASSONL02X
               TYPE=QASXBLOG                                      ONL02
         QSCE  DO,SEARCH,FNC=12,           DISP ALL IN OUTPUT CLASONL02X
               TYPE=QASXBLOG                                      ONL02
         QSCE  WO,SEARCH,FNC=52,           OUTPUT QUEUE W/ WTRID  ONL02X
               TYPE=QASXBLOG                                      ONL02
         QSCE  AI,SEARCH,FNC=16,           DISP ALL AVAIL FOR EXECONL02X
               TYPE=QASXBLOG                                      ONL02
         QSCE  AO,SEARCH,FNC=20,           DISP ALL AVAIL OUTPUT  ONL02X
               TYPE=QASXBLOG                                      ONL02
         QSCE  HI,SEARCH,FNC=24,           DISP ALL IN INPUT HOLD ONL02X
               TYPE=QASXBLOG                                      ONL02
         QSCE  HO,SEARCH,FNC=28,           DISP ALL IN OUTPUT HOLDONL02X
               TYPE=QASXBLOG                                      ONL02
         QSCE  HQ,SEARCH,FNC=60,           DISP ALL IN $OUTPUT Q  WGH42X
               TYPE=QASXBLOG                                      ONL02
         QSCE  DT,SEARCH,FNC=32,TYPE=QASXACTV  DISP TSO USERS     ONL02
         QSCE  DJ,SEARCH,FNC=36            DISPLAY JOB            ONL02
         QSCE  DS,SEARCH,FNC=40,TYPE=QASXACTV  DISPLAY STC        ONL02
         QSCE  DQ,SEARCH,FNC=44,TYPE=QASXBLOG  DISP INPUT QUEUES  ONL02
         QSCE  DF,SEARCH,FNC=48,TYPE=QASXBLOG  DISP OUTPUT QUEUES ONL02
         QSCE  DELE,SYSOUT,TYPE=QASXSSSM   DELETE JOB             ONL07
         QSCE  DEL,SYSOUT,TYPE=QASXSSSM    DELETE JOB             ONL02
         QSCE  DE,SYSOUT,TYPE=QASXSSSM     DELETE JOB             ONL02
         QSCE  REQ,SYSOUT,FNC=4,TYPE=QASXSSSM  REQUEUE JOB        ONL02
         QSCE  RE,SYSOUT,FNC=4,TYPE=QASXSSSM  REQUEUE JOB         ONL02
         QSCE  CANC,SYSOUT,FNC=8,TYPE=QASXSSSM  CANCEL JOB        ONL07
         QSCE  CAN,SYSOUT,FNC=8,TYPE=QASXSSSM  CANCEL JOB         ONL02
         QSCE  CA,SYSOUT,FNC=8,TYPE=QASXSSSM  CANCEL JOB          ONL02
         QSCE  TSO,CTSO,TYPE=QASXEXEC      ISSUE ANY TSO COMMAND  ONL02
         QSCE  EX,CTSO,TYPE=QASXEXEC       IMPLICIT CLIST INVOKE  ONL02
         QSCE  EXEC,CTSO,TYPE=QASXEXEC     IMPLICIT CLIST INVOKE  ONL02
         QSCE  SLOG,SYSLOG,TYPE=QASXSLOG   LIST THE SYSTEM LOG    ONL02
         QSCE  SL,SYSLOG,TYPE=QASXSLOG     LIST THE SYSTEM LOG    ONL02
         QSCE  XI,INITS,TYPE=QASXSYSP      DISP ACTIVE INITS      ONL02
         QSCE  XB,HEXBLK,TYPE=QASXSYSP     DISP SPOOL BLOCK       ONL02
         QSCE  XJ,SEARCH,FNC=36,TYPE=QASXSYSP  DISP HEX JQE/JOE   ONL02
         QSCE  XD,XDS,TYPE=QASXSYSP        UNRESTRICTED FILE DISP ONL02
         QSCE  XP,HELP,TYPE=QASXOPER       ENTER PASSWORD         ONL02
         QSCE  JQE,CJQE,TYPE=QASXSYSP      HEX DUMP OF JQE        ONL02
         QSCE  JCT,CJCT,TYPE=QASXSYSP      HEX DUMP OF JCT        ONL02
         QSCE  IOT,CIOT,TYPE=QASXSYSP      HEX DUMP OF IOTS          CL
         QSCE  HCT,CHCT,TYPE=QASXSYSP      HEX DUMP OF HCT SAVEAREONL02
         QSCE  JOE,CJOE,TYPE=QASXSYSP      HEX DUMP OF JOE        ONL02
         QSCE  TGPS,CTGPS,TYPE=QASXSYSP    LIST LARGE SPOOL USERS ONL02
         QSCE  PFX,SEARCH,FNC=56,TYPE=QASXSYSP  SET PREFIX        WGH
         QSCE  QUIE,CQUIET                 SET QUIET MODE         WGH
         QSCE  TALK,CQUIET                 SET TALK  MODE         WGH
         QSCE  NOS,CQUIET                  SET NOSTACK MODE       WGH
         QSCE  NOST,CQUIET                 SET NOSTACK MODE       WGH
         QSCE  ENDS,CQUIET                 SET ENDSTACK MODE      WGH
         QSCE  TERM,CQUIET                 SET ENDSTACK MODE      WGH
         QSCE  STAC,CQUIET                 SET    STACK MODE      WGH
         QSCE  DIE,CDIE,TYPE=QASXSYSP      FORCE 0C1 PROGRAM CHECKONL02
         SPACE 1                                                  ONL02
HELPCC   DC    F'-1',V(HELP),Y(0),AL1(0,0) MARK END OF TABLE      ONL02
         EJECT ,                                                  ONL02
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
WPASS    DC    X'C1115D7F1140403C4040001D4C13'  *** SPF TCAM
         SPACE 1                                                  UF024
WORK     DSECT
         DS    CL72
OFFSET   DS    H
LENGTH   DS    H
FIELD    DS    CL8
RPASS    DS    CL8
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
