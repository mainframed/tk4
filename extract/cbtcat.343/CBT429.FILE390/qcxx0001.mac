CKPT     TITLE 'QUEUE COMMAND - READ JES2 CKPT RECORDS'           ONL01
CKPT     QSTART ,                  START CKPT CSECT ASSEMBLY      ONL01
         USING QCKPT,R10      BASE REG FOR HASP WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   REPOSITION DATASET                                                *
*                                                                     *
***********************************************************************
         POINT HASPCKPT,TIR3  POINT PAST SYNC RECORDS
***********************************************************************
*                                                                     *
*   READ FIRST CHECKPOINT RECORD                                      *
*                                                                     *
***********************************************************************
         L     R2,QCJQTL      ADDR OF IOAREA FOR CKPT HEADER REC
         READ  HDECB1,SF,,(R2),MF=E                               UF039
         CHECK HDECB1
         AH    R2,=H'4096'    INCREMENT PAST HEADER RECORD        UF049
***********************************************************************
*                                                                     *
*   READ REMAINDER OF CHECKPOINT RECORDS                              *
*                                                                     *
***********************************************************************
         L     R3,QCJOTL      NUMBER OF RECORDS IN CKPT DATASET   UF039
         BCTR  R3,0           DROP FOR PREVIOUSLY READ HEADER REC UF039
***********************************************************************
*LOOP    READ  HDECB1,SF,,(R2),MF=E                               UF039
*        CHECK HDECB1                                             UF039
*        AH    R2,=H'4096'    INCREMENT TO NEXT BUFFER            UF039
*        BCT   R3,LOOP        READ NEXT RECORD.                   UF039
***********************************************************************
LOOP1    LR    R6,R3          ASSUME CAN READ ALL BLOCKS AT ONCE  UF039
         SR    R0,R0          CLEAR FOR IC                        UF039
         IC    R0,DCBNCP-IHADCB+HASPCKPT  GET NCP ALLOWED         UF039
         CR    R6,R0                                              UF039
         BNH   *+6                                                UF039
         LR    R6,R0          LIMIT READS TO NCP MAX              UF039
         LR    R4,R6          SAVE NUMBER OF READS TO DO          UF039
         L     R7,QCDECBPT    POINTER TO DECB WORK AREA           UF039
LOOP2    MVC   0(HDECB1L,R7),HDECB1  PRIME THE DECB               UF039
         READ  (R7),SF,,(R2),MF=E  START A READ                   UF039
         AH    R2,=H'4096'    BUMP TO NEXT BUFFER AREA            UF039
         LA    R7,HDECB1L(,R7) BUMP TO NEXT DECB ADDR             UF039
         BCT   R4,LOOP2       LOOP FOR MAX READS                  UF039
         SPACE 1                                                  UF039
         LR    R4,R6          NUMBER OF CHECKS TO DO              UF039
         L     R7,QCDECBPT    POINT TO DECBS                      UF039
LOOP3    CHECK (R7)           CHECK A READ                        UF039
         LA    R7,HDECB1L(,R7) BUMP TO NEXT DECB                  UF039
         BCT   R4,LOOP3       CHECK ALL READS                     UF039
         SPACE 1                                                  UF039
         SR    R3,R6          TOTAL TO READ - JUST READ           UF039
         BP    LOOP1          CONTINUE IF MORE TO READ            UF039
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
         QSTOP
         LTORG
         DS    0F
TIR3     DC    X'00000300'    POINT PAST SYNC RECORDS
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
*        IHADCB DSORG=PS                                          UF039
         DCBD   DSORG=PS                                          UF039
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
