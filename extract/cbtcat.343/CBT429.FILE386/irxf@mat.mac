IRXF@MAT TITLE 'L_MATCH'
IRXF@MAT CSECT
IRXF@MAT AMODE 31
IRXF@MAT RMODE ANY
         @EQUS
******************
* ESTABLECER DIR.*
******************
         BAKR  R14,0
         LR    R12,R15
         USING IRXF@MAT,R12
         LR    R02,R01
         USING EFPL,R02
         L     R03,EFPLARG
         L     R04,EFPLEVAL
         USING ARGTABLE_ENTRY,R03
         L     R04,0(R04)
*******************
* LEER PARAMETRO 1*
*******************
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    FINNOK
         L     R06,ARGTABLE_ARGSTRING_LENGTH
         C     R06,F256
         BH    FINNOK
         LR    R00,R05
         LR    R01,R06
*******************
* LEER PARAMETRO 2*
*******************
         LA    R03,ARGTABLE_NEXT
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    FINNOK
         L     R06,ARGTABLE_ARGSTRING_LENGTH
         C     R06,F256
         BH    FINNOK
         LR    R14,R05
         LR    R15,R06
         LR    R05,R04
         USING EVALBLOCK,R05
******************
* CARGAR PARMS   *
******************
         LR    R03,R00                   -> SOURCE
         LR    R02,R14                   -> PATTERN
******************
* INIT SOURCE    *
******************
         LR    R06,R01
         BCTR  R06,0
         EX    R06,MOVESTR               MOVE A WORK
         LA    R10,MATSTR
         LA    R06,1(R06)
         AR    R10,R06
         MVI   0(R10),MATNULL            PONER NULO AL FINAL
         LA    R03,MATSTR
MENOSSTR BCTR  R10,0
         CR    R10,R03
         BL    INIPAT
         CLI   0(R10),MATNULL
         BE    MENOSSTR
         CLI   0(R10),MATBLANK
         BNE   INIPAT
         MVI   0(R10),MATNULL            MOVE NULOS A BLANCOS DE COLA
         B     MENOSSTR
******************
* INIT PATTERN   *
******************
INIPAT   LR    R07,R15
         BCTR  R07,0
         EX    R07,MOVEPAT               MOVE A WORK
         LA    R10,MATPAT
         LA    R07,1(R07)
         AR    R10,R07
         MVI   0(R10),MATNULL            PONER NULO AL FINAL
         LA    R02,MATPAT
MENOSPAT BCTR  R10,0
         CR    R10,R02
         BL    COMPARE
         CLI   0(R10),MATNULL
         BE    MENOSPAT
         CLI   0(R10),MATBLANK
         BNE   COMPARE
         MVI   0(R10),MATNULL            MOVE NULOS A BLANCOS DE COLA
         B     MENOSPAT
********************
* COMIENZO COMPARE *
********************
COMPARE  DS    0H
         LA    R08,MATSCANS
         CLI   0(R02),MATGEN
         BNE   CHKNXT1
         CLI   1(R02),MATNULL
         BE    MATCH
CHKNXT1  DS    0H
         CR    R06,R07
         BCTR  R06,0
         BNE   CHKNXT5
         EX    R06,CHKMATCH
         BE    MATCH
CHKNXT5  DS    0H
         EX    R06,CHKBLNK2
         BE    NOMATCH
CHKNXT2  DS    0H
         BCTR  R07,0
         EX    R07,CHKBLNKS
         BE    MATCH
         SR    R04,R04
SPCHARCK DS    0H
         CLI   0(R02),MATGEN
         BE    FNDASTR
         CLI   0(R02),MATPLACE
         BNE   REGALPHA
         SR    R04,R04
NXTFCHAR DS    0H
         LA    R03,1(,R03)
NXTPCHAR DS    0H
         LA    R02,1(,R02)
         CLI   0(R02),MATNULL
         BE    PATRNEND
         CLI   0(R02),MATGEN
         BNE   CHKNXT4
         CLI   1(R02),MATNULL
         BE    MATCH
CHKNXT3  DS    0H
         CLI   0(R02),MATGEN
         BNE   CHKNXT4
         CLC   1(R02),MATGEN
         BE    NXTPCHAR
CHKNXT4  DS    0H
         CLI   0(R03),MATNULL
         BE    FAIL
         B     SPCHARCK
PATRNEND DS    0H
         CLI   0(R03),MATNULL
         BE    MATCH
         BCTR  R02,0
         B     FAIL1
REGALPHA DS    0H
         CLC   0(1,R02),0(R03)
         BNE   FAIL1
         SR    R04,R04
         B     NXTFCHAR
FNDASTR  DS    0H
         BCTR  R04,0
         CLI   1(R02),MATPLACE
         BNE   MULTICHK
         XC    0(1,R02),1(R02)
         XC    1(1,R02),0(R02)
         XC    0(1,R02),1(R02)
         B     NXTFCHAR
MULTICHK DS    0H
         STM   R02,R03,0(R08)
         LA    R08,8(,R08)
         B     NXTPCHAR
FAIL1    DS    0H
         LTR   R04,R04
         BZ    FAIL
         BCTR  R02,0
         B     NXTFCHAR
FAIL     DS    0H
         LA    R03,MATSCANS
         CR    R08,R03
         BE    NOMATCH
         SH    R08,MATH8
         LM    R02,R03,0(R08)
         BCTR  R02,0
         B     NXTFCHAR
**************
* FIN        *
**************
NOMATCH  MVI   EVALBLOCK_EVDATA,C'0'
         LA    R09,1
         ST    R09,EVALBLOCK_EVLEN
         SR    R15,R15
         PR
MATCH    MVI   EVALBLOCK_EVDATA,C'1'
         LA    R09,1
         ST    R09,EVALBLOCK_EVLEN
         SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
MOVESTR  MVC   MATSTR(0),0(R03)
MOVEPAT  MVC   MATPAT(0),0(R02)
CHKMATCH CLC   MATSTR(0),MATPAT
CHKBLNKS CLC   MATPAT(0),MAT8BLNK
CHKBLNK2 CLC   MATSTR(0),MAT8BLNK
F256     DC    F'256'
MATNULL  EQU   X'00'
MATBLANK EQU   C' '
MATGEN   EQU   C'*'
MATPLACE EQU   C'%'
MATH8    DC    H'8'
MAT8BLNK DC    CL8' '
MATSCANS DS    18F
MATPAT   DS    256C
MATSTR   DS    256C
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@MAT
