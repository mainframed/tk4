**********************
* ESTABLECER DIRECC. *
**********************
IRXF@WTO CSECT
IRXF@WTO AMODE 31
IRXF@WTO RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R02,R01
         USING IRXF@WTO,R12
         USING EFPL,R02
         L     R03,EFPLARG
         L     R04,EFPLEVAL
         USING ARGTABLE_ENTRY,R03
         L     R04,0(R04)
         USING EVALBLOCK,R04
*******************
* LEER PARAMETRO 1*
*******************
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    FINNOK
         L     R06,ARGTABLE_ARGSTRING_LENGTH
         LTR   R06,R06
         BZ    FINNOK
         C     R06,F122
         BNH   MOVTWK
         LA    R06,122
MOVTWK   STH   R06,WTOTEXT
         BCTR  R06,R00
         EX    R06,MOVWT
*******************
* LEER PARAMETRO 2*
*******************
         LA    R03,ARGTABLE_NEXT
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    HWTO
         CLI   0(R05),C'R'
         BNE   FINNOK
************************
* HACER WTOR           *
************************
HWTOR    XC    ECBREPLY,ECBREPLY
         MVI   REPLY,C' '
         MVC   REPLY+1(118),REPLY
         WTOR  TEXT=(WTOTEXT,REPLY,119,ECBREPLY)
         LTR   R15,R15
         BNZ   FINNOK
         WAIT  ECB=ECBREPLY
         MVC   EVALBLOCK_EVDATA(119),REPLY
         LA    R09,119
         LA    R05,EVALBLOCK_EVDATA+118
STRIPT   CLI   0(R05),C' '
         BNE   SETL
         BCTR  R05,R00
         BCT   R09,STRIPT
SETL     LTR   R09,R09
         BNZ   SETL2
         LA    R09,1
SETL2    ST    R09,EVALBLOCK_EVLEN
         B     EXIT
************************
* HACER WTO            *
************************
HWTO     WTO   TEXT=WTOTEXT
         LTR   R15,R15
         BNZ   FINNOK
         MVI   EVALBLOCK_EVDATA,C'1'
         LA    R09,1
         ST    R09,EVALBLOCK_EVLEN
************************
* EXIT                 *
************************
EXIT     SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
MOVWT    MVC   WTOTEXT+2(0),0(R05)
F122     DC    F'122'
WTOTEXT  DS    CL124
REPLY    DS    CL119
ECBREPLY DS    F
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@WTO
