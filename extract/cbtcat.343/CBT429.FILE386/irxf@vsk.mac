IRXF@VSK TITLE 'IRXF@VSK  - INTERFASE VSAM/REXX'
***************************
* DIRECCIONAMIENTO        *
*                         *
* R0  - WORK              *
* R1  - WORK              *
* R2  -                   *
* R3  - RC                *
* R4  - RPL               *
* R5  - BUFF              *
* R6  - ACB               *
* R7  - IRXEXCOM          *
* R8  - IRXEXTE           *
* R9  - ENV-BLOCK (REXX)  *
* R10 - EFPL              *
* R11 - BASE+4096         *
* R12 - BASE              *
* R13 - SAVER/DATA        *
* R14 - WORK (BALR)       *
* R15 - WORK              *
*                         *
***************************
IRXF@VSK CSECT
IRXF@VSK AMODE 31
IRXF@VSK RMODE ANY
         @EQUS
**********************
* ESTABLECER DIRECC. *
**********************
         BAKR  R14,0
         LR    R12,R15
         LR    R10,R1
         LR    R9,R0
         LA    R1,1
         LA    R11,4095(R1,R12)
         USING IRXF@VSK,R12,R11
         USING EFPL,R10
         L     R3,EFPLARG
         L     R4,EFPLEVAL
         USING ARGTABLE_ENTRY,R3
         L     R4,0(R4)
         USING ENVBLOCK,R9
         GETMAIN RU,LV=LDATA,LOC=(BELOW,ANY)
         LR    R13,R1
         USING DATA,R13
         ST    R4,SAVEEVAL
         LA    R5,BUFFER
         ST    R5,BUFF_A+8
         LA    R1,ENQ_N
         ST    R1,ENQ_A+0
         LA    R1,ENQ_L
         ST    R1,ENQ_A+4
         LA    R1,ENQ_V
         ST    R1,ENQ_A+8
         LA    R1,ENQ_S
         ST    R1,ENQ_A+12
         LA    R1,ACBA_N
         ST    R1,ACBA_A+0
         LA    R1,ACBA_L
         ST    R1,ACBA_A+4
         LA    R1,ACBA_V
         ST    R1,ACBA_A+8
         LA    R1,ACBA_S
         ST    R1,ACBA_A+12
         LA    R1,ACBR_N
         ST    R1,ACBR_A+0
         LA    R1,ACBR_L
         ST    R1,ACBR_A+4
         LA    R1,ACBR_V
         ST    R1,ACBR_A+8
         LA    R1,ACBR_S
         ST    R1,ACBR_A+12
         LA    R1,BUFF_N
         ST    R1,BUFF_A+0
         LA    R1,BUFF_L
         ST    R1,BUFF_A+4
         LA    R1,BUFF_S
         ST    R1,BUFF_A+12
         MVC   BUFF_S,=F'32760'
         L     R2,ARGTABLE_ARGSTRING_PTR
         DROP  R3
         LTR   R2,R2
         BM    EXITNOK
         L     R8,ENVBLOCK_IRXEXTE
         USING IRXEXTE,R8
         L     R7,IRXEXCOM
         DROP  R8
***********
* MAIN    *
***********
         CLC   0(4,R2),=CL4'OPEN'
         BE    FOPEN
         CLC   0(5,R2),=CL5'CLOSE'
         BE    FCLOSE
         CLC   0(3,R2),=CL6'ENQ'
         BE    ENQS
         CLC   0(3,R2),=CL6'DEQ'
         BE    DEQS
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),ARG=(R5),      X
               RECLEN=(R2),OPTCD=(DIR,NUP,KEQ),MF=(L,MODCB1,MODCBL)
         CLC   0(6,R2),=CL6'INSERT'
         BE    FINSERT
         CLC   0(6,R2),=CL6'SELECT'
         BE    FSELECT
         CLC   0(6,R2),=CL6'DELETE'
         BE    FDELETE
         CLC   0(6,R2),=CL6'UPDATE'
         BE    FUPDATE
         CLC   0(6,R2),=CL6'SELECN'
         BE    FSELECN
         CLC   0(6,R2),=CL6'SELECF'
         BE    FSELECF
         CLC   0(6,R2),=CL6'SELECL'
         BE    FSELECL
         CLC   0(6,R2),=CL6'SELECB'
         BE    FSELECB
EXITNOK  FREEMAIN RU,A=(R13),LV=LDATA
         LA    R15,16
         PR
***********
* SALIDAS *
***********
EXIT     EQU   *
         L     R4,SAVEEVAL
         USING EVALBLOCK,R4
         CVD   R3,WORKD
         MVC   EDWK(4),=X'21202040'
         ED    EDWK(4),WORKD+6
         MVC   EVALBLOCK_EVDATA(2),EDWK+1
         LA    R1,2
         ST    R1,EVALBLOCK_EVLEN
         FREEMAIN RU,A=(R13),LV=LDATA
         SR    R15,R15
         PR
         DROP  R4
**************
* GETVREX    *
* R2 - VAR_A *
**************
GETVREX  EQU   *
         ST    R14,PUTVREXS
         LA    R8,SHV1
         USING SHVBLOCK,R8
         MVI   SHVCODE,SHVFETCH
         L     R1,12(R2)
         MVC   SHVBUFL,0(R1)
         B     SETREX
**************
* PUTVREX    *
* R2 - VAR_A *
**************
PUTVREX  EQU   *
         ST    R14,PUTVREXS
         LA    R8,SHV1
         USING SHVBLOCK,R8
         MVI   SHVCODE,SHVSTORE
SETREX   MVC   SHVNEXT,=F'0'
         MVC   SHVUSER,=F'0'
         MVC   SHVRET,=F'0'
         L     R1,0(R2)
         ST    R1,SHVNAMA
         L     R1,4(R2)
         MVC   SHVNAML,0(R1)
         L     R1,8(R2)
         ST    R1,SHVVALA
         L     R1,12(R2)
         MVC   SHVVALL,0(R1)
         LA    R1,VP1
         ST    R1,CP1
         LA    R1,VP2Y3
         ST    R1,CP2
         ST    R1,CP3
         LR    R1,R8
         ST    R1,CP4
         OI    CP4,X'80'
         LA    R1,CP1
         LR    R0,R9
         LR    R15,R7
         BALR  R14,R15
         LTR   R15,R15
         BZ    SETREXR
         LA    R3,9
         B     EXIT
SETREXR  L     R14,PUTVREXS
         BR    R14
**********************************
* ENQS                           *
**********************************
ENQS     EQU   *
         LA    R2,ENQ_A
         BAL   R14,GETVREX
         LA    R2,ENQ_V
         ENQ   (MAJOR,(R2),E,,SYSTEMS),MF=(E,ENQ1)
         SR    R3,R3
         B     EXIT
**********************************
* DEQS                           *
**********************************
DEQS     EQU   *
         LA    R2,ENQ_A
         BAL   R14,GETVREX
         LA    R2,ENQ_V
         DEQ   (MAJOR,(R2),,SYSTEMS),MF=(E,DEQ1)
         SR    R3,R3
         B     EXIT
**********************************
* GETACBE                        *
**********************************
GETACBE  EQU   *
         ST    R14,GETACBES
         LA    R2,ACBA_A
         BAL   R14,GETVREX
         L     R6,ACBA_V
         LA    R2,ACBR_A
         BAL   R14,GETVREX
         L     R4,ACBR_V
         L     R14,GETACBES
         BR    R14
*************
* FOPEN     *
*************
FOPEN    EQU   *
         LH    R2,KEINTVF+2
         GETMAIN RU,LV=(R2),LOC=ANY
         LR    R6,R1
         BCTR  R2,R0
         EX    R2,COPACB
         MVI   OPEN1,X'FF'
         OPEN  ((R6),UPDAT),MODE=31,MF=(E,OPEN1)
         LTR   R15,R15
         BZ    SAVACB
         LA    R3,10
         B     EXIT
SAVACB   ST    R6,ACBA_V
         LA    R2,ACBA_A
         BAL   R14,PUTVREX
         GENCB BLK=RPL,                                                X
               ACB=(R6),AREALEN=(R1),AREA=(R1),MF=(L,GENCB1,GENCBL)
         GENCB BLK=RPL,                                                X
               ACB=(R6),AREALEN=(R1),AREA=(R1),MF=(E,GENCB1)
         LTR   R15,R15
         BZ    OKGEN
         LA    R3,19
         B     EXIT
OKGEN    LR    R4,R1
         ST    R4,ACBR_V
         LA    R2,ACBR_A
         BAL   R14,PUTVREX
         SR    R3,R3
         B     EXIT
COPACB   MVC   0(0,R6),KEINTVF
*************
* FCLOSE    *
*************
FCLOSE   EQU   *
         BAL   R14,GETACBE
         MVI   CLOSE1,X'80'
         CLOSE ((R6)),MODE=31,MF=(E,CLOSE1)
         LTR   R15,R15
         BZ    FREEW
         LA    R3,11
         B     EXIT
FREEW    LH    R2,KEINTVF+2
         FREEMAIN RU,LV=((R2)),A=((R6))
         SR    R3,R3
         B     EXIT
*************
* FINSERT   *
*************
FINSERT  EQU   *
         BAL   R14,GETACBE
         LA    R2,BUFF_A
         BAL   R14,GETVREX
         L     R2,SHVVALL
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               RECLEN=(R2),OPTCD=(DIR,NUP,KEQ),MF=(E,MODCB1)
         PUT   RPL=(R4)
         LTR   R15,R15
         BZ    INSOK
         LA    R3,12
         B     EXIT
INSOK    SR    R3,R3
         B     EXIT
*************
* FSELECT   *
*************
FSELECT  EQU   *
         BAL   R14,GETACBE
         LA    R2,BUFF_A
         BAL   R14,GETVREX
         L     R2,=F'32760'
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               ARG=(R5),OPTCD=(DIR,UPD,KEQ),MF=(E,MODCB1)
         GET   RPL=(R4)
         LTR   R15,R15
         BZ    SELOK
         LA    R3,13
         B     EXIT
SELOK    SHOWCB RPL=(R4),AREA=BUFF_S,LENGTH=4,FIELDS=(RECLEN)
         LA    R2,BUFF_A
         BAL   R14,PUTVREX
         SR    R3,R3
         B     EXIT
*************
* FSELECN   *
*************
FSELECN  EQU   *
         BAL   R14,GETACBE
         L     R2,=F'32760'
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               OPTCD=(SEQ,NUP,KEQ,FWD),MF=(E,MODCB1)
         GET   RPL=(R4)
         LTR   R15,R15
         BZ    NEXOK
         LA    R3,18
         B     EXIT
NEXOK    SHOWCB RPL=(R4),AREA=BUFF_S,LENGTH=4,FIELDS=(RECLEN)
         LA    R2,BUFF_A
         BAL   R14,PUTVREX
         SR    R3,R3
         B     EXIT
*************
* FSELECB   *
*************
FSELECB  EQU   *
         BAL   R14,GETACBE
         L     R2,=F'32760'
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               OPTCD=(SEQ,NUP,KEQ,BWD),MF=(E,MODCB1)
         GET   RPL=(R4)
         LTR   R15,R15
         BZ    NEBOK
         LA    R3,21
         B     EXIT
NEBOK    SHOWCB RPL=(R4),AREA=BUFF_S,LENGTH=4,FIELDS=(RECLEN)
         LA    R2,BUFF_A
         BAL   R14,PUTVREX
         SR    R3,R3
         B     EXIT
*************
* FSELECL   *
*************
FSELECL  EQU   *
         BAL   R14,GETACBE
         LA    R2,BUFF_A
         BAL   R14,GETVREX
         L     R2,=F'32760'
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               OPTCD=(DIR,NUP,KEQ,BWD),ARG=(R5),MF=(E,MODCB1)
         POINT RPL=(R4)
         LTR   R15,R15
         BZ    NELOK
         LA    R3,22
NELOK    B     EXIT
*************
* FSELECF   *
*************
FSELECF  EQU   *
         BAL   R14,GETACBE
         LA    R2,BUFF_A
         BAL   R14,GETVREX
         L     R2,=F'32760'
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               OPTCD=(SKP,NUP,KGE,FWD),ARG=(R5),MF=(E,MODCB1)
         POINT RPL=(R4)
         LTR   R15,R15
         BZ    NEFOK
         LA    R3,20
NEFOK    B     EXIT
*************
* FDELETE   *
*************
FDELETE  EQU   *
         BAL   R14,GETACBE
         LA    R2,BUFF_A
         BAL   R14,GETVREX
         L     R2,=F'32760'
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               ARG=(R5),OPTCD=(DIR,UPD,KEQ),MF=(E,MODCB1)
         GET   RPL=(R4)
         LTR   R15,R15
         BZ    DELOK
         LA    R3,14
         B     EXIT
DELOK    ERASE RPL=(R4)
         LTR   R15,R15
         BZ    DEL2OK
         LA    R3,15
         B     EXIT
DEL2OK   SR    R3,R3
         B     EXIT
*************
* FUPDATE   *
*************
FUPDATE  EQU   *
         BAL   R14,GETACBE
         LA    R2,BUFF_A
         BAL   R14,GETVREX
         L     R2,=F'32760'
         MODCB RPL=(R4),ACB=(R6),AREA=(R5),AREALEN=(R2),               X
               ARG=(R5),OPTCD=(DIR,UPD,KEQ),MF=(E,MODCB1)
         GET   RPL=(R4)
         LTR   R15,R15
         BZ    UPDOK
         LA    R3,16
         B     EXIT
UPDOK    LA    R2,BUFF_A
         BAL   R14,GETVREX
         PUT   RPL=(R4)
         LTR   R15,R15
         BZ    UPD2OK
         LA    R3,17
         B     EXIT
UPD2OK   SR    R3,R3
         B     EXIT
         DROP  R13
*************
* VARIABLES *
*************
* TRABAJO
MAJOR    DC    CL8'IRXF@VSK'
VP1      DC    CL8'IRXEXCOM'
VP2Y3    DC    F'0'
ENQ_N    DC    CL10'KEINTV_ENQ'
ENQ_L    DC    AL4(L'ENQ_N)
ENQ_S    DC    AL4(L'ENQ_V)
ACBA_N   DC    CL11'KEINTV_ACBA'
ACBA_L   DC    AL4(L'ACBA_N)
ACBA_S   DC    AL4(L'ACBA_V)
ACBR_N   DC    CL11'KEINTV_ACBR'
ACBR_L   DC    AL4(L'ACBR_N)
ACBR_S   DC    AL4(L'ACBR_V)
BUFF_N   DC    CL11'KEINTV_BUFF'
BUFF_L   DC    AL4(L'BUFF_N)
* ACBS / RPLS
KEINTVF  ACB   DDNAME=KEINTVF,MACRF=(KEY,DIR,SEQ,IN,OUT),RMODE31=ALL
*************
* DATA AREA *
*************
DATA     DSECT
SAVER    DS    18F
SHV1     DS    8F
WORKD    DS    D
SAVEEVAL DS    F
PUTVREXS DS    F
GETACBES DS    F
CP1      DS    F
CP2      DS    F
CP3      DS    F
CP4      DS    F
EDWK     DS    CL4
ENQ_V    DS    CL255
ACBA_V   DS    F
ACBR_V   DS    F
BUFF_S   DS    F
ENQ_A    DS    F,F,F,F
ACBA_A   DS    F,F,F,F
ACBR_A   DS    F,F,F,F
BUFF_A   DS    F,F,F,F
ENQ1     ENQ   (,,,,),MF=L
DEQ1     DEQ   (,,,),MF=L
OPEN1    OPEN  (R0,),MODE=31,MF=L
CLOSE1   CLOSE (R0),MODE=31,MF=L
GENCB1   DS    0D,XL(GENCBL)
MODCB1   DS    0D,XL(MODCBL)
BUFFER   DS    0D
LDATA    EQU   (*-DATA)+32760
*************
* MAPAS     *
*************
PARM     DSECT
TXTADD   DS    F
TXTLEN   DS    F
         IRXENVB
         IRXEXTE
         IRXSHVB
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@VSK
