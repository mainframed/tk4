**********************
* ESTABLECER DIRECC. *
**********************
IRXF@MON CSECT
IRXF@MON AMODE 31
IRXF@MON RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R02,R01
         USING IRXF@MON,R12
         USING EFPL,R02
         L     R03,EFPLARG
         L     R04,EFPLEVAL
         USING ARGTABLE_ENTRY,R03
         L     R04,0(R04)
         USING EVALBLOCK,R04
*******************
* LEER PARAMETRO 1*
*******************
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,F8
         BH    FINNOK
         LTR   R6,R6
         BZ    FINNOK
         LR    R0,R6
         LR    R1,R5
         LA    R15,I
         BAL   R14,TOFIX
*******************
* LEER PARAMETRO 2*
*******************
         LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,F8
         BH    FINNOK
         LTR   R6,R6
         BZ    FINNOK
         LR    R0,R6
         LR    R1,R5
         LA    R15,N
         BAL   R14,TOFIX
*******************
* LEER PARAMETRO 3*
*******************
         LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINNOK
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,=F'30'
         BH    FINNOK
         LTR   R6,R6
         BZ    FINNOK
         BCTR  R6,R0
         MVC   T,BLANCOS
         EX    R6,MOVT
*******************
* LEER PARAMETRO 4*
*******************
         LA    R3,ARGTABLE_NEXT
         L     R5,ARGTABLE_ARGSTRING_PTR
         LTR   R5,R5
         BM    FINIT
         L     R6,ARGTABLE_ARGSTRING_LENGTH
         C     R6,=F'4'
         BH    FINNOK
         LTR   R6,R6
         BZ    FINNOK
         MVC   W(4),0(R5)
         B     CM
FINIT    MVC   W,=F'0'
************************
* LLAMAR A MONIT       *
************************
CM       LA    R1,W1
         CALL  IRXFMONI
************************
* EXIT                 *
************************
EXIT     MVC   EVALBLOCK_EVDATA(4),W
         LA    R09,4
         ST    R09,EVALBLOCK_EVLEN
         SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
MOVT     MVC   T(0),0(R5)
****************
* TO-FIX       *
* R0 - LENGTH  *
* R1 -> PIC    *
* R15 -> OUT   *
****************
TOFIX    BAKR  R14,0
         SR    R2,R2
         LR    R14,R0
         BCTR  R14,0
         CLI   0(R1),C'-'
         BNE   TOFIXM
         LA    R2,1
         LA    R1,1(R1)
         BCTR  R14,R0
         B     TOFIXP
TOFIXM   CLI   0(R1),C'+'
         BNE   TOFIXP
         LA    R1,1(R1)
         BCTR  R14,R0
TOFIXP   N     R14,=X'0000000F'
         O     R14,=X'00000070'
         EX    R14,PACKN
         LTR   R2,R2
         BZ    TOFIXC
         NI    WORKD+7,X'FD'
TOFIXC   CVB   R14,WORKD
         ST    R14,0(R15)
         PR
PACKN    PACK  WORKD(0),0(,R1)
BLANCOS  DC    CL30' '
WORKD    DS    D
F8       DC    F'8'
W1       DC    A(I)
W2       DC    A(N)
W3       DC    A(W)
W4       DC    A(T)
I        DS    F
N        DS    F
W        DS    F
T        DS    CL30
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@MON
