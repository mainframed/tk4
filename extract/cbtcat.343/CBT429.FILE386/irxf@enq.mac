**********************
* ESTABLECER DIRECC. *
**********************
IRXF@ENQ CSECT
IRXF@ENQ AMODE 31
IRXF@ENQ RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R02,R01
         USING IRXF@ENQ,R12
         USING EFPL,R02
         L     R03,EFPLARG
         L     R04,EFPLEVAL
         USING ARGTABLE_ENTRY,R03
         L     R04,0(R04)
         USING EVALBLOCK,R04
*******************
* LEER PARAMETRO 1*
*******************
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    FINNOK
         L     R06,ARGTABLE_ARGSTRING_LENGTH
         C     R06,F1
         BNE   FINNOK
         MVC   TIP(1),K0(R05)
*******************
* LEER PARAMETRO 2*
*******************
         LA    R03,ARGTABLE_NEXT
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    FINNOK
         L     R06,ARGTABLE_ARGSTRING_LENGTH
         LTR   R06,R06
         BM    FINNOK
         MVI   MINOR,C' '
         MVC   MINOR+1(254),MINOR
         BCTR  R06,R00
         EX    R06,MOVMINOR
************************
* VER TIPO             *
************************
         CLI   TIP,C'E'
         BE    HENQ
         CLI   TIP,C'D'
         BE    HDEQ
         B     FINNOK
************************
* HACER ENQ            *
************************
HENQ     ENQ   (MAJOR,MINOR,E,,SYSTEMS)
         LTR   R15,R15
         BNZ   FINNOK
         B     EXIT
************************
* HACER DEQ            *
************************
HDEQ     DEQ   (MAJOR,MINOR,,SYSTEMS)
         LTR   R15,R15
         BNZ   FINNOK
************************
* EXIT                 *
************************
EXIT     MVI   EVALBLOCK_EVDATA,C'1'
         LA    R09,1
         ST    R09,EVALBLOCK_EVLEN
         SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
MOVMINOR MVC   MINOR(0),0(R05)
MAJOR    DC    CL8'IRXF@ENQ'
F1       DC    F'1'
TIP      DS    C
MINOR    DS    CL255
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@ENQ
