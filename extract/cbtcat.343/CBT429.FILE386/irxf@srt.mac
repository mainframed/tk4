**********************
* ESTABLECER DIRECC. *
**********************
IRXF@SRT CSECT
IRXF@SRT AMODE 31
IRXF@SRT RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R02,R01
         USING IRXF@SRT,R12
         ST    R00,STKENV
         USING EFPL,R02
         L     R03,EFPLARG
         L     R04,EFPLEVAL
         USING ARGTABLE_ENTRY,R03
         L     R04,0(R04)
         USING EVALBLOCK,R04
*******************
* LEER PARAMETRO 1*
*******************
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    FINNOK
         L     R06,ARGTABLE_ARGSTRING_LENGTH
         LTR   R06,R06
         BZ    FINNOK
         MVI   SORTCTU,C' '
         MVC   SORTCTU+1(255),SORTCTU
         C     R06,F256
         BNH   HMOV
         LA    R06,256
HMOV     BCTR  R06,R00
         EX    R06,MOVCTU
*******************
* LEER PARAMETRO 2*
*******************
         LA    R03,ARGTABLE_NEXT
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    NMSG
         L     R06,ARGTABLE_ARGSTRING_LENGTH
         LTR   R06,R06
         BZ    NMSG
         CLI   0(R05),C'M'
         BE    SMSG
NMSG     MVC   SORTCT1,SORTCTN
         B     HSORT
SMSG     MVC   SORTCT1,SORTCTM
         B     HSORT
************************
* LLAMAR SORT          *
************************
HSORT    LOAD  EP=IRXSTK
         ST    R12,SORTPL4
         LA    R01,SORTPL1
         MVC   STKFUNC,KPULL
         ST    R00,IRXEP
         LINK  EP=SORT
         LTR   R15,R15
         BNZ   FINNOK
         LA    R08,1
         ST    R08,EVALBLOCK_EVLEN
         MVI   EVALBLOCK_EVDATA,C'1'
************************
* EXIT                 *
************************
EXIT     SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
F256     DC    F'256'
MOVCTU   MVC   SORTCTU(0),0(R05)
IRXEP    DS    F
KPULL    DC    CL8'PULL    '
STKFUNC  DS    CL8
STKDAT   DS    F
STKLEN   DS    F
STKRC    DS    F
STKENV   DS    F
STKRCE   DS    F
IRXSTKP  DS    0F
P1       DC    A(STKFUNC)
P2       DC    A(STKDAT)
P3       DC    A(STKLEN)
P4       DC    A(STKRC)
P5       DC    A(STKENV)
P6       DC    A(STKRCE+X'80000000')
MOVDTA   MVC   0(0,R15),0(R14)
SORTCTM  DC    C' OPTION HIPRMAX=0,DYNALLOC=OFF            '
SORTCTN  DC    C' OPTION HIPRMAX=0,DYNALLOC=OFF,MSGPRT=NONE'
*******************************************************
* LLAMADA AL SORT                                     *
*******************************************************
SORTPL1  DC    A(SORTCTL)
SORTPL2  DC    A(SORTE15+X'80000000')
SORTPL3  DC    A(SORTE35+X'80000000')
SORTPL4  DS    A
SORTPL5  DC    F'-1'
SORTCTL  DS    0H
         DC    AL2(SORTCTE-SORTCT1)
SORTCT1  DC    C'                                          '
SORTCT2  DC    C' RECORD TYPE=V,LENGTH=4096 '
SORTCTU  DS    CL256
SORTCTE  EQU   *
         DROP  R12
***************************
* DAR REGISTROS AL SORT   *
***************************
SORTE15  CSECT
SORTE15  AMODE 31
SORTE15  RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING SORTE15,R12
         USING SORPE15,R01
         L     R07,SORUEC
         DROP  R01
         USING IRXF@SRT,R07
         LA    R01,IRXSTKP
         L     R15,IRXEP
         BALR  R14,R15
         LTR   R15,R15
         BNZ   NOMAS
         L     R08,STKLEN
         C     R08,F4092
         BH    ERROR
         LA    R08,4(R08)
         STH   R08,BUFF
         XC    BUFF+2(2),BUFF+2
         LR    R01,R08
         LR    R15,R08
         LA    R00,BUFF+4
         L     R14,STKDAT
         MVCL  R00,R14
         LA    R01,BUFF
         LA    R15,12
         PR
NOMAS    LA    R15,8
         MVC   STKFUNC,KQUEUE
         PR
ERROR    LA    R15,16
         PR
         DROP  R07,R12
F4092    DC    F'4092'
KQUEUE   DC    CL8'QUEUE'
BUFF     DS    CL4100
SORPE15  DSECT
SORADD   DS    F
SORUEC   DS    F
********************************
* RECIBIR REGISTROS DEL SORT   *
********************************
SORTE35  CSECT
SORTE35  AMODE 31
SORTE35  RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING SORTE35,R12
         USING SORPE35,R01
         L     R07,SORUEC2
         L     R08,SORADIN
         DROP  R01
         LTR   R08,R08
         BZ    NMS
         USING IRXF@SRT,R07
         LH    R01,0(R08)
         S     R01,FIJO4
         ST    R01,STKLEN
         LA    R01,4(R08)
         ST    R01,STKDAT
         LA    R01,IRXSTKP
         L     R15,IRXEP
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ERRORS
         LA    R15,4
         PR
NMS      LA    R15,8
         PR
ERRORS   LA    R15,16
         PR
         DROP  R07,R12
FIJO4    DC    F'4'
SORPE35  DSECT
SORADIN  DS    F
SORADOU  DS    F
SORUEC2  DS    F
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         END   IRXF@SRT
