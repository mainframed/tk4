**********************
* ESTABLECER DIRECC. *
**********************
IRXF@EDM CSECT
IRXF@EDM AMODE 31
IRXF@EDM RMODE ANY
         @EQUS
         BAKR  R14,0
         LR    R12,R15
         LR    R02,R01
         USING IRXF@EDM,R12
         USING EFPL,R02
         L     R03,EFPLARG
         L     R04,EFPLEVAL
         USING ARGTABLE_ENTRY,R03
         L     R04,0(R04)
         USING EVALBLOCK,R04
*******************
* LEER PARAMETROS *
*******************
         L     R05,ARGTABLE_ARGSTRING_PTR
         LTR   R05,R05
         BM    FINNOK
         L     R06,ARGTABLE_ARGSTRING_LENGTH
************************
* VER SIGNO            *
************************
         MVC   SIGNO,K0(R05)
         CLI   SIGNO,C'-'
         BNE   BPD
         LA    R05,1(R05)
         BCTR  R06,R00
************************
* BUSCAR PUNTO DECIMAL *
************************
BPD      EQU   *
         LR    R08,R06
         LR    R10,R05
NP       CLI   K0(R05),C'.'
         BE    PEND
         LA    R05,1(R05)
         BCT   R08,NP
         SR    R07,R07
         B     CZON
PEND     LR    R07,R06
         SR    R06,R08
         SR    R07,R06
         BCTR  R07,R00
************************
* CREAR ZONED          *
************************
CZON     EQU   *
         MVI   ZONED,C'0'
         MVC   ZONED+1(20),ZONED
         LTR   R07,R07
         BZ    CZON2
         C     R07,F5
         BL    CZON1
         LA    R07,5
CZON1    BCTR  R07,R00
         LA    R15,ZONED+16
         LR    R14,R10
         AR    R14,R06
         LA    R14,1(R14)
         EX    R07,MOV1
CZON2    LTR   R06,R06
         BZ    PKNUM
         C     R06,F16
         BH    FINNOK
         BCTR  R06,R00
         LR    R14,R10
         LA    R15,ZONED+15
         SR    R15,R06
         EX    R06,MOV1
************************
* EMPAQUETAR NUMERO    *
************************
PKNUM    EQU   *
         CLI   SIGNO,C'-'
         BNE   PKNUM1
         NI    ZONED+20,X'DF'
PKNUM1   EQU   *
         PACK  NUM(8),ZONED(15)
         PACK  NUM+7(4),ZONED+14(7)
************************
* EDITAR MASCARA       *
************************
         LA    R10,EVALBLOCK_EVDATA
         LA    R01,21(R10)
         MVC   0(29,R10),MASK
         EDMK  0(29,R10),NUM
         BCTR  R01,R00
         MVC   0(1,R01),28(R10)
VDEC     EQU   *
         LA    R09,28
         ST    R09,EVALBLOCK_EVLEN
EXIT     SR    R15,R15
         PR
FINNOK   LA    R15,16
         PR
MOV1     MVC   K0(0,R15),0(R14)
*                1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7
MASK     DC    X'40204B2020204B2020204B2020204B2020204B2021206B20202020X
               2060'
F5       DC    F'5'
F16      DC    F'16'
*************
* MAPAS     *
*************
D        DSECT
         IRXEFPL
         IRXARGTB
         IRXEVALB
         ORG   EVALBLOCK_EVDATA+30
SIGNO    DS    C
NUM      DS    PL11
ZONED    DS    CL21
         ORG
         END   IRXF@EDM
