PDSALLOC CSECT
*
* THIS ROUTINE DYNAMICALLY ALLOCATES A DATASET VIA SVC 99
* WITH A DISPOSITION OF SHR.
* THE PARAMETERS MUST BE PASSED ACCORDING TO STANDARD
* LINKING CONVENTIONS,THAT IS:
*    R1 MUST POINT TO A LIST OF FOUR ADDRESSES POINTING TO
*       - THE DDNAME ( 8 CHARACTERS )
*       - THE DSNAME ( 44 CHARACTERS )
*                         PADDED WITH BLANKS IF NECCESSARY.
*       - THE FEEDBACK FIELD ( 1 FULLWORD)
*       - THE INFO/ERROR FIELD ( 1 FULLWORD)
*       - THE DSORG FIELD ( 1 HALFWORD)
*
* 25.11.90: ADDED DSORG RETURN TO DETECT CATALOGED DATASETS NOT
*           IN VTOC. PREVENTS ABEND S213 ON DS OPEN.
*
         STM   14,12,12(13)
         BALR  11,0
         USING *,11
         ST    13,SAVE13
         LA    13,SAVEAREA
*
* PARM LIST ADDRESS TO REG 6 - 9
*
         LM    6,10,0(1)
         MVC   DDNAME(8),0(6)
         MVC   DSNAME(44),0(7)
         LA    1,S99RBP
*
* MARK END OF REQUEST LIST
*
         OI    S99TUPLE,HIGHBIT
         OI    S99RBP,HIGHBIT
*
* DYNALLOC SVC
*
         SVC   99
*
* RETURN TO CALLER
*
         ST    15,0(8)             PUT RETURN CODE IN 3RD PARM
         L     15,S99ERROR
         ST    15,0(9)             ERROR / INFO FIELD
         LH    15,DSORG
         STH   15,0(10)            DSORG
         L     13,SAVE13
         LM    14,12,12(13)
         LA    15,0
         BR    14
*
* DATA AREAS
*
SAVE13   DS    F
SAVEAREA DS    20F
*
* DYNAMIC ALLOCATION PARMS
*
HIGHBIT  EQU   X'80'
S99RBP   DC    A(S99RB)            REQUEST BLOCK POINTER
S99RB    DS    0F                  REQUEST BLOCK
         DC    X'14'               LENGTH=20
         DC    X'01'               VERB CODE = ALLOCATE BY DSNAME
* S99NOMNT = DO NOT MOUNT VOLUMES
         DC    X'2000'             FLAGS1
S99ERROR DC    H'0'                ERROR CODE
         DC    H'0'                INFO CODE
         DC    A(S99TUPL)          POINTER TO TEXT UNIT POINTER LIST
         DC    F'0'
         DC    F'0'
* INDICATE WAIT FOR DATASET (REMOVED)
*        DC    X'04000000'         FLAGS2
S99TUPL  DC    A(TXTU1)
         DC    A(TXTU2)
         DC    A(TXTU3)
S99TUPLE DC    A(TXTU4)
TXTU1    DC    X'0001'             DDNAME SPECIFICATION
         DC    X'0001'             # OF PARMS
         DC    H'8'                PARM LENGTH
DDNAME   DC    CL8' '              DDNAME
TXTU2    DC    X'0002'             DSNAME SPECIFICATION
         DC    X'0001'             # OF PARMS
         DC    H'44'               PARM LENGTH
DSNAME   DC    CL44' '             DSNAME
TXTU3    DC    X'0057'             DSORG RETURN
         DC    X'0001'             # OF PARMS
         DC    H'2'                PARM LENGTH
DSORG    DC    H'0'                DSORG
TXTU4    DC    X'0004'             STATUS SPECIFICATION
         DC    X'0001'             # OF PARMS
         DC    X'0001'             PARM LENGTH
         DC    X'08'               DISP=SHR
         END
