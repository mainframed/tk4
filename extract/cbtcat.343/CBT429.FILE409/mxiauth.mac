         MACRO
.*--------------------------------------------------------------------
.* Name       : MXIAUTH
.*
.* Function   : Constructs the MXI security table
.*
.*
.* Syntax     : MXIAUTH START,NOENTRY=(ALLOW/DENY)
.*
.*            : MXIAUTH END
.*
.*            : MXIAUTH SET,
.*                    CMD=cmd_name,
.*                    USER=userid,
.*                    GROUP=group,
.*                    ACCESS=(ALLOW/DENY)
.*
.* Keywords   : START
.*                 Denotes the beginning of the security table.
.*                 This must be the first entry. The 'NOENTRY'
.*                 keyword specifies the access for any command
.*                 not specifically listed.
.*
.*            : NOENTRY=(ALLOW/DENY)
.*                 Specifies which access will be given for a command
.*                 that is not specifically specified in the security
.*                 table. The default value is DENY.
.*
.*            : END
.*                 Denotes the end of the security table.
.*
.*            : SET
.*                 Creates an security rule in the current table.
.*
.*            : CMD=cmd_name
.*                 Specifies the MXI command that is to have a security
.*                 rule applied. The command must be specified in full
.*                 (ie NO masking is allowed).
.*
.*            : USER=userid
.*                 Specifies the userid pattern mask to be used for
.*                 authority checking. The user is compared to the
.*                 ACCEUSRI field of the ACEE.
.*                 You can specify a placeholder character of '%' and
.*                 a wildcard character of '*' in the userid value.
.*
.*            : GROUP=group
.*                 Specifies the group pattern mask to be used for
.*                 authority checking. The group is compared to the
.*                 ACCEGRPN field of the ACEE.
.*                 You can specify a placeholder character of '%' and
.*                 a wildcard character of '*' in the group name.
.*
.*            : ACCESS=(ALLOW/DENY)
.*                 Specifies whether access to the comamnd is to be
.*                 allowed or denied. Default value is ALLOW.
.*
.*
.*
.*------------------------------------------------------------------*
.* Changes
.* 02/05/2000   Rob Scott     Code Written                     V2.2.A
.*--------------------------------------------------------------------
&LABEL    MXIAUTH &INVOKE,                                             X
               &NOENTRY=DENY,                                          X
               &CMD=,                                                  X
               &USER=,                                                 X
               &GROUP=,                                                X
               &ACCESS=ALLOW
.*--------------------------------------------------------------------
.* Ensure that we have all required parms
.*--------------------------------------------------------------------
           AIF ('&INVOKE' EQ 'START').RULESTRT
           AIF ('&INVOKE' EQ 'END').RULEEND
           AIF ('&INVOKE' EQ 'SET').RULESET
           AGO .MSG0001
.RULESTRT  ANOP
           AIF ('&USER' NE '').MSG0003
           AIF ('&CMD' NE '').MSG0003
           AIF ('&GROUP' NE '').MSG0003
MXISECTB   CSECT
           DC   CL8'MXISECTB'
           DC   CL8'&SYSDATE'
           DC   CL8'&SYSTIME'
           DC   AL4(NUMSECTB)
           AIF  ('&NOENTRY' EQ 'DENY').STARTDNY
           AIF  ('&NOENTRY' EQ 'ALLOW').STARTALW
           AGO .MSG0011
.STARTDNY  ANOP
           DC   XL4'00000001'
           AGO .ENDSTART
.STARTALW  ANOP
           DC   XL4'00000002'
           AGO .ENDSTART
.ENDSTART  ANOP
BEGSECTB   DS   0D
           AGO  .END
.RULESET   ANOP
.*--------------------------------------------------------------------
.* Process the defined rules
.*--------------------------------------------------------------------
           AIF ('&CMD' EQ '').MSG0005
           AIF (K'&CMD GT 4).MSG0002
&CMDL      SETA K'&CMD
&CMDLC     SETC '&CMDL'
           DC   AL1(&CMDLC)       Set length of CMD
           DC   CL4'&CMD'         Command name
.SETTYPE   ANOP
           AIF ('&ACCESS' EQ 'ALLOW').SETALLOW
           AIF ('&ACCESS' EQ 'DENY').SETDENY
           AGO .MSG0006
.SETALLOW  ANOP
           DC   X'02'             Set ACCESS = ALLOW
           AGO  .SETUTYP
.SETDENY   ANOP
           DC   X'01'             Set ACCESS = DENY
           AGO  .SETUTYP
.SETUTYP   ANOP
           AIF ('&USER' NE '').SETUSER
           AIF ('&GROUP' NE '').SETGROUP
           AGO  .MSG0007
.SETUSER   ANOP
           AIF ('&GROUP' NE '').MSG0008
           DC   X'04'             Set USER rule
           AIF (K'&USER GT 8).MSG0009
&USERL     SETA K'&USER
&USERLC    SETC '&USERL'
           DC   AL1(&USERLC)      Set length of USER
           DC   CL8'&USER'        Set userid value
           AGO  .SETEND
.SETGROUP  ANOP
           AIF ('&USER' NE '').MSG0008
           DC   X'08'             Set GROUP rule
           AIF (K'&GROUP GT 8).MSG0010
&GRPL      SETA K'&GROUP
&GRPLC     SETC '&GRPL'
           DC   AL1(&GRPLC)       Set length of GROUP
           DC   CL8'&GROUP'       Set group value
           AGO  .SETEND
.SETEND    ANOP
           AGO  .END
.*
.RULEEND   ANOP
           AIF ('&USER' NE '').MSG0004
           AIF ('&CMD' NE '').MSG0004
           AIF ('&GROUP' NE '').MSG0004
ENDSECTB   DS 0H
NUMSECTB   EQU (ENDSECTB-BEGSECTB)/16
           END
           AGO  .END
.MSG0001   MNOTE 12,'Invalid invocation - Use START/SET/END'
           AGO .END
.MSG0002   MNOTE 12,'Invalid length for CMD - Must be 1-4 characters'
           AGO .END
.MSG0003   MNOTE 12,'Do not specify any keywords for START'
           AGO .END
.MSG0004   MNOTE 12,'Do not specify any keywords for END'
           AGO .END
.MSG0005   MNOTE 12,'CMD value missing'
           AGO .END
.MSG0006   MNOTE 12,'Invalid value for ACCESS - Use ALLOW/DENY'
           AGO .END
.MSG0007   MNOTE 12,'USER or GROUP must be specified'
           AGO .END
.MSG0008   MNOTE 12,'USER and GROUP are mutually exclusive'
           AGO .END
.MSG0009   MNOTE 12,'Invalid length of USER - Must be 1-8 characters'
           AGO .END
.MSG0010   MNOTE 12,'Invalid length of GROUP - Must be 1-8 characters'
           AGO .END
.MSG0011   MNOTE 12,'NOENTRY keyword must be specified'
           AGO .END
.END     MEND
