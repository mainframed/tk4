//ASQCCCPM JOB (X0002,QCC,ASQCC),'X-COOK, BRIAN',
//   MSGCLASS=X,CLASS=X,NOTIFY=ASQCC,MSGLEVEL=(1,1)
/*JOBPARM L=99,SYSAFF=MVSA
//*
//ASM     EXEC PGM=IEV90,REGION=1024K,PARM='OBJECT,NODECK,TERM'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,1)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
ETPSMEMC TITLE 'ETPS - COPY MEMBER'
ETPSMEMC CSECT
         USING *,12
*
         SAVE  (14,12),,ETPSMEMC_11/04/86_BRIAN COOK_&SYSTIME_&SYSDATE
         LR    R12,R15            SET BASE REGISTER
*
         LM    R3,R10,0(R1)        R3  = INPUT DSN
*                                  R4  = INPUT MEM
*                                  R5  = INPUT VOLSER
*                                  R6  = INPUT PASSWORD
*                                  R7  = OUTPUT DSN
*                                  R8  = OUTPUT MEM
*                                  R9  = OUTPUT VOLSER
*                                  R10 = OUTPUT PASSWORD
         MVC   INDS(44),0(R3)
         MVC   INM(8),0(R4)
         MVC   INV(6),0(R5)
         MVC   INP(8),0(R6)
         MVC   OUDS(44),0(R7)
         MVC   OUM(8),0(R8)
         MVC   OUV(6),0(R9)
         MVC   OUP(8),0(R10)
*
PROLOG   DS    0H
         GETMAIN R,LV=4096        GET MY SAVE AREA ADDRESS
         LR    R11,R1             SAVE GETMAIN POINTER
         ST    R13,4(R11)         STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11
         USING MYSAVE,13
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
         EJECT
ALLOCATE DS    0H
*
         OI    INV,C' '
         CLI   INV,C' '
         BNE   *+10
         XC    INVADDR(4),INVADDR
         OI    INP,C' '
         CLI   INP,C' '
         BNE   *+10
         XC    INPADDR(4),INPADDR
*
         LA    R1,RB99IN
         DYNALLOC
         L     R0,RB99IN+8
         LTR   R5,R15
         BZ    *+6
         DC    H'0'
*
         OI    OUV,C' '
         CLI   OUV,C' '
         BNE   *+10
         XC    OUVADDR(4),OUVADDR
         OI    OUP,C' '
         CLI   OUP,C' '
         BNE   *+10
         XC    OUPADDR(4),OUPADDR
*
         LA    R1,RB99OUT
         DYNALLOC
         L     R0,RB99OUT+8
         LTR   R5,R15
         BZ    *+6
         DC    H'0'
*
OPENS    DS    0H
*
         MVC   SYSUT1(96),UT1   *  MOVE IN DCB'S
         MVC   SYSUT1+40(8),INDD   INPUT DDNAME
         MVC   SYSUT2(96),UT2   *
         MVC   SYSUT2+40(8),OUDD  OUTPUT DDNAME
         MVC   OPENEM(8),MFL    *  MOVE IN OPEN MAC
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT)),MF=(E,OPENEM)
*
         TM    SYSUT1+48,X'10'  *  TEST FOR GOOD OPEN
         BZ    ALLDONE          *  IF NO GOOD, LEAVE
         TM    SYSUT2+48,X'10'  *  TEST FOR GOOD OPEN
         BZ    ALLDONE          *  IF NO GOOD, LEAVE
*
         SR    R7,7             *  CLEAR RECORD COUNTER
*
GETLOOP  DS    0H
         GET   SYSUT1
         LR    R8,R1
         PUT   SYSUT2,(8)
         B     GETLOOP
ALLDONE  DS    0H
         CLOSE (SYSUT1,,SYSUT2),MF=(E,OPENEM)
         SR    R5,R5
BADRC    DS    0H
         L     R13,4(,R13)  PICK UP CALLING SAVE AREA
         FREEMAIN R,LV=4096,A=(11)
         LR    R15,R5
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
         EJECT
UT1      DCB   DDNAME=SYSUT1,MACRF=(GL),DSORG=PS,EODAD=ALLDONE
UT2      DCB   DDNAME=SYSUT2,MACRF=(PM),DSORG=PS
MFL      OPEN  (UT1,(INPUT),UT2,(OUTPUT)),MF=L
RB99IN   DC    A(*+4+X'80000000')
         DC    X'14010000'
         DC    F'0'
         DC    A(INTEXT1)
         DC    F'0'
         DC    F'0'
INTEXT1  DC    A(INDDNAME)
         DC    A(INDSNAME)
         DC    A(INMEM)
         DC    A(DISPSHR)
         DC    A(FREECLOS)
INVADDR  DC    A(INVOLSER)
INPADDR  DC    A(INPASS)
         DC    A(DISPKEEP+X'80000000')
RB99OUT  DC    A(*+4+X'80000000')
         DC    X'14010000'
         DC    F'0'
         DC    A(OUTEXT1)
         DC    F'0'
         DC    F'0'
OUTEXT1  DC    A(OUDDNAME)
         DC    A(OUDSNAME)
         DC    A(FREECLOS)
         DC    A(OUMEM)
         DC    A(DISPSHR)
OUVADDR  DC    A(OUVOLSER)
OUPADDR  DC    A(OUPASS)
         DC    A(DISPKEEP+X'80000000')
INDDNAME DC    X'0055',H'1',H'8'
INDD     DC    CL8' '
INDSNAME DC    X'0002',H'1',H'44'
INDS     DC    CL44' '
INMEM    DC    X'0003',H'1',H'8'
INM      DC    CL8' '
INVOLSER DC    X'0010',H'1',H'6'
INV      DC    CL6' '
INPASS   DC    X'0050',H'1',H'8'
INP      DC    CL8' '
DISPSHR  DC    X'0004',H'1',H'1',X'0800'
DISPKEEP DC    X'0005',H'1',H'1',X'0800'
FREECLOS DC    X'001C',H'0'
OUDDNAME DC    X'0055',H'1',H'8'
OUDD     DC    CL8' '
OUDSNAME DC    X'0002',H'1',H'44'
OUDS     DC    CL44' '
OUMEM    DC    X'0003',H'1',H'8'
OUM      DC    CL8' '
OUVOLSER DC    X'0010',H'1',H'6'
OUV      DC    CL6' '
OUPASS   DC    X'0050',H'1',H'8'
OUP      DC    CL8' '
         DS    0F
         EJECT
MYSAVE   DSECT
         DS    18F
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=(GL),DSORG=PS
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=(PM),DSORG=PS
OPENEM   OPEN  (UT1,(INPUT),UT2,(OUTPUT)),MF=L
DWORK    DS    D
         END
/*
//LKED1    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,REUS,TERM),
//             COND=(2,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LINKLIB,UNIT=SYSDA,VOL=SER=MVSTGT
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  SETCODE AC(1)
     NAME ETPSMEMC(R)
/*
//LKED2    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,REUS,TERM),
//             COND=(2,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LINKLIB,UNIT=SYSDA,VOL=SER=MVSYSA
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  SETCODE AC(1)
     NAME ETPSMEMC(R)
/*
