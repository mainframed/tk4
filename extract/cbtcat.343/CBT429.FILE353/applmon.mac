//ASQCCMON  JOB (X0002,QCC,ASQCC),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=99,SYSAFF=MVSA
//ASM     EXEC PGM=IEV90,REGION=1024K,
//             PARM='XREF(SHORT),OBJECT,NODECK,TERM,RENT'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DISP=SHR,DSN=SYS2.MACLIB       <== TCBUVTAM
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD  DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(1,1)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
APPLMON  TITLE 'CHANGE LOG'
***********************************************************************
*                                                                     *
* CHANGE LOG                                                          *
*                                                                     *
*  1/12/87 BRIAN COOK   FIXED BUG IN "QEDIT" ROUTINE, S0C1            *
*                                                                     *
* 10/17/85 BRIAN COOK   ADDED "QEDIT" FOR SHUTDOWN.                   *
*                                                                     *
*  7/24/85 BRIAN COOK   CHANGED SOME OF THE SHUTDOWN LOGIC. STILL     *
*                       SHUTS DOWN REGION WHEN A USER LOG'S OFF.      *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'A P P L M O N - VTAM APPLICATION MONITOR'
***********************************************************************
*                                                                     *
*     MODULE NAME: APPLMON1                                           *
*                                                                     *
*     DESCRIPTIVE NAME: VTAM APPLICATION                              *
*                                                                     *
*     FUNCTION: ALLOWS COMMUNICATION FROM TERMINALS IN                *
*               THE VTAM NETWORK                                      *
*                                                                     *
*     AUTHOR: BRIAN COOK, MORTON, 1984                                *
*                                                                     *
*                                                                     *
*      APPLMON PROC IN PROCLIB IS AS FOLLOWS:                         *
*                                                                     *
* //APPLMON PROC                                                    ***
* //APPLMON EXEC PGM=APPLMON1                                       ***
* //STEPLIB DD DSN=SYS2.LINKLIB,DISP=SHR <-- AUTH LIB               ***
* //*                                                               ***
* //*******************************************************************
*                                                                     *
*                                                                     *
*      APPLMON APPL DEFINITIONS ARE AS FOLLOWS:                       *
*                                                                     *
*  APPLMON APPL  BUFFACT=1,AUTH=(ACQ,PASS,NVPACE)                     *
*                                                                     *
*                                                                     *
***********************************************************************
*
*
***********************************************************************
*                                                                     *
* INITIAL HOUSEKEEPING - ENTRY CODE FOR OS/VS.                        *
* SAVE REGISTERS, ESTABLISH ADDRESSABILITY, SET UP SAVE AREA.         *
*                                                                     *
***********************************************************************
*
APPLMON  CSECT
         USING *,11           TEMP BASE
         STM   14,12,12(13)                      SAVE REGISTERS
         LR    R11,15         LOAD BASE REG
         L     R3,0(R1)       PICK UP PARM REGISTER
         B     PSTART         JUMP ID
MODULEID DC    AL1(7),C'APPLMON'   ID
PSTART   DS    0H
         L     R9,GTSIZE1     SIZE OF SAVE AREA REQUIRED
*
         GETMAIN R,LV=(9)
*
         LR    R8,R1           SAVE ADDRESS
*
         ST    R8,8(,13)       SAVE
         ST    13,4(,R8)        AREA
         LR    R13,R8            FOR CALLEES
         USING APPLVTAM,13,12     SAVE AREA DSECT ADDRESSING
         LR    R12,R13            SET DATA
         A     R12,FOURK                   BASE 2
*
* SET INITIAL VALUES IN MY TCBUSER AREA
*
         MVI   APGMCMD,C' '
*
         LA    R1,ACBTERMS        SET POINTER TO ACB
         ST    R1,APPLACBA        SET POINTER TO ACB
*
         LA    R4,LOGONNIB        SET POINTER TO NIB
         ST    R4,APPLNIBA        SET POINTER TO NIB
         LA    R2,PRIMARPL        SET POINTER TO RPL
         ST    R2,APPLRPLA        SET POINTER TO RPL
*
         MVC   APPLKILL(4),SPACES
*
         LA    R15,VGET
         ST    R15,APPLVGET
         LA    R15,VPUT
         ST    R15,APPLVPUT
         LA    R15,VSIZE
         ST    R15,APPLVSIZ
*
         LA    R15,APGMECB1
         ST    R15,APPLECB1
         LA    R15,APGMECB2
         ST    R15,APPLECB2
*
         LA    R15,APGMCMD
         ST    R15,APPLCMD
         LA    R15,APGMRPLA
         ST    R15,APPLWORD
*
         MVC   QEDXTRC(QEDXTRCL),QEDXTRC1
*
* FILL IN TCBUSER ADDRESS FOR MY TCB
*
         MODESET KEY=ZERO          KEY ZERO TO STORE INTO TCB
         USING PSA,0
         L     R1,PSATOLD
         USING TCB,1
         ST    R13,TCBUSER
         DROP  1
*
* BECOME NON-SWAPPABLE
*
         SYSEVENT DONTSWAP
*
* FILL IN TCBUSER ADDRESS FOR THE REGION CONTROL TASK TCB
*
         L     R1,PSAAOLD
         USING ASCB,1
         L     R1,ASCBRCTP        REGION CONTROL TASK TCB
         DROP  1
         USING TCB,1
         ST    R13,TCBUSER
         DROP  1
         MODESET KEY=NZERO         RESET KEY
*
         USING IFGRPL,R2          RPL DSECT ADDRESSING
         LA    R8,ACBTERMS        SET BASE FOR ACB
         USING IFGACB,R8          ACB DSECT ADDRESSING
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
         PRINT NOGEN
*
***********************************************************************
*        SET UP ESTAE ENVIRONMENT                                     *
***********************************************************************
*
SETSTAE  DS    0H
         B     AFTESTAE                       BYPASS
         ESTAE ABEXIT,ASYNCH=YES,TERM=YES
AFTESTAE DS    0H
*
         TITLE 'A P P L M O N - OPEN ACB'
***********************************************************************
*                                                                     *
* OPEN THE ACB FOR TERMINALS.                                         *
*                                                                     *
***********************************************************************
*
OPENTCNT DS    0H
OPENT    OPEN  ((R8))             OPEN THE ACB FOR TERMINALS
         LTR   R2,R15             CHECK AND SAVE RETURN CODE
         BZ    OPENPRNT           GO OPEN THE PRINT FILE
*
* OPEN DID NOT SUCCEED: CHECK ERROR TYPE
*
         CLI   ACBERFLG,ACBONVRT  STORAGE SHORTAGE?
         BE    OPENT              YES - RETRY
         CLI   ACBERFLG,90        APPL NOT DEFINED?
         BE    OPNOTDEF           YES - ISSUE MSG
         SR    R3,3
         IC    R3,ACBERFLG
OPAB1024 DS    0H
         ABEND 1024,DUMP,STEP
OPNOTDEF DS    0H
         WTO   'ERROR  APPLMON APPLID NOT DEFINED TO VTAM           ', *
               ROUTCDE=11
*
         B     MAINRET
*
OPENPRNT DS    0H
         OPEN  (NETPRINT,(OUTPUT))
         B     STARTLGN           GO LET'R RIP
*
GTSIZE1  DC    A(APPLLENG)       LENGTH OF MY GETMAIN
*
FOURK    DC    F'4096'            4K FOR BUMPING BASE REGS
*
         TITLE 'A P P L M O N - START LOGONS'
*
STARTLGN DS    0H
         LA    R2,PRIMARPL
         SETLOGON OPTCD=START,    START LOGONS                         *
               ACB=ACBTERMS,                                           *
               RPL=PRIMARPL
         LTR   R4,R15            OK?
         BZ    DOQEDIT            YES
         CH    R0,RETRY           IF RETRIABLE ERROR
         BE    STARTLGN            THEN RETRY
         STM   R0,R1,ABREGS SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 10,DUMP,STEP
*
         TITLE 'ESTABLISH STOP/MODIFY ENVIRONMENT'
DOQEDIT  DS    0H
***********************************************************************
*
*        MODIFYING:
*
*          F APPLMON,STOP          END   AFTER ALL USERS ARE STOPPED
*          F APPLMON,CANCEL,UID    CANCEL USER SESSION AND CLSDST
*
*        STOPPING:
*
*          P APPLMON               END IMMEDIATELY
*
***********************************************************************
*
**       LOCATE STOP/MODIFY ECB
*
         EXTRACT QEDCOMMP,MF=(E,QEDXTRC)
         L     R1,QEDCOMMP         GET PTR TO COMM/CIB ORG WORDS
         MVC   QEDCOMM,0(R1)       COPY ADDR OF ECB
         OI    QEDCOMM,X'80'       INDICATE COMM ECB IS LAST IN LIST
         LA    R2,4(R1)            POINT TO CIB ORIGIN
         QEDIT ORIGIN=(R2),CIBCTR=2 ALLOW 2 MODIFIES
*
         TITLE 'WAIT FOR WORK FROM LOGGED-ON USERS, OR OPERATOR'
LOGONWT  DS    0H
*
         MVI   APGMCMD,C' '
         STIMER WAIT,BINTVL=TENSEC
         B     QEDMCK
*
         TITLE 'CHECK FOR STOP/MODIFY'
QEDMCK   DS    0H
         L     R1,QEDCOMM          GET COMM PTRS
         TM    0(R1),X'40'         POSTED?
         BNO   LOGONWT             NO - WAIT AGAIN
*        BNO   4(R11)              NO - RETURN +4
QEDMLOOK DS    0H                  LOOK FOR CIB S
         L     R2,QEDCOMMP         GET COMM PTRS
         L     R2,4(R2)            CIB ORG
         LTR   R2,R2               END OF CHAIN
         BZ    QEDMFINI            END
         CLI   4(R2),X'40'         STOP
         BE    QEDMSTOP
         CLI   4(R2),X'44'         MODIFY
         BE    QEDMMOD
         CLI   4(R2),X'04'         START
         BE    QEDMMOD             HANDLE LIKE MOD
QEDMDEL  DS    0H                  DELETE A CIB
         L     R3,QEDCOMMP         ORG.
         LA    R3,4(R3)            POINT TO POINTER (TRUST ME)
         QEDIT ORIGIN=(R3),BLOCK=(2)  DELETE BLOCK
         B     QEDMLOOK            LOOK FOR MORE
         TITLE 'PICK UP MODIFY TEXT'
*
****PROCESS MODIFY
*
QEDMMOD  DS    0H
         LA    R0,QEDTXT           HOLD AREA
         LA    R1,L'QEDTXT         L'
         LH    R15,14(R2)          L' OF MODIFY
         LA    R15,2(R15)          +2BYTE LEN
         ICM   R15,8,=C' '         PAD W/ BLANKS
         LA    R14,14(R2)          GET FROM ADDR
         MVCL  R0,R14              MOVE MODIFY TEXT
         MVC   QEDCONID,12(R2)     SAVE MCS ID. FOR RESPONSE
*
**             NULL INPUT
*
         CLI   QEDTXT+2,C' '       NULL INPUT
         BE    QEDMDEL             JUST DELETE IT
*
         TITLE 'PROCESS MODIFY  STOP  COMMAND'
QEDMSTOP DS    0H
*
         CLC   =C'STOP',QEDTXT+2   STOP AFTER CURR. D.S.
         BNE   QEDMCANC            NO - TRY CANCEL USER
         MVI   QEDSTOPA,C'Y'       SET IT
*
**             STOP (AFTER STOPPING ALL USERS)
*
         B     SHUTDOWN
*
         TITLE 'PROCESS MODIFY  CANCEL USER   COMMAND'
QEDMCANC DS    0H
*
         CLC   =C'CANCEL',QEDTXT+2  CANCEL USER
         BNE   QEDOTHER            NO-TRY START
*
**             CANCEL USERID
*
         B     QEDMFINI
*
         TITLE 'PROCESS MODIFY  OTHER   COMMAND'
QEDOTHER DS    0H
*
**             PUT OTHER TESTS HERE
*
         B     QEDMDEL             JUST DELETE IT
QEDMFINI DS    0H
*
         CLI   APGMCMD,C' '       COMMAND REQUESTED?
         BE    LOGONWT
*
*
CHECKCMD DS    0H
         CLC   APGMCMD(4),=C'CLOS'   CLSDST REQUESTED
         BNE   CHECKDN               NO
*
         L     R2,APGMRPLA           PICK UP RPL
*
RECLOSE  DS    0H
         CLSDST RPL=(R2),AREA=0,OPTCD=(RELEASE,SYN)
         LTR   R15,R15            OK?
         BZ    CLOSEED            YES
         CH    R0,RETRY           IF RETRIABLE ERROR
         BE    RECLOSE             THEN RETRY
*
CLOSEED  DS    0H
         LA    R2,APGMECB2
         POST  (2)
         B     LOGONWT            BRANCH IF SUCCESSFUL
*
CHECKDN  DS    0H
         CLC   APGMCMD(4),=C'SHUT'   SHUTDOWN REQUESTED
         BE    SHUTDOWN              YES
*
*
         ABEND 99,DUMP,STEP
*
SHUTDOWN DS    0H
*
*   FIRST POST THE SUBTASK THAT REQUESTED TERMINATE
*
         LA    R2,APGMECB2
         POST  (2)
*
*   NEXT WAIT FOR SUBTASKS TO TERMINATE
*
         STIMER WAIT,BINTVL=TENSEC
*
*   NEXT CLOSE THE VTAM ACB
*
         LA    R8,ACBTERMS        SET ACB ADDRESS
         SR    R15,R15            CLEAR RETURN CODE REGISTER
         CLOSE ((R8))             CLOSE ACB FOR TERMINALS
*
         B     MAINRET            GO CLOSE DOWN
*
         TITLE 'A P P L M O N - CLOSE DOWN'
*
* INDICATE PO IS CLOSING DOWN
*
CLOSNTP  DS    0H
         LA    R8,ACBTERMS        SET ACB ADDRESS
         SR    R15,R15            CLEAR RETURN CODE REGISTER
         CLOSE ((R8))             CLOSE ACB FOR TERMINALS
         LTR   R6,R15             CHECK RETURN CODE
         BZ    MAINRET            BRANCH IF SUCCESSFUL
         CLI   ACBERFLG,ACBTVTCL  CLOSE DURING ABTERM?
         BE    MAINRET            YES - IGNORE
* CLOSE FAILED - INFORM OPERATOR AND TERMINATE PO.
*        WTO   ,MF=(E,NTP07MSG)
*TP07MSG WTO   'ERROR  UNABLE TO CLOSE ACB FOR APPLMON  ',
*              ROUTCDE=2,MF=L
         WTO   'ERROR  UNABLE TO CLOSE ACB FOR APPLMON  ',             *
               ROUTCDE=11
*
*
***********************************************************************
* EXIT CODE                                                           *
***********************************************************************
*
MAINRET  DS    0H
         TM    NETPRINT+48,X'10'  PRINT FILE OPEN
         BZ    MAINRET3           NO - IGNORE
         CLOSE NETPRINT
MAINRET3 DS    0H
*
         LR    R2,R13
*
         L     R9,GTSIZE1      LENGTH OF GETMAINED AREA
*
         L     R13,4(,R13)     RESTORE SAVE AREA POINTER
         FREEMAIN R,LV=(9),A=(2)
         LM    R14,R12,12(R13) RESTORE REGS
         SR    R15,15
         BR    R14             RETURN TO CALLER
SPACES   DC    CL8' '
TENSEC   DC    F'1000'         10-SECOND WAIT INTERVAL
         LTORG
*
         TITLE 'A P P L M O N - ESTAE EXIT'
*        ESTAE EXIT
*
         DS    0F                 ALIGN
         USING *,10
ABEXIT   DS    0H
         STM   14,12,12(13)                      SAVE REGISTERS
         LR    R10,R15            LOAD EXIT BASE ADDRESS
         LR    R4,R1          SAVE SDWA PTR
         USING SDWA,R4        TELL ASMBLR
         B     ABEXIT2
         DC    CL8'ABEXIT'        EXIT ID
ABEXIT2  DS    0H
*
* PICK UP TCBUSER ADDRESS FOR THE REGION CONTROL TASK TCB
*
         L     R1,PSAAOLD
         USING ASCB,1
         L     R1,ASCBRCTP        REGION CONTROL TASK TCB
         DROP  1
         USING TCB,1
         L     R1,TCBUSER
         DROP  1
         USING APPLVTAM,1
         LR    R2,R13             PRESERVE HIGHER R13
         LA    R13,APPLESAV
         USING APPLESAV,13
*
         ST    R13,8(,R2)         CHAIN FORWARD
         ST    R2,4(,R13)         CHAIN BACK
*
         MVC   APGMCMD(4),=C'SHUT'   SHUTDOWN REQUESTED
         XC    APGMECB2(4),APGMECB2
         LA    R2,APGMECB1
         POST  (2)
         LA    R2,APGMECB2
         WAIT  ECB=(2)
ABRET    MVI   SDWARCDE,0         SET RETURN CODE
*
         L     R13,4(,R13)     RESTORE SAVE AREA POINTER
         LM    R14,R12,12(R13) RESTORE REGS
         SR    R15,15
         BR    R14             RETURN TO CALLER
*
         LTORG
         TITLE 'A P P L M O N - LOGON EXIT'
***********************************************************************
*                                                                     *
* NAME: LOGON EXIT                                                    *
*                                                                     *
* FUNCTION: TO ESTABLISH CONNECTIONS WITH A TERMINAL.                 *
*                                                                     *
* DESCRIPTION: SETS UP THE TCB USER AREA AND ATTACHES APPLOGO  TO     *
*              COMPLETE THE LOGON.                                    *
*                                                                     *
* ENTRY POINT: LOGON                                                  *
*                                                                     *
* REGISTERS AT ENTRY:                                                 *
*                                                                     *
*        R0    UNPREDICTABLE                                          *
*        R1    @ PARAMETER LIST CONTAINING                            *
*              (1) @ ACBTERMS                                         *
*              (2) @ TERMINAL NAME                                    *
*              (3) RESERVED                                           *
*              (4) LENGTH OF LOGON MESSAGE                            *
*        R2-13 UNPREDICTABLE                                          *
*        R14   RETURN ADDRESS IN VTAM                                 *
*        R15   ADDRESS OF LOGON EXIT                                  *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*              POINTER TO RPL     R2                                  *
*              POINTER TO NIB     R4                                  *
*              WORK REGS          R6,R7                               *
*              SUBROUTINE RETURN  R14                                 *
*              SAVE AREA ADDRESS  R13                                 *
*              BASES              R10,R11,R12                         *
*              VTAM CALLS         R15,R15,R14                         *
*                                                                     *
* EXIT,NORMAL: BR 14                                                  *
*                                                                     *
* EXIT, ERROR: ABEND                                                  *
*                                                                     *
* ATTRIBUTES: SERIALLY REUSABLE                                       *
*              VTAM EXLST EXIT                                        *
*                                                                     *
*
***********************************************************************
*
         USING *,10
LOGON    DS    0H
         LR    R10,R15            LOAD EXIT BASE ADDRESS
         LR    R9,R1              SAVE LOGON PARM POINTER
         B     LOGON2
         DC    CL8'LOGON '        EXIT ID
LOGON2   DS    0H
*
* PICK UP TCBUSER ADDRESS FOR THE REGION CONTROL TASK TCB
*
         L     R1,PSAAOLD
         USING ASCB,1
         L     R1,ASCBRCTP        REGION CONTROL TASK TCB
         DROP  1
         USING TCB,1
         L     R1,TCBUSER
         DROP  1
         USING APPLVTAM,1
         LA    R13,APPLLSAV
         USING APPLLSAV,13
*
         ST    R14,0(,R13)        SAVE RETURN ADDRESS
*
***********************************************************************
*        SET UP APPLICATION GETMAINED AREA                            *
***********************************************************************
*
*
         L     R4,GTSIZE2         GET APPLICATION TCBUSER AREA
         GETMAIN R,LV=(4)
         LR    R3,R1              SAVE GETMAIN POINTER
         USING TCBUVTAM,3
*
* FILL IN TCBUSER AREA
*
*
         LA    R1,ACBTERMS        SET POINTER TO ACB
         ST    R1,TCBUACBA        SET POINTER TO ACB
*
         MVC   TCBUNIB(128),LOGONNIB
         LA    R4,TCBUNIB         SET POINTER TO NIB
         ST    R4,TCBUNIBA        SET POINTER TO NIB
         USING ISTDNIB,R4         NIB DSECT ADDRESSING
*
         LR    R1,R9              SAVE LOGON PARM POINTER
         MVC   TCBURPL(128),MOVERPL
         LA    R2,TCBURPL         SET POINTER TO RPL
         ST    R2,TCBURPLA        SET POINTER TO RPL
         USING LOGONPRM,R1        LOGON PARM DSECT ADDRESSING
         L     R15,LOGONLU        PICK UP TERMINAL NAME
         DROP  1
*
         MVC   NIBSYM(8),0(R15) SET TO LU NAME
         ST    R3,NIBUSER
         ST    R3,RPLUSFLD
         MVC   TCBULU(8),0(R15) SET TO LU NAME
         MVC   TCBUSRID(8),=CL8'ANY'
         MVC   TCBUAPPL(8),=CL8'APPLMON'
*
         MVC   RPLARG(4),TCBUNIBA
         OI    RPLEXTD1,RPLNIB
*
*        GET SESSION PARAMETERS
*
         INQUIRE RPL=(2),                                              *
               ACB=ACBTERMS,                                           *
               NIB=(4),                                                *
               AREALEN=80,AREA=SESSBIND,                               *
               OPTCD=(SYN,SESSPARM)
*
*
*        SET TERMINAL ROWS AND COLUMNS FOR GTSIZE
*
         LA    15,SESSBIND
         USING ISTDBIND,15
         MVC   TCBUROWS(1),BINSALTR
         MVC   TCBUCOLS(1),BINSALTC
         DROP  15
*
         MVC   TCBUKILL(4),BLANKS
*
         LA    R15,VGET
         ST    R15,TCBUVGET
         LA    R15,VPUT
         ST    R15,TCBUVPUT
         LA    R15,VSIZE
         ST    R15,TCBUVSIZ
*
         LA    R15,APGMECB1
         ST    R15,TCBUECB1
         LA    R15,APGMECB2
         ST    R15,TCBUECB2
*
         LA    R15,APGMCMD
         ST    R15,TCBUCMD
         LA    R15,APGMRPLA
         ST    R15,TCBUWORD
*
* ISSUE OPNDST TO ACCEPT THE TERMINAL.
*
         OPNDST RPL=(2),          ACCEPT THE TERMINAL                  *
               ACB=ACBTERMS,                                           *
               NIB=(4),                                                *
               OPTCD=Q
         LTR   R15,R15            IF RTNCD = UNSUCCESSFUL
         BNZ   BADOPN              THEN FORGET IT
*
GOODOPN  DS    0H
*
         ST    R3,0(,R3)         SAVE PARM POINTER IN FIRST WORD
         OI    0(R3),X'80'
         LA    R1,0(,R3)
         ATTACH EP=APPLOGO
*
         B     LGNRET
*
BADOPN   DS    0H
*
         L     R4,GTSIZE2        LENGTH OF APPLICATION TCBUSER AREA
         FREEMAIN R,LV=(4),A=(3)
*
LGNRET   DS    0H
*
         L     R14,0(,R13)        SAVE RETURN ADDRESS
         SR    R15,15
         BR    R14             RETURN TO CALLER
*
         LTORG
GTSIZE2  DC    A(TCBULENG)       LENGTH OF APPLICATION GETMAIN
BLANKS   DC    CL4' '
         TITLE 'A P P L M O N - LOSTERM EXIT'
***********************************************************************
*                                                                     *
* NAME: LOSTERM EXIT                                                  *
*                                                                     *
* FUNCTION: TO TAKE APPROPRIATE ACTION WHEN CONTACT HAS BEEN LOST     *
*        WITH A TERMINAL.                                             *
*                                                                     *
* DESCRIPTION: THIS ROUTINE SHOULD EXAMINE THE RETURN CODE IN THE     *
*        PARAMETER LIST BUT FOR THE MOMENT IT CHECKS FOR MASTER LOST, *
*        REASSIGNS MASTER IF SO, AND ISSUES CLSDST UNLESS CLOSEDOWN   *
*        IS IN PROGRESS.                                              *
*                                                                     *
* ENTRY POINT: LOSTERM                                                *
*                                                                     *
* REGISTERS AT ENTRY:                                                 *
*                                                                     *
*        R0    UNPREDICTABLE                                          *
*        R1    @ PARAMETER LIST CONTAINING                            *
*              (1) @ ACBTERMS                                         *
*              (2) CID                                                *
*              (3) NIB USERFLD                                        *
*              (4) CODE INDICATING WHY ENTERED                        *
*                       CODES ARE:                                    *
*                         0 - DIAL-LINE DISCONNECTED ON DIAL-IN TERM  *
*                         4 - DIAL-LINE DISCONNECTED ON DIAL-OUT TERM *
*                         8 - RESERVED                                *
*                        12 - MANY REASONS SEE MANUAL - SHUTDOWN      *
*                        16 - SUCCESSFULLY RECONTACTED                *
*                        20 - UNCONDITIONAL TERMINATE SELF            *
*                        24 - POSSIBLE TO RECOVER                     *
*                        28 - INCOMPLETE OR INVALID SEGEMENTED REQUEST*
*                        32 - CONDITIONAL TERMINATE SELF              *
*                        36 - BUFFER LIMIT EXECEEDED                  *
*                        40 - TEST REQ KEY OR TOLTEP IS TAKING OVER   *
*                                                                     *
*        R2-13 UNPREDICTABLE                                          *
*        R14   RETURN ADDRESS IN VTAM                                 *
*        R15   ADDRESS OF LOSTERM EXIT                                *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*              POINTER TO RPL     R2                                  *
*              WORK REGS          R6,R7                               *
*              SAVE AREA ADDRESS  R13                                 *
*              BASES              R10,DR10,R12                        *
*              VTAM CALLS         R15,R15,R14                         *
*                                                                     *
* EXIT,NORMAL: BR 14                                                  *
*                                                                     *
* EXIT, ERROR: ABEND                                                  *
*                                                                     *
* CALLS: NONE                                                         *
*                                                                     *
* ATTRIBUTES: VTAM EXLST EXIT                                         *
*                                                                     *
***********************************************************************
*
         USING *,10
LOSTERM  DS    0H
         LR    R10,R15            LOAD EXIT BASE ADDRESS
         LR    R11,R1         SAVE PARM PTR
         B     LOSTERM2
         DC    CL8'LOSTERM'       EXIT ID
LOSTERM2 DS    0H
*
* PICK UP TCBUSER ADDRESS FOR THE REGION CONTROL TASK TCB
*
         L     R1,PSAAOLD
         USING ASCB,1
         L     R1,ASCBRCTP        REGION CONTROL TASK TCB
         DROP  1
         USING TCB,1
         L     R1,TCBUSER
         DROP  1
         USING APPLVTAM,1
         LR    R2,R13             PRESERVE HIGHER R13
         LA    R13,APPLTSAV
         USING APPLTSAV,13
         ST    R14,0(,R13)        SAVE RETURN ADDRESS
*
         TM    LOSINUSE,INUSE     SAVE AREA IN USE?
         BZ    NOLOSABE           NO
         STM   R0,R1,ABREGS   SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 13,DUMP,STEP
NOLOSABE DS    0H
*
         OI    LOSINUSE,INUSE     FLAG SAVE AREA IN USE
         USING LOSTPARM,R11       LOSTERM PARM DSECT ADDRESSING
         L     R11,LOSTUSER
         USING TCBUVTAM,11
*
         MVC   APGMCMD(4),=C'CLOS'   CLOSE THIS TERMINAL
         XC    APGMECB2(4),APGMECB2
         MVC   APGMRPLA(4),TCBURPLA
         LA    R2,APGMECB1
         POST  (2)
         LA    R2,APGMECB2
         WAIT  ECB=(2)
*                                 RETURN
LOSTRET  NI    LOSINUSE,255-INUSE FLAG SAVE AREA FREE
         L     R14,0(,R13)        SAVE RETURN ADDRESS
         BR    R14                     AND RETURN
         LTORG
         TITLE 'APPLMON - TPEND EXIT'
***********************************************************************
*                                                                     *
* NAME: TPEND EXIT                                                    *
*                                                                     *
* FUNCTION: TO NOTIFY PO OF VTAM SHUTDOWN.                            *
*                                                                     *
* DESCRIPTION: SAVES REASON CODE AND POSTS CLOSEDOWN ECB.             *
*                                                                     *
* ENTRY POINT: TPEND                                                  *
*                                                                     *
* REGISTERS AT ENTRY:                                                 *
*                                                                     *
*        R1    @ PARAMETER LIST CONTAINING:                           *
*              (1) @ ACBTERMS                                         *
*              (2) REASON CODE                                        *
*        R14   RETURN ADDRESS                                         *
*        R15   TPEND ENTRY POINT ADDRESS                              *
*                                                                     *
* EXIT, NORMAL: BR 14                                                 *
*                                                                     *
* EXIT, ERROR: NONE                                                   *
*                                                                     *
* ATTRIBUTES: SERIALLY REUSABLE                                       *
*              VTAM EXLST EXIT                                        *
*                                                                     *
***********************************************************************
*
         USING *,10
TPEND    DS    0H
         LR    R10,R15            LOAD EXIT BASE ADDRESS
         LR    R11,R1         SAVE PARM PTR
         B     TPEND2
         DC    CL8'TPEND'         EXIT ID
TPEND2 DS      0H
*
* PICK UP TCBUSER ADDRESS FOR THE REGION CONTROL TASK TCB
*
         L     R1,PSAAOLD
         USING ASCB,1
         L     R1,ASCBRCTP        REGION CONTROL TASK TCB
         DROP  1
         USING TCB,1
         L     R1,TCBUSER
         DROP  1
         USING APPLVTAM,1
         LR    R2,R13             PRESERVE HIGHER R13
         LA    R13,APPLTSAV
         USING APPLTSAV,13
         ST    R14,0(,R13)        SAVE RETURN ADDRESS
*
         TM    TPEINUSE,INUSE     SAVE AREA IN USE?
         BZ    NOTPEABE           NO
         STM   R0,R1,ABREGS   SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 13,DUMP,STEP
NOTPEABE DS    0H
*
         ST    R13,8(,R2)         CHAIN FORWARD
         ST    R2,4(,R13)         CHAIN BACK
*
         OI    TPEINUSE,INUSE     FLAG SAVE AREA IN USE
*
         MVC   APGMCMD(4),=C'SHUT'   SHUTDOWN REGION
         XC    APGMECB2(4),APGMECB2
         LA    R2,APGMECB1
         POST  (2)
         LA    R2,APGMECB2
         WAIT  ECB=(2)
*                                 RETURN
TPETRET  NI    TPEINUSE,255-INUSE FLAG SAVE AREA FREE
         L     R14,0(,R13)        SAVE RETURN ADDRESS
         BR    R14                     AND RETURN
         LTORG
         TITLE 'A P P L M O N - ERROR EXIT'
***********************************************************************
*                                                                     *
* NAME: SYNAD/LERAD                                                   *
*                                                                     *
* FUNCTION: TO SET A RETURN CODE IN R15 BASED ON ERROR TYPE.          *
*                                                                     *
* ENTRY POINT: ERROR                                                  *
*                                                                     *
* REGISTERS AT ENTRY:                                                 *
*                                                                     *
*        R0    RECOVERY ACTION RETURN CODE                            *
*        R1    @RPL                                                   *
*                                                                     *
*        R2-12 UNDETERMINED                                           *
*        R13   UNMODIFIED SINCE MACRO ISSUED - CALLERS SA, SAVED      *
*        R14   RETURN ADDRESS IN VTAM, SAVED                          *
*        R15   ADDRESS OF ERROR EXIT                                  *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*              BASE REG R15                                           *
*              RPL BASE R1                                            *
*                                                                     *
* EXIT,NORMAL: BR 14                                                  *
*              R15 SET TO:        16 ENVIRONMENT ERROR (FROM LERAD)   *
*                                 20 LOGIC ERROR (FROM LERAD)         *
*                                                                     *
* EXIT, ERROR: NONE                                                   *
*                                                                     *
* CALLS: NONE (IF ANY ARE MADE TO VTAM ANOTHER SA MUST BE CHAINED)    *
*                                                                     *
* ATTRIBUTES: VTAM EXLST EXIT (BOTH SYNAD AND LERAD)                  *
*                                                                     *
* NOTE:       ERRSAVE%+C% POINTS TO MACRO IN ERROR                    *
*                                                                     *
***********************************************************************
*
*
*                              HAIN SAVE AREAS
         DS    0F                 ALIGN
         USING *,R15
         USING IFGRPL,R1
ERROR    DS    0H
         CLI   RPLRTNCD,USFLOGIC  LOGICAL ERROR?
         BE    LERERR         YES
         CLI   RPLRTNCD,USFENVER  ENVIRONMENT ERROR?
         BE    ENVERR         YES
         LA    R15,24
         BR    R14
*
*        ENVIRONMENT ERRORS
*
ENVERR   DS    0H
         LA    R15,16
         BR    R14
*
*        LOGICAL ERRORS
*
LERERR   DS    0H
         LA    R15,20
         BR    R14
         DROP  1,15
         TITLE 'VPUT AND VGET - VTAM SEND AND RECEIVE'
***********************************************************************
*                                                                     *
* FUNCTION: TO PROVIDE A CALLABLE INTERFACE TO VTAM SEND PROCESSING.  *
*                                                                     *
* REGISTERS AT ENTRY:                                                 *
*                                                                     *
*              R0 = SCREEN LENGTH                                     *
*              R1 = SCREEN ADDRESS                                    *
*                                                                     *
*                                                                     *
***********************************************************************
*
*
VGET     DS    0H
*
*   ESTABLISH BASE REG & LINKAGE
*
         USING *,12
         STM   R14,R12,12(R13)
         LR    R12,R15
         SR    R11,R11
         B     VGETPUT
*
VPUT     DS    0H
*
*   ESTABLISH BASE REG & LINKAGE
*
         STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R11,VPUT-VGET
         SR    R12,R11
*
VGETPUT  DS    0H
         LR    R3,R0     R3 = LENGTH OF SCREEN
         LR    R8,R1     R8 = LOCATION OF SCREEN
*
         L     R4,PSATOLD
         USING TCB,4
         LR    R9,R13            PRESERVE CALLING PGM'S R13
         L     R13,TCBUSER
         DROP  4
         USING TCBUVTAM,13
         LA    R13,TCBUSVCS
         USING TCBUSVCS,13
*
         ST    R9,4(,R13)
         ST    R13,8(,R9)
*
         CLC   TCBUAPPL(7),APPLEYE IS THIS MY TCBUSER AREA???
         BNE   VPUTGET4          IF NOT, JUST SET RC.
*
         CLI   TCBULU,C' '       IS THERE AN LU-NAME PRESENT?
         BE    VPUTGET4          IF NOT, JUST SET RC.
*
         CLI   TCBUSRID,C' '     IS THERE A USERID PRESENT?
         BE    VPUTGET4          IF NOT, JUST SET RC.
*
         LM    R5,R6,TCBUACBA
         USING IFGRPL,R6          RPL DSECT ADDRESSING
*
         LTR   R11,R11           IF R11 IS ZERO, DOING VGET
         BZ    VRECV
*
*
VSEND    DS    0H
         SEND  RPL=(R6),                                               *
               ACB=(R5),                                               *
               AREA=(R8),                                              *
               RECLEN=(R3),                                            *
               POST=RESP,                                              *
               OPTCD=SYN,                                              *
               RESPOND=(NEX,FME,NRRN)
         LTR   R15,R15            OK?
         BZ    VPUTGETE           YES
         CH    R0,V8              IF RETRIABLE ERROR
         BE    VSEND               THEN RETRY
*                                  ELSE RETURN NO GO
         B     VPUTGETE
         TITLE 'VGET - VTAM RECEIVE'
VRECV    DS    0H
       RECEIVE RPL=(R6),                                               *
               ACB=(R5),                                               *
               AREA=(R8),                                              *
               AREALEN=(R3),                                           *
               OPTCD=(SPEC,SYN,TRUNC),                                 *
               RTYPE=DFSYN
*
         MVC   TCBUGETL(2),RPLRLEN+2  LENGTH OF RECORD RECEIVED
*
         LTR   R15,R15            OK?
         BZ    VRECVLOG           YES
         CH    R0,V8              IF RETRIABLE ERROR
         BE    VRECV               THEN RETRY
*                                  ELSE RETURN NO GO
         B     VPUTGETE
*
VRECVLOG DS    0H
         SR    R15,R15            SET RC=0
         B     VPUTGETE           ALL DONE
         TITLE 'VPUT AND VGET     - EOJ'
VPUTGET4 DS    0H
*
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         LA    R15,12
         BR    R14                     AND RETURN
*
VPUTGETE DS    0H
*
         L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14                     AND RETURN
*
         TITLE 'VPUT AND VGET - CONSTANTS'
*
V8       DC    H'8'
*
APPLEYE  DC    CL8'APPLMON'
*
         TITLE 'VSIZE - RETRIEVE ROWS AND COLUMNS'
***********************************************************************
*                                                                     *
* FUNCTION: TO PROVIDE A CALLABLE INTERFACE TO VTAM INQUIRE.          *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*
*
VSIZE    DS    0H
*
*   ESTABLISH BASE REG & LINKAGE
*
         USING *,12
         STM   R14,R12,12(R13)
         LR    R12,R15
         SR    R11,R11
*
         L     R4,PSATOLD
         USING TCB,4
         LR    R9,R13            PRESERVE CALLING PGM'S R13
         L     R13,TCBUSER
         DROP  4
         USING TCBUVTAM,13
         LA    R13,TCBUSVCS
         USING TCBUSVCS,13
*
         ST    R9,4(,R13)
         ST    R13,8(,R9)
*
*
*        GET SESSION PARAMETERS
*
         L     R2,TCBURPLA
         L     R4,TCBUNIBA
         INQUIRE RPL=(2),                                              *
               ACB=ACBTERMS,                                           *
               NIB=(4),                                                *
               AREALEN=80,AREA=TCBUBIND,                               *
               OPTCD=(SYN,SESSPARM)
*
*
*        SET TERMINAL ROWS AND COLUMNS FOR GTSIZE
*
         LA    15,TCBUBIND
         USING ISTDBIND,15
         MVC   TCBUROWS(1),BINSALTR
         MVC   TCBUCOLS(1),BINSALTC
         DROP  15
VSIZEND  DS    0H
*
         L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14                     AND RETURN
*
         TITLE 'A P P L M O N - DATA AREAS'
         LTORG
*
INUSE    EQU   X'80'
RETRY    DC    H'8'               ACTION CODE FOR RETRIABLE ERROR
*
LOGLN#   DS    H                  LOG LINE COUNT
LOGPG#   DS    H                  LOG PAGE COUNT
PAGESIZE DC    H'60'              # LINES PER PAGE
EOFPGCNT DC    H'3'               # PAGES BETWEEN WRITE-EOFS
LOGHDR   DC    AL2(133,0)         LOG HEADER LINE
         DC    CL30'1'
         DC    C'APPLMON LOG'
         DC    CL72' '
         DC    C'DATE '
LOGHDRDT DS    CL5
*
LOGSKP   DC    AL2(5,0)           LOG SKIP AFTER HEADER LINE
         DC    C'-'
*
         TITLE 'A P P L M O N - CONTROL BLOCKS'
*
***********************************************************************
*                                                                     *
*        DATA CONTROL BLOCKS FOR LOGGING                              *
*                                                                     *
***********************************************************************
*
NETPRINT DCB   DDNAME=NETPRINT,                                        *
               BLKSIZE=133,LRECL=133,                                  *
               DSORG=PS,RECFM=FA,MACRF=(PM)
*
* PO VTAM CONTROL BLOCKS.
*
PRIMARPL RPL   AM=VTAM,                                                *
               ACB=ACBTERMS,                                           *
               OPTCD=ANY,                                              *
               NIB=LOGONNIB
*
MOVERPL  RPL   AM=VTAM,                                                *
               ACB=ACBTERMS,                                           *
               OPTCD=(SPEC,SYN,TRUNC),                                 *
               BRACKET=BB
*
LOGONNIB NIB   PROC=TRUNC,                                             *
               MODE=RECORD
*
ACBTERMS ACB   AM=VTAM,                                                *
               APPLID=APPLNAME,                                        *
               EXLST=TXLST,                                            *
               MACRF=LOGON
*
APPLNAME DC    X'07',CL7'APPLMON'      APPLICATION NAME
*
TXLST    EXLST AM=VTAM,                                                *
               LERAD=ERROR,                                            *
               SYNAD=ERROR,                                            *
               TPEND=TPEND,                                            *
               LOGON=LOGON,                                            *
               RELREQ=LOSTERM,                                         *
               LOSTERM=LOSTERM
*
QEDXTRC1 EXTRACT ,FIELDS=COMM,MF=L
QEDXTRCL EQU   *-QEDXTRC1
*
* PARAMETER LIST FOR TPEND EXIT
*
TPENDPRM DSECT
TPACB@   DS    F                  @ ACBTERMS
TPREASON DS    F                  REASON CODE
*
* PARAMETER LIST FOR LOGON AND RELREQ EXITS
*
LOGONPRM DSECT
LGNACB@  DS    F                  @ ACBTERMS
LOGONLU  DS    F                  @ TERMINAL NAME
         DS    F                  RESERVED
LGNMSGLN DS    F                  LENGTH OF LOGON MESSAGE
*
* PARAMETER LIST FOR LOSTERM EXIT
*
LOSTPARM DSECT                    FOR LOSTERM AND RESP EXITS
LOSTACB  DS    F                  @ ACBTERMS
LOSTCID  DS    F                  CID OF TERMINAL
LOSTUSER DS    F                  USER FIELD INFO - @ TICB
LOSTCODE DS    F                  REASON CODE (UNUSED FOR RESP EXIT)
RESPRPL  DS    F                  RESPONSE EXIT READ-ONLY RPL
*
         IFGACB AM=VTAM
         ISTUSFBC
         IFGRPL AM=VTAM
         ISTDNIB
         IHASDWA
         IEFZB4D2             DYNALLOC EQUATES
         IKJTSB   EXT=YES,LIST=YES
         IKJTCB
         IKTTCAST
         IHAASCB
         CVT
         IHAPSA
         ISTDBIND
         PRINT GEN
         TITLE 'A P P L M O N - TCBUSER FIELD FOR APPLICATIONS'
TCBUVTAM DSECT
         TCBUVTAM PFX=TCBU
         TITLE 'A P P L M O N - TCBUSER FIELD FOR SUPERVISOR'
APPLVTAM DSECT
         TCBUVTAM PFX=APPL
         ORG   APPLFILL           MAINLINE SAVE AREA
APPLLSAV DS    18F         LOGON  EXIT SAVE AREA
APPLESAV DS    18F         ESTAE  EXIT SAVE AREA
ERRORSA  DS    18F         LERAD  ERROR EXIT SAVE AREA
ENDSA    DS    18F                TPEND OR ABEND EXIT SAVE AREA
APPLTSAV DS    18F
ABREGS   DS    18F
*
LOSINUSE DS    XL1
LERINUSE DS    XL1
TPEINUSE DS    XL1
ABEINUSE DS    XL1
*
APGMECB1 DS    F                  MASTER LOGON AND CLOSEDOWN ECB
APGMRPLA DS    F                     RPL ADDRESS FOR CLSDST
*
APGMECB2 DS    F                     ECB ADDRESS FOR CLSDST DONE
*
APGMCMD  DS    CL80                  COMMAND TEXT
*
SESSBIND DS    CL80
*
         TITLE 'QEDIT WORK AREAS'
QEDXTRC  DS    XL(QEDXTRCL)
QEDCOMM  DS    A                   PTR TO COMM PTRS
QEDCOMMP DS    A                   PTR TO COMM PTRS
QEDTXT   DS    CL80                MODIFY TEXT
QEDCONID DS    X                   MCS CONSOLE ID THAT ISSUED MODIFY
QEDSTOPA DS    X                   STOP WAS ISSUED
         END
/*
//LKED    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,NCAL,TERM),
//             COND=(2,LT,ASM),REGION=1024K
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLIB   DD  DSN=SYS2.LINKLIB,DISP=SHR
//SYSLMOD  DD  DISP=SHR,
//             DSN=TECH.SERV.LOAD
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD  *
  SETCODE AC(1)
     NAME APPLMON(R)
/*
