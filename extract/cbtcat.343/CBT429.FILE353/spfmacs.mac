         EJECT
***********************************************************************
*
*  THIS IS A SET OF MACROS INTENDED TO REDUCE THE AMOUNT OF CODING
*  REQUIRED FOR ISPF/PDF APPLICATIONS. THIS MEMBER SHOULD BE COPIED
*  INTO YOUR PROGRAM.
*
*  MACROS INCLUDED ARE:
*
*   NAME        EXAMPLE
*
*  CONTROL      CONTROL  DISPLAY,SAVE
*  DISPLAY      DISPLAY  PANEL-NAME,MESSAGE-NAME,CURSOR-FIELD
*  PDFBROWS     PDFBROWS DSNAME,VOLSER
*  PDFEDIT      PDFEDIT  DSNAME,VOLSER
*  PQUERY       PQUERY   PLF2P2,ZDATA
*  SELECT       SELECT   PANEL(ABC)
*  SETMSG       SETMSG   MESSAGE-NAME
*  TBADD        TBADD    TABLE-NAME
*  TBBOTTOM     TBBOTTOM TABLE-NAME
*  TBCLOSE      TBCLOSE  TABLE-NAME
*  TBCREATE     TBCREATE TABLE-NAME,ENTRY1,ENTRY2,ENTRY3,ENTRY4
*  TBDELETE     TBDELETE TABLE-NAME
*  TBDISPL      TBDISPL  TABLE-NAME,PANEL-NAME,MESSAGE-NAME
*  TBEND        TBEND    TABLE-NAME
*  TBGET        TBGET    TABLE-NAME
*  TBPUT        TBPUT    TABLE-NAME
*  TBSAVE       TBSAVE   TABLE-NAME
*  TBSKIP       TBSKIP   TABLE-NAME,AMTFLD
*  TBTOP        TBTOP    TABLE-NAME
*  TBVCLEAR     TBVCLEAR TABLE-NAME
*  TLDSECT      TLDSECT
*  VCOPY        VCOPY    FIELD-NAME,LENGTH,OUTPUT-FIELD,MODE
*  VDEFINE      VDEFINE  FIELD-NAME,LENGTH
*  VDELETE      VDELETE FIELD-NAME1,FIELD-NAME2,FIELD-NAME3,FIELD-NAME4
*  VGET         VGET     FIELD-NAME
*  VPUT         VPUT    FIELD-NAME1,FIELD-NAME2,FIELD-NAME3,FIELD-NAME4
*  VREPLACE     VREPLACE VARIABLE-NAME,FULL-WORD-LENGTH,DATA-AREA
*
*
*  NOTE THAT ALL SERVICE-NAMES AND VARIABLE NAMES ARE GENERATED AS
*  LITERALS; THIS MAY AFFECT YOUR PLACEMENT OF "LTORG" STATEMENTS.
*
*
*  NOTE ALSO THAT THE "CALL" MACRO EXPANSION INCLUDES "MF=(E,ISPARMS)",
*  SO YOU MUST HAVE ISPARMS DEFINED AS A SET OF FULL-WORDS IN YOUR PGM.
*
***********************************************************************
         EJECT
***********************************************************************
*
*  CONTROL      UP TO 4 PARAMETERS ARE ALLOWED
*
***********************************************************************
         MACRO
&NAME    CONTROL &CTL1,&CTL2,&CTL3,&CTL4
         AIF   ('&CTL4' EQ '').TCTL3
         CALL  ISPLINK,                                                X
               (=CL8'CONTROL',                                         X
               =CL8'&CTL1',=CL8'&CTL2',=CL8'&CTL3',=CL8'&CTL4'),       X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TCTL3  ANOP
         AIF   ('&CTL3' EQ '').TCTL2
         CALL  ISPLINK,                                                X
               (=CL8'CONTROL',                                         X
               =CL8'&CTL1',=CL8'&CTL2',=CL8'&CTL3'),                   X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TCTL2  ANOP
         AIF   ('&CTL2' EQ '').TCTL1
         CALL  ISPLINK,                                                X
               (=CL8'CONTROL',                                         X
               =CL8'&CTL1',=CL8'&CTL2'),                               X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TCTL1  ANOP
         CALL  ISPLINK,                                                X
               (=CL8'CONTROL',                                         X
               =CL8'&CTL1'),                                           X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  DISPLAY      THE FIRST PARAMETER IS PANEL-NAME. OPTIONAL PARAMETERS
*               ARE MESSAGE-NAME AND CURSOR FIELD-NAME.
*
***********************************************************************
         MACRO
&NAME    DISPLAY &DPNL,&DMSG,&DCURS,&ENDKEY=NONE
         AIF   ('&DCURS' EQ '').NOCURS
         CALL  ISPLINK,                                                X
               (=CL8'DISPLAY',                                         X
               =CL8'&DPNL',=CL8'&DMSG',=CL8'&DCURS'),                  X
               VL,MF=(E,ISPARMS)
         AGO   .PF3
.NOCURS ANOP
         AIF   ('&DMSG' EQ '').NOMSG
         CALL  ISPLINK,                                                X
               (=CL8'DISPLAY',                                         X
               =CL8'&DPNL',=CL8'&DMSG'),                               X
               VL,MF=(E,ISPARMS)
         AGO   .PF3
.NOMSG  ANOP
         CALL  ISPLINK,                                                X
               (=CL8'DISPLAY',                                         X
               =CL8'&DPNL'),                                           X
               VL,MF=(E,ISPARMS)
.PF3    ANOP
         AIF   ('&ENDKEY' EQ 'NONE').ZZZ
         LA    14,8       END KEY?
         CR    15,14      END KEY?
         BE    &ENDKEY
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  PDFBROWS     PARAMETERS ARE DSNAME AND VOLSER
*
***********************************************************************
         MACRO
&NAME    PDFBROWS &BDSN,&BVOL
         AIF   ('&BVOL' EQ '').NOVOL
         CALL  ISPLINK,                                                X
               (=CL8'BROWSE',                                          X
               &BDSN,&BVOL),                                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.NOVOL  ANOP
         CALL  ISPLINK,                                                X
               (=CL8'BROWSE',                                          X
               &BDSN),                                                 X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  PDFEDIT      PARAMETERS ARE DSNAME AND VOLSER
*
***********************************************************************
         MACRO
&NAME    PDFEDIT &EDSN,&EVOL
         AIF   ('&EVOL' EQ '').NOVOL
         CALL  ISPLINK,                                                X
               (=CL8'EDIT',                                            X
               &EDSN,&EVOL),                                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.NOVOL  ANOP
         CALL  ISPLINK,                                                X
               (=CL8'EDIT',                                            X
               &EDSN),                                                 X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  PQUERY       PARAMETERS ARE :
*
*               1. PANEL-NAME
*               2. AREA-NAME
* "AREATYP"     3. VARIABLE-NAME THAT WILL BE SET TO DYNAMIC OR GRAPHIC
* "COLS"        4. VARIABLE-NAME THAT WILL BE SET TO AREA-WIDTH (COLS)
* "ROWS"        5. VARIABLE-NAME THAT WILL BE SET TO AREA-LENGTH (ROWS)
* "TLROW"       6. VARIABLE-NAME THAT WILL BE SET TO TOP LEFT ROW NUM
* "TLCOL"       7. VARIABLE-NAME THAT WILL BE SET TO TOP LEFT COL NUM
*
*
* BEFORE ISSUING "PQUERY", VDEFINES MUST BE DONE FOR 3 THROUGH 7.
*
*
***********************************************************************
         MACRO
&NAME    PQUERY  &QPNL,&QAREA
         CALL  ISPLINK,                                                X
               (=CL8'PQUERY',                                          X
               =CL8'&QPNL',=CL8'&QAREA',=CL8'AREATYP',                 X
               =CL8'COLS',=CL8'ROWS',=CL8'TLROW',=CL8'TLCOL'),         X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  SELECT       UP TO 3 PARAMETERS
*
*
***********************************************************************
         MACRO
&NAME    SELECT  &SELDAT1,&SELDAT2,&SELDAT3
         CALL  ISPLINK,                                                X
               (=CL8'SELECT',                                          X
               =F'40',                                                 X
               =CL40'&SELDAT1 &SELDAT2 &SELDAT3             '),        X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  SETMSG       THE ONLY PARAMETER IS MESSAGE-NAME
*
***********************************************************************
         MACRO
&NAME    SETMSG &MSG
         CALL  ISPLINK,                                                X
               (=CL8'SETMSG',                                          X
               =CL8'&MSG'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBADD        THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBADD &TAB
         CALL  ISPLINK,                                                X
               (=CL8'TBADD',                                           X
               =CL8'&TAB'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBBOTTOM     THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBBOTTOM &TAB
         CALL  ISPLINK,                                                X
               (=CL8'TBBOTTOM',                                        X
               =CL8'&TAB'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBCLOSE      THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBCLOSE &TAB
         CALL  ISPLINK,                                                X
               (=CL8'TBCLOSE',                                         X
               =CL8'&TAB'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBCLOSE      THE FIRST PARAMETER IS TABLE-NAME, FOLLOWED BY 1 TO 4
*               VARIABLE NAMES. THE NAME LIST IS GENERATED AS A LITERAL
*               WITH THE NAMES ENCLOSED IN PARENTHESES.
*
***********************************************************************
         MACRO
&NAME    TBCREATE &TABC,&EN1,&EN2,&EN3,&EN4,&KEY=()
         AIF   ('&EN4' EQ '').TRY3
         CALL  ISPLINK,                                                X
               (=CL8'TBCREATE',=CL8'&TABC',                            X
               =C'&KEY',                                               X
               =C'(&EN1. &EN2. &EN3. &EN4.)',                          X
               =CL8'NOWRITE',=CL8'REPLACE'),                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY3   ANOP
         AIF   ('&EN3' EQ '').TRY2
         CALL  ISPLINK,                                                X
               (=CL8'TBCREATE',=CL8'&TABC',                            X
               =C'&KEY',                                               X
               =C'(&EN1. &EN2. &EN3.)',                                X
               =CL8'NOWRITE',=CL8'REPLACE'),                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY2   ANOP
         AIF   ('&EN2' EQ '').TRY1
         CALL  ISPLINK,                                                X
               (=CL8'TBCREATE',=CL8'&TABC',                            X
               =C'&KEY',                                               X
               =C'(&EN1. &EN2.)',                                      X
               =CL8'NOWRITE',=CL8'REPLACE'),                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY1   ANOP
         CALL  ISPLINK,                                                X
               (=CL8'TBCREATE',=CL8'&TABC',                            X
               =C'&KEY',                                               X
               =C'(&EN1.)',                                            X
               =CL8'NOWRITE',=CL8'REPLACE'),                           X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  TBDELETE     THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBDELETE &TAB
         CALL  ISPLINK,                                                X
               (=CL8'TBDELETE',                                        X
               =CL8'&TAB'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBDISPL      THE FIRST PARAMETER IS TABLE-NAME. OPTIONAL PARAMETERS
*               ARE PANEL-NAME AND MESSAGE-NUMBER.
*
***********************************************************************
         MACRO
&NAME    TBDISPL &TAB,&PAN,&MSG,&ENDKEY=NONE
         AIF   ('&PAN' EQ '').NOPAN
         AIF   ('&MSG' EQ '').CCC
.BBB     ANOP
         CALL  ISPLINK,(=CL8'TBDISPL',                                 X
               =CL8'&TAB',=CL8'&PAN',=CL8'&MSG'),                      X
               VL,MF=(E,ISPARMS)
         AGO   .XXX
.CCC     ANOP
         CALL  ISPLINK,(=CL8'TBDISPL',=CL8'&TAB',=CL8'&PAN'),          X
               VL,MF=(E,ISPARMS)
         AGO   .XXX
.NOPAN   ANOP
         CALL  ISPLINK,(=CL8'TBDISPL',=CL8'&TAB'),                     X
               VL,MF=(E,ISPARMS)
         AGO   .XXX
.XXX     ANOP
         AIF   ('&ENDKEY' EQ 'NONE').ZZZ
         LA    14,8       END KEY?
         CR    15,14      END KEY?
         BE    &ENDKEY
.ZZZ     ANOP
         MEND
         EJECT
***********************************************************************
*
*  TBEND        THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBEND &TAB
         CALL  ISPLINK,                                                X
               (=CL8'TBEND',                                           X
               =CL8'&TAB'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBGET        THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBGET &TABG
         CALL  ISPLINK,                                                X
               (=CL8'TBGET',                                           X
               =CL8'&TABG'),                                           X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBSAVE       THE ONLY PARAMETER IS TABLE-NAME
*
***********************************************************************
         MACRO
&NAME    TBPUT &TABP
         CALL  ISPLINK,                                                X
               (=CL8'TBPUT',                                           X
               =CL8'&TABP'),                                           X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBSAVE       THE ONLY PARAMETER IS TABLE-NAME, THE SECOND IS
*
***********************************************************************
         MACRO
&NAME    TBSAVE &TABP
         CALL  ISPLINK,                                                X
               (=CL8'TBSAVE',                                          X
               =CL8'&TABP'),                                           X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBSKIP       PARAMETERS ARE TABLE-NAME AND AMOUNT FIELD
*
***********************************************************************
         MACRO
&NAME    TBSKIP &TABSK,&AMTFLD
         CALL  ISPLINK,                                                X
               (=CL8'TBSKIP',                                          X
               =CL8'&TABSK',&AMTFLD),                                  X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBTOP        THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBTOP &TAB
         CALL  ISPLINK,                                                X
               (=CL8'TBTOP',                                           X
               =CL8'&TAB'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TBVCLEAR     THE ONLY PARAMETER IS TABLE-NAME.
*
***********************************************************************
         MACRO
&NAME    TBVCLEAR &TAB
         CALL  ISPLINK,                                                X
               (=CL8'TBVCLEAR',                                        X
               =CL8'&TAB'),                                            X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
***********************************************************************
*
*  TLDSECT      NO PARAMETERS, JUST PROVIDES THE ISPF/PDF TLD
*
***********************************************************************
         MACRO
         TLDSECT
*                      ISPF/PDF  TERMINAL DATA CONTROL BLOCK
TLDSECT  DSECT
         DS    CL4        EYE-CATCHER "TLD0" (OR TLD1, ETC)
         DS    4F
THISTLD  DS    F           THIS TLD POINTER
         DS    7F
SPFSUBPT DS    F           SPF SUBS POINTER
         DS    10F
SPFSCRN  DS    F           ADDRESS OF LOGICAL SCREEN
         DS    11F
SPFFUNC  DS    0XL8
SPFFUNC1 DS    X
SPFFUNC2 DS    X
HELP     EQU   X'10'        PF1 = HELP
RETURN   EQU   X'08'        PF3 = END
SPFFUNC3 DS    X
ENDKEY   EQU   X'80'        PF3 = END
SPFFUNC4 DS    X                   SCROLL KEY BYTE
ENTER    EQU   X'00'        ENTER
FIND     EQU   X'08'        PF5 = FIND
CHANGE   EQU   X'04'        PF6 = CHANGE
UP       EQU   X'80'        PF7 = SCROLL UP
DOWN     EQU   X'40'        PF8 = SCROLL DOWN
LEFT     EQU   X'10'        PF10= SCROLL LEFT
RIGHT    EQU   X'20'        PF11= SCROLL RIGHT
         DS    XL4
SPFENABL DS    XL8                 ENABLE FUNCTIONS BITS
         DS    F
SPFCURSR DS    F                   RELATIVE CURSOR POSITION
         DS    2F
SPFGTSIZ DS    F                   MAXIMUN SCREEN SIZE
SPFMAXLN DS    F                   MAXIMUM NUMBER OF LINES ON TERMINAL
SPFMXLEN DS    F                   MAXIMUN LINE WIDTH ON TERMINAL
SPFDLINE DS    F                   CURRENT NUMBER OF LINES FOR SPFDISP
SPFDLLEN DS    F                   CURRENT LINE WIDTH FOR SPFDISP
         DS    2F
SPFSPLIT DS    F                   MAX LINES IF IN SPLIT SCREEN
         DS    11F
SPFSCROL DS    F                   SCROLL INPUT BYTE POINTER
SPFAPPID DS    CL8                 APP KEYWORD FOR SAVING
         DS    3F
SPFPREQ  DS    X                   REQUEST BITS
         DS    3X
         DS    X
SPFPSTAT DS    X                   STATUS FLAGS
         DS    2X
         DS    19F
SPFHELP  DS    CL8                 HELP MENU NAME
*
*                         FIELD ATTRIBUTE BYTES FOR SPF DISPLAY
*
ATTRONON EQU   X'04'               OUTPUT NON-DISPLAY
ATTROLOW EQU   X'05'               OUTPUT LOW INTENSITY
ATTROHI  EQU   X'07'               OUTPUT HIGH INTENSITY
ATTRIAN  EQU   X'10'               INPUT ASIS NON-DISPLAY
ATTRIAL  EQU   X'11'               INPUT ASIS LOW INTENSITY
ATTRIAH  EQU   X'13'               INPUT ASIS HIGH INTENSITY
ATTRICN  EQU   X'1C'               INPUT CAPS (BLANK-PAD) NON-DISPLAY
ATTRICL  EQU   X'1D'               INPUT CAPS (BLANK-PAD) LOW INTENSITY
ATTRICH  EQU   X'1F'               INPUT CAPS (BLANK-PAD) HIGH INT
*
SPFSUBRS DSECT
         DS    24F
SPFDISPL DS    F                   ISPF/PDF DISPLAY ROUTINE
         MEND
         EJECT
***********************************************************************
*
*  VCOPY        PARAMETERS ARE FIELD-NAME, LENGTH, OUTPUT FIELD-NAME,
*               AND MODE.
*
***********************************************************************
         MACRO
&NAME    VCOPY &ZFLD,&LEN,&OFLD,&MODE
         AIF   ('&MODE' EQ '').MOVE
         CALL  ISPLINK,(=CL8'VCOPY',                                   X
               =CL10'(&ZFLD)',&LEN,&OFLD,=CL8'&MODE'),                 X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.MOVE   ANOP
         CALL  ISPLINK,(=CL8'VCOPY',                                   X
               =CL10'(&ZFLD)',&LEN,&OFLD,=CL8'MOVE'),                  X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  VDEFINE      PARAMETERS ARE FIELD-NAME AND LENGTH. THE FIELD-NAME
*               MUST MATCH THE NAME CODED ON A PANEL, AND MUST BE
*               DEFINED AS A DATA AREA IN YOUR PROGRAM.
*
*               FOR EXAMPLE:
*
*                       VDEFINE ENTRYNM,8
*                       .
*                       .
*                       .
*                       .
*               ENTRYNM DS    CL8
*
***********************************************************************
         MACRO
&NAME    VDEFINE &FLD,&LEN,&FMT=CHAR,&OPT=
         AIF   ('&OPT' EQ '').NOOPT
         CALL  ISPLINK,(=CL8'VDEFINE',                                 X
               =CL10'(&FLD)',&FLD,=CL8'&FMT',=F'&LEN.',                X
               =CL10'(&OPT)'),                                         X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.NOOPT  ANOP
         CALL  ISPLINK,(=CL8'VDEFINE',                                 X
               =CL10'(&FLD)',&FLD,=CL8'&FMT',=F'&LEN.'),               X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  VDELETE      PARAMETERS ARE 1 TO 4 VARIABLE-NAMES. THESE ARE
*               GENERATED IN NAME-LIST FORMAT IN THE LITERAL POOL.
*
***********************************************************************
         MACRO
&NAME    VDELETE &VD1,&VD2,&VD3,&VD4
         AIF   ('&VD4' EQ '').TRY3
         CALL  ISPLINK,                                                X
               (=CL8'VDELETE',                                         X
               =C'(&VD1. &VD2. &VD3. &VD4.)'),                         X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY3   ANOP
         AIF   ('&VD3' EQ '').TRY2
         CALL  ISPLINK,                                                X
               (=CL8'VDELETE',                                         X
               =C'(&VD1. &VD2. &VD3.)'),                               X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY2   ANOP
         AIF   ('&VD2' EQ '').TRY1
         CALL  ISPLINK,                                                X
               (=CL8'VDELETE',                                         X
               =C'(&VD1. &VD2.)'),                                     X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY1   ANOP
         CALL  ISPLINK,                                                X
               (=CL8'VDELETE',                                         X
               =C'(&VD1.)'),                                           X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  VGET         THE ONLY PARAMETER IS VARIABLE-NAME. THIS MUST HAVE
*               BEEN PREVIOUSLY DEFINED BY A "VDEFINE" MACRO.
*
***********************************************************************
         MACRO
&NAME    VGET &FLD,&POOL=PROFILE
         CALL  ISPLINK,(=CL8'VGET',                                    X
               =CL10'(&FLD)',=CL8'&POOL'),                             X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  VPUT         PARAMETERS ARE 1 TO 4 VARIABLE-NAMES. THEY MUST HAVE
*               BEEN PREVIOUSLY DEFINED BY A "VDEFINE" MACRO.
*
***********************************************************************
         MACRO
&NAME    VPUT  &VP1,&VP2,&VP3,&VP4,&POOL=PROFILE
         AIF   ('&VP4' EQ '').TRY3
         CALL  ISPLINK,                                                X
               (=CL8'VPUT',                                            X
               =C'(&VP1. &VP2. &VP3. &VP4.)',                          X
               =CL8'&POOL'),                                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY3   ANOP
         AIF   ('&VP3' EQ '').TRY2
         CALL  ISPLINK,                                                X
               (=CL8'VPUT',                                            X
               =C'(&VP1. &VP2. &VP3.)',                                X
               =CL8'&POOL'),                                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY2   ANOP
         AIF   ('&VP2' EQ '').TRY1
         CALL  ISPLINK,                                                X
               (=CL8'VPUT',                                            X
               =C'(&VP1. &VP2.)',                                      X
               =CL8'&POOL'),                                           X
               VL,MF=(E,ISPARMS)
         AGO   .ZZZ
.TRY1   ANOP
         CALL  ISPLINK,                                                X
               (=CL8'VPUT',                                            X
               =C'(&VP1.)',                                            X
               =CL8'&POOL'),                                           X
               VL,MF=(E,ISPARMS)
.ZZZ    ANOP
         MEND
         EJECT
***********************************************************************
*
*  VREPLACE     THE FIRST PARAMETER IS VARIABLE-NAME, SECOND A FULLWORD
*               LENGTH, THIRD THE DATA.
*
***********************************************************************
         MACRO
&NAME    VREPLACE &FLD,&LEN,&DATA
         CALL  ISPLINK,(=CL8'VREPLACE',                                X
               =CL10'(&FLD)',&LEN,&DATA),                              X
               VL,MF=(E,ISPARMS)
         MEND
         EJECT
