//ASQCCLOG  JOB (X0002,QCC,ASQCC),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=99
//ASM     EXEC PGM=IEV90,REGION=1024K,
//             PARM='XREF(SHORT),OBJECT,NODECK,TERM'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DISP=SHR,DSN=SYS2.MACLIB       <== TCBUVTAM
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD  DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(1,1)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
APPLOGO  TITLE 'A P P L O G O - PRIMARY APPLICATION'
***********************************************************************
*
* CHANGE LOG:
*
*  7/24/85  BRIAN COOK   ADDED BLDL FOR APPLICATION PROGRAMS
*
***********************************************************************
*                                                                     *
*     MODULE NAME: APPLOGO                                            *
*                                                                     *
*     DESCRIPTIVE NAME: VTAM APPLICATION. THIS IS THE PRIMARY         *
*                       APPLICATION INVOKED BY APPLMON.               *
*                                                                     *
*     FUNCTION: 1. DISPLAYS LOGON SCREEN                              *
*               2. INVOKES PASSWORD/USERID VALIDATION EXIT (APPLXIT)  *
*                                                                     *
*     AUTHOR: BRIAN COOK, MORTON, 1984                                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* INITIAL HOUSEKEEPING - ENTRY CODE FOR OS/VS.                        *
* SAVE REGISTERS, ESTABLISH ADDRESSABILITY, SET UP SAVE AREA.         *
*                                                                     *
***********************************************************************
*
APPLOGO CSECT
         USING *,11,10
         STM   14,12,12(13)                      SAVE REGISTERS
         LR    R11,15         LOAD BASE REG1
         LR    R10,R11                       AND 2
         A     R10,FOURK
         L     R1,0(,R1)      PICK UP PASSED AREA
         ST    R1,8(,13)       SAVE
         ST    13,4(,R1)        AREA
         LR    13,R1             FOR CALLEES
         LR    R12,R13
         A     R12,FOURK
         USING TCBUVTAM,13,12     SAVE AREA DSECT ADDRESSING
*
*
* FILL IN TCBUSER ADDRESS
*
         MODESET KEY=ZERO          KEY ZERO FOR SVC 34
         USING PSA,0
         L     R1,PSATOLD
         USING TCB,1
         ST    R13,TCBUSER
         MODESET KEY=NZERO         RESET KEY
*
         B     SETESTAE       START THE DANCE
*
FOURK    DC    F'4096'
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
         EJECT
*
***********************************************************************
*        SET UP ESTAE ENVIRONMENT                                     *
***********************************************************************
*
SETESTAE DS    0H
         B     AFTESTAE
         ESTAE ABEXIT,ASYNCH=YES,TERM=YES
AFTESTAE DS    0H
*
*
         TITLE 'A P P L M O N - DISPLAY LOGON LOGO'
*
* DISPLAY THE LOGON LOGO
*
SHOWLOGO DS    0H
*
SENDLOGO DS    0H
*
         LA    R0,LOGSCRSZ
         LA    R1,LOGTEXT
         L     R15,TCBUVPUT
         BALR  R14,R15
*
         LR    R5,R15
         LTR   R5,R5            OK?
         BZ    RECVLOGO           YES
*
VPUTFAIL DS    0H
*
         LR    R4,R15              ELSE SET RC
         LR    R5,R0               ELSE SET RC
         STM   R0,R1,TCBUREGS SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 22,DUMP,STEP
*
         TITLE 'A P P L M O N - ISSUE RECEIVE'
***********************************************************************
*                                                                     *
* ISSUE RECEIVE MACRO TO GET USERID, PASSWORD                         *
*                                                                     *
***********************************************************************
*
RECVLOGO DS    0H
*
         LA    R1,TCBUWORK
         LA    R0,4095
         L     R15,TCBUVGET
         BALR  R14,R15
*
         LTR   R4,R15            OK?
         BZ    GOTDATA1           YES
*
VGETFAIL DS    0H
*
         STM   R0,R1,TCBUREGS SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 23,DUMP,STEP
GOTDATA1 DS    0H
*
         MVC   TCBUFILL(96),NETLOG
         LA    R4,TCBUFILL
         OPEN  ((4),(EXTEND))
*
         TM    TCBUFILL+48,X'80'
         BZ    GOTDATA2
*
         PUT   (4),TCBUWORK
         CLOSE ((4))
         B     GOTDATA2
*
NETLOG   DCB   DDNAME=NETLOG,DSORG=PS,MACRF=(PM)
*
GOTDATA2 DS    0H
         LA    R4,TCBUWORK
         LA    R15,204                 LOOP CONTROL
*
         CLI   0(R4),X'7D'             ENTER?
         BNE   SENDLOGO                NO, JUST RE-PROMPT
*
LOOPLOG1 DS    0H
         CLI   0(R4),X'11'             PARAMETER?
         BE    GOTDATA3                YES, FILL IN TCB-USER AREA
         LA    R4,1(,R4)               BUMP POINTER
         BCT   R15,LOOPLOG1
*
         B     SENDLOGO                NO, JUST RE-PROMPT
*
GOTDATA3 DS    0H
         MVC   TCBUSRID(8),3(R4)       MOVE USERID FROM BUFFER
         OC    TCBUSRID(8),SPACES      UPPER-CASE IT
         LA    R4,8(,R4)               BUMP POINTER
*
         CLC   TCBUSRID(4),=C'STOP'    STOP THIS SESSION
         BE    MAINRET                 GO DO IT
         CLC   TCBUSRID(4),=C'SHUT'    SHUTDOWN THE REGION
         BE    CLOSHUT                 GO DO IT
*
LOOPLOG2 DS    0H
         CLI   0(R4),X'11'             PARAMETER?
         BE    GOTDATA4                YES, FILL IN TCB-USER AREA
         LA    R4,1(,R4)               BUMP POINTER
         BCT   R15,LOOPLOG2
*
         B     SENDLOGO                NO, JUST RE-PROMPT
*
GOTDATA4 DS    0H
         MVC   TCBUPASS(8),3(R4)       MOVE PASSWORD FROM BUFFER
         OC    TCBUPASS(8),SPACES      UPPER-CASE IT
         LA    R4,8(,R4)               BUMP POINTER
*
LOOPLOG3 DS    0H
         CLI   0(R4),X'11'             PARAMETER?
         BE    GOTDATA5                YES, FILL IN TCB-USER AREA
         LA    R4,1(,R4)               BUMP POINTER
         BCT   R15,LOOPLOG3
*
         B     CHKPASS                 JUST INVOKE PASSWORD CHECKER
*
GOTDATA5 DS    0H
         MVC   TCBURACF(8),3(R4)       MOVE RACF GROUP FROM BUFFER
         OC    TCBURACF(8),SPACES      UPPER-CASE IT
*
         TITLE 'A P P L M O N - CHECK PASSWORD AND USERID'
*
CHKPASS  DS    0H
         LINK  EP=APPLXIT
*
         LTR   R4,R15            OK?
         BZ    PRIMOPTS           YES
*
PASSFAIL DS    0H
*
         STM   R0,R1,TCBUREGS SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 24,DUMP,STEP
         TITLE 'A P P L M O N - DISPLAY PRIMARY OPTION MENU'
*
PRIMOPTS DS    0H
*
* DISPLAY THE PRIMARY OPTION MENU
*
SHOWM1   DS    0H
*
         LA    R0,POMSCRSZ
         LA    R1,POMTEXT
         L     R15,TCBUVPUT
         BALR  R14,R15
*
         LR    R5,R15
         LTR   R5,R5            OK?
         BZ    RECVPOM            YES
*
         LR    R4,R15              ELSE SET RC
         LR    R5,R0               ELSE SET RC
         STM   R0,R1,TCBUREGS SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 28,DUMP,STEP
*
*
         TITLE 'A P P L M O N - READ IN PRIMARY OPTION MENU'
*
RECVPOM  DS    0H
*
* RECEIVE THE PRIMARY OPTION MENU
*
         LA    R1,TCBUWORK
         LA    R0,4095
         L     R15,TCBUVGET
         BALR  R14,R15
*
         LTR   R4,R15            OK?
         BZ    POMDATA1           YES
*
         STM   R0,R1,TCBUREGS SAVE REGS CLOBBERED BY ABEND MACRO
         ABEND 23,DUMP,STEP
POMDATA1 DS    0H
*
         MVC   TCBUFILL(96),NETLOG
         LA    R4,TCBUFILL
         OPEN  ((4),(EXTEND))
*
         TM    TCBUFILL+48,X'80'
         BZ    POMDATA2
*
         PUT   (4),TCBUWORK
         CLOSE ((4))
*
POMDATA2 DS    0H
         LA    R4,TCBUWORK
         LA    R15,80                  LOOP CONTROL
*
         CLI   0(R4),X'7D'             ENTER?
         BNE   SHOWLOGO                NO, REDISPLAY LOGON LOGO
*
LOOPPOM1 DS    0H
         CLI   0(R4),X'11'             PARAMETER?
         BE    POMDATA3                YES, GO SEE WHICH OPTION
         LA    R4,1(,R4)               BUMP POINTER
         BCT   R15,LOOPPOM1
*
         B     CLOSHUT                 NO, JUST SHUT DOWN
*
POMDATA3 DS    0H
         CLI   3(R4),C'1'              OPTION 1
         BE    POMOPT1                 YES, GO DO IT
         CLI   3(R4),C'2'              OPTION 2
         BE    POMOPT2                 YES, GO DO IT
         CLI   3(R4),C'3'              OPTION 3
         BE    POMOPT3                 YES, GO DO IT
         CLI   3(R4),C'4'              OPTION 4
         BE    POMOPT4                 YES, GO DO IT
*
         B     SHOWM1                  RESHOW PRIMARY OPTION MENU
*
POMOPT1  DS    0H
*
*                                      SYSTEM ACTIVITY DISPLAY
         BLDL  0,BLDLSAD
         LTR   R15,R15
         BNZ   SHOWM1                  RESHOW PRIMARY OPTION MENU
*
         LINK  EP=APPLSAD
*
         B     SHOWM1                  RESHOW PRIMARY OPTION MENU
*
POMOPT2  DS    0H
*
*                                      VTAM OPERATOR
         BLDL  0,BLDLSPO
         LTR   R15,R15
         BNZ   SHOWM1                  RESHOW PRIMARY OPTION MENU
*
         LINK  EP=APPLSPO
*
         B     SHOWM1                  RESHOW PRIMARY OPTION MENU
*
POMOPT3  DS    0H
*
*                                      MVS  OPERATOR
         BLDL  0,BLDLOPR
         LTR   R15,R15
         BNZ   SHOWM1                  RESHOW PRIMARY OPTION MENU
*
         LINK  EP=APPLOPR
*
         B     SHOWM1                  RESHOW PRIMARY OPTION MENU
*
POMOPT4  DS    0H
*
*                                      PROGRAMMER SERVICES
         BLDL  0,BLDLPGM
         LTR   R15,R15
         BNZ   SHOWM1                  RESHOW PRIMARY OPTION MENU
*
         LINK  EP=APPLPGM
*
         B     SHOWM1                  RESHOW PRIMARY OPTION MENU
*
         TITLE 'A P P L M O N - CLOSE DOWN'
*
CLOSHUT  DS    0H
*
         ENQ   (APPLNAME,COMMAND,E,,STEP),RET=HAVE
*
         L     R2,TCBUECB1    PICK UP ECB ADDRESS TO POST SUPERVISOR
         L     R3,TCBUECB2    PICK UP ECB ADDRESS TO WAIT ON
         XC    0(4,R3),0(R3)
         L     R4,TCBUCMD     PICK UP ADDRESS TO SET COMMAND IN
         MVC   0(4,R4),=C'CLOS'    SHUTDOWN
         L     R8,TCBUWORD    PICK UP ADDRESS OF FULLWORD FOR RPL
         MVC   0(4,R8),TCBURPLA    MOVE IN MY RPL
*
         POST  (2)
         WAIT  ECB=(3)
*
         MVC   0(4,R4),=C'STOP'    SHUTDOWN
         XC    0(4,R3),0(R3)
*
CLOSHUT2 DS    0H
*
         TM    0(R2),X'80'         APPLMON WAITING?
         BO    CLOSHUT3            YES, POST HIM
*
         STIMER WAIT,BINTVL=TWOSEC
         B     CLOSHUT2            TEST AGAIN
*
CLOSHUT3 DS    0H
*
         POST  (2)
         WAIT  ECB=(3)
*
         MVI   0(R4),C' '          TURN OFF COMMAND FLAG
*
         DEQ   (APPLNAME,COMMAND,,STEP),RET=HAVE
*
         B     MAINRET2
*
TWOSEC   DC    F'200'
*
***********************************************************************
* EXIT CODE                                                           *
***********************************************************************
*
MAINRET  DS    0H
*
         ENQ   (APPLNAME,COMMAND,E,,STEP),RET=HAVE
*
         L     R6,TCBUECB1    PICK UP ECB ADDRESS TO POST SUPERVISOR
         L     R7,TCBUECB2    PICK UP ECB ADDRESS TO WAIT ON
         XC    0(4,R7),0(R7)
         L     R8,TCBUCMD     PICK UP ADDRESS TO SET COMMAND IN
         MVC   0(4,R8),=C'CLOS'    SHUTDOWN
         L     R8,TCBUWORD    PICK UP ADDRESS OF FULLWORD FOR RPL
         MVC   0(4,R8),TCBURPLA    MOVE IN MY RPL
*
         POST  (6)
         WAIT  ECB=(7)
*
         DEQ   (APPLNAME,COMMAND,,STEP),RET=HAVE
*
MAINRET2 DS    0H
         L     R13,4(,R13)        PICK UP HIGHER SAVE AREA
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
SPACES   DC    CL8' '
RETRY    DC    H'8'
         LTORG
*
         TITLE 'A P P L M O N - ESTAE EXIT'
*        ESTAE EXIT
*
         DS    0F                 ALIGN
         USING *,10
ABEXIT   DS    0H
         STM   14,12,12(13)                      SAVE REGISTERS
         LR    R10,R15            LOAD EXIT BASE ADDRESS
*
         LR    R4,R1          SAVE SDWA PTR
         USING SDWA,R4        TELL ASMBLR
*
         LR    R2,R13         SAVE CALLER'S R13
         L     R1,PSATOLD     PICK UP TCB ADDRESS
         L     R13,TCBUSER    PICK UP TCBUSER ADDRESS
         LA    R13,TCBUESTA   POINT TO ESTAE SAVE AREA
         DROP  13
         USING TCBUESTA,13
         ST    R13,8(,R2)        CHAIN FORWARD
         ST    R2,4(,R13)         CHAIN BACK
         SR    R3,R3          DEFAULT NO RETRY
         L     R6,SDWACMPF    PICK UP CMP CODE
         LA    R6,0(,R6)      CLEAR HI-ORDER BYTE
         SRDL  R6,12          SPLIT SYS FROM USER
         SRL   R7,20
         LTR   R6,R6          SYS CMP CODE?
         BNZ   SYSCMP         YES
         LR    R6,R7          NO - MUST BE USER CMP
*        MVI   TBUF,C'U'      CMP ID
         B     LOGABM         NO
SYSCMP   DS    0H
*        MVI   TBUF,C'S'      CMP ID
         CH    R6,=AL2(X'D37') VTAMLOGO DATASET FULL?
         BNE   ISZCAN         NO
         NI    SDWACMPF,255-SDWAREQ  YES - SET NO DUMP
         B     LOGABM         ALLOW ABEND TO CONTINUE
ISZCAN   CH    R6,=AL2(X'0A7') Z NET,CANCEL?
         BNE   ISCANCEL       NO
         NI    SDWACMPF,255-SDWAREQ  SET NO DUMP
         B     LOGABM         ALLOW ABEND TO CONTINUE
ISCANCEL CH    R6,=AL2(X'222') OPER CANCEL?
         BNE   LOGABM         NO
         TM    SDWACMPF,SDWAREQ  DUMP REQUESTED?
         BO    LOGABM          YES - PROBABLY LEGIT
*                              ONVERT BYTE 2
LOGABM   DS    0H
ABRET    DS    0H
*
         ENQ   (APPLNAME,COMMAND,E,,STEP),RET=HAVE
*
         L     R6,TCBUECB1    PICK UP ECB ADDRESS TO POST SUPERVISOR
         L     R7,TCBUECB2    PICK UP ECB ADDRESS TO WAIT ON
         XC    0(4,R7),0(R7)
         L     R8,TCBUCMD     PICK UP ADDRESS TO SET COMMAND IN
         MVC   0(4,R8),=C'CLOS'    SHUTDOWN
         L     R8,TCBUWORD    PICK UP ADDRESS OF FULLWORD FOR RPL
         MVC   0(4,R8),TCBURPLA    MOVE IN MY RPL
*
         POST  (6)
         WAIT  ECB=(7)
*
         DEQ   (APPLNAME,COMMAND,,STEP),RET=HAVE
*
         MVI   SDWARCDE,0         SET RETURN CODE
         L     R13,4(,R13)        RELOAD CALLERS SA
*
         RETURN (14,12)           ALLOW ABEND TO CONTINUE
         LTORG
         EJECT
*
         TITLE 'A P P L M O N - LOGO DATA AREA'
*
LOGTEXT  DS    0H
         DC    X'7EC2'
L01 DC X'1140401D60',40C'* '
L02 DC X'11C1501D60',CL5'*',CL75'   AA     PPPP    PPPP   LL          '
L03 DC X'11C2601D60',CL5'*',CL75'  AA A   P   PP  P   PP  LL          '
L04 DC X'11C3F01D60',CL5'*',CL75' AA  AA  P   PP  P   PP  LL      --- '
L05 DC X'11C5401D60',CL5'*',CL75' AAAAAA  PPPPP   PPPPP   LL      --- '
L06 DC X'11C6501D60',CL5'*',CL75' AA  AA  P       P       LL          '
L07 DC X'11C7601D60',CL5'*',CL75' AA  AA  P       P       LLLLLL      '
L08 DC X'11C8F01D60',CL5'*',CL75'                                     '
L09 DC X'114A401D60',CL5'*',CL75'  M       M    OOO     N    N        '
L10 DC X'114B501D60',CL5'*',CL75'  MM     MM   O   O    NN   N        '
L11 DC X'114C601D60',CL5'*',CL75'  M M   M M  O     O   N N  N        '
L12 DC X'114DF01D60',CL5'*',CL75'  M  MMM  M  O     O   N  N N        '
L13 DC X'114F401D60',CL5'*',CL75'  M   M   M  O     O   N   NN        '
L14 DC X'1150501D60',CL5'*',CL75'  M       M   O   O    N    N        '
L15 DC X'11D1601D60',CL5'*',CL75'  M       M    OOO     N    N        '
L16 DC X'11D2F01D60',CL5'*',CL75'                                     '
L17 DC X'11D4401D60',CL14'ENTER USER ID:',X'1D40',X'13',CL20' '       '
    DC X'1D60',CL44'(ENTER "STOP" TO RETURN TO VTAM)'
L18 DC X'11D5501D60',CL14' AND PASSWORD:',X'1D4C',CL66' '       '
L19 DC X'11D6601D60',CL14'   RACF GROUP:',X'1D40',CL66' '       '
L20 DC X'11D7F01D60',40C'* '
LOGSCRSZ EQU   *-LOGTEXT
*
         TITLE 'A P P L M O N - PRIMARY OPTION MENU'
*
POMTEXT  DS    0H
         DC    X'7EC2'
O01 DC X'1140401D60',CL80' '
O02 DC X'11C1501DE8',23C'-',CL32'  APPLMON PRIMARY OPTION MENU',24C'-'
O03 DC X'11C2601DE8',CL13'OPTION  ===>',X'1D40',X'13',CL67' '
O04 DC X'11C3F01D60',CL80' '
O05 DC X'11C5401DE8',CL6'   1  ',X'1D60',CL74'SYSTEM ACTIVITY DISPLAY '
O06 DC X'11C6501DE8',CL6'   2  ',X'1D60',CL74'VTAM OPERATOR           '
O07 DC X'11C7601DE8',CL6'   3  ',X'1D60',CL74'MVS CONSOLE             '
O08 DC X'11C8F01DE8',CL6'   4  ',X'1D60',CL74'PROGRAMMER SERVICES     '
O09 DC X'114A401DE8',CL6'      ',X'1D60',CL74'                        '
O10 DC X'114B501DE8',CL6'      ',X'1D60',CL74'                        '
O11 DC X'114C601DE8',CL6'      ',X'1D60',CL74'                        '
O12 DC X'114DF01DE8',CL6'      ',X'1D60',CL74'                        '
O13 DC X'114F401DE8',CL6'      ',X'1D60',CL74'                        '
O14 DC X'1150501DE8',CL6'      ',X'1D60',CL74'                        '
O15 DC X'11D1601DE8',CL6'      ',X'1D60',CL74'                        '
O16 DC X'11D2F01DE8',CL6'      ',X'1D60',CL74'                        '
O17 DC X'11D4401DE8',CL6'      ',X'1D60',CL74'                        '
O18 DC X'11D5501D60',CL80' '
O19 DC X'11D6601D60',CL80'ENTER END COMMAND TO TERMINATE APPLMON'
O20 DC X'11D7F01D60',CL80' '
POMSCRSZ EQU   *-POMTEXT
*
         TITLE 'A P P L M O N - DATA AREAS'
APPLNAME DC    CL8'APPLMON1'
COMMAND  DC    C'COMMAND'
BLDLSAD  DC    H'1',H'58',CL8'APPLSAD',CL50' '
BLDLSPO  DC    H'1',H'58',CL8'APPLSPO',CL50' '
BLDLOPR  DC    H'1',H'58',CL8'APPLOPR',CL50' '
BLDLPGM  DC    H'1',H'58',CL8'APPLPGM',CL50' '
         TITLE 'A P P L M O N - DSECTS'
TCBUVTAM DSECT
         TCBUVTAM PFX=TCBU
         PRINT NOGEN
         IKJTCB
         IHAASCB
         IHAPSA
         IHASDWA
         END
/*
//LKED    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,NCAL,TERM),
//             COND=(4,LT,ASM),REGION=1024K
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLIB   DD  DSN=SYS2.LINKLIB,DISP=SHR
//SYSLMOD  DD  DISP=SHR,
//             DSN=TECH.SERV.LOAD
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD  *
  SETCODE AC(1)
     NAME APPLOGO(R)
/*
