         MACRO
&LABEL   TIMEROU   &REG=1
*        REG = THE REGISTER CONTAINING THE TIME IN BIN FORMAT
*        THIS ROUTINE ALSO USES REGISTERS 1,2,3
*        THIS ROUTINE CONVERTS THE TIME INTO HH:MM:SS
&LABEL   DS    0H
         CVD   &REG,WORKD0
         CVD   &REG,WORKD1
         DP    WORKD1(8),P100(2)
         XC    WORKD2(8),WORKD2
         MVC   WORKD2+2(6),WORKD1 GET NUMBER OF SECONDS IN WORKD2
*
         DP    WORKD2(8),P3600(3)
         XC    WORKD3(8),WORKD3
         MVC   WORKD3+5(3),WORKD2+5   NUMBER OF MINUTES IN WORKD3
         UNPK  WORKC8(8),WORKD2(5)    HOURS
         MVC   OUTTIME(2),WORKC8+6
         OI    OUTTIME+1,X'F0'        MAKE PRINTABLE
         MVI   OUTTIME+2,C':'
*
         DP    WORKD3(8),P60(2)
         UNPK  WORKC8(8),WORKD3(6)    NUMBER OF MINUTES
         MVC   OUTTIME+3(2),WORKC8+6  NUMBER OF MINUTES
         OI    OUTTIME+4,X'F0'        MAKE PRINTABLE
         MVI   OUTTIME+5,C':'
*
         UNPK  WORKC8(8),WORKD3+6(2)  NUMBER OF SECONDS
         MVC   OUTTIME+6(2),WORKC8+6
         OI    OUTTIME+7,X'F0'        MAKE PRINTABLE
         LA    &REG,OUTTIME
         B     TIMEEND
*
F0       DC    F'0'
P100     DC    PL2'+100'
P60      DC    PL2'+60'
P3600    DC    PL3'+3600'
         EJECT
WORKD0   DS    D
WORKD1   DS    D
WORKD2   DS    D
WORKD3   DS    D
WORKC8   DS    CL8
OUTTIME  DS    CL9
TIMEEND  DS    0H
         MEND
