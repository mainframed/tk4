//ASQCCSVC  JOB (X0041,QCC,ASQCC),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=99
//*
//*  TECH.XA.SOURCE   (IGC0023F)
//*
//JOBLIB   DD  DSN=SYS1.LINKLIB,DISP=SHR,
//         UNIT=3380,VOL=SER=MVSTGT
//*
//ASM1    EXEC PGM=IEV90,REGION=1024K,
//        PARM='OBJECT,NODECK,TERM,XREF(SHORT),RENT'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,
//         UNIT=3380,VOL=SER=MVSTGT
//         DD  DSN=SYS1.AMODGEN,DISP=SHR,
//         UNIT=3380,VOL=SER=MVSDLB
//         DD  DSN=SYS2.MACLIB,DISP=SHR    <== CVTUSERS
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
SUPERSVC TITLE 'SVC ROUTER--USER ESR'
***********************************************************************
*                                                                     *
*                                                                     *
*                 USER SVC INTERFACE                                  *
*                                                                     *
*                                                                     *
* ABSTRACT: INVOKE MULTIPLE USER SVC FUNCTIONS                        *
*                                                                     *
*                                                                     *
* FUNCTION: THIS IS DESIGNED TO BE INVOKED FROM ANY ASSEMBLER         *
*           PROGRAM.                                                  *
*                                                                     *
*           IT PROVIDES FUNCTION SIMILAR TO AN ESR SVC.               *
*                                                                     *
*           IT INVOKES USER SVC FUNCTIONS BASED ON A ROUTE CODE SET   *
*           IN REGISTER 15. THE USER SVC'S MUST PREVIOUSLY HAVE BEEN  *
*           LOADED INTO CSA, AND THEIR EPA ADDRESSES PLACED IN AN     *
*           SVC TABLE LOCATED IN THE CVTUSER AREA. THE LOADER PROGRAM *
*           IS "LOADSVCS"--SEE THE SOURCE CODE FOR DOCUMENTATION.     *
*                                                                     *
*           THE USER SVC'S THAT ARE INVOKED RECEIVE CONTROL JUST AS   *
*           THOUGH THEY WERE INVOKED AS INDIVIDUAL TYPE 3/ SVC'S.     *
*                                                                     *
*                                                                     *
* INVOKING:                                                           *
*                                                                     *
*         LA    R15,04       <== SPECIAL ROUTE CODE                   *
*         SVC   236          <== THIS SVC NUMBER                      *
*                                                                     *
*         NOTE: THE CONTENTS OF R0 AND R1 ARE PASSED WITHOUT CHANGE   *
*               TO THE USER SVC FUNCTION. INTERFACE REQUIREMENTS ARE  *
*               DEPENDENT ON THE USER SVC FUNCTION, NOT THIS MODULE,  *
*               WHICH ONLY DOES ROUTING.                              *
*                                                                     *
* OUTPUT:                                                             *
*                                                                     *
*         NONE. THE OUTPUT IS BASED ON THE OUTPUT OF THE USER SVC     *
*               FUNCTION. THE ONLY ERROR RETURN CODES SET BY THIS     *
*               SVC ROUTER PROGRAM ARE:                               *
*                                                                     *
*               512 -- REQUESTED USER SVC FUNCTION NOT IN CVTUSER TAB *
*               516 -- CVTUSER AREA NOT AVAILABLE.                    *
*               520 -- CVTUSER AREA NOT FORMATTED PROPERLY.           *
*                                                                     *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: EACH USER SVC FUNCTION.                         *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* CHANGE LOG:                                                         *
*                                                                     *
*   2/07/85  BRIAN COOK.     INITIAL ENTRY.                           *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
IGC0023F CSECT
         USING *,12
         LR    R12,R6         SET SVC BASE REGISTER
         B     START
         DC  C'IGC0023F_SUPERSVC_02/07/86_BRIAN COOK_&SYSTIME_&SYSDATE'
START    DS    0H
         USING PSA,0
         L     R6,FLCCVT      POINT TO CVT
         USING CVT,6
         L     R6,CVTUSER     POINT TO CVT USER AREA
*
         USING CVTUSERS,6
         LTR   R6,R6
         BZ    RC516
*
         CLI   CVTULABL,C'C'      LOOKING FOR LITERAL "CVTUSER"
         BNE   RC520
         CLI   CVTULABL+1,C'V'
         BNE   RC520
         CLI   CVTULABL+2,C'T'
         BNE   RC520
         CLI   CVTULABL+3,C'U'
         BNE   RC520
*
         L     R6,CVTUSVCT(R15)   SET EPA IN REG 6
         LTR   R6,R6
         BNZR  R6
RC512    DS    0H
         LA    R15,512            SET BAD RETURN CODE
         BR    R14                RETURN
*
RC516    DS    0H
         LA    R15,516            SET BAD RETURN CODE
         BR    R14                RETURN
*
RC520    DS    0H
         LA    R15,520            SET BAD RETURN CODE
         BR    R14                RETURN
*
*
*
*
         CVTUSERS
         PRINT NOGEN
         IHAPSA
         CVT   DSECT=YES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
         END
/*
//LKED1    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,TERM,RENT),
//            COND=(2,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LPALIB,
//         UNIT=3380,VOL=SER=MVSTGT
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
     NAME IGC0023F(R)
/*
