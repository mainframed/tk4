//ASQCCOMP JOB (X0002,QCC,ASQCC),'X-COOK, BRIAN',
//   MSGCLASS=X,CLASS=X,NOTIFY=ASQCC,MSGLEVEL=(1,1)
/*JOBPARM L=99,SYSAFF=MVSA
//*
//ASM     EXEC PGM=IEV90,REGION=1024K,PARM='OBJECT,NODECK,TERM'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,1)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
ETPSCOMP TITLE 'ETPS - COMPRESS LIBRARY'
ETPSCOMP CSECT
         USING *,12
*
         SAVE  (14,12),,ETPSCOMP_11/05/86_BRIAN COOK_&SYSTIME_&SYSDATE
         LR    R12,R15            SET BASE REGISTER
*
         LM    R3,R5,0(R1)         R3  = INPUT DSN
*                                  R4  = INPUT VOLSER
*                                  R5  = INPUT PASSWORD
         MVC   INDS(44),0(R3)
         MVC   INV(6),0(R4)
         MVC   INP(8),0(R5)
*
         SR    R4,R4
         SR    R5,R5
*
PROLOG   DS    0H
         GETMAIN R,LV=4096        GET MY SAVE AREA ADDRESS
         LR    R11,R1             SAVE GETMAIN POINTER
         ST    R13,4(R11)         STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11
         USING MYSAVE,13
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
         EJECT
FREES    DS    0H
*
         LA    R1,FREE1
         DYNALLOC
         LA    R1,FREE2
         DYNALLOC
         LA    R1,FREE3
         DYNALLOC
         LA    R1,FREE4           FREE SYSIN
         DYNALLOC
         LA    R1,FREE5
         DYNALLOC
*
ALLOCATE DS    0H
*
         OI    INV,C' '
         CLI   INV,C' '
         BNE   *+10
         XC    INVADDR(4),INVADDR
         OI    INP,C' '
         CLI   INP,C' '
         BNE   *+10
         XC    INPADDR(4),INPADDR
*
         LA    R1,RB99LIB
         DYNALLOC
         L     R4,RB99LIB+8
         LTR   R5,R15
         BNZ   ALLDONE
*
         LA    R1,RB99UT3
         DYNALLOC
         L     R4,RB99UT3+8
         LTR   R5,R15
         BNZ   ALLDONE
*
         LA    R1,RB99UT4
         DYNALLOC
         L     R4,RB99UT4+8
         LTR   R5,R15
         BNZ   ALLDONE
*
         LA    R1,RB99INP
         DYNALLOC
         L     R4,RB99INP+8
         LTR   R5,R15
         BNZ   ALLDONE
*
         LA    R1,RB99PRNT
         DYNALLOC
         L     R4,RB99PRNT+8
         LTR   R5,R15
         BNZ   ALLDONE
*
OPENS    DS    0H
*
         OPEN  (SYSIN,(OUTPUT))
*
         TM    SYSIN+48,X'10'  *  TEST FOR GOOD OPEN
         BZ    ALLDONE          *  IF NO GOOD, LEAVE
*
         PUT   SYSIN,DDCARD
*
         CLOSE (SYSIN)
*
TSTAUTH  DS    0H
*
         TESTAUTH FCTN=1
         LTR   R15,R15
         BNZ   SETAUTH
*
         LA    R1,AH0           * SET PARM REG
         LINK  EP=IEBCOPY
         LR    R5,R15           * PICK UP RETURN CODE
         SR    R4,R4
*
         B     ALLDONE
*
SETAUTH  DS    0H
*
         LA    R0,1             SET AUTH ON
         SVC   235
*
         LA    R1,AH0           * SET PARM REG
         LINK  EP=IEBCOPY
         LR    R5,R15           * PICK UP RETURN CODE
         SR    R4,R4
*
         SR    R0,0             SET AUTH OFF
         SVC   235
*
         B     ALLDONE
*
ALLDONE  DS    0H
*
         LA    R1,FREE1
         DYNALLOC
         LA    R1,FREE2
         DYNALLOC
         LA    R1,FREE3
         DYNALLOC
         LA    R1,FREE4
         DYNALLOC
         LA    R1,FREE5
         DYNALLOC
*
BADRC    DS    0H
         L     R13,4(,R13)  PICK UP CALLING SAVE AREA
         FREEMAIN R,LV=4096,A=(11)
         LR    R15,R5
         LR    R0,R4
         L     R14,12(,R13)
         LM    R1,R12,24(R13)
         BR    R14
         EJECT
DDCARD   DC    CL80'  COPY I=SYSUT1,O=SYSUT1 '
AH0      DC    A(H0+X'80000000')
H0       DC    H'0'
FREE1    DC    A(*+4+X'80000000')
         DC    X'14020000'
         DC    F'0'
         DC    A(*+12+X'80000000')
         DC    F'0'
         DC    F'0'
         DC    A(UNCOND)
         DC    A(INDDNAME+X'80000000')
*
FREE2    DC    A(*+4+X'80000000')
         DC    X'14020000'
         DC    F'0'
         DC    A(*+12+X'80000000')
         DC    F'0'
         DC    F'0'
         DC    A(UNCOND)
         DC    A(INDDUT3+X'80000000')
*
FREE3    DC    A(*+4+X'80000000')
         DC    X'14020000'
         DC    F'0'
         DC    A(*+12+X'80000000')
         DC    F'0'
         DC    F'0'
         DC    A(UNCOND)
         DC    A(INDDUT4+X'80000000')
*
FREE4    DC    A(*+4+X'80000000')
         DC    X'14020000'
         DC    F'0'
         DC    A(*+12+X'80000000')
         DC    F'0'
         DC    F'0'
         DC    A(UNCOND)
         DC    A(INDDINP+X'80000000')
*
FREE5    DC    A(*+4+X'80000000')
         DC    X'14020000'
         DC    F'0'
         DC    A(*+12+X'80000000')
         DC    F'0'
         DC    F'0'
         DC    A(UNCOND)
         DC    A(INDDPRNT+X'80000000')
*
RB99LIB  DC    A(*+4+X'80000000')
         DC    X'14010000'
         DC    F'0'
         DC    A(*+12+X'80000000')
         DC    F'0'
         DC    F'0'
INTEXT1  DC    A(INDDNAME)
         DC    A(INDSNAME)
         DC    A(DISPSHR)
INVADDR  DC    A(INVOLSER)
INPADDR  DC    A(INPASS)
         DC    A(DISPKEEP+X'80000000')
RB99UT3  DC    A(*+4+X'80000000')
         DC    X'14010000'
         DC    F'0'
         DC    A(INTEXT2+X'80000000')
         DC    F'0'
         DC    F'0'
INTEXT2  DC    A(INDDUT3)
         DC    A(DISPNEW)
         DC    A(PRIMRY)
         DC    A(SECNRY)
         DC    A(CYLS+X'80000000')
*
RB99UT4  DC    A(*+4+X'80000000')
         DC    X'14010000'
         DC    F'0'
         DC    A(INTEXT3+X'80000000')
         DC    F'0'
         DC    F'0'
INTEXT3  DC    A(INDDUT4)
         DC    A(DISPNEW)
         DC    A(PRIMRY)
         DC    A(SECNRY)
         DC    A(KEYLEN)
         DC    A(CYLS+X'80000000')
*
RB99INP  DC    A(*+4+X'80000000')
         DC    X'14010000'
         DC    F'0'
         DC    A(INTEXT4+X'80000000')
         DC    F'0'
         DC    F'0'
INTEXT4  DC    A(INDDINP)
         DC    A(DISPNEW)
         DC    A(PRIMRY)
         DC    A(SECNRY)
         DC    A(TRACKS+X'80000000')
*
RB99PRNT DC    A(*+4+X'80000000')
         DC    X'14010000'
         DC    F'0'
         DC    A(INTEXT5+X'80000000')
         DC    F'0'
         DC    F'0'
INTEXT5  DC    A(INDDPRNT)
         DC    A(SYSOUT+X'80000000')
*
INDDNAME DC    X'0001',H'1',H'6'
INDD     DC    CL8'SYSUT1'
INDSNAME DC    X'0002',H'1',H'44'
INDS     DC    CL44' '
INVOLSER DC    X'0010',H'1',H'6'
INV      DC    CL6' '
INPASS   DC    X'0050',H'1',H'8'
INP      DC    CL8' '
PRIMRY   DC    X'000A',H'1',H'3',X'00000A',X'00'
SECNRY   DC    X'000B',H'1',H'3',X'00000A',X'00'
CYLS     DC    X'0008',H'0'
TRACKS   DC    X'0007',H'0'
KEYLEN   DC    X'0040',H'1',H'1',X'0800'
SYSOUT   DC    X'0018',H'1',H'1',C'Z '
DISPNEW  DC    X'0004',H'1',H'1',X'0400'
DISPSHR  DC    X'0004',H'1',H'1',X'0800'
DISPKEEP DC    X'0005',H'1',H'1',X'0800'
UNCOND   DC    X'0007',H'0'
INDDUT3  DC    X'0001',H'1',H'6',C'SYSUT3'
INDDUT4  DC    X'0001',H'1',H'6',C'SYSUT4'
INDDINP  DC    X'0001',H'1',H'5',C'SYSIN '
INDDPRNT DC    X'0001',H'1',H'8',C'SYSPRINT'
         DS    0F
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(PM),                       X
               LRECL=80,BLKSIZE=80,RECFM=F
         EJECT
MYSAVE   DSECT
         DS    18F
         END
/*
//LKED1    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,REUS,TERM),
//             COND=(2,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LINKLIB,UNIT=SYSDA,VOL=SER=MVSTGT
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  SETCODE AC(1)
     NAME ETPSCOMP(R)
/*
//LKED2    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,REUS,TERM),
//             COND=(2,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD   DDNAME=SYSIN
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LINKLIB,UNIT=SYSDA,VOL=SER=MVSYSA
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  SETCODE AC(1)
     NAME ETPSCOMP(R)
/*
