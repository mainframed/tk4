//ASQCCASM  JOB (X0041,QCC,ASQCC),'X-COOK BRIAN',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=99,SYSAFF=MVSB
//*
//*  TECH.XA.SOURCE(QSAMPUT)
//*
//ASM1    EXEC PGM=IEV90,REGION=1024K,
//        PARM='OBJECT,NODECK,TERM,XREF(SHORT),BATCH,RENT'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS2.MACLIB,DISP=SHR  <== $AMODE
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
QSAMPUT  TITLE 'CHANGE LOG'
QSAMPUT  CSECT
QSAMPUT  AMODE ANY
QSAMPUT  RMODE 24
***********************************************************************
*                                                                     *
* CHANGE LOG                                                          *
*                                                                     *
*  9/30/86 BRIAN COOK    IF INVOKED IN 24-BIT MODE, RETURN IN 24-BIT. *
*                                                                     *
*  9/29/86 BRIAN COOK    INITIAL ENTRY.                               *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
           TITLE 'LINK-ABLE QSAM PUT MODULE FOR 31-BIT PROGRAMS'
***********************************************************************
*                                                                     *
*                                                                     *
*                   QSAM PUT PROGRAM                                  *
*                                                                     *
*                                                                     *
* ABSTRACT: ISSUE QSAM PUT MACRO ON BEHALF OF 31-BIT PROGRAMS.        *
*                                                                     *
*                                                                     *
* FUNCTION: THIS IS A RE-ENTRANT PROGRAM THAT CAN BE DYNAMICALLY      *
*           "LINK"ED TO BY A 31-BIT PROGRAM TO PERFORM A QSAM "PUT".  *
*                                                                     *
*           NOTE THAT BOTH THE DCB AND THE RECORD PASSED MUST BE IN   *
*           24-BIT MEMORY.                                            *
*                                                                     *
*                                                                     *
*                                                                     *
* LINKAGE:  STANDARD PARAMETER LIST FORMAT, POINTED TO BY REG 1:      *
*                                                                     *
*           FULL-WORD 1 ==>  ADDRESS OF DCB                           *
*           FULL-WORD 2 ==>  ADDRESS OF RECORD                        *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'ENTRY POINT LOGIC'
         PRINT GEN
         USING *,12
*
         SAVE  (14,12),,QSAMPUT_09/29/86_BRIAN_COOK_&SYSTIME_&SYSDATE
*
         LR    R12,R15        SET BASE 1
*
         LR    R9,R1          SAVE PARM REGISTER
         LM    R10,R11,0(R1)  SET R10=DCB, R11=RECORD
         USING IHADCB,10
*
         GETMAIN R,LV=4096,LOC=(BELOW)
*
         ST    R1,8(R13)      LINK SAVE AREAS
         ST    R13,4(R1)      SAVE THE CALLER'S R13
         LR    R13,R1         SET SAVE AREA
         USING WORKAREA,13
*
         $AMODE 24
*
         SR    R8,R8
         IC    R8,0(,R13)          PICK UP AMODE INDICATOR
         SRL   R8,7                LEAVE 31-BIT INDICATOR
*
         CLM   R11,15,FOXES        "CLOSE" REQUEST?
         BNE   TSTOPEN             NO, TEST FOR DCB OPEN
*
         TM    DCBOFLGS,DCBOFOPN   DCB OPEN?
         BO    DOCLOSE             YES, CLOSE IT
*
         SR    R15,R15             JUST SET RETURN CODE ZERO
         B     MODE31
*
DOCLOSE  DS    0H
*
         MVC   OPENMFE(OPENMFLN),OPENMFL
         CLOSE ((10)),MF=(E,OPENMFE)
*
         LR    R5,R15              SAVE RETURN CODE FROM CLOSE
*
         B     MODE31
*
TSTOPEN  DS    0H
*
         TM    DCBOFLGS,DCBOFOPN   DCB OPEN?
         BO    OPENED              YES, TEST MACRF
*
OPENOUT  DS    0H
*
         MVC   OPENMFE(OPENMFLN),OPENMFL
         OPEN  ((10),(OUTPUT)),MF=(E,OPENMFE)
*
         TM    DCBOFLGS,DCBOFOPN   DCB OPEN?
         BO    OPENED              YES, TEST MACRF
*
         LR    R5,R15              SAVE RETURN CODE FROM OPEN
*
         B     MODE31
*
OPENED   DS    0H
*
         TM    DCBMACR2,DCBMRLCP   LOCATE MODE?
         BO    PUTLOCAT            YES, ISSUE PUT-LOCATE
*
PUTMOVE  DS    0H
*
         PUT   (10),(11)
*
         LR    R5,R15         SAVE RETURN CODE
*
         B     MODE31
*
PUTLOCAT DS    0H
*
         PUT   (10)
         LA    R1,0(,R1)      CLEAR LEFT BYTE
         ST    R1,4(,R9)      SET BUFFER ADDRESS
*
         LR    R5,R15         SAVE RETURN CODE
*
MODE31   DS    0H
*
         LTR   R8,R8          ENTERED IN 31-BIT MODE?
         BZ    RETURNRC       NO
*
         $AMODE 31
*
RETURNRC DS    0H
*
         L     R3,4(,R13)     RESTORE THE CALLER'S R13
         FREEMAIN R,LV=4096,A=(13)
*
         LR    R13,R3
         LR    R15,R5
         LM    R0,R12,20(R13)
         L     R14,12(,R13)
         BR    R14
*
         TITLE 'CONSTANTS'
         DS    0F
OPENMFL  OPEN  (OPENMFL,(OUTPUT)),MF=L
OPENMFLN EQU   *-OPENMFL
FOXES    DC    X'FFFFFFFF'
         LTORG
         CNOP  0,8
         TITLE 'GETMAINED AREA'
WORKAREA DSECT
         DS    18F
*
         DS    0F
OPENMFE  DS    XL(OPENMFLN)
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
         TITLE 'DCB DSECT'
         DCBD  DSORG=PS,DEVD=DA
         END
/*
//LKED1    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,TERM,RENT,REUS),
//            COND=(2,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DISP=SHR,DSN=ISP.V2R2M0.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=SYS2.LINKLIB
//*YSLMOD  DD   DISP=SHR,DSN=ASQCC.TEST.STEPLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
    MODE AMODE(ANY),RMODE(24)
    NAME QSAMPUT(R)
/*
