EMPF     TITLE 'DATASET CONTENTION MPF EXIT'
*---------------------------------------------------------------------*
*---     THIS PROGRAM IS A MPF EXIT FOR MVS, DESIGNED TO BE        ---*
*---     INVOKED WHENEVER MSG IEF099I IS ISSUED BY THE OPERATING   ---*
*---     SYSTEM.  IT WILL CREATE A SERIES OF MESSAGES, ON CONSOLES ---*
*---     AND SYSLOG, DESCRIBING THE NATURE OF THE CONTENTION.      ---*
*---                                                               ---*
*---     ATTRIBUTES: RMODE-ANY, AMODE-31, REENTRANT, REUSABLE      ---*
*---           AND REFRESHABLE.                                    ---*
*---                                                               ---*
*---     TO INVOKED THIS EXIT, LINK IT INTO A LNKLST LIBRARY,      ---*
*---           REFRESH LLA, AND INSERT THE FOLLOWING LINE INTO     ---*
*---           YOUR MPF LIST:                                      ---*
*---                                                               ---*
*---   IEF099I,USEREXIT(ENQMPF)                                    ---*
*---                                                               ---*
*---     RICK FOCHTMAN, SR. SYSTEMS PROGRAMMING SPECIALIST         ---*
*---     BOARD OF TRADE CLEARING CORP.                             ---*
*---     141 W. JACKSON BLVD, SUITE 1460                           ---*
*---     CHICAGO, IL  60604                                        ---*
*---     (312) 786-5772                                            ---*
*---                                                               ---*
*---     SEPTEMBER, 1998                                           ---*
*---------------------------------------------------------------------*
         SPACE 3
ENQMPF   CSECT
ENQMPF   AMODE 31
ENQMPF   RMODE ANY
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         B     12(,R15)            BRANCH AROUND
         DC    AL1(7),CL7'ENQMPF'  CSECT IDENTIFIER
         STM   R14,R12,12(R13)     SAVE ENTRY REGISTERS
         LR    R12,R15             COPY ENTRY ADDRESS
         USING ENQMPF,R12          AND DECLARE THE BASE REGISTER
         L     R3,WALEN            LOAD WORK AREA LENGTH
         GETMAIN RU,LV=(3)         GRAB W.A. STORAGE
         LR    R2,R1               COPY STORAGE ADDRESS
         LA    R4,0                CLEAR SOURCE ADDRESS
         LA    R5,0                  AND LENGTH
         MVCL  R2,R4               CLEAR THE WORKING STORAGE
         ST    R13,4(,R1)          SAVE BACK S.A. POINTER
         ST    R1,8(,R13)          AND FORWARD POINTER
         LR    R13,R1              AND BUMP DOWN THE CHAIN
         USING WORK,R13            AND BASE ITS DSECT
         LA    R2,LINE2            LOAD MODEL ADDRESS
         LA    R3,ENDTXT           POINT TO MODEL END AREA
         SR    R3,R2               COMPUTE LENGTH TO MOVE
         LA    R4,NOTE2            POINT TO DYN. AREA
         LR    R5,R3               COPY LENGTH
         MVCL  R4,R2               COPY MODELS TO DYN AREA
*---------------------------------------------------------------------*
*---     1. GRAB STORAGE FOR GQSCAN RETURN AREA AND BASE ITS       ---*
*---        DSECT(S)                                               ---*
*---------------------------------------------------------------------*
         L     R2,GQSCSIZE         LOAD SIZE
         GETMAIN RU,LV=(2),SP=241,BNDRY=PAGE
         LR    R4,R1               COPY STORAGE ADDRESS
         ST    R1,GQAREA           AND SAVE FOR LATER
         LR    R5,R2               COPY AREA SIZE
         LA    R6,0                SOURCE ADDRESS OF ZERO
         LA    R7,0                AND LENGTH OF ZERO
         MVCL  R4,R6               CLEAR IT OUT...
         LR    R11,R1              COPY AREA POINTER FOR LATER
*---------------------------------------------------------------------*
*---     5. ISSUE GQSCAN FOR SYSDSN CONTENTION                     ---*
*---------------------------------------------------------------------*
         MVC   SCANB(SCANLEN),SCANA COPY GQSCAN PARMS
         L     R3,GQSCSIZE         LOAD AREA SIZE
         GQSCAN AREA=((R11),(R3)),SCOPE=ALL,REQLIM=MAX,                X
               WAITCNT=1,XSYS=NO,RESNAME=QNAME,MF=(E,SCANB)
         ST    R1,RIBSLEFT         SAVE RIB COUNT
         CH    R15,=H'4'           ANY CONTENTION ??
         BL    PARSEIT             SOMEONE IS WAITING; CHECK IT OUT
*---------------------------------------------------------------------*
*---     NOTHING TO DO; BAIL OUT QUIETLY.                          ---*
*---------------------------------------------------------------------*
         SPACE 3
ENDITALL DS    0H
         L     R2,GQSCSIZE         LOAD SIZE
         FREEMAIN RU,LV=(2),A=(11),SP=241 RELEASE GQSCAN AREA
         LR    R1,R13              COPY W.A. POINTER
         L     R13,4(,R13)
         L     R0,WALEN            LOAD WORK AREA LENGTH
         FREEMAIN RU,LV=(0),A=(1)  AND RELEASE SAVE AREA
         LM    R14,R12,12(R13)
         LA    R15,0
         BR    R14
*---------------------------------------------------------------------*
*---     PARSE THE RIB RETURNED BY GQSCAN                          ---*
*---------------------------------------------------------------------*
PARSEIT  DS    0H
         LR    R10,R11             COPY AREA POINTER FOR LATER
         USING RIB,R10             AND BASE ITS DSECT
NEWRIB   DS    0H
         ST    R1,RIBSLEFT         SAVE RIB COUNT
         LA    R3,RIBEND           POINT TO START OF RIBVAR
         AH    R3,RIBVLEN          ADD VARIABLE AREA LENGTH
         USING RIBE,R3             AND BASE ANOTHER DSECT
         L     R4,RIBTRIBE         LOAD RIBE COUNT
         LA    R5,OWNERS           POINT TO OWNERS' LIST
         LA    R6,WAITERS          POINT TO WAITERS' LIST
         LA    R7,0                NO OWNERS YET
         LA    R8,0                NO WAITERS YET
FRSTLOOP DS    0H
*---
*---     FIND THE OWNERS AND WAITERS FIRST
*---
         TM    RIBESFLG,RIBESTAT   WAITING FOR RESOURCE ??
         BZ    WAIT1               YES ---
         MVC   0(16,R5),RIBEJBNM   COPY JOB/SYSTEM NAMES
         MVC   16(1,R5),RIBERFLG   COPY REQUEST FLAGS
         LA    R5,17(,R5)          POINT TO NEXT SLOT
         LA    R7,1(,R7)           INCREMENT COUNTER BY ONE
         B     LOOP1               AND CONTINUE
WAIT1    DS    0H
         MVC   0(16,R6),RIBEJBNM   COPY JOB/SYSTEM NAMES
         MVC   16(1,R6),RIBERFLG   COPY REQUEST FLAGS
         LA    R6,17(,R6)          POINT TO NEXT SLOT
         LA    R8,1(,R8)           INCREMENT COUNTER BY ONE
LOOP1    DS    0H
         LA    R3,RIBEEND          POINT TO NEXT RIBE
         BCT   R4,FRSTLOOP         AND CONTINUE ---
*---
*--- START BUILDING/SPITING MESSAGES
*---
*--- USE PRINTED MESSAGES FOR DEBUGGING
*---
         LA    R15,RIBEND          POINT TO RIB VARIABLE AREA
         USING RIBVAR,R15
         LH    R1,RIBVLEN          LOAD VARIABLE AREA LENGTH
         BCTR  R1,R0
         MVC   DSNAME,BLANKS
         EX    R1,RIBM             MOVE DSNAME TO MESSAGE
         B     RIBM+6
RIBM     MVC   DSNAME(1),RIBRNAME COPY THE DSNAME
         DROP  R15
         LA    R0,0                CLEAR MCS CONSOLE ID
         MVC   WTOLIST(WTOLEN),WTOMDL COPY BASIC PARM LIST
         WTO   TEXT=LINE1,MF=(E,WTOLIST)
         LA    R0,0                CLEAR MCS CONSOLE ID
         MVC   WTOLIST(WTOLEN),WTOMDL COPY BASIC PARM LIST
         WTO   TEXT=NOTE2,MF=(E,WTOLIST)
         LA    R5,OWNERS           POINT TO LIST OF OWNERS
MSG1     DS    0H
         MVC   JOBNAME1,0(R5)      COPY THE JOB NAME
         MVC   SYSTEM1,8(R5)       AND SYSTEM ID
         MVC   USE1,=CL3'EXC'      ASSUME SHARED FOR NOW
         TM    16(R5),X'80'        EXCLUSIVE USE ??
         BNO   MSG2                NOPE ---
         MVC   USE1,=CL3'SHR'      YES, MARK IT
MSG2     DS    0H
         LA    R0,0                CLEAR MCS CONSOLE ID
         MVC   WTOLIST(WTOLEN),WTOMDL COPY BASIC PARM LIST
         WTO   TEXT=NOTE3,MF=(E,WTOLIST)
         LA    R5,17(,R5)          POINT TO NEXT ENTRY
         BCT   R7,MSG1             AND CONTINUE ---
         LA    R5,WAITERS          POINT TO LIST OF OWNERS
MSG3     DS    0H
         MVC   JOBNAME2,0(R5)      COPY THE JOB NAME
         MVC   SYSTEM2,8(R5)       AND SYSTEM ID
         MVC   USE2,=CL3'EXC'      ASSUME SHARED FOR NOW
         TM    16(R5),X'80'        EXCLUSIVE USE ??
         BNO   MSG4                NOPE ---
         MVC   USE2,=CL3'SHR'      YES, MARK IT
MSG4     DS    0H
         LA    R0,0                CLEAR MCS CONSOLE ID
         MVC   WTOLIST(WTOLEN),WTOMDL COPY BASIC PARM LIST
         WTO   TEXT=NOTE4,MF=(E,WTOLIST)
         LA    R5,17(,R5)          POINT TO NEXT ENTRY
         BCT   R8,MSG3             AND CONTINUE ---
         LA    R0,0                CLEAR MCS CONSOLE ID
         LR    R10,R3              POINT TO NEW RIB
         L     R1,RIBSLEFT         LOAD REMAINDER COUNT
         BCT   R1,NEWRIB           AND LOOP, LOVINGLY ---
         B     ENDITALL
         DROP  R10
         SPACE 3
*
SCANA    GQSCAN MF=L
SCANLEN  EQU   *-SCANA
WTOMDL   WTO   TEXT=,MF=L
WTOLEN   EQU   *-WTOMDL
*
QNAME    DC    CL8'SYSDSN'
BLANKS   DC    CL44' '
*
LINE1    DC    AL2(LINE2-TXT1)
TXT1     DC    C' ENQMPF01 : DATASET CONTENTION DETECTED'
*
LINE2    DC    AL2(LINE3-TXT2)
TXT2     DC    C' ENQMPF02 : DSNAME='
         DC    CL44' '
*
LINE3    DC    AL2(LINE4-TXT3)
TXT3     DC    C' ENQMPF03: JOB '
         DC    CL08' '
         DC    C' ON SYSTEM '
         DC    CL08' '
         DC    C' HAS IT '
         DC    CL03' '
*
LINE4    DC    AL2(ENDTXT-TXT4)
TXT4     DC    C' ENQMPF04: JOB '
         DC    CL08' '
         DC    C' ON SYSTEM '
         DC    CL08' '
         DC    C' WANTS IT '
         DC    CL03' '
ENDTXT   EQU   *
GQSCSIZE DC    F'1048576'          ONE MEGABYTE
POOL1    EQU   99                  SUBPOOL FOR GQSCAN RETURN AREA
POOL2    EQU   8                   SUBPOOL FOR MY CHAINS OF NAMES
         LTORG
WALEN    DC    A(ENDWORK-WORK)
WORK     DSECT
SA       DS    18F
RIBSLEFT DS    F
GQAREA   DS    A
WTOLIST  WTO   TEXT=,MF=L
*
SCANB    GQSCAN MF=L
*
NOTE2    DC    AL2(LINE3-TXT2)
         DC    C' ENQMPF02 : DSNAME='
DSNAME   DC    CL44' '
*
NOTE3    DC    AL2(LINE4-TXT3)
         DC    C' ENQMPF03: JOB '
JOBNAME1 DC    CL08' '
         DC    C' ON SYSTEM '
SYSTEM1  DC    CL08' '
         DC    C' HAS IT '
USE1     DC    CL03' '
*
NOTE4    DC    AL2(ENDTXT-TXT4)
         DC    C' ENQMPF04: JOB '
JOBNAME2 DC    CL08' '
         DC    C' ON SYSTEM '
SYSTEM2  DC    CL08' '
         DC    C' WANTS IT '
USE2     DC    CL03' '
WAITERS  DS    100CL17
OWNERS   DS    100CL17
ENDWORK  DS    0D
         ISGRIB
         END
