TRACE    TITLE 'TRACE390 Interface Stub'
R00      EQU   0
R01      EQU   1
R02      EQU   2
R03      EQU   3
R04      EQU   4
R05      EQU   5
R06      EQU   6
R07      EQU   7
R08      EQU   8
R09      EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         SYSSTATE ASCENV=P
&AsmDate SETC  '&SYSDATC'(1,4).'/'.'&SYSDATC'(5,2).'/'.'&SYSDATC'(7,2)
         SPACE
*---------------------------------------------------------------------*
*        Entry Logic                                                  *
*---------------------------------------------------------------------*
         SPACE
TRACE    CSECT ,
TRACE    AMODE 31
TRACE    RMODE ANY
         USING TRACE,R12
         J     AA010                   Branch around ID string
         DC    AL1(AA010-*-1)          ID string length
         DC    CL9'TRACE'              Program id
         DC    CL12'v03.r01.m00'        - Version, Release & Mod. level
         DC    CL9'HLA&SYSVER'          - HL Assembler version
         DC    CL11'&AsmDate'           - Assembly date
         DC    CL5'&SYSTIME'            - Assembly time
         SPACE
*---------------------------------------------------------------------*
*        Load module TRACE390 if first time through                   *
*---------------------------------------------------------------------*
         SPACE
AA010    DS    0H
         STM   R14,R12,12(R13)         Save callers registers
         LR    R12,R15                 Load our program base
         ICM   R00,B'1111',aTRACE      Q. Trace routine loaded yet?
         JNZ   AA020                   Y. Branch to it then
         LOAD  EPLOC=TRACE390          N. Load trace module
         ST    R00,aTRACE                 Save entry point address
         SPACE
AA020    DS    0H
         LR    R15,R00                 Load branch register
         L     R14,12(,R13)            Load return address
         LM    R00,R12,20(R13)         Restore callers registers
         BR    R15                     Invoke TRACE390
         SPACE 3
aTRACE   DC    A(0)                    Entry point address of TRACE390
TRACE390 DC    CL8'TRACE390'           Name of real trace routine
         SPACE 2
         LTORG ,
         SPACE 3
         END   TRACE
         PUNCH ' SETOPT PARM(REUS=SERIAL,CALL)'
         PUNCH ' NAME TRACE(R)'
         END
