./ ADD NAME=$$INDEX  0100-00063-00063-1618-00023-00023-00000-INDEX
$$README - Documentation
$$INDEX  - This member
$ADDTO   - JCL to ASSEMBLE/LINK
$CATL    - JCL to ASSEMBLE/LINK
$LDS     - JCL to ASSEMBLE/LINK
$PRU     - JCL to ASSEMBLE/LINK
$RETCODE - JCL to ASSEMBLE/LINK
$STEPLIB - JCL to ASSEMBLE/LINK
$USERINF - JCL to ASSEMBLE/LINK
$WHOSGOT - JCL to ASSEMBLE/LINK

ADDTO    - Assembler source
CATL     - Assembler source
HEXDUMP  - Assembler source - general HEX display/print routine
LDS      - Assembler source
PACKMAP  - Assembler source
PRU      - Assembler source
PRUREXX  - REXX to invoke PRU
RETCODE  - Assembler source
STEPLIB  - Assembler source
TSOPARSE - Assembler source - general IKJSCAN/IKJPARS routine
USERINFO - Assembler source
WHOSGOT  - Assembler source
./ ADD NAME=$$README 0100-00063-00063-1614-00094-00094-00000-DOC

(C)  COPYRIGHT 1999, 2000 MVS-JES2@Home.com    All Rights Reserved.

Licensed material-program, Property of MVS-JES2@Home.com
All Rights Reserved by MVS-JES2@Home.com

As with the CBT source rules, this code may be freely distributed.
However I retain ownership of this code, thus it may not be used,
fully or in part, in a commercial product or sold in any way.

The files included are all in TSO TRANSMIT format (XMIT).
They must be transferred to your mainframe using BINARY FB80 format.

After the files have been uploaded, they can be expanded back into their
original PDS format using the following commands:
 RECEIVE INDATASET(HELP.XMI)
 RECEIVE INDATASET(LOADLIB.XMI)
 RECEIVE INDATASET(MACLIB.XMI)
 RECEIVE INDATASET(SOURCE.XMI)

The HELP file contains TSO help members for the various command
processors.
The LOADLIB file contains executable formats of all of the tools.
The MACLIB file contains various macros required for assembly of the
tools.
The SOURCE file contains the tool source as well as JCL to assemble and
LINK.

If any updates are made at your installation, I would like to hear about
them.  Your changes may be useful to other shops.  I will update my
master copy with your changes so that they are included in future
releases.  If I am not made aware of these changes, you may have to
re-do your updates.

If any modifications are required, and you do not feel comfortable
updating the source, send an email to me, and I'll be glad to see what I
can do.

Lastly, if you need assistance with any of this code, feel free to email
me.

As this is a pre-release of what will be provided on the CBT tape/site,
I would like to hear of any installation or documentation problems you
encounter.  Please email the address below with any comments.

Thank you,
Dan
MVS-JES2@Home.com

Installation Instructions:

Most of these tools require no special installation instructions.
Simply assemble and linkedit them into a library of your choice.  The
LOADLIB.XMI file is provided to allow you to try them without even
performing this step.

ADDTO, CATL, LDS, USERINFO and WHOSGOT are all command processors that
must reside in either your STEPLIB, ISPLLIB or a LINKLISTED library.
STEPLIB must reside in an authorized LINKLISTED library and the IKJTSOxx
member of SYS1.PARMLIB must be updated to include STEPLI B in the
AUTHCMD section.  PACKMAP, RETCODE and PRU may be run from any library.
PACKMAP will enqueue on the volume's VTOC while it is being read if it
is authorized.  RETCODE will not issue appropriate error messages for
invalid PARM= if it is not authorized.

TSO HELP members are provided for the various command processors.  A
REXX to invoke PRU has also been provided by one of the users.  It has
been provided with his permission.  Thank you Ortega.

Sample Batch JCL:

//PACKMAP   JOB   ...jobcard...
//PACKMAP   EXEC  PGM=PACKMAP,REGION=4096K,TIME=1440
//STEPLIB   DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR
//DISK      DD    UNIT=SYSALLDA,VOL=SER=volser,DISP=SHR
//SYSPRINT  DD    SYSOUT=*

//PRU       JOB   ...jobcard...
//PRU       EXEC  PGM=PRU,REGION=4096K,TIME=1440
//*         PARM=ALL   <--- COPY "LIVE" MEMBERS AS WELL
//STEPLIB   DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR
//SYSPRINT  DD    SYSOUT=*
//INPUT     DD    DSN=original.PDS.file,DISP=SHR
//OUTPUT    DD    DSN=new.PDS.file,DISP=(,CATLG),
//          UNIT=SYSDA,SPACE=(CYL,(30,10,90))

//RETCODE   JOB   ...jobcard...
//JOBLIB    DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR
//RC00      EXEC  PGM=RETCODE,PARM='RC(0)'
//RC04      EXEC  PGM=RETCODE,PARM='RC(4)'
//RC08      EXEC  PGM=RETCODE,PARM='RC(8)'
//RC12      EXEC  PGM=RETCODE,PARM='RC(12)'
//USER99    EXEC  PGM=RETCODE,PARM='USER(99)'
//SYSTEM99  EXEC  PGM=RETCODE,PARM='SYSTEM(99)',COND=EVEN
./ ADD NAME=$ADDTO   0100-00063-00063-1614-00029-00029-00000-JCL
//ADDTO    JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*------------------------------------------------------------------*
//*
//ASM      EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(ADDTO),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN   DD    DSN=userid.MVS-JES2.SOURCE(ADDTO),DISP=SHR <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(ADDTO)
  ENTRY    ADDTO
  NAME     ADDTO(R)
/*
./ ADD NAME=$CATL    0100-00063-00063-1614-00029-00029-00000-JCL
//CATL     JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*------------------------------------------------------------------*
//*
//ASM      EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(CATL),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN    DD    DSN=userid.MVS-JES2.SOURCE(CATL),DISP=SHR <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(CATL)
  ENTRY    CATL
  NAME     CATL(R)
/*
./ ADD NAME=$LDS     0100-00063-00063-1614-00035-00035-00000-JCL
//LDS      JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*                                                                  *
//*        IF YOU DO NOT HAVE "LIBRARIAN" SIMPLY REMOVE THE //LIBDSN *
//*        DD STATEMENT AND THE "INCLUDE LIBDSN" IN LINKEDIT STEP.   *
//*                                                                  *
//*------------------------------------------------------------------*
//*
//ASM      EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(LDS),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN    DD    DSN=userid.MVS-JES2.SOURCE(LDS),DISP=SHR  <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//LIBDSN   DD    DSN=librarian.load/obj.dsname,DISP=SHR    <--- change
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(LDS)
  INCLUDE  LIBDSN(FAIROPN,FAIRCLS,FARIMOD)
  ENTRY    LDS
  NAME     LDS(R)
/*
./ ADD NAME=$PACKMAP 0100-00063-00063-1614-00046-00046-00000-JCL
//PACKMAP  JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*                                                                  *
//*        IF PACKMAP IS NOT LINK'D AUTHORIZED, OR DOES NOT RESIDE   *
//*        IN AN AUTHORIZED LIBRARY, IT WILL STILL WORK.  IT JUST    *
//*        WON'T ENQ ON THE VTOC WHEN IT'S BEING READ.               *
//*                                                                  *
//*------------------------------------------------------------------*
//*
//ASM1     EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(PACKMAP),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN DD    DSN=userid.MVS-JES2.SOURCE(PACKMAP),DISP=SHR <--- change
//ASM2     EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(HEXDUMP),DISP=(OLD,PASS)
//SYSIN DD    DSN=userid.MVS-JES2.SOURCE(HEXDUMP),DISP=SHR <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(PACKMAP)
  INCLUDE  SYSPUNCH(HEXDUMP)
  SETCODE  AC(1)
  ENTRY    PACKMAP
  NAME     PACKMAP(R)
/*
./ ADD NAME=$PRU     0100-00063-00063-1614-00029-00029-00000-JCL
//PRU      JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*------------------------------------------------------------------*
//*
//ASM      EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(PRU),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN    DD    DSN=userid.MVS-JES2.SOURCE(PRU),DISP=SHR  <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(PRU)
  ENTRY    PRU
  NAME     PRU(R)
/*
./ ADD NAME=$RETCODE 0100-00063-00063-1614-00047-00047-00000-JCL
//RETCODE  JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*                                                                  *
//*        IF RETCODE IS NOT LINK'D AUTHORIZED, OR DOES NOT RESIDE   *
//*        IN AN AUTHORIZED LIBRARY, IT WILL STILL WORK.  IT JUST    *
//*        WON'T DISPLAY "PARSE" ERROR MESSAGES WHEN AN INVALID      *
//*        PARM= IS SPECIFIED.                                       *
//*                                                                  *
//*------------------------------------------------------------------*
//*
//ASM1     EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(RETCODE),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN DD    DSN=userid.MVS-JES2.SOURCE(RETCODE),DISP=SHR <--- change
//ASM2     EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(TSOPARSE),DISP=(OLD,PASS)
//SYSIN DD   DSN=userid.MVS-JES2.SOURCE(TSOPARSE),DISP=SHR <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(RETCODE)
  INCLUDE  SYSPUNCH(TSOPARSE)
  SETCODE  AC(1)
  ENTRY    RETCODE
  NAME     RETCODE(R)
/*
./ ADD NAME=$STEPLIB 0100-00063-00063-1614-00035-00035-00000-JCL
//STEPLIB  JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*                                                                  *
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*                                                                  *
//*        STEPLIB "MUST" RESIDE IN A LINKLISTED LIBRARY.            *
//*        FOLLOW THE "READ.ME" INSTRUCTIONS FOR FURTHER DETAILS.    *
//*                                                                  *
//*------------------------------------------------------------------*
//*
//ASM      EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,RENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(STEPLIB),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN DD    DSN=userid.MVS-JES2.SOURCE(STEPLIB),DISP=SHR <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,RENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(STEPLIB)
  SETCODE  AC(1)
  ENTRY    STEPLIB
  NAME     STEPLIB(R)
/*
./ ADD NAME=$USERINF 0100-00063-00063-1614-00029-00029-00000-JCL
//USERINFO JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*------------------------------------------------------------------*
//*
//ASM      EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(USERINFO),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN DD   DSN=userid.MVS-JES2.SOURCE(USERINFO),DISP=SHR <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(USERINFO)
  ENTRY    USERINFO
  NAME     USERINFO(R)
/*
./ ADD NAME=$WHOSGOT 0100-00063-00063-1614-00029-00029-00000-JCL
//WHOSGOT  JOB   ...JOBCARD...
//*------------------------------------------------------------------*
//*        MAKE SURE YOU CHANGE "userid" TO AN APPROPRIATE VALUE.    *
//*------------------------------------------------------------------*
//*
//ASM      EXEC  PGM=ASMA90,REGION=4096K,TIME=1440,
//         PARM='NOOBJECT,DECK,XREF(SHORT),TERM,NOUSING,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSTERM  DD    SYSOUT=*
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(10,10))
//SYSLIB   DD    DSN=userid.MVS-JES2.MACLIB,DISP=SHR       <--- change
//         DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD    DSN=&&OBJ(WHOSGOT),DISP=(,PASS),
//         UNIT=SYSDA,SPACE=(CYL,(1,1,45)),
//         DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=3120)
//SYSIN DD    DSN=userid.MVS-JES2.SOURCE(WHOSGOT),DISP=SHR <--- change
//*
//LINKEDIT EXEC  PGM=IEWL,REGION=4096K,TIME=1440,COND=(0,NE),
//         PARM='NCAL,LET,LIST,XREF,NORENT'
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=userid.MVS-JES2.LOADLIB,DISP=SHR      <--- change
//SYSPUNCH DD    DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN   DD    *
  INCLUDE  SYSPUNCH(WHOSGOT)
  ENTRY    WHOSGOT
  NAME     WHOSGOT(R)
/*
./ ADD NAME=ADDTO    0100-00040-00040-1222-00360-00360-00000-SOURCE
ADDTO    TITLE '- ADDTO - ADD DATASETS TO CONCATENATION'
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
ADDTO    #START ,                  START PROCEDURE                     *
               AMODE=CAP31,        EXECUTE IN 31-BIT AMODE             *
               APARS=,             NO APAR SLOTS REQUIRED              *
               BASE=(R12),         DEFINE BASE REGISTER(S)             *
               LOC=BELOW,          INDICATE 24-BIT WORKAREA            *
               REG0=R0,            SAVE INPUT PARAMETER ADDRESS        *
               REG1=R8,            SAVE INPUT PARAMETER ADDRESS        *
               USING=(CPPL,R8),    ADDRESS THE PARAMETER LIST          *
               RMODE=24,           EXECUTE IN 24-BIT RMODE             *
               SPLEVEL=2,          USE XA/ESA MACRO EXPANSIONS         *
               WKDSECT=ADDTWRK,    DEFINE WORKAREA NAME                *
               LEVEL=#V001R01,     MODIFICATION LEVEL          #DD99182*
               COPYYR=1999,        COPYRIGHT YEAR                      *
               COPY=YES            GENERATE COPYRIGHT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        INITIALIZATION THE WORK AREA                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   WRKDDNL@(2),=AL2(DCCDDNAM)
         MVC   WRKDDNL@+2(2),=AL2(2)
         MVC   WRKDDN1,=AL2(8)     SET 1ST DDNAME LENGTH
         MVC   WRKDDN1+2(8),BLANKS
         MVC   WRKDDN2,=AL2(8)     SET 2ND DDNAME LENGTH
         MVC   WRKDDN2+2(8),BLANKS
         LA    R0,WRKDDN
         ST    R0,WRKDDN@          SAVE DDNAME ADDRESS
         LA    R1,L'WRKDDN
         STH   R1,WRKDDN@+4        SAVE DDNAME LENGTH
         LA    R0,WRKDSN
         ST    R0,WRKDSN@          SAVE DSNAME ADDRESS
         LA    R1,L'WRKDSN
         STH   R1,WRKDSN@+4        SAVE DSNAME LENGTH
         LA    R0,WRKVOL
         ST    R0,WRKVOL@          SAVE VOLSER ADDRESS
         LA    R1,L'WRKVOL
         STH   R1,WRKVOL@+4        SAVE VOLSER LENGTH
         MVC   WRKPERM(2),=AL2(DCCPERMC)
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PARSE THE INPUT COMMAND BUFFER (DDNAME/NEW DSNAME LIST)      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         LA    R1,WRKPPL           A(PARSE PARAMETER LIST)
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT      COPY UPT ADDRESS
         MVC   PPLECT,CPPLECT      COPY ECT ADDRESS
         MVC   PPLCBUF,CPPLCBUF    COPY COMMAND BUFFER ADDRESS
         LA    R0,WRKECB
         ST    R0,PPLECB           SAVE ECB ADDRESS
         MVC   PPLPCL,=A(ADDPCL)   COPY PCL ADDRESS
         LA    R0,WRKANS
         ST    R0,PPLANS           SAVE PDL RETURN ADDRESS
         DROP  R1
         CALLTSSR EP=IKJPARS       INVOKE PARSE ROUTINE
         LTR   R15,R15             CHECK PARSE RETURN CODE
         BNZ   ADDTERR1            B. IF PARSE FAILED
         SPACE 1
         L     R7,WRKANS           A(PDL)
         USING IKJPARMD,R7
         SPACE 1
         CLI   KWCREATE+1,1        CHECK FOR "CREATE" KEYWORD  #DD99182
         BNE   ADDT0100            B. IF NOT                   #DD99182
         CLI   KWREMOVE+1,1        CHECK FOR "REMOVE" KEYWORD  #DD99182
         BE    ADDTERR4            B. IF YES (MUT-EXCL)        #DD99182
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        GET THE CURRENT DSNAME INFORMATION                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ADDT0100 DS    0H                                              #DD99182
         L     R1,DDNAME           A(SPECIFIED DDNAME)
         LH    R2,DDNAME+4         L(SPECIFIED DDNAME)
         MVC   WRKDDN,BLANKS
         #EXEC -R2,MVC,WRKDDN(*-*),0(R1)
         MVC   DCBDDNAM-IHADCB+SYSLIB,WRKDDN
         MVC   WRKDDN1+2(8),WRKDDN
         MVC   ERR1MSG1,WRKDDN
         SPACE 1
         LA    R2,DCBDDNAM-IHADCB+SYSLIB
         TIOTSCAN DDN=(R2),NOK=ADDTERR2
         SPACE 1
         OI    ARLOPT1,ARLLANY     RETURN INFO CAN BE ABOVE THE LINE
         RDJFCB SYSLIB             GET CURRENTLY ALLOCATED DSNAMES
         LTR   R15,R15             CHECK IF INFORMATION RETURNED
         BNZ   ADDTERR2            B. IF NOT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        FREE THE CURRENT DD STATEMENT                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         FREE  PERM,DDN=WRKDDN@    FREE THE CURRENT ALLOCATIONS
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ALLOCATE THE 1ST "NON-REMOVED" DATASET TO THE SPECIFIED DD   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         CLI   KWREMOVE+1,1        CHECK FOR "REMOVE" OPTION   #DD99182
         BNE   ADDT0400            B. IF NOT                   #DD99182
         SPACE 1
         L     R5,ARLAREA          A(1ST ELEMENT)              #DD99182
         LH    R6,ARLRTRVD         #(RETRUNED ELEMENTS)        #DD99182
ADDT0200 DS    0H                                              #DD99182
         LA    R15,4(,R5)          A(ELEMENT'S JFCB)           #DD99182
         USING JFCB,R15                                        #DD99182
         MVC   WRKDSN,JFCBDSNM     COPY THE DATASET NAME       #DD99182
         MVC   WRKVOL,JFCBVOLS                                 #DD99182
         DROP  R15                                             #DD99182
         BAL   R14,CHECKDSN        PROCESS THIS DSNAME         #DD99182
         LTR   R15,R15             CHECK IF TO BE "REMOVED"    #DD99182
         BZ    ADDT0300            B. IF NOT (ALLOCATE IT)     #DD99182
         SPACE 1
         LH    R15,0(,R5)          L(ELEMENT)                  #DD99182
         LA    R5,0(R15,R5)        A(NEXT ELEMENT)             #DD99182
         BCT   R6,ADDT0200         PROCESS THE ELEMENT LIST    #DD99182
         B     ADDT0900            B. IF ALL DSNAMES "REMOVED" #DD99182
         SPACE 1
ADDT0300 DS    0H                                              #DD99182
         ALLOC PERM,DDN=DDNAME,DISP=SHR,                       #DD99182*
               DSN=WRKDSN@,VOL=WRKVOL@,UNIT='SYSALLDA',        #DD99182*
               ERROR=ADDTERR3                                  #DD99182
         SPACE 1
         LH    R15,0(,R5)          L(ELEMENT)                  #DD99182
         LA    R5,0(R15,R5)        A(NEXT ELEMENT)             #DD99182
         CH    R6,=AL2(1)          CHECK FOR LAST ELEMENT      #DD99182
         BE    ADDT0900            B. IF LIST EXHAUSTED        #DD99182
         BCTR  R6,0                SUBTRACT 1 FROM COUNT       #DD99182
         B     ADDT0700                                        #DD99182
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ALLOCATE/CONCATENATE THE "NEW" DATASETS                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ADDT0400 DS    0H                                              #DD99182
         ALLOC PERM,DDN=DDNAME,DSN=DSNAMES,DISP=SHR,                   *
               ERROR=ADDTERR3
         CLI   DSNAMES+24,X'FF'    CHECK FOR END OF LIST
         BE    ADDT0600            B. IF YES
         ICM   R5,15,DSNAMES+24    A(NEXT DSNAME)
         BZ    ADDT0600            B. IF ONLY THE ONE
         SPACE 1
ADDT0500 DS    0H
         ALLOC PERM,DDNTO=WRKDDN2+2,                                   *
               DSN=(R5),DISP=SHR,                                      *
               ERROR=ADDTERR3
         ALLOC VERB=S99VRBCC,TU=(WRKDDNL@,WRKPERM),                    *
               ERROR=ADDTERR3
         SPACE 1
         CLI   24(R5),X'FF'        CHECK FOR END OF LIST
         BE    ADDT0600            B. IF YES
         ICM   R5,15,24(R5)        A(NEXT DSNAME)
         BNZ   ADDT0500            B. IF ANOTHER DSNAME SPECIFIED
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ALLOCATE/CONCATENATE THE "OLD" DATASETS                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ADDT0600 DS    0H
         TM    WRKFLAG1,WRK$CREA   CHECK IF TO BE "CREATE"ED   #DD99182
         BO    ADDT0900            B. IF YES (NO OLD DSNS)     #DD99182
         L     R5,ARLAREA          A(1ST ELEMENT)
         LH    R6,ARLRTRVD         #(RETURNED ELEMENTS)
ADDT0700 DS    0H
         LA    R15,4(,R5)          A(ELEMENT'S JFCB)
         USING JFCB,R15
         MVC   WRKDSN,JFCBDSNM     COPY THE DATASET NAME
         MVC   WRKVOL,JFCBVOLS     COPY THE VOLSER
         DROP  R15
         SPACE 1
         BAL   R14,CHECKDSN        PROCESS THIS DSNAME         #DD99182
         LTR   R15,R15             CHECK IF TO BE "REMOVED"    #DD99182
         BNZ   ADDT0800            B. IF YES (DON'T CONCAT)    #DD99182
         SPACE 1
         ALLOC PERM,DDNTO=WRKDDN2+2,                                   *
               DSN=WRKDSN@,VOL=WRKVOL@,UNIT='SYSALLDA',DISP=SHR,       *
               ERROR=ADDTERR3
         ALLOC VERB=S99VRBCC,TU=(WRKDDNL@,WRKPERM),                    *
               ERROR=ADDTERR3
         SPACE 1
ADDT0800 DS    0H                                              #DD99182
         LH    R15,0(,R5)          L(ELEMENT)
         LA    R5,0(R15,R5)        A(NEXT ELEMENT)
         BCT   R6,ADDT0700         PROCESS THE ELEMENT LIST
         SPACE 1
         L     R3,ARLAREA          A(ELEMENT BUFFER)
         SLR   R4,R4
         IC    R4,ARLPOOL          ARA SUBPOOL
         SLR   R5,R5
         ICM   R5,7,ARLRLEN        L(ELEMENT BUFFER)
         FREEMAIN RC,A=(R3),SP=(R4),LV=(R5)
         SPACE 1
ADDT0900 DS    0H                                              #DD99182
         IKJRLSA WRKANS            RELEASE THE PDL BUFFER
         B     ADDTEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        TERMINATE                                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ADDTERR1 DS    0H
         #SETRC 4                  INDICATE "PARSE ERROR'
         B     ADDTEXIT
         SPACE 1
ADDTERR2 DS    0H
         OI    WRKFLAG1,WRK$CREA   INDICATE "CREATE" NEW DD    #DD99182
         CLI   KWCREATE+1,1        CHECK FOR "CREATE" OPTION   #DD99182
         BE    ADDT0400            B. IF YES                   #DD99182
         NI    WRKFLAG1,255-WRK$CREA                           #DD99182
         TPUT  ERR1MSG,L'ERR1MSG   DISPLAY "UNABLE TO LOCATE DD DDNAME"
         #SETRC 8                  INDICATE "DD STATEMENT NOT FOUND"
         B     ADDTEXIT
         SPACE 1
ADDTERR3 DS    0H
         S99FAIL ,                 DISPLAY ALLOCATION ERROR MESSAGE
         #SETRC 12                 INDICATE "ALLOCATION ERROR"
         B     ADDTEXIT
         SPACE 1
ADDTERR4 DS    0H                                              #DD99182
         TPUT  ERR2MSG,L'ERR2MSG   DISPLAY "MUTUALLY EXCLUSIVE" DD99182
         #SETRC 16                 INDICATE "ALLOCATION ERROR" #DD99182
         B     ADDTEXIT                                        #DD99182
         SPACE 1
ADDTEXIT #STOP ,                   RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CHECK IF DATASET IS TO BE "REMOVED" FROM CONCATENATION       *
*                                                                     *
*---------------------------------------------------------------------*
CHECKDSN DS    0H                  START SUBROUTINE            #DD99182
         STM   R0,R15,SUBSAVE      SAVE ALL REGISTERS          #DD99182
         LA    R15,0               INITIALIZE RETURN CODE      #DD99182
         SPACE 1
         CLI   KWREMOVE+1,1        CHECK FOR "REMOVE" OPTION   #DD99182
         BNE   CHECKXIT            B. IF NOT                   #DD99182
         SPACE 1
         LA    R5,DSNAMES          A(DSNAMES LIST)             #DD99182
CHEC0100 DS    0H                                              #DD99182
         L     R4,0(,R5)           A(DSNAME)                   #DD99182
         LH    R3,4(,R5)           L(DSNAME)                   #DD99182
         MVC   WRKDSN2,BLANKS                                  #DD99182
         #EXEC -R3,MVC,WRKDSN2(*-*),0(R4)                      #DD99182
         CLC   WRKDSN,WRKDSN2      CHECK IF DSNAMES MATCH      #DD99182
         BNE   CHEC0200            B. IF NAME DOESN'T MATCH    #DD99182
         LA    R15,4               INDICATE "REMOVE" DSNAME    #DD99182
         B     CHECKXIT                                        #DD99182
         SPACE 1
CHEC0200 DS    0H                                              #DD99182
         CLI   24(R5),X'FF'        CHECK FOR END OF LIST       #DD99182
         BE    CHECKXIT            B. IF DSNAME NOT FOUND      #DD99182
         ICM   R5,15,24(R5)        A(NEXT DSNAME)              #DD99182
         BNZ   CHEC0100            B. IF ANOTHER NAME EXISTS   #DD99182
         SPACE 1
CHECKXIT DS    0H                  RETURN TO CALLER            #DD99182
         LM    R0,R14,SUBSAVE      RESTORE ALL REGISTERS       #DD99182
         BR    R14                 RETURN TO CALLER            #DD99182
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CONSTANTS AND WORK AREA                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ERR1MSG  DC    C'ADDT001I UNABLE TO LOCATE DD XXXXXXXX '
ERR1MSG1 EQU   ERR1MSG+29,8
ERR2MSG  DC    C'ADDT002I "CREATE" AND "REMOVE" ARE MUTUALLY EXCLUSIVE'
         SPACE 1
BLANKS   DC    CL80' '             LOTS OF BLANKS
SYSLIB   DCB   DSORG=PO,DDNAME=*-*,MACRF=R,EXLST=EXITLIST
EXITLIST DS    0F
         DC    X'93',AL3(WRKARL)   "LIST" RDJFCB EXIT
WRKARL   DS    0F
         IHAARL DSECT=NO           ALLOCATION RETRIEVAL LIST
         SPACE 1
         #STARTWA ,
SUBSAVE  DS       16F              SUBROUTINE SAVE AREA        #DD99182
         DYNSPACE ,                DEFINE DYNALLOC WORK AREA
WRKFLAG1 DS    X                   FLAG BYTE #1                #DD99182
WRK$CREA EQU   X'80'   1... ....   - CREATE NEW FILE ONLY      #DD99182
WRKANS   DS    F                   PARSE ANSWER BUFFER
WRKPERM  DS    AL2,AL2
WRKDDNL@ DS    AL2,AL2             DDNAME LIST ADDRESS/COUNT
WRKDDN1  DS    AL2,CL8             DDNAME LENGTH / DDNAME
WRKDDN2  DS    AL2,CL8             DDNAME LENGTH / DDNAME
WRKDDN@  DS    A,AL2               DDNAME ADDRESS/LENGTH
WRKDDN   DS    CL8                 DDNAME
WRKDSN@  DS    A,AL2               DSNAME ADDRESS/LENGTH
WRKDSN   DS    CL44                DSNAME
WRKDSN2  DS    CL44                SPECIFIED DSNAME WORK AREA  #DD99182
WRKECB   DS    F                   PARSE ECB
WRKPPL   DS    7F                  PARSE PARAMETER LIST
WRKVOL@  DS    A,AL2               VOLSER ADDRESS/LENGTH
WRKVOL   DS    CL6                 VOLSER
         #STOPWA ,
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PARSE PARAMETER LIST                                         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
ADDPCL   IKJPARM ,
DDNAME   IKJPOSIT DSNAME,DDNAM,                                        *
               PROMPT='CURRENT DDNAME',                                *
               HELP='NAME OF CURRENTLY ALLOCATED DD'
DSNAMES  IKJPOSIT DSNAME,USID,LIST,                                    *
               PROMPT='NEW DSNAME(S)',                                 *
               HELP='NAME OF DATASET(S) TO BE ADDED'
KWCREATE IKJKEYWD ,                                            #DD99182
         IKJNAME 'CREATE'                                      #DD99182
KWBLKSIZ IKJKEYWD ,
         IKJNAME 'BLKSIZE',SUBFLD=SUBFLD1
KWREMOVE IKJKEYWD ,                                            #DD99182
         IKJNAME 'REMOVE'                                      #DD99182
SUBFLD1  IKJSUBF ,
BLKSIZE  IKJIDENT 'BLKSIZE',INTEG,                                     *
               HELP='BLKSIZE OF LARGEST DATASET (OR GREATER)'
         IKJENDP ,
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         #DSECTS ALLOC,CVT,DCB,JFCB,TSO
         END   ,
./ ADD NAME=CATL     0100-00040-00040-1222-00781-00781-00000-SOURCE
CATL     TITLE 'CATL - CATALOG LIST UTILITY'
***********************************************************************
*                     *                                               *
*   C A T L           *            CATALOG LIST UTILITY               *
*                     *                                               *
***********************                                               *
*                                                                     *
*   FUNCTION :        LIST CATALOGED DATASETS AND OPTIONALLY LIST     *
*                     DCB × SPACE × VOLUME INFORMATION                *
*                                                                     *
*   SYNTAX:           CATL -                                          *
*                         LEVEL(QUALIFIER) -                 (OPTIONAL)
*                         DCB × SPACE × VOLUMES × HORIZONTAL (OPTIONAL)
*                                                                     *
*   REGISTER USAGE :   R0 - WORK REGISTER                             *
*                      R1 - WORK REGISTER                             *
*                      R2 - WORK REGISTER                             *
*                      R3 - WORK REGISTER                             *
*                      R4 - WORK REGISTER                             *
*                      R5 - WORK REGISTER                             *
*                      R6 - WORK REGISTER                             *
*                      R7 - WORK REGISTER                             *
*                      R8 - WORK REGISTER                             *
*                      R9 - WORK REGISTER                             *
*                     R10 - WORK REGISTER                             *
*                     R11 - >>---> PDL                                *
*                     R12 - BASE                                      *
*                     R13 - >>---> WORK AREA                          *
*                     R14 - RETURN ADDRESS                            *
*                     R15 - RETURN CODE                               *
*                                                                     *
*   MACROS : #CTGPL   #DSECTS  #INSTACK #IOSRL   #MODE    #START      *
*            #STARTWA #STOP    #STOPWA  CALLTSSR CAMLST   CATALOG     *
*            #EXEC    FREEMAIN GETMAIN  IECSDSL1 IKJEFFMT IKJENDP     *
*            IKJKEYWD          IKJNAME  IKJPARM  IKJPOSIT IKJSUBF     *
*            IKJTSMSG LINK     LOCATE   MSG      OBTAIN   PUTLINE     *
*                                                                     *
*   ROUTINES CALLED : IEFEB4UV IKJPARS  IKJEFF02 IKJEFF19             *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   HISTORY : #DD84123 - MAY  2,1984 - USE FORMAT2 CATALOG WORKAREA   *
*             #DD99183 - JUL  2,1999 - CONVERT UNITNAME VIA IEFEB4UV  *
*             #DD00033 - FEB  2,2000 - SUPPORT PDSE/HFS DATASETS      *
*                                                                     *
***********************************************************************
         EJECT 1
CATL     #START BASE=R12,SPLEVEL=2,WKCSECT=CATLWORK,                   *
               APARS=,COPY=YES,COPYYR=1999,AMODE=24,           #DD99183*
               REG1=R2,USING=(PSA,0),                                  *
               LEVEL=#V001R03                                  #DD00033
         EJECT 1
         USING CPPL,R2
         ST    R2,CATLCPPL         SAVE CPPL ADDRESS
         EJECT 1
*--------------------------------------------------------------------*
*        SET UP AN I/O SERVICE ROUTINE PARAMETER LIST                *
*--------------------------------------------------------------------*
         SPACE 1
         LA    R1,CATLIOPL         >>---> IOPL
         USING IOPL,R1
         MVC   IOPLUPT,CPPLUPT     INSERT ADDRESS OF UPT
         MVC   IOPLECT,CPPLECT     INSERT ADDRESS OF ECT
         MVC   IOPLECB,=A(CATLECB) INSERT ADDRESS OF ECB
         DROP  R1
         SPACE 1
         MVC   PUTLPARM(4),CPPLUPT   SAVE UPT ADDRESS
         MVC   PUTLPARM+4(4),CPPLECT SAVE ECT ADDRESS
         MVC   PUTLPARM+8(4),=A(CATLECB)
         EJECT 1
*--------------------------------------------------------------------*
*        SET UP IKJEFF02 (MESSAGE ROUTINE) PARAMETER LIST            *
*--------------------------------------------------------------------*
         SPACE 1
         LA    R0,MTCSECTP         >>---> MSG DESC SECTION
         ST    R0,MTPLPTR
         MVC   MTCPPLP,CATLCPPL    >>---> CPPL
         MVC   MTECBP,=A(CATLECB)  >>---> COMMUNICATION ECB
         OI    MTHIGH,B'10000000'  ON FOR STD LINKAGE
         MVC   MTCSECTP,=A(MSGCSECT)
         MVI   MTSW1,MTPUTLSW      PUTLINE MESSAGES
         LA    R0,INSERT           >>---> INSERT AREA
         STCM  R0,B'0111',MTADDR
         MVI   MTLEN,44            SET INSERT LENGTH
         MVC   INSERT,BLANKS
         EJECT 1
*--------------------------------------------------------------------*
*        SET UP A PARSE PARAMETER LIST (PPL)                         *
*--------------------------------------------------------------------*
         SPACE 1
         LA    R1,CATLPPL          >>---> PPL
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT      INSERT ADDRESS OF UPT
         MVC   PPLECT,CPPLECT      INSERT ADDRESS OF ECT
         MVC   PPLCBUF,CPPLCBUF    INSERT ADDRESS OF CBUF
         MVC   PPLECB,=A(CATLECB)  INSERT ADDRESS OF ECB
         MVC   PPLANS,=A(CATLPDLP) INSERT ADDRESS OF ANSWER PLACE
         MVC   PPLPCL,=A(PCL)      INSERT ADDRESS OF PCL
         CALLTSSR EP=IKJPARS,MF=(E,(1))
         DROP  R1
         LTR   R15,R15             CHECK IF PARSE WAS SUCCESSFUL
         BNZ   CATLERR1            IF NOT PROCESS ERROR
         L     R11,CATLPDLP        >>---> PDL
         USING IKJPARMD,R11
         EJECT 1
*---------------------------------------------------------------------*
*        SETUP THE LEVEL ACCORDINGLY     (PREFIX/LEVEL./'LEVEL')      *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   DSNBUF,BLANKS       CLEAR THE DSNAME FIELD
         CLI   LEVELKW+1,1         CHECK IF LEVEL(?) WAS SPECIFIED
         BE    CATL0010
         L     R1,CPPLUPT          >>---> UPT
         USING UPT,R1
         MVC   DSNBUF+1(7),UPTPREFX
         SLR   R9,R9               CLEAR FOR INSERT
         ICM   R9,B'0001',UPTPREFL INSERT PREFIX LENGTH
         BZ    CATLERR2
         B     CATL0020
         DROP  R1
CATL0010 DS    0H
         L     R1,DSNPDE           >>---> DSNAME
         LH    R9,DSNPDE+4         LOAD DSNAME LENGTH
         #EXEC -R9,MVC,DSNBUF+1(*-*),0(R1)
         LA    R9,1(,R9)           RESTORE LENGTH OF DSNAME
         TM    DSNPDE+6,X'40'      CHECK IF LEVEL WAS ENTERED IN QUOTES
         BO    CATL0030
CATL0020 DS    0H
         LA    R1,DSNBUF+1(R9)     >>---> END OF LEVEL
         MVI   0(R1),C'.'          INSERT A PERIOD
         LA    R9,1(,R9)           INCREMENT LENGTH OF DSNAME
CATL0030 DS    0H
         STC   R9,DSNBUF           SAVE LENGTH OF LEVEL
         EJECT 1
*---------------------------------------------------------------------*
*        LOCATE ALL CATALOG ENTRIES FOR THIS PREFIX                   *
*---------------------------------------------------------------------*
         SPACE 1
         GETMAIN VU,LA=WORKSIZE,A=WORKADDR
CATL0040 DS    0H
         LM    R2,R3,WORKADDR      LOAD WORKAREA ADDRESS & SIZE
         XC    @CTGPL,@CTGPL
         #CTGPL LISTCAT,           SETUP CATALOG PARAMETER LIST        *
               CRI=DSNBUF,                                             *
               WORK=((R2),(R3),FORMAT2),                       #DD84123*
               OPTIONS=(NAME,GENLD,RCATN,SUPLT,AM0),                   *
               MF=(E,@CTGPL)
         CATALOG @CTGPL
         LTR   R15,R15             CHECK CATALOG RETURN CODE
         BNZ   CATLERR3            CHECK FOR RECOVERABLE ERROR
         EJECT 1
*---------------------------------------------------------------------*
*        CALCULATE THE NUMBER OF ENTRIES & PRINT THE TITLES           *
*---------------------------------------------------------------------*
         SPACE 1
         CLC   4(4,R2),=X'00000008'
         BNE   CATL0050
         XC    8(2,R2),8(R2)       CLEAR TOP HALF OF CVOL LENGTH
         LA    R2,4(,R2)           >>---> CVOL WORK AREA
CATL0050 DS    0H
         L     R7,4(,R2)           LOAD AMOUNT USED IN WORKAREA
         SLR   R6,R6               PREPARE FOR DIVIDE
         LA    R5,8(,R2)           >>---> CATALOG NAME
         MVC   INSERT,1(R5)        COPY CATALOG NAME TO INSERT AREA
         D     R6,=F'45'           CALCULATE THE NUMBER OF DSNAMES
         BCTR  R7,R0               SUBTRACT 1 FOR CATALOG NAME
         ST    R7,ENTRIES          SAVE THE NUMBER OF ENTRIES
         LTR   R7,R7               CHECK ENTRY COUNT
         BNP   CATLER3C
         CLI   KEYWORD1+1,4        CHECK FOR "HORIZONTAL"
         BE    CATL0070
         MVC   MTMSGID,=C'0004'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         MVC   PRINTLNE+4(44),MESSAGE1
         MVC   PRINTLNE+49(31),MESSAGE2
         CLI   KEYWORD1+1,1        CHECK FOR "DCB"
         BE    CATL0060
         MVC   PRINTLNE+49(31),MESSAGE3
         CLI   KEYWORD1+1,2        CHECK FOR "SPACE"
         BE    CATL0060
         MVC   PRINTLNE+49(31),MESSAGE4
         CLI   KEYWORD1+1,3        CHECK FOR "VOLUMES"
         BNE   CATL0120
CATL0060 DS    0H
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
         B     CATL0120            SKIP SIDEWAYS PRINTING
         EJECT 1
*---------------------------------------------------------------------*
*        PRODUCE A "HORIZONTAL" CATALOG LIST                          *
*---------------------------------------------------------------------*
         SPACE 1
CATL0070 DS    0H
         LA    R5,45(,R5)          >>---> PAST CATALOG NAME
CATL0080 DS    0H
         MVC   PRINTLNE+4(79),BLANKS
         LA    R8,PRINTLNE+4       >>---> START OF MESSAGE AREA
         LA    R10,PRINTLNE+4+79   >>---> END   OF MESSAGE AREA
CATL0090 DS    0H
         CLI   0(R5),C'0'          CHECK FOR CATALOG NAME ENTRY
         BE    CATL0110            B. IF YES
         LA    R1,45(R5)           >>---> END OF THE DSNAME
         TRT   1(44,R5),TRTABLE    FIND THE END OF THE DSNAME
         SLR   R2,R2               CLEAR FOR INSERT
         IC    R2,DSNBUF           LOAD PREFIX LENGTH
         LA    R3,1(R2,R5)         >>---> PAST DATASET NAME PREFIX
         SR    R1,R3               CALCULATE LENGTH OF THE DSNAME
         LA    R2,0(R1,R8)         >>---> WHERE DSNAME WILL END
         CR    R2,R10              CHECK IF IT WILL OVERFLOW
         BL    CATL0100
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
         B     CATL0080            INSERT THE CURRENT DSNAME
CATL0100 DS    0H
         #EXEC -R1,MVC,0(*-*,R8),0(R3)
         LA    R8,2(R1,R8)         >>---> NEXT DSN BUFFER
CATL0110 DS    0H
         LA    R5,45(,R5)          >>---> NEXT DATASET NAME
         BCT   R7,CATL0090
         LA    R1,PRINTLNE+4       >>---> START OF PRINT LINE
         CR    R1,R8               CHECK IF ANYTHING IN THE LINE
         BE    CATL0420
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
         B     CATL0420
         EJECT 1
*---------------------------------------------------------------------*
*        FORMAT & PRINT THE INDIVIDUAL DATASET LINES                  *
*---------------------------------------------------------------------*
         SPACE 1
CATL0120 DS    0H
         LA    R5,45(,R5)          >>---> NEXT DATASET NAME
         MVC   PRINTLNE+4(44),1(R5)
         MVC   PRINTLNE+49(31),BLANKS
         MVC   DSNAME,1(R5)        SAVE THE DATASET NAME
         CLI   0(R5),C'0'          CHECK FOR CATALOG NAME ENTRY
         BNE   CATL0130            B. IF NOT
         L     R1,ENTRIES
         BCTR  R1,0                SUBTRACT 1 FROM ENTRY COUNT
         ST    R1,ENTRIES
         B     CATL0410
CATL0130 DS    0H
         CLI   KEYWORD2+1,1        CHECK FOR "NOPREFIX"
         BNE   CATL0140
         MVC   PRINTLNE+4(44),BLANKS
         LA    R1,43               LOAD MAXIMUM DATASET LENGTH
         SLR   R2,R2               CLEAR FOR INSERT
         IC    R2,DSNBUF           LOAD PREFIX LENGTH
         LA    R3,DSNAME           >>---> DATASET NAME
         LA    R3,0(R2,R3)         >>---> PAST DATASET NAME PREFIX
         SR    R1,R2               CALCULATE REMAINING LENGTH
         #EXEC R1,MVC,PRINTLNE+4(*-*),0(R3)
         EJECT 1
*---------------------------------------------------------------------*
*        FORMAT THE "DCB" LINE                                        *
*---------------------------------------------------------------------*
         SPACE 1
CATL0140 DS    0H
         CLI   KEYWORD1+1,1        CHECK FOR "DCB"
         BNE   CATL0270
         CLI   0(R5),CTGTGBS       CHECK FOR "GDG BASE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTCL        CHECK FOR "CLUSTER"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTMCAT      CHECK FOR "MASTER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTPGSP      CHECK FOR "PAGE SPACE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTUCAT      CHECK FOR "USER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         LOCATE NAMEPL             GET CATALOG INFORMATION
         LTR   R15,R15             CHECK LOCATE RETURN CODE
         BNZ   CATL0400
         MVC   VOLSER,WORKAREA+6   SAVE VOLSER
         TM    WORKAREA+4,UCB3DACC CHECK FOR DIRECT ACCESS DEVICES
         BO    CATL0150
         MVC   MESSAGE9(6),VOLSER
         MVC   PRINTLNE+49(31),MESSAGE9
         B     CATL0400
CATL0150 DS    0H
         OBTAIN SEARCHPL           GET THE VTOC INFORMATION
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0160
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0160 DS    0H
         MVC   PRINTLNE+50(3),=C'POE'                          #DD00033
         TM    DS1SMSFG,DS1PDSE    CHECK IF DSN IS "PDSE"      #DD00033
         BO    CATL0180                                        #DD00033
         MVC   PRINTLNE+50(3),=C'HFS'                          #DD00033
         TM    DS1SMSFG,DS1PDSEX   CHECK IF DSN IS "HFS"       #DD00033
         BO    CATL0180                                        #DD00033
         MVC   PRINTLNE+50(3),=C'PS '
         TM    DS1DSORG,DS1DSGPS   CHECK IF DSN IS "PS"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'PO '
         TM    DS1DSORG,DS1DSGPO   CHECK IF DSN IS "PO"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'DA '
         TM    DS1DSORG,DS1DSGDA   CHECK IF DSN IS "DA"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'IS '
         TM    DS1DSORG,DS1DSGIS   CHECK IF DSN IS "IS"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'VS '
         TM    DS1DSORG+1,DS1ORGAM CHECK IF DSN IS "AM"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'?? '
CATL0170 DS    0H
         TM    DS1DSORG,DS1DSGU    CHECK FOR UNMOVABLE DSORG
         BNO   CATL0180
         MVI   PRINTLNE+52,C'U'
CATL0180 DS    0H
         LA    R3,PRINTLNE+55      >>---> RECORD FORMAT SPACE
         TM    DS1RECFM,X'C0'      CHECK FOR RECFM = U
         BNO   CATL0190
         MVI   0(R3),C'U'
         LA    R3,1(,R3)           >>---> PAST "U"
         B     CATL0220            CHECK FOR TRACK OVERFLOW
CATL0190 DS    0H
         TM    DS1RECFM,X'80'      CHECK FOR RECFM = F
         BNO   CATL0200
         MVI   0(R3),C'F'
         LA    R3,1(,R3)           >>---> PAST "F"
         B     CATL0210            CHECK FOR BLOCKING
CATL0200 DS    0H
         TM    DS1RECFM,X'40'      CHECK FOR RECFM = V
         BNO   CATL0210
         MVI   0(R3),C'V'
         LA    R3,1(,R3)           >>---> PAST "V"
CATL0210 DS    0H
         TM    DS1RECFM,X'10'      CHECK IF DATASET IS BLOCKED
         BNO   CATL0220
         MVI   0(R3),C'B'
         LA    R3,1(,R3)           >>---> PAST "B"
CATL0220 DS    0H
         TM    DS1RECFM,X'20'      CHECK FOR TRACK OVERFLOW
         BNO   CATL0230
         MVI   0(R3),C'T'
         LA    R3,1(,R3)           >>---> PAST "T"
CATL0230 DS    0H
         TM    DS1RECFM,X'08'      CHECK FOR SPANNED OR STANDARD
         BNO   CATL0240
         MVI   0(R3),C'S'
         LA    R3,1(,R3)           >>---> PAST "S"
CATL0240 DS    0H
         TM    DS1RECFM,X'04'      CHECK FOR ASA CNTL CHARS
         BNO   CATL0250
         MVI   0(R3),C'A'
         LA    R3,1(,R3)           >>---> PAST "A"
CATL0250 DS    0H
         TM    DS1RECFM,X'02'      CHECK FOR MACHINE CNTL CHARS
         BNO   CATL0260
         MVI   0(R3),C'M'
CATL0260 DS    0H
         SLR   R0,R0
         ICM   R0,B'0011',DS1LRECL LOAD RECORD LENGTH
         CVD   R0,DBLWORD
         MVC   PRINTLNE+60(6),=X'402020202120'
         ED    PRINTLNE+60(6),DBLWORD+5
         SLR   R0,R0
         ICM   R0,B'0011',DS1BLKL  LOAD BLOCK SIZE
         CVD   R0,DBLWORD
         MVC   PRINTLNE+67(6),=X'402020202120'
         ED    PRINTLNE+67(6),DBLWORD+5
         MVC   FULLWORD(3),DS1EXPDT    SAVE THE EXPIRY DATE
         L     R0,FULLWORD             LOAD THE EXPIRY DATE
         SRL   R0,24                   SHIFT TO GET THE YEAR
         CVD   R0,DBLWORD
         UNPK  PRINTLNE+75(2),DBLWORD
         OI    PRINTLNE+76,X'F0'   CONVERT SIGN BIT
         L     R0,FULLWORD             LOAD THE EXPIRY DATE
         SLL   R0,8                    SHIFT OFF THE YEAR
         SRL   R0,16                   SHIFT BACK FOR DATE
         CVD   R0,DBLWORD
         UNPK  PRINTLNE+77(3),DBLWORD
         OI    PRINTLNE+79,X'F0'   CONVERT SIGN BIT
         B     CATL0400
         EJECT 1
*---------------------------------------------------------------------*
*        FORMAT THE "SPACE" LINE                                      *
*---------------------------------------------------------------------*
         SPACE 1
CATL0270 DS    0H
         CLI   KEYWORD1+1,2        CHECK FOR "SPACE"
         BNE   CATL0390
         CLI   0(R5),CTGTGBS       CHECK FOR "GDG BASE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTCL        CHECK FOR "CLUSTER"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTMCAT      CHECK FOR "MASTER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTPGSP      CHECK FOR "PAGE SPACE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTUCAT      CHECK FOR "USER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         LOCATE NAMEPL             GET CATALOG INFORMATION
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   CATL0400
         MVC   VOLSER,WORKAREA+6   SAVE VOLSER
         TM    WORKAREA+4,UCB3DACC CHECK FOR DIRECT ACCESS DEVICES
         BO    CATL0280
         MVC   MESSAGE9(6),VOLSER
         MVC   PRINTLNE+49(31),MESSAGE9
         B     CATL0400
CATL0280 DS    0H
         OBTAIN VTOCPL             GET VTOC INFORMATION
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0290
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0290 DS    0H
         MVC   TRKCYL,DS4DEVSZ+2   SAVE # TRACKS PER CYL
         OBTAIN SEARCHPL           GET FORMAT 1 DSCB
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0300
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0300 DS    0H
         SLR   R3,R3
         IC    R3,DS1NOEPV         LOAD THE NUMBER OF EXTENTS
         CVD   R3,DBLWORD
         MVC   PRINTLNE+62(4),=X'40202120'
         ED    PRINTLNE+62(4),DBLWORD+6
         MVC   PRINTLNE+74(4),=C'CYLS'
         TM    DS1SCALO,X'C0'          CHECK FOR CYLINDER ALLOCATION
         BO    CATL0310
         MVC   PRINTLNE+74(4),=C'BLKS'
         TM    DS1SCALO,X'40'          CHECK FOR BLOCK ALLOCATION
         BO    CATL0310
         MVC   PRINTLNE+74(4),=C'TRKS'
         TM    DS1SCALO,X'80'          CHECK FOR TRACK ALLOCATION
         BO    CATL0310
         MVC   PRINTLNE+74(4),=C'ABST'
CATL0310 DS    0H
         SLR   R0,R0
         ICM   R0,B'0111',DS1SCALO+1   LOAD SECONDARY ALLOCATION
         CVD   R0,DBLWORD
         MVC   PRINTLNE+67(6),=X'402020202120'
         ED    PRINTLNE+67(6),DBLWORD+5
         NI    DS1LSTAR,X'7F'      TURN OFF HIGH ORDER BIT
         SLR   R3,R3
         ICM   R3,B'0011',DS1LSTAR LOAD USED TRACKS
         CLC   DS1LSTAR,=X'000000' CHECK IF ANY ARE USED
         BE    CATL0320
         LA    R3,1(,R3)
CATL0320 DS    0H
         CVD   R3,DBLWORD
         MVC   PRINTLNE+54(6),=X'402020202120'
         ED    PRINTLNE+54(6),DBLWORD+5
         XC    FULLWORD,FULLWORD   CLEAR ALLOCATED TRK COUNTER
         LA    R1,DS1EXT1          >>---> FIRST EXTENT
         LA    R2,3                LOAD LOOP FACTOR
CATL0330 DS    0H
         BAL   R14,CONVCCHH        CONVERT + TOTAL TRACKS
         LA    R1,10(,R1)          >>---> NEXT EXTENT
         BCT   R2,CATL0330         LOOP 4 TIMES
         CLC   DS1PTRDS,=X'0000000000' CHECK FOR A FMT2 OR FMT3 DSCB
         BE    CATL0380
         MVC   CCHHR,DS1PTRDS      SAVE DSCB POINTER
CATL0340 DS    0H
         OBTAIN SEEKPL
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0350
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0350 DS    0H
         CLI   DS3FMTID,C'3'       CHECK FOR FORMAT 3 DSCB
         BNE   CATL0340
         LA    R1,DS3EXTNT         >>---> FOUR EXTENTS
         LA    R2,4                LOAD LOOP FACTOR
CATL0360 DS    0H
         BAL   R14,CONVCCHH        CONVERT + TOTAL TRACKS
         LA    R1,10(,R1)          >>---> NEXT EXTENT
         BCT   R2,CATL0360         LOOP 4 TIMES
         LA    R1,DS3ADEXT         >>---> NINE EXTENTS
         LA    R2,9                LOAD LOOP FACTOR
CATL0370 DS    0H
         BAL   R14,CONVCCHH        CONVERT + TOTAL TRACKS
         LA    R1,10(,R1)          >>---> NEXT EXTENT
         BCT   R2,CATL0370         LOOP 9 TIMES
         MVC   CCHHR,DS3PTRDS      SAVE DSCB POINTER
         CLC   CCHHR,=X'0000000000' CHECK FOR MORE DSCBS
         BNE   CATL0340
CATL0380 DS    0H
         L     R0,FULLWORD         LOAD ALLOCATED TRACK COUNT
         CVD   R0,DBLWORD
         MVC   PRINTLNE+48(6),=X'402020202120'
         ED    PRINTLNE+48(6),DBLWORD+5
         B     CATL0400
         EJECT 1
*---------------------------------------------------------------------*
*        FORMAT THE "VOLUMES" LINE                                    *
*---------------------------------------------------------------------*
         SPACE 1
CATL0390 DS    0H
         CLI   KEYWORD1+1,3        CHECK FOR "VOLUMES"
         BNE   CATL0400
         LOCATE NAMEPL             GET THE CATALOG INFORMATION
         LTR   R15,R15             CHECK LOCATE RETURN CODE
         BNZ   CATL0400
         MVC   PRINTLNE+49(6),WORKAREA+6
         NI    WORKAREA+3,X'DF'    TURN OFF SHARED BIT IN THE DEVTYPE
         MVC   UVDEVT,WORKAREA+2   COPY DEVICE TYPE            #DD99183
         NI    UVDEVT+1,X'DF'      TURN OFF "SHARED" BIT       #DD99183
         LA    R0,UVUNTAB                                      #DD99183
         ST    R0,UVUNTAB@         SAVE UNIT TABLE ADDRESS     #DD99183
         LA    R0,UVFLAGS                                      #DD99183
         ST    R0,UVFLAGS@         SAVE FLAGS ADDRESS          #DD99183
         OI    UVFLAGS@,X'80'      INDICATE END OF LIST        #DD99183
         MVC   UVFLAGS,=X'2100'    INSERT REQUEST FLAGS        #DD99183
         LA    R1,UVPLIST                                      #DD99183
         LINK  EP=IEFEB4UV         CALL SERVICE ROUTINE        #DD99183
         MVC   PRINTLNE+57(8),=CL8'????????'                   #DD99183
         LTR   R15,R15             CHECK IF UNITNAME RETURNED  #DD99183
         BNZ   CATL0400            B. IF NOT                   #DD99183
         MVC   PRINTLNE+57(8),UVUNAME                          #DD99183
         SPACE 1
CATL0400 DS    0H
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
CATL0410 DS    0H
         BCT   R7,CATL0120         PROCESS ALL ENTRIES IN THE BUFFER
         EJECT 1
*---------------------------------------------------------------------*
*        PRINT THE FOOTING TITLES                                     *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   INSERT,BLANKS
         MVC   INSERT(4),ENTRIES   COPY # OF DATASETS
         OI    MTLEN,X'80'         SET HEX CONVERSION FLAG
         MVC   MTMSGID,=C'0005'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         EJECT 1
*---------------------------------------------------------------------*
*        FREE THE CATALOG WORK AREA IF ONE WAS OBTAINED               *
*---------------------------------------------------------------------*
         SPACE 1
CATL0420 DS    0H
         LM    R3,R4,WORKADDR      LOAD WORKAREA ADDRESS & SIZE
         LTR   R3,R3               CHECK IF A WORKAREA EXISTS
         BZ    CATLEXIT
         FREEMAIN RU,LV=(R4),A=(R3)
CATLEXIT #STOP ,                   RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*        EXTENT SIZE CALCULATION SUBROUTINE                           *
*---------------------------------------------------------------------*
         SPACE 1
CONVCCHH DS    0H
         CLC   2(4,R1),=F'0'       IS THERE A START CCHHR
         BER   R14                 IF NOT RETURN TO CALLER
         LH    R3,2(R1)            LOAD START CYL ADDRESS
         MH    R3,TRKCYL           CONVERT TO TRKS
         AH    R3,4(R1)            ADD ADDITIONAL TRKS
         LH    R4,6(R1)            LOAD END CYL ADDRESS
         MH    R4,TRKCYL           CONVERT TO TRKS
         AH    R4,8(R1)            ADD ADDITIONAL TRKS
         LA    R4,1(,R4)           ADD 1 FOR RELATIVE TRACK 0
         SR    R4,R3               CALCULATE EXTENT SIZE
         A     R4,FULLWORD         ADD ALREADY ACCUMULATED
         ST    R4,FULLWORD         SAVE NEW TOTAL
         BR    R14                 RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*        OBTAIN ERROR MESSAGE FORMATING SUBROUTINE                    *
*---------------------------------------------------------------------*
         SPACE 1
OBTAINFO DS    0F
         MVC   MESSAGE5(6),VOLSER
         MVC   MESSAGE6+23(6),VOLSER
         MVC   MESSAGE7+18(6),VOLSER
         MVC   PRINTLNE+49(31),BLANKS
         LA    R1,OBTABLE          >>---> MESSAGE TABLE
         LA    R1,0(R15,R1)        >>---> SPECIFIC MESSAGE POINTER
         ICM   R1,B'1111',0(R1)    >>---> SPECIFIC MESSAGE
         BZR   R14                 IF NO MESSAGE RETURN TO CALLER
         MVC   PRINTLNE+49(31),0(R1)
         BR    R14                 RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*        IKJPARSE ERROR PROCESSING                                    *
*---------------------------------------------------------------------*
         SPACE 1
CATLERR1 DS    0H
         LA    R1,JEFF19PL         >>---> PARAMETER LIST
         LA    R0,4(,R1)           >>---> PARAMETERS
         ST    R0,0(R1)            SAVE IN PARAMETER LIST
         XC    4(44,R1),4(R1)      CLEAR REMAINDER OF PARAMETER LIST
         ST    R15,8(R1)           SAVE RETURN CODE
         MVI   17(R1),21           SET CALLER ID (PARSE ERROR)
         MVC   20(4,R1),CATLCPPL   INSERT CPPL ADDRESS
         LA    R0,CATLECB          >>---> ECB
         ST    R0,24(R1)           SAVE ECB ADDRESS
         LINK  EP=IKJEFF19         ISSUE PARSE ERROR MESSAGE
         B     CATL0420
         EJECT 1
*---------------------------------------------------------------------*
*        NULL PREFIX ERROR PROCESSING                                 *
*---------------------------------------------------------------------*
         SPACE 1
CATLERR2 DS    0H
         MVC   MTMSGID,=C'0002'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         B     CATL0420
         EJECT 1
*---------------------------------------------------------------------*
*        CATALOG ERROR PROCESSING                                     *
*---------------------------------------------------------------------*
         SPACE 1
CATLERR3 DS    0H
         C     R15,=A(RCVLSZ)      CHECK IF "WORKAREA" IS TOO SMALL
         BNE   CATLER3B
         LM    R3,R4,WORKADDR      LOAD WORKAREA ADDRESS & SIZE
         L     R2,4(,R3)           LOAD REQUIRED WORK SIZE
         FREEMAIN RU,LV=(R4),A=(R3)
         GETMAIN  RU,LV=(R2)       OBTAIN REQUIRED "WORKAREA" STORAGE
         STM   R1,R2,WORKADDR      SAVE NEW "WORKAREA" ADDRESS & SIZE
         B     CATL0040            BRANCH BACK TO REISSUE LISTCAT
CATLER3B DS    0H
         C     R15,=A(RCENT)       CHECK IF NO ENTRIES FOUND
         BNE   CATLER3D
CATLER3C DS    0H
         MVC   MTMSGID,=C'0006'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         B     CATL0420
CATLER3D DS    0H
         ST    R15,INSERT
         OI    MTLEN,X'80'         SET HEX CONVERSION FLAG
         MVC   MTMSGID,=C'0003'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         B     CATL0420
         EJECT 1
*---------------------------------------------------------------------*
*        WORK AREA                                                    *
*---------------------------------------------------------------------*
         SPACE 1
         #STARTWA ,
         IKJEFFMT MTDSECT=NO,MTNINST=2
@CTGPL   DS    CL(CTGPLLEN)
BLANKS   DC    CL80' '
CATLCPPL DS    F                   @ CMD PROCESSOR PARM LIST
CATLECB  DS    F                   ECB
CATLIOPL DS    4F                  I/O PARAM LIST
CATLPDLP DC    F'0'                PDL POINTER
CATLPPL  DS    7F                  PARSE PARAMETER LIST
CCHHR    DS    XL5                     CCHHR
DBLWORD  DS    D
DSNBUF   DS    CL45                DSNAME SAVE AREA
DSNAME   DS    CL44                DATASET NAME
ENTRIES  DS    F
FMT4DSN  DC    44X'04'             FORMAT4.DSCB NAME
FULLWORD DS    F
INSERT   DS    CL44                MESSAGE INSERT # 1
IOPLADS  DS    4F
JEFF19PL DS    12F                 DAIRFAIL PARAMETER LIST
MESSAGE1 DC    CL44'- DATASET NAME -'
MESSAGE2 DC    CL31'DSORG-RECFM-LRECL-BLKSIZE-EXPDT'
MESSAGE3 DC    CL31'ALLOC--USED-EXTENTS-SECONDARY'
MESSAGE4 DC    CL31'VOLSER DEVTYPE'
MESSAGE5 DC    CL31'XXXXXX NOT MOUNTED'
MESSAGE6 DC    CL31'FMT1 DSCB NOT FOUND ON XXXXXX'
MESSAGE7 DC    CL31'I/O ERROR READING XXXXXX VTOC'
MESSAGE8 DC    CL31'INVALID DSCB CHAIN POINTER'
MESSAGE9 DC    CL31'XXXXXX IS NOT A DASD VOLUME'
NAMEPL   CAMLST NAME,DSNAME,,WORKAREA
         SPACE 1
UVPLIST  DS    0F                                              #DD99183
UVUNTAB@ DS    A                   A(UNIT TABLE)               #DD99183
UVFLAGS@ DS    A                   A(FLAGS)                    #DD99183
UVUNTAB  DS    0XL16               UNIT TABLE                  #DD99183
UVUNAME  DS    CL8                 RETURNED UNIT NAME          #DD99183
UVDEVT   DS    XL4                 DEVICE TYPE                 #DD99183
UVATTR@  DS    A                   A(ATTRIBUTE AREA)           #DD99183
UVFLAGS  DS    XL2                 FLAGS                       #DD99183
         SPACE 1
OBTABLE  DC    A(0)
         DC    A(MESSAGE5)
         DC    A(MESSAGE6)
         DC    A(MESSAGE7)
         DC    A(0)
         DC    A(MESSAGE8)
PRINTLNE MSG   '                                                       *
                                       ' - 4 BYTE HEADER & 79 BYTE MSG
PUTLINEL PUTLINE MF=L
PUTLPARM DS    3F                  >>---> UPT/ECT/ECB
SEARCHPL CAMLST SEARCH,DSNAME,VOLSER,WORKAREA
SEEKPL   CAMLST SEEK,CCHHR,VOLSER,WORKAREA
TRKCYL   DS    H                   TRACKS PER CYLINDER
TRTABLE  DC    XL256'00'           TRANSLATE TABLE
         ORG   TRTABLE+C' '
         DC    X'FF'               FIND A BLANK
         ORG   ,
VOLSER   DS    CL6
VTOCPL   CAMLST SEARCH,FMT4DSN,VOLSER,WORKAREA
WORKADDR DS    D                   RETURNED TABLE ADDRESS & SIZE
WORKAREA DS    0F,265C             LOCATE/OBTAIN WORKAREA
         PRINT NOGEN
         ORG   WORKAREA-L'DS1DSNAM
         IECSDSL1 1                FORMAT 1 DSCB
         ORG   WORKAREA
         IECSDSL1 3                FORMAT 3 DSCB
         ORG   WORKAREA
         IECSDSL1 4                FORMAT 4 DSCB
         ORG   ,
         PRINT GEN
WORKSIZE DC    F'53'               MINIMUM TABLE SIZE (45*1+8)
         DC    F'45008'            MAXIMUM TABLE SIZE (45*100+8)
         #STOPWA ,
         EJECT 1
*---------------------------------------------------------------------*
*        COMMAND PARAMETERS                                           *
*---------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
         SPACE 1
PCL      IKJPARM
LEVELKW  IKJKEYWD
         IKJNAME 'L',SUBFLD=LEVELSUB,ALIAS='LEVEL'
KEYWORD1 IKJKEYWD
         IKJNAME 'DCB',                                                *
               ALIAS=('DSORG','RECFM','LRECL','BLKSIZE','EXPDT')
         IKJNAME 'SPACE',ALIAS='TRACKS'
         IKJNAME 'VOLUMES',ALIAS='UNIT'
         IKJNAME 'HORIZONTAL',ALIAS='SIDEWAYS'
KEYWORD2 IKJKEYWD
         IKJNAME 'NOPREFIX'
LEVELSUB IKJSUBF
DSNPDE   IKJPOSIT DSNAME,PROMPT='DATA SET NAME HIGH LEVEL QUALIFIER'
         IKJENDP
         EJECT 1
*---------------------------------------------------------------------*
*        MESSAGES                                                     *
*---------------------------------------------------------------------*
         SPACE 1
MSGCSECT CSECT ,
         IKJTSMSG ('CATL001I UNABLE TO LIST CATALOG+'),0001
         IKJTSMSG ('CATL002I NO PREFIX IN USER PROFILE TABLE'),0002,   *
               0001
         IKJTSMSG ('CATL003I SUPERLOCATE FAILED.  RETURN CODE=',,),0003
         IKJTSMSG ('CATL004I ENTRIES FROM CATALOG ',,),0004
         IKJTSMSG ('CATL005I NUMBER OF ENTRIES LISTED = ',,),0005
         IKJTSMSG ('CATL006I NO ENTRIES FOUND'),0006
         IKJTSMSG
         EJECT 1
*---------------------------------------------------------------------*
*        DSECTS                                                       *
*---------------------------------------------------------------------*
         SPACE 1
         #DSECTS CATLG,CVT,DCB,EDT,JESCT,JFCB,TCB,TIOT,TSO,UCB,PSA
*?       #INSTACK
*?       #IOSRL
         END   ,
./ ADD NAME=HEXDUMP  0100-00040-00040-1222-00405-00405-00000-SOURCE
HEXDUMP  TITLE 'H E X D U M P  - STORAGE DUMP FORMAT ROUTINE'
***********************************************************************
*                *                                                    *
*  H E X D U M P *                                                    *
*                *                                                    *
***********************************************************************
*                                                                     *
*   FUNCTION - THIS SUBROUTINE PROVIDES A FORMATTED DUMP OF STORAGE,  *
*              WITH BOTH A HEX AND CHARACTER DISPLAY IN THE AMASPZAP  *
*              FORMAT. THE PRINT LINE CAN BE FORMATED FOR AN 80 BYTE  *
*              LINE FOR TERMINALS, OR A 130 BYTE LINE FOR PRINTED     *
*              MATTER.  THIS IS CONTROLLED BY A SWITCH.  ALSO, THE    *
*              PROVISION FOR PRINTING THE ABSOLUTE STORAGE ADDRESS    *
*              AND/OR A RELATIVE ADDRESS, WHOSE STARTING VALUE IS     *
*              SUPPLIED IN THE PARAMETER LIST.  THIS SUBROUTINE       *
*              PASSES THE FORMATED LINE IN THE BUFFER SUPPLIED BY     *
*              THE CALLER.  THE ROUTINE SHOULD BE REINVOLKED UNTIL    *
*              THE STARTING ADDRESS IN THE PARAMETER LIST EQUALS OR   *
*              EXCEEDS THE END ADDRESS.                               *
*                                                                     *
*   ENTRY -   STANDARD SUBROUTINE LINKAGE                             *
*                                                                     *
*                       +0 4 BYTE START ADDRESS FOR DUMP              *
*                       +4 4 BYTE ADDRESS OF LAST BYTE FOR DUMP       *
*                       +8 4 BYTE RELATIVE ADDRESS (OPTIONAL)         *
*                      +12 1 BYTE OF SWITCHES                         *
*                          X'80' PRINT ABSOLUTE STORAGE ADDRESS ON    *
*                                EACH LINE OF DUMP                    *
*                          X'40' PRINT RELATIVE STORAGE ADDRESS ON    *
*                                EACH LINE OF DUMP                    *
*                          X'20' PRINT LINE IS 130 BYTES WIDE (ON)    *
*                                OTHERWISE IT IS 80 BYTES WIDE        *
*                     +13 3 BYTE ADDRESS OF BUFFER                    *
*                                                                     *
*   EXIT PARAMETERS NORMAL -  PARAMETER LIST START AND RELATIVE ADDRE *
*                             INCREMENTED BY NO OF BYTES PROCESSED.   *
*                             ALL REGISTERS RESTORED.                 *
*                                                                     *
*   EXIT PARAMETERS ABNORMAL - NONE                                   *
*                                                                     *
*   REGISTER USAGE - R0  -  USED                                      *
*                    R1  -  PARM LIST POINTER                         *
*                    R2  -  CURRENT DATA ADDRESS                      *
*                    R3  -  LENGTH OF LINE LEFT TO FORMAT             *
*                    R4  -  RELATIVE ADDRESS                          *
*                    R5  -  BUFFER ADDRESS                            *
*                    R6  -  CURRENT BUFFER SLOT ADDRESS               *
*                    R7  -  WORK REGISTER                             *
*                    R8  -  WORK REGISTER                             *
*                    R9  -  USED FOR SAVING CALLERS MODE              *
*                    R10 -  FREE                                      *
*                    R11 -  FREE                                      *
*                    R12 -  FREE                                      *
*                    R13 -  CALLER'S SAVE AREA ADDRESS                *
*                    R14 -  RETURN ADDRESS                            *
*                    R15 -  ENTRY POINT ADDRESS AND BASE              *
*                                                                     *
*   ATTRIBUTES -   REENTRANT                                          *
*                                                                     *
*   CONTROL BLOCK DEPENDENCIES - NONE                                 *
*                                                                     *
*   ROUTINES CALLED - NONE                                            *
*                                                                     *
*   ABEND CODES -  NONE                                               *
*                                                                     *
*   MESSAGES ISSUED - NONE                                            *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        HISTORY:                                                     *
*                                                                     *
***********************************************************************
         EJECT 1
HEXDUMP  #START REG1=R3,AMODE=CAP31,SPLEVEL=2,WKDSECT=HEXWORK
         EJECT 1
         L     R1,0(0,R3)               >>---> PARAMETER LIST
         NI    0(R1),FF-BIT0            ENSURE HIGH ORDER BIT
         NI    4(R1),FF-BIT0            ENSURE HIGH ORDER BIT
         LM    R2,R4,0(R1)         LOAD START/LAST/RELATIVE ADDRESSES
         SLR   R5,R5                    CLEAR BEFORE INSERT
         ICM   R5,B'0111',13(R1)        LOAD BUFFER ADDRESS
         LA    R3,1(0,R3)               >>---> PAST LAST BYTE
         LR    R6,R5                    >>---> BUFFER
         MVI   0(R5),C' '               INSERT PAD CHARACTER
         TM    12(R1),BIT2              CHECK IF 130 BYTE BUFFER
         BO    DUMPL130
         MVC   1(79,R5),0(R5)           CLEAR FIRST 80 BYTES
         B     DUMP10
         SPACE 1
DUMPL130 MVC   1(129,R5),0(R5)          CLEAR FIRST 130 BYTES
         EJECT 1
***********************************************************************
* CHECK THE PARAMETERS IF RELATIVE ADDRESS OPTION WAS SPECIFIED, THEN *
* BRANCH ACCORDINGLY.                                                 *
***********************************************************************
         SPACE 1
DUMP10   DS    0H
         TM    12(R1),BIT1         CHECK FOR RELATIVE ADDRESS REQUEST
         BZ    DUMP20
         LTR   R4,R4               CHECK FOR NEGATIVE RELATIVE ADDRESS
         BNM   DUMP11
         LCR   R4,R4                    LOAD COMPLEMENT OF ADDRESS
         ST    R4,8(0,R1)               SAVE COMPLIMENT FOR UNPACK
         LCR   R4,R4                    RESTORE REGISTER 4
         SPACE 1
***********************************************************************
* UNPACK THE HEX DIGITS AND SURPRESS 5 LEADING ZEROS.                 *
***********************************************************************
         SPACE 1
DUMP11   DS    0H
         UNPK  2(7,R6),9(4,R1)          SPREAD HEX DIGITS
         MVI   8(R6),C' '               BLANK EXTRA DIGIT
         TR    2(6,R6),HEXTAB2          MAKE HEX DIGITS PRINTABLE
         LA    R8,5                     SUPPRESS 5 LEADING ZEROES
         LA    R7,2(0,R6)               POINT TO FIRST DIGIT
         SPACE 1
***********************************************************************
* IF MORE LEADING ZEROS ARE PRESENT, THEN SURPRESS THEM AND SET       *
* RELATIVE SWITCH ON.                                                 *
***********************************************************************
         SPACE 1
DUMP12   DS    0H
         CLI   0(R7),C'0'               HAVE WE A LEADING ZERO?
         BNE   DUMP14                   NO, HAVE A SIG DIGIT
         MVI   0(R7),C' '               BLANK OUT THE LEADING ZERO
         LA    R7,1(0,R7)               POINT TO NEXT CHARACTER
         BCT   R8,DUMP12                REPEAT 4 MORE TIMES
DUMP14   BCTR  R7,0                     BACK UP BY 1
         MVI   0(R7),C'+'               INDICATE A RELATIVE ADDRESS
         LTR   R4,R4                    TEST RELATIVE ADDRESS
         BNM   DUMP16                   BRANCH IF POSITIVE
         MVI   0(R7),C'-'               ELSE INDICATE REL ADDR NEG
DUMP16   DS    0H
         LA    R6,8(0,R6)               BUMP UP BUFFER POINTER
         EJECT 1
***********************************************************************
* CHECK IF THE ABSOLUTE ADDRESS OPTION WAS SPECIFIED, AND BRANCH      *
* ACCORDINGLY.                                                        *
***********************************************************************
         SPACE 1
DUMP20   DS    0H
         TM    12(R1),BIT0              ABSOLUTE ADDRESS REQUIRED?
         BZ    DUMP30                   NO, SKIP
         UNPK  1(9,R6),0(5,R1)          SPREAD HEX DIGITS OF ABS ADDR
         TR    1(8,R6),HEXTAB2          MAKE HEX DIGITS PRINTABLE
         MVI   9(R6),C' '               PAD
         LA    R6,9(0,R6)               UPDATE BUFFER POINTER
         SPACE 1
***********************************************************************
* TEST OUTPUT LENGTH SPECIFIED AND BRANCH ACCORDINGLY TO FORMAT       *
* OUTPUT.                                                             *
***********************************************************************
         SPACE 1
DUMP30   DS    0H
         LA    R6,2(0,R6)               ADD PADDING CHARACTERS
         USING BUFDSECT,R6              MAP BUFFER
         SR    R3,R2                    GET LENGTH OF DATA LEFT
         TM    12(R1),BIT2              WAS 130 BYTE BUFFER WANTED?
         BO    DUMP50                   YES, BRANCH
         SPACE 1
***********************************************************************
* IF OUTPUT LENGTH IS 80 CHARACTERS THEN SET SWITCH AND MOVE DATA     *
* INTO BUFFER CONVERTING THE HEX DIGITS INTO PRINTABLE FORM IF        *
* POSSIBLE AND TO DOTS IF UNPRINTABLE.                                *
***********************************************************************
         SPACE 1
         LA    R8,4                     INDICATE 80 BYTE BUFFER
         MVI   BL80HEX,C'*'             FORMAT CHARACTER DISPLAY
         MVI   BL80HEX+17,C'*'
         CL    R3,=A(16)                TEST LENGTH LEFT TO DO
         BNL   DUMP40                   BRANCH IF >= 16
         AR    R4,R3                    UPDATE RELATIVE ADDR
         #EXEC -R3,MVC,BL80HEX+1(*-*),0(R2)
         #EXEC R3,TR,BL80HEX+1(*-*),HEXTAB1
         LA    R3,1(0,R3)               RESTORE LENGTH
         B     DUMP60
         SPACE 1
DUMP40   MVC   BL80HEX+1(16),0(R2)      MOVE DATA TO LINE
         TR    BL80HEX+1(16),HEXTAB1    CONVERT UNPRINTABLES TO DOTS
         A     R4,=A(16)                UPDATE REL ADDR (CAN'T USE LA)
         B     DUMP60
         EJECT 1
***********************************************************************
* IF OUTPUT LENGTH IS 130 CHARACTERS THEN SET SWITCH AND MOVE DATA    *
* INTO BUFFER CONVERTING HEX DIGITS INTO PRINTABLE FORM IF POSSIBLE   *
* AND INTO DOTS IF NOT POSSIBLE.                                      *
***********************************************************************
         SPACE 1
DUMP50   DS    0H                       130 BYTE BUFFER
         LA    R8,8                     INDICATE 80 BYTE BUFFER
         MVI   BL130HEX,C'*'            FORMAT CHARACTER DISPLAY
         MVI   BL130HEX+33,C'*'
         CL    R3,=A(32)                TEST LENGTH LEFT TO DO
         BNL   DUMP55                   BRANCH IF >= 32
         AR    R4,R3                    UPDATE RELATIVE ADDR
         #EXEC -R3,MVC,BL130HEX+1(*-*),0(R2)
         #EXEC R3,TR,BL130HEX+1(*-*),HEXTAB1
         LA    R3,1(0,R3)               RESTORE LENGTH
         B     DUMP60
         SPACE 1
DUMP55   MVC   BL130HEX+1(32),0(R2)     MOVE DATA TO LINE
         TR    BL130HEX+1(32),HEXTAB1   CONVERT UNPRINTABLES TO DOTS
         A     R4,=A(32)                UPDATE REL ADDR (CAN'T USE LA)
         EJECT 1
***********************************************************************
* CHECK IF 5 OR MORE BYTES LEFT IN BUFFER AND BRANCH ACCORDINGLY TO   *
* FILL BUFFER COMPLETELY.                                             *
***********************************************************************
         SPACE 1
DUMP60   DS    0H
         CL    R3,=A(5)                 AT LEAST 5 BYTES LEFT?
         BL    DUMP80                   NO
         SPACE 1
***********************************************************************
* FIVE OR MORE BYTES LEFT IN BUFFER, THEN FILL 4 AT A TIME UNTIL LESS *
* THEN 5 BYTES ARE LEFT, THEN BRANCH ACCORDINGLY TO FILL BUFFER.      *
***********************************************************************
         SPACE 1
         UNPK  0(9,R6),0(5,R2)          UNPACK 4 BYTES
         TR    0(8,R6),HEXTAB2          CONVERT TO PRINTABLE HEX
         LA    R2,4(0,R2)               BUMP UP DATA POINTER
         MVI   8(R6),C' '               ALLOW ONE PADDING BLANK
         LA    R6,9(0,R6)               BUMP UP BUFFER POINTER
         SL    R3,=A(4)                 DECREMENT LENGTH LEFT TO DO
         CL    R8,=A(5)                 FIRST FOUR WORDS OUT ?
         BNE   *+8                      NO, SKIP
         LA    R6,1(0,R6)               YES, EXTRA SPACE
         BCT   R8,DUMP60                CONVERT IF SPACE LEFT ON LINE
         B     DUMPEXIT                 ELSE HAVE DONE OUR JOB
         EJECT 1
***********************************************************************
* CHECK THE NUMBER OF BYTES LEFT TO FILL AND BRANCH ACCORDINGLY TO    *
* FILL BUFFER COMPLETELY. IF FULL THEN BRANCH TO END PROGRAM.         *
***********************************************************************
         SPACE 1
DUMP80   DS    0H
         SLL   R3,2                     GET A BRANCH INDEX
         B     *+4(R3)                  USE A BRANCH TABLE
         B     DUMPEXIT                 NO MORE BYTES LEFT
         B     DUMPL1                   ONE BYTE LEFT
         B     DUMPL2                   TWO BYTES LEFT
         B     DUMPL3                   3 BYTES LEFT
         SPACE 1
***********************************************************************
* FOUR BYTES LEFT TO FILL, FILL IT AND BRANCH TO END PROGRAM.         *
***********************************************************************
         SPACE 1
DUMPL4   DS    0H
         MVC   12(4,R13),0(R2)          MOVE IN LAST 4 BYTES
         UNPK  0(9,R6),12(5,R13)        SPREAD OUT HEX DIGITS
         TR    0(8,R6),HEXTAB2          CONVERT TO PRINTABLE HEX
         LA    R2,4(0,R2)               BUMP UP DATA PTR
         MVI   8(R6),C' '               PAD
         B     DUMPEXIT                 DUMP COMPLETE
         SPACE 1
***********************************************************************
* THREE BYTES LEFT TO FILL, FILL IT AND BRANCH TO END PROGRAM.        *
***********************************************************************
         SPACE 1
DUMPL3   DS    0H
         MVC   12(3,R13),0(R2)          MOVE IN LAST 3 BYTES
         UNPK  0(7,R6),12(4,R13)        SPREAD OUT HEX DIGITS
         TR    0(6,R6),HEXTAB2          CONVERT TO PRINTABLE HEX
         LA    R2,3(0,R2)               BUMP UP DATA PTR
         MVI   6(R6),C' '               PAD
         B     DUMPEXIT                 DUMP COMPLETE
         SPACE 1
***********************************************************************
* TWO BYTES LEFT TO FILL, FILL IT AND BRANCH TO END PROGRAM.          *
***********************************************************************
         SPACE 1
DUMPL2   DS    0H
         MVC   12(2,R13),0(R2)          MOVE IN LAST 2 BYTES
         UNPK  0(5,R6),12(3,R13)        SPREAD OUT HEX DIGITS
         TR    0(4,R6),HEXTAB2          CONVERT TO PRINTABLE HEX
         LA    R2,2(0,R2)               BUMP UP DATA PTR
         MVI   4(R6),C' '               PAD
         B     DUMPEXIT                 DUMP COMPLETE
         SPACE 1
***********************************************************************
* ONE BYTE LEFT TO FILL, FILL IT AND BRANCH TO END PROGRAM.           *
***********************************************************************
         SPACE 1
DUMPL1   DS    0H
         MVC   12(1,R13),0(R2)          MOVE IN LAST 1 BYTE
         UNPK  0(3,R6),12(2,R13)        SPREAD OUT HEX DIGITS
         TR    0(2,R6),HEXTAB2          CONVERT TO PRINTABLE HEX
         LA    R2,1(0,R2)               BUMP UP DATA PTR
         MVI   2(R6),C' '               PAD
         SPACE 1
***********************************************************************
* PROGRAM IS FINISHED, RESTORE REGISTERS, AND PASS OUTPUT BUFFER BACK *
* TO CALLER.                                                          *
***********************************************************************
         SPACE 1
DUMPEXIT DS    0H
         ST    R2,0(0,R1)               SAVE NEW DATA POINTER
         ST    R4,8(0,R1)               SAVE NEW RELATIVE ADDRESS
         SPACE 1
         #STOP ,                        RETURN
         EJECT 1
         CNOP  0,8
HEXTAB1  DC    256C'.'
         ORG   HEXTAB1+X'81'                                   #DD99292
         DC    9AL1(*-HEXTAB1)      LOWER CASE A-I             #DD99292
         ORG   HEXTAB1+X'91'                   J-R             #DD99292
         DC    9AL1(*-HEXTAB1)                 S-Z             #DD99292
         ORG   HEXTAB1+X'A2'                                   #DD99292
         DC    9AL1(*-HEXTAB1)                                 #DD99292
         ORG   HEXTAB1+C' '
         DC    C' '
         ORG   HEXTAB1+C'<'
         DC    C'<'
         ORG   HEXTAB1+C'('
         DC    C'('
         ORG   HEXTAB1+C'+'
         DC    C'+'
         ORG   HEXTAB1+C'×'
         DC    C'×'
         ORG   HEXTAB1+C'&&'
         DC    C'&&'
         ORG   HEXTAB1+C'!'
         DC    C'!'
         ORG   HEXTAB1+C'$'
         DC    C'$'
         ORG   HEXTAB1+C'*'
         DC    C'*'
         ORG   HEXTAB1+C')'
         DC    C')'
         ORG   HEXTAB1+C';'
         DC    C';'
         ORG   HEXTAB1+C'^'
         DC    C'^'
         ORG   HEXTAB1+C'-'
         DC    C'-'
         ORG   HEXTAB1+C'/'
         DC    C'/'
         ORG   HEXTAB1+C','
         DC    C','
         ORG   HEXTAB1+C'%'
         DC    C'%'
         ORG   HEXTAB1+C'_'
         DC    C'_'
         ORG   HEXTAB1+C'>'
         DC    C'>'
         ORG   HEXTAB1+C'?'
         DC    C'?'
         ORG   HEXTAB1+C':'
         DC    C':'
         ORG   HEXTAB1+C'#'
         DC    C'#'
         ORG   HEXTAB1+C'@'
         DC    C'@'
         ORG   HEXTAB1+C''''
         DC    C''''
         ORG   HEXTAB1+C'='
         DC    C'='
         ORG   HEXTAB1+C'"'
         DC    C'"'
         ORG   HEXTAB1+C'{'
         DC    C'{'
         ORG   HEXTAB1+C'A'
         DC    9AL1(*-HEXTAB1)
         ORG   HEXTAB1+C'}'
         DC    C'}'
         ORG   HEXTAB1+C'J'
         DC    9AL1(*-HEXTAB1)
         ORG   HEXTAB1+C'S'
         DC    8AL1(*-HEXTAB1)
         ORG   HEXTAB1+C'0'
         DC    10AL1(*-HEXTAB1)
         ORG   ,
         SPACE 1
         ORG   *-C'0'
HEXTAB2  DS    0H
         ORG   *+C'0'
         DC    C'0123456789ABCDEF'
         SPACE 1
         #STARTWA ,
         #STOPWA ,
         EJECT 1
BUFDSECT DSECT ,R6
         ORG   BUFDSECT+(4*9)+2
BL80HEX  DS    0C
         ORG   BUFDSECT+(8*9)+2
BL130HEX DS    0C
         SPACE 1
         #DSECTS CVT
         IEZBITS ,
         END   ,
./ ADD NAME=LDS      0100-00040-00040-1222-01942-01942-00000-SOURCE
LDS      TITLE   'LDS - TSO "LISTDS" + "LSPACE" REPLACEMENT COMMAND'
***********************************************************************
**                                                                    *
**   FUNCTION-                                                        *
**      THE LDS COMMAND DISPLAYS BASIC ATTRIBUTES OF USER SPECIFIED   *
**     DATASETS, EITHER CATALOGUED OR UNCATALOGUED.                   *
**                                                                    *
**   SYNTAX-                                                          *
**      LDS 'DSLIST' SPACE DCB MEMBERS FROM(MEMBER) TO(MEMBER) LOCATE *
**                   VOL(VOLSER LIST)                                 *
**                                                                    *
**      REQUIRED- 'DSLIST'                                            *
**      DEFAULTS- NONE                                                *
**                                                                    *
**   OPERANDS                                                         *
**      'DSLIST'  - A LIST OF ONE OR MORE DSNAMES CONFORMING          *
**                  TO TSO DATASET NAMING CONVENTIONS, FOR            *
**                  WHICH THE INFORMATION IS REQUESTED. IF A          *
**                  DATASET IS NOT CATALOGUED, THE USER IS            *
**                  PROMPTED FOR A VOLUME/SERIAL NUMBER.              *
**                                                                    *
**      DCB       - THE FOLLOWING DCB ATTRIBUTES ARE LISTED.          *
**                    .DSORG                                          *
**                    .RECFM                                          *
**                    .LRECL                                          *
**                    .BLKSIZE                                        *
**                    .OPTCD                                          *
**                    .KEYLEN                                         *
**                    .RKP                                            *
**                    .CREATE DATE                                    *
**                    .EXPIRY DATE                                    *
**                    .DATE LAST REFERENCED                           *
**                                                                    *
**      SPACE     - THE FOLLOWING SPACE PARAMETERS ARE LISTED.        *
**                    .TRACKS ALLOCATED                               *
**                    .TRACKS USED                                    *
**                    .NUMBER OF EXTENTS                              *
**                    .SECONDARY ALLOCATION                           *
**                                                                    *
**      MEMBERS   - MEMBER AND ALIAS NAMES OF PARTITIONED             *
**                  DATASETS ARE DISPLAY. ALIASES ARE FLAGGED         *
**                  WITH AN ASTERISK(*). THE NUMBER OF                *
**                  DIRECTORY BLOCKS USED AND NOT USED ARE            *
**                  ALSO DISPLAYED.                                   *
**                                                                    *
**                - A MASK MAY BE ENTERED WITH THE MEMBERS PARAMETER  *
**                  TO CONTROL WHICH MEMBER NAMES WILL BE DISPLAYED.  *
**                - A ? FOLLOWED BY 1 OR MORE CHARACTERS, INDICATES   *
**                  THAT THE MEMBER NAME MUST END WITH THE CHARACTERS *
**                  FOLLOWING THE ?.                                  *
**                - AN * IS A PLACE HOLDER IN THE MASK. THE CORRES-   *
**                  PONDING POSITION IN THE MEMBER NAME MAY CONTAIN   *
**                  ANY NON-BLANK CHARACTER.                          *
**                                                                    *
**      ALL       - THIS IS EQUIVALENT TO SPECIFYING 'DCB', 'SPACE',  *
**                  AND 'MEMBERS' PARAMETERS. I.E. ALL OPTIONS ARE    *
**                  TO BE USED.                                       *
**                                                                    *
**      FROM      - THE BEGINNING MEMBER NAME OF A RANGE OF NAMES     *
**                  TO BE LISTED BY THE 'MEMBERS' OPERAND. IF THIS    *
**                  OPERAND IS OMITTED, THE MEMBER NAMES ARE LISTED   *
**                  STARTING AT THE BEGINNING MEMBER NAME THROUGH TO  *
**                  THE MEMBER NAME SPECIFIED BY THE 'TO' OPERAND.    *
**                  IF 'FROM' IS SPECIFIED, 'MEMBERS' IS ASSUMED.     *
**                                                                    *
**      TO        - THE LAST MEMBER NAME OF A RANGE OF NAMES TO BE    *
**                  LISTED BY THE 'MEMBERS' OPERAND. IF THIS OPERAND  *
**                  IS NOT SPECIFIED THE NAMES STARTING AT THE        *
**                  'FROM' NAME TO THE LAST MEMBER NAME ARE LISTED.   *
**                  IF 'TO' IS SPECIFIED, 'MEMBERS' IS ASSUMED.       *
**                                                                    *
**      LOCATE    - ALL OCCURRENCES OF THE DATASET(S) ARE LISTED.     *
**                  THE ONLINE PACKS (DEFAULT) ARE SCANNED            *
**                  FOR ANY OCCURRENCES OF THE DATASET.               *
**                                                                    *
**      VOL       - A LIST OF VOLSERS WHICH ARE TO BE SCANNED         *
**                  FOR THE DATASETS.                                 *
**                                                                    *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
**                                                                    *
**       HISTORY:                                                     *
**                                                                    *
***********************************************************************
         EJECT 1
LDS      CSECT ,
         USING *,R15
***********************************************************************
***  SET UP OUR SAVE AREA AND CHAINING PTRS.
***********************************************************************
         SPACE 1
         SAVE  (14,12),,*         SAVE CALLER'S REGISTERS
         #COPY GEN,BR=YES,YEAR=1999
         LR    R2,R1              GET ADDRESS OF CPPL FROM TMP.
         USING CPPL,R2            SET UP ADDRESSABILITY TO IT.
         SPACE 1
***********************************************************************
*  REGISTER 13 IS USED AS A BASE REGISTER IN THIS PROGRAM. BECAUSE OF *
*  THIS, EXTREME CARE SHOULD BE TAKEN IF CHANGING THE FOLLOWING CODE. *
***********************************************************************
         SPACE 1
         BALR  R15,0              SET UP TEMPORARY BASE
         USING *,R15              SET UP ADDRESSABILITY TO BASE
         LA    R1,SAVEAREA        LOAD ADDRESS OF THIS SAVEAREA
         ST    R1,8(R13)          STORE ADDRESS OF THIS SAVEAREA IN
*                                 CALLERS SAVEAREA
         ST    R13,SAVEAREA+4     STORE ADDRESS OF CALLERS SAVEAREA
*                                 IN THIS SAVEAREA
         LR    R13,R1             LOAD ADDRESS OF THIS SAVEAREA
*                                 INTO R13
         B     LDSCONT            BRANCH AROUND ADDRESSES
         DROP  R15                DROP TEMPORARY BASE
         DS    0F                 FULLWORD BOUNDARY FOR BASE
         USING *,R13,R11,R12      ESTABLISH NEW BASE REGISTERS
SAVEAREA DC    18F'-1'
BASE2    DC    A(SAVEAREA+4096)
BASE3    DC    A(SAVEAREA+8192)
LDSCONT  DS    0H
         LM    R11,R12,BASE2      LOAD SECOND AND THIRD BASE REGS.
         SPACE 1
***********************************************************************
         SPACE 1
         EXTRACT EXTOUT,'S',FIELDS=(TIOT,TSO)
         L     R10,TSOFLAG        LOAD ADDRESS OF T.S. FLAG
         TM    0(R10),X'80'       IS TSO FLAG ON???
         BO    TSO                YES...
         OI    NOPSKIP+1,X'F0'
TSO      DS    0H
         SPACE 1
***********************************************************************
*** EXTRACT THE DATASET NAME PREFIX FROM THE USER'S PROFILE TABLE
***********************************************************************
         SPACE 1
         L     R5,CPPLPSCB        GET ADDRESS OF PSCB
         USING PSCB,R5            SET UP ADDRESSABILITY TO IT.
         L     R6,PSCBUPT         GET ADDRESS OF UPT
         SR    R7,R7              ZERO OUT REG. 7
         IC    R7,23(R6)          PLACE PREFIX LENGTH INTO REG. 7
         ST    R7,DSNPREFL        STORE THE PREFIX LENGTH FOR LATER
         MVC   DSNPREF(7),16(R6) STORE THE PREFIX FOR LATER
         SPACE 1
***********************************************************************
***  BUILD THE PARSE PARAMETER LIST (PDL) AND
***  THE DAIR PARAMETER LIST (DAPL).
***********************************************************************
         SPACE 1
         L     R3,CPPLUPT         ADDRESS OF UPT
         L     R4,CPPLECT         ADDRESS OF ECT
         LA    R5,ECB             EVENT CONTROL BLOCK
         LA    R7,IOPLADS
         STM   R3,R5,0(R7)
         L     R6,=A(PCL)         ADDRESS OF PARSE CONTROL LIST
         LA    R7,PARSEPDL        ADDRESS OF PARSE DESCRIPTOR LIST
         L     R8,CPPLCBUF        ADDRESS OF COMMAND BUFFER
         SR    R9,R9              ANSWER WORD AREA
         STM   R3,R9,WRKPPL       STORE INTO PARSE PARAMETER LIST
         L     R6,CPPLPSCB        ADDRESS OF PSCB
         STM   R3,R6,DAPL         STORE INTO DAIR PARAMETER LIST
         ST    R6,DFPBPSCB        STORE PSCB ADDRESS IN DFPB
         L     R6,=A(DFPB)        ADDRESS OF DFPB
         STM   R3,R6,DFPL         STORE INTO DEFAULT PARAMETER LIST
         LA    R3,DSNLEN          ADDRESS OF DSNLEN, DSN
         ST    R3,DFPBDSN         STORE IN DFPB
         MVI   DFPBCODE,X'04'     SET DFPB CODE FOR DEFAULT
         DROP  R2                 FREE REGISTER 2
         SPACE 1
***********************************************************************
***  LINK TO THE PARSING ROUTINE TO PARSE THE INPUT COMMAND.
***********************************************************************
         SPACE 1
         LA    R1,WRKPPL          LOAD ADDRESS OF WRKPPL
         LINK  EP=IKJPARS         LINK TO PARSE ROUTINE
         LTR   R15,R15            CHECK FOR ZERO RETURN CODE
         BNZ   BADRET             NOPE -> TROUBLE WITH PARSE.
         CLC   PARSEPDL,=XL4'FF000000' CHECK FOR VALID ADDRESS.
         BNE   CHKCMD
BADRET   DS    0H
         LA    R9,BADMSG
         BAL   R10,ONEMESSG
         B     CMDEND
         SPACE 1
***********************************************************************
***  CHECK OUT THE COMMAND PARAMETERS.
***********************************************************************
         SPACE 1
CHKCMD   DS    0H
         L     R10,PARSEPDL       GET ADDRESS OF OUR PDE LIST
         USING IKJPARMD,R10       SET UP ADDRESSABILITY TO IT
         MVC   REQFLAGS(5),=CL5'NNNYN' SET REQUEST FLAGS
* CHECK THE 'ALL' PARAMETER.
         LH    R8,ALLWD           GET ALL VALUE FROM PDE
         CH    R8,=H'2'           WAS 'ALL' SPECIFIED?
         BNE   CHKDCB             NO...
         MVC   DCB(3),=C'YYY'      YES...TURN ON DCB, SPACE AND MEMBERS
         B     CHKLOCA            SKIP CHECKING OF NEXT 3 PARMS
* CHECK THE 'DCB' PARAMETER.
CHKDCB   DS    0H
         LH    R8,DCBWD           GET DCB VALUE FROM PDE
         CH    R8,=H'2'           WAS 'DCB' SPECIFIED
         BNE   CHKSPACE           NO...LEAVE 'N'
         MVI   DCB,C'Y'           YES...TURN ON 'Y'
* CHECK THE 'SPACE' PARAMETER.
CHKSPACE DS    0H
         LH    R8,SPACEWD         GET SPACE VALUE FROM PDE
         CH    R8,=H'2'           WAS 'SPACE' SPECIFIED
         BNE   CHKMEM             NO...LEAVE 'N'
         MVI   SPACE,C'Y'         YES...TURN ON 'Y'
* CHECK THE 'MEMBERS' PARAMETER.
CHKMEM   DS    0H
         LH    R8,MEMBWD          GET MEMBERS VALUE FROM PDE
         CH    R8,=H'2'           WAS 'MEMBERS' SPECIFIED
         BNE   CHKLOCA            NO..CONTINUE
         MVI   MEMB,C'Y'          MEMBER LIST WANTED
         L     R2,MASK            GET ADDRESS OF MASK
         LTR   R2,R2              IS IT ZERO?
         BZ    CHKLOCA            YES, CONTINUE THERE IS NO MASK
         MVI   MASKFLG,C'Y'       TURN ON MASK INDICATOR
         LH    R3,MASK+4          GET LENGTH OF MASK
         BCTR  R3,0               DECREASE LENGTH BY 1 FOR MOVE
         LA    R5,MEMBMASK        STORE ADDRESS OF STORAGE AREA
*                                 FOR MASK
         #EXEC R3,MVC,0(*-*,R5),0(R2)
* CHECK THE 'LOCATE' PARAMETER.
CHKLOCA  DS    0H
         LH    R8,LOCWD           GET LOCATE VALUE FROM PDE
         CH    R8,=H'2'           WAS 'LOCATE' SPECIFIED
         BE    CHKVOL             YES...SKIP NEXT INSTRUCTION
         MVI   LOCATE,C'N'        NO LOCATE SCAN WANTED
* CHECK THE 'VOL' PARAMETER
CHKVOL   DS    0H
         LA    R4,VOLTABLE        STORAGE TABLE
         USING VOLPDE,R2          R2 ADDRESSABILITY TO VOL PDE
         SR    R2,R2              CLEAR
         L     R2,VOLSER          GET VOLSER VALUE FROM PDE
         LTR   R2,R2              WAS 'VOL' SPECIFIED???
         BZ    DEFAULT            NO...SET DEFAULT LIST
         MVI   LOCATE,C'Y'
         LA    R2,VOLSER          YES...ADDRESS OF FIRST VOLSER PDE
         L     R6,ADDRVOL         ADDRESS OF VOLSER
         CLC   0(3,R6),=CL3'ALL' DOES HE WANT ALL VOLUMES???
         BNE   LOADADDR+4         NO...CONTINUE PROCESSING...FILL TABLE
         B     DEFAULTT           YES...SET DEFAULT LIST
LOADADDR DS    0H
         L     R6,ADDRVOL         ADDRESS OF VOLSER
         LH    R3,LENVOL          LENGTH OF VOLSER ENTRY
         CH    R3,=H'2'           MINIMUM OF 2             PT/MAY86
         BL    NEXTONE
         CH    R3,=H'6'           MAX. OF 6 CHARACTERS
         BNH   *+8
         LA    R3,6
         BCTR  R3,R0              DECREMENT FOR MOVE AND COMPARISON
         STH   R3,0(0,R4)         STORE FOR COMPARES
         #EXEC R3,MVC,2(*-*,R4),0(R6)
NEXTONE  DS    0H
         LA    R4,8(R4)           NEXT ENTRY IN TABLE
         L     R2,NEXTVOL         ADDRESS OF NEXT VOL PWE
         C     R2,=XL4'FF000000' ARE THERE ANY MORE???
         BNE   LOADADDR           YES...CONTINUE
         MVC   0(4,R4),0(R2)      MOVE 'FF000000' TO EOT
         B     CHKFROM            CHECK NEXT PARM
DEFAULT  DS    0H
         CLI   LOCATE,C'N'        WAS 'LOCATE' SPECIFIED???
         BE    CHKFROM            NO...
DEFAULTT DS    0H
         MVC   VOLTABLE(20),DEFAULTV YES...MOVE IN DEFAULT VOLSER LIST
         B     CHKFROM
DEFAULTV DC    H'2',CL6'RES   ',H'2',CL6'TPR   ',X'FF000000'
* CHECK THE 'FROM' PARAMETER.
CHKFROM  DS    0H
         L     R2,FROM            GET ADDRESS OF 'FROM' STRING
         LTR   R2,R2              IS IT ZERO?
         BZ    CHKTO              YES- USE THE DEFAULT
         MVI   MEMB,C'Y'          SET 'MEMBERS' INDICATOR TO 'YES'
         LH    R3,FROM+4          GET LENGTH OF 'FROM' STRING
         BCTR  R3,0               DECRMENT LENGTH BY 1 FOR MOVE
         LA    R5,FRMEMB          GET ADDRESS OF 'FROM' NAME
         #EXEC R3,MVC,0(*-*,R5),0(R2)
* CHECK THE 'TO' PARAMETER.
CHKTO    DS    0H
         L     R2,TO              GET ADDRESS OF 'TO' STRING
         LTR   R2,R2              IS IT ZERO?
         BZ    CHKDCBSP           YES- USE THE DEFAULT
         MVI   MEMB,C'Y'          SET 'MEMBERS' INDICATOR TO 'YES'
         LH    R3,TO+4            GET LENGTH OF 'TO' STRING
         BCTR  R3,0               DECRMENT LENGTH BY 1 FOR MOVE
         LA    R5,TOMEMB          GET ADDRESS OF 'TO' NAME
         #EXEC R3,MVC,0(*-*,R5),0(R2)
* DID HE ASK FOR ANY OF DCB, SPACE OR MEMBER INFORMATION???
CHKDCBSP DS    0H
         CLC   DCB(3),=C'NNN'     DID HE ASK FOR DCB,SPACE,MEMBER???
         BNE   SKIPTO             ONE OF THEM...SO LEAVE HIM ALONE
         MVC   DCB(2),=C'YY'      NO..SO TURN ON DEFAULTS FOR DCB,SPACE
SKIPTO   DS    0H
         SPACE 1
***********************************************************************
***  LOOP THROUGH THE PARSE DESCRIPTOR ENTRIES (PDE'S) AND
***  PROCESS EACH DSNAME FOUND.
***********************************************************************
         SPACE 1
         LA    R8,DSNLIST         GET ADDRESS OF THE FIRST DSN PDE
         USING DSNPDE,R8          SET UP ADDRESSABILITY TO IT
         DROP  R10                FREE REGISTER 10
NEXTDSN  DS    0H
         BAL   R10,BLDDSN         BRANCH TO BUILD FULL DSNAME.
         XC    FLGS,FLGS
         CLI   LOCATE,C'Y'        WAS 'LOCATE' SPECIFIED?
         BE    PACKSCAN           IF SO, DO PACK SCANING
         BAL   R10,FINDONE        OTHERWISE, USE THE CATALOGUE
         B     GETPDE             GET THE NEXT DATASET NAME
PACKSCAN DS    0H
         BAL   R10,FINDALL        BRANCH TO PACK SCANNING RTN
GETPDE   DS    0H
         L     R6,NEXTPDE         GET ADDRESS OF THE NEXT PDE
         C     R6,=XL4'FF000000' ARE THERE ANY MORE???
         BE    CMDEND
         LR    R8,R6
         B     NEXTDSN
         EJECT 1
***********************************************************************
***  RETURN TO THE TSO TERMINAL MONITOR PROGRAM.
***********************************************************************
         SPACE 1
CMDEND   DS    0H
         L     R13,4(R13)         RESET REGISTER 13
         RETURN (14,12),T,RC=0    RETURN TO THE TMP.
         LTORG ,
         EJECT 1
***********************************************************************
***  THIS ROUTINE SEARCHES THE CATALOGUE FOR THE SPECIFIED DATASET.
***  IF AN ENTRY IS FOUND FOR THE DATASET, THE REQUESTED INFORMATION
***  IS DISPLAYED. IF THE DATASET IS NOT CATALOGUED, THE USER IS
***  PROMPTED FOR A VOLUME SERIAL NUMBER.
***********************************************************************
         SPACE 1
FINDONE  DS    0H
         ST    R10,SAVE10         STORE THE RETURN ADDRESS
         BAL   R10,DETVOL         GET THE VOL/SER NUMBER
         LTR   R9,R9              IS EVERYTHING OKAY?
         BZ    CONTIN0            YES...
         CH    R9,=H'2'           WAS RETURN = 2 ?
         BE    FORETRN            YES..DATASET NOT CATALOGUED AND
*                                 VOLSER NOT SPECIFIED
         MVI   ERRMSG,C' '
         MVC   ERRMSG+1(79),ERRMSG
         LA    R5,ERRMSG
         AH    R5,DSNLEN
         MVC   ERRMSG(44),DSN
         MVC   1(35,R5),ALLONACC
         LA    R9,ERRMSGP
         BAL   R10,ONEMESSG
         B     FORETRN
CONTIN0  DS    0H
         LA    R5,VOL             ADDRESS OF VOLSER
         BAL   R10,ALLODSET       ATTEMPT TO ALLOCATE THIS DATASET
         LTR   R9,R9              WAS IT SUCCESSFUL?
         BNZ   FORETRN            IF NOT- RETURN TO MAINLINE
         BAL   R10,GETDSCB        BRANCH TO READ DSCB'S
         LTR   R9,R9              WAS THE DSCB READ SUCCESSFUL?
         BNZ   FREEDS             IF NOT- TRY NEXT PDE.
         CLI   DCB,C'Y'           DOES HE WANT DCB INFO?
         BNE   *+8                NO, SKIP NEXT INSTRUCTION
         BAL   R10,LISTDCB        BRANCH TO LIST DCB INFO.
         CLI   SPACE,C'Y'         DOES HE WANT SPACE INFO?
         BNE   *+8                NO, SKIP NEXT INSTRUCTION
         BAL   R10,LISTSPC        BRANCH TO LIST SPACE INFO.
         CLI   MEMB,C'Y'          DOES HE WANT MEMBER NAMES?
         BNE   *+8                NO, SKIP NEXT INSTRUCTION.
         BAL   R10,LISTMEM        BRANCH TO LIST MEMBER NAMES.
FREEDS   DS    0H
         BAL   R10,FREEDSET       FREE UP THE CURR. DATASET ALLOC.
FORETRN  DS    0H
         L     R10,SAVE10         RESTORE THE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
         EJECT 1
***********************************************************************
***  THIS ROUTINE USES THE UCB ADDRESS TABLE TO GET THE VOLUME SERIAL
***  NUMBER OF EACH ONLINE PACK WHICH IS MOUNTED. EACH SUCH PACK
***  IS SCANNED FOR THE EXISTENCE OF THE SPECIFIED DATASET. IF THE
***  DATASET EXISTS ON A PACK, THE REQUESTED INFORMATION IS THEN
***  DISPLAYED AND THE NEXT PACK IS THEN SCANNED.
***********************************************************************
         SPACE 1
FINDALL  DS    0H
         ST    R10,SAVE10         STORE THE RETURN ADDRESS
         LOCATE CATBLK            CHECK IF DATASET IS CATALOGUED?
         BAL   R10,CHKALIAS       GO SEE IF IT'S DATASET NAME WAS
*                                 AN ALIAS
         MVC   DSNLINE+5(75),DSNLINE+4 BUILD DISPLAY LINE
         MVC   DSNLINE+7(44),DSN  MOVE IN THE DATASET NAME
         LA    R4,DSNLINE         GET DSN LINE ADDRESS
         AH    R4,DSNLEN          POSITION FOR OUR MESSAGE
         CH    R15,=H'0'          WAS DATASET CATALOGUED?
         BE    CATLGD             YES..
         CH    R15,=H'8'          IS IT UNAUTHORIZED???
         BNE   NOTCATLG           NO...SOME OTHER PROBLEM
         CH    R0,=H'56'          IS IT UNAUTHORIZED???
         BE    FARETRN            YES...SO BYPASS AND GET OUT OF HERE
         B     NOTCATLG
CATLGD   DS    0H
         MVC   10(17,R4),=CL17'IS CATALOGUED ON '
         MVC   27(6,R4),WKAREA+6 MOVE IN THE VOLUME SERIAL NUMBER
         B     TPUTDSNL           BRANCH TO TPUT CODE
NOTCATLG DS    0H
         MVC   10(18,R4),=CL18'IS NOT CATALOGUED.'
TPUTDSNL DS    0H
         LA    R9,DSNLINEP
         BAL   R10,ONEMESSG
         XC    FLGS,FLGS          INDICATE DATASET NOT FOUND YET
         SPACE 1
*   THIS CODE REPLACES THE OLD UCB SEARCH ROUTINE AND IS BOTH
*   MVS/370 AND MVS/XA COMPATABLE.               ***        MAY83   ***
*   AGAIN...REPLACED CVTUCBSC WITH UCBSCAN       ***   DSD  MAY98   ***
         SPACE 1
         XC    UCBSWORK,UCBSWORK  INITIALIZE WORK AREA         #DD98129
NEXTUCB1 DS    0H
         UCBSCAN COPY,            REQUEST A COPY OF A UCB      #DD98129*
               DEVNCHAR=UCBDEV#,                               #DD98129*
               DEVCLASS=DASD,                                  #DD98129*
               DYNAMIC=YES,                                    #DD98129*
               RANGE=ALL,                                      #DD98129*
               UCBAREA=UCBSAREA,                               #DD98129*
               WORKAREA=UCBSWORK,                              #DD98129*
               MF=(E,UCBSLST)                                  #DD98129
         LTR   R15,R15            ARE YOUR HANDS DIRTY ? NO MORE UCBS ?
         BNZ   UCBEND             IF SO, GO WASH THEM.  WE ARE DONE.
         LA    R3,UCBSAREA        A(RETURNED UCB)              #DD98129
         USING UCBOB,R3
         TM    UCBSTAT,UCBONLI    CHECK FOR ONLINE STATUS ???
         BNO   NEXTUCB1           IF NOT TRY TRY AGAIN
***   ***   ***   ***   ***   ***   ***   ***   ***        MAY83   ***
         SPACE 1
         LA    R4,VOLTABLE
         SR    R5,R5
DOWANT   DS    0H
         LH    R5,VOLTABLE
         EX    R5,COMPVOL
         BE    GOCHECK
         LA    R4,8(R4)
         CLI   0(R4),X'FF'
         BE    NEXTUCB1
         B     DOWANT
COMPVOL  CLC   2(0,R4),UCBVOLI     EXECUTED
GOCHECK  DS    0H
         MVC   VOL,UCBVOLI        LOAD IN PACK VOLUME SERIAL NUMBER
         MVC   DYNAMUCB,UCBNAME   STORE UCB NAME FOR DYNAM ALLOC.
         MVC   UCBTYPE,UCBUNTYP   STORE UCB DEVICE TYPE CODE
         STM   R2,R3,SAVE28
         BAL   R10,ALLODSET       ATTEMPT DATASET ALLOCATION
         LTR   R9,R9              DID IT WORK?
         BNZ   DONEPACK           IF NOT, SKIP THIS PACK
         BAL   R10,GETDSCB        ATTEMPT TO READ THE DSCB'S
         LTR   R9,R9              DID WE GET THEM?
         BNZ   FREEUP             IF NOT, SKIP THIS PACK
         OI    FLGS,FOUND         INDICATE DATASET HAS BEEN FOUND
         CLI   DCB,C'Y'           DOES HE WANT DCB INFO?
         BNE   *+8                NO, SKIP NEXT INSTRUCTION
         BAL   R10,LISTDCB        BRANCH TO LIST DCB INFO.
         CLI   SPACE,C'Y'         DOES HE WANT SPACE INFO?
         BNE   *+8                NO, SKIP NEXT INSTRUCTION
         BAL   R10,LISTSPC        BRANCH TO LIST SPACE INFO.
         CLI   MEMB,C'Y'          DOES HE WANT MEMBER NAMES?
         BNE   *+8                NO, SKIP NEXT INSTRUCTION.
         BAL   R10,LISTMEM        BRANCH TO LIST MEMBER NAMES.
FREEUP   DS    0H
         BAL   R10,FREEDSET       FREE UP THE CURR. DATASET ALLOC.
DONEPACK DS    0H
         LM    R2,R3,SAVE28       RESTORE REGISTERS 2 AND 3
         B     NEXTUCB1           UCB
UCBEND   DS    0H
         TM    FLGS,FOUND+ALLOERR DATASET FOUND /ALLOC ERROR?
         BNZ   FARETRN            IF SO, RETURN TO MAINLINE
         LA    R4,DSNLINE         GET DSNLINE ADDRESS
         AH    R4,DSNLEN
         MVC   10(23,R4),=CL23'WAS NOT FOUND.    '
         LA    R9,DSNLINEP
         BAL   R10,ONEMESSG
FARETRN  DS    0H
         L     R10,SAVE10         RESTORE RETURN REGISTER
         BR    R10                RETURN TO MAINLINE
SAVREG03 DC    F'0'
H3       DC    H'3'
H13      DC    H'13'
DS3PTRDS DC    CL5' '
         EJECT 1
***********************************************************************
***  THIS ROUTINE PROCESSES ONE DSNAME FROM THE PDE LIST.
***  IT TAKES THE CURRENT DSNAME AND IF IT WAS NOT ENTERED
***  IN QUOTES, THE DATASET NAME PREFIX IS ADDED TO THE
***  BEGINNING OF IT. THE FULL DATASET NAME IS THEN STORED
***  IN THE FIELD CALLED 'DSN'.
***********************************************************************
         SPACE 1
BLDDSN   DS    0H
         SR    R9,R9
         MVI   DSN,C' '           SET DSNAME FIELD TO BLANKS
         MVC   DSN+1(43),DSN
         L     R6,ADDRDSN         GET THE ADDRESS OF THE CURRENT DSN
         LH    R5,LENDSN          GET THE DSNAME LENGTH
         TM    FLGSDSN,X'C0'      WAS IT SPECIFIED WITHIN QUOTES?
         BNO   NOQUOTES           IF NO, THEN BUILD FULL DSN.
DECREM5  DS    0H
         STH   R5,DSNLEN          STORE THE DSN LENGTH
         BCTR  R5,0               DECREMENT LENGTH BY 1 FOR EX
         #EXEC R5,MVC,DSN(*-*),0(R6)
         BR    R10                RETURN TO MAINLINE
NOQUOTES DS    0H
         LA    R4,DSN             GET ADDRESS OF OUR DSN FIELD
         L     R3,DSNPREFL        GET THE DSN PREFIX LENGTH
         LTR   R3,R3              DID HE SPECIFY 'NOPREFIX' IN PROFILE?
         BZ    DECREM5            YES...USE DSN AS IS
         BCTR  R3,0               DECREMENT IT BY 1 FOR EX
         #EXEC R3,MVC,DSN(*-*),DSNPREF
         AR    R4,R3              ADD LENGTH TO ADDRESS
         MVI   1(R4),C'.'         STICK IN THE DOT AFTER THE PREFIX
         LA    R4,2(R4)           GET ADDRESS OF NEXT BLANK BYTE
         SR    R3,R3              CLEAR REG. 3
         LH    R3,LENDSN          GET THE LENGTH OF THE DSN
         BCTR  R3,0               DECREMENT IT BY 1 FOR EX
         #EXEC R3,MVC,0(*-*,R4),0(R6)
         L     R3,DSNPREFL        GET DSN PREFIX LENGTH
         AH    R3,LENDSN          ADD LENGTH OF SUPPLIED DSN
         LA    R3,1(R3)           INCREMENT LENGTH BY 1 FOR DOT
         STH   R3,DSNLEN          STORE THE DSN LENGTH
         LA    R1,DFPL            CHECK FOR A FULLY QUALIFIED DSN
         LINK  EP=IKJEHDEF        LINK TO DEFAULT SERVICE RTN
         MVC   STDSN,DSN          STORE DSN IN CASE ITS AN ALIAS
         BR    R10                RETURN TO MAINLINE.
         EJECT 1
***********************************************************************
***  THIS ROUTINE DETERMINES THE VOLUME/SERIAL NUMBER FOR THE
***  CURRENT DATASET. IF THE DATASET IS CATALOGUED, IT OBTAINS
***  THE VOLUME/SERIAL NUMBER FROM THE CATALOGUE. OTHERWISE IT
***  PROMPTS THE USER FOR THE VOLUME/SERIAL NUMBER.
***  ONCE THIS IS DONE, IT THEN GOES AND OBTAINS THE DSCB'S
***  FOR THE CURRENT DATASET AND RETURNS.
***********************************************************************
         SPACE 1
DETVOL   DS    0H
         MVC   VOL(6),=CL6' '
         ST    R10,DETSTORE
         LOCATE CATBLK            READ THE CATALOGUE FOR THE DSN
         BAL   R10,CHKALIAS       GO SEE IF DSNAME WAS AN ALIAS
         LTR   R9,R15             DID WE GET A VALID ENTRY?
         BZ    GOTCAT             IF YES, MOVE VOLSER NUMBER
         MVI   ERRMSG,C' '
         MVC   ERRMSG+1(79),ERRMSG  SET MESSAGE BUFFER TO BLANKS
         MVC   ERRMSG(44),DSN
         LA    R4,ERRMSG
         AH    R4,DSNLEN
         MVC   1(18,R4),=CL18'IS NOT CATALOGUED.'
         LA    R9,ERRMSGP
         BAL   R10,ONEMESSG
         SPACE 1
NOPSKIP  NOP   SKIPDS              SELF MODIFYING CODE (BARF)
         TPUT  GETVOLM,L'GETVOLM,ASIS PROMPT THE USER FOR
         TGET  TGETVOL,10         THE VOLUME SERIAL NUMBER.
         LTR   R15,R15            DID WE GET IT FROM HIM?
         BNZ   SKIPDS             IF NO, SKIP THIS DATASET
         LTR   R1,R1              IS THE LENGTH ZERO?
         BZ    SKIPDS             YES, SKIP THIS DATASET.
         MVC   VOL(6),TGETVOL     MOVE VOL/SER FROM MESSAGE BUFFER
         OC    VOL(6),BLK8        CONVERT TO UPPERCASE
         SR    R9,R9              INDICATE NO ERRORS SO FAR
         B     GOTCAT1
SKIPDS   DS    0H
         LA    R9,2               SET RETURN CODE REGISTER
         L     R10,DETSTORE       RESTORE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
GOTCAT   DS    0H
         MVC   VOL,WKAREA+6       MOVE VOL/SER FROM WORK AREA
GOTCAT1  DS    0H
         SR    R9,R9
         SPACE 1
*   THIS CODE REPLACES THE OLD UCB SEARCH ROUTINE AND IS BOTH
*   MVS/370 AND MVS/XA COMPATABLE.   ***   ***   ***        MAY83   ***
*   AGAIN...REPLACED CVTUCBSC WITH UCBSCAN       ***   DSD  MAY98   ***
         SPACE 1
         XC    UCBSWORK,UCBSWORK  INITIALIZE WORK AREA         #DD98129
NEXTUCB2 DS    0H
         UCBSCAN COPY,            REQUEST A COPY OF A UCB      #DD98129*
               DEVNCHAR=UCBDEV#,                               #DD98129*
               DEVCLASS=DASD,                                  #DD98129*
               DYNAMIC=YES,                                    #DD98129*
               RANGE=ALL,                                      #DD98129*
               UCBAREA=UCBSAREA,                               #DD98129*
               WORKAREA=UCBSWORK,                              #DD98129*
               MF=(E,UCBSLST)                                  #DD98129
         LTR   R15,R15            ARE YOUR HANDS DIRTY ? NO MORE UCBS ?
         BNZ   UCBEND2            IF SO, GO WASH THEM.  WE ARE DONE.
         LA    R3,UCBSAREA        A(RETURNED UCB)              #DD98129
         USING UCBOB,R3
         TM    UCBSTAT,UCBONLI    CHECK FOR ONLINE STATUS ???
         BNO   NEXTUCB2           IF NOT TRY TRY AGAIN
***   ***   ***   ***   ***   ***   ***   ***   ***        MAY83   ***
         SPACE 1
         CLC   VOL,UCBVOLI        IS THIS THE ONE WE'RE LOOKING FOR
         BNE   NEXTUCB2           NO....TRY NEXT UCB
         SR    R9,R9
         MVC   DYNAMUCB,UCBNAME   STORE UCB NAME FOR DYNAM ALLOC.
         MVC   UCBTYPE,UCBUNTYP   STORE UCB DEVICE TYPE CODE
         L     R10,DETSTORE
         BR    R10
UCBEND2  DS    0H
         LA    R9,1
         L     R10,DETSTORE
         BR    R10
         LTORG ,
         EJECT 1
***********************************************************************
*** THIS ROUTINE DETERMINES IF THE DATASET NAME SPECIFIED IN THE LDS **
*** COMMAND IS AN ALIAS, AND IF IT IS CALCULATES THE CORRECT DATASET **
*** NAME LENGTH.                                                     **
***********************************************************************
CHKALIAS DS    0H
         CLC   DSN,STDSN           IS DSNAME FROM LOCATE = DSNAME
*                                  ENTERED WITH LDS COMMAND?
         BE    CHKRET              YES..DSNAME NOT AN ALIAS
         LA    R2,DSN              START OF DSN FIELD
         LA    R3,DSN+43           END OF DSN FIELD
CHK010   DS    0H
         CLI   0(R3),C' '          IS THIS THE END OF THE DSNAME?
         BNE   CHK020              YES..
         BCT   R3,CHK010           NO..KEEP LOOKING
CHK020   DS    0H
         SR    R3,R2
         LA    R3,1(R3)            LENGTH OF DSNAME
         STH   R3,DSNLEN           STORE LENGTH
CHKRET   DS    0H
         BR    R10                 RETURN
         EJECT 1
***********************************************************************
***  THIS ROUTINE ATTEMPTS TO ALLOCATE THE CURRENT DATASET.          **
***  UPON RETURN, REGISTER 9 CONTAINS THE RESULT OF THE ALLOCATION   **
***********************************************************************
ALLODSET DS    0H
         ST    R10,RETADDR
         MVC   DYNAMVOL(6),VOL     PUT VOLUME SERIAL IN CTL LIST
         MVC   DYNAMDSN(44),DSN
         MVC   DYNAMDDN(2),=C'$$'
         MVC   DYNAMDDN+2(6),VOL
         OBTAIN DSCBLK            ATTEMPT TO GET FMT 1 DSCB
         LTR   R9,R15             CHECK FOR SUCCESS
         BZ    LINKALLO
         CLI   LOCATE,C'Y'        CHECK FOR PACK SCANNING
         BE    ALLOERET
         B     DSCBERR            NO...SKIP THIS PACK TO AVOID
*               ENQ ERROR WITH IBM ALLOCATION ERROR
LINKALLO DS    0H
         CLI   MEMB,C'Y'              MEMBER LIST WANTED?
         BNE   SUCCESS                IF NOT, RETURN
         LA    R1,DYNRBADD
         DYNALLOC
         LTR   R15,R15
         BZ    SUCCESS
         OI    FLGS,ALLOERR           SET FLAG TO ALLOC ERROR
         CLI   LOCATE,C'Y'            FOR LOCATE, BYPASS THE MESSAGES
         BE    ALLOERET
         SPACE 1
         LA    R14,DYNAMRB
         S99FAIL
         SPACE 1
         B     ALLOERET
         SPACE 1
*
*  SUCCESSFUL COMPLETION
*
SUCCESS  DS    0H
         MVC   SYSLIB+40(8),DYNAMDDN
         MVC   FAIRDDDN,DYNAMDDN          SAVE DDNAME FOR LIBRARIAN
         BR    R10
         SPACE 1
*
*  R15 = 4
*  AN ERROR RESULTED FROM THE CURRENT ENVIRONMENT, THE UNAVAILABILITY
*  OF A SYSTEM RESOURCE, OR A SYSTEM ROUTINE FAILURE.
*
         SPACE 1
*
*  R15 = 8
*  THE INSTALLATION VALIDATION ROUTINE DENIED THE REQUEST.
*
*
*  R15 = 12
*  THE ERROR IS DUE TO AN INVALID PARAMETER LIST.
*
         EJECT 1
*
*  SUCCESSFUL COMPLETION
*
ALLOERET DS    0H
         LA    R9,1               RAISE THE ERROR FLAG
         L     R10,RETADDR        RESTORE THE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
         LTORG ,
         EJECT 1
***********************************************************************
***  THIS ROUTINE FREES UP THE CURRENT DATASET ALLOCATION.
***********************************************************************
FREEDSET DS    0H
         FREE  DDN=DDNPTR,ERROR=FREEERR
FREEERR  DS    0H
         BR    R10                RETURN TO MAINLINE
         EJECT 1
***********************************************************************
***  THIS ROUTINE FIRST DISPLAYS THE FULL DATASET NAME, AND THEN
***  THE VOLUME SERIAL NUMBER. IT THEN ATTEMPTS TO READ INTO CORE
***  THE DATASET CONTROL BLOCKS FOR THE CURRENT DATASET.
***********************************************************************
GETDSCB  DS    0H
         ST    R10,RETADDR        STORE THE RETURN ADDRESS
         MVC   DSNLINE+5(75),DSNLINE+4 CLEAR DSNAME PRINT LINE
         MVC   DSNLINE+7(44),DSN  MOVE IN THE DSNAME
         LA    R4,DSNLINE         GET ADDRESS OF THE DSN LINE
         AH    R4,DSNLEN          ADD LENGTH OF DSN
         MVC   10(8,R4),=CL8'VOLUME: ' MOVE IN THE VOLUME
         MVC   18(6,R4),VOL       SERIAL NUMBER
         LA    R9,DSNLINEP
         BAL   R10,ONEMESSG
         SR    R9,R9
         SPACE 1
         OBTAIN DSCBLK            ATTEMPT TO GET FMT 1 DSCB
         LTR   R15,R15            CHECK FOR SUCCESS
         BNZ   DSCBERR            IF NOT, INFORM USER
         CLI   WKAREA,X'F1'       IS IT A FORMAT 1 DSCB?
         BNE   DSCBERR            IF NOT, INFORM USER
         MVC   DS1(96),WKAREA     STORE IN OUR FMT 1 AREA
         CLC   DS1PTRDS(5),HEX00  SEE IF A FMT 2 OR 3 EXISTS.
         BE    DSCBRET            IF NOT, BRANCH TO RETURNING CODE
OBNEXT   DS    0H
         OBTAIN NEXTBLK            GET FMT 2 OR FMT 3 DSCB
         LTR   R15,R15            CHECK FOR SUCCESS
         BNZ   DSCBERR            IF NOT, INFORM USER.
         SR    R9,R9
         CLI   WKAREA,X'03'       IS IT A FMT 3
         BE    DSCBRET            OKAY. BRANCH TO RETURNING CODE
         CLI   WKAREA,X'02'       IS IT A FMT 2?
         BNE   DSCBERR        NO...
         CLC   WKAREA+135(5),HEX00  SEE IF A FORMAT 3 EXISTS
         BE    DSCBRET        IF NOT, BRANCH TO RETURNING CODE
         MVC   DS1PTRDS(5),WKAREA+135
         B     OBNEXT
DSCBERR  DS    0H
         LR    R9,R15             DSCB ERROR OCCURRED.
         C     R9,=F'16'          IS IT GREATER THAN 16
         BNH   *+8                IF NOT, SKIP NEXT INSTRUCTION
         LA    R9,20              SET VALUE TO 20 (MAX).
         LA    R4,DSCBTBL         GET ADDRESS OF DSCB ERROR TABLE
         AR    R4,R9              ADD REG. 09 TO ADDRESS OF TABLE
         L     R10,0(R4)          R10->ERROR MESSAGE
         C     R15,=F'8'          ERROR CODE = 8???
         BNE   PUTR10             NO...VOLSER NOT NEEDED IN MESSAGE
         MVC   27(6,R10),VOL      YES...STORE VOLSER IN CORRECT SPOT
PUTR10   DS    0H
         LR    R9,R10
         BAL   R10,ONEMESSG
         LA    R9,1               RAISE THE ERROR FLAG.
DSCBRET  DS    0H
         L     R10,RETADDR        RESTORE THE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
         LTORG ,
         EJECT 1
***********************************************************************
***  THIS ROUTINE DECODES AND FORMATS THE DCB INFORMATION.
***********************************************************************
         SPACE 1
LISTDCB  DS    0H
         ST    R10,RETADDR        STORE THE RETURN ADDRESS
         MVC   DSORG(3),BLK8      BLANK OUT THE DSORG FIELD
         MVC   RECFM(5),BLK8      BLANK OUT THE RECFM FIELD
         SPACE 1
***********************************************************************
**  DECODE THE DATASET ORGANIZATION.
***********************************************************************
         SPACE 1
         LA    R3,DSORGTBL        GET ADDRESS OF THE DSORG TABLE
         LA    R4,5               GET LENGTH OF A TABLE ENTRY
         LA    R5,DSORGTBX        ADDRESS OF LAST TABLE ENTRY
LIDSORG  DS    0H
         CLC   DS1DSORG(1),4(R3) CHECK FOR DSORG EQUAL
         BNE   LIDSORGB           IF NO TRY NEXT ENTRY FROM TABLE
LIDSORGA DS    0H
         MVC   DSORG(3),1(R3)     MOVE IN THE CHAR. EQUIVALENT
         B     CHCKVSAM           BRANCH TO DECODE RECFM           ×
LIDSORGB DS    0H
         BXLE  R3,R4,LIDSORG      GET ADDRESS OF NEXT TABLE ENTRY  ×
         LA    R3,DSORGUN         DSORG IS UNKNOWN                 ×
         B     LIDSORGA           BRANCH TO PUT IT INTO PRT LINE   ×
         SPACE 1
***********************************************************************
**  CHECK IF THIS HERE MIGHT BE VSAM     82/10/19  ...
***********************************************************************
         SPACE 1
CHCKVSAM DS    0H
         CLI   DS1DSORG+1,X'08'   IF VSAM ????????????????
         BNE   DORECFM            NO ........
         MVC   DSORG(3),=C'VSM'   YES ... THEN SAY SO YOU DUMMY
         SPACE 1
***********************************************************************
**  DECODE THE RECORD FORMAT.
***********************************************************************
         SPACE 1
DORECFM  DS    0H
         LA    R4,RECFM           GET ADDRESS OF RECFM IN PRT LINE
         TM    DS1RECFM,X'C0'     TEST FOR UNDEFINED LENGTH
         BNO   LIRECFA
         MVI   0(R4),C'U'
         LA    R4,1(R4)           INCRMENT NEXT AVAIL CHAR. POSITION
         B     LIRECFE
LIRECFA  DS    0H
         TM    DS1RECFM,X'80'     TEST FOR FIXED LENGTH
         BNO   LIRECFB
         MVI   0(R4),C'F'
         LA    R4,1(R4)           SET NEXT AVAIL. CHAR. POSITION
         B     LIRECFC
LIRECFB  DS    0H
         TM    DS1RECFM,X'40'     TEST FOR VARYING LENGTH
         BNO   LIRECFC
         MVI   0(R4),C'V'
         LA    R4,1(R4)           SET NEXT AVAIL. CHAR. POSITION
LIRECFC  DS    0H
         TM    DS1RECFM,X'10'     TEST FOR BLOCKED
         BNO   LIRECFD
         MVI   0(R4),C'B'
         LA    R4,1(R4)           SET NEXT AVAIL. CHAR. POSITION
LIRECFD  DS    0H
         TM    DS1RECFM,X'08'     TEST FOR STANDARD
         BNO   LIRECFE
         MVI   0(R4),C'S'
         LA    R4,1(R4)           SET NEXT AVAIL. CHAR. POSITION
LIRECFE  DS    0H
         TM    DS1RECFM,X'02'     TEST FOR MACHINE CTL CHAR.
         BNO   LIRECFF
         MVI   0(R4),C'M'
         LA    R4,1(R4)           SET NEXT. AVAIL. CHAR. POSITION
         B     LIRECFG
LIRECFF  DS    0H
         TM    DS1RECFM,X'04'     TEST FOR ASA CTL CHAR.
         BNO   LIRECFG
         MVI   0(R4),C'A'
         LA    R4,1(R4)           SET NEXT AVAIL. CHAR. POSITION
LIRECFG  DS    0H
         TM    DS1RECFM,X'20'     TEST FOR TRACK OVERFLOW
         BNO   DOLRECL
         MVI   0(R4),C'T'
         SPACE 1
***********************************************************************
*** DECODE THE LOGICAL RECORD LENGTH.
***********************************************************************
         SPACE 1
DOLRECL  DS    0H
         LH    R4,DS1LRECL        GET THE LRECL (BINARY FORMAT)
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   LRECL,DWD2+11      STORE VALUE IN PRINT LINE
         SPACE 1
**********************************************************************
***  DECODE THE BLOCK SIZE
**********************************************************************
         SPACE 1
         LH    R4,DS1BLKL         GET THE BLK LENGTH (BINARY)
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   BLKSIZE,DWD2+11    STORE VALUE IN PRINT LINE
         SPACE 1
**********************************************************************
*** DETERMINE HEX REPRESENTATION OF THE OPTCD FIELD
**********************************************************************
         SPACE 1
         SR    R4,R4              CLEAR REG. 4
         IC    R4,DS1OPTCD        GET THE OPTCD CHARACTER
         SRL   R4,4               CHOP OFF LAST 4 BITS
         STC   R4,OPTCD           STORE LAST CHAR.
         TR    OPTCD(1),HEXTAB    CONVERT TO DISPLAY CHAR.
         IC    R4,DS1OPTCD        GET THE OPTCD CHARACTER
         N     R4,=XL4'FFFFFF0F' WIPE OUT ALL BUT LAST 4 BITS
         STC   R4,OPTCD+1         STORE CHAR.
         TR    OPTCD+1(1),HEXTAB  CONVERT TO DISPLAY CHAR.
         SPACE 1
**********************************************************************
*** DECODE THE KEY LENGTH.
**********************************************************************
         SPACE 1
         SR    R4,R4              CLEAR REG. 4
         IC    R4,DS1KEYL         MOVE IN THE KEY LENGTH
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   KEYLEN,DWD2+12     STORE VALUE IN PRINT LINE
         SPACE 1
**********************************************************************
*** DECODE THE RELATIVE KEY POSITION.
**********************************************************************
         SPACE 1
         MVC   DWD1+2(2),DS1RKP   GET THE RKP FIELD
         LH    R4,DWD1+2          LOAD IT INTO REG 4
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   RKP,DWD2+13        STORE VALUE IN PRINT LINE
         SPACE 1
**********************************************************************
*** DECODE THE CREATE DATE.
**********************************************************************
         SPACE 1
         MVC   FWORD(4),HEX00     ZERO OUT A FULLWORD AREA
         MVC   FWORD(3),DS1CREDT  MOVE IN 3-BYTE CREATE DATE
         L     R4,FWORD           LOAD IT INTO REG. 4
         SRL   R4,24              SHIFT TO GET THE YEAR
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   CRTDT,DWD2+14      STORE VALUE IN PRINT LINE
         L     R4,FWORD           LOAD CRTDT BACK INTO REG. 4
         SLL   R4,8               SHIFT TO LOSE THE YEAR
         SRL   R4,16              SHIFT BACK TO GET THE DAY
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   CRTDT+2(3),DWD2+13 STORE VALUE IN PRINT LINE
         MVZ   CRTDT(5),=C'00000' SET THE ZONE FIELDS
         SPACE 1
**********************************************************************
*** DECODE THE EXPIRY DATE
**********************************************************************
         SPACE 1
         MVC   FWORD(4),HEX00     ZERO OUT A FULLWORD AREA
         MVC   FWORD(3),DS1EXPDT  MOVE IN 3-BYTE EXPIRY DATE
         L     R4,FWORD           LOAD IT INTO REG. 4
         SRL   R4,24              SHIFT TO GET THE YEAR
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   EXPDT,DWD2+14      STORE VALUE IN PRINT LINE
         L     R4,FWORD           LOAD EXPDT BACK INTO REG. 4
         SLL   R4,8               SHIFT TO LOSE THE YEAR
         SRL   R4,16              SHIFT BACK TO GET THE DAY
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   EXPDT+2(3),DWD2+13 STORE VALUE IN PRINT LINE
         MVZ   EXPDT(5),=C'00000' SET THE ZONE FIELDS
         SPACE 1
**********************************************************************
*** DECODE THE DATE LAST REFERENCED
**********************************************************************
         SPACE 1
         MVC   FWORD(4),HEX00     ZERO OUT A FULLWORD AREA
         MVC   FWORD(3),DS1REFD   MOVE IN 3-BYTE DATE LAST REF'D
         L     R4,FWORD           LOAD IT INTO REG. 4
         SRL   R4,24              SHIFT TO GET THE YEAR
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   REFDT,DWD2+14      STORE VALUE IN PRINT LINE
         L     R4,FWORD           LOAD REFDT BACK INTO REG. 4
         SLL   R4,8               SHIFT TO LOSE THE YEAR
         SRL   R4,16              SHIFT BACK TO GET THE DAY
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   REFDT+2(3),DWD2+13 STORE VALUE IN PRINT LINE
         MVZ   REFDT(5),=C'00000' SET THE ZONE FIELDS
         LA    R9,DCBTITLE
         BAL   R10,TWOMESSG
         L     R10,RETADDR        RESTORE THE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
         LTORG ,
         EJECT 1
***********************************************************************
***  THIS ROUTINE DECODES AND FORMATS THE SPACE INFORMATION.
***********************************************************************
         SPACE 1
LISTSPC  DS    0H
         ST    R10,RETADDR        STORE THE RETURN ADDRESS
         CLI   UCBTYPE,X'0B'      IS IT 3350 ?
         BNE   D3380
         MVC   TRKCYL,=H'030'     3350 HAS 30 TRKS/CYL
         B     LISTALC
D3380    DS    0H
         CLI   UCBTYPE,X'0E'      IS IT 3380 ?
         BNE   D3390                                           #DD98056
         MVC   TRKCYL,=H'015'     3380 HAS 15 TRKS/CYL
         B     LISTALC
D3390    DS    0H
         CLI   UCBTYPE,X'0F'      IS IT 3390 ?                 #DD98056
         BNE   D3330              NO - MUST BE 3330 THEN       #DD98056
         MVC   TRKCYL,=H'015'     3390 HAS 15 TRKS/CYL         #DD98056
         B     LISTALC                                         #DD98056
D3330    DS    0H
         MVC   TRKCYL,=H'019'     3330 HAS 19 TRKS/CYL
         SPACE 1
***********************************************************************
**  DETERMINE THE SECONDARY ALLOCATION UNITS (TRKS, CYLS, BLKS)
***********************************************************************
         SPACE 1
LISTALC  DS    0H
         MVC   SECU,=C' TRKS'     ASSUME DEFAULT OF TRACKS
         TM    DS1SCALO,X'C0'     TEST FOR CYLINDERS
         BNO   LI2NDAA            IF NO, TRY NEXT TEST
         MVC   SECU,=C' CYLS'     SET TO CYLINDERS
         B     DO2NDALL           BRANCH TO DECODE THE NUMBER
LI2NDAA  DS    0H
         TM    DS1SCALO,X'40'     TEST FOR BLOCKS
         BNO   DO2NDALL           IF NO, MUST BE TRACKS
         MVC   SECU,=C' BLKS'     SET TO BLOCKS
         SPACE 1
***********************************************************************
**  DETERMINE THE NUMBER OF SECONDARY ALLOCATION UNITS
***********************************************************************
         SPACE 1
DO2NDALL DS    0H
         MVI   FWORD,X'00'        ZERO FIRST BYTE OF FULLWORD
         MVC   FWORD+1(3),DS12NDQY GET THE SECONDARY QTY.
         L     R4,FWORD           LOAD REG 4
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   SECA,DWD2+13       STORE VALUE INTO PRINT LINE
         SPACE 1
***********************************************************************
**  DECODE THE NUMBER OF EXTENTS
***********************************************************************
         SPACE 1
         SR    R4,R4              CLEAR REG 4
         IC    R4,DS1NOEPV        GET NO. OF EXTENTS (BINARY)
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   EXTS,DWD2+14       STORE VALUE IN PRINT LINE
         SPACE 1
***********************************************************************
**  DETERMINE AND DECODE THE TOTAL TRACKS ALLOCATED.
***********************************************************************
         SPACE 1
         LA    R2,EXTTBL          GET ADDRESS OF EXTENTS TABLE
         SR    R4,R4              CLEAR REG 4
         ST    R4,TOTRK           INITIALIZE TOTAL TRKS TO 0
         IC    R4,DS1NOEPV        GET THE NO. OF EXTENTS
         ST    R3,SAVREG03        SAVE REGISTER 3
         XR    R3,R3              CLEAR REGISTER 3
         LH    R3,H3              REG 3, COUNTER FOR 1ST 3 EXT.
EXT03LP  DS    0H
         L     R5,0(R2)           GET NEXT EXTENT ADDRESS
         USING EXTENT,R5          SET UP ADDRESSABILITY TO IT
         CLI   ID,X'00'           SEE IF THIS IS THE LAST
         BE    NOMORE             IF YES, BRANCH
         LH    R6,CCA             GET CYL LOWER LIMIT
         MH    R6,TRKCYL          CONVERT TO TRACKS
         AH    R6,TTA             ADD TRKS LOWER LIMIT
         LH    R7,CCB             GET CYL UPPER LIMIT
         MH    R7,TRKCYL          CONVERT TO TRACKS
         AH    R7,TTB             ADD TRKS UPPER LIMIT
         LA    R7,1(R7)           ADD 1
         SR    R7,R6              SUBTRACT TO GET NO. TRACKS
         AH    R7,TOTRK           ADD TO TOTAL TRACKS
         STH   R7,TOTRK           STORE BACK INTO TOTAL TRKS
         LA    R2,4(R2)           GET ADDRESS OF NEXT TABLE ENTRY
         BCTR  R4,0               DECREMENT # OF EXTENTS
         LTR   R4,R4              ANYMORE EXTENTS?
         BZ    NOMORE             NO, THEN GET OUT OF LOOP
         BCT   R3,EXT03LP         STILL WORKING ON FIRST 3 EXT.
         XR    R3,R3              CLEAR REGISTER 03
         LH    R3,H13             REG 3,COUNTER FOR NEXT 13 EXT.
EXT13LP1 DS    0H
         LA    R2,WKTBL           GET ADDRESS OF WORK TABLE
EXT13LP2 DS    0H
         L     R5,0(R2)           GET NEXT EXTENT ADDRESS
         USING EXTENT,R5          SET UP ADDRESSABILITY TO IT
         CLI   ID,X'00'           SEE IF THIS IS THE LAST
         BE    NOMORE             IF YES, BRANCH
         LH    R6,CCA             GET CYL LOWER LIMIT
         MH    R6,TRKCYL          CONVERT TO TRACKS
         AH    R6,TTA             ADD TRKS LOWER LIMIT
         LH    R7,CCB             GET CYL UPPER LIMIT
         MH    R7,TRKCYL          CONVERT TO TRACKS
         AH    R7,TTB             ADD TRKS UPPER LIMIT
         LA    R7,1(R7)           ADD 1
         SR    R7,R6              SUBTRACT TO GET NO. TRACKS
         AH    R7,TOTRK           ADD TO TOTAL TRACKS
         STH   R7,TOTRK           STORE BACK INTO TOTAL TRKS
         LA    R2,4(R2)           GET ADDRESS OF NEXT TABLE ENTRY
         BCTR  R4,0               DECREMENT # OF EXTENTS
         LTR   R4,R4              ANYMORE EXTENTS?
         BZ    NOMORE             NO, GET OUT
         BCT   R3,EXT13LP2        STILL WORKING ON FMT3 DSCB?
         LA    R3,WKAREA          NO, PREPARE NEXT FMT3 DSCB
         MVC   DS3PTRDS,135(R3)   GET PTR TO NEXT FMT3 DSCB
         XR    R3,R3              CLEAR REGISTER 03
         LH    R3,H13             PREPARE R3 FOR NEXT 13 EXT.
         OBTAIN NEXT13            GET FMT3 DSCB
         LTR   R15,R15            IS EVERYTHING OKAY?
         BNZ   DSCBERR            NO, THEN SAY SO WITH MSG
         B     EXT13LP1           ELSE KEEP GOING
NOMORE   DS    0H
         L     R3,SAVREG03        RESTORE REGISTER 3
         SR    R4,R4              CLEAR REG 4
         LH    R4,TOTRK           STORE TOTAL TRACKS INTO REG 4
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   TRKA,DWD2+11       STORE VALUE IN PRINT LINE
         DROP  R5                 FREE REG 5
         SPACE 1
***********************************************************************
**  DETERMINE AND DECODE THE TOTAL TRACKS USED.
***********************************************************************
         SPACE 1
         TM    DS1DSORG,X'62'     PS OR PO OR DA???
         BNZ   SPACUSED           YES...ONE OF THEM...USE CALCULATIONS
         MVC   TRKU,DWD2+11       DEFAULT TO SPACE ALLOCATED
         B     PRTSPACE
SPACUSED DS    0H
         SLR   R4,R4                                           #DD99222
         ICM   R4,3,DS1LSTAR       GET THE LAST BLOCK PTR.     #DD99222
         CLC   DS1LSTAR,=X'000000' ARE THE TRACK AND BLOCK NUMBER ZERO?
         BE    *+8                YES, SKIP NEXT INSTRUCTION
         LA    R4,1(R4)           INCREMENT LAST BLOCK PTR BY 1
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   TRKU,DWD2+11       STORE VALUE IN PRINT LINE
         SPACE 1
***********************************************************************
**  PRINT THE SPACE LINES
***********************************************************************
         SPACE 1
PRTSPACE DS    0H
         LA    R9,SPATITLE
         BAL   R10,TWOMESSG
         L     R10,RETADDR        RESTORE THE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
         LTORG ,
         EJECT 1
***********************************************************************
***  ROUTINE PDSDSET WILL DISPLAY THE MEMBER NAMES OF THE CURRENT
***  PARTITIONED DATASET.  ROUTINE DADSET WILL DISPLAY THE MODULES
***  OF THE CURRENT LIBRARIAN DATASET.
***********************************************************************
         SPACE 1
LISTMEM  DS    0H
         ST    R10,RETADDR        STORE THE RETURN ADDRESS
         TM    DS1DSORG,X'02'     CHECK FOR A PDS (PO OR POU)
         BO    PDSDSET            PROCESS MEMBER LIST
         TM    DS1DSORG,X'20'     CHECK FOR A 'DA' PDS (DA OR DAU)
         BNO   MEMRET             IF NOT, SKIP MEMBER LIST
         B     DADSET
         SPACE 1
***********************************************************************
***  OPEN THIS PARTITIONED DATASET.
***********************************************************************
         SPACE 1
PDSDSET  DS    0H
         MVI   NOM,X'00'          INITIALIZE COUNTERS TO ZERO
         MVC   NOM+1(15),NOM
         OPEN  (SYSLIB)           OPEN THE PDS DIRECTORY
         TM    SYSLIB+48,X'10'    CHECK FOR SUCCESS
         BNO   CANTOPEN           IF NOT, INFORM USER
         MVI   MBRLINE,C' '       CLEAR OUT THE MEMBER LIST
         MVC   MBRLINE+1(75),MBRLINE
         MVC   MBRLINE(5),=CL5'MBRS:'
         SPACE 1
***********************************************************************
***  LOOP THROUGH THE DIRECTORY BLOCKS.
***********************************************************************
         SPACE 1
         MVI   LASTDIR,C'N'       LAST DIR. ENTRY NOT FOUND YET.
         SR    R7,R7              SET LIST COUNTER TO ZERO
         LA    R5,MBRS            GET ADDRESS OF MEMBER NAME LIST
GETBLK   DS    0H
         GET   SYSLIB             READ A DIRECTORY BLOCK
         L     R3,DIRALL          GET THE CURRENT DIR. CTR
         LA    R3,1(R3)           ADD 1 TO IT
         ST    R3,DIRALL          STORE IT BACK
         CLI   LASTDIR,C'Y'       CHECK FOR END REACHED.
         BE    GETBLK             IF SO, READ NEXT BLOCK.
         LR    R6,R1              GET ADDRESS OF DIR BLK.
         LH    R2,0(R1)           GET LENGTH OF DIR. BLK
         LA    R4,2(R1)           GET REAL ADDRESS OF DIR. BLK
         AR    R6,R2              POINT TO LAST BYTE
         CLC   0(4,R4),HEXFF      SEE IF VALID NAME
         BNE   VALIDNM            IF NOT, A VALID NAME.
         MVI   LASTDIR,C'Y'       INDICATE END REACHED.
         B     GETBLK             BUT KEEP READING DIR. BLOCKS
VALIDNM  DS    0H
         L     R3,DIRU            GET THE CURR. DIR. USED CTR
         LA    R3,1(R3)           ADD 1 TO IT
         ST    R3,DIRU            STORE IT BACK
CHKEND   DS    0H
         CR    R6,R4             SEE IF THAT WAS LAST ENTRY IN BLOCK
         BE    GETBLK            YES...
         CLC   0(4,R4),HEXFF     WAS IT LAST VALID ENTRY???
         BNE   GOING             NO...
         MVI   LASTDIR,C'Y'      YES...INDICATE END OF DATA
         B     GETBLK            BUT KEEP READING DIRECTORY BLOCKS
GOING    DS    0H
         L     R3,NOM             GET THE NO. MEMBERS CTR
         LA    R3,1(R3)           ADD 1 TO IT
         ST    R3,NOM             STORE IT BACK
         TM    11(R4),X'80'       TEST FOR AN ALIAS
         BNO   CHKRANGE           IF NO, SKIP CTR INCREMENTING
         L     R3,NOA             GET NO. ALIASES CTR
         LA    R3,1(R3)           ADD 1 TO IT
         ST    R3,NOA             STORE IT BACK
CHKRANGE DS    0H
         CLC   0(8,R4),FRMEMB     CHECK TO SEE IF NAME IN RANGE
         BL    NEXTENT            LESS THEN 'FROM' NAME- FORGET
         CLC   0(8,R4),TOMEMB     CHECK AGAINST LAST NAME IN RANGE
         BH    NEXTENT            GREATER THAN 'TO' NAME- FORGET
         CLI   MASKFLG,C'Y'       IS THERE A MASK?
         BNE   ACCMEMB            NO..ACCEPT THIS MEMBER
         MVC   MEMBDATA,0(R4)     YES..
         MASK  MEMBMASK,MEMBDATA,MAXLEN,MAXLEN  CHECK OUT MEMBER
         LTR   R15,15             DOES MEMBER NAME FIT MASK?
         BNZ   NEXTENT            NO..GET NEXT MEMBER
ACCMEMB  DS    0H
         MVC   2(8,R5),0(R4)      YES PUT NAME IN MEMBER NAME LIST
         TM    11(R4),X'80'       TEST FOR AN ALIAS
         BNO   NOTALIAS           IF NO, SKIP NEXT INSTRUCTION
         MVI   1(R5),C'*'         INDICATE AN ALIAS
NOTALIAS DS    0H
         LA    R5,10(R5)          GET NEXT AVAILABLE NAME POS.
         LA    R7,1(R7)           ADD 1 TO NO. IN LIST CTR
         CH    R7,=H'7'           SEE IF CURRENT LIST FULL
         BNE   NEXTENT            IF NOT, PROCESS NEXT ENTRY
         LA    R9,MBRLINEP
         BAL   R10,ONEMESSG
         LA    R5,MBRS            RESET ADDRESS OF NEXT SLOT
         MVI   MBRLINE,C' '       CLEAR OUT THE MBR NAME LINE
         MVC   MBRLINE+1(75),MBRLINE
         SR    R7,R7              SET LIST COUNTER TO ZERO
NEXTENT  DS    0H
         SR    R2,R2              CLEAR REG. 2
         IC    R2,11(R4)          GET ENTRY LENGTH
         SLL   R2,27              DETERMINE ADDRESS OF THE
         SRL   R2,26              NEXT PDS ENTRY
         LA    R4,12(R2,R4)       STORE ADDRESS BACK IN REG. 4
         B     CHKEND             CHECK FOR END
         SPACE 1
***********************************************************************
***  CLOSE THE PARTITIONED DATASET AND PRINT LAST LINES.
***********************************************************************
         SPACE 1
EOFDIR   DS    0H
         CLOSE (SYSLIB)           DONE. CLOSE PDS
         LTR   R7,R7              ANY NAMES IN CURRENT LIST?
         BZ    PRTENT             IF NOT, DONT PRINT
         LA    R9,MBRLINEP
         BAL   R10,ONEMESSG
PRTENT   DS    0H
         L     R4,NOM             GET THE NO. OF MEMBERS
         S     R4,NOA             SUBTRACT THE NO. ALIASES
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   NOMEM,DWD2+12      STORE VALUE IN PRINT LINE
         L     R4,NOA             GET NO. OF ALIASES
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   NOALIAS,DWD2+12    STORE VALUE IN PRINT LINE
         L     R4,DIRU            GET NO. OF DIR. BLKS USED
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   DIRUSE,DWD2+13     STORE VALUE IN PRINT LINE
         L     R4,DIRALL          GET NO. OF DIR. BLKS
         S     R4,DIRU            SUBTRACT NO. USED -> UNUSED
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   DIRUNUSE,DWD2+13   STORE VALUE IN PRINT LINE
         PUTLINE PARM=PUTBLOCK,                                        *
               OUTPUT=(ENTLINE,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     MEMRET             BRANCH TO RETURNING CODE
CANTOPEN DS    0H
         LA    R9,OPENERR
         BAL   R10,ONEMESSG
         LA    R9,1               SET ERROR FLAG.
MEMRET   DS    0H
         L     R10,RETADDR        RESTORE THE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
         LTORG ,
         EJECT 1
***********************************************************************
***  OPEN THIS LIBRARIAN DATASET.
***********************************************************************
         SPACE 1
DADSET   DS    0H
         LA    R1,FAIROPL         POINT AT OPEN PARM LIST
         WXTRN FAIROPN
         ICM   R15,15,=V(FAIROPN) LOAD LIBRARIAN "OPEN" ROUTINE
         BZ    FAIRERR1           B. IF NOT AVAILABLE
         CALL  (15)
         LTR   R15,R15            PARM LIST O.K. ?
         BNZ   FAIRERR1
         CLI   FAIRORES,C'0'      WERE WE SUCCESSFUL???
         BE    DACONTIN           YES...
         LA    R1,FAIRCPL         POINT AT CLOSE PARM LIST
         WXTRN FAIRCLS
         ICM   R15,15,=V(FAIRCLS) LOAD LIBRARIAN "CLOSE" ROUTINE
         BZ    FAIRER1B           B. IF NOT AVAILABLE
         CALL  (15)               FAIR OPENED THE DA DATASET
         MVC   OPENDA+42(1),FAIRORES NO...SO TELL HIM
FAIRERR1 DS    0H
         LA    R9,OPENDA
         BAL   R10,ONEMESSG
FAIRER1B DS    0H
         LA    R9,1               SET ERROR FLAG
         L     R10,RETADDR        RESTORE THE RETURN ADDRESS
         BR    R10                RETURN TO MAINLINE
         SPACE 1
***********************************************************************
***  LOOP THROUGH THE INDEX
***********************************************************************
         SPACE 1
DACONTIN DS    0H
         XC    NOM(16),NOM
         MVC   MBRLINE(5),=CL5'MBRS:'
NEXTLINE DS    0H
         MVI   MBRLINE+5,C' '     CLEAR OUT THE MEMBER LIST
         MVC   MBRLINE+6(70),MBRLINE+5
         SR    R7,R7              SET LIST COUNTER TO ZERO
         LA    R5,MBRS            GET ADDRESS OF MEMBER NAME LIST
GETDAMOD DS    0H
         MVC   MODNAME(8),BLANKS  CLEAR PARAMETER
         LA    R1,FAIRMPL         POINT AT MOD PARM LIST
         WXTRN FAIRMOD
         ICM   R15,15,=V(FAIRMOD) LOAD LIBRARIAN "MODULE" ROUTINE
         BZ    FAIRERR2           B. IF NOT AVAILABLE
         CALL  (15)
         LTR   R15,R15            CHECK IF ENVIRONMENT O.K.
         BNZ   FAIRERR2
         CLI   FAIRMRES,C'0'      RETURNED WITH A MODULE???
         BE    SETUP              YES...
         CLI   FAIRMRES,C'1'      AT END OF INDEX???
         BE    EOFDA              YES...
         MVC   LOADERR+39(1),FAIRMRES STORE ERROR CODE
FAIRERR2 DS    0H
         LA    R9,LOADERR
         BAL   R10,ONEMESSG
         LA    R9,1               SET ERROR FLAG
         L     R10,RETADDR
         BR    R10                RETURN TO MAINLINE
SETUP    DS    0H
         L     R3,NOM
         LA    R3,1(R3)           INCREMENT COUNTER
         ST    R3,NOM
         MVC   FAIRMMDN(8),MODNAME
         CLC   MODNAME(8),FRMEMB  CHECK TO SEE IF NAME IN RANGE
         BL    GETDAMOD           LESS THAN 'FROM' NAME...FORGET
         CLC   MODNAME(8),TOMEMB CHECK AGAINST LAST NAME IN RANGE
         BH    GETDAMOD           GREATER THAN 'TO' NAME...FILE END
         CLI   MASKFLG,C'Y'       IS THERE A MASK?
         BNE   ACCMOD             NO..ACCEPT THIS MEMBER
         MASK  MEMBMASK,MODNAME,8,8   CHECK OUT MEMBER
         LTR   R15,15             DOES MEMBER NAME FIT MASK?
         BNZ   GETDAMOD           NO..GET NEXT MEMBER
ACCMOD   DS    0H
         MVC   2(8,R5),MODNAME
         LA    R5,10(R5)          GET NEXT AVAILABLE NAME POSITION
         LA    R7,1(R7)           ADD 1 TO NUMBER IN LIST CTR.
         CH    R7,=H'7'           SEE IF CURRENT LIST FULL
         BNE   GETDAMOD           IF NOT,PROCESS NEXT ENTRY
         LA    R9,MBRLINEP
         BAL   R10,ONEMESSG
         MVC   MBRLINE(5),BLANKS  CLEAR OUT 'MBRS:'
         B     NEXTLINE
         SPACE 1
***********************************************************************
***  CLOSE THE DIRECT ACCESS DATASET AND PRINT THE LAST LINE
***********************************************************************
         SPACE 1
EOFDA    DS    0H
         LA    R1,FAIRCPL         POINT AT CLOSE PARM LIST
         ICM   R15,15,=V(FAIRCLS) LOAD LIBRARIAN "CLOSE" ROUTINE
         BZ    DAMEMS             B. IF NOT AVAILABLE
         CALL  (15)
         LTR   R7,R7              ANY NAMES IN CURRENT LIST???
         BZ    DAMEMS
         LA    R9,MBRLINEP
         BAL   R10,ONEMESSG
DAMEMS   DS    0H
         L     R4,NOM             LOAD NUMBER OF MODULES
         BAL   R10,CONVERT        CONVERT TO DISPLAY MODE
         MVC   DAMEM,DWD2+12      STORE IN PRINT LINE
         LA    R9,DENTLINE
         BAL   R10,ONEMESSG
         L     R10,RETADDR
         BR    R10
         EJECT 1
***********************************************************************
***  THIS ROUTINE CONVERTS THE CONTENTS OF REGISTER 4 TO DISPLAY
***  FORMAT AND STORES THE RESULT INTO DOUBLEWORD 'DWD2'.
***********************************************************************
         SPACE 1
CONVERT  DS    0H
         CVD   R4,DWD1
         MVC   DWD2(16),PATTERN
         ED    DWD2(16),DWD1
         BR    R10
         EJECT 1
***********************************************************************
*
*** ALL TSO PUTLINES EXECUTED FROM THIS ROUTINE.
*
***********************************************************************
         SPACE 1
ONEMESSG DS    0H
         PUTLINE PARM=PUTBLOCK,                                        *
               OUTPUT=((R9),TERM,SINGLE,DATA),MF=(E,IOPLADS)
         BR    R10
         SPACE 1
TWOMESSG DS    0H
         PUTLINE PARM=PUTBLOCK,                                        *
               OUTPUT=((R9),TERM,MULTLIN,DATA),MF=(E,IOPLADS)
         BR    R10
         EJECT 1
***********************************************************************
***    THIS ROUTINE VALIDATES THE MASK IF THERE IS ONE ENTERED      ***
***********************************************************************
         SPACE 1
CHKMASK  DS    0H
         SAVE  (14,12),,*         HOUSEKEEPING
         LR    R12,R15
         USING CHKMASK,R12
         ST    R13,CHKSAVE+4
         LA    R2,CHKSAVE
         ST    R2,8(R13)
         LR    R13,R2
         XR    R15,R15
         L     R2,0(0,R1)         LOAD ADDRESS OF PDE
         LH    R3,4(0,R2)         STORE LENGTH OF MASK
         L     R4,0(0,R2)         LOAD POINTER TO MASK
         NI    QUESFLG,X'00'      INITIALIZE FLAG
CHK001   DS    0H
         CLI   0(R4),C' '         IS MASK CHAR BLANK?
         BE    CHKERR             YES..MASK IS INVALID
         CLI   0(R4),C'?'         IS MASK CHAR A ? MARK ?
         BNE   CHK002             NO..CONTINUE
         TM    QUESFLG,X'FF'      YES..IS IT THE FIRST ? TO BE FOUND?
         BO    CHKERR             NO..INVALID MASK, MORE THAN 1 ?
         OI    QUESFLG,X'FF'      SET FLAG TO INDICATE ? FOUND
CHK002   DS    0H
         BCT   R3,*+8             IF NOT END OF MASK, GO AROUND EXIT
         B     CHKEXIT            YES..END
         LA    R4,1(R4)           ADJUST PTR TO NEXT CHAR IN MASK
         B     CHK001             CONTINUE
CHKERR   DS    0H
         L     R15,=F'4'          SET ERROR RETURN CODE
CHKEXIT  DS    0H
         L     R13,4(R13)         RESTORE POINTER TO CALLERS SAVEAREA
         DROP  R12
         RETURN (14,12),RC=(15)   RESTORE REGISTERS
         LTORG ,
         EJECT 1
***********************************************************************
*** FORMAT 1 DSCB AREA
***********************************************************************
         SPACE 1
DS1      DS    0D
DS1FMTID DS    CL1
DS1DSSN  DS    CL6
DS1VOLSQ DS    CL2
DS1CREDT DS    CL3
DS1EXPDT DS    CL3
DS1NOEPV DS    CL1
DS1ROBDB DS    CL1
DS1RSV01 DS    CL1
DS1SYSCD DS    CL13
DS1REFD  DS    CL3
DS1RSV02 DS    CL4
DS1DSORG DS    CL1
         DS    CL1
DS1RECFM DS    CL1
DS1OPTCD DS    CL1
DS1BLKL  DS    CL2
DS1LRECL DS    CL2
DS1KEYL  DS    CL1
DS1RKP   DS    CL2
DS1DSIND DS    CL1
DS1SCALO DS    CL1
DS12NDQY DS    CL3
DS1LSTAR DS    CL3
DS1TRBAL DS    CL2
DS1RSV03 DS    CL2
DS1EXT1  DS    CL10
DS1EXT2  DS    CL10
DS1EXT3  DS    CL10
DS1PTRDS DS    CL5
         EJECT 1
***********************************************************************
*** PRINT LINES
***********************************************************************
         SPACE 1
DSNLINEP DC    H'84'
         DC    H'0'
DSNLINE  DC    CL80'DSN: '
         SPACE 1
DCBTITLE DC    A(DCBLINE)
         DC    H'78'
         DC    H'0'
         DC    C'DCB:   DSORG-RECFM-LRECL-BLKSIZE-OPTCD-'
         DC    C'KEYLEN-RKP--CREATED-EXPIRES-LASTREF'
DCBLINE  DC    A(0)
         DC    H'78'
         DC    H'0'
         DC    C'        '
DSORG    DS    CL3
         DC    C'  '
RECFM    DS    CL5
         DC    C' '
LRECL    DS    CL5
         DC    C'  '
BLKSIZE  DS    CL5
         DC    C'    '
OPTCD    DS    CL2
         DC    C'   '
KEYLEN   DS    CL4
         DC    C'  '
RKP      DS    CL3
         DC    C'   '
CRTDT    DS    CL5
         DC    C'   '
EXPDT    DS    CL5
         DC    C'   '
REFDT    DS    CL5
         DC    C' '
         SPACE 1
SPATITLE DC    A(SPALINE)
         DC    H'51'
         DC    H'0'
         DC    C'SPACE: UNITS--ALLOC---USED--EXTENTS--'
         DC    C'2NDARY QTY.'
SPALINE  DC    A(0)
         DC    H'51'
         DC    H'0'
         DC    C'        TRKS  '
TRKA     DS    CL5
         DC    C'  '
TRKU     DS    CL5
         DC    C'     '
EXTS     DS    CL2
         DC    C'     '
SECA     DS    CL3
SECU     DS    CL5
         DC    C' '
         SPACE 1
MBRLINEP DC    H'80'
         DC    H'0'
MBRLINE  DS    0CL76
MBR1     DS    CL6
MBRS     DS    CL70
         SPACE 1
ENTLINE  DC    H'70'
         DC    H'0'
         DS    0CL66
         DC    C'       '
NOMEM    DS    CL4
         DC    C' MEMBERS  '
NOALIAS  DS    CL4
         DC    C' ALIASES   DIR. BLKS: '
DIRUSE   DS    CL3
         DC    C' USED '
DIRUNUSE DS    CL3
         DC    C' UNUSED'
         SPACE 1
DENTLINE DC    H'24'
         DC    H'0'
         DS    0CL20
         DC    CL7' '
DAMEM    DS    CL4
         DC    C' MEMBERS '
         SPACE 1
ERRMSGP  DC    H'84'
         DC    H'0'
ERRMSG   DS    CL80
         SPACE 1
BADMSG   DC    H'27'
         DC    H'0'
         DC    C' ERROR IN PARSE ROUTINE.'
         SPACE 1
OPENERR  DC    H'23'
         DC    H'0'
         DC    C' UNABLE TO OPEN DATASET.'
         SPACE 1
OPENDA   DC    H'44'
         DC    H'0'
         DC    C' UNABLE TO OPEN LIBRARIAN DATASET (RC=0)'
         SPACE 1
LOADERR  DC    H'41'
         DC    H'0'
         DC    C' ERROR READING LIBRARIAN INDEX (RC=0)'
         EJECT 1
***********************************************************************
*** WORK AREAS, TABLES AND CAMLSTS.
***********************************************************************
         SPACE 1
VERSION  DC    C'V1.8...COMPILE DATE= &SYSTIME &SYSDATE'
         SPACE 1
VOLADDR  DS    F
         SPACE 1
VOLTABLE DS    CL100
*
CHKSAVE  DS    18F
SAVE10   DS    F
DETSTORE DS    F
SAVE28   DS    8F
RETADDR  DS    F
QUESFLG  DC    X'00'
REQFLAGS DS    0CL5
DCB      DS    C
SPACE    DS    C
MEMB     DS    C
LOCATE   DS    C
MASKFLG  DS    C
DSNLEN   DS    H
DSN      DS    CL44
STDSN    DS    CL44
DSNPREF  DC    CL7' '
DSNPREFL DS    F
VOL      DS    CL6
TGETVOL  DS    CL10
FLGS     DS    X
ALLOERR  EQU   X'08'
FOUND    EQU   X'80'
INTERNAL DC    C'N'
FRMEMB   DC    XL8'0000000000000000'
TOMEMB   DC    XL8'FFFFFFFFFFFFFFFF'
MEMBMASK DC    XL8'4040404040404040'
MEMBDATA DC    XL8'4040404040404040'
MAXLEN   DC    F'8'
DWD1     DS    D
DWD2     DS    2D
PATTERN  DC    X'40',13X'20',X'2120'
FWORD    DS    XL4
BLK8     DC    CL8' '
HEX00    DC    XL8'0000000000000000'
HEXFF    DC    XL8'FFFFFFFFFFFFFFFF'
HEXTAB   DC    CL16'0123456789ABCDEF'
         EJECT 1
********************************************************************
*                                                                  *
*  CONSTANTS FOR LIBRARIAN INTERFACE ROUTINES                      *
*                                                                  *
********************************************************************
FAIRWKAR DC    CL20' '
FAIROMCD DC    CL4' '
FAIROOPS DC    CL20'2011  '
FAIRDDDN DC    CL8' '
FAIRMMDN DC    CL8' '
FAIRMOPS DC    CL20'11000000 '
FAIRORES DC    CL120' '          FAIROPN RESULT AREA
FAIRMRES DS    0CL800            FAIRMOD RESULT AREA
         DC    CL12' '
MODNAME  DC    CL8' '
         DC    CL140' '
         DC    CL200' '
         DC    CL200' '
         DC    CL200' '
FAIRCRES DC    CL8' '            FAIRCLS RESULT AREA
********************************************************************
*                                                                  *
*  PARAMETER LISTS FOR LIBRARIAN INTERFACE ROUTINES                *
*                                                                  *
********************************************************************
FAIROPL  DS    0F                OPN PARM LIST
         DC    A(FAIRWKAR)
         DC    A(FAIRORES)
         DC    A(FAIROMCD)
         DC    A(FAIROOPS)
         DC    A(FAIRDDDN)
         DC    A(0)              FAIRCDDN
         DC    A(0)              FAIRTDDN
         DC    A(0)              FAIRBA1
         DC    X'80'
         DC    AL3(0)            FAIRBA2
FAIRMPL  DS    0F                MOD PARM LIST
         DC    A(FAIRWKAR)
         DC    A(FAIRMRES)
         DC    A(FAIRMMDN)
         DC    A(FAIRMOPS)
         DC    A(0)              FAIRMARL
         DC    X'80'
         DC    AL3(0)            FAIRMDAL
FAIRCPL  DS    0F                CLS PARM LIST
         DC    A(FAIRWKAR)
         DC    X'80'
         DC    AL3(FAIRCRES)
         EJECT 1
BLANKS   DC    CL8' '
MODINFO  DS    CL100
         SPACE 1
DSORGTBL DC    X'03',CL3'ISU',X'81'
         DC    X'03',CL3'PSU',X'41'
         DC    X'03',CL3'DAU',X'21'
         DC    X'03',CL3'POU',X'03'
         DC    X'02',CL3'IS ',X'80'
         DC    X'02',CL3'PS ',X'40'
         DC    X'02',CL3'DA ',X'20'
         DC    X'02',CL3'PO ',X'02'
DSORGUN  DC    X'02',CL3'?? ',X'00'
DSORGTBX DC    X'01',CL3'U  ',X'01'
         EJECT 1
EXTTBL   DC    AL4(DS1+61)
         DC    AL4(DS1+71)
         DC    AL4(DS1+81)
WKTBL    DC    AL4(WKAREA+4)
         DC    AL4(WKAREA+14)
         DC    AL4(WKAREA+24)
         DC    AL4(WKAREA+34)
         DC    AL4(WKAREA+45)
         DC    AL4(WKAREA+55)
         DC    AL4(WKAREA+65)
         DC    AL4(WKAREA+75)
         DC    AL4(WKAREA+85)
         DC    AL4(WKAREA+95)
         DC    AL4(WKAREA+105)
         DC    AL4(WKAREA+115)
         DC    AL4(WKAREA+125)
WKAREA   DS    0D
         DS    265C
UCBTYPE  DS    C
TRKCYL   DC    H'0'
TOTRK    DS    H
NOM      DS    F
NOA      DS    F
DIRALL   DS    F
DIRU     DS    F
LASTDIR  DS    C
GETVOLM  DC    CL27'ENTER VOLUME/SERIAL NUMBER-'
WRKPPL   DS    7F
PARSEPDL DS    F
DAPL     DS    4F
DAPLAPB  DS    F
IOPLADS  DC    4F'0'
         SPACE 1
PARMLIST DS    0F               PARMLIST FOR UCB LOOK-UP WITH
PARMWA   DS    F                IOSVSUCB SERVICE ROUTINE.
PARMDEVT DS    F
PARMUCB  DS    F
ADDRUCB  DS    F
         SPACE 1
         DS    0D
         UCBSCAN MF=(L,UCBSLST)    UCBSCAN PARAMETER LIST      #DD98129
UCBDEV#  DS    F                   UCBSCAN RETURNED DEVICE NO. #DD98129
UCBSWORK DS    CL100               UCBSCAN WORK AREA           #DD98129
UCBSAREA DS    XL48                UCBSCAN UCB AREA            #DD98129
         SPACE 1
ECB      DC    F'0'
DSCBTBL  DC    F'0'
         DC    AL4(DSCBM04)
         DC    AL4(DSCBM08)
         DC    AL4(DSCBM12)
         DC    AL4(DSCBM16)
         DC    AL4(DSCBM20)
DSCBM04  DC    H'38',H'0',C' - REQUIRED CATALOG NOT ACCESSIBLE'
DSCBM08  DC    H'38',H'0',C' - NOT FOUND ON VOLUME XXXXXX.    '
DSCBM12  DC    H'38',H'0',C' - PERMANENT I/O ERROR ON VOLUME. '
DSCBM16  DC    H'38',H'0',C' - INVALID WORK AREA POINTER.     '
DSCBM20  DC    H'38',H'0',C' - UNKNOWN VTOC ERROR.            '
         SPACE 1
ALLONACC DC    C'- REQUIRED VOLUME NOT ACCESSIBLE.  '
ALLOSECM DC    C'- NOT AUTHORIZED TO ACCESS DATASET.'
         SPACE 1
DEVCLASS DS    CL1
         SPACE 1
         #EXEC ,
         SPACE 1
EXTOUT   DS    0F
ATIOT    DC    F'0'
TSOFLAG  DC    F'0'
         EJECT 1
         DC    C' '
PRTLINE  DS    CL133
         SPACE 1
CATBLK   CAMLST NAME,DSN,,WKAREA
         SPACE 1
DSCBLK   CAMLST SEARCH,DSN,VOL,WKAREA
         SPACE 1
NEXTBLK  CAMLST SEEK,DS1PTRDS,VOL,WKAREA
         SPACE 1
NEXT13   CAMLST SEEK,DS3PTRDS,VOL,WKAREA
         SPACE 1
FMT4DSCB CAMLST SEARCH,DSCB4KEY,VOL,DSCBWORK
         SPACE 1
         DS    0F
DSCBWORK DS    XL148
DSCB4KEY DS    0XL44
         DC    44XL1'04'
         SPACE 1
PUTBLOCK PUTLINE MF=L
         EJECT 1
***********************************************************************
DYNRBADD DC    0F'0',X'80',AL3(DYNAMRB)
DYNAMRB  EQU   *
DYNAMLTH DC    AL1(20)        LENGTH
DYNAMVRB DC    XL1'01'        VERB
DYNAMFL1 DC    XL2'0000'      FLAGS
DYNAMERR DC    XL2'0000'      ERROR CODE
DYNAMINF DC    XL2'0000'      INFORMATION CODE
DYNAMTXA DC    A(DYNAMTXT)    DYNAMIC TEXT ADDRESS POINTERS
         DC    F'0'
DYNAMFL2 DC    F'0'           FLAGS 2
*
DYNAMTXT DC    A(DYNAMXXA)
         DC    A(DYNAMXXB)
         DC    A(DYNAMXXC)
         DC    A(DYNAMXXD)
         DC    A(DYNAMXXE)
         DC    X'80',AL3(DYNAMXXF)
*
DYNAMTBL EQU   *
DYNAMXXA DC    XL2'0001',XL2'0001',AL2(8)
DYNAMDDN DS    CL8            DDNAME
DYNAMXXB DC    XL2'0002',XL2'0001'
DYNAMDSL DC    H'44'
DYNAMDSN DS    CL44           DSNAME
DYNAMXXC DC    XL2'0004',XL2'0001',AL2(1),XL1'08'
DYNAMXXD DC    XL2'0010',XL2'0001',AL2(6)
DYNAMVOL DS    CL6            VOLSER
DYNAMXXE DC    XL2'0015',XL2'0001',AL2(3)
DYNAMUCB DS    CL3            UCB NAME
DYNAMXXF DC    XL2'001C',XL2'0000'
DDNPTR   DC    A(DYNAMDDN),Y(8)
*
***********************************************************************
*** DEFAULT SERVICE ROUTINE (IKJEHDEF) CONTROL BLOCKS
***********************************************************************
         SPACE 1
DFPL     DS    0F      DEFAULT PARAMETER LIST
DFPLUPT  DS    A            PTR TO UPT
DFPLECT  DS    A            PTR TO ECT
DFPLECB  DS    A            PTR TO CP'S ECB
DFPLDFPB DS    A            PTR TO DEFAULT PARAMETER BLOCK
DFPB     DS    0F      DEFAULT PARAMETER BLOCK
DFPBCODE DS    0AL1         DEFAULT ENTRY CODE
DFPBDSN  DS    A            PTR TO DSNLEN AND DSN
DFPBCNTL DS    0AL1         DEFAULT CONTROL CODE
DFPBPSCB DS    A            PTR TO PSCB
DFPBLORC DS    0AL1         LOCATE RETURN CODE
DFPBQUAL DC    AL4(0)       PTR TO DEFAULT QUALIFIER
DFPBCAT  DC    AL4(0)       PTR TO USER CATALOG
DFPBPSWD DC    AL4(0)       PTR TO PASSWORD
         EJECT 1
***********************************************************************
*** DCB FOR PDS'S WHOSE MEMBERS ARE BEING LISTED
***********************************************************************
         SPACE 1
SYSLIB   DCB   DSORG=PS,RECFM=F,LRECL=256,BLKSIZE=256,                 X
               MACRF=GL,EODAD=EOFDIR
         EJECT 1
         PRINT NOGEN
PCL      IKJPARM
         SPACE 1
DSNLIST  IKJPOSIT DSNAME,LIST,UPPERCASE,                               C
               PROMPT='ONE OR MORE DATASET NAMES'
         SPACE 1
ALLWD    IKJKEYWD DEFAULT='NOALL'
         SPACE 1
         IKJNAME  'NOALL'
         SPACE 1
         IKJNAME  'ALL'
         SPACE 1
DCBWD    IKJKEYWD DEFAULT='NODCB'
         SPACE 1
         IKJNAME  'NODCB'
         SPACE 1
         IKJNAME  'DCB'
         SPACE 1
SPACEWD  IKJKEYWD DEFAULT='NOSPACE'
         SPACE 1
         IKJNAME  'NOSPACE'
         SPACE 1
         IKJNAME  'SPACE'
         SPACE 1
MEMBWD   IKJKEYWD DEFAULT='NOMEMB'
         SPACE 1
         IKJNAME  'NOMEMB'
         SPACE 1
         IKJNAME  'MEMBERS',SUBFLD=MEMSUB
         SPACE 1
LOCWD    IKJKEYWD DEFAULT='NOLOC'
         IKJNAME  'NOLOC'
         SPACE 1
         IKJNAME  'LOCATE'
         SPACE 1
FROMWD   IKJKEYWD
         SPACE 1
         IKJNAME  'FROM',SUBFLD=FROMSUB
         SPACE 1
TOWD     IKJKEYWD
         SPACE 1
         IKJNAME  'TO',SUBFLD=TOSUB
         SPACE 1
VOLWD    IKJKEYWD
         SPACE 1
         IKJNAME 'VOL',SUBFLD=VOLSUB
         SPACE 1
MEMSUB   IKJSUBF
         SPACE 1
MASK     IKJIDENT 'MASK',UPPERCASE,MAXLNTH=8,                          X
               CHAR,VALIDCK=CHKMASK
         SPACE 1
FROMSUB  IKJSUBF
         SPACE 1
FROM     IKJIDENT 'FROM',MAXLNTH=8,OTHER=ALPHANUM,                     X
               PROMPT='THE ''FROM'' MEMBER NAME',                      X
               HELP=('THE FIRST MEMBER NAME IN A RANGE OF MEMBER NAMES'X
               )
         SPACE 1
TOSUB    IKJSUBF
TO       IKJIDENT 'TO',MAXLNTH=8,OTHER=ALPHANUM,                       X
               PROMPT='THE ''TO'' MEMBER NAME',                        X
               HELP=('THE LAST MEMBER NAME IN A RANGE OF MEMBER NAMES')
         SPACE 1
VOLSUB   IKJSUBF
         SPACE 1
VOLSER   IKJIDENT 'VOL',LIST,OTHER=ALPHANUM,                           X
               PROMPT='THE ''VOLSER'' LIST',                           X
               HELP=('THE VOLUMES TO BE SCANNED')
         SPACE 1
         IKJENDP
         PRINT GEN
         EJECT 1
         DYNSPACE
         ORG   DYNSP1+4+4         4 = START OF RB  + 4 = ERROR CODE
DYNAMERA DS    XL2                ERROR CODE
         ORG
         SPACE 1
***********************************************************************
***  PARSE DESCRIPTOR ENTRY FORMAT.
***********************************************************************
         SPACE 1
DSNPDE   DSECT
ADDRDSN  DS    A
LENDSN   DS    H
FLGSDSN  DS    H
         DS    4F
NEXTPDE  DS    A
         SPACE 1
VOLPDE   DSECT
ADDRVOL  DS    F
LENVOL   DS    H
FLAGVOL  DS    H
NEXTVOL  DS    F
         SPACE 1
***********************************************************************
***  DSCB EXTENT FORMAT
***********************************************************************
         SPACE 1
EXTENT   DSECT
ID       DS    C
SEQ      DS    C
CCA      DS    H
TTA      DS    H
CCB      DS    H
TTB      DS    H
         EJECT 1
         #DSECTS ALLOC,CVT,TSO,UCB
         SPACE 1
         #REGS ,
         END   ,
./ ADD NAME=PACKMAP  0100-00040-00040-1222-02065-02065-00000-SOURCE
PACKMAP  TITLE '- PACKMAP -  DASD VOLUME MAPPING UTILITY'
***********************************************************************
*                     *                                               *
*   P A C K M A P     *                                               *
*                     *                                               *
***********************                                               *
*                                                                     *
*   FUNCTION :        PRODUCE DATASET/FREESPACE EXTENT LIST FOR       *
*                     ANY DASD VOLUME                                 *
*                                                                     *
*   REGISTER USAGE :   R0 - WORK REGISTER                             *
*                      R1 - WORK REGISTER                             *
*                      R2 - WORK REGISTER                             *
*                      R3 - WORK REGISTER                             *
*                      R4 - WORK REGISTER                             *
*                      R5 - WORK REGISTER                             *
*                      R6 - WORK REGISTER                             *
*                      R7 - WORK REGISTER                             *
*                      R8 - WORK REGISTER                             *
*                      R9 - WORK REGISTER                             *
*                     R10 - WORK REGISTER                             *
*                     R11 - BASE                                      *
*                     R12 - BASE                                      *
*                     R13 - SAVE AREA                                 *
*                     R14 - RETURN ADDRESS                            *
*                     R15 - RETURN CODE                               *
*                                                                     *
*   MACROS USED :     MANY                                            *
*                                                                     *
*   CONTROL BLOCK :   IECSDSL1 - DSCB DESCRIPTIONS                    *
*   DEPENDENCIES      DCBD     - DCB MAP                              *
*                     ICVAFBFL - CVAF BUFFER MAP                      *
*                     ICVAFPL  - CVAF PARAMETER LIST                  *
*                     IEZDEB   - DEB MAP                              *
*                     IEFJFCBN - JFCB MAP                             *
*                     IHAPSA   - PSA MAP                              *
*                     IEFTIOT1 - TIOT MAP                             *
*                     IEFUCBOB - UCB MAP                              *
*                                                                     *
*   ABEND CODES :     U100 - DISK DD STATEMENT NOT SUPPLIED           *
*                     U200 - INSUFFICIENT STORAGE TO CONTINUE         *
*                                                                     *
*   ATTRIBUTES :      AUTHORIZED                                      *
*                                                                     *
*   ROUTINES CALLED : MSGRTN - MESSAGE FORMATING SUBROUTINE           *
*                     HEXDUMP  - FORMAT HEX DUMP OUTPUT               *
*                     GETFMT5  - FORMAT5.DSCB TABLE SUBROUTINE        *
*                     SORTRTN  - TABLE SORT SUBROUTINE                *
*                                                                     *
*   MESSAGES ISSUED :  PAK0001I *** VTOC ERRORS EXIST ON VOLSER ***   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   HISTORY :         MAY/82 - #DD82130 - INITIAL INSTALL             *
*                     APR/88 - #DD88103 - 3380K SUPPORT               *
*                                                                     *
***********************************************************************
         EJECT 1
         MACRO ,
&NAME    FMTMSG
.**********************************************************************
.*                             FMTMSG                                 *
.*                             --------                               *
.* FUNCTION:                                                          *
.*       CREATE A VARIABLE LENGTH CHARACTER STRING FOR INPUT TO       *
.*       MSGRTN.                                                      *
.*                                                                    *
.* OUTPUT FORMAT:                                                     *
.*                                                                    *
.* NAME   DS   0CLXXX                                                 *
.*        DC   AL1(LENGTH)                                            *
.* LABEL  DC   CLN'TEXT'                                              *
.* LABEL  DC   NNC' '                                                 *
.*        DC   C'TEXT'                                                *
.*        DC   NNNC' '                                                *
.*                                                                    *
.*                                                                    *
.* THE OPERANDS ARE IN FORMAT 'TEXT' OR (LABEL,'TEXT') OR             *
.* (LABEL,'C',M) WHERE 'C' IS ONE CHAR AND M IS THE MULTPLCATN FACTOR *
.* (LABEL,'TEXT',L) WHERE 'TEXT' IS GT 1 CHAR AND L IS THE LENGTH     *
.* DESIRED FOR THE FIELD (TO BE PADDED OR TRUNCATED IN DC CL_'TEXT' ) *
.*   ANY NUMBER OF SEPARATE OPERANDS MAY BE SPECIFIED ON THE          *
.* MACRO CALL PROTOTYPE STMT. SOME EXAMPLES FOLLOW                    *
.* LABEL FMTMSG 'IEA001',(ID,' ',1),(,' ',20),'***',(MSG,'****',80)   *
.* WILL GENERATE FOLLOWING PARAMETER LISTS AND CONSTANTS              *
.* LABEL DS    0CL111                                                 *
.*       DC    AL1(110)       LENGTH OF TOTAL    MSG                  *
.*       DC    C'IEA001'      FIRST OPERAND                           *
.* ID    DC    1C' '          SECOND OPERAND                          *
.*       DC    20C' '         THIRD OPERAND                           *
.*       DC    C'***'          FOURTH OPERAND                         *
.* MSG   DC    CL80'***'       FIFTH OPERAND                          *
.**********************************************************************
         LCLC  &LBL   LABEL VARIABLE
         LCLA  &N1,&CNT,&LEN
         LCLC  &TEMP1
&CNT     SETA  1              . COUNT IS ONE
&LEN     SETA  0              . TOTAL LEN IS 0
&N1      SETA  N'&SYSLIST          . NUMBER OPERANDS
.LOOP1   ANOP
.* THIS LOOP ADDS UP ALL THE STRING LENGTHS
         AIF   (N'&SYSLIST(&CNT) NE 1).SUBS1
.* IF SUBSCRIPTED IE 'TEXT' AS OPPOSED (LABL,'TEXT') SKIP
&LEN     SETA  &LEN+K'&SYSLIST(&CNT)-2 .    ADD LENGTH
         AGO   .COUNT
.SUBS1   ANOP
         AIF   (T'&SYSLIST(&CNT,3) EQ 'O').SUBS3
.* IF THIRD SUBSET IS OMITTED USE LENGTH OF SECOND OPERAND
&LEN     SETA  &LEN+&SYSLIST(&CNT,3)  . ADD GIVEN LENGTH
         AGO   .COUNT
.SUBS3   ANOP
&LEN     SETA  &LEN+K'&SYSLIST(&CNT,2)-2 . ELSE USE LENGTH STRING 2
.COUNT   ANOP
&CNT     SETA  &CNT+1   . ADD ONE TO COUNT OF OPERANDS
         AIF   ('&CNT' LE '&N1').LOOP1
&LEN     SETA  &LEN+1     .   ADD 1 FOR LENGTH BYTE
&NAME    DS    0CL&LEN
&LEN     SETA  &LEN-1      .  SUBTRACT 1 FOR LENGTH BYTE
         DC    AL1(&LEN)                LENGTH OF MESSAGE
&CNT     SETA  0              . START AT BEGINNING
.LOOP2   ANOP
.* THIS LOOP GENERATES DEFINE CONSTANTS FOR FIELDS GIVEN
&CNT     SETA  &CNT+1         . ADD ONE
&LBL     SETC  ' '     . SET LABEL BLANK
.NOSETL  AIF   (N'&SYSLIST(&CNT) GT 1).SUBS4
.* IF MORE THAN 1 FIELD PROCESS AS SUCH
&LEN     SETA  K'&SYSLIST(&CNT)-2    . GET LENGTH OF STRING
&TEMP1   SETC  '&SYSLIST(&CNT)'         .  AND SET TEXT
         AGO   .GENLINE
.SUBS4   ANOP
&LBL     SETC  '&SYSLIST(&CNT,1)' . GENERATE LABEL
&LEN     SETA  K'&SYSLIST(&CNT,2)-2   . SET LENGTH OF STRING
&TEMP1   SETC  '&SYSLIST(&CNT,2)'   . GET CHAR STRING
         AIF   (T'&SYSLIST(&CNT,3) EQ 'O').SUBS5
&LEN     SETA  &SYSLIST(&CNT,3)   . GET LENGTH/MULT FACTOR
.* IF NO LENGTH OR MULT FACTOR GIVEN PROCESS
.SUBS5   AIF   (K'&SYSLIST(&CNT,2) NE 3).GENLINE
.* IF MORE THAN 1 CHAR LOG PROCESS AS LENGTH
&LBL     DC    &LEN.C&TEMP1
         AGO   .INCR2
.SUBS6   ANOP
.* NO LENGTH OR MULT FACTOR GIVEN SET LENGTH OF TEXT
&LEN     SETA  K'&SYSLIST(&CNT,2)-2   . SET LENGTH OF STRING
.GENLINE ANOP
.* GENERATE ASSEMBLER DEFINE FOR TEXT
&LBL     DC    CL&LEN.&TEMP1
.INCR2   ANOP
         AIF   ('&CNT' LT '&N1').LOOP2
         AGO   .MEND
.MEND    ANOP
         MEND
         EJECT 1
PACKMAP  #START ,                                                      *
               AMODE=CAP24,                                            *
               APARS=,                                                 *
               BASE=(R11,R12),                                         *
               SPLEVEL=2,                                              *
               WKDSECT=MAP,                                            *
               COPY=YES,COPYYR=1982,                                   *
               LEVEL=#V001R02
         TITLE '- PACKMAP -  INITIALIZATION'
*---------------------------------------------------------------------*
*        INITIALIZE ALL LIST FORM MACROS AND LOAD HEX FORMAT RTN      *
*---------------------------------------------------------------------*
         MVC   RDJFCB(1),=AL1(128)          OPTION BYTE
         MVC   ENQL(1),=AL1(192)            OPTION BYTE
         MVC   OPENLIST(1),=AL1(128)        OPTION BYTE
         WXTRN HEXDUMP
         ICM   R0,15,=V(HEXDUMP)            CHECK IF HEXDUMP AVAILABLE
         BNZ   START                        B. IF YES
         LOAD  EP=HEXDUMP,ERRET=VTOC0000    LOAD HEXDUMP RTN
START    DS    0H
         ST    R0,HEXRTN                    SAVE HEXDUMP ADDR
         LA    R1,HEXPARM+4                 POINT TO HEX PARMS
         ST    R1,HEXPARM                   SAVE PARM ADDRESS
         TITLE '- PACKMAP -  TIOT AND JFCB CHECKS'
*---------------------------------------------------------------------*
*        CHECK THE TIOT AND SETUP THE JFCB TO READ THE FORMAT4        *
*---------------------------------------------------------------------*
VTOC0000 DS    0H
         L     R1,PSATOLD-PSA(R0)           >>--> TCB
         L     R1,TCBTIO-TCB(R1)            >>--> TIOT
         LA    R1,TIOENTRY-TIOT1(R1)        SKIP TIOT HEADER
VTOC0100 DS    0H
         CLC   VTOCDCB+DCBDDNAM-IHADCB(8),TIOEDDNM-TIOENTRY(R1)
         BE    VTOC0200
         SLR   R0,R0                        CLEAR REGISTER
         IC    R0,TIOELNGH-TIOENTRY(R1)     INSERT ENTRY LENGTH
         ALR   R1,R0                        POINT TO NEXT ENTRY
         CLI   TIOELNGH-TIOENTRY(R1),0      CHECK FOR TIOT END
         BNE   VTOC0100                     IF NOT CHECK ENTRY
         B     ERROR001                     IF NOT FOUND ERROR
VTOC0200 DS    0H
         MVC   DDNAME(8),VTOCDCB+DCBDDNAM-IHADCB
         DEVTYPE DDNAME,DEVTAREA,DEVTAB     GET DEVICE INFO
         SLR   R0,R0                                           #DD88103
         SLR   R1,R1                                           #DD88103
         ICM   R1,3,DEVTAREA+8     LOAD # OF CYLS ON VOLUME    #DD88103
         SLR   R2,R2                                           #DD88103
         ICM   R2,3,DEVTAREA+10    LOAD # OF TRKS ON CYLINDER  #DD88103
         MR    R0,R2               CALC # OF TRKS ON VOLUME    #DD88103
         BCTR  R1,0                SUBTRACT 1 FOR TRACK ZERO   #DD88103
         ST    R1,LASTTRK                   SAVE LAST TRK ADDR
         MVC   TRKCYL(2),DEVTAREA+10        COPY TRKS PER CYL
         RDJFCB (VTOCDCB),MF=(E,RDJFCB)     READ THE JFCB
         MVC   VTOCJFCB+JFCBDSNM-INFMJFCB(44),FMT4DSNM
         MVC   VOLSER1(6),VTOCJFCB+JFCBVOLS-INFMJFCB
         MVC   VOLSER2(6),VTOCJFCB+JFCBVOLS-INFMJFCB
         MVC   VOLSER3(6),VTOCJFCB+JFCBVOLS-INFMJFCB
         TITLE '- PACKMAP -  OPEN ROUTINE'
*---------------------------------------------------------------------*
*        OPEN THE VTOC AND SAVE THE DEB AND UCB ADDRESSES             *
*---------------------------------------------------------------------*
         OPEN  (VTOCDCB,INPUT),TYPE=J,MF=(E,OPENLIST)
         TM    VTOCDCB+DCBOFLGS-IHADCB,DCBOFOPN
         BNO   ERROR001                     IF NOT OPEN ERROR
         L     R1,VTOCDCB+DCBDEBAD-IHADCB   LOAD DEB ADDRESS
         L     R1,DEBSUCBA-DEBBASIC(R1)     LOAD UCB ADDRESS
         ST    R1,@UCB             SAVE UCB ADDRESS
         UNPK  UCB1(5),UCBCHAN-UCBOB(3,R1)                     #DD99204
         TR    UCB1(4),TRTABLE1-C'0'                           #DD99204
         MVI   UCB1+4,C' '                                     #DD99204
         MVC   UCB2,UCB1              COPY DEVICE NUMBER       #DD99183
         TITLE '- PACKMAP -  HEADING INITIALIZATION'
*---------------------------------------------------------------------*
*        SET UP THE REPORT HEADING                                    *
*---------------------------------------------------------------------*
         #DATE DATE1,FORMAT='MMM DD,CCYY'   PUT DATE IN TITLE
         MVC   DATE2,DATE1                  COPY THE DATE
         TIME  DEC                          GET SYSTEM TIME
         ST    R0,DBL                       SAVE THE TIME
         MVC   TIME1(9),=X'4021207A20207A2020'
         ED    TIME1(9),DBL                 INSERT INTO TITLE
         MVC   TIME2(9),TIME1               COPY TIME
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#TITLE,MSGWORD,TITLE,HEADING1),VL,MF=(E,CALLL)
         TITLE '- PACKMAP -  FORMAT 5 DSCB ROUTINE'
*---------------------------------------------------------------------*
*        GET FREESPACE TABLE                                          *
*---------------------------------------------------------------------*
         L     R15,=A(GETFMT5)     A(FORMAT 5 EXTRACT RTN)     #DD99183
         CALL  (15),(VTOCDCB),VL,MF=(E,CALLL)
         LTR   R15,R15                      CHECK RETURN CODE
         BNZ   ERROR003                     IF ERROR REPORT IT
         ST    R1,FREESPCE                  SAVE FREESPACE ADDR
         TITLE '- PACKMAP -  GET VTOC TABLE'
*---------------------------------------------------------------------*
*        GET STORAGE SPACE TO HOLD VTOC INFO                          *
*---------------------------------------------------------------------*
VTOC0600 DS    0H
         GETMAIN VU,LA=TAB1SIZE,A=TAB1ADDR,MF=(E,GETMAINL)
         L     R2,TAB1ADDR                  LOAD START ADDR
         L     R5,TAB1ADDR+4                LOAD STORAGE LENGTH
         #BLANK 0(R2),LEN=(R5),PAD=X'00'    CLEAR THE STORAGE
         L     R4,TAB1SIZE                  LOAD RECORD LENGTH
         LA    R5,0(R5,R2)                  LOAD END   ADDR
         ST    R5,TAB1ADDR+4                SAVE END ADDRESS
         TITLE '- PACKMAP -  VOLUME RESERVATION'
*---------------------------------------------------------------------*
*        GET EXCLUSIVE CONTROL OF THE VTOC IF RUNNING BATCH           *
*---------------------------------------------------------------------*
         TESTAUTH FCTN=1,STATE=YES,RBLEVEL=1                   #DD88103
         LTR   R15,R15             CHECK IF AUTHORIZED         #DD88103
         BNZ   VTOC0700            B. IF NOT                   #DD88103
         MVC   RNAME(6),VOLSER1             ENQ ON THIS VOLSER
         RESERVE (QNAME,RNAME,E,6,SYSTEMS),RET=HAVE,UCB=@UCB,          X
               MF=(E,ENQL)
         TITLE '- PACKMAP -  READ THE VTOC'
*---------------------------------------------------------------------*
*        READ THE VTOC AND SAVE ALL THE RELEVENT INFORMATION          *
*---------------------------------------------------------------------*
VTOC0700 DS    0H
         SLR   R3,R3                        CLEAR COUNTER
         USING DSCBTAB,R2
VTOC0800 DS    0H
         LR    R1,R2                        COPY TABLE ADDRESS
         LA    R1,144(,R1)                  POINT PAST ENTRY
         C     R1,TAB1ADDR+4                CHECK FOR END
         BNL   ERROR002                     IF NO ROOM ERROR
         LR    R6,R2                        COPY TABLE ADDRESS
         LA    R6,4(,R6)                    MAKE ROOM FOR TTR0
         MVI   IOERROR,0                    CLEAR IO ERROR FLAG
         READ  READECB,SF,VTOCDCB,(R6),MF=(E,READL)
         CHECK READECB                      WAIT FOR THE I/O
         CLI   IOERROR,0                    CHECK FOR I/O ERROR
         BE    VTOC0900                     IF OK CONTINUE
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR6MSG,ERR7MSG,BLANKLNE),         X
               VL,MF=(E,CALLL)
         #SETRC 8                           SET RETURN CODE
         B     VTOC0800                     KEEP ON READING
VTOC0900 DS    0H
         CLI   DS1FMTID,X'00'               CHECK FOR FORMAT0
         BNE   VTOC1000                     IF NOT SKIP
         LA    R3,1(,R3)                    ADD 1 TO FREE COUNT
         B     VTOC0800                     CONTINUE READING
VTOC1000 DS    0H
         CLI   DS1FMTID,C'1'                CHECK FOR FORMAT1
         BNE   VTOC1100                     IF NOT KEEP CHECKIN
         CLI   DS1NOEPV,X'00'               CHECK EXTENT COUNT
         BNE   VTOC1400                     IF SOME PROCESS
         MVC   ERR5DSN,DS1DSNAM
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR5MSG,BLANKLNE),                 X
               VL,MF=(E,CALLL)
         B     VTOC0800                     SKIP THIS DSCB
VTOC1100 DS    0H
         CLI   DS2FMTID,C'2'                CHECK FOR FORMAT2
         BE    VTOC1400                     IF MATCH KEEP
         CLI   DS3FMTID,C'3'                CHECK FOR FORMAT3
         BE    VTOC1400                     IF MATCH KEEP
         CLI   DS4IDFMT,C'4'                CHECK FOR FORMAT4
         BNE   VTOC1200                     IF NOT CHECK NEXT
         MVC   CCHH(4),DS4VTOCE+2           POINT TO START CCHH
         BAL   R10,CCHHTOTT                 CONVERT TO TT
         MVC   VTOCTT(4),TT                 SAVE START TT
         ST    R6,HEXPARM+4                 SAVE START ADDRESS
         LA    R1,139(,R6)                  POINT TO END
         ST    R1,HEXPARM+8                 SAVE END ADDRESS
         NOTE  VTOCDCB                      GET RELATIVE TTR0
         MVC   HEXIN(4),DS4VTOCE+2          COPY CCHH
         STCM  R1,B'0010',HEXIN+4           SAVE RECORD NUMBER
         LA    R1,5                         LOAD FIELD LENGTH
         BAL   R10,HEXFORM                  CONVERT TO CHAR
         MVC   HEXKEYWD(31),=C'FORMAT4.DSCB AT   CCCCHHHHRR = '
         MVC   HEXKEYWD+18(10),HEXOUT       COPY CONVERTED CCHHR
         LA    R1,HEXPARM                   POINT TO PARM FIELD
         BAL   R10,HEXDUMPR                 PRINT RECORD IN HEX
         B     VTOC1400                     SAVE FORMAT4 INFO
VTOC1200 DS    0H
         CLI   DS5FMTID,C'5'                CHECK FOR FORMAT5
         BE    VTOC0800                     IF SO SKIP IT
         CLI   DS6FMTID,C'6'                CHECK FOR FORMAT6
         BNE   VTOC1300                     IF NOT SKIP
         ST    R6,HEXPARM+4                 SAVE START ADDRESS
         LA    R1,139(,R6)                  POINT TO END
         ST    R1,HEXPARM+8                 SAVE END ADDRESS
         NOTE  VTOCDCB                      GET RELATIVE TTR0
         XC    TT,TT                        CLEAR TT
         STCM  R1,B'1100',TT+2              SAVE TT
         STCM  R1,B'0010',HEXIN+4           SAVE RECORD NUMBER
         L     R1,TT                        LOAD RELATIVE TT
         A     R1,VTOCTT                    ADD VTOC CCHH
         ST    R1,TT                        SAVE REAL TT
         BAL   R10,TTTOCCHH                 CONVERT TO CCHH
         MVC   HEXIN(4),CCHH                COPY CCHH
         LA    R1,5                         LOAD FIELD LENGTH
         BAL   R10,HEXFORM                  CONVERT TO CHAR
         MVC   HEXKEYWD(31),=C'FORMAT6.DSCB AT   CCCCHHHHRR = '
         MVC   HEXKEYWD+18(10),HEXOUT       COPY CONVERTED CCHHR
         LA    R1,HEXPARM                   POINT TO PARM FIELD
         BAL   R10,HEXDUMPR                 PRINT RECORD IN HEX
         #SETRC 4                           SET RETURN CODE
         B     VTOC0800                     CONTINUE READING
VTOC1300 DS    0H
         ST    R6,HEXPARM+4                 SAVE START ADDRESS
         LA    R1,139(,R6)                  POINT TO END
         ST    R1,HEXPARM+8                 SAVE END ADDRESS
         NOTE  VTOCDCB                      GET RELATIVE TTR0
         XC    TT,TT                        CLEAR TT
         STCM  R1,B'1100',TT+2              SAVE TT
         STCM  R1,B'0010',HEXIN+4           SAVE RECORD NUMBER
         L     R1,TT                        LOAD RELATIVE TT
         A     R1,VTOCTT                    ADD VTOC CCHH
         ST    R1,TT                        SAVE REAL TT
         BAL   R10,TTTOCCHH                 CONVERT TO CCHH
         MVC   HEXIN(4),CCHH                COPY CCHH
         LA    R1,5                         LOAD FIELD LENGTH
         BAL   R10,HEXFORM                  CONVERT TO CHAR
         MVC   HEXKEYWD(31),=C'UNKNOWN DSCB AT   CCCCHHHHRR = '
         MVC   HEXKEYWD+18(10),HEXOUT       COPY CONVERTED CCHHR
         LA    R1,HEXPARM                   POINT TO PARM FIELD
         BAL   R10,HEXDUMPR                 PRINT RECORD IN HEX
         #SETRC 4                           SET RETURN CODE
         B     VTOC0800                     CONTINUE READING
VTOC1400 DS    0H
         NOTE  VTOCDCB                      GET RELATIVE TTR0
         ST    R1,DSCBTAB                   SAVE TTR0
         BXLE  R2,R4,VTOC0800               LOOP TILL END
         B     ERROR002                     TERMINATE
         TITLE '- PACKMAP -  VOLUME RELEASING'
*---------------------------------------------------------------------*
*        RELEASE CONTROL OF VTOC IF RUNNING BATCH                     *
*---------------------------------------------------------------------*
VTOC1500 DS    0H
         TESTAUTH FCTN=1,STATE=YES,RBLEVEL=1                   #DD88103
         LTR   R15,R15             CHECK IF AUTHORIZED         #DD88103
         BNZ   VTOC1600            B. IF NOT                   #DD88103
         DEQ   (QNAME,RNAME,6,SYSTEMS),RET=HAVE,MF=(E,ENQL)
         TITLE '- PACKMAP -  FREE UNUSED VTOC TABLE'
*---------------------------------------------------------------------*
*        FREE THE UNUSED STORAGE SPACE                                *
*---------------------------------------------------------------------*
VTOC1600 DS    0H
         ST    R2,TAB1ADDR+4                SAVE LAST ADDRESS
         SR    R5,R2                        CALC UNUSED SPACE
         FREEMAIN R,A=(R2),LV=(R5)          FREE UNUSED STORAGE
         TITLE '- PACKMAP -  GET EXTENT TABLE'
*---------------------------------------------------------------------*
*        CREATE EXTENT TABLE                                          *
*---------------------------------------------------------------------*
VTOC1700 DS    0H
         GETMAIN VU,LA=TAB2SIZE,A=TAB2ADDR,MF=(E,GETMAINL)
         L     R2,TAB2ADDR                  LOAD START ADDR
         L     R5,TAB2ADDR+4                LOAD STORAGE LENGTH
         #BLANK 0(R2),LEN=(R5),PAD=X'00'    CLEAR THE STORAGE
         LA    R5,0(R5,R2)                  LOAD END   ADDR
         ST    R5,TAB2ADDR+4                SAVE END ADDRESS
         L     R2,TAB1ADDR                  LOAD START ADDRESS
         SLR   R3,R3                        CLEAR COUNTER
         L     R4,TAB1SIZE                  LOAD RECORD LENGTH
         L     R5,TAB1ADDR+4                LOAD END ADDRESS
         SR    R5,R4                        BACKUP 1 RECORD
         L     R6,TAB2ADDR                  LOAD START ADDR
         USING EXTABLE,R6
         TITLE '- PACKMAP -  FILL THE EXTENT TABLE'
*---------------------------------------------------------------------*
*        FILL THE EXTENT TABLE                                        *
*---------------------------------------------------------------------*
VTOC1800 DS    0H
         CLI   DS1FMTID,C'1'                CHECK FOR FORMAT1
         BNE   VTOC3800                     IF NOT TRY NEXT
         MVC   TEMPDSN,DS1DSNAM             SAVE THE DSNAME
         MVC   EXTENTS(1),DS1NOEPV          SAVE TOTAL EXTENTS
         XC    EXTENTNO,EXTENTNO            CLEAR THE COUNTER
         TM    DS1DSORG,DS1DSGPS            CHECK FOR "PS"
         BNO   VTOC1900                     IF NOT CONTINUE
         MVC   EXDSORG(2),=C'PS'            INSERT DSORG
         B     VTOC2400                     CHECK FOR "XXU"
VTOC1900 DS    0H
         TM    DS1DSORG,DS1DSGPO            CHECK FOR "PO"
         BNO   VTOC2000                     IF NOT CONTINUE
         MVC   EXDSORG(2),=C'PO'            INSERT DSORG
         B     VTOC2400                     CHECK FOR "XXU"
VTOC2000 DS    0H
         TM    DS1DSORG,DS1DSGDA            CHECK FOR "DA"
         BNO   VTOC2100                     IF NOT CONTINUE
         MVC   EXDSORG(2),=C'DA'            INSERT DSORG
         B     VTOC2400                     CHECK FOR "XXU"
VTOC2100 DS    0H
         TM    DS1DSORG,DS1DSGIS            CHECK FOR "IS"
         BNO   VTOC2200                     IF NOT CONTINUE
         MVC   EXDSORG(2),=C'IS'            INSERT DSORG
         B     VTOC2400                     CHECK FOR "XXU"
VTOC2200 DS    0H
         TM    DS1DSORG+1,DS1ORGAM          CHECK FOR "AM"
         BNO   VTOC2300                     IF NOT CONTINUE
         MVC   EXDSORG(2),=C'AM'            INSERT DSORG
         B     VTOC2400                     CHECK FOR "XXU"
VTOC2300 DS    0H
         MVC   EXDSORG(2),=C'??'            INSERT DSORG
VTOC2400 DS    0H
         TM    DS1DSORG,DS1DSGU             CHECK FOR "XXU"
         BNO   VTOC2500                     IF NOT CONTINUE
         MVI   EXDSORG+2,C'U'               FLAG AS UNMOVABLE
VTOC2500 DS    0H
         LA    R1,EXRECFM                   POINT TO RECFM
         TM    DS1RECFM,X'C0'               CHECK FOR RECFM = U
         BNO   VTOC2600                     IF NOT CONTINUE
         MVI   0(R1),C'U'                   INSERT RECFM
         LA    R1,1(,R1)                    ADVANCE POINTER
         B     VTOC2900                     CONTINUE
VTOC2600 DS    0H
         TM    DS1RECFM,X'80'               CHECK FOR RECFM = F
         BNO   VTOC2700                     IF NOT CONTINUE
         MVI   0(R1),C'F'                   INSERT RECFM
         LA    R1,1(,R1)                    ADVANCE POINTER
         B     VTOC2800                     CONTINUE
VTOC2700 DS    0H
         TM    DS1RECFM,X'40'               CHECK FOR RECFM = V
         BNO   VTOC2800                     IF NOT CONTINUE
         MVI   0(R1),C'V'                   INSERT RECFM
         LA    R1,1(,R1)                    ADVANCE POINTER
VTOC2800 DS    0H
         TM    DS1RECFM,X'10'               CHECK IF BLOCKED
         BNO   VTOC2900                     IF NOT CONTINUE
         MVI   0(R1),C'B'                   INSERT BLOCKED FLAG
         LA    R1,1(,R1)                    ADVANCE POINTER
VTOC2900 DS    0H
         TM    DS1RECFM,X'20'               CHECK FOR TRK OVFLO
         BNO   VTOC3000                     IF NOT CONTINUE
         MVI   0(R1),C'T'                   INSERT TRK OVF FLAG
         LA    R1,1(,R1)                    ADVANCE POINTER
VTOC3000 DS    0H
         TM    DS1RECFM,X'08'               CHECK FOR SPAN/STD
         BNO   VTOC3100                     IF NOT CONTINUE
         MVI   0(R1),C'S'                   INSERT FLAG
         LA    R1,1(,R1)                    ADVANCE POINTER
VTOC3100 DS    0H
         TM    DS1RECFM,X'04'               CHECK FOR ASA CNTL
         BNO   VTOC3200                     IF NOT CONTINUE
         MVI   0(R1),C'A'                   INSERT FLAG
         LA    R1,1(,R1)                    ADVANCE POINTER
VTOC3200 DS    0H
         TM    DS1RECFM,X'02'               CHECK FOR MACHINE C
         BNO   VTOC3300                     IF NOT CONTINUE
         MVI   0(R1),C'M'                   INSERT FLAG
         LA    R1,1(,R1)                    ADVANCE POINTER
VTOC3300 DS    0H
         SLR   R0,R0                                           #DD88103
         ICM   R0,3,DS1LRECL                LOAD RECORD LEN    #DD88103
         CVD   R0,DBL                       CONVERT
         MVC   EXLRECL(6),=X'402020202120'  MOVE EDIT MASK
         ED    EXLRECL(6),DBL+5             INSERT INTO OUTPUT
         SLR   R0,R0                                           #DD88103
         ICM   R0,3,DS1BLKL                 LOAD BLOCK LEN     #DD88103
         CVD   R0,DBL                       CONVERT
         MVC   EXBLKSIZ(6),=X'402020202120' MOVE EDIT MASK
         ED    EXBLKSIZ(6),DBL+5            INSERT INTO OUTPUT
         LA    R1,DS1EXT1                   POINT TO 1ST EXTENT
         LA    R9,3                         SETUP FOR 3 EXTENTS
VTOC3400 DS    0H
         BAL   R10,EXTUPDAT                 PROCESS THIS EXTENT
         LA    R1,10(,R1)                   POINT TO NEXT FIELD
         BCT   R9,VTOC3400                  LOOP FOR FORMAT1
         LR    R7,R2                        SAVE FORMAT1 ADDR
         TM    DS1DSORG,DS1DSGIS            CHECK FOR ISAM DSN
         BNO   VTOC3500                     IF NOT PROCEED
         CLC   DS1VOLSQ(2),=X'0001'         CHECK FOR 1ST VOL
         BNE   VTOC3500                     IF NOT PROCEED
         MVC   DSCBTAB,=C'DONE'             FLAG AS PROCESSED
         MVI   FORMATID,C'2'                PROCESS FORMAT2
         CLC   DS1PTRDS(5),=X'0000000000'   CHECK FOR ZEROS
         BE    ERROR004                     IF SO ERROR
         MVC   CCHH(4),DS1PTRDS             COPY FORWARD PTR
         BAL   R10,CCHHTOTT                 CONVERT TO TT
         L     R8,TT                        LOAD THIS TT
         S     R8,VTOCTT                    FIND RELATIVE TT
         STCM  R8,3,CCHH                    SAVE RELATIVE TT   #DD88103
         MVC   CCHH+2(1),DS1PTRDS+4         COPY R
         MVI   CCHH+3,X'00'                 ASSURE ZEROS
         L     R1,CCHH                      LOAD RECORD NUMBER
         BAL   R10,FINDDSCB                 FIND THE FORMAT2
         LTR   R15,R15                      CHECK RETURN CODE
         BNZ   ERROR004                     IF NOT FOUND ERROR
         CLI   DS2FMTID-DSCBTAB(R1),C'2'    CHECK FOR FORMAT2
         BNE   ERROR005                     IF NOT ERROR
         LR    R2,R1                        POINT TO FORMAT2
VTOC3500 DS    0H
         MVC   DSCBTAB,=C'DONE'             FLAG AS PROCESSED
         CLC   DS1PTRDS(5),=X'0000000000'   CHECK FOR ZEROS
         BE    VTOC3900                     IF SO QUIT PROCESS
         MVI   FORMATID,C'3'                PROCESS FORMAT3
         MVC   CCHH(4),DS1PTRDS             COPY FORWARD PTR
         BAL   R10,CCHHTOTT                 CONVERT TO TT
         L     R8,TT                        LOAD THIS TT
         S     R8,VTOCTT                    FIND RELATIVE TT
         STCM  R8,3,CCHH                    SAVE RELATIVE TT   #DD88103
         MVC   CCHH+2(1),DS1PTRDS+4         COPY R
         MVI   CCHH+3,X'00'                 ASSURE ZEROS
         L     R1,CCHH                      LOAD RECORD NUMBER
         BAL   R10,FINDDSCB                 FIND THE FORMAT3
         LTR   R15,R15                      CHECK RETURN CODE
         BNZ   ERROR004                     IF NOT FOUND ERROR
         CLI   DS3FMTID-DSCBTAB(R1),C'3'    CHECK FOR FORMAT3
         BNE   ERROR004                     IF NOT ERROR
         LR    R2,R1                        POINT TO FORMAT3
         LA    R1,DS3EXTNT                  POINT TO EXTENT
         LA    R9,4                         SETUP FOR 4 EXTENTS
VTOC3600 DS    0H
         BAL   R10,EXTUPDAT                 PROCESS THIS EXTENT
         LA    R1,10(,R1)                   POINT TO NEXT FIELD
         BCT   R9,VTOC3600                  LOOP FOR FORMAT3
         LA    R1,DS3ADEXT                  POINT TO EXTENT
         LA    R9,9                         SETUP FOR 9 EXTENTS
VTOC3700 DS    0H
         BAL   R10,EXTUPDAT                 PROCESS THIS EXTENT
         LA    R1,10(,R1)                   POINT TO NEXT FIELD
         BCT   R9,VTOC3700                  LOOP FOR FORMAT3
         B     VTOC3500
VTOC3800 DS    0H
         CLI   DS4IDFMT,C'4'                CHECK FOR FORMAT4
         BNE   VTOC4000                     IF NOT TRY NEXT
         MVC   EXTENTS(1),=X'01'            SET EXTENTS TO 1
         XC    EXTENTNO,EXTENTNO            ZERO EXTENT NUMBER
         MVC   TEMPDSN,=CL44'*** VTOC ***'  INSERT VTOC NAME
         LA    R1,DS4VTOCE                  POINT TO EXTENT
         BAL   R10,EXTUPDAT                 PROCESS THIS EXTENT
         MVC   DSCBTAB,=C'DONE'             FLAG ENTRY AS DONE
         B     VTOC4000                     SKIP EXTENT CHECK
VTOC3900 DS    0H
         LR    R2,R7                        RESTORE FMT1 ADDR
         CLC   EXTENTS(1),EXTENTNO+3        COMPARE EXTENTS
         BNE   ERROR006                     IF NOT ERROR
VTOC4000 DS    0H
         BXLE  R2,R4,VTOC1800               LOOP TILL END
         TITLE '- PACKMAP -  CHECK FOR UNCHAINED DSCBS'
*---------------------------------------------------------------------*
*        CHECK FOR UNCHAINED DSCBS                                    *
*---------------------------------------------------------------------*
         L     R2,TAB1ADDR                  LOAD START ADDRESS
         SLR   R3,R3                        CLEAR REGISTER
         L     R4,TAB1SIZE                  LOAD ENTRY SIZE
         L     R5,TAB1ADDR+4                LOAD END   ADDRESS
         SR    R5,R4                        BACKUP TO LAST REC
VTOC4100 DS    0H
         CLC   DSCBTAB,=C'DONE'             WAS DSCB CHAINED
         BE    VTOC4200                     IF SO CHECK NEXT
         XC    TT,TT                        CLEAR TRACK AREA
         MVC   TT+2(2),DSCBTAB              MOVE RELATIVE TRK
         L     R1,TT                        LOAD RELATIVE TT
         A     R1,VTOCTT                    ADD VTOC CCHH
         ST    R1,TT                        SAVE REAL TT
         BAL   R10,TTTOCCHH                 CONVERT TO CCHH
         MVC   HEXIN(4),CCHH                MOVE TO HEX AREA
         MVC   HEXIN+4(1),DSCBTAB+2         MOVE RELATIVE REC
         LA    R1,5                         LOAD INPUT LENGTH
         BAL   R10,HEXFORM                  CONVERT TO HEX
         MVC   HEXKEYWD(31),=C'UNCHAINED DSCB AT CCCCHHHHRR = '
         MVC   HEXKEYWD+18(10),HEXOUT       COPY CONVERTED CCHHR
         LA    R1,4(,R2)                    POINT TO DSCB START
         ST    R1,HEXPARM+4                 SAVE START ADDRESS
         LA    R1,139(,R1)                  POINT TO DSCB END
         ST    R1,HEXPARM+8                 SAVE END ADDRESS
         LA    R1,HEXPARM                   POINT TO PARM FIELD
         BAL   R10,HEXDUMPR                 PRINT RECORD IN HEX
         #SETRC 4                           SET RETURN CODE
VTOC4200 DS    0H
         BXLE  R2,R4,VTOC4100               LOOP THROUGH TABLE
         TITLE '- PACKMAP -  PROCESS FREESPACE EXTENTS'
*---------------------------------------------------------------------*
*        FILL IN THE FREESPACE EXTENTS                                *
*---------------------------------------------------------------------*
         ICM   R1,B'1111',FREESPCE          LOAD FREESPACE TBL
         BZ    VTOC4400                     IF NONE SKIP IT
         SLR   R9,R9                        CLEAR REGISTER
         ICM   R9,B'1111',0(R1)             LOAD EXTENT COUNT
         BZ    VTOC4400                     IF NONE SKIP IT
         STCM  R9,3,EXTENTS                 SAVE TOTAL EXTENTS #DD88103
         XC    EXTENTNO,EXTENTNO            CLEAR EXTENT COUNT
         LA    R3,4(,R1)                    POINT PAST COUNT
VTOC4300 DS    0H
         MVC   STRTRACK+2(2),0(R3)          INSERT START TRK
         SLR   R0,R0                                           #DD88103
         SLR   R1,R1                                           #DD88103
         ICM   R1,3,2(R3)                   LOAD # OF CYLS     #DD88103
         SLR   R2,R2                                           #DD88103
         ICM   R2,3,TRKCYL                  LOAD # OF TRKS/CYL #DD88103
         MR    R0,R2                        CALCULATE TO TRKS  #DD88103
         SLR   R2,R2                        CLEAR REGISTER
         IC    R2,4(R3)                     LOAD # OF TRKS
         AR    R2,R1                        CALC LENGTH
         ST    R2,EXTNTLEN                  INSERT EXTENT LEN
         BCTR  R2,R0                        DECREMENT FOR TRK 0
         SLR   R0,R0                                           #DD88103
         ICM   R0,3,0(R3)                   LOAD START TRACK   #DD88103
         AR    R2,R0                        ADD START TRACK    #DD88103
         ST    R2,ENDTRACK                  INSERT END TRK
         MVC   TT,STRTRACK                  COPY START TRK
         BAL   R10,TTTOCCHH                 CONVERT TO CCHH
         MVC   STRTCCHH,CCHH                INSERT START CCHH
         MVC   TT,ENDTRACK                  COPY END TRACK
         BAL   R10,TTTOCCHH                 CONVERT TO CCHH
         MVC   ENDCCHH,CCHH                 INSERT END   CCHH
         L     R1,EXTENTNO                  LOAD OLD COUNT
         LA    R1,1(,R1)                    ADD 1 TO COUNTER
         ST    R1,EXTENTNO                  STORE NEW COUNT
         ST    R1,EXTNTNO                   INSERT EXTENT NO.
         MVC   TOTEXTNT+2(2),EXTENTS        INSERT TOTAL EXTENTS
         MVC   EXDSNAME,=CL44'*** FREE SPACE ***'
         LA    R3,5(,R3)                    ADVANCE TABLE
         LA    R6,EXTNTEND                  ADVANCE TABLE
         SLR   R1,R1                                           #DD88103
         ICM   R1,3,TAB2CNT                 LOAD TOTAL COUNT   #DD88103
         LA    R1,1(,R1)                    ADD 1 TO COUNT
         STCM  R1,3,TAB2CNT                 SAVE TOTAL COUNT   #DD88103
         BCT   R9,VTOC4300                  LOOP FOR EACH EXTNT
         TITLE '- PACKMAP -  SORT THE TABLE'
*---------------------------------------------------------------------*
*        SORT THE EXTENT TABLE                                        *
*---------------------------------------------------------------------*
VTOC4400 DS    0H
         ST    R6,TAB2ADDR+4                SAVE END ADDRESS
         L     R2,TAB2ADDR                  LOAD START ADDRESS
         L     R15,=A(SORTRTN)     A(SORT ROUTINE)             #DD99183
         CALL  (15),(TAB2CNT,TAB2SIZE+2,SKEY,STYPE,(R2)),              X
               VL,MF=(E,CALLL)
         TITLE '- PACKMAP -  PRINT THE TABLE'
*---------------------------------------------------------------------*
*        PRINT THE EXTENT TABLE                                       *
*---------------------------------------------------------------------*
         L     R2,TAB2ADDR                  LOAD START ADDRESS
         DROP  R6
         USING EXTABLE,R2
         SLR   R3,R3                        CLEAR COUNTER
         L     R4,TAB2SIZE                  LOAD RECORD LENGTH
         L     R5,TAB2ADDR+4                LOAD END ADDRESS
         SR    R5,R4                        BACKUP 1 RECORD
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#TITLE,MSGWORD,TITLE,HEADING2,BLANKLNE,HEADING3,HEX
               ADING4),VL,MF=(E,CALLL)
VTOC4500 DS    0H
         L     R1,PREVEND                   LOAD END OF PREVIOUS
         LA    R1,1(,R1)                    ADD 1 TO END
         C     R1,STRTRACK                  COMPARE WITH THIS DSN
         BE    VTOC4700                     IF OK CONTINUE
         BL    VTOC4600                     IF LOWER..MISSING
         MVC   TRKERROR(30),=C'*** SHARED TRACKS ***         '
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR8MSG),VL,MF=(E,CALLL)
         OI    FLAG,EXTNTERR                SET EXTENT ERROR FLAG
         B     VTOC4700                     CONTINUE
VTOC4600 DS    0H
         MVC   TRKERROR(30),=C'*** MISSING TRACKS ***        '
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR8MSG),VL,MF=(E,CALLL)
         OI    FLAG,EXTNTERR                SET EXTENT ERROR FLAG
VTOC4700 DS    0H
         L     R0,STRTRACK                  LOAD START TRACK
         CVD   R0,DBL                       CONVERT
         OI    DBL+7,X'0F'                  CHANGE SIGN
         UNPK  RSTRT(5),DBL                 INSERT INTO OUTPUT
         L     R0,ENDTRACK                  LOAD END   TRACK
         CVD   R0,DBL                       CONVERT
         OI    DBL+7,X'0F'                  CHANGE SIGN
         UNPK  REND(5),DBL                  INSERT INTO OUTPUT
         L     R0,EXTNTLEN                  LOAD EXTENT LENGTH
         CVD   R0,DBL                       CONVERT
         MVC   LEN+1(6),=X'402020202120'    MOVE EDIT MASK
         ED    LEN+1(6),DBL+5               INSERT INTO OUTPUT
         L     R0,EXTNTNO                   LOAD EXTENT NUMBER
         CVD   R0,DBL                       CONVERT
         MVC   EXNO(4),=X'40202120'         MOVE EDIT MASK
         ED    EXNO(4),DBL+6                INSERT INTO OUTPUT
         L     R0,TOTEXTNT                  LOAD TOTAL EXTENTS
         CVD   R0,DBL                       CONVERT
         MVC   HEXIN(6),=X'402021204040'    MOVE EDIT MASK
         ED    HEXIN(4),DBL+6               EDIT TOTAL EXTENTS
         CLI   HEXIN+1,C' '                 CHECK FOR BLANK
         BE    VTOC4800                     IF SO TRY NEXT
         MVC   EXTOT(3),HEXIN+1             INSERT INTO OUTPUT
         B     VTOC5000                     PROCEED
VTOC4800 DS    0H
         CLI   HEXIN+2,C' '                 CHECK FOR BLANK
         BE    VTOC4900                     IF NOT TRY NEXT
         MVC   EXTOT(3),HEXIN+2             INSERT INTO OUTPUT
         B     VTOC5000                     PROCEED
VTOC4900 DS    0H
         MVC   EXTOT(3),HEXIN+3             INSERT INTO OUTPUT
VTOC5000 DS    0H
         MVC   DSN(44),EXDSNAME
         MVC   HEXIN(4),STRTCCHH            MOVE START CCHH
         LA    R1,4                         LOAD FIELD LENGTH
         BAL   R10,HEXFORM                  CONVERT TO CHAR
         MVC   STCCCHH+1(3),HEXOUT+1        INSERT CCC
         MVC   STCCCHH+5(2),HEXOUT+6        INSERT HH
         MVC   HEXIN(4),ENDCCHH             MOVE START CCHH
         LA    R1,4                         LOAD FIELD LENGTH
         BAL   R10,HEXFORM                  CONVERT TO CHAR
         MVC   ENDCCCHH+1(3),HEXOUT+1       INSERT CCC
         MVC   ENDCCCHH+5(2),HEXOUT+6       INSERT HH
         OC    EXDSORG(20),=20C' '          ASSURE UPPER CASE
         MVC   PRDSORG(3),EXDSORG           INSERT DSORG
         MVC   PRRECFM(5),EXRECFM           INSERT RECFM
         MVC   PRLRECL(6),EXLRECL           INSERT LRECL
         MVC   PRBLKSIZ(6),EXBLKSIZ         INSERT BLOCKSIZE
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,PRINTLNE),VL,MF=(E,CALLL)
         CLC   LASTTRK,ENDTRACK             CHECK FOR EOV
         BNL   VTOC5100                     IF NOT OVER SKIP
         TM    FLAG,SIZEERR                 CHECK FOR PREVIOUS
         BO    VTOC5100                     PROCEED
         MVC   TRKERROR(30),=C'*** EXTENDS PAST EOV ***      '
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR8MSG),VL,MF=(E,CALLL)
         OI    FLAG,SIZEERR                 SET EOV FLAG
VTOC5100 DS    0H
         CLC   ENDTRACK,PREVEND             COMPARE ENDS
         BNH   VTOC5200                     IF NOT HIGHER SKIP
         MVC   PREVEND,ENDTRACK             COPY NEW END
VTOC5200 DS    0H
         BXLE  R2,R4,VTOC4500               LOOP TILL END
         CLC   LASTTRK,ENDTRACK             CHECK FOR EOV
         BNL   VTOC5300                     IF SO PROCEED
         MVC   TRKERROR(30),=C'*** MISSING TRACKS ***        '
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR8MSG),VL,MF=(E,CALLL)
         OI    FLAG,EXTNTERR                SET EXTENT ERROR FLAG
VTOC5300 DS    0H
         TM    FLAG,VTOCERRS                CHECK FOR ANY ERRORS
         BNO   VTOC5400                     IF NONE TERMINATE
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#WTO,MSGWORD,WTOL2,ERR9MSG),VL,MF=(E,CALLL)
         #SETRC 8                           SET RETURN CODE
VTOC5400 DS    0H
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,BLANKLNE,ENDLINE),                 X
               VL,MF=(E,CALLL)
         #STOP ,
         TITLE '- PACKMAP -  PROCESS ERRORS'
*---------------------------------------------------------------------*
*        PROCESS ANY ERRORS THAT MAY OCCUR                            *
*---------------------------------------------------------------------*
ERROR001 DS    0H
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#WTO,MSGWORD,WTOL1,ERR1MSG),VL,MF=(E,CALLL)
         LA    R2,100                       SET ABEND CODE
         B     ABEND                        ABNORMALLY TERMINATE
ERROR002 DS    0H
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR10MSG),VL,MF=(E,CALLL)
         LA    R2,200                       SET ABEND CODE
         B     ABEND                        TERMINATE ABNORMALLY
ERROR003 DS    0H
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR3MSG,BLANKLNE),                 X
               VL,MF=(E,CALLL)
         OI    FLAG,SPACEERR                SET FREESPACE FLAG
         B     VTOC0600                     CONTINUE PROCESS
ERROR004 DS    0H
         MVC   ERR2DSN,TEMPDSN              COPY DATASET NAME
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR2MSG,BLANKLNE),                 X
               VL,MF=(E,CALLL)
         #SETRC 4                           SET CONDITION CODE
         B     VTOC3900                     CONTINUE PROCESS
ERROR005 DS    0H
         MVC   ERR2DSN,TEMPDSN              COPY DATASET NAME
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR2MSG,BLANKLNE),                 X
               VL,MF=(E,CALLL)
         #SETRC 4                           SET CONDITION CODE
         B     VTOC3500                     CONTINUE PROCESS
ERROR006 DS    0H
         MVC   ERR4DSN,TEMPDSN              COPY DATASET NAME
         SLR   R0,R0                        CLEAR REGISTER
         IC    R0,EXTENTS                   LOAD TOTAL EXTENTS
         CVD   R0,DBL                       CONVERT
         OI    DBL+7,X'0F'                  CHANGE SIGN
         UNPK  ERR4EX1(3),DBL               INSERT INTO OUTPUT
         SLR   R0,R0                        CLEAR REGISTER
         L     R0,EXTENTNO                  LOAD FOUND EXTENTS
         CVD   R0,DBL                       CONVERT
         OI    DBL+7,X'0F'                  CHANGE SIGN
         UNPK  ERR4EX2(3),DBL               INSERT INTO OUTPUT
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,ERR4MSG,BLANKLNE),                 X
               VL,MF=(E,CALLL)
         #SETRC 4                           SET CONDITION CODE
         B     VTOC4000                     CONTINUE PROCESS
ABEND    ABEND (R2)
         TITLE '- PACKMAP -  SUBRTN - CCHH >>--->> TTR'
*---------------------------------------------------------------------*
*        CONVERT CCHH TO RELATIVE TRACK ADDRESS                       *
*---------------------------------------------------------------------*
CCHHTOTT DS    0H
         STM   R0,R15,SAVEREG2              SAVE ALL REGS      #DD88103
         SLR   R0,R0                                           #DD88103
         SLR   R1,R1                                           #DD88103
         ICM   R1,3,CCHH                    LOAD CC            #DD88103
         SLR   R2,R2                                           #DD88103
         ICM   R2,3,TRKCYL                  LOAD TRKS/CYL      #DD88103
         MR    R0,R2                        CONVERT TO TRACKS  #DD88103
         SLR   R0,R0                                           #DD88103
         ICM   R0,3,CCHH+2                  LOAD TT            #DD88103
         AR    R1,R0                        CALC TOTAL TRKS    #DD88103
         ST    R1,TT                        SAVE RELATIVE TT
         LM    R0,R15,SAVEREG2              RESTORE ALL REGS   #DD88103
         BR    R10                          RETURN TO CALLER
         TITLE '- PACKMAP -  SUBRTN - TTR >>--->> CCHH'
*---------------------------------------------------------------------*
*        CONVERT RELATIVE TRACK ADDRESS TO CCHH                       *
*---------------------------------------------------------------------*
TTTOCCHH DS    0H
         STM   R0,R15,SAVEREGS              SAVE ALL REGS
         SLR   R2,R2                        CLEAR REGISTER
         L     R3,TT                        LOAD RELATIVE TRK
         SLR   R4,R4                                           #DD88103
         ICM   R4,3,TRKCYL                  LOAD TRKS / CYL    #DD88103
         DR    R2,R4                        CALCULATE CCHH
         STCM  R3,3,CCHH                    SAVE CC            #DD88103
         STCM  R2,3,CCHH+2                  SAVE HH            #DD88103
         LM    R0,R15,SAVEREGS              RESTORE ALL REGS
         BR    R10                          RETURN TO CALLER
         TITLE '- PACKMAP -  SUBRTN - PROCESS A SINGLE EXTENT'
*---------------------------------------------------------------------*
*        UPDATE THE EXTENT TABLE FOR ONE EXTENT                       *
*---------------------------------------------------------------------*
EXTUPDAT DS    0H
         DROP  R2
         USING EXTABLE,R6
         STM   R0,R15,SAVEREGS              SAVE ALL REGS
         CLC   0(10,R1),=10X'00'            CHECK IF NO EXTENT
         BER   R10                          IF NONE RETURN
         LR    R2,R6                        COPY TABLE ADDRESS
         LA    R2,144(,R2)                  POINT PAST ENTRY
         C     R2,TAB2ADDR+4                CHECK FOR END
         BNL   ERROR002                     IF NO ROOM ERROR
         MVC   STRTCCHH,2(R1)               SAVE START CCHH
         MVC   ENDCCHH,6(R1)                SAVE END   CCHH
         MVC   CCHH,STRTCCHH                COPY   START CCHH
         BAL   R10,CCHHTOTT                 CONVERT TO TT
         MVC   STRTRACK,TT                  INSERT START TT
         MVC   CCHH,ENDCCHH                 COPY   END   CCHH
         BAL   R10,CCHHTOTT                 CONVERT TO TT
         MVC   ENDTRACK,TT                  INSERT END   TT
         L     R7,STRTRACK                  LOAD START TT
         L     R8,ENDTRACK                  LOAD END   TT
         LA    R8,1(,R8)                    ADD 1 FOR TRK 0
         SR    R8,R7                        CALCULATE LENGTH
         ST    R8,EXTNTLEN                  INSERT EXTENT LEN
         L     R1,EXTENTNO                  LOAD OLD EXTENT CNT
         LA    R1,1(,R1)                    ADD 1 TO EXTENT CNT
         ST    R1,EXTENTNO                  SAVE NEW EXTENT CNT
         ST    R1,EXTNTNO                   INSERT EXTENT NO
         MVC   TOTEXTNT+3(1),EXTENTS        INSERT TOT EXTENTS
         MVC   EXDSNAME,TEMPDSN             INSERT DATASET NAME
         SLR   R1,R1                                           #DD88103
         ICM   R1,3,TAB2CNT                 LOAD TABLE 2 COUNT #DD88103
         LA    R1,1(,R1)                    ADD 1 TO COUNTER
         STCM  R1,3,TAB2CNT                 SAVE NEW TABLE CNT #DD88103
         LM    R0,R15,SAVEREGS              RESTORE ALL REGS
         LA    R6,EXTNTEND                  ADVANCE TABLE
         BR    R10                          RETURN TO CALLER
         TITLE '- PACKMAP -  SUBRTN - FIND A DSCB IN THE TABLE'
*---------------------------------------------------------------------*
*        FIND THE CHAINED FORMAT2 OR FORMAT3 DSCB                     *
*---------------------------------------------------------------------*
FINDDSCB DS    0H
         STM   R2,R14,SAVEREGS              SAVE ALL REGISTERS
         SLR   R15,R15                      ASSUME IT IS THERE
         L     R2,TAB1ADDR                  LOAD START ADDRESS
         SLR   R3,R3                        CLEAR COUNTER
         L     R4,TAB1SIZE                  LOAD RECORD LENGTH
         L     R5,TAB1ADDR+4                LOAD END ADDRESS
FIND0100 DS    0H
         C     R1,0(R2)                     IS THIS OUR DSCB
         BE    FIND0200                     IF MATCH GET OUT
         BXLE  R2,R4,FIND0100               LOOP TILL END
         LA    R15,8                        SET RETURN CODE
         B     FIND0999                     TERMINATE
FIND0200 DS    0H
         LA    R1,0(,R2)                    POINT TO DSCB
FIND0999 DS    0H
         LM    R2,R14,SAVEREGS              RESTORE ALL REGS
         BR    R10                          RETURN TO CALLER
         TITLE '- PACKMAP -  SUBRTN - CONVERT A TO C1'
*---------------------------------------------------------------------*
*        CONVERT A STRING TO HEX FORMAT                               *
*---------------------------------------------------------------------*
HEXFORM  DS    0H
         STM   R0,R15,SAVEREGS              SAVE ALL REGS
         LA    R6,HEXIN                     POINT TO INPUT
         LA    R7,HEXOUT                    POINT TO OUTPUT
HEX00000 DS    0H
         MVC   0(1,R7),0(R6)                MOVE ONE CHARACTER
         MVC   1(1,R7),0(R6)                REPEAT IT
         LA    R6,1(,R6)                    POINT TO NEXT ONE
         LA    R7,2(,R7)                    SKIP DOUBLE CHARS
         BCT   R1,HEX00000                  LOOP TILL DONE
         NC    HEXOUT(20),ANDTABLE          FLIP THE BITS
         TR    HEXOUT(20),TRTABLE2          TRANSLATE TO PRINT
         LM    R0,R15,SAVEREGS              RESTORE ALL REGS
         BR    R10                          RETURN TO CALLER
         TITLE '- PACKMAP -  SUBRTN - FORMAT STORAGE DUMP'
*---------------------------------------------------------------------*
*        PRINT STORAGE IN DUMP FORMAT                                 *
*---------------------------------------------------------------------*
HEXDUMPR DS    0H
         STM   R0,R15,SAVEREGS              SAVE ALL REGS
         XC    HEXPARM+12(4),HEXPARM+12     CLEAR 3RD PARM
         LA    R1,PRNTHEX                   POINT TO AREA
         ST    R1,HEXPARM+16                SAVE IN PARM
         MVI   HEXPARM+16,X'40'             80 CHARACTER DUMP
HEXDUMP0 DS    0H
         ICM   R15,B'1111',HEXRTN           LOAD HEX ROUTINE
         BNZ   HEXDUMP1                     IF NOT LOADED END
         MVC   HEXPARM+8(4),HEXPARM+4       ASSURE ONLY 1 LINE
         B     HEXDUMP2                     SKIP HEXDUMP RTN
HEXDUMP1 DS    0H
         LA    R1,HEXPARM                   LOAD HEX PARM
         BALR  R14,R15                      LINK TO ROUTINE
HEXDUMP2 DS    0H
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,HEXLINE),VL,MF=(E,CALLL)
         #BLANK HEXKEYWD,LEN=31             CLEAR DESCRIPTOR
         CLC   HEXPARM+8(4),HEXPARM+4       CHECK FOR MORE
         BH    HEXDUMP0                     IF MORE LOOP BACK
         L     R15,=A(MSGRTN)      A(MESSAGE PROCESSING RTN)   #DD99183
         CALL  (15),(#PRINT,MSGWORD,BLANKLNE),VL,MF=(E,CALLL)
         LM    R0,R15,SAVEREGS              RESTORE ALL REGS
         BR    R10
         TITLE '- PACKMAP -  SUBRTN - PROCESS READ I/O ERRORS'
*---------------------------------------------------------------------*
*        PROCESS ANY I/O ERRORS WHILE READING THE VTOC                *
*---------------------------------------------------------------------*
VTOCERR  DS    0H
         SYNADAF ACSMETH=BSAM
         MVC   SYNADBUF,0(R1)
         MVI   IOERROR,255
         SYNADRLS
         BR    R14
         TITLE '- PACKMAP -  CONSTANTS AND DSECTS'
*---------------------------------------------------------------------*
*                     CONSTANTS + DSECTS                              *
*---------------------------------------------------------------------*
VTOCDCB  DCB   DDNAME=DISK,LRECL=96,KEYLEN=44,BLKSIZE=96,DEVD=DA,      X
               MACRF=RP,RECFM=F,DSORG=PS,EODAD=VTOC1500,EXLST=EXTLIST, X
               SYNAD=VTOCERR
EXTLIST  DS    0F                           *
         DC    X'87',AL3(VTOCJFCB)          *  DCB EXTENT LIST
VTOCJFCB DS    CL176                        *  DCB JFCB AREA
FMT4DSNM DC    44X'04'                      *  FORMAT4.DSCB
TAB1SIZE DC    F'144'                       *  ALLOW FOR
         DC    F'14400000'                  *    1 TO 100,000
TAB2SIZE DC    F'92'                        *  ALLOW FOR
         DC    F'9200000'                   *    1 TO 100,000
SKEY     DC    H'04'                        *  SORT START OFFSET
STYPE    DC    C'A'                         *  ASCENDING SORT
QNAME    DC    CL8'SYSVTOC'                 *  QNAME FOR ENQ
BLANKLNE FMTMSG ' '
TITLE    FMTMSG 'DISK PACK MAPPING UTILITY'
HEADING1 FMTMSG 'DATE = ',(DATE1,'MMM DD,CCYY'),(,' ',28),             X
               'INFORMATION FOR ',(VOLSER1,'  ',6),' ON ',             X
               (UCB1,'  ',4),(,' ',41),'TIME =',(TIME1,' HH:MM:SS')
HEADING2 FMTMSG 'DATE = ',(DATE2,'MMM DD,CCYY'),(,' ',28),             X
               'VOLUME MAP FOR ',(VOLSER2,'  ',6),' ON ',(UCB2,'  ',4),X
               (,' ',43),'TIME =',(TIME2,' HH:MM:SS')
HEADING3 FMTMSG 'RELATIVE TRK   EXTENT  EXTENT ',(,'  ',44),           X
               'HEX CYL/TRK (CCC/HH)'
HEADING4 FMTMSG 'START /  END   LENGTH  NO./OF  DATASET NAME',         X
               (,'  ',30),'    START -  END',                          X
               '         DSORG    RECFM    LRECL    BLKSIZE'
PRINTLNE FMTMSG (RSTRT,'-----'),' - ',(REND,'-----'),(LEN,'  -----'),  X
               ' ',(EXNO,' ---'),'/',(EXTOT,'---'),'  ',               X
               (DSN,'  ',44),(STCCCHH,' CCC/HH -'),                    X
               (ENDCCCHH,' CCC/HH '),(,' ',7),(PRDSORG,'   '),(,' ',6),X
               (PRRECFM,'     '),(,' ',2),(PRLRECL,'      '),(,' ',4), X
               (PRBLKSIZ,'      ')
ERR1MSG  FMTMSG 'IEC130I DISK     DD STATEMENT MISSING.'
ERR2MSG  FMTMSG 'UNABLE TO LOCATE FORMAT',(FORMATID,' '),              X
               '.DSCB ON VOLUME FOR ',(ERR2DSN,'  ',44)
ERR3MSG  FMTMSG 'ERROR OCCURRED BUILDING FREESPACE TABLE'
ERR4MSG  FMTMSG 'EXTENT COUNT ERROR FOR ',(ERR4DSN,'  ',44),           X
               ' SHOULD BE ',(ERR4EX1,'   '),' EXTENTS, BUT FOUND ',   X
               (ERR4EX2,'   ')
ERR5MSG  FMTMSG (ERR5DSN,'  ',44),' HAS ZERO EXTENTS'
ERR6MSG  FMTMSG 'I/O ERROR OCCURRED WHILE READING VTOC...SYNAD :'
ERR7MSG  FMTMSG '   ',(SYNADBUF,'  ',128)
ERR8MSG  FMTMSG '*** ERROR ***',(,' ',18),(TRKERROR,'  ',30)
ERR9MSG  FMTMSG 'PAK0001I *** VTOC ERRORS EXIST ON ',                  X
               (VOLSER3,'      '),' ***'
ERR10MSG FMTMSG 'INSUFFICIENT REGION SIZE TO MAP VOLUME'
HEXLINE  FMTMSG (HEXKEYWD,'  ',31),(PRNTHEX,'  ',80)
ENDLINE  FMTMSG (,'  ',48),'*** END OF VOLUME MAP ***'
         EJECT 1
#PRINT   EQU   1    BATCH - ISSUE TO SYSPRINT
*                   TSO   - ISSUED TO SYSPRINT OR TERMINAL
*
#WTO     EQU   2    BATCH - WRITE TO OPERATOR (AUTO DOM AFTER 15 MSGS)
*                   TSO   - TPUT MESSAGE TO TSO TERMINAL
*                   NOTE: SECOND PARAMETER MUST BE LIST FORM WTO
*
#TITLE   EQU   3    INITIALIZE TITLE TEXT AREA (AUTOMATICALLY CENTRED)
*                   NOTE: THIRD...NTH PARAMETERS ARE USED AS HEADINGS
*
#HEADING EQU   4    RE-INITIALIZE HEADING AREA
*
#WTOR    EQU   5    BATCH - ISSUE WRITE TO OPERATOR WITH REPLY
*                   TSO   - ISSUE TPUT/TGET FROM TSO TERMINAL
*                   NOTE: SECOND PARAMETER MUST BE LIST FOR WTOR
*
#DOM     EQU   6    ISSUE DOM FOR OUTSTANDING WTO MESSAGES
*                   NOTE : SECOND...NTH PARAMETER MAY BE THE ADDRESS
*                          OF A PARTICUALR WTO TO BE DOMMED
*
#TPUT    EQU   7    ISSUE TPUT TO TSO TERMINAL
*                   NOTE : IGNORED WHEN RUNNING BATCH
         EJECT 1
WTOL1    WTO   ' ',ROUTCDE=(1,2,11),MF=L    *  LIST FORM WTO
WTOL2    WTO   ' ',ROUTCDE=(1,2,11),MF=L,DESC=(2)
ANDTABLE DC    50X'F00F'                    *  AND TABLE
TRTABLE1 DC    C'0123456789ABCDEF'                             #DD99204
TRTABLE2 DC    256X'00'                     *  TRANSLATE TABLE
         ORG   TRTABLE2                     *
         DC    C'0123456789ABCDEF'          *
         ORG   TRTABLE2+X'10'               *
         DC    C'1'                         *
         ORG   TRTABLE2+X'20'               *
         DC    C'2'                         *
         ORG   TRTABLE2+X'30'               *
         DC    C'3'                         *
         ORG   TRTABLE2+X'40'               *
         DC    C'4'                         *
         ORG   TRTABLE2+X'50'               *
         DC    C'5'                         *
         ORG   TRTABLE2+X'60'               *
         DC    C'6'                         *
         ORG   TRTABLE2+X'70'               *
         DC    C'7'                         *
         ORG   TRTABLE2+X'80'               *
         DC    C'8'                         *
         ORG   TRTABLE2+X'90'               *
         DC    C'9'                         *
         ORG   TRTABLE2+X'A0'               *
         DC    C'A'                         *
         ORG   TRTABLE2+X'B0'               *
         DC    C'B'                         *
         ORG   TRTABLE2+X'C0'               *
         DC    C'C'                         *
         ORG   TRTABLE2+X'D0'               *
         DC    C'D'                         *
         ORG   TRTABLE2+X'E0'               *
         DC    C'E'                         *
         ORG   TRTABLE2+X'F0'               *
         DC    C'F'                         *
         ORG   ,                            *
         #STARTWA ,                         *
RDJFCB   RDJFCB (0),MF=L                    *  LIST FORM RDJFCB
OPENLIST OPEN  (0,INPUT),TYPE=J,MF=L        *  LIST FORM OPEN
ENQL     RESERVE (0,0,E,6,SYSTEMS),RET=HAVE,UCB=0,MF=L         #DD99183
@UCB     DS    F
GETMAINL GETMAIN VU,LA=0,A=0,MF=L           *  LIST FORM GETMAIN
READL    READ  READECB,SF,VTOCDCB,0,MF=L    *  LIST FORM READ
CALLL    CALL  ,(0,0,0,0,0,0,0,0,0,0,0,0),VL,MF=L
TEMPDSN  DS    CL44                         *  FORMAT1 DSNAME
DDNAME   DS    CL8                          *  DDNAME FROM DCB
DEVTAREA DS    5F                           *  DEVICE INFO
SAVEREGS DS    16F                          *  REGS SAVE AREA
SAVEREG2 DS    16F                          *  REGS SAVE AREA2 #DD88103
IOERROR  DS    X                            *  SYNAD ERROR FLAG
HEXRTN   DS    F                            *  HEX ROUTINE ADDR
FREESPCE DS    F                            *  FREE SPACE TABLE
LASTTRK  DS    F                            *  LAST TRK ON VOL
TRKCYL   DS    H                            *  NO OF TRKS/CYL
VTOCTT   DS    F                            *  VTOC START TT
CCHH     DS    F                            *  CONVERSION CCHH
TT       DS    F                            *  CONVERSION TT
EXTENTS  DS    H                            *  TOTAL EXTENTS
EXTENTNO DS    F                            *  EXTENT NUMBER
RNAME    DS    CL6                          *  RNAME FOR ENQ
TAB1ADDR DS    D                            *  DSCB START + END
TAB2ADDR DS    D                            *  EXTENT STRT + END
TAB2CNT  DS    H                            *  EXTENT COUNTER
DBL      DS    D                            *  DBLEWORD WRK AREA
MSGWORD  DS    F                            *  MSGRTN WRK ADDR
PREVEND  DS    F                            *  PREV EXTENT END
HEXIN    DS    CL10                         *  CONVERSION INPUT
HEXOUT   DS    CL20                         *  CONVERSION OUTPUT
HEXPARM  DS    0F                           *
         DC    A(*-*)                       *  POINTER TO PARMS
         DC    A(*-*)                       *  HEX START ADDRESS
         DC    A(*-*)                       *  HEX END   ADDRESS
         DC    A(0)                         *  RELATIVE ADDRESS
         DC    X'40'                        *  FLAG BYTE
         DC    AL3(*-*)                     *  BUFFER ADDRESS
FLAG     DS    X                            *  ERROR FLAG
VTOCERRS EQU   X'01'                        *  ANY VTOC ERROR
EXTNTERR EQU   X'03'                        *  EXTENT ERROR
SIZEERR  EQU   X'05'                        *  EOV EXTENT ERROR
SPACEERR EQU   X'09'                        *  FREESPACE ERROR
         #STOPWA ,                          *
         PRINT NOGEN
         DSECT ,                            *
DSCBTAB  DS    CL4                          *
         IECSDSL1 1                         *  FORMAT1 DSCB
         ORG   IECSDSL1                     *
         IECSDSL1 2                         *  FORMAT2 DSCB
         ORG   IECSDSL1                     *
         IECSDSL1 3                         *  FORMAT3 DSCB
         ORG   IECSDSL1+L'DS1DSNAM          *
         IECSDSL1 4                         *  FORMAT4 DSCB
         ORG   IECSDSL1                     *
         IECSDSL1 5                         *  FORMAT5 DSCB
         ORG   IECSDSL1                     *
         IECSDSL1 6                         *  FORMAT6 DSCB
DSCBEND  EQU   *                            *  END OF DSCB
         DCBD  DEVD=DA,DSORG=PS             *  DCB MAP
CVAFBUFF ICVAFBFL DSECT=YES                 *  CVAF BUFFER MAP
CVPL     ICVAFPL  DSECT=YES                 *  CVAF PARM LIST
         DSECT ,                            *
         IEZDEB   LIST=YES                  *  DEB MAP
         DSECT ,                            *
         IEFJFCBN LIST=YES                  *  JFCB MAP
         DSECT ,                            *
         IHAPSA ,                           *  PSA MAP
         DSECT ,                            *
         IKJTCB ,                           *  TCB MAP
         DSECT ,                            *
         IEFTIOT1 ,                         *  TIOT MAP
         DSECT ,                            *
         IEFUCBOB LIST=NO                   *  UCB MAP
         DSECT ,                            *
EXTABLE  DS    0F                           *  ENTENT TABLE
STRTRACK DS    XL4                          *  - START REL TRK
ENDTRACK DS    XL4                          *  - END   REL TRK
STRTCCHH DS    XL4                          *  - START CCHH
ENDCCHH  DS    XL4                          *  - END   CCHH
EXTNTLEN DS    XL4                          *  - EXTENT LENGTH
EXTNTNO  DS    XL4                          *  - EXTENT NUMBER
TOTEXTNT DS    XL4                          *  - TOTAL EXTENTS
EXDSNAME DS    CL44                         *  - DATASET NAME
EXDSORG  DS    CL3                          *  - DSORG
EXRECFM  DS    CL5                          *  - RECFM
EXLRECL  DS    CL6                          *  - LRECL
EXBLKSIZ DS    CL6                          *  - BLKSIZE
EXTNTEND EQU   *                            *  - END OF TABLE
         PUSH  USING,PRINT
         PRINT ON,GEN
         DROP  ,
         TITLE '- MSGRTN -  MESSAGE FORMATING SUBROUTINE'
MSGRTN   #START REG1=R10,USING=(MSGWRK,R11),BASE=R12
         TITLE '- MSGRTN -  WORK AREA INITIALIZATION ROUTINE.'
*---------------------------------------------------------------------*
*        INITIALIZE WORK AREA FIRST TIME AROUND                       *
*---------------------------------------------------------------------*
INITIAL0 DS    0H
         L     R2,4(R10)                    LOAD WORK AREA PTR
         ICM   R11,B'1111',0(R2)            LOAD WORK AREA
         BNZ   BRANCH00                     SKIP IF ALREADY DONE
         L     R3,=A(MSGWKLEN)              LOAD WORK AREA LENGTH
         GETMAIN R,LV=(R3)                  GET WORK SPACE
         ST    R1,0(R2)                     SAVE WORK AREA PTR
         LR    R11,R1                       COPY WORK AREA PTR
         LR    R2,R1                        POINT TO START
         LR    R4,R1                        POINT TO START
         SLR   R5,R5                        CLEAR REGISTER
         MVCL  R2,R4                        CLEAR WORK AREA
         LA    R1,DOMLIST                   POINT TO DOM LIST
         ST    R1,DOMPTR                    INITIALIZE POINTER
         MVI   TITLES,C' '                  INSERT A BLANK
         MVC   TITLES+1(255),TITLES         PROPAGATE BLANKS
         MVI   LINE,C' '                    MOVE IN A BLANK
         MVC   LINE+1(255),LINE             PROPAGATE BLANKS
         MVC   LINENO,=F'99'                FORCE FIRST TITLE
         MVC   MSGDCB(DMYDCBL),DMYDCB       INITIALIZE DCB
         LA    R1,EXITLIST                  POINT TO EXITLIST
         STCM  R1,B'0111',MSGDCB+DCBEXLSA-IHADCB
         LA    R1,PRNTJFCB                  POINT TO JFCB AREA
         ST    R1,EXITLIST                  SAVE JFCB ADDRESS
         MVI   EXITLIST,X'87'               SETUP EXITLIST
         MVC   OPENL(1),=AL1(143)           SETUP FOR OPEN
         MVC   RDJFCBL(1),=AL1(128)         SERUP FOR RDJFCB
         L     R1,PSATOLD-PSA(R0)           LOAD TCB ADDRESS
         L     R1,TCBTIO-TCB(R1)            LOAD TIOT ADDRESS
         LA    R1,TIOENTRY-TIOT1(R1)        SKIP OVER HEADER
*---------------------------------------------------------------------*
*        SCAN FOR THE SYSPRINT FILE                                   *
*---------------------------------------------------------------------*
INITIAL1 DS    0H
         CLC   MSGDCB+DCBDDNAM-IHADCB(8),TIOEDDNM-TIOENTRY(R1)
         BE    INITIAL2                     IF SO OPEN IT
         SLR   R0,R0                        CLEAR REGISTER
         IC    R0,TIOELNGH-TIOENTRY(R1)     INSERT ENTRY LEN
         ALR   R1,R0                        POINT TO NEXT ONE
         CLI   TIOELNGH-TIOENTRY(R1),0      ENTRY LENGTH = 0
         BNE   INITIAL1                     IF NOT KEEP TRYING
         OI    MSGFLAG,NOPRINT              SET DDNAME FLAG
*---------------------------------------------------------------------*
*        SETUP SYSPRINT DCB TO SPECIFIED LRECL OR DEFAULT TO 133      *
*---------------------------------------------------------------------*
INITIAL2 DS    0H
         LA    R2,MSGDCB                    POINT TO DCB
         RDJFCB ((R2)),MF=(E,RDJFCBL)       READ THE JFCB
         CLI   PRNTJFCB+JFCRECFM-INFMJFCB,X'00'
         BNE   INITIAL3                     IF RECFM THERE SKIP
         OI    PRNTJFCB+JFCRECFM-INFMJFCB,JFCFIX+JFCRFB+JFCASA
INITIAL3 DS    0H
         CLC   PRNTJFCB+JFCLRECL-INFMJFCB(2),=H'00'
         BNE   INITIAL4                     IF LRECL THERE SKIP
         MVC   PRNTJFCB+JFCLRECL-INFMJFCB(2),=H'133'
INITIAL4 DS    0H
         CLC   PRNTJFCB+JFCBLKSI-INFMJFCB(2),=H'00'
         BNE   INITIAL5                     IF BLKSI THERE SKIP
         MVC   PRNTJFCB+JFCBLKSI-INFMJFCB(2),PRNTJFCB+JFCLRECL-INFMJFCB
INITIAL5 DS    0H
         CLC   PRNTJFCB+JFCBDSNM-INFMJFCB(44),=CL44'TERMFILE '
         BNE   INITIAL6                     IF NOT CONTINUE
         OI    MSGFLAG,NOPRINT              SET DDNAME FLAG
INITIAL6 DS    0H
         TM    MSGDCB+DCBOFLGS-IHADCB,DCBOFOPN
         BO    INITIAL7                     SKIP IF NOW OPEN
         TM    MSGFLAG,NOPRINT              CHECK FOR NO PRINT
         BO    INITIAL7                     SKIP IF NO PRINT
         OPEN  ((R2),(OUTPUT)),TYPE=J,MF=(E,OPENL)
         TM    MSGDCB+DCBOFLGS-IHADCB,DCBOFOPN
         BO    INITIAL7                     CONTINUE IF OPEN
         OI    MSGFLAG,NOPRINT              SET NOPRINT FLAG
*---------------------------------------------------------------------*
*        SAVE LRECL AND SETUP THE TITLE AREAS                         *
*---------------------------------------------------------------------*
INITIAL7 DS    0H
         LH    R3,PRNTJFCB+JFCLRECL-INFMJFCB
         CH    R3,=F'12'                    CHECK FOR MINIMUM
         BL    ERROR030                     IF LOWER ERROR
         BCTR  R3,R0                        DECREMENT LENGTH
         STH   R3,MSGLRECL                  SAVE MAXIMUM LENGTH
         SH    R3,=H'10'                    SUB PAGE NO. LENGTH
         STH   R3,TITLEN                    SAVE TITLE LENGTH
         LA    R5,TITLES                    POINT TO TITLE
         LA    R3,1(R3,R5)                  POINT TO PAGE AREA
         MVC   0(6,R3),=C' PAGE '           MOVE PAGE CONSTANT
         LA    R3,6(,R3)                    PNT PAST CONSTANT
         ST    R3,PAGEADDR                  SAVE PAGE NO. PTR
         L     R15,PSAAOLD-PSA(R0)          LOAD ASCB ADDRESS
         CLC   ASCBJBNI-ASCB(4,R15),=F'0'   CHECK FOR JOBNM PTR
         BNE   BRANCH00                     IF NONE...TSO USER
         OI    MSGFLAG,TSOUSER              SET TSOUSER FLAG
         TITLE '- MSGRTN -  BRANCH TO PROPER MESSAGE ROUTINE.'
*---------------------------------------------------------------------*
*        BRANCH TO PROPER ROUTINE                                     *
*---------------------------------------------------------------------*
BRANCH00 DS    0H
         L     R3,0(R10)                    LOAD MESSAGE TYPE
         MH    R3,=H'04'                    CALC BRANCH OFFSET
         CH    R3,=H'28'                    IS TYPE VALID
         BH    ERROR020                     IF NOT TERMINATE
         B     BRANCH10(R3)                 BRANCH TO ROUTINE
BRANCH10 DS    0H
         B     ERROR020      (R3=00)        BRANCH FOR UNKNOWN
         B     PRINT000      (R3=04)        BRANCH FOR #PRINT
         B     WTO00000      (R3=08)        BRANCH FOR #WTO
         B     TITLE000      (R3=12)        BRANCH FOR #TITLE
         B     HEAD0000      (R3=16)        BRANCH FOR #HEADING
         B     WTOR0000      (R3=20)        BRANCH FOR #WTOR
         B     DOM00000      (R3=24)        BRANCH FOR #DOM
         B     TPUT0000      (R3=28)        BRANCH FOR #TPUT
         TITLE '- MSGRTN -  PROCESS #PRINT PARAMETERS.'
*---------------------------------------------------------------------*
*        PROCESS THE #PRINT PARAMETERS                                *
*---------------------------------------------------------------------*
PRINT000 DS    0H
         TM    MSGFLAG,NOPRINT              CHECK FOR DD CARD
         BO    ERROR010                     IF MISSING ERROR
         LA    R10,8(,R10)                  SKIP USED PARMS
PRINT010 DS    0H
         CLC   LINENO,=F'60'                PAGE LIMIT EXCEEDED
         BL    PRINT030                     IF NOT PRINT LINE
         L     R2,PAGENO                    LOAD OLD PAGE COUNT
         LA    R2,1(,R2)                    ADD 1 TO COUNT
         ST    R2,PAGENO                    SAVE NEW PAGE COUNT
         CVD   R2,DOUBLE                    CONVERT TO DECIMAL
         L     R2,PAGEADDR                  POINT TO TITLE AREA
         MVC   0(4,R2),PAGEMASK             MOVE EDIT MASK
         ED    0(4,R2),DOUBLE+6             EDIT PAGE NUMBER
         MVI   TITLES,C'1'                  SET PAGE EJECT
         MVI   HEADINGS,C'0'                SET LINE SKIP
         MVI   LINE,C'0'                    SET LINE SKIP
         PUT   MSGDCB,TITLES                PRINT TITLE
         LA    R5,HEADINGS                  POINT TO HEADING
         LA    R2,2                         SET LINECOUNT TO 2
         ST    R2,LINENO                    SAVE LINE COUNT
         ICM   R6,B'1111',HEADNOS           LOAD HEADING COUNT
         BZ    PRINT030                     IF NO HEADING SKIP
         LA    R2,1(,R2)                    ADD 1 FOR LINE SKIP
PRINT020 DS    0H
         PUT   MSGDCB,0(R5)                 PRINT HEADING
         LA    R5,256(,R5)                  POINT TO NEXT ONE
         LA    R2,1(,R2)                    ADD 1 TO LINE COUNT
         BCT   R6,PRINT020                  LOOP TILL ALL DONE
         ST    R2,LINENO                    SAVE LINE COUNT
PRINT030 DS    0H
         L     R2,0(R10)                    LOAD TEXT ADDRESS
         SLR   R5,R5                        CLEAR REGISTER
         ICM   R5,B'0001',0(R2)             LOAD TEXT LENGTH
         BZ    PRINT040                     CHECK FOR ZERO
         BCTR  R5,R0                        DECREMENT FOR EXEC
         EX    R5,EXECMVC1                  MVC LINE+1(?),1(R2)
PRINT040 DS    0H
         PUT   MSGDCB,LINE                  PRINT TEXT LINE
         MVI   LINE,C' '                    MOVE IN A BLANK
         MVC   LINE+1(254),LINE             PROPAGATE BLANKS
         L     R2,LINENO                    LOAD OLD LINECOUNT
         LA    R2,1(R2)                     ADD 1 TO LINECOUNT
         ST    R2,LINENO                    SAVE NEW LINECOUNT
         TM    0(R10),X'80'                 CHECK FOR PARM END
         BO    EXIT                         IF ALL DONE QUIT
         LA    R10,4(,R10)                  SKIP TO NEXT PARM
         B     PRINT010                     KEEP PRINTING
         TITLE '- MSGRTN -  PROCESS #WTO PARAMETERS.'
*---------------------------------------------------------------------*
*        PROCESS THE #WTO PARAMETERS                                  *
*---------------------------------------------------------------------*
WTO00000 DS    0H
         L     R4,8(R10)                    LOAD WTO LIST ADDR
         LA    R10,12(,R10)                 SKIP USED PARMS
         TM    MSGFLAG,TSOUSER              CHECK FOR TSO USER
         BO    WTOTPUT0                     IF SO ISSUE TPUT
         MVC   MCSFLAGS(2),2(R4)            SAVE MCS FLAGS
         LH    R5,0(R4)                     LOAD TEXT LENGTH
         LA    R4,0(R5,R4)                  POINT PAST TEXT
         MVC   DESCODES(4),0(R4)            DESC + ROUTE CODES
WTO00010 DS    0H
         L     R2,0(R10)                    LOAD MESSAGE TEXT
         SLR   R5,R5                        CLEAR REGISTER
         ICM   R5,B'0001',0(R2)             LOAD TEXT LENGTH
         BZ    WTO00050                     CHECK FOR ZERO
         BCTR  R5,R0                        DECREMENT FOR EXEC
         CH    R5,=H'123'                   CHECK FOR MAX - 124
         BNH   WTO00020                     IF OK CONTINUE
         LH    R5,=H'123'                   IF OVER 124 TRUNCATE
WTO00020 DS    0H
         EX    R5,EXECMVC2                  MVC WTOTEXT(?),1(R2)
         LA    R5,5(,R5)                    ALLOW FOR LEN + MCS
         STH   R5,WTOL                      SAVE IN PARM LIST
         LA    R4,WTOL                      POINT TO LIST FORM
         LA    R4,0(R5,R4)                  POINT PAST TEXT
         MVC   0(4,R4),DESCODES             DESC + ROUTE CODES
         LA    R1,WTOL                      POINT TO LIST FORM
         WTO   MF=(E,(R1))                  ISSUE WTO
         TM    DESCODES,B'11000000'         DESC CODE = 1 OR 2
         BO    WTO00030                     IF SO SAVE MSGID
         TM    DESCODES+1,B'00100000'       DESC CODE = 11
         BNO   WTO00050                     IF NOT DONT SAVE MSGID
WTO00030 DS    0H
         LR    R3,R1                        COPY MSGID
         L     R4,DOMPTR                    LOAD AVAILABLE PTR
         LA    R5,DOMEND                    POINT TO END OF LIST
         CR    R4,R5                        CHECK IF AT END
         BNE   WTO00040                     IF NOT JUST SAVE ID
         SH    R4,=H'04'                    BACK UP TO PREVIOUS
         OI    0(R4),X'80'                  SET END OF DOM LIST
         LA    R4,DOMLIST                   POINT TO DOM LIST
         DOM   MSGLIST=(R4)                 DOM ALL MESSAGES
         XC    DOMLIST(60),DOMLIST          CLEAR THE LIST
WTO00040 DS    0H
         ST    R3,0(R4)                     SAVE THIS MSGID
         LA    R4,4(,R4)                    POINT TO NEXT ONE
         ST    R4,DOMPTR                    SAVE NEW POINTER
WTO00050 DS    0H
         TM    0(R10),X'80'                 CHECK FOR PARM END
         BO    EXIT                         IF ALL DONE QUIT
         LA    R10,4(,R10)                  SKIP USED PARM
         B     WTO00010                     ISSUE NEXT WTO
WTOTPUT0 DS    0H
         L     R2,0(R10)                    LOAD TEXT ADDRESS
         SLR   R5,R5                        CLEAR REGISTER
         ICM   R5,B'0001',0(R2)             LOAD TEXT LENGTH
         BZ    WTOTPUT1                     CHECK FOR ZERO
         BCTR  R5,R0                        DECREMENT FOR EXEC
         EX    R5,EXECMVC1                  MVC LINE+1(?),1(R2)
WTOTPUT1 DS    0H
         LA    R5,1(,R5)                    RESTORE LENGTH
         TPUT  LINE+1,(R5)                  TPUT TEXT LINE
         MVI   LINE,C' '                    MOVE IN A BLANK
         MVC   LINE+1(255),LINE             PROPAGATE BLANKS
         TM    0(R10),X'80'                 CHECK FOR PARM END
         BO    EXIT                         IF ALL DONE QUIT
         LA    R10,4(,R10)                  SKIP TO NEXT PARM
         B     WTOTPUT0                     KEEP TPUTING
         TITLE '- MSGRTN -  PROCESS #TITLE PARAMETERS.'
*---------------------------------------------------------------------*
*        PROCESS THE #TITLE PARAMETERS                                *
*---------------------------------------------------------------------*
TITLE000 DS    0H
         LA    R10,8(,R10)                  SKIP USED PARMS
         L     R2,0(R10)                    LOAD TITLE ADDR
         LH    R6,TITLEN                    LOAD TITLE LENGTH
         BCTR  R6,R0                        DECREMENT FOR EXEC
         MVI   TITLES+1,C' '                MOVE IN A BLANK
         EX    R6,EXECMVC3                  MVC TITLES+2(?),TITLES+1
         LA    R6,1(,R6)                    RESET LENGTH
         SLR   R5,R5                        CLEAR REGISTER
         ICM   R5,B'0001',0(R2)             INSERT TEXT LENGTH
         CR    R5,R6                        CHECK FOR MAXIMUM
         BNH   TITLE010                     IF OK PROCEED
         LR    R5,R6                        TRUNCATE TO MAX LEN
TITLE010 DS    0H
         LH    R8,TITLEN                    LOAD TITLE LENGTH
         SR    R8,R5                        CALC DIFFERENCE
         BZ    TITLE020                     IF ZERO PROCEED
         SRL   R8,1                         DIV TO FIND OFFSET
TITLE020 DS    0H
         LA    R7,TITLES+1                  POINT TO TITLE LINE
         LA    R7,0(R8,R7)                  CENTER THE TITLE
         BCTR  R5,R0                        DECREMENT FOR EXEC
         EX    R5,EXECMVC4                  MVC 0(?,R7),1(R2)
         TM    0(R10),X'80'                 ANY MORE PARMS
         BO    EXIT                         IF NOT TERMINATE
         LA    R10,4(,R10)                  ADVANCE POINTER
         MVC   LINENO,=F'99'                SET LINECT TO MAX
         B     HEAD0010                     PROCESS ANY HEADINGS
         TITLE '- MSGRTN -  PROCESS #HEADING PARAMETERS.'
*---------------------------------------------------------------------*
*        PROCESS THE #HEADING PARAMETERS                              *
*---------------------------------------------------------------------*
HEAD0000 DS    0H
         LA    R10,8(,R10)                  SKIP USED PARMS
HEAD0010 DS    0H
         LA    R7,HEADINGS+1                POINT TO HEADINGS
         SLR   R6,R6                        CLEAR HEADING COUNTER
         ST    R6,HEADNOS                   SET COUNT TO ZERO
HEAD0020 DS    0H
         CH    R6,=H'06'                    CHECK FOR MAXIMUM
         BE    EXIT                         IF MAX TERMINATE
         L     R2,0(R10)                    LOAD HEADING TEXT
         MVI   0(R7),C' '                   MOVE IN A BLANK
         MVC   1(255,R7),0(R7)              PROPAGATE BLANKS
         SLR   R4,R4                        CLEAR REGISTER
         ICM   R4,B'0001',0(R2)             INSERT TEXT LENGTH
         BZ    HEAD0030                     IF ZERO TRY NEXT
         BCTR  R4,0                         DECREMENT FOR EXEC
         EX    R4,EXECMVC4                  MVC 0(?,R7),1(R2)
HEAD0030 DS    0H
         LA    R7,256(,R7)                  ADVANCE POINTER
         LA    R6,1(,R6)                    ADD 1 TO COUNTER
         ST    R6,HEADNOS                   SAVE HEADINGS COUNT
         TM    0(R10),X'80'                 ANY MORE PARMS
         BO    EXIT                         IF NOT TERMINATE
         LA    R10,4(,R10)                  POINT TO NEXT PARM
         B     HEAD0020                     PROCESS NEXT HEADING
         TITLE '- MSGRTN -  PROCESS #WTOR PARAMETERS.'
*---------------------------------------------------------------------*
*        PROCESS THE #WTOR PARAMETERS                                 *
*---------------------------------------------------------------------*
WTOR0000 DS    0H
         TM    MSGFLAG,TSOUSER              CHECK FOR TSO USER
         BO    PUTGET00                     IF SO ISSUE TPUT/TGET
         L     R4,8(R10)                    LOAD WTOR LIST ADDR
         LA    R10,12(,R10)                 SKIP USED PARMS
         MVC   WTORL(8),0(R4)               SAVE LEN/REPLY/ECB
         MVC   MCSFLAGS(2),10(R4)           SAVE MCS FLAGS
         LH    R5,8(R4)                     LOAD TEXT LENGTH
         LA    R4,8(R5,R4)                  POINT PAST TEXT
         MVC   DESCODES(4),0(R4)            DESC + ROUTE CODES
WTOR0010 DS    0H
         L     R2,0(R10)                    LOAD MESSAGE TEXT
         SLR   R5,R5                        CLEAR REGISTER
         ICM   R5,B'0001',0(R2)             LOAD TEXT LENGTH
         BZ    WTOR0030                     CHECK FOR ZERO
         BCTR  R5,R0                        DECREMENT FOR EXEC
         CH    R5,=H'123'                   CHECK FOR MAX - 124
         BNH   WTOR0020                     IF OK CONTINUE
         LH    R5,=H'123'                   IF OVER 124 TRUNCATE
WTOR0020 DS    0H
         EX    R5,EXECMVC2                  MVC WTOTEXT(?),1(R2)
         LA    R5,5(,R5)                    ALLOW FOR LEN + MCS
         STH   R5,WTOL                      SAVE IN PARM LIST
         LA    R4,WTOL                      POINT TO LIST FORM
         LA    R4,0(R5,R4)                  POINT PAST TEXT
         MVC   0(4,R4),DESCODES             DESC + ROUTE CODES
         LA    R1,WTORL                     POINT TO LIST FORM
         WTOR  MF=(E,(R1))                  ISSUE WTO
WTOR0030 DS    0H
         LR    R3,R1                        COPY MSGID
         L     R4,DOMPTR                    LOAD AVAILABLE PTR
         LA    R5,DOMEND                    POINT TO END OF LIST
         CR    R4,R5                        CHECK IF AT END
         BNE   WTOR0040                     IF NOT JUST SAVE ID
         SH    R4,=H'04'                    BACK UP TO PREVIOUS
         OI    0(R4),X'80'                  SET END OF DOM LIST
         LA    R4,DOMLIST                   POINT TO DOM LIST
         DOM   MSGLIST=(R4)                 DOM ALL MESSAGES
         XC    DOMLIST(60),DOMLIST          CLEAR THE LIST
WTOR0040 DS    0H
         ST    R3,0(,R4)                    SAVE THIS MSGID
         LA    R4,4(,R4)                    POINT TO NEXT ONE
         ST    R4,DOMPTR                    SAVE NEW POINTER
         B     EXIT                         ONLY 1 WTOR ALLOWED
         TITLE '- MSGRTN -  PROCESS #DOM PARAMETERS.'
*---------------------------------------------------------------------*
*        PROCESS THE #DOM PARAMETERS                                  *
*---------------------------------------------------------------------*
DOM00000 DS    0H
         TM    MSGFLAG,TSOUSER              CHECK FOR TSO USER
         BO    EXIT                         IF SO NO NEED TO DOM
         TM    0(R10),X'80'                 ANY SPECIFIC MSG
         BO    DOM00020                     IF NOT CONTINUE
DOM00010 DS    0H
         LA    R10,4(,R10)                  POINT TO NEXT PARM
         L     R2,0(R10)                    POINT TO MSG TO DOM
         DOM   MSG=(R2),REPLY=YES           DOM THIS MSGS
         TM    0(R10),X'80'                 ANY MORE MSGS TO DOM
         BNO   DOM00010                     IF SO LOOP TILL DONE
         B     EXIT                         IF NOT TERMINATE
DOM00020 DS    0H
         L     R2,DOMPTR                    LOAD LAST ADDRESS
         LA    R3,DOMLIST                   LOAD START ADDRESS
         CR    R2,R3                        ANY MSGS TO DOM
         BE    EXIT                         IF NOT TERMINATE
         SH    R2,=H'04'                    POINT TO LAST MSGID
         OI    0(R2),X'80'                  SET END OF DOMLIST
         LA    R3,DOMLIST                   POINT TO DOM LIST
         DOM   MSGLIST=(R3)                 DOM MESSAGE LIST
         XC    DOMLIST(60),DOMLIST          CLEAR THE LIST
         ST    R3,DOMPTR                    SAVE MSGID POINTER
         B     EXIT                         TERMINATE
         TITLE '- MSGRTN -  PROCESS #TPUT PARAMETERS.'
*---------------------------------------------------------------------*
*        PROCESS THE #TPUT PARAMETERS                                 *
*---------------------------------------------------------------------*
TPUT0000 DS    0H
         TM    MSGFLAG,TSOUSER              CHECK FOR TSO USER
         BNO   EXIT                         IF NOT TERMINATE
         LA    R10,8(,R10)                  SKIP USED PARMS
         LH    R7,MSGDCB+DCBLRECL-IHADCB    LOAD LRECL
         BCTR  R7,R0                        SUB CARRAGE CONTROL
TPUT0010 DS    0H
         CLC   LINENO,=F'24'                IS LINECOUNT <= 24
         BNH   TPUT0030                     IF SO PROCEED
         L     R2,PAGENO                    LOAD OLD PAGE COUNT
         LA    R2,1(,R2)                    ADD 1 TO COUNT
         ST    R2,PAGENO                    SAVE NEW PAGE COUNT
         CVD   R2,DOUBLE                    CONVERT TO DECIMAL
         L     R2,PAGEADDR                  POINT TO PAGE#
         MVC   0(4,R2),PAGEMASK             MOVE EDIT MASK
         ED    0(4,R2),DOUBLE+6             EDIT PAGE NUMBER
         TPUT  TITLES+1,(R7)                TITLE TO TERMINAL
         LA    R5,HEADINGS+1                POINT TO HEADING
         LA    R2,1                         SET LINECOUNT TO 1
         ST    R2,LINENO                    SAVE LINE COUNT
         ICM   R6,B'1111',HEADNOS           LOAD HEADING COUNT
         BZ    TPUT0030                     IF NO HEADING PROCEED
TPUT0020 DS    0H
         TPUT  (R5),(R7)                    TPUT HEADING
         LA    R5,256(,R5)                  POINT TO NEXT ONE
         LA    R2,1(R2)                     ADD 1 TO LINE COUNT
         BCT   R6,TPUT0020                  LOOP TILL ALL DONE
         ST    R2,LINENO                    SAVE LINE COUNT
TPUT0030 DS    0H
         L     R2,0(R10)                    LOAD TEXT ADDRESS
         SLR   R5,R5                        CLEAR REGISTER
         ICM   R5,B'0001',0(R2)             LOAD TEXT LENGTH
         BZ    TPUT0040                     CHECK FOR ZERO
         BCTR  R5,R0                        DECREMENT FOR EXEC
         EX    R5,EXECMVC1                  MVC LINE+1(?),1(R2)
TPUT0040 DS    0H
         TPUT  LINE+1,(R7)                  TPUT TEXT LINE
         MVI   LINE,C' '                    MOVE IN A BLANK
         MVC   LINE+1(255),LINE             PROPAGATE BLANKS
         L     R2,LINENO                    LOAD OLD LINECOUNT
         LA    R2,1(,R2)                    ADD 1 TO LINECOUNT
         ST    R2,LINENO                    SAVE NEW LINECOUNT
         TM    0(R10),X'80'                 CHECK FOR PARM END
         BO    EXIT                         IF ALL DONE QUIT
         LA    R10,4(,R10)                  SKIP TO NEXT PARM
         B     TPUT0010                     KEEP TPUTING
         TITLE '- MSGRTN -  PROCESS #WTOR PARAMETERS FOR TSO.'
*---------------------------------------------------------------------*
*        PROCESS THE #WTOR PARAMETERS FOR A TSO USER                  *
*---------------------------------------------------------------------*
PUTGET00 DS    0H
         L     R2,8(R10)                    LOAD WTOR LIST FORM
         SLR   R5,R5                        CLEAR REGISTER
         IC    R5,0(R2)                     LOAD REPLY LENGTH
         SLR   R6,R6                        CLEAR REGISTER
         ICM   R6,B'0111',1(R2)             LOAD REPLY ADDRESS
         L     R7,4(R2)                     GET ECB ADDR
         L     R2,12(R10)                   LOAD TEXT ADDRESS
         SLR   R3,R3                        CLEAR REGISTER
         ICM   R3,B'0001',0(R2)             INSERT TEXT LENGTH
         BZ    PUTGET40                     IF ZERO POST ECB
         BCTR  R3,R0                        DECREMENT FOR EXEC
         MVC   LINE(4),=C'*00 '             SIMULATE WTOR MESSAGE
         EX    R3,EXECMVC5                  MVC LINE+4(?),1(R2)
         LA    R3,5(,R3)                    RESTORE TEXT LENGTH#DD88103
         TPUT  LINE,(R3)                    TPUT MSG
PUTGET10 DS    0H
         MVI   LINE,C' '                    MOVE IN A BLANK
         MVC   LINE+1(255),LINE             PROPAGATE BLANKS
         TGET  LINE,256,EDIT,WAIT           GET RESPONSE FROM TERMINAL
         CR    R1,R5                        CHECK RESPONSE LENGTH
         BNH   PUTGET30                     IF ACCEPTABLE CONTINUE
         TPUT  TGETERR,L'TGETERR            TPUT ERROR MESSAGE
         B     PUTGET10                     RETRY TGET
PUTGET30 DS    0H
         OC    LINE(256),=256X'40'          CONVERT TO UPPER CASE
         BCTR  R5,R0                        DECREMENT FOR EXEC
         EX    R5,EXECMVC6                  MVC 0(?,R6),LINE
PUTGET40 DS    0H
         POST  (R7)                         STOP WAITING
         B     EXIT                         TERMINATE
         TITLE '- MSGRTN -  ERROR ROUTINES.'
*---------------------------------------------------------------------*
*        PROCESS ANY ERRORS                                           *
*---------------------------------------------------------------------*
ERROR010 DS    0H
         TM    MSGFLAG,TSOUSER              CHECK FOR TSO USER
         BO    TPUT0000                     IF SO ISSUE TPUT
         TM    MSGFLAG,PRERR10              CHECK FOR PREVIOUS ERROR
         BO    ERROR011                     IF SO NO ERROR MSG
         WTO   'IEC130I SYSPRINT DD STATEMENT MISSING.',ROUTCDE=11
ERROR011 DS    0H
         LA    R15,4                        SET RC = 4
         B     TERMINAT                     TERMINATE IMMEDIATELY
ERROR020 DS    0H
         LA    R15,8                        SET RC = 8
         B     TERMINAT                     TERMINATE IMMEDIATELY
ERROR030 DS    0H
         LA    R15,12                       SET RC = 12
         B     TERMINAT                     TERMINATE IMMEDIATELY
         TITLE '- MSGRTN -  TERMINATE MESSAGE ROUTINE.'
*---------------------------------------------------------------------*
*        TERMINATION ROUTINE                                          *
*---------------------------------------------------------------------*
EXIT     DS    0H
         LA    R15,0                        SET RC = 0
TERMINAT DS    0H
         #STOP RC=(R15)                     RETURN TO CALLER
         TITLE '- MSGRTN -  CONSTANTS AND STORAGE AREA.'
*---------------------------------------------------------------------*
*        CONSTANTS AND DSECTS                                         *
*---------------------------------------------------------------------*
TGETERR  DC  C'IEE700I REPLY 00 IGNORED; REPLY TOO LONG FOR REQUESTOR.'
*
PAGEMASK DC    XL4'40202021'                PAGE NUMBER EDIT MASK
*
EXECMVC1 MVC   LINE+1(*-*),1(R2)            **** EXECUTED ****
EXECMVC2 MVC   WTOTEXT(*-*),1(R2)           **** EXECUTED ****
EXECMVC3 MVC   TITLES+2(*-*),TITLES+1       **** EXECUTED ****
EXECMVC4 MVC   0(*-*,R7),1(R2)              **** EXECUTED ****
EXECMVC5 MVC   LINE+4(*-*),1(R2)            **** EXECUTED ****
EXECMVC6 MVC   0(*-*,R6),LINE               **** EXECUTED ****
*
DMYDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,EXLST=DMYDCB
DMYDCBL  EQU   *-DMYDCB                     DCB LENGTH
EXITLIST DS    0F
         DC    X'87',AL3(PRNTJFCB)          JFCB EXIT
PRNTJFCB DS    CL176                        JFCB AREA
         LTORG
*
         TITLE '- MSGRTN -  MESSAGE ROUTINE WORK AREA'
MSGWRK   DSECT
*
HEADNOS  DS    F                            HEADING COUNTER
DOUBLE   DS    D                            DOUBLEWORD WORK AREA
*
DOMPTR   DS    F                            FIRST AVAILABLE SLOT
DOMLIST  DS    15F                          15 MSGID SLOTS
DOMEND   EQU   DOMLIST+60                   END OF MSGID LIST
*
WTORL    DS    0F
         DC    AL1(*-*)                     REPLY LENGTH
         DC    AL3(*-*)                     REPLY ADDRESS
         DC    A(*-*)                       ECB ADDRESS
WTOL     DS    0F
         DC    AL2(*-*)                     REXT LENGTH
MCSFLAGS DC    B'0000000000000000'          MCS FLAGS
WTOTEXT  DC    CL124' '                     TEXT
DESCODES DC    B'0000000000000000'          DESCRIPTOR CODES
         DC    B'0000000000000000'          ROUTING CODES
*
TITLES   DS    CL256                        TITLE BUFFER
HEADINGS DS    6CL256                       6 HEADING SLOTS
LINE     DS    CL256                        PRINT LINE BUFFER
*
TITLEN   DS    H                            TITLE DATA LENGTH
PAGEADDR DS    F                            PAGE NUMBER ADDRESS
*
PAGENO   DS    F                            CURRENT PAGE NUMBER
LINENO   DS    F                            CURRENT LINE NUMBER
*
MSGLRECL DS    H                            USABLE RECORD LENGTH
*
MSGFLAG  DS    X                            FLAG
NOPRINT  EQU   B'00000001'                  NO OUTPUT DD CARD
TSOUSER  EQU   B'00000010'                  TSO USER
PRERR10  EQU   B'00000100'                  PREVIOUS PRINT ERROR
*
MSGDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,EXLST=MSGDCB
*
OPENL    OPEN  (MSGDCB,(OUTPUT)),MF=L
*
RDJFCBL  RDJFCB (MSGDCB),MF=L
*
MSGWKLEN EQU   *-MSGWRK
         PRINT NOGEN
         TITLE '- MSGRTN -  ADDRESS SPACE CONTROL BLOCK'
         DSECT
         IHAASCB
         PUSH  USING,PRINT
         PRINT ON,GEN
         DROP  ,
         TITLE '- GETFMT5  -  FORMAT5.DSCB FORMATING SUBROUTINE'
GETFMT5  #START ,
         TITLE '- GETFMT5  -  INITIALIZATION'
*---------------------------------------------------------------------*
*        FIND THE DCB / DEB / UCB ADDRESSES                           *
*---------------------------------------------------------------------*
         L     R5,0(R1)                     >>---> DCB
         LA    R5,0(,R5)                    TURN OFF HIGH ORDER
         L     R1,DCBDEBAD-IHADCB(R5)       >>---> DEB
         ST    R1,DEBADDR                   SAVE DEB ADDRESS
         SLR   R2,R2               CLEAR BEFORE INSERT
         ICM   R2,B'0111',DEBSUCBA+1-DEBBASIC(R1)     >>---> UCB
         ST    R2,UCBADDR                   SAVE UCB ADDRESS
         TITLE '- GETFMT5  -  CALCULATE AND AQUIRE STORAGE SPACE'
*---------------------------------------------------------------------*
*        FIND THE NUMBER OF EXTENTS AND GET ENOUGH SPACE              *
*---------------------------------------------------------------------*
         LR    R0,R2                        >>---> UCB
         LA    R1,LSPACE                    >>---> RETURN BUFFER
         SVC   78                           ISSUE LSPACE SVC
         CLI   LSPEXTS+4,C'/'               MAKE SURE IT WORKED
         BNE   EXITRC8                      IF NOT TERMINATE
         PACK  DBLWORD,LSPEXTS(4)           PACK EXTENTS NUMBER
         CVB   R2,DBLWORD                   GET IN BINARY
         LR    R9,R2                        SAVE EXTENT COUNT
         MH    R2,=H'5'                     CALCULATE SPACE REQ
         LA    R2,4(,R2)                    ALLOW FOR COUNT FLD
         GETMAIN R,LV=(R2)                  GET FMT5 SPACE
         ST    R1,TABADDR                   SAVE START ADDRESS
         LR    R3,R1                        SAVE START ADDRESS
         LA    R2,0(R2,R1)                  POINT TO END ADDRESS
         ST    R2,ENDADDR                   SAVE END   ADDRESS
         ST    R9,0(R1)                     SAVE EXTENT COUNT
         TITLE '- GETFMT5  -  VTOC TYPE DETERMINATION'
*---------------------------------------------------------------------*
*        DETERMINE IF THE VOLUME IS INDEXED OR NOT                    *
*---------------------------------------------------------------------*
         L     R6,UCBADDR                   LOAD UCB ADDRESS
         CVAFTST UCB=(R6)                   CHECK IF INDEXED
         B     *+4(R15)                     BRANCH ACCORDINGLY
         B     SPACE040                     DF/DS NOT ON SYSTEM
         B     SPACE040                     VTOC IS NOT INDEXED
         B     SPACE020                     VTOC IS INDEXED
         B     EXITRC8                      VOLUME NOT MOUNTED
         B     EXITRC8                      UCB NOT DASD DEVT
         TITLE '- GETFMT5  -  BUILD INDEXED VTOC FREESPACE TABLE'
*---------------------------------------------------------------------*
*        PROCESS ANY VOLUMES WITH INDEXED VTOCS                       *
*---------------------------------------------------------------------*
SPACE020 DS    0H
         LTR   R9,R9                        CHECK EXTENT COUNT
         BZ    EXITRC0                      IF NONE TERMINATE
         LA    R3,4(,R3)                    SKIP LENGTH
         MVI   F5EXTNT,X'01'                ONLY 1 AT A TIME
         LA    R4,F5EXTNT                   >>---> F5 BUFFER
         L     R6,DEBADDR                   >>---> DEB
SPACE030 DS    0H
         CVAFDSM ACCESS=MAPDATA,            GET ALL POSSIBLE           X
               MAP=VOLUME,                      FMT5'S                 X
               EXTENTS=(R4),                                           X
               DEB=(R6),                                               X
               COUNT=NO,                                               X
               BRANCH=NO
         MVC   0(5,R3),F5EXTNT+1            SAVE THIS EXTENT
         LA    R3,5(,R3)                    ADVANCE POINTER
         BCT   R9,SPACE030                  FILL THE ENTIRE TBL
         B     EXITRC0                      WHEN ALL DONE QUIT
         TITLE '- GETFMT5  -  BUILD NON-INDEXED VTOC FREESPACE TABLE'
*---------------------------------------------------------------------*
*        PROCESS ANY VOLUMES WITH NON-INDEXED VTOCS                   *
*---------------------------------------------------------------------*
SPACE040 DS    0H
         XC    READF5,READF5                CLEAR THE ECB
         READ  READF5,SF,(R5),FORMAT4,MF=(E,READFMT5)
         CHECK READF5                       WAIT FOR I/O TO END
         NOTE  (R5)                         GET FMT4 TTR0
         ST    R1,VTOCTTR                   SAVE FOR LATER
         MVC   FULLWORD,=X'00000200'        START AT FIRST FMT5
         LA    R7,FORMAT5                   POINT TO BUFFER
         LA    R3,4(,R3)                    SKIP COUNT
SPACE050 DS    0H
         C     R3,ENDADDR                   CHECK FOR END
         BE    SPACE080                     IF SO QUIT
         POINT (R5),FULLWORD                POINT TO FMT5 BLOCK
         XC    READF5,READF5                CLEAR ECB
         READ  READF5,SF,(R5),FORMAT5,MF=(E,READFMT5)
         CHECK READF5                       WAIT FOR I/O TO END
         MVC   0(5,R3),FORMAT5+DS5AVEXT-IECSDSL5
         LA    R3,5(,R3)                    ADVANCE POINTER
         LA    R9,FORMAT5+DS5EXTAV-IECSDSL5 POINT TO FORMAT5
         LA    R10,7                        LOAD LOOP COUNT
SPACE060 DS    0H
         C     R3,ENDADDR                   CHECK FOR END
         BE    SPACE080                     IF SO QUIT
         MVC   0(5,R3),0(R9)                MOVE AN EXTENT
         LA    R3,5(,R3)                    ADVANCE POINTER
         LA    R9,5(,R9)                    ADVANCE POINTER
         BCT   R10,SPACE060
         LA    R9,FORMAT5+DS5MAVET-IECSDSL5 POINT TO FORMAT5
         LA    R10,18                       LOAD LOOP COUNT
SPACE070 DS    0H
         C     R3,ENDADDR                   CHECK FOR END
         BE    SPACE080                     IF SO QUIT
         MVC   0(5,R3),0(R9)                MOVE AN EXTENT
         LA    R3,5(,R3)                    ADVANCE POINTER
         LA    R9,5(,R9)                    ADVANCE POINTER
         BCT   R10,SPACE070                 MOVE ALL EXTENTS
         SLR   R1,R1                        CLEAR REGISTER
         ICM   R1,B'0011',FORMAT4+44+DS4VTOCE+2-IECSDSL4
         MH    R1,FORMAT4+44+DS4DEVSZ+2-IECSDSL4
         SLR   R0,R0                        CLEAR REGISTER
         ICM   R0,B'0011',FORMAT4+44+DS4VTOCE+4-IECSDSL4
         AR    R1,R0                        CALC VTOC TT
         SLR   R2,R2                        CLEAR REGISTER
         ICM   R2,B'0011',FORMAT5+DS5PTRDS-IECSDSL5
         MH    R2,FORMAT4+44+DS4DEVSZ+2-IECSDSL4
         SLR   R0,R0                        CLEAR REGISTER
         ICM   R0,B'0011',FORMAT5+DS5PTRDS+2-IECSDSL5
         AR    R2,R0                        CALC FMT4 TT
         SR    R2,R1                        CALC DIFFERENCE
         STH   R2,FULLWORD                  SAVE RELATIVE TT
         MVC   FULLWORD+2(1),FORMAT5+DS5PTRDS+4-IECSDSL5
         MVI   FULLWORD+3,X'00'             MAKE SURE ZEROS
         CLC   FULLWORD,=F'00'              CHECK FOR ZEROS
         BNE   SPACE050                     IF NOT KEEP LOOPING
SPACE080 DS    0H
         POINT (R5),VTOCTTR                 POINT AT THE FMT4
         B     EXITRC0                      TERMINATE NORMALLY
         TITLE '- GETFMT5  -  TERMINATION ROUTINES'
*---------------------------------------------------------------------*
*        TERMINATE GIVING THE APPROPRIATE RETURN CODE                 *
*---------------------------------------------------------------------*
EXITRC8  DS    0H
         LA    R15,8                        SET RC = 8
         B     SPACEND                      TERMINATE
EXITRC4  DS    0H
         LA    R15,4                        SET RC = 4
         B     SPACEND                      TERMINATE
EXITRC0  DS    0H
         L     R1,TABADDR                   LOAD TABLE ADDRESS
         LA    R15,0                        SET RC = 0
SPACEND  DS    0H
         #STOP RC=(R15),R1=R1
         EJECT 1
*---------------------------------------------------------------------*
*                         CONSTANTS                                   *
*---------------------------------------------------------------------*
READFMT5 READ  READF5,SF,0,0,MF=L
F5EXTNT  DS    XL1,CL5                      LENGTH + F5INFO
DBLWORD  DS    D
FULLWORD DS    F
VTOCTTR  DS    F
DEBADDR  DS    F
UCBADDR  DS    F
TABADDR  DS    F
ENDADDR  DS    F
LSPACE   DS    0CL30
         DC    CL6'SPACE='
LSPTCYL  DC    CL4' ',CL1' '
LSPTTRK  DC    CL4' ',CL1' '
LSPEXTS  DC    CL4' ',CL1' '
LSPLCYL  DC    CL4' ',CL1' '
LSPLTRK  DC    CL4' '
FORMAT4  DS    CL140
FORMAT5  DS    CL140
         LTORG
         PUSH  USING,PRINT
         PRINT ON,GEN
         DROP  ,
         TITLE '- SORTRTN  -  TABLE SORT SUBROUTINE'
SORTRTN  #START WKDSECT=SORTWORK
*---------------------------------------------------------------------*
*        PROCESS THE SORT PARAMETERS                                  *
*---------------------------------------------------------------------*
         LM    R2,R7,0(R1)                  LOAD PARAMETERS
         LH    R2,0(R2)                     LOAD ENTRY COUNT
         STH   R2,ENTRYCNT                  SAVE ENTRY COUNT
         LH    R3,0(R3)                     LOAD ENTRY LENGTH
         STH   R3,ENTRYLEN                  SAVE ENTRY LENGTH
         LH    R4,0(R4)                     LOAD KEY LENGTH
         STH   R4,KEYLEN                    SAVE KEY LENGTH
         MVC   SEQUENCE(1),0(R5)            SAVE SORT SEQUENCE
         ST    R6,STRTADDR                  SAVE START ADDRESS
         NI    STRTADDR,X'7F'               TURN OFF HIGH BIT
         ST    R7,SAVER7                    SAVE OFFSET
         SLR   R7,R7                        CLEAR REGISTER
         TM    16(R1),X'80'                 CHECK FOR OFFSET PARM
         BO    SORT000                      IF NONE SKIP LOAD
         L     R7,SAVER7                    RESTORE OFFSET
         LH    R7,0(R7)                     LOAD KEY OFFSET
SORT000  DS    0H
         STH   R7,OFFSET                    SAVE OFFSET
         LR    R0,R3                        LOAD ENTRY LENGTH
         GETMAIN R,LV=(0)                   GET TEMPORARY STORAGE
         ST    R1,SAVEAREA                  SAVE WORKAREA ADDRESS
         NI    SAVEAREA,X'7F'               TURN OFF HIGH BIT
         CLC   ENTRYCNT(2),=H'2'            CHECK ENTRY COUNT
         BL    SORTRC0                      IF 0 OR 1 FORGET SORT
         CLC   ENTRYLEN(2),=H'00'           CHECK ELEMENT LENGTH
         BE    SORTRC4                      IF 0 ERROR
         CLC   KEYLEN(2),=H'00'             CHECK KEY LENGTH
         BE    SORTRC8                      IF 0 ERROR
         CLC   KEYLEN(2),ENTRYLEN           COMPARE KEYLEN / ENTRYLEN
         BH    SORTRC12                     IF HIGHER ERROR
         CLC   OFFSET(2),ENTRYLEN           COMPARE OFFSET / ENTRYLEN
         BH    SORTRC16                     IF GREATER ERROR
         LH    R0,OFFSET                    LOAD OFFSET
         AH    R0,KEYLEN                    ADD KEYLENGTH TO OFFSET
         CH    R0,ENTRYLEN                  COMPARE TOTAL / ENTRYLEN
         BH    SORTRC20                     IF GREATER ERROR
         CLI   SEQUENCE,C'D'                CHECK FOR DESCENDING
         BNE   SORT010                      IF NOT DEFAULT TO ASCENDING
         LA    R5,13                        SETUP FOR CONDITIONAL BRNCH
         B     SORT020                      SKIP ASCENDING SETUP
SORT010  LA    R5,11                        MUST BE ASCENDING
SORT020  SLL   R5,4                         SHIFT TO BITS 28-31
         ST    R5,BRSWITCH                  SAVE BRANCHING SWITCH
         NI    BRSWITCH,X'7F'               TURN OFF HIGH BIT
         MVI   SWITCH,0                     SEQUENCE SWITCH OFF
         LH    R8,ENTRYLEN                  LOAD ENTRY LENGTH
         LH    R9,ENTRYCNT                  LOAD RECORD COUNT
         BCTR  R9,R0                        SUBTRACT 1 FROM COUNT
         MH    R9,ENTRYLEN                  CALCULATE LENGTH OF TABLE
         LA    R9,0(R9,R6)                  POINT TO LAST ENTRY
SORT030  L     R7,STRTADDR                  POINT TO FIRST ENTRY
SORT040  BXH   R7,R8,SORT060                LOOP TILL END OF TABLE
         LR    R2,R7                        LOAD THIS ENTRY ADDRESS
         SR    R2,R8                        BACKUP TO PREVIOUS ENTRY
         AH    R2,OFFSET                    ADD OFFSET
         LH    R3,KEYLEN                    LOAD KEY LENGTH
         LR    R4,R7                        LOAD THIS ENTRY ADDRESS
         AH    R4,OFFSET                    ADD OFFSET
         LH    R5,KEYLEN                    LOAD KEY LENGTH
         CLCL  R4,R2                        COMPARE CURRENT TO PREVIOUS
         L     R5,BRSWITCH                  LOAD BRANCHING SWITCH
         EX    R5,SORT050                   BR ACCORDING TO SEQUENCE
         MVI   SWITCH,1                     SEQUENCE SWITCH ON
         LR    R2,R7                        LOAD THIS ENTRY ADDRESS
         LH    R3,ENTRYLEN                  LOAD ENTRY LENGTH
         L     R4,SAVEAREA                  LOAD SAVE AREA ADDRESS
         LH    R5,ENTRYLEN                  LOAD ENTRY LENGTH
         MVCL  R4,R2                        MOVE ENTRY TO SAVE AREA
         LR    R2,R7                        LOAD THIS ENTRY ADDRESS
         SH    R2,ENTRYLEN                  BACKUP TO PREVIOUS
         LH    R3,ENTRYLEN                  LOAD ENTRY LENGTH
         LR    R4,R7                        LOAD THIS ENTRY ADDRESS
         LH    R5,ENTRYLEN                  LOAD ENTRY LENGTH
         MVCL  R4,R2                        MOVE PREVIOUS TO ENTRY
         L     R2,SAVEAREA                  LOAD SAVE AREA ADDRESS
         LH    R3,ENTRYLEN                  LOAD ENTRY LENGTH
         LR    R4,R7                        LOAD THIS ENTRY ADDRESS
         SH    R4,ENTRYLEN                  BACKUP TO PREVIOUS
         LH    R5,ENTRYLEN                  LOAD ENTRY LENGTH
         MVCL  R4,R2                        MOVE SAVE AREA TO PREVIOUS
         B     SORT040                      PROCEED WITH LOOP
*
SORT050  NOP   SORT040                      **** EXECUTED ****
*
SORT060  CLI   SWITCH,0                     TEST SEQUENCE SWITCH
         BE    SORTRC0                      IF ZERO SORT COMPLETE
         MVI   SWITCH,0                     SEQUENCE SWITCH OFF
         B     SORT030                      START OVER AGAIN
SORTRC0  LA    R15,0                        SET RC = 0
         B     SORTEND                      TERMINATE
SORTRC4  LA    R15,4                        SET RC = 4
         B     SORTEND                      TERMINATE
SORTRC8  LA    R15,8                        SET RC = 8
         B     SORTEND                      TERMINATE
SORTRC12 LA    R15,12                       SET RC = 12
         B     SORTEND                      TERMINATE
SORTRC16 LA    R15,16                       SET RC = 16
         B     SORTEND                      TERMINATE
SORTRC20 LA    R15,20                       SET RC = 20
SORTEND  #STOP RC=(R15)                     TERMINATE
         #STARTWA ,
ENTRYCNT DS    H                            NUMBER OF ENTRIES
ENTRYLEN DS    H                            LENGTH OF EACH ENTRY
KEYLEN   DS    H                            LENGTH OF SORT KEY
SEQUENCE DS    C                            SEQUENCE (A/D)
STRTADDR DS    F                            TABLE START ADDRESS
OFFSET   DS    H                            KEY OFFSET INTO TABLE
BRSWITCH DS    F                            BRANCHING SWITCH FOR A/D
SAVEAREA DS    F                            TEMPORARY ENTRY ADDRESS
SWITCH   DS    C                            SORT SWITCH
SAVER7   DS    F                            SEQUENCE SAVE AREA
         #STOPWA ,
         END   ,                            *
./ ADD NAME=PRU      0100-00040-00040-1222-00542-00542-00000-SOURCE
PRU      TITLE 'PDS RESCUE UTILITY'
*=====================================================================*
*                                                                     *
*PURPOSE:IF YOU HAVE EVER ACCIDENTLY DONE A SAVE AT THE WRONG TIME    *
*        UNDER SPF, PROBABLY LIKE MOST PEOPLE -- YOU REALIZE          *
*        10 MILLISECS AFTER HITTING THE ENTER KEY.                    *
*                                                                     *
*        THIS PROGRAM SHOULD HELP YOU RECOVER THE ORIGINAL MEMBER     *
*        PROVIDING THAT YOU RUN THIS PROGRAM BEFORE DOING ANY         *
*        COMPRESSES. IT LOCATES ALL DEAD MEMBERS IN THE PDS AND       *
*        COPIES THEM TO AN OUTPUT PDS GIVING THEM MEMBER NAMES OF     *
*        MEM001 TO MEMNNN WHERE MEMNNN WILL BE THE MOST RECENTLY      *
*        SAVED MEMBER. (DELETED MEMBERS ARE ALSO COPIED BUT THEIR     *
*        MEM NUMBER IS NOT PREDICATABLE).                             *
*                                                                     *
*        SPF BROWSE, EDIT OR ANY EQUIVALENT MEANS MAY THEN BE USED TO *
*        LOCATE THE DESIRED ONE. IF THIS IS DONE IMMEDIATLY AFTER THE *
*        10 MILLISECS MENTIONED ABOVE IT SHOULD BE MEMNNN.            *
*                                                                     *
*                                                                     *
*METHOD: (1) READS PDS DIRECTORY AND STORES ALL ACTIVE MEMBER TTRS.   *
*                                                                     *
*        (2) READS ENTIRE INPUT PDS DATA. IF A BLOCK BEGINS AFTER     *
*            AN END-OF-FILE IT IS DEEMED TO BE THE BEGINNING OF THE   *
*            MEMBER.  IF NOT A TTR FOUND ABOVE THEN THIS IS A DEAD    *
*            MEMBER AND WRITTEN TO THE OUTPUT PDS. A SUMMARY OF EACH  *
*            MEMBER THUS WRITTEN IS LOGGED ON  THE PRT FILE.          *
*                                                                     *
*        (3) AT THE END OF PDS (HIGHEST TTR ON FILE IS REACHED) THE   *
*            OUTPUT PDS IS FIXED TO GIVE PROPER DCB ATTRUIBUTES.      *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        #DD99253 - ADD RETURN CODE 4 FOR "NO OUTPUT MEMBERS"         *
*                                                                     *
*=====================================================================*
         EJECT 1
PRU      #START BASE=(R12),WKCSECT=WORKAREA,SPLEVEL=2,                 *
               APARS=,COPY=YES,COPYYR=1999,                            *
               LEVEL=V001R01                                   #DD99253
         EJECT 1
*-------
*
*        VALIDATE PARAMETER LIST
*
*-------
         SPACE 1
         L     R1,0(R1)            >>---> PARAMETER LIST
         LH    R2,0(R1)            LOAD PARAMETER LENGTH
         LA    R1,2(,R1)           >>---> PARAMETER DATA
         CH    R2,=AL2(3)          CHECK FOR VALID LENGTH
         BNE   PRU00100
         CLC   0(3,R1),=C'ALL'     CHECK FOR PARM='ALL'
         BNE   PRU00100
         OI    FLAGBYTE,FLAGALL    REMEMBER TO COPY ALL MEMBERS
         EJECT 1
*-------
*
*        OPEN OUTPUT MESSAGE FILE
*
*-------
         SPACE 1
PRU00100 DS    0H
         LA    R2,DCBDDNAM-IHADCB+SYSPRINT
         MVC   ERROR1+17(8),0(R2)
         TIOTSCAN DDN=(R2),NOK=ERROR1
         OPEN  (SYSPRINT,OUTPUT)   OPEN THE MESSAGE FILE
         TM    DCBOFLGS-IHADCB+SYSPRINT,DCBOFOPN
         BNO   ERROR1
         EJECT 1
*-------
*
*        BUILD A MEMBER NAME/TTR TABLE
*
*-------
         SPACE 1
         LA    R2,DCBDDNAM-IHADCB+DIRDCB
         MVC   ERROR1+17(8),0(R2)
         TIOTSCAN DDN=(R2),NOK=ERROR1
         OPEN  (DIRDCB,INPUT)      OPEN THE DIRECTORY
         TM    DIRDCB+DCBOFLGS-IHADCB,DCBOFOPN
         BNO   ERROR1
         LA    R3,MEMTAB@          >>---> 1ST ENTRY POINTER
         SPACE 1
         B     PRU00300            READ THE FIRST DIRECTORY BLOCK
PRU00200 DS    0H
         IC    R1,11(0,R2)         LOAD LENGTH OF NEXT ENTRY
         SLL   R1,27
         SRL   R1,26
         LA    R2,12(R1,R2)        >>---> NEXT DIRECTORY ENTRY
         CLR   R2,R4               CHECK FOR END OF DIRECTORY BLOCK
         BL    PRU00400
PRU00300 DS    0H
         GET   DIRDCB              READ IN A DIRECTORY BLOCK
         LH    R4,0(0,R1)          LOAD LENGTH OF CURRENT BLOCK
         LA    R4,0(R1,R4)         >>---> LAST USED BYTE IN DIRECTORY
         LA    R2,2(0,R1)          >>---> FIRST ENTRY IN DIRECTORY
PRU00400 DS    0H
         CLI   0(R2),255           CHECK FOR END OF DIRECTORY
         BE    PRU00500
         SPACE 1
         GETMAIN RU,LV=MEMTABLN    GET STORAGE FOR NEW ENTRY
         ST    R1,0(R3)            CHAIN TO PREVIOUS ENTRY
         LR    R3,R1               >>---> NEW MEMBER ENTRY
         USING MEMTABD,R3
         XC    0(MEMTABLN,R3),0(R3)
         MVC   MEMNAME,0(R2)       INSERT MEMBER NAME
         MVC   MEMTTR,8(R2)        INSERT RELATIVE TTR OF DATA
         CLC   MEMTTR,MAXTTR       CHECK FOR HIGHEST TTR SO FAR
         BL    PRU00200            IF NOT PROCEED TO NEXT MEMBER
         MVC   MAXTTR,MEMTTR       SAVE HIGHEST TTR
         DROP  R3
         B     PRU00200            LOOP TILL END OF DIRECTORY
         SPACE 1
PRU00500 DS    0H
         CLOSE DIRDCB              CLOSE THE DIRECTORY
         EJECT 1
*-------
*
*        COPY ALL MEMBERS TO OUTPUT DATASET
*
*-------
         SPACE 1
         LA    R2,DCBDDNAM-IHADCB+BSAMDCB
         MVC   ERROR1+17(8),0(R2)
         TIOTSCAN DDN=(R2),NOK=ERROR1
         OPEN  (BSAMDCB,INPUT)
         TM    BSAMDCB+DCBOFLGS-IHADCB,DCBOFOPN
         BNO   ERROR1
         SPACE 1
         LA    R2,DCBDDNAM-IHADCB+BPAMDCB
         MVC   ERROR1+17(8),0(R2)
         TIOTSCAN DDN=(R2),NOK=ERROR1
         OPEN  (BPAMDCB,OUTPUT)
         TM    BPAMDCB+DCBOFLGS-IHADCB,DCBOFOPN
         BNO   ERROR1
         SPACE 1
         ESTAE TRAPABND,PARAM=(R12) SET ESTAE EXIT
         SPACE 1
*-------
*
*        READ EACH MEMBER WHETHER IT IS IN THE DIRECTORY OR NOT
*
*-------
         SPACE 1
PRU00600 DS    0H
         MVI   CURRTTR+3,1         SETUP TO READ NEXT BLOCK
         POINT BSAMDCB,CURRTTR     POINT TO NEXT TTR
         READ  RDECB,SF,BSAMDCB,BUFF,'S'
         CHECK RDECB               WAIT FOR COMPLETION OF READ
         SPACE 1
         TM    FLAGBYTE,FLAGSYNA   CHECK FOR SYNAD ERROR
         BO    ERROR2M             ISSUE ERROR MESSAGE
         SPACE 1
         NOTE  BSAMDCB             GET CURRENT TTR
         ST    R1,CURRTTR          SAVE CURRENT TTR
         MVI   CURRTTR+3,0
         SPACE 1
         TM    FLAGBYTE,FLAGDIR    CHECK IF DIRECTORY HAS BEEN READ
         BNO   PRU00600            IF NOT READ TILL END OF DIRECTORY
         EJECT 1
*-------
*
*        CHECK IF THE CURRENT MEMBER IS IN THE DIRECTORY
*
*-------
         SPACE 1
         L     R1,BLKCNT           LOAD EXISTING BLOCK COUNT
         LA    R1,1(,R1)           ADD 1 TO BLOCK COUNT
         ST    R1,BLKCNT           SAVE NEW BLOCK COUNT
         C     R1,=F'1'            CHECK FOR FIRST BLOCK
         BNE   PRU01000
         SPACE 1
         NOTE  BSAMDCB             GET CURRENT TTR
         ST    R1,STARTTTR         SAVE STARTING TTR ADDRESS
         SPACE 1
         MVC   PR1STLNE,BUFF       COPY FIRST LINE TO OUTPUT BUFFER
         CLC   MAXTTR,CURRTTR      CHECK IF THIS IS THE LAST MEMBER
         BNE   PRU00700            IF SO QUIT COPYING
         OI    FLAGBYTE,FLAGLAST   STOP AFTER THIS MEMBER
         SPACE 1
PRU00700 DS    0H
         NI    FLAGBYTE,255-FLAGDEAD
         LA    R3,MEMTAB@          >>---> START OF MEMBER TABLE
PRU00800 DS    0H
         USING MEMTABD,R3
         CLC   MEMTTR,CURRTTR      CHECK FOR MATCHING TTR
         BE    PRU00900            IF SO DO REMEMBER MEMBER NAME
         ICM   R3,B'1111',MEMNEXT@ >>---> NEXT MEMBER IN DIRECTORY
         BNZ   PRU00800
         OI    FLAGBYTE,FLAGDEAD   COPY THIS MEMBER
         B     PRU01000
         SPACE 1
PRU00900 DS    0H
         MVC   STOWNAME(8),MEMNAME
         DROP  R3
         SPACE 1
PRU01000 DS    0H
         TM    FLAGBYTE,FLAGDEAD   CHECK IF THIS IS A DEAD MEMBER
         BO    PRU01100            IF SO ALWAYS COPY IT
         TM    FLAGBYTE,FLAGALL    CHECK IF COPYING ALL MEMBERS
         BNO   PRU00600            IF NOT READ NEXT THROUGH THIS MEMBER
         SPACE 1
PRU01100 DS    0H
         LH    R1,DCBBLKSI-IHADCB+BSAMDCB
         L     R14,RDECB+16        >>---> IOB
         SH    R1,14(R14)          SUBTRACT RESIDUAL
         STH   R1,DCBBLKSI-IHADCB+BPAMDCB
         A     R1,BLKBYTES
         ST    R1,BLKBYTES         ACCUMULATE TOTAL BYTES WRITTEN
         WRITE WDECB,SF,BPAMDCB,BUFF,'S'
         CHECK WDECB
         B     PRU00600            LOOP TILL END OF CURRENT MEMBER
         EJECT 1
*-------
*
*        PROCESS AFTER END OF MEMBER
*
*-------
         SPACE 1
PRU01200 DS    0H
         NOTE  BSAMDCB             GET CURRENT TTR
         ST    R1,CURRTTR          SAVE CURRENT TTR
         ST    R1,ENDTTR           SAVE LAST TTR
         SPACE 1
         MVI   CURRTTR+3,0
         TM    FLAGBYTE,FLAGDIR    CHECK IF DIRECTORY WAS READ
         BO    PRU01300
         OI    FLAGBYTE,FLAGDIR    REMEMBER THE DIRECTORY WAS PROCESSED
         B     PRU01900            PROCEED WITH NEXT ENTRY
PRU01300 DS    0H
         TM    FLAGBYTE,FLAGDEAD   WAS THIS MEMBER IN THE DIRECTORY
         BNO   PRU01500            IF NOT PROCEED WITH NEXT ENTRY
         MVC   STOWNAME(3),=C'MEM' INSERT MEMBER NAME PREFIX
PRU01400 DS    0H
         AP    MEMNUM,=PL3'1'      ADD 1 TO MEMBER NUMBER
         UNPK  STOWNAME+3(5),MEMNUM
         OI    STOWNAME+7,X'F0'
         SPACE 1
PRU01500 DS    0H
         TM    FLAGBYTE,FLAGALL    CHECK IF COPYING ALL MEMBERS
         BO    PRU01600            IF SO PROCEED AS NORMAL
         TM    FLAGBYTE,FLAGDEAD   WAS THIS A DEAD MEMBER
         BNO   PRU01900            IF NOT DO NOT UPDATE DIRECTORY
PRU01600 DS    0H
         STOW  BPAMDCB,STOWNAME    STORE MEMBER NAME IN THE DIRECTORY
         LTR   R15,R15             CHECK FOR STOW ERROR
         BZ    PRU01700            IF NOT PROCEED
         TM    FLAGBYTE,FLAGDEAD   WAS THIS MEMBER IN THE DIRECTORY
         BNO   ERROR3              ERROR3 IF SO PROCESS ERROR
         CH    R15,=H'04'          CHECK FOR DUPLICATE MEMBER NAME
         BE    PRU01400            IF SO TRY A NEW MEMBER NAME
         B     ERROR3
PRU01700 DS    0H
         L     R1,STOW#                                        #DD99253
         LA    R1,1(,R1)           INCREMENT NUMBER OF MEMBERS #DD99253
         ST    R1,STOW#                                        #DD99253
         MVI   PRDEADFL,C' '
         TM    FLAGBYTE,FLAGDEAD   WAS THIS MEMBER IN THE DIRECTORY
         BNO   PRU01800
         MVI   PRDEADFL,C'*'
PRU01800 DS    0H
         MVC   PRMEMBER,STOWNAME   COPY MEMBER NAME TO OUTPUT
         SPACE 1
         SLR   R0,R0
         ICM   R0,B'1110',STARTTTR GET STARTING TTR
         L     R1,DCBDEBAD-IHADCB+BPAMDCB
         LA    R2,DBLWORD          >>---> OUTPUT AREA
         L     R14,CVTPTR          >>---> CVT
         L     R15,CVTPCNVT-CVTMAP(R14)
         STM   R3,R13,SAVEREGS     SAVE DESTROYED REGISTERS
         LA    R3,SAVEREGS         SAVE SAVE AREA ADDRESS
         BALR  R14,R15             LINK TO RESIDENT CONVERT RTN
         LM    R3,R13,0(R3)        RESTORE DESTROYED REGISTERS
         UNPK  PRTTRSTA(11),DBLWORD+3(6) UNPACK CCHHR
         TR    PRTTRSTA(11),TRTBL  XLATE TO EBCDIC
         SPACE 1
         ICM   R0,B'1110',ENDTTR   GET ENDING TTR
         L     R1,DCBDEBAD-IHADCB+BPAMDCB
         LA    R2,DBLWORD          >>---> OUTPUT AREA
         L     R14,CVTPTR          >>---> CVT
         L     R15,CVTPCNVT-CVTMAP(R14)
         STM   R3,R13,SAVEREGS     SAVE DESTROYED REGISTERS
         LA    R3,SAVEREGS         SAVE SAVE AREA ADDRESS
         BALR  R14,R15             LINK TO RESIDENT CONVERT RTN
         LM    R3,R13,0(R3)        RESTORE DESTROYED REGISTERS
         UNPK  PRTTREND(11),DBLWORD+3(6) UNPACK CCHHR
         TR    PRTTREND(11),TRTBL  XLATE TO EBCDIC
         SPACE 1
         L     R1,BLKCNT           LOAD BLOCK COUNT
         CVD   R1,DBLWORD
         MVC   PRBLKCNT,=X'402020202021'
         ED    PRBLKCNT,DBLWORD+5
         CLC   PRBLKCNT,=CL6' '    CHECK BLOCK COUNT           $DD97349
         BE    PRU02000            B. IF NOT (END)             $DD97349
         LA    R1,PRINTLNE         >>---> LINE TO BE PRINTED
         BAL   R14,PRINTRTN        PRINT THE LINE
PRU01900 DS    0H
         XC    BLKCNT,BLKCNT
         TM    FLAGBYTE,FLAGLAST   WAS THIS THE LAST MEMBER
         BNO   PRU00600            IF NOT PROCEED WITH NEXT MEMBER
         EJECT 1
*-------
*
*        RE-CONSTRUCT THE DCB'S TO SIMULATE THE INPUT DATASET
*
*-------
         SPACE 1
PRU02000 DS    0H
         CLOSE (BSAMDCB,,BPAMDCB,,SYSPRINT)
         MVC   EXITLST2+1(3),=AL3(SETDCBO2) CHANGE EXIT
         OPEN  (BPAMDCB,OUTPUT)    OPEN FOR OUTPUT TO COPY INPUT DCB'S
         CLOSE (BPAMDCB)
         SPACE 1
         ICM   R0,15,STOW#         CHECK FOR OUTPUT MEMBERS    #DD99253
         BNZ   PRUEXIT             B. IF SOME WRITTEN          #DD99253
         #SETRC 4                  SET RETURN CODE             #DD99253
         EJECT 1
*-------
*
*        RETURN TO CALLER WITH APPROPRIATE RETURN CODE
*
*-------
         SPACE 1
PRUEXIT  #STOP ,                   RETURN TO CALLER
         EJECT 1
ERROR1   WTO   'PRU0001I XXXXXXXX DD STATEMENT MISSING',ROUTCDE=11
         #SETRC 8                  SET RETURN CODE
         B     PRUEXIT
         SPACE 1
ERROR2   DS    0H
         SYNADAF ACSMETH=BSAM
         MVC   ERROR2M+17(78),50(R1)
         OI    FLAGBYTE,FLAGSYNA
         SYNADRLS
         BR    R14
         SPACE 1
ERROR2M  WTO   'PRU0002I XXXXXXXXX+XXXXXXXXX+XXXXXXXXX+XXXXXXXXX+XXXXXX*
               XXX+XXXXXXXXX+XXXXXXXXX+XXXXXXXX',ROUTCDE=11
         #SETRC 12                 SET RETURN CODE
         B     PRU02000
         SPACE 1
ERROR3   DS    0H
         MVC   ERROR3M+40(8),STOWNAME INSERT MEMBER NAME
         CVD   R15,DBLWORD
         OI    DBLWORD+7,X'0F'
         UNPK  ERROR3M+53(4),DBLWORD
ERROR3M  WTO   'PRU0003I STOW FAILED FOR MEMBER XXXXXXXX R15=XXXX ',   *
               ROUTCDE=11
         #SETRC 16                 SET RETURN CODE
         B     PRU01900
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ESTAE EXIT                                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PUSH  USING
         SPACE 1
TRAPABND DS    0H
         USING *,R15
         CH    R0,=F'12'           CHECK IF RTM PROVIDED AN SDWA
         BNE   TRAP0010
         SLR   R15,R15             IF NOT: PERCOLATE
         BR    R14                 RETURN TO CALLER
         SPACE 1
         DROP  R15
         SPACE 1
TRAP0010 DS    0H
         STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         LR    R4,R1               >>---> SDWA
         USING SDWA,R4
         L     R12,SDWAPARM        RE-LOAD BASE REGISTER
         SPACE 1
         LR    R5,R13              >>---> CALLERS SAVE AREA
         LA    R13,TRAPSAVE        >>---> ESTAE SAVE AREA
         ST    R5,TRAPSAVE+4       SAVE CALLERS SAVE AREA ADDRESS
         ST    R13,8(0,R5)         SAVE OUR SAVE AREA ADDRESS
         SPACE 1
         CLOSE (BPAMDCB)
         MVC   EXITLST2+1(3),=AL3(SETDCBO2) CHANGE EXIT
         OPEN  (BPAMDCB,OUTPUT)    OPEN FOR OUTPUT TO COPY INPUT DCB'S
         CLOSE (BPAMDCB)
         SPACE 1
         L     R13,4(,R13)         RESTORE CALLERS SAVE AREA ADDRESS
         SETRP RC=0,                                                   *
               WKAREA=(4),                                             *
               REGS=(14,12),                                           *
               DUMP=YES
         SPACE 1
         POP   USING
         EJECT 1
STOWNAME DS    0H
         DC    CL8'MEM-----',XL4'0'
MEMNUM   DC    PL3'0'
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,RECFM=FBA,                     *
               DSORG=PS,EXLST=EXITLST3
DIRDCB   DCB   DDNAME=INPUT,MACRF=GL,                                  *
               DSORG=PS,RECFM=F,LRECL=256,BLKSIZE=256
BSAMDCB  DCB   DDNAME=INPUT,MACRF=RP,                                  *
               EODAD=PRU01200,SYNAD=ERROR2,                            *
               DSORG=PS,EXLST=EXITLIST
BPAMDCB  DCB   DDNAME=OUTPUT,MACRF=W,                                  *
               DSORG=PO,EXLST=EXITLST2
EXITLIST DC    0F'0',AL1(128+5),AL3(SAVEDCBI)
EXITLST2 DC    0F'0',AL1(128+5),AL3(SETDCBO)
EXITLST3 DC    0F'0',AL1(128+5),AL3(DCBEXIT)
*=================================================================*
* SAVE ORIGINAL DCB FORMAT AND MAKE RECFM UNDEFINED.              *
*=================================================================*
SAVEDCBI DC    0H'0'
         MVC   RECFM,DCBRECFM-IHADCB+BSAMDCB
         MVI   DCBRECFM-IHADCB+BSAMDCB,DCBRECU
         MVC   LRECL,DCBLRECL-IHADCB+BSAMDCB
         MVC   BLKSIZE,DCBBLKSI-IHADCB+BSAMDCB
         BR    R14
*=================================================================*
* MAKE OUTPUT DCB LIKE INPUT                                      *
*=================================================================*
SETDCBO  DC    0H'0'
         MVC   DCBRECFM-IHADCB+BPAMDCB,DCBRECFM-IHADCB+BSAMDCB
         MVC   DCBLRECL-IHADCB+BPAMDCB,=H'00'
         MVC   DCBBLKSI-IHADCB+BPAMDCB,DCBBLKSI-IHADCB+BSAMDCB
         BR    R14
SETDCBO2 DC    0H'0'
         MVC   DCBBLKSI-IHADCB+BPAMDCB,BLKSIZE
         MVC   DCBLRECL-IHADCB+BPAMDCB,LRECL
         MVC   DCBRECFM-IHADCB+BPAMDCB,RECFM
         BR    R14
DCBEXIT  DS    0H
         #DCBEXIT LRECL=133
PRINTRTN DS    0H
         STM   R0,R15,SAVEREGS     SAVE ALL REGISTERS
         LR    R10,R1              >>>---> RECORD TO BE PRINTED
         AP    LINE#,=PL3'1'       ADD 1 TO LINE NUMBER
         CP    LINE#,=PL3'60'      CHECK IF TITLES ARE REQUIRED
         BNE   PRINT010
         L     R1,PAGE#            LOAD OLD PAGE NUMBER
         LA    R1,1(,R1)           ADD 1 TO PAGE NUMBER
         ST    R1,PAGE#            SAVE CURRENT PAGE NUMBER
         CVD   R1,DBLWORD
         MVC   PRTPAGE#,=X'402020202021'
         ED    PRTPAGE#,DBLWORD+5
         PUT   SYSPRINT,TITLE1
         PUT   SYSPRINT,TITLE2
         PUT   SYSPRINT,TITLE3
         PUT   SYSPRINT,TITLE4
         PUT   SYSPRINT,TITLE5
         MVC   LINE#,=PL3'6'
PRINT010 DS    0H
         PUT   SYSPRINT,0(R10)     PRINT RECORD
         LM    R0,R15,SAVEREGS     RESTORE ALL REGISTERS
         BR    R14
         EJECT 1
         LTORG ,
LINE#    DC    PL3'+59'            LINE NUMBER
PAGE#    DC    F'0'                PAGE NUMBER
TITLE1   DC    CL133'1 '
         ORG   TITLE1+57
         DC    C'PDS RESCUE UTILITY'
         ORG   TITLE1+123
         DC    C'PAGE'
PRTPAGE# DS    CL6
         ORG   ,
TITLE2   DC    CL133'0 '
         ORG   TITLE2+2
         DC    C' MEMBER   INPUT DATASET CCHHR    BLOCK'
         ORG   ,
TITLE3   DC    CL133'  '
         ORG   TITLE3+2
         DC    C'  NAME   STARTING       ENDING   COUNT FIRST RECORD'
         ORG   ,
TITLE4   DC    CL133'+ '
         ORG   TITLE4+2
         DC    C'  ____   _____________________   _____ ____________'
         ORG   ,
TITLE5   DC    CL133'  '
ABENDCDE DC    XL3'00'
PRINTLNE DC    CL133' '
         ORG   PRINTLNE+1
PRDEADFL DC    CL1' '
PRMEMBER DC    CL8' '
         DC    CL1' '
PRTTRSTA DC    CL11' '
PRTTREND DC    CL11' '
         DC    CL1' '
PRBLKCNT DC    CL6' '
         DC    CL1' '
PR1STLNE DC    CL80' '
         ORG   ,
TRAPSAVE DS    18F                 ESTAE SAVE AREA
BLKSIZE  DS    H                   INPUT BLOCKSIZE
LRECL    DS    H                     "   LRECL
RECFM    DS    X                     "   RECFM
TRTBL    DS    0CL256
         DC    CL240' '
         DC    C'0123456789ABCDEF'
         SPACE 1
BUFF     DS    0D,CL(32*1024)      READ/WRITE BUFFER
         SPACE 1
         #STARTWA ,
BLKBYTES DS    F
BLKCNT   DS    F
CURRTTR  DS    F
DBLWORD  DS    D
ENDTTR   DS    F
FLAGBYTE DS    X
FLAGDEAD EQU   X'80'               CURRENT MEMBER IS NOT IN DIRECTORY
FLAGDIR  EQU   X'40'               DIRECTORY HAS BEEN READ
FLAGLAST EQU   X'20'               LAST MEMBER IN DATASET
FLAGSYNA EQU   X'10'               BSAM SYNAD ERROR OCCURRED
FLAGALL  EQU   X'08'               ALL MEMBERS ARE TO BE COPIED
MAXTTR   DS    XL3
MEMTAB@  DS    F                   >>---> MEMBER NAME TABLE
SAVEREGS DS    16F
STARTTTR DS    F
STOW#    DS    F                   NUMBER OF OUTPUT MEMBERS    #DD99253
         #STOPWA ,
         SPACE 1
MEMTABD  DSECT ,
MEMNEXT@ DS    F                   >>---> NEXT ENTRY IN TABLE
MEMNAME  DS    CL8                 MEMBER NAME
MEMTTR   DS    CL3                 TTR
MEMTABLN EQU   *-MEMTABD
         #DSECTS CVT,DCB,SDWA
         END   ,
./ ADD NAME=PRUREXX  0103-00063-00063-1613-00103-00103-00000-REXX
/*-REXX/PRUREXX ------------------------------
   SYNTAX:   PRUREXX dsn {anychar}
   AUTHOR:   Oscar Omar Ortega Ortega
   OUTPUT:   dsn.NEW{.NEW{...}}
   NOTE:  change "userid.MVS-JES2.LOADLIB"

   This EXEC will create various datasets under the user's prefix.
   You may need to review this REXX if your installation has any
   file name or other restrictions.

  ---------------------------------------------*/
Trace O
address TSO
parse upper arg bib param
if bib = "" then do
   Say 'You must provide PDS name to process'
   exit
end
if param <> '' then param = "'ALL'"

upre1 = sysvar(SYSPREF)
upref = upre1                          /* to avoid second call later */
if upref = "" then upref = Userid()    /* either prefix or userid    */

apos1 = pos("'",bib)
if apos1 = 0 then do                       /* if it has no quotes    */
   if upre1 <> "" then bib = upref"."bib   /* append prefix or uid   */
   bib = "'"bib"'"
end

Exist = SYSDSN(bib)                    /* check dsn existance        */
If Exist <> 'OK'                       /* if not exists, say so      */
then do
   Say 'PDS 'bib' NOT found or not catalogued'
   exit
end

dsprin = "'"upref".PRUPGM.SYSPRINT'"           /* pru pgm sysprint   */

bib2 = bib
do forever
   totlng = length(bib2)                   /* check current length   */
   bib2 = left(bib2,totlng-1)              /* delete last quote      */
   bib2 = bib2".NEW'"                      /* append .NEW to dsn     */
   if totlng > 41 then call Change_Name    /* check if dsn > 44 char */
   Exist = sysdsn(bib2)
   If Exist <> 'OK'                        /* check dsn existance    */
   then leave                              /* if so, iterate         */
end

msgst =  MSG('OFF')
if sysdsn(dsprin) = 'OK' then "DELETE "dsprin /* if exists, delete it*/

x = LISTDSI(bib directory)                 /* to check if its a PDS  */
msgst =  MSG(msgst)

if sysdsorg <> 'PO' & sysdsorg <> 'POU' then do  /* if dsn not a PDS */
   Say 'Dataset 'bib' is NOT a PDS, it is 'sysdsorg
   exit
end

msgst =  MSG('OFF')
"FREE FI(SYSPRINT,INPUT,OUTPUT)"   /* include SYSUDUMP as optional   */
msgst =  MSG(msgst)

/*  allocate all work files       */

"ALLOC FI(SYSPRINT) DA("dsprin") REUSE UNIT(SYSDA) ",
       "SPACE(5 1) TRACKS RECFM(F,B,A) LRECL(133) BLK(27930)"
"ALLOC FI(INPUT)    DA("bib") SHR REUSE"
"ALLOC FI(OUTPUT)   DA("bib2") NEW UNIT(SYSDA) LIKE("bib") RELEASE"

"CALL 'userid.MVS-JES2.LOADLIB(PRU)' "param   /* call PRU program    */
RcPRU = rc                             /* save return code    */

"FREE FI(SYSPRINT,INPUT,OUTPUT)"   /* include SYSUDUMP as optional   */
"ALLOC FI(SYSPRINT) DA(*)"         /* reallocate SYSPRINT to terminal*/

select
   when RcPRU = 0 then do
      if sysvar("SYSISPF") = "ACTIVE"
      then address ISPEXEC "VIEW DATASET("dsprin")"
      else Say 'PDS 'bib' procesed with name 'bib2' successfully'
   end
   when RcPRU = 4 then do
      Say 'PDS 'bib' processed and no members were recovered'
      msgst =  MSG('OFF')
      "DELETE "dsprin
      "DELETE "bib2
      msgst =  MSG(msgst)
   end
   otherwise Say 'PDS 'bib' procesed with errors RC='RcPRU
end
exit

Change_Name:
 fecS = date('S')
 timN = time()
 timN = left(timN,2)substr(timN,4,2)right(timN,2)
 Say 'Output PDS 'bib2
 bib2 = "'"upref".PRUPGM.RECOVERD.D"right(fecS,6)".T"timN"'"
 Say 'changed to 'bib2' due to dsn length'
return
./ ADD NAME=RETCODE  0100-00040-00040-1222-00205-00205-00000-SOURCE
RETCODE  TITLE '- GENERATE RETURN CODE/ABEND BASED ON PARM='
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT 1
RETCODE  #START SPLEVEL=2,WKDSECT=WORKAREA,APARS=,AMODE=CAP24,         *
               COPY=Y,COPYYR=1999,                                     *
               LEVEL=#V001R01
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CONSTRUCT COMMAND BUFFER FROM THE PARAMETER LIST             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         L     R1,0(,R1)           >>---> PARM FIELD
         SR    R2,R2
         ICM   R2,3,0(R1)          LOAD PARAMETER LENGTH
         BZ    RETC0100            B. IF NO PARAMETER SPECIFIED
         SPACE 1
         #EXEC -R2,MVC,COMMAND+8(*-*),2(R1)
         LA    R2,9(,R2)           CALCULATE LENGTH OF COMMAND BUFFER
         STH   R2,COMMAND          RESET LENGTH OF COMMAND BUFFER
         SPACE 1
RETC0100 DS    0H
         MVC   PPLPCL-PPL+WRKPPL,=A(PARSEPCL)
         LA    R0,PDLADDR          >>---> IKJPARS ANSWER PLACE
         ST    R0,PPLANS-PPL+WRKPPL
         LA    R0,COMMAND          >>---> INPUT COMMAND BUFFER
         ST    R0,PPLCBUF-PPL+WRKPPL
         ST    R0,CSPLCBUF-CSPL+WRKCSPL
         ST    R13,PPLUWA-PPL+WRKPPL
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        SCAN & PARSE THE COMMAND BUFFER                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         LA    R1,WRKCSPL          >>---> SCAN PARAMETER LIST
         LA    R0,4                INDICATE "IKJSCAN" FUNCTION
         L     R15,=V(TSOPARSE)
         #BALR R14,R15
         SPACE 1
         LA    R1,WRKPPL           >>---> PARSE PARAMETER LIST
         LA    R0,0                INDICATE "IKJPARS" FUNCTION
         L     R15,=V(TSOPARSE)
         #BALR R14,R15
         #SETRC (R15)              SET RETURN CODE
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ISSUE MESSAGES TRAPPED BY PARSE SVC                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         LTR   R3,R1               CHECK IF ANY MESSAGES RETURNED
         BZ    RETC0400            B. IF NO
         SPACE 1
RETC0200 DS    0H
         MVC   WTOBUFFR+4(80),BLANKS
         LH    R2,4(,R3)           LOAD MESSAGE TEXT LENGTH
         CH    R2,=H'80'           CHECK FOR MAXIMUM LENGTH
         BNH   RETC0300            B. IF NOT HIGHER THAN 80
         LH    R2,=H'80'           SET TO MAXIMUM LENGTH
RETC0300 DS    0H
         #EXEC -R2,MVC,WTOBUFFR+4(*-*),8(R3)
         SPACE 1
         WTO   MF=(E,WTOBUFFR)     ISSUE ERROR MESSAGE
         SPACE 1
         ICM   R3,15,0(R3)         >>---> NEXT MESSAGE
         BNZ   RETC0200            B. IF MORE MESSAGES
         SPACE 1
RETC0400 DS    0H
         ICM   R15,15,RETRCODE     CHECK IKJPARS RETURN CODE
         BZ    RETC0450            B. IF SUCCESSFUL PARSE
         WTO   'RETC001I INVALID PARAMETER LIST',ROUTCDE=11
         B     RETCEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        SET RETURN CODE TO REQUESTED VALUE                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
RETC0450 DS    0H
         L     R10,PDLADDR         LOAD PDL ADDRESS
         USING IKJPARMD,R10
         SPACE 1
         CLI   TYPEKW+1,1          CHECK FOR "RC(...)"
         BNE   RETC0500            B. IF NOT
         L     R1,RC               LOAD REQUESTED RETURN CODE ADDRESS
         L     R15,0(,R1)          LOAD REQUESTED RETURN CODE
         #SETRC (R15),FORCE=Y      SET RETURN CODE
         B     RETCEXIT
         SPACE 1
RETC0500 DS    0H
         CLI   TYPEKW+1,2          CHECK FOR "USER(...)"
         BNE   RETC0600            B. IF NOT
         L     R1,USER             LOAD REQUESTED USER CODE ADDRESS
         L     R15,0(,R1)          LOAD REQUESTED USER CODE
         ABEND (15)
RETC0600 DS    0H
         CLI   TYPEKW+1,3          CHECK FOR "SYSTEM(...)"
         BNE   RETCEXIT            B. IF NOT
         L     R1,SYSTEM           LOAD REQUESTED SYSTEM CODE ADDRESS
         SLR   R15,R15
         ICM   R15,1,0(R1)         LOAD 1 BYTE SYSTEM CODE
         CLC   =X'0001',SYSTEM+4   CHECK FOR 1 BYTE CODE
         BE    RETC0700            B. IF YES
         ICM   R15,3,0(R1)         LOAD 2 BYTE SYSTEM CODE
         CLC   =X'0002',SYSTEM+4   CHECK FOR 2 BYTE CODE
         BE    RETC0700            B. IF YES
         ICM   R15,7,0(R1)         LOAD 3 BYTE SYSTEM CODE
         CLC   =X'0003',SYSTEM+4   CHECK FOR 3 BYTE CODE
         BE    RETC0700            B. IF YES
         ICM   R15,15,0(R1)        LOAD 4 BYTE SYSTEM CODE
RETC0700 DS    0H
         ABEND (15),,,SYSTEM
         DROP  R10
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        RETURN TO CALLER                                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
RETCEXIT DS    0H
         IKJRLSA PDLADDR           RELEASE THE IKJPARS BUFFERS
         #STOP ,                   RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
BLANKS   DC    CL256' '            LOTS OF BLANKS
COMMAND  DS    0F
         DC    AL2(7,0),CL104'XXX '
WTOBUFFR WTO   '---------+---------+---------+---------+---------+-----*
               ----+---------+---------+',ROUTCDE=11,MF=L
         SPACE 1
         #STARTWA ,
PDLADDR  DS    F                   IKJPARS ANSWER PLACE
WRKCSPL  DS    CL(CSPLLEN)         SCAN PARAMETER LIST
WRKPPL   DS    CL(PPLLEN)          PARSE PARAMETER LIST
         #STOPWA ,
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        COMMAND OPERANDS                                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
PARSEPCL IKJPARM ,
         SPACE 1
TYPEKW   IKJKEYWD DEFAULT='RC(0)'
         IKJNAME 'RC',SUBFLD=RCSUB
         IKJNAME 'USER',SUBFLD=USERSUB
         IKJNAME 'SYSTEM',SUBFLD=SYSSUB
         SPACE 1
RCSUB    IKJSUBF
RC       IKJIDENT 'RC',                                                *
               INTEG,                                                  *
               MAXLNTH=4,                                              *
               PROMPT='RETURN CODE'
         SPACE 1
USERSUB  IKJSUBF
USER     IKJIDENT 'USER',                                              *
               INTEG,                                                  *
               MAXLNTH=3,                                              *
               PROMPT='USER ABEND CODE'
         SPACE 1
SYSSUB   IKJSUBF
SYSTEM   IKJIDENT 'SYSTEM',                                            *
               HEX,                                                    *
               MAXLNTH=3,                                              *
               PROMPT='SYSTEM ABEND CODE'
         SPACE 1
         IKJENDP
         PRINT GEN
         EJECT 1
         PRINT NOGEN
*              'PARSE PARAMETER LIST'
         IKJPPL ,
PPLLEN   EQU   *-PPL               LENGTH OF PPL
*              'SCAN PARAMETER LIST'
         IKJCSPL ,
CSPLLEN  EQU   *-CSPL              LENGTH OF CSPL
         END   ,
./ ADD NAME=STEPLIB  0100-00040-00040-1222-01133-01133-00000-SOURCE
STEPLIB  TITLE '- STEPLIB - COMMAND PROCESSOR'
*---------------------------------------------------------------------*
*                 ×                                                   *
*  S T E P L I B  ×       TSO STEPLIB COMMAND PROCESSOR               *
*                 ×                                                   *
*------------------                                                   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* COMMNAD SYNTAX:                                                     *
*                                                                     *
*        STEPLIB ADD DATASETS(DSNAME LIST) FIRST×LAST NOMSGS          *
*        - ADDS THE DSNAME LIST TO THE EXISTING STEPLIB               *
*                                                                     *
*        STEPLIB ALLOCATE DATASETS(DSNAME LIST) SHR REUSE NOMSGS      *
*        - ALLOCATES A NEW STEPLIB & CLOSES/FREES THE OLD             *
*                                                                     *
*        STEPLIB FREE NOMSGS                                          *
*        - CLOSES/FREES THE OLD STEPLIB                               *
*                                                                     *
*        STEPLIB LIST                                                 *
*        - DISPLAYS THE STEPLIB DDNAME AND DATASETS                   *
*                                                                     *
*        STEPLIB REMOVE DATASETS(DSNAME LIST) NOMSGS                  *
*        - REMOVES THE DSNAME LIST FROM THE EXISTING STEPLIB          *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*     HISTORY:          - JAN 13,1996 -  RE-WRITTEN FROM OLD "MODS"   *
*              #DD99244 - SEP 01,1999 -  VARIOUS BUG FIXES:           *
*              - IF OLD STEPLIB DDNAME IS "STEPLIB" BYPASS "UNALLOC". *
*              - GET "REAL" NAME FROM CATALOG IN CASE AN ALIAS WAS    *
*                SPECIFIED ON THE "STEPLIB REMOVE".                   *
*              - FIX REMOVAL OF "LAST" DATASET FROM LIST.             *
*              #DD99245 - SEP 02,1999 -  IF THE ORIGINAL DDNAME       *
*                FOR A STEPLIB IS NOT A "$TEP" PREFIX, IT IS FREE'D   *
*                AND RE-ALLOCATED WITH THE $TEP000# DDNAME.           *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT 1
STEPLIB  #START ,                  START PROCEDURE                     *
               AMODE=CAP31,        EXECUTE IN 31-BIT ADDRESSING MODE   *
               APARS=,             NO NEED FOR APAR "ZAP" SLOTS        *
               RMODE=24,           EXECUTE IN 24-BIT RESIDENCY MODE    *
               BASE=(R11,R12),     DEFINE BASE REGISTER(S)             *
               COPY=YES,           INSERT COPYRIGHT NOTICE             *
               COPYYR=1999,        COPYRIGHT YEAR                      *
               SPLEVEL=2,          XA/ESA MACRO LEVEL SUPPORT          *
               WKDSECT=STEPWRK,    DEFINE WORKAREA NAME                *
               LEVEL=#V001R02                                  #DD99245
         SPACE 1
         USING PSA,0
         LR    R8,R1               A(CPPL)
         USING CPPL,R8
         SPACE 1
         MVC   WRKUPT@,CPPLUPT     SAVE A(UPT)
         MVC   WRKECT@,CPPLECT     SAVE A(ECT)
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        FIND THE NEXT "$TEP####" TO USE AS THE NEW STEPLIB DDNAME    *
*        NOTE: THIS DDNAME IS USED TO ALLOCATE THE DATASETS WITHIN    *
*              THE IKJPARS VALIDATION SUBROUTINE.                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   WRKDDNEW,=CL8'$TEP0001'
STEP0100 DS    0H                                              #DD99245
         LA    R2,WRKDDNEW         A("NEW" FAKE STEPLIB DDNAME)#DD99245
         TIOTSCAN DDN=(R2),NOK=STEP0200  SCAN THE TIOT         #DD99245
         PACK  WRKDBL1,WRKDDNEW+4(4)                           #DD99245
         AP    WRKDBL1,=P'+1'      INCREMENT STEPLIB DD NUMBER #DD99245
         OI    WRKDBL1+7,X'0F'                                 #DD99245
         UNPK  WRKDDNEW+4(4),WRKDBL1                           #DD99245
         B     STEP0100                                        #DD99245
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PARSE THE STEPLIB COMMAND BUFFER                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEP0200 DS    0H                                              #DD99245
         L     R4,PSATOLD          A(TCB)                      #DD99245
         USING TCB,R4                                          #DD99245
         L     R5,TCBTCT           A(TCT)                      #DD99245
         USING SMFTCT,R5                                       #DD99245
         L     R6,TCTLCTAD         A(LCT)                      #DD99245
         DROP  R4,R5                                           #DD99245
         ST    R6,WRKLCT@          SAVE LCT ADDRESS            #DD99245
         SPACE 1
         LA    R1,WRKPPL           LOAD PPL ADDRESS
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT      SET UPT ADDRESS
         MVC   PPLECT,CPPLECT      SET ECT ADDRESS
         LA    R15,WRKECB
         ST    R15,PPLECB          SET ECB ADDRESS
         XC    WRKECB,WRKECB
         MVC   PPLPCL,=A(STEPPCL)  SET PCL ADDRESS
         LA    R15,WRKANS@
         ST    R15,PPLANS          SET PDL ADDRESS
         MVC   PPLCBUF,CPPLCBUF    SET COMMAND BUFFER ADDRESS
         ST    R13,PPLUWA          PASS WRKDSECT TO VALIDATION EXIT
         DROP  R1
         SPACE 1
         CALLTSSR EP=IKJPARS,MF=(E,WRKPPL)
         L     R8,WRKANS@          LOAD PDL ADDRESS
         USING IKJPARMD,R8
         #SETRC (R15)              SET RETURN CODE
         LTR   R15,R15             CHECK PARSE RETURN CODE
         BZ    STEP0300            B. IF SUCCESSFUL            #DD99245
         SPACE 1
*-------
*        FREE ANY ALLOCATED DATASETS IF THE PARSE ROUTINE FAILED
*-------
         TM    WRKFLAG1,WRK$1STD   CHECK IF 1ST DATASET ALLOCATED
         BZ    STEPEXIT            B. IF NOT
         LA    R0,WRKDDNEW         A(STEPLIB DDNAME)
         ST    R0,WRKDDN@
         LA    R0,L'WRKDDNEW       L(STEPLIB DDNAME)
         STH   R0,WRKDDN@+4
         FREE  PERM,DDN=WRKDDN@    FREE THE "NEW" STEPLIB DATASETS
         B     STEPEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        RE-ALLOCATE THE CURRENT STEPLIB IF THE OLD STEPLIB WAS       *
*        A "REAL" STEPLIB. (IE. //STEPLIB WITHIN THE LOGON PROC)      *
*                                                                     *
*        THIS SECTION ALSO RETRIEVES THE DDNAME OF THE CURRENTLY      *
*        ALLOCATED STEPLIB.                                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEP0300 DS    0H                                              #DD99245
         CLI   $TEPFUNC+1,3        CHECK FOR "FREE" FUNCTION   #DD99245
         BE    STEP1200            B. IF YES                   #DD99245
         CLI   $TEPFUNC+1,4        CHECK FOR "LIST" FUNCTION   #DD99245
         BE    STEP1200            B. IF YES                   #DD99245
         SPACE 1
         MVC   WRKDDTMP,=CL8'$TEP0001'                         #DD99245
STEP0400 DS    0H                                              #DD99245
         LA    R2,WRKDDTMP         A("NEW" FAKE STEPLIB DDNAME)#DD99245
         TIOTSCAN DDN=(R2),NOK=STEP0500  SCAN THE TIOT         #DD99245
         PACK  WRKDBL1,WRKDDTMP+4(4)                           #DD99245
         AP    WRKDBL1,=P'+1'      INCREMENT STEPLIB NUMBER    #DD99245
         OI    WRKDBL1+7,X'0F'                                 #DD99245
         UNPK  WRKDDTMP+4(4),WRKDBL1                           #DD99245
         B     STEP0400                                        #DD99245
         SPACE 1
*-------
*        CLOSE & OPEN THE STEPLIB AND RETRIEVE THE DDNAME
*-------
STEP0500 DS    0H                                              #DD99245
         LA    R0,CLOSEIRB         A(STEPLIB CLOSE ROUTINE)    #DD99245
         LA    R1,WRKDDOLD         A(OLD DDNAME RETURN ADDRESS)#DD99245
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY      #DD99245
         LTR   R15,R15             CHECK IF CLOSE SUCCESSFUL   #DD99245
         BNZ   STEP1200            B. IF NOT                   #DD99245
         SPACE 1
         LA    R0,OPENIRB          A(STEPLIB OPEN ROUTINE)     #DD99245
         LA    R1,WRKDDOLD         A(OLD STEPLIB DDNAME)       #DD99245
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY      #DD99245
         LTR   R15,R15             CHECK IF OPEN SUCCEEDED     #DD99245
         BNZ   STEP1200            B. IF NOT                   #DD99245
         SPACE 1
         CLC   =C'$TEP',WRKDDOLD   CHECK FOR "FAKE" STEPLIB DD #DD99245
         BE    STEP1200            B. IF YES                   #DD99245
         SPACE 1
*-------
*        CLOSE THE PREVIOUSLY ALLOCATED STEPLIB
*-------
         LA    R0,CLOSEIRB         A(STEPLIB CLOSE ROUTINE)    #DD99245
         LA    R1,WRKDDOLD         A(OLD DDNAME RETURN ADDRESS)#DD99245
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY      #DD99245
         LTR   R15,R15             CHECK IF CLOSE SUCCESSFUL   #DD99245
         BNZ   STEPEXIT            B. IF NOT                   #DD99245
         SPACE 1
*-------
*        SCAN THE DSAB CHAIN FOR THE OLD STEPLIB DDNAME
*-------
         NI    WRKFLAG1,255-WRK$1STD                           #DD99245
         SPACE 1
         L     R4,PSATOLD          A(TCB)                      #DD99245
         USING TCB,R4                                          #DD99245
         L     R5,TCBJSCB          A(JSCB)                     #DD99245
         USING IEZJSCB,R5                                      #DD99245
         L     R5,JSCBACT          A(ACTIVE JSCB)              #DD99245
         L     R5,JSCDSABQ         A(QDB)                      #DD99245
         USING QDB,R5                                          #DD99245
         ICM   R5,15,QDBFELMP      A(DSAB)                     #DD99245
         BZ    STEP1200            B. IF CHAIN DOES NOT EXIST  #DD99245
         USING DSABID,R5                                       #DD99245
         SPACE 1
STEP0600 DS    0H                                              #DD99245
         L     R4,DSABSIOT         A(SIOT)                     #DD99245
         USING INDMSIOT,R4                                     #DD99245
         CLC   SCTDDNAM,WRKDDOLD   CHECK PREV. STEPLIB DDNAME  #DD99245
         BE    STEP0700            B. IF FOUND                 #DD99245
         ICM   R5,15,DSABFCHN      A(NEXT DSAB)                #DD99245
         BNZ   STEP0600            B. IF ANOTHER DSAB EXISTS   #DD99245
         B     STEP1200            ELSE. UNABLE TO FIND DDNAME #DD99245
         SPACE 1
*-------
*        ALLOCATE THE 1ST DSNAME TO THE NEW STEPLIB DDNAME
*-------
STEP0700 DS    0H                                              #DD99245
         L     R1,SJFCBPTR         A(JFCB)                     #DD99245
         USING INFMJFCB,R1                                     #DD99245
         MVC   WRKDSN1,JFCBDSNM    SAVE THE CURRENT DSN        #DD99245
         DROP  R1                                              #DD99245
         LA    R0,WRKDSN1                                      #DD99245
         ST    R0,WRKDSN@          A(CURRENT STEPLIB DSNAME)   #DD99245
         LA    R0,L'WRKDSN1                                    #DD99245
         STH   R0,WRKDSN@+4        L(CURRENT STEPLIB DSNAME)   #DD99245
         SPACE 1
         TM    WRKFLAG1,WRK$1STD   CHECK IF 1ST DSN ALLOCATED  #DD99245
         BO    STEP0800            B. IF YES                   #DD99245
         OI    WRKFLAG1,WRK$1STD   REMEMBER 1ST DSN ALLOCATED  #DD99245
         LA    R0,WRKDDTMP                                     #DD99245
         ST    R0,WRKDDN@          A(CURRENT STEPLIB DDNAME)   #DD99245
         LA    R0,L'WRKDDTMP                                   #DD99245
         STH   R0,WRKDDN@+4        L(CURRENT STEPLIB DDNAME)   #DD99245
         ALLOC PERM,DSN=WRKDSN@,DISP=SHR,                      #DD99245*
               DDN=WRKDDN@,FLAG1=S99NOCNV+S99NOMNT             #DD99245
         LTR   R15,R15             CHECK ALLOCATION RETURN CODE#DD99245
         BZ    STEP1000            B. IF SUCCESSFUL            #DD99245
         B     STEP0900            ELSE. ISSUE ALLOC ERROR MSG #DD99245
         SPACE 1
*-------
*        CONCATENATE THE REMAINING DSNAMES TO THE NEW STEPLIB DDNAME
*-------
STEP0800 DS    0H                                              #DD99245
         ALLOC PERM,DSN=WRKDSN@,DISP=SHR,                      #DD99245*
               DDNTO=WRKDDRET,FLAG1=S99NOCNV+S99NOMNT          #DD99245
         LTR   R15,R15             CHECK ALLOCATION RETURN CODE#DD99245
         BNZ   STEP0900            B. IF ALLOCATION FAILED     #DD99245
         SPACE 1
         MVC   WRKCCTU1(4),=AL2(DCCDDNAM,2)                    #DD99245
         MVC   WRKCCDD1(2),=AL2(8) SET LENGTH OF DDNAME        #DD99245
         MVC   WRKCCDD2(2),=AL2(8) SET LENGTH OF DDNAME        #DD99245
         MVC   WRKCCTU2(4),=AL2(DCCPERMC,0)                    #DD99245
         MVC   WRKCCDD1+2(8),WRKDDTMP                          #DD99245
         MVC   WRKCCDD2+2(8),WRKDDRET                          #DD99245
         ALLOC VERB=S99VRBCC,TU=(WRKCCTU1,WRKCCTU2)            #DD99245
         LTR   R15,R15             CHECK CONCAT RETURN CODE    #DD99245
         BZ    STEP1000            B. IF CONCAT SUCCEEDED      #DD99245
         SPACE 1
*-------
*        PROCESS ALLOCATION/CONCATENATION ERRORS
*-------
STEP0900 DS    0H                                              #DD99245
         S99FAIL CPPL=(R8),        ISSUE ALLOC FAILURE MESSAGE #DD99245*
               MF=(E,WRKFAIL)                                  #DD99245
         B     STEPEXIT            CONTINUE DSAB SCAN          #DD99245
         SPACE 1
*-------
*        CHECK IF NEXT DSAB IS A CONCATENATION OF THE STEPLIB
*-------
STEP1000 DS    0H                                              #DD99245
         ICM   R5,15,DSABFCHN      A(NEXT DSAB)                #DD99245
         BZ    STEP1100            B. IF END OF  DSAB CHAIN    #DD99245
         L     R4,DSABSIOT         A(SIOT)                     #DD99245
         CLC   SCTDDNAM,=CL8' '    CHECK FOR CONCATENATION     #DD99245
         BE    STEP0700            B. IF YES                   #DD99245
         DROP  R4,R5                                           #DD99245
         SPACE 1
*-------
*        OPEN THE NEWLY ALLOCATED STEPLIB
*-------
STEP1100 DS    0H                                              #DD99245
         LA    R0,OPENIRB          A(STEPLIB OPEN ROUTINE)     #DD99245
         LA    R1,WRKDDTMP         A(NEW STEPLIB DDNAME)       #DD99245
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY      #DD99245
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS THE "STEPLIB ADD" FUNCTION                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEP1200 DS    0H
         CLI   $TEPFUNC+1,1        CHECK FOR "ADD" FUNCTION
         BNE   STEP1600            B. IF NOT
         CLI   $TEPDSNS+1,1        CHECK IF DATASET NAMES SPECIFIED
         BNE   $TEPERR1            B. IF NOT
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         ICM   R2,15,TCBJLB        A(STEPLIB DCB)
         BZ    STEP1700            B. IF ONE DOES NOT EXIST (ALLOCATE)
         DROP  R4
         SPACE 1
*-------
*        CLOSE THE PREVIOUSLY ALLOCATED STEPLIB
*-------
         LA    R0,CLOSEIRB         A(STEPLIB CLOSE ROUTINE)
         LA    R1,WRKDDOLD         A(OLD DDNAME RETURN ADDRESS)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF CLOSE WAS SUCCESSFUL
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
*-------
*        CONCATENATE THE NEW DATASET(S) WITH THE OLD STEPLIB DATASETS
*-------
         MVC   WRKCCTU1(4),=AL2(DCCDDNAM,2)
         MVC   WRKCCDD1(2),=AL2(8) SET LENGTH OF DDNAME
         MVC   WRKCCDD2(2),=AL2(8) SET LENGTH OF DDNAME
         MVC   WRKCCTU2(4),=AL2(DCCPERMC,0)
         MVC   WRKCCDD1+2(8),WRKDDNEW
         MVC   WRKCCDD2+2(8),WRKDDOLD
         CLI   $TEPOPTS+1,1        CHECK FOR "FIRST" OPTION
         BE    STEP1300            B. IF YES
         MVC   WRKCCDD1+2(8),WRKDDOLD
         MVC   WRKCCDD2+2(8),WRKDDNEW
STEP1300 DS    0H
         ALLOC VERB=S99VRBCC,TU=(WRKCCTU1,WRKCCTU2)
         LTR   R15,R15             CHECK CONCATENATION RETURN CODE
         BZ    STEP1400            B. IF CONCATENATION SUCCEEDED
         SPACE 1
*-------
*        IF CONCATENATION FAILS, ISSUE MESSAGE & REINSTATE OLD STEPLIB
*-------
         S99FAIL CPPL=(R8),        ISSUE ALLOCATION FAILURE MESSAGE    *
               MF=(E,WRKFAIL)
         LA    R0,OPENIRB          A(STEPLIB OPEN ROUTINE)
         LA    R1,WRKDDOLD         A(OLD STEPLIB DDNAME)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         TM    WRKFLAG1,WRK$1STD   CHECK IF 1ST DATASET ALLOCATED
         BZ    STEPEXIT            B. IF NOT
         LA    R0,WRKDDNEW         A(STEPLIB DDNAME)
         ST    R0,WRKDDN@
         LA    R0,L'WRKDDNEW       L(STEPLIB DDNAME)
         STH   R0,WRKDDN@+4
         FREE  PERM,DDN=WRKDDN@    FREE THE STEPLIB DDNAME
         B     STEPEXIT
         SPACE 1
*-------
*        RE-OPEN THE CONCATENATED STEPLIB
*-------
STEP1400 DS    0H
         CLI   $TEPOPTS+1,1        CHECK FOR "FIRST" OPTION
         BE    STEP1500            B. IF YES
         MVC   WRKDDNEW,WRKDDOLD   NEW STEPLIB DDNAME TO SAME AS BEFORE
         SPACE 1
STEP1500 DS    0H
         LA    R0,OPENIRB          A(STEPLIB OPEN ROUTINE)
         LA    R1,WRKDDNEW         A(NEW STEPLIB DDNAME)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF OPEN SUCCEEDED
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
         CLI   $TEPMSGS+1,2        CHECK FOR "NOMSGS" OPTION
         BE    STEPEXIT            B. IF YES
         LA    R0,1                SET NUMBER OF SEGMENTS
         LA    R1,MSG001I          'STEPLIB EXTENDED'
         SLR   R2,R2
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         B     STEPEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS THE "STEPLIB ALLOCATE" FUNCTION                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEP1600 DS    0H
         CLI   $TEPFUNC+1,2        CHECK FOR "ALLOCATE" FUNCTION
         BNE   STEP1800            B. IF NOT
         CLI   $TEPDSNS+1,1        CHECK IF DATASET NAMES SPECIFIED
         BNE   $TEPERR1            B. IF NOT
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         ICM   R2,15,TCBJLB        A(STEPLIB DCB)
         BZ    STEP1700            B. IF ONE DOES NOT EXIST
         DROP  R4
         SPACE 1
*-------
*        CLOSE & FREE THE PREVIOUSLY ALLOCATED STEPLIB
*-------
         LA    R0,CLOSEIRB         A(STEPLIB CLOSE ROUTINE)
         LA    R1,WRKDDOLD         A(OLD DDNAME RETURN ADDRESS)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF CLOSE WAS SUCCESSFUL
         BNZ   STEPEXIT            B. IF NOT
         LA    R0,WRKDDOLD         A(OLD STEPLIB DDNAME)
         ST    R0,WRKDDN@
         LA    R0,L'WRKDDOLD       L(OLD STEPLIB DDNAME)
         STH   R0,WRKDDN@+4
         CLC   =C'$TEP',WRKDDOLD   CHECK FOR "FAKE" STEPLIB DD #DD99244
         BNE   STEP1700            B. IF NOT                   #DD99244
         FREE  PERM,DDN=WRKDDN@    FREE THE OLD STEPLIB DDNAME
         SPACE 1
*-------
*        OPEN THE NEWLY ALLOCATED STEPLIB
*-------
STEP1700 DS    0H
         LA    R0,OPENIRB          A(STEPLIB OPEN ROUTINE)
         LA    R1,WRKDDNEW         A(NEW STEPLIB DDNAME)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF OPEN SUCCEEDED
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
         CLI   $TEPMSGS+1,2        CHECK FOR "NOMSGS" OPTION
         BE    STEPEXIT            B. IF YES
         LA    R0,1                SET NUMBER OF SEGMENTS
         LA    R1,MSG002I          'STEPLIB ALLOCATED'
         SLR   R2,R2
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         B     STEPEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS THE "STEPLIB FREE" FUNCTION                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEP1800 DS    0H
         CLI   $TEPFUNC+1,3        CHECK FOR "FREE" FUNCTION
         BNE   STEP2000            B. IF NOT
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         ICM   R2,15,TCBJLB        A(STEPLIB DCB)
         BZ    STEP1900            B. IF ONE DOES NOT EXIST
         DROP  R4
         SPACE 1
*-------
*        CLOSE & FREE THE PREVIOUSLY ALLOCATED STEPLIB
*-------
         LA    R0,CLOSEIRB         A(STEPLIB CLOSE ROUTINE)
         LA    R1,WRKDDOLD         A(OLD DDNAME RETURN ADDRESS)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF CLOSE WAS SUCCESSFUL
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
         LA    R0,WRKDDOLD         A(OLD STEPLIB DDNAME)
         ST    R0,WRKDDN@
         LA    R0,L'WRKDDOLD       L(OLD STEPLIB DDNAME)
         STH   R0,WRKDDN@+4
         CLC   =C'$TEP',WRKDDOLD   CHECK FOR "FAKE" STEPLIB DD #DD99244
         BNE   STEP1900            B. IF NOT                   #DD99244
         FREE  PERM,DDN=WRKDDN@    FREE THE OLD STEPLIB DDNAME
         SPACE 1
STEP1900 DS    0H
         CLI   $TEPMSGS+1,2        CHECK FOR "NOMSGS" OPTION
         BE    STEPEXIT            B. IF YES
         LA    R0,1                SET NUMBER OF SEGMENTS
         LA    R1,MSG003I          'STEPLIB FREED'
         SLR   R2,R2
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         B     STEPEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS THE "STEPLIB LIST" FUNCTION                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEP2000 DS    0H
         CLI   $TEPFUNC+1,4        CHECK FOR "LIST" FUNCTION
         BNE   STEP2400            B. IF NOT
         SPACE 1
*-------
*        CHECK IF THERE IS CURRENTLY A STEPLIB
*-------
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         ICM   R2,15,TCBJLB        A(STEPLIB DCB)
         BZ    STEPEXIT            B. IF ONE DOES NOT EXIST
         DROP  R4
         SPACE 1
*-------
*        CLOSE & OPEN THE STEPLIB AND RETRIEVE THE DDNAME
*-------
         LA    R0,CLOSEIRB         A(STEPLIB CLOSE ROUTINE)
         LA    R1,WRKDDOLD         A(OLD DDNAME RETURN ADDRESS)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF CLOSE WAS SUCCESSFUL
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
         LA    R0,OPENIRB          A(STEPLIB OPEN ROUTINE)
         LA    R1,WRKDDOLD         A(OLD STEPLIB DDNAME)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF OPEN SUCCEEDED
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
*-------
*        SCAN THE DSAB CHAIN FOR THE START OF THE STEPLIB DD
*-------
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         L     R5,TCBJSCB          A(JSCB)
         USING IEZJSCB,R5
         L     R5,JSCBACT          A(ACTIVE JSCB)
         L     R5,JSCDSABQ         A(QDB)
         USING QDB,R5
         ICM   R5,15,QDBFELMP      A(DSAB)
         BZ    STEPEXIT            B. IF CHAIN DOES NOT EXIST
         USING DSABID,R5
         SPACE 1
STEP2100 DS    0H
         L     R4,DSABSIOT         A(SIOT)
         USING INDMSIOT,R4
         CLC   SCTDDNAM,WRKDDOLD   CHECK FOR THE STEPLIB DDNAME
         BE    STEP2200            B. IF FOUND
         ICM   R5,15,DSABFCHN      A(NEXT DSAB)
         BNZ   STEP2100            B. IF ANOTHER DSAB EXISTS
         B     STEPEXIT            ELSE. UNABLE TO FIND OLD DDNAME
         SPACE 1
*-------
*        ONCE 1ST DSAB ENTRY FOUND DISPLAY ASSOCIATED DSNAMES
*-------
STEP2200 DS    0H
         MVC   WRKINSRT(4),=AL2(12,8)
         MVC   WRKINSRT+4(8),WRKDDOLD
         LA    R0,2                SET NUMBER OF SEGMENTS
         LA    R1,MSG006I          '$TEPNNNN LIST:'
         LA    R2,WRKINSRT
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         SPACE 1
STEP2300 DS    0H
         L     R1,SJFCBPTR         A(JFCB)
         USING INFMJFCB,R1
         MVC   WRKINSRT(4),=AL2(48,10)
         MVC   WRKINSRT+4(44),JFCBDSNM
         DROP  R1
         LA    R0,2                SET NUMBER OF SEGMENTS
         LA    R1,MSG007I          '- DSNAME'
         LA    R2,WRKINSRT
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         SPACE 1
*-------
*        CONTINUE PROCESSING DSAB'S TILL NON STEPLIB DD FOUND
*-------
         ICM   R5,15,DSABFCHN      A(NEXT DSAB)
         BZ    STEPEXIT            B. IF END OF  DSAB CHAIN
         L     R4,DSABSIOT         A(SIOT)
         CLC   SCTDDNAM,=CL8' '    CHECK IF NEXT IS A CONCATENATION
         BE    STEP2300            B. IF YES
         DROP  R4,R5
         B     STEPEXIT            ELSE. END OF LIST
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS THE "STEPLIB REMOVE" FUNCTION                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEP2400 DS    0H
         CLI   $TEPFUNC+1,5        CHECK FOR "REMOVE" FUNCTION
         BNE   STEPEXIT            B. IF NOT
         CLI   $TEPDSNS+1,1        CHECK IF DATASET NAMES SPECIFIED
         BNE   $TEPERR1            B. IF NOT
         SPACE 1
*-------
*        FREE THE DATASETS ALLOCATED DURING PARSE
*-------
         LA    R0,WRKDDNEW         A(NEW STEPLIB DDNAME)
         ST    R0,WRKDDN@
         LA    R0,L'WRKDDNEW       L(NEW STEPLIB DDNAME)
         STH   R0,WRKDDN@+4
         FREE  PERM,DDN=WRKDDN@    FREE THE OLD STEPLIB DDNAME
         SPACE 1
*-------
*        CHECK IF THERE IS CURRENTLY A STEPLIB
*-------
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         ICM   R2,15,TCBJLB        A(STEPLIB DCB)
         BZ    STEPEXIT            B. IF ONE DOES NOT EXIST
         DROP  R4
         SPACE 1
*-------
*        CLOSE THE PREVIOUSLY ALLOCATED STEPLIB
*-------
         LA    R0,CLOSEIRB         A(STEPLIB CLOSE ROUTINE)
         LA    R1,WRKDDOLD         A(OLD DDNAME RETURN ADDRESS)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF CLOSE WAS SUCCESSFUL
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
*-------
*        SCAN THE DSAB CHAIN FOR THE OLD STEPLIB DDNAME
*-------
         NI    WRKFLAG1,255-WRK$1STD
         SPACE 1
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         L     R5,TCBJSCB          A(JSCB)
         USING IEZJSCB,R5
         L     R5,JSCBACT          A(ACTIVE JSCB)
         L     R5,JSCDSABQ         A(QDB)
         USING QDB,R5
         ICM   R5,15,QDBFELMP      A(DSAB)
         BZ    STEPEXIT            B. IF CHAIN DOES NOT EXIST
         USING DSABID,R5
         SPACE 1
STEP2500 DS    0H
         L     R4,DSABSIOT         A(SIOT)
         USING INDMSIOT,R4
         CLC   SCTDDNAM,WRKDDOLD   CHECK FOR PREVIOUS STEPLIB DDNAME
         BE    STEP2600            B. IF FOUND
         ICM   R5,15,DSABFCHN      A(NEXT DSAB)
         BNZ   STEP2500            B. IF ANOTHER DSAB EXISTS
         B     STEPEXIT            ELSE. UNABLE TO FIND OLD DDNAME
         SPACE 1
*-------
*        SCAN THE USER SPECIFIED LIST FOR A MATCHING STEPLIB DSNAME
*-------
STEP2600 DS    0H
         L     R1,SJFCBPTR         A(JFCB)
         USING INFMJFCB,R1
         MVC   WRKDSN1,JFCBDSNM    SAVE THE CURRENT DATASET NAME
         DROP  R1
         SPACE 1
         LA    R3,$TEPLIST         A(1ST USER SPECIFIED DSNAME)
STEP2700 DS    0H
         L     R2,0(,R3)           A(SPECIFIED DSNAME)
         LH    R1,4(,R3)           L(SPECIFIED DSNAME)
         MVC   WRKDSN2,BLANKS      CLEAR DSNAME AREA           #DD99244
         #EXEC -R1,MVC,WRKDSN2(*-*),0(R2)                      #DD99244
         MVC   WRKLOCAT(MODLLOCL),MODLLOC                      #DD99244
         LA    R0,WRKDSN2                                      #DD99244
         ST    R0,WRKLOCAT+4       SAVE DSNAME ADDRESS         #DD99244
         LA    R0,WRKAREA                                      #DD99244
         ST    R0,WRKLOCAT+12      SAVE WORK AREA ADDRESS      #DD99244
         LOCATE WRKLOCAT                                       #DD99244
         CLC   WRKDSN1,WRKDSN2     COMPARE DSNAMES             #DD99244
         BE    STEP3100            B. IF MATCH FOUND           #DD99244
         CLI   24(R3),X'FF'        CHECK FOR END OF LIST
         BE    STEP2800            B. IF YES
         L     R3,24(,R3)          A(NEXT DSNAME PDE)
         B     STEP2700            SCAN TILL END OF LIST
         SPACE 1
*-------
*        ALLOCATE THE DSNAME TO THE NEW STEPLIB DDNAME
*-------
STEP2800 DS    0H
         LA    R0,WRKDSN1
         ST    R0,WRKDSN@          A(CURRENT STEPLIB DSNAME)
         LA    R0,L'WRKDSN1
         STH   R0,WRKDSN@+4        L(CURRENT STEPLIB DSNAME)
         SPACE 1
         TM    WRKFLAG1,WRK$1STD   CHECK IF 1ST DATASET ALLOCATED
         BO    STEP2900            B. IF YES
         OI    WRKFLAG1,WRK$1STD   REMEMBER 1ST DATASET WAS ALLOCATED
         LA    R0,WRKDDNEW
         ST    R0,WRKDDN@          A(CURRENT STEPLIB DDNAME)
         LA    R0,L'WRKDDNEW
         STH   R0,WRKDDN@+4        L(CURRENT STEPLIB DDNAME)
         ALLOC PERM,DSN=WRKDSN@,DISP=SHR,                              *
               DDN=WRKDDN@,FLAG1=S99NOCNV+S99NOMNT
         LTR   R15,R15             CHECK ALLOCATION RETURN CODE
         BZ    STEP3100            B. IF SUCCESSFUL
         B     STEP3000            ELSE. ISSUE ALLOCATION ERROR MSG
         SPACE 1
*-------
*        CONCATENATE THE DSNAME TO THE NEW STEPLIB DDNAME
*-------
STEP2900 DS    0H
         ALLOC PERM,DSN=WRKDSN@,DISP=SHR,                              *
               DDNTO=WRKDDRET,FLAG1=S99NOCNV+S99NOMNT
         LTR   R15,R15             CHECK ALLOCATION RETURN CODE
         BNZ   STEP3000            B. IF ALLOCATION FAILED
         SPACE 1
         MVC   WRKCCTU1(4),=AL2(DCCDDNAM,2)
         MVC   WRKCCDD1(2),=AL2(8) SET LENGTH OF DDNAME
         MVC   WRKCCDD2(2),=AL2(8) SET LENGTH OF DDNAME
         MVC   WRKCCTU2(4),=AL2(DCCPERMC,0)
         MVC   WRKCCDD1+2(8),WRKDDNEW
         MVC   WRKCCDD2+2(8),WRKDDRET
         ALLOC VERB=S99VRBCC,TU=(WRKCCTU1,WRKCCTU2)
         LTR   R15,R15             CHECK CONCATENATION RETURN CODE
         BZ    STEP3100            B. IF CONCATENATION SUCCEEDED
         SPACE 1
*-------
*        PROCESS ALLOCATION/CONCATENATION ERRORS
*-------
STEP3000 DS    0H
         S99FAIL CPPL=(R8),        ISSUE ALLOCATION FAILURE MESSAGE    *
               MF=(E,WRKFAIL)
         B     STEPEXIT            CONTINUE DSAB SCAN
         SPACE 1
*-------
*        CHECK IF NEXT DSAB IS A CONCATENATION OF THE STEPLIB
*-------
STEP3100 DS    0H
         ICM   R5,15,DSABFCHN      A(NEXT DSAB)
         BZ    STEP3200            B. IF END OF  DSAB CHAIN    #DD99244
         L     R4,DSABSIOT         A(SIOT)
         CLC   SCTDDNAM,=CL8' '    CHECK IF NEXT IS A CONCATENATION
         BE    STEP2600            B. IF YES
         DROP  R4,R5
         SPACE 1
*-------
*        OPEN THE NEWLY ALLOCATED STEPLIB
*-------
STEP3200 DS    0H                                              #DD99244
         LA    R0,WRKDDOLD         A(OLD STEPLIB DDNAME)
         ST    R0,WRKDDN@
         LA    R0,L'WRKDDOLD       L(OLD STEPLIB DDNAME)
         STH   R0,WRKDDN@+4
         CLC   =C'$TEP',WRKDDOLD   CHECK FOR "FAKE" STEPLIB DD #DD99244
         BNE   STEP3300            B. IF NOT                   #DD99244
         FREE  PERM,DDN=WRKDDN@    FREE THE OLD STEPLIB DDNAME
         SPACE 1
STEP3300 DS    0H                                              #DD99244
         TM    WRKFLAG1,WRK$1STD   CHECK IF ALL DATASETS REMOVED
         BZ    STEP3400            B. IF YES
         SPACE 1
         LA    R0,OPENIRB          A(STEPLIB OPEN ROUTINE)
         LA    R1,WRKDDNEW         A(NEW STEPLIB DDNAME)
         BAL   R14,SENDIRB         PERFORM ASYNCHRONOUSLY
         LTR   R15,R15             CHECK IF OPEN SUCCEEDED
         BNZ   STEPEXIT            B. IF NOT
         SPACE 1
         CLI   $TEPMSGS+1,2        CHECK FOR "NOMSGS" OPTION
         BE    STEPEXIT            B. IF YES
         LA    R0,1                SET NUMBER OF SEGMENTS
         LA    R1,MSG004I          'STEPLIB SHRUNK'
         SLR   R2,R2
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         B     STEPEXIT
         SPACE 1
*-------
*        INDICATE THAT ALL DATASETS WERE REMOVED FROM THE STEPLIB
*-------
STEP3400 DS    0H
         CLI   $TEPMSGS+1,2        CHECK FOR "NOMSGS" OPTION
         BE    STEPEXIT            B. IF YES
         LA    R0,1                SET NUMBER OF SEGMENTS
         LA    R1,MSG003I          'STEPLIB DEACTIVATED'
         SLR   R2,R2
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         B     STEPEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS ANY ERRORS                                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
$TEPERR1 DS    0H
         LA    R0,1                SET NUMBER OF SEGMENTS
         LA    R1,MSG005I          'MISSING STEPLIB DSNAMES'
         SLR   R2,R2
         BAL   R14,ISSUEMSG        ISSUE MESSAGE
         B     STEPEXIT
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        TERMINATE                                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
STEPEXIT DS    0H
         IKJRLSA WRKANS@           RELEASE ANSWER BUFFER STORAGE
         DROP  R8
         #STOP ,                   TERMINATE
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ISSUE MESSAGE VIA PUTLINE                                    *
*                                                                     *
*---------------------------------------------------------------------*
ISSUEMSG DS    0H
         STM   R0,R14,WRKREGS      SAVE ALL REGISTERS
         STM   R0,R2,WRKOLD        #SEGMENTS, SEGMENT1 SEGMENT2
         L     R4,WRKUPT@          A(UPT)
         L     R5,WRKECT@          A(ECT)
         XC    WRKECB,WRKECB
         PUTLINE PARM=WRKPUTL,UPT=(R4),ECT=(R5),ECB=WRKECB,            *
               OUTPUT=(WRKOLD,TERM,SINGLE,INFOR),                      *
               MF=(E,WRKIOPL)
         LM    R0,R14,WRKREGS      RESTORE LL REGISTERS
         BR    R14                 RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CREATE IRB TO PERFORM FUNCTION UNDER MAIN TASK'S TCB.        *
*                                                                     *
*---------------------------------------------------------------------*
SENDIRB  DS    0H
         ST    R1,WRKREGS          SAVE ROUTINE PARAMETER
         ST    R14,WRKRET@         SAVE RETURN ADDRESS
         MODESET MODE=SUP,KEY=ZERO
         CIRB  EP=(0),MODE=SUPR,KEY=SUPR,RETIQE=NO,STAB=(DYN),         *
               SVAREA=YES,WKAREA=24
         USING RBBASIC,R1
         L     R2,RBNEXAV          A(IQE)
         USING IQESECT,R2
         LA    R0,WRKREGS
         ST    R0,IQEPARAM         PASS REGISTERS TO IRB EXIT
         ST    R1,IQEIRB           PASS IRB ADDRESS
         L     R6,WRKLCT@          A(LCT)                      #DD99245
         MVC   IQETCB,LCTTCBAD-LCT(R6)  TCB ADDRESS
         XC    WRKREGS+4(4),WRKREGS+4
         L     R3,CVTPTR           A(CVT)
         USING CVT,R3
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,               *
               RELATED=(FREELOCK)
         LCR   R1,R2               A(COMPLIMENT IQE ADDRESS)
         L     R15,CVT0EF00        A(ASYNC EXIT SCHEDULE ROUTINE)
         BALR  R14,R15             QUEUE THE ASYNCRONOUS EXIT
FREELOCK SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,RELATED=(GETLOCK)
         MODESET MODE=PROB,KEY=NZERO
         WAIT  ECB=WRKREGS+4       WAIT FOR THE EXIT TO END
         LH    R15,WRKREGS+6       RETURN EXIT RETURN CODE
         L     R14,WRKRET@         RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         DROP  R1,R2,R3
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ASYNCRONOUS EXIT TO CLOSE THE STEPLIB DD STATEMENT.          *
*                                                                     *
*---------------------------------------------------------------------*
         PUSH  USING
         DROP  ,
CLOSEIRB DS    0H
         LR    R12,R15             A(EXIT ENTRY POINT)
         USING CLOSEIRB,R12
         SPACE 1
         LR    R9,R1               A(INPUT PARAMETER)
         L     R7,0(,R9)           A(STEPLIB DDNAME SAVE AREA)
         MVC   0(8,R7),BLANKS      CLEAR RETURN DDNAME AREA    #DD99245
         SPACE 1
         USING PSA,0
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         L     R5,TCBTCT           A(TCT)
         USING SMFTCT,R5
         L     R6,TCTLCTAD         A(LCT)
         USING LCT,R6
         L     R2,LCTJOBLB         A(JOBLIB DCB)
         SLR   R0,R0
         ST    R0,LCTJOBLB         ZERO LCT'S STEPLIB POINTER
         SPACE 1
CLOSE100 DS    0H
         ICM   R4,15,TCBTCB        A(NEXT LOWER TCB)
         BZ    CLOSE200            B. IF AT END
         C     R2,TCBJLB           CHECK FOR MATCHING DCB POINTER
         BNE   CLOSE100            B. IF NOT LEAVE ALONE
         ST    R0,TCBJLB           ZERO CURRENT TCB'S STEPLIB POINTER
         B     CLOSE100            CONTINUE TILL END OF TCBS
         SPACE 1
CLOSE200 DS    0H
         LTR   R2,R2               CHECK IF A STEPLIB EXISTED  #DD99245
         BZ    CLOSE300            B. IF NOT                   #DD99245
         SPACE 1
         L     R4,PSATOLD          A(TCB)
         L     R8,TCBTIO           A(TIOT)
         L     R1,TCBLTC           A(LAST TASK TCB)
         MVC   TCBTIO,TCBTIO-TCB(R1)
         MVI   0(R9),128           SET CLOSE OPTION
         CLOSE ((R2)),MF=(E,(R9))
         ST    R8,TCBTIO           RESTORE A(TIOT)
         MVC   0(8,R7),DCBDDNAM-IHADCB(R2)
         CLI   0(R8),C'$'          CHECK FOR "$TEP..."         #DD99245
         BNE   CLOSE300            B. IF NOT                   #DD99245
         FREEMAIN RC,LV=MODLDCBL,A=(R2),SP=253                 #DD99244
         SPACE 1
CLOSE300 DS    0H                                              #DD99245
         SLR   R0,R0               SET SUCCESSFUL RETURN CODE
         POST  4(,R9),(0)          POST WITH RETURN CODE
         SVC   3                   RETURN TO CALLER
         POP   USING
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ASYNCRONOUS EXIT TO OPEN  THE STEPLIB DD STATEMENT.          *
*                                                                     *
*---------------------------------------------------------------------*
         PUSH  USING
         DROP  ,
OPENIRB  DS    0H
         LR    R12,R15             A(EXIT ENTRY POINT)
         USING OPENIRB,R12
         SPACE 1
         LR    R9,R1               A(INPUT PARAMETER)
         L     R7,0(,R9)           A(STEPLIB DDNAME)
         SPACE 1
         LA    R0,8                SET FAILURE CODE            #DD99245
         TM    0(R7),X'BF'         CHECK FOR VALID DDNAME      #DD99245
         BZ    OPENEXIT            B. IF NOT                   #DD99245
         SPACE 1
         USING PSA,0
         L     R4,PSATOLD          A(TCB)
         USING TCB,R4
         L     R5,TCBTCT           A(TCT)
         USING SMFTCT,R5
         L     R6,TCTLCTAD         A(LCT)
         USING LCT,R6
         GETMAIN RU,LV=MODLDCBL,SP=253,LOC=BELOW
         LR    R2,R1               A(DCB)
         MVC   0(MODLDCBL,R2),MODLDCB
         MVC   DCBDDNAM-IHADCB(,R2),0(R7)
         SPACE 1
         L     R8,TCBTIO           A(TIOT)
         L     R1,TCBLTC           A(LAST TASK TCB)
         MVC   TCBTIO,TCBTIO-TCB(R1)
         MVI   0(R9),128           SET OPEN OPTION
         OPEN  ((R2)),MF=(E,(R9))
         ST    R8,TCBTIO           RESTORE A(TIOT)
         SPACE 1
         TM    DCBOFLGS-IHADCB(R2),DCBOFOPN
         BO    OPEN0100            B. IF OPEN WAS SUCCESSFUL
         FREEMAIN RC,LV=MODLDCBL,A=(R2),SP=253                 #DD99244
         LA    R0,12               SET FAILURE CODE
         B     OPENEXIT
         SPACE 1
OPEN0100 DS    0H
         ST    R2,LCTJOBLB-LCT(,R6) SAVE STEPLIB DCB IN LCT
         SPACE 1
OPEN0200 DS    0H
         ICM   R4,15,TCBTCB        A(NEXT LOWER TCB)
         BZ    OPEN0300            B. IF AT END
         CLC   TCBJLB,=F'0'        CHECK IF A JOB IS HANGING AROUND
         BNE   OPEN0200            B. IF YES LEAVE ALONE
         ST    R2,TCBJLB           SET NEW STEPLIB POINTER
         B     OPEN0200            CONTINUE TILL END OF TCBS
         SPACE 1
OPEN0300 DS    0H
         SLR   R0,R0               SET SUCCESSFUL RETURN CODE
         SPACE 1
OPENEXIT DS    0H
         POST  4(,R9),(0)          POST WITH RETURN CODE
         SVC   3                   RETURN TO CALLER
         POP   USING
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        DATASET NAME VALIDITY CHECKING ROUTINE (CONCATENATION)       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PUSH  USING
         DROP  ,
DDALLOC  DS    0H
         USING DDALLOC,R15
         STM   R14,R12,12(R13)     SAVE ALL REGISTERS
         LR    R12,R15             SET SUBROUTINE BASE REGISTER
         DROP  R15
         USING DDALLOC,R12
         L     R5,0(,R1)           A(DSNAME PDE)
         LR    R10,R13             A(ORIGINAL SAVEAREA)
         L     R9,4(,R1)           A(USER WORKAREA)
         USING STEPWRK,R9
         LA    R13,WRKREGS         A(SUBROUTINE SAVE AREA)
         SPACE 1
         TM    WRKFLAG1,WRK$1STD   CHECK IF 1ST DATASET WAS ALLOCATED
         BO    DDAL0200            B. IF YES
         LA    R0,WRKDDNEW
         ST    R0,WRKDDN@          A(CURRENT STEPLIB DDNAME)
         LA    R0,L'WRKDDNEW
         STH   R0,WRKDDN@+4        L(CURRENT STEPLIB DDNAME)
DDAL0100 DS    0H
         ALLOC PERM,DSN=((R5)),DISP=SHR,                               *
               DDN=WRKDDN@,FLAG1=S99NOCNV+S99NOMNT
         OI    WRKFLAG1,WRK$1STD   REMEMBER 1ST DATASET WAS ALLOCATED
         LTR   R15,R15             CHECK ALLOCATION RETURN CODE
         BZ    DDALEXIT            B. IF SUCCESSFUL
         USING S99RB,R14
         CLC   S99ERROR,=X'0410'   CHECK FOR "DDNAME IN USE" ERROR
         BNE   DDAL0300            B. IF NOT
         PACK  WRKDBL1,WRKDDNEW+4(4)
         AP    WRKDBL1,=P'+1'      INCREMENT STEPLIB NUMBER
         OI    WRKDBL1+7,X'0F'
         UNPK  WRKDDNEW+4(4),WRKDBL1
         DROP  R14
         B     DDAL0100            RETRY ALLOCATION
         SPACE 1
DDAL0200 DS    0H
         ALLOC PERM,DSN=((R5)),DISP=SHR,                               *
               DDNTO=WRKDDRET,FLAG1=S99NOCNV+S99NOMNT
         LTR   R15,R15             CHECK ALLOCATION RETURN CODE
         BNZ   DDAL0300            B. IF ALLOCATION FAILED
         SPACE 1
         MVC   WRKCCTU1(4),=AL2(DCCDDNAM,2)
         MVC   WRKCCDD1(2),=AL2(8) SET LENGTH OF DDNAME
         MVC   WRKCCDD2(2),=AL2(8) SET LENGTH OF DDNAME
         MVC   WRKCCTU2(4),=AL2(DCCPERMC,0)
         MVC   WRKCCDD1+2(8),WRKDDNEW
         MVC   WRKCCDD2+2(8),WRKDDRET
         ALLOC VERB=S99VRBCC,TU=(WRKCCTU1,WRKCCTU2)
         LTR   R15,R15             CHECK CONCATENATION RETURN CODE
         BZ    DDALEXIT            B. IF CONCATENATION SUCCEEDED
         SPACE 1
DDAL0300 DS    0H
         S99FAIL CPPL=(R8),        ISSUE ALLOCATION FAILURE MESSAGE    *
               MF=(E,WRKFAIL)
         LA    R15,12              SET ERROR CODE
         SPACE 1
DDALEXIT DS    0H
         LR    R13,R10             A(ORIGINAL SAVE AREA)
         ST    R15,16(,R13)        OVERLAY R15 IN SAVE AREA
         LM    R14,R12,12(R13)     RESTORE ALL REGISTERS
         BR    R14                 RETURN TO IKJPARS
         POP   USING
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CONSTANTS & DYNAMIC WORKAREA                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
BLANKS   DC    80C' '              LOTS OF BLANKS              #DD99244
         SPACE 1
MODLDCB  DCB   DDNAME=*,DSORG=PO,RECFM=U,MACRF=E
MODLDCBL EQU   *-MODLDCB         DCB LENGTH
         SPACE 1
MODLLOC  CAMLST NAME,*-*,,*-*      CATALOG PARAMETER LIST      #DD99244
MODLLOCL EQU   *-MODLLOC           CATALOG PARAMETER LIST LEN  #DD99244
         SPACE 1
MSG001I  MSG   '$TEP01I STEPLIB EXPANDED'
MSG002I  MSG   '$TEP02I STEPLIB ACTIVATED'
MSG003I  MSG   '$TEP03I STEPLIB DEACTIVATED'
MSG004I  MSG   '$TEP04I STEPLIB SHRUNK'
MSG005I  MSG   '$TEP05I MISSING STEPLIB DATASET NAME(S)'
MSG006I  MSG   '$TEP06I  DATASET LIST:'
MSG007I  MSG   '$TEP07I - '
         SPACE 1
         #STARTWA ,
         DYNSPACE ,                DYNAMIC ALLOCATION PARAMETER LIST
WRKUPT@  DS    A                   UPT ADDRESS
WRKECT@  DS    A                   ECT ADDRESS
WRKANS@  DS    A                   RETURNED PDL BUFFER ADDRESS
WRKCCTU1 DS    AL2,AL2             CONCATENATION DDNAMES TEXT UNIT
WRKCCDD1 DS    AL2,CL8             PRIMARY DDNAME
WRKCCDD2 DS    AL2,CL8             SECONDARY DDNAME
WRKCCTU2 DS    AL2,AL2             CONCATENATION "PERMANENT" TEXT UNIT
WRKDBL1  DS    D                   DOUBLEWORD WORK AREA
WRKDDN@  DS    A,AL2               CURRENT DATASET'S DDNAME PDE
WRKDDOLD DS    CL8                 ORIGINAL STEPLIB DDNAME
WRKDDNEW DS    CL8                 CURRENT STEPLIB DDNAME - $TEP0001
WRKDDTMP DS    CL8                 //STEPLIB REPLACEMENT NAME  #DD99245
WRKDDRET DS    CL8                 CURRENT DATASET'S RETURNED DDNAME
WRKDSN@  DS    A,AL2               CURRENT DATASET NAME PDE
WRKDSN1  DS    CL44                CURRENT STEPLIB DATASET NAME
WRKDSN2  DS    CL44                CURRENT "INPUT" DATASET NAME
WRKECB   DS    A                   COMMAND PROCESSOR ECB
WRKFAIL  DS    6F                  S99FAIL PARAMETER LIST
WRKFLAG1 DS    X                   FLAG BYTE #1
WRK$1STD EQU   BIT1   .1.. ....    - 1ST DSNAME ALLOCATED
WRKIOPL  DS    4F                  IOPL
WRKINSRT DS    AL2,AL2,CL80        PUTLINE MESSAGE INSERT
WRKLCT@  DS    A                   LCT ADDRESS                 #DD99245
WRKLOCAT DS    5F                  CATALOG LOCATE PLIST        #DD99244
WRKOLD   DS    F                   NUMBER OF MESSAGE SEGMENTS
         DS    A                   A(MESSAGE SEGMENT)
         DS    A                   A(MESSAGE INSERT SEGMENT)
WRKPPL   DS    7F                  PARSE PARAMETER LIST
WRKPUTL  PUTLINE MF=L              PUTLINE PARAMETER LIST
WRKRET@  DS    F                   SUBROUTINE RETURN ADDRESS
WRKREGS  DS    18F                 REGISTER SAVE AREA
WRKAREA  DS    0F,XL265            CATALOG LOCATE WORK AREA    #DD99244
         #STOPWA ,
         EJECT 1
         PRINT NOGEN
*---------------------------------------------------------------------*
*                                                                     *
*        "STEPLIB" COMMAND KEYWORDS:                                  *
*                                                                     *
*        NOTE: SOME KEYWORDS (SHR & REUSE) ARE THERE SIMPLY TO MAKE   *
*              THE COMMAND LINE LOOK MORE LIKE AN "ALLOC" COMMAND.    *
*              A PREVIOUS VERSION HOOKED INTO THE "ALLOC" COMMAND,    *
*              AND REMOVED THE "STEPLIB" DDNAME RESTRICTION.          *
*              THIS VERSION WAS CREATED WHEN "USERMODS" WERE BEING    *
*              ELIMINATED FROM THE SYSTEM.                            *
*                                                                     *
*---------------------------------------------------------------------*
STEPLIB  CSECT ,
         SPACE 1
STEPPCL  IKJPARM
         SPACE 1
$TEPFUNC IKJKEYWD DEFAULT='ALLOCATE'
         IKJNAME 'ADD'
         IKJNAME 'ALLOCATE'
         IKJNAME 'FREE',ALIAS='CLOSE'
         IKJNAME 'LIST'
         IKJNAME 'REMOVE',ALIAS='DELETE'
         SPACE 1
$TEPDSNS IKJKEYWD ,
         IKJNAME 'DATASETS',ALIAS='DSNAMES',SUBFLD=$TEPSUBF
         SPACE 1
$TEPSHR  IKJKEYWD
         IKJNAME 'SHR'
         SPACE 1
$TEPREUS IKJKEYWD
         IKJNAME 'REUSE'
         SPACE 1
$TEPOPTS IKJKEYWD DEFAULT='FIRST'
         IKJNAME 'FIRST',ALIAS='FRONT'
         IKJNAME 'LAST',ALIAS='END'
         SPACE 1
$TEPMSGS IKJKEYWD DEFAULT='MSGS'
         IKJNAME 'MSGS'
         IKJNAME 'NOMSGS'
         SPACE 1
$TEPSUBF IKJSUBF
$TEPLIST IKJPOSIT DSNAME,LIST,USID,UPPERCASE,                          *
               PROMPT='STEPLIB DATASET NAME(S)',VALIDCK=DDALLOC
         IKJENDP
         SPACE 1
         PRINT GEN
         EJECT 1
*-------
*        DSECTS
*-------
         #DSECTS ALLOC,CVT,DCB,DEB,DSAB,ECT,IQE,LCT,PSA,RB,TCB,TCT,TSO,*
               ASCB,ASXB,JSCB,QDB,SIOT,JFCB
         PRINT NOGEN
         IHAQDB ,
         PRINT GEN
         END   ,
./ ADD NAME=TSOPARSE 0100-00040-00040-1222-00450-00450-00000-SOURCE
TSOPARSE TITLE ' - "IKJPARS" / "IKJSCAN" SERVICE ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        "IKJPARS" / "IKJSCAN" SERVICE ROUTINE                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT 1
TSOPARSE #START ,                  *** HOUSEKEEPING ***                *
               AMODE=CAP24,        EXECUTE IN 24-BIT ADDRESSING MODE   *
               BASE=(R12),         DEFINE BASE REGISTER(S)             *
               LOC=BELOW,          INDICATE 24-BIT WORKAREA            *
               PREFIX=PAR,         DEFINE MACRO LABEL PREFIX           *
               REG0=R0,            SAVE INPUT PARAMETER ADDRESS        *
               REG1=R6,            SAVE INPUT PARAMETER ADDRESS        *
               RMODE=24,           EXECUTE IN 24-BIT RESIDENCY MODE    *
               SP=0,               DEFINE WORKAREA SUBPOOL             *
               SPLEVEL=2,          INDICATE XA/ESA MACRO LEVEL SUPPORT *
               WKDSECT=PARSWRK     DEFINE WORKAREA NAME
         SPACE 1
         USING PSA,0
         USING PPL,R6
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PERFORM INITIALIZATION                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         STC   R0,FUNCTION         SAVE CALLER SPECIFIED FUNCTION CODE
         CLI   FUNCTION,0          CHECK FOR "IKJSCAN" FUNCTION
         BE    PARS0100            B. IF YES
         CLI   FUNCTION,4          CHECK FOR "IKJPARS" FUNCTION
         BE    PARS0100            B. IF YES
         EX    0,*                 ELSE. ABEND S0C3
         SPACE 1
PARS0100 DS    0H
         IPK   ,                   ACQUIRE CALLER'S PROTECT KEY
         STC   R2,OLDKEY           SAVE PROTECT KEY
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CHECK IF CALLER IS IN SUPERVISOR STATE OR APF AUTHORIZED     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         TESTAUTH STATE=YES,RBLEVEL=1
         LTR   R15,R15             CHECK IF ALREADY IN SUPERVISOR STATE
         BZ    PARS0200            B. IF YES
         SPACE 1
         TESTAUTH FCTN=1
         LTR   R15,R15             CHECK IF APF AUTHORIZED
         BNZ   PARS0300            B. IF NOT
         SPACE 1
         MODESET MODE=SUP          CHANGE TO SUPERVISOR STATE
         SPACE 1
         OI    WRKFLAG1,WRK$PROB   INDICATE "PROBLEM PROGRAM" STATE
         SPACE 1
PARS0200 DS    0H
         OI    WRKFLAG1,WRK$AUTH   INDICATE "AUTHORIZED"
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ACQUIRE MISSING CONTROL BLOCKS IN KEY SAME KEY AS PLIST      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
PARS0300 DS    0H
         L     R4,PSATOLD          LOAD TCB  ADDRESS
         USING TCB,R4
         SPACE 1
         IVSK  R1,R6               LOAD CPPL/CSPL PLIST KEY
         SPKA  0(R1)               GET INTO APPROPRIATE KEY
         SPACE 1
*-------
*        ACQUIRE & INITIALIZE "UPT" IF NOT SUPPLIED
*-------
         SPACE 1
         ICM   R1,15,PPLUPT        CHECK IF "UPT" WAS SUPPLIED
         BNZ   PARS0400            B. IF YES
         GETMAIN RU,LV=UPTLNGTH,SP=0,LOC=BELOW
         XC    0(UPTLNGTH,R1),0(R1) CLEAR ACQUIRED STORAGE
         ST    R1,PPLUPT           SAVE ADDRESS IN CALLER'S PPL
         SPACE 1
PARS0400 DS    0H
         USING UPT,R1
         SPACE 1
         MVC   SAVEUPTS,UPTSWS     SAVE UPT SWITCHES
         MVI   UPTSWS,UPTNPRM+UPTNCOM+UPTMID+UPTWTP
         SPACE 1
         DROP  R1
         SPACE 1
*-------
*        ACQUIRE & INITIALIZE "ECT" IF NOT SUPPLIED
*-------
         SPACE 1
         ICM   R1,15,PPLECT        CHECK IF "ECT" WAS SUPPLIED
         BNZ   PARS0500            B. IF YES
         GETMAIN RU,LV=ECTLEN,SP=1,LOC=BELOW
         XC    0(ECTLEN,R1),0(R1)  CLEAR ACQUIRED STORAGE
         ST    R1,PPLECT           SAVE ADDRESS IN CALLER'S PPL
         SPACE 1
PARS0500 DS    0H
         USING ECT,R1
         SPACE 1
         MVC   SAVEECTS,ECTSWS     SAVE ECT SWITCHES
         MVI   ECTSWS,ECTNMAL+ECTNNOT+ECTBKGRD
         MVC   SAVEECT2,ECTSWS2    SAVE ECT SWITCHES
         MVI   ECTSWS2,ECTDEFCS
         DROP  R1
         SPACE 1
*-------
*        ACQUIRE & INITIALIZE "ECB" IF NOT SUPPLIED
*-------
         SPACE 1
         ICM   R0,15,PPLECB        CHECK IF "ECB" WAS SUPPLIED
         BNZ   PARS0600            B. IF YES
         GETMAIN RU,LV=4,SP=0,LOC=BELOW
         XC    0(4,R1),0(R1)       CLEAR ACQUIRED STORAGE
         ST    R1,PPLECB           SAVE ADDRESS IN CALLER'S PPL
         SPACE 1
*-------
*        ACQUIRE & INITIALIZE "CSPL FLAG" IF NOT SUPPLIED
*-------
         SPACE 1
PARS0600 DS    0H
         CLI   FUNCTION,4          CHECK FOR "IKJSCAN" FUNCTION
         BNE   PARS0800            B. IF NOT
         SPACE 1
         USING CSPL,R6
         SPACE 1
         ICM   R0,15,CSPLFLG       CHECK IF "FLAG WORD" WAS SUPPLIED
         BNZ   PARS0700            B. IF YES
         GETMAIN RU,LV=4,SP=1,LOC=BELOW
         XC    0(4,R1),0(R1)       CLEAR ACQUIRED STORAGE
         ST    R1,CSPLFLG          SAVE ADDRESS IN CALLER'S CSPL
         SPACE 1
*-------
*        ACQUIRE & INITIALIZE "CSOA" IF NOT SUPPLIED
*-------
         SPACE 1
PARS0700 DS    0H
         ICM   R0,15,CSPLOA        CHECK IF "OUPUT AREA" WAS SUPPLIED
         BNZ   PARS0800            B. IF YES
         GETMAIN RU,LV=CSOALEN,SP=1,LOC=BELOW
         XC    0(CSOALEN,R1),0(R1) CLEAR ACQUIRED STORAGE
         ST    R1,CSPLOA           SAVE ADDRESS IN CALLER'S CSPL
         SPACE 1
*-------
*        ACQUIRE & INITIALIZE "IOWA" IF NOT SUPPLIED
*-------
         SPACE 1
PARS0800 DS    0H
         L     R2,CSPLECT          LOAD ECT ADDRESS
         USING ECT,R2
         ICM   R1,15,ECTIOWA       CHECK IF "I/O WORKAREA" WAS SUPPLIED
         BNZ   PARS0900            B. IF YES
         SPACE 1
         GETMAIN RU,LV=IOSRLEN,SP=0,LOC=BELOW
         XC    0(IOSRLEN,R1),0(R1) CLEAR ACQUIRED STORAGE
         ST    R1,ECTIOWA          SAVE ADDRESS IN CALLER'S ECT
         SPACE 1
PARS0900 DS    0H
         LR    R2,R1               LOAD IOWA ADDRESS
         USING IOSRL,R2
         SPACE 1
*-------
*        ACQUIRE & INITIALIZE "STACK"
*-------
         SPACE 1
         ICM   R0,15,IOSTELM       CHECK IF "STACK ELEMENT" EXISTS
         BNZ   PARS1000            B. IF YES
         SPACE 1
         GETMAIN RU,LV=INSLEN,SP=0,LOC=BELOW
         XC    0(INSLEN,R1),0(R1)  CLEAR ACQUIRED STORAGE
         ST    R1,IOSTELM          SAVE ADDRESS IN CALLER'S IOWA
         SPACE 1
         DROP  R2
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        ACQUIRE, INITIALIZE & ACTIVATE "SVC SCREEN" FOR TPUTS        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
PARS1000 DS    0H
         TM    WRKFLAG1,WRK$AUTH   CHECK IF CALLER IS AUTHORIZED
         BNO   PARS1100            B. IF NOT
         SPACE 1
         SPKA  0                   GET INTO KEY ZERO
         SPACE 1
         GETMAIN RU,LV=268,SP=254,LOC=BELOW
         L     R0,=A(SCREEN)       LOAD SVC SCREEN ROUTINE ADDRESS
         ST    R0,0(,R1)           SAVE ADDRESS IN SVC SCREEN TABLE
         ST    R13,264(,R1)        SAVE WORKAREA ADDRESS
         MVC   4(4,R1),=X'C0000000'
         MVI   8(R1),X'80'         INSERT MASK TO ALLOW SVC
         MVC   9(255,R1),8(R1)
         MVI   8+93(R1),0          INSERT MAST TO INTERCEPT "TPUT"
         SPACE 1
         ST    R1,TCBSVCA2         SAVE SVC TABLE ADDRESS
         OI    TCBFLGS7,TCBSVCS    ACTIVATE THE SCREEN
         OI    TCBFLGS7,TCBSVCSP
         SPACE 1
         IC    R2,OLDKEY           LOAD CALLER'S PROTECT KEY
         SPKA  0(R2)               RETURN TO ORIGINAL PROTECT KEY
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS THE INPUT BUFFER (CALL IKJSCAN OR IKJPARS)           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
PARS1100 DS    0H
         LA    R15,CALLPARS        LOAD IKJPARS ROUTINE ADDRESS
         CLI   FUNCTION,0          CHECK FOR "PARSE" FUNCTION
         BE    PARS1200            B. IF YES
         LA    R15,CALLSCAN        LOAD IKJSCAN ROUTINE ADDRESS
         SPACE 1
PARS1200 DS    0H
         IC    R2,OLDKEY           LOAD CALLER'S PROTECT KEY
         SPKA  0(R2)               RETURN TO ORIGINAL PROTECT KEY
         SPACE 1
         SYNCH (15),RESTORE=YES,AMODE=24,MF=(E,SYNCHL)
         SPACE 1
         #SETRC ,                  SET RETURN CODE
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        DEACTIVATE AND RELEASE THE "SVC SCREEN"                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         TM    WRKFLAG1,WRK$AUTH   CHECK IF CALLER IS AUTHORIZED
         BNO   PARS1300            B. IF NOT
         SPACE 1
         SPKA  0                   GET INTO KEY ZERO
         NI    TCBFLGS7,FF-TCBSVCS DEACTIVATE THE SCREEN
         NI    TCBFLGS7,FF-TCBSVCSP
         SPACE 1
         L     R1,TCBSVCA2         LOAD SVC TABLE ADDRESS
         XC    TCBSVCA2,TCBSVCA2   CLEAR TABLE POINTER IN TCB
         SPACE 1
         FREEMAIN RU,LV=268,A=(R1),SP=254
         SPACE 1
         IC    R2,OLDKEY           LOAD CALLER'S PROTECT KEY
         SPKA  0(R2)               RETURN TO ORIGINAL PROTECT KEY
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        RESTORE ORIGINAL CONTROL BLOCK FIELDS                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
*-------
*        RESTORE "UPT" SWITCHES
*-------
         SPACE 1
PARS1300 DS    0H
         L     R1,CSPLUPT          LOAD UPT ADDRESS
         USING UPT,R1
         SPACE 1
         MVC   UPTSWS,SAVEUPTS     RESTORE UPT SWITCHES
         SPACE 1
         DROP  R1
         SPACE 1
*-------
*        RESTORE "ECT" SWITCHES
*-------
         SPACE 1
         L     R1,CSPLECT          LOAD ECT ADDRESS
         USING ECT,R1
         SPACE 1
         MVC   ECTSWS,SAVEECTS     RESTORE ECT SWITCHES
         MVC   ECTSWS,SAVEECT2     RESTORE ECT SWITCHES
         SPACE 1
         DROP  R1
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        TERMINATE                                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         TM    WRKFLAG1,WRK$PROB   CHECK IF CALLER WAS IN PROBLEM STATE
         BNO   PARS1400            B. IF NOT
         SPACE 1
         MODESET MODE=PROB         CHANGE BACK TO PROBLEM PROGRAM STATE
         SPACE 1
PARS1400 DS    0H
         IC    R2,OLDKEY           LOAD CALLER'S PROTECT KEY
         SPKA  0(R2)               RETURN TO ORIGINAL PROTECT KEY
         SPACE 1
         L     R1,WRKMSG1          LOAD 1ST MESSAGE ADDRESS
PARSEXIT #STOP R1=(R1)             RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        TPUT SVC SCREEN SUBROUTINE                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PUSH  USING
         DROP  R12
         SPACE 1
         USING CVT,R3
         USING TCB,R4
         USING RBBASIC,R5
         USING SCREEN,R6
         USING ASCB,R7
SCREEN   DS    0H
         L     R3,TCBSVCA2         LOAD SVC TABLE ADDRESS
         L     R13,264(,R3)        LOAD WORKAREA ADDRESS
         USING PARSWRK,R13
         SPACE 1
         ST    R14,SAVER14         SAVE RETURN ADDRESS
         STM   R0,R1,SAVER0        SAVE MESSAGE LENGTH & ADDRESS
         SPACE 1
         IC    R2,OLDKEY           LOAD CALLER'S PROTECT KEY
         SPKA  0(R2)               GET INTO CALLER'S KEY
         SPACE 1
         LH    R1,SAVER0+2         LOAD LENGTH OF MESSAGE
         LA    R1,8(,R1)           ADD CHAIN POINTER & RDW FIELDS
         GETMAIN RU,LV=(R1),SP=1,LOC=BELOW
         XC    0(8,R1),0(R1)       CLEAR HEADER SECTION
         LR    R3,R1
         SPACE 1
         SPKA  0                   RETURN TO KEY ZERO
         SPACE 1
         ICM   R0,15,WRKMSG1       CHECK IF THIS IS THE 1ST MESSAGE
         BNZ   SCREEN10            B. IF NOT
         ST    R3,WRKMSG1          SAVE ADDRESS OF 1ST MESSAGE
         B     SCREEN20
         SPACE 1
SCREEN10 DS    0H
         L     R2,WRKMSGS          LOAD PREVIOUS CHAIN POINTER ADDRESS
         ST    R3,0(,R2)           ADD NEW MESSAGE TO THE CHAIN
         SPACE 1
SCREEN20 DS    0H
         LA    R0,0(,R3)           LOAD CURRENT MESSAGE CHAIN POINTER
         ST    R0,WRKMSGS          SAVE FOR NEXT MESSAGE
         SPACE 1
         L     R1,SAVER1           LOAD CURRENT MESSAGE TEXT ADDRESS
         LH    R2,SAVER0+2         LOAD CURRENT MESSAGE TEXT LENGTH
         STH   R2,4(,R3)           SAVE MESSAGE LENGTH IN RDW
         BCTR  R2,0                DECREMENT FOR EXECUTE
         #EXEC R2,MVC,8(*-*,R3),0(R1)
         SPACE 1
         SLR   R15,R15             SET RETURN CODE
         L     R14,SAVER14         RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO SVC FLIH
         SPACE 1
         POP   USING
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        IKJPARS LINKAGE ROUTINE                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PUSH  USING
         DROP  R12
         SPACE 1
CALLPARS DS    0H
         LR    R12,R15             LOAD BASE REGISTER
         USING CALLPARS,R12
         SPACE 1
         #MODE 31
         CALLTSSR EP=IKJPARS,      CALL TSO PARSE ROUTINE              *
               MF=(E,(R6))
         #MODE 24
         SVC   3                   RETURN TO CALLER
         SPACE 1
         POP   USING
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        IKJSCAN LINKAGE ROUTINE                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PUSH  USING
         DROP  R12
         SPACE 1
CALLSCAN DS    0H
         LR    R12,R15             LOAD BASE REGISTER
         USING CALLSCAN,R12
         SPACE 1
         #MODE 31
         CALLTSSR EP=IKJSCAN,      CALL TSO SCAN ROUTINE               *
               MF=(E,(R6))
         #MODE 24
         SVC   3                   RETURN TO CALLER
         SPACE 1
         POP   USING
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CONSTANTS AND WORK AREA                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         #STARTWA ,
WRKFLAG1 DC    X'00'               FLAG BYTE #1
WRK$AUTH EQU   BIT0                - AUTHORIZED PROGRAM
WRK$PROB EQU   BIT1                - PROBLEM PROGRAM
FUNCTION DC    X'0'                FUNCTION CODE
OLDIOWA@ DC    A(*-*)              INITIAL IOWA ADDRESS
OLDKEY   DC    X'0'                CALLER'S PSW PROTECT KEY
SAVEECTS DC    XL(L'ECTSWS)'0'     SAVE AREA FOR ECTSWS
SAVEECT2 DC    XL(L'ECTSWS2)'0'    SAVE AREA FOR ECTSWS2
SAVER0   DC    F'0'                REGISTER  0 SAVE AREA
SAVER1   DC    F'0'                REGISTER  1 SAVE AREA
SAVER14  DC    F'0'                REGISTER 14 SAVE AREA
SAVEUPTS DC    XL(L'UPTSWS)'0'     SAVE AREA FOR UPTSWS
SYNCHL   SYNCH MF=L                SYNCH PARAMETER LIST
WRKMSGS  DC    A(*-*)              MESSAGE CHAIN WORD ADDRESS
WRKMSG1  DC    A(*-*)              ADDRESS OF 1ST MESSAGE
         #STOPWA ,
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         #IOSRL ,                  I/O SERVICE ROUTINE WORK AREA
IOSRLEN  EQU   *-IOSRL
         SPACE 1
         #INSTACK ,                STACK ELEMENT
INSLEN   EQU   *-INSTACK
         SPACE 1
         #DSECTS ASCB,CSOA,CSPL,CVT,ECT,PPL,PSA,RB,TCB,UPT
         END   ,
./ ADD NAME=USERINFO 0100-00053-00053-1317-00954-00954-00000-SOURCE
USERINFO TITLE '- PASS TSO USER INFORMATION TO CLIST VIA &&SYSCMD OR VI*
                A SYMBOLIC SYSTEM VARIABLES'
***********************************************************************
*                                                                     *
*   FUNCTION: PASS USER INFO TO CLIST VIA SYMBOLIC VARIABLES          *
*                                                                     *
*   THE SUPPORTED VARIABLES ARE:                                      *
*   (1) ACCT  - JOB CARD ACCOUNTING INFORMATION                       *
*   (2) CPID  - CPU SERIAL NUMBER                                     *
*   (3) DSSL  - DF/DSS RELEASE/VERSION                                *
*   (4) FMID  - SCP SMP FMID                                          *
*   (5) IPLD  - LAST IPL DATE                                         *
*   (6) IPLT  - LAST IPL TIME                                         *
*   (7) IPLU  - IPL SYSRES UNIT ADDRESS                               *
*   (8) IPLV  - IPL SYSRES VOLUME SERIAL                              *
*   (9) JOB#  - CURRENT JOB NUMBER (TSU 1234)                         *
*  (10) JOBN  - CURRENT JOB NAME                                      *
*  (11) MCAT  - MASTER CATALOG NAME                                   *
*  (12) MODE  - SYSTEM EXECUTION MODE (XA OR 370)                     *
*  (13) MODL  - CPU MODEL NUMBER (IE. 3083)                           *
*  (14) PGMR  - PROGRAMMER NAME                                       *
*  (15) REGN  - USER'S REQUESTED REGION SIZE                          *
*  (16) REL#  - SCP RELEASE/LEVEL NUMBER                              *
*  (17) SCPN  - SCP PRODUCT NAME                                      *
*  (18) SMFI  - SMF IDENTIFIER FOR THIS SYSTEM                        *
*  (19) SNAM  - SYSTEM IDENTIFIER                                     *
*  (20) SSNM  - SUBSYSTEM NAME (JES2)                                 *
*  (21) TERM  - TERMINAL TYPE                                         *
*  (22) TMID  - TERMINAL ID                                           *
*  (23) USER  - CURRENT USERID                                        *
*  (24) VERS  - USER SCP VERSION IDENTIFIER                           *
*  (25) WDAY  - DAY OF THE WEEK                                       *
*                                                                     *
*   THE SYMBOLS DECLARED IN IEASYMXX WILL ALSO BE CREATED.            *
*   IF ANY OF THESE SYMBOLS ARE DUPLICATES OF THE STANDARD "USERINFO" *
*   SYMBOLS, THE IEASYMXX SYMBOLS WILL PREVAIL.                       *
*                                                                     *
*   FUNCTION:                                                         *
*   THIS COMMAND IS USED TO OBTAIN USER & SYSTEM INFORMATION FROM     *
*   WITHIN A COMMAND PROCEDURE (CLIST).  SYSXXXX VARIABLES ARE        *
*   CREATED, AND IF AN OPTION IS SPECIFIED, THE CORRESPONDING         *
*   INFORMATION IS INSERTED INTO THE &SYSSCMD VARIABLE.  AS THE       *
*   &SYSSCMD VARIABLE REPRESENTS THE LAST SUBCOMMAND NAME, THE        *
*   VARIABLE'S INFORMATION MUST BE USED IMMEDIATELY AFTER INVOKING    *
*   USERINFO, OR COPIED TO ANOTHER VARIABLE OF THE USER'S CHOICE.     *
*                                                                     *
*   EXTERNAL ROUTINES: IKJCT441                                       *
*                                                                     *
***********************************************************************
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT 1
***********************************************************************
*                                                                     *
*   AN EXAMPLE OF A CLIST WHICH USES USERINFO:                        *
*                                                                     *
*   PROC 0                                                            *
*   USERINFO ACCT                                                     *
*   SET &A EQ &SYSSCMD                                                *
*   WRITE USERID &SYSUID - ACCOUNT:&SYSACCT TERMINAL:&SYSTMID         *
*                                                                     *
*   OR                                                                *
*                                                                     *
*   PROC 0                                                            *
*   USERINFO                                                          *
*   WRITE USERID &SYSUID ACCOUNT &SYSACCT                             *
*                                                                     *
*        #DD99252 - ALLOW FOR 4 BYTE UCB ADDRESS                      *
*        #DD99328 - ADD SYSTEM SYMBOLS DECLARED IN IEASYMXX           *
*        #DD00053 - ADD MASTER CATALOG NAME                           *
*                                                                     *
***********************************************************************
         EJECT 1
USERINFO #START BASE=(R12,R11),                                #DD00053*
               WKDSECT=USERWORK,SP=0,SPLEVEL=2,                #DD00053*
               REG1=R10,USING=(CPPL,R10,PSA,0),                #DD00053*
               APARS=,COPY=YES,COPYYR=1999,                            *
               LEVEL=V001R03                                   #DD00053
         L     R9,CPPLECT          LOAD ECT ADDRESS
         USING ECT,R9
         EJECT 1
*-------
*        INITIALIZE WORKAREA
*-------
         XC    CPECB,CPECB         CLEAR ECB
         LA    R15,GFPARMS         LOAD IKJEFF19 PLIST ADDRESS
         ST    R15,JEFF19@
         LA    R15,TSVEUPDT        LOAD ENTRY CODE
         ST    R15,ECODE
         LA    R15,ECODE           LOAD ENTRY CODE ADDRESS
         ST    R15,CVAREC@
         MVC   VARNAME,=CL7'SYSXXXX'
         LA    R15,VARNAME         LOAD VARIABLE NAME ADDRESS
         ST    R15,VARNAME@
         LA    R15,VARNAME@        LOAD VARIABLE NAME POINTER ADDRESS
         ST    R15,CVARNPT@
         MVC   NAMELEN,=F'7'       INITIALIZE VARIABLE NAME LENGTH
         LA    R15,NAMELEN         LOAD VARIABLE NAME LENGTH  ADDRESS
         ST    R15,CVARNLN@
         LA    R15,VARDATA         LOAD VARIABLE DATA ADDRESS
         ST    R15,VARDATA@
         LA    R15,VARDATA@        LOAD VARIABLE DATA POINTER ADDRESS
         ST    R15,CVARDPT@
         LA    R15,DATALEN         LOAD VARIABLE DATA LENGTH  ADDRESS
         ST    R15,CVARDLN@
         LA    R15,TOKEN           LOAD TOKEN ADDRESS
         O     R15,=X'80000000'    INDICATE END OF PARAMETER LIST
         ST    R15,CVARTKN@
         SPACE 1
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         L     R15,CVTTVT          LOAD TVT ADDRESS
         USING TSVT,R15
         L     R15,TSVTVACC        LOAD IKJCT441 ADDRESS
         DROP  R15
         ST    R15,CLISTRTN
         EJECT 1
*-------
*        PARSE THE CURRENT COMMAND BUFFER
*-------
         LA    R1,WRKPPL           LOAD PPL ADDRESS
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT      COPY UPT ADDRESS
         MVC   PPLECT,CPPLECT      COPY ECT ADDRESS
         MVC   PPLCBUF,CPPLCBUF    COPY COMMAND BUFFER ADDRESS
         LA    R15,CPECB           LOAD ECB ADDRESS
         ST    R15,PPLECB
         MVC   PPLPCL,=A(INFOPCL)  LOAD PCL ADDRESS
         LA    R15,CPANS           LOAD PDL ANSWER ADDRESS
         ST    R15,PPLANS
         DROP  R1
         CALLTSSR EP=IKJPARS,MF=(E,(R1))
         LTR   R15,R15             CHECK PARSE RETURN CODE
         BZ    USER0100            B. IF SUCCESSFUL
         #SETRC (R15)              SET RETURN CODE
         ST    R15,GFRCODE         STORE RETURN CODE
         MVC   GFCALLID,=AL2(GFPARSE)
         ST    R10,GFCPPLP         STORE CPPL ADDRESS          #DD00053
         LA    R15,CPECB
         ST    R15,GFECBP          STORE ECB ADDRESS
         LA    R1,JEFF19@          LOAD PLIST POINTER ADDRESS
         LINK  EP=IKJEFF19         CALL GENERAL FAIL ROUTINE
         B     USEREXIT
         EJECT 1
*-------
*        LOAD ADDRESS OF FIELD REQUESTED TO BE INSERTED INTO &SYSCMD
*-------
USER0100 DS    0H
         L     R3,CPANS            LOAD PDL ADDRESS
         USING IKJPARMD,R3
         L     R5,PARAM            LOAD PARAMETER ADDRESS
         LH    R6,PARAM+4          LOAD PARAMETER LENGTH
         EJECT 1
*-------
*        CREATE "SYSACCT" VARIABLE
*-------
         L     R15,PSATOLD         LOAD TCB  ADDRESS
         USING TCB,R15
         L     R14,TCBJSCB         LOAD JSCB ADDRESS
         USING IEZJSCB,R14
         L     R14,JSCBACT         LOAD ACTIVE JSCB ADDRESS
         L     R14,JSCBSSIB        LOAD SSIB ADDRESS
         USING SSIB,R14
         MVC   SYSJOB#,SSIBJBID    "JOB09999" / "TSU09999" / "STC09999"
         L     R14,TCBTCT          LOAD TCT  ADDRESS
         USING SMFTCT,R14
         L     R14,TCTLCTAD        LOAD LCT  ADDRESS
         USING LCT,R14
         L     R14,LCTJCTAD        LOAD JCT  ADDRESS
         USING JCT,R14
         MVC   SYSJOBN,JCTJNAME    SAVE JOB NAME
         MVC   SYSUSER,JCTUSER     SAVE USER IDENTIFIER
         TM    SYSUSER,X'BF'       CHECK IF "USER" FILLED IN
         BM    USER0199            B. IF YES
         MVC   SYSUSER,SYSJOBN     ELSE DEFAULT TO JOBNAME
USER0199 DS    0H
         SLR   R15,R15
         ICM   R15,7,JCTACTAD      LOAD ACT  ADDRESS
         USING IEFAACTB-16,R15
         MVC   SYSPGMR,ACTPRGNM    SAVE PROGRAMMER NAME
         SPACE 1
         MVC   SYSACCT,BLANKS
         LA    R1,ACTACCNT         LOAD ACCOUNTING FIELD ADDRESS
         MVI   SYSACCT,C'('        INSERT OPENNING BRACKET
         LA    R2,SYSACCT+1        POINT TO START OF OUTPUT BUFFER
         SLR   R4,R4
         ICM   R4,1,ACTJNFLD       CHECK IF ANY ACCOUNTING FIELDS EXIST
         BZ    USER0500            B. IF NOT
         B     USER0300
USER0200 DS    0H
         MVI   0(R2),C','          INSERT ACCOUNTING FIELD SEPARATOR
         LA    R2,1(,R2)           ADVANCE TO NEXT BYTE
USER0300 DS    0H
         SLR   R3,R3
         ICM   R3,1,0(R1)          LOAD LENGTH OF ACCOUNTING FIELD
         BZ    USER0400            B. IF ZERO LENGTH ACCOUNTING FIELD
         BCTR  R3,0                DECREMENT FOR EXECUTE
         #EXEC R3,MVC,0(*-*,R2),1(R1)
         LA    R1,2(R3,R1)         POINT TO NEXT ACCOUNTING FIELD
         LA    R2,1(R3,R2)         POINT TO NEXT OUTPUT AREA
USER0400 DS    0H
         BCT   R4,USER0200         PROCESS ALL ACCOUNTING FIELDS
USER0500 DS    0H
         MVI   0(R2),C')'          INSERT ENDING QUOTE
         LA    R2,1(,R2)           POINT TO END OF ACCOUNTING AREA
         LA    R1,SYSACCT          POINT TO START OF ACCOUNTING AREA
         SPACE 1
         SR    R2,R1               CALCULATE LENGTH OF ACCOUNTING FIELD
         LA    R1,SYSACCT          LOAD ADDRESS OF VARIABLE DATA
         LA    R15,=C'ACCT'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSCPID" VARIABLE
*-------
         L     R15,PSAPCCAV        LOAD PCCA ADDRESS
         USING PCCA,R15
         MVC   SYSCPID,PCCACPID    SAVE CPU SERIAL NUMBER
         DROP  R15
         LA    R1,SYSCPID          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSCPID        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'CPID'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSWDAY" VARIABLE
*-------
         #DATE SYSWDAY,FORMAT='DAYOFWEEK'
         LA    R1,SYSWDAY          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSWDAY        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'WDAY'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSFMID" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         LA    R0,256              LOAD PREFIX LENGTH
         SR    R15,R0              BACK UP TO START OF PREFIX
         USING CVTFIX,R15
         MVC   SYSFMID,CVTPRODI    SAVE SCP FMID
         DROP  R15
         LA    R1,SYSFMID          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSFMID        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'FMID'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSIPLD" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         ICM   R15,15,CVTSMCA      LOAD SMCA ADDRESS
         BZ    USER0600            B. IF NOT FOUND
         USING SMCABASE,R15
         L     R0,SMCAIDTE         INSERT LAST IPL DATE (YYDDDF)
         DROP  R15
         #DATE SYSIPLD,FORMAT='MM/DD/CCYY',FROM=(R0)
USER0600 DS    0H
         LA    R1,SYSIPLD          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSIPLD        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'IPLD'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSIPLT" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         ICM   R15,15,CVTSMCA      LOAD SMCA ADDRESS
         BZ    USER0700            B. IF NOT FOUND
         USING SMCABASE,R15
         L     R1,SMCAITME         INSERT LAST IPL TIME (BINARY)
         DROP  R15
         LA    R14,SYSIPLT         LOAD OUTPUT BUFFER ADDRESS
         SLR   R0,R0
         D     R0,=A(100)          R0=HUNDREDTHS OF SECONDS
         CVD   R0,USEDBL1
         UNPK  9(2,R14),USEDBL1
         SLR   R0,R0
         D     R0,=A(60)           R0=SECONDS
         CVD   R0,USEDBL1
         UNPK  6(2,R14),USEDBL1
         SLR   R0,R0
         D     R0,=A(60)           R0=MINUTES
         CVD   R0,USEDBL1
         UNPK  3(2,R14),USEDBL1
         CVD   R1,USEDBL1          R1=HOURS
         UNPK  0(2,R14),USEDBL1
         OC    0(11,R14),=C'00:00:00.00'
USER0700 DS    0H
         LA    R1,SYSIPLT          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSIPLT        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'IPLT'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSIPLU" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         #MODE 31                  GET INTO 31 BIT AMODE       #DD99252
         L     R15,CVTSYSAD        LOAD SYSRES UCB ADDRESS
         USING UCBOB,R15
         UNPK  WRKDBL1(5),UCBCHAN(3)                           #DD99252
         TR    WRKDBL1(4),TRTABLE3-C'0'                        #DD99252
         MVC   SYSIPLU,WRKDBL1     COPY UNIT ADDRESS (4 BYTES) #DD99252
         #MODE 24                  RETURN TO AMODE 24          #DD99252
         DROP  R15
         LA    R1,SYSIPLU          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSIPLU        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'IPLU'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSIPLV" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         L     R15,CVTSYSAD        LOAD SYSRES UCB ADDRESS
         USING UCBOB,R15
         MVC   SYSIPLV,UCBVOLI     SAVE VOLUME SERIAL NUMBER
         DROP  R15
         LA    R1,SYSIPLV          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSIPLV        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'IPLV'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSJOB#" VARIABLE
*-------
         LA    R1,SYSJOB#          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSJOB#        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'JOB#'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSJOBN" VARIABLE
*-------
         LA    R1,SYSJOBN          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSJOBN        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'JOBN'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------                                                       #DD00053
*        CREATE "SYSMCAT" VARIABLE                             #DD00053
*-------                                                       #DD00053
         XC    @CTGPL,@CTGPL       ZERO PARAMETER LIST         #DD00053
         #CTGPL LISTCAT,                                       #DD00053*
               CRI=DUMMYPRE,                                   #DD00053*
               WORK=(WRKAREA,256),                             #DD00053*
               OPTIONS=(NAME,GENLD,RCATN,SUPLT,AM0),           #DD00053*
               MF=(E,@CTGPL)                                   #DD00053
         CATALOG @CTGPL                                        #DD00053
         LTR   R15,R15             CHECK IF DATA RETURNED      #DD00053
         BZ    USER0710            B. IF SUCCESSFUL            #DD00053
         MVC   SYSMCAT,=CL44'UNKNOWN'                          #DD00053
         B     USER0720                                        #DD00053
         SPACE 1
USER0710 DS    0H                                              #DD00053
         MVC   SYSMCAT,WRKAREA+5   COPY MASTER CATALOG NAME    #DD00053
         SPACE 1
USER0720 DS    0H                                              #DD00053
         LA    R1,SYSMCAT          LOAD ADDRESS OF VARIABLE    #DD00053
         LA    R2,L'SYSMCAT        LOAD LENGTH OF VARIABLE     #DD00053
         LA    R15,=C'MCAT'        LOAD VARIABLE SUFFIX        #DD00053
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE#DD00053
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME"    #DD00053
         SPACE 1
         EJECT 1
*-------
*        CREATE "SYSMODE" VARIABLE
*-------
         LA    R2,3                LOAD LENGTH OF VARIABLE DATA
         MVC   SYSMODE,=CL9'370'   INDICATE 370 SYSTEM
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         TM    CVTDCB,CVTMVSE      CHECK FOR XA OR ESA SYSTEM
         BNO   USER0800            B. IF NOT
         LA    R2,3                LOAD LENGTH OF VARIABLE DATA
         MVC   SYSMODE,=CL9'ESA'   INDICATE ESA SYSTEM
         TM    CVTDCB,X'08'        CHECK FOR ESA SYSTEM
         BO    USER0750            B. IF YES
         LA    R2,2                LOAD LENGTH OF VARIABLE DATA
         MVC   SYSMODE,=CL9'XA'    INDICATE XA SYSTEM
         B     USER0800
USER0750 DS    0H
         TM    CVTOSLV1,X'02'      CHECK FOR "OS/390" SYSTEM
         BZ    USER0800            B. IF NOT
         #MODE 31                  GET INTO 31 BIT AMODE       #DD99218
         L     R15,CVTECVT         A(E-CVT)                    #DD99218
         USING ECVT,R15                                        #DD99218
         MVC   SYSMODE(8),=CL8'OS/390 R'                       #DD99218
         LA    R1,SYSMODE+8                                    #DD99218
         SPACE 1
         MVC   0(1,R1),ECVTPVER+1                              #DD99218
         CLI   ECVTPVER,C'0'       LEADING ZERO?               #DD99218
         BE    USER0760            B. IF YES                   #DD99218
         MVC   0(2,R1),ECVTPVER                                #DD99218
         LA    R1,1(,R1)                                       #DD99218
USER0760 DS    0H                                              #DD99218
         LA    R1,1(,R1)                                       #DD99218
         MVI   0(R1),C'.'                                      #DD99218
         LA    R1,1(,R1)                                       #DD99218
         SPACE 1
         MVC   0(1,R1),ECVTPREL+1                              #DD99218
         CLI   ECVTPREL,C'0'       LEADING ZERO?               #DD99218
         BE    USER0770            B. IF YES                   #DD99218
         MVC   0(2,R1),ECVTPREL                                #DD99218
         LA    R1,1(,R1)                                       #DD99218
USER0770 DS    0H                                              #DD99218
         LA    R1,1(,R1)                                       #DD99218
         MVI   0(R1),C'.'                                      #DD99218
         LA    R1,1(,R1)                                       #DD99218
         SPACE 1
         MVC   0(1,R1),ECVTPMOD+1                              #DD99218
         CLI   ECVTPMOD,C'0'       LEADING ZERO?               #DD99218
         BE    USER0780            B. IF YES                   #DD99218
         MVC   0(2,R1),ECVTPMOD                                #DD99218
         LA    R1,1(,R1)                                       #DD99218
USER0780 DS    0H                                              #DD99218
         LA    R1,1(,R1)                                       #DD99218
         DROP  R15                                             #DD99218
         SPACE 1
         #MODE 24                  RETURN TO AMODE 24          #DD99218
         LR    R2,R1               A(END OF &SYSMODE)          #DD99218
         LA    R1,SYSMODE          A(START OF &SYSMODE)        #DD99218
         SR    R2,R1               CALCULATE LENGTH            #DD99218
USER0800 DS    0H
         LA    R1,SYSMODE          LOAD ADDRESS OF VARIABLE DATA
         LA    R15,=C'MODE'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSMODL" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         LA    R0,6
         SR    R15,R0              LOAD CVTMDL ADDRESS
         UNPK  SYSMODL(5),0(3,R15) CONVERT MODEL NUMBER TO DECIMAL
         TR    SYSMODL(4),TRTABLE3-C'0'
         LA    R1,SYSMODL          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,4                LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'MODL'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSPGMR" VARIABLE
*-------
         LA    R1,SYSPGMR          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSPGMR        LOAD DEFAULT LENGTH OF VARIABLE
         CLC   SYSPGMR,BLANKS      CHECK FOR NULL PROGRAMMER NAME
         BE    USER1100            B. IF YES
         LA    R3,L'SYSPGMR-1(,R1) POINT TO END OF VARIABLE
USER0900 DS    0H
         TM    0(R3),X'BF'         CHECK FOR REAL END OF VARIABLE
         BM    USER1000            B. IF YES
         BCT   R3,USER0900         BACK UP 1 BYTE & CONTINUE
USER1000 DS    0H
         LA    R2,1(,R3)           POINT TO END OF PROGRAMMER NAME
         SR    R2,R1               CALCULATE REAL LENGTH OF VARIABLE
USER1100 DS    0H
         LA    R15,=C'PGMR'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSREGN" VARIABLE
*-------
         #MODE 31                  GET INTO 31 BIT AMODE
         L     R15,PSAAOLD         LOAD ASCB ADDRESS
         USING ASCB,R15
         L     R15,ASCBLDA         LOAD LDA  ADDRESS
         USING LDA,R15
         L     R15,LDAREGRQ        LOAD REQUESTED REGION SIZE
         SRL   R15,10              DIVIDE BY 1024
         CVD   R15,USEDBL1         CONVERT TO DECIMAL
         #MODE 24                  RETURN TO AMODE 24
         DROP  R15
         MVC   SYSREGN,=X'40402020202120D2'
         ED    SYSREGN+1(6),USEDBL1+5
         LA    R1,SYSREGN          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSREGN        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'REGN'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSSCPN" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         LA    R0,256              LOAD PREFIX LENGTH
         SR    R15,R0              BACK UP TO START OF PREFIX
         USING CVTFIX,R15
         MVC   SYSSCPN,CVTPRODN    SAVE SCP NAME
         DROP  R15
         LA    R1,SYSSCPN          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSSCPN        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'SCPN'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSDSSL" VARIABLE
*-------
         ADRMCLVL ,                GET DF/DSS LEVEL
         SLL   R1,8                SHIFT LEVEL
         ST    R1,USEDBL1          SAVE CURRENT LEVEL
         UNPK  USEDBL2,USEDBL1(4)
         MVC   SYSDSSL,=C'V#R##.#'
         MVC   SYSDSSL+1(1),USEDBL2+2 INSERT VERSION
         MVC   SYSDSSL+3(2),USEDBL2+3 INSERT RELEASE
         MVC   SYSDSSL+6(1),USEDBL2+5 INSERT LEVEL
         LA    R1,SYSDSSL          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSDSSL        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'DSSL'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSREL#" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         LA    R0,256              LOAD PREFIX LENGTH
         SR    R15,R0              BACK UP TO START OF PREFIX
         USING CVTFIX,R15
         MVC   SYSREL#(2),CVTNUMB  SAVE SCP RELEASE NUMBER
         MVI   SYSREL#+2,C'.'
         MVC   SYSREL#+3(2),CVTLEVL     SCP RELEASE LEVEL
         DROP  R15
         LA    R1,SYSREL#          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSREL#        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'REL#'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSSMFI" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         MVC   SYSSMFI,=CL4'????'  INSERT UNKNOWN IDENTIFIER
         ICM   R15,15,CVTSMCA      LOAD SMCA ADDRESS
         BZ    USER1200            B. IF NOT FOUND
         USING SMCABASE,R15
         MVC   SYSSMFI,SMCASID     INSERT SMF SYSTEM IDENTIFIER
         DROP  R15
USER1200 DS    0H
         LA    R1,SYSSMFI          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSSMFI        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'SMFI'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSSNAM" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVTMAP,R15
         MVC   SYSSNAM,CVTSNAME    SAVE SYSTEM NAME
         DROP  R15
         LA    R1,SYSSNAM          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSSNAM        LOAD DEFAULT LENGTH OF VARIABLE
         LA    R15,=C'SNAM'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSSSNM" VARIABLE
*-------
         L     R15,PSATOLD         LOAD TCB ADDRESS
         USING TCB,R15
         L     R15,TCBJSCB         LOAD JSCB ADDRESS
         USING IEZJSCB,R15
         L     R15,JSCBACT         LOAD ACTIVE JSCB ADDRESS
         L     R15,JSCBSSIB        LOAD DEFAULT SSIB ADDRESS
         USING SSIB,R15
         SPACE 1
         MVC   SYSSSNM,SSIBSSNM    SAVE CURRENT SUBSYSTEM NAME
         DROP  R15
         LA    R1,SYSSSNM          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSSSNM        LOAD DEFAULT LENGTH OF VARIABLE
         LA    R15,=C'SSNM'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSTERM" VARIABLE
*-------
         #TSOTERM ,                GET CURRENT TERMINAL TYPE
         ST    R15,SYSTERM         SAVE TERMINAL/LINE INFORMATION
         SPACE 1
         LA    R7,TYPETABL         LOAD TERMINAL TABLE START ADDRESS
         LA    R1,NRENT            LOAD NUMBER OF ENTRIES IN TABLE
USER1300 DS    0H
         CLC   0(1,R7),SYSTERM+3   CHECK FOR MATCHING TERMINAL TYPE
         BE    USER1400            B. IF YES
         LA    R7,5(,R7)           LOAD ADDRESS OF NEXT TABLE ENTRY
         BCT   R1,USER1300         LOOP THROUGH TERMINAL TYPE TABLE
         SPACE 1
USER1400 DS    0H
         LA    R1,1(,R7)           LOAD ADDRESS OF VARIABLE DATA
         LA    R2,4                LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'TERM'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSTMID" VARIABLE
*-------
         LA    R2,1                LOAD DEFAULT VARIABLE LENGTH
         LA    R3,BLANKS           LOAD DEFAULT VARIABLE VALUE
         ICM   R15,15,PSAAOLD      LOAD ASCB ADDRESS
         BZ    USER1500
         USING ASCB,R15
         ICM   R15,15,ASCBASXB     LOAD ASXB ADDRESS
         BZ    USER1500
         USING ASXB,R15
         ICM   R15,15,ASXBSENV     LOAD ACEE ADDRESS
         BZ    USER1500
         USING ACEE,R15
         LA    R2,8                LOAD LENGTH OF VARIABLE DATA
         LA    R3,ACEETRID         LOAD TERMINAL ID ADDRESS
         DROP  R15
USER1500 DS    0H
         LR    R1,R3               LOAD ADDRESS OF VARIABLE DATA
         LA    R15,=C'TMID'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSUSER" VARIABLE
*-------
         LA    R1,SYSUSER          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSUSER        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'USER'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE "SYSVERS" VARIABLE
*-------
         L     R15,CVTPTR          LOAD CVT ADDRESS
         LA    R0,256              LOAD PREFIX LENGTH
         SR    R15,R0              BACK UP TO START OF PREFIX
         USING CVTFIX,R15
         MVC   SYSVERS,CVTVERID    SAVE USER SCP VERSION IDENTIFIER
         DROP  R15
         LA    R1,SYSVERS          LOAD ADDRESS OF VARIABLE DATA
         LA    R2,L'SYSVERS        LOAD LENGTH OF VARIABLE DATA
         LA    R15,=C'VERS'        LOAD VARIABLE SUFFIX
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE
         SPACE 1
         BAL   R14,ECTUPDTE        UPDATE "SUBCOMMAND NAME" IF REQ'D
         EJECT 1
*-------
*        CREATE VARIABLES FOR SYMBOLS DECLARED IN IEASYMXX
*-------
         #MODE 31                  GET INTO 31 BIT AMODE       #DD99328
         L     R5,CVTPTR           A(CVT)                      #DD99328
         USING CVT,R5                                          #DD99328
         ICM   R5,15,CVTECVT       A(ECVT)                     #DD99328
         BZ    USER1900            B. IF NOT AVAILABLE         #DD99328
         USING ECVT,R5                                         #DD99328
         ICM   R5,15,ECVTSYMT      A(SYMBOL TABLE)             #DD99328
         BZ    USER1900            B. IF NONE DEFINED          #DD99328
         USING SYMBT,R5                                        #DD99328
         SLR   R7,R7                                           #DD99328
         ICM   R7,3,SYMBTNUMBEROFSYMBOLS  NUMBER OF SYMBOLS    #DD99328
         BZ    USER1900            B. IF NONE DEFINED          #DD99328
         LA    R6,SYMBTTABLEENTRIES A(START OF SYMBOL ENTRIES) #DD99328
         LR    R3,R6               A(CURRENT SYMBOL ENTRY)     #DD99328
         USING SYMBTE,R3                                       #DD99328
USER1600 DS    0H                                              #DD99328
         LM    R1,R2,SYMBTESYMBOLOFFSET O(NAME) & L(NAME)      #DD99328
         LA    R1,0(R1,R6)         A(IEASYMXX SYMBOL NAME)     #DD99328
         CLI   0(R1),C'&&'         CHECK FOR LEADING "&"       #DD99328
         BNE   USER1700            B. IF NOT                   #DD99328
         LA    R1,1(,R1)           ADVANCE PAST "&"            #DD99328
         BCTR  R2,0                SUBTRACT 1 FROM NAME LENGTH #DD99328
USER1700 DS    0H                                              #DD99328
         LA    R15,0(R2,R1)        A(END OF SYMBOL NAME)       #DD99328
         BCTR  R15,0               BACK UP TO LAST BYTE        #DD99328
         CLI   0(R15),C'.'         CHECK FOR TRAILING "."      #DD99328
         BNE   USER1800            B. IF NOT                   #DD99328
         BCTR  R2,0                SUBTRACT 1 FROM NAME LENGTH #DD99328
USER1800 DS    0H                                              #DD99328
         MVC   SYSSYMBN,BLANKS     CLEAR SYMBOL NAME AREA      #DD99328
         #EXEC -R2,MVC,SYSSYMBN(*-*),0(R1)    COPY SYMBOL NAME #DD99328
         LM    R1,R2,SYMBTESUBTEXTOFFSET  O(VALUE) & L(VALUE)  #DD99328
         LA    R1,0(R1,R6)         A(IEASYMXX SYMBOL VALUE)    #DD99328
         #EXEC -R2,MVC,SYSSYMBV(*-*),0(R1)    COPY SYMBOL NAME #DD99328
         SPACE 1
         #MODE 24                  RETURN TO AMODE 24          #DD99328
         LA    R1,SYSSYMBV         A(IEASYMXX SYMBOL VALUE)    #DD99328
         LA    R2,L'SYSSYMBV       L(IEASYMXX SYMBOL VALUE)    #DD99328
         LA    R15,SYSSYMBN        A(IEASYMXX SYMBOL NAME)     #DD99328
         OI    WRKFLAG1,FULLNAME   INDICATE 8 BYTE SYMBOL NAME #DD99328
         BAL   R14,TSOCVAR         CREATE/MODIFY CLIST VARIABLE#DD99328
         #MODE 31                  GET INTO 31 BIT AMODE       #DD99328
         SPACE 1
         LA    R3,SYMBTE_LEN(,R3)  A(NEXT SYMBOL TABLE ENTRY)  #DD99328
         BCT   R7,USER1600         LOOP THROUGH SYMBOL TABLE   #DD99328
         SPACE 1
USER1900 DS    0H                                              #DD99328
         #MODE 24                  RETURN TO AMODE 24          #DD99328
         EJECT 1
*-------
*        TERMINATE
*-------
USEREXIT #STOP ,                   TERMINATE
         EJECT 1
*---------------------------------------------------------------------*
*                                                                     *
*        CREATE CLIST VARIABLES                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         PUSH  USING                                           #DD99333
TSOCVAR  DS    0H
         STM   R0,R15,SAVEREGS     SAVE ALL REGISTERS
         L     R6,CPANS            LOAD PDL ADDRESS            #DD99333
         USING IKJPARMD,R6                                     #DD99333
         SPACE 1
         MVC   VARNAME,0(R15)      INSERT "FULL" SYMBOL NAME   #DD99328
         TM    WRKFLAG1,FULLNAME   CHECK IF FULL NAME PROVIDED #DD99328
         BO    TSOC0100            B. IF YES                   #DD99328
         MVC   VARNAME,BLANKS                                  #DD99328
         MVC   VARNAME(3),=CL3'SYS'                            #DD99328
         MVC   VARNAME+3(4),0(R15) COMPLETE VARIABLE NAME (&SYSXXXX)
         SPACE 1
TSOC0100 DS    0H                                              #DD99328
         CLC   PREFIX+4(2),=AL2(3) CHECK FOR VALID PREFIX LEN. #DD99333
         BNE   TSOC0150            B. IF NOT                   #DD99333
         ICM   R3,15,PREFIX        A(VARIABLE PREFIX)          #DD99333
         BZ    TSOC0150            B. IF NO "PREFIX(...)"      #DD99333
         MVC   VARNAME(3),0(R3)    COPY USER SPECIFIED PREFIX  #DD99333
TSOC0150 DS    0H                                              #DD99333
         LA    R15,VARNAME+L'VARNAME-1                         #DD99333
TSOC0160 DS    0H                                              #DD99333
         TM    0(R15),X'BF'        CHECK FOR END OF NAME       #DD99333
         BM    TSOC0170            B. IF FOUND                 #DD99333
         BCT   R15,TSOC0160                                    #DD99333
TSOC0170 DS    0H                                              #DD99333
         LA    R15,1(,R15)         A(END OF NAME)              #DD99333
         LA    R14,VARNAME         A(START OF NAME)            #DD99333
         SR    R15,R14             CALCULATE NAME LENGTH       #DD99333
         ST    R15,NAMELEN         SET VARIABLE NAME LENGTH    #DD99333
         MVC   VARDATA,BLANKS      INITIALIZE DATA AREA
         #EXEC -R2,MVC,VARDATA(*-*),0(R1)
         LA    R2,1(,R2)
         SPACE 1
         LR    R14,R2              COPY LENGTH OF DATA
TSOC0200 DS    0H
         TM    VARDATA,X'BF'       CHECK FOR BLANK OR ZERO
         BNZ   TSOC0300            B. IF NOT
         MVC   VARDATA,VARDATA+1   SHIFT VARIABLE DATA OVER 1 BYTE
         BCT   R14,TSOC0200        LOOP TILL AT 1ST BYTE OF DATA
         SPACE 1
TSOC0300 DS    0H
         LA    R15,VARDATA(R2)     POINT PAST LAST BYTE IN VARIABLE
TSOC0400 DS    0H
         BCTR  R15,0               BACK UP 1 BYTE
         TM    0(R15),X'BF'        CHECK FOR BLANK OR ZERO
         BNZ   TSOC0500            B. IF NOT
         BCT   R2,TSOC0400         LOOP TILL AT 1ST BYTE OF DATA
         SPACE 1
TSOC0500 DS    0H
         ST    R2,DATALEN          SAVE LENGTH OF VARIABLE DATA
         SPACE 1
         LA    R1,CVARPARM         LOAD ADDRESS OF IKJCT441 PLIST
         L     R15,CLISTRTN        LOAD IKJCT441 ADDRESS
         BALR  R14,R15             INVOKE IKJCT441
         SPACE 1
         NI    WRKFLAG1,255-FULLNAME                           #DD99328
         LM    R0,R15,SAVEREGS     RESTORE ALL REGISTERS
         BR    R14                 RETURN TO CALLER
         POP   USING                                           #DD99333
         EJECT 1
*---------------------------------------------------------------------*
*        UPDATE SUBCOMMAND NAME WITH SPECIFIED VARIABLE               *
*---------------------------------------------------------------------*
ECTUPDTE DS    0H
         CLC   0(4,R5),VARNAME+3   CHECK IF CURRENT VARIABLE SPECIFIED
         BNER  R14                 B. IF NOT
         SPACE 1
         STM   R0,R15,SAVEREGS     SAVE ALL REGISTERS
         CL    R2,=F'8'            CHECK IF VARIABLE LENGTH > 8
         BNH   *+8                 B. IF NOT
         LA    R2,8                SET MAXIMUM LENGTH
         BCTR  R2,0
         MVC   ECTSCMD,BLANKS      CLEAR &SYSSCMD BUFFER
         #EXEC R2,MVC,ECTSCMD(*-*),0(R1)
         LM    R0,R15,SAVEREGS     RESTORE ALL REGISTERS
         BR    R14                 RETURN TO CALLER
         EJECT 1
*---------------------------------------------------------------------*
*        CONSTANTS & TABLES                                           *
*---------------------------------------------------------------------*
BLANKS   DC    CL256' '            SOME BLANKS
DUMMYPRE DC    AL1(12),CL44'SYS1.INVALID'                      #DD00053
         SPACE 1
TYPETABL DC    X'00',C'????'       TERMINAL TYPE TABLE
         DC    X'04',C'3270'
         DC    X'08',C'5041'
         DC    X'0C',C'2741'
         DC    X'10',C'TWX '
         DC    X'14',C'1050'
NRENT    EQU   (*-TYPETABL)/5      NUMBER OF TERMINAL TYPES
         DC    X'00',C'    '       LAST TERMINAL TYPE (IF NOT FOUND)
         SPACE 1
TRTABLE1 DC    256X'00'
         ORG   TRTABLE1+C' '       FIND BLANKS
         DC    X'01'
         ORG   TRTABLE1+C'-'       FIND DASHES
         DC    X'02'
         ORG   ,
TRTABLE2 DC    256X'00'
         ORG   TRTABLE2+C' '       FIND BLANKS
         DC    X'01'
         ORG   ,
TRTABLE3 DC    C'0123456789ABCDEF' HEX TRANSLATION TABLE
         EJECT 1
         #STARTWA PATCH=NO
         IKJEFFGF GFDSECT=NO       IKJEFF19 PARAMETER LIST
WRKDBL1  DS    D                   DOUBLEWORD WORK AREA        #DD99252
WRKFLAG1 DS    X                   FLAG BYTE #1                #DD99328
FULLNAME EQU   BIT0   1... ....    - DON'T PREFIX SYMBOL NAME  #DD99328
WRKAREA  DS    0F,CL256            CATALOG WORKAREA BUFFER     #DD00053
@CTGPL   DS    XL(CTGPLLEN)        CATALOG PARAMETER LIST      #DD00053
SYSACCT  DS    CL146               ACCOUNTING INFORMATION
SYSCPID  DS    CL12                CPU SERIAL NUMBER
SYSDSSL  DS    CL7                 DF/DSS LEVEL
SYSWDAY  DS    CL9                 DAY OF THE WEEK
SYSFMID  DS    CL8                 SCP FMID
SYSIPLD  DS    CL10                LAST IPL DATE (MM/DD/CCYY)
SYSIPLT  DS    CL11                LAST IPL TIME (HH:MM:SS.HT)
SYSIPLU  DS    CL4                 SYSRES UNIT ADDRESS         #DD99252
SYSIPLV  DS    CL6                 SYSRES VOLUME SERIAL NUMBER
SYSJOB#  DS    CL8                 JOB NUMBER
SYSJOBN  DS    CL8                 JOB NAME
SYSMCAT  DS    CL44                MASTER CATALOG NAME         #DD00053
SYSMODE  DS    CL9,CL10            PROCESSING MODE
SYSMODL  DS    CL8                 PROCESSOR MODEL NUMBER
SYSPGMR  DS    CL(L'ACTPRGNM)      PROGRAMMER NAME             #DD99328
SYSREGN  DS    CL8                 REQUESTED REGION SIZE
SYSSCPN  DS    CL8                 SCP PRODUCT NAME
SYSREL#  DS    CL5                 SCP RELEASE NUMBER
SYSSMFI  DS    CL4                 SMF IDENTIFIER
SYSSSNM  DS    CL(L'SSIBSSNM)      SUBSYSTEM NAME
SYSSYMBN DS    CL8                 IEASYMXX SYMBOL NAME        #DD99328
SYSSYMBV DS    CL254               IEASYMXX SYMBOL VALUE       #DD99328
SYSSNAM  DS    CL8                 SYSTEM IDENTIFIER
SYSTERM  DS    F                   TERMINAL TYPE
SYSTMID  DS    CL8                 TERMINAL IDENTIFIER
SYSUSER  DS    CL(L'JCTUSER)       USER IDENTIFIER
SYSVERS  DS    CL16                USER SCP VERSION IDENTIFIER
CLISTRTN DS    A                   IKJCT441 ADDRESS
CPANS    DS    A                   RETURNED PDL BUFFER ADDRESS
CPECB    DS    A                   COMMAND PROCESSOR ECB
CVARPARM DS    0F                  IKJCT441 PARAMETER LIST
CVAREC@  DS    A                   ENTRY CODE ADDRESS
CVARNPT@ DS    A                   VARIABLE NAME POINTER ADDRESS
CVARNLN@ DS    A                   VARIABLE NAME LENGTH  ADDRESS
CVARDPT@ DS    A                   VARIABLE DATA POINTER ADDRESS
CVARDLN@ DS    A                   VARIABLE DATA LENGTH  ADDRESS
CVARTKN@ DS    A                   TOKEN ADDRESS
DATALEN  DS    F                   VARIABLE DATA LENGTH
ECODE    DS    A                   CLIST VARIABLE ROUTINE ENTRY CODE
JEFF19@  DS    F                   IKJEFF19 PARAMETER LIST ADDRESS
NAMELEN  DS    F                   VARIABLE NAME LENGTH
SAVEREGS DS    16F                 REGISTER SAVE AREA
TOKEN    DS    A                   TOKEN USED BY IKJCT441
VARDATA  DS    CL256               VARIABLE DATA BUFFER
VARDATA@ DS    A                   VARIABLE DATA POINTER
VARNAME  DS    CL8                 VARIABLE NAME "SYSXXXX"
VARNAME@ DS    A                   VARIABLE NAME POINTER
WRKPPL   DS    7F                  PARSE PARAMETER LIST
         #STOPWA ,
         EJECT 1
         PRINT NOGEN
         SPACE 1
INFOPCL  IKJPARM
PARAM    IKJIDENT 'USER INFORMATION FUNCTION',MAXLNTH=4,               *
               DEFAULT='SETV',                                         *
               HELP='ACCT, IPLD, IPLT, IPLU, IPLV, JOB#, JOBN, MODE, MO*
               DL, PGMR, TERM, TMID OR USER'
         SPACE 1
PREFKW   IKJKEYWD DEFAULT='PREFIX(SYS)'                        #DD99333
         IKJNAME 'PREFIX',SUBFLD=PREFSUB                       #DD99333
PREFSUB  IKJSUBF ,                                             #DD99333
PREFIX   IKJIDENT 'PREFIX',OTHER=ALPHANUM,MAXLNTH=3,           #DD99333X
               PROMPT='VARIABLE PREFIX'                        #DD99333
         IKJENDP
         SPACE 1
         IKJTSVT ,            MAP TSO VECTOR TABLE
         IHAACEE ,
         IHAPCCA ,
         ASASYMBP DSECT=YES,SYMBP=NO,SYMBT=YES                 #DD99328
         #DSECTS ACT,ASCB,ASXB,CVT,ECT,ECVT,JCT,LCT,LDA,PSA,SMCA,SSIB, *
               TCB,TCT,TSO,UCB                                 #DD99328
         #DSECTS CATLG                                         #DD00053
         END   ,
./ ADD NAME=WHOSGOT  0100-00040-00040-1222-00473-00473-00000-SOURCE
WHOSGOT  TITLE 'WHOSGOT - DISPLAY DATASET(S) ENQ STATUS'
*---------------------------------------------------------------------*
* MODNAME     : WHOSGOT                                               *
*                                                                     *
* FUNCTION    : THIS MODULE USES GQSCAN TO DISPLAY THE ENQ STATUS OF  *
*               A SPECIFIED DATASET(S) .                              *
*                                                                     *
* ENTRY  PARMS: R1 -> CPPL                                            *
* EXIT PARMS  : ENQ STATUS DISPLAYED                                  *
*                                                                     *
* REGS USED   : R0  -> WORK                                           *
*               R1  -> WORK                                           *
*               R2  -> CPPL, WORK                                     *
*               R3  -> PDL, PDE, WORK                                 *
*               R4  -> WORK                                           *
*               R5  -> WORK                                           *
*               R6  -> NOT USED                                       *
*               R7  -> NOT USED                                       *
*               R8  -> NOT USED                                       *
*               R9  -> NOT USED                                       *
*               R10 -> NOT USED                                       *
*               R11 -> 1ST BASE REG                                   *
*               R12 -> NOT USED                                       *
*               R13 -> REGISTER SAVE AREA AND WORK AREA               *
*               R14 -> WORK                                           *
*               R15 -> WORK                                           *
*                                                                     *
* ATTRIBUTES : RENT                                                   *
*                                                                     *
* MESSAGES ISSUED : GRS300I                                           *
*                   GRS301I                                           *
*                   GRS302I                                           *
*                   GRS303I                                           *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*                                                                     *
*         (C)  COPYRIGHT 2000 MVS-JES2@HOME.COM                       *
*                   ALL RIGHTS RESERVED                               *
*                                                                     *
*         LICENSED MATERIAL-PROGRAM, PROPERTY OF MVS-JES2@HOME.COM    *
*                                                                     *
*         ALL RIGHTS RESERVED BY MVS-JES2@HOME.COM. NO PART OF THIS   *
*         PROGRAM MAY BE REPRODUCED, STORED IN A RETRIEVAL            *
*         SYSTEM OR TRANSMITTED IN ANY FORM OR BY ANY MEANS,          *
*         INCLUDING PHOTOCOPYING, RECORDING, ELECTRONIC,              *
*         MECHANICAL OR OTHERWISE WITHOUT THE WRITTEN CONSENT         *
*         OF THE COPYRIGHT HOLDER.                                    *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
* HISTORY :                                                           *
*                                                                     *
***********************************************************************
         EJECT 1
         PUNCH ' ALIAS WG'
         SPACE 1
WHOSGOT  #START BASE=R11,WKDSECT=WRKWG,SPLEVEL=2,REG1=R2,              X
               USING=(CPPL,R2),                                        X
               APARS=,COPY=YES,COPYYR=1999,                            X
               LEVEL=#V001R02                                  #DD99281
         EJECT 1
*-------
*        BUILD I/O SERVICE ROUTINE PARAMETER LIST
*-------
         SPACE 1
         LA    R1,WIOPL                -> IOPL IN WORKAREA
         USING IOPL,R1
         MVC   IOPLUPT,CPPLUPT         COPY UPT ADDR
         MVC   IOPLECT,CPPLECT         COPY ECT ADDR
         LA    R0,WORKECB              -> ECB IN WORKAREA
         ST    R0,IOPLECB              PUT IN IOPL
         EJECT 1
*-------
*        BUILD PARSE PARAMETER LIST AND LINK TO IKJPARS TO PARSE
*        ANY OPERANDS.
*-------
         SPACE 1
         LA    R1,WORKPPL              A(PPL IN WORKAREA)
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT          COPY ADDRESS OF UPT
         MVC   PPLECT,CPPLECT          COPY ADDRESS OF ECT
         MVC   PPLCBUF,CPPLCBUF        COPY ADDRESS OF COMMAND BUFFER
         LA    R0,WORKPDLP             A(PDL POINTER IN WORKAREA)
         ST    R0,PPLANS               PUT IN PPL
         LA    R0,WORKECB              A(ECB IN WORKAREA)
         ST    R0,PPLECB               PUT IN PPL
         L     R0,=A(WGPCL)            -> PCL
         ST    R0,PPLPCL              PUT IN PPL
         ST    R13,PPLUWA              PASS ADDR OF WORKAREA
         CALLTSSR EP=IKJPARS,MF=(E,(1)) PARSE THE COMMAND BUFFER
         LTR   R15,R15                 PARSE O.K.?
         BNZ   WGEXIT                  TERMINATE IF NOT
         DROP  R2
         EJECT 1
*-------
*        OBTAIN STORAGE FOR GQSCAN AREA.
*-------
         SPACE 1
         MVC   WGETMAIN,GETMAIN        COPY MF=L TO WORKAREA
         GETMAIN VU,A=VGETAREA,MF=(E,WGETMAIN)
         SPACE 1
*-------
*        COPY INFORMATION TO WORKAREA.
*-------
         SPACE 1
         MVC   WPTPB,PTPB              COPY MF=L TO WORKAREA
         L     R3,WORKPDLP             PICK UP ADDR OF PDL
         USING WGPDL,R3
         LA    R3,WGDSNAME             -> DSN PDE
         USING WGDSNAME,R3
SCANLOOP DS    0H
         L     R1,WGDSNAME             PICK UP ADDR OF CHAR STRING
         LH    R15,WGDSNAME+4          PICK UP LNTH OF CHAR STRING
         STH   R15,WRNAMELN            SAVE LNTH FOR GQSCAN
         #EXEC -R15,MVC,WRKDSNAM(*-*),0(R1) COPY RNAME (DSNAME)
         EJECT 1
*-------
*        USE GQSCAN TO FIND DATASET'S ENQ STATUS
*-------
         SPACE 1
         MVC   WGQSCAN,GQSCAN          COPY MF=L TO WORKAREA
         LH    R2,WRNAMELN             PICK UP LNTH OF RNAME
         LM    R4,R5,VGETAREA          PICK UP ADDR AND LNTH
         GQSCAN AREA=((R4),(R5)),RESNAME=(SYSDSN,WRKDSNAM,(R2)),       X
               MF=(E,WGQSCAN)
         STM   R15,R0,SCANREGS         SAVE IMPORTANT REGS
         EJECT 1
*-------
*        PROCESS RETURN CODE FROM GQSCAN
*-------
         SPACE 1
         C     R15,=F'8'               ERROR?
         BH    SCANERR                 B. IF YES
         C     R15,=F'4'               NO MATCH?
         BE    SCANNONE                B. IF YES
         SPACE 1
*-------
*        BEGIN PROCESSING THE RIBE
*-------
         SPACE 1
SCANPROC DS    0H
         L     R2,VGETAREA             PICK UP ADDR OF SCAN AREA
         USING RIB,R2
         L     R4,RIBNRIBE             PICK UP # OF RIBE
         AH    R2,RIBVLEN              POINT TO
         AH    R2,SCANR0HI                     FIRST RIBE
         USING RIBE,R2
SCANRIBE DS    0H
         MVC   WQUEUE,=CL9'ALLOCATED'  INDICATE "ALLOCATED"    #DD99281
         TM    RIBESFLG,RIBESTAT       CHECK IF WAITING        #DD99281
         BNZ   NOWAIT                  B. IF NOT               #DD99281
         MVC   WQUEUE,=CL9'WAITING  '  INDICATE "WAITING"      #DD99281
NOWAIT   DS    0H                                              #DD99281
         MVC   WALLNAME,RIBEJBNM       COPY JOBNAME TO MSG
         MVC   WALLSYSN,RIBESYSN       COPY SYSTEM NAME TO MSG
         MVC   WALLSTAT,=C'SHR'        DEFAULT TO SHR
         TM    RIBERFLG,RIBETYPE       SHARED?
         BO    SCANBOLD                B. IF YES
         MVC   WALLSTAT,=C'EXC'        SET TO EXCLUSIVE
         EJECT 1
*-------
*        BUILD THE OLD FOR THE ALLOCATION STATUS MESSAGE.
*-------
         SPACE 1
SCANBOLD DS    0H
         LA    R0,6                    # OF MSG SEGMENTS       #DD99281
         ST    R0,OLD                  SET IN OLD
         LA    R0,GRS302               -> 1ST MSG SEGMENT
         ST    R0,OLD+4                SET IN OLD
         LA    R0,WDSNSEG              -> 2ND MSG SEGMENT
         ST    R0,OLD+8                SET IN OLD
         LH    R1,WRNAMELN             PICK UP DSNAME LNTH
         LA    R1,4(,R1)               ADD 4 FOR SEGMENT HEADER
         STH   R1,WDSNSEG              SET LNTH OF DSN MSG SEGMENT
         LA    R0,GRS302DS             OFFSET TO DSN IN MSG
         STH   R0,WDSNSEG+2            SET OFFSET TO DSN IN MSG
         LA    R0,L'WQUEUE+4           LENGTH OF ALLOC/WAIT SEG#DD99281
         STH   R0,WQUESEG              SET LENGTH OF INSERT    #DD99281
         LA    R0,GRS302AW             OFFSET TO MSG INSERT    #DD99281
         STH   R0,WQUESEG+2            SET OFFSET TO INSERT    #DD99281
         LA    R0,WQUESEG              -> 3RD MSG SEGMENT      #DD99281
         ST    R0,OLD+12               SET IN OLD              #DD99281
         LA    R0,WSTATSEG             -> 3RD MSG SEGMENT
         ST    R0,OLD+16               SET IN OLD              #DD99281
         LA    R0,7                    LNTH OF STATUS MSG SEGMENT
         STH   R0,WSTATSEG             SET LNTH OF STATUS MSG SEGMENT
         LA    R0,GRS302ST             OFFSET TO STATUS IN MSG
         STH   R0,WSTATSEG+2           SET OFFSET TO STATUS IN MSG
         LA    R0,WNAMESEG             -> 5TH MSG SEGMENT
         ST    R0,OLD+20               SET IN OLD              #DD99281
         LA    R0,12                   LNTH OF JOBNAME MSG SEGMENT
         STH   R0,WNAMESEG             SET LNTH OF JOBNAME MSG SEGMENT
         LA    R0,GRS302NM             OFFSET TO JOBNAME IN MSG
         STH   R0,WNAMESEG+2           SET OFFSET TO JOBNAME IN MSG
         LA    R0,WSYSNSEG             -> 6TH MSG SEGMENT
         ST    R0,OLD+24               SET IN OLD              #DD99281
         LA    R0,12                   LNTH OF SYSNAME MSG SEGMENT
         STH   R0,WSYSNSEG             SET LNTH OF SYSNAME MSG SEGMENT
         LA    R0,GRS302SY             OFFSET TO SYSNAME IN MSG
         STH   R0,WSYSNSEG+2           SET OFFSET TO SYSNAME IN MSG
         EJECT 1
*-------
*        ISSUE THE ALLOCATION STATUS MESSAGE
*-------
         SPACE 1
SCANPUTL DS    0H
         PUTLINE PARM=WPTPB,                                           X
               OUTPUT=(OLD,TERM,SINGLE,INFOR),                         X
               MF=(E,WIOPL)
         SPACE 1
*-------
*        POINT TO NEXT RIBE
*-------
         SPACE 1
SCANNRIB DS    0H
         AH    R2,SCANR0L              -> NEXT RIBE
         BCT   R4,SCANRIBE             CONTINUE IF MORE TO PROCESS
         SPACE 1
*-------
*        ALL RIBE HAVE BEEN PROCESSED. IF THE GQSCAN RETURN CODE WAS
*        ZERO, PROCESS THE NEXT DATASET.
*-------
         SPACE 1
         ICM   R0,15,SCANR15           PICK UP RETURN CODE
         BZ    SCANNEXT                B. IF ZERO
         EJECT 1
*-------
*        THERE WAS INSUFFICIENT STORAGE SUPPLIED TO COMPLETE THE SCAN
*        FOR THE CURRENT DATASET.
*-------
         SPACE 1
         LA    R0,2                    # OF MSG SEGMENTS
         ST    R0,OLD                  SET IN OLD
         LA    R0,GRS303               -> 1ST MSG SEGMENT
         ST    R0,OLD+4                SET IN OLD
         LA    R0,WDSNSEG              -> 2ND MSG SEGMENT
         ST    R0,OLD+8                SET IN OLD
         LH    R1,WRNAMELN             PICK UP DSNAME LNTH
         LA    R1,4(,R1)               ADD 4 FOR SEGMENT HEADER
         STH   R1,WDSNSEG              SET LNTH OF DSN MSG SEGMENT
         LA    R0,GRS303DS             OFFSET TO DSNAME IN MSG
         STH   R0,WDSNSEG+2            SET OFFSET TO DSN IN MSG
         PUTLINE PARM=WPTPB,                                           X
               OUTPUT=(OLD,TERM,SINGLE,INFOR),                         X
               MF=(E,WIOPL)
         B     SCANNEXT                CONTINUE WITH NEXT DATASET
         EJECT 1
*-------
*        AN ERROR OCCURRED DURING THE GQSCAN
*-------
         SPACE 1
SCANERR  DS    0H
         STC   R15,WORKRC              SAVE RETURN CODE
         UNPK  GQSCANRC,WORKRC         CONVERT RETURN CODE
         TR    GQSCANRC,HEXCONV-C'0'   MAKE IT PRINTABLE
         LA    R0,2                    # OF MSG SEGMENTS
         ST    R0,OLD                  SET IN OLD
         LA    R0,GRS301               -> 1ST MSG SEGMENT
         ST    R0,OLD+4                SET IN OLD
         LA    R0,RCSEG                -> 2ND MSG SEGMENT
         ST    R0,OLD+8                SET IN OLD
         LA    R0,6                    LNTH OF RC MSG SEGMENT
         STH   R0,RCSEG                SET LNTH OF RC MSG SEGMENT
         LA    R0,GRS301RC             OFFSET TO RC IN MSG
         STH   R0,RCSEG+2              SET OFFSET TO RC IN MSG
         PUTLINE PARM=WPTPB,                                           X
               OUTPUT=(OLD,TERM,SINGLE,INFOR),                         X
               MF=(E,WIOPL)
         B     SCANEND                 TERMINATE NOW
         EJECT 1
*-------
*        THE REQUESTED DATASET IS NOT ALLOCATED
*-------
         SPACE 1
SCANNONE DS    0H
         LA    R0,2                    # OF MSG SEGMENTS
         ST    R0,OLD                  SET IN OLD
         LA    R0,GRS300               -> 1ST MSG SEGMENT
         ST    R0,OLD+4                SET IN OLD
         LA    R0,WDSNSEG              -> 2ND MSG SEGMENT
         ST    R0,OLD+8                SET IN OLD
         LH    R1,WRNAMELN             PICK UP DSNAME LNTH
         LA    R1,4(,R1)               ADD 4 FOR SEGMENT HEADER
         STH   R1,WDSNSEG              SET LNTH OF DSN MSG SEGMENT
         LA    R0,GRS300DS             OFFSET TO DSNAME IN MSG
         STH   R0,WDSNSEG+2            SET OFFSET TO DSN IN MSG
         PUTLINE PARM=WPTPB,                                           X
               OUTPUT=(OLD,TERM,SINGLE,INFOR),                         X
               MF=(E,WIOPL)
         SPACE 1
*-------
*        SCAN FOR NEXT DATASET REQUESTED
*-------
         SPACE 1
SCANNEXT DS    0H
         ICM   R3,15,WGDSNAME+24       -> NEXT DSNAME SPECIFIED
         BNM   SCANLOOP                B. IF NOT AT END
         DROP  R3
         EJECT 1
*-------
*        FREE STORAGE USED FOR GQSCAN AREA
*-------
         SPACE 1
SCANEND  DS    0H
         L     R0,VGETAREA+4           PICK UP LNTH OBTAINED
         L     R1,VGETAREA             PICK UP ADDR
         FREEMAIN RU,LV=(0),A=(1),SP=1
         EJECT 1
*-------
*        RESTORE REGS AND RETURN
*-------
         SPACE 1
WGEXIT   #STOP ,
         EJECT 1
*-------
*        IKJPARS DATA SET NAME PROCESSING VALIDATION EXIT
*-------
         SPACE 1
         PUSH  USING
         DROP  ,
DSNCHECK DS    0H
         STM   R14,R12,12(R13)         SAVE PARSE'S REG
         LR    R11,R15                 GET A BASE REG
         USING DSNCHECK,R11
         L     R12,4(,R1)              PICK UP ADDR OF WORKAREA
         USING WRKWG,R12
         L     R3,0(,R1)               PICK UP ADDR OF PDE
         USING WGDSNAME,R3
         SPACE 1
*-------
*        COPY THE DATASET NAME TO THE WORKAREA
*-------
         SPACE 1
         GETMAIN RU,LV=44              ACQUIRE A DSNAME WORK AREA
         LR    R4,R1
         MVI   0(R4),C' '              CLEAR DSNAME
         MVC   1(43,R4),0(R4)
         L     R1,WGDSNAME             PICK UP ADDR OF CHAR STRING
         LH    R15,WGDSNAME+4          PICK UP LNTH OF CHAR STRING
         #EXEC -R15,MVC,0(*-*,R4),0(R1) COPY DSNAME TO WORKAREA
         ST    R4,WGDSNAME             SAVE CONVERTED DSNAME ADDRESS
         SPACE 1
         MVC   WRKCTGPL(LOCATEL),LOCATE
         ST    R4,WRKCTGPL+4           FORMAT
         LA    R0,WRKCATLG                CATALOG
         ST    R0,WRKCTGPL+12                PARAMETER LIST
         LOCATE WRKCTGPL               CONVERT ALIAS TO REAL NAME
         SPACE 1
         LA    R1,44                   LOAD MAXIMUM DSNAME LENGTH
         LA    R2,43(,R4)              LOAD ADDRESS OF LAST CHARACTER
DSNCH001 DS    0H
         CLI   0(R2),C' '              CHECK FOR END OF DSNAME
         BNE   DSNCH002                B. IF YES
         BCTR  R2,0                    BACK UP 1 CHARACTER
         BCT   R1,DSNCH001             LOOP BACK TO LAST CHARACTER
DSNCH002 DS    0H
         STH   R1,WGDSNAME+4           SAVE CONVERTED DSNAME LENGTH
         SPACE 1
         LA    R15,0                   INDICATE DSNAME IS INVALID
         L     R14,12(,R13)            RESTORE RETURN ADDR
         LM    R0,R12,20(R13)          RESTORE OTHER REGS
         BR    R14                     RETURN TO PARSE
         DROP  ,
         POP   USING
         EJECT 1
*-------
*        CONSTANTS AND MF=L FORMS OF MACROS
*-------
         SPACE 1
HEXCONV  DC    C'0123456789ABCDEF'     USED IN HEX CONVERSION
VULA     DC    A(1024),A(500*1024)     VAR GETMAIN VAULES
BLANKS   DC    CL80' '                 USEFUL CONSTANT
SYSDSN   DC    CL8'SYSDSN'             QNAME FOR ALL DATASETS
GQSCAN   GQSCAN REQLIM=MAX,                                            X
               SCOPE=ALL,                                              X
               REQCNT=1,                                               X
               MF=L
GQSCANLN EQU   *-GQSCAN                LNTH OF GQSCAN PARAMATER LIST
         EJECT 1
*-------
*        CONSTANTS AND MF=L FORMS OF MACROS
*-------
         SPACE 1
PTPB     PUTLINE MF=L                  GENERAL PUTLINE PARAMETER BLOCK
LPTPB    EQU   *-PTPB                  LNTH OF PTPB
DUMMY    DS    0C
         SPACE 1
GETMAIN  GETMAIN VU,LA=VULA,SP=1,MF=L
LGETMAIN EQU   *-GETMAIN
         EJECT 1
*-------
*        MESSAGES
*-------
         SPACE 1
GRS300   MSG   'WHO300I DATASET  NOT ALLOCATED'
GRS300DS EQU   16                      OFFSET TO DSN IN MSG
         SPACE 1
GRS301   MSG   'WHO301I UNEXPECTED RETURN CODE FROM GQSCAN, RC='
GRS301RC EQU   47                      OFFSET TO RETURN CODE IN MSG
         SPACE 1
GRS302   MSG   'WHO302I    BY ON '                             #DD99281
GRS302DS EQU   8                       OFFSET TO DSN IN MSG
GRS302AW EQU   9                       OFFSET TO ALLOCATED/WAITING  281
GRS302ST EQU   10                      OFFSET TO SHR/EXC IN MSG     281
GRS302NM EQU   14                      OFFSET TO JOBNAME IN MSG     281
GRS302SY EQU   17                      OFFSET TO SYSTEM NAME IN MSG 281
         SPACE 1
GRS303   MSG   'WHO303I INSUFFICIENT STORAGE TO COMPLETE SCAN FOR '
GRS303DS EQU   50                      OFFSET TO DSN IN MSG
         EJECT 1
*-------
*        PCL FOR WHOSGOT COMMAND
*-------
         SPACE 1
         PRINT ON,NOGEN
WGPCL    IKJPARM DSECT=WGPDL
WGDSNAME IKJPOSIT DSNAME,USID,LIST,VALIDCK=DSNCHECK,                   X
               PROMPT='DATASET NAME'
         IKJENDP
         PRINT ON,GEN
         EJECT 1
LOCATE   CAMLST NAME,*-*,,*-*
LOCATEL  EQU   *-LOCATE
         #STARTWA ,
OLD      DS    10F                     OLD WITH UP TO 9 MSG SEGMENTS
WORKECB  DS    F                       ECB FOR PUTLINE AND IKJPARS
WORKPDLP DS    A                       POINTER TO PDL
VGETAREA DS    2F                      VAR GETMAIN ADDR AND LNTH
SCANREGS DS    2F                      GQSCAN REGS R15,R0
         ORG   SCANREGS
SCANR15  DS    F                       RETURN FROM GQSCAN
SCANR0HI DS    H                       HI-ORDER  2 BYTES = RIB  LNTH
SCANR0L  DS    H                       LOW-ORDER 2 BYTES = RIBE LNTH
         ORG   ,
WRKCTGPL CAMLST NAME,*-*,,*-*
WRKCATLG DS    0F,265C                 CATALOG LOCATE WORKAREA
WORKPPL  DS    0F,XL(LPPL)             PARSE PARAMETER LIST
WGETMAIN DS    XL(LGETMAIN)            MF=E GETMAIN
WPTPB    DS    XL(LPTPB)               MF=E PUTLINE
WIOPL    DS    XL(LIOPL)               I/O SERVICES PARAMETER LIST
WGQSCAN  DS    XL(GQSCANLN)            MF=E GQSCAN
WRNAMELN DS    H                       LENGTH OF DSNAME AS RNAME
WDSNSEG  DS    2H                      DSN MSG SEGMENT
WRKDSNAM DS    CL44                    RNAME=DSNAME FOR GQSCAN
WQUESEG  DS    2H                      ENQ QUEUE MSG SEGMENT   #DD99281
WQUEUE   DS    CL9                     ALLOCATED/WAITING       #DD99281
WSTATSEG DS    2H                      SHR/EXC MSG SEGMENT
WALLSTAT DS    CL3' '                  SHR/EXC
WSYSNSEG DS    2H                      SYSTEM NAME MSG SEGMENT
WALLSYSN DS    CL8' '                  SYSTEM NAME
WNAMESEG DS    2H                      JOBNAME/TSOUSER/STCNAME MSG SEG
WALLNAME DS    CL8' '                  JOBNAME/TSOUSER/STC NAME
WORKRC   DS    XL2                     WORK FOR RETURN CODE CONVERSION
RCSEG    DS    2H                      RC MSG SEGMENT
GQSCANRC DS    CL3                     WORK FOR RETURN CODE CONVERSION
         #STOPWA ,
         EJECT 1
*-------
*        THE FOLLOWING MACROS GENERATE DSECTS WHICH ARE MAPPED IN THE
*        DEBUGGING HANDBOOKS AND THUS ARE NOT EXPANDED HERE.
*-------
         SPACE 1
         PRINT ON,NOGEN
         IKJCPPL
         IKJUPT
         IKJPPL
LPPL     EQU   *-PPL
         IKJIOPL
LIOPL    EQU   *-IOPL
         ISGRIB
         #DSECTS CVT
         END   ,
