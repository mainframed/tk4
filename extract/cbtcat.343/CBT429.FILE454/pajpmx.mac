PMX      TITLE 'PROGRAM TO MOVE MAIL-BOXES DATA.'
PAJPMX   START 0
         SPACE 1
* PURPOSE :    MOVE MAIL-BOXES DATA FROM INPUT TO OUTPUT RECORD,
* ---------    WITHOUT DBCS'S SCANNING (THIS PROGRAM RUN ONLY FOR
*              THE MAIL-BOXES APPLICATION UNDER ISPF).
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12
         LA    R10,MAXERR
        CALL   ISPLINK,(VDEF,MXRINN,MXRINF,TYPEC,MXRINL),VL
         BAS   R14,SETRC           RC OF VDEFINE MXRIN
        CALL   ISPLINK,(VGET,MXRINN,POOL),VL
         BAS   R14,STRRC           RC OF VGET MXRIN
        CALL   ISPLINK,(VDEL,MXRINN),VL
         BAS   R14,STRRC           RC OF VDELETE MXRIN
         OC    RCODES(3*4),RCODES  TEST CONDITION CODES
         BNZ   ERR1
        CALL   ISPLINK,(VDEF,MXLINN,MXLINF,TYPEF,MXLINL),VL
         BAS   R14,SETRC           RC OF VDEFINE MXLIN
        CALL   ISPLINK,(VGET,MXLINN,POOL),VL
         BAS   R14,STRRC           RC OF VGET MXLIN
        CALL   ISPLINK,(VDEL,MXLINN),VL
         BAS   R14,STRRC           RC OF VDELETE MXLIN
         OC    RCODES(3*4),RCODES  TEST CONDITION CODES
         BNZ   ERR2
        CALL   ISPLINK,(VDEF,MXPRQN,MXPRQF,TYPEC,MXPRQL),VL
         BAS   R14,SETRC           RC OF VDEFINE MXPRQ
        CALL   ISPLINK,(VGET,MXPRQN,POOL),VL
         BAS   R14,STRRC           RC OF VGET MXPRQ
        CALL   ISPLINK,(VDEL,MXPRQN),VL
         BAS   R14,STRRC           RC OF VDELETE MXPRQ
         OC    RCODES(3*4),RCODES  TEST CONDITION CODES
         BNZ   ERR3
         L     R2,MXLINF
         LTR   R4,R2
         BNP   ERR4
         CLI   MXPRQF,C'M'
         BE    MVORQ
         MVC   MXROUF(8),=CL8' '   BROWSE/LIST REQUESTED
         LA    R4,8
         CLR   R2,R4
         BNH   SETLN
         CL    R2,=F'80'
         BH    *+L'*+4
         L     R2,=F'80'
         SLR   R2,R4
         LA    R4,5(R2)
         LA    R3,MXROUF+5
         CLI   MXPRQF,C'L'
         BE    *+L'*+4
         BCTR  R3,0
         BCTR  R4,0
         LA    R1,MXRINF
         MVZ   WORK(8),0(R1)       CHECK AND ADJUST FOR VARIABLE
         CLC   WORK(8),=CL8'00000000'
         BNE   *+L'*+4
         LA    R1,8(R1)
         BCT   R2,*+L'*+6
         MVC   0(*-*,R3),0(R1)
         EX    R2,*-6
         B     SETLN
MVORQ    BCT   R2,*+L'*+6          ONLY MOVE REQUESTED
         MVC   MXROUF(*-*),MXRINF
         EX    R2,*-6
SETLN    ST    R4,MXROUL
         ST    R4,MXLOUF
        CALL   ISPLINK,(VDEF,MXROUN,MXROUF,TYPEC,MXROUL),VL
         BAS   R14,SETRC           RC OF VDEFINE MXROU
        CALL   ISPLINK,(VPUT,MXROUN,POOL),VL
         BAS   R14,STRRC           RC OF VPUT MXROU
        CALL   ISPLINK,(VDEL,MXROUN),VL
         BAS   R14,STRRC           RC OF VDELETE MXROU
         OC    RCODES(3*4),RCODES  TEST CONDITION CODES
         BNZ   ERR5
        CALL   ISPLINK,(VDEF,MXLOUN,MXLOUF,TYPEF,MXLOUL),VL
         BAS   R14,SETRC           RC OF VDEFINE MXLOU
        CALL   ISPLINK,(VPUT,MXLOUN,POOL),VL
         BAS   R14,STRRC           RC OF VPUT MXLOU
        CALL   ISPLINK,(VDEL,MXLOUN),VL
         BAS   R14,STRRC           RC OF VDELETE MXLOU
         OC    RCODES(3*4),RCODES  TEST CONDITION CODES
         BNZ   ERR6
         XR    R10,R10             RC=0
LEAVE   $XRET  CC=(R10)
ERR1     BCTR  R10,0               VGET MXRIN VARIABLE
ERR2     BCTR  R10,0               VGET MXLIN VARIABLE
ERR3     BCTR  R10,0               VGET MXPRQ VARIABLE
ERR4     BCTR  R10,0               MXLIN INVALID VALUE
ERR5     BCTR  R10,0               VPUT MXROU VARIABLE
ERR6     BCTR  R10,0               VPUT MXLOU VARIABLE
MAXERR   EQU   ((*-ERR1))/2+1
         LA    R2,RCODES           BUILD COMBINED RETURN CODE
         LA    R3,3                B'AAABBBCCCDDD'
CMBRC    L     R11,0(R2)           WHERE AAA = ERROR NUMBER RC
         SLL   R11,27                    BBB = VDEFINE RC
         SLDL  R10,3                     CCC = VGET OR VPUT RC
         LA    R2,4(R2)                  DDD = VDELETE RC
         BCT   R3,CMBRC            WITH RC 0, 8, 16, 20 SHIFTED
         B     LEAVE                     AS 0, 2, 4, 5.
         SPACE 2
SETRC    LA    R2,RCODES           RETURN CODES ADDRESS.
STRRC    ST    R15,0(R2)           STORE THIS RC
         LA    R2,4(R2)            BUMP TO NEXT CELL
         BR    R14                 RETURN
         EJECT
*        CONSTANTS AND WORK AREAS.
         SPACE 1
RCODES   DC    3F'0'
WORK     DC    XL8'0'
VDEF     DC    CL8'VDEFINE'
VGET     DC    CL8'VGET   '
VPUT     DC    CL8'VPUT   '
VDEL     DC    CL8'VDELETE'
TYPEC    DC    CL8'CHAR'
TYPEF    DC    CL8'FIXED'
POOL     DC    CL8'SHARED'
MXRINN   DC    CL8'MXRIN  '
MXRINF   DC    XL80'0'
MXRINL   DC    F'80'
MXLINN   DC    CL8'MXLIN  '
MXLINF   DC    F'0'
MXLINL   DC    F'4'
MXPRQN   DC    CL8'MXPRQ    '
MXPRQF   DC    XL4'0'
MXPRQL   DC    F'4'
MXROUN   DC    CL8'MXROU  '
MXROUF   DC    XL80'0'
MXROUL   DC    F'0'
MXLOUN   DC    CL8'MXLOU  '
MXLOUF   DC    F'0'
MXLOUL   DC    F'4'
         SPACE 2
        LTORG
         SPACE 2
         END
