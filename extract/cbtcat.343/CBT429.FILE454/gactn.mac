GACT     TITLE 'GET ACCOUNTING INFORMATION SUBROUTINE.'
GACTN    START 0
         SPACE 1
* CALLING SEQUENCE : CALL GACTN,(A,B),VL
* ------------------            (A)
*
*        WHERE : A - IS THE ADDRESS OF A FIELD (FORMAT CL8) INTO
*                    WHICH THE ACCOUNTING NUMBER INFORMATION FOR
*                    THE CURRENT JOB IS PLACED. THIS ARGUMENT IS
*                    MANDATORY BECAUSE INFORMATION IS RETURNED TO
*                    THE CALLER.
*
*                B - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                     0 - ACCOUNTING NUMBER IS RETURNED BACK.
*                     1 - CALLING SEQUENCE OR ARGUMENT ALIGNMENT ERROR.
*                     2 - JOB CONTROL TABLE (SWA/JCT) NOT FOUND.
*                     3 - ACCOUNTING CONTROL TABLE (SWA/ACT) NOT FOUND.
*                     4 - INVALID OR NO ACCOUNTING NUMBER IN ACT.
*
* CALLING SEQUENCE : CALL GNAME,(A,B),VL
* ------------------            (A)
*
*        WHERE : A - IS THE ADDRESS OF A FIELD (FORMAT CL20) INTO
*                    WHICH THE PROGRAMMER'S NAME OF THE CURRENT JOB
*                    IS PLACED. THIS ARGUMENT IS MANDATORY BECAUSE
*                    INFORMATION IS RETURNED TO THE CALLER.
*
*                B - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                     0 - PROGRAMMER'S NAME IS RETURNED BACK.
*                     1 - CALLING SEQUENCE OR ARGUMENT ALIGNMENT ERROR.
*                     2 - JOB CONTROL TABLE (SWA/JCT) NOT FOUND.
*                     3 - ACCOUNTING CONTROL TABLE (SWA/ACT) NOT FOUND.
*
* GENERAL NOTICE : THESE ROUTINES ARE REENTRANT.
* ================
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
GACTN   AMODE  ANY
GACTN   RMODE  ANY
         SPACE 1
        PRINT  NOGEN
         SPACE 1
        CVT    DSECT=YES,LIST=YES
         SPACE 1
        IKJTCB LIST=YES
         SPACE 1
        IECDSECS (JSCB),EXPAND=YES
         SPACE 1
        IEFZB502
ZB502L   EQU   *-ZB502
         SPACE 1
JCT      DSECT
        IEFAJCTB
         SPACE 1
GACTN    CSECT                     RESTORE ORIGINAL CSECT.
         SPACE 1
        PRINT  GEN
         EJECT
        $DEFREG
         SPACE 2
*        MISCELLANEOUS DEFINITIONS.
         SPACE 1
OFF      EQU   X'0F'               NOP OPERATION SWITCH SETTING.
ON       EQU   X'F0'               BRANCH OPERATION SWITCH SETTING.
         EJECT
         ENTRY GNAME
         SPACE 1
         USING *,R15
         LA    R14,0(R14)          SET GETACT ENTRY.
         LA    R15,GNAME-GACTN(R15)
         DROP  R15
         SPACE 1
GNAME   $XENT   BASE=R12,ID=GACTN,TYPE=RENT
         EJECT
         LR    R7,R14
         SRL   R7,24
         LA    R10,MAXERR
         XR    R8,R8
         TM    0(R1),X'80'         CONTROL ARGUMENTS.
         BO    GAR
         TM    7(R1),X'03'
         BNZ   ERR1
         L     R8,4(R1)
         LA    R8,0(R8)
         TM    4(R1),X'80'
         BZ    ERR1
GAR      L     R9,0(R1)
         LA    R9,0(R9)
         XC    0(8,R9),0(R9)
         L     R2,CVTPTR           GET CVT ADDRESS.
         USING CVT,R2
         L     R3,CVTTCBP          POINTS TO IEATCBP.
         DROP  R2
         L     R2,4(R3)            CURRENT TCB ADDRESS.
         USING TCB,R2
         XR    R3,R3
         ICM   R3,B'0111',TCBJSCBB JSCB ADDRESS.
         DROP  R2
         BZ    ERR2
         USING IEZJSCB,R3
         XR    R2,R2
         ICM   R2,B'0111',JSCBJCTA SWA-BLOCK FOR JCT.
         DROP  R3
         BZ    ERR2
         USING ZB502,R2
         CLI   SWPID,JCTID         JCT BLOCK ID?
         BNE   ERR2                NO.
         CLC   SWPLNGTH,BLKL       BLOCK LENGTH?
         BNE   ERR2                NO.
         CLC   SWPACRO,ACROJCT     BLOCK ACRONYM?
         BNE   ERR2                NO.
         DROP  R2
         LA    R2,ZB502L(R2)       JCT.
         USING INJMJCT,R2
         CLI   JCTIDENT,JCTID      JCT CONTROL TABLE?
         BNE   ERR2                NO.
         L     R3,JCTACTAD         SWA-BLOCK FOR ACT.
         DROP  R2
         SRL   R3,8
         LTR   R3,R3
         BZ    ERR3
         USING ZB502,R3
         CLI   SWPID,ACTID         ACT BLOCK ID?
         BNE   ERR3                NO.
         CLC   SWPLNGTH,BLKL       BLOCK LENGTH?
         BNE   ERR3                NO.
         CLC   SWPACRO,ACROACT     BLOCK ACRONYM?
         BNE   ERR3                NO.
         DROP  R3
         LA    R3,ZB502L(R3)       ACT.
         USING IEFAACTB,R3
         CLI   ACTIDENT,ACTID      ACCOUNTING CONTROL TABLE?
         BNE   ERR3                NO.
         LTR   R7,R7
         BNZ   GNM
         CLI   ACTJNFLD,1          NUMBER OF ACC. FIELDS?          -EU-
         BNE   ERR4                NOT SUPPLIED OR MORE THAN ONE.  -EU-
         CLI   ACTJNFLD+1,8        ACC. NO. FIELD LENGTH?          -EU-
         BNE   ERR4                INVALID.                        -EU-
         MVC   0(8,R9),ACTJNFLD+2  GET BACK ACC. NO.               -EU-
         B     GEX
GNM      MVC   0(L'ACTPRGNM,R9),ACTPRGNM     GET BACK PRG. NAME.
         DROP  R3
         SPACE 1
GEX      XR    R10,R10             C.C. = 0.
         B     LEAVE
         SPACE 1
ERR1     BCTR  R10,0
ERR2     BCTR  R10,0
ERR3     BCTR  R10,0
ERR4     BCTR  R10,0
         SPACE 1
MAXERR   EQU   ((*-ERR1)/2)+1
         EJECT
LEAVE    LTR   R8,R8
         BZ    EXIT
         ST    R10,0(R8)           STORE INDICATOR.
EXIT    $XRET  CC=(R10),TYPE=RENT
         SPACE 2
*        CONSTANTS AND WORK-AREAS.
         SPACE 1
ACROJCT  DC    CL4'JCT '
ACROACT  DC    CL4'ACT '
BLKL     DC    AL3(JCTLNGTH)
         SPACE 2
         END
