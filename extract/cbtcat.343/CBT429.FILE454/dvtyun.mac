DTUN     TITLE 'DEVICE TYPE TO UNIT NAME SUBROUTINE.'
DVTYUN   START 0
         SPACE 1
* CALLING SEQUENCE : CALL DVTYUN,(A,B,C),VL
* ------------------             (A,B)
*
*        WHERE : A - IS THE ADDRESS OF THE FOUR BYTES FIELD CONTAINING
*                    THE DEVICE TYPE (HEXADECIMAL FORMAT, EXACTLY AS
*                    UCBTYP FIELD OF THE UCB).
*
*                B - IS THE ADDRESS OF AN EIGTH BYTES FIELD RECEIVING
*                    THE CORRESPONDING UNIT NAME RIGHT PADDED WITH
*                    BLANKS.
*
*                C - RECEIVES THE RETURN CODE (FORMAT FULLWORD -
*                    F BOUNDARY) IF SUPPLIED (OPTIONAL).
*
*        RETURN CODES (REGISTER 15) :
*
*                        0 = UNIT NAME SUPPLIED.
*                        4 = INVALID DEVICE TYPE.
*                        8 = UNIT NAMES TABLE SPACE OVERFLOW.
*                       12 = CALLING SEQUENCE OR PARAMETER ALIGNMENT
*                            ERROR.
*
* ORIGIN :     ROUTINE 'DTYP2UNT' (CONVERT UCB-TYPE HEX DEVICE CODE
* --------     TO UNIT NAME - JOHN A SHELL) FROM FILE 360 OF THE CBT
*              86 FEB TAPE.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
DVTYUN  AMODE  24
DVTYUN  RMODE  24
         SPACE 1
        $DEFREG
NTBL     EQU   200                 MAXIMUM UNIT NAMES IN TABLE SPACE
         EJECT
        $XENT  BASE=R12
         XR    R7,R7
         LR    R8,R7
         LR    R9,R7
         LR    R10,R7
         TM    0(R1),X'80'
         BO    ERSA
         TM    4(R1),X'80'
         BO    GADD
         TM    8(R1),X'80'
         BZ    ERSA
         TM    11(R1),X'03'
         BNZ   ERSA
         L     R9,8(R1)
         LA    R9,0(R9)
GADD     L     R8,4(R1)
         LA    R8,0(R8)
         L     R7,0(R1)
         LA    R7,0(R7)
         MVC   0(8,R8),=CL8' '
         LM    R3,R5,SCAN          UNIT NAMES TABLE SCAN ADDRESSES
         CLI   0(R3),C' '          ALREADY EXISTS?
         BNE   LOOP                YES
        $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE
         L     R1,CVTPTR           CVT ADDRESS
         USING CVTMAP,R1
         L     R1,CVTJESCT         JES CONTROL TABLE
         DROP  R1
         USING JESCT,R1            ADDRESS TO DSECT
         L     R1,JESEDT           EDT POINTER
         DROP  R1
         L     R5,EDTLUVSP(,R1)    LOOK-UP SECTION
         L     R2,LUVENTNO(,R5)    NUMBER OF UNIT NAMES
         CH    R2,=Y(NTBL)         TABLE LARGE ENOUGH?
         BH    OVERFL              NO
         LA    R0,L'LUVENTRY       YES, LENGTH OF AN ENTRY
         LA    R1,LUVENTRY(,R5)    FIRST ENTRY
         B     CPYUN
BUMP     ALR   R3,R4
         ALR   R1,R0
CPYUN    MVC   0(12,R3),LUVUNAME(R1)    COPY UNIT NAME AND DEVICE TYPE
         BCT   R2,BUMP
OVERFL  $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE
         LTR   R2,R2
         BNZ   EROV
         ST    R3,SCAN+8           SET END OF TABLE SCAN ADDRESS
         LM    R3,R5,SCAN          SET SCAN ADDRESSES
LOOP     CLC   0(4,R7),8(R3)       IS THIS DEVICE TYPE?
         BE    FOUND               YES
         BXLE  R3,R4,LOOP          POINT TO NEXT IF ANY
         LA    R10,4
         B     LEAVE
EROV     LA    R10,8
         B     LEAVE
ERSA     LA    R10,12
         B     LEAVE
FOUND    MVC   0(8,R8),0(R3)       SET UNIT NAME
LEAVE    LTR   R9,R9
         BZ    EXIT
         ST    R10,0(R9)           STORE INDICATOR
EXIT    $XRET  CC=(R10)
         EJECT
SCAN     DC    A(UNTB,L'UNTB,*-*)
         SPACE 1
        LTORG
         SPACE 2
UNTB     DC    (NTBL)CL12' '       UNIT NAMES TABLE SPACE
         SPACE 1
*        ----------------------------------------------------
*        DSECT IEFZB421 SHOULD BE SUBSTITUTED WHEN AVAILABLE.
*        ----------------------------------------------------
         SPACE 1
EDTLUVSP EQU   28,4           POINTER TO LOOK UP VALUE SECTION
LUVENTNO EQU   8,4            NUMBER OF ENTRIES IN THIS SECTION
LUVENTRY EQU   16,32          TABLE OF LOOK UP ENTRIES
LUVUNAME EQU   0,8            UNIT NAME IN EBCDIC
         SPACE 1
        PRINT  NOGEN
        CVT    DSECT=YES,LIST=YES
        IEFJESCT
         SPACE 2
         END
