ISREDIT  MACRO (APARM,BPARM) NOPROCESS                              */
/*  MACRO NAME : TRAP                                               */
/*    FUNCTION : PUT ANY TSO COMMAND OUTPUT INTO EDIT               */
/*      SYNTAX : TRAP COMMAND                                       */
/*               TRAP 'COMMAND ...'                                 */
/*               TRAP HELP OR H                                     */
/*               OPERAND : DESIRED TSO COMMAND (MUST USE PUTLINE)   */
/*       DEBUG : TRAP COMMAND LIST                                  */
/*               TRAP 'COMMAND ...' LIST                            */
/*      ORIGIN : EXTRACTED FROM CBT TAPE 89 FEB, FILE 015.          */
/*      UPDATE : MOINIL P.A.                                        */
  IF &STR(&APARM.) = &Z THEN DO
    SET ZEDSMSG = &STR(OPERAND MISSING)
    SET ZEDLMSG = &STR(== FIRST OPERAND TSO COMMAND IS MANDATORY ==)
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTTRAP)
      SET &TPCC = &LASTCC
      ISPEXEC CONTROL DISPLAY RESTORE
      EXIT CODE(&TPCC)
      END
  IF &STR(&BPARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
  IF &SUBSTR(1:1,&APARM) = &STR(') THEN DO
    SET &CL = &LENGTH(&STR(&APARM)) - 1
    SET &CMDNM = &SUBSTR(2:&CL,&APARM)
    END
  ELSE SET &CMDNM = &STR(&APARM.)
  CONTROL ASIS
  SET &SYSOUTTRAP = 400
  TSOEXEC &CMDNM
  SET &SYSOUTTRAP = 0
  SET &TL = &SYSOUTLINE
  SET &TN = 1
  SET &C = &STR(&&)
  ISREDIT PROCESS DEST
  IF &LASTCC = 0 THEN DO
    ISREDIT LOCATE .ZDEST
    IF &LASTCC = 8 THEN GOTO IN2
IN1: ISREDIT (LINEPTR) = LINENUM .ZDEST
    SET &WH = &LINEPTR
    DO WHILE &TL >= &TN
      SET &TF = &NRSTR(&C.SYSOUTLINE&TN)
      SET &TT = &SYSNSUB(2,&TF)
      SET &TS = 1
      SET &TM = &LENGTH(&NRSTR(&TT))
      SET &TQ =
      DO WHILE &TS <= &TM
        SET &TX = &SUBSTR(&TS,&NRSTR(&TT))
        IF &NRSTR(&TX) = &STR(") THEN -
          SET &TX = &STR(')
        SET &TQ = &NRSTR(&TQ&TX)
        SET &TS = &TS + 1
        END
      ISREDIT MASKLINE = &SYSNSUB(1,"&TQ")
      ISREDIT LINE_AFTER &LINEPTR = MASKLINE
      SET &LINEPTR = &LINEPTR + 1
      SET &TN = &TN + 1
      END
    ISREDIT MASKLINE = "OUTPUT FROM : &CMDNM"
    ISREDIT LINE_AFTER &WH = MSGLINE MASKLINE
    END
  ELSE DO
IN2: ISREDIT (LINEPTR) = LINENUM .ZCSR
    SET &LINEPTR = .ZLAST
    DO WHILE &TL >= &TN
      SET &WH = &TN - 1
      SET &TF = &NRSTR(&C.SYSOUTLINE&TN)
      SET &TT = &SYSNSUB(2,&TF)
      SET &TS = 1
      SET &TM = &LENGTH(&NRSTR(&TT))
      SET &TQ =
      DO WHILE &TS <= &TM
        SET &TX = &SUBSTR(&TS,&NRSTR(&TT))
        IF &NRSTR(&TX) = &STR(") THEN -
          SET &TX = &STR(')
        SET &TQ = &NRSTR(&TQ&TX)
        SET &TS = &TS + 1
        END
      ISREDIT MASKLINE = &SYSNSUB(1,"&TQ")
      ISREDIT LINE_AFTER &WH = MASKLINE
      SET &TN = &TN + 1
      END
    ISREDIT MASKLINE = "OUTPUT FROM : &CMDNM"
    ISREDIT LINE_AFTER 0 = MSGLINE MASKLINE
    END
  ISREDIT MASKLINE = " "
  CONTROL MSG
  CONTROL CAPS
  EXIT CODE(0)
