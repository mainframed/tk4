TDSN     TITLE 'TSO - LIST OWNERS OF DATA-SET COMMAND.'
TSODSN   START 0
         SPACE 1
* FUNCTION :   DISPLAY FOR THE SPECIFIED DATA-SET :
* ----------       1) USERS
*                  2) DISPOSITION
*                  3) WAITING FOR DATASETS
*                  4) SYSTEM
*                  5) MUST COMPLETE FLAG
*                  6) RESERVE FLAG
*                  7) GLOBAL FLAG
*                  8) OWNERSHIP FLAG
* COMMAND :    SYNTAX : TSODSN ...DATA-SET NAME...
* ---------    DEFAULT : NONE.
* NOTE :     - THIS CODE HAS NOT BEEN TESTED WITH A GRS RING.
* ------       SCOPE=GLOBAL WILL HAVE TO BE SPECIFIED ON THE GQSCAN
*              MACRO AND THE GQSCAN HAS TO RUN AUTHORIZED KEY ZERO.
*            - THE 'SYSNAME' IN IEASYS00 SHOULD SPECIFY YOUR SMFID
*              TO GET THE CORRECT SYSTEM NAME IN THE 'SYSTEM' FIELD,
*              OTHERWISE (NONA) NO-NAME WILL BE DISPLAYED.
* AUTHOR :     ORIGINAL VERSION FROM CBT TAPE (FILE 324).
* --------     MODIFIED BY : RAVARANI N. - MOINIL P.A.
*                            COMPUTING CENTRE (TP 361)
*                            J.R.C. - ISPRA ESTABLISHMENT
*                            21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
SIAL     EQU   12*1024             SCAN INFO AREA LENGTH.
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R5,R13              SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R5
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
        $TEW$PP PARM=PARMLST
         TM    DSNAM+6,X'80'       IS DSNAME THERE?
         BZ    ERRDS1              NO.
         TM    DSNAM+14,X'80'      ANY MEMBER NAME ALSO?
         BO    ERRDS2              YES, NOT ALLOWED.
         MVC   REQDSN(L'REQDSN),BLANK
         LH    R4,DSNAM+4          GET DSNAME LENGTH.
         L     R14,DSNAM           GET DSNAME POINTER.
         LR    R15,R4
         BCT   R15,*+L'*+6
         MVC   REQDSN(*-*),0(R14)  <<EXECUTED>>
         EX    R15,*-6             MOVE DSNAME.
         DROP  R2
        IKJRLSA #TSANSW
         SPACE 1
         MVC   LINE,BLANK
         L     R3,CVTPTR           GET ADDRESS OF CVT.
         USING CVT,R3
         L     R1,CVTSMCA
         DROP  R3
         USING SMCABASE,R1
         MVC   LINE(24),=CL24'DATA-SET USAGE DISPLAY ('
         MVC   LINE+24(4),SMCASID  MOVE IN SYSTEM ID.
         DROP  R1
         MVI   LINE+29,C'-'
        TIME   DEC                 R0 : HHMMSSTH
         STM   R0,R1,WORKD         TIME - DATE
         UNPK  LINE+40(7),WORKD(4)
         MVC   LINE+46(2),LINE+44  XXSS
         MVC   LINE+44(1),LINE+43  MXSS
         MVC   LINE+43(1),LINE+42  HHXM MXSS
         MVI   LINE+42,C'.'
         MVI   LINE+45,C'.'        HH.MM.SS
         UNPK  LINE+31(7),WORKD+4(4)
         MVC   LINE+31(2),LINE+33  YY
         MVC   LINE+34(3),LINE+35  DDD
         MVI   LINE+33,C'.'        YY.DDDX
         MVI   LINE+37,C' '        YY.DDD
         MVI   LINE+38,C'-'
         MVI   LINE+48,C')'
        STLINENO LINE=1            VTAM CLEAR SCREEN.
         LA    R0,L'LINE           TITLE AND TIME.
         LA    R1,LINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         LA    R0,L'BLANK          A BLANK LINE.
         LA    R1,BLANK
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         MVC   LINE,BLANK
         MVC   LINE(16),=CL16'DATA-SET NAME :'
         MVC   LINE+16(L'REQDSN),REQDSN MOVE IN DSNAME.
         LA    R0,L'LINE
         LA    R1,LINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         LA    R0,L'BLANK          A BLANK LINE.
         LA    R1,BLANK
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         LA    R7,REQDSN           ADDRESS OF DSN MINOR NAME.
         LA    R8,SCANAREA         ADDRESS OF RETURNED DATA.
         LA    R9,SYSDSN           ADDRESS OF SYSDSN MAJOR NAME.
         MVC   SYSDSN,=CL8'SYSDSN'
         LA    R13,SCANSAVE        ADDRESS GQSCAN SAVE AREA.
         XC    0(18*4,R13),0(R13)
         MVC   SCAN(LSCAN),SCANP
        GQSCAN AREA=((R8),),RESNAME=((R9),(R7),(R4)),MF=(E,SCAN)
         LR    R13,R5              RESTORE MAIN SAVE AREA.
         B     *+L'*(R15)          HOW COMPLETE ?
         B     CODE00              +0 - O.K.
         B     CODE04              +4 - NO RESOURCES MATCHED REQUEST
         B     CODE08              +8 - GQSCAN AREA TOO SHORT
         B     CODE0C              +12 - ERROR WHILE PROCESSING
         B     CODE10              +16 - INVALID SYSNAME
         B     CODE14              +20 - GQSCAN AREA TOO SHORT
         SPACE 1
CODE04   LA    R0,L'CODE04M        DSNAME NOT FOUND.
         LA    R1,CODE04M
         B     FINISH
CODE08   LA    R0,L'CODE08M        GQSCAN AREA TOO SHORT.
         LA    R1,CODE08M
         B     FINISH
CODE0C   LA    R0,L'CODE0CM        ERROR IN GQSCAN.
         LA    R1,CODE0CM
         B     FINISH
CODE10   LA    R0,L'CODE10M        INVALID SYSNAME.
         LA    R1,CODE10M
         B     FINISH
CODE14   LA    R0,L'CODE14M        GQSCAN AREA TOO SHORT.
         LA    R1,CODE14M
FINISH   BAS   R14,PUTLNE
         B     SETRC
         SPACE 1
CODE00   ST    R0,RIBLNGTH         STORE RIB AND RIBE LENGTH.
         LA    R10,SCANAREA        ADDRESS OF GQSCAN AREA.
         USING RIB,R10             RESOURCE INFORMATION BLOCK.
         L     R9,RIBNRIBE         LOAD THE NO. OF RIBE'S.
         LTR   R9,R9
         BNP   CODE04              NO RIBE'S SUPPLIED.
         LA    R0,L'FLAGHD         PRINT FLAG HEADER.
         LA    R1,FLAGHD
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         MVI   LINE,C'-'
         MVC   LINE+1(L'LINE-1),LINE
         LA    R0,L'FLAGHD         UNDER LINE IT.
         LA    R1,LINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         LA    R0,L'BLANK          A BLANK LINE.
         LA    R1,BLANK
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         LR    R11,R10             SAVE RIB POINTER.
         AH    R11,RIBLNGTH        POINT TO RIB VARIABLE SECTION.
         AH    R11,RIBVLEN         POINT TO FIRST RIBE.
         USING RIBE,R11            RESOURCE INFORMATION BLOCK EXT.
         LA    R6,LINE
         USING JOBLINE,R6
SEARCH   MVC   LINE,BLANK
         MVC   JOBNAME,RIBEJBNM    JOBNAME OF REQUESTOR.
         MVC   SYSTEM(4),RIBESYSN  SYSTEM NAME OF REQUESTOR.
         TM    RIBERFLG,RIBETYPE   IS THIS EXCLUSIVE OR SHARED?
         BO    *+L'*+10            BRANCH IF SHARED.
         MVC   EXCLFLAG,=CL4'EXCL' NO, EXCLUSIVE.
         B     *+L'*+6
         MVC   EXCLFLAG,=CL4'SHR'
         TM    RIBERFLG,RIBEMC     IS MUST COMPLETE?
         BO    *+L'*+8             BRANCH IF YES.
         MVI   MCFLAG,C'N'
         B     *+L'*+4
         MVI   MCFLAG,C'Y'
         TM    RIBERFLG,RIBERESV   IS THIS RESERVE REQUEST?
         BO    *+L'*+8             BRANCH IF YES.
         MVI   RESVFLAG,C'N'
         B     *+L'*+4
         MVI   RESVFLAG,C'Y'
         TM    RIBERFLG,RIBERESC   IS THIS RESERVE REQUEST GLOBAL?
         BO    *+L'*+8             BRANCH IF YES.
         MVI   GLOBFLAG,C'N'
         B     *+L'*+4
         MVI   GLOBFLAG,C'Y'
         TM    RIBESFLG,RIBESTAT   IS THIS GUY THE OWNER?
         BO    *+L'*+14            BRANCH IF YES.
         MVI   OWNFLAG,C'N'
         MVC   WAITFLAG,=CL9'*WAITING*'
         B     *+L'*+4
         MVI   OWNFLAG,C'Y'
         DROP  R6,R10,R11
         LA    R0,L'LINE           PRINT IT.
         LA    R1,LINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     *+L'*+4             +0 - CONTINUE.
         B     SETRC               +4 - ERROR.
         AH    R11,RIBELENG        POINT TO NEXT RIBE.
         BCT   R9,SEARCH           DO IT FOR NEXT USER.
         XR    R15,R15             RC = 0 -----------------------------
QUIT    $XRET  CC=(R15),LV=WRKL,TYPE=RENT
ERRDS1   LA    R0,L'ERRMS1
         LA    R1,ERRMS1
         B     OUTMS
ERRDS2   LA    R0,L'ERRMS2
         LA    R1,ERRMS2
OUTMS    BAS   R14,PUTLNE
        IKJRLSA #TSANSW
SETRC    LA    R15,16              RC = 16 ----------------------------
         B     QUIT
         EJECT
*------- SKELETONS, MESSAGES AND CONSTANTS
         SPACE 1
        PRINT  NOGEN
SCANP   GQSCAN AREA=(,SIAL),REQLIM=MAX,SCOPE=ALL,MF=L
        PRINT  GEN
LSCAN    EQU   *-SCANP
         SPACE 1
FLAGHD   DS   0CL72
         DC    CL14'JOBNAME/USERID'
         DC    CL5' '
         DC    CL4'DISP'
         DC    CL2' '
         DC    CL8' SYSTEM '
         DC    CL2' '
         DC    CL13'MUST COMPLETE'
         DC    CL2' '
         DC    CL7'RESERVE'
         DC    CL2' '
         DC    CL6'GLOBAL'
         DC    CL2' '
         DC    CL5'OWNER'
         SPACE 1
CODE04M  DC    C'NOT IN-USE OR NOT FULLY QUALIFIED DATA-SET NAME.'
CODE08M  DC    C' -> GQSCAN : "AREA" TOO SHORT'
CODE0CM  DC    C' -> GQSCAN : ERROR WHILE PROCESSING'
CODE10M  DC    C' -> GQSCAN : "SYSNAME" IS INVALID'
CODE14M  EQU   CODE08M,L'CODE08M
ERRMS1   DC    C'DATA-SET NAME MISSING.'
ERRMS2   DC    C'MEMBER NAME NOT ALLOWED.'
BLANK    DC    CL72' '
         EJECT
        LTORG
         SPACE 1
        PRINT  NOGEN
PARMLST IKJPARM
DSNAM   IKJPOSIT DSNAME,USID,PROMPT='DATA-SET NAME'
        IKJENDP
        PRINT  GEN
         EJECT
*------- WORK-AREAS DESCRIPTION
         SPACE 1
JOBLINE  DSECT                     INFO LINE DESCRIPTION.
JOBNAME  DS    CL8
         DC    C' '
WAITFLAG DS    CL9' '
         DC    C' '
EXCLFLAG DS    CL4
         DC    CL4' '
SYSTEM   DS    CL4
         DC    CL4' '
         DC    CL6' '
MCFLAG   DS    CL1
         DC    CL6' '
         DC    CL2' '
         DC    CL3' '
RESVFLAG DS    CL1
         DC    CL3' '
         DC    CL2' '
         DC    CL2' '
GLOBFLAG DS    CL1
         DC    CL3' '
         DC    CL2' '
         DC    CL2' '
OWNFLAG  DS    CL1
         DC    CL2' '
         SPACE 1
WRKDSECT DSECT                     WORK AREAS. DESCRIPTION.
SVA      DS    18F                 SAVE AREA.
SCANSAVE DS    18F                 18 WORDS SAVE AREA REQ FOR GQSCAN.
WORKD    DS    D
        $TEW$WA
SCAN     DS    0F,(LSCAN)X
RIBLNGTH DC    H'0'
RIBELENG DC    H'0'
SYSDSN   DS    CL8
REQDSN   DS    CL44
LINE     DS    CL72
SCANAREA DS    0F,(SIAL)X          SCAN INFO AREA.
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 1
        PRINT  NOGEN
       $TEW$DS
        IEESMCA
        ISGRIB
        PRINT  GEN
         SPACE 1
         END
