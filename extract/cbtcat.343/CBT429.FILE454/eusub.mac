SBMT     TITLE 'J.R.C. - EURATOM SUBMIT PROGRAM.'
EUSUB    START 0
         SPACE 1
* PURPOSE :    ALLOWS THE SUBMIT OF A BATCH JOB FOR CONVENTIONAL
* ---------    PROCESSING. THE JOB SUBMITTED MAY RESIDE IN A SEQUENTIAL
*              DATA-SET OR IN A MEMBER OF A PARTITIONED DATA-SET (FIXED
*              80-BYTES RECORDS, BLOCKED OR UNBLOCKED), BUT THE DD-NAME
*              TO ACCESS IT MUST BE 'SYSPO'.
*
*       NOTE - JCL JOB STATEMENT CODING : MUST BE THE FIRST AND MAY
*              CONTAIN ALL THE NEEDED INFORMATION. THE SUBMIT FUNCTION
*              WILL SUPPLY THE PASSWORD ONLY WHEN THE USER=... KEYWORD
*              IS CODED AND THE PASSWORD=... KEYWORD IS NOT CODED (THIS
*              ASSUMES THE JOB STATEMENT IS MORE THAN ONE CARD CODED).
* - SUPPRESS - IF YOU DON'T WANT SUPPLY THE PASSWORD AS DESCRIBED
*              ABOVE, YOU MAY BYPASS THIS PROCESS BY SETTING THE BIT
*              'SWJS' PERMANENTLY ON IN THE SWITCH BYTE 'SWJBSC'.
*
* INVOKED BY : CONSOLE START COMMAND :
* ------------      S R1,FROM='...PO-DSNAME...',
*                          M=...MEMBER-NAME...
*
*              WHERE R1 IS THE FOLLOWING PROCEDURE :
*                   //R1       PROC FROM='->.OPER.PDS.CNTL',M=
*                   //R1       EXEC PGM=EUSUB
*                   //SYSPO    DSN=&FROM(&M),DISP=SHR
*                   //SYSSU    DD SYSOUT=(,INTRDR)
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
LV1      EQU   1         OWNER LEVEL, IN TSO ONLY.
LV0      EQU   0         REJECT LEVEL.
         SPACE 1
        $MDL@IX
         EJECT
        $DEFREG
        $XENT  BASE=R12
         EJECT
*        VERIFY AUTHORITY.
*        -----------------
         SPACE 1
        $EACM  REQAUT
         CLI   AUTH,LV0            AUTHORIZED?
         BNE   ENVRT               YES.
         LA    R9,16               C.C. = 16.
         B     LEAVE
         SPACE 1
*        FIND ENVIRONMENT.
*        -----------------
         SPACE 1
ENVRT   EXTRACT ANSWER,'S',FIELDS=(COMM,TSO,PSB)
         L     R2,ACOMM            COMMUNICATION AREA ADDRESS.
         L     R3,4(R2)            CIB ADDRESS.
         LA    R3,0(R3)            CLEAR HIGH BYTE.
         LTR   R3,R3               CURRENT CIB SET UP?
         BZ    NOTST               NO.
         USING CIBDSECT,R3
         CLI   CIBVERB,CIBSTART    START COMMAND (SYSTEM TASK)?
         BNE   NOTST               NO.
         DROP  R3
         OI    SWITCH,SWST         SET STARTED TASK.
         B     INIT
NOTST    L     R2,ATSO             TSO INDICATOR ADDRESS.
         LTR   R2,R2
         BZ    INIT
         TM    0(R2),X'80'         ARE WE IN TSO?
         BZ    INIT                NO.
         OI    SWITCH,SWTS         SET TSO TASK.
         EJECT
*        SUBMIT PROCESSING.
*        ------------------
         SPACE 1
INIT     ZAP   CCJB,=PL1'+0'       INITIALIZE PROCESSING.
         AIF   ('&SYSPARM' EQ 'NOVSAM').O1S1
        OPEN   (INRDR)             OPEN JES2 ACB.
         LTR   R15,R15
         BZ    READ
         MVC   MSEIR+8(15),=CL15'IN.RDR ACB OPEN'
         BAS   R8,SETRC
         XR    R9,R9               C.C. = 0.
         B     LEAVE
.O1S1    ANOP
READ    $READ  TEXT,SBDDN
         LTR   R15,R15
         BM    ENDS
         BZ    READOK
         MVC   MSEIR+8(15),=CL15'SUBMIT DATA-SET'
         B     JSUC
READOK   TM    SWJBSC,SWJS         BYPASS REQUESTED?
         BO    JPR                 YES.
         TM    SWJBSC,SWJB         JOB STATEMENT PROCESSED?
         BO    JPR                 YES.
         AP    CCJB,=PL1'+1'
         LM    R3,R5,SCTXT
         CLC   0(2,R3),=CL2'//'
         BNE   JERR
         LA    R3,2(R3)
         TM    SWJBSC,SWJN         JOB STATEMENT CONTINUATION?
         BZ    JNC                 NO.
         CLI   0(R3),C' '
         BE    JCN
JERR     SL    R3,SCTXT            RELATIVE POSITION IN CARD.
         LA    R3,1(R3)
         CVD   R3,WORK
         BAS   R14,EDIT
         MVC   MSEJB+31(2),WORK+2
         MVC   WORK+6(2),CCJB      JOB RELATIVE CARD.
         BAS   R14,EDIT
         MVC   MSEJB+26(2),WORK+2
         LA    R2,MSEJB
         BAS   R8,SMSG
         B     JPURGE
JNC      CLI   0(R3),C' '          SCAN JOB STATEMENT FOR :
         BE    JERR                     USER=...
         BXLE  R3,R4,*+L'*+4            PASSWORD=...
         B     JERR
         CLI   0(R3),C' '
         BNE   *-12
         BXLE  R3,R4,*+L'*+4
         B     JERR
         CLI   0(R3),C' '
         BE    *-12
         CLC   0(4,R3),=CL4'JOB '
         BNE   JERR
         AIF   ('&SYSPARM' EQ 'NOVSAM').O2S1
         LA    R0,TEXT+3
         LR    R1,R3
         SLR   R1,R0
         LA    R0,7
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         MVC   MSJNN+16(*-*),TEXT+2
         EX    R1,*-6              SET JOB-NAME IN MESSAGE.
.O2S1    LA    R3,3(R3)
JCN      BXLE  R3,R4,*+L'*+4
         B     JERR
         CLI   0(R3),C' '
         BE    JCN
         TM    SWJBSC,SWJN
         BZ    JCNT
         LR    R0,R3
         S     R0,=A(TEXT+15)
         BP    JERR
JCNT     OI    SWJBSC,SWJN
JCNQS    CLI   0(R3),C''''
         BE    JCNQF
         TM    SWJBSC,SWJA
         BO    JCNSS
         CLC   0(2,R3),=CL2', '
         BE    JPR
         CLI   0(R3),C' '
         BE    JTPS
         CLC   0(9,R3),=CL9'PASSWORD='  PASSWORD CODED?
         BE    JPSW                YES.
         TM    SWJBSC,SWJU
         BO    JCNSS
         CLC   0(5,R3),=CL5'USER='
         BE    JUSR
JCNSS    BXLE  R3,R4,*+L'*+4
         B     JERR
         CLR   R3,R5
         BL    JCNQS
         CLI   0(R3),C','
         BNE   JCNQS
         B     JPR
JCNQF    TM    SWJBSC,SWJA
         BO    JQFF
         OI    SWJBSC,SWJA
         B     JCNSS
JQFF     NI    SWJBSC,255-SWJA
         B     JCNSS
JUSR     LA    R3,4(R3)            EXTRACT USER-ID.
         BXLE  R3,R4,*+L'*+4
         B     JERR
         CLI   0(R3),C' '
         BE    JPR
         CLI   0(R3),C','
         BE    JPR
         LR    R6,R3
JUSL     BXLE  R3,R4,*+L'*+4
         B     JUSF
         CLI   0(R3),C' '
         BE    JUSF
         CLI   0(R3),C','
         BNE   JUSL
JUSF     LR    R1,R3
         SR    R1,R6
         STC   R1,JUSERL
         MVC   JUSER,=CL8' '
         BCTR  R1,0
         EX    R1,SBMMVUS
         CLI   AUTH,LV1            OWNER LEVEL?
         BNE   JSCN                NO.
         TM    SWITCH,SWTS         YES, TSO?
         BZ    JPSW                NO, IGNORE.
         L     R1,APSCB
         USING PSCB,R1
         CLC   PSCBUSER(L'PSCBUSER),JUSER    OWNER?
         BE    JSCN                YES.
         B     JPSW                NO, IGNORE.
         DROP  R1
JSCN     CLR   R3,R5
         BH    JTUS
         CLI   0(R3),C' '
         BE    JTUS
         OI    SWJBSC,SWJU
         B     JCNQS
SBMMVUS  MVC   JUSER(*-*),0(R6)    << EXECUTED >>
JTPS     TM    SWJBSC,SWJU
         BZ    JPSW
JTUS     CP    CCJB,=PL1'+1'       JOB STATEMENT IN ONLY ONE CARD?
         BE    JPSW                YES.
         XC    GPWAR(GPWAL-L'GPWA),GPWAR     GET PASSWORD FROM RACF.
        SETAUTH
        ICHEINTY LOCATE,TYPE='USR',ENTRY=JUSERL,                       1
               ACTIONS=(GPSW),WKAREA=GPWA
         LR    R6,R15
        RESAUTH
         LTR   R6,R6
         BZ    PSWCTL
         CVD   R6,WORK
         BAS   R14,EDIT
         MVC   MSEPL+32(2),WORK+2
         LA    R2,MSEPL
         BAS   R8,SMSG
         B     JPSW
PSWCTL   CLC   GPPL,=XL2'0008'
         BNE   JPSW
         MVI   INSTXTP,C' '        DECODE RACF PASSWORD.
         MVC   INSTXTP+1(10),INSTXTP
        CALL   PSWREST,(GPPW,INSTXTP,LGPPW),VL
         LTR   R15,R15
         BNZ   JPSW
         CLI   INSTXTP,C' '
         BE    JPSW
         LA    R1,INSTXTP+8        ADD JOB PASSWORD KEYWORD.
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(2,R1),=CL2'),'
         LA    R2,INSTXT
         BAS   R8,SUBMT
         LTR   R15,R15
         BNZ   JSUE
JPSW     OI    SWJBSC,SWJB
JPR      LA    R2,TEXT
         BAS   R8,SUBMT
         LTR   R15,R15
         BZ    READ
JSUE     MVC   MSEIR+8(15),=CL15'INTERNAL READER'
         OI    SWITCH,SWEI         SET ERROR.
         AIF   ('&SYSPARM' NE 'NOVSAM').O4S1
JSUC     CVD   R15,WORK            SET R.C. IN MESSAGE.
         BAS   R14,EDIT
         MVC   MSEIR+35(2),WORK+2
         LA    R2,MSEIR
         BAS   R8,SMSG
         AGO   .O4S2
.O4S1    ANOP
JSUC     BAS   R8,SETRC
.O4S2    ANOP
JPURGE   MVI   TEXT,C' '           PURGE JOB.
         MVC   TEXT+1(L'TEXT-1),TEXT
         LA    R2,TEXT
         MVC   TEXT(7),=CL7'/*PURGE'    JES2 PURGE.
         TM    SWITCH,SWSI         STARTED?
         BO    SCLS                YES.
         OI    SWITCH,SWEI         NO, BYPASS.
         B     SCLS
         EJECT
*        END OF PROCESSING.
*        ------------------
         SPACE 1
ENDS     MVI   TEXT,C' '           CLOSE JOB.
         MVC   TEXT+1(L'TEXT-1),TEXT
         LA    R2,TEXT
         MVC   TEXT(2),INSTXT      NULL JCL.
SCLS     BAS   R8,SUBMT
        $RCLOSE SBDDN
         MVC   TEXT(7),=CL7'/*EOF  '    JES2 END-OF-FILE.
         BAS   R8,SUBMT
         AIF   ('&SYSPARM' NE 'NOVSAM').O5S1
        $UCLOSE TXTDDN
         XR    R9,R9               C.C. = 0.
         AGO   .O5S2
.O5S1    TM    SWITCH,SWEI         IN ERROR?
         BO    CLINR               YES.
        ENDREQ RPL=REQPL           GET JOB NUMBER.
         LTR   R15,R15             ENDREQ OK?
         BZ    SETJN               YES.
         MVC   MSEIR+8(15),=CL15'IN.RDR - ENQREQ'
         BAS   R8,SETRC
         B     CLINR
SETJN    MVC   MSJNN+26(8),REQPL+(RPLRBAR-IFGRPL) MOVE JOB NUMBER.
         LA    R2,MSJNN+29
         CLI   0(R2),C'0'
         BNE   *+L'*+12
         MVI   0(R2),C' '
         LA    R2,1(R2)
         B     *-16
         LA    R2,MSJNN
         BAS   R8,SMSG
CLINR   CLOSE  (INRDR)             CLOSE JES2 ACB.
         LR    R9,R15              SET R.C. OF CLOSE AS C.C.
.O5S2    ANOP
LEAVE   $XRET  CC=(R9)
         SPACE 1
         AIF   ('&SYSPARM' EQ 'NOVSAM').O5S3
*        SET R.C IN MESSAGE ROUTINE.
*        ---------------------------
*              R15 = R.C.
         SPACE 1
SETRC    CVD   R15,WORK            SET R.C. IN MESSAGE.
         BAS   R14,EDIT
         MVC   MSEIR+35(2),WORK+2
         LA    R2,MSEIR
.O5S3    SPACE 1
*        SEND MESSAGE ROUTINE.
*        ---------------------
*              R0, R1, R14, R15 = WORK REGISTERS.
*              R2 = MESSAGE ADDRESS (WTO).
*              R8 = LINK REGISTER.
         SPACE 1
SMSG     TM    SWITCH,SWST         STARTED TASK?
         BO    SMSG1               YES.
         LH    R0,0(R2)
         TM    SWITCH,SWTS         TSO?
         BO    SMSG2               YES.
         LR    R1,R2
         AR    R1,R0
         MVC   0(4,R1),MSSKEL+6
SMSG1   WTO    MF=(E,(R2))
         BR    R8
SMSG2    SH    R0,=H'4'
         LA    R1,4(R2)
        TPUT   (1),(0)
         BR    R8
         SPACE 2
*        EDIT 'WORK' ROUTINE.
*        --------------------
*              R14 = LINK REGISTER.
         SPACE 1
EDIT     MVC   WORK(4),=XL4'40202120'
         ED    WORK(4),WORK+6
         BR    R14
         EJECT
*        SUBMIT CARD ROUTINE.
*        --------------------
*              R0, R1, R14, R15 = WORK REGISTERS.
*              R2 = CARD IMAGE ADDRESS.
*              R8 = LINK REGISTER.
*        AT EXIT, R15 = CC, UNLESS PREVIOUS ERROR DETECTED.
         SPACE 1
SUBMT    TM    SWITCH,SWEI         IN ERROR?
         BOR   R8                  YES, BYPASS.
         OI    SWITCH,SWSI         SAY STARTED.
         AIF   ('&SYSPARM' EQ 'NOVSAM').O7S1
        MODCB  RPL=REQPL,AREA=(R2)
         LTR   R15,R15
         BNZR  R8
        PUT    RPL=REQPL
         AGO   .O7S2
.O7S1   $PUNCH (R2),,TXTDDN
.O7S2    BR    R8
         EJECT
*        CONSTANTS AND WORK AREAS.
*        -------------------------
         SPACE 1
WORK     DC    D'0'
ANSWER   DS    0F                  EXTRACT RESPONSES.
ACOMM    DC    A(*-*)
ATSO     DC    A(*-*)
APSCB    DC    A(*-*)
SCTXT    DC    A(TEXT,1,TEXT+70)
LGPPW    DC    A(L'GPPW)
         SPACE 1
        PRINT  NOGEN
MSEIR   WTO    '---                 ERROR (R15=  ) - SUBMIT FAILED -', 1
               ROUTCDE=1,DESC=3,MF=L
MSEJB   WTO    '--- JOB CARD ERROR (C=  ,P=  ) - SUBMIT FAILED -',     1
               ROUTCDE=1,DESC=3,MF=L
MSEPL   WTO    '--- RACF-ICHEINTY ERROR (RC=  ) - SUBMIT FAILED -',    1
               ROUTCDE=1,DESC=3,MF=L
         AIF   ('&SYSPARM' EQ 'NOVSAM').O10S1
MSJNN   WTO    '--- EU-S00I          (        ) SUBMITTED.',           1
               ROUTCDE=1,DESC=3,MF=L
.O10S1   ANOP
MSSKEL  WTO    '  ',ROUTCDE=11,DESC=7,MF=L
        PRINT  GEN
         EJECT
GPSW    ICHEACTN FIELD=PASSWORD,MF=L
GPWA     DC    A(GPWAL)            WORK AREA LENGTH.
GPWAR    DC    XL20'0'             RESERVED.
         DC    F'0'                ACTION RESPONSE LENGTH.
GPPL     DC    XL2'0'              PASSWORD LENGTH (8).
GPPW     DC    XL8'0'              PASSWORD.
         DC    XL2'0'
GPWAL    EQU   *-GPWA
         SPACE 1
SWJBSC   DC    AL1(SWJS)
SWJB     EQU   X'01'               JOB STATEMENT PROCESSED.
SWJA     EQU   X'02'               START-STOP TEXT QUOTES.
SWJN     EQU   X'04'               JOB STATEMENT CONTINUATION.
SWJU     EQU   X'08'               USER= KEYWORD PROCESSED.
SWJS     EQU   X'80'               BYPASS JOB CARD PROCESSING.
         SPACE 1
         DC    AL1(L'TEXT)
TEXT     DC    CL80' '
         DC    AL1(L'INSTXT+L'INSTXTT+L'INSTXTP)
INSTXT   DC    CL15'// '
INSTXTT  DC    CL10'PASSWORD=('
INSTXTP  DC    CL35'               * SUBMIT SUPPLIED * '
         AIF   ('&SYSPARM' EQ 'NOVSAM').O10S2
         DC    CL20' '
         AGO   .O10S3
.O10S2   ANOP
TXTDDN   DC    CL8'SYSSU'
.O10S3   ANOP
SBDDN    DC    CL8'SYSPO'
SWITCH   DC    XL1'0'
SWST     EQU   X'80'               STARTED TASK.
SWTS     EQU   X'40'               TSO TASK.
SWSI     EQU   X'02'               INTERNAL READER STARTED.
SWEI     EQU   X'01'               INTERNAL READER ERROR.
JUSERL   DC    AL1(*-*)
JUSER    DC    CL8' '
CCJB     DC    PL2'+0'
AUTH     DC    XL1'0'
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUTH)
         AIF   ('&SYSPARM' EQ 'NOVSAM').O15S1
         EJECT
INRDR   ACB    MACRF=(ADR,SEQ,OUT),DDNAME=SYSSU
         EJECT
REQPL   RPL    ACB=INRDR,RECLEN=80,AREALEN=80,OPTCD=(ADR,SEQ,SYN,NUP)
.O15S1   EJECT
        LTORG
         SPACE 2
*        DSECT DEFINITIONS.
*        ------------------
         SPACE 1
        PRINT  NOGEN
         SPACE 1
        IKJPSCB
CIBDSECT DSECT
CIB     IEZCIB
         AIF   ('&SYSPARM' EQ 'NOVSAM').O20S1
        IFGRPL
.O20S1   SPACE 2
         END
