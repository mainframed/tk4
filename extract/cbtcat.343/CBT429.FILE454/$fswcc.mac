         MACRO
&LABEL  $FSWCC
.*
.* MACRO : INTERNALLY USED BY $FS MACRO.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLA  &GMFCI
         GBLB  &GFUSW,&GFESW,&GFISW
         GBLC  &GFSMF,&GFSMI
         LCLA  &ATT,&N,&I,&L,&M1,&M2,&M3,&M4,&M5,&NR
         LCLB  &CLRSW
         LCLC  &ATTRB,&OFF,&RG
         AIF   (NOT &GFUSW).USEERR
&N       SETA  N'&SYSLIST(1)
         AIF   (&N EQ 0).ARGERR
         AIF   ('&SYSLIST(1)' EQ 'OFF').OFF
         AIF   ('&SYSLIST(1)' EQ 'CLEAR').CLR
         AIF   ('&SYSLIST(1,1)' NE 'OFF').TSC
.OFF     ANOP
         AIF   (&N GT 2).ARGERR
&CLRSW   SETB  1
&M1      SETA  1
&M2      SETA  2
&L       SETA  12
&OFF     SETC  '11404013'
         AIF   (&N EQ 1).GWCC
         AIF   ('&SYSLIST(1,2)' NE 'AL').NAL
&M3      SETA  4
         AGO   .GWCC
.NAL     ANOP
&ATTRB   SETC  '&SYSLIST(1,2)'
         MNOTE 4,'--- UNKNOWN KEYWORD &ATTRB - IGNORED'
         AGO   .GWCC
.TSC     AIF   ('&SYSLIST(1,1)' NE 'CLEAR').SCAN
.CLR     ANOP
&CLRSW   SETB  1
&L       SETA  8
&I       SETA  1
.SCAN    ANOP
&I       SETA  &I+1
         AIF   (&I GT &N).GWCC
         AIF   (&CLRSW).SKIP
         AIF   ('&SYSLIST(1,&I)' EQ 'NE').NE
         AIF   ('&SYSLIST(1,&I)' EQ 'P40').P40
         AIF   ('&SYSLIST(1,&I)' EQ 'P64').P64
         AIF   ('&SYSLIST(1,&I)' EQ 'P80').P80
         AIF   ('&SYSLIST(1,&I)' EQ 'SP').SP
.SKIP    AIF   ('&SYSLIST(1,&I)' EQ 'AL').AL
         AIF   ('&SYSLIST(1,&I)' EQ 'KBR').KBR
         AIF   ('&SYSLIST(1,&I)' EQ 'RMDT').RMDT
&ATTRB   SETC  '&SYSLIST(1,&I)'
         MNOTE 4,'--- UNKNOWN KEYWORD &ATTRB - IGNORED'
         AGO   .SCAN
.NE      ANOP
&M5      SETA  0
         AGO   .SCAN
.P40     ANOP
&M5      SETA  16
         AGO   .SCAN
.P64     ANOP
&M5      SETA  32
         AGO   .SCAN
.P80     ANOP
&M5      SETA  48
         AGO   .SCAN
.SP      ANOP
&M4      SETA  8
         AGO   .SCAN
.AL      ANOP
&M3      SETA  4
         AGO   .SCAN
.KBR     ANOP
&M2      SETA  2
         AGO   .SCAN
.RMDT    ANOP
&M1      SETA  1
         AGO   .SCAN
.GWCC    ANOP
&ATT     SETA  2*(&M1+&M2+&M3+&M4+&M5)+1
&ATTRB   SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5X
               B5C5D5E5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6FX
               7F8F97A7B7C7D7E7F'(&ATT,2)
         AIF   (&GFISW).GIX
         AIF   (&GFESW).GEX
         AIF   (&CLRSW).GCLR
&LABEL   DC    XL1'&ATTRB'
         MEXIT
.GCLR    ANOP
&LABEL   DC    XL&L'&ATTRB.1140403C404000&OFF'
         MEXIT
.GEX     AIF   (&CLRSW).GCLM
&LABEL   MVI   0(&GFSMF),X'&ATTRB'
         LA    &GFSMF,1(,&GFSMF)
         MEXIT
.GCLM    ANOP
&LABEL   MVC   0(&L,&GFSMF),=XL&L'&ATTRB.1140403C404000&OFF'
         LA    &GFSMF,&L.(,&GFSMF)
         MEXIT
.GIX     AIF   ('&GFSMI'(1,1) EQ '(').GIXR
         AIF   (&CLRSW).GCLI
&LABEL   MVI   &GFSMI+&GMFCI,X'&ATTRB'
&GMFCI   SETA  &GMFCI+1
         MEXIT
.GCLI    ANOP
&LABEL   MVC   &GFSMI+&GMFCI.(&L),=XL&L'&ATTRB.1140403C404000&OFF'
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.GIXR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
         AIF   (&CLRSW).GCLIR
&LABEL   MVI   &GMFCI.(&RG),X'&ATTRB'
&GMFCI   SETA  &GMFCI+1
         MEXIT
.GCLIR   ANOP
&LABEL   MVC   &GMFCI.(&L,&RG),=XL&L'&ATTRB.1140403C404000&OFF'
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.USEERR  MNOTE 8,'--- INVALID MACRO USAGE'
         MEXIT
.ARGERR  MNOTE 8,'--- WCC=(...) INVALID CODING'
         MEND
