ISREDIT  MACRO (WHAT)
/*  MACRO NAME : PLUG                                               */
/* DESCRIPTION : EDIT MACRO TO SET CURSOR AT END OF CURRENT         */
/*               SCREEN LINE                                        */
/*      AUTHOR : J. KALINICH, X4521                                 */
/*  CBT ORIGIN : EXTRACTED FROM CBT TAPE 94 APR, FILE 078           */
/*  ADAPTED BY : MOINIL P.A.                                        */
  IF (&STR(&WHAT) EQ &STR(HELP)) OR +
    (&STR(&WHAT) EQ &STR(H)) THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJEOL)
    SET &EOLCC = &LASTCC
    ISPEXEC CONTROL DISPLAY RESTORE
    EXIT CODE(&EOLCC)
    END
  ISREDIT (CROW,CCOL) = CURSOR
  SET SCCOL = &CCOL
  ISREDIT (WDTH) = DATA_WIDTH
  ISPEXEC CONTROL ERRORS RETURN
  ISREDIT (LINE) = LINE &CROW
  IF &LASTCC NE 0 THEN DO
    SET CROW = &CROW - 1
    END
  ELSE DO
    SET I = &WDTH
    SET CCOL = 0
    DO WHILE &I GE 1
      IF &SUBSTR(&I:&I,&NRSTR(&LINE)) NE &Z THEN DO
        SET CCOL = &I
        SET I = 1
        END
      SET I = &I - 1
      END
    IF &CCOL NE &WDTH THEN SET CCOL = &CCOL + 1
/*       IF ALREADY AT EOL, THEN JUMP TO NEXT LINE AND GO TO EOL.   */
/*       NOTE : THIS CAN NOT JUMP OVER MORE THAN 1 EXCLUDED LINE.   */
    IF &SCCOL EQ &CCOL THEN DO
      SET CROW = &CROW + 1
      ISREDIT (LINE) = LINE &CROW
      IF &LASTCC NE 0 THEN DO
        SET CROW = &CROW - 1
        END
      ELSE DO
        SET I = &WDTH
        SET CCOL = 0
        DO WHILE &I GE 1
          IF &SUBSTR(&I:&I,&NRSTR(&LINE)) NE &Z THEN DO
            SET CCOL = &I
            SET I = 1
            END
          SET I = &I - 1
          END
        IF &CCOL NE &WDTH THEN SET CCOL = &CCOL + 1
        END
      END
    END
  ISREDIT CURSOR = &CROW &CCOL
  EXIT CODE(0)
