         MACRO
&LABEL  $FSCC
.*
.* MACRO : INTERNALLY USED BY $FS MACRO.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLA  &GMFCI
         GBLB  &GFUSW,&GFESW,&GFISW
         GBLC  &GFSMF,&GFSMI
         LCLA  &NR
         LCLB  &SW
         LCLC  &CC,&OP,&RG
         AIF   (NOT &GFUSW).USEERR
         AIF   (N'&SYSLIST NE 1).ARGERR
         AIF   ('&SYSLIST(1)'(1,1) NE '(').NMRL
&NR      SETA  K'&SYSLIST(1)-2
&CC      SETC  '&SYSLIST(1)'(2,&NR)
&SW      SETB  1
         AGO   .TSTC
.NMRL    ANOP
&CC      SETC  '&SYSLIST(1)'
&SW      SETB  0
.TSTC    AIF   ('&CC' EQ 'W').W
         AIF   ('&CC' EQ 'EW').EW
         AIF   ('&CC' EQ 'EWA').EWA
         AIF   ('&CC' EQ 'EAU').EAU
         AIF   ('&CC' NE 'WSF').ARGERR
&OP      SETC  'F3'
.TGS     AIF   (&GFISW).SMVI
         AIF   (&GFESW).SMVL
&LABEL   DC    XL1'&OP'
         MEXIT
.SMVL    ANOP
&LABEL   MVI   0(&GFSMF),X'&OP'
         LA    &GFSMF,1(,&GFSMF)
         MEXIT
.SMVI    AIF   ('&GFSMI'(1,1) EQ '(').SMVIR
&LABEL   MVI   &GFSMI+&GMFCI,X'&OP'
&GMFCI   SETA  &GMFCI+1
         MEXIT
.SMVIR   ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVI   &GMFCI.(&RG),X'&OP'
&GMFCI   SETA  &GMFCI+1
         MEXIT
.W       ANOP
&OP      SETC  'F1'
         AGO   .TGX
.EW      ANOP
&OP      SETC  'F5'
         AGO   .TGX
.EWA     ANOP
&OP      SETC  '7E'
         AGO   .TGX
.EAU     ANOP
&OP      SETC  '6F'
.TGX     AIF   (&SW).TGS
         AIF   (&GFISW).MVI
         AIF   (&GFESW).MVL
&LABEL   DC    XL2'27&OP'
         MEXIT
.MVL     ANOP
&LABEL   MVC   0(2,&GFSMF),=XL2'27&OP'
         LA    &GFSMF,2(,&GFSMF)
         MEXIT
.MVI     AIF   ('&GFSMI'(1,1) EQ '(').MVIR
&LABEL   MVC   &GFSMI+&GMFCI.(2),=XL2'27&OP'
&GMFCI   SETA  &GMFCI+2
         MEXIT
.MVIR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(2,&RG),=XL2'27&OP'
&GMFCI   SETA  &GMFCI+2
         MEXIT
.USEERR  MNOTE 8,'--- MACRO USAGE INVALID'
         MEXIT
.ARGERR  MNOTE 8,'--- CC=... INVALID CODING'
         MEND
