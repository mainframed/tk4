SIZE     TITLE 'TSO SCRSZ COMMAND - GET THE CURRENT SCREEN SIZE.'
SCRSZ    START 0
         SPACE 1
* PURPOSE :    SET A RETURN CODE IN REGISTER 15 TO INFORM ABOUT THE
* ---------    CURRENT TERMINAL TYPE AND SCREEN SIZE.
*              THE RETURN CODE (REGISTER 15) MAY BE AS FOLLOWS :
*                  0 = NOT A DISPLAY TERMINAL.
*                  1 = TERMINAL SIZE (24,80)
*                  2 = TERMINAL SIZE (27,132)
*                  3 = TERMINAL SIZE (32,80)
*                  4 = TERMINAL SIZE (43,80)
*                 99 = UNKNOWN TERMINAL SIZE.
*                100 = NOT TSO ENVIRONMENT.
*
* AUTHOR :     MOINIL P.A.
* ---------    COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R11,R13             SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R11
         MVC   EXTR(LEXTR),EXTRP
         XC    ATSO,ATSO
        EXTRACT ATSO,'S',FIELDS=(TSO),MF=(E,EXTR)
         L     R2,ATSO
         TM    0(R2),X'80'
         BZ    NOTTSO              WE ARE'NT IN TSO.
        GTSIZE
         LTR   R15,R15
         BNZ   EXITUNK             ERROR RETURN CODE.
         LTR   R15,R0
         BZ    EXIT                NOT DISPLAY TERMINAL.
         CL    R1,=F'80'           TEST LINE LENGTH.
         BE    T1
         CL    R1,=F'132'
         BNE   EXITUNK
         CL    R0,=F'27'           TEST NUMBER OF LINES.
         BNE   EXITUNK
         LA    R15,2
         B     EXIT
T1       CL    R0,=F'24'
         BNE   T2
         LA    R15,1
         B     EXIT
T2       CL    R0,=F'32'
         BNE   T3
         LA    R15,3
         B     EXIT
T3       CL    R0,=F'43'
         BNE   EXITUNK
         LA    R15,4
         B     EXIT
NOTTSO   LA    R15,100             SET NOT TSO FOREGROUND C.C.
         B     EXIT
EXITUNK  LA    R15,99              SET UNKNOWN C.C.
EXIT    $XRET  CC=(R15),LV=WRKL,TYPE=RENT
         EJECT
*        CONSTANTS AND SKELETONS.
         SPACE 1
EXTRP   EXTRACT *-*,'S',MF=L
         SPACE 2
        LTORG
         SPACE 2
*        WORK AREAS DESCRIPTION.
         SPACE 1
WRKDSECT DSECT
         SPACE 1
SVA      DS    18F                 LOCAL SAVE AREA.
         SPACE 1
EXTR    EXTRACT *-*,'S',MF=L
LEXTR    EQU   *-EXTR
         SPACE 1
ATSO     DS    F
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 2
         END
