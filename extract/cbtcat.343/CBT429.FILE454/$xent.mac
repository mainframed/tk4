         MACRO           ENTER EXECUTION.
&NAME   $XENT  &TYPE=NORENT,&BASE=(11,12),&B1=,&B2=,                   1
               &ID=,&DT=YES,&LV=72,&SP=0
         LCLA  &SN,&ST
         LCLC  &NCS,&R1,&R2,&BS
         SPACE 1
.*
.* $XENT MACRO : ENTER CONTROL.
.* -------------
.*
.*    1. TYPE=NORENT (NOT REENTERABLE)
.*
.*       - STORE THE REGISTERS IN THE CALLER'S SAVE AREA (REGISTER 13).
.*       - SET ONE OR MORE BASE REGISTERS (BASE OR B1 AND B2 OPERANDS).
.*       - GENERATE THE IDENTIFIER NAME.
.*       - GENERATE THE ASSEMBLY DATE AND TIME (DT OPERAND).
.*       - LINK A NEW SAVE AREA (NEW REGISTER 13), WHICH IS
.*         LOCATED AT THE ENTRY POINT ADDRESS + 32 (X'20').
.*
.*    2. TYPE=RENT (REENTERABLE)
.*
.*       - STORE THE REGISTERS IN THE CALLER'S SAVE AREA (REGISTER 13).
.*       - SET ONE OR MORE BASE REGISTERS (BASE OR B1 AND B2 OPERANDS).
.*       - GENERATE THE IDENTIFIER NAME.
.*       - GENERATE THE ASSEMBLY DATE AND TIME (DT OPERAND).
.*       - ACQUIRE (GETMAIN) MAIN STORAGE AREA (L AND S OPERAND) INTO
.*         WHICH IS CREATED, AT THE STARTING ADDRESS, THE NEW SAVE
.*         AREA (NEW REGISTER 13).
.*
.* TYPE OPERAND       - RENT OR NORENT (DEFAULT).
.*
.* BASE OPERAND       - CAN BE USED TO DEFINE THE DESIRED BASE
.*                      REGISTERS (DEFAULT VALUES ARE 11 AND 12).
.*                      MORE REGISTERS VALUES SEPARATED BY COMMAS
.*                      AND ENCLOSED WITHIN PARENTHESES MAY BE
.*                      SPECIFIED. THE FIRST BASE REGISTER POINTS
.*                      TO THE ENTRY ADDRESS, AND THE SUCCESSIVE
.*                      ARE INCREMENTED BY A DISPLACEMENT OF +4096.
.*                      THIS OPERAND SUPERSEDES B1 AND B2 OPERANDS.
.*                      DO NOT SPECIFY REGISTER 0, 1, 13, 14 OR 15
.*                      TO ANY VALUE OF THIS OPERAND.
.*
.* B1 AND B2 OPERANDS - THESE OPERANDS ARE SUPERSEDED BY THE BASE
.*                      OPERAND. THEY ARE MAINTAINED FOR OLD
.*                      SOURCE COMPATIBILITY ONLY.
.*                      CAN BE USED TO DEFINE THE DESIRED BASE
.*                      REGISTERS (DEFAULT VALUES ARE 11 AND 12,
.*                      WHICH RESPECTIVELY POINT TO THE ENTRY
.*                      ADDRESS AND THE ENTRY ADDRESS + 4096).
.*                      DO NOT SPECIFY REGISTER 0, 1, 13, 14 OR 15
.*                      TO ANY ONE OF THESE OPERANDS.
.*
.* ID OPERAND         - IDENTIFIER NAME (8 CHARACTERS LENGTH MAXIMUM).
.*                      IF OMITTED, THE LABEL NAME IS USED IF SUPPLIED,
.*                      OR THE CURRENT CONTROL SECTION NAME.
.*
.* DT OPERAND         - CAN BE USED TO SUPPRESS THE ASSEMBLY DATE AND
.*                      TIME GENERATION (I.E. - DT=NO).
.*
.* LV OPERAND         - IS THE LENGTH OF THE MAIN STORAGE AREA TO BE
.*                      ACQUIRED (LV OPERAND OF THE GETMAIN R-TYPE
.*                      MACRO). DEFAULT VALUE IS 72 (18*4 IS EQUAL
.*                      TO A SAVE AREA).
.*
.* SP OPERAND         - IS THE SUBPOOL NUMBER OF THE MAIN STORAGE TO
.*                      BE ALLOCATED (SP OPERAND OF THE GETMAIN R-TYPE
.*                      MACRO). DEFAULT VALUE IS 0 (ZERO).
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   ('&B1' EQ '').T1
&R1      SETC  '&B1'
&R2      SETC  '&B2'
&ST      SETA  0
         AGO   .T3
.T1      AIF   ('&B2' NE '').ERR1
         AIF   ('&BASE' EQ '').ERR1
&R1      SETC  '&BASE(1)'
&R2      SETC  '&BASE(2)'
&ST      SETA  1
         AIF   ('&BASE'(1,1) EQ '(').T2
&BS      SETC  '&BASE'
         AGO   .T3
.T2      ANOP
&SN      SETA  K'&BASE-2
&BS      SETC  '&BASE'(2,&SN)
.T3      AIF   ('&R1' EQ '&R2').ERR2
         AIF   ('&NAME' EQ '').N0
&NCS     SETC  '&NAME'
         AGO   .N1
.N0      ANOP
&NCS     SETC  '&SYSECT'
.N1      AIF   (&ST EQ 0).N2
         USING &NCS,&BS SET ADDRESSABILITY.
         AGO   .N4
.N2      AIF   ('&R2' NE '').N3
         USING &NCS,&R1 SET ADDRESSABILITY.
         AGO   .N4
.N3      USING &NCS,&R1,&R2 SET ADDRESSABILITY.
.N4      SPACE 1
         CNOP  0,8
&NAME    STM   14,12,12(13) SAVE REGISTERS.
         AIF   ('&TYPE' EQ 'NORENT').N10
         AIF   ('&TYPE' NE 'RENT').ERR3
         AIF   ('&DT' EQ 'YES').N5
         B     16(,15) BRANCH AROUND ID.
         AGO   .N6
.N5      B     30(,15) BRANCH AROUND ID, DATE AND TIME.
.N6      SPACE 1
         AIF   ('&ID' EQ '').N7
         DC    CL8'&ID' IDENTIFIER.
         AGO   .N8
.N7      DC    CL8'&NCS' IDENTIFIER.
.N8      AIF   ('&DT' NE 'YES').N9
         DC    CL8'&SYSDATE' ASSEMBLY DATE (MM/DD/YY).
         DC    C'-'
         DC    CL5'&SYSTIME' ASSEMBLY TIME (HH.MM).
.N9      SPACE 1
         LR    &R1,15 SET BASE REGISTER.
        GETMAIN R,LV=&LV,SP=&SP
         LR    15,13 PREVIOUS SAVE AREA.
         LR    13,1 CURRENT SAVE AREA.
         XC    0(18*4,13),0(13) CLEAR SAVE AREA.
         ST    13,8(15) LINK SAVE AREAS.
         ST    15,4(13)
         LM    14,1,12(15) RESTORE ENTRY REGISTERS.
         AGO   .N17
.N10     LR    &R1,15 SET BASE REGISTER.
         AIF   ('&DT' EQ 'YES').N11
         LR    15,13 PREVIOUS SAVE AREA.
         LA    13,32(&R1) CURRENT SAVE AREA.
         ST    13,8(15) LINK SAVE AREAS.
         ST    15,4(13)
         B     *+L'*+(18*4)+8 BRANCH AROUND ID AND SAVE AREA.
         AGO   .N12
.N11     B     *+L'*+(18*4)+22 BRANCH AROUND ID, DATE, TIME AND S.A.
.N12     SPACE 1
         AIF   ('&ID' EQ '').N13
         DC    CL8'&ID' IDENTIFIER.
         AGO   .N14
.N13     DC    CL8'&NCS' IDENTIFIER.
.N14     AIF   ('&DT' NE 'YES').N15
         DC    CL8'&SYSDATE' ASSEMBLY DATE (MM/DD/YY).
         DC    C'-'
         DC    CL5'&SYSTIME' ASSEMBLY TIME (HH.MM).
.N15     DC    18F'0' . . . . . . . . . . SAVE AREA.
         SPACE 1
         AIF   ('&DT' EQ 'YES').N16
         AIF   ('&R2' EQ '').END
         AGO   .N18
.N16     LR    15,13 PREVIOUS SAVE AREA.
         LA    13,32(&R1) CURRENT SAVE AREA.
         ST    13,8(15) LINK SAVE AREAS.
         ST    15,4(13)
.N17     AIF   ('&R2' EQ '').END
.N18     LA    &R2,2048(0,&R1)
         LA    &R2,2048(0,&R2)
         AIF   (&ST EQ 0).END
&SN      SETA  3
.N19     AIF   ('&BASE(&SN)' EQ '').END
&ST      SETA  1
.N20     AIF   ('&BASE(&SN)' EQ '&BASE(&ST)').ERR2
&ST      SETA  &ST+1
         AIF   (&ST LT &SN).N20
         LA    &BASE(&SN),2048(0,&BASE(&SN-1))
         LA    &BASE(&SN),2048(0,&BASE(&SN))
&SN      SETA  &SN+1
         AGO   .N19
.ERR1    MNOTE 8,' FIRST BASE REGISTER UNDEFINED ******************** '
         AGO   .END
.ERR2    MNOTE 8,' IDENTICAL BASE REGISTERS ************************* '
         AGO   .END
.ERR3    MNOTE 8,' INVALID TYPE OPERAND SPECIFIED ******************* '
.END     SPACE 1
         MEND
