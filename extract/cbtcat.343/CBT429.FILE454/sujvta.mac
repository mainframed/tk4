SUVT     TITLE 'SEARCH IEFUJV - UJVTABLE SUBROUTINE.'
SUJVTA   START 0
         SPACE 1
         ENTRY DUJVTA
         SPACE 1
* CALLING SEQUENCES : CALL SUJVTA
* -------------------
*
*        PURPOSE : SEARCH IN IEFUJV MODULE THE UJVTABLE SECTION START
*                  ADDRESS AND PASS IT BACK TO CALLER.
*        AT EXIT : R15 = 0 AND R1 = UJVTABLE SECTION START ADDRESS.
*                      NOTE THAT THE IEFUJV SMF EXIT ROUTINE IS IN THE
*                      LPA (LLA ADDRESS SPACE).
*                  R15 = 4 AND R1 = 0 IF UJVTABLE SECTION NOT FOUND
*                      (DELETE HAS BEEN DONE).
*
*                     CALL DUJVTA
*
*        PURPOSE : DELETE IEFUJV MODULE (UJVTABLE SECTION) ACCESS.
*        AT EXIT : R15 = 0
*
* GENERAL NOTICE : THIS ROUTINE IS RE-ENTERABLE.
* ================
*
* AUTHOR :     P.A. MOINIL
* -------      COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12,TYPE=RENT
        LOAD   EP=IEFUJV
         LR    R5,R0
         L     R4,=F'-4'
         LR    R3,R0
         SLL   R1,8
         SRL   R1,8
         BCTR  R1,0
         BCTR  R1,0
         LTR   R1,R1
         BNP   QUIT
         SLL   R1,3
         ALR   R3,R1
LOOK     CLC   0(8,R3),=CL8'UJVT-PT='
         BNE   NEXT
         CLC   12(4,R3),=CL8'UJVT-PT='
         BE    UJVTFND
NEXT     BXH   R3,R4,LOOK
QUIT    DELETE EP=IEFUJV
         XR    R2,R2
         LA    R9,4
         B     LEAVE
UJVTFND  L     R2,8(R3)
         XR    R9,R9
LEAVE    L     R1,4(R13)
         ST    R2,24(R1)
        $XRET  CC=(R9),TYPE=RENT
         DROP  R12
         SPACE 1
        LTORG
         EJECT
        CNOP   0,8
DUJVTA  $XENT  BASE=R12,DT=NO,TYPE=RENT
        DELETE EP=IEFUJV
        $XRET  CC=0,TYPE=RENT
         DROP  R12
         SPACE 2
         END
