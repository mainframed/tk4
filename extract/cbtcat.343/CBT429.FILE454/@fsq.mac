         MACRO           CALL FSRTNQ SUPPLY.
&NAME   @FSQ   &A,&MF=
         LCLA  &Y
         LCLB  &W
         LCLC  &X
.*
.* @FSQ MACRO : CALL FSRTNQ SUPPLY (SEE FSRTN).
.* ------------
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   (N'&SYSLIST LE 1).WHT
         AIF   (N'&SYSLIST GT 2).BAD
         AIF   (N'&SYSLIST(1) NE 0).BAD
         AIF   (N'&SYSLIST(2) NE 0).BAD
.NL1     ANOP
&NAME    XR    1,1                 NO PARM. LIST.
         L     15,=V(FSRTNQ)       LOAD REG. 15 WITH ENTRY.
         BASR  14,15               BRANCH TO ENTRY POINT.
         MEXIT
.WHT     ANOP
&W       SETB  0
         AIF   ('&MF' EQ '').XI1
         AIF   ('&MF' EQ 'E').XI1
         AIF   ('&MF' NE 'L').XL1
         AIF   (N'&SYSLIST EQ 0).LS1
         AIF   (T'&A EQ 'O').LS1
         AIF   ('&A'(1,1) EQ '(').REG
&NAME    DC    A(&A+X'80000000')   LAST AND ONLY P.P. PARAMETER.
         MEXIT
.LS1     ANOP
&NAME    DC    A(X'80000000')      LAST AND ONLY P.P. PARAMETER.
         MEXIT
.XL1     AIF   ('&MF(1)' NE 'L').XL2
         AIF   ('&MF(2)' EQ '').ERR
         AIF   (N'&SYSLIST EQ 0).MIS
         AIF   (T'&A EQ 'O').MIS
&W       SETB  1
         AGO   .XL3
.XL2     AIF   ('&MF(1)' NE 'E').ERR
         AIF   ('&MF(2)' EQ '').XI1
.XL3     AIF   ('&MF(2)'(1,1) EQ '(').XL4
&NAME    LA    1,&MF(2)
         AGO   .XL10
.XL4     AIF   ('&MF(2)' NE '(1)').XL5
         AIF   ('&NAME' EQ '').XL10
&NAME    DS    0H
         AGO   .XL10
.XL5     ANOP
&Y       SETA  K'&MF(2)-2
&X       SETC  '&MF(2)'(2,&Y)
&NAME    LR    1,&X
.XL10    AIF   (N'&SYSLIST EQ 0).XL13
         AIF   (T'&A EQ 'O').XL13
         AIF   ('&A'(1,1) EQ '(').XL11
         LA    15,&A
         ST    15,0(1)             STORE INTO PARM. LIST.
         AGO   .XL12
.XL11    ST    &A(1,1),0(1)        STORE INTO PARM. LIST.
.XL12    OI    0(1),X'80'          SET LAST WORD BIT ON.
.XL13    AIF   (&W).XL14
         L     15,=V(FSRTNQ)       LOAD REG. 15 WITH ENTRY.
         BASR  14,15               BRANCH TO ENTRY POINT.
.XL14    MEXIT
.XI1     AIF   (N'&SYSLIST EQ 0).NL1
         AIF   (T'&A EQ 'O').NL1
&X       SETC  'IHB'.'&SYSNDX'
         AIF   ('&NAME' EQ '').XI2
&NAME    DS    0H
.XI2     AIF   ('&A'(1,1) NE '(').XI3
         ST    &A(1,1),&X          STORE INTO PARM. LIST.
         OI    &X,X'80'            SET LAST WORD BIT ON.
.XI3     LA    1,&X                LOAD LIST ADDRESS IN REG. 1.
         B     &X.A                BYPASS LIST.
         CNOP  0,4
&X       DS    0F
         AIF   ('&A'(1,1) EQ '(').XI4
         DC    A(&A+X'80000000')   LAST AND ONLY P.P. PARAMETER.
         AGO   .XI5
.XI4     DC    A(X'80000000')      LAST AND ONLY P.P. PARAMETER.
.XI5     ANOP
&X.A     L     15,=V(FSRTNQ)       LOAD REG. 15 WITH ENTRY.
         BASR  14,15               BRANCH TO ENTRY POINT.
         MEXIT
.MIS     MNOTE 8,' MISSING OPERAND ********************************** '
         MEXIT
.BAD     MNOTE 8,' TOO MANY OPERANDS ******************************** '
         MEXIT
.REG     MNOTE 8,' REGISTER NOTATION INVALID ************************ '
         MEXIT
.ERR     MNOTE 8,' MF= INVALID CODING ******************************* '
         MEND
