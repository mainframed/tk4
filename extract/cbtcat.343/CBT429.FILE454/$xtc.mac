         MACRO           CALL XTC/XTCE SUPPLY.
&NAME   $XTC   &A,&B,&C,&D,&MSG=,&MF=,&TSO=E
         LCLA  &Y,&Z
         LCLC  &X,&NME
.*
.* $XTC MACRO : CALL XTC/XTCE SUPPLY (SEE XTC/XTCE).
.* ------------
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   (N'&SYSLIST GT 4).BAD
&NME     SETC  '&NAME'
         AIF   ('&MF' NE 'L').XQ
         AIF   ('&MSG' EQ '').A1
         MNOTE 0,' MSG= IS ONLY EXECUTE FORM OPERAND **************** '
.A1      ANOP
&Z       SETA  1
.B1      AIF   (N'&SYSLIST EQ &Z).F1
         AIF   (T'&SYSLIST(&Z) EQ 'O').C1
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').INV
&NME     DC    A(&SYSLIST(&Z))     P.P. PARAMETER.
         AGO   .D1
.C1      ANOP
&NME     DC    A(0)                P.P. PARAMETER.
.D1      AIF   ('&NME' EQ '').E1
&NME     SETC  ''
.E1      ANOP
&Z       SETA  &Z+1
         AGO   .B1
.F1      AIF   (T'&SYSLIST(&Z) EQ 'O').G1
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').INV
&NME     DC    A(&SYSLIST(&Z)+X'80000000')   LAST P.P. PARAMETER.
         MEXIT
.G1      ANOP
&NME     DC    A(X'80000000')      LAST P.P. PARAMETER.
         MEXIT
.XQ      AIF   ('&MF' EQ '').IL
         AIF   ('&MF' NE 'E').OL
.IL      ANOP
&Y       SETA  0
&Z       SETA  1
&X       SETC  'IHB'.'&SYSNDX'
.A2      AIF   (T'&SYSLIST(&Z) EQ 'O').B2
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').D2
.B2      AIF   (N'&SYSLIST EQ &Z).E2
.C2      ANOP
&Y       SETA  &Y+4
&Z       SETA  &Z+1
         AGO   .A2
.D2      ANOP
&NME     ST    &SYSLIST(&Z,1),&X+&Y STORE INTO PARM. LIST.
&NME     SETC  ''
         AIF   (N'&SYSLIST NE &Z).C2
         OI    &X+&Y,X'80'         SET LAST WORD BIT ON.
.E2      ANOP
&NME     LA    1,&X                LOAD LIST ADDRESS IN REG. 1.
&NME     SETC  ''
         B     &X.A                BYPASS LIST.
         CNOP  0,4
&X       DS    0F
&Z       SETA  1
.F2      AIF   (N'&SYSLIST EQ &Z).I2
         AIF   (T'&SYSLIST(&Z) EQ 'O').G2
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').G2
         DC    A(&SYSLIST(&Z))     P.P. PARAMETER.
         AGO   .H2
.G2      DC    A(0)                P.P. PARAMETER.
.H2      ANOP
&Z       SETA  &Z+1
         AGO   .F2
.I2      AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').K2
         AIF   (T'&SYSLIST(&Z) EQ 'O').J2
         DC    A(&SYSLIST(&Z)+X'80000000')   LAST P.P. PARAMETER.
         AGO   .L2
.J2      DC    A(X'80000000')      LAST P.P. PARAMETER.
         AGO   .L2
.K2      DC    A(0)                LAST P.P. PARAMETER.
.L2      ANOP
&X.A     SR    0,0                 SET MSG OPTION
         AGO   .CA
.OL      AIF   ('&MF(1)' NE 'E').MFE
         AIF   ('&MF(2)'(1,1) EQ '(').A3
&NME     LA    1,&MF(2)            LOAD LIST ADDRESS IN REG. 1.
         AGO   .B3
.A3      AIF   ('&MF(2)' EQ '(1)').C3
&Y       SETA  K'&MF(2)-2
&X       SETC  '&MF(2)'(2,&Y)
&NME     LR    1,&X                LOAD LIST ADDRESS IN REG. 1.
.B3      ANOP
&NME     SETC  ''
.C3      ANOP
&Y       SETA  0
&Z       SETA  1
.D3      ANOP
&X       SETC  '&Y'.'(1)'
         AIF   (T'&SYSLIST(&Z) NE 'O').F3
         AIF   (N'&SYSLIST EQ &Z).J3
.E3      ANOP
&Y       SETA  &Y+4
&Z       SETA  &Z+1
         AGO   .D3
.F3      AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').G3
&NME     LA    15,&SYSLIST(&Z,1)
         ST    15,&X               STORE INTO PARM. LIST.
         AGO   .H3
.G3      ANOP
&NME     ST    &SYSLIST(&Z,1),&X   STORE INTO PARM. LIST.
.H3      AIF   ('&NME' EQ '').I3
&NME     SETC  ''
.I3      AIF   (N'&SYSLIST NE &Z).E3
         OI    &X,X'80'            SET LAST WORD BIT ON.
.J3      ANOP
&NME     SR    0,0                 SET MSG OPTION
.CA      AIF   ('&MSG' EQ 'NO').L3
         AIF   ('&MSG' EQ 'ALL').K3
         ICM   0,B'1000',=X'0F'
         AGO   .L3
.K3      ICM   0,B'1000',=X'F0'
.L3      AIF   ('&TSO' EQ 'E').M3
         AIF   ('&TSO' EQ '').N3
         MNOTE 0,' TSO= INVALID CODING, TSO=E ASSUMED *************** '
.M3      L     15,=V(XTCE)         LOAD REG. 15 WITH ENTRY.
         AGO   .O3
.N3      L     15,=V(XTC)          LOAD REG. 15 WITH ENTRY.
.O3      BASR  14,15               BRANCH TO ENTRY POINT.
         MEXIT
.BAD     MNOTE 8,' TOO MANY OPERANDS ******************************** '
         MEXIT
.INV     MNOTE 8,' INVALID OPERAND ********************************** '
         MEXIT
.MFE     MNOTE 8,' MF= INVALID CODING ******************************* '
         MEND
