         MACRO
&LABEL  $FSTXT &TXT
.*
.* MACRO : INTERNALLY USED BY $FS MACRO.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLA  &GMFCI
         GBLB  &GFUSW,&GFESW,&GFISW
         GBLC  &GFSMF,&GFSMI
         LCLA  &N,&I,&L,&W,&NR
         LCLC  &RG
         AIF   (NOT &GFUSW).USEERR
         AIF   ('&TXT'(1,1) NE '(').NLG
         AIF   ('&TXT(2)'(1,1) LT '0').NUMERR
         AIF   (&GFISW).MVI
         AIF   (&GFESW).MVL
&LABEL   DC    CL&TXT(2)&TXT(1)
         MEXIT
.MVL     ANOP
&LABEL   MVC   0(&TXT(2),&GFSMF),=CL&TXT(2)&TXT(1)
         LA    &GFSMF,&TXT(2).(,&GFSMF)
         MEXIT
.MVI     AIF   ('&GFSMI'(1,1) EQ '(').MVIR
&LABEL   MVC   &GFSMI+&GMFCI.(&TXT(2)),=CL&TXT(2)&TXT(1)
&N       SETA  &TXT(2)
&GMFCI   SETA  &GMFCI+&N
         MEXIT
.MVIR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(&TXT(2),&RG),=CL&TXT(2)&TXT(1)
&N       SETA  &TXT(2)
&GMFCI   SETA  &GMFCI+&N
         MEXIT
.NLG     ANOP
&N       SETA  K'&TXT
         AIF   ('&TXT'(1,1) NE '''').ARGERR
         AIF   ('&TXT'(&N,1) NE '''').ARGERR
&I       SETA  2
&N       SETA  &N-1
.SCAN    AIF   (&I GT &N).CNTL
         AIF   ('&TXT'(&I,1) NE '''').TAM
&I       SETA  &I+1
         AIF   (&I GT &N).ARGERR
         AIF   ('&TXT'(&I,1) NE '''').ARGERR
         AGO   .ADD
.TAM     ANOP
&W       SETA  &I+1
         AIF   (&W GT &N).ADD
         AIF   ('&TXT'(&I,2) NE '&&').ADD
&I       SETA  &W
.ADD     ANOP
&L       SETA  &L+1
&I       SETA  &I+1
         AGO   .SCAN
.CNTL    AIF   (&L EQ 0).ARGERR
         AIF   (&GFISW).MI
         AIF   (&GFESW).MV
&LABEL   DC    CL&L&TXT
         MEXIT
.MV      ANOP
&LABEL   MVC   0(&L,&GFSMF),=CL&L&TXT
         LA    &GFSMF,&L.(,&GFSMF)
         MEXIT
.MI      AIF   ('&GFSMI'(1,1) EQ '(').MIR
&LABEL   MVC   &GFSMI+&GMFCI.(&L),=CL&L&TXT
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.MIR     ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(&L,&RG),=CL&L&TXT
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.USEERR  MNOTE 8,'--- MACRO USAGE INVALID'
         MEXIT
.NUMERR  MNOTE 8,'--- NOT ABSOLUTE NUMERIC TEXT LENGTH VALUE'
         MEXIT
.ARGERR  MNOTE 8,'--- TEXT=... INVALID CODING'
         MEND
