PROC     0 DEBUG(NO)
/*------------------------------------------------------------------*/
/* ORIGIN : EXTRACTED FROM CBT 94 DEC TAPE, FILE 012, MEMBERS       */
/*          NAMES : "JH#TEDPR" = ISPF PROCEDURE                     */
/*                  "JHTEPE31" AND "JHTEPE32" = ISPF PANELS         */
/*                  "JHA@M00" = ISPF MESSAGES                       */
/* UPDATE : SOME ENHANCEMENTS DONE BY MOINIL P.A.                   */
/* PRE-REQUISITES : "DSNCHECK", "EURACFT", "PRINTO" AND "WHATDSN"   */
/*                  TSO COMMANDS.                               (PM)*/
/*------------------------------------------------------------------*/
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN GOTO QUIT
         IF &DEBUG EQ YES THEN CONTROL MSG LIST SYMLIST CONLIST
         ELSE CONTROL NOMSG
/* - - - - - - - - - - - - - - - - - - - - - - - - - INITIALIZATION */
         SET &AMP = &STR(&&)
         SET &PRP = )
         WHATDSN FILE(ISPPROF)
         SET &RCC = &LASTCC
         IF  &RCC NE 0 THEN GOTO ERRDDN
         ISPEXEC VGET (ZSCREEN ZUSER ZDATE ZTIME) SHARED
         SET &PXD = &STR(&ZDATE)
         SET &PXT = &STR(&ZTIME)
         SET &DDN = &STR(JHEDPR&ZSCREEN)
         FREE F(&DDN)
         CONTROL NOMSG
         ALLOC F(&DDN) SPACE(5 5) TRACKS DIR(2) +
           BLKSIZE(3120) LRECL(80) RECFM(F B)
         ISPEXEC LIBDEF ISPTLIB LIBRARY ID(&DDN)
         ISPEXEC LIBDEF ISPTABL LIBRARY ID(&DDN)
         SET &W = &STR(W&ZUSER)
         SET &X = &STR(X&ZUSER)
         SET &Y = &STR(Y&ZUSER)
         SET &TB = &STR(&Y)
         SET &PMDSN = &Z
         SET &PMT = &STR(OFF)
         SET &PXP = 0
         SET &PDL = &Z
         SET &JHEDPRID = &Z
         SET &JHEDPRDS = &Z
         SET &JHEDPRAP = &Z
/* - - - - - - - - - - - - - - - - - - - - - - - - - - -  SELECTION */
DSPLIN:  ISPEXEC DISPLAY PANEL(PAJVEP00)
         IF &LASTCC NE 0 THEN GOTO LEAVE
DSPLPR:  IF &PCM NE &Z THEN GOTO DSPLIN
         SET &CSR = &Z
         SET &JHPROFDS = &STR(&JHEDPRID..&JHEDPRDS)
         IF &PMT EQ SLM THEN SET &JHRACC = &STR(UPDATE)
         ELSE SET &JHRACC = &STR(READ)
         EURACFT '&JHPROFDS' ACCESS(&JHRACC) NOLIST
         SET &RCC = &LASTCC
         IF &RCC GT 4 THEN GOTO ERRACC
         IF &PMT EQ SLM THEN GOTO SLMTBL
/* - - - - - - - - - - - - - - - - - - - -  COPY EDIT PROFILE TABLE */
         SET &TN = &STR(&JHEDPRAP.EDIT)
         DSNCHECK '&JHPROFDS(&TN)' NOLIST
         SET &RCC = &LASTCC
         IF &RCC NE 0 THEN GO TO ERRDSN
         ISPEXEC LMINIT DATAID(JHDID) DATASET('&JHPROFDS')
         ISPEXEC LMINIT DATAID(JHDOD) DDNAME(&DDN)
         ISPEXEC LMCOPY FROMID(&JHDID) FROMMEM(&TN) +
           TODATAID(&JHDOD) TOMEM(&Y) REPLACE
         ISPEXEC LMFREE DATAID(&JHDID)
         ISPEXEC LMCOMP DATAID(&JHDOD)
         ISPEXEC LMFREE DATAID(&JHDOD)
         IF &PMT EQ DLT THEN GOTO DLTTBL
         IF &PMT NE TBL THEN GOTO CRDSPT
/* - - - - - - - - - - - - - - - - - - - - - - - - - TABLE ANALYSIS */
         ISPEXEC TBSTATS &TB CDATE(PMCDT)     CTIME(PMCTM)     +
           UDATE(PMUDT)      UTIME(PMUTM)     USER(PMUSR)      +
           ROWCREAT(PMRWC)   ROWCURR(PMRCU)   ROWUPD(PMRUP)    +
           TABLEUPD(PMTUP)   SERVICE(PMSRV)   RETCODE(PMRCD)   +
           STATUS1(PMST1)    STATUS2(PMST2)   STATUS3(PMST3)
         ISPEXEC TBOPEN &TB NOWRITE
         ISPEXEC TBTOP &TB
         ISPEXEC TBSKIP &TB NUMBER(1) SAVENAME(PMSVN)
         ISPEXEC TBQUERY &TB KEYS(PMKEY) NAMES(PMNMS) ROWNUM(PMRNM)   +
           KEYNUM(PMKNM) NAMENUM(PMNMN)
         SET &PMK01 = &Z
         SET &PMK02 = &Z
         SET &PMK03 = &Z
         SET &PMX = &Z
         IF &PMKNM NE 0 THEN DO
           SET &PLL = 2
           SET &PLH = &LENGTH(&STR(&PMKEY))
           IF &SUBSTR(&PLH:&PLH,&PMKEY) EQ &PRP THEN +
             SET &PLH = &PLH - 1
           SYSCALL VDSPL PMK01 PLL PLH PMKEY
           IF &PLL LE &PLH THEN SYSCALL VDSPL PMK02 PLL PLH PMKEY
           IF &PLL LE &PLH THEN SYSCALL VDSPL PMK03 PLL PLH PMKEY
           IF &PLL LE &PLH THEN SET &PMX = &STR(...)
           END
         SET &PMN01 = &Z
         SET &PMN02 = &Z
         SET &PMN03 = &Z
         SET &PMN04 = &Z
         SET &PMY = &Z
         IF &PMNMN NE 0 THEN DO
           SET &PLL = 2
           SET &PLH = &LENGTH(&STR(&PMNMS))
           IF &SUBSTR(&PLH:&PLH,&PMNMS) EQ &PRP THEN +
             SET &PLH = &PLH - 1
           SYSCALL VDSPL PMN01 PLL PLH PMNMS
           IF &PLL LE &PLH THEN SYSCALL VDSPL PMN02 PLL PLH PMNMS
           IF &PLL LE &PLH THEN SYSCALL VDSPL PMN03 PLL PLH PMNMS
           IF &PLL LE &PLH THEN SYSCALL VDSPL PMN04 PLL PLH PMNMS
           IF &PLL LE &PLH THEN SET &PMY = &STR(...)
           END
         SET &PMS01 = &Z
         SET &PMS02 = &Z
         SET &PMS03 = &Z
         SET &PMZ = &Z
         IF &PMSVN NE &Z THEN DO
           SET &PLL = 2
           SET &PLH = &LENGTH(&STR(&PMSVN))
           IF &SUBSTR(&PLH:&PLH,&PMSVN) EQ &PRP THEN +
             SET &PLH = &PLH - 1
           SYSCALL VDSPL PMS01 PLL PLH PMSVN
           IF &PLL LE &PLH THEN SYSCALL VDSPL PMS02 PLL PLH PMSVN
           IF &PLL LE &PLH THEN SYSCALL VDSPL PMS03 PLL PLH PMSVN
           IF &PLL LE &PLH THEN SET &PMZ = &STR(...)
           END
DSPLST:  SET &PSW = 0
         ISPEXEC DISPLAY PANEL(PAJVEP03)
         SET &RCC = &LASTCC
         IF &RCC NE 0 THEN GOTO DSPLEX
         IF &PCM EQ &Z THEN GOTO DSPLQR       /* JUST PRESSED ENTER */
         GOTO LSDSPL
DSPLQR:  SET &PSW = 1
         ISPEXEC DISPLAY PANEL(PAJVEP04)
         SET &RCC = &LASTCC
         IF &RCC NE 0 THEN GOTO DSPLEX
         IF &PCM EQ &Z THEN GOTO DSPLST       /* JUST PRESSED ENTER */
         GOTO LSDSPL
DSPLBK:  ISPEXEC SETMSG MSG(PAJV000X)
         IF &PSW EQ 0 THEN GOTO DSPLST
         GOTO DSPLQR
DSPLEX:  SET &PMT = &STR(OFF)
         GOTO ENDTBS
/* - - - - - - - - - - - - - - - - - - - - - - - - -  DISPLAY TABLE */
CRDSPT:  SET &JHEDUDT = &Z
         SET &JHEDUTM = &Z
         ISPEXEC TBSTATS &TB UDATE(JHEDUDT) UTIME(JHEDUTM)
         SET &ZEDPIMAC = &Z                  /* NOT IN OLD PROFILES */
         ISPEXEC TBOPEN &TB NOWRITE
         ISPEXEC TBTOP &TB
         IF &PMT EQ ON THEN DO
           ISPEXEC TBCREATE &W NOWRITE +
             NAMES(PMPTYPE PMPLRCL PMPRCFM PMPFLAG PMPBNDL +
                   PMPBNDR PMPTABC PMPMASK PMPTABS PMPIMAC)
           SET &PNL = &STR(PAJVEP02)
           END
         ELSE DO
           ISPEXEC TBCREATE &W NOWRITE +
             NAMES(JHEDPROF JHEDATTR JHEDLOCK JHEDIMAC JHEDSTAT +
                   JHEDPACK JHEDRECV JHEDNULL JHEDCAPS JHEDTABS +
                   JHEDNUMB JHEDAUTN JHEDBNDL JHEDBNDR)
           SET &PNL = &STR(PAJVEP01)
           END
         SET &MSG = &Z
SKPTBN:  ISPEXEC TBSKIP &TB NUMBER(1)
         IF &LASTCC EQ 8 THEN GOTO DSPLTB
         IF &PMT NE ON THEN DO
           SET &JHEDPROF = &STR(&ZEDPTYPE)               /* PROFILE */
           SET &JHEDATTR = &STR(&ZEDPRCFM-&ZEDPLRCL)        /* ATTR */
           IF &SUBSTR(2,&ZEDPFLAG) EQ &STR(1) THEN          /* CAPS */ +
             SET &JHEDCAPS = &STR(ON)
           ELSE SET &JHEDCAPS = &STR(OFF)
           IF &SUBSTR(4,&ZEDPFLAG) EQ &STR(1) THEN DO        /* HEX */
             IF &SUBSTR(5,&ZEDPFLAG) EQ &STR(1) THEN +
               SET &JHEDHEXM = &STR(ON DATA)
             ELSE SET &JHEDHEXM = &STR(ON VERT)
             END
           ELSE SET &JHEDHEXM = &STR(OFF)
           IF &SUBSTR(6,&ZEDPFLAG) EQ &STR(1) THEN          /* PACK */ +
             SET &JHEDPACK = &STR(ON)
           ELSE SET &JHEDPACK = &STR(OFF)
           IF &SUBSTR(7,&ZEDPFLAG) EQ &STR(1) THEN DO      /* NULLS */
             IF &SUBSTR(8,&ZEDPFLAG) EQ &STR(1) THEN +
               SET &JHEDNULL = &STR(ON ALL)
             ELSE SET &JHEDNULL = &STR(ON STD)
             END
           ELSE SET &JHEDNULL = &STR(OFF)
           IF &SUBSTR(11,&ZEDPFLAG) EQ &STR(1) THEN       /* NUMBER */ +
             SET &JHEDNUMB = &STR(COBOL)
           ELSE SET &JHEDNUMB = &Z
           IF &SUBSTR(10,&ZEDPFLAG) EQ &STR(1) THEN DO
             IF &JHEDNUMB = &Z THEN SET &JHEDNUMB = &STR(STD)
             ELSE SET &JHEDNUMB = &STR(STD &JHEDNUMB)
             IF &SUBSTR(9,&ZEDPFLAG) EQ &STR(1) THEN +
               SET &JHEDNUMB = &STR(DISPLAY &JHEDNUMB)
             ELSE SET &JHEDNUMB = &STR(ON &JHEDNUMB)
             END
           IF &STR(&JHEDNUMB) EQ &Z THEN SET &JHEDNUMB = &STR(OFF)
           IF &SUBSTR(14,&ZEDPFLAG) EQ &STR(1) THEN DO      /* TABS */
             IF &SUBSTR(15:16,&ZEDPFLAG) EQ &STR(00) THEN +
               SET &JHEDTABS = &STR(ON STD)
             ELSE IF &SUBSTR(15:16,&ZEDPFLAG) = &STR(01) THEN +
               SET &JHEDTABS = &STR(ON &ZEDPTABC)
             ELSE SET &JHEDTABS = &STR(ON ALL)
             END
           ELSE SET &JHEDTABS = &STR(OFF)
           IF &SUBSTR(18,&ZEDPFLAG) EQ &STR(1) THEN      /* AUTONUM */ +
             SET &JHEDAUTN = &STR(ON)
           ELSE SET &JHEDAUTN = &STR(OFF)
           IF &SUBSTR(19,&ZEDPFLAG) EQ &STR(1) THEN     /* AUTOLIST */ +
             SET &JHEDAUTL = &STR(ON)
           ELSE SET &JHEDAUTL = &STR(OFF)
           IF &SUBSTR(20,&ZEDPFLAG) EQ &STR(1) THEN        /* STATS */ +
             SET &JHEDSTAT = &STR(ON)
           ELSE SET &JHEDSTAT = &STR(OFF)
           IF &SUBSTR(21,&ZEDPFLAG) EQ &STR(1) THEN         /* RECV */ +
             SET &JHEDRECV = &STR(ON)
           ELSE SET &JHEDRECV = &STR(OFF)
           IF &SUBSTR(22,&ZEDPFLAG) EQ &STR(1) THEN         /* LOCK */ +
             SET &JHEDLOCK = &STR(YES)
           ELSE SET &JHEDLOCK = &STR(NO)
           IF &SUBSTR(23,&ZEDPFLAG) EQ &STR(1) THEN DO  /* AUTOSAVE */
             IF &SUBSTR(24,&ZEDPFLAG) EQ &STR(1) THEN +
               SET &JHEDAUTS = &STR(OFF PROMPT)
             ELSE SET &JHEDAUTS = &STR(OFF NOPROMPT)
             END
           ELSE SET &JHEDAUTS = &STR(ON)
           SET &JHEDBNDL = &STR(&ZEDPBNDL)           /* LEFT BOUNDS */
           SET &JHEDBNDR = &STR(&ZEDPBNDR)          /* RIGHT BOUNDS */
           SET &JHEDIMAC = &STR(&ZEDPIMAC)    /* INITIAL EDIT MACRO */
           END
         ELSE DO
           SET &PMPTYPE = &STR(&ZEDPTYPE)
           SET &PMPLRCL = &STR(&ZEDPLRCL)
           SET &PMPRCFM = &STR(&ZEDPRCFM)
           SET &PMPFLAG = &STR(&ZEDPFLAG)
           SET &PMPBNDL = &STR(&ZEDPBNDL)
           SET &PMPBNDR = &STR(&ZEDPBNDR)
           SET &PMPTABC = &STR(&ZEDPTABC)
           SET &PMPMASK = &STR(&ZEDPMASK)
           SET &PMPTABS = &STR(&ZEDPTABS)
           SET &PMPIMAC = &STR(&ZEDPIMAC)
           END
         ISPEXEC TBADD &W
         GOTO SKPTBN
DSPLTB:  ISPEXEC TBTOP &W
         ISPEXEC TBDISPL &W PANEL(&PNL) MSG(&MSG)
         SET &RCC = &LASTCC
         SET &PNL = &Z
         IF &RCC NE 0 THEN GOTO ENDTBD
         IF &PCM EQ &Z THEN GOTO DSPLTB       /* JUST PRESSED ENTER */
/* - - - - - - - - - - - - - - - - - - - - -  PRINT CURRENT DISPLAY */
LSDSPL:  CONTROL NOMSG
         ERROR DO
           ERROR OFF
           FREE ATTR(VBA)
           ATTR VBA RECFM(V B A) LRECL(137) BLKSIZE(6356) DSORG(PS)
           ALLOC F(ISPFILE) DSN(VEPTEMP.LISTING) REUSE CATALOG +
             USING(VBA) SP(5 2) TRACKS
           SET &PDL = &STR(DELETE)
           END
         ALLOC F(ISPFILE) DSN(VEPTEMP.LISTING) OLD REUSE
         ERROR OFF
         ISPEXEC FTOPEN
         IF &PMT EQ TBL THEN ISPEXEC FTINCL PAJSVEP3
         ELSE IF &PMT EQ ON THEN ISPEXEC FTINCL PAJSVEP2
         ELSE ISPEXEC FTINCL PAJSVEP1
         ISPEXEC FTCLOSE
         SET &POP = &Z
         IF &PDS NE &Z THEN SET &POP = &STR(DEST(&PDS))
         IF &PCL NE &Z THEN SET &POP = &STR(&POP CLASS(&PCL))
         PRINTO VEPTEMP.LISTING &POP NOHEADING NOMSG
         CONTROL MSG
         IF &PMT EQ TBL THEN GOTO DSPLBK
         IF &PMT EQ ON THEN SET &MSG = PAJV000Y
         ELSE SET &MSG = PAJV000Z
         GOTO DSPLTB
ENDTBD:  ISPEXEC TBEND &W
ENDTBS:  ISPEXEC TBEND &TB
         GOTO DSPLIN
/* - - - - - - - - - - - - - - - - - - - - - - - - DISPLAY APPLID'S */
SLMTBL:  IF &PMDSN EQ &Z THEN GOTO LDAPTB
         SET &PSW = 1
         IF &STR(&PMDSN) EQ &STR(&JHPROFDS) THEN GOTO DSPLSL
         ISPEXEC TBEND &X
         SET &PMDSN = &Z
LDAPTB:  ISPEXEC LMINIT DATAID(JHDID) DATASET('&JHPROFDS')
         ISPEXEC LMOPEN DATAID(&JHDID)
         ISPEXEC TBCREATE &X NOWRITE +
           NAMES(XN1 XN2 XN3 XN4 XN5 XN6 XN7 XN8)
         SET &PSW = 0
         SET &N = 1
         DO WHILE &N LE 8
           SET XN&N = &Z
           SET &N = &N + 1
           END
         SET &N = 1
         SET &PMMSL = &Z
         SET &RCC = 0
         DO WHILE &RCC EQ 0
           ISPEXEC LMMLIST DATAID(&JHDID) MEMBER(PMMSL) OPTION(LIST)
           SET &RCC = &LASTCC
           IF &RCC EQ 0 THEN DO
             SET &PMMEM = &PMMSL
             SET &L = &LENGTH(&STR(&PMMEM))
             IF &L GT 4 THEN DO
               SET &I = &L - 3
               IF &SUBSTR(&I:&L,&PMMEM) EQ &STR(EDIT) THEN DO
                 SET &PSW = 1
                 SET &I = &L - 4
                 SET XN&N = &SUBSTR(1:&I,&PMMEM)
                 IF &N EQ 8 THEN DO
                   ISPEXEC TBADD &X
                   SET &N = 1
                   DO WHILE &N LE 8
                     SET XN&N = &Z
                     SET &N = &N + 1
                     END
                   SET &N = 1
                   END
                 ELSE SET &N = &N + 1
                 END
               END
             END
           END
         IF &N NE 1 THEN ISPEXEC TBADD &X
         ISPEXEC LMMLIST DATAID(&JHDID) OPTION(FREE)
         ISPEXEC LMCLOSE DATAID(&JHDID)
         ISPEXEC LMFREE DATAID(&JHDID)
DSPLSL:  IF &PSW EQ 1 THEN DO
           ISPEXEC TBTOP &X
           SET &MSG = &Z
           SET &I = 0
           DO WHILE &I EQ 0
             ISPEXEC TBDISPL &X PANEL(PAJVEP10) MSG(&MSG) +
               POSITION(CRP)
             SET &RCC = &LASTCC
             IF &RCC GT 4 THEN SET &I = 1
             ELSE IF &ZTDSELS GT 1 THEN SET &MSG = PAJV001C
             ELSE IF &XERR EQ ON THEN SET &MSG = PAJV001D
             ELSE IF &ZTDSELS EQ 1 THEN DO
               SET &N = &XSL
               SET &L = &STR(&AMP.XN&N)
               IF &L NE &Z THEN SET &I = 1
               ELSE SET &MSG = PAJV001E
               END
             ELSE SET &MSG = PAJV001B
             END
           IF &RCC NE 0 THEN DO
             ISPEXEC TBEND &X                          /* FORCE END */
             SET &PMDSN = &Z
             END
           ELSE DO
             SET &JHEDPRAP = &STR(&AMP.XN&XSL)    /* SELECTION DONE */
             SET &PMDSN = &STR(&JHPROFDS)
             END
           END
         ELSE DO
           ISPEXEC TBEND &X                          /* TABLE EMPTY */
           SET &PMDSN = &Z
           ISPEXEC SETMSG MSG(PAJV001F)
           END
         SET &PMT = &STR(OFF)
         GOTO DSPLIN
/* - - - - - - - - - - - - - - - - - - - - - - -  DELETE FROM TABLE */
DLTTBL:  ISPEXEC TBOPEN &TB WRITE
         SET &ZTDTOP = 0
         SET &PMUPT = NO
         SET &RCC = 0
DLTTBD:  IF &RCC EQ 4 THEN ISPEXEC TBDISPL &TB POSITION(CRP)
         ELSE DO
           ISPEXEC TBTOP &TB
           ISPEXEC TBSKIP &TB NUMBER(&ZTDTOP)
           ISPEXEC VGET (ZVERB ZSCROLLN)
           IF &ZVERB EQ UP THEN ISPEXEC TBSKIP &TB +
             NUMBER(-&ZSCROLLN)
           ELSE IF &ZVERB EQ DOWN THEN ISPEXEC TBSKIP &TB +
             NUMBER(&ZSCROLLN)
           ISPEXEC TBDISPL &TB PANEL(PAJVEP20) POSITION(CRP)
           END
         SET &RCC = &LASTCC
         IF &PCM EQ CANCEL OR &RCC GT 4 THEN GOTO DLTTBX
         ELSE IF &ZTDSELS GE 1 THEN DO
           IF (&STR(&XERR) EQ ON THEN DO
             ISPEXEC SETMSG MSG(PAJV002B)
             SET RCC = 0
             END
           ELSE DO WHILE &N GE 1
             ISPEXEC TBDELETE &TB
             ISPEXEC TBSKIP &TB
             IF &LASTCC NE 0 THEN SET &N = 0
             ELSE SET &N = &N - 1
             SET &PMUPT = YES
             END
           END
         GOTO DLTTBD
DLTTBX:  IF (&PCM EQ CANCEL) OR (&PMUPT NE YES) THEN DO
           ISPEXEC TBEND &TB
           IF (&PCM EQ CANCEL) THEN ISPEXEC SETMSG MSG(PAJV002X)
           ELSE ISPEXEC SETMSG MSG(PAJV002Y)
           END
         ELSE DO
           ISPEXEC TBCLOSE &TB
           SET &PRF = &STR(NO)
           SET &PCF = &STR(NO)
           ISPEXEC DISPLAY PANEL(PAJVEP21)
           SET &RCC = &LASTCC
           IF (&RCC NE 0) OR (&PCM NE &Z) OR +
             (&PRF NE YES) THEN ISPEXEC SETMSG MSG(PAJV002W)
           ELSE DO
             ISPEXEC LMINIT DATAID(JHDID) DDNAME(&DDN)
             ISPEXEC CONTROL ERRORS RETURN
             ISPEXEC LMINIT DATAID(JHDOD) DATASET('&JHPROFDS') +
               ENQ(EXCLU)
             SET &ICC = &LASTCC
             IF &ICC EQ 0 THEN DO
               ISPEXEC LMCOPY FROMID(&JHDID) FROMMEM(&Y) +
                 TODATAID(&JHDOD) TOMEM(&TN) REPLACE
               SET &CCC = &LASTCC
               END
             ISPEXEC CONTROL ERRORS CANCEL
             ISPEXEC LMFREE DATAID(&JHDID)
             IF (&ICC EQ 0) AND (&CCC EQ 0) AND (&PCF EQ YES) THEN DO
               ISPEXEC CONTROL ERRORS RETURN
               ISPEXEC LMCOMP DATAID(&JHDOD)
               ISPEXEC CONTROL ERRORS CANCEL
               SET &PCC = &LASTCC
               END
             IF &CCC EQ 0 THEN ISPEXEC LMFREE DATAID(&JHDOD)
             IF &ICC NE 0 THEN ISPEXEC SETMSG MSG(PAJV002V)
             ELSE IF &CCC NE 0 THEN ISPEXEC SETMSG MSG(PAJV002U)
             ELSE IF (&PCF EQ YES) AND (&PCC NE 0) THEN +
               ISPEXEC SETMSG MSG(PAJV002T)
             ELSE ISPEXEC SETMSG MSG(PAJV002Z)
             END
           END
         SET &PMT = &STR(OFF)
         GOTO DSPLIN
/* - - - - - - - - - - - - - - - - - - - - - - - - ERROR PROCESSING */
ERRDDN:  IF &RCC EQ 8 THEN SET &MSG = PAJV000G
         ELSE SET &MSG = PAJV000H
         GOTO QUIT
ERRACC:  IF &RCC EQ 8 THEN SET &MSG = PAJV000A
         ELSE SET &MSG = PAJV000B
         SET &CSR = JHEDPRID
         GOTO ERRMSG
ERRDSN:  IF &RCC EQ 4 THEN SET &MSG = PAJV000C
         ELSE IF &RCC EQ 8 THEN SET &MSG = PAJV000D
         ELSE IF &RCC EQ 12 THEN SET &MSG = PAJV000E
         ELSE SET &MSG = PAJV000F
         SET &CSR = JHEDPRAP
ERRMSG:  ISPEXEC DISPLAY MSG(&MSG) CURSOR(&CSR)
         IF &LASTCC EQ 0 THEN GOTO DSPLPR
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - TERMINATED */
LEAVE:   ISPEXEC LIBDEF ISPTLIB LIBRARY
         ISPEXEC LIBDEF ISPTABL LIBRARY
         FREE F(&DDN)
         IF &PDL EQ &STR(DELETE) THEN DO
           FREE ATTR(VBA) DELETE
           FREE F(ISPFILE) DELETE
           END
QUIT:    IF &PMDSN NE &Z THEN ISPEXEC TBEND &X
         EXIT CODE(0)
/* ============== INTERNAL PROCEDURE (PREPARE DISPLAY OF VARIABLES) */
VDSPL:   PROC 4 PV1 PV2 PV3 PV4
         SYSREF &PV1 &PV2 &PV3 &PV4
         SET &BLK = &STR(        .)
         SET &L = 0
         SET &M = 1
         DO WHILE &M LE 7
           IF &PV2 GT &PV3 THEN SET &M = 8
           ELSE DO
             IF &SUBSTR(&PV2:&PV2,&PV4) NE &Z THEN DO
               SET &PV1 = &STR(&PV1.&SUBSTR(&PV2:&PV2,&PV4))
               SET &L = &L + 1
               END
             ELSE DO
               SET &N = 8 - &L
               IF &N EQ 0 THEN SET &PV1 = &STR(&PV1 )
               ELSE SET &PV1 = &STR(&PV1.&SUBSTR(1:&M,&BLK) )
               SET &L = 0
               SET &M = &M + 1
               END
             SET &PV2 = &PV2 + 1
             END
           END
         END
