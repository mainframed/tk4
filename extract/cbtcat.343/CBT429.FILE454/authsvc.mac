AUTH     TITLE 'AUTHORIZATION SVC FOR MVS.'
IGC00### START 0                   AUTHORIZATION ON/OFF SVC.
         SPACE 1
        $DEFREG
         SPACE 1
         USING *,R6                SET LOCAL ADDRESSABILITY.
         SPACE 1
         LTR   R0,R0               TIME CALL?
         BNM   AUTHCALL            NO.
         BCT   R1,*+L'*+6
         USING ASCB,R7
         L     R0,ASCBJSTL         1 = STEP TIME LIMIT.
         BR    R14                 RETURN.
         BCT   R1,*+L'*+6
         LM    R0,R1,ASCBEJST      2 = CPU TIME USED IN THIS STEP.
         BR    R14                 RETURN.
         BCT   R1,*+L'*+6
         L     R0,ASCBR15C         3 = SRB TIME LIMIT.
         BR    R14                 RETURN.
         BCT   R1,*+L'*+4
         LM    R0,R1,ASCBSRBT      4 = SRB TIME USED IN THIS STEP.
         DROP  R7
         BR    R14                 RETURN.
         SPACE 1
         USING TCB,R4              SET TCB ADDRESSABILITY.
AUTHCALL L     R2,TCBJSCB          POINTER TO JSCB IN REGISTER 2.
         DROP  R4
         USING IEZJSCB,R2          SET JSCB ADDRESSABILITY.
         BCT   R0,AUTHOFF          IF R0 NOT = 1, AUTH OFF.
* ------ INSERT HERE ANY CONTROL YOU WANT DO ON CALLER ----------------
         OI    JSCBOPTS,JSCBAUTH   SET JSCB AUTH BIT.
         BR    R14                 RETURN.
AUTHOFF  NI    JSCBOPTS,255-JSCBAUTH    CLEAR JSCB AUTH BIT.
         BR    R14                 RETURN.
         DROP  R2
         SPACE 1
        PRINT  OFF
        IHAASCB LIST=YES
        IKJTCB
        IECDSECS (JSCB),EXPAND=YES
        PRINT  ON
         SPACE 1
         END
