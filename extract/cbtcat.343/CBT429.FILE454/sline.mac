ISREDIT  MACRO (APARM,BPARM) NOPROCESS
/*  MACRO NAME : SLINE                                              */
/*    FUNCTION : INSERT A SEPARATION LINE                           */
/*      SYNTAX : SLINE                                              */
/*               SLINE C=CHARACTER TO BE USED                       */
/*               SLINE COMMA OR CODEC                               */
/*               SLINE HELP OR H                                    */
/*       DEBUG : SLINE ... LIST                                     */
/*      ORIGIN : CBT TAPE 89 FEB, FILE 95                           */
/*      UPDATE : SEPARATION CHARACTER ADDED (MOINIL P.A.)           */
  SET &AMPSD = &STR(&&)
  SET &LPRTH = &NRSTR((
  SET &SPLUS = &NRSTR(+)
  IF &STR(&BPARM.) = &STR(LIST) THEN CONTROL LIST SYMLIST CONLIST
  IF &NRSTR(&APARM.) = &STR() THEN SET &CH = &STR(-)
  SET &TL = &LENGTH(&NRSTR(&APARM))
  IF &TL = 3 OR &TL = 5 THEN DO
    IF &TL = 3 THEN DO
      SET &TC = &SUBSTR(1:2,&NRSTR(&APARM))
      IF &STR(&TC) = &STR(C=) THEN +
        SET &CH = &SUBSTR(3:3,&NRSTR(&APARM))
      ELSE DO
        SET ZEDSMSG = INVALID OPERAND
        SET ZEDLMSG = ENTER "SLINE HELP" COMMAND FOR OPERAND SYNTAX
        ISPEXEC SETMSG MSG(ISRZ000)
        EXIT CODE(0)
        END
      END
    ELSE DO
      IF &NRSTR(&APARM.) = &STR(COMMA) THEN SET &CH = &NRSTR(,)
      IF &NRSTR(&APARM.) = &STR(CODEC) THEN DO
        ISPEXEC VGET (ZDEL) PROFILE
        SET &CH = &ZDEL
        END
      ELSE DO
        SET ZEDSMSG = INVALID OPERAND
        SET ZEDLMSG = ENTER "SLINE HELP" COMMAND FOR OPERAND SYNTAX
        ISPEXEC SETMSG MSG(ISRZ000)
        EXIT CODE(0)
        END
      END
    END
  ELSE IF &NRSTR(&APARM.) ^= &STR() THEN DO
    IF &NRSTR(&APARM.) = &STR(HELP) OR +
      &NRSTR(&APARM.) = &STR(H) THEN DO
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTSEPL)
        SET &RCX = &LASTCC
        ISPEXEC CONTROL DISPLAY RESTORE
        EXIT CODE(&RCX)
        END
    ELSE DO
      SET ZEDSMSG = INVALID OPERAND
      SET ZEDLMSG = ENTER "SLINE HELP" COMMAND FOR OPERAND SYNTAX
      ISPEXEC SETMSG MSG(ISRZ000)
      EXIT CODE(0)
      END
    END
  ISREDIT (THIS) = MEMBER
  ISREDIT PROCESS DEST
  IF &LASTCC = 0 THEN DO
    ISREDIT LOCATE .ZDEST
    IF &LASTCC = 8 THEN DO
      SET ZEDSMSG = &STR(EMPTY DATA)
      SET ZEDLMSG = &STR(SLINE NEEDS VALID DATA LINE NUMBERS)
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(12)
      END
    ISREDIT (LN) = LRECL
    SET &RULE1 = &NRSTR(&CH&CH&CH&CH&CH&CH&CH&CH&CH&CH)
    SET &RULE2 = &NRSTR(&RULE1&RULE1&RULE1&RULE1)
    SET &RULE3 = &NRSTR(&RULE2&RULE2&RULE2&RULE2&RULE2&RULE2&RULE2)
    IF &NRSTR(&CH) = &NRSTR(&AMPSD) THEN DO
      SET &RULE4 = &NRSTR(&RULE3&RULE3)
      SET &LL = 2*&LN
      SET &RULES = &SUBSTR(1:&LL,&NRSTR(&RULE4))
      END
    ELSE SET &RULES = &SUBSTR(1:&LN,&NRSTR(&RULE3))
    IF &NRSTR(&CH) = &STR(=) OR +
      &NRSTR(&CH) = &STR(,) OR +
      &NRSTR(&CH) = &NRSTR(&SPLUS) OR +
      &NRSTR(&CH) = &NRSTR(&LPRTH) THEN +
      ISREDIT LINE_AFTER .ZDEST = '&NRSTR(&RULES)'
    ELSE ISREDIT LINE_AFTER .ZDEST = &NRSTR(&RULES)
ERROROUT: ISREDIT MASKLINE = ' '
    SET RC = 0
    END
  ELSE DO
    IF &MAXCC = 8 THEN DO
      SET ZEDSMSG = &STR(SAY WHERE)
      SET ZEDLMSG = &STR(USE 'A' OR 'B' TO INDICATE WHERE +
                         LINE SHOULD BE INSERTED)
      END
    ELSE DO
      SET ZEDSMSG = &STR(PROCESS ERROR)
      SET ZEDLMSG = &STR(UNABLE TO PROCESS REQUEST, RECHECK INPUT)
      END
    ISPEXEC SETMSG MSG(ISRZ000)
    SET RC = 12
    END
EXIT CODE(&RC)
