         MACRO           CALL ISPF VGET-VPUT SUPPLY.
&NAME   $ISPF  &TYPE,&ID,&DATA,&POOL,&MF=
         LCLA  &Y
         LCLB  &W
         LCLC  &X,&Z
.*
.* $ISPF MACRO : CALL ISPF VGET-VPUT SUPPLY (SEE VGPSUB).
.* -------------
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   (N'&SYSLIST GT 4).OPND
         AIF   ('&MF' EQ '').XI1
         AIF   ('&MF' EQ 'E').XI1
         AIF   ('&MF' NE 'L').XL1
         AIF   ('&TYPE' NE '').INVT
         AIF   ('&POOL' NE '').INVT
         AIF   ('&ID' EQ '').LS1
         AIF   (K'&ID GT 8).BADI
&NAME    DC    0H'0',CL8'&ID'
         AGO   .LS2
.LS1     ANOP
&NAME    DC    0H'0',CL8' '
.LS2     AIF   ('&DATA' EQ '').LS6
         AIF   ('&DATA'(1,1) EQ '(').LS3
         DC    AL2(L'&DATA),AL4(&DATA)
         MEXIT
.LS3     AIF   ('&DATA(1)' EQ '').LS5
         AIF   ('&DATA(2)' EQ '').LS4
         DC    AL2(&DATA(2)),AL4(&DATA(1))
         MEXIT
.LS4     DC    AL2(*-*),AL4(&DATA(1))
         MEXIT
.LS5     AIF   ('&DATA(2)' EQ '').LS6
         DC    AL2(&DATA(2)),AL4(*-*)
         MEXIT
.LS6     DC    AL2(*-*),AL4(*-*)
         MEXIT
.XL1     AIF   ('&MF(1)' EQ 'L').XL2
         AIF   ('&MF(1)' NE 'E').INVM
         AIF   ('&MF(2)' EQ '').XI1
         AIF   ('&TYPE' EQ '').MIST
         AGO   .XL3
.XL2     AIF   ('&TYPE' NE '').INVT
         AIF   ('&MF(2)' EQ '').INVM
&W       SETB  1
.XL3     AIF   ('&MF(2)'(1,1) EQ '(').XL4
&NAME    LA    1,&MF(2)
         AGO   .XL6
.XL4     AIF   ('&MF(2)' NE '(1)').XL5
         AIF   ('&NAME' EQ '').XL6
&NAME    DS    0H
         AGO   .XL6
.XL5     ANOP
&Y       SETA  K'&MF(2)-2
&Z       SETC  '&MF(2)'(2,&Y)
&NAME    LR    1,&Z
.XL6     AIF   ('&ID' EQ '').XL10
         AIF   ('&ID'(1,1) EQ '(').XL7
         MVC   0(L'&ID,1),&ID
         AGO   .XL10
.XL7     AIF   ('&ID(1)' EQ '').XL9
         AIF   ('&ID(2)' EQ '').XL8
         MVC   0(&ID(2),1),&ID(1)
         AGO   .XL10
.XL8     MVC   0(8,1),&ID(1)
         AGO   .XL10
.XL9     AIF   ('&ID(2)' NE '').INVI
.XL10    AIF   ('&DATA' EQ '').XL15
         AIF   ('&DATA'(1,1) EQ '(').XL11
         MVC   8(2,1),=AL2(L'&DATA)
         MVC   10(4,1),=A(&DATA)
         AGO   .XL15
.XL11    AIF   ('&DATA(2)' EQ '').XL13
         AIF   ('&DATA(2)'(1,1) EQ '(').XL12
         MVC   8(2,1),=AL2(&DATA(2))
         AGO   .XL13
.XL12    ANOP
&Y       SETA  K'&DATA(2)-2
&Z       SETC  '&DATA(2)'(2,&Y)
         AIF   ('&Z' EQ '(1)').IRDL
         STCM  &Z,B'0011',8(1)
.XL13    AIF   ('&DATA(1)' EQ '').XL15
         AIF   ('&DATA(1)'(1,1) EQ '(').XL14
         MVC   10(4,1),=A(&DATA(1))
         AGO   .XL15
.XL14    ANOP
&Y       SETA  K'&DATA(1)-2
&Z       SETC  '&DATA(1)'(2,&Y)
         AIF   ('&Z' EQ '(1)').IRDA
         STCM  &Z,B'1111',10(1)
.XL15    AIF   (&W).ENDX
         AIF   ('&TYPE' NE 'VGET').LX1
         XR    0,0                 VGET
         AGO   .LX2
.LX1     AIF   ('&TYPE' NE 'VPUT').ERRT
         LA    0,1                 VPUT
.LX2     AIF   ('&POOL' EQ '').LX3
         AIF   ('&POOL' EQ 'SHARED').LX3
         AIF   ('&POOL' NE 'PROFILE').INVP
         O     0,=A(X'80000000')
.LX3     L     15,=V(VGPSUB)
         BASR  14,15
         AGO   .ENDX
.XI1     AIF   ('&TYPE' EQ '').MIST
         AIF   ('&ID' EQ '').MISI
         AIF   ('&DATA' EQ '').MISD
&X       SETC  'IHB'.'&SYSNDX'
&NAME    LA    1,&X
         AIF   ('&ID'(1,1) NE '(').XI2
         AIF   ('&ID' EQ '(1)').IRID
&Y       SETA  K'&ID-2
&Z       SETC  '&ID'(2,&Y)
         MVC   0(8,1),0(&Z)
.XI2     AIF   ('&DATA'(1,1) NE '(').XI4
         AIF   ('&DATA(1)' EQ '').INVD
         AIF   ('&DATA(2)' EQ '').INVD
         AIF   ('&DATA(2)'(1,1) NE '(').XI3
&Y       SETA  K'&DATA(2)-2
&Z       SETC  '&DATA(2)'(2,&Y)
         AIF   ('&Z' EQ '(1)').IRDL
         STCM  &Z,B'0011',8(1)
.XI3     AIF   ('&DATA(1)'(1,1) NE '(').XI4
&Y       SETA  K'&DATA(1)-2
&Z       SETC  '&DATA(1)'(2,&Y)
         AIF   ('&Z' EQ '(1)').IRDL
         STCM  &Z,B'1111',10(1)
.XI4     AIF   ('&TYPE' NE 'VGET').XI5
         XR    0,0                 VGET
         AGO   .XI6
.XI5     AIF   ('&TYPE' NE 'VPUT').ERRT
         LR    0,1                 VPUT
.XI6     AIF   ('&POOL' EQ '').XI7
         AIF   ('&POOL' EQ 'SHARED').XI7
         AIF   ('&POOL' NE 'PROFILE').INVP
         O     0,=A(X'80000000')
.XI7     B     &X.A
         AIF   ('&ID'(1,1) EQ '(').XI8
         AIF   (K'&ID GT 8).BADI
&X       DC    0H'0',CL8'&ID'
         AGO   .XI9
.XI8     ANOP
&X       DC    0H'0',CL8' '
.XI9     AIF   ('&DATA'(1,1) EQ '(').XI10
         DC    AL2(L'&DATA),AL4(&DATA)
         AGO   .XI14
.XI10    AIF   ('&DATA(2)'(1,1) EQ '(').XI11
         AIF   ('&DATA(1)'(1,1) EQ '(').XI12
         DC    AL2(&DATA(2)),AL4(&DATA(1))
         AGO   .XI14
.XI11    AIF   ('&DATA(1)'(1,1) EQ '(').XI13
         DC    AL2(*-*),AL4(&DATA(1))
         AGO   .XI14
.XI12    DC    AL2(&DATA(2)),AL4(*-*)
         AGO   .XI14
.XI13    DC    AL2(*-*),AL4(*-*)
.XI14    ANOP
&X.A     L     15,=V(VGPSUB)
         BASR  14,15
         MEXIT
.OPND    MNOTE 8,' TOO MANY OPERANDS ******************************** '
         MEXIT
.INVM    MNOTE 8,' MF= INVALID CODING ******************************* '
         MEXIT
.INVT    MNOTE 8,' TYPE OPERAND INVALID IN LIST FORM **************** '
         MEXIT
.INVI    MNOTE 8,' ID OPERAND INVALID CODING ************************ '
         MEXIT
.INVD    MNOTE 8,' DATA OPERAND INVALID CODING ********************** '
         MEXIT
.INVP    MNOTE 8,' POOL OPERAND INVALID CODING ********************** '
         MEXIT
.MIST    MNOTE 8,' MISSING TYPE OPERAND ***************************** '
         MEXIT
.MISI    MNOTE 8,' MISSING ID OPERAND ******************************* '
         MEXIT
.MISD    MNOTE 8,' MISSING DATA OPERAND ***************************** '
         MEXIT
.IRID    MNOTE 8,' ID OPERAND INVALID REGISTER ********************** '
         MEXIT
.IRDL    MNOTE 8,' DATA (LENGTH) OPERAND INVALID REGISTER *********** '
         MEXIT
.IRDA    MNOTE 8,' DATA (ADDRESS) OPERAND INVALID REGISTER ********** '
         MEXIT
.BADI    MNOTE 8,' ID OPERAND TOO LONG ****************************** '
         MEXIT
.ERRT    MNOTE 8,' INVALID TYPE OPERAND ***************************** '
.ENDX    MEND
