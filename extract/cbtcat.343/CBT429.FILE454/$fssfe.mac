         MACRO
&LABEL  $FSSFE
.*
.* MACRO : INTERNALLY USED BY $FS MACRO.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLA  &GMFCI
         GBLB  &GFUSW,&GFESW,&GFISW,&GFWSW
         GBLC  &GFSMF,&GFSMI,&GFSOP,&GFWTY,&GFWVA
         LCLA  &I,&L,&N,&NA,&NR,&T,&U
         LCLC  &CC,&CT,&CU,&RG,&TY,&VA
         AIF   (NOT &GFUSW).USEERR
&N       SETA  N'&SYSLIST(1)
         AIF   (&N LE 1).ARGERR
&NA      SETA  &N/2
         AIF   (&NA GT 255).ARGERR
&L       SETA  &NA*2
         AIF   (&N NE &L).ARGERR
&T       SETA  1+(&NA/16)
&U       SETA  1+(&NA-((&NA/16)*16))
&CT      SETC  '0123456789ABCDEF'(&T,1)
&CU      SETC  '0123456789ABCDEF'(&U,1)
&CC      SETC  '&CT&CU'
&I       SETA  1
.LOOP    ANOP
&N       SETA  &I+1
&TY      SETC  '&SYSLIST(1,&I)'
&VA      SETC  '&SYSLIST(1,&N)'
        $FS$TV &TY,&VA
         AIF   (NOT &GFWSW).TGX
         MEXIT
.TGX     AIF   (&GFISW).MVI
         AIF   (&GFESW).MVL
         AIF   (&I NE 1).CNT
&LABEL   DC    XL4'&GFSOP&CC&GFWTY&GFWVA'
         AGO   .NXT
.CNT     DC    XL2'&GFWTY&GFWVA'
         AGO   .NXT
.MVL     AIF   (&I NE 1).MVLC
&LABEL   MVC   0(4,&GFSMF),=XL4'&GFSOP&CC&GFWTY&GFWVA'
         LA    &GFSMF,4(,&GFSMF)
         AGO   .NXT
.MVLC    MVC   0(2,&GFSMF),=XL2'&GFWTY&GFWVA'
         LA    &GFSMF,2(,&GFSMF)
         AGO   .NXT
.MVI     AIF   ('&GFSMI'(1,1) EQ '(').MVIR
         AIF   (&I NE 1).MVIC
&LABEL   MVC   &GFSMI+&GMFCI.(4),=XL4'&GFSOP&CC&GFWTY&GFWVA'
&GMFCI   SETA  &GMFCI+4
         AGO   .NXT
.MVIC    MVC   &GFSMI+&GMFCI.(2),=XL2'&GFWTY&GFWVA'
&GMFCI   SETA  &GMFCI+2
         AGO   .NXT
.MVIR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
         AIF   (&I NE 1).MVIRC
&LABEL   MVC   &GMFCI.(4,&RG),=XL4'&GFSOP&CC&GFWTY&GFWVA'
&GMFCI   SETA  &GMFCI+4
         AGO   .NXT
.MVIRC   MVC   &GMFCI.(2,&RG),=XL2'&GFWTY&GFWVA'
&GMFCI   SETA  &GMFCI+2
.NXT     ANOP
&I       SETA  &I+2
         AIF   (&I LT &L).LOOP
         MEXIT
.ARGERR  MNOTE 8,'--- SFE=(...) INVALID CODING'
         MEXIT
.USEERR  MNOTE 8,'--- INVALID MACRO USAGE'
         MEND
