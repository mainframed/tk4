TRD      TITLE 'TEXT READ SUBROUTINE.'
READ     START 0
         SPACE 1
* CALLING SEQUENCE : CALL READ,(A,B,C),VL
* ------------------           (A,B)
*                              (A)
*
*        WHERE : A - IS THE ADDRESS OF AN ARRAY (FORMAT CL...) WHICH
*                    RECEIVES THE TEXT IMAGE.
*
*                B - IS THE ADDRESS OF THE DD-NAME (FORMAT CL8) IF
*                    SUPPLIED (OPTIONAL). WHEN THIS ARGUMENT IS
*                    OMITTED, OR THE ADDRESS OF THIS ARGUMENT IS
*                    SET TO ZERO IN THE PARAMETERS LIST, THE
*                    DEFAULT DD-NAME USED IS 'SYSIN   '.
*
*                C - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                    -1 - END-OF-FILE.
*                     0 - READ CORRECTLY EXECUTED.
*                     1 - DD-CARD MISSING.
*                     2 - OPEN ERROR OR NOT FIXED RECORD FORMAT.
*                     3 - CALLING SEQUENCE OR ARGUMENT ALIGNMENT ERROR.
*                     4 - READ ERROR.
*
*                     --- IF THE ROUTINE IS RE-CALLED AFTER IND = 1
*                         OR 2, THE PROGRAM IS SENDED IN ABEND U0001.
*                         IF THE INDICATOR RESPONSE IS DETECTED 10
*                         TIMES DIFFERENT FROM 0, THE PROGRAM IS
*                         SENDED IN ABEND U0002.
*
* HOW TO SUPPRESS THE 'DD-CARD MISSING' MESSAGE.
* ----------------------------------------------
*
*        BEFORE ENTERING THE FIRST TIME THE READ ROUTINE FOR A FILE
* (DD-NAME), EXECUTE THE FOLLOWING TWO INSTRUCTIONS :
*                    ...
*                    L     RX,=V(#RMSW#)
*                    MVI   0(RX),1
*                    ...
*        THEN IF ERR-1 OCCURS DURING THE FIRST CALL (OPEN FILE TIME),
* THE MESSAGE IS BYPASSED (THE REGISTER 15 AND THE INDICATOR RESPONSE
* ARE EVENTUALLY SET).
         EJECT
* CALLING SEQUENCE : CALL RCLOSE,(A,B),VL
* ------------------             (A)
*
*        WHERE : A - IS THE ADDRESS OF THE DD-NAME (FORMAT CL8) TO
*                    CLOSE. IF THIS ARGUMENT CONTAINS THE 'CLOSEALL'
*                    KEYWORD, OR THE ADDRESS OF THIS ARGUMENT IS
*                    SET TO ZERO IN THE PARAMETERS LIST, OR THE
*                    PARAMETERS LIST POINTER (REGISTER 1) IS ZERO,
*                    ALL THE OPENED FILES ARE CLOSED.
*
*                B - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                     0 - CLOSE CORRECTLY EXECUTED.
*                     1 - CALLING SEQUENCE, ARGUMENT ALIGNMENT OR
*                         DD-NAME ERROR.
*
* GENERAL NOTICE : ON ANY ERROR DETECTION, A MESSAGE IS INCLUDED
* ---------------- IN THE MESSAGE CLASS FOR THE JOB.
*                  MVS/XA : MUST BE AMODE AND RMODE 24.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
         ENTRY #RMSW#
         SPACE 1
        $DEFREG
*        MISCELLANEOUS DEFINITIONS.
         SPACE 1
OFF      EQU   X'0F'               NOP OPERATION SWITCH SETTING.
ON       EQU   X'F0'               BRANCH OPERATION SWITCH SETTING.
         EJECT
        PRINT  NOGEN
TIODSECT DSECT
        IEFTIOT1
        DCBD   DSORG=PS,DEVD=DA
        PRINT  GEN
         SPACE 1
*        FILE BLOCK WORK AREA DESCRIPTION.
         SPACE 1
FBWAD    DSECT
FCHAIN   DC    F'0'                CHAIN WORD.
FDDNM    DC    CL8' '              DD-NAME.
FERCNT   DC    H'10'               ERROR COUNT.
FFLAGS   DC    XL1'0'              FLAGS BYTE.
FAB      EQU   X'01'                    ABEND.
FAM      EQU   X'02'                    ABEND S013-18.
FOE      EQU   X'04'                    OPEN ERROR.
FRE      EQU   X'08'                    READ ERROR.
         DC    XL1'0'              UNUSED.
         SPACE 1
READ     CSECT                     RESTORE MAIN CSECT.
         EJECT
        $XENT  BASE=R12
         SPACE 1
         SR    R9,R9               SET DEFAULTS.
         LA    R8,DDDEF
         LA    R10,MAXERR
         TM    0(R1),X'80'         CONTROL ARGUMENTS.
         BO    RD1
         L     R7,4(R1)
         LA    R7,0(R7)
         LTR   R7,R7
         BZ    *+L'*+2
         LR    R8,R7
         TM    4(R1),X'80'
         BO    RD1
         TM    8(R1),X'80'
         BZ    ERR3
         TM    11(R1),X'03'
         BNZ   ERR3
         L     R9,8(R1)
         LA    R9,0(R9)
RD1      L     R7,0(R1)
         LA    R7,0(R7)
         SPACE 1
         LA    R3,CHAIN            LOOK IF ALREADY DEFINED.
         USING FBWAD,R4
RD2      L     R4,0(R3)
         LTR   R4,R4
         BZ    RD3
         CLC   FDDNM(L'FDDNM),0(R8)
         BE    RD8
         LR    R3,R4
         B     RD2
RD3     GETMAIN R,LV=WSZ
         LR    R4,R1
         ST    R4,0(R3)            NEW FILE.
         MVC   FCHAIN(WSZ),BFILE   SET SKELETON.
         MVC   FDDNM(L'FDDNM),0(R8)
         LA    R2,DZ(R4)
         USING IHADCB,R2
         MVC   DCBDDNAM(L'DCBDDNAM),FDDNM
         OC    ATIOT(L'ATIOT),ATIOT     TEST FIRST TIME.
         BNZ   RD4
        EXTRACT    ATIOT,'S',FIELDS=(TIOT)
RD4      L     R3,ATIOT            SEARCH DD AND OPEN IT.
         USING TIODSECT,R3
RD5      CLC   TIOELNGH(4),ZEROS
         BE    ERR1
         CLC   TIOEDDNM(L'DCBDDNAM),DCBDDNAM
         BE    RD6
         SR    R1,R1
         IC    R1,TIOELNGH
         ALR   R3,R1
         B     RD5
         DROP  R3
RD6     OPEN   ((R2),(INPUT))
         TM    FFLAGS,FAM          TEST ABEND S013-18 SWITCH.
         BZ    RD7
        WTO    MF=(E,ERAB)
         B     ERR2
RD7      TM    FFLAGS,FOE          TEST OPEN ERROR SWITCH.
         BO    ERR2
         TM    DCBOFLGS,DCBOFOPN   TEST OPEN SUCCESSFULL.
         BO    RD9
         B     ERR2
RD8      TM    FFLAGS,FAB          TEST ABEND SWITCH.
         BO    ABND1
         LA    R2,DZ(R4)
         TM    DCBOFLGS,DCBOFOPN   TEST ALREADY OPENED.
         BZ    RD6
         DROP  R2
         SPACE 1
RD9      NI    FFLAGS,255-FRE      CLEAR READ ERROR SWITCH.
        GET    (R2),(R7)
         TM    FFLAGS,FRE          TEST READ ERROR SWITCH.
         BO    ERR4
         SR    R10,R10
         B     RD13
         SPACE 1
RDERR    OI    FFLAGS,FRE          SET READ ERROR SWITCH.
         BR    R14
         SPACE 1
RDEND   CLOSE  ((R2))
         SR    R10,R10
         BCTR  R10,0
         B     RD13
         SPACE 1
ERR1     BCTR  R10,0
         MVC   SWMS+1(1),SWMS
ERR2     BCTR  R10,0
         OI    FFLAGS,FAB          SET ABEND SWITCH TO PREVENT RE-CALL.
ERR3     BCTR  R10,0
ERR4     BCTR  R10,0
         SPACE 1
MAXERR   EQU   (((*-ERR1)-10)/2)+1
         SPACE 1
         LR    R2,R10
         BCTR  R2,0
         SLL   R2,2
         L     R3,TABD(R2)
         NI    SWEX+1,OFF          SET SWITCH OFF.
         CLI   12(R3),C' '
         BNE   *+L'*+10
         OI    SWEX+1,ON           SET SWITCH ON.
         MVC   12(8,R3),0(R8)
         LR    R1,R3
         CLI   SWMS+1,0
         BNE   SWEX
        WTO    MF=(E,(1))
SWEX     B     RD10
         LA    R3,CNTR             CONTROL MAX. ERROR.
         B     RD11
RD10     MVC   12(8,R3),BLKS
         LA    R3,FERCNT
RD11     LH    R2,0(R3)
         BCT   R2,RD12
         LA    R1,2                SET ABEND CODE.
         B     ABND
RD12     STH   R2,0(R3)
         DROP  R4
         SPACE 1
RD13     XC    SWMS,SWMS
         LTR   R9,R9
         BZ    RD14
         ST    R10,0(R9)           STORE IND.
RD14    $XRET  CC=(R10)
         SPACE 1
ABND1    LA    R1,1                SET ABEND CODE.
         SPACE 1
ABND     LR    R15,R10             SET C.C. IN REG. 15.
        ABEND  (1),DUMP
         SPACE 1
         CNOP  0,4
         USING IHADCB,R1
         USING FBWAD,R4
DCBEXIT  TM    DCBRECFM,DCBRECF    MUST BE FIXED RECORD LENGTH.
         BZ    EX1
         TM    DCBRECFM,DCBRECV
         BZR   R14
         DROP  R1
EX1      OI    FFLAGS,FOE          SET OPEN ERROR SWITCH.
         BR    R14
         SPACE 1
         CNOP  0,4
DCBABND  TM    3(R1),X'04'         TEST OK TO IGNORE.
         BZ    *+L'*+10            NO.
         CLC   0(3,R1),S013RC18    YES, TEST ABEND S013-18.
         BE    *+L'*+6             YES.
         MVI   3(R1),0             NO, ABEND TASK IMMEDIATELY.
         BR    R14
         MVI   3(R1),4             SET IGNORE ABEND CONDITION.
         OI    FFLAGS,FAM
         BR    R14
S013RC18 DC    XL3'013018'
         SPACE 1
         DROP  R4,R12
         EJECT
         ENTRY RCLOSE
         SPACE 1
RCLOSE  $XENT  BASE=R12,DT=NO
         SPACE 1
         SR    R9,R9               CONTROL ARGUMENTS.
         LTR   R8,R1
         BZ    RC2
         L     R8,0(R1)
         LA    R8,0(R8)
         TM    0(R1),X'80'
         BO    RC1
         TM    4(R1),X'80'
         BZ    ERCL
         TM    7(R1),X'03'
         BNZ   ERCL
         L     R9,4(R1)
         LA    R9,0(R9)
RC1      LTR   R8,R8
         BZ    RC2
         CLC   0(8,R8),KEYALL
         BNE   RC2
         SR    R8,R8
RC2      LA    R3,CHAIN
         USING FBWAD,R4
RC3      L     R4,0(R3)
         LTR   R4,R4
         BZ    RC6
         LTR   R8,R8
         BZ    RC4
         CLC   0(L'FDDNM,R8),FDDNM
         BE    RC4
         LR    R3,R4
         B     RC3
RC4      LA    R2,DZ(R4)
         USING IHADCB,R2
         TM    DCBOFLGS,DCBOFOPN   TEST OPENED.
         BZ    RC5
        CLOSE  ((R2))
         DROP  R2
RC5      MVC   0(L'FCHAIN,R3),FCHAIN    DEQUEUE FILE.
         LR    R1,R4
         DROP  R4
        FREEMAIN R,A=(1),LV=WSZ
         LTR   R8,R8
         BNZ   RC7
         B     RC3
RC6      LTR   R8,R8
         BNZ   ERCL
RC7      SR    R10,R10
         LTR   R9,R9
         BZ    RC8
         ST    R10,0(R9)           STORE IND.
RC8     $XRET  CC=(R10)
         SPACE 1
ERCL    WTO    'ERR=1 - RCLOSE CALLING SEQUENCE, ARGUMENT ALIGNMENT OR 1
               DD-NAME ERROR.',ROUTCDE=11,DESC=7
         LA    R10,1
         B     RC7+L'RC7
         SPACE 1
         DROP  R12
         EJECT
*        CONSTANTS AND WORK AREAS.
         SPACE 1
CHAIN    DC    A(*-*)
ATIOT    DC    A(*-*)
TABD     DC    A(DIAG1,DIAG2,DIAG3,DIAG4)
ZEROS    DC    F'0'
ERAB    WTO    'ERR=2 - ABEND CODE SYSTEM=S013-18 (IEC141I).',         1
               ROUTCDE=11,DESC=7,MF=L
DIAG1   WTO    'ERR=1 -          DD-CARD MISSING.',                    1
               ROUTCDE=11,DESC=7,MF=L
DIAG2   WTO    'ERR=2 -          OPEN ERROR OR DD-CARD CONFLICT.',     1
               ROUTCDE=11,DESC=7,MF=L
DIAG3   WTO    'ERR=3 - READ CALLING SEQUENCE OR ARGUMENT ALIGNMENT ERR1
               OR.',ROUTCDE=11,DESC=7,MF=L
DIAG4   WTO    'ERR=4 -          I/O ERROR.',                          1
               ROUTCDE=11,DESC=7,MF=L
CNTR     DC    H'0'
DDDEF    DC    CL8'SYSIN  '
KEYALL   DC    CL8'CLOSEALL'
BLKS     DC    CL8' '
         SPACE 2
#RMSW#   DS    0H
SWMS     DC    H'0'
         EJECT
        CNOP   0,8
         SPACE 1
BFILE    DC    F'0'                CHAIN WORD.
         DC    CL8' '              DD-NAME.
         DC    H'10'               ERROR COUNT.
         DC    XL1'0'              FLAGS BYTE.
         DC    XL1'0'              UNUSED.
         SPACE 1
DCBRDR  DCB    DSORG=PS,DEVD=DA,MACRF=GM,BFALN=D,                      1
               EXLST=REXLIST,SYNAD=RDERR,EODAD=RDEND,DDNAME=XXXXXXXX
         SPACE 1
        CNOP   0,8
         SPACE 1
DZ       EQU   DCBRDR-BFILE
WSZ      EQU   (((*-BFILE)+7)/8)*8
         SPACE 1
REXLIST  DC    0F'0',XL1'05',AL3(DCBEXIT),XL1'91',AL3(DCBABND)
         SPACE 2
         END
