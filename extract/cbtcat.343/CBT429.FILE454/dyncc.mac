DYCC     TITLE 'DYNAMIC CONCATENATION-DECONCATENATION ROUTINE.'
DYNCC    START 0
         SPACE 1
         ENTRY DYNDC
         SPACE 1
* CALLING SEQUENCE : CALL DYNCC,(A,B),VL
* ------------------ CALL DYNDC,(A,B),VL
*                               (A)
*
*        WHERE : A - IS THE ADDRESS OF THE LIST OF THE DDNAMES TO BE
*                    CONCATENATED (DYNCC) OR THE DDNAME OF THE GROUP
*                    TO BE DECONCATENATED (DYNDC).
*                    DYNCC : A ===> DC CL8'DD1'
*                                   DC CL8'DD2'
*                                   ...
*                                   DC CL8'DDN'
*                                   DC XL1'0' OR CL1' ' = END OF LIST.
*                    DYNDC : A ===> DC CL8'DDG'
*
*                B - RECEIVES THE RETURN CODES (FORMAT FULLWORDS -
*                    F BOUNDARIES) IF SUPPLIED (OPTIONAL).
*                            B ===> DC 2F'0'
*                    IN ANY CASE, THE RETURN CODES ARE ALWAYS GIVEN
*                    BACK IN REGISTERS 15 AND 0.
*
*        RETURN CODES (REGISTER 15) :
*                 0-4-8-12 = SVC 99 RETURN CODES. R0 = REASON CODE
*                            FIELDS = 0-1 - ERROR CODE
*                                     2-3 - INFORMATION CODE
*                       16 = CALLING SEQUENCE OR PARAMETER ALIGNMENT
*                            ERROR (R0 = 0).
*                       20 = DDNAME CODING ERROR. R0 = RELATIVE DDNAME
*                            POSITION FROM THE BEGINNING OF THE LIST
*                            (0 = 1ST, 1 = 2ND, ...).
*
* MODULE ATTRIBUTE : THIS ROUTINE IS RE-ENTERABLE.
* ------------------
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
WRKDSECT DSECT                     WORK AREAS DESCRIPTION
         SPACE 1
SVA      DS    18F                 SAVE AREA
RCODES   DS    2F                  RETURN CODES (R15-R0)
RBKPTR   DS    F
         SPACE 1
WRKL     EQU   512                 WORK AREAS LENGTH
DYNWRKL  EQU   (WRKL-(*-SVA))
DYNWORK  DS    (DYNWRKL)X
         SPACE 1
DYNCC    CSECT                     RESTORE ORIGINAL CSECT
         SPACE 2
        $DEFREG
         EJECT
         USING *,R15
         N     R1,SETOFF           SAY DYNCC ENTRY
         B     COMMON              GO COMMON ENTRY
SETOFF   DC    0F'0',XL4'7FFFFFFF'
         DROP  R15
         SPACE 1
         USING *,R15
DYNDC    O     R1,SETON            SAY DYNDC ENTRY
         B     COMMON              GO COMMON ENTRY
SETON    DC    0F'0',XL4'80000000'
         DROP  R15
        CNOP   6,8
COMMON   BALR  R15,0               SET COMMON ENTRY ADDRESS
         SPACE 1
DYNCOM  $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R10,R13
         USING WRKDSECT,R10
         LA    R2,0(R1)            SAVE PARM. POINTER
         STCM  R1,B'1000',SVA      SAVE ENTRY SWITCH
         EJECT
         LA    R14,RCODES          CLEAR WORK AREA
         L     R15,=A(WRKL-(18*4))
         XR    R0,R0
         LR    R1,R0
         MVCL  R14,R0
         SPACE 1
         XR    R8,R8               GET ARGUMENTS ADDRESSES
         XR    R9,R9
         TM    0(R2),X'80'
         BO    GADD
         TM    4(R2),X'80'
         BZ    ERSA
         TM    7(R2),X'03'
         BNZ   ERSA
         L     R9,4(R2)
         LA    R9,0(R9)
GADD     L     R8,0(R2)
         LA    R8,0(R8)
         SPACE 1
         LA    R2,DYNWORK
         USING S99RB,R2
         ST    R2,RBKPTR           POINT TO REQUEST BLOCK
         OI    RBKPTR,S99RBPND
         LA    R1,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK
         STC   R1,S99RBLN
         LA    R1,S99RBEND         PICK UP ADDRESS FOR TEXT POINTERS
         ST    R1,S99TXTPP
         LA    R3,4(R1)            TEXT UNIT POINTER
         USING S99TUPL,R1
         ST    R3,S99TUPTR
         OI    S99TUPTR,S99TUPLN   INDICATE END OF LIST
         DROP  R1
         USING S99TUNIT,R3
         MVC   S99TUKEY,=H'1'      SET KEY
         TM    SVA,X'80'           GO TO SPECIFIC REQUEST
         BO    DDC
         MVI   S99VERB,S99VRBCC    REQUEST IS CONCATENATION
         LA    R4,S99TUENT
         USING S99TUFLD,R4
         CLI   0(R8),C' '
         BE    ERDD
         CLI   0(R8),0
         BE    ERDD
         XR    R7,R7
DCC1     LA    R14,8
         LA    R15,7(R8)
         CLI   0(R15),C' '
         BNE   DCC3
         BCTR  R15,0
         BCT   R14,*-10
DCC2     ST    R7,RCODES+4
         B     ERDD
DCCMNM   MVC   S99TUPRM(*-*),0(R8) <<EXECUTED>>
DCC3     STCM  R14,B'0011',S99TULEN
         BCTR  R14,0
         EX    R14,DCCMNM
         LA    R4,S99TUPRM+1(R14)
         AH    R7,=H'1'
         LA    R8,8(R8)
         CLI   0(R8),C' '
         BE    *+L'*+8
         CLI   0(R8),0
         BNE   DCC1
         DROP  R4
         CH    R7,=H'2'
         BL    DCC2
         STCM  R7,B'0011',S99TUNUM
         B     REQCD
DDC      MVI   S99VERB,S99VRBDC    REQUEST IS DECONCATENATION
         MVC   S99TUNUM,=H'1'
         LA    R14,8
         LA    R15,7(R8)
         CLI   0(R15),C' '
         BNE   DDC1
         BCTR  R15,0
         BCT   R14,*-10
         B     ERDD
DDCMNM   MVC   S99TUPAR(*-*),0(R8) <<EXECUTED>>
DDC1     STCM  R14,B'0011',S99TULNG
         BCTR  R14,0
         EX    R14,DDCMNM
         DROP  R3
         SPACE 1
REQCD    LA    R1,RBKPTR
        DYNALLOC
         ST    R15,RCODES          SET RETURN CODE
         MVC   RCODES+4(L'S99RSC),S99RSC     PASS REASON CODES
         DROP  R2
         EJECT
LEAVE    LTR   R9,R9
         BZ    EXIT
         MVC   0(8,R9),RCODES      PASS RETURN CODES
EXIT     L     R1,4(R13)
         MVC   20(4,R1),RCODES+4   SET R0
         L     R15,RCODES          GET R15
        $XRET  CC=(R15),LV=WRKL,TYPE=RENT
ERSA     MVI   RCODES+3,16         SET R.C. = 16
         B     LEAVE
ERDD     MVI   RCODES+3,20         SET R.C. = 20
         B     LEAVE
         DROP  R10
         SPACE 2
        LTORG
         SPACE 2
        PRINT  NOGEN
        IEFZB4D0
        IEFZB4D2
         SPACE 2
         END
