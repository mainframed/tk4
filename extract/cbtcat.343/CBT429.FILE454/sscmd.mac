SSCS     TITLE 'SEND A SYSTEM COMMAND SUBROUTINE.'
SSCMD    START 0
         SPACE 1
* CALLING SEQUENCE : CALL SSCMD,(A,B,C),VL
* ------------------            (A,B)
*                               (A)
*
*        WHERE : A - IS THE ADDRESS OF THE AREA CONTAINING THE SYSTEM
*                    COMMAND TEXT.
*
*                B - IS THE ADDRESS OF THE SYSTEM COMMAND TEXT AREA
*                    LENGTH (NUMBER OF CHARACTERS, FORMAT FULLWORD -
*                    F BOUNDARY). WHEN THIS ARGUMENT IS OMITTED, OR
*                    THE ADDRESS OF THIS ARGUMENT IS SET TO ZERO IN
*                    THE PARAMETERS LIST, THE LENGTH OF THE TEXT AREA
*                    IS SUPPOSED TO BE IN FRONT OF THE TEXT AREA, AT
*                    THE ADDRESS OF A-1 (FORMAT XL1).
*
*                C - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                         0 = COMMAND SENDED CORRECTLY.
*                    LT 256 = SVC 34 RETURN CODE.
*                       256 = COMMAND TEXT LENGTH IS ZERO.
*                       260 = COMMAND TEXT IS ALL BLANKS.
*                       264 = COMMAND TEXT LENGTH GREATER THAN 126.
*                       268 = CALLING SEQUENCE OR PARAMETER ALIGNMENT
*                             ERROR.
*                       272 = UNAUTHORIZED CALLER.
*
* GENERAL NOTICE : THIS SUBROUTINE IS REENTRANT.
* ================ ONLY A SYSTEM OR STARTED TASK MAY USE THIS ROUTINE.
*                  THE SYSTEM COMMAND TEXT IS EXTRACTED FROM THE AREA
*                  ELIMINATING ALL EVENTUAL STARTING AND/OR TRAILING
*                  BLANKS, THEN IT CANNOT BE MORE THAN 126 CHARACTERS
*                  LONG. ALL COMMANDS SENDED TO THE SYSTEM ARE LOGGED
*                  ON 'SYSLOG' WITH THE SENDER NAME.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R11,R13
         USING WRKDSECT,R11        WORK AREAS ADDRESSABILITY
         EJECT
         XR    R7,R7
         XR    R8,R8
         XR    R9,R9
         XR    R10,R10
         TM    0(R1),X'80'
         BO    SSC1
         TM    7(R1),X'03'
         BNZ   ER268
         L     R8,4(R1)
         LA    R8,0(R8)
         TM    4(R1),X'80'
         BO    SSC1
         TM    8(R1),X'80'
         BZ    ER268
         TM    11(R1),X'03'
         BNZ   ER268
         L     R9,8(R1)
         LA    R9,0(R9)
SSC1     L     R7,0(R1)
         LA    R7,0(R7)
         LTR   R8,R8
         BNZ   SSC2
         LR    R8,R7
         BCTR  R8,0
         XR    R3,R3
         ICM   R3,B'0001',0(R8)
         BNZ   SSC3
         B     ER256
SSC2     L     R3,0(R8)
         LTR   R3,R3
         BNP   ER256
SSC3     LA    R5,0(R7,R3)
         BCTR  R5,0
         LA    R4,1
SSC4     CLI   0(R7),C' '          ELIMINE STARTING BLANKS IF ANY
         BNE   SSC5
         BCT   R3,*+L'*+4
         B     ER260
         BXLE  R7,R4,SSC4
         B     ER260
SSC5     CLI   0(R5),C' '          ELIMINE TRAILING BLANKS IF ANY
         BNE   SSC6
         BCTR  R5,0
         BCT   R3,SSC5
         B     ER260
SSC6     CH    R3,=Y(L'MGCRTEXT)
         BH    ER264
         MVC   SLOG(SLOGL),SLOGSK
         XC    SLOGMS,SLOGMS
         LR    R4,R3
         CH    R4,=Y(SLOGLT)
         BNH   *+L'*+4
         LH    R4,=Y(SLOGLT)
         BCT   R4,*+L'*+6          DO EXECUTE LENGTH
         MVC   SLOGMS(*-*),0(R7)   <<EXECUTED>>
         EX    R4,*-6              MOVE COMMAND TEXT TO LOG
         LA    R1,SLOGL+1(R4)
         STH   R1,SLOG             SET LENGTH FOR WTL
         MVI   SWITCH,0
         L     R4,CVTPTR
         USING CVT,R4
         L     R5,CVTTCBP
         DROP  R4
         L     R4,4(R5)
         USING TCB,R4
         L     R5,TCBJSCB
         USING IEZJSCB,R5
         ICM   R6,B'1111',JSCBCSCB GET THE CSCB ADDRESS
         BZ    SSC10               CSCB MISSING, SYSTEM TASK
         USING CSCB,R6
         ICM   R1,B'0111',CHUNIT   ANY UNIT NAME?
         BNZ   SSC10               YES, STARTED TASK
         CLI   CHTRKID,CHSASID     SYSTEM A.S.?
         BE    SSC10               YES
         CLI   CHTRKID,CHINITID    INITIATOR?
         BNE   ER272               NO
         DROP  R6
SSC10    TM    JSCBOPTS,JSCBAUTH   AUTHORIZED?
         BZ    *+L'*+4             NO
         OI    SWITCH,PPAUTH       YES, ALREADY AUTHORIZED
         DROP  R5
         L     R5,TCBRBP
         USING RBBASIC,R5
         TM    RBOPSWB2,RBOPSWPS   PROBLEM STATE?
         BO    *+L'*+4             YES
         OI    SWITCH,PPSUPM       NO, ALREADY SUPERVISOR MODE
         DROP  R5
         L     R5,TCBTIO           GET TIOT ADDRESS
         DROP  R4
         USING TIODSECT,R5
         MVC   SLOGNM,TIOCNJOB     SET NAME TO LOG
         DROP  R5
         LA    R2,CMND             COMMAND BUFFER ADDRESS
         USING MGCRPL,R2
         XC    MGCRPL(MGCRLTH),MGCRPL   CLEAR PARAMETER LIST
         MVI   MGCRFLG1,MGCRFI     SET MGCRFLG2 VALID
         BCT   R3,*+L'*+6          DO EXECUTE LENGTH
         MVC   MGCRTEXT(*-*),0(R7) <<EXECUTED>>
         EX    R3,*-6              MOVE COMMAND TEXT
         LA    R1,(MGCRTEXT-MGCRPL)+1(R3)
         STC   R1,MGCRLGTH         SET LENGTH IN MGCRPL
         DROP  R2
        WTL    MF=(E,SLOG)         LOG THE COMMAND
         TM    SWITCH,PPAUTH
         BO    SSC20
        SETAUTH ,                  SET AUTHORIZATION ON
SSC20    TM    SWITCH,PPSUPM
         BO    SSC21
        MODESET MODE=SUP           ENTER SUPERVISOR MODE
SSC21    XR    R2,R2
        MODESET EXTKEY=ZERO,SAVEKEY=(2) ENTER KEY ZERO
         XR    R0,R0               SEND COMMAND
         XR    R15,R15
        MGCR   CMND
         LR    R10,R15             GET RETURN CODE
        MODESET KEYADDR=(2)        RE-ENTER PROBLEM KEY
         TM    SWITCH,PPSUPM
         BO    SSC22
        MODESET MODE=PROB          RE-ENTER PROBLEM MODE
SSC22    TM    SWITCH,PPAUTH
         BO    SSC80
        RESAUTH ,                  SET AUTHORIZATION OFF
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* NOTE - MVS/XA (MVS/SP 2.1.3) :                                      *
*        THE FOUR INSTRUCTIONS BELOW ARE ADDED JUST BECAUSE SOME      *
*        OPERATORS COMMANDS (LIKE VARY B20,MSTCONS), PASSED THROUGH   *
*        SVC 34, GIVES BACK AN ADDRESS INTO MODULE 'IEE0003D' INSTEAD *
*        OF A RETURN CODE IN THE REGISTER 15 (DIAGNOSE HAS BEEN DONE  *
*        USING DIDOCS).                                               *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
SSC80    CL    R10,=F'256'         R.C. CONTROL
         BL    SSC90               OK
         XR    R10,R10             RESET
SSC90    LTR   R9,R9
         BZ    *+L'*+4
         ST    R10,0(R9)           STORE INDICATOR
        $XRET  CC=(R10),LV=WRKL,TYPE=RENT
ER272    LA    R10,4(R10)
ER268    LA    R10,4(R10)
ER264    LA    R10,4(R10)
ER260    LA    R10,4(R10)
ER256    LA    R10,256(R10)
         B     SSC90
         EJECT
*        CONSTANTS.
*        ----------
         SPACE 1
SLOGSK  WTL    '==>J-         : ',MF=L  SYSTEM LOG INFO
         SPACE 2
        LTORG
         SPACE 2
         DROP  R11,R12
         EJECT
*        DSECT DEFINITIONS.
*        ------------------
         SPACE 1
WRKDSECT DSECT ,                   WORK AREAS
         DC    18F'0'              SAVE AREA
         SPACE 1
CMND     DC    0F'0',XL(MGCRLTH)'0'     OPERATOR COMMANDS BUFFER
SLOG    WTL    '==>J-         : ',MF=L  SYSTEM LOG INFO
SLOGL    EQU   *-SLOG
SLOGLT   EQU   L'MGCRTEXT-(SLOGL-4)
SLOGNM   EQU   SLOG+9,8
SLOGMS   DC    XL(SLOGLT)'0'
         SPACE 1
SWITCH   DC    XL1'0'              PROCESSING SWITCHES
PPAUTH   EQU   X'80'                    PP ALREADY AUTHORIZED
PPSUPM   EQU   X'40'                    PP ALREADY SUPERVISOR MODE
         SPACE 1
WRKL     EQU   (((*-WRKDSECT)+7)/8)*8
         SPACE 2
        PRINT  NOGEN
TIODSECT DSECT
        IEFTIOT1
        CVT    DSECT=YES,LIST=YES
        IKJTCB
        IHARB
        IEZJSCB
CSCB     DSECT
        IEECHAIN
        IEZMGCR
        PRINT  GEN
         SPACE 2
         END
