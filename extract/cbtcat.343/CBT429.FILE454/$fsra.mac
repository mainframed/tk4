         MACRO
&LABEL  $FSRA
.*
.* MACRO : INTERNALLY USED BY $FS MACRO.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLA  &FSMDLN,&GMFCI
         GBLB  &GFUSW,&GFESW,&GFISW
         GBLC  &GFSMF,&GFSMI
         LCLA  &BS,&I1,&I2,&R,&C,&LSZ,&L,&NR
         LCLC  &B1,&B2,&O,&RCH,&RG,&T1,&T2
         AIF   (NOT &GFUSW).USEERR
         AIF   (N'&SYSLIST(1) EQ 4).M0
         AIF   (N'&SYSLIST(1) NE 3).ARGERR
.M0      AIF   (&FSMDLN EQ 1).M1
         AIF   (&FSMDLN EQ 2).M2
         AIF   (&FSMDLN EQ 3).M3
         AIF   (&FSMDLN EQ 4).M4
         AIF   (&FSMDLN EQ 5).M5
         AGO   .DFLT
.M1      AIF   (&SYSLIST(1,1) GT 12).M1RERR
         AIF   (&SYSLIST(1,2) GT 40).M1CERR
&LSZ     SETA  40
         AGO   .SROW
.M3      AIF   (&SYSLIST(1,1) GT 32).M3RERR
         AIF   (&SYSLIST(1,2) GT 80).M3CERR
         AGO   .SLNL
.M4      AIF   (&SYSLIST(1,1) GT 43).M4RERR
         AIF   (&SYSLIST(1,2) GT 80).M4CERR
         AGO   .SLNL
.M5      AIF   (&SYSLIST(1,1) GT 27).M5RERR
         AIF   (&SYSLIST(1,2) GT 132).M5CERR
&LSZ     SETA  132
         AGO   .SROW
.DFLT    ANOP
.M2      AIF   (&SYSLIST(1,1) GT 24).M2RERR
         AIF   (&SYSLIST(1,2) GT 80).M2CERR
.SLNL    ANOP
&LSZ     SETA  80
.SROW    ANOP
&R       SETA  1
         AIF   (&SYSLIST(1,1) LE 1).SCOL
&R       SETA  &SYSLIST(1,1)
.SCOL    ANOP
&C       SETA  1
         AIF   (&SYSLIST(1,2) LE 1).CMPTE
&C       SETA  &SYSLIST(1,2)
.CMPTE   ANOP
&BS      SETA  &LSZ*(&R-1)+&C-1
&I1      SETA  &BS/64
&I2      SETA  2*(&BS-(64*&I1)+1)-1
&I1      SETA  2*(&I1+1)-1
&B1      SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5X
               B5C5D5E5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6FX
               7F8F97A7B7C7D7E7F'(&I1,2)
&B2      SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5X
               B5C5D5E5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6FX
               7F8F97A7B7C7D7E7F'(&I2,2)
         AIF   (K'&SYSLIST(1,3) NE 3).CNT
         AIF   (N'&SYSLIST(1) NE 3).ARGERR
         AIF   ('&SYSLIST(1,3)' EQ 'EUA').EUA
         AIF   ('&SYSLIST(1,3)'(1,1) NE '''').ARGERR
         AIF   ('&SYSLIST(1,3)'(3,1) NE '''').ARGERR
&RCH     SETC  '&SYSLIST(1,3)'(2,1)
         AGO   .CH
.CNT     AIF   (K'&SYSLIST(1,3) NE 2).NGE
         AIF   ('&SYSLIST(1,3)' NE 'GE').NGE
         AIF   (N'&SYSLIST(1) NE 4).ARGERR
&RCH     SETC  '&SYSLIST(1,4)'
         AIF   (K'&RCH NE 4).NF
         AIF   ('&RCH' NE 'FILL').ARGERR
&RCH     SETC  '00'
         AGO   .STC
.NF      AIF   (K'&RCH NE 2).ARGERR
&T1      SETC  '&RCH'(1,1)
&T2      SETC  '&RCH'(2,1)
         AIF   ('&T1' EQ '4').TSN
         AIF   ('&T1' EQ '5').TSN
         AIF   ('&T1' EQ '6').TSN
         AIF   ('&T1' EQ '7').TSN
         AIF   ('&T1' EQ '8').TSN
         AIF   ('&T1' EQ '9').TSN
         AIF   ('&T1' EQ 'A').TSN
         AIF   ('&T1' EQ 'B').TSN
         AIF   ('&T1' EQ 'C').TSN
         AIF   ('&T1' EQ 'D').TSN
         AIF   ('&T1' EQ 'E').TSN
         AIF   ('&T1' NE 'F').ARGERR
.TSN     AIF   ('&T2' EQ '0').STC
         AIF   ('&T2' EQ '1').STC
         AIF   ('&T2' EQ '2').STC
         AIF   ('&T2' EQ '3').STC
         AIF   ('&T2' EQ '4').STC
         AIF   ('&T2' EQ '5').STC
         AIF   ('&T2' EQ '6').STC
         AIF   ('&T2' EQ '7').STC
         AIF   ('&T2' EQ '8').STC
         AIF   ('&T2' EQ '9').STC
         AIF   ('&T2' EQ 'A').STC
         AIF   ('&T2' EQ 'B').STC
         AIF   ('&T2' EQ 'C').STC
         AIF   ('&T2' EQ 'D').STC
         AIF   ('&T2' EQ 'E').STC
         AIF   ('&T1' EQ 'F').ARGERR
         AIF   ('&T2' NE 'F').ARGERR
.STC     ANOP
&RCH     SETC  '08&RCH'
&L       SETA  5
&O       SETC  '3C'
         AGO   .GRA
.NGE     AIF   (N'&SYSLIST(1) EQ 4).ARGERR
&RCH     SETC  '&SYSLIST(1,3)'
         AIF   (K'&RCH EQ 1).CH
         AIF   (K'&RCH NE 2).ARGERR
         AIF   ('&RCH' EQ '&&').CH
         AIF   ('&RCH' NE '''''').HEX
.CH      AIF   (&GFISW).GXI
         AIF   (&GFESW).GXQ
&LABEL   DC    XL3'3C&B1&B2',C'&RCH'
         MEXIT
.GXQ     ANOP
&LABEL   MVC   0(3,&GFSMF),=XL3'3C&B1&B2'
         MVI   3(&GFSMF),C'&RCH'
         LA    &GFSMF,4(,&GFSMF)
         MEXIT
.GXI     AIF   ('&GFSMI'(1,1) EQ '(').GXIR
&LABEL   MVC   &GFSMI+&GMFCI.(3),=XL3'3C&B1&B2'
&GMFCI   SETA  &GMFCI+3
         MVI   &GFSMI+&GMFCI,C'&RCH'
&GMFCI   SETA  &GMFCI+1
         MEXIT
.GXIR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(3,&RG),=XL3'3C&B1&B2'
&GMFCI   SETA  &GMFCI+3
         MVI   &GMFCI.(&RG),C'&RCH'
&GMFCI   SETA  &GMFCI+1
         MEXIT
.HEX     ANOP
&L       SETA  4
&O       SETC  '3C'
         AGO   .GRA
.EUA     ANOP
&L       SETA  3
&O       SETC  '12'
.GRA     AIF   (&GFISW).GIX
         AIF   (&GFESW).GEX
&LABEL   DC    XL&L.'&O&B1&B2&RCH'
         MEXIT
.GEX     ANOP
&LABEL   MVC   0(&L,&GFSMF),=XL&L.'&O&B1&B2&RCH'
         LA    &GFSMF,&L.(,&GFSMF)
         MEXIT
.GIX     AIF   ('&GFSMI'(1,1) EQ '(').GIXR
&LABEL   MVC   &GFSMI+&GMFCI.(&L),=XL&L.'&O&B1&B2&RCH'
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.GIXR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(&L,&RG),=XL&L.'&O&B1&B2&RCH'
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.USEERR  MNOTE 8,'--- INVALID MACRO USAGE'
         MEXIT
.ARGERR  MNOTE 8,'--- RA=(...) INVALID CODING'
         MEXIT
.M1RERR  MNOTE 8,'--- RA - ROW NOT 1 THRU 12 FOR M=1'
         AGO   .DUMMY
.M1CERR  MNOTE 8,'--- RA - COLUMN NOT 1 THRU 40 FOR M=1'
         AGO   .DUMMY
.M2RERR  MNOTE 8,'--- RA - ROW NOT 1 THRU 24 FOR M=2'
         AGO   .DUMMY
.M2CERR  MNOTE 8,'--- RA - COLUMN NOT 1 THRU 80 FOR M=2'
         AGO   .DUMMY
.M3RERR  MNOTE 8,'--- RA - ROW NOT 1 THRU 32 FOR M=3'
         AGO   .DUMMY
.M3CERR  MNOTE 8,'--- RA - COLUMN NOT 1 THRU 80 FOR M=3'
         AGO   .DUMMY
.M4RERR  MNOTE 8,'--- RA - ROW NOT 1 THRU 43 FOR M=4'
         AGO   .DUMMY
.M4CERR  MNOTE 8,'--- RA - COLUMN NOT 1 THRU 80 FOR M=4'
         AGO   .DUMMY
.M5RERR  MNOTE 8,'--- RA - ROW NOT 1 THRU 27 FOR M=5'
         AGO   .DUMMY
.M5CERR  MNOTE 8,'--- RA - COLUMN NOT 1 THRU 132 FOR M=5'
.DUMMY   AIF   (&GFESW).GXD
&LABEL   DC    XL4'3C000000'
         MEXIT
.GXD     ANOP
&LABEL   MVC   0(4,&GFSMF),=XL4'3C000000'
         LA    &GFSMF,4(,&GFSMF)
         MEND
