PROC     0
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       PURPOSE : CLIST TO EXECUTE "SHOWMVS"                    (PM)*/
/*       PRE-REQUISITE PRODUCTS : IOFSPF                             */
/*       PRE-REQUISITE COMMANDS : AUTHC                              */
/*                                DOCP (DOPROG)                      */
/*                                FILEINFO                           */
/*                                EUSID                              */
/*                                EUSUB                              */
/*                                INTRDR                             */
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN DO
           CONTROL MSG
           EXIT CODE(16)
           END
         ISPEXEC CONTROL ERRORS RETURN
         EUSID
         SET &LSCC = &LASTCC
         IF &LSCC NE 0 THEN DO
           SET ZERRSM = &STR(EUSID ERROR)
           SET ZERRLM = +
             &STR(ENVIRONMENT VARIABLES SETTING FAILED / CC=&LSCC)
           ISPEXEC SETMSG MSG(ISRZ002)
           ISPEXEC CONTROL ERRORS CANCEL
           CONTROL MSG
           EXIT CODE(16)
           END
         SET &SHFLD = &Z
         SET &SWDS = &STR(NO)
         SET &SHENV = &STR(ISPF)
         SET &CAPS = &STR(OFF)
         SET &HPG = &STR(YES)
         SET &LPG = &STR(60)
         SET &PGN = &STR(1)
         SET &SJCL = &Z
         SET &SHWMDS = &Z
         SET &DDS = &STR(OLD)
         SET &SHCUR = &STR(CURSOR(SHENV))
         GOTO RDSP
MDSP:    SET &ZERRALRM = &STR(NO)
         SET &ZERRHM = &STR(PAJTSHW0)
         SET &SHMSG = &STR(MSG(ISRZ002))
         GOTO DDSP
RDSP:    SET &SHMSG = &Z
DDSP:    SET &WWENV = &NRSTR(&SHENV)
         SET &WWCAPS = &NRSTR(&CAPS)
         SET &WWHPG = &NRSTR(&HPG)
         SET &WWLPG = &NRSTR(&LPG)
         SET &WWPGN = &NRSTR(&PGN)
         SET &WWSJCL = &NRSTR(&SJCL)
         SET &WWDS = &NRSTR(&SHWMDS)
         SET &WWDDS = &NRSTR(&DDS)
BDSP:    ISPEXEC DISPLAY PANEL(PAJSHWM) &SHMSG &SHCUR
         SET &LSCC = &LASTCC
         IF ((&LSCC EQ 0) AND ((&SHCM EQ CANCEL) OR (&SHCM EQ END))) +
           THEN SET &LSCC = 8
         IF &LSCC NE 0 THEN DO
           IF &LSCC GT 8 THEN DO
             SET ZERRSM = &STR(PANEL ERROR)
             SET ZERRLM = +
               &STR(PANEL DISPLAY FAILED / CC=&LSCC)
             ISPEXEC SETMSG MSG(ISRZ002)
             END
           ELSE SET &LSCC = 0
           ISPEXEC CONTROL ERRORS CANCEL
           CONTROL MSG
           EXIT CODE(&LSCC)
           END
         IF &SHCMD NE &Z THEN DO
           IF &SHOPN EQ &Z THEN AUTHC &SHCMD
           ELSE AUTHC &SHCMD &SHOPN
           SET &SWDS = &STR(NO)
           SET &SHCM = &Z
           SET &SHCMD = &Z
           SET &SHOPN = &Z
           GOTO BDSP
           END
         IF ((&NRSTR(&WWENV) NE (&NRSTR(&SHENV)) OR +
           (&NRSTR(&WWCAPS) NE (&NRSTR(&CAPS)) OR +
           (&NRSTR(&WWHPG) NE (&NRSTR(&HPG)) OR +
           (&NRSTR(&WWLPG) NE (&NRSTR(&LPG)) OR +
           (&NRSTR(&WWPGN) NE (&NRSTR(&PGN)) OR +
           (&NRSTR(&WWDS) NE (&NRSTR(&SHWMDS)) OR +
           (&NRSTR(&WWSJCL) NE (&NRSTR(&SJCL)) OR +
           (&NRSTR(&WWDDS) NE (&NRSTR(&DDS))) THEN DO
           SET &SWDS = &STR(NO)
           GOTO RDSP
           END
         IF &SWDS EQ &STR(NO) THEN DO
           SET &SWDS = &STR(YES)
           GOTO RDSP
           END
         SET &SWDS = &STR(NO)
         SET &PARM = &Z
         IF &CAPS EQ &STR(ON) THEN SET &PARM = &STR(C)
         IF &HPG EQ &STR(NO) THEN DO
           IF &PARM EQ &Z THEN SET &PARM = &STR(H(N))
           ELSE SET &PARM = &STR(&PARM H(N))
           END
         ELSE DO
           IF &LPG NE &STR(60) THEN DO
             IF &PARM EQ &Z THEN SET &PARM = &STR(L(&LPG))
             ELSE SET &PARM = &STR(&PARM L(&LPG))
             END
           IF &PGN NE &STR(1) THEN DO
             IF &PARM EQ &Z THEN SET &PARM = &STR(P(&PGN))
             ELSE SET &PARM = &STR(&PARM P(&PGN))
             END
           END
         IF (&NRSTR(&SHWMDS) NE &Z) THEN DO
           SET &SHCUR = &STR(CURSOR(SHWMDS))
           FILEINFO DSN('&SHWMDS')
           SET &LSCC = &LASTCC
           IF &LSCC NE 0 THEN DO
             SET &SWERR = &STR(YES)
             IF &LSCC EQ 24 THEN DO
               IF ((&MEMCNTL EQ &STR(YES) AND +
                 (&MEMFND EQ &STR(NO)) THEN SET &SWERR = &STR(NO)
               END
             IF &SWERR EQ &STR(YES) THEN DO
               SET ZERRSM = &STR(FILEINFO ERROR)
               SET ZERRLM = +
                 &STR(FILEINFO FAILED / CC=&LSCC / &OUTMSG)
               GOTO MDSP
               END
             END
           IF ((&DSORG NE &STR(PS)) AND (&DSORG NE &STR(PO))) THEN DO
             SET ZERRSM = &STR(DSORG ERROR)
             SET ZERRLM = +
               &STR(DATA-SET DSORG=&DSORG - IT MUST BE PS OR PO)
             GOTO MDSP
             END
           IF &DSORG EQ &STR(PO) THEN SET &DDS = &STR(OLD)
           IF ((&RECFM NE &STR(F)) AND (&CNTLCHAR NE &STR(A))) THEN DO
             SET ZERRSM = &STR(RECFM ERROR)
             SET ZERRLM = +
               &STR(DATA-SET RECFM=&RECFM&CNTLCHAR - IT MUST BE FA)
             GOTO MDSP
             END
           IF &LRECL NE &STR(00101) THEN DO
             SET ZERRSM = &STR(LRECL ERROR)
             SET ZERRLM = +
               &STR(DATA-SET LRECL=&LRECL - IT MUST BE 101)
             GOTO MDSP
             END
           IF &SHENV NE &STR(BATCH) THEN DO
             FREE F(SHOWMVS)
             ALLOC F(SHOWMVS) DA('&SHWMDS') &DDS
             SET &LSCC = &LASTCC
             IF &LSCC NE 0 THEN DO
               SET ZERRSM = &STR(ALLOC ERROR)
               SET ZERRLM = +
                 &STR(FILE ALLOCATION FAILED / CC=&LSCC)
               GOTO MDSP
               END
             END
           END
         SET &SHCUR = &STR(CURSOR(SHENV))
         IF (&SHENV NE &STR(BATCH)) THEN DO
           IF (&SHENV EQ &STR(TSO)) THEN DO
             IF &PARM EQ &Z THEN AUTHC SHOWMVS
             ELSE AUTHC SHOWMVS &PARM
             END
           ELSE DO
             IF &PARM EQ &Z THEN DOCP SHOWMVS 'SHOWMVS' AC
             ELSE DOCP SHOWMVS 'SHOWMVS &PARM' AC
             END
           IF (&NRSTR(&SHWMDS) NE &Z) THEN FREE F(SHOWMVS)
           ELSE DO
             IF (&SHENV EQ &STR(TSO)) THEN DO
               CONTROL MSG
               SET &IOPARM = &STR(I.&EURUSID)
               ISPEXEC CONTROL ERRORS CANCEL
               ISPEXEC SELECT PGM(IOFSPF) PARM(&IOPARM) NEWAPPL(IOF)
               CONTROL NOMSG
               ISPEXEC CONTROL ERRORS RETURN
               END
             END
           END
         ELSE DO
           ISPEXEC CONTROL ERRORS CANCEL
           FREE F(SYSPO SYSSU)
           CONTROL MSG
           ISPEXEC FTOPEN TEMP
           ISPEXEC FTINCL PAJSSHWM
           ISPEXEC FTCLOSE
           ISPEXEC VGET ZTEMPF
           ALLOC F(SYSPO) DA('&ZTEMPF')
           INTRDR SYSSU
           EUSUB
           CONTROL NOMSG
           FREE F(SYSPO SYSSU)
           CONTROL MSG
           SET &IOPARM = &STR(I.&EURUSID.S)
           ISPEXEC SELECT PGM(IOFSPF) PARM(&IOPARM) NEWAPPL(IOF)
           CONTROL NOMSG
           ISPEXEC CONTROL ERRORS RETURN
           END
           IF (&NRSTR(&SHWMDS) NE &Z) THEN DO
             CONTROL MSG
             ISPEXEC CONTROL ERRORS CANCEL
             ISPEXEC BROWSE DATASET('&SHWMDS')
             CONTROL NOMSG
             ISPEXEC CONTROL ERRORS RETURN
             END
         GOTO RDSP
