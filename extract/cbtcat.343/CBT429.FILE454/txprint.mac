TPR      TITLE 'TEXT PRINT SUBROUTINE.'
PRINT    START 0
         SPACE 2
* CALLING SEQUENCE : CALL PRINT,(A,B,C,D),VL
* ------------------            (A,B,C)
*                               (A,B)
*                               (A)
*
*        WHERE : A - IS THE ADDRESS OF THE TEXT TO BE PRINTED (THE
*                    FIRST CHARACTER MUST BE ASA CONTROL CHARACTER).
*
*                B - IS THE LENGTH (NUMBER OF CHARACTERS) OF THE TEXT
*                    (FORMAT FULLWORD - F BOUNDARY). WHEN THIS ARGUMENT
*                    IS OMITTED, OR THE ADDRESS OF THIS ARGUMENT IS
*                    SET TO ZERO IN THE PARAMETERS LIST, THE LENGTH OF
*                    THE TEXT IS SUPPOSED TO BE IN FRONT OF THE TEXT,
*                    AT THE ADDRESS OF A-1 (FORMAT XL1).
*
*                C - IS THE ADDRESS OF THE DD-NAME (FORMAT CL8) IF
*                    SUPPLIED (OPTIONAL). WHEN THIS ARGUMENT IS
*                    OMITTED, OR THE ADDRESS OF THIS ARGUMENT IS SET
*                    TO ZERO IN THE PARAMETERS LIST, THE DEFAULT
*                    DD-NAME USED IS 'SYSPRINT'.
*
*                D - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                     0 - PRINT CORRECTLY EXECUTED.
*                     1 - DD-CARD MISSING.
*                     2 - OPEN ERROR, NOT ASA OR LRECL TOO LARGE.
*                     3 - CALLING SEQUENCE OR ARGUMENT ALIGNMENT ERROR.
*                     4 - LENGTH IS NEGATIVE , ZERO OR GREATER THAN THE
*                         LOGICAL RECORD LENGTH (MAXIMUM ALLOWABLE IS
*                         145). THE LINE IS PRINTED, BUT TRUNCATED.
*                     5 - PRINT ERROR.
*
*                     --- IF THE ROUTINE IS RE-CALLED AFTER IND = 1
*                         OR 2, THE PROGRAM IS SENDED IN ABEND U0001.
*                         IF THE INDICATOR RESPONSE IS DETECTED 10
*                         TIMES DIFFERENT FROM 0, THE PROGRAM IS
*                         SENDED IN ABEND U0002.
         EJECT
* CALLING SEQUENCE : CALL PCLOSE,(A,B),VL
* ------------------             (A)
*
*        WHERE : A - IS THE ADDRESS OF THE DD-NAME (FORMAT CL8) TO
*                    CLOSE. IF THIS ARGUMENT CONTAINS THE 'CLOSEALL'
*                    KEYWORD, OR THE ADDRESS OF THIS ARGUMENT IS SET
*                    TO ZERO IN THE PARAMETERS LIST, OR THE PARAMETERS
*                    LIST POINTER (REGISTER 1) IS ZERO, ALL THE
*                    OPENED FILES ARE CLOSED.
*
*                B - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                     0 - CLOSE CORRECTLY EXECUTED.
*                     1 - CALLING SEQUENCE, ARGUMENT ALIGNMENT OR
*                         DD-NAME ERROR.
*
* GENERAL NOTICE : ON ANY ERROR DETECTION, A MESSAGE IS INCLUDED
* ---------------- IN THE MESSAGE CLASS FOR THE JOB.
*                  MVS/XA : MUST BE AMODE AND RMODE 24.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
         SPACE 1
*        MISCELLANEOUS DEFINITIONS.
         SPACE 1
OFF      EQU   X'0F'               NOP OPERATION SWITCH SETTING.
ON       EQU   X'F0'               BRANCH OPERATION SWITCH SETTING.
NPRTLGTH EQU   133                 DEFAULT PRINTER RECORD LENGTH.
         EJECT
        PRINT  NOGEN
TIODSECT DSECT
        IEFTIOT1
        DCBD   DSORG=PS,DEVD=DA
        PRINT  GEN
         SPACE 1
*        FILE BLOCK WORK AREA DESCRIPTION.
         SPACE 1
FBWAD    DSECT
FCHAIN   DC    F'0'                CHAIN WORD.
FDDNM    DC    CL8' '              DD-NAME.
FERCNT   DC    H'10'               ERROR COUNT.
FFLAGS   DC    XL1'0'              FLAGS BYTE.
FAB      EQU   X'01'                    ABEND.
FOE      EQU   X'02'                    OPEN ERROR.
FPE      EQU   X'04'                    PRINT ERROR.
FTR      EQU   X'08'                    TRUNCATED.
FRB      EQU   X'10'                    VARIABLE RECORD FORMAT.
         DC    XL1'0'              UNUSED.
FMLRL    DC    F'0'                MAX. LOGICAL RECORD LENGTH.
         SPACE 1
PRINT    CSECT                     RESTORE MAIN CSECT.
         EJECT
        $XENT  BASE=R12
         SPACE 1
         XR    R9,R9               SET DEFAULTS.
         LA    R6,DDDEF
         LA    R10,MAXERR
         XR    R8,R8
         TM    0(R1),X'80'         CONTROL ARGUMENTS.
         BO    PR1
         L     R8,4(R1)
         LA    R8,0(R8)
         TM    7(R1),X'03'
         BNZ   ERR3
         TM    4(R1),X'80'
         BO    PR1
         L     R7,8(R1)
         LA    R7,0(R7)
         LTR   R7,R7
         BZ    *+L'*+2
         LR    R6,R7
         TM    8(R1),X'80'
         BO    PR1
         TM    12(R1),X'80'
         BZ    ERR3
         TM    15(R1),X'03'
         BNZ   ERR3
         L     R9,12(R1)
         LA    R9,0(R9)
PR1      L     R7,0(R1)
         LA    R7,0(R7)
         LTR   R8,R8
         BZ    PR2
         L     R8,0(R8)
         B     PR3
PR2      BCTR  R7,0
         IC    R8,0(R7)
         LA    R7,1(R7)
PR3      LTR   R8,R8
         BNP   ERR4
         SPACE 1
         LA    R3,CHAIN            LOOK IF ALREADY DEFINED.
         USING FBWAD,R4
PR4      L     R4,0(R3)
         LTR   R4,R4
         BZ    PR5
         CLC   FDDNM(L'FDDNM),0(R6)
         BE    PR9
         LR    R3,R4
         B     PR4
PR5     GETMAIN R,LV=WSZ
         LR    R4,R1
         ST    R4,0(R3)            NEW FILE.
         MVC   FCHAIN(WSZ),BFILE   SET SKELETON.
         MVC   FDDNM(L'FDDNM),0(R6)
         LA    R2,DZ(R4)
         USING IHADCB,R2
         MVC   DCBDDNAM(L'DCBDDNAM),FDDNM
         OC    ATIOT(L'ATIOT),ATIOT     TEST FIRST TIME.
         BNZ   PR6
        EXTRACT    ATIOT,'S',FIELDS=(TIOT)
PR6      L     R3,ATIOT            SEARCH DD AND OPEN IT.
         USING TIODSECT,R3
PR7      CLC   TIOELNGH(4),ZEROS
         BE    ERR1
         CLC   TIOEDDNM(L'DCBDDNAM),DCBDDNAM
         BE    PR8
         XR    R1,R1
         IC    R1,TIOELNGH
         ALR   R3,R1
         B     PR7
         DROP  R3
PR8     OPEN   ((R2),(OUTPUT))
         TM    FFLAGS,FOE          TEST OPEN ERROR SWITCH.
         BO    ERR2
         TM    DCBOFLGS,DCBOFOPN   TEST OPEN SUCCESSFULL.
         BO    PR10
         B     ERR2
PR9      TM    FFLAGS,FAB          TEST ABEND SWITCH.
         BO    ABND1
         LA    R2,DZ(R4)
         TM    DCBOFLGS,DCBOFOPN   TEST ALREADY OPENED.
         BZ    PR8
         DROP  R2
         SPACE 1
PR10     NI    FFLAGS,255-FPE-FTR  RESET SWITCHES.
         L     R0,FMLRL
         CLR   R8,R0
         BNH   PR11
         OI    FFLAGS,FTR          SET TRUNCATE SWITCH.
         LR    R8,R0
PR11     MVI   MESSAGE,C' '
         MVC   MESSAGE+1(L'MESSAGE-1),MESSAGE
         TM    FFLAGS,FRB
         BZ    *+L'*+16
         LA    R3,MESSLEN
         LA    R1,4(R8)
         STH   R1,0(R3)
         B     *+L'*+4
         LA    R3,MESSAGE
         BCTR  R8,0
         EX    R8,PRMV
        PUT    (R2),(R3)
         TM    FFLAGS,FPE          TEST PRINT ERROR SWITCH.
         BO    ERR5
         TM    FFLAGS,FTR          TEST TRUNCATE SWITCH.
         BO    ERR4
         XR    R10,R10
         B     PR15
         SPACE 1
PRMV     MVC   MESSAGE(*-*),0(R7)
         SPACE 1
PRERR    OI    FFLAGS,FPE          SET PRINT ERROR SWITCH.
         BR    R14
         SPACE 1
ERR1     BCTR  R10,0
ERR2     BCTR  R10,0
         OI    FFLAGS,FAB          SET ABEND SWITCH TO PREVENT RE-CALL.
ERR3     BCTR  R10,0
ERR4     BCTR  R10,0
ERR5     BCTR  R10,0
         SPACE 1
MAXERR   EQU   (((*-ERR1)-4)/2)+1
         SPACE 1
         LR    R2,R10
         BCTR  R2,0
         SLL   R2,2
         L     R3,TABD(R2)
         NI    SWEX+1,OFF          SET SWITCH OFF.
         CLI   12(R3),C' '
         BNE   *+L'*+10
         OI    SWEX+1,ON           SET SWITCH ON.
         MVC   12(8,R3),0(R6)
         LR    R1,R3
        WTO    MF=(E,(1))
SWEX     B     PR12
         LA    R3,CNTR             CONTROL MAX. ERROR.
         B     PR13
PR12     MVC   12(8,R3),BLKS
         LA    R3,FERCNT
PR13     LH    R2,0(R3)
         BCT   R2,PR14
         LA    R1,2                SET ABEND CODE.
         B     ABND
PR14     STH   R2,0(R3)
         DROP  R4
         SPACE 1
PR15     LTR   R9,R9
         BZ    PR16
         ST    R10,0(R9)           STORE IND.
PR16    $XRET  CC=(R10)
         SPACE 1
ABND1    LA    R1,1                SET ABEND CODE.
         SPACE 1
ABND     LR    R15,R10             SET C.C. IN REG. 15.
        ABEND  (1),DUMP
         EJECT
         USING IHADCB,R1
         USING FBWAD,R4
DCBEXIT  CLI   DCBRECFM,0          IS FILE DEFINED,
         BNE   EX0                 BRANCH IF YES.
         LA    R3,NPRTLGTH         SET DEFAULT VALUES.
         OI    DCBRECFM,DCBRECF+DCBRECCA
         STH   R3,DCBBLKSI
         STH   R3,DCBLRECL
         ST    R3,FMLRL
         BR    R14
EX0      TM    DCBRECFM,DCBRECCA   MUST BE ASA CONTROL CHARACTER.
         BZ    EX2
         TM    DCBRECFM,DCBRECF
         BO    *+L'*+12
         TM    DCBRECFM,DCBRECV
         BZ    *+L'*+4
         OI    FFLAGS,FRB          SET VARIABLE RECORDS FORMAT.
         LA    R3,L'MESSAGE        CONTROL THE LOGICAL RECORD LENGTH.
         LH    R2,DCBLRECL
         LTR   R2,R2
         BP    EX1
         LH    R2,DCBBLKSI
         LTR   R2,R2
         BNP   EX2
EX1      CLR   R2,R3
         BH    EX2
         ST    R2,FMLRL
         BR    R14
         DROP  R1
EX2      OI    FFLAGS,FOE          SET OPEN ERROR SWITCH.
         BR    R14
         SPACE 1
         DROP  R4
         DROP  R12
         EJECT
         ENTRY PCLOSE
         SPACE 1
PCLOSE  $XENT  BASE=R12,DT=NO
         SPACE 1
         XR    R9,R9               CONTROL ARGUMENTS.
         LTR   R8,R1
         BZ    PC2
         L     R8,0(R1)
         LA    R8,0(R8)
         TM    0(R1),X'80'
         BO    PC1
         TM    4(R1),X'80'
         BZ    ERCL
         TM    7(R1),X'03'
         BNZ   ERCL
         L     R9,4(R1)
         LA    R9,0(R9)
PC1      LTR   R8,R8
         BZ    PC2
         CLC   0(8,R8),KEYALL
         BNE   PC2
         XR    R8,R8
PC2      LA    R3,CHAIN
         USING FBWAD,R4
PC3      L     R4,0(R3)
         LTR   R4,R4
         BZ    PC6
         LTR   R8,R8
         BZ    PC4
         CLC   0(L'FDDNM,R8),FDDNM
         BE    PC4
         LR    R3,R4
         B     PC3
PC4      LA    R2,DZ(R4)
         USING IHADCB,R2
         TM    DCBOFLGS,DCBOFOPN   TEST OPENED.
         BZ    PC5
        CLOSE  ((R2))
         DROP  R2
PC5      MVC   0(L'FCHAIN,R3),FCHAIN    DEQUEUE FILE.
         LR    R1,R4
         DROP  R4
        FREEMAIN R,A=(1),LV=WSZ
         LTR   R8,R8
         BNZ   PC7
         B     PC3
PC6      LTR   R8,R8
         BNZ   ERCL
PC7      XR    R10,R10
         LTR   R9,R9
         BZ    PC8
         ST    R10,0(R9)           STORE IND.
PC8     $XRET  CC=(R10)
         SPACE 1
ERCL     WTO   'ERR=1 - PCLOSE CALLING SEQUENCE, ARGUMENT ALIGNMENT OR 1
               DD-NAME ERROR.',ROUTCDE=11,DESC=7
         LA    R10,1
         B     PC7+L'PC7
         SPACE 1
         DROP  R12
         EJECT
*        CONSTANTS AND WORK AREAS.
         SPACE 1
CHAIN    DC    A(*-*)
ATIOT    DC    A(*-*)
TABD     DC    A(DIAG1,DIAG2,DIAG3,DIAG4,DIAG5)
ZEROS    DC    F'0'
DIAG1   WTO    'ERR=1 -          DD-CARD MISSING.',                    1
               ROUTCDE=11,DESC=7,MF=L
DIAG2   WTO    'ERR=2 -          OPEN ERROR OR DD-CARD CONFLICT.',     1
               ROUTCDE=11,DESC=7,MF=L
DIAG3   WTO    'ERR=3 - PRINT CALLING SEQUENCE OR ARGUMENT ALIGNMENT ER1
               ROR.',ROUTCDE=11,DESC=7,MF=L
DIAG4   WTO    'ERR=4 - LENGTH IS NEGATIVE, ZERO OR GREATER THAN THE LO1
               GICAL RECORD.',ROUTCDE=11,DESC=7,MF=L
DIAG5   WTO    'ERR=5 -          I/O ERROR.',                          1
               ROUTCDE=11,DESC=7,MF=L
CNTR     DC    H'10'
DDDEF    DC    CL8'SYSPRINT'
KEYALL   DC    CL8'CLOSEALL'
BLKS     DC    CL8' '
MESSLEN  DC    F'0'
MESSAGE  DC    CL145' '
         EJECT
        CNOP   0,8
         SPACE 1
BFILE    DC    F'0'                CHAIN WORD.
         DC    CL8' '              DD-NAME.
         DC    H'10'               ERROR COUNT.
         DC    XL1'0'              FLAGS BYTE.
         DC    XL1'0'              UNUSED.
         DC    F'0'                MAX. LOGICAL RECORD LENGTH.
         SPACE 1
DCBPRT  DCB    DSORG=PS,DEVD=DA,MACRF=PM,BFALN=D,                      1
               EXLST=PEXLIST,SYNAD=PRERR,DDNAME=XXXXXXXX
         SPACE 1
        CNOP   0,8
         SPACE 1
DZ       EQU   DCBPRT-BFILE
WSZ      EQU   (((*-BFILE)+7)/8)*8
         SPACE 1
PEXLIST  DC    0F'0',XL1'85',AL3(DCBEXIT)
         SPACE 2
         END
