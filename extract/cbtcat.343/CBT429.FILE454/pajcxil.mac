PROC     0
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       PURPOSE : IMS LOG DATA-SET(S).                          (PM)*/
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN EXIT
         ISPEXEC CONTROL ERRORS RETURN
IMSL:    ISPEXEC DISPLAY PANEL(PAJXIL)
         SET &ILCC = &LASTCC
         IF &ILCC NE 0 THEN DO
           IF &ILCC LE 8 THEN GOTO IMSX
           ISPEXEC SETMSG MSG(PAJI001A)
           GOTO IMSL
           END
         IF &MAS EQ YES THEN DO
           CONTROL MSG
           SET &ILST =
           END
         ELSE DO
           CONTROL NOMSG
           SET &ILST = &STR(N)
           END
         IF &JI NE THEN SET &ILST = &STR(J(&JI) &ILST)
         ELSE SET &ILST = &STR(&ILST)
         IF &DD NE THEN SET &ILST = &STR(D(&DD/&DM/&DY) &ILST)
         ELSE IF &DM NE THEN DO
           IF &DF NE THEN DO
             IF &DT NE THEN -
               SET &ILST = &STR(F(&DF/&DM/&DY) T(&DT/&DM/&DY) &ILST)
             ELSE SET &ILST = &STR(F(&DF/&DM/&DY) &ILST)
             END
           ELSE SET &ILST = &STR(F(01/&DM/&DY) &ILST)
           END
         SET &EUGENDS =
         IMSLDS &ILST
         SET &ILRC = &LASTCC
         CONTROL NOMSG
         ISPEXEC VPUT (ILST ILRC EUGENDS)
IMSD:    ISPEXEC DISPLAY PANEL(PAJXILX)
         SET &ILCC = &LASTCC
         IF &ILCC NE 0 THEN DO
           IF &ILCC EQ 4 THEN GOTO IMSL
           IF &ILCC EQ 8 THEN GOTO IMSX
           ISPEXEC SETMSG MSG(PAJI001A)
           GOTO IMSD
           END
         IF &EUGENDS EQ THEN GOTO IMSL
         IF &DX NE D THEN DO
           IF &DX EQ B THEN ISPEXEC BROWSE DATASET('&EUGENDS')
           ELSE ISPEXEC EDIT DATASET('&EUGENDS')
           SET &ILCC = &LASTCC
           IF &ILCC NE 0 THEN DO
             IF &ILCC GT 8 THEN DO
               IF &DX EQ B THEN ISPEXEC SETMSG MSG(PAJI002A)
               ELSE ISPEXEC SETMSG MSG(PAJI002B)
               END
             END
           GOTO IMSD
           END
         IF &MAS EQ YES THEN CONTROL MSG
         DELETE '&EUGENDS'
         SET &ILCC = &LASTCC
         IF &MAS EQ YES THEN CONTROL NOMSG
         IF &ILCC NE 0 THEN ISPEXEC SETMSG MSG(PAJI002C)
         SET &EUGENDS =
         GOTO IMSL
IMSX:    ISPEXEC CONTROL ERRORS CANCEL
         CONTROL MSG
         EXIT CODE(0)
