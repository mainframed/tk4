DAUN     TITLE 'DIRECT-ACCESS UNIT TYPE SUBROUTINE.'
DAUNTY   START 0
         SPACE 1
* CALLING SEQUENCE : CALL DAUNTY,(A,B,C),VL
* ------------------             (A,B)
*
*        WHERE : A - IS THE ADDRESS OF A ONE BYTE FIELD CONTAINING
*                    A DIRECT-ACCESS TYPE (HEXADECIMAL FORMAT, LIKE
*                    IN UCBTBYT4).
*
*                B - IS THE ADDRESS OF AN EIGTH BYTES FIELD RECEIVING
*                    THE CORRESPONDING UNIT TYPE RIGHT PADDED WITH
*                    BLANKS (THE TRUE LENGTH IS PASSED BACK IN THE
*                    REGISTER 0).
*
*                C - RECEIVES THE RETURN CODE (FORMAT FULLWORD -
*                    F BOUNDARY) IF SUPPLIED (OPTIONAL).
*
*        RETURN CODES (REGISTER 15) :
*
*                        0 = NORMAL.
*                        4 = INVALID DIRECT-ACCESS TYPE.
*                        8 = CALLING SEQUENCE OR PARAMETER ALIGNMENT
*                            ERROR.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12
         SR    R0,R0
         LR    R7,R0
         LR    R8,R0
         LR    R9,R0
         LR    R10,R0
         TM    0(R1),X'80'
         BO    ERSA
         TM    4(R1),X'80'
         BO    GADD
         TM    8(R1),X'80'
         BZ    ERSA
         TM    11(R1),X'03'
         BNZ   ERSA
         L     R9,8(R1)
         LA    R9,0(R9)
GADD     L     R8,4(R1)
         LA    R8,0(R8)
         L     R7,0(R1)
         LA    R7,0(R7)
         MVC   0(8,R8),=CL8' '
         L     R2,=A(DAUTTB)       GET UNIT TYPE TABLE ADDRESS.
LOOP     CLI   0(R2),X'FF'         END OF TABLE?
         BE    NOTFND              YES.
         CLC   0(1,R7),0(R2)       IS THIS TYPE?
         BE    FOUND               YES.
         LA    R2,10(R2)           POINT TO NEXT.
         B     LOOP
FOUND    IC    R0,1(R2)            GET TRUE LENGTH.
         MVC   0(8,R8),2(R2)       SET UNIT TYPE.
         B     LEAVE
NOTFND   LA    R10,4
         B     LEAVE
ERSA     LA    R10,8
LEAVE    L     R7,4(R13)
         ST    R0,20(R7)           SET REGISTER 0.
         LTR   R9,R9
         BZ    EXIT
         ST    R10,0(R9)           STORE INDICATOR.
EXIT    $XRET  CC=(R10)
         SPACE 2
        LTORG
         SPACE 2
         ENTRY DAUTTB
         SPACE 1
DAUTTB   DS    0F                  UNIT TYPES TABLE.
         DC    XL1'01',AL1(4),CL8'2311'
         DC    XL1'02',AL1(4),CL8'2301'
         DC    XL1'03',AL1(4),CL8'2303'
         DC    XL1'04',AL1(4),CL8'2302'
         DC    XL1'05',AL1(4),CL8'2321'
         DC    XL1'06',AL1(6),CL8'2305-1'
         DC    XL1'07',AL1(6),CL8'2305-2'
         DC    XL1'08',AL1(4),CL8'2314'
         DC    XL1'09',AL1(4),CL8'3330'
         DC    XL1'0A',AL1(4),CL8'3340'
         DC    XL1'0B',AL1(4),CL8'3350'
         DC    XL1'0C',AL1(4),CL8'3375'
         DC    XL1'0D',AL1(6),CL8'3330-1'
         DC    XL1'0E',AL1(4),CL8'3380'
         DC    X'FF',AL1(0)        END OF TABLE.
         SPACE 2
         END
