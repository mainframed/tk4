IRDA     TITLE 'INTERNAL READER DYNAMIC ALLOCATION TSO-COMMAND.'
INTRDR   START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * *                                                         * * * *
* * * *     INTERNAL READER DYNAMIC ALLOCATION TSO-COMMAND.     * * * *
* * * *                                                         * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
* PURPOSE :    DYNAMICALLY ALLOCATE BY DDNAME AN INTERNAL READER.
* ---------
*
* COMMAND :    SYNTAX : INTRDR DD-NAME
* ---------    DEFAULT : DD-NAME 'SYSIR'.
*
* AUTHOR :     RAVARANI N. - MOINIL P.A.
* --------     COMPUTING CENTRE (TP 361)
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
INTRDR  AMODE  24
INTRDR  RMODE  24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R11,R13             SET WORK-AREAS ADDRESSABILITY.
         USING WRKDSECT,R11
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
         LA    R1,RBBK
         ST    R1,RBBKPTR
         OI    RBBKPTR,X'80'
         MVC   RBBK(L'RBBK),RBBKC
         XC    RBRSC(RBBKEND-RBRSC),RBRSC
         LA    R1,TXP
         ST    R1,RBTXP
         LA    R1,TXTUN1
         ST    R1,TXP
         LA    R1,TXTUN2
         ST    R1,TXP+4
         LA    R1,TXTUN3
         ST    R1,TXP+8
         LA    R1,TXTUN4
         ST    R1,TXP+12
         LA    R1,TXTUN5
         ST    R1,TXP+16
         LA    R1,TXTUN6
         ST    R1,TXP+20
         OI    TXP+20,X'80'
         MVC   TXTUN1(TXTUNL),TXTUNC
        $TEW$PP PARM=PARMLST,REG=R3
         TM    DDNME+6,X'80'       IS DD-NAME SUPPLIED?
         BZ    NONME               NO, USE DEFAULT.
         L     R1,DDNME            GET POINTER TO DD-NAME.
         LH    R2,DDNME+4          GET LENGTH.
         BCTR  R2,0                - 1 FOR MOVE.
         MVC   TXTDDN(8),=CL8' '   CLEAR TARGET FIELD.
         EX    R2,MOVEDDN          MOVE REQUESTED DD-NAME.
         MVC   TXTDDNL(2),DDNME+4  SET TRUE DD-NAME LENGTH.
         DROP  R3
NONME   IKJRLSA #TSANSW
         SPACE 1
         LA    R1,RBBKPTR          DYNALLOC LIST.
        DYNALLOC
         LTR   R15,R15             CHECK FOR ERRORS.
         BZ    LEAVE               OK, ALLOCATED, C.C. = 0.
         MVC   MSLNE(64),=CL64' -> INT-READER DYNAM ALL. FAILED (R15=  X
               ) - ERC=    , INFO=    .'
         STC   R15,MSLNE+39
         SRL   R15,4
         STC   R15,MSLNE+38
         NC    MSLNE+38(2),=XL4'0F0F0F0F'
         TR    MSLNE+38(2),=CL16'0123456789ABCDEF'
         UNPK  MSLNE+48(5),RBRSC(3)
         MVI   MSLNE+52,C','
         NC    MSLNE+48(4),=XL4'0F0F0F0F'
         TR    MSLNE+48(4),=CL16'0123456789ABCDEF'
         UNPK  MSLNE+59(5),RBRSC+2(3)
         MVI   MSLNE+63,C'.'
         NC    MSLNE+59(4),=XL4'0F0F0F0F'
         TR    MSLNE+59(4),=CL16'0123456789ABCDEF'
         LA    R0,64
         LA    R1,MSLNE
         BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     LEAVE               +0 - NORMAL COMPLETION
*                                  +4 - ERROR
         SPACE 1
*------- END OF PROCESSING.
         SPACE 1
SETRC    LA    R15,16              C.C. = 16.
LEAVE   $XRET  CC=(R15),LV=WRKL,TYPE=RENT
         SPACE 1
MOVEDDN  MVC   TXTDDN(*-*),0(R1)   << EXECUTED >>
         EJECT
*------- CONSTANTS AND CONTROL BLOCKS SKELETONS.
         SPACE 1
*        DYNAMIC ALLOCATION CONSTANTS.
         SPACE 1
RBBKC    DC    AL1(RBBKEND-RBBK,S99VRBAL,S99NOCNV+S99NOMNT,0)
         SPACE 1
TXTUNC   DC    AL2(DALDDNAM),AL2(1)
         DC    AL2(5)              DD-NAME LENGTH.
         DC    CL8'SYSIR'          DD-NAME.
         DC    AL2(DALDSNAM),AL2(1),AL2(5),CL5'&&SBMT'
         DC    AL2(DALSYSOU),AL2(1),AL2(1),CL1'A'
         DC    AL2(DALSPGNM),AL2(1),AL2(6),CL6'INTRDR'
         DC    AL2(DALLRECL),AL2(1),AL2(2),AL2(80)
         DC    AL2(DALRECFM),AL2(1),AL2(1),AL1(DCBRECF)
TXTUNL   EQU   *-TXTUNC
         SPACE 1
        LTORG
         SPACE 1
PARMLST IKJPARM
DDNME   IKJIDENT 'DD-NAME',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM
        IKJENDP
         EJECT
*------- WORK-AREAS DESCRIPTION.
         SPACE 1
WRKDSECT DSECT
         SPACE 1
SVA      DS    18F                 SAVE AREA.
         SPACE 1
        $TEW$WA
         SPACE 1
RBBKPTR  DC    0F'0',XL1'80',AL3(*-*)
         SPACE 1
RBBK     DC    0F'0',AL1(RBBKEND-RBBK,S99VRBAL,S99NOCNV+S99NOMNT,0)
RBRSC    DC    F'0'                ERROR/INFO REASON CODES.
RBTXP    DC    A(*-*)
         DC    F'0',XL4'0'
RBBKEND  EQU   *
         SPACE 1
TXP      DC    A(*-*,*-*,*-*,*-*,*-*),XL1'80',AL3(*-*)
         SPACE 1
TXTUN1   DC    AL2(DALDDNAM),AL2(1)
TXTDDNL  DC    AL2(5)              DD-NAME LENGTH.
TXTDDN   DC    CL8'SYSIR'          DD-NAME.
TXTUN2   DC    AL2(DALDSNAM),AL2(1),AL2(5),CL5'&&SBMT'
TXTUN3   DC    AL2(DALSYSOU),AL2(1),AL2(1),CL1'A'
TXTUN4   DC    AL2(DALSPGNM),AL2(1),AL2(6),CL6'INTRDR'
TXTUN5   DC    AL2(DALLRECL),AL2(1),AL2(2),AL2(80)
TXTUN6   DC    AL2(DALRECFM),AL2(1),AL2(1),AL1(DCBRECF)
         SPACE 1
MSLNE    DS    CL64
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK-AREAS LENGTH.
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        IEFZB4D0
        IEFZB4D2
        DCBD   DEVD=DA,DSORG=PS
        PRINT  GEN
         SPACE 1
         END
