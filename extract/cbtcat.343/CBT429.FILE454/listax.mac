LISTAX   TITLE 'LISTAX -- LIST ALL ALLOCATED DATA-SETS.'
LISTAX   START 0
         SPACE 1
* FUNCTION :   THE LISTAX COMMAND LISTS ALL ALLOCATED DATA-SETS
*              IN THE FOLLOWING FORMAT
*              DDNAME   DISP         ORG DSNAME
*              XXXXXXXX XXX,XXXX,XXXX XX XXXXXXXXXXXXXXXXXXXX...
*
* SYNTAX :     LISTAX
*
* ORIGIN :     EXTRACTED FROM TAPE CBT 89 FEB, FILE 300.
*              LISTAX WAS WRITTEN BY BOB JUCH
*                                    POSTAL DATA CENTER
*                                    850 CHERRY AVE.
*                                    SAN BRUNO CA. 94097
*                                    415 876-9176
*              ADAPTED BY MOINIL P.A.
*
* ATTRIBUTES : RENT
         SPACE 1
LISTAX  AMODE  24
LISTAX  RMODE  24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=@SIZE,TYPE=RENT
         LR    R11,R13
         USING @STORAGE,R11        STORAGE BASE REGISTER
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
         LA    R0,L'HEADER         POINT TO HEAD LINE
         LA    R1,HEADER
         BAS   R14,PUTLNE
         LTR   R15,R15             HOW COMPLETE ?
         BNZ   SETRC               ERROR
         LA    R0,UNDRL            POINT TO UNDER LINE
         LA    R1,UNDERL
         BAS   R14,PUTLNE
         LTR   R15,R15             HOW COMPLETE ?
         BNZ   SETRC               ERROR
         LA    R1,DYNRBP
         USING S99RBP,R1
         LA    R2,DYNRB
         ST    R2,S99RBPTR
         OI    S99RBPTR,S99RBPND
         DROP  R1
         USING S99RB,R2
         MVI   S99RBLN,S99RBEND-S99RB
         MVI   S99VERB,S99VRBIN
         XC    S99FLAG1,S99FLAG1
         XC    S99ERROR,S99ERROR
         XC    S99INFO,S99INFO
         LA    R1,DYNTUPL
         ST    R1,S99TXTPP
         XC    S99S99X,S99S99X     (OLD S99RSV01 FIELD)
         XC    S99FLAG2,S99FLAG2
         DROP  R2
         USING S99TUNIT,R1
         LA    R1,S99TUN04
         LA    R2,DINRTDDN
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'8'
         MVC   S99TUPAR(8),=CL80' '
         ST    R1,S99TUP04
         LA    R1,S99TUN05
         LA    R2,DINRTDSN
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'44'
         MVC   S99TUPAR(44),=CL80' '
         ST    R1,S99TUP05
         LA    R1,S99TUN06
         LA    R2,DINRTMEM
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'8'
         MVC   S99TUPAR(8),=CL80' '
         ST    R1,S99TUP06
         LA    R1,S99TUN07
         LA    R2,DINRTSTA
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'1'
         MVI   S99TUPAR,0
         ST    R1,S99TUP07
         LA    R1,S99TUN08
         LA    R2,DINRTNDP
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'1'
         MVI   S99TUPAR,0
         ST    R1,S99TUP08
         LA    R1,S99TUN09
         LA    R2,DINRTCDP
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'1'
         MVI   S99TUPAR,0
         ST    R1,S99TUP09
         LA    R1,S99TUN10
         LA    R2,DINRTORG
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'2'
         MVC   S99TUPAR(2),=H'0'
         ST    R1,S99TUP10
         LA    R1,S99TUN13
         LA    R2,DINRTLST
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'1'
         MVI   S99TUPAR,0
         ST    R1,S99TUP13
         LA    R1,S99TUN14
         LA    R2,DINRTTYP
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'1'
         MVI   S99TUPAR,0
         ST    R1,S99TUP14
         LA    R1,S99TUN15
         LA    R2,DINRELNO
         STH   R2,S99TUKEY
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'2'
         MVC   S99TUPAR(2),=H'0'
         ST    R1,S99TUP15
         OI    S99TUP15,S99TUPLN
         DROP  R1
         XR    R3,R3               CLEAR FOR COUNTER
         B     SETRLN
TOP      MVC   DDNAME(2),=H'8'
         MVC   DDNAME+2(8),=CL80' '
         MVC   DSNAME(2),=H'44'
         MVC   DSNAME+2(44),=CL80' '
         MVC   MEMBER(2),=H'8'
         MVC   MEMBER+2(8),=CL80' '
         MVI   STATUS,0
         MVI   NDISP,0
         MVI   CDISP,0
         MVC   DSORG(2),=H'0'
         MVI   LASTENT,0
         MVI   DSTYPE,0
SETRLN   AH    R3,=H'1'            ADD FOR REQUEST NUMBER
         STH   R3,RELNO            PUT IT IN TEXT UNIT
         LA    R1,DYNRBP           POINT TO REQUEST BLOCK
        DYNALLOC ,                 GO DO IT
         MVC   OUTLINE(L'OUTLINE),=CL80' '
         MVC   OUTDDN,DDNAME+2     MOVE DDNAME TO OUTPUT LINE
         CLI   DSTYPE,X'00'        NORMAL DATA-SET ?
         BE    MOVEDSN
         CLI   DSTYPE,X'80'        ALLOC TO DUMMY ?
         BE    NULLDSN
         CLI   DSTYPE,X'40'        ALLOC TO TERM ?
         BE    TERMDSN
         CLI   DSTYPE,X'20'        ALLOC TO SYSIN ?
         BE    SYSIDSN
         CLI   DSTYPE,X'10'        ALLOC TO SYSOUT ?
         BE    SYSODSN
         MVC   OUTDSN(9),=CL9'*UNKNOWN*'
         LA    R0,OUTDSN-OUTLINE+9 SET REDUCED LINE LENGTH
         B     CHKSTAT
NULLDSN  MVC   OUTDSN(8),=CL8'NULLFILE'
         LA    R0,OUTDSN-OUTLINE+8 SET REDUCED LINE LENGTH
         B     CHKSTAT
TERMDSN  MVI   OUTDSN,C'*'
         LA    R0,OUTDSN-OUTLINE+1 SET SHORT LINE LENGTH
         B     CHKSTAT
SYSIDSN  MVC   OUTDSN(5),=CL5'SYSIN'
         LA    R0,OUTDSN-OUTLINE+5 SET REDUCED LINE LENGTH
         B     CHKSTAT
SYSODSN  MVC   OUTDSN(6),=CL6'SYSOUT'
         LA    R0,OUTDSN-OUTLINE+6 SET REDUCED LINE LENGTH
         B     CHKSTAT
MOVEDSN  MVC   OUTDSN,DSNAME+2     MOVE DSNAME
         MVC   OUTMEM,MEMBER+2     MOVE MEMBER NAME
         LA    R0,L'OUTLINE        SET FULL LINE LENGTH
CHKSTAT  MVC   OUTSTA(L'OUTSTA),=CL80' '
         LA    R2,OUTSTA
         CLI   STATUS,X'01'        SEE IF OLD
         BNE   *+L'*+6
         MVC   0(3,R2),=CL3'OLD'
         CLI   STATUS,X'02'        SEE IF MOD
         BNE   *+L'*+6
         MVC   0(3,R2),=CL3'MOD'
         CLI   STATUS,X'04'        SEE IF NEW
         BNE   *+L'*+6
         MVC   0(3,R2),=CL3'NEW'
         CLI   STATUS,X'08'        SEE IF SHR
         BNE   *+L'*+6
         MVC   0(3,R2),=CL3'SHR'
         CLI   0(R2),C' '
         BE    *+L'*+4
         LA    R2,3(R2)
         MVI   0(R2),C','
         LA    R2,1(R2)
         CLI   NDISP,X'01'         SEE IF UNCATLG
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'UNCT'
         CLI   NDISP,X'02'         SEE IF CATLG
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'CTLG'
         CLI   NDISP,X'04'         SEE IF DELETE
         BNE   *+L'*+6
         MVC   0(3,R2),=CL3'DEL'
         CLI   NDISP,X'08'         SEE IF KEEP
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'KEEP'
         CLI   NDISP,X'10'         SEE IF PASS
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'PASS'
         CLI   0(R2),C' '
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'????'  NO DISP ?
         CLI   3(R2),C' '
         BNE   *+L'*+2
         BCTR  R2,0
         MVI   4(R2),C','
         LA    R2,5(R2)
         CLI   CDISP,X'01'         SEE IF UNCATLG
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'UNCT'
         CLI   CDISP,X'02'         SEE IF CATLG
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'CTLG'
         CLI   CDISP,X'04'         SEE IF DELETE
         BNE   *+L'*+6
         MVC   0(3,R2),=CL3'DEL'
         CLI   CDISP,X'08'         SEE IF KEEP
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'KEEP'
         CLI   CDISP,X'10'         SEE IF PASS
         BNE   *+L'*+6
         MVC   0(4,R2),=CL4'PASS'
         CLI   0(R2),C' '
         BNE   *+L'*+6
         BCTR  R2,0                NO DISP
         MVI   0(R2),C' '
         LA    R2,LNEOUT
         TM    DSORG,DCBDSGPS      SEE IF PS
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'PS'
         BR    R2
         TM    DSORG,DCBDSGPO      SEE IF PO
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'PO'
         BR    R2
         TM    DSORG+1,DCBACBM     SEE IF VSAM
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'VS'
         BR    R2
         TM    DSORG,DCBDSGDA      SEE IF DA
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'DA'
         BR    R2
         TM    DSORG,DCBDSGIS      SEE IF ISAM
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'IS'
         BR    R2
         TM    DSORG,DCBDSGCX      SEE IF CX
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'CX'
         BR    R2
         TM    DSORG+1,DCBDSGGS    SEE IF GS
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'GS'
         BR    R2
         TM    DSORG+1,DCBDSGTX    SEE IF TX
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'TX'
         BR    R2
         TM    DSORG+1,DCBDSGTQ    SEE IF TQ
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'TQ'
         BR    R2
         TM    DSORG+1,DCBDSGTR    SEE IF TR
         BZ    *+L'*+8
         MVC   OUTORG,=CL2'TR'
         BR    R2
         MVC   OUTORG,=CL2'**'
LNEOUT   LA    R1,OUTLINE
         BAS   R14,PUTLNE
         LTR   R15,R15             HOW COMPLETE ?
         BNZ   SETRC               ERROR
         TM    LASTENT,X'80'       WAS THIS LAST ENTRY
         BZ    TOP
         XR    R2,R2               RC=0 -------------------------------
LEAVE   $XRET  CC=(R2),LV=@SIZE,TYPE=RENT
SETRC    LA    R2,16               RC=16 ------------------------------
         B     LEAVE
         EJECT
*------- CONSTANTS
HEADER   DC    C'DDNAME   DISP         ORG DSNAME'
UNDERL   DC    C'-------- ------------- -- '
         DC    44C'-'
UNDRL    EQU   *-UNDERL
         SPACE 1
        LTORG
         EJECT
@STORAGE DSECT
         DS    18F                 S.A.
        $TEW$WA
         SPACE 1
DYNRBP   DS    F
DYNRB    DS    0F,((((S99RBEND-S99RB)+3)/4)*4)X
         SPACE 1
DYNTUPL  DS    0F
S99TUP04 DS    F
S99TUP05 DS    F
S99TUP06 DS    F
S99TUP07 DS    F
S99TUP08 DS    F
S99TUP09 DS    F
S99TUP10 DS    F
S99TUP13 DS    F
S99TUP14 DS    F
S99TUP15 DS    F
         SPACE 1
S99TUN04 DS    2H
DDNAME   DS    H,CL8
S99TUN05 DS    2H
DSNAME   DS    H,CL44
S99TUN06 DS    2H
MEMBER   DS    H,CL8
S99TUN07 DS    3H
STATUS   DS    XL1
S99TUN08 DS    3H
NDISP    DS    XL1
S99TUN09 DS    3H
CDISP    DS    XL1
S99TUN10 DS    3H
DSORG    DS    XL2
S99TUN13 DS    3H
LASTENT  DS    XL1
S99TUN14 DS    3H
DSTYPE   DS    XL1
S99TUN15 DS    3H
RELNO    DS    H
         SPACE 1
OUTLINE  DS    0CL79 ------------- COLS
OUTDDN   DS    CL8                 1-8
         DS    C                   9
OUTSTA   DS    CL13                10-22
         DS    C                   23
OUTORG   DS    CL2                 24-25
         DS    C                   26
OUTDSN   DS    CL44                27-70
         DS    C                   71
OUTMEM   DS    CL8                 72-79
         SPACE 1
@SIZE    EQU   (((*-@STORAGE)+7)/8)*8
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        IEFZB4D0
        IEFZB4D2
        DCBD   DSORG=PS,DEVD=DA
        PRINT  GEN
         SPACE 1
         END
