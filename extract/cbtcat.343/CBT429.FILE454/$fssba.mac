         MACRO
&LABEL  $FSSBA
.*
.* MACRO : INTERNALLY USED BY $FS MACRO.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLA  &FSMDLN,&GMFCI
         GBLB  &GFUSW,&GFESW,&GFISW
         GBLC  &GFSMF,&GFSMI
         LCLA  &BS,&I1,&I2,&R,&C,&LSZ,&NR
         LCLC  &B1,&B2,&RG
         AIF   (NOT &GFUSW).USEERR
         AIF   (N'&SYSLIST(1) NE 2).ARGERR
         AIF   (&FSMDLN EQ 1).M1
         AIF   (&FSMDLN EQ 2).M2
         AIF   (&FSMDLN EQ 3).M3
         AIF   (&FSMDLN EQ 4).M4
         AIF   (&FSMDLN EQ 5).M5
         AGO   .DFLT
.M1      AIF   (&SYSLIST(1,1) GT 12).M1RERR
         AIF   (&SYSLIST(1,2) GT 40).M1CERR
&LSZ     SETA  40
         AGO   .SROW
.M3      AIF   (&SYSLIST(1,1) GT 32).M3RERR
         AIF   (&SYSLIST(1,2) GT 80).M3CERR
         AGO   .SLNL
.M4      AIF   (&SYSLIST(1,1) GT 43).M4RERR
         AIF   (&SYSLIST(1,2) GT 80).M4CERR
         AGO   .SLNL
.M5      AIF   (&SYSLIST(1,1) GT 27).M5RERR
         AIF   (&SYSLIST(1,2) GT 132).M5CERR
&LSZ     SETA  132
         AGO   .SROW
.DFLT    ANOP
.M2      AIF   (&SYSLIST(1,1) GT 24).M2RERR
         AIF   (&SYSLIST(1,2) GT 80).M2CERR
.SLNL    ANOP
&LSZ     SETA  80
.SROW    ANOP
&R       SETA  1
         AIF   (&SYSLIST(1,1) LE 1).SCOL
&R       SETA  &SYSLIST(1,1)
.SCOL    ANOP
&C       SETA  1
         AIF   (&SYSLIST(1,2) LE 1).CMPTE
&C       SETA  &SYSLIST(1,2)
.CMPTE   ANOP
&BS      SETA  &LSZ*(&R-1)+&C-1
&I1      SETA  &BS/64
&I2      SETA  2*(&BS-(64*&I1)+1)-1
&I1      SETA  2*(&I1+1)-1
&B1      SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5X
               B5C5D5E5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6FX
               7F8F97A7B7C7D7E7F'(&I1,2)
&B2      SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5X
               B5C5D5E5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6FX
               7F8F97A7B7C7D7E7F'(&I2,2)
         AIF   (&GFISW).GIX
         AIF   (&GFESW).GEX
&LABEL   DC    XL3'11&B1&B2'
         MEXIT
.GEX     ANOP
&LABEL   MVC   0(3,&GFSMF),=XL3'11&B1&B2'
         LA    &GFSMF,3(,&GFSMF)
         MEXIT
.GIX     AIF   ('&GFSMI'(1,1) EQ '(').GIXR
&LABEL   MVC   &GFSMI+&GMFCI.(3),=XL3'11&B1&B2'
&GMFCI   SETA  &GMFCI+3
         MEXIT
.GIXR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(3,&RG),=XL3'11&B1&B2'
&GMFCI   SETA  &GMFCI+3
         MEXIT
.USEERR  MNOTE 8,'--- INVALID MACRO USAGE'
         MEXIT
.ARGERR  MNOTE 8,'--- SBA=(...) INVALID CODING'
         MEXIT
.M1RERR  MNOTE 8,'--- SBA - ROW NOT 1 THRU 12 FOR M=1'
         AGO   .DUMMY
.M1CERR  MNOTE 8,'--- SBA - COLUMN NOT 1 THRU 40 FOR M=1'
         AGO   .DUMMY
.M2RERR  MNOTE 8,'--- SBA - ROW NOT 1 THRU 24 FOR M=2'
         AGO   .DUMMY
.M2CERR  MNOTE 8,'--- SBA - COLUMN NOT 1 THRU 80 FOR M=2'
         AGO   .DUMMY
.M3RERR  MNOTE 8,'--- SBA - ROW NOT 1 THRU 32 FOR M=3'
         AGO   .DUMMY
.M3CERR  MNOTE 8,'--- SBA - COLUMN NOT 1 THRU 80 FOR M=3'
         AGO   .DUMMY
.M4RERR  MNOTE 8,'--- SBA - ROW NOT 1 THRU 43 FOR M=4'
         AGO   .DUMMY
.M4CERR  MNOTE 8,'--- SBA - COLUMN NOT 1 THRU 80 FOR M=4'
         AGO   .DUMMY
.M5RERR  MNOTE 8,'--- SBA - ROW NOT 1 THRU 27 FOR M=5'
         AGO   .DUMMY
.M5CERR  MNOTE 8,'--- SBA - COLUMN NOT 1 THRU 132 FOR M=5'
.DUMMY   AIF   (&GFISW).GXI
         AIF   (&GFESW).GXQ
&LABEL   DC    XL3'110000'
         MEXIT
.GXQ     ANOP
&LABEL   MVC   0(3,&GFSMF),=XL3'110000'
         LA    &GFSMF,3(,&GFSMF)
         MEXIT
.GXI     AIF   ('&GFSMI'(1,1) EQ '(').GXIR
&LABEL   MVC   &GFSMI+&GMFCI.(3),=XL3'110000'
&GMFCI   SETA  &GMFCI+3
         MEXIT
.GXIR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(3,&RG),=XL3'110000'
&GMFCI   SETA  &GMFCI+3
         MEND
