XTCE     TITLE 'TSO/E COMMANDS AND/OR CLISTS INVOKER.'
XTCE     START 0
         SPACE 1
* PURPOSE :    ALLOWS A PROGRAM RUNNING IN A TSO/E ENVIRONMENT TO
* ---------    INVOKE AND THUS EXECUTE TSO COMMANDS AND/OR CLISTS.
*
* CALLING SEQUENCE : CALL XTCE,(A,B,C,D),VL
* ------------------           (A,B,C)
*                              (A,B)
*                              (A)
*
*        WHERE : A - IS THE ADDRESS OF THE COMMAND OR CLIST TEXT TO
*                    BE INVOKED (NO ALIGNMENT NEEDED, BUT MAXIMUM 240
*                    CHARACTERS LONG ALLOWED).
*                B - IS THE ADDRESS OF THE COMMAND OR CLIST TEXT
*                    LENGTH (NUMBER OF CHARACTERS - FULLWORD). WHEN
*                    THIS ARGUMENT IS OMITTED, OR THE ADDRESS OF THIS
*                    ARGUMENT IS SET TO ZERO IN THE PARAMETERS LIST,
*                    THE LENGTH IS ASSUMED TO BE IN THE BYTE (FORMAT
*                    XL1) JUST PRECEEDING THE COMMAND OR CLIST TEXT,
*                    AT THE ADDRESS OF A-1.
*                C - IS THE ADDRESS OF A FULLWORD INTO WHICH THE
*                    COMMAND OR CLIST EXECUTION COMPLETION CODE IS
*                    STORED, IF SUPPLIED. THE FORMAT IS XL4'CCSSSUUU',
*                    WHERE CC IS THE COMPLETION CODE (TSO SERVICE
*                    RETURN CODE), SSS IS THE SYSTEM CODE (ABEND CODE)
*                    AND UUU IS THE USER CODE (REASON CODE OR FUNCTION
*                    RETURN CODE).
*                    IN ANY CASE THE VALUE IS ALWAYS PLACED IN THE
*                    REGISTER 0.
*                D - IS THE ADDRESS OF A FULLWORD TO STORE THE RETURN
*                    CODE, IF SUPPLIED. VALUE IS ALWAYS PLACED IN
*                    REGISTER 15 AND CAN BE :
*                     0 - ALL PROCESSINGS DONE.
*                     1 - COMMAND FAILED.
*                     2 - INVALID COMMAND TEXT LENGTH.
*                     3 - ARGUMENT ALIGNMENT ERROR.
*                     4 - MISSING COMMAND.
*                     5 - INVALID COMMAND.
*                     6 - COMMAND REJECTED (UNSUPPORTED).
*                     7 - UNABLE TO LOCATE CPPL.
*                     8 - NOT TSO ENVIRONMENT.
*
*        AT ENTRY, REGISTER 0 IS USED AS MESSAGES SWITCH :
*                - ZERO TO SUPPRESS ALL MESSAGES.
*                - POSITIVE TO OBTAIN ERROR MESSAGES ONLY.
*                - NEGATIVE TO OBTAIN ALL MESSAGES.
         EJECT
* OPERATIONAL NEEDS : XTC IS INCLUDED AT PROGRAM LINK-EDIT TIME.
* ------------------- XTCLE MUST BE LINK-EDITED IN A LIBRARY DEFINED
*              IN THE LINKLIST (LNKLST00 OF SYS1.PARMLIB) AND THIS
*              LIBRARY IS RACF PROTECTED AGAINST ANY USERS READ OR
*              WRITE (ONLY SYSTEM GROUP MAY ACCESS IT).
*              THIS ALLOWS MODIFICATIONS WITHOUT INVOLVING USERS.
*              REMARK : THE CODE IS NOT 'RENT' NOR 'REUS', IT IS
*                       BASED ON THE FACT THAT THE MODULE IS EACH
*                       TIME RE-LOADED.
*
*              WHEN USED BY A PROGRAM IN BATCH, YOU MUST START WITH
*              THE FOLLOWING JCL STATEMENTS :
*
*                       //IKJ     EXEC PGM=IKJEFT01
*                       //SYSPROC   DD ... TSO COMMANDS PROCEDURES
*                       //...       DD ... PROGRAM DD'S IF NEEDED
*                       //SYSTSPRT  DD SYSOUT=*
*                       //SYSTSIN   DD *
*                       CALL ...PROGRAM...
*                       END
*                       /*
*
* NOTES :    - WHEN RUNNING IN A BATCH ENVIRONMENT, THE COMMANDS OR
* =======      PROGRAMS USING TGET/TPUT ARE NOT SUPPORTED.
*            - SEE THE 'TSO EXTENSIONS GUIDE TO WRITING A TERMINAL
*              MONITOR PROGRAM OR A COMMAND PROCESSOR' MANUAL FOR
*              MORE DETAILS (IKJEFTSR SECTION).
*            - MVS/XA : MUST BE AMODE AND RMODE 24.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
         EJECT
        $XENT  BASE=R12
         LR    R5,R1               SAVE PARAMETERS LIST POINTER
         LTR   R0,R0               WANT MESSAGES?
         BP    INIT                YES
         BM    SALL                ALL
         OI    XSWX,NOMSG          SET MSG=NO
         B     INIT
SALL     OI    XSWX,ALLMSG         SET MSG=ALL
         SPACE 1
INIT    EXTRACT ATSO,'S',FIELDS=(TSO,PSB)
         EJECT
*        ARGUMENTS CONTROL.
         SPACE 1
         XR    R1,R1
         XR    R2,R2
         XR    R3,R3
         XR    R4,R4
         LA    R10,MAXERR
         ICM   R4,B'0111',1(R5)    GET ARGUMENTS
         TM    0(R5),X'80'
         BO    ARAN
         ICM   R3,B'0111',5(R5)
         TM    4(R5),X'80'
         BO    ARAN
         ICM   R1,B'0111',9(R5)
         TM    8(R5),X'80'
         BO    ARAN
         ICM   R2,B'0111',13(R5)
ARAN     STM   R1,R2,ACCC
         LTR   R1,R1
         BZ    NOCC
         TM    ACCC+3,X'03'
         BNZ   ERR3
         XC    0(4,R1),0(R1)
NOCC     LTR   R2,R2
         BZ    NORC
         TM    ACRC+3,X'03'
         BNZ   ERR3
         XC    0(4,R2),0(R2)
NORC     LTR   R4,R4
         BZ    ERR4
         LTR   R3,R3               GET LENGTH
         BNZ   ARSL
         LR    R1,R4
         BCTR  R1,0
         IC    R3,0(R1)
         B     ARST
ARSL     TM    7(R5),X'03'
         BNZ   ERR3
         L     R3,0(R3)
ARST     L     R1,ATSO
         TM    0(R1),X'80'
         BZ    *+L'*+4
         OI    XSWX,XFORE
         OC    APSCB,APSCB
         BZ    ERR8
         LTR   R3,R3               VALIDATE ARGUMENTS
         BNP   ERR2
         CH    R3,=H'240'
         BH    ERR2
         AR    R3,R4
         LR    R1,R3
         BCTR  R3,0
         EJECT
*        COMMAND/CLIST CONTROL.
         SPACE 1
         LA    R2,1
LNME     CLI   0(R4),C' '          FIND NAME
         BNE   CNME
FNME     BXLE  R4,R2,LNME
         B     ERR4
CNME     CLI   0(R4),C'%'          CLIST EXECUTION?
         BE    FNME                YES
         SR    R1,R4
         LR    R0,R1
         ST    R0,LCMD             SET COMMAND BUFFER LENGTH
         BCTR  R1,0
         EX    R1,SNCMD            MOVE COMMAND TEXT
         LR    R1,R4
FENME    BXLE  R4,R2,*+L'*+4
         B     ENME
         CLI   0(R4),C' '
         BNE   FENME
ENME     LR    R5,R4
         SR    R5,R1
         BNP   ERR5
         CH    R5,=H'8'
         BH    ERR5
         BCTR  R5,0
         MVC   TCMD,=CL8' '
         EX    R5,MVCMD
         TM    XSWX,XFORE
         BO    SFORE
         L     R1,=A(TCMDB)        SCAN RESERVED COMMAND (BACKGROUND)
         B     LSCT
SFORE    L     R1,=A(TCMDF)        SCAN RESERVED COMMAND (FOREGROUND)
LSCT     CLI   0(R1),X'FF'         END OF TABLE?
         BE    TXEQ                YES
         CLC   TCMD(L'TCMD),0(R1)  MATCH?
         BE    ERR6                YES, REJECT IT
         LA    R1,L'TCMDB(R1)      BUMP TO NEXT
         B     LSCT
         EJECT
*        TSO ENVIRONMENT CONTROL.
         SPACE 1
         USING PSA,R0
TXEQ     L     R2,PSATOLD          CURRENT TCB ADDRESS
         DROP  R0
         XR    R4,R4
         USING TCB,R2
TSCT     L     R3,TCBRBP           CURRENT RB ADDRESS
         LTR   R3,R3
         BZ    NXTTCB
         USING RBBASIC,R3
         ICM   R4,B'0111',RBCDE1   CURRENT CDE ADDRESS
         BZ    NXTTCB
         USING CDENTRY,R4
         CLC   CDNAME,=CL8'IKJEFT02'
         BE    TFND
NXTTCB   L     R2,TCBOTC           ORIGINATING TCB
         LTR   R2,R2
         BNZ   TSCT
         B     ERR7
         DROP  R3,R4
TFND     L     R3,TCBGRS9          GET TMP WORK-AREA ADDRESS
         DROP  R2
         USING TPL,R3
         L     R4,CPPLPTR
         DROP  R3
         USING CPPL,R4
         L     R2,APSCB
         USING PSCB,R2
         CLC   PSCBUPT,CPPLUPT     UPT ADDRESSES MATCH?
         BNE   ERR7                NO
         CL    R2,CPPLPSCB         CPPL+8 = PSCB ADDRESS?
         BNE   ERR7                NO
         DROP  R2,R4
         EJECT
*        EXECUTE COMMAND/CLIST.
         SPACE 1
         TM    XSWX,ALLMSG         ALL MESSAGES REQUEST?
         BZ    GOCMD               NO
         L     R1,=A(DIAGA)
         LA    R8,DIAGAS(R1)
         MVI   0(R8),C' '
         MVC   1(L'DIAGAS-1,R8),0(R8)
         L     R14,LCMD            COMMAND LENGTH
         CH    R14,DIAGAL
         BNH   *+L'*+4
         LH    R14,DIAGAL
         BCT   R14,*+L'*+L'MVMSG
MVMSG    MVC   0(*-*,R8),CMND      <<EXECUTED>>
         EX    R14,MVMSG
         CLC   L'DIAGAS-5(5,R8),=CL8' '
         BE    *+L'*+6
         MVC   L'DIAGAS-5(5,R8),=CL5' ... '
         BAS   R8,SMSG
GOCMD    L     R2,CVTPTR           CVT ADDRESS
         USING CVT,R2
         L     R1,CVTTVT           TSO VECTOR TABLE
         DROP  R2
         USING TSVT,R1
         L     R15,TSVTASF         TSO SERVICE ROUTINE
         DROP  R1
        CALL   (15),(FLAGS,CMND,LCMD,RETCODE,RSNCODE,ABNDCODE),VL
         LTR   R15,R15             ANY COMPLETION CODE?
         BZ    DONE                NO
         CH    R15,=H'4'           FUNCTION RETURN CODE?
         BNE   ERRCC               NO
         STC   R15,XCC             SET COMPLETION CODE
         LA    R15,X'FFF'
         L     R1,RETCODE
         NR    R1,R15
         STCM  R1,B'0011',XCC+2    SET FUNCTION RETURN CODE
         TM    XSWX,NOMSG          DISPLAY RC REQUESTED?
         BO    DONE                NO
         L     R1,=A(DIAGB)
         UNPK  DIAGBR(L'DIAGBR+1,R1),RETCODE(L'RETCODE+1)
         TR    DIAGBR(L'DIAGBR,R1),TRTAB-C'0'
         MVI   DIAGBR+L'DIAGBR(R1),C'.'
         BAS   R8,SMSG
         B     DONE
ERRCC    ST    R15,WORK
         STC   R15,XCC             SET COMPLETION CODE
         LA    R15,X'FFF'
         L     R1,RSNCODE
         NR    R1,R15
         CLC   ABNDCODE,=F'-1'
         BE    *+L'*+12
         L     R0,ABNDCODE
         NR    R0,R15
         SLL   R0,12
         OR    R1,R0
         STCM  R1,B'0111',XCC+1    SET ABEND AND REASON CODES
         TM    XSWX,NOMSG          DISPLAY RC REQUESTED?
         BO    DONE                NO
         L     R1,=A(DIAGC)
         UNPK  DIAGCR(L'DIAGCR+1,R1),WORK(5)
         TR    DIAGCR(L'DIAGCR,R1),TRTAB-C'0'
         MVI   DIAGCR+L'DIAGCR(R1),C','
         UNPK  DIAGCS(L'DIAGCS+1,R1),RSNCODE(L'RSNCODE+1)
         TR    DIAGCS(L'DIAGCS,R1),TRTAB-C'0'
         MVI   DIAGCS+L'DIAGCS(R1),C','
         UNPK  DIAGCA(L'DIAGCA+1,R1),ABNDCODE(L'ABNDCODE+1)
         TR    DIAGCA(L'DIAGCA,R1),TRTAB-C'0'
         MVI   DIAGCA+L'DIAGCA(R1),C'.'
         BAS   R8,SMSG
DONE     ICM   R1,B'1111',ACCC
         BZ    *+L'*+6
         MVC   0(4,R1),XCC
         OC    XCC,XCC             ANY FINAL COMPLETION CODE?
         BNZ   CMXC                YES
         XR    R10,R10             ALL OK
         B     LEAVE
CMXC     L     R1,=A(DIAG1)
         XR    R0,R0               SET COMPLETION CODE
         IC    R0,XCC
         CVD   R0,WORK
         UNPK  DIAG1C(L'DIAG1C,R1),WORK+6(2)
         OI    DIAG1C+L'DIAG1C-1(R1),C'0'
         MVC   WORK+5(3),XCC+1
         UNPK  WORK(5),WORK+5(3)   SET SYSTEM CODE
         TR    WORK(3),TRTAB-C'0'
         MVC   DIAG1S(L'DIAG1S,R1),WORK
         NI    WORK+6,X'0F'        SET USER CODE
         LH    R0,WORK+6
         CVD   R0,WORK
         UNPK  DIAG1U(L'DIAG1U,R1),WORK+5(3)
         OI    DIAG1U+L'DIAG1U-1(R1),C'0'
ERR1     BCTR  R10,0          COMMAND FAILED
ERR2     BCTR  R10,0          INVALID COMMAND TEXT LENGTH
ERR3     BCTR  R10,0          ARGUMENT ALIGNMENT ERROR
ERR4     BCTR  R10,0          MISSING COMMAND
ERR5     BCTR  R10,0          INVALID COMMAND
ERR6     BCTR  R10,0          COMMAND REJECTED (UNSUPPORTED)
ERR7     BCTR  R10,0          UNABLE TO LOCATE CPPL
ERR8     BCTR  R10,0          NOT TSO ENVIRONMENT
MAXERR   EQU   ((*-ERR1)/2)+1
         TM    XSWX,NOMSG
         BO    LEAVE
         LR    R1,R10
         BCTR  R1,0
         SLL   R1,2
         AL    R1,=A(TABD)
         L     R1,0(R1)
         BAS   R8,SMSG
         EJECT
*        END OF PROCESSING.
         SPACE 1
LEAVE    L     R1,4(R13)
         MVC   20(4,R1),XCC        SET R0
         L     R1,ACRC
         LTR   R1,R1
         BZ    *+L'*+4
         ST    R10,0(R1)
        $XRET  CC=(R10)
         EJECT
*        SEND MESSAGES ROUTINE.
*              R1 : WTO MESSAGE ADDRESS.
*              R8 : LINK REGISTER.
         SPACE 1
SMSG     TM    XSWX,XFORE
         BO    STSO
        WTO    MF=(E,(1))
         BR    R8
STSO     LH    R0,0(R1)
         SH    R0,=H'4'
         LA    R1,4(R1)
        TPUT   (1),(0)
         BR    R8
         SPACE 2
SNCMD    MVC   CMND(*-*),0(R4)     << EXECUTED >>
MVCMD    MVC   TCMD(*-*),0(R1)     << EXECUTED >>
         EJECT
*        CONSTANTS AND WORK AREAS.
         SPACE 1
WORK     DC    D'0'
ATSO     DC    A(*-*) -------/
APSCB    DC    A(*-*)        /---> MAINTAIN FIELDS ORDER
ACCC     DC    A(*-*) -------/
ACRC     DC    A(*-*)        /---> MAINTAIN FIELDS ORDER
XCC      DC    F'0'                FINAL COMPLETION CODES
RETCODE  DC    F'0'                RETURN CODE FROM COMMAND
RSNCODE  DC    F'0'                REASON CODE
ABNDCODE DC    F'0'                ABEND CODE
FLAGS    DS    0F -----------/     FIRST PARM TO IKJEFTSR
         DC    H'0'          /     FLAG WORD
         DC    XL1'0'        /     NO DUMP IF ABEND OCCURS
         DC    XL1'05'       /     TELL TSR TO EXECUTE COMMAND/CLIST
*                            /---> MAINTAIN FIELDS ORDER
         SPACE 1
LCMD     DC    F'0'                COMMAND LENGTH
CMND     DC    CL240' '            COMMAND BUFFER
         SPACE 1
TCMD     DC    CL8' '              TEST COMMAND/CLIST NAME
TRTAB    DC    CL16'0123456789ABCDEF'
         SPACE 1
DIAGAL   DC    0H'0',AL2(L'DIAGAS)
XSWX     DC    X'0'                PROCESSING SWITCHES
XFORE    EQU   X'80'               FOREGROUND REGION
NOMSG    EQU   X'40'               NO MESSAGES REQUEST
ALLMSG   EQU   X'20'               ALL MESSAGES REQUEST
         SPACE 2
        LTORG
         SPACE 2
*- - - - END OF BASE REGISTER ADDRESSING - - - - - - - - - - - - - - -*
         EJECT
*        ERROR MESSAGES.
         SPACE 1
TABD     DC    A(DIAG1,DIAG2,DIAG3,DIAG4,DIAG5,DIAG6,DIAG7,DIAG8)
         SPACE 1
        PRINT  NOGEN
DIAGA   WTO    '   ---> COMMAND :                                      X
                                      ',ROUTCDE=11,DESC=7,MF=L
DIAGAS  EQU    22,60               START OF COMMAND TEXT
DIAGB   WTO    'XTC - COMMAND RETURN CODE :         .',                X
               ROUTCDE=11,DESC=7,MF=L
DIAGBR  EQU    32,8                FUNCTION RETURN CODE
DIAGC   WTO    'XTC - IKJEFTSR FAILED : R15=        , R.CC=        , A.X
               CC=        .',ROUTCDE=11,DESC=7,MF=L
DIAGCR  EQU    32,8                R15 CODE
DIAGCS  EQU    47,8                REASON CODE
DIAGCA  EQU    62,8                ABEND CODE
         SPACE 1
DIAG1   WTO    'XTC - ERR=1 : COMMAND(S) FAILED    (S   /U    ).',     X
               ROUTCDE=11,DESC=7,MF=L
DIAG1C  EQU    36,2                COMPLETION CODE
DIAG1S  EQU    41,3                SYSTEM CODE
DIAG1U  EQU    46,4                USER (REASON OR FUNCTION) CODE
DIAG2   WTO    'XTC - ERR=2 : INVALID COMMAND TEXT LENGTH.',           X
               ROUTCDE=11,DESC=7,MF=L
DIAG3   WTO    'XTC - ERR=3 : ARGUMENT ALIGNMENT ERROR.',              X
               ROUTCDE=11,DESC=7,MF=L
DIAG4   WTO    'XTC - ERR=4 : MISSING COMMAND.',                       X
               ROUTCDE=11,DESC=7,MF=L
DIAG5   WTO    'XTC - ERR=5 : INVALID COMMAND.',                       X
               ROUTCDE=11,DESC=7,MF=L
DIAG6   WTO    'XTC - ERR=6 : COMMAND REJECTED (UNSUPPORTED).',        X
               ROUTCDE=11,DESC=7,MF=L
DIAG7   WTO    'XTC - ERR=7 : UNABLE TO LOCATE CPPL.',                 X
               ROUTCDE=11,DESC=7,MF=L
DIAG8   WTO    'XTC - ERR=8 : NOT TSO ENVIRONMENT.',                   X
               ROUTCDE=11,DESC=7,MF=L
        PRINT  GEN
         EJECT
*        THE FOLLOWING TABLES MUST BE IN ALPHABETIC ORDER.
         SPACE 1
TCMDB    DS    0CL8           RESERVED COMMANDS TABLE (BACKGROUND)
         DC    CL8'ACCOUNT'
         DC    CL8'AUTHC'
         DC    CL8'CDSCB'
         DC    CL8'CJSD'
         DC    CL8'DIDOCS'
         DC    CL8'ISPF'
         DC    CL8'LISTENQ'
         DC    CL8'OPER'
         DC    CL8'PDF'            ALIAS OF ISPF
         DC    CL8'PDFINIT'
         DC    CL8'POF'
         DC    CL8'PSWR'
         DC    CL8'PUSE'
         DC    CL8'QSP'
         DC    CL8'RGROUP'
         DC    CL8'RSVENQ'
         DC    CL8'SCHED'
         DC    CL8'SCHEDULE'
         DC    CL8'TEST'
         DC    CL8'TIME'
         DC    CL8'TSODSN'
         DC    CL8'TSOENQ'
         DC    CL8'UJVD'
         DC    CL8'UNIT'
         DC    CL8'UNITS'
         DC    CL8'VCOREZAP'
         DC    CL8'VSM'
         DC    CL8'WAITR'
         DC    CL8'ZAP'
         DC    XL1'FF'             END OF TABLE INDICATOR
         SPACE 1
TCMDF    DS    0CL8           RESERVED COMMANDS TABLE (FOREGROUND)
         DC    CL8'ACCOUNT'
         DC    CL8'AUTHC'
         DC    CL8'CDSCB'
         DC    CL8'OPER'
         DC    CL8'POF'
         DC    CL8'PSWR'
         DC    CL8'PUSE'
         DC    CL8'QSP'
         DC    CL8'TEST'
         DC    CL8'TIME'
         DC    CL8'UNIT'
         DC    CL8'UNITS'
         DC    CL8'VCOREZAP'
         DC    CL8'VSM'
         DC    CL8'ZAP'
         DC    XL1'FF'             END OF TABLE INDICATOR
         EJECT
        PRINT  NOGEN
        CVT    DSECT=YES,LIST=YES
        IHAPSA
        IHARB
        IHACDE
        IKJCPPL
        IKJPSCB
        IKJTCB
        IKJTMPWA
        IKJTSVT
         SPACE 2
         END
