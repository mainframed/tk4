VGPS     TITLE 'ISPF VGET-VPUT SUBROUTINE.'
VGPSUB   START 0
         SPACE 1
* CALLING SEQUENCES : ONLY BY ASSEMBLER MACROS.
* -------------------
*
*        1. $ISPF VGET,ID,DATA,POOL
*           WHERE : - 'VGET'.
*                   - ID IS THE NAME OF THE ISPF VARIABLE.
*                   - DATA IS THE ADDRESS OF A FIELD INTO WHICH THE
*                     ISPF VARIABLE CONTENTS VALUE WILL BE STORED.
*                     FIELD DEFINITION AS BELOW :
*                     DATA ---> DC C'...CONTENTS VALUE...'
*                   - POOL IS 'SHARED' (DEFAULT) OR 'PROFILE'
*
*        2. $ISPF VPUT,ID,DATA,POOL
*           WHERE : - 'VPUT'.
*                   - ID IS THE NAME OF THE ISPF VARIABLE.
*                   - DATA IS THE ADDRESS OF THE NEW CONTENTS VALUE TO
*                     STORE IN THE ISPF VARIABLE.
*                     FIELD DEFINITION AS BELOW :
*                     DATA ---> DC C'...NEW CONTENTS VALUE...'
*                   - POOL IS 'SHARED' (DEFAULT) OR 'PROFILE'
*
* PROCESS DESCRIPTION :
* ---------------------
*
*        1. VGET : - PERFORM A VDEFINE TO INFORM ISPF OF THE VARIABLE
*                    WE WANT (R.C. SET IN RCVDEF).
*                  - PERFORM A VGET TO MOVE THE ISPF VARIABLE INTO THE
*                    RECEIVING  FIELD (R.C. SET IN RCVGVP).
*                  - A VDELETE IS THEN DONE TO FREE THE STORAGE AND
*                    DEFINITION (R.C. SET IN RCVDEL).
*
*        2. VPUT : - PERFORM A VDEFINE TO INFORM ISPF OF THE VARIABLE
*                    WE WANT TO UPDATE (R.C. SET IN RCVDEF).
*                  - PERFORM A VPUT TO UPDATE THE ISPF VARIABLE FROM
*                    THE GIVEN FIELD (R.C. SET IN RCVGVP).
*                  - A VDELETE IS THEN DONE TO FREE THE STORAGE AND
*                    DEFINITION (R.C. SET IN RCVDEL).
*
*        AT ENTRY, THE REGISTER 0 IS SET AS FOLLOWS :
*                  0 = VGET            + = SHARED POOL
*                  1 = VPUT            - = PROFILE POOL
*        AT END, THE CONDITION CODE IS SET AS FOLLOWS :
*                  ZERO - ALL O.K.
*                  NON ZERO - AN ERROR HAS OCCURED.
*        AND THE REGISTER 15 POINTS TO THE RETURN CODES AREA AS SHOWN :
*                  R15 ---> DC F'...RC OF VDEFINE...'
*                           DC F'...RC OF VGET OR VPUT...'
*                           DC F'...RC OF VDELETE...'
         EJECT
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R12
         MVC   ID(L'ID),0(R1)      SET ISPF VARIABLE NAME.
         MVC   LEN+2(2),8(R1)      SET DATA FIELD LENGTH.
         ICM   R3,B'1111',10(R1)   GET DATA FIELD ADDRESS.
         LR    R1,R0
         N     R0,=A(X'7FFFFFFF')
         LTR   R0,R0               SET REQUESTED OPERATION.
         BNZ   *+L'*+10
         MVC   VOP,=CL8'VGET'
         B     *+L'*+6
         MVC   VOP,=CL8'VPUT'
         O     R1,=A(X'0FFFFFFF')
         LTR   R1,R1               SET REQUESTED POOL.
         BM    *+L'*+10
         MVC   VPL,=CL8'SHARED'
         B     *+L'*+6
         MVC   VPL,=CL8'PROFILE'
         LA    R4,RCODES           RETURN CODES ADDRESS.
         SPACE 1
        CALL   ISPLINK,(VDEF,ID,(R3),CHAR,LEN),VL
         ST    R15,0(R4)           R.C. OF VDEFINE.
        CALL   ISPLINK,(VOP,ID,VPL),VL
         ST    R15,4(R4)           R.C. OF VGET OR VPUT.
        CALL   ISPLINK,(VDEL,ID),VL
         ST    R15,8(R4)           R.C. OF VDELETE.
         OC    0(12,R4),0(R4)      SET CONDITION CODE.
         SPACE 1
        $XRET  CC=(R4)
         EJECT
*------- CONSTANTS AND WORK AREAS.
         SPACE 1
RCODES   DC    3F'0'
VDEF     DC    CL8'VDEFINE'
VOP      DC    CL8' '
VDEL     DC    CL8'VDELETE'
CHAR     DC    CL8'CHAR'
VPL      DC    CL8' '
LEN      DC    F'0'
ID       DC    CL8' '
         SPACE 1
        LTORG
         SPACE 2
         END
