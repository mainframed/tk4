BUMU     TITLE 'BROADCAST USER MESSAGES UNLOAD.'
PAJPBUMU START 0
         SPACE 1
* PURPOSE :    SUPPLY THE ACCOUNT NUMBER (TSO UADS), THE PASSWORD
* ---------    AND DEFAULT GROUP NAME (RACF) OF A GIVEN USERID.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12
         LA    R10,MAXERR
         MVI   SWITCH,ISPFM
        $ISPF  VGET,USID,USIDV     GET USERID VALUE.
         BNZ   ERR1                ERROR.
         NI    SWITCH,255-ISPFM
         LA    R1,USIDV+L'USIDV-1  COMPUTE USERID LENGTH.
         LA    R2,L'USIDV
LOOP     CLI   0(R1),C' '
         BNE   SETL
         BCTR  R1,0
         BCT   R2,LOOP
         B     ERR2
SETL     STC   R2,USIDVL
        $GUAR  USIDV,ARRAY
         LR    R2,R15              SAVE COMPLETION OF GUAR CALL.
        $GUACL
         LR    R1,R15              SAVE COMPLETION OF GUACL CALL.
         LTR   R15,R2              TEST COMPLETION OF GUAR CALL.
         BM    ERR3                NOT FOUND.
         BP    ERR4                ERROR.
         LTR   R15,R1              TEST COMPLETION OF GUACL CALL.
         BNZ   ERR5                ERROR.
         OC    ARRAY,ARRAY
         BZ    ERR6                NO ACCOUNT NUMBER.
         CLI   ARRAY+L'ARRAY,8
         BNE   ERR7                WRONG ACCOUNT NUMBER.
        SETAUTH
        ICHEINTY MF=(E,RPR)        GET RACF PASSWORD.
         LR    R2,R15
        RESAUTH
         OI    SWITCH,RACFM
         LTR   R15,R2
         BNZ   ERR8                ERROR.
         NI    SWITCH,255-RACFM
         CLC   GPL,=XL2'0'
         BE    NOPP                NO PASSWORD PROVIDED.
         OI    SWITCH,PSWEM
         LA    R2,GPL
         CLC   GPL,=XL2'0008'
         BNE   ERR9                UNEXPECTED PASSWORD LENGTH.
        CALL   PSWREST,(PSWDM,PSWV,PSWDML),VL
         LTR   R15,R15
         BNZ   ERR9                UNKNOWN CHARACTERS IN PASSWORD.
         NI    SWITCH,255-PSWEM
         LA    R1,PSWV+L'PSWV-1    COMPUTE PASSWORD LENGTH.
         LA    R2,L'PSWV
SCAN     CLI   0(R1),C' '
         BNE   STLG
         BCTR  R1,0
         BCT   R2,SCAN
         XC    GPL,GPL             SET LIKE NO PASSWORD.
         B     NOPP
STLG     STCM  R2,B'0011',VPPSW+18
NOPP     CLC   GDL,=XL2'0'
         BE    NODFG               NO GROUP PROVIDED.
         OI    SWITCH,PSWEM
         CLC   GDL,=XL2'0008'
         BE    GRPOK
         LA    R2,GDL
         MVC   ERRMPA(8),=CL8'DF-GROUP'
         B     ERR9                UNEXPECTED GROUP LENGTH.
GRPOK    NI    SWITCH,255-PSWEM
         LA    R1,DFGRP+L'DFGRP-1  COMPUTE GROUP LENGTH.
         LA    R2,L'DFGRP
SCRH     CLI   0(R1),C' '
         BNE   STLGP
         BCTR  R1,0
         BCT   R2,SCRH
         XC    GDL,GDL             SET LIKE NO GROUP.
         B     NODFG
STLGP    STCM  R2,B'0011',VPGRP+18
NODFG    OI    SWITCH,ISPFM
        $ISPF  VPUT,ACCN,ACCNO     SET ACC.NO. VALUE.
         BNZ   ERR10               ERROR.
         CLC   GPL,=XL2'0008'
         BNE   NOPSWD              NO PASSWORD PROVIDED.
VPPSW   $ISPF  VPUT,PSWD,PSWV      SET PSWD VALUE.
         BNZ   ERR11               ERROR.
NOPSWD   CLC   GDL,=XL2'0008'
         BNE   NODGRP              NO GROUP PROVIDED.
VPGRP   $ISPF  VPUT,DGRP,DFGRP     SET DGRP VALUE.
         BNZ   ERR12               ERROR.
NODGRP   NI    SWITCH,255-ISPFM
         XR    R10,R10             SET C.C. = 0.
LEAVE   $XRET  CC=(R10)            RETURN TO CALLER.
         SPACE 2
ERR1     BCTR  R10,0               USID VGET ERROR.
ERR2     BCTR  R10,0               NO USERID.
ERR3     BCTR  R10,0               USERID NOT FOUND.
ERR4     BCTR  R10,0               CALL GUAR ERROR.
ERR5     BCTR  R10,0               CALL GUACL ERROR.
ERR6     BCTR  R10,0               NO ACCOUNT NUMBER.
ERR7     BCTR  R10,0               WRONG ACCOUNT NUMBER.
ERR8     BCTR  R10,0               RACF LOCATE ERROR.
ERR9     BCTR  R10,0               RACF PASSWORD OR GROUP ERROR.
ERR10    BCTR  R10,0               ACCN VPUT ERROR.
ERR11    BCTR  R10,0               PSWD VPUT ERROR.
ERR12    BCTR  R10,0               DGRP VPUT ERROR.
         SPACE 1
MAXERR   EQU   ((*-ERR1)/2)+1
         SPACE 1
         TM    SWITCH,RACFM
         BO    RACFE
         TM    SWITCH,PSWEM
         BO    PASSE
         TM    SWITCH,ISPFM
         BZ    LEAVE
         LM    R1,R3,0(R15)
         CVD   R1,WORK
         ED    ERRMSA,WORK+6
         CVD   R2,WORK
         ED    ERRMSB,WORK+6
         CVD   R3,WORK
         ED    ERRMSC,WORK+6
        TPUT   ERRMS,ERRMSL
         B     LEAVE
RACFE    CVD   R15,WORK
         ED    ERRMRA,WORK+6
        TPUT   ERRMR,ERRMRL
         B     LEAVE
PASSE    UNPK  ERRMPB(15),0(8,R2)
         UNPK  ERRMPB+14(7),7(4,R2)
         TR    ERRMPB,TRTAB-X'F0'
         MVC   ERRMPB+L'ERRMPB,C'.'
        TPUT   ERRMP,ERRMPL
         B     LEAVE
         EJECT
*        CONSTANTS AND WORK-AREAS.
         SPACE 1
WORK     DC    D'0'
RPR     ICHEINTY LOCATE,TYPE='USR',ENTRY=USIDVL,ACTIONS=(GPSW,GDFG),   1
               WKAREA=WA,OPTIONS=(FLDEF,ACTION),MF=L
GPSW    ICHEACTN FIELD=PASSWORD,MF=L
GDFG    ICHEACTN FIELD=DFLTGRP,MF=L
WA       DC    A(WAL)              WORK AREA LENGTH.
         DC    XL20'0'             RESERVED.
         DC    F'0'                ACTION RESPONSE LENGTH.
GPL      DC    XL2'0'              PASSWORD LENGTH (8).
PSWDM    DC    XL8'0'              PASSWORD.
GDL      DC    XL2'0'              DEFAULT GROUP NAME LENGTH (8).
DFGRP    DC    XL8'0'              DEFAULT GROUP NAME.
         DC    H'0'
WAL      EQU   *-WA
         SPACE 1
PSWDML   DC    A(L'PSWDM)
TRTAB    DC    CL16'0123456789ABCDEF'
PSWV     DC    CL8' '
ARRAY    DC    H'0',XL1'0'
ACCNO    DC    CL8' ',3XL9'0'
USIDVL   DC    AL1(*-*)
USIDV    DC    CL8' '
SWITCH   DC    XL1'0'
ISPFM    EQU   X'01'
RACFM    EQU   X'02'
PSWEM    EQU   X'04'
         SPACE 1
ERRMS    DC    C' ---> PAJPBUMU : $ISPF R.C.'
ERRMSA   DC    XL4'40202120',C','
ERRMSB   DC    XL4'40202120',C','
ERRMSC   DC    XL4'40202120',C'.'
ERRMSL   EQU   *-ERRMS
ERRMR    DC    C' ---> PAJPBUMU : RACF (ICHEINTY) R.C.'
ERRMRA   DC    XL4'40202120',C'.'
ERRMRL   EQU   *-ERRMR
ERRMP    DC    C' ---> PAJPBUMU : RACF '
ERRMPA   DC    C'PASSWORD',C' ERROR '
ERRMPB   DC    CL20' ',C'.'
ERRMPL   EQU   *-ERRMR
         SPACE 2
        LTORG
         SPACE 2
         END
