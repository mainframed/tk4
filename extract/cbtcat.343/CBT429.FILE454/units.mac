UNTS     TITLE 'U N I T S  - TSO COMMAND.'                         -EU-
UNITS    START 0                                                   -EU-
         SPACE 1                                                   -EU-
UNITS   AMODE  24                                                  -EU-
UNITS   RMODE  24                                                  -EU-
         SPACE 1                                                   -EU-
        $DEFREG ,                                                  -EU-
         SPACE 1                                                   -EU-
LV0      EQU   0         AUTHORITY LEVEL DEFINITION : REJECT LEVEL.-EU-
        $MDL@IX ,        MODULE INDEX.                             -EU-
         EJECT
*          DATA-SET CBT910 AT LEVEL 002 AS OF 09/21/83
*          EXTRACTED FROM CBT TAPE 86 FEB, FILE 127 (TSDYNLXA).    -EU-
         SPACE 1                                                   -EU-
***********************************************************************
***********************************************************************
*******             C. B. T.         8/83               LMB-CBT *******
*******                                                         *******
*******  UPDATED WITH UCB CODING CONVENTIONS FOR X-A        CBT *******
*******  THIS CODE SHOULD RUN UNDER MVS SP AND X-A          CBT *******
*******                                                     CBT *******
*******  CONVERTED TO RUN UN-AUTHORIZED                     CJH *******
*******  THIS MODULE HAS BEEN CONVERTED TO PUTLINE/GETLINE  CBT *******
*******                                                         *******
***********************************************************************
***********************************************************************
         SPACE 1                                                   -EU-
*        ADAPTED BY : MOINIL P.A.                                  -EU-
*                     COMPUTING CENTRE (TP 361)                    -EU-
*                     J.R.C. - ISPRA ESTABLISHMENT                 -EU-
*                     21020 ISPRA (VA), ITALY                      -EU-
         SPACE 1                                                   -EU-
NTBL     EQU   200                 'NAMETAB' ELEMENTS NUMBER       -EU-
         SPACE 1                                                   -EU-
*        ----------------------------------------------------
*        DSECT IEFZB421 SHOULD BE SUBSTITUTED WHEN AVAILABLE.
*        ----------------------------------------------------
         SPACE 1
EDTLUVSP EQU   28,4           POINTER TO LOOK UP VALUE SECTION
LUVENTNO EQU   8,4            NUMBER OF ENTRIES IN THIS SECTION
LUVENTRY EQU   16,32          TABLE OF LOOK UP ENTRIES
LUVUNAME EQU   0,8            UNIT NAME IN EBCDIC
         EJECT
        $XENT  BASE=R12,LV=LDYNAMIC,TYPE=RENT                      -EU-
         LR    R10,R13                                             -EU-
         USING DYNMAREA,R10
         MVC   ESTLIST(ESTLEN),ESTSKEL  SET ESTAE LIST             -EU-
         MVC   MYESCTR,=PL3'1024'  SET LOOP PROTECTION VALUE       -EU-
         LA    R2,NAMETAB          CLEAR NAME TABLE TO BLANKS      -EU-
         L     R3,=A((NTBL+1)*L'NAMETAB)                           -EU-
         LA    R4,*                                                -EU-
         XR    R5,R5                                               -EU-
         ICM   R5,B'1000',=CL8' '                                  -EU-
         MVCL  R2,R4                                               -EU-
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE,                   -EU-X
               GET=GETLNE,REL=RELGIB                               -EU-
         XC    USREQAUT(USRQAUTL),USREQAUT                         -EU-
         MVC   USREQAUT+1(3),=AL3(MDL@IX)                          -EU-
         LA    R1,USRQA                                            -EU-
         ST    R1,USREQAUT+4                                       -EU-
        $EACM  USREQAUT                                            -EU-
         CLI   USRQA,LV0           AUTHORIZED?                     -EU-
         BE    NOTAUT              NO, REJECT                      -EU-
        $TEW$PP PARM=PARMLST,REG=R3,ATT=SETRC                      -EU-
         MVI   MSGTXT,C' '
         MVC   MSGTXT+1(L'MSGTXT-1),MSGTXT
        $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE               -EU-
         L     R1,CVTPTR           CVT ADDRESS
         USING CVTMAP,R1           ADDRESS TO DSECT
         L     R1,CVTJESCT         JES CONTROL TABLE
         DROP  R1
         USING JESCT,R1            ADDRESS TO DSECT
         L     R15,JESCTEXT             EXTENSION
         L     R15,JESGB4UV-JESPEXT(R15)     ADDR OF IEFGB4UV
         ST    R15,DSAGB4UV
         L     R1,JESEDT           EDT
         DROP  R1
         L     R6,EDTLUVSP(,R1)    LOOK-UP SECTION
         L     R0,LUVENTNO(,R6)    # OF UNIT NAMES
         CL    R0,=A(NTBL)         TABLE LARGE ENOUGH?             -EU-
         BH    TBSMALL             NO                              -EU-
         LA    R8,L'LUVENTRY       LENGTH OF ENTRY
         LA    R15,LUVENTRY(,R6)   FIRST ENTRY
         LA    R1,NAMETAB
TABLSCAN MVC   0(8,R1),LUVUNAME(R15)
         MVI   8(R1),C' '
         LA    R1,L'NAMETAB(R1)    INCREMENT OUTPUT STAK
         ALR   R15,R8              INCREMENT NAME TABLE POINTER
         BCT   R0,TABLSCAN
         CLI   NAMEKW+1,1          'NAMES' SPECIFIED?              -EU-
         BNE   GET                 NO                              -EU-
         DROP  R3                                                  -EU-
REPEAT   LA    R2,NAMETAB          AXC NAME
NAMESOUT MVC   MSGTXT(8*L'NAMETAB),0(R2)     MOVE OUT 8 NAMES
         BAS   R4,TPTLIT           GO PRINT IT OUT                 -EU-
         LA    R2,8*L'NAMETAB(R2)
         CLI   0(R2),C' '
         BNE   NAMESOUT
GET      MVC   MSGTXT(L'MSGENTER),MSGENTER
         LA    R0,L'MSGENTER
         BAS   R4,TPTLIT+L'TPTLIT                                  -EU-
         BAS   R14,GETLNE                                          -EU-
         B     *+L'*(R15)          HOW COMPLETE?                   -EU-
         B     *+L'*+8             +0 - OK                         -EU-
         B     TGETNO              +4 - NONE                       -EU-
         B     LEAVE+L'LEAVE       +8 - ERROR                      -EU-
         L     R7,#TSIBSP+4        POINT TO INPUT DATA END         -EU-
         L     R2,#TSIBSP+8        POINT TO INPUT DATA START       -EU-
         SLR   R7,R2                                               -EU-
         CH    R7,=H'7'                                            -EU-
         BNH   ACCEPT                                              -EU-
         BAS   R14,RELGIB          RELEASE INPUT BUFFER            -EU-
         MVC   MSGTXT(8),0(R2)                                     -EU-
         MVC   MSGTXT+9(28),=CL28'... INPUT TOO LONG - REENTER'    -EU-
         BAS   R4,TPTLIT                                           -EU-
         B     GET                                                 -EU-
TGETNO   BAS   R14,RELGIB          RELEASE INPUT BUFFER            -EU-
         B     GET                 AGAIN                           -EU-
         OC    UNITNAME(*-*),0(R2) <<EXECUTED>>                    -EU-
ACCEPT   MVC   UNITNAME,=CL8' '                                    -EU-
         EX    R7,ACCEPT-6         MOVE WITH UPPER-CASE            -EU-
         BAS   R14,RELGIB          RELEASE INPUT BUFFER            -EU-
         CLC   UNITNAME,=CL8'END'                                  -EU-
         BE    EODAD
         CLC   UNITNAME,=CL8'NAMES'     THE ABILITY TO REPEAT AXC  -EU-
         BE    REPEAT              THE ABILITY TO REPEAT AXC
         MVC   MSGTXT(L'UNITNAME),UNITNAME                         -EU-
         SPACE 1                                                   -EU-
*------- CHECK THAT THE CLASS NAME ENTERED IS VALID. THIS IS DONE
*        BY COMPARING THE NAME (GENERIC OR ESOTERIC) AGAINST THE
*        CLASS NAMES IN THE EDT LOOK UP SECTION.
         SPACE 1                                                   -EU-
         L     R0,LUVENTNO(,R6)    # CLASS TYPES IN EDT
         LA    R8,L'LUVENTRY       LENGTH OF ENTRY
         LA    R15,LUVENTRY(,R6)   1ST ENTRY
NAMESCAN CLC   UNITNAME(L'UNITNAME),LUVUNAME(R15)
         BE    HAVENAME
         ALR   R15,R8              GET NEXT ENTRY
         BCT   R0,NAMESCAN
         MVC   MSGTXT+10(20),=CL20': INVALID CLASS NAME'           -EU-
         BAS   R4,TPTLIT                                           -EU-
         B     GET
         SPACE 1                                                   -EU-
*------- THE DEVICE NAME ENTERED IS VALID (IT WAS FOUND AGAIN IN
*        THE EDT LOOKUP SECTION). MOVE UNITNAME TO KEY 0 STORAGE
*        AND INVOKE IEFAB4UV TO RETURN ASSOCIATED UCB ADDRESSES.
*        R0, R1, R2, R7, R8, R14 CAN BE REUSED NOW.
         SPACE 1                                                   -EU-
HAVENAME GETMAIN R,LV=LKEY0SP,SP=0      FOR KEY 0 SUBPOOL STORAGE  -EU-
         ST   R1,KEY0ADDR          SAVE KEY 0 GETMAIN'D AREA PTR
         LR   R7,R1                PTR TO KEY 0 STORAGE
         USING KEY0SP,R7           ADDR TO DSECT
         SPACE 1                                                   -EU-
*------- SET UP PARAMETER LIST FOR KEY 0 STORAGE FOR IEFAB4UV.
         SPACE 1                                                   -EU-
         XC   KEY0SP(LKEY0SP),KEY0SP                               -EU-
         LA   R1,UNITABLE
         ST   R1,UTBLPTR
         LA   R1,FLAGS
         ST   R1,FLAGPTR
         MVC  UNAME(L'UNAME),UNITNAME
         MVC  FLAGS,=XL2'1000'     BIT 3 SET FOR UCB SEARCH
         ESTAE MYESTAE,CT,PARAM=(R10),MF=(E,ESTLIST)               -EU-
         LA   R1,PARMLIST
         L     R15,DSAGB4UV
         BASR  R14,R15
         ST    R15,DSAGB4RC        SAVE RC
         ESTAE 0
         MVC  UCBLIST,UCBPTR       SAVE PTR TO UCB LIST
         DROP  R7                  FROM KEY 0 PARM DSECT
         SPACE 1                                                   -EU-
*------- KEY 0 STORAGE CAN NOW BE FREED, THEN PROCESS THE UCB LIST.
         SPACE 1                                                   -EU-
         L    R1,KEY0ADDR
         FREEMAIN R,A=(1),LV=LKEY0SP,SP=0
         L     R15,DSAGB4RC        RESTORE RC
         LTR  R15,R15              HAVE UCBS BEEN RETURNED?
         BZ   GOTUCBS              UCBS RETURNED GO LOOK AT THEM
         CH   R15,=H'4'            R15 = 4
         BE   NONAME               SAY CLASS NAME NOT FOUND
         CH   R15,=H'16'           R15 = 16
         BE   NOSTORGE             NO STORAGE AVAIL FOR UCB LIST
         B    ABENDIT              ABEND ALL OTHER RETURN CODES
NONAME   MVC   MSGTXT+10(30),=CL30': CLASS NAME NOT FOUND - RETRY'
         BAS   R4,TPTLIT                                           -EU-
         B     GET
         SPACE 1                                                   -EU-
*------- UCBS RETURNED IN SUBPOOL 0 STORAGE.
         SPACE 1                                                   -EU-
GOTUCBS  ICM   R9,B'1111',UCBLIST  PTR TO UCB LIST?                -EU-
         BZ    NOTUCBLS            NOT UCB LIST SEND MSG
         MVC   LISTLEN,0(R9)       HOLD SUBPOOL & LEN OF LIST
         L     R7,4(R9)            NUMBER OF UCBS IN LIST
         LA    R9,8(R9)            ADDRESS OF 1ST UCB PTR
         L     R15,0(R9)           FIRST UCB PTR
         USING UCBOB,R15
         LA    R4,MSGTXT+10                                        -EU-
         LA    R5,4                                                -EU-
GETUCB   MVC   0(L'UCBNAME,R4),UCBNAME                             -EU-
         TM    UCBDVCLS,UCB3DACC   DIRECT ACCESS?
         BZ    NOTDA               NO
         TM    UCBSTAT,UCBONLI     ONLINE?
         BZ    NOTONL              NO
         CLI   UCBVOLI,X'00'       YES, VOLID KNOWN?
         BE    NOTDA               NO
         MVI   4(R4),C'-'                                          -EU-
         MVC   6(L'UCBVOLI,R4),UCBVOLI                             -EU-
         B     NOTDA
         DROP  R15
NOTONL   MVI   4(R4),C'-'                                          -EU-
         MVC   6(7,R4),=CL7'OFFLINE'                               -EU-
NOTDA    LA    R4,16(R4)                                           -EU-
         BCT   R5,TSUCBS                                           -EU-
         BAS   R4,TPTLIT                                           -EU-
         LA    R4,MSGTXT+10                                        -EU-
         LA    R5,4                                                -EU-
TSUCBS   BCT   R7,*+L'*+4                                          -EU-
         B     LSUCBS                                              -EU-
         LA    R9,4(,R9)
         L     R15,0(R9)
         B     GETUCB
LSUCBS   CLI   MSGTXT+10,C' '                                      -EU-
         BE    *+L'*+4                                             -EU-
         BAS   R4,TPTLIT                                           -EU-
         SPACE 1                                                   -EU-
*------- NOW FREE THE UCB LIST SUBPOOL 0 STORAGE.                  -EU-
         SPACE 1                                                   -EU-
         L     R0,LISTLEN          SUBPOOL 0 + LEN OF STORAGE      -EU-
         N     R0,FSP0             FORCE SUBPOOL 0                 -EU-
         L     R1,UCBLIST          PTR TO GETMAIN'D AREA           -EU-
         FREEMAIN R,A=(1),LV=(0)   FREEMAIN OF SUBPOOL 0           -EU-
         B     GET
         SPACE 1                                                   -EU-
TPTLIT   LA    R0,L'MSGTXT         PRINT MESSAGE ROUTINE           -EU-
         LA    R1,MSGTXT                                           -EU-
         BAS   R14,PUTLNE                                          -EU-
         B     *+L'*(R15)          HOW COMPLETE?                   -EU-
         B     *+L'*+4             +0 - OK                         -EU-
         B     LEAVE+L'LEAVE       +4 - ERROR                      -EU-
         MVI   MSGTXT,C' '                                         -EU-
         MVC   MSGTXT+1(L'MSGTXT-1),MSGTXT                         -EU-
         BR    R4                  BACK TO CONTINUE PROCESSING     -EU-
         SPACE 1
NOTAUT   LA    R0,L'NAUTM                                          -EU-
         LA    R1,NAUTM                                            -EU-
         BAS   R4,PUTLNE                                           -EU-
SETRC    LA    R2,16               RC=16 ------------------------- -EU-
         B     USEXIT                                              -EU-
TBSMALL  LA    R0,L'MTBSM                                          -EU-
         LA    R1,MTBSM                                            -EU-
         B     LEAVE                                               -EU-
NOSTORGE MVC   MSGTXT+10(35),=CL35': NO STORAGE AVAILABLE FOR UCB LIST'
         B     LEAVE                                               -EU-
NOTUCBLS MVC   MSGTXT+10(29),=CL29': UCB LIST NOT BEING SEARCHED'
         B     LEAVE                                               -EU-
ABENDIT  MVC   MSGTXT+10(10),=CL10': ABEND IT'
LEAVE    BAS   R4,PUTLNE                                           -EU-
         LA    R2,16               RC=16 ------------------------- -EU-
         B     EODAD+L'EODAD                                       -EU-
EODAD    XR    R2,R2               RC=0 -------------------------- -EU-
        $TSWXA 24,EXPAND=ONLY      BACK 24-BIT MODE                -EU-
USEXIT  $XRET  CC=(R2),LV=LDYNAMIC,TYPE=RENT                       -EU-
         SPACE 1                                                   -EU-
         LTORG
         SPACE 1
         DROP  R10,R12                                             -EU-
         EJECT ,                                                   -EU-
FSP0     DC    0F'0',X'00FFFFFF'   FORCE SUBPOOL 0                 -EU-
ESTSKEL  ESTAE *-*,MF=L                                            -EU-
ESTLEN   EQU   *-ESTSKEL                                           -EU-
         SPACE 1                                                   -EU-
NAUTM    DC    C'UNAUTHORIZED COMMAND - REJECTED.'                 -EU-
MTBSM    DC    C'NOT ENOUGH STORAGE FOR NAMES TABLE.'              -EU-
MSGENTER DC    C'ENTER DESIRED GROUP NAME, "NAMES" OR "END"'       -EU-
         EJECT ,                                                   -EU-
         USING MYESTAE,R15
         CNOP  0,4                                                 -EU-
         USING SDWA,R1
MYESTAE  CH    R0,=H'12'           Q/SDWA
         BE    MYESQUIT            N/GET OUT
         L     R10,SDWAPARM                                        -EU-
         USING DYNMAREA,R10                                        -EU-
         SP    MYESCTR,=P'1'       LOOP FAILSAFE
         DROP  R10                                                 -EU-
         BZ    MYESQUIT
         CLC   =X'0C2000',SDWACMPC PRIV OP INTERRUPT?
         BNE   MYESGFF             YES, SKIP IT
         L     R0,SDWANXT1         NEW INSTR AFTER PRIVOP
         B     MYESRTRY
MYESGFF  CLC   =X'B78000',SDWACMPC GETMAIN/FREEMAIN FAIL
         BNE   MYESQUIT
         ICM   R0,B'1111',SDWASRSV+15*4 GET ORIG R15
         N     R0,=X'FFFF00FF'     SET SUBPOOL TO ZERO
         STCM  R0,B'1111',SDWASRSV+15*4 NEW R15
         L     R0,SDWANXT2
         SH    R0,=H'2'            BACKUP
MYESRTRY SETRP RC=4,RETADDR=(R0),FRESDWA=YES,DUMP=NO,RETREGS=YES
         BR    R14
MYESQUIT SETRP RC=0,FRESDWA=NO,DUMP=YES
         BR    R14
         SPACE 1
         LTORG
         SPACE 1
         DROP  R1,R15                                              -EU-
         EJECT ,                                                   -EU-
*------- PARSE PCL.                                                -EU-
         SPACE 1                                                   -EU-
         PRINT NOGEN                                               -EU-
PARMLST  IKJPARM ,                                                 -EU-
NAMEKW   IKJKEYWD ,                                                -EU-
         IKJNAME 'NAMES'                                           -EU-
         IKJENDP ,                                                 -EU-
         PRINT GEN                                                 -EU-
         EJECT ,                                                   -EU-
*------- DSECT'S.
         SPACE 1
DYNMAREA DSECT
         DS    18F                 SAVE AREA
ESTLIST  ESTAE *-*,MF=L                                            -EU-
DSAGB4UV DS    F                   EP OF IEFGB4UV
DSAGB4RC DS    F                   RC OF IEFGB4UV
KEY0ADDR DS    F
LISTLEN  DS    F                   SUBPOOL & LENGTH OF LIST
UCBLIST  DS    F                   PTR TO UCB LIST
        $TEW$WA ,                                                  -EU-
MSGTXT   DS    CL80                                                -EU-
USREQAUT DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(*-*)            -EU-
USRQA    DC    XL1'0'                                              -EU-
USRQAUTL EQU   *-USREQAUT                                          -EU-
MYESCTR  DS    PL3                 LOOP PROTECTION COUNTER         -EU-
UNITNAME DS    CL8,CL1                                             -EU-
NAMETAB  DS    (NTBL+1)CL9                                         -EU-
         SPACE 1                                                   -EU-
LDYNAMIC EQU   (((*-DYNMAREA)+7)/8)*8   SIZE OF DYNAMIC AREA       -EU-
         SPACE 1                                                   -EU-
*------- DSECT FOR KEY 0 SUBPOOL GETMAIN'D STORAGE.
         SPACE 1
KEY0SP   DSECT                     GETMAIN DSECT FOR KEY 0 SUBPOOL
PARMLIST DS    2F                  PARMLIST MAPPING
         ORG   PARMLIST
UTBLPTR  DS    F                   ADDRESS OF THE UNIT TABLE
FLAGPTR  DS    F                   ADDRESS OF THE FUNCTION REQUEST
         DS    0F
UNITABLE DS    CL20
         ORG   UNITABLE
UNAME    DS    CL8
UCBPTR   DS    F
         DS    F
         DS    F
FLAGS    DS    XL2
         SPACE 1                                                   -EU-
LKEY0SP  EQU   (((*-KEY0SP)+7)/8)*8                                -EU-
         SPACE 1                                                   -EU-
         PRINT NOGEN                                               -EU-
        $TEW$DS ,                                                  -EU-
         IEFUCBOB ,
         IEFJESCT ,
         IHASDWA ,
         PRINT GEN                                                 -EU-
         SPACE 1
         END
