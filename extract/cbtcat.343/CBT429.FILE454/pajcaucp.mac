PROC     0
         CONTROL MAIN MSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN EXIT
         ISPEXEC CONTROL ERRORS CANCEL
         SET &PCMD =
         SET &FCMD =
         ISPEXEC TBCREATE XYTABLE REPLACE NOWRITE NAMES(XY)
         SET &XY = &STR(*** READY ***)
         ISPEXEC TBADD XYTABLE
TBDR:    SET &RCMD =
         SET &TBRC = 0
         SET &ZTDTOP = 0
TBDX:    IF &TBRC EQ 4 THEN GOTO TBDP
         ISPEXEC TBTOP XYTABLE
         ISPEXEC TBSKIP XYTABLE NUMBER(&ZTDTOP)
         ISPEXEC VGET (ZVERB ZSCROLLN)
         IF &ZVERB EQ UP THEN -
           ISPEXEC TBSKIP XYTABLE NUMBER(-&ZSCROLLN)
         ELSE IF &ZVERB EQ DOWN THEN -
           ISPEXEC TBSKIP XYTABLE NUMBER(&ZSCROLLN)
TBDP:    ISPEXEC TBDISPL XYTABLE PANEL(PAJAUCP) POSITION(ROW)
         SET &TBRC = &LASTCC
         IF &STR(&RCMD) EQ &STR(CANCEL) OR &TBRC GT 4 THEN GOTO TBEX
         IF &STR(&RCMD) EQ &STR(*) THEN SET &RCMD = &PCMD
         IF &STR(&RCMD) EQ &STR(?) THEN GOTO TBRD
         IF &STR(&RCMD) EQ &STR(F) THEN GOTO TBFN
         SET &PCMD = &NRSTR(&RCMD)
         IF &STR(&RCMD) EQ THEN GOTO TBNC
         SET &SYSOUTTRAP = 5000
         IF &RAUT EQ ON THEN DO
           AUTHC &RCMD
           SET &CMRC = &LASTCC
           END
         ELSE DO
           TSOEXEC &RCMD
           SET &CMRC = &LASTCC
           END
         SET &NL = &SYSOUTLINE
         SET &SYSOUTTRAP = 0
         SET &N = 1
         SET &LN = &STR(&&)
         ISPEXEC TBCREATE XYTABLE REPLACE NOWRITE NAMES(XY)
         CONTROL ASIS
         DO WHILE &N LE &NL
           SET &XY = &SYSNSUB(2,&LN.SYSOUTLINE&N)
           ISPEXEC TBADD XYTABLE
           SET &N = &N + 1
           END
         CONTROL CAPS
         SET &XY = &STR(*** COMMAND ENDED WITH RETURN CODE &CMRC ***)
         ISPEXEC TBADD XYTABLE
         GOTO TBDR
TBNC:    ISPEXEC TBCREATE XYTABLE REPLACE NOWRITE NAMES(XY)
         SET &XY = &STR(*** THERE IS NO PREVIOUS COMMAND ***)
         ISPEXEC TBADD XYTABLE
         GOTO TBDR
TBRD:    SET &RCMD = &NRSTR(&PCMD)
         IF &STR(&RCMD) EQ THEN GOTO TBNC
         SET &TBRC = 4
         GOTO TBDX
TBFN:    SET &RCMD =
         ISPEXEC TBSKIP XYTABLE NUMBER(&ZTDTOP)
         ISPEXEC TBSKIP XYTABLE NUMBER(+1) POSITION(ROW)
         SET &FNRC = &LASTCC
         IF &FNRC NE 8 THEN GOTO TBFT
         ISPEXEC TBTOP XYTABLE
TBFS:    ISPEXEC TBSKIP XYTABLE NUMBER(+1) POSITION(ROW)
         SET &FNRC = &LASTCC
TBFT:    IF &FNRC NE 0 THEN GOTO TBFE
         ISPEXEC TBGET XYTABLE POSITION(ROW)
         SET &FNRC = &LASTCC
         IF &FNRC NE 0 THEN GOTO TBFE
         SET &N = &SYSINDEX(&NRSTR(&FCMD),&NRSTR(&XY))
         IF &N EQ 0 THEN GOTO TBFS
         SET &TBRC = 4
         GOTO TBDX
TBFE:    IF &FNRC EQ 8 THEN -
           ISPEXEC TBBOTTOM XYTABLE NOREAD POSITION(ROW)
         ELSE IF &ROW NE 0 THEN -
           ISPEXEC TBSKIP XYTABLE NUMBER(-1) NOREAD POSITION(ROW)
         SET &TBRC = 4
         GOTO TBDX
TBEX:    ISPEXEC TBEND XYTABLE
         IF &TBRC NE 8 THEN DO
           WRITE *** EXIT CODE &TBRC ***
           END
         CONTROL MSG
