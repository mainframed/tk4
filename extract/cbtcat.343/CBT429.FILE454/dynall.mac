DYAL     TITLE 'DYNAMIC DATA-SET ALLOCATION ROUTINE.'
*        DATA SET S062A10 : AT LEVEL 001 AS OF 03/09/77.
*        DATA SET S062A10 : AT LEVEL 002 AS OF 10/22/76.
         SPACE 1
DYNALL   START 0
         SPACE 1
* PURPOSE :
* ---------
*
* THIS MODULE WILL FUNCTION ONLY IN OS/VS2 RELEASE 2 AND LATER
* RELEASES (IE. MVS). IT ALLOWS THE USER TO PERFORM ANY ONE OF THE
* FOLLOWING OPERATIONS :
*
*        1.    DYNAMICALLY ALLOCATE AN EXISTING DISK DATA SET. ANY
*                          DISK DATA SET, INCLUDING AN ISAM DATA SET,
*                          MAY BE ALLOCATED.
*        2.    DYNAMICALLY ALLOCATE A NEW DISK DATA SET. ANY VALID
*                          DISK DATA SET ORGANIZATION EXCEPT ISAM
*                          MAY BE SPECIFIED.
*        3.    DYNAMICALLY ALLOCATE A 'DD DUMMY' DATA SET.
*        4.    DYNAMICALLY DE-ALLOCATE A PREVIOUS ALLOCATION.
*
* IT IS ASSUMED THAT THE USER OF THIS MODULE HAS A GOOD WORKING
* KNOWLEDGE OF OS/VS2 JCL AS DESCRIBED IN THE OS/VS2 JCL MANUAL
* (GC28-0692). THE USER OF THIS MODULE WILL BUILD A LIST OF
* PARAMETERS IN MUCH THE SAME WAY AS HE WOULD FILL OUT A 'DD CARD'.
* THE INFORMATION IN THE PARAMETER LIST WILL BE USED TO DO THE
* REQUESTED FUNCTION.
*
* THE LINKAGE TO THIS MODULE CAN BE MADE WITH A STANDARD OS 'CALL',
* 'LINK' OR 'ATTACH'. THE ADDRESS LIST SPECIFIED IN THE 'CALL' (OR
* 'LINK' OR 'ATTACH') MUST ALWAYS INCLUDE THE ADDRESS OF THE
* PARAMETER LIST (DESCRIBED BELOW) AS ITS FIRST OR ONLY ENTRY. THE
* SECOND (OPTIONAL) ADDRESS IN THE LIST MUST BE THE ADDRESS OF 2
* CONSECUTIVE FULLWORDS IN THE CALLER'S PROGRAM. R13 MUST CONTAIN
* THE ADDRESS OF A STANDARD OS SAVE AREA. EXAMPLES OF CALL'S ARE
* SHOWN BELOW.
*
*        CALL  DYNALL,(DYNLIST),VL
*        CALL  DYNALL,(DYNLIST,RCODES),VL
*
* UPON RETURN FROM THIS MODULE, R0 AND R15 WILL CONTAIN CODES
* INDICATING HOW THE REQUEST COMPLETED. IF THE SECOND ITEM IS NAMED
* IN THE ADDRESS LIST, THEN THE VALUES PLACED IN R0 AND R15 WILL
* ALSO BE STORED IN ITS FIRST AND SECOND WORDS, RESPECTIVELY. THIS
* WILL ALLOW PROGRAMS WRITTEN IN COBOL OR OTHER HIGH LEVEL LANGUAGES
* TO USE THIS MODULE. DETAILED INFORMATION ABOUT THE RETURN CODES IS
* GIVEN FOLLOWING THE INFORMATION ABOUT THE PARAMETER LIST.
*
* THIS MODULE IS RE-ENTERABLE.
         EJECT
        $DYNLIST
         EJECT
* RETURN CODES :
* --------------
*
* THIS MODULE USES THE DYNAMIC ALLOCATION ROUTINE DESCRIBED IN THE
* OS/VS2 SYSTEMS PROGRAMMING LIBRARY : JOB MANAGEMENT MANUAL
* (GC28-0627). THIS MODULE DOES SOME PRELIMINARY EDITING PRIOR TO
* REQUESTING DYNAMIC ALLOCATION. IF IT FINDS SOME ERROR WHICH
* PREVENTS COMPLETION OF THE REQUEST, IT WILL PLACE 15 (DECIMAL) IN
* R15. R0 WILL CONTAIN AN OFFSET FROM THE BEGINNING OF THE PARAMETER
* LIST TO SHOW WHICH FIELD IS IN ERROR. FOR EXAMPLE, IF THE
* 'DSNDISP' FIELD CONTAINS 'PASS' (WHICH IS INVALID), R0 WILL CONTAIN
* 76 (DECIMAL) AND R15 WILL CONTAIN 15 (DECIMAL).
*
* UNLESS THIS MODULE DETECTS AN ERROR, IT WILL MAKE A REQUEST TO THE
* DYNAMIC ALLOCATION ROUTINE. THE DYNAMIC ALLOCATION ROUTINE ISSUES
* A RETURN CODE IN R15 AND ALSO GIVES AN 'ERROR REASON CODE' AND AN
* 'INFORMATIONAL REASON CODE'. EACH OF THE TWO 'REASON CODES' IS 2
* BYTES LONG. THIS MODULE WILL PLACE THE 'ERROR REASON CODE' IN THE
* FIRST 2 BYTES OF R0 AND THE 'INFORMATIONAL REASON CODE' IN THE
* SECOND 2 BYTES OF R0. THIS MODULE WILL LEAVE THE DYNAMIC
* ALLOCATION RETURN CODE IN R15. THE MEANING OF THESE CODES CAN BE
* FOUND IN THE MANUAL (GC28-0627) MENTIONED ABOVE.
*
* IT WILL ALWAYS BE POSSIBLE TO KNOW THE MEANING OF AN R0 RETURN CODE
* (IE. DOES IT CONTAIN AN OFFSET OR DOES IT CONTAIN 'REASON CODES'),
* SINCE THE DYNAMIC ALLOCATION ROUTINE NEVER ISSUES A 15 (DECIMAL)
* RETURN CODE. THEREFORE, WHEN R15 CONTAINS 15 (DECIMAL), R0 MUST
* CONTAIN AN OFFSET.
*
* IF THE PROGRAM THAT INVOKES THIS MODULE PASSES 2 ADDRESSES IN ITS
* ADDRESS LIST, THIS MODULE WILL PLACE THE CONTENTS OF R0 AND R15 IN
* THE FIRST AND SECOND FULLWORDS, RESPECTIVELY, OF THE SECOND ITEM IN
* THE ADDRESS LIST.
         SPACE 2
        PRINT  NOGEN
        IEFZB4D0
        IEFZB4D2
        DCBD   DSORG=PS,DEVD=DA
        PRINT  GEN
         SPACE 1
DYNALL   CSECT                     RESTORE ORIGINAL CSECT.
         EJECT
WRKDSECT DSECT                     WORK AREAS DESCRIPTION.
         SPACE 1
SVA      DS    18F                 SAVE AREA.
REQBKPTR DS    F
WKRTNVOL DS    F
WKRTDSRG DS    F
         DS    F
DBLWORD  DS    D
         DS    4F
         SPACE 1
WRKL     EQU   1024                WORK AREAS LENGTH.
DYNWRKL  EQU   (WRKL-(*-SVA))
DYNWORK  DS    (DYNWRKL)X
         SPACE 2
DYNALL   CSECT                     RESTORE ORIGINAL CSECT.
         SPACE 2
        $DEFREG
         EJECT
*        REGISTERS USE :
         SPACE 1
*              R6  : ADDRESS OF REQUEST BLOCK.
*              R7  : HAS OFFSET OF CURRENT ENTRY IN PARAMETER LIST
*                    FOR DYNAMIC ALLOCATION.
*              R8  : ADDRESS OF CURRENT TEXT POINTER.
*              R9  : ADDRESS OF CURRENT TEXT UNIT.
*              R10 : ADDRESS OF CALLER'S PARAMETER LIST.
*              R11 : BASE REGISTER.
*              R13 : WORK AREA ADDRESS (SAVE AREA IS FIRST 72 BYTES).
         SPACE 2
        $XENT  BASE=R11,LV=WRKL,TYPE=RENT
         EJECT
         USING WRKDSECT,R13
         L     R10,0(0,R1)         PICK UP PARAMETER LIST ADDRESS.
         USING DYNLIST,R10
         LR    R14,R13             SET UP FOR CLEAR OF WORK AREA.
         L     R15,WORKLEN
         LA    R0,18*4
         AR    R14,R0
         SR    R15,R0
         SR    R0,R0
         LR    R1,R0
         MVCL  R14,R0              CLEAR WORK AREA TO ALL X'00'.
         LA    R6,DYNWORK
         USING S99RB,R6
         ST    R6,REQBKPTR         POINT TO REQUEST BLOCK.
         OI    REQBKPTR,X'80'
         LA    R1,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK.
         STC   R1,S99RBLN
         CLC   DSNAME,=XL44'0'     CHECK FOR UNALLOCATION REQUEST.
         BNE   *+L'*+8
         MVI   S99VERB,S99VRBUN    REQUEST UNALLOCATION.
         B     *+L'*+8
         MVI   S99VERB,S99VRBAL    REQUEST ALLOCATION.
         OI    S99FLG11,S99NOCNV+S99NOMNT
         LA    R8,S99RBEND         PICK UP ADDRESS FOR TEXT POINTERS.
         ST    R8,S99TXTPP
         L     R9,TXPTRSPC         PICK UP SPACE FOR TEXT POINTERS.
         ALR   R9,R8               POINT TO FIRST TEXT UNIT.
         USING S99TUNIT,R9
         EJECT
* DDNAME **************************
         SPACE 1
         LA    R7,DDNAME-DYNLIST   INIT 'OFFSET' REGISTER.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BNE   *+L'*+8
         LA    R1,DUNDDNAM
         B     *+L'*+4
         LA    R1,DALDDNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         LA    R14,DDNAME          FIND OUT ACTUAL LENGTH OF DDNAME.
         LA    R15,L'DDNAME
         BAL   R1,PP1
         LTR   R15,R15
         BNP   QQ1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DDNAME.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BNE   AA1                 GO DO OTHER REQUEST.
         SPACE 1
* DSUFORCE ************************
         SPACE 1
         LA    R7,DSUFORCE-DYNLIST
         LA    R14,DSUFORCE
         LA    R15,L'DSUFORCE
         BAL   R1,PP1              GET DSUFORCE LENGTH.
         LTR   R15,R15
         BNP   MM1
         CLC   DSUFORCE,=CL8'FORCE '
         BNE   MM1
         LA    R1,DUNUNALC         S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY              CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         B     MM1                 GO DO REQUEST.
         SPACE 1
* DSNAME **************************
         SPACE 1
AA1      LA    R7,DSNAME-DYNLIST   OFFSET.
         CLC   DSNAME,=CL44'NULLFILE '  CHECK FOR DUMMY ALLOCATION.
         BNE   AA1A
         LA    R1,DALDUMMY         S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY              CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     AA10
AA1A     LA    R14,DSNAME
         LA    R15,L'DSNAME
         BAL   R1,PP1              GET DSNAME LENGTH.
         LTR   R15,R15
         BNP   AA2
         LA    R1,DALDSNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSNAME.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSMEMBER ************************
         SPACE 1
AA2      LA    R7,DSMEMBER-DYNLIST
         LA    R14,DSMEMBER
         LA    R15,L'DSMEMBER
         BAL   R1,PP1              GET DSMEMBER LENGTH.
         LTR   R15,R15
         BNP   AA3
         LA    R1,DALMEMBR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSMEMBER.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* PASSWORD ************************
         SPACE 1
AA3      LA    R7,PASSWORD-DYNLIST
         LA    R14,PASSWORD
         LA    R15,L'PASSWORD
         BAL   R1,PP1              GET PASSWORD LENGTH.
         LTR   R15,R15
         BNP   AA4
         LA    R1,DALPASSW
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE PASSWORD.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSSTATUS ************************
         SPACE 1
AA4      LA    R7,DSSTATUS-DYNLIST
         LA    R14,DSSTATUS
         LA    R15,L'DSSTATUS
         BAL   R1,PP1              GET DSSTATUS LENGTH.
         LTR   R15,R15
         BNP   AA5
         LA    R1,DALSTATS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSSTATUS,=CL8'OLD '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'01'
         B     AA4A
         CLC   DSSTATUS,=CL8'MOD '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     AA4A
         CLC   DSSTATUS,=CL8'NEW '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'04'
         B     AA4A
         CLC   DSSTATUS,=CL8'SHR '
         BNE   QQ1
         MVI   S99TUPAR,X'08'
AA4A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSNDISP *************************
         SPACE 1
AA5      LA    R7,DSNDISP-DYNLIST
         LA    R14,DSNDISP
         LA    R15,L'DSNDISP
         BAL   R1,PP1              GET DSNDISP LENGTH.
         LTR   R15,R15
         BNP   AA6
         LA    R1,DALNDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSNDISP,=CL8'UNCATLG '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'01'
         B     AA5A
         CLC   DSNDISP,=CL8'CATLG   '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     AA5A
         CLC   DSNDISP,=CL8'DELETE  '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'04'
         B     AA5A
         CLC   DSNDISP,=CL8'KEEP    '
         BNE   QQ1
         MVI   S99TUPAR,X'08'
AA5A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSADISP *************************
         SPACE 1
AA6      LA    R7,DSADISP-DYNLIST
         LA    R14,DSADISP
         LA    R15,L'DSADISP
         BAL   R1,PP1              GET DSADISP LENGTH.
         LTR   R15,R15
         BNP   AA7
         LA    R1,DALCDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSADISP,=CL8'UNCATLG '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'01'
         B     AA6A
         CLC   DSADISP,=CL8'CATLG   '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     AA6A
         CLC   DSADISP,=CL8'DELETE  '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'04'
         B     AA6A
         CLC   DSADISP,=CL8'KEEP    '
         BNE   QQ1
         MVI   S99TUPAR,X'08'
AA6A     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSUNIT **************************
         SPACE 1
AA7      LA    R7,DSUNIT-DYNLIST
         LA    R14,DSUNIT
         LA    R15,L'DSUNIT
         BAL   R1,PP1              GET DSUNIT LENGTH.
         LTR   R15,R15
         BNP   AA8
         LA    R1,DALUNIT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSUNIT.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV1 ************************
         SPACE 1
AA8      LA    R7,$RESERV1-DYNLIST
         CLC   $RESERV1,=CL44' '
         BNE   QQ1
         SPACE 1
* DSVOLSER ************************
         SPACE 1
         LA    R7,DSVOLSER-DYNLIST
         CLC   DSVOLSER,=XL44'0'
         BNE   AA8A
         LA    R1,DALRTVOL         SET UP TO HAVE VOLSER RETURNED
         STH   R1,S99TUKEY              BACK TO US.
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,6
         MVC   S99TUPAR(6),=CL44' '
         LA    R1,S99TUPAR
         ST    R1,WKRTNVOL         SAVE ADDRESS OF PARAMETER FIELD.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+6       NEXT TEXT UNIT.
         B     AA9
AA8A     LA    R14,DSVOLSER
         LA    R15,L'DSVOLSER
         BAL   R1,PP1              GET DSVOLSER LENGTH.
         LTR   R15,R15
         BNP   AA9
         LA    R1,DALVLSER
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLSER.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV2 ************************
         SPACE 1
AA9      LA    R7,$RESERV2-DYNLIST
         CLC   $RESERV2,=CL44' '
         BNE   QQ1
         SPACE 1
* DSVOLREF ************************
         SPACE 1
         LA    R7,DSVOLREF-DYNLIST
         LA    R14,DSVOLREF
         LA    R15,L'DSVOLREF
         BAL   R1,PP1              GET DSVOLREF LENGTH.
         LTR   R15,R15
         BNP   AA10
         CLC   DSVOLSER,=CL44' '   CHECK TO SEE THAT DSVOLREF DOES NOT
         BE    *+L'*+10                 CONFLICT WITH DSVOLSER.
         CLC   DSVOLSER,=XL44'0'
         BNE   QQ1
         LA    R1,DALVLRDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLREF.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSFREE **************************
         SPACE 1
AA10     LA    R7,DSFREE-DYNLIST
         LA    R14,DSFREE
         LA    R15,L'DSFREE
         BAL   R1,PP1              GET DSFREE LENGTH.
         LTR   R15,R15
         BNP   AA10A
         CLC   DSFREE,=CL8'END   '
         BE    AA10A
         CLC   DSFREE,=CL8'CLOSE '
         BNE   QQ1
         LA    R1,DALCLOSE         S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY              CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
AA10A    CLC   DSNAME,=CL44'NULLFILE '  CHECK FOR DUMMY ALLOCATION.
         BE    CC1                 GO DO DCB PARAMETERS.
         SPACE 1
* DSLABEL *************************
         SPACE 1
         LA    R7,DSLABEL-DYNLIST
         LA    R14,DSLABEL
         LA    R15,L'DSLABEL
         BAL   R1,PP1              GET DSLABEL LENGTH.
         LTR   R15,R15
         BNP   AA11
         LA    R1,DALLABEL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSLABEL,=CL4'SL  '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'02'
         B     *+L'*+14
         CLC   DSLABEL,=CL4'SUL '
         BNE   QQ1
         MVI   S99TUPAR,X'08'
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSINOUT *************************
         SPACE 1
AA11     LA    R7,DSINOUT-DYNLIST
         LA    R14,DSINOUT
         LA    R15,L'DSINOUT
         BAL   R1,PP1              GET DSINOUT LENGTH.
         LTR   R15,R15
         BNP   AA12
         LA    R1,DALINOUT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSINOUT,=CL4'IN  '
         BNE   *+L'*+8
         MVI   S99TUPAR,X'80'
         B     *+L'*+14
         CLC   DSINOUT,=CL4'OUT '
         BNE   QQ1
         MVI   S99TUPAR,X'40'
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* $RESERV3 ************************
         SPACE 1
AA12     LA    R7,$RESERV3-DYNLIST
         CLC   $RESERV3,=CL44' '
         BNE   QQ1
         SPACE 5
         CLC   DSSTATUS,=CL44' '   CHECK FOR NEW ALLOCATION.
         BE    BB1
         CLC   DSSTATUS,=CL8'NEW '
         BNE   CC1                 GO DO DCB PARAMETERS.
         EJECT
* DSPWDLBL ************************
         SPACE 1
BB1      LA    R7,DSPWDLBL-DYNLIST
         LA    R14,DSPWDLBL
         LA    R15,L'DSPWDLBL
         BAL   R1,PP1              GET DSPWDLBL LENGTH.
         LTR   R15,R15
         BNP   BB2
         LA    R1,DALPASPR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSPWDLBL,=CL8'PASSWORD'
         BNE   *+L'*+8
         MVI   S99TUPAR,X'10'
         B     *+L'*+14
         CLC   DSPWDLBL,=CL8'NOPWREAD'
         BNE   QQ1
         MVI   S99TUPAR,X'30'
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDATE **************************
         SPACE 1
BB2      LA    R7,DSDATE-DYNLIST
         LA    R14,DSDATE
         LA    R15,L'DSDATE
         BAL   R1,PP1              GET DSDATE LENGTH.
         LTR   R15,R15
         BNP   BB3
         CLC   DSDATE(6),=C'EXPDT='     CHECK FOR DATE TYPE.
         BNE   BB2A
         SH    R15,=H'6'
         CH    R15,=H'5'           CHECK FOR VALID LENGTH.
         BNE   QQ1
         LA    R14,6(0,R14)        BUMP POINTER FOR MVCPARM.
         LA    R1,DALEXPDT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE EXPDT DATE.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         B     BB3
BB2A     CLC   DSDATE(6),=C'RETPD='
         BNE   QQ1                 ERROR.
         SH    R15,=H'6'
         BNP   QQ1
         CH    R15,=H'4'
         BH    QQ1
         LA    R14,6(0,R14)        BUMP POINTER TO START OF NUMBER.
         BAL   R1,PP2              EDIT AND CONVERT RETPD.
         LA    R1,DALRETPD
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR        PLACE RETPD VALUE IN TEXT UNIT.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSALLOC *************************
         SPACE 1
BB3      LA    R7,DSALLOC-DYNLIST
         LA    R14,DSALLOC
         LA    R15,L'DSALLOC
         BAL   R1,PP1              GET DSALLOC LENGTH.
         LTR   R15,R15
         BNP   QQ1                 ERROR.
         CLC   DSALLOC,=CL5'TRK '  CHECK FOR TRACK ALLOCATION.
         BNE   BB3A
         LA    R1,DALTRK           S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY              CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     BB4
BB3A     CLC   DSALLOC,=CL5'CYL '  CHECK FOR CYLINDER ALLOCATION.
         BNE   BB3B
         LA    R1,DALCYL           S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY              CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     BB4
BB3B     BAL   R1,PP2              CHECK FOR AVERAGE BLOCK ALLOCATION.
         LA    R1,DALBLKLN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSPRI ***************************
         SPACE 1
BB4      LA    R7,DSPRI-DYNLIST
         LA    R14,DSPRI
         LA    R15,L'DSPRI
         BAL   R1,PP1              GET DSPRI LENGTH.
         LTR   R15,R15
         BNP   QQ1
         BAL   R1,PP2              CHECK FOR PRIMARY QUANTITY.
         LA    R1,DALPRIME
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSSEC ***************************
         SPACE 1
         LA    R7,DSSEC-DYNLIST
         LA    R14,DSSEC
         LA    R15,L'DSSEC
         BAL   R1,PP1              GET DSSEC LENGTH.
         LTR   R15,R15
         BNP   BB5
         BAL   R1,PP2              CHECK FOR SECONDARY QUANTITY.
         LA    R1,DALSECND
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSDIR ***************************
         SPACE 1
BB5      LA    R7,DSDIR-DYNLIST
         LA    R14,DSDIR
         LA    R15,L'DSDIR
         BAL   R1,PP1              GET DSDIR LENGTH.
         LTR   R15,R15
         BNP   BB6
         BAL   R1,PP2              CHECK FOR DIRECTORY QUANTITY.
         LA    R1,DALDIR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSRLSE **************************
         SPACE 1
BB6      LA    R7,DSRLSE-DYNLIST
         LA    R14,DSRLSE
         LA    R15,L'DSRLSE
         BAL   R1,PP1              GET DSRLSE LENGTH.
         LTR   R15,R15
         BNP   BB7
         CLC   DSRLSE,=CL8'RLSE '
         BNE   QQ1
         LA    R1,DALRLSE          S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY              CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* DSCONTIG ************************
         SPACE 1
BB7      LA    R7,DSCONTIG-DYNLIST
         LA    R14,DSCONTIG
         LA    R15,L'DSCONTIG
         BAL   R1,PP1              GET DSCONTIG LENGTH.
         LTR   R15,R15
         BNP   BB8
         LA    R1,DALSPFRM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSCONTIG,=CL8'CONTIG '
         BNE   QQ1
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSROUND *************************
         SPACE 1
BB8      LA    R7,DSROUND-DYNLIST
         LA    R14,DSROUND
         LA    R15,L'DSROUND
         BAL   R1,PP1              GET DSROUND LENGTH.
         LTR   R15,R15
         BNP   BB9
         CLC   DSROUND,=CL8'ROUND '
         BNE   QQ1
         LA    R1,DALROUND         S99TUNUM SET TO X'0000' BY WORKAREA
         STH   R1,S99TUKEY              CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* $RESERV4 ************************
         SPACE 1
BB9      LA    R7,$RESERV4-DYNLIST
         CLC   $RESERV4,=CL44' '
         BNE   QQ1
         EJECT
* DSBLKSI *************************
         SPACE 1
CC1      LA    R7,DSBLKSI-DYNLIST
         LA    R14,DSBLKSI
         LA    R15,L'DSBLKSI
         BAL   R1,PP1              GET DSBLKSI LENGTH.
         LTR   R15,R15
         BNP   CC2
         BAL   R1,PP2              CHECK FOR BLKSIZE VALUE.
         CH    R15,=H'32767'       CHECK FOR MAXIMUM BLOCKSIZE.
         BH    QQ1                 ERROR.
         LA    R1,DALBLKSZ
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSORG ***************************
         SPACE 1
CC2      LA    R7,DSORG-DYNLIST
         CLC   DSORG,=XL44'0'
         BNE   CC2A
         LA    R1,DALRTORG         SET UP TO HAVE DSORG RETURNED
         STH   R1,S99TUKEY              BACK TO US.
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         MVC   S99TUPAR(2),=XL44'0'
         LA    R1,S99TUPAR
         ST    R1,WKRTDSRG         SAVE ADDRESS OF PARAMETER FIELD.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         B     CC3
CC2A     LA    R14,DSORG
         LA    R15,L'DSORG
         BAL   R1,PP1              GET DSORG LENGTH.
         LTR   R15,R15
         BNP   CC3
         LA    R1,DALDSORG
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSORG,=CL8'VSAM '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(0,DCBACBM)
         B     CC2B
         CLC   DSORG,=CL8'PO   '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DCBDSGPO,0)
         B     CC2B
         CLC   DSORG,=CL8'POU  '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DCBDSGPO+DCBDSGU,0)
         B     CC2B
         CLC   DSORG,=CL8'DA   '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DCBDSGDA,0)
         B     CC2B
         CLC   DSORG,=CL8'DAU  '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DCBDSGDA+DCBDSGU,0)
         B     CC2B
         CLC   DSORG,=CL8'PS   '
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL1(DCBDSGPS,0)
         B     CC2B
         CLC   DSORG,=CL8'PSU  '
         BNE   QQ1                 ERROR.
         MVC   S99TUPAR(2),=AL1(DCBDSGPS+DCBDSGU,0)
CC2B     ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSKEYLEN ************************
         SPACE 1
CC3      LA    R7,DSKEYLEN-DYNLIST
         LA    R14,DSKEYLEN
         LA    R15,L'DSKEYLEN
         BAL   R1,PP1              GET DSKEYLEN LENGTH.
         LTR   R15,R15
         BNP   CC4
         BAL   R1,PP2              CHECK FOR KEYLENGTH QUANTITY.
         CH    R15,=H'255'         CHECK FOR MAX KEYLENGTH.
         BH    QQ1                 ERROR.
         LA    R1,DALKYLEN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         STC   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSLRECL *************************
         SPACE 1
CC4      LA    R7,DSLRECL-DYNLIST
         LA    R14,DSLRECL
         LA    R15,L'DSLRECL
         BAL   R1,PP1              GET DSLRECL LENGTH.
         LTR   R15,R15
         BNP   CC5
         LA    R1,DALLRECL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSLRECL,=CL5'X '    CHECK FOR SPANNED RECORDS.
         BNE   *+L'*+10
         MVC   S99TUPAR(2),=AL2(32768)
         B     *+L'*+16
         BAL   R1,PP2              CHECK FOR LRECL QUANTITY.
         CH    R15,=H'32767'
         BH    QQ1
         STH   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSRECFM1 THRU DSRECFM8 **********
         SPACE 1
CC5      LA    R7,DSRECFM1-DYNLIST
         CLC   DSRECFM,=CL44' '    CHECK FOR NO INFORMATION.
         BE    CC6
         LA    R1,DALRECFM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSRECFM1,C' '
         BE    CC5A
         CLI   DSRECFM1,C'V'
         BNE   *+L'*+8
         OI    S99TUPAR,DCBRECV
         B     CC5A
         CLI   DSRECFM1,C'F'
         BNE   *+L'*+8
         OI    S99TUPAR,DCBRECF
         B     CC5A
         CLI   DSRECFM1,C'U'
         BNE   QQ1                 ERROR.
         OI    S99TUPAR,DCBRECU
CC5A     LA    R7,DSRECFM2-DYNLIST
         CLI   DSRECFM2,C' '
         BE    *+L'*+12
         CLI   DSRECFM2,C'B'
         BNE   QQ1                 ERROR.
         OI    S99TUPAR,DCBRECBR
         LA    R7,DSRECFM3-DYNLIST
         CLI   DSRECFM3,C' '
         BE    *+L'*+12
         CLI   DSRECFM3,C'S'
         BNE   QQ1                 ERROR.
         OI    S99TUPAR,DCBRECSB
         LA    R7,DSRECFM4-DYNLIST
         CLI   DSRECFM4,C' '
         BE    *+L'*+12
         CLI   DSRECFM4,C'T'
         BNE   QQ1                 ERROR.
         OI    S99TUPAR,DCBRECTO
         LA    R7,DSRECFM5-DYNLIST
         CLI   DSRECFM5,C' '
         BE    CC5B
         CLI   DSRECFM5,C'M'
         BNE   *+L'*+8
         OI    S99TUPAR,DCBRECCM
         B     CC5B
         CLI   DSRECFM5,C'A'
         BNE   QQ1                 ERROR.
         OI    S99TUPAR,DCBRECCA
CC5B     LA    R7,DSRECFM6-DYNLIST
         CLI   DSRECFM6,C' '
         BNE   QQ1                 ERROR.
         LA    R7,DSRECFM7-DYNLIST
         CLI   DSRECFM7,C' '
         BNE   QQ1                 ERROR.
         LA    R7,DSRECFM8-DYNLIST
         CLI   DSRECFM8,C' '
         BNE   QQ1                 ERROR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDCBDS *************************
         SPACE 1
CC6      LA    R7,DSDCBDS-DYNLIST
         LA    R14,DSDCBDS
         LA    R15,L'DSDCBDS
         BAL   R1,PP1              GET DSDCBDS LENGTH.
         LTR   R15,R15
         BNP   CC7
         LA    R1,DALDCBDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV5 ************************
         SPACE 1
CC7      LA    R7,$RESERV5-DYNLIST
         CLC   $RESERV5,=CL44' '
         BNE   QQ1
         EJECT
MM1      SH    R8,=H'4'            BACK UP TO LAST USED TEXT POINTER.
         OI    0(R8),X'80'         INDICATE END OF LIST.
         LA    R1,REQBKPTR         GET THE BEGINNING OF THIS MESS.
        DYNALLOC
         L     R0,S99RSC           PICK UP REASON CODES.
         LTR   R15,R15             CHECK FOR ERRORS.
         BNZ   ZZ1
         L     R1,WKRTNVOL         CHECK FOR VOLSER RETURN REQUEST.
         LTR   R1,R1
         BZ    *+L'*+6
         MVC   DSVOLSER,0(R1)      GIVE CALLER INFO.
         L     R1,WKRTDSRG         CHECK FOR DSORG RETURN REQUEST.
         LTR   R1,R1
         BZ    ZZ1
         CLC   0(2,R1),=AL1(0,DCBACBM)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'VSAM '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGPO,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'PO   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGPO+DCBDSGU,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'POU  '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGDA,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'DA   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGDA+DCBDSGU,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'DAU  '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGPS,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'PS   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGPS+DCBDSGU,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'PSU  '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGIS,0)
         BNE   *+L'*+10
         MVC   DSORG,=CL8'IS   '
         B     ZZ1
         CLC   0(2,R1),=AL1(DCBDSGIS+DCBDSGU,0)
         BNE   ZZ1
         MVC   DSORG,=CL8'ISU  '
         B     ZZ1
         EJECT
QQ1      LR    R0,R7               SET UP RETURN CODES.
         LA    R15,15
         SPACE 2
ZZ1      L     R3,4(R13)           PREPARE TO RETURN.
         LTR   R10,R10             CHECK TO SEE IF PARAMETER LIST IS
         BNP   ZZ1A                     ONLY ENTRY IN CALLER'S ADDRESS
         L     R2,24(0,R3)              LIST.
         L     R2,4(0,R2)          GET SECOND ADDRESS IN LIST.
         LA    R2,0(R2)
         LTR   R2,R2
         BZ    ZZ1A
         ST    R0,0(0,R2)          PUT REGISTERS INFORMATION IN LIST.
         ST    R15,4(0,R2)
ZZ1A     ST    R0,20(R3)           SET ERROR AND INFORMATIONAL R.C.
        $XRET  CC=(R15),LV=WRKL,TYPE=RENT
         EJECT
*        ROUTINE TO DETERMINE LENGTH OF NON-BLANK CHARACTERS IN FIELD.
*
*        AT ENTRY : R1 CONTAINS THE RETURN ADDRESS.
*                   R14 CONTAINS THE FIELD ADDRESS.
*                   R15 CONTAINS THE LENGTH OF FIELD.
*
*        AT EXIT  : R15 WILL BE ALTERED TO CONTAIN LENGTH OF
*                   NON-BLANK CHARACTERS IN FIELD.
         SPACE 1
PP1      LTR   R15,R15
         BNPR  R1
         ALR   R15,R14
PP1A     BCTR  R15,0
         CLI   0(R15),C' '
         BNE   PP1B
         CLR   R15,R14
         BH    PP1A
         SR    R15,R15
         BR    R1
PP1B     SLR   R15,R14
         LA    R15,1(0,R15)
         BR    R1
         EJECT
*        ROUTINE TO EDIT EBCDIC NUMERIC DATA AND, IF VALID, TO
*        CONVERT IT TO BINARY.
*
*        AT ENTRY : R1 CONTAINS THE RETURN ADDRESS.
*                   R14 CONTAINS DATA ADDRESS.
*                   R15 CONTAINS LENGTH OF DATA.
*
*        AT EXIT  : R15 WILL BE ALTERED TO CONTAIN THE BINARY
*                   EQUIVALENT OF THE NUMBER.
         SPACE 1
PP2      LTR   R15,R15
         BNPR  R1
         ST    R15,DBLWORD
         ALR   R15,R14
PP2A     BCTR  R15,0
         CLI   0(R15),C'0'
         BL    QQ1                     AN ABNORMAL EXIT WILL BE TAKEN
         CLI   0(R15),C'9'             TO THE COMMON ERROR ROUTINE IF
         BH    QQ1                     DATA IS NOT NUMERIC.
         CLR   R15,R14
         BH    PP2A
         L     R15,DBLWORD
         BCTR  R15,0
         EX    R15,PACKNUM
         CVB   R15,DBLWORD
         BR    R1
         SPACE 2
*        SOME EXECUTED INSTRUCTIONS.
         SPACE 1
MVCPARM  MVC   S99TUPAR(0),0(R14)
PACKNUM  PACK  DBLWORD,0(0,R14)
         SPACE 2
*        CONSTANTS.
         SPACE 1
WORKLEN  DC    A(WRKL)
TXPTRSPC DC    A(4*40)             RESERVE SPACE FOR 40 TEXT POINTERS.
         EJECT
        LTORG
         SPACE 1
         END
