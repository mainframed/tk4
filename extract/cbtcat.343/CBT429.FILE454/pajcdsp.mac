PROC     0 DDSN DVOL PCLASS PCOPIES PFOLD PHOLD PDEST PTEST()
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN EXIT
/*-------------------------------------------------------------------*/
/* PRE-REQUISITE : "PRINTO" TSO COMMAND (AVAILABLE IN CBT FILES      */
/*                 433 AND 437).                                 (PM)*/
/*-------------------------------------------------------------------*/
         ISPEXEC VGET (PRJO LIBO TYPO DDSN DVOL MBRO +
                       PCLASS PCOPIES PHEAD PFOLD PHOLD PDEST)
         IF &PTEST EQ YES THEN CONTROL LIST SYMLIST CONLIST MSG
         ELSE CONTROL MSG
         SET &TBPDEL =
         SET &PVSN =
         SET &PDST =
         IF &DVOL NE THEN SET &PVSN = &STR(VOLUME(&DVOL))
         IF &PHEAD EQ YES THEN SET PHDN = &STR(HEADING)
         ELSE SET PHDN = &STR(NOHEADING)
         IF &PDEST NE THEN SET &PDST = &STR(DEST(&PDEST))
         IF &DDSN NE THEN DO
           IF &SUBSTR(1:1,&DDSN) NE &STR(') THEN DO
             SET &PDSN = &STR(&DDSN)
             END
           ELSE DO
             SET &LDSN = &LENGTH(&STR(&DDSN)) - 1
             SET &PDSN = &SUBSTR(2:&LDSN,&DDSN)
             END
           SET &LDSN = &LENGTH(&STR(&PDSN))
           IF &SUBSTR(&LDSN:&LDSN,&PDSN) EQ ) THEN GOTO PRT1
           GOTO MBR1
           END
         SET &PDSN = &STR(&PRJO..&LIBO..&TYPO)
MBR1:    IF &STR(&MBRO) EQ THEN GOTO STB1
         IF &STR(&MBRO) EQ &STR(*) THEN GOTO PRT1
         SET &PDSN = &STR(&PDSN.(&MBRO))
PRT1:    PRINTO '&PDSN' &PVSN CLASS(&PCLASS) +
                COPIES(&PCOPIES) &PHDN &PFOLD &PHOLD &PDST
         GOTO XTF1
STB1:    ISPEXEC CONTROL ERRORS CANCEL
         ISPEXEC LMINIT DATAID(DSID) DATASET('&PDSN') +
                 &PVSN ORG(DSOR)
         IF &DSOR NE PO THEN DO
           ISPEXEC LMFREE DATAID(&DSID)
           IF &LASTCC EQ 0 THEN GOTO PRT1
           WRITE -- INPUT DATA-SET FREE FAILED
           WRITE -- ERROR : &ZERRLM
           GOTO PRT1
           END
         IF &LASTCC NE 0 THEN DO
           WRITE -- INPUT DATA-SET ALLOCATION FAILED
           WRITE -- ERROR : &ZERRLM
           GOTO XTF1
           END
         ISPEXEC LMOPEN DATAID(&DSID)
         IF &LASTCC NE 0 THEN DO
           WRITE -- INPUT DATA-SET OPEN FAILED
           WRITE -- ERROR : &ZERRLM
           GOTO XTF1
           END
         ISPEXEC TBCREATE MEMTABLE REPLACE NOWRITE +
                 KEYS(MEMBER) NAMES(STATS)
         SET &LMLRC = 0
         DO WHILE &LMLRC = 0
           ISPEXEC LMMLIST DATAID(&DSID) MEMBER(MEMBER) +
                   OPTION(LIST) STATS(YES)
           SET &LMLRC = &LASTCC
           IF &LMLRC EQ 0 THEN DO
             SET &STATS = &STR(&ZLVERS..&ZLMOD  &ZLCDATE +
                               &ZLMDATE &ZLMTIME &ZLCNORC +
                               &ZLINORC &ZLMNORC &ZLUSER)
             ISPEXEC TBADD MEMTABLE
             END
           ELSE IF &LMLRC NE 8 THEN DO
             WRITE -- TABLE CREATION FAILED. RC=&LMLRC
             WRITE -- ERROR : &ZERRLM
             END
           END
         ISPEXEC LMMLIST DATAID(&DSID) OPTION(FREE)
         ISPEXEC LMCLOSE DATAID(&DSID)
         IF &LASTCC NE 0 THEN DO
           WRITE -- INPUT DATA-SET CLOSE FAILED
           WRITE -- ERROR : &ZERRLM
           END
         ISPEXEC LMFREE DATAID(&DSID)
         IF &LASTCC NE 0 THEN DO
           WRITE -- INPUT DATA-SET FREE FAILED
           WRITE -- ERROR : &ZERRLM
           END
         IF &LMLRC NE 8 THEN GOTO XTB1
         ISPEXEC TBTOP MEMTABLE
         IF &PTEST EQ ONLY THEN CONTROL LIST SYMLIST CONLIST MSG
LTB1:    ISPEXEC TBDISPL MEMTABLE PANEL(PAJDSPL) POSITION(ROW)
LTB2:    SET &TBDRC = &LASTCC
         IF &TBCMD EQ THEN GOTO PTB1
         IF &TBCMD NE D THEN GOTO LTB3
         CONTROL NOMSG
         ERROR DO
           ERROR OFF
           FREE ATTR(VBA)
           ATTR VBA RECFM(V B A) LRECL(137) BLKSIZE(6356) DSORG(PS)
           ALLOC F(ISPFILE) DSN(DIRTEMP.LISTING) REUSE CATALOG +
             USING(VBA) SP(2 2) TRACKS
           SET &TBPDEL = DELETE
           END
         ALLOC F(ISPFILE) DSN(DIRTEMP.LISTING) OLD REUSE
         ERROR OFF
         CONTROL MSG
         ISPEXEC FTOPEN
         ISPEXEC FTINCL PAJSDSP
         ISPEXEC FTCLOSE
         PRINTO DIRTEMP.LISTING CLASS(&PCLASS) +
                COPIES(&PCOPIES) &PHDN &PFOLD &PHOLD &PDST
         SET &TBCMD =
         SET &TBMNM =
         GOTO LTB1
LTB3:    IF &TBMNM EQ THEN GOTO CML5
         SET &SFL = 1
         SET &LFL = &LENGTH(&STR(&TBMNM))
CML1:    IF &SUBSTR(&SFL:&SFL,&TBMNM) NE &STR( ) THEN GOTO CML2
         IF &SFL EQ &LFL THEN GOTO CML5
         SET &SFL = &SFL + 1
         GOTO CML1
CML2:    SET &EFL = &SFL
CML3:    IF &EFL EQ &LFL THEN GOTO CML4
         SET &EFL = &EFL + 1
         IF &SUBSTR(&EFL:&EFL,&TBMNM) NE &STR( ) THEN GOTO CML3
         SET &EFL = &EFL - 1
CML4:    SET &STMEM = &SUBSTR(&SFL:&EFL,&TBMNM)
         SET &LFL = &LENGTH(&STR(&STMEM))
         IF &LFL GT 8 THEN DO
           ISPEXEC TBDISPL MEMTABLE MSG(PAJX000D) POSITION(ROW)
           GOTO LTB2
           END
         SET &MEMBER = &STMEM
         SET &TBCMD =
         SET &TBMNM =
         ISPEXEC TBTOP MEMTABLE
         ISPEXEC TBSCAN MEMTABLE ARGLIST(MEMBER) NOREAD +
                 POSITION(ROW) CONDLIST(GE)
         IF &LASTCC EQ 8 THEN -
           ISPEXEC TBBOTTOM MEMTABLE NOREAD POSITION(ROW)
         ELSE IF &ROW NE 0 THEN -
           ISPEXEC TBSKIP MEMTABLE NUMBER(-1) NOREAD POSITION(ROW)
         GOTO LTB1
CML5:    ISPEXEC TBDISPL MEMTABLE MSG(PAJX000C) POSITION(ROW)
         GOTO LTB2
PTB1:    SET &DONE = NONE
         DO WHILE &DONE = NONE
           IF &TBDRC GT 4 THEN SET &DONE = ERROR
           ELSE IF &TBCMD NE THEN SET &DONE = LOCATE
           ELSE IF &S EQ S THEN DO
             SET &TBDRC = 0
             SET &DONE = PRINT
             END
           ELSE IF &S EQ B THEN DO
             SET &TBDRC = 0
             SET &DONE = BROWSE
             END
           ELSE IF &S EQ &STR( ) THEN DO
             ISPEXEC TBDISPL MEMTABLE POSITION(ROW)
             SET &TBDRC = &LASTCC
             END
           ELSE DO
             ISPEXEC TBDISPL MEMTABLE MSG(PAJX000B) POSITION(ROW) +
                     CSRROW(&ROW)
             SET &TBDRC = &LASTCC
             END
           END
         IF &TBDRC EQ 0 THEN DO
           IF &DONE EQ LOCATE THEN GOTO LTB3
           SET &MBRO = &MEMBER
           SET &XDSN = &STR(&PDSN.(&MBRO))
           IF &DONE EQ BROWSE THEN -
             ISPEXEC BROWSE DATASET('&XDSN') &PVSN
           ELSE PRINTO '&XDSN' &PVSN CLASS(&PCLASS) +
                       COPIES(&PCOPIES) &PHDN &PFOLD &PHOLD &PDST
           GOTO LTB1
           END
         IF &TBDRC NE 8 THEN DO
           WRITE -- EXIT CODE : &DONE = &TBDRC
           END
XTB1:    ISPEXEC TBEND MEMTABLE
XTF1:    IF &TBPDEL EQ DELETE THEN FREE F(ISPFILE) ATTR(VBA) &TBPDEL
