TESR     TITLE 'TERMINAL ERASE SCREEN SUBROUTINE/TSO COMMAND.'
ERASE    START 0
         SPACE 1
* PURPOSE :    ERASE THE SCREEN OF A TERMINAL AND SET CURSOR AT THE
* ---------    TOP (ROW 1 - COLUMN 1).
*
* CALLING SEQUENCE : CALL ERASE
* ------------------
*
* AUTHOR :     RAVARANI N. - MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12,TYPE=RENT
        GTSIZE
         LTR   R4,R0               IS IT A SCREEN TERMINAL?
         BZ    EXIT                NO, BYPASS
         USING PSA,R0              WE HAVE TO GET IT FROM ASXB
         L     R3,PSAAOLD          GET OUR ASCB ADDRESS
         DROP  R0
         USING ASCB,R3
         L     R3,ASCBASXB         GET ASXB ADDRESS
         DROP  R3
         USING ASXB,R3
         L     R2,ASXBMPST         VTAM MEMORY PROCESS SCHED. TABLE
         DROP  R3
         LTR   R2,R2               CHECK IF VTAM OR TCAM?
         BZ    TCAM                ADDRESS DOESN'T EXIST : TCAM
         CLM   R4,B'0001',SZ24     NORMAL SCREEN SIZE?
         BNE   ALTSZ               NO
         LA    R0,CLERNL           NORMAL SIZE
         LA    R1,CLERN            NORMAL CLEAR
         B     SEND
ALTSZ    LA    R0,CLERAL           ALTERNATE SIZE
         LA    R1,CLERA            ALTERNATE CLEAR
SEND     ICM   R1,B'1000',FUSCR
        TPUT   (1),(0),R
        STLINENO LINE=1
        STTMPMD OFF
         B     EXIT
TCAM    TPUT   ERBUF,ERBUFL,ASIS   ERASE SCREEN AND SET CURSOR
EXIT    $XRET  TYPE=RENT
         EJECT
*        CONSTANTS AND WORK AREAS.
         SPACE 1
ERBUF   $FS    WCC=CLEAR,SF=IC,MF=L
ERBUFL   EQU   *-ERBUF
CLERN   $FS    CC=EW,WCC=(RMDT),SBA=(1,1),SF=IC,MF=L
CLERNL   EQU   *-CLERN
CLERA   $FS    CC=EWA,WCC=(RMDT),SBA=(1,1),SF=IC,MF=L
CLERAL   EQU   *-CLERA
SZ24     DC    AL1(24)             24 LINES SCREEN
FUSCR    DC    XL1'03'
         SPACE 1
        PRINT  NOGEN
        IHAPSA
        IHAASCB
        IHAASXB
         SPACE 2
         END
