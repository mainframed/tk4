         MACRO           DO'NT SWAP REQUEST TO SRM.
&NAME   $NOSWAP     &A
         LCLC  &L
&L       SETC  '&NAME'
.*
.* $NOSWAP MACRO : ISSUED TO NOTIFY SRM THAT THE ISSUING ADDRESS
.* --------------- SPACE MUST NOT BE SWAPPED OUT UNTIL A $OKSWAP
.*                 HAS OCCURED.
.*
.* OPERAND - OMITTED : SYSEVENT CODE AND ASID ARE ASSUMED IN THE
.*                 REGISTER 0.
.*           ADDRESS : THE ADDRESS OF THE ASID (HALFWORD).
.*           (REGISTER) : THE INDICATED REGISTER CONTAINS THE ASID
.*                 (BITS 16-31).
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   (N'&SYSLIST EQ 0).NL
         AIF   (T'&A EQ 'O').NL
         AIF   ('&A' EQ '').NL
         AIF   ('&A' EQ '(0)').ST
         AIF   ('&A'(1,1) EQ '(').SR
&L       LA    0,X'29'             SET SYSEVENT CODE.
&L       SETC  ''
         ICM   0,B'1100',&A        INSERT THE ASID.
         AGO   .NL
.SR      ANOP
&L       LA    0,0(&A(1))          SET THE ASID.
&L       SETC  ''
.ST      ANOP
&L       SLL   0,16                SHIFT INTO PROPER POSITION.
&L       SETC  ''
         LA    1,X'29'             SYSEVENT CODE.
         OR    0,1                 PUT CODE INTO.
.NL      ANOP
&L       SR    1,1                 CLEAR SRM RETURNS INFORMATION.
         SVC   95                  NOTIFY SRM.
         MEXIT
.BAD     MNOTE 8,' TOO MANY OPERANDS ******************************** '
         MEND
