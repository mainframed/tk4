VTTM     TITLE 'TEST TCAM OR VTAM RUNNING SUBROUTINE.'
VTTCAM   START 0
         SPACE 1
* CALLING SEQUENCE : CALL VTTCAM
* ------------------
*
*        AT EXIT, REGISTER 15 IS SET AS BELOW :
*              ZERO (0) IF RUNNING UNDER TCAM,
*              ONE (1) IF RUNNING UNDER VTAM.
*
* AUTHOR :     N. RAVARANI
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R11,TYPE=RENT
         EJECT
         USING PSA,R0              WE HAVE TO GET IT FROM ASXB.
         L     R3,PSAAOLD          GET OUR ASCB ADDRESS.
         DROP  R0
         USING ASCB,R3
         L     R3,ASCBASXB         GET ASXB ADDRESS.
         DROP  R3
         USING ASXB,R3
         XR    R15,R15             SET ZERO.
         L     R2,ASXBMPST         VTAM MEMORY PROCESS SCHED. TABLE.
         DROP  R3
         LTR   R2,R2
         BZ    EXIT                ADDRESS DOESN'T EXIST : TCAM.
         LA    R15,1               ADDRESS EXIST : VTAM, SET ONE.
EXIT    $XRET  TYPE=RENT,CC=(R15)
         SPACE 1
        PRINT  NOGEN
        IHAPSA
        IHAASCB
        IHAASXB
         SPACE 2
         END
