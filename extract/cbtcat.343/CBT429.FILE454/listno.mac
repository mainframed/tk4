LNMP     TITLE 'LIST MANUAL''S MEMBERS.'
LISTNO   START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *        LIST MANUAL'S MEMBERS        * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* AUTHOR :     P.A. MOINIL                                            *
* --------     COMPUTING CENTRE                                       *
*              J.R.C. - ISPRA ESTABLISHMENT                           *
*              21020 ISPRA (VA), ITALY                                *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
* PURPOSE :    PRINT ENTIRELY A SELECTED MANUAL OR ONLY THE PAGES     *
* ---------    UPDATED STARTING FROM A SPECIFIED DATE.                *
*                                                                     *
* CALL OF THE PROGRAM :                                               *
* ---------------------                                               *
*                                                                     *
*        //NOTES  EXEC  PGM=LISTNO,PARM='...'                         *
*        //SYSPRINT DD  SYSOUT=A                                      *
*        //SYSPDS   DD  ... NOTES DATA-SET (DSORG=PO) ...             *
*        //SYSIN    DD  ... REQUESTS CARDS  ...                       *
*                                                                     *
* PARM. FIELD DESCRIPTION :                                           *
* -------------------------                                           *
*                                                                     *
*        MANUAL=... - THE NAME (1 TO 6 ALPHAMERICS) OF THE MANUAL     *
*        M=...        MAIN MEMBER, CONTAINING THE TITLE AND THE       *
*                     LIST OF ALL THE MEMBERS COMPONENT OF THIS       *
*                     MANUAL. THE NAME GIVEN BY THIS PARAMETER IS     *
*                     SURROUNDED BY THE CHARACTER # TO FORM THE       *
*                     MANUAL MAIN MEMBER NAME. IF OMITTED, THE        *
*                     DEFAULT NAME USED IS '#MANUAL#'.                *
*                                                                     *
*        DATE=...   - THE DATE OF THE LAST TIME THE MANUAL OR THE     *
*        D=...        UPDATED PAGES HAS BEEN REQUESTED. THE FORMAT    *
*                     IS DDMMYY, DD.MM.YY OR DD/MM/YY, WHERE DD IS    *
*                                                                     *
*        LIST=...   - THE NAME (1 TO 8 ALPHAMERICS) OF A SPECIFIC     *
*        L=...        MEMBER (NO DEFAULT SUPPLIED). THIS WAY OF USE   *
*                     EXCLUDE ANY OTHER SELECTION TYPE (MANUAL=...,   *
*                     DATE=... OR REQUESTS CARDS).                    *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
* REQUESTS CARDS (SYSIN) :                                            *
* ------------------------                                            *
*                                                                     *
*        THE REQUESTS CARDS ARE FREE FORMAT CODED FROM COLUMN 1 TO    *
*        72 INCLUDED, AND EVERY REQUEST MUST BE SEPARATED FROM EACH   *
*        OTHER BY AT LEAST ONE BLANK. A REQUEST IS A MANUAL NAME      *
*        (1 TO 6 ALPHAMERICS), EVENTUALLY FOLLOWED BY A DATE (FORMAT  *
*        MAY BE DDMMYY, DD.MM.YY OR DD/MM/YY) TO OBTAIN THE UPDATED   *
*        PAGES OF THE MANUAL.                                         *
*        NEVER A MANUAL NAME OR A DATE MUST BE SPLITTED BETWEEN TWO   *
*        SUCCESSIVE CARDS, BUT I.E., A MANUAL NAME MAY BE IN A CARD   *
*        AND THE CORRESPONDING SELECTION DATE IN THE NEXT.            *
*                                                                     *
* MEMBERS CONTROL CARDS :                                             *
* -----------------------                                             *
*                                                                     *
*        1. PAGE CONTROL CARDS : ALL MEMBERS.                         *
*                                                                     *
*              COL.  1 -  4 : =/EJ OR 1 FOLLOWED BY THREE BLANKS.     *
*              COL.  5 - 12 : THE DATE OF THE PAGE CREATION OR        *
*                             UPDATE (FORMAT MAY BE : DD/MM/YY,       *
*                             DD.MM.YY OR DDMMYY).                    *
*                      NOTE : THIS DATE IS CONTROLED WHEN 'DATE=...'  *
*                             IS SPECIFIED IN THE PARAMETERS FIELD.   *
*                             IT IS NEVER CHECKED FOR THE FIRST PAGE  *
*                             OF THE MANUAL MAIN MEMBER.              *
*                                                                     *
*        2. SELECTION CONTROL CARDS : ONLY IN THE MANUAL MAIN MEMBER. *
*                                                                     *
*              COL.  1 -  4 : =/MB                                    *
*              COL. 10 - 17 : THE NAME OF A MEMBER PART OF THIS       *
*                             MANUAL.                                 *
*              COL. 20 - 72 : MEMBER TITLE.                           *
*                      NOTE : THIS CARD IS ALWAYS PRINTED UNLESS      *
*                             ENCOUNTERED IN THE FIRST PAGE OF THE    *
*                             MANUAL MAIN MEMBER.                     *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         EJECT
        $DEFREG
        $XENT  BASE=R12
         XR    R10,R10             SET C.C. = 0.
         EJECT
*        ANALYZE PARM. FIELD REQUEST IF ANY.
         SPACE 1
         L     R1,0(R1)            PARM. FIELD ADDRESS.
         LH    R3,0(R1)
         LTR   R3,R3
         BNP   ENDPARM
         LA    R2,2(R1)            SCAN PARM. FIELD.
         LA    R3,1(R1,R3)
SCAN     TM    SWITCH,SWD
         BO    TESTL
         CLC   0(L'DTL,R2),DTL     DATE REQUEST?
         BE    DT1
         CLC   0(L'DTS,R2),DTS
         BE    DT2
TESTL    TM    OPERSW,SWL
         BO    TESTM
         CLC   0(L'LSL,R2),LSL     LIST REQUEST?
         BE    LS1
         CLC   0(L'LSS,R2),LSS
         BE    LS2
TESTM    TM    SWITCH,SWM
         BO    TESTT
         CLC   0(L'MNL,R2),MNL     MANUAL REQUEST?
         BE    MN1
         CLC   0(L'MNS,R2),MNS
         BE    MN2
TESTT    TM    OPERSW,SWT
         BO    TESTS
         CLC   0(L'TEST,R2),TEST   TEST REQUEST?
         BE    TS1
TESTS    TM    OPERSW,SWS
         BO    ERPARM
         CLC   0(L'SEOM,R2),SEOM   SKIP REQUEST?
         BE    SE1
         B     ERPARM
         SPACE 1
DT1      LA    R2,L'DTL(R2)
         B     DT3
DT2      LA    R2,L'DTS(R2)
DT3      CLR   R2,R3               DATE= OR D=
         BH    ENDPARM
         TM    OPERSW,SWL
         BO    ERPARM              ALREADY LIST REQUESTED.
         CLI   0(R2),C','
         BE    DTMNX
         LR    R6,R2               CONTROL AND GET DATE.
         BAS   R9,SDATE
         BNZ   ERPARM
         MVC   TDATE,WORK
         CLI   2(R2),C'/'
         BE    *+L'*+8
         CLI   2(R2),C'.'
         BNE   *+L'*+4
         LA    R2,2(R2)
         LA    R2,6(R2)
         OI    SWITCH,SWD
DTMN     CLR   R2,R3
         BH    ENDPARM
         CLI   0(R2),C','
         BNE   ERPARM
DTMNX    LA    R2,1(R2)
         B     SCAN
         SPACE 1
MN1      LA    R2,L'MNL(R2)        MANUAL= OR M=
         B     MN3
MN2      LA    R2,L'MNS(R2)
MN3      CLR   R2,R3
         BH    ENDPARM
         TM    OPERSW,SWL
         BO    ERPARM              ALREADY LIST REQUESTED.
         CLI   0(R2),C','
         BE    DTMNX
         CLI   0(R2),C'A'
         BL    ERPARM
         CLI   0(R2),C'Z'
         BH    ERPARM
         LA    R1,5(R2)
         LR    R4,R2
MN4      LA    R2,1(R2)
         CLR   R2,R3
         BH    MN5
         CLI   0(R2),C','
         BE    MN5
         CLR   R2,R1
         BH    ERPARM
         CLI   0(R2),C'#'
         BE    MN4
         CLI   0(R2),C'$'
         BE    MN4
         CLI   0(R2),C'@'
         BE    MN4
         CLI   0(R2),C'A'
         BNL   MN4
         B     ERPARM
MN5      LR    R1,R2
         SR    R1,R4
         BCTR  R1,0
         EX    R1,MVMAN
         EX    R1,MVNMB
         MVI   MANME,C'#'
         LA    R1,MANME+L'MANME-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   1(R1),C'#'
         OI    SWITCH,SWM
         B     DTMN
MVMAN    MVC   MANME+1(*-*),0(R4)
MVNMB    MVC   NMBN(*-*),0(R4)
         SPACE 1
LS1      LA    R2,L'LSL(R2)        LIST= OR L=
         B     LS3
LS2      LA    R2,L'LSS(R2)
LS3      CLR   R2,R3
         BH    ENDPARM
         TM    SWITCH,SWD+SWM
         BNZ   ERPARM              LIST IS EXCLUSIVE.
         CLI   0(R2),C','
         BE    DTMNX
         CLI   0(R2),C'#'
         BE    LS4
         CLI   0(R2),C'$'
         BE    LS4
         CLI   0(R2),C'@'
         BE    LS4
         CLI   0(R2),C'A'
         BL    ERPARM
         CLI   0(R2),C'Z'
         BH    ERPARM
LS4      LA    R1,7(R2)
         LR    R4,R2
LS5      LA    R2,1(R2)
         CLR   R2,R3
         BH    LS6
         CLI   0(R2),C','
         BE    LS6
         CLR   R2,R1
         BH    ERPARM
         CLI   0(R2),C'#'
         BE    LS5
         CLI   0(R2),C'$'
         BE    LS5
         CLI   0(R2),C'@'
         BE    LS5
         CLI   0(R2),C'A'
         BNL   LS5
         B     ERPARM
LS6      LR    R1,R2
         SR    R1,R4
         BCTR  R1,0
         L     R5,SLIST
         A     R5,SLIST+4
         EX    R1,MVNML
         OI    OPERSW,SWL
         B     DTMN
MVNML    MVC   0(*-*,R5),0(R4)
         SPACE 1
TS1      LA    R2,L'TEST(R2)       TEST
         OI    OPERSW,SWT
         B     DTMN
         SPACE 1
SE1      LA    R2,L'SEOM(R2)       SKIP
         OI    OPERSW,SWS
         B     DTMN
         SPACE 1
ENDPARM  TM    OPERSW,SWL
         BO    NOIN                LIST REQUESTED.
        $GDATE DAY,,WORK
         LTR   R10,R15
         BNZ   ERRG
         MVC   TODAY(2),WORK+4
         MVC   TODAY+3(2),WORK
         MVC   TODAY+6(2),WORK+2
         CLC   TDATE,TODAY
         BNL   ERDATE
         SPACE 2
*        ANALYZE REQUESTS CARDS (SYSIN) IF ANY.
         SPACE 1
         L     R1,=V(#RMSW#)
         MVI   0(R1),1
        $READ  CTCD
         LTR   R10,R15
         BM    NOIN
         BP    ANAL
         OI    OPERSW,SWR
         B     NOIN
ANAL     CH    R15,=H'1'
         BNE   END5
NOIN    $MOPEN WORK
         LTR   R10,R15
         BNZ   END4
         CLI   WORK+3,L'CARD
         BNE   ERLRL
         TM    OPERSW,SWL
         BO    STPM                LIST REQUESTED.
         TM    OPERSW,SWR
         BZ    SETDEF
         CLI   MANME,C' '
         BNE   FNM
GNXT     LM    R3,R5,LOOK
         MVI   SWITCH,0
         MVC   MANME,BLKS
         MVC   NMBN,BLKS
RSTRT    CLI   0(R3),C' '
         BNE   WHAT
CONT     BXLE  R3,R4,*-8
        $READ  CTCD
         LTR   R10,R15
         BP    END5
         BM    REOF
         LA    R3,CTCD
         B     RSTRT
REOF     NI    OPERSW,255-SWR
         CLI   MANME,C' '
         BNE   PRCS
         TM    SWITCH,SWD
         BO    PRCS
         XR    R10,R10
         B     END4
WHAT     CLI   0(R3),C'0'          IS IT A DATE?
         BNL   GDTE                YES.
         CLI   MANME,C' '
         BNE   PRCS
         MVI   MANME,C'#'
         LA    R2,MANME+1
         LA    R1,6
SRCH     CLI   0(R3),C'A'
         BL    ERSEL
         MVC   0(1,R2),0(R3)
         LA    R2,1(R2)
         BXLE  R3,R4,*+L'*+4
         B     STRE
         CLI   0(R3),C' '
         BE    STRE
         BCT   R1,SRCH
         B     ERSEL
STRE     MVC   NMBN(6),MANME+1
         MVI   0(R2),C'#'
         B     CONT
GDTE     LR    R6,R3
         BAS   R9,SDATE
         BNZ   ERSEL
         MVC   TDATE,WORK
         CLI   2(R3),C'/'
         BE    *+L'*+8
         CLI   2(R3),C'.'
         BNE   *+L'*+4
         LA    R3,2(R3)
         LA    R3,4(R3)
         BXLE  R3,R4,*+L'*+6
         LR    R3,R6
         B     ERSEL
         BXLE  R3,R4,TSTE
         BCTR  R3,0
         MVI   0(R3),C' '
         B     OKGDT
TSTE     CLI   0(R3),C' '
         BNE   ERSEL
OKGDT    OI    SWITCH,SWD
PRCS     ST    R3,LOOK
SETDEF   CLI   MANME,C' '
         BNE   FNM
         MVC   MANME(L'DEFMAN),DEFMAN
         SPACE 2
*        SELECT MAIN MANUAL MEMBER AND PRINT TITLE.
         SPACE 1
FNM     $MFIND MANME
         LTR   R10,R15
         BNZ   END4
         TM    OPERSW,SWT
         BZ    NOST
         MVC   TESTW+22(8),MANME
         LA    R1,TESTW
        WTO    MF=(E,(1))
NOST     LM    R3,R5,SLIST
RDM     $MREAD CARD
         LTR   R10,R15
         BM    ENDLTM
         BP    END4
         CLC   CARD(L'MB),MB       MEMBERS LIST.
         BE    SML
         CLC   CARD(L'EJ),EJ       SKIP PAGE.
         BE    *+L'*+10
         CLC   CARD(L'EJS),EJS
         BNE   LTT
         TM    SWITCH,SWN
         BO    SKP
         TM    SWITCH,SWF
         BO    PDT
         OI    SWITCH,SWF
         B     SKP
PDT      OI    SWITCH,SWN
         MVC   SVDAY(L'SVDAY),CARD+L'EJ
         MVC   CARD(L'CARD),CARD-1 PRINT CURRENT DATE OF THIS COPY.
         LA    R2,5
SKIP     BAS   R9,PLINE
         BCT   R2,SKIP
         MVC   CARD+9(L'NMB+L'NMBN),NMB
         CLC   CARD+L'NMB+9(L'NMBN),BLKS
         BNE   *+L'*+6
         MVC   CARD+L'NMB+9(6),DEFMAN+1
         BAS   R9,PLINE
         MVC   CARD(L'CARD),CARD-1
         BAS   R9,PLINE
         MVC   CARD+9(L'TDT),TDT
         MVC   CARD+L'TDT+9(2),TODAY+6
         MVC   CARD+L'TDT+11(4),TODAY+2
         MVC   CARD+L'TDT+15(2),TODAY
         MVI   CARD+L'TDT+L'TODAY+10,C'('
         MVC   CARD+L'TDT+L'TODAY+13(L'DAY),DAY
         MVI   CARD+L'TDT+L'TODAY+L'DAY+13,C')'
         BAS   R9,PLINE
         OI    SWITCH,SWC
         MVC   CARD(L'CARD),CARD-1
         MVC   CARD+L'EJ(L'SVDAY),SVDAY
SKP      TM    SWITCH,SWD
         BZ    SK3
         TM    SWITCH,SWN
         BZ    SK3
         LA    R6,CARD+L'EJ        CONTROL AND GET DATE.
         BAS   R9,SDATE
         BNZ   SK0
         CLC   WORK,TDATE
         BH    SK1
         OI    SWITCH,SWP
         B     RDM
SK0      MVC   CARD+L'EJ+20(L'ERDTP),ERDTP
         B     SK2
SK1      MVC   CARD+L'EJ(2),WORK+6
         MVC   CARD+L'EJ+2(4),WORK+2
         MVC   CARD+L'EJ+6(2),WORK
SK2      NI    SWITCH,255-SWP
SK3      MVC   CARD(L'EJ),LINE+1
         MVI   LINE,C'1'
         MVI   CARD+L'EJ+9,C'-'
         MVC   CARD+L'EJ+11(6),NMBN
         CLC   CARD+L'EJ+11(6),BLKS
         BNE   *+L'*+6
         MVC   CARD+L'EJ+11(6),DEFMAN+1
         TM    SWITCH,SWN
         BZ    *+L'*+4
         OI    SWITCH,SWU
         MVC   CARD(L'EJ),LINE+1
         MVI   LINE,C'1'
         B     LTM
LTT      TM    SWITCH,SWP
         BO    RDM
LTM      MVC   CARD+72(8),LINE+1
         BAS   R9,PLINE
         MVI   LINE,C' '
         B     RDM
SML      BXLE  R3,R4,*+L'*+4
         B     TABOV
         MVC   0(L'TLIST,R3),CARD+9
         MVI   L'TLIST(R3),C' '
         TM    SWITCH,SWP
         BO    RDM
         TM    SWITCH,SWN
         BZ    RDM                 DON'T PRINT IF FIRST PAGE.
         MVC   CARD(L'MB+5),LINE+1
         MVC   CARD+L'MB+4(8),CARD+L'MB+5
         MVC   CARD+L'MB+12(3),NMB+4
         B     LTM
         SPACE 2
*        PRINT THE CURRENT DATE OF THIS COPY.
         SPACE 1
ENDLTM   TM    SWITCH,SWC
         BO    STPM                ALREADY PRINTED.
         MVC   CARD(L'CARD),CARD-1
         LA    R2,5
SPCE     BAS   R9,PLINE
         BCT   R2,SPCE
         MVC   CARD+9(L'NMB+L'NMBN),NMB
         CLC   CARD+L'NMB+9(L'NMBN),BLKS
         BNE   *+L'*+6
         MVC   CARD+L'NMB+9(6),DEFMAN+1
         BAS   R9,PLINE
         MVC   CARD(L'CARD),CARD-1
         BAS   R9,PLINE
         MVC   CARD+9(L'TDT),TDT
         MVC   CARD+L'TDT+9(2),TODAY+6
         MVC   CARD+L'TDT+11(4),TODAY+2
         MVC   CARD+L'TDT+15(2),TODAY
         MVI   CARD+L'TDT+L'TODAY+10,C'('
         MVC   CARD+L'TDT+L'TODAY+13(L'DAY),DAY
         MVI   CARD+L'TDT+L'TODAY+L'DAY+13,C')'
         BAS   R9,PLINE
STPM     L     R4,SLIST
         A     R4,SLIST+4
         SPACE 2
*        SELECT A MEMBER PART OF THIS MANUAL.
         SPACE 1
NEXT     CLI   0(R4),C' '
         BE    ENDMAN
        $MFIND (R4)
         LTR   R10,R15
         BNZ   END4
         TM    OPERSW,SWT
         BZ    NOTS
         MVC   TESTW+22(8),0(R4)
         LA    R1,TESTW
        WTO    MF=(E,(1))
NOTS     LA    R4,L'TLIST(R4)
         NI    SWITCH,255-SWP
         SPACE 2
*        PRINT THE MEMBER PART OF THIS MANUAL.
         SPACE 1
RNXT    $MREAD CARD
         LTR   R10,R15
         BM    NEXT
         BP    END4
         CLC   CARD(L'EJ),EJ       SKIP PAGE.
         BE    *+L'*+10
         CLC   CARD(L'EJS),EJS
         BNE   RM4
         TM    SWITCH,SWD
         BZ    RM3
         LA    R6,CARD+L'EJ        CONTROL AND GET DATE.
         BAS   R9,SDATE
         BNZ   RM0
         CLC   WORK,TDATE
         BH    RM1
         OI    SWITCH,SWP
         B     RNXT
RM0      MVC   CARD+L'EJ+20(L'ERDTP),ERDTP
         B     RM2
RM1      MVC   CARD+L'EJ(2),WORK+6
         MVC   CARD+L'EJ+2(4),WORK+2
         MVC   CARD+L'EJ+6(2),WORK
RM2      NI    SWITCH,255-SWP
RM3      MVC   CARD(L'EJ),LINE+1
         MVI   LINE,C'1'
         OI    SWITCH,SWU
         TM    OPERSW,SWL
         BO    RM5
         MVI   CARD+L'EJ+9,C'-'
         MVC   CARD+L'EJ+11(6),NMBN
         CLC   CARD+L'EJ+11(6),BLKS
         BNE   RM5
         MVC   CARD+L'EJ+11(6),DEFMAN+1
         B     RM5
RM4      TM    SWITCH,SWP
         BO    RNXT
RM5      MVC   CARD+72(8),LINE+1
         BAS   R9,PLINE
         MVI   LINE,C' '
         B     RNXT
         SPACE 2
*        END PRINTING OF THIS MANUAL.
         SPACE 1
ENDMAN   XR    R10,R10             SET C.C. = 0.
         TM    OPERSW,SWL
         BO    END4
         TM    SWITCH,SWU
         BO    END1
         MVC   UMSG+L'UMSG-L'NMBN(L'NMBN),NMBN
         LA    R2,UMSG+L'UMSG-1
         CLI   0(R2),C' '
         BNE   *+L'*+4
         BCT   R2,*-8
         MVI   1(R2),C'.'
         LA    R2,UMSG
         B     END2
END1     TM    OPERSW,SWS
         BO    END3
         LA    R2,EMSG
END2    $PRINT (R2)
         LTR   R10,R15
         BNZ   END4
END3     TM    OPERSW,SWR
         BO    GNXT
         SPACE 2
*        ALL REQUESTS TERMINATED.
         SPACE 1
END4    $MCLOSE
END5    $RCLOSE
        $PCLOSE
        $XRET  CC=(R10)
         SPACE 2
*        ERRORS DIAGNOSTICS.
         SPACE 1
ERRG     LA    R2,GMSG
         B     ERR2
TABOV    LA    R2,OMSG
         B     ERR1
ERLRL    LA    R2,LMSG
ERR1    $PRINT (R2)
         B     END4
ERDATE   LA    R2,DMSG
         B     ERPARM+L'ERPARM
ERPARM   LA    R2,PMSG
         LA    R10,16
ERR2    $PRINT (R2)
         B     END5
ERSEL    LA    R2,RMSG
        $PRINT (R2)
        $PRINT DEBG
         MVC   CTCD(L'CTCD),CTCD-1
         MVI   DEBG,C' '
         MVI   0(R3),C'$'          SET THE SCAN POINTER.
        $PRINT DEBG
         LA    R10,16
         B     END4
         EJECT
*        SEARCH DATE ROUTINE.
*
* AT ENTRY :   R6 = ADDRESS OF DATE FIELD.
*                   FORMAT MAY BE : DDMMYY
*                                   DD.MM.YY
*                                   DD/MM/YY
*              R9 = LINK REGISTER.
*
* AT EXIT :    CONDITION CODE = ZERO : ALL OK.
*                               NON-ZERO : INPUT FIELD FORMAT ERROR.
*              'WORK' (FORMAT CL8) CONTAINS THE DATE AS YY/MM/DD.
         SPACE 1
SDATE    MVC   WORK,MDATE
         CLI   2(R6),C'/'          DD/MM/YY
         BE    SD1
         CLI   2(R6),C'.'          DD.MM.YY
         BE    SD2
         MVC   WORK+6(2),0(R6)     DDMMYY
         MVC   WORK+3(2),2(R6)
         MVC   WORK(2),4(R6)
         B     SD4
SD1      CLI   5(R6),C'/'
         BE    SD3
         BR    R9
SD2      CLI   5(R6),C'.'
         BNER  R9
SD3      MVC   WORK+6(2),0(R6)
         MVC   WORK+3(2),3(R6)
         MVC   WORK(2),6(R6)
SD4      MVC   CDATE,WORK
         NC    CDATE,MDATE
         CLC   CDATE,MDATE
         BR    R9
         EJECT
*        PRINT ROUTINE.
*
*              R9 = LINK REGISTER.
         SPACE 1
PLINE   $PRINT LINE
         LTR   R10,R15
         BZR   R9
         B     END4
         SPACE 2
TESTW   WTO    'MEMBER SELECTED :         ',ROUTCDE=11,DESC=7,MF=L
         SPACE 2
         LTORG
         EJECT
*        CONSTANTS AND WORK-AREAS.
         SPACE 1
WORK     DC    D'0'
LOOK     DC    A(CTCD,1,CTCD+71)
SLIST    DC    A(TLIST-L'TLIST,L'TLIST,ELIST)
CDATE    DC    CL8' '
TDATE    DC    CL8'00/00/00'
MDATE    DC    CL8'00/00/00'
TODAY    DC    CL8'00/00/00'
SVDAY    DC    CL8' '
DAY      DC    CL20' '
MANME    DC    CL8' '
DEFMAN   DC    CL8'#MANUAL#'
EJ       DC    CL4'=/EJ'
EJS      DC    CL4'1   '
MB       DC    CL4'=/MB'
DTL      DC    C'DATE='
DTS      DC    C'D='
MNL      DC    C'MANUAL='
MNS      DC    C'M='
LSL      DC    C'LIST='
LSS      DC    C'L='
NMB      DC    C'NAME : '
NMBN     DC    CL8' '
TDT      DC    C'DATE : '
TEST     DC    C'TEST'
SEOM     DC    C'SKIP'
BLKS     DC    CL8' '
         SPACE 1
SWITCH   DC    XL1'0'
SWD      EQU   X'01'               DATE REQUEST IN PARM. FIELD.
SWM      EQU   X'02'               MANUAL SELECT IN PARM. FIELD.
SWP      EQU   X'04'               PAGE TO BE PRINTED.
SWU      EQU   X'08'               UPDATED PAGES PRINTED.
SWF      EQU   X'10'               FIRST PAGE PRINT CURRENT DATE.
SWN      EQU   X'20'               FIRST PAGE CONTROL.
SWC      EQU   X'40'               PRINT CURRENT DATE.
         SPACE 1
OPERSW   DC    XL1'0'
SWT      EQU   X'01'               TEST REQUEST IN PARM. FIELD.
SWR      EQU   X'02'               REQUESTS CARDS PRESENT (SYSIN).
SWL      EQU   X'04'               LIST SELECT IN PARM. FIELD.
SWS      EQU   X'08'               SKIP END OF MANUAL MESSAGE.
         EJECT
         DC    AL1(L'LINE)
LINE     DC    0CL91' ',CL11' '
CARD     DC    CL80' '
         DC    AL1(L'DEBG)
DEBG     DC    0CL91' ',CL11'0 '
CTCD     DC    CL80' '
         SPACE 1
ERDTP    DC    C'*** PAGE PRINT FORCED : WRONG DATE DETECTED.'
         SPACE 1
         DC    AL1(L'EMSG)
EMSG     DC    C'1     --- END OF MANUAL ---'
         DC    AL1(L'GMSG)
GMSG     DC    C'1     -GDATE- SUBROUTINE ERROR.'
         DC    AL1(L'LMSG)
LMSG     DC    C'1     LOGICAL RECORD LENGTH NOT CARD IMAGE LENGTH.'
         DC    AL1(L'OMSG)
OMSG     DC    C'1     MANUAL MEMBERS TABLE OVERFLOW.'
         DC    AL1(L'DMSG)
DMSG     DC    C'1     INVALID DATE IN PARAMETERS FIELD.'
         DC    AL1(L'PMSG)
PMSG     DC    C'1     PARAMETERS FIELD ERROR.'
         DC    AL1(L'RMSG)
RMSG     DC    C'0     INVALID REQUEST DETECTED.'
         DC    AL1(L'UMSG)
UMSG     DC    C'1     NO UPDATE PAGES TO MANUAL :         '
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        SYMBOLS BELOW ARE NEVER DIRECTLY ADDRESSED BY PROGRAM.       *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
TLIST    DC    200CL8' '
         DC    200CL8' '
         DC    100CL8' '
ELIST    DC    CL8' ',C' '
         SPACE 2
         END
