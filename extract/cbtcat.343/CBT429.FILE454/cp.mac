CP       TITLE 'TSO CP COMMAND - ENTER VM COMMANDS.'
CP       START 0
         SPACE 1
* PURPOSE :    SOLVE A PARTICULAR PROBLEM IN RUNNING TSO UNDER MVS
* ---------    UNDER VM. NAMELY, WHEN A TERMINAL IS DIALED TO A
*              VIRTUAL MACHINE, IT CAN ONLY BE RELEASED BY ISSUING
*              A 'DETACH' OR 'RESET' COMMAND FROM THE VM CONSOLE.
*              IF ONE CONSOLE IS BEING USED FOR BOTH ROLES, THIS
*              POSES A PROBLEM. THE INSTALLATION OF THIS TSO COMMAND
*              ALLOWS CP COMMANDS SUCH AS 'DETACH' AND 'RESET' TO
*              BE ISSUED FROM A TSO CONSOLE. THE CONSOLE CAN THEN
*              RELEASE ITSELF FROM TSO AND BE DIALED IN AS THE VM
*              MASTER CONSOLE.
*
* AUTHOR:      REFER TO AMDAHL MVS INSTALLATION (EDUCATION COURSE)
* -------                 AND AMDAHL MVS-VM/PE (TCAM MCP).
*              SUNNYVALE (USA)
*              ADAPTED BY P.A. MOINIL, COMPUTING CENTRE,
*              J.R.C. - ISPRA ESTABLISHMENT, 21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
         EJECT
*------- MISCELLANEOUS DEFINITIONS.
         SPACE 1
DISPLAY  EQU   X'01'               DISPLAY REGISTERS.
NATIVE   EQU   X'10'               NATIVE OPERATING SYSTEM.
ACTIVE   EQU   X'20'               VM/PE ACTIVE.
DOWN     EQU   X'40'               VM/PE DOWN.
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
LV0      EQU   0         REJECT LEVEL.
         SPACE 1
        $MDL@IX
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R11,R13               SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R11
        $TEW$EC NOTTSO=(,ONLY),MSG=PUTMSG,ERR=SETRC
         XC    REQAUT(REQAUTL),REQAUT
         MVC   REQAUT+1(3),=AL3(MDL@IX)
         LA    R1,AUTH
         ST    R1,REQAUT+4
        $EACM  REQAUT
         CLI   AUTH,LV0              AUTHORIZED?
         BNE   STACC                 YES.
        TPUT   ERM,L'ERM
         B     SETRC
SETDSP   OI    SWITCH,DISPLAY
         B     ACCEPT
RSTDSP   NI    SWITCH,255-DISPLAY
         B     ACCEPT
STACC    MVI   SWITCH,0
ACCEPT  TPUT   SMG,L'SMG             SEND ENTER MSG.
         MVC   BUF(L'BLK),BLK        CLEAR BUFFER.
         L     R2,LGRES              CLEAR RESPONSE.
         BCTR  R2,0
         SRDL  R2,8
         SRL   R3,24
         LA    R4,RES
         EX    R3,CLEAR
         LTR   R2,R2
         BZ    READ
LOOP     LA    R4,1(R3,R4)
         LA    R3,255
         EX    R3,CLEAR
         BCT   R2,LOOP
         B     READ
CLEAR    XC    0(*-*,R4),0(R4)
READ    TGET   BUF,L'BUF             READ VM COMMAND FROM TERMINAL.
         OC    BUF(L'BLK),BLK        UPPERCASE TEXT.
         CLC   BUF(L'END),END
         BE    EXIT                  EXIT TAKEN ON END FROM KEYBOARD.
         CLC   BUF(L'DSP),DSP
         BE    SETDSP                DISPLAY REGISTERS REQUEST.
         CLC   BUF(L'RST),RST
         BE    RSTDSP                RESET DISPLAY.
         LA    R4,L'BUF              MAX LENGTH OF COMMAND.
         O     R4,FLAGS
         L     R5,LGRES              MAX LENGTH OF RESPONSE.
         XC    WORK,WORK
         SR    R10,R10               RESPONSE INDICATOR.
        ZEROKEY
         STIDP WORK                  STORE CPU ID.
         CLI   WORK,X'FF'            ARE WE UNDER NORMAL VM/370?
         BE    GDGN                  YES, GO DO DIAGNOSE.
         TM    X'A0',X'40'           ARE WE HYPERVISOR (VM/PE)?
         BZ    NDGN                  NO.
         TM    X'A0',X'01'           IS VM/PE DOWN?
         BZ    VMPE                  NO, GO ISSUE VM/PE SVC.
         OI    SWITCH,DOWN           SET VM/PE DOWN.
         B     NTVM
NDGN     OI    SWITCH,NATIVE         SET NATIVE OPERATING SYSTEM.
         B     NTVM
VMPE     OI    SWITCH,ACTIVE         WE ARE UNDER VM/PE.
GDGN     LRA   R2,BUF                ADDRESS OF VM COMMAND.
         LRA   R3,RES                ADDRESS OF VM RESPONSE.
         TM    SWITCH,ACTIVE         ARE WE UNDER VM/PE(HYPERVISOR)?
         BZ    SDGN                  NO, NORMAL VM/370, SKIP SVC.
        CNOP   0,8                   ASSURE DIAG & SVC IN ONE PAGE.
         SVC   209                   ISSUE VM/PE SVC.
SDGN     DC    X'83',X'24',XL2'0008' DIAGNOSE : ISSUE COMMAND TO VM.
         BZ    *+L'*+4
         LA    R10,255               SET CONDITION CODE INDICATOR.
NTVM     TM    SWITCH,DISPLAY
         BZ    *+L'*+4
         STM   R0,R15,SVREGS
        RESETKEY
         TM    SWITCH,DISPLAY
         BZ    ANAL
         MVC   BUF(L'CPUMSG),CPUMSG  DISPLAY CPU ID.
         LA    R1,L'WORK
         LA    R2,WORK
         LA    R3,BUF+L'CPUMSG-(2*L'WORK)-2
         BAL   R14,EDIT
        TPUT   BUF,L'CPUMSG
         TM    SWITCH,NATIVE+DOWN
         BNZ   ANAL
        TPUT   DMSG,L'DMSG           DISPLAY REGISTERS.
         MVC   BUF(L'BLK),BLK
         LA    R8,4
         LA    R2,SVREGS
         LR    R7,R8
SRG1     LA    R3,BUF+2
         LR    R6,R8
SRG2     LR    R1,R8
         BAL   R14,EDIT
         LA    R3,2(R3)
         BCT   R6,SRG2
        TPUT   BUF,L'BUF
         BCT   R7,SRG1
         LM    R0,R10,SVREGS
ANAL     TM    SWITCH,NATIVE
         BO    SNMVS
         TM    SWITCH,DOWN
         BZ    LOOK
        TPUT   DVMPE,L'DVMPE
         B     SETRC
SNMVS   TPUT   NMVS,L'NMVS
         B     SETRC
LOOK     LTR   R10,R10
         BZ    TEST
         LR    R10,R5                SAVE EXCESSION.
         L     R5,LGRES
TEST     LTR   R5,R5
         BNP   LAST
         CLI   RES,0                 NO RESPONSE?
         BE    LAST                  YES.
         LA    R2,RES
         LR    R3,R2
SCAN     CLI   0(R3),X'15'           LINE SEPARATOR?
         BNE   NEXT                  NO.
         LR    R4,R3
         SR    R4,R2
         LTR   R4,R4
         BNP   SKIP
        TPUT   (R2),(R4)             SEND RESPONSE BACK TO TERMINAL.
SKIP     LA    R2,1(R3)
NEXT     LA    R3,1(R3)
         BCT   R5,SCAN
         CLR   R2,R3
         BE    LAST
         SR    R3,R2
        TPUT   (R2),(R3)
LAST     LTR   R10,R10               ANY EXCESSION?
         BZ    ACCEPT                NO, READ NEXT VM COMMAND.
         CVD   R10,WORK
         MVC   BUF(LMGL),LMG
         MVC   BUF+L'LMG(L'MASK),MASK
         ED    BUF+L'LMG(L'MASK),WORK+5
        TPUT   LMG,LMGL
         B     ACCEPT                READ NEXT VM COMMAND.
SETRC    LA    R15,16
         B     EXIT+L'EXIT
EXIT     XR    R15,R15
        $XRET  CC=(R15),LV=WRKL,TYPE=RENT
         EJECT
*------- HEXADECIMAL OUTPUT ROUTINE.
*              R0  = WORK REGISTER.
*              R1  = INPUT FIELD LENGTH.
*              R2  = INPUT FIELD ADDRESS.
*              R3  = OUTPUT FIELD ADDRESS.
*              R4  = WORK REGISTER.
*              R14 = LINK REGISTER.
*              R15 = WORK REGISTER.
*        NOTE. AT EXIT :
*              R2  = POINTS TO THE NEXT BYTE OF THE INPUT FIELD.
*              R3  = POINTS TO THE NEXT BYTE OF THE OUTPUT FIELD.
         SPACE 1
EDIT     LR    R15,R1
         LR    R4,R3
         SR    R0,R0
ED1      IC    R0,0(R2)
         STC   R0,1(R3)
         NI    1(R3),X'0F'
         SRL   R0,4
         STC   R0,0(R3)
         LA    R2,1(R2)
         LA    R3,2(R3)
         BCT   R1,ED1
         SLL   R15,1
         BCTR  R15,0
         EX    R15,ED2
         BR    R14
ED2      TR    0(*-*,R4),TRTAB
         EJECT
*------- CONSTANTS.
         SPACE 1
LGRES    DC    A(LRES)
NRES     DC    A(LRES/L'RES)
FLAGS    DC    0F'0',XL4'40000000'
SMG      DC    C'ENTER VM-COMMAND OR END :'
LMG      DC    C'...ETC... (EXCESSION :'
         DC    CL6' '
         DC    C' BYTES)'
LMGL     EQU   *-LMG
CPUMSG   DC    C'CPU ID = X''                ''.'
DMSG     DC    C'REGISTERS (0-15) :'
ERM      DC    C' -> UNAUTHORIZED COMMAND.'
NMVS     DC    C' -> CP COMMAND NOT AVAILABLE (NATIVE MVS).'
DVMPE    DC    C' -> CP COMMAND NOT AVAILABLE (VM/PE DOWN).'
TRTAB    DC    C'0123456789ABCDEF'
MASK     DC    XL6'402020202120'
DSP      DC    C'FUNGULA'
RST      DC    C'FUNGA'
END      DC    C'END'
ASYS     DC    C'SYS'
BLK      DC    CL80' '
         SPACE 1
        LTORG
         EJECT
*------- WORK-AREAS.
         SPACE 1
WRKDSECT DSECT
         SPACE 1
SVA      DS    18F
SVREGS   DS    16F
WORK     DS    D
         SPACE 1
        $TEW$WA
         SPACE 1
BUF      DS    CL80
RES      DS    20CL80
LRES     EQU   *-RES
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(*-*)
AUTH     DC    XL1'0'
REQAUTL  EQU   *-REQAUT
SWITCH   DS    XL1
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 1
        PRINT  NOGEN
       $TEW$DS
        PRINT  GEN
         SPACE 1
         END
