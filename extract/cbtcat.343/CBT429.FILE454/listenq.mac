LENQ     TITLE 'TSO - LIST ENQS FOR A JOB OR USERID COMMAND.'
LISTENQ  START 0
         SPACE 1
* PURPOSE :    THIS COMMAND SHOWS THE DATA-SETS ALLOCATIONS FOR A
* ---------    JOB OR USERID.
*              COMMAND EXAMPLES :
*              LISTENQ ABCDEFG   - TO LOCATE ENQS FOR A JOB OR USERID.
*              LISTENQ XXXX USER - TO LOCATE ONLY ENQS FOR A USERID.
*              LISTENQ XXXX JOB  - TO LOCATE ONLY ENQS FOR A JOB.
*              LISTENQ *         - TO LOCATE ENQS OF USERID ISSUING CP.
*              RESPONSES EXAMPLES :
*              ABC.THIS.THAT                      ALLOCATED SHR
*              XYZ.THE.OTHER.THING                ALLOCATED OLD
*              THIS.IS.A.NOTHER                   WAITING   OLD
* RETURN CODES :    0 - JOB OR USER FOUND IN EXECUTION, CONFLICTS MAY
* --------------        OR MAY NOT HAVE BEEN FOUND.
*                   4 - JOB OR USER WAS NOT FOUND IN EXECUTION.
*                   8 - PARSE ERROR OCCURRED.
*                  12 - COMMAND COULD NOT FIND THE DSN QCB CHAIN.
*                  16 - COMMAND ERROR DETECTED.
* AUTHOR :     ORIGINAL VERSION FROM CBT TAPE (FILE 141).
* --------     THIS IS THE SP1.3 VERSION OF LISTENQ.
*              MODIFIED BY : RAVARANI N. - MOINIL P.A.
*                            COMPUTING CENTRE
*                            J.R.C. - ISPRA ESTABLISHMENT
*                            21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
SIAL     EQU   12*1024             SCAN INFO AREA LENGTH
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R9,R13              SET WORK AREAS ADDRESSABILITY
         USING WRKDSECT,R9
        $TEW$EC MSG=PUTMSG,ERR=CMDERR,PUT=PUTLNE
        $TEW$PP PARM=PARMLST,REG=R3,ERR=PARSERR
         MVC   JOBNAME,=CL8' '     CLEAR JOBNAME
         L     R1,JOBN             ADDRESS OF JOBNAME
         LH    R2,JOBN+4           GET LENGTH OF JOBNAME
         BCT   R2,*+L'*+6
         MVC   JOBNAME(*-*),0(R1)  <<EXECUTED>>
         EX    R2,*-6              MOVE JOBNAME
         MVI   SWITCH,0
         CLI   JOBNAME,C'*'        CHECK FOR * (OR USERID THATS ON)
         BNE   SETUFLAG
         L     R2,#TSCPPL
         USING CPPL,R2             CPPL ADDRESSABILITY
         L     R1,CPPLPSCB         -> PSCB
         DROP  R2
         USING PSCB,R1
         XR    R2,R2
         IC    R2,PSCBUSRL         LENGTH OF USERID
         BCT   R2,*+L'*+6
         MVC   JOBNAME(*-*),PSCBUSER <<EXECUTED>>
         EX    R2,*-6              MOVE USERID
         OI    SWITCH,RQSTUSER     ONLY TAKE USER
         B     RLSESA
         DROP  R1
SETUFLAG CLI   USER+1,1            WAS USER SPECIFIED?
         BNE   *+L'*+4
         OI    SWITCH,RQSTUSER
         CLI   JOB+1,1             WAS JOB SPECIFIED?
         BNE   RLSESA
         OI    SWITCH,RQSTJOB
         DROP  R3
RLSESA  IKJRLSA #TSANSW
         SPACE 1
*------- CVT -> ASVT -> ASCB -> CSCB
*        (BOY WHAT FUN ...)
         SPACE 1
         L     R2,CVTPTR           -> CVT
         USING CVT,R2
         L     R3,CVTASVT          -> ASVT
         DROP  R2
         USING ASVT,R3
         LA    R7,ASVTFRST-4       -> ENTRY ADDS (MINUS 4)
         L     R8,ASVTMAXU         -> # OF ASCBS
         DROP  R3
CHKASID  LA    R7,4(R7)            ENTRY + 4
         TM    0(R7),ASVTAVAI      IN USE?
         BO    NXTASID             NO... DON'T LOOK AT IT THEN
         L     R6,0(R7)            -> ASCB
         USING ASCB,R6
         L     R5,ASCBCSCB         -> POSSIBLE CSCB
         LTR   R5,R5               TEST FOR POSITIVE
         BNP   *+L'*+8             NO -> TRY NEXT ONE
         TM    28(R5),X'03'        IS THIS A USER OR JOB?
         BM    TJBNM               YES
         L     R5,ASCBJBNI         TRY NEXT ONE
         LTR   R5,R5               ONE MORE TIME
         BNP   *+L'*+8
         S     R5,=F'8'            ADJUST POINTER BACK SOME
         B     GOTCSCB
         L     R5,ASCBJBNS         ONE LAST TRY
         LTR   R5,R5               CROSS YOUR FINGERS
         BNP   NXTASID             OH WELL... TRY NEXT ASID
         S     R5,=F'16'           GOT TO ADJUST THIS ONE TOO
GOTCSCB  TM    28(R5),X'03'        IS THIS A USER OR JOB?
         BNM   NXTASID             NO...
TJBNM    CLC   8(8,R5),JOBNAME
         BNE   NXTASID
         TM    SWITCH,RQSTUSER     DID THEY ONLY WANT A USER?
         BZ    *+L'*+12            NO, CHECK FOR JOB ONLY
         TM    28(R5),X'01'        IS IT TSU?
         BZ    NXTASID             NO, SKIP IT EVEN IF IT COMPARED
         B     GOTASID
         TM    SWITCH,RQSTJOB      DID THEY ONLY WANT A JOB?
         BZ    GOTASID             NO, THEN TAKE WHAT WE GOT
         TM    28(R5),X'02'        IS IT JOB?
         BO    GOTASID             YES, WE GOT WHAT WE WANT
NXTASID  BCT   R8,CHKASID          GO FOR NEXT ASID
         B     NOTFND              JOB DOESN'T EXIST
GOTASID  MVC   ASID,ASCBASID       SAVE THE ASID
         XC    TKN,TKN             ASSURE TOKEN ZERO
         DROP  R6
DOGQ     LA    R13,SCANSAVE        ADDRESS GQSCAN SAVE AREA
         XC    0(18*4,R13),0(R13)
         MVC   SCAN(LSCAN),SCANP
         LH    R8,ASID             GET THE ASID # FOR THE JOB
         LA    R7,TABLE
        GQSCAN AREA=((R7),),TOKEN=TKN,SYSNAME=(,(R8)),MF=(E,SCAN)
         LR    R13,R9              RESTORE MAIN SAVE AREA
         B     *+L'*(R15)          HOW COMPLETE ?
         B     SEARCH              +0 - O.K.
         B     CODE04              +4 - NO RESOURCES MATCHED REQUEST
         B     SEARCH              +8 - GQSCAN AREA TOO SHORT
         B     CODE0C              +12 - ERROR WHILE PROCESSING
         B     CODE10              +16 - INVALID SYSNAME
         B     CODE14              +20 - GQSCAN AREA TOO SHORT
         SPACE 1
CODE04   LA    R0,L'CODE04M        NO RESOURCES FOUND
         LA    R1,CODE04M
         B     GQERR
CODE0C   LA    R0,L'CODE0CM        ERROR IN GQSCAN
         LA    R1,CODE0CM
         B     GQERR
CODE10   LA    R0,L'CODE10M        INVALID SYSNAME
         LA    R1,CODE10M
         B     GQERR
CODE14   LA    R0,L'CODE14M        GQSCAN AREA TOO SHORT
         LA    R1,CODE14M
GQERR    BAS   R14,PUTLNE
         B     *+L'*+4             +0 - NORMAL COMPLETION
         B     CMDERR              +4 - ERROR
         LA    R15,12              RC=12 ------------------------------
         B     QUIT
         SPACE 1
SEARCH   LTR   R6,R1               DID SOME DUMMY ASK IF HIS JOB WAS
         BNP   BYEBYE                   WAITING EVEN IF IT DIDN'T
*                                       ALLOCATE ANYTHING
         STCM  R0,B'1111',RIBLNGTH STORE RIB AND RIBE LENGTH
         LA    R7,TABLE            FIRST RIB, R6 : NO. OF RIBS
         USING RIB,R7
RIBLOOP  CLC   RIBQNAME,=CL8'SYSDSN' IS THIS A DSNAME RIB?
         BNE   BUMPRIB
         L     R5,RIBNRIBE         NUMBER OF RIBE'S
         LTR   R5,R5
         BNP   BUMPRIB             NO RIBE'S SUPPLIED
         OI    SWITCH,EFLAG        SHOW WE GOT ONE
         MVI   WAITLINE,C' '
         MVC   WAITLINE+1(L'WAITLINE-1),WAITLINE
         LR    R8,R7               GET RIB POINTER
         AH    R8,RIBLNGTH         POINT TO RIB VARIABLE SECTION
         USING RIBVAR,R8
         XR    R1,R1
         IC    R1,RIBRNMLN         RNAME LENGTH
         BCT   R1,*+L'*+6          -1 FOR MOVE
         MVC   WAITDSN(*-*),RIBRNAME <<EXECUTED>>
         EX    R1,*-6              GET THE DSNAME
         DROP  R8
         AH    R8,RIBVLEN          POINT TO FIRST RIBE
         USING RIBE,R8
NEXTRIBE TM    RIBERFLG,RIBETYPE   WHAT TYPE OF HOLD?
         BZ    *+L'*+10
         MVC   HOWTYPE,=CL3'SHR'   TYPE OF WAIT = SHARE
         B     *+L'*+6
         MVC   HOWTYPE,=CL3'OLD'   TYPE OF WAIT = EXCLUSIVE
         TM    RIBESFLG,RIBESTAT   DO I WAIT OR HOLD?
         BZ    *+L'*+10
         MVC   WHATMSG,=CL10'ALLOCATED' I HAVE IT
         B     *+L'*+6
         MVC   WHATMSG,=CL10'WAITING  ' I WANT IT
         DROP  R8
         LA    R0,L'WAITLINE       AND PUT IT
         LA    R1,WAITLINE
         BAS   R14,PUTLNE
         B     *+L'*+4             +0 - NORMAL COMPLETION
         B     CMDERR              +4 - ERROR
         AH    R8,RIBELENG         POINT TO NEXT RIBE
         BCT   R5,NEXTRIBE
BUMPRIB  XR    R14,R14
         L     R15,RIBNRIBE        NUMBER OF RIBE'S
         LTR   R15,R15
         BNP   *+L'*+8             NO RIBE'S SUPPLIED
         AH    R14,RIBELENG        ADD RIBE LENGTH
         BCT   R15,*-4             ADD TIMES NO. OF RIBE'S
         AH    R14,RIBLNGTH        ADD LENGTH OF RIB
         AH    R14,RIBVLEN         ADD LENGTH OF VARIABLE SECTION
         AR    R7,R14              POINT TO NEXT RIB
         BCT   R6,RIBLOOP          LOOP UNTIL NO MORE RIBS
         DROP  R7
         CLC   TKN,=F'0'           ANY MORE RIBS NOT YET SCANNED
         BNE   DOGQ                YES
BYEBYE   TM    SWITCH,EFLAG        WAS THERE ONE?
         BO    LEAVE               YES, PRINTED SOMETHING
         LA    R0,L'NUTTIN         TELL THEM NO D.S. ALLOCATED...
         LA    R1,NUTTIN
         BAS   R14,PUTLNE
         B     LEAVE               +0 - NORMAL COMPLETION
*                                  +4 - ERROR
CMDERR   LA    R15,16              RC=16 ------------------------------
         B     QUIT
LEAVE    XR    R15,R15             RC=0 -------------------------------
QUIT    $XRET  CC=(R15),LV=WRKL,TYPE=RENT
NOTFND   LA    R0,L'NOTF
         LA    R1,NOTF
         BAS   R14,PUTLNE
         B     *+L'*+4             +0 - NORMAL COMPLETION
         B     CMDERR              +4 - ERROR
         LA    R15,4               RC=4 -------------------------------
         B     QUIT
PARSERR  LA    R15,8               RC=8 -------------------------------
         B     QUIT
         EJECT
*------- SKELETONS, MESSAGES AND CONSTANTS
         SPACE 1
         PRINT NOGEN
SCANP   GQSCAN AREA=(,SIAL),SCOPE=ALL,SYSNAME=(0,),REQLIM=MAX,MF=L
         PRINT GEN
LSCAN    EQU   *-SCANP
         SPACE 1
NOTF     DC    C' -> JOB OR USER NOT FOUND IN EXECUTION'
NUTTIN   DC    C' -> JOB OR USER HAS NO DATA-SETS ALLOCATED'
CODE04M  DC    C' -> JOB OR USER HAS NO RESOURCES ENQUEUED'
CODE0CM  DC    C' -> GQSCAN : ERROR WHILE PROCESSING'
CODE10M  DC    C' -> GQSCAN : "SYSNAME" IS INVALID'
CODE14M  DC    C' -> GQSCAN : "AREA" TOO SHORT'
         SPACE 1
        LTORG
         SPACE 1
        PRINT  NOGEN
PARMLST IKJPARM
JOBN    IKJIDENT 'JOBNAME',FIRST=ANY,OTHER=ANY,MAXLNTH=8,              1
               PROMPT='JOB-NAME OR USER-ID'
USER    IKJKEYWD
        IKJNAME 'USER'
JOB     IKJKEYWD
        IKJNAME 'JOB'
        IKJENDP
        PRINT  GEN
         EJECT
*------- WORK-AREAS DESCRIPTION
         SPACE 1
WRKDSECT DSECT
SVA      DS    18F                 SAVE AREA
SCANSAVE DS    18F                 18 WORDS SAVE AREA REQ FOR GQSCAN
        $TEW$WA
SCAN     DS    0F,(LSCAN)X
TKN      DS    F
JOBNAME  DS    CL8
RIBLNGTH DS    H                   RIB LENGTH
RIBELENG DS    H                   RIBE LENGTH
WAITLINE DS    0CL62,CL2
WAITDSN  DS    CL44,CL2
WHATMSG  DS    CL10,CL1
HOWTYPE  DS    CL3
ASID     DS    H
SWITCH   DS    XL1
EFLAG    EQU   X'01'
RQSTUSER EQU   X'10'
RQSTJOB  EQU   X'20'
TABLE    DS    0F,(SIAL)X          SCAN INFO AREA
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        IHAASVT
        IHAASCB
        ISGRIB
        PRINT  GEN
         SPACE 1
         END
