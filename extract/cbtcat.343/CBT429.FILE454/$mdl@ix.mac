         MACRO           EURATOM ASSIGNED MODULES INDEXES.
&LABEL  $MDL@IX     &USE=
.*
.* $MDL@IX MACRO : SEE EURATOM AUTHORITY CONTROL MODULE (AUTHUGT).
.* ---------------
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         LCLA  &I,&LA,&LS,&LV,&M,&MC,&N,&TMX
         LCLB  &SA
         LCLC  &NMLV,&WK,&ZT
         LCLA  &L(26)         MODULES LEVELS NUMBERS.
         LCLB  &S(26)         MODULES SWITCHES.
         LCLC  &T(26)         MODULES NAMES TABLE.
         LCLC  &D(26)         MODULES DESCRIPTIONS TABLE.
&TMX     SETA  26             TABLES DIMENSION (ONE EMPTY = LAST).
&T(1)    SETC  'DIDOCS  '
&D(1)    SETC  'SIMULATE OS CONSOLE AT TERMINAL.'
&T(2)    SETC  'QSP     '
&D(2)    SETC  'QUEUE/SPOOL.'
&T(3)    SETC  'PSWR    '
&D(3)    SETC  'RACF PASSWORD READ.'
&T(4)    SETC  'AUTOCI  '
&D(4)    SETC  'AUTOMATIC OPERATORS COMMANDS ISSUER.'
&T(5)    SETC  'EUSUB   '
&D(5)    SETC  'EURATOM SUBMIT.'
&T(6)    SETC  'SCHEDULE'
&D(6)    SETC  'AUTOMATIC OPERATORS COMMANDS SCHEDULER.'
&T(7)    SETC  'CP      '
&D(7)    SETC  'ENTER VM COMMANDS.'
&T(8)    SETC  'AUTHC   '
&D(8)    SETC  'COMMAND AUTHORIZATION INTERFACE.'
&T(9)    SETC  'UNIT    '
&D(9)    SETC  'PHYSICAL DEVICES INQUIRY.'
&T(10)   SETC  'PUSE    '
&D(10)   SETC  'PAGEING UTILIZATION PROGRAM.'
&T(11)   SETC  'POF     '
&D(11)   SETC  'VTAM SECONDARY PROGRAM OPERATOR.'
&T(12)   SETC  'TSOENQ  '
&D(12)   SETC  'DATA-SETS ENQUE''''S CONFLICTS.'
&T(13)   SETC  'RSVENQ  '
&D(13)   SETC  'RESOURCES ENQUE''''S RESERVE.'
&T(14)   SETC  'VSM     '
&D(14)   SETC  'VIRTUAL STORAGE MONITOR.'
&T(15)   SETC  'INCORZAP'
&D(15)   SETC  'MODIFY IN CORE STORAGE.'
&T(16)   SETC  'CDSCB   '
&D(16)   SETC  'VTOC FORMAT-1 DSCB.'
&T(17)   SETC  'UNITS   '
&D(17)   SETC  'UNIT NAMES OF DEVICE NAME TABLE.'
&T(18)   SETC  'ZAP     '
&D(18)   SETC  'DISPLAY AND MODIFY DATA-SETS.'
&T(19)   SETC  'UJVD    '
&D(19)   SETC  'IEFUJV COMPUTATIONS.'
&T(20)   SETC  'FSPACE  '
&D(20)   SETC  'DASD VOLUMES FREE SPACE.'
&T(21)   SETC  'CONAN   '
&D(21)   SETC  'OPERATOR CONSOLE ANALYSIS FACILITY.'
&T(22)   SETC  'VCOREZAP'
&D(22)   SETC  'VIRTUAL CORE ZAP.'
&T(23)   SETC  'AUT98   '
&D(23)   SETC  'AUT.-PRG. CONTROL CALLER SERVICE.'
&T(24)   SETC  'PSWSAMP '
&D(24)   SETC  'PACKLIST - SAMPLE TAKER.'
&T(25)   SETC  'NOTICE  '
&D(25)   SETC  'TSO - BROADCAST NOTICES.'
         SPACE 1
         AIF   ('&USE' NE '').D1
&N       SETA  1
         AIF   ('&LABEL' EQ '').A1
&WK      SETC  '&LABEL         '
         AGO   .A2
.A1      ANOP
&WK      SETC  '&SYSECT        '
.A2      AIF   ('&WK'(1,8) NE 'AUTHUGT ').C1
*        ASSIGNED MODULES INDEXES.
*        -------------------------
         SPACE 1
.B1      AIF   ('&T(&N)' EQ '').B5
&M       SETA  &N-1
         AIF   (&M GT 9).B2
         MNOTE *,'   &M - &T(&N)   : &D(&N) '
         AGO   .B4
.B2      AIF   (&M GT 99).B3
         MNOTE *,'  &M - &T(&N)   : &D(&N) '
         AGO   .B4
.B3      MNOTE *,' &M - &T(&N)   : &D(&N) '
.B4      ANOP
&N       SETA  &N+1
         AGO   .B1
.B5      AIF   (&M EQ 0).TBLERR
         SPACE 1
MAXGN    EQU   &M             MAX. MODULE'S INDEX.
         EJECT
         MEXIT
.C1      AIF   ('&T(&N)' EQ '').MODERR
         AIF   ('&WK'(1,8) EQ '&T(&N)').C2
&N       SETA  &N+1
         AGO   .C1
.C2      ANOP
&M       SETA  &N-1
MDL@IX   EQU   &M        MODULE INDEX.
         MEXIT
.D1      AIF   ('&USE' EQ 'USER').D2
         AIF   ('&USE' EQ 'GROUP').D2
         AIF   ('&USE' EQ 'DEFAULT').D3
         AGO   .USEERR
.D2      AIF   ('&LABEL' EQ '').UGLERR
.D3      ANOP
&SA      SETB  0
&N       SETA  1
.D4      ANOP
&L(&N)   SETA  0
&S(&N)   SETB  0
         AIF   (&N EQ &TMX).D5
&N       SETA  &N+1
         AGO   .D4
.D5      ANOP
&LS      SETA  N'&SYSLIST
         AIF   (&LS EQ 0).H2
&MC      SETA  1
.E1      ANOP
&WK      SETC  '&SYSLIST(&MC)'
&LA      SETA  K'&WK
&N       SETA  1
.E2      AIF   ('&WK'(&N,1) EQ '-').E3
         AIF   ('&WK'(&N,1) EQ '.').E3
         AIF   ('&WK'(&N,1) EQ ':').E3
         AIF   ('&WK'(&N,1) EQ '/').E3
         AIF   (&N EQ &LA).SYNERR
&N       SETA  &N+1
         AGO   .E2
.E3      AIF   (&N EQ 1).SYNERR
&M       SETA  &N-1
         AIF   (&M GT 8).MNSERR
&NMLV    SETC  '&WK'(1,&M)
&ZT      SETC  '&NMLV         '
         AIF   ('&ZT'(1,8) NE 'ALL     ').G1
         AIF   (&SA).ALLERR
&SA      SETB  1
&M       SETA  &LA-&N
&N       SETA  &N+1
&ZT      SETC  '&WK'(&N,&M)
&LV      SETA  &ZT
         AIF   (&LV EQ 0).H1
         AIF   (&LV GT 15).LVLERR
&N       SETA  1
.F1      AIF   (&S(&N)).F2
&L(&N)   SETA  &LV
.F2      AIF   (&N EQ &TMX).H1
&N       SETA  &N+1
         AGO   .F1
.G1      ANOP
&I       SETA  1
.G2      AIF   ('&T(&I)' EQ '').MODERR
         AIF   ('&ZT'(1,8) EQ '&T(&I)').G3
&I       SETA  &I+1
         AGO   .G2
.G3      AIF   (&S(&I)).DUPERR
&S(&I)   SETB  1
&M       SETA  &LA-&N
&N       SETA  &N+1
&ZT      SETC  '&WK'(&N,&M)
&LV      SETA  &ZT
&L(&I)   SETA  &LV
.H1      AIF   (&MC EQ &LS).H2
&MC      SETA  &MC+1
         AGO   .E1
.H2      SPACE 1
         AIF   ('&USE' EQ 'DEFAULT').H4
         AIF   ('&USE' EQ 'GROUP').H3
         DC    CL8'&LABEL'         USER NAME.
         AGO   .H6
.H3      DC    CL8'&LABEL'         GROUP NAME.
         AGO   .H6
.H4      AIF   ('&LABEL' EQ '').H5
&LABEL   DS    0AL1                EVERY BODY LEVELS.
         AGO   .H6
.H5      ANOP
DEFAULT  DS    0AL1                EVERY BODY LEVELS.
.H6      ANOP
&N       SETA  1
.I1      AIF   ('&T(&N)' EQ '').I8
&M       SETA  &N-1
         AIF   (&M GT 9).I2
         AIF   (&L(&N) GT 9).I3
&ZT      SETC  '                &M - &T(&N)'
         AGO   .I7
.I2      AIF   (&M GT 99).I4
         AIF   (&L(&N) GT 9).I5
.I3      ANOP
&ZT      SETC  '               &M - &T(&N)'
         AGO   .I7
.I4      AIF   (&L(&N) GT 9).I6
.I5      ANOP
&ZT      SETC  '              &M - &T(&N)'
         AGO   .I7
.I6      ANOP
&ZT      SETC  '             &M - &T(&N)'
.I7      DC    AL1(&L(&N))&ZT
&N       SETA  &N+1
         AGO   .I1
.I8      AIF   ('&USE' EQ 'DEFAULT').I9
         EJECT
         MEXIT
.I9      SPACE 2
         MEXIT
.TBLERR  MNOTE 8,' NO MODULES NAMES IN TABLE ************************ '
         MEXIT
.MODERR  MNOTE 8,' MODULE NAME NOT IN TABLE ************************* '
         MEXIT
.LBLERR  MNOTE 8,' MODULE NAME MISSING ****************************** '
         MEXIT
.USEERR  MNOTE 8,' USE= KEYWORD SYNTAX ERROR ************************ '
         MEXIT
.UGLERR  MNOTE 8,' USER OR GROUP NAME MISSING *********************** '
         MEXIT
.MNSERR  MNOTE 8,' MODULE NAME SYNTAX ERROR ************************* '
         MEXIT
.SYNERR  MNOTE 8,' LEVEL SPECIFICATION SYNTAX ERROR ***************** '
         MEXIT
.ALLERR  MNOTE 8,' ALL= KEYWORD ALREADY SPECIFIED ******************* '
         MEXIT
.LVLERR  MNOTE 8,' LEVEL NUMBER (0-15) ERROR ************************ '
         MEXIT
.DUPERR  MNOTE 8,' MODULE NAME ALREADY SPECIFIED ******************** '
         MEND
