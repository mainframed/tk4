         MACRO
        $FS$TV
.*
.* MACRO : INTERNALLY USED BY $FSSA AND $FSSFE MACROS.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLB  &GFWSW
         GBLC  &GFSOP,&GFWTY,&GFWVA
         LCLA  &ATT,&I,&L,&M1,&M2,&M3,&M4,&N
         LCLC  &TY,&VA,&VC
&GFWSW   SETB  0
&N       SETA  N'&SYSLIST
         AIF   (&N EQ 2).STV
         MNOTE 8,'--- "TYPE,VALUE" : PAIR INCOMPLETE'
         AGO   .ERX
.STV     ANOP
&TY      SETC  '&SYSLIST(1)'
&VA      SETC  '&SYSLIST(2)'
         AIF   ('&VA'(1,1) NE '(').NXT
&L       SETA  K'&VA-2
&VA      SETC  '&VA'(2,&L)
.NXT     AIF   ('&TY' EQ 'HILITE').HLT
         AIF   ('&TY' EQ 'COLOUR').CLR
         AIF   ('&TY' EQ 'COLOR').CLR
         AIF   ('&TY' EQ 'PGSYM').PGS
         AIF   ('&TY' EQ 'PGRSYM').PGS
         AIF   ('&TY' EQ 'PGMSYM').PGS
         AIF   ('&GFSOP' EQ '28').UNK
         AIF   ('&TY' EQ 'ATTR').ATT
         AIF   ('&TY' EQ 'ATTRB').ATT
.UNK     MNOTE 8,'--- TYPE : "&TY" UNKNOWN OR WRONGLY CODED'
         AGO   .ERX
.HLT     AIF   ('&VA' EQ 'BLINK').BLK
         AIF   ('&VA' EQ 'REVERSE').RVS
         AIF   ('&VA' EQ 'USCORE').USC
         AIF   ('&VA' NE 'DEFAULT').IVA
&GFWVA   SETC  '00'
         AGO   .SHL
.BLK     ANOP
&GFWVA   SETC  'F1'
         AGO   .SHL
.RVS     ANOP
&GFWVA   SETC  'F2'
         AGO   .SHL
.USC     ANOP
&GFWVA   SETC  'F4'
.SHL     ANOP
&GFWTY   SETC  '41'
         MEXIT
.CLR     AIF   ('&VA' EQ 'BLUE').BLU
         AIF   ('&VA' EQ 'RED').RED
         AIF   ('&VA' EQ 'PINK').PNK
         AIF   ('&VA' EQ 'GREEN').GRN
         AIF   ('&VA' EQ 'TURQ').TRQ
         AIF   ('&VA' EQ 'YELLOW').YLW
         AIF   ('&VA' EQ 'WHITE').WHT
         AIF   ('&VA' NE 'DEFAULT').IVA
&GFWVA   SETC  '00'
         AGO   .SCL
.BLU     ANOP
&GFWVA   SETC  'F1'
         AGO   .SCL
.RED     ANOP
&GFWVA   SETC  'F2'
         AGO   .SCL
.PNK     ANOP
&GFWVA   SETC  'F3'
         AGO   .SCL
.GRN     ANOP
&GFWVA   SETC  'F4'
         AGO   .SCL
.TRQ     ANOP
&GFWVA   SETC  'F5'
         AGO   .SCL
.YLW     ANOP
&GFWVA   SETC  'F6'
         AGO   .SCL
.WHT     ANOP
&GFWVA   SETC  'F7'
.SCL     ANOP
&GFWTY   SETC  '42'
         MEXIT
.PGS     AIF   ('&VA' EQ 'DEFAULT').DFL
         AIF   (K'&VA EQ 2).HEX
.IVA     MNOTE 8,'--- VALUE : "&VA" UNKNOWN OR WRONGLY CODED'
.ERX     ANOP
&GFWSW   SETB  1
         MEXIT
.HEX     ANOP
&GFWVA   SETC  '&VA'
         AGO   .SPG
.DFL     ANOP
&GFWVA   SETC  '00'
.SPG     ANOP
&GFWTY   SETC  '43'
         MEXIT
.ATT     ANOP
&M1      SETA  0
&M2      SETA  0
&M3      SETA  0
&M4      SETA  0
         AIF   ('&VA' EQ 'NORMAL').GFA
&N       SETA  K'&VA
         AIF   (&N EQ 0).GFA
&J       SETA  1
.CNT     ANOP
&I       SETA  &J
.LSC     AIF   ('&VA'(&I,1) EQ ',').PRC
&I       SETA  &I+1
         AIF   (&I LE &N).LSC
.PRC     ANOP
&L       SETA  &I-&J
         AIF   (&L EQ 0).SCAN
&VC      SETC  '&VA'(&J,&L)
         AIF   ('&VC' EQ 'SKIP').SKIP
         AIF   ('&VC' EQ 'PROT').PROT
         AIF   ('&VC' EQ 'NUM').NUM
         AIF   ('&VC' EQ 'DARK').DARK
         AIF   ('&VC' EQ 'INT').INT
         AIF   ('&VC' EQ 'HIGH').INT
         AIF   ('&VC' EQ 'PEN').PEN
         AIF   ('&VC' EQ 'MED').PEN
         AIF   ('&VC' EQ 'MDT').MDT
         MNOTE 4,'--- UNKNOWN KEYWORD "&VC" - IGNORED'
         AGO   .SCAN
.SKIP    ANOP
&M4      SETA  32
&M3      SETA  16
         AGO   .SCAN
.PROT    ANOP
&M4      SETA  32
         AGO   .SCAN
.NUM     ANOP
&M3      SETA  16
         AGO   .SCAN
.DARK    ANOP
&M2      SETA  12
         AGO   .SCAN
.INT     ANOP
&M2      SETA  8
         AGO   .SCAN
.PEN     ANOP
&M1      SETA  4
         AGO   .SCAN
.MDT     ANOP
&M1      SETA  1
.SCAN    ANOP
&J       SETA  &I+1
         AIF   (&J LE &N).CNT
.GFA     ANOP
&ATT     SETA  2*(&M1+&M2+&M3+&M4)+1
&GFWVA   SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5X
               B5C5D5E5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6FX
               7F8F97A7B7C7D7E7F'(&ATT,2)
&GFWTY   SETC  'C0'
         MEND
