PROC     0
         CONTROL MSG
         ISPEXEC VGET (DDSN1 DVOL1 DDSN2 DVOL2 DDSN3 DVOL3 -
                       CMOP1 CMOP2 CMOP3 CMOP4)
         SET &CMOPT =
         SET &CMOVL =
         SET &CMNVL =
         IF &CMOP1 ^= YES THEN GOTO OPTA
         SET &CMOPT = &STR(FULL )
OPTA:    IF &CMOP2 ^= YES THEN GOTO OPTB
         SET &CMOPT = &STR(&CMOPT ASM)
OPTB:    IF &CMOP3 ^= YES THEN GOTO OPTC
         SET &CMOPT = &STR(&CMOPT IEBCOMPR)
OPTC:    IF &CMOP4 = THEN GOTO OVLT
         IF &CMOP4 = PRINT THEN GOTO OVLT
         IF &CMOP4 = SYSOUT THEN GOTO OPTE
         IF &CMOP4 = NOPRINT THEN GOTO OPTE
         IF &CMOP4 = BROWSE THEN GOTO OPTD
         SET &CMOPT = &STR(&CMOPT OUTFILE(&CMOP4))
         GOTO OVLT
OPTD:    CONTROL NOMSG
         FREE F(OUT)
         DELETE COMPARE.REPORT
         CONTROL MSG
         ALLOC F(OUT) DA(COMPARE.REPORT) NEW BL(1210) SPA(112,56)
         SET &CMOPT = &STR(&CMOPT OUTFILE(OUT))
         GOTO OVLT
OPTE:    SET &CMOPT = &STR(&CMOPT &CMOP4)
OVLT:    IF &DVOL1 = THEN GOTO NVLT
         SET &CMOVL = &STR(OVOL(&DVOL1))
NVLT:    IF &DVOL2 = THEN GOTO TIAL
         SET &CMNVL = &STR(NVOL(&DVOL2))
TIAL:    IF &DDSN3 = THEN GOTO CXEQ
         IF &DVOL3 = THEN GOTO IALL
         ALLOC F(IGNORE) DA(&DDSN3) VOL(&DVOL3) SHR
         GOTO CXEQ
IALL:    ALLOC F(IGNORE) DA(&DDSN3) SHR
CXEQ:    CONTROL NOMSG
         ERROR DO
           ERROR OFF
           CONTROL MSG
           WRITE  *** ERROR EXECUTING "COMPARE" TSO COMMAND ***
           IF &CMOP4 = BROWSE THEN DO
             DELETE COMPARE.REPORT
             FREE F(OUT)
             END
           IF &DDSN3 ^= THEN FREE F(IGNORE)
           EXIT CODE(8)
           END
         COMPARE &DDSN1 &DDSN2 &CMOVL &CMNVL &CMOPT
         SET &COMCC = &LASTCC
         ERROR OFF
         IF &CMOP4 = BROWSE THEN DO
           IF &COMCC = 0 THEN ISPEXEC BROWSE DATASET(COMPARE.REPORT)
           DELETE COMPARE.REPORT
           FREE F(OUT)
           END
         IF &DDSN3 ^= THEN FREE F(IGNORE)
         CONTROL MSG
         EXIT CODE(0)
