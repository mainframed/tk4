WTRS     TITLE 'TSO - LIST ENQ CONFLICTS FOR A JOB OR USERID COMMAND.'
WAITR    START 0
         SPACE 1
* PURPOSE :    THIS COMMAND WILL SEARCH THE QCB CHAIN TO LOCATE ALL
* ---------    DSN ENQS THAT RELATE TO A PARTICULAR JOB OR USERID
*              (* CAN BE USED TO DENOTE THE USER ISSUING THE CP).
*              IT WILL THEN DETERMINE IF THERE ARE EXIST ANY CONFLICTS.
*              IF THE JOB IS WAITING AS A RESULT OF THIS (I.E. - OLD IF
*              ALREADY ALLOCATED OR SHR IF IT IS ALLOCATED OLD), OR IF
*              THE JOB HOLDS DATA-SETS THAT ANOTHER JOB WANTS, IT WILL
*              SHOW OUT THE TYPE OF ALLOCATION AND THE DATA-SET NAME.
*              COMMAND EXAMPLES :
*              WAITR XXXX      - TO LOCATE ENQS FOR A JOB OR USERID.
*              WAITR XXXX USER - TO LOCATE ONLY ENQS FOR A USERID.
*              WAITR XXXX JOB  - TO LOCATE ONLY ENQS FOR A JOB.
*              WAITR *         - TO LOCATE ENQS OF USERID ISSUING CP.
*              RESPONSES EXAMPLES :
*              WAITING SHR FOR DSN = ABC.THIS.THAT
*              WAITING OLD FOR DSN = XYZ.THE.OTHER.THING
*              WAITING FOR DSN = THIS.IS.A.NAME, ALLOCATED SHR
*              WAITING FOR DSN = THIS.IS.A.NOTHER, ALLOCATED OLD
* RETURN CODES :    0 - JOB OR USER FOUND IN EXECUTION, CONFLICTS MAY
* --------------        OR MAY NOT HAVE BEEN FOUND.
*                   4 - JOB OR USER WAS NOT FOUND IN EXECUTION.
*                  16 - COMMAND ERROR DETECTED.
* AUTHOR :     ORIGINAL VERSION FROM CBT TAPE (FILE 141).
* --------     THIS IS THE SP1.3 VERSION OF WAITR.
*              MODIFIED BY : RAVARANI N. - MOINIL P.A.
*                            COMPUTING CENTRE (TP 361)
*                            J.R.C. - ISPRA ESTABLISHMENT
*                            21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
SIAL     EQU   12*1024             SCAN INFO AREA LENGTH
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R9,R13              SET WORK AREAS ADDRESSABILITY
         USING WRKDSECT,R9
         MVI   SWITCH,0
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
        $TEW$PP PARM=PARMLST,REG=R11
         MVC   JOBNAME,=CL8' '     CLEAR JOBNAME
         L     R1,JOBN             ADDRESS OF JOBNAME
         LH    R2,JOBN+4           GET LENGTH OF JOBNAME
         BCT   R2,*+L'*+6
         MVC   JOBNAME(*-*),0(R1)  <<EXECUTED>>
         EX    R2,*-6              MOVE JOBNAME
         CLI   JOBNAME,C'*'        CHECK FOR * (OR USERID THATS ON)
         BNE   SETFLGS
         L     R1,#TSPSCB          ---> PSCB
         USING PSCB,R1
         XR    R2,R2
         IC    R2,PSCBUSRL         LENGTH OF USERID
         BCT   R2,*+L'*+6
         MVC   JOBNAME(*-*),PSCBUSER <<EXECUTED>>
         EX    R2,*-6              MOVE USERID
         OI    SWITCH,RQSTUSER     ONLY TAKE USER
         B     RLSESA
         DROP  R1
SETFLGS  CLI   USER+1,1            WAS USER SPECIFIED?
         BNE   *+L'*+4
         OI    SWITCH,RQSTUSER
         CLI   JOB+1,1             WAS JOB SPECIFIED?
         BNE   RLSESA
         OI    SWITCH,RQSTJOB
         DROP  R11
RLSESA  IKJRLSA #TSANSW
         SPACE 1
*------- CVT ---> ASVT ---> ASCB ---> CSCB
*        (BOY WHAT FUN ...)
         SPACE 1
         L     R2,CVTPTR           ---> CVT
         USING CVT,R2
         L     R3,CVTASVT          ---> ASVT
         DROP  R2
         USING ASVT,R3
         LA    R7,ASVTFRST-4       ---> ENTRY ADDS (MINUS 4)
         L     R8,ASVTMAXU         ---> # OF ASCBS
         DROP  R3
CHKASID  LA    R7,4(R7)            ENTRY + 4
         TM    0(R7),ASVTAVAI      IN USE?
         BO    NXTASID             NO... DON'T LOOK AT IT THEN
         L     R6,0(R7)            ---> ASCB
         USING ASCB,R6
         L     R5,ASCBCSCB         ---> POSSIBLE CSCB
         LTR   R5,R5               TEST FOR POSITIVE
         BNP   *+L'*+8             NO ---> TRY NEXT ONE
         TM    28(R5),X'03'        IS THIS A USER OR JOB?
         BM    TJBNM               YES
         L     R5,ASCBJBNI         TRY NEXT ONE
         LTR   R5,R5               ONE MORE TIME
         BNP   *+L'*+8
         S     R5,=F'8'            ADJUST POINTER BACK SOME
         B     GOTCSCB
         L     R5,ASCBJBNS         ONE LAST TRY
         LTR   R5,R5               CROSS YOUR FINGERS
         BNP   NXTASID             OH WELL... TRY NEXT ASID
         S     R5,=F'16'           GOT TO ADJUST THIS ONE TOO
GOTCSCB  TM    28(R5),X'03'        IS THIS A USER OR JOB?
         BNM   NXTASID             NO...
TJBNM    CLC   8(8,R5),JOBNAME
         BNE   NXTASID
         TM    SWITCH,RQSTUSER     DID THEY ONLY WANT A USER?
         BZ    CHKTJOB             NO, CHECK FOR JOB ONLY
         TM    28(R5),X'01'        IS IT TSU?
         BZ    NXTASID             NO, SKIP IT EVEN IF IT COMPARED
         B     GOTASID
CHKTJOB  TM    SWITCH,RQSTJOB      DID THEY ONLY WANT A JOB?
         BZ    GOTASID             NO, THEN TAKE WHAT WE GOT
         TM    28(R5),X'02'        IS IT JOB?
         BO    GOTASID             YES WE GOT WHAT WE WANT
NXTASID  BCT   R8,CHKASID          GO FOR NEXT ASID
         B     NOTFND              JOB DOESN'T EXIST
GOTASID  MVC   ASID,ASCBASID       SAVE THE ASID
         XC    TKN,TKN             ASSURE TOKEN ZERO
         DROP  R6
DOGQ     LA    R13,SCANSAVE        ADDRESS GQSCAN SAVE AREA
         XC    0(18*4,R13),0(R13)
         MVC   SCAN(LSCAN),SCANP
         LH    R8,ASID             GET THE ASID # FOR THE JOB
         LA    R7,TABLE
        GQSCAN AREA=((R7),),TOKEN=TKN,SYSNAME=(,(R8)),MF=(E,SCAN)
         LR    R13,R9              RESTORE MAIN SAVE AREA
         B     *+L'*(R15)          HOW COMPLETE ?
         B     SEARCH              +0 - O.K.
         B     CODE04              +4 - NO RESOURCES MATCHED REQUEST
         B     SEARCH              +8 - GQSCAN AREA TOO SHORT
         B     CODE0C              +12 - ERROR WHILE PROCESSING
         B     CODE10              +16 - INVALID SYSNAME
         B     CODE14              +20 - GQSCAN AREA TOO SHORT
         SPACE 1
CODE04   LA    R0,L'CODE04M        NO RESOURCES FOUND
         LA    R1,CODE04M
         BAS   R14,PUTLNE
         B     NTFRC
CODE0C   LA    R0,L'CODE0CM        ERROR IN GQSCAN
         LA    R1,CODE0CM
         B     GQERR
CODE10   LA    R0,L'CODE10M        INVALID SYSNAME
         LA    R1,CODE10M
         B     GQERR
CODE14   LA    R0,L'CODE14M        GQSCAN AREA TOO SHORT
         LA    R1,CODE14M
GQERR    BAS   R14,PUTLNE
         B     SETRC
         SPACE 1
SEARCH   LTR   R6,R1               DID SOME DUMMY ASK IF HIS JOB WAS
         BNP   BYEBYE                   WAITING EVEN IF IT DIDN'T
*                                       ALLOCATE ANY DATA-SETS
         STCM  R0,B'1111',RIBLNGTH STORE RIB AND RIBE LENGTH
         LA    R7,TABLE            FIRST RIB, R6 : NO. OF RIBS
         USING RIB,R7
         MVC   WAITLINE(79),=CL79' WAITING XXX FOR DSN = '
         MVC   HOLDLINE(79),=CL79' WAITING FOR DSN = '
         MVC   HOLDALC,=CL12', ALLOCATED '
RIBLOOP  CLC   RIBQNAME,=CL8'SYSDSN'    IS THIS A DSNAME RIB?
         BNE   BUMPRIB
         L     R5,RIBNRIBE         NO. OF RIBE'S
         LTR   R5,R5
         BNP   BUMPRIB             NO RIBE'S SUPPLIED
         CLC   RIBNTWE(8),=XL8'0'  ANY TASKS WAITING?
         BE    BUMPRIB             NO, NOT EVEN ME
         OI    SWITCH,EFLAG        SHOW WE GOT ONE
         MVI   WAITDSN,C' '
         MVC   WAITDSN+1(L'WAITDSN-1),WAITDSN
         LR    R8,R7               GET RIB POINTER
         AH    R8,RIBLNGTH         POINT TO RIB VARIABLE SECTION
         USING RIBVAR,R8
         XR    R1,R1
         IC    R1,RIBRNMLN         RNAME LENGTH
         BCT   R1,*+L'*+6          -1 FOR MOVE
         MVC   WAITDSN(*-*),RIBRNAME <<EXECUTED>>
         EX    R1,*-6              GET THE DSNAME
         MVC   HOLDDSN,WAITDSN     TO BOTH MESSAGES
         DROP  R8
         AH    R8,RIBVLEN          POINT TO FIRST RIBE
         USING RIBE,R8
NEXTRIBE TM    RIBERFLG,RIBETYPE   WHAT TYPE OF HOLD?
         BZ    *+L'*+16
         MVC   SHRTYPE,=CL3'SHR'   TYPE OF WAIT = SHARE
         MVC   HOLDTYPE,=CL3'SHR'  OR HOLD = SHARE
         B     *+L'*+12
         MVC   SHRTYPE,=CL3'OLD'   TYPE OF WAIT = EXCLUSIVE
         MVC   HOLDTYPE,=CL3'OLD'  OR HOLD = EXCLUSIVE
         XR    R1,R1
         IC    R1,RIBRNMLN
         LA    R1,HOLDDSN(R1)
         MVC   0(15,R1),HOLDALC
         TM    RIBESFLG,RIBESTAT   DO I WAIT OR HOLD?
         BZ    *+L'*+8
         LA    R1,HOLDLINE
         B     *+L'*+4
         LA    R1,WAITLINE
         LA    R0,79               SEND IT
         BAS   R14,PUTLNE
         B     *+L'*+4             +0 - NORMAL COMPLETION
         B     SETRC               +4 - ERROR
         AH    R8,RIBELENG         POINT TO NEXT RIBE
         BCT   R5,NEXTRIBE
BUMPRIB  XR    R14,R14
         L     R15,RIBNRIBE        NO. OF RIBE'S
         LTR   R15,R15
         BNP   *+L'*+8             NO RIBE'S SUPPLIED
         AH    R14,RIBELENG        ADD RIBE LENGTH
         BCT   R15,*-4             ADD TIMES NO. OF RIBE'S
         AH    R14,RIBLNGTH        ADD LENGTH OF RIB
         AH    R14,RIBVLEN         ADD LENGTH OF VARIABLE SECTION
         AR    R7,R14              POINT TO NEXT RIB
         BCT   R6,RIBLOOP          LOOP UNTIL NO MORE RIBS
         DROP  R7
         CLC   TKN,=F'0'           ANY MORE RIBS NOT YET SCANNED?
         BNE   DOGQ                YES
BYEBYE   TM    SWITCH,EFLAG        WAS THERE ONE?
         BO    LEAVE               YES, PRINTED SOMETHING
         LA    R0,L'NUTTIN         TELL THEM NO D.S. ALLOCATED...
         LA    R1,NUTTIN
         BAS   R14,PUTLNE
         B     *+L'*+4             +0 - NORMAL COMPLETION
         B     SETRC               +4 - ERROR
LEAVE    XR    R4,R4               RC=0 -------------------------------
QUIT    $XRET  CC=(R4),LV=WRKL,TYPE=RENT
NOTFND   LA    R0,L'NOTF
         LA    R1,NOTF
         BAS   R14,PUTLNE
         B     NTFRC               +0 - NORMAL COMPLETION
*                                  +4 - ERROR
SETRC    LA    R4,16               RC=16 ------------------------------
         B     QUIT
NTFRC    LA    R4,4                RC=4  ------------------------------
         B     QUIT
         SPACE 1
        LTORG
*------- SKELETONS, MESSAGES AND CONSTANTS
         SPACE 1
        PRINT  NOGEN
SCANP   GQSCAN AREA=(,SIAL),SCOPE=ALL,SYSNAME=(0,),REQLIM=MAX,MF=L
        PRINT  GEN
LSCAN    EQU   *-SCANP
         SPACE 1
NOTF     DC    C'JOB OR USER NOT FOUND IN EXECUTION.'
NUTTIN   DC    C'JOB OR USER IS NOT WAITING FOR DATA-SET(S) OR HOLDING 1
               UP ANY OTHER JOBS.'
CODE04M  DC    C'JOB OR USER HAS NO RESOURCES ENQUEUED.'
CODE0CM  DC    C' -> GQSCAN : ERROR WHILE PROCESSING'
CODE10M  DC    C' -> GQSCAN : "SYSNAME" IS INVALID'
CODE14M  DC    C' -> GQSCAN : "AREA" TOO SHORT'
         SPACE 1
        PRINT  NOGEN
PARMLST IKJPARM
JOBN    IKJIDENT 'JOBNAME',FIRST=ANY,OTHER=ANY,MAXLNTH=8,              1
               PROMPT='JOB-NAME OR USER-ID'
USER    IKJKEYWD
        IKJNAME 'USER'
JOB     IKJKEYWD
        IKJNAME 'JOB'
        IKJENDP
        PRINT  GEN
         EJECT
*------- WORK-AREAS DESCRIPTION
         SPACE 1
WRKDSECT DSECT                     WORK AREAS DESCRIPTION
SVA      DS    18F                 SAVE AREA
SCANSAVE DS    18F                 18 WORDS SAVE AREA REQ FOR GQSCAN
        $TEW$WA
JOBNAME  DS    CL8
SCAN     DS    0F,(LSCAN)X
TKN      DS    F
RIBLNGTH DS    H                   RIB LENGTH
RIBELENG DS    H                   RIBE LENGTH
WAITLINE DS    0CL79,CL9
SHRTYPE  DS    CL3
         DS    CL11
WAITDSN  DS    CL44
         DS    CL12
HOLDLINE DS    0CL79,CL19
HOLDDSN  DS    CL44
         DS    CL15,CL1
HOLDALC  DS    CL12
HOLDTYPE DS    CL3
SWITCH   DS    XL1
EFLAG    EQU   X'01'
RQSTUSER EQU   X'10'
RQSTJOB  EQU   X'20'
ASID     DS    H
TABLE    DS    0F,(SIAL)X          SCAN INFO AREA
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        IHAASVT
        IHAASCB
        ISGRIB
        PRINT  GEN
         SPACE 1
         END
