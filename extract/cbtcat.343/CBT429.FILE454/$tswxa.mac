         MACRO           TEST AND SWITCH ADDRESSING MODE.
&NAME   $TSWXA      &MODE,&EXPAND=ALL,&ASS=H,&CVT=YES
         LCLC  &X
.*
.* $TSWXA MACRO : MACRO TO TEST THE CURRENT ADDRESSING MODE OR TO
.* -------------- SWITCH BETWEEN 24-BIT AND 31-BIT ADDRESSING MODE.
.*                IT ALLOWS PROGRAMS TO RUN ON MVS/370 AND MVS/XA.
.*
.* MODE OPERAND : THE DESIRED ADDRESSING MODE TO ENTER (24 OR 31).
.*                IF SYSTEM IS SPECIFIED, ONLY THE TEST OF THE
.*                CURRENT MVS VERSION IS EXECUTED. THE CONDITION
.*                CODE IS SET AS FOLLOWS :
.*                     - 0 (ZERO) = MVS/370 VERSION,
.*                     - 3 (ONE) = MVS/XA VERSION.
.*                IF OMITED, THAN THE TEST OF CURRENT MVS VERSION
.*                AND ADDRESSING MODE IS EXECUTED. THE CONDITION
.*                CODE IS SET AS FOLLOWS :
.*                     - 0 (ZERO) = MVS/370 MODE,
.*                     - 1 (MINUS) = MVS/XA 31-BIT ADDRESSING MODE,
.*                     - 2 (PLUS) = MVS/XA 24-BIT ADDRESSING MODE,
.*                AND THE REGISTER 14 CONTAINS THE CURRENT MVS VERSION
.*                AND AMODE INDICATORS AS BELOW :
.*                     - BIT 0 : OFF = 24-BIT AMODE,
.*                               ON = 31-BIT AMODE,
.*                     - BIT 1 : OFF = MVS/370,
.*                               ON = MVS/XA,
.*                     - THE REMAINING BITS (2-31) ARE BINARY ZEROES.
.*
.* EXPAND OPERAND : ONLY USED WITH MODE OPERAND 24 OR 31 TO REQUEST
.*                THE FULL EXPANSION (EXPAND=ALL, DEFAULT) OR ONLY
.*                THE ENTER SPECIFIC MODE INSTRUCTIONS (EXPAND=ONLY).
.*
.* ASS OPERAND :  NORMALLY THIS MACRO NEEDS ASSEMBLER H (IEV90) FOR
.*                BSM INSTRUCTION, BUT THE ASSEMBLER F MAY BE USED
.*                SPECIFYING ASS=F.
.*
.* CVT OPERAND :  NORMALLY THIS MACRO REFERS TO FIELDS MAPPED BY THE
.*                CVT MACRO (CVT=YES), BUT IT MAY BE SUPPRESSED WHEN
.*                SPECIFYING CVT=NO (ABSOLUTE LOCATIONS ARE USED).
.*
.* NOTES - REGISTER 14 IS ALWAYS ALTERED.
.*       - IF THE OPERATING SYSTEM DON'T SUPPORT 31-BIT ADDRESSING,
.*         OR THE CURRENT ADDRESSING MODE IS ALREADY THAT REQUESTED,
.*         NO MODE SWITCH OCCURS.
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
&X       SETC  'IHB'.'&SYSNDX'
         AIF   ('&ASS' NE 'H' AND '&ASS' NE 'F').INVA
         AIF   ('&CVT' NE 'YES' AND '&CVT' NE 'NO').INVC
         AIF   (T'&MODE EQ 'O').OMIT
         AIF   ('&MODE' EQ 'SYSTEM').OMIT
         AIF   ('&MODE' NE '24' AND '&MODE' NE '31').INVM
         AIF   ('&EXPAND' EQ 'ALL').OMIT
         AIF   ('&EXPAND' NE 'ONLY').INVE
&NAME    LA    14,&X+L'&X          REG. 14 = AMODE AFTER BSM
         AIF   ('&MODE' EQ '31').ENXA
         AGO   .STMD
.OMIT    AIF   ('&CVT' EQ 'YES').OKCVT
&NAME    L     14,16               REG. 14 ---> CVT
         TM    116(14),X'80'       XA (CVTDCB, BIT 0) ON?
         AGO   .TSMD
.OKCVT   ANOP
&NAME    L     14,CVTPTR           REG. 14 ---> CVT
         USING CVTMAP,14
         TM    CVTDCB,CVTMVSE      XA (BIT 0) ON?
         DROP  14
.TSMD    AIF   ('&MODE' EQ 'SYSTEM').TSYS
         LA    14,0                CLEAR REG. 14
         BZ    &X+L'&X             NO, LET'S GO (MVS/370)
         AIF   (T'&MODE NE 'O').NORM
         ICM   14,B'1000',=X'40'   INDICATE MVS/XA RUNNING
.NORM    AIF   ('&ASS' EQ 'F').ASSF
         BSM   14,0                GET CURRENT AMODE
         AGO   .TSTM
.ASSF    DC    XL2'0BE0'           BSM 14,0 TO GET CURRENT AMODE
.TSTM    AIF   (T'&MODE EQ 'O').TEND
         LTR   14,14               CURRENT MODE IS?
         LA    14,&X+L'&X          REG. 14 = AMODE AFTER BSM
         AIF   ('&MODE' EQ '31').STXA
         BZ    &X+L'&X             24-BIT, ALREADY IN
         AGO   .STMD
.STXA    BM    &X+L'&X             31-BIT, ALREADY IN
.ENXA    O     14,=A(X'80000000')  SET ADDRESSING TO 31-BIT MODE
.STMD    AIF   ('&ASS' EQ 'F').ASMF
&X       BSM   0,14                ENTER NEW AMODE
         MEXIT
.ASMF    ANOP
&X       DC    XL2'0B0E'           BSM 0,14 TO ENTER NEW AMODE
         MEXIT
.TEND    ANOP
&X       LTR   14,14               SET CC TO CURRENT MODE
         MEXIT
.INVA    MNOTE 8,' INVALID ASS OPERAND ****************************** '
         MEXIT
.INVC    MNOTE 8,' INVALID CVT OPERAND ****************************** '
         MEXIT
.INVM    MNOTE 8,' INVALID MODE OPERAND ***************************** '
         MEXIT
.INVE    MNOTE 8,' INVALID EXPAND OPERAND *************************** '
.TSYS    MEND
