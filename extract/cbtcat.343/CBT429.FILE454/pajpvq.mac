PVQ      TITLE 'VENQ - GRS/ENQ DISPLAY.'
PAJPVQ   START 0
         SPACE 1
* ORIGIN : EXTRACTED FROM CBT TAPE 90 MAY, FILE 103.
*          DATA SET VUTL16 AT LEVEL 005 AS OF 06/25/85
*          SOME ADJUSMENTS DONE BY : MOINIL P.A.
         SPACE 1
        $DEFREG
*------- INITIALIZATION
         SPACE 1
        $XENT  BASE=(R11,R12)
         EJECT
*------- DEFINE DISPLAY VARIABLES TO ISPF
         SPACE 1
        PRINT  NOGEN
        CALL   ISPLINK,(#VDEFINE,MS,MST,#CHAR,MSL),VL
         LTR   R10,R15
         BNZ   QUIT
        CALL   ISPLINK,(#VDEFINE,ML,MLT,#CHAR,MLL),VL
         LTR   R10,R15
         BNZ   QUIT
        CALL   ISPLINK,(#VDEFINE,#JOB,JOB,#CHAR,LGH8),VL
         LTR   R15,R15
         BNZ   ERR1
        CALL   ISPLINK,(#VDEFINE,#MAJOR,MAJOR,#CHAR,LGH8),VL
         LTR   R15,R15
         BNZ   ERR2
        CALL   ISPLINK,(#VDEFINE,#MINOR,MINOR,#CHAR,MINLG),VL
         LTR   R15,R15
         BNZ   ERR3
        CALL   ISPLINK,(#VDEFINE,#X,X,#CHAR,LG80),VL
         LTR   R15,R15
         BNZ   ERR4
        CALL   ISPLINK,(#VDEFINE,#T1,T1,#CHAR,LG80),VL
         LTR   R15,R15
         BNZ   ERR5
        CALL   ISPLINK,(#VDEFINE,#T2,T2,#CHAR,LG80),VL
         LTR   R15,R15
         BNZ   ERR6
         EJECT
*------- DISPLAY FIRST PANEL
         SPACE 1
DVENQ1  CALL   ISPLINK,(#DISPLAY,#VENQ1),VL
         CH    R15,=H'8'
         BE    RETURN
         LTR   R15,R15
         BNZ   ERR10
        CALL   ISPLINK,(#TBCREAT,#ENQTAB,,,#NOWRITE,#REPLACE),VL
         CH    R15,=H'4'
         BH    ERR11
         BE    *+L'*+4
         OI    SWITCH,TBLCRT
         MVC   TCOUNT(4),=F'0'     BLANK OUT COUNT FIELD
         XR    R3,R3               WAIT COUNT ZERO BY DEFAULT
         MVC   T1,XB               BLANK OUT T1
         MVC   T2,XB               BLANK OUT T2
         CLI   JOB,C' '            JOB BLANK ?
         BE    NOTJOB1             YES
         MVC   T1(27),=CL27'ENQ STATUS FOR JOB XXXXXXXX'
         MVC   T1+19(8),JOB
         MVC   T2(35),=CL35'TYPE STATUS  SCOPE   MAJOR    MINOR'
         B     CONT1
NOTJOB1  MVC   T2(22),=CL22'SCOPE   MAJOR    MINOR'
         CLI   MAJOR,C' '          MAJOR BLANK ?
         BNE   NOTCONT             NO
         CLI   MINOR,C' '          MINOR BLANK ?
         BNE   NOTCONT             NO
         LA    R3,1                YES, CONTENTION REQUESTED
         MVC   T1(14),=CL14'ENQ CONTENTION'
         B     CONT1
NOTCONT  MVC   T1(35),=CL35'ENQ STATUS FOR MAJOR XXXXXXXX MINOR'
         MVC   T1+21(8),MAJOR      MOVE MAJOR NAME TO MESSAGE
         MVC   T1+36(L'MINOR),MINOR MOVE MINOR NAME
CONT1    MVC   TKN,=F'0'           RESET TOKEN
         LA    R5,MINOR            POINT TO MINOR
         L     R14,=A(BLTABLE)
         TRT   MINOR,0(R14)        LOOK FOR BLANK
         BNZ   FONE                FOUND A BLANK
         LA    R6,L'MINOR          NO BLANK, ALL BYTES ARE PRESENT
         B     SCAN                GO DO IT
FONE     SR    R1,R5               GET MINOR LENGTH
         LR    R6,R1               LOAD IT INTO R6
         EJECT
*------- PREPARE TO SCAN GRS CONTROL BLOCKS
         SPACE 1
SCAN     L     R0,SVGETL           GETMAIN AREA FOR RIB/RIBE
        GETMAIN R,LV=(0)
         ST    R1,SVGETM           SAVE GETMAIN ADDR
         LR    R4,R1               LOAD R4 WITH GETMAINED ADDR
         USING RIB,R4
         L     R0,SVGETL           GETMAIN AREA FOR RIB/RIBE
         CLI   MAJOR,C' '
         BE    SCANALL
         CLI   MINOR,C' '
         BE    SCANDSN
         SPACE 1
*------- SCAN WITH BOTH MAJOR AND MINOR NAMES SPECIFIED
         SPACE 1
        GQSCAN AREA=((R4),(R0)),SCOPE=ALL,                             C
               RESNAME=(MAJOR,(R5),(R6)),                              C
               TOKEN=TKN
         B     CKRTN
         SPACE 1
*------- SCAN WITH JUST MAJOR NAME SPECIFIED
         SPACE 1
SCANDSN GQSCAN AREA=((R4),(R0)),SCOPE=ALL,                             C
               RESNAME=(MAJOR),                                        C
               TOKEN=TKN
         B     CKRTN               GO CHECK RETURN CODE
         SPACE 1
*------- SCAN FOR ALL DATA
         SPACE 1
SCANALL GQSCAN AREA=((R4),(R0)),SCOPE=ALL,WAITCNT=(R3),                C
               TOKEN=TKN
CKRTN    MVI   MLT,C' '
         MVC   MLT+1(L'MLT-1),MLT
         B     *+L'*(R15)
         B     FOUND               RC=0  RESOURCE FOUND
         B     NOTFOUND            RC=4  RESOURCE NOT FOUND
         B     FULL                RC=8  QUEUE AREA FULL
         B     ABEND               RC=12 ABNORMAL SITUATION
         B     INVLD               RC=16 INVALID SYSNAME
FOUND    BAS   R9,LISTDTL          DISPLAY COMPLETED TABLE
         CLC   TCOUNT(4),=F'0'     ANYTHING IN THE TABLE ?
         BNE   GOODTAB             YES
         MVC   MST(8),=CL8'NONE ...' SET SHORT MESSAGE
         CLI   JOB,C' '            JOB BLANK ?
         BE    NOTJOB2             YES
         CLI   MINOR,C' '          MINOR BLANK ?
         BE    *+L'*+10            YES
         MVC   MLT(28),=CL28'NONE FOR JOB-NAME/MINOR-NAME'
         B     STMSG
         MVC   MLT(17),=CL17'NONE FOR JOB-NAME'
         B     STMSG
NOTJOB2  MVC   MLT(19),=CL19'NONE FOR MINOR-NAME'
         B     STMSG
GOODTAB CALL   ISPLINK,(#TBTOP,#ENQTAB),VL
         LTR   R15,R15
         BNZ   ERR12
        CALL   ISPLINK,(#TBDISPL,#ENQTAB,#VENQ2),VL
         LTR   R15,R15
         BZ    DVENQ1
         CH    R15,=H'8'
         BE    DVENQ1
         B     ERR13
NOTFOUND MVC   MST(8),=CL8'NONE ...' SET SHORT MESSAGE
         CLI   MINOR,C' '          MINOR BLANK ?
         BE    TSTMAJ              YES
         CLI   JOB,C' '            JOB BLANK ?
         BE    *+L'*+10            YES
         MVC   MLT(39),=CL39'NONE FOR JOB-NAME/MAJOR-NAME/MINOR-NAME'
         B     STMSG
         MVC   MLT(30),=CL30'NONE FOR MAJOR-NAME/MINOR-NAME'
         B     STMSG
TSTMAJ   CLI   MAJOR,C' '          MAJOR BLANK ?
         BE    CNTNTN              YES
         CLI   JOB,C' '            JOB BLANK ?
         BE    *+L'*+10            YES
         MVC   MLT(28),=CL28'NONE FOR JOB-NAME/MAJOR-NAME'
         B     STMSG
         MVC   MLT(19),=CL19'NONE FOR MAJOR-NAME'
         B     STMSG
CNTNTN   MVC   MLT(17),=CL17'NO CONTENTION ...'
         B     STMSG
FULL     BAS   R9,LISTDTL          LIST FIRST BLOCK
         L     R1,SVGETM           LOAD ADDR OF GETMAIN
         L     R0,SVGETL           LOAD LENGTH OF GETMAIN
        FREEMAIN R,LV=(0),A=(1)
         XC    SVGETM,SVGETM
         B     SCAN                REISSUE GQSCAN
ABEND    MVC   MST(8),=CL8'ERROR   ' SET SHORT MESSAGE
         MVC   MLT(34),=CL34'RC=12 - ENQ/GRS ABNORMAL SITUATION'
         B     STMSG
INVLD    MVC   MST(8),=CL8'INVALID ' SET SHORT MESSAGE
         MVC   MLT(31),=CL31'RC=16 - ENQ/GRS INVALID SYSNAME'
         SPACE 1
*------- PASS MESSAGES TO ISPF
         SPACE 1
STMSG   CALL   ISPLINK,(#VREP,MS,MSL,MST),VL
         LTR   R10,R15
         BNZ   ERRSM
        CALL   ISPLINK,(#VREP,ML,MLL,MLT),VL
         LTR   R10,R15
         BNZ   ERRSM
        CALL   ISPLINK,(#SETM,#MSGID),VL
         LTR   R10,R15
         BZ    DVENQ1              GO DISPLAY INITIAL PANEL
ERRSM    O     R10,=A(X'0000FF00')
         B     LEAVE
         EJECT
*------- DELETE DISPLAY VARIABLES TO ISPF
         SPACE 1
RETURN  CALL   ISPLINK,(#VDELETE,#JOB),VL
         LTR   R15,R15
         BNZ   ERR20
        CALL   ISPLINK,(#VDELETE,#MAJOR),VL
         LTR   R15,R15
         BNZ   ERR21
        CALL   ISPLINK,(#VDELETE,#MINOR),VL
         LTR   R15,R15
         BNZ   ERR22
        CALL   ISPLINK,(#VDELETE,#X),VL
         LTR   R10,R15
         BNZ   ERR23
         SPACE 1
*------- TERMINATION
         SPACE 1
        PRINT  GEN
LEAVE    TM    SWITCH,TBLCRT       TABLE CREATED ?
         BZ    FRAREA              NO
        CALL   ISPLINK,(#TBEND,#ENQTAB),VL
         LTR   R10,R10
         BNZ   FRAREA
         LR    R10,R15
FRAREA   ICM   R1,B'1111',SVGETM   LOAD ADDR OF GETMAIN
         BZ    QUIT                BRANCH IF NONE
         L     R0,SVGETL           LOAD LENGTH OF GETMAIN
        FREEMAIN R,LV=(0),A=(1)
         XC    SVGETM,SVGETM
QUIT    $XRET  CC=(R10)            RETURN
         EJECT
*------- GENERATE ERROR MESSAGES
         SPACE 1
ERR1     MVC   MLT+47(8),=CL8'JOB'
         B     ERRA
ERR2     MVC   MLT+47(8),=CL8'MAJOR'
         B     ERRA
ERR3     MVC   MLT+47(8),=CL8'MINOR'
         B     ERRA
ERR4     MVC   MLT+47(8),=CL8'X'
         B     ERRA
ERR5     MVC   MLT+47(8),=CL8'T1'
         B     ERRA
ERR6     MVC   MLT+47(8),=CL8'T2'
ERRA     MVC   MLT+34(10),=CL10'  VARIABLE'
         MVC   MLT+23(8),#VDEFINE
         B     ERRET
ERR10    MVC   MLT+47(8),#VENQ1
         MVC   MLT+34(10),=CL10'     PANEL'
         MVC   MLT+23(8),#DISPLAY
         B     ERRET
ERR11    MVC   MLT+23(8),#TBCREAT
         B     ERRB
ERR12    MVC   MLT+23(8),#TBTOP
         B     ERRB
ERR13    MVC   MLT+23(8),#TBDISPL
         B     ERRB
ERR14    MVC   MLT+23(8),#TBADD
ERRB     MVC   MLT+47(8),#ENQTAB
         MVC   MLT+34(10),=CL10'TABLE NAME'
         B     ERRET
ERR20    MVC   MLT+47(8),=CL8'JOB'
         B     ERRC
ERR21    MVC   MLT+47(8),=CL8'MAJOR'
         B     ERRC
ERR22    MVC   MLT+47(8),=CL8'MINOR'
         B     ERRC
ERR23    MVC   MLT+47(8),=CL8'X'
ERRC     MVC   MLT+34(10),=CL10'  VARIABLE'
         MVC   MLT+23(8),#VDELETE
ERRET    CVD   R15,WORKD           EDIT RC
         MVC   MST+4(4),=XL4'40202120'
         ED    MST+4(4),WORKD+6
         MVC   MST(4),=CL4'RC ='
         MVC   MLT(23),=CL23'-- ERROR AT FUNCTION : '
         MVC   MLT+31(3),=CL3' / '
         MVC   MLT+44(3),=CL3' : '
         MVC   MLT+55(4),=CL4' -- '
         MVC   MLT+59(L'MLT-59),MLT+58
        CALL   ISPLINK,(#VREP,MS,MSL,MST),VL
         LR    R10,R15
        CALL   ISPLINK,(#VREP,ML,MLL,MLT),VL
         LTR   R10,R10
         BZ    *+L'*+6
         SLL   R15,8
         OR    R10,R15
        CALL   ISPLINK,(#SETM,#MSGID),VL
         LTR   R10,R10
         BZ    LEAVE
         SLL   R15,16
         OR    R10,R15
         B     LEAVE
         EJECT
*------- BUILD 'ENQTAB' ROUTINE (LINK = R9)
         SPACE 1
LISTDTL  L     R5,SVGETM           GETMAIN'D AREA ADDRESS.
         AL    R5,SVGETL           POINT PAST END
         CR    R4,R5               POINTING PAST END ?
         BNLR  R9                  YES.
         CLC   0(4,R4),=F'0'       CHECK IF LAST RESOURCE
         BER   R9                  YES... RETURN
         LA    R5,40(R4)           LOAD ADDR OF RIBVAR
         USING RIBVAR,R5
         LR    R6,R5
         AH    R6,RIBVLEN          ADDRESS RIBE DESECT
         USING RIBE,R6
         L     R7,RIBNRIBE         LOAD NUMBER OF ENTRIES
         TM    RIBSCOPE,RIBSYS     SYSTEM
         BNO   CKRIBSS
         MVC   X1SCOPE(8),=CL8'SYSTEM'
         B     CKMAJ
CKRIBSS  TM    RIBSCOPE,RIBSYSS    SYSTEMS
         BNO   CKRIBST
         MVC   X1SCOPE(8),=CL8'SYSTEMS'
         B     CKMAJ
CKRIBST  TM    RIBSCOPE,RIBSTEP    STEP
         BNO   CKRIBGL
         MVC   X1SCOPE(8),=CL8'STEP'
         B     CKMAJ
CKRIBGL  TM    RIBSCOPE,RIBGLBL    GLOBAL
         BNO   CKRIBLO
         MVC   X1SCOPE(8),=CL8'GLOBAL'
         B     CKMAJ
CKRIBLO  MVC   X1SCOPE(8),=CL8'LOCAL'
CKMAJ    MVC   X3SCOPE(8),X1SCOPE
         MVC   X1MAJOR(8),RIBQNAME MOVE MAJOR NAME
         MVC   X3MAJOR(8),X1MAJOR
CKMIN    XR    R2,R2               CLEAR R2
         IC    R2,RIBRNMLN
         CL    R2,MINLG            CHECK IF MINOR GREATER MAX CHAR
         BNH   *+L'*+4             NO... OK
         LA    R2,L'MINOR          MAKE MAX CHAR
         BCTR  R2,0                DECREMENT BY 1 FOR MOVE
         MVC   X1MINOR(L'MINOR),XB BLANK OUT
         MVC   X3MINOR(L'MINOR),XB MINORS
         EX    R2,DSPLMIN1         MOVE MINOR
         EX    R2,DSPLMIN2         MOVE MINOR
         L     R14,=A(TT)
         TR    X1MINOR(L'X1MINOR),0(R14) TRANSLATE NON PRINTABLES
         TR    X3MINOR(L'X3MINOR),0(R14)
         NI    SWITCH,255-MNTOK    * MINOR OK *
         CLI   MINOR,C' '          MINOR BLANK ?
         BE    NOTMIN              YES
         CLC   MINOR(L'MINOR),X1MINOR IS THIS THE RIGHT MINOR ?
         BE    NOTMIN              YES
         OI    SWITCH,MNTOK        * MINOR NOT OK *
         B     OWNER
NOTMIN   CLI   JOB,C' '            IS JOB BLANK ?
         BNE   OWNER               NO
         MVC   X,X1                YES, CREATE LINE 1
        CALL   ISPLINK,(#TBADD,#ENQTAB,#X),VL
         LTR   R15,R15
         BNZ   ERR14
         L     R2,TCOUNT
         LA    R2,1(R2)
         ST    R2,TCOUNT
OWNER    TM    SWITCH,MNTOK        MINOR OK ?
         BO    CONT3               NO, SKIP
         TM    RIBERFLG,RIBETYPE   SHR/EXC
         BNO   CKRIBEX
         MVC   X2TYPE(4),=CL4'SHR '
         MVC   X3TYPE(4),X2TYPE
         B     CKOWN
CKRIBEX  MVC   X2TYPE(4),=CL4'EXCL'
         MVC   X3TYPE(4),X2TYPE
CKOWN    MVC   X2JOB(8),RIBEJBNM   MOVE JOBNAME OF REQUESTER
         TM    RIBESFLG,RIBESTAT
         BZ    *+L'*+10
         MVC   X2STATUS(7),=CL7'OWNER  '
         B     *+L'*+6
         MVC   X2STATUS(7),=CL7'WAITING'
         MVC   X3STATUS(7),X2STATUS
         MVC   X2(8),=XL8'0000000000000000'
         MVC   X2SYSID(4),RIBESYSN
         CLI   JOB,C' '
         BE    NOTJOB3
         CLC   JOB,RIBEJBNM
         BNE   CONT3
         MVC   X,X3
        CALL   ISPLINK,(#TBADD,#ENQTAB,#X),VL
         LTR   R15,R15
         BNZ   ERR14
         L     R2,TCOUNT
         LA    R2,1(R2)
         ST    R2,TCOUNT
         B     CONT3
NOTJOB3  MVC   X,X2
        CALL   ISPLINK,(#TBADD,#ENQTAB,#X),VL
         LTR   R15,R15
         BNZ   ERR14
         L     R2,TCOUNT
         LA    R2,1(R2)
         ST    R2,TCOUNT
CONT3    LA    R6,36(R6)
         MVC   X1,XB
         MVC   X2,XB
         BCT   R7,OWNER
         LR    R4,R6
         B     LISTDTL
         SPACE 1
DSPLMIN1 MVC   X1MINOR(0),RIBRNAME <<EXECUTED>>
DSPLMIN2 MVC   X3MINOR(0),RIBRNAME <<EXECUTED>>
         EJECT
*------- CONSTANTS AND WORK-AREAS
         SPACE 1
WORKD    DC    D'0'
SVGETM   DC    F'0'
SVGETL   DC    F'4096'
TKN      DC    F'0'
TCOUNT   DC    F'0'
LGH8     DC    F'8'
LG80     DC    F'80'
MINLG    DC    A(L'MINOR)
MSL      DC    A(L'MST)
MLL      DC    A(L'MLT)
         SPACE 1
JOB      DC    CL8' '
MAJOR    DC    CL8' '
MINOR    DC    CL44' '
X        DC    CL80' '
T1       DC    CL80' '
T2       DC    CL80' '
XB       DC    CL80' '
MST      DC    CL8' '
MLT      DC    CL72' '
SWITCH   DC    XL1'0'
TBLCRT   EQU   X'80'
MNTOK    EQU   X'01'
         SPACE 1
#DISPLAY DC    CL8'DISPLAY'
#NOWRITE DC    CL8'NOWRITE'
#REPLACE DC    CL8'REPLACE'
#ENQTAB  DC    CL8'ENQTAB'
#VDEFINE DC    CL8'VDEFINE'
#VDELETE DC    CL8'VDELETE'
#VREP    DC    CL8'VREPLACE'
#TBCREAT DC    CL8'TBCREATE'
#TBDISPL DC    CL8'TBDISPL'
#TBADD   DC    CL8'TBADD'
#TBTOP   DC    CL8'TBTOP'
#TBEND   DC    CL8'TBEND'
#VENQ1   DC    CL8'PAJVQ1'
#VENQ2   DC    CL8'PAJVQ2'
#SETM    DC    CL8'SETMSG'
#MSGID   DC    CL8'PAJZ000'
         SPACE 1
MS       DC    C'(EUMSGS) '
ML       DC    C'(EUMSGL) '
#JOB     DC    C'(JOB) '
#MAJOR   DC    C'(MAJOR) '
#MINOR   DC    C'(MINOR) '
#X       DC    C'(X) '
#T1      DC    C'(T1) '
#T2      DC    C'(T2) '
#CHAR    DC    C'CHAR '
         SPACE 1
*------- LINE 1 FOR NON-JOB-SPECIFIC
         SPACE 1
X1       DC    CL80' '
X1SCOPE  EQU   X1
X1MAJOR  EQU   X1+8
X1MINOR  EQU   X1+17
         SPACE 1
*------- LINE 2 FOR NON-JOB-SPECIFIC
         SPACE 1
X2       DC    CL80' '
X2STATUS EQU   X2+8
X2TYPE   EQU   X2+17
X2SYSID  EQU   X2+22
X2JOB    EQU   X2+27
         SPACE 1
*------- LINE 1 FOR JOB-SPECIFIC
         SPACE 1
X3       DC    CL80' '
X3TYPE   EQU   X3
X3STATUS EQU   X3+5
X3SCOPE  EQU   X3+13
X3MAJOR  EQU   X3+21
X3MINOR  EQU   X3+30
         EJECT
        LTORG
         EJECT
TT       EQU   *,256
*                0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  00-0F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  10-1F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  20-2F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  30-3F
         DC    X'404B4B4B4B4B4B4B4B4B4A4B4C4D4E4F'  40-4F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  50-5F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  60-6F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  70-7F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  80-8F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  90-9F
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  A0-AF
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'  B0-BF
         DC    X'C0C1C2C3C4C5C6C7C8C94B4B4B4B4B4B'  C0-CF
         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B'  D0-DF
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'  E0-EF
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'  F0-FF
         SPACE 1
BLTABLE  EQU   *,256
*                0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'00000000000000000000000000000000'  00-0F
         DC    X'00000000000000000000000000000000'  10-1F
         DC    X'00000000000000000000000000000000'  20-2F
         DC    X'00000000000000000000000000000000'  30-3F
         DC    X'01000000000000000000000000000000'  40-4F
         DC    X'00000000000000000000000000000000'  50-5F
         DC    X'00000000000000000000000000000000'  60-6F
         DC    X'00000000000000000000000000000000'  70-7F
         DC    X'00000000000000000000000000000000'  80-8F
         DC    X'00000000000000000000000000000000'  90-9F
         DC    X'00000000000000000000000000000000'  A0-AF
         DC    X'00000000000000000000000000000000'  B0-BF
         DC    X'00000000000000000000000000000000'  C0-CF
         DC    X'00000000000000000000000000000000'  D0-DF
         DC    X'00000000000000000000000000000000'  E0-EF
         DC    X'00000000000000000000000000000000'  F0-FF
         SPACE 1
        PRINT  NOGEN
RIB     ISGRIB
        CVT    DSECT=YES,LIST=YES
         SPACE 2
         END
