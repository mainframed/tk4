ISREDIT  MACRO (APARM,BPARM) NOPROCESS
/*  MACRO NAME : FC                                                 */
/*    FUNCTION : FIND PENDING EDIT LINE COMMANDS                    */
/*      SYNTAX : FC OR FC F(IRST) OR FC N(EXT) OR FC P(REV)         */
/*               FC H(ELP)                                          */
/*       DEBUG : FC L(IST)                                          */
/*               FC F(IRST)/N(EXT)/P(REV) L(IST)                    */
/*      ORIGIN : CBT TAPE 93 FEB, FILE 95                           */
  IF ((&STR(&BPARM.) = &STR(LIST)) OR +
     (&STR(&BPARM.) = &STR(L))) THEN CONTROL LIST CONLIST SYMLIST
  ELSE IF ((&STR(&APARM.) = &STR(LIST)) OR +
    (&STR(&APARM.) = &STR(L))) THEN DO
    CONTROL LIST CONLIST SYMLIST
    SET &APARM = &Z
    END
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
    ISPEXEC CONTROL DISPLAY SAVE
    ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTFC)
    SET &FCCC = &LASTCC
    ISPEXEC CONTROL DISPLAY RESTORE
    EXIT CODE(&FCCC)
    END
  SET &PTR = &Z
  IF &STR(&APARM.) = &Z THEN SET &PTR = &STR(FIRST)
  IF ((&STR(&APARM.) = &STR(FIRST)) OR +
     (&STR(&APARM.) = &STR(F))) THEN SET &PTR = &STR(FIRST)
  IF ((&STR(&APARM.) = &STR(NEXT)) OR +
     (&STR(&APARM.) = &STR(N))) THEN SET &PTR = &STR(NEXT)
  IF ((&STR(&APARM.) = &STR(PREV)) OR +
     (&STR(&APARM.) = &STR(P))) THEN SET &PTR = &STR(PREV)
  IF &PTR = &Z THEN DO
    SET ZEDSMSG = &STR(INVALID OPERAND)
    SET ZEDLMSG = ENTER "FC HELP" COMMAND FOR OPERAND SYNTAX
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(0)
    END
  ISREDIT LOCATE COM &PTR
  SET &FCCC = &LASTCC
  EXIT CODE(&FCCC)
