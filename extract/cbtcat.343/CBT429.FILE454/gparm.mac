GPAR     TITLE 'GET PARM. FIELD SUBROUTINE.'
GPARM    START 0
         SPACE 1
GPARM   AMODE  ANY
GPARM   RMODE  ANY
         SPACE 1
* CALLING SEQUENCE : CALL GPARM,(A),VL
* ------------------
*
*        WHERE : A - IS THE ADDRESS OF A FIELD (FORMAT CL...) INTO
*                    WHICH THE CONTENTS OF THE 'EXEC STATEMENT'
*                    PARM. FIELD IS MOVED AS DESCRIBED BELOW :
*
*                    A --->          DC    AL2(L'PARM)
*                           PARM     DC    C'...TEXT...'
*
*                    OR IN CASE THERE IS NO PARM. FIELD :
*
*                    A --->          DC    AL2(0)
*
*                    THIS ARGUMENT IS MANDATORY BECAUSE INFORMATION IS
*                    RETURNED TO THE CALLER.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
        PRINT  NOGEN
         SPACE 1
        CVT    DSECT=YES,LIST=YES
         SPACE 1
        IKJTCB LIST=YES
         SPACE 1
GPARM    CSECT                     RESTORE ORIGINAL CSECT.
         SPACE 1
        PRINT  GEN
         EJECT
        $XENT  BASE=R12
         L     R2,0(R1)
         L     R3,CVTPTR           CVT POINTER.
         USING CVT,R3
         L     R4,CVTTCBP
         DROP  R3
         L     R3,4(R4)            CURRENT TCB.
         USING TCB,R3
         L     R4,TCBFSA           FIRST SAVE AREA OF PROBLEM.
         DROP  R3
         L     R1,24(R1)           RESTORE ENTRY REG. 1.
         L     R1,0(R1)            PARM. FIELD ADDRESS.
         LH    R3,0(R1)            GET PARM. FIELD LENGTH.
         LA    R3,1(R3)
         EX    R3,MVPARM           GIVE PARM. FIELD TO CALLER.
EXIT    $XRET  CC=0
         SPACE 1
MVPARM   MVC   0(*-*,R2),0(R1)
         SPACE 2
         END
