PDSW     TITLE 'PARTITIONED DATA-SET - WHERE FUNCTION.'
PDSW     START 0
         SPACE 1
* PURPOSE :    SCAN A PARTITIONED DATA-SET DIRECTORY TO SEARCH A
* ---------    MEMBER NAME OR ALL MEMBERS HAVING THE NAME STARTING
*              BY A GIVEN CHARACTERS STRING.
*
* FILE NAMES : DD-NAME 'SYSUT0'   - INPUT DATA-SET (DSORG=PO).
* ------------ DD-NAME 'SORTPRT'  - OUTPUT LIST.
*
* TSO ACCESS : USE %CERCA CLIST TO INVOKE THE WHERE FUNCTION.
* ------------ CERCA CLIST DESCRIPTION :
*                   PROC   2 LIBRARY MODULE
*                   ALLOC  F(SYSUT0) DA(&LIBRARY) SHR
*                   ALLOC  F(SORTPRT) DA(*)
*                   PDSW   &MODULE
*                   FREE   F(SYSUT0 SORTPRT)
*                   TSO    END
         SPACE 1
PDSW    AMODE  24
PDSW    RMODE  24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12
         LR    R8,R1               SAVE PARM. FIELD ADDRESS
        $TEW$EC NOTTSO=CALLM,NOTCMD=CALLM,MSG=PUTMSG,ERR=SETRC
        $TEW$PP PARM=PARMLST,REG=R3
         TM    MODULE+6,X'80'      MODULE SPECIFIED?
         BZ    NOMOD               NO
         L     R2,MODULE           GET POINTER TO MODULE NAME
         LH    R8,MODULE+4         GET LENGTH
         LTR   R8,R8
         BNP   NOMOD
         BCT   R8,MOVE+L'MOVE
MOVE     MVC   PAR(*-*),0(R2)      <<EXECUTED>>
         EX    R8,MOVE
         DROP  R3
         BAS   R2,NOMOD+L'NOMOD
NOMOD    XR    R2,R2
        IKJRLSA #TSANSW
         LTR   R2,R2
         BNZ   TPAR
         XR    R8,R8
         B     NOPARM
         SPACE 1
CALLM    L     R2,0(R8)            GET PARM. FIELD ADDRESS
         LH    R8,0(R2)            PARM. FIELD LENGTH
         LTR   R8,R8
         BNP   NOPARM
         LA    R2,2(R2)            START OF PARM. FIELD TEXT
         CH    R8,=Y(L'PAR)
         BNH   *+L'*+4
         LH    R8,=Y(L'PAR)
         BCTR  R8,0
         EX    R8,MOVE
TPAR     CLI   PAR,C'*'
         BE    NOPARM
         OI    SWITCH,SELEZ
         SPACE 1
NOPARM   MVC   ERMODD,UTILIN+(DCBDDNAM-IHADCB)
        OPEN   (UTILIN,INPUT)
         TM    UTILIN+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    OPNERR
         OI    SWITCH,UTOP
         MVC   ERMODD,SORTPRT+(DCBDDNAM-IHADCB)
        OPEN   (SORTPRT,OUTPUT)
         TM    SORTPRT+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    OPNERR
         OI    SWITCH,SPOP
         LA    R7,LINE
         LA    R5,LINE+72
GTNX    GET    UTILIN,AREA+8       READ 1 DIRECTORY BLOCK
         LH    R2,AREA+8           DIRECTORY BLOCK LENGTH
         LA    R3,AREA+8           START ADDRESS
         AR    R2,R3               END OF BLOCK ADDRESS
         LA    R3,2(R3)            START OF NAME ADDRESS
LOOP     CLC   =XL8'FFFFFFFFFFFFFFFF',0(R3) DIRECTORY BLOCK END?
         BE    QUIT                YES
         TM    SWITCH,SELEZ        NO, SELEZ IS ON?
         BZ    *+L'*+18            NO
         CLC   0(1,R3),PAR
         BH    QUIT
         EX    R8,COMPAR
         BNE   NULLFO
         TM    11(R3),X'80'
         BNO   NOTALIAS
         LR    R4,R3
         LA    R4,11(R4)
         IC    R1,0(R4)
         N     R1,=F'31'
         SLL   R1,1
         AR    R4,R1
         CLI   0(R4),0
         BE    MVSALIA
         CLI   0(R4),1
         BE    MVSALIA
         SH    R4,=H'7'
         B     MVSALIA+L'MVSALIA
MVSALIA  SH    R4,=H'9'
         MVC   QUA,0(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI1
         MVC   QUA,1(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI1
         MVC   QUA,2(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI1
         MVC   QUA,3(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI1
         MVC   QUA,4(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI1
         MVC   QUA,5(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI1
         MVC   QUA,6(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI1
         MVC   QUA,7(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BNE   QUI1+L'QUI1
QUI1     SH    R4,=H'4'
         MVC   QUA,0(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI2
         MVC   QUA,1(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI2
         MVC   QUA,2(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BE    QUI2
         MVC   QUA,3(R4)
         BAS   R10,CER
         CLI   CAR,C'0'
         BNE   QUI2+L'QUI2
QUI2     MVC   0(8,R4),=CL8' '
         MVC   TRA(8),0(R4)
         MVI   BLI,C' '
         MVC   BLI+1(L'BLI-1),BLI
         MVC   BLI(8),0(R3)
         LA    R6,BLI
         CLI   0(R6),C' '
         BE    *+L'*+8
         LA    R6,1(R6)
         B     *-12
         MVI   0(R6),C'='
         MVC   1(8,R6),TRA
         BAS   R10,ROUTINE
         B     NULLFO
NOTALIAS NI    11(R3),X'1F'        REMAIN ONLY LENGTH(HW) OF FIELDS
         CLI   11(R3),X'0B'
         BE    NOSSI               MVT LINK WITHOUT SSI
         CLI   11(R3),X'0C'
         BE    NOSSI               MVS MVS WITHOUT SSI
         CLI   11(R3),X'0D'
         BE    SSI                 MVT LINK WITH SSI
         CLI   11(R3),X'0E'
         BE    SSI                 MVS LINK WITH SSI
         CLI   11(R3),X'00'
         BE    NOSSI               IEBUPDTE WITHOUT SSI
         CLI   11(R3),X'02'
         BE    IEBSSI              IEBUPDTE WITH SSI
NOSSI    MVC   BLI(8),0(R3)        MOVE MEMBER NAME
         MVI   BLI+8,C' '
         MVC   BLI+9(L'BLI-9),BLI+8
         BAS   R10,ROUTINE
         B     NULLFO
IEBSSI   UNPK  TRA(9),12(5,R3)
         B     SSI+L'SSI
SSI      UNPK  TRA(9),34(5,R3)
         TR    TRA(8),TBZ-C'0'
         MVI   BLI,C' '
         MVC   BLI+1(L'BLI-1),BLI
         MVC   BLI(8),0(R3)
         LA    R6,BLI
         CLI   0(R6),C' '
         BE    *+L'*+8
         LA    R6,1(R6)
         B     *-12
         MVI   0(R6),C'/'
         MVC   1(8,R6),TRA
         BAS   R10,ROUTINE
NULLFO   LA    R3,11(R3)
         IC    R1,0(R3)
         N     R1,=F'31'
         SLL   R1,1                TWICE
         LA    R3,1(R3)
         AR    R3,R1               POINTS TO NEW NAME OF NEXT BLDL
         CR    R3,R2
         BL    LOOP
         B     GTNX
OPNERR   LA    R1,ERMO
         B     DIRERR+L'DIRERR
DIRERR   LA    R1,ERMC
         BAS   R14,PUTMSG
SETRC    LA    R2,16               RC = 16 ----------------------------
         B     CLUT
QUIT    PUT    SORTPRT,PRTLNE
         XR    R2,R2               RC = 0 -----------------------------
CLUT     TM    SWITCH,UTOP
         BZ    CLSP
        CLOSE  (UTILIN)
CLSP     TM    SWITCH,SPOP
         BZ    LEAVE
        CLOSE  (SORTPRT)
LEAVE   $XRET  CC=(R2)
         SPACE 1
COMPAR   CLC   0(*-*,R3),PAR       <<EXECUTED>>
         SPACE 1
CER      CLI   QUA,C' '
         BE    QUI
         CLI   QUA,C'$'
         BE    QUI
         CLI   QUA,C'@'
         BE    QUI
         CLI   QUA,C'#'
         BE    QUI
         CLI   QUA,C'A'
         BNL   QUI
         MVI   CAR,C'0'
         BR    R10
QUI      MVI   CAR,C'1'
         BR    R10
         SPACE 1
ROUTINE  CR    R7,R5
         BL    ADVN
        PUT    SORTPRT,PRTLNE
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         LA    R7,LINE
ADVN     MVC   0(L'BLI,R7),BLI
         LA    R7,L'BLI(R7)
         BR    R10
         EJECT
*------- CONSTANTS AND WORK AREAS.
         SPACE 1
AREA     DC    0D'0',264X'0'
        $TEW$WA
PAR      DC    CL8' '
         SPACE 1
SWITCH   DC    XL1'0'
UTOP     EQU   X'80'
SPOP     EQU   X'40'
SELEZ    EQU   X'01'
         SPACE 1
PRTLNE   DC    CL1' '
LINE     DC    CL78' '
PRTLG    EQU   *-PRTLNE
CAR      DC    CL1' '
QUA      DC    CL1' '
TBZ      DC    CL16'0123456789ABCDEF'
TRA      DC    CL16' '
BLI      DC    CL18' '
         SPACE 1
ERMO    WTO    ' ->          : OPEN FAILED.',ROUTCDE=11,DESC=7,MF=L
ERMODD   EQU   ERMO+8,8
ERMC    WTO    ' -> SYSUT0   : DIR-EOF ERROR.',ROUTCDE=11,DESC=7,MF=L
         SPACE 1
        LTORG
         SPACE 1
        PRINT  NOGEN
         SPACE 1
UTILIN  DCB    DSORG=PS,MACRF=GM,RECFM=FB,LRECL=256,BLKSIZE=256,       X
               EODAD=DIRERR,DDNAME=SYSUT0
SORTPRT DCB    DSORG=PS,MACRF=PM,RECFM=FA,LRECL=PRTLG,BLKSIZE=PRTLG,   X
               DDNAME=SORTPRT
         SPACE 1
PARMLST IKJPARM
MODULE  IKJIDENT 'MODULE-PREFIX',MAXLNTH=8,FIRST=ANY,OTHER=ANY
        IKJENDP
         SPACE 1
        $TEW$DS
        DCBD   DSORG=PS,DEVD=DA
         SPACE 1
         END
