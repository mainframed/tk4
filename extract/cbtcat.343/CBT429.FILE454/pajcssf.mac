PROC     0 DEBUG(NONE)
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       PURPOSE : CLIST TO CREATE JCL TO RUN OPTION 3.14 SEARCH-FOR */
/*                 IN BATCH                                          */
/*        AUTHOR : J. KALINICH, X4521                                */
/*    CBT ORIGIN : EXTRACTED FROM CBT TAPE 94 APR, FILE 078 (SUPERC) */
/*    ADAPTED BY : MOINIL P.A.                                       */
/*                 PRE-REQUISITE PRODUCTS : IOFSPF                   */
/*                 PRE-REQUISITE COMMANDS : EUSID                    */
/*                                          EUSUB                    */
/*                                          INTRDR                   */
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN DO
           CONTROL MSG
           EXIT CODE(16)
           END
         IF &STR(&DEBUG) EQ &STR(DEBUG) THEN +
           CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
         ELSE CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
         IF (&STR(&DEBUG) NE &STR(SCAN)) AND +
           (&STR(&DEBUG) NE &STR(COPY)) THEN SET DEBUG = &Z
         ISPEXEC CONTROL ERRORS RETURN
/*                 ASK BATCH JOB RUNNING VALUES :                    */
/*                 CPU TIME, I/O TIME AND LINES                      */
         SET EUNMN = &Z
         SET EUNSC = &Z
         SET EUNIO = &Z
         SET EUNLN = &Z
         SET EUIOF = &Z
         SET RCC = 0
         DO WHILE &RCC LT 8
           ISPEXEC DISPLAY PANEL(PAJSSF)
           SET RCC = &LASTCC
           IF &RCC GT 8 THEN DO
             SET ZERRSM = &STR(DISPLAY ERROR)
             SET ZERRLM = &STR(DISPLAY PANEL "PAJSSF" FAILED +
                               / RC=&RCC)
             ISPEXEC SETMSG MSG(ISRZ002)
             ISPEXEC CONTROL ERRORS CANCEL
             EXIT CODE(20)
             END
           END
         IF &EUNMN NE 0 AND &EUNSC NE 0 THEN +
           SET EUCPU = &STR(&EUNMN,&EUNSC)
         ELSE IF &EUNMN EQ 0 THEN SET EUCPU = &STR(,&EUNSC)
         ELSE SET EUCPU = &STR(&EUNMN)
         SET EUIO = &STR(&EUNIO)
         SET EULINES = &STR(&EUNLN)
/*                 GET TSO CLIST VARIABLES OF CURRENT ENVIRONMENT    */
         EUSID
         SET RCC = &LASTCC
         IF &RCC NE 0 THEN DO
           SET ZERRSM = &STR(EUSID ERROR)
           SET ZERRLM = &STR(GET TSO CLIST VARIABLES "EUSID" FAILED +
                             / RC=&RCC)
           ISPEXEC SETMSG MSG(ISRZ002)
           ISPEXEC CONTROL ERRORS CANCEL
           EXIT CODE(20)
           END
BACK:    ISPEXEC DISPLAY PANEL(ISRSFSPR)
         SET RCC = &LASTCC
         IF &RCC EQ 8 THEN EXIT CODE(8)
         ELSE IF &RCC GT 8 THEN DO
           SET ZERRSM = &STR(DISPLAY ERROR)
           SET ZERRLM = &STR(DISPLAY PANEL "ISRSFSPR" FAILED +
                             / RC=&RCC)
           ISPEXEC SETMSG MSG(ISRZ002)
           ISPEXEC CONTROL ERRORS CANCEL
           EXIT CODE(20)
           END
         IF &STR(&ZSSFNDSN) EQ &Z THEN +
           SET NEWDD = &ZSSFNPRJ..&ZSSFNGR1..&ZSSFNTYP
         ELSE DO
           IF &SUBSTR(1:1,&ZSSFNDSN) EQ &STR(') THEN +
             SET NEWDD = &STR(&ZSSFNDSN)
           ELSE SET NEWDD = &STR(&SYSUID..&ZSSFNDSN)
/*                 STRIP OUT (*) IF ENTERED WITH OTHER DSNAME        */
           IF &SUBSTR(&LENGTH(&STR(&NEWDD))-3:+
             &LENGTH(&STR(&NEWDD)),&STR(&NEWDD)) EQ &STR((*)') THEN +
             SET NEWDD = &SUBSTR(2:&LENGTH(&STR(&NEWDD))-4,&STR(&NEWDD))
           IF &SUBSTR(&LENGTH(&STR(&NEWDD))-2:+
             &LENGTH(&STR(&NEWDD)),&STR(&NEWDD)) EQ &STR((*)) THEN +
             SET NEWDD = &SUBSTR(1:&LENGTH(&STR(&NEWDD))-3,&STR(&NEWDD))
           END
         IF &ZSSFNVOL EQ &Z THEN SET NEWVOL = &Z
         ELSE SET NEWVOL = &STR(,VOL=SER=&ZSSFNVOL,UNIT=SYSALLDA)
         IF &ZSSFMULT EQ &STR(YES) THEN DO
           ISPEXEC DISPLAY PANEL(ISRSFSST)
           SET RCC = &LASTCC
           IF &RCC EQ 8 THEN GOTO BACK
           ELSE IF &RCC GT 8 THEN DO
             SET ZERRSM = &STR(DISPLAY ERROR)
             SET ZERRLM = &STR(DISPLAY PANEL "ISRSFSST" FAILED +
                               / RC=&RCC)
             ISPEXEC SETMSG MSG(ISRZ002)
             ISPEXEC CONTROL ERRORS CANCEL
             EXIT CODE(20)
             END
           END
         SET I = 1
         DO WHILE &I LE 10
           SET SRCH&I = &Z
           SET SFS = &&ZSSFS&I
           IF &STR(&SFS) NE &STR() THEN DO
             IF &SUBSTR(1:1,&STR(&SFS)) NE &STR(') THEN DO
               IF &LENGTH(&STR(&SFS)) GT 5 THEN DO
                 IF &SUBSTR(&LENGTH(&STR(&SFS))-4:+
                  &LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR( WORD) THEN +
                  SET SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-3,&STR(&SFS))
                 END
               IF &LENGTH(&STR(&SFS)) GT 7 THEN DO
                 IF &SUBSTR(&LENGTH(&STR(&SFS))-6:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR( PREFIX) THEN +
                  SET SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-5,&STR(&SFS))
                 END
               IF &LENGTH(&STR(&SFS)) GT 7 THEN DO
                 IF &SUBSTR(&LENGTH(&STR(&SFS))-6:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR( SUFFIX) THEN +
                  SET SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-5,&STR(&SFS))
                 END
               IF &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR( C) OR +
                 &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR( P) OR +
                 &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR( S) OR +
                 &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                 &LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR( W) THEN DO
                 SET SFS = &STR(')+
                   &SUBSTR(1:&LENGTH(&STR(&SFS))-2,&STR(&SFS))+
                   &STR(',)&SUBSTR(&LENGTH(&STR(&SFS)),&STR(&SFS))
                 END
               ELSE SET SFS = &STR('&STR(&SFS)')
               END
             IF &SUBSTR(&LENGTH(&STR(&SFS)),&STR(&SFS)) EQ &STR(C) +
               THEN DO
               SET SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-2,&STR(&SFS))
               SET SRCH&I = &STR(SRCHFORC)
               END
             ELSE SET SRCH&I = &STR(SRCHFOR)
             SET SFS&I = &STR(&SFS)
             END
           SET I = &I + 1
           END
         ISPEXEC CONTROL ERRORS CANCEL
         CONTROL NOMSG
         FREE F(SYSPO SYSSU)
         CONTROL MSG
         ISPEXEC FTOPEN TEMP
         ISPEXEC FTINCL PAJSSSF
         ISPEXEC FTCLOSE
         ISPEXEC VGET ZTEMPF
         ALLOC F(SYSPO) DA('&ZTEMPF')
         INTRDR SYSSU
         EUSUB
         CONTROL NOMSG
         FREE F(SYSPO SYSSU)
         CONTROL MSG
         IF &STR(&EUIOF) EQ &STR(YES) THEN DO
           SET &IOPARM = &STR(I.&EURUSID.S)
           ISPEXEC SELECT PGM(IOFSPF) PARM(&IOPARM) NEWAPPL(IOF)
           END
         EXIT CODE(0)
