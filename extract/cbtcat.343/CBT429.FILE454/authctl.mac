EACM     TITLE 'EURATOM AUTHORITY CONTROL MODULE.'
AUTHCTL  START 0
         SPACE 1
* HOW TO ACCESS : ONLY USING THE ASSEMBLER MACRO, AS FOLLOWS
* ---------------              ...
*                             $EACM   PARM-LIST
*                              ...
*                  PARM-LIST  DC  BL1'A00R000U' ---> BIT U=0
*                             DC  AL3(ENTRY-NUMBER)
*                             DC  AL4(AUTH-BYTE)
*               OR PARM-LIST  DC  XL1'A00R000U' ---> BIT U=1
*                             DC  AL3(ENTRY-NUMBER)
*                             DC  AL4(AUTH-BYTE)
*                             DC  CL8'USER-ID'
*                              ...
*             WITH AUTH-BYTE  DC  AL1(*-*) IF BIT R=0
*                         OR  DC  AL1(*-*),CL16' ' IF BIT R=1, TO ASK
*                                        BACK USER-ID AND GROUP-NAME.
*   AND 1. ENTRY-NUMBER : RELATIVE INDEX IN USERS-GROUPS TABLE.
*                         SEE MODULE 'AUTHUGT' AND MACRO $MDL@IX.
*       2. BIT A MAY BE : 0 - NOT RACF AUTHORIZED CALLING MODULE.
*                         1 - ALREADY RACF AUTHORIZED CALLING MODULE.
*
* GENERAL NOTICE : THIS MODULE IS REENTRANT.
* ================ IT IS LINK-EDITED IN A LIBRARY DEFINED IN LNKLST00,
*                  SO IT MAY BE CHANGED WITHOUT INVOLVING USERS, AND
*                  THIS LIBRARY IS RACF PROTECTED AGAINST ANY USERS
*                  READ OR WRITE (ONLY SYSTEM GROUP MAY ACCESS IT).
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        PRINT  OFF
         LCLB  &SW1
&SW1     SETB  0
         AIF   ('&SYSPARM' EQ '').O0S0
         AIF   ('&SYSPARM'(1,6) NE 'NORACF').O0S0
&SW1     SETB  1
.O0S0   PRINT  ON
         SPACE 2
        $DEFREG
AUTHCTL  AMODE 24
AUTHCTL  RMODE 24
         SPACE 2
*        MISCELLANEOUS DEFINITIONS.
*        --------------------------
         SPACE 1
FLEXIT   EQU   X'01'       FLAGS - EXIT SCHEDULED.
SWUSER   EQU   X'01'      SWITCH - USER-ID SUPPLIED IN LIST.
SWBACK   EQU   X'10'               USER-ID AND GROUP-NAME BACK REQ.
         AIF   (&SW1).O1S1
SWAUTH   EQU   X'80'               CALLING MODULE ALREADY RACF AUTH.
.O1S1    EJECT
         USING *,R12               SET ADDRESSABILITY.
         STM   R14,R12,12(R13)     SAVE REGISTERS.
         LR    R12,R15             SET BASE REGISTER.
         B     BEGIN               BRANCH AROUND ID, DATE AND TIME.
         SPACE 1
         DC    CL8'AUTHCTL'        MODULE IDENTIFIER.
         DC    CL8'&SYSDATE'       ASSEMBLY DATE (MM/DD/YY).
         DC    C'-',CL5'&SYSTIME'  ASSEMBLY TIME (HH.MM).
         SPACE 1
BEGIN   GETMAIN R,LV=AREAL,SP=1
         LR    R15,R13             PREVIOUS SAVE AREA.
         LR    R13,R1              NEW SAVE AREA.
         LR    R6,R1               WORK AREAS.
         USING AREA,R6
         XC    SVA(18*4),SVA       CLEAR NEW SAVE AREA.
         ST    R13,8(R15)          LINK SAVE AREAS.
         ST    R15,4(R13)
         L     R2,24(R15)          RESTORE PARM'S POINTER.
         MVI   FLAGS,0             RESET FLAGS.
         MVC   USERID,=CL8' '
         MVC   GROUP,=CL8' '
         MVC   SWITCH(1),0(R2)     GET INDICATORS.
         SR    R7,R7               SET DEFAULT LEVEL NUMBER.
         LR    R8,R7
         LR    R9,R7
         ST    R7,16(R15)          SET RC=0 (REG 15).
         ICM   R9,B'0111',1(R2)    GET ENTRY-NUMBER.
         ICM   R10,B'1111',4(R2)   GET AUTH-BYTE ADDRESS.
         TM    SWITCH,SWUSER       USER-ID SUPPLIED?
         BZ    *+L'*+4             NO.
         LA    R8,8(R2)            POINT TO USER-ID.
         SPACE 1
         MVC   EXTR(EXTRL),EXTRSK
         XC    APSCB,APSCB
        EXTRACT APSCB,'S',FIELDS=(PSB),MF=(E,EXTR)
         L     R3,APSCB
         LTR   R3,R3
         BZ    NOTTSO
         SPACE 1
         MVC   SPIELST(SPIELSL),SPIELSK
        SPIE   SPIEXIT,(4),MF=(E,SPIELST)
         ST    R1,PRVSPIE          SAVE PREVIOUS.
         USING CPPL,R2
         USING PSCB,R3
         CLC   PSCBUPT,CPPLUPT     CPPL+4 = UPT ADDRESS?
         BNE   *+L'*+10            NO.
         DROP  R3
         CLC   APSCB,CPPLPSCB      TSO COMMAND USED?
         BE    SETEX               YES.
         LR    R1,R2
         USING IKJEBECA,R1
         L     R2,CAPTTMP
         CLC   APSCB,CPPLPSCB      TSO SUB-COMMAND USED?
         BNE   RSTSPIE             NO.
         DROP  R1,R2
SETEX    OI    FLAGS,FLEXIT        SET EXIT REQUEST.
RSTSPIE  L     R1,PRVSPIE          OLD PICA ADDRESS.
        SPIE   MF=(E,(1))
         TM    FLAGS,FLEXIT        EXIT SCHEDULED?
         BO    EXIT                YES.
NOTTSO   STC   R7,0(R10)           RESET AUTHORITY LEVEL NUMBER.
         SPACE 1
         AIF   (&SW1).O2S1
        RACSTAT ,                  TEST FOR RACF.
         LTR   R15,R15             ACTIVE?
         BNZ   TDFLT               NO.
         LTR   R8,R8               USER-ID SUPPLIED?
         BNZ   GETGRP              YES, CHECK REQUEST.
         L     R1,CVTPTR           GET CVT ADDRESS.
         USING CVT,R1
         L     R2,CVTTCBP          GET ASCB ADDRESS.
         DROP  R1
         L     R2,12(R2)
         USING ASCB,R2
         L     R2,ASCBASXB         GET ASXB ADDRESS.
         DROP  R2
         USING ASXB,R2
         L     R2,ASXBSENV         GET ACEE ADDRESS.
         DROP  R2
         LA    R2,0(,R2)
         LTR   R2,R2               ANY ADDRESS?
         BZ    TDFLT               NOT A RACF USER.
         USING ACEE,R2
         CLC   ACEEACEE,=CL4'ACEE' VALID ACRONYM?
         BNE   TDFLT               NO.
         TM    ACEEFLG1,ACEERACF   RACF DEFINED USER?
         BZ    TDFLT               NO.
         SR    R1,R1
         IC    R1,ACEEUSRL         GET USER-ID LENGTH.
         BCTR  R1,0
         EX    R1,MVUSID           SET CURRENT USER-ID.
         IC    R1,ACEEGRPL         GET GROUP-NAME LENGTH.
         BCTR  R1,0
         EX    R1,MVGRPN           SET CURRENT GROUP-NAME.
         B     ANAL
MVUSID   MVC   USERID(*-*),ACEEUSRI     << EXECUTED >>
MVGRPN   MVC   GROUP(*-*),ACEEGRPN << EXECUTED >>
         DROP  R2
         SPACE 1
GETGRP   MVC   USERID(L'USERID),0(R8)
         LA    R2,L'USERID
         LA    R3,USERID+L'USERID-1
         CLI   0(R3),C' '
         BNE   *+L'*+6
         BCTR  R3,0
         BCT   R2,*-10
         STC   R2,RUSIDL
         MVC   RUSID,USERID
         XC    LRGP(LRGPL),LRGP    REQUEST DEFAULT GROUP-NAME.
         MVC   RGP(RGPL),RGPA
         XC    WA(WAL),WA
         LA    R1,WAL
         ST    R1,WA
         TM    SWITCH,SWAUTH       CALLER ALREADY RACF AUTHORIZED?
         BO    LOCUSR              YES.
        SETAUTH ,
LOCUSR  ICHEINTY LOCATE,TYPE='USR',ENTRY=RUSIDL,ACTIONS=(RGP),         X
               WKAREA=WA,OPTIONS=(FLDEF,ACTION),MF=(E,LRGP)
         LR    R2,R15
         TM    SWITCH,SWAUTH       CALLER ALREADY RACF AUTHORIZED?
         BO    NORES               YES.
        RESAUTH ,
NORES    LTR   R2,R2
         BNZ   TDFLT
         CLC   DFGL,=XL2'0008'
         BE    SETGRP
TDFLT    CLC   USERID(3),=CL3'SYS' SYSTEM GROUP USER?
         BE    SYSLVL              YES.
         CLC   GROUP(3),=CL3'SYS'  SYSTEM GROUP?
         BNE   LEAVE               NO.
SYSLVL   LA    R7,15               SET HIGHER LEVEL.
         B     LEAVE
SETGRP   MVC   GROUP(L'GROUP),DFG  GET GROUP-NAME.
         AGO   .O2S2
.O2S1    LTR   R8,R8               USER-ID SUPPLIED?
         BNZ   SETIT               YES, CHECK REQUEST.
         L     R1,CVTPTR           GET CVT ADDRESS.
         USING CVT,R1
         L     R2,CVTTCBP          GET ASCB ADDRESS.
         DROP  R1
         L     R2,12(R2)
         USING ASCB,R2
         L     R1,ASCBJBNS         GET PTR TO STARTED/MOUNT/LOGON'ID.
         LTR   R1,R1
         BZ    NOTSML
         MVC   USERID(L'USERID),0(R1)   SET STARTED/MOUNT/LOGON'ID.
         B     ANAL
NOTSML   L     R1,ASCBJBNI         GET PTR TO JOB NAME.
         LTR   R1,R1
         BZ    LEAVE
         MVC   GROUP(L'GROUP),0(R1)     SET JOB NAME.
         DROP  R2
         SPACE 1
SETIT    MVC   USERID(L'USERID),0(R8)   SET AS STARTED/MOUNT/LOGON'ID.
         MVC   GROUP(L'GROUP),0(R8)     SET AS JOB NAME.
.O2S2    SPACE 1
ANAL     L     R2,=V(AUTHUGT)      AUTHORITY USER-GROUP TABLE.
         USING VECTOR,R2
         CL    R9,MAXGN            VALID ENTRY-NUMBER?
         BH    LEAVE               NO.
         L     R1,MAXGN            MAX. MODULE'S INDEX VALUE.
         L     R3,USERS            USER LIST.
SCUT     CLI   0(R3),0             END OF LIST?
         BE    SCGP                YES.
         CLC   0(L'USERID,R3),USERID    THIS USER-ID?
         BE    SLVL                YES.
         LA    R3,L'USERID+1(R1,R3)     BUMP TO NEXT.
         B     SCUT
SCGP     L     R3,GROUPS           GROUP LIST.
SCGT     CLI   0(R3),0             END OF LIST?
         BE    GTDF                YES.
         CLC   0(L'GROUP,R3),GROUP THIS GROUP-NAME?
         BE    SLVL                YES.
         LA    R3,L'GROUP+1(R1,R3) BUMP TO NEXT.
         B     SCGT
GTDF     L     R3,DEFAULT          GET DEFAULT VALUE.
         DROP  R2
         IC    R7,0(R9,R3)         GET AUTHORITY LEVEL NUMBER.
         B     LEAVE
SLVL     IC    R7,8(R9,R3)         GET AUTHORITY LEVEL NUMBER.
         SPACE 1
LEAVE    LA    R0,15               FORCE 0 TO 15.
         NR    R7,R0
         STC   R7,0(R10)           STORE AUTHORITY LEVEL NUMBER.
         TM    SWITCH,SWBACK       BACK USER-ID AND GROUP REQUESTED?
         BZ    EXIT                NO.
         MVC   1(L'USERID+L'GROUP,R10),USERID
EXIT     LR    R1,R6               MAIN STORAGE AREA TO BE RELEASED.
         DROP  R6
         L     R13,4(R13)          PREVIOUS SAVE AREA.
        FREEMAIN R,LV=AREAL,A=(1),SP=1
         LM    R14,R15,12(R13)     RESTORE REGISTERS 14-15.
         SR    R15,R15             CC = 0.
         LM    R0,R12,20(R13)      RESTORE REGISTERS 0-12.
         MVI   12(R13),X'FF'       SET RETURN INDICATOR.
         BR    R14                 RETURN.
         SPACE 2
*        INTERCEPT 0C4 EXIT ROUTINE.
*        ---------------------------
*              R1 ---> PROGRAM INTERRUPTION ELEMENT.
         SPACE 1
         CNOP  0,8
         USING *,R15
         USING PIE,R1
SPIEXIT  MVC   PIEPSW+5(3),=AL3(RSTSPIE)     SET RETURN ADDRESS.
         BR    R14                 BACK TO CONTROL PROGRAM.
         DROP  R1,R15
         EJECT
*        CONSTANTS AND SKELETONS.
*        ------------------------
         SPACE 1
EXTRSK  EXTRACT *-*,'S',MF=L
SPIELSK SPIE   *-*,,MF=L
         AIF   (&SW1).O10S1
RGPA    ICHEACTN FIELD=DFLTGRP,MF=L
.O10S1   SPACE 2
        LTORG
         SPACE 1
         DROP  R12
         EJECT
*        AREA'S DESCRIPTION.
*        -------------------
         SPACE 1
AREA     DSECT
SVA      DC    18F'0'              SAVE AREA.
         SPACE 1
EXTR    EXTRACT *-*,'S',MF=L
EXTRL   EQU    *-EXTR
SPIELST SPIE   *-*,,MF=L
SPIELSL EQU    *-SPIELST
PRVSPIE  DS    F                   PREVIOUS PICA ADDRESS.
         AIF   (&SW1).O20S1
LRGP    ICHEINTY LOCATE,TYPE='USR',ACTIONS=(*-*),MF=L
LRGPL    EQU   *-LRGP
RGP     ICHEACTN FIELD=DFLTGRP,MF=L
RGPL     EQU   *-RGP
         SPACE 1
WA       DS    F                   WORK AREA LENGTH.
         DS    XL20                RESERVED.
         DS    F                   ACTION RESPONSE LENGTH.
DFGL     DS    XL2                 DEFAULT GROUP LENGTH (8).
DFG      DS    CL8                 DEFAULT GROUP.
         DS    H
WAL      EQU   *-WA
.O20S1   SPACE 1
APSCB    DC    A(0)
USERID   DC    CL8' '              USER-ID.
GROUP    DC    CL8' '              GROUP-NAME.
FLAGS    DC    XL1'0'              PROCESSING FLAGS.
SWITCH   DC    XL1'0'              PROCESSING SWITCHES.
         AIF   (&SW1).O20S2
RUSIDL   DC    XL1'0'              USER-ID LENGTH.
RUSID    DC    CL8' '              USER-ID NAME.
.O20S2   SPACE 1
         CNOP  0,8
AREAL    EQU   *-AREA              TOTAL LENGTH.
         EJECT
*        VECTOR TABLE DESCRIPTION (AUTHUGT).
*        -----------------------------------
         SPACE 1
VECTOR   DSECT
         SPACE 1
MAXGN    DS    F
USERS    DS    A
GROUPS   DS    A
DEFAULT  DS    A
         SPACE 2
        PRINT  NOGEN
         SPACE 1
        CVT    DSECT=YES,TSO=YES,RPS=YES,LIST=YES
         SPACE 1
        IKJCPPL
        IKJPSCB
        IKJEBECA
        IHAASCB
        IHAPIE
         AIF   (&SW1).O30S1
         SPACE 1
        IHAASXB
         SPACE 1
        IHAACEE
.O30S1   SPACE 2
         END
