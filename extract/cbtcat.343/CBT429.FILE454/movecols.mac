ISREDIT MACRO (START STOP TO) NOPROCESS
/*  MACRO NAME : MOVECOLS                                           */
/* DESCRIPTION : EDIT MACRO TO MOVE COLUMNS WITHIN A DAT-SET        */
/*  CBT ORIGIN : EXTRACTED FROM CBT TAPE 94 AUG, FILE 095           */
/*  ADAPTED BY : MOINIL P.A.                                        */
         IF (&STR(&START) EQ &STR(HELP)) OR +
           (&STR(&START) EQ &STR(H)) THEN DO
           ISPEXEC CONTROL DISPLAY SAVE
           ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTMVC)
           SET &MVCCC = &LASTCC
           ISPEXEC CONTROL DISPLAY RESTORE
           GOTO XEND
           END
         ISPEXEC CONTROL ERRORS RETURN
         CONTROL ASIS
         ISREDIT PROCESS RANGE S X
         SET LCC = &LASTCC
         ISREDIT (RC) = RANGE_CMD
         IF &LASTCC = 4 OR (&STR(&RC) NE &STR(S) AND +
           &STR(&RC) NE &STR(X)) THEN DO
           SET &ZEDSMSG = NO RANGE
           SET &ZEDLMSG = SPECIFY A RANGE OF LINES USING "S" OR "X"
           GOTO XERR
           END
         IF &STOP EQ &Z OR &START EQ &Z OR &TO EQ &Z THEN DO
           SET &ZEDSMSG = MISSING
           SET &ZEDLMSG = SPECIFY THE "START STOP TO" COLUMNS OF DATA
           GOTO XERR
           END
         IF &START GT &STOP THEN DO
           SET &ZEDSMSG = INVALID
           SET &ZEDLMSG = YOU HAVE SPECIFIED "START" GREATER THAN +
             "STOP"
           GOTO XERR
           END
         ISREDIT (DW) = DATA_WIDTH
         ISREDIT (LB,RB) = BOUNDS
         IF &START LT &LB THEN DO
           SET &ZEDSMSG = OUTBOUNDS
           SET &ZEDLMSG = "START" SPECIFIED OUT OF LEFT BOUNDARY
           GOTO XERR
           END
         IF &STOP GT &RB THEN DO
           SET &ZEDSMSG = OUTBOUNDS
           SET &ZEDLMSG = "STOP" SPECIFIED OUT OF RIGHT BOUNDARY
           GOTO XERR
           END
         IF &TO LT &LB THEN DO
           SET &ZEDSMSG = OUTBOUNDS
           SET &ZEDLMSG = "TO" SPECIFIED OUT OF LEFT BOUNDARY
           GOTO XERR
           END
         IF &TO GT &RB THEN DO
           SET &ZEDSMSG = OUTBOUNDS
           SET &ZEDLMSG = "TO" SPECIFIED OUT OF RIGHT BOUNDARY
           GOTO XERR
           END
         SET TL = &TO + (&STOP - &START)
         IF &TL  GT &RB THEN DO
           SET &ZEDSMSG = OUTBOUNDS
           SET &ZEDLMSG = "TO" END TARGET OUT OF RIGHT BOUNDARY
           GOTO XERR
           END
         ISREDIT (FR) = LINENUM .ZFRANGE
         ISREDIT (LR) = LINENUM .ZLRANGE
         SET I = &FR
         DO WHILE  &I LE &LR
           ISREDIT (LN) = LINE &I
           SET SEGX = &STR(&SUBSTR(&START:&STOP,&NRSTR(&LN)))
           IF &STR(&RC) EQ &STR(S) THEN DO
             IF &START = 1 THEN SET &SEG1 = &Z
             ELSE SET SEG1 = &STR(&SUBSTR(1:&START-1,&NRSTR(&LN)))
             SET SEG2 = &STR(&SUBSTR(&STOP+1:&DW,&NRSTR(&LN)))
             SET &LO = &NRSTR(&SEG1&SEG2)
             IF &TO = 1 THEN SET &SEG1 = &Z
             ELSE SET SEG1 = &STR(&SUBSTR(1:&TO-1,&NRSTR(&LO)))
             SET SEG2 = +
               &STR(&SUBSTR(&TO:&LENGTH(&NRSTR(&LO)),&NRSTR(&LO)
             END
           ELSE DO
             SET SEGY = &STR(&SUBSTR(&TO:&TL,&NRSTR(&LN)))
             IF &START = 1 THEN SET &SEG1 = &Z
             ELSE SET SEG1 = &STR(&SUBSTR(1:&START-1,&NRSTR(&LN)))
             SET SEG2 = &STR(&SUBSTR(&STOP+1:&DW,&NRSTR(&LN)))
             SET &LO = &NRSTR(&SEG1&SEGY&SEG2)
             IF &TO = 1 THEN SET &SEG1 = &Z
             ELSE SET SEG1 = &STR(&SUBSTR(1:&TO-1,&NRSTR(&LO)))
             SET SEG2 = &STR(&SUBSTR(&TL+1:&DW,&NRSTR(&LO)))
             END
           SET &LF = &NRSTR(&SEG1&SEGX&SEG2)
           ISREDIT LINE &I = (LF)
           SET I = &I + 1
           END
         SET &MVCCC = 0
         GOTO XEND
XERR:    ISPEXEC SETMSG MSG(ISRZ001)
         SET &MVCCC = 12
XEND:    EXIT CODE(&MVCCC)
