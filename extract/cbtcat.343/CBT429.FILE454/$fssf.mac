         MACRO
&LABEL  $FSSF
.*
.* MACRO : INTERNALLY USED BY $FS MACRO.
.* -------
.*
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         GBLA  &GMFCI
         GBLB  &GFUSW,&GFESW,&GFISW
         GBLC  &GFSMF,&GFSMI
         LCLA  &ATT,&N,&I,&L,&M1,&M2,&M3,&M4,&NR
         LCLC  &ATTRB,&PT,&IC,&SF,&RG
         AIF   (NOT &GFUSW).USEERR
&N       SETA  N'&SYSLIST(1)
         AIF   (&N EQ 0).GSF
         AIF   ('&SYSLIST(1)' EQ 'NORMAL').GSF
         AIF   ('&SYSLIST(1)' EQ 'PT').SPT
         AIF   ('&SYSLIST(1)' EQ 'IC').SIC
         AIF   ('&SYSLIST(1,1)' EQ 'IC').SIC
         AIF   ('&SYSLIST(1,1)' NE 'PT').SCAN
.SPT     ANOP
&L       SETA  &L+1
&PT      SETC  '05'
         AIF   (&N EQ 1).GPI
&I       SETA  &I+1
         AIF   ('&SYSLIST(1,2)' NE 'IC').TPI
.SIC     ANOP
&L       SETA  &L+1
&IC      SETC  '13'
         AIF   (&N EQ 2).GPI
&I       SETA  &I+1
.TPI     ANOP
&I       SETA  &I+1
         AIF   (&I GT &N).GPI
&ATTRB   SETC  '&SYSLIST(1,&I)'
         MNOTE 4,'--- REMAINING KEYWORD &ATTRB - IGNORED'
         AGO   .TPI
.SCAN    ANOP
&I       SETA  &I+1
         AIF   (&I GT &N).GSF
         AIF   ('&SYSLIST(1,&I)' EQ 'SKIP').SKIP
         AIF   ('&SYSLIST(1,&I)' EQ 'PROT').PROT
         AIF   ('&SYSLIST(1,&I)' EQ 'NUM').NUM
         AIF   ('&SYSLIST(1,&I)' EQ 'DARK').DARK
         AIF   ('&SYSLIST(1,&I)' EQ 'INT').INT
         AIF   ('&SYSLIST(1,&I)' EQ 'HIGH').INT
         AIF   ('&SYSLIST(1,&I)' EQ 'PEN').PEN
         AIF   ('&SYSLIST(1,&I)' EQ 'MED').PEN
         AIF   ('&SYSLIST(1,&I)' EQ 'MDT').MDT
         AIF   ('&SYSLIST(1,&I)' EQ 'PT').FPT
         AIF   ('&SYSLIST(1,&I)' EQ 'IC').FIC
&ATTRB   SETC  '&SYSLIST(1,&I)'
         MNOTE 4,'--- UNKNOWN KEYWORD &ATTRB - IGNORED'
         AGO   .SCAN
.SKIP    ANOP
&M4      SETA  32
&M3      SETA  16
         AGO   .SCAN
.PROT    ANOP
&M4      SETA  32
         AGO   .SCAN
.NUM     ANOP
&M3      SETA  16
         AGO   .SCAN
.DARK    ANOP
&M2      SETA  12
         AGO   .SCAN
.INT     ANOP
&M2      SETA  8
         AGO   .SCAN
.PEN     ANOP
&M2      SETA  4
         AGO   .SCAN
.MDT     ANOP
&M1      SETA  1
         AGO   .SCAN
.FPT     ANOP
&L       SETA  &L+1
&PT      SETC  '05'
&I       SETA  &I+1
         AIF   (&I GT &N).GSF
         AIF   ('&SYSLIST(1,&I)' NE 'IC').RKI
.FIC     ANOP
&L       SETA  &L+1
&IC      SETC  '13'
.TEL     ANOP
&I       SETA  &I+1
         AIF   (&I GT &N).GSF
.RKI     ANOP
&ATTRB   SETC  '&SYSLIST(1,&I)'
         MNOTE 4,'--- REMAINING KEYWORD &ATTRB - IGNORED'
         AGO   .TEL
.GSF     ANOP
&ATT     SETA  2*(&M1+&M2+&M3+&M4)+1
&ATTRB   SETC  '40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F50D1D2D3D4D5D6D7D8D95A5X
               B5C5D5E5F6061E2E3E4E5E6E7E8E96A6B6C6D6E6FF0F1F2F3F4F5F6FX
               7F8F97A7B7C7D7E7F'(&ATT,2)
&L       SETA   &L+2
&SF      SETC  '1D'
.GPI     AIF   (&GFISW).GIX
         AIF   (&GFESW).GEX
&LABEL   DC    XL&L'&SF&ATTRB&PT&IC'
         MEXIT
.GEX     ANOP
&LABEL   MVC   0(&L,&GFSMF),=XL&L'&SF&ATTRB&PT&IC'
         LA    &GFSMF,&L.(,&GFSMF)
         MEXIT
.GIX     AIF   ('&GFSMI'(1,1) EQ '(').GIXR
&LABEL   MVC   &GFSMI+&GMFCI.(&L),=XL&L'&SF&ATTRB&PT&IC'
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.GIXR    ANOP
&NR      SETA  K'&GFSMI-2
&RG      SETC  '&GFSMI'(2,&NR)
&LABEL   MVC   &GMFCI.(&L,&RG),=XL&L'&SF&ATTRB&PT&IC'
&GMFCI   SETA  &GMFCI+&L
         MEXIT
.USEERR  MNOTE 8,'--- INVALID MACRO USAGE'
         MEND
