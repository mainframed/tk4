EURT     TITLE 'EURACFT -- RACF DATA-SET ACCESS TEST - TSO COMMAND.'
EURACFT  START 0
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                           E U R A C F T                             *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*
* FUNCTION :   THIS TSO COMMAND PROCESSOR CHECKS IF A GIVEN RACF ACCESS
*              IS ALLOWED TO THE CURRENT USER FOR A NON-VSAM DATA-SET.
*
* SYNTAX :     EURACFT  DSNAME  VOLUME(......)
*                       ACCESS(READ/UPDATE/CONTROL/ALTER)
*                       NOLIST/NOMSG/LIST/MSG/TEST
*
* OPERANDS :   REQUIRED - DSNAME
*              OPTIONAL - VOLUME, ACCESS AND OPTION
*                         DEFAULTS = ACCESS(READ) NOLIST
*                                    VOLUME IS RETRIEVED BY CATALOG
*
* ATTRIBUTES : RENT
* LKED OPTIONS : RENT,AC=1
* RETURN CODES :   0 - ACCESS ALLOWED
*                  4 - RESOURCE NOT PROTECTED
*                  8 - ACCESS REJECTED
*                 12 - PARSE ERROR
*                 16 - LOCATE ERROR
*                 20 - RACHECK ERROR
*                 24 - OTHER (MESSAGE SENT)
*                 28 - ATTENTION INTERRUPT
*                 32 - PUTLINE ERROR
*
* AUTHOR :     MOINIL P.A.
*              COMPUTING CENTRE (TP 361)
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
EURACFT  AMODE 24
EURACFT  RMODE 24
         EJECT
        $DEFREG
* REGISTER USAGE :
*              R0-R1 - LINKAGE CONVENTIONS, MACROS
*              R2-R5 - WORK
*                 R6 - (UNUSED)
*                 R7 - LOCAL LINKAGE REGISTER
*                 R8 - COMPLETION CODE
*                 R9 - IKJPARMD DSECT
*                R10 - WKAREA DSECT
*                R11 - (UNUSED)
*                R12 - BASE REGISTER
*                R13 - SAVE AREA POINTER
*                R14 - LINKAGE CONVENTIONS, RETURN ADDRESS
*                R15 - LINKAGE CONVENTIONS, RETURN CODE
         EJECT
        $XENT  BASE=R12,LV=WKLEN,TYPE=RENT
         LR    R10,R13             GET WORK-AREA ADDRESS
         USING WKAREA,R10          ADDRESSABILITY OF WORK-AREA
         LA    R2,18*4(R10)
         L     R3,=A(WKLEN-(18*4))
         LR    R4,R2
         XR    R5,R5
         MVCL  R2,R4
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
        $TEW$PP PARM=PARMLST,REG=R9,ATT=ATTINT,ERR=PARSERR
         SPACE 1
*------- GET DATA-SET NAME
         SPACE 1
         MVI   DSNTF,C' '          CLEAR DSN + TRAILING SPACE
         MVC   DSNTF+1(L'DSNTF-1),DSNTF
         LH    R3,PDSN+4           R3 IS LENGTH OF DSN
         L     R2,PDSN             R2 POINTS TO DSN
         BCT   R3,*+L'*+6
         MVC   DSNTF(*-*),0(R2)    GET DSN
         EX    R3,*-6
         SPACE 1
*------- GET VOLUME SERIAL NUMBER IF SPECIFIED
         SPACE 1
         MVC   VOLTF,=CL6' '
         TM    KVOL+6,X'80'        VOLUME SPECIFIED?
         BZ    *+L'*+22            NO
         LH    R3,KVOL+4           R3 IS LENGTH OF VOL
         L     R2,KVOL             R2 POINTS TO VOL
         BCT   R3,*+L'*+6
         MVC   VOLTF(*-*),0(R2)    GET VOL
         EX    R3,*-6
         SPACE 1
*------- GET ACCESS-NAME IF SPECIFIED
         SPACE 1
         LH    R2,KACC
         XR    R0,R0
         IC    R0,ACCNT(R2)
         STC   R0,ACCTF
         SPACE 1
*------- SET OPTION IF SPECIFIED
         SPACE 1
         CLC   KLIST(2),=H'2'
         BL    *+L'*+12
         BE    *+L'*+4
         OI    SWITCH,SWTEST       SAY TEST MESSAGES
         OI    SWITCH,SWLMSG       SAY LIST MESSAGES
         SPACE 1
*------- RETRIEVE ON VOLUME
         SPACE 1
         MVC   CAMLIST(CAMLLEN),CAMSKEL
         LA    R1,DSNTF
         ST    R1,CAMLIST+4
         LA    R1,LOCAREA
         ST    R1,CAMLIST+12
         XC    LOCAREA(256),LOCAREA
         XC    LOCAREA+256(LLOCAREA-256),LOCAREA+256
        LOCATE CAMLIST
         LTR   R15,R15             CATALOGED ?
         BZ    CTLGOK              YES
         CH    R15,=H'8'
         BNE   CTLERR
         CH    R0,=H'8'
         BNE   CTLERR
         TM    SWITCH,SWTEST+SWLMSG TEST OR LIST MESSAGES ?
         BZ    NOLMLO              NONE
         MVC   MSGAREA(12),=CL12' DATA-SET :'
         MVC   MSGAREA+12(L'DSNTF),DSNTF
         LA    R1,MSGAREA+12+L'DSNTF-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(13,R1),=CL13' - NOT FOUND.'
         LA    R0,MSGAREA
         SLR   R1,R0
         LA    R0,14(R1)
         LA    R1,MSGAREA
         BAS   R7,SNDLNE
         TM    SWITCH,SWTEST       TEST MESSAGES ?
         BO    NOLMLO              YES
         MVC   MSGAREA(37),=CL37' YOU MAY RETRY SPECIFYING THE VOLUME.'
         LA    R0,37
         LA    R1,MSGAREA
         BAS   R7,SNDLNE
         B     NOLMLO
CTLERR   TM    SWITCH,SWTEST       TEST MESSAGES ?
         BZ    NOTSLO              NO
         STM   R15,R1,DIAGR
         MVC   MSGAREA(17),=CL17' LOCATE R15-R1 :'
         UNPK  MSGAREA+17(9),DIAGR(5)
         TR    MSGAREA+17(8),TRHEX-X'F0'
         MVI   MSGAREA+25,C'-'
         UNPK  MSGAREA+26(9),DIAGR+4(5)
         TR    MSGAREA+26(8),TRHEX-X'F0'
         MVI   MSGAREA+34,C'-'
         UNPK  MSGAREA+35(9),DIAGR+4(5)
         TR    MSGAREA+35(8),TRHEX-X'F0'
         MVI   MSGAREA+43,C'.'
         LA    R0,44
         LA    R1,MSGAREA
         BAS   R7,SNDLNE
         L     R15,DIAGR
NOTSLO   TM    SWITCH,SWLMSG       LIST MESSAGES ?
         BZ    NOLMLO              NO
         MVC   MSGAREA(35),=CL35' CATALOG (LOCATE) ERROR (RC =    ).'
         CVD   R15,DBLWRD
         MVC   DBLWRD(4),=CL4'40202120'
         ED    DBLWRD(4),DBLWRD+L'DBLWRD-2
         MVC   MSGAREA+29(4),DBLWRD+1
         LA    R0,35
         LA    R1,MSGAREA
         BAS   R7,SNDLNE
         B     NOLMLO
CTLGOK   MVC   OLDVSN,=CL6' '
         CLI   VOLTF,C' '          VOLUME SPECIFIED ?
         BE    GOVOLS              NO, GO SUPPLY IT
         CLC   VOLTF(L'VOLTF),LOCAREA+6 IS IT THE FIRST ONE ?
         BE    GOCHKR              YES
         LH    R0,LOCAREA          NO, CHECK IF MULTI-VOLUMES
* NOTE - AT OUR INSTALLATION WE HAVE VERY FEW NUMBER OF MULTI-VOLUMES
*        DATA-SETS, AND THEY NEVER HAVE MORE THAN TWO OR THREE VOLUMES.
*        SO, HERE AFTER, WE CHECK ONLY FOR THE FIRST TWENTY VOLUMES,
*        AND IF NO MATCH IS FOUND, IT MEANS A NOT CATALOGED DATA-SET.
         LA    R1,20
         CLR   R0,R1
         BNH   *+L'*+2
         LR    R0,R2
         LA    R2,LOCAREA+2
MVCHKL   CLC   VOLTF(L'VOLTF),4(R2)
         BE    *+L'*+12
         LA    R2,12(,R2)
         BCT   R0,MVCHKL
         B     GOCHKR
         MVC   OLDVSN,VOLTF
GOVOLS   MVC   VOLTF(L'VOLTF),LOCAREA+6
         SPACE 1
*------- CHECK THE RESOURCE (RACHECK)
         SPACE 1
GOCHKR   LA    R2,DSNTF
         LA    R3,VOLTF
         XR    R4,R4
         IC    R4,ACCTF
         MVC   RPLIST(RPLLEN),RPSKEL
         XR    R5,R5
         CLI   OLDVSN,C' '
         BE    *+L'*+4
         LA    R5,OLDVSN
        ZEROKEY
        RACHECK ENTITY=((R2)),VOLSER=(R3),ATTR=(R4),OLDVOL=(R5),       X
               MF=(E,RPLIST)
         STM   R15,R0,DIAGR
        RESETKEY
         TM    SWITCH,SWTEST       TEST MESSAGES ?
         BZ    NOTSRA              NO
         MVC   MSGAREA(25),=CL25' RACHECK - RETURN CODE :'
         UNPK  MSGAREA+25(3),DIAGR+3(2)
         TR    MSGAREA+25(2),TRHEX-X'F0'
         CH    R15,=H'8'
         BH    NORSC
         MVC   MSGAREA+27(17),=CL17' / REASON CODE :'
         UNPK  MSGAREA+44(3),DIAGR+7(2)
         TR    MSGAREA+44(2),TRHEX-X'F0'
         MVI   MSGAREA+46,C'.'
         B     NORSC+L'NORSC
NORSC    MVC   MSGAREA+27(20),=CL20' / NO REASON CODE.'
         LA    R0,47
         LA    R1,MSGAREA
         BAS   R7,SNDLNE
NOTSRA   TM    SWITCH,SWLMSG       LIST MESSAGES ?
         BZ    NOLMRA              NO
         L     R15,DIAGR
         CH    R15,=H'8'
         BNH   *+L'*+10
         MVC   MSGAREA(32),=CL32' UNEXPECTED RACHECK RETURN CODE.'
         B     RCMSG
         MVC   MSGAREA(13),=CL13' RESOURCE IS'
         CH    R15,=H'4'
         BH    RC08
         BE    RC04
         MVC   MSGAREA+13(19),=CL19'RACF AUTHORIZED.'
         B     RCMSG
RC04     MVC   MSGAREA+13(19),=CL19'NOT RACF PROTECTED.'
         B     RCMSG
RC08     MVC   MSGAREA+13(19),=CL19'RACF UNAUTHORIZED.'
RCMSG    LA    R0,32
         LA    R1,MSGAREA
         BAS   R7,SNDLNE
NOLMRA   L     R8,DIAGR            SET RETURN CODE(S) ----------- 0 4 8
         CH    R8,=H'8'
         BNH   LEAVE
         LA    R8,20               SET RETURN CODE ----------------- 20
         B     LEAVE
         SPACE 1
*------- TERMINATION
         SPACE 1
SETRC    LA    R8,24               SET RETURN CODE ----------------- 24
         B     QUIT
ATTINT   LA    R8,28               SET RETURN CODE ----------------- 28
         B     QUIT
PUTERR   LA    R8,32               SET RETURN CODE ----------------- 32
         B     LEAVE
NOLMLO   LA    R8,16               SET RETURN CODE ----------------- 16
         B     LEAVE
PARSERR  LA    R8,12               SET RETURN CODE ----------------- 12
LEAVE   IKJRLSA #TSANSW            RELEASE PDL
QUIT    $XRET  CC=(R8),LV=WKLEN,TYPE=RENT
         SPACE 1
*------- SEND LINE TO TERMINAL ROUTINE
*              R7 = LINK REGISTER
         SPACE 1
SNDLNE   BAS   R14,PUTLNE
         B     *+L'*(R15)
         B     0(R7)               +0 - NORMAL COMPLETION
         B     PUTERR              +4 - ERROR
         DROP  R9,R10
         EJECT
*------- DATA CONSTANTS
         SPACE 1
CAMSKEL CAMLST NAME,*-*,,*-*
CAMLLEN  EQU   *-CAMSKEL
RPSKEL  RACHECK ENTITY=(*-*),VOLSER=*-*,CLASS='DATASET',ATTR=READ,     X
               OLDVOL=*-*,RELEASE=1.8,LOG=NOSTAT,MF=L
RPLLEN   EQU   *-RPSKEL
TRHEX    DC    CL16'0123456789ABCDEF'
ACCNT    DC    AL1(X'02',X'02',X'04',X'08',X'80')
         SPACE 1
        LTORG
         EJECT
*------- PARSE PARAMETERS LIST
         SPACE 1
         PRINT NOGEN
PARMLST  IKJPARM  DSECT=IKJPARMD
PDSN     IKJPOSIT DSNAME,USID,PROMPT='NAME OF DATA-SET TO BE CHECKED'
VOLKW    IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=VOLSUB,ALIAS=('VOL','V')
ACCKW    IKJKEYWD
         IKJNAME 'ACCESS',SUBFLD=ACCSUB,ALIAS=('ACC','A')
KLIST    IKJKEYWD DEFAULT='NOLIST'
         IKJNAME  'NOLIST',ALIAS=('NOMSG','NL','NM','N')
         IKJNAME  'LIST',ALIAS=('MSG','L','M')
         IKJNAME  'TEST',ALIAS=('T')
VOLSUB   IKJSUBF
KVOL     IKJIDENT 'VOLUME',FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=6,    X
               PROMPT='VOLUME SERIAL NUMBER'
ACCSUB   IKJSUBF
KACC     IKJKEYWD DEFAULT='READ'
         IKJNAME 'READ',ALIAS=('R')
         IKJNAME 'UPDATE',ALIAS=('U')
         IKJNAME 'CONTROL',ALIAS=('C')
         IKJNAME 'ALTER',ALIAS=('A')
         IKJENDP
         PRINT GEN
         EJECT
*------- DATA WORK-AREA
         SPACE 1
WKAREA   DSECT
         DS    18F                 REGISTER SAVE AREA
         SPACE 1
CAMLIST  DS    0F,(CAMLLEN)X
LOCAREA  DS    0D,XL268
LLOCAREA EQU   *-LOCAREA
RPLIST   DS    0F,(RPLLEN)X
         SPACE 1
        $TEW$WA
         SPACE 1
DBLWRD   DS    D
DIAGR    DS    3F
MSGAREA  DS    CL80                OUTPUT MESSAGE LINE
         SPACE 1
DSNTF    DS    CL45                DSNAME + A TRAILING SPACE
ACCTF    DS    XL1                 ACCESS
VOLTF    DS    CL6                 VOLUME SERIAL NUMBER
OLDVSN   DS    CL6                 VOLUME SERIAL NUMBER
SWITCH   DS    XL1
SWLMSG   EQU   X'80'               LIST MESSAGES
SWTEST   EQU   X'40'               TEST MESSAGES
         SPACE 1
WKLEN    EQU   (((*-WKAREA)+7)/8)*8     LENGTH OF WKAREA DATA AREA
         SPACE 1
*------- MAPPING MACROS
         SPACE 1
         PRINT NOGEN
       $TEW$DS
         SPACE 1
         END
