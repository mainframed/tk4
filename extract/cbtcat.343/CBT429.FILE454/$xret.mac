         MACRO           END EXECUTION.
&NAME   $XRET       &TYPE=NORENT,&CC=,&LV=72,&SP=0
         LCLA  &SW
&SW      SETA  0
         SPACE 1
.*
.* $XRET MACRO : RETURN CONTROL.
.* -------------
.*
.*    1. TYPE=NORENT (NOT REENTERABLE)
.*
.*       - RESTORE REGISTER 13 WITH THE PREVIOUS SAVE AREA ADDRESS.
.*       - SET A RETURN CODE IN THE REGISTER 15 (CC OPERAND).
.*       - RESTORE ALL THE OTHER REGISTERS FROM THE PREVIOUS
.*         SAVE AREA.
.*       - FLAG THE SAVE AREA USED BY THE RETURNING PROGRAM.
.*       - RETURN CONTROL TO THE CALLING PROGRAM (REGISTER 14).
.*
.*    2. TYPE=RENT (REENTERABLE)
.*
.*       - RESTORE REGISTER 13 WITH THE PREVIOUS SAVE AREA ADDRESS.
.*       - RELEASE (FREEMAIN) AN AREA OF MAIN STORAGE (LV AND SP
.*         OPERANDS) WHICH IS POINTED TO BY REGISTER 13 (CURRENT
.*         SAVE AREA ADDRESS).
.*       - SET A RETURN CODE IN THE REGISTER 15 (CC OPERAND).
.*       - RESTORE ALL THE OTHER REGISTERS FROM THE PREVIOUS
.*         SAVE AREA.
.*       - FLAG THE SAVE AREA USED BY THE RETURNING PROGRAM.
.*       - RETURN CONTROL TO THE CALLING PROGRAM (REGISTER 14).
.*
.* TYPE OPERAND - RENT OR NORENT (DEFAULT).
.*
.* CC OPERAND - OMITTED : EQUIVALENT TO CC=0 CODING.
.*              CC=VALUE : A NUMBER (FROM 0 TO 4095).
.*              CC=SYMBOL : A SYMBOL (VALUE MUST RANGE FROM 0 TO 4095).
.*              CC=(REGISTER) : VALUE PREVIOUSLY LOADED INTO A
.*                              REGISTER (0 TO 4095). DO NOT SPECIFY
.*                              REGISTER 13 WHEN NORENT TYPE, AND
.*                              REGISTER 0, 1, 13, 14 OR 15 WHEN RENT
.*                              TYPE.
.*
.* LV OPERAND - IS THE LENGTH OF THE MAIN STORAGE AREA TO BE RELEASED
.*              (LV OPERAND OF THE FREEMAIN R-TYPE MACRO). DEFAULT
.*              VALUE IS 72 (18*4 IS EQUAL TO A SAVE AREA).
.*
.* SP OPERAND - IS THE SUBPOOL NUMBER OF THE MAIN STORAGE TO BE
.*              FREED (SP OPERAND OF THE FREEMAIN R-TYPE MACRO).
.*              DEFAULT VALUE IS 0 (ZERO).
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   ('&TYPE' EQ 'NORENT').N1
         AIF   ('&TYPE' NE 'RENT').ERR1
&NAME    LR    1,13 MAIN STORAGE AREA TO BE RELEASED.
         L     13,4(13) PREVIOUS SAVE AREA.
         AGO   .N2
.N1      ANOP
&NAME    L     13,4(13) PREVIOUS SAVE AREA.
.N2      AIF   ('&CC' EQ '').N3
         AIF   ('&CC'(1,1) EQ '(').N4
         AIF   ('&CC' NE '0').N5
.N3      SR    15,15 SET RETURN CODE ZERO.
         ST    15,16(13) SET RETURN CODE.
         AIF   ('&TYPE' NE 'RENT').N8
         AGO   .N7
.N4      ST    &CC(1),16(13) SET RETURN CODE.
         AIF   ('&TYPE' NE 'RENT').N8
         AGO   .N7
.N5      AIF   ('&TYPE' EQ 'RENT').N6
         MVC   16(4,13),*+L'*+10 SET RETURN CODE.
&SW      SETA  1
         AGO   .N8
.N6      MVC   16(4,13),*+L'*+4 SET RETURN CODE.
         B     *+L'*+4 SKIP RETURN CODE.
         SPACE 1
         DC    AL4(&CC) RETURN CODE.
         SPACE 1
.N7     FREEMAIN R,LV=&LV,A=(1),SP=&SP
.N8      LM    14,12,12(13) RESTORE REGISTERS.
         MVI   12(13),X'FF' SET RETURN INDICATOR.
         BR    14 RETURN.
         AIF   (&SW EQ 0).END
         SPACE 1
         DC    AL4(&CC) RETURN CODE.
         AGO   .END
.ERR1    MNOTE 8,' INVALID TYPE OPERAND SPECIFIED ******************* '
.END     SPACE 1
         MEND
