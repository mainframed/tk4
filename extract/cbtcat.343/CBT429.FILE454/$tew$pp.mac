         MACRO      TSO EASY-WAY OF CODING - PARSE PROCESS
&NAME   $TEW$PP     &PARM=,&REG=R2,&MSG=,&ERR=,&ATT=,&UWA=
.* AUTHOR :    MOINIL P.A.
.* --------    COMPUTING CENTRE (TP 361)
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
         GBLB  &TEWPPSW,&TEWPPUW
         GBLC  &TEWMRNM,&TEWMRNE
         LCLC  &U,&V,&W,&X,&Y,&Z
         AIF   ('&PARM' EQ '').ERR1
&U       SETC  '&MSG'
&V       SETC  '&ERR'
         AIF   ('&U' NE '').A
&U       SETC  '&TEWMRNM'
.A       AIF   ('&V' NE '').A1
&V       SETC  '&TEWMRNE'
.A1      AIF   ('&U' EQ '').ERR2
         AIF   ('&V' EQ '').ERR3
&W       SETC  '&ATT'
         AIF   ('&W' NE '').A2
&W       SETC  '&V'
.A2      SPACE 1
*------- Set up PARSE Parameter Block
         SPACE 1
&TEWPPSW SETB  1
&X       SETC  'GIB'.'&SYSNDX'
&Y       SETC  'GIM'.'&SYSNDX'
&Z       SETC  'GIW'.'&SYSNDX'
&NAME    L     R1,#TSCPPL
         USING CPPL,R1           . SET CPPL ADDRESSABILITY
         LA    R14,#TSPPL
         USING PPL,R14           . SET PPL ADDRESSABILITY
         XC    PPLUPT(#TSPPLL),PPLUPT
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R0,#TSECBS
         ST    R0,PPLECB
         MVC   PPLPCL,&Y.A
         LA    R0,#TSANSW
         ST    R0,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         AIF   ('&UWA' EQ '').B2
&TEWPPUW SETB  1
         AIF   ('&UWA'(1,1) EQ '(').B1
         LA    R0,&UWA
         ST    R0,PPLUWA
         AGO   .B2
.B1      ST    &UWA(1),PPLUWA
.B2      DROP  R1,R14
         SPACE 1
*------- PARSE - Check syntax of command operands
         SPACE 1
        CALLTSSR EP=IKJPARS,MF=(E,#TSPPL)
         B     *+L'*(R15)        . BRANCH ON RETURN CODE
         B     &X.B              .  0 - SUCCESSFULL
         B     &Y.1              .  4 - UNABLE TO PROMPT
         B     &W                .  8 - USER ATTENTION OCCURRED
         B     &Y.2              . 12 - PROGRAM BUG
         B     &Y.3              . 16 - SPACE UNAVAILABLE IN REGION
         B     &Y.4              . 20 - INVALID PARAMETERS
         B     &Y.5              . 24 - PARAMETERS CONFLICT
         B     &Y.6              . 28 - TERMINAL DISCONNECTED
         SPACE 1
&Y.1     LA    R1,&Z.1
         B     &X.A
&Y.2     LA    R1,&Z.2
         B     &X.A
&Y.3     LA    R1,&Z.3
         B     &X.A
&Y.4     LA    R1,&Z.3
         B     &X.A
&Y.5     LA    R1,&Z.5
         B     &X.A
&Y.6     LA    R1,&Z.6
         B     &X.A
&Y.7     LA    R1,&Z.7
&X.A     BAS   R14,&U
         B     &V
&Y.A     DC    A(&PARM)
&Z.1    WTO    ' => incomplete parameters and unable to prompt',       X
               ROUTCDE=11,DESC=7,MF=L
&Z.2    WTO    ' => PARSE Parameter Block invalid',                    X
               ROUTCDE=11,DESC=7,MF=L
&Z.3    WTO    ' => not enough space in region',                       X
               ROUTCDE=11,DESC=7,MF=L
&Z.4    WTO    ' => invalid parameters',                               X
               ROUTCDE=11,DESC=7,MF=L
&Z.5    WTO    ' => parameters conflict',                              X
               ROUTCDE=11,DESC=7,MF=L
&Z.6    WTO    ' => terminal has been disconnected',                   X
               ROUTCDE=11,DESC=7,MF=L
&Z.7    WTO    ' => PARSE Answer Address error',                       X
               ROUTCDE=11,DESC=7,MF=L
         SPACE 1
&X.B     L     &REG,#TSANSW      . CHECK ANSWER ADDRESS
         LA    &REG,0(&REG)
         LTR   &REG,&REG
         BZ    &Y.7              . PARSE ERROR
         USING IKJPARMD,&REG     . SET PARSE PARM LIST ADDRESSABILITY
         MEXIT
.ERR1    MNOTE 8,' PARM=... OPERAND MISSING ************************* '
         MEXIT
.ERR2    MNOTE 8,' MSG=... OPERAND MISSING ************************** '
         MEXIT
.ERR3    MNOTE 8,' ERR=... OPERAND MISSING ************************** '
         MEND
