AGDG     TITLE 'ALLOCGDG -- GDG DYNAMIC ALLOCATION TSO-COMMAND.'
ALLOCGDG START 0
         SPACE 1
*--------------------------------------------------------------------*
*                          A L L O C G D G                           *
*--------------------------------------------------------------------*
* NAME :       ALLOCGDG                                              *
* ATTRIBUTES : RENT                                                  *
* AUTHOR :     MOINIL P.A.                                           *
*              COMPUTING CENTRE (TP 361)                             *
*              J.R.C. - ISPRA ESTABLISHMENT                          *
*              21020 ISPRA (VA), ITALY                               *
*--------------------------------------------------------------------*
         SPACE 1
*  COMMAND     OPERANDS
*  --------    ------------------------------------------------------
*  ALLOCGDG    FILE('DDNAME')  DSNAME('GDG DATA-SET NAME') NOLIST
*                                                          LIST
*  OPERANDS :
*  ----------
*  1. REQUIRED :
*        FILE('DDNAME') = 'DDNAME' IS THE DDNAME TO WHICH YOU WISH
*                  ALLOCATE THE 'GDG DATA-SET NAME'.
*             ALIASES : FI, F, DDNAME, DDN OR DD.
*        DSNAME('GDG DATA-SET NAME') = 'GDG DATA-SET NAME' IS THE
*                  GENERATION DATA GROUP MEMBER EXPRESSED WITH THE
*                  RELATIVE GENERATION NUMBER.
*                  I.E. : DA('ZULU.TEST.DATA(+0)')
*                  IF THE 'GDG DATA-SET NAME' IS NOT QUOTED, THEN THE
*                  PROFILE PREFIX WILL BE APPENDED.
*                  I.E. : DA(TEST.DATA(-1))
*             ALIASES : DSN, DATASET, DS OR DA.
*             NOTE : THE DATA-SET IS ALWAYS ALLOCATED WITH DISP=SHR.
*  2. OPTIONAL (KEYWORDS) :
*        NOLIST = NO ERROR MESSAGE(S) WILL BE DISPLAYED, ONLY THE
*                  RETURN CODE (&LASTCC) IS SET.
*             ALIASES : NL, NOMSG, NM OR N.
*        LIST = FULL ERROR MESSAGE(S) WILL BE DISPLAYED.
*             ALIASES : L, MSG OR M.
*
*  RETURN CODES :   0 - GDG SUCCESSFULLY ALLOCATED
*  --------------   8 - SYNTAX ERROR OR MISSING OPERAND
*                  12 - GDG ALLOCATION FAILED
*                  16 - ALL OTHER ERRORS (MESSAGE SENT)
         SPACE 1
ALLOCGDG AMODE 24
ALLOCGDG RMODE 24
         PRINT NOGEN
         SPACE 1
        $DEFREG
        $XENT  BASE=(R11,R12),LV=WKLEN,TYPE=RENT
         EJECT
         LR    R9,R13
         USING WKAREA,R9
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE,                       X
               GET=GETLNE,REL=RELGIB,SCAN=YES
         MVI   SWITCH,0
         USING CPPL,R2             CPPL ADDRESSABILITY
         L     R3,CPPLUPT          UPT ADDRESS
         USING UPT,R3
         TM    UPTSWS,UPTNPRM      NOPROMPT ?
         BZ    *+L'*+4             NO
         OI    SWITCH,SWNPRM       YES, RETAIN INFO
         MVI   PRFXL,0             GET CURRENT PREFIX IF ANY
         MVC   PRFX,=CL8' '          ELSE GET THE CURRENT USER-ID
         XR    R1,R1
         IC    R1,UPTPREFL
         LTR   R1,R1
         BNP   GTUSID
         STC   R1,PRFXL            PREFIX LENGTH
         BCT   R1,*+L'*+6
         MVC   PRFX(*-*),UPTPREFX  <<EXECUTED>>
         EX    R1,*-6              PREFIX
         DROP  R3
         B     HCOPNDS
GTUSID   L     R3,CPPLPSCB         PSCB ADDRESS
         USING PSCB,R3
         MVC   PRFXL,PSCBUSRL      USER-ID LENGTH
         MVC   PRFX(L'PSCBUSER),PSCBUSER USER-ID
         DROP  R2,R3
HCOPNDS  MVC   DDNME,=CL8' '
         MVI   DSNME,C' '
         MVC   DSNME+1(L'DSNME-1),DSNME
         OC    #TSIBSP(3*L'#TSIBSP),#TSIBSP COMMAND SCAN POINTERS ?
         BZ    NOPNDS              +0 - NO OPERANDS
NXTOPND  TM    SWITCH,SWRENT       +4 - REENTERED OPERAND ?
         BO    FRIBF               YES
         BAS   R14,GTOPND          GET AN OPERAND
         B     EORTLO              +0 - END OR TOO LONG
         LR    R6,R1               +4 - RETAIN LAST POSITION
         LA    R1,WKLINE           OPERAND IS IN WKLINE
         CLC   0(5,R1),=CL5'FILE(' SO NOW CHECK WHICH ONE
         BE    FIDD5
         CLC   0(3,R1),=CL3'FI('
         BE    FIDD3
         CLC   0(2,R1),=CL2'F('
         BE    FIDD2
         CLC   0(7,R1),=CL7'DDNAME('
         BE    FIDD7
         CLC   0(4,R1),=CL4'DDN('
         BE    FIDD4
         CLC   0(3,R1),=CL3'DD('
         BE    FIDD3
         CLC   0(7,R1),=CL7'DSNAME('
         BE    DSDA7
         CLC   0(4,R1),=CL4'DSN('
         BE    DSDA4
         CLC   0(3,R1),=CL3'DS('
         BE    DSDA3
         CLC   0(8,R1),=CL8'DATASET('
         BE    DSDA8
         CLC   0(3,R1),=CL3'DA('
         BE    DSDA3
         CLC   0(7,R1),=CL7'NOLIST '
         BE    NLNM7
         CLC   0(3,R1),=CL3'NL '
         BE    NLNM3
         CLC   0(6,R1),=CL6'NOMSG '
         BE    NLNM6
         CLC   0(3,R1),=CL3'NM '
         BE    NLNM3
         CLC   0(2,R1),=CL2'N '
         BE    NLNM2
         CLC   0(5,R1),=CL5'LIST '
         BE    LSMS5
         CLC   0(2,R1),=CL2'L '
         BE    LSMS2
         CLC   0(4,R1),=CL4'MSG '
         BE    LSMS4
         CLC   0(2,R1),=CL2'M '
         BE    LSMS2
         MVC   2(11,R6),=CL11'??? UNKNOWN'
         LA    R0,L'WKLINE+L'WKLREQ
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     NXTOPND             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
DUPLOP   MVC   2(12,R6),=CL12'??? REPEATED'
         LA    R0,L'WKLINE+L'WKLREQ
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     NXTOPND             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
ERRSOP   LR    R6,R1               RETAIN POSITION
         LA    R0,L'WKLINE+L'WKLREQ
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     *+L'*+4             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE+L'WKLREQ-1),WKLINE
         MVC   0(18,R6),=CL18'$---> SYNTAX ERROR'
         LA    R0,L'WKLINE+L'WKLREQ
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     NXTOPND             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
FIDD7    LA    R1,2(R1)            FILE OPERAND PROCESS
FIDD5    LA    R1,1(R1)
FIDD4    LA    R1,1(R1)
FIDD3    LA    R1,1(R1)
FIDD2    LA    R1,2(R1)
         CLI   DDNME,C' '
         BNE   DUPLOP              ALREADY SPECIFIED OPERAND
FIDDS    LR    R5,R1               RETAIN START ADDRESS OF DDNAME
         LA    R0,1
         LA    R2,L'TCHARS1
         LA    R3,TCHARS1
         BAS   R14,CHKCHRS         CHECK FIRST CHARACTER OF DDNAME
         B     ERRSOP              +0 = ERROR, SYNTAX ERROR
         LA    R0,7                +4 = OK FIRST
         LA    R2,L'TCHARS1+L'TCHARS2+L'TCHARS3
         BAS   R14,CHKCHRS         CHECK NEXT CHARACTERS OF DDNAME
         NOP   0                   +0 = ERROR OR END
         TM    SWITCH,SWRENT       +4 = OK ALL NEXT, REENTERED ?
         BZ    *+L'*+6             NO
         LR    R2,R1
         B     *+L'*+14
         CLI   0(R1),C')'          CHECK FOR )
         BNE   ERRSOP              NO, SYNTAX ERROR
         LR    R2,R1
         LA    R1,1(R1)
         CLI   0(R1),C' '          CHECK FOR BLANK
         BNE   ERRSOP              NO, SYNTAX ERROR
         SLR   R2,R5
         BCT   R2,*+L'*+6
         MVC   DDNME(*-*),0(R5)    <<EXECUTED>>
         EX    R2,*-6
         B     NXTOPND
DSDA8    LA    R1,1(R1)            DSNAME OPERAND PROCESS
DSDA7    LA    R1,3(R1)
DSDA4    LA    R1,1(R1)
DSDA3    LA    R1,3(R1)
         CLI   DSNME,C' '
         BNE   DUPLOP              ALREADY SPECIFIED OPERAND
DSDAS    NI    SWITCH,255-SWPNRQ   SET PREFIX REQUESTED
         CLI   0(R1),C''''         QUOTED DSNAME ?
         BNE   *+L'*+8             NO
         OI    SWITCH,SWPNRQ       YES, SET PREFIX NOT REQUESTED
         LA    R1,1(R1)
         LR    R5,R1               RETAIN START ADDRESS OF DSNAME
DSDAL    LA    R0,1
         LA    R2,L'TCHARS1+L'TCHARS2
         LA    R3,TCHARS1
         BAS   R14,CHKCHRS         CHECK FIRST CHARACTER OF DSNAME
         B     ERRSOP              +0 = ERROR, SYNTAX ERROR
         LA    R0,7                +4 = OK FIRST
         LA    R2,L'TCHARS1+L'TCHARS2+L'TCHARS3
         BAS   R14,CHKCHRS         CHECK NEXT CHARACTERS OF DSNAME
         NOP   0                   +0 = ERROR OR END
         CLI   0(R1),C'('          +4 = OK ALL NEXT, CHECK FOR (
         BE    DSDAG               YES, IT IS
         CLI   0(R1),C'.'          IS IT A PERIOD ?
         BNE   ERRSOP              NO, SYNTAX ERROR
         LA    R1,1(R1)            YES, BUMP POINTER AND LOOP
         B     DSDAL
DSDAG    LA    R1,1(R1)
         CLI   0(R1),C'+'
         BE    *+L'*+8
         CLI   0(R1),C'-'
         BNE   *+L'*+4
         LA    R1,1(R1)
         LA    R0,1
         LA    R2,L'TCHARS3
         LA    R3,TCHARS3
         BAS   R14,CHKCHRS         CHECK FIRST OF GENERATION NUMBER
         B     ERRSOP              +0 = ERROR, SYNTAX ERROR
         LA    R0,7                +4 = OK FIRST
         BAS   R14,CHKCHRS         CHECK NEXT OF GENERATION NUMBER
         NOP   0                   +0 = ERROR OR END
         CLI   0(R1),C')'          +4 = OK ALL NEXT, CHECK FOR )
         BNE   ERRSOP              NO, SYNTAX ERROR
         LA    R1,1(R1)
         LR    R2,R1
         TM    SWITCH,SWPNRQ       PREFIX REQUESTED ?
         BZ    *+L'*+12            YES
         CLI   0(R1),C''''         NO, CHECK QUOTED DSNAME ?
         BNE   ERRSOP              NO, SYNTAX ERROR
         LA    R1,1(R1)
         TM    SWITCH,SWRENT       REENTERED OPERAND ?
         BO    *+L'*+12            YES
         CLI   0(R1),C')'          NO, CHECK FOR )
         BNE   ERRSOP              NO, SYNTAX ERROR
         LA    R1,1(R1)
         CLI   0(R1),C' '          CHECK FOR BLANK
         BNE   ERRSOP              NO, SYNTAX ERROR
         LA    R4,DSNME
         TM    SWITCH,SWPNRQ       PREFIX NOT REQUESTED ?
         BZ    *+L'*+8             NO, ADD IT
         SLR   R2,R5               YES
         LR    R0,R2
         B     DSDAN
         XR    R3,R3
         IC    R3,PRFXL
         BCT   R3,*+L'*+6
         MVC   0(*-*,R4),PRFX      <<EXECUTED>>
         EX    R3,*-6
         LA    R4,1(R3,R4)
         MVI   0(R4),C'.'
         LA    R4,1(,R4)
         SLR   R2,R5
         LA    R0,2(R3,R2)
DSDAN    CH    R0,=Y(L'DSNME)
         BNH   DSDAM
OPNTL    MVC   2(12,R6),=CL12'??? TOO LONG'
         LA    R0,L'WKLINE+L'WKLREQ
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     NXTOPND             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
DSDAM    BCT   R2,*+L'*+6
         MVC   0(*-*,R4),0(R5)     <<EXECUTED>>
         EX    R2,*-6
         B     NXTOPND
NLNM7    LA    R1,1(R1)            NOLIST OPERAND PROCESS
NLNM6    LA    R1,3(R1)
NLNM3    LA    R1,1(R1)
NLNM2    LA    R1,2(R1)
         TM    SWITCH,SWOPTK       OPTION ALREADY ENCOUNTERED ?
         BO    OPTCFL              YES
         NI    SWITCH,255-SWLSMS   RESET LIST/MSG
         OI    SWITCH,SWOPTK       SET OPTION ENCOUNTERED
         B     NXTOPND
LSMS5    LA    R1,1(R1)            LIST OPERAND PROCESS
LSMS4    LA    R1,2(R1)
LSMS2    LA    R1,2(R1)
         TM    SWITCH,SWOPTK       OPTION ALREADY ENCOUNTERED ?
         BO    OPTCFL              YES
         OI    SWITCH,SWLSMS+SWOPTK SET LIST/MSG + OPTION ENCOUNTERED
         B     NXTOPND
OPTCFL   MVC   2(28,R6),=CL28'??? OPTION CONFLICT, IGNORED'
         LA    R0,L'WKLINE+L'WKLREQ
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     NXTOPND             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
NOPNDS   MVC   WKLINE(25),=CL25'NO OPERANDS (ALL MISSING)'
         LA    R0,25
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     CHKDDN              +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
EORTLO   LR    R6,R1               RETAIN LAST POSITION
         LTR   R0,R0
         BZ    OPNTL               OPERAND TOO LONG
FRIBF    TM    SWITCH,SWRENT       REENTERED OPERAND ?
         BZ    CHKDDN              NO
         NI    SWITCH,255-SWRENT   RESET
         BAS   R14,RELGIB          RELEASE BUFFER
         LTR   R15,R15
         BZ    CHKDDN
         TM    SWITCH,SWLSMS       LIST/MSG REQUESTED ?
         BZ    CHKDDN              NO
         MVC   WKLINE(33),=CL33'NOT ALL VIRTUAL STORAGE IS FREED'
         MVC   WKLINE+33(14),=CL14'(FREEMAIN R15='
         ST    R15,DBLWRD
         UNPK  WKLINE+47(9),DBLWRD(5)
         TR    WKLINE+47(8),TBHEX
         MVI   WKLINE+55,C')'
         LA    R0,56
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     CHKDDN              +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
CHKDDN   CLI   DDNME,C' '          DDNAME OK ?
         BNE   CHKDSN              YES
         TM    SWITCH,SWNPRM       NO, PROMPTING ALLOWED ?
         BO    XRC08               NO, EXIT
         MVC   WKLINE(20),=CL20' -- ENTER DDNAME OR'
         MVC   WKLINE+20(20),=CL20'JUST ENTER TO EXIT :'
         LA    R0,40
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     *+L'*+4             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
         BAS   R14,GETLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     *+L'*+8             +0 - CONTINUE
         B     XRC08               +4 - NONE, EXIT
         B     SETRC               +8 - ERROR
         BAS   R14,GTOPND          GET OPERAND
         NOP   0                   +0 - TOO LONG
         LA    R1,WKLINE           +4 - OPERAND IS IN WKLINE
         OI    SWITCH,SWRENT       SET REENTERED OPERAND
         B     FIDDS
CHKDSN   CLI   DSNME,C' '          DSNAME OK ?
         BNE   XGDGAL              YES
         TM    SWITCH,SWNPRM       NO, PROMPTING ALLOWED ?
         BO    XRC08               NO, EXIT
         MVC   WKLINE(31),=CL31' -- ENTER GDG DATA-SET NAME OR'
         MVC   WKLINE+31(20),=CL20'JUST ENTER TO EXIT :'
         LA    R0,51
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     *+L'*+4             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
         BAS   R14,GETLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     *+L'*+8             +0 - CONTINUE
         B     XRC08               +4 - NONE, EXIT
         B     SETRC               +8 - ERROR
         BAS   R14,GTOPND          GET OPERAND
         NOP   0                   +0 - TOO LONG
         LA    R1,WKLINE           +4 - OPERAND IS IN WKLINE
         OI    SWITCH,SWRENT       SET REENTERED OPERAND
         B     DSDAS
XGDGAL   LA    R1,PARMLST          ALLOCATE GDG
         LA    R15,DDNME
         ST    R15,0(R1)
         LA    R15,DSNME
         ST    R15,4(R1)
         OI    4(R1),X'80'         SET VL
         L     R15,=V(GDGALLR)
         BASR  R14,R15
         TM    SWITCH,SWLSMS       LIST/MSG REQUESTED ?
         BZ    TSTRC               NO
         STM   R15,R0,DBLWRD
         BCTR  R1,0
         XR    R14,R14
         IC    R14,0(R1)
         BCT   R14,*+L'*+6
         MVC   WKLINE(*-*),1(R1)   <<EXECUTED>>
         EX    R14,*-6
         LTR   R0,R0
         BZ    NOSCC
         LA    R15,WKLINE+1(R14)
         MVC   0(5,R15),=CL5' (R0='
         UNPK  5(9,R15),DBLWRD+4(5)
         TR    5(8,R15),TBHEX
         MVI   13(R15),C')'
         LA    R14,14(R14)
NOSCC    LA    R0,1(R14)
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     *+L'*+4             +0 - CONTINUE
         B     SETRC               +4 - INVALID OR ERROR
         L     R15,DBLWRD
TSTRC    LTR   R15,R15
         BZ    LEAVE               SET RC=0 ----------------------   0
         LA    R15,12              SET RC=12 ---------------------  12
         B     LEAVE
XRC08    LA    R15,8               SET RC=8 ----------------------   8
         B     LEAVE
SETRC    LA    R15,16              SET RC=16 ---------------------  16
LEAVE   $XRET  CC=(R15),LV=WKLEN,TYPE=RENT
         EJECT
*------- GTOPND - GET AN OPERAND ROUTINE.
*              R0-R4 : WORK REGISTERS.
*              R14 : LINK REGISTER.
* ON ENTRY --- #TSIBSP = SCAN BUFFER POINTERS.
* ON EXIT ---- #TSIBSP = CURRENT SCAN BUFFER POINTERS ARE SAVED.
*              RETURN = +0 - NO OPERAND IF R0 IS NON-ZERO, OR
*                            OPERAND TOO LONG IF R0 IS ZERO (REST OF
*                            OPERAND IS SKIPED)
*              RETURN = +4 - NORMAL.
*                            WKLINE = OPERAND IS UPPER-CASE.
*                            R1 : POINTS TO LAST CHARACTER OF OPERAND
*                                 IN WKLINE.
         SPACE 1
GTOPND   MVI   WKLINE,C' '         RESET TO BLANKS
         MVC   WKLINE+1(L'WKLINE+L'WKLREQ-1),WKLINE
         LA    R1,WKLINE
         LA    R0,L'WKLINE
         LM    R2,R4,#TSIBSP       GET SCAN POINTERS
         CLR   R4,R3
         BHR   R14                 NONE OR EXHAUSTED, RETURN TO CALLER
         CLI   0(R4),C' '
         BNE   GTOPNDL
         BXLE  R4,R2,*-8
         B     GTOPNDX+L'GTOPNDX   NONE, RETURN TO CALLER
GTOPNDN  BCT   R0,GTOPNDC
         CLI   0(R4),C' '          TOO LONG, SKIP REST OF OPERAND
         BE    GTOPNDX+L'GTOPNDX     AND RETURN TO CALLER
         BXLE  R4,R2,*-8
         B     GTOPNDX+L'GTOPNDX
GTOPNDC  CLI   0(R4),C' '
         BE    GTOPNDX
         LA    R1,1(R1)
GTOPNDL  OC    0(1,R1),0(R4)
         BXLE  R4,R2,GTOPNDN
GTOPNDX  LA    R14,4(R14)          SET NORMAL RETURN
         STM   R2,R4,#TSIBSP       SET SCAN POINTERS
         BR    R14                 RETURN TO CALLER
         EJECT
*------- CHKCHRS - CHECK CHARACTERS ROUTINE.
*              R0-R4 : WORK REGISTER.
*              R15 : WORK REGISTER.
*              R14 : LINK REGISTER.
* ON ENTRY --- R0 : NUMBER OF CHRACTERS TO TEST.
*              R1 : START STRING ADDRESS.
*              R2 : CHARACTERS TABLE LENGTH.
*              R3 : CHARACTERS TABLE ADDRESS.
* ON EXIT ---- RETURN = +0 - ERROR, STRING NOT EXHAUSTED.
*                            R1 : POINTS TO THE WRONG CHARACTER.
*              RETURN = +4 - NORMAL, ALL OK, STRING EXHAUSTED.
*                            R1 : POINTS TO THE NEXT CHARACTER.
         SPACE 1
CHKCHRS  LR    R4,R3
         LR    R15,R2
CHKCHRST CLC   0(1,R1),0(R4)       CHECK CHARACTERS
         BE    CHKCHRSN
         BCT   R15,*+L'*+2
         BR    R14                 ERROR, RETURN TO CALLER
         LA    R4,1(R4)
         B     CHKCHRST
CHKCHRSN LA    R1,1(R1)
         BCT   R0,CHKCHRS
         B     4(R14)              ALL OK, RETURN TO CALLER
         EJECT
*------- CONSTANTS, DSECT'S, ETC...
         SPACE 1
TCHARS1  DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
TCHARS2  DC    C'$#@'
TCHARS3  DC    C'0123456789',C'ABCDEF'
TBHEX    EQU   TCHARS3-X'F0'
         SPACE 1
        LTORG
         EJECT
WKAREA   DSECT
         DS    18F            FIRST S.A.
         SPACE 1
DBLWRD   DS    D
         SPACE 1
        $TEW$WA
         SPACE 1
PARMLST  DS    2F             GDGALLR PARM LIST
DDNME    DS    CL8
DSNME    DS    CL54
         SPACE 1
PRFX     DS    CL8
WKLINE   DS    CL60
WKLREQ   DS    CL18
PRFXL    DS    XL1
SWITCH   DS    XL1
SWNPRM   EQU   X'80'               NOPROMPT STATUS (PROFILE)
SWLSMS   EQU   X'40'               LIST/MSG REQUESTED
SWPNRQ   EQU   X'20'               PREFIX NOT REQUESTED
SWOPTK   EQU   X'10'               OPTION KEY PROCESSED
SWRENT   EQU   X'08'               REENTER PROCESS
         SPACE 1
WKLEN    EQU   (((*-WKAREA)+7)/8)*8
         SPACE 1
        $TEW$DS
        IKJUPT
         SPACE 1
         END
