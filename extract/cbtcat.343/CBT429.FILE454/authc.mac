AUTH     TITLE 'TSO - COMMAND AUTHORIZATION INTERFACE.'
AUTHC    START 0
         SPACE 1
* PURPOSE :    EXECUTE AN AUTHORIZED TSO-COMMAND (ONLY SYSTEM GROUP
* ---------    USER-ID'S ALLOWED).
*
* COMMAND SYNTAX : AUTHC ...COMMAND...
* ---------------- I.E. - AUTHC  CALL  'SYS1.LINKLIB(IEHDASDR)'
*
* AUTHOR :     RAVARANI N. - MOINIL P.A.
* --------     COMPUTING CENTRE (TP 361)
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
LV0      EQU   0         REJECT LEVEL.
         SPACE 1
        $MDL@IX
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R11,R13             SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R11
         XR    R10,R10             C.C. = 0.
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE,SCAN=YES
         USING CPPL,R2             SET CPPL ADDRESSABILITY.
         XC    REQAUT(REQAUTL),REQAUT
         MVC   REQAUT+1(3),=AL3(MDL@IX)
         LA    R1,AUTH
         ST    R1,REQAUT+4
        $EACM  REQAUT
         CLI   AUTH,LV0            AUTHORIZED?
         BE    NOTAUT              NO.
         SPACE 1
*------- HANDLE COMMAND.
         SPACE 1
         OC    #TSIBSP(3*L'#TSIBSP),#TSIBSP OPERAND MISSING?
         BZ    OPMISS              YES.
         MVI   TEXT,C' '           INITIALIZE NEW BUFFER.
         MVC   TEXT+1(L'TEXT-1),TEXT
         MVC   COMMAND(L'COMMAND),TEXT
         LM    R4,R6,#TSIBSP       GET SCAN POINTERS
         LR    R1,R5
         SLR   R1,R6
         EX    R1,MOVETXT          MOVE NEW CBUF TEXT.
         ALR   R1,R4
         STH   R1,CBUF             SET NEW CBUF LENGTH.
         LR    R1,R6
         CLI   0(R6),C' '          END OF NEW COMMAND?
         BE    *+L'*+4             YES.
         BXLE  R6,R4,*-8
         LR    R15,R6              COMPUTE NEW COMMAND LENGTH.
         SR    R15,R1
         LA    R0,L'COMMAND
         CLR   R15,R0              NEW COMMAND TOO LONG?
         BH    NCLERR              YES.
         BCTR  R15,0               - 1 (MOVE).
         EX    R15,UPCASE          UPPERCASE COMMAND NAME.
         EX    R15,MOVECMD         MOVE NEW COMMAND NAME FOR LINK.
         CLR   R6,R5               ANY OPERAND OF NEW COMMAND?
         BH    FNDOP               NO.
SCHSO    BXLE  R6,R4,*+L'*+4
         B     FNDOP
         CLI   0(R6),C' '          START OF OPERAND?
         BE    SCHSO               NO.
FNDOP    SR    R6,R1               COMPUTE OFFSET OF OPERAND.
         STH   R6,OFFSET           STORE IT IN NEW CBUF.
         MVC   CBUFOA,CPPLCBUF     SAVE ORIGINAL CBUF ADDRESS.
         LA    R1,NEWBUF           NEW BUFFER ADDRESS.
         ST    R1,CPPLCBUF         SET IT IN CPPL.
        SETAUTH
         MVC   LINK(LLINK),LINKP
         LR    R1,R2               SET CPPL ADDRESS.
        LINK   EPLOC=COMMAND,SF=(E,LINK)     LINK TO REQUESTED COMMAND.
         LR    R10,R15             RETAIN RETURN CODE.
        RESAUTH
         MVC   CPPLCBUF,CBUFOA     RESTORE ORIGINAL CBUF ADDRESS.
         DROP  R2
RETURN  $XRET  CC=(R10),LV=WRKL,TYPE=RENT
SETRC    LA    R10,16              C.C. = 16.
         B     RETURN
MOVETXT  MVC   TEXT(*-*),0(R6)     << EXECUTED >>
UPCASE   OC    TEXT(*-*),COMMAND   << EXECUTED >>
MOVECMD  MVC   COMMAND(*-*),TEXT   << EXECUTED >>
         SPACE 1
*------- DIAGNOSTIC'S MESSAGES.
         SPACE 1
NOTAUT   LA    R0,L'AUTMSG
         LA    R1,AUTMSG
         B     SHOWIT
OPMISS   LA    R0,L'OPMMSG
         LA    R1,OPMMSG
         B     SHOWIT
NCLERR   LA    R0,L'NCLMSG
         LA    R1,NCLMSG
SHOWIT   BAS   R14,PUTLNE
         B     SETRC
         EJECT
*------- CONSTANTS.
         SPACE 1
LINKP   LINK   SF=L
         SPACE 1
AUTMSG   DC    C' -> UNAUTHORIZED COMMAND'
OPMMSG   DC    C' -> OPERAND (AUTH. COMMAND) MISSING'
NCLMSG   DC    C' -> OPERAND (AUTH. COMMAND) SYNTAX ERROR'
         SPACE 1
        LTORG
         SPACE 1
*------- WORK AREAS.
         SPACE 1
WRKDSECT DSECT                     WORK AREAS DESCRIPTION.
         SPACE 1
SVA      DS    18F                 SAVE AREA.
         SPACE 1
        $TEW$WA
         SPACE 1
LINK    LINK   SF=L                LINK PARAMETER LIST.
LLINK    EQU   *-LINK
         SPACE 1
CBUFOA   DC    A(*-*)
COMMAND  DC    CL8' '              COMMAND NAME.
NEWBUF   DS    0D             ---> NEW COMMAND BUFFER.
CBUF     DC    H'0'                BUFFER LENGTH.
OFFSET   DC    H'0'                OFFSET TO OPERANDS.
TEXT     DC    CL256' '            COMMAND TEXT.
         SPACE 1
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(*-*)
AUTH     DC    XL1'0'
REQAUTL  EQU   *-REQAUT
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        PRINT  GEN
         SPACE 1
         END
