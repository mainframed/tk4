ISREDIT  MACRO (APARM,BPARM) NOPROCESS
/*  MACRO NAME : SKEL                                               */
/*    FUNCTION : INSERT JCL SKELETONS                               */
/*      SYNTAX : SKEL ... (VARIOUS SKELETONS, SEE HELP)             */
/*                    HELP OR H                                     */
/*       DEBUG : SKEL ... LIST                                      */
/*      AUTHOR : MOINIL P.A.                                        */
  IF &STR(&BPARM.) = &STR(LIST) THEN CONTROL LIST CONLIST SYMLIST
  IF &STR(&APARM.) = &STR(HELP) OR +
    &STR(&APARM.) = &STR(H) THEN DO
      ISPEXEC CONTROL DISPLAY SAVE
      ISPEXEC SELECT PGM(ISPTUTOR) PARM(PAJTSKL)
      SET &RCX = &LASTCC
      ISPEXEC CONTROL DISPLAY RESTORE
      EXIT CODE(&RCX)
      END
  SET &SKC = &STR(&&)
/* --- PS / PO SKELETON - - - - - - - - - - - - - - - - - - - - --- */
  IF &STR(&APARM.) = &STR(PS) OR +
    &STR(&APARM.) = &STR(PO) THEN DO
      SET &MSK1 = +
        &NRSTR(//........  DD UNIT=DISK,)
      SET &MSK2 = +
        &NRSTR(//             VOLUME=SER=......,)
      SET &MSK3 = +
        &NRSTR(//             DSNAME=.......................,)
      IF &STR(&APARM.) = &STR(PO) THEN +
        SET &MSK4 = &NRSTR(//             SPACE=(CYL,(1,1,5)),)
      ELSE SET &MSK4 = &NRSTR(//             SPACE=(CYL,(1,1)),)
      SET &MSK5 = +
        &NRSTR(//             DCB=(RECFM=FB,BLKSIZE=3120,LRECL=80),)
      SET &MSK6 = +
        &NRSTR(//             DISP=(NEW,CATLG))
      SET &SKM = 6
      END
/* --- TA SKELETON  - - - - - - - - - - - - - - - - - - - - - - --- */
  ELSE IF &STR(&APARM.) = &STR(TA) THEN DO
      SET &MSK1 = +
        &NRSTR(//........  DD UNIT=(TAPE,,DEFER),)
      SET &MSK2 = +
        &NRSTR(//             VOLUME=(PRIVATE,RETAIN,SER=......),)
      SET &MSK3 = +
        &NRSTR(//             DSNAME=.......................,)
      SET &MSK4 = +
        &NRSTR(//             LABEL=(1,SL,,OUT),)
      SET &MSK5 = +
        &NRSTR(//             DCB=(RECFM=FB,BLKSIZE=3120,LRECL=80),)
      SET &MSK6 = +
        &NRSTR(//             DISP=(NEW,PASS))
      SET &SKM = 6
      END
/* --- ASH SKELETON - - - - - - - - - - - - - - - - - - - - - - --- */
  ELSE IF &STR(&APARM.) = &STR(ASH) THEN DO
      SET &MSKN = +
        &NRSTR(//ASS     EXEC PGM=IEV90,)
      SET &MSK1 = +
        &NRSTR(&MSKN.PARM='XREF(SHORT),NODECK,OBJECT')
      SET &MSK2 = +
        &NRSTR(//SYSLIB    DD DSN=.......................,DISP=SHR)
      SET &MSK3 = +
        &NRSTR(//          DD DSN=SYS1.MACLIB,DISP=SHR)
      SET &MSK4 = +
        &NRSTR(//          DD DSN=SYS1.MODGEN,DISP=SHR)
      SET &MSKN = +
        &NRSTR(//SYSUT1    DD UNIT=VIO,DSN=&&&&SYSUT1,)
      SET &MSK5 = +
        &NRSTR(&MSKN.SPACE=(CYL,(10,5)))
      SET &MSKN = +
        &NRSTR(//SYSLIN    DD UNIT=SYSDA,)
      SET &MSK6 = +
        &NRSTR(&MSKN.SPACE=(CYL,(1,1)),DSN=&&LOADSET,)
      SET &MSK7 = +
        &NRSTR(//             DCB=(BLKSIZE=800),DISP=(,PASS,DELETE))
      SET &MSK8 = +
        &NRSTR(//SYSPRINT  DD SYSOUT=*)
      SET &MSKN = +
        &NRSTR(//SYSIN     DD DSN=.......................)
      SET &MSK9 = +
        &NRSTR(&MSKN.(........),DISP=SHR)
      SET &SKM = 9
      END
/* --- LNK SKELETON - - - - - - - - - - - - - - - - - - - - - - --- */
  ELSE IF &STR(&APARM.) = &STR(LNK) THEN DO
      SET &MSKN = +
        &NRSTR(//LNK     EXEC PGM=HEWL,)
      SET &MSK1 = +
        &NRSTR(&MSKN.PARM='XREF,LIST,....',COND=(4,LT))
      SET &MSKN = +
        &NRSTR(//SYSLMOD   DD UNIT=SYSDA,)
      SET &MSK2 = +
        &NRSTR(&MSKN.SPACE=(CYL,(1,1,1)),DSN=&&GOSET,)
      SET &MSK3 = +
        &NRSTR(//             DISP=(,PASS,DELETE))
      SET &MSK4 = +
        &NRSTR(//SYSADD    DD DSN=.......................,DISP=SHR)
      SET &MSK5 = +
        &NRSTR(//SYSUT1    DD UNIT=VIO,SPACE=(CYL,(1,1)))
      SET &MSK6 = +
        &NRSTR(//SYSPRINT  DD SYSOUT=* )
      SET &MSK7 = +
        &NRSTR(//SYSLIN    DD DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE))
      SET &MSK8 = +
        &NRSTR(//          DD *)
      SET &MSK9 = +
        &NRSTR(  INCLUDE      SYSADD(........,........,........))
      SET &MSK10 = +
        &NRSTR(  ENTRY   ........)
      SET &MSK11 = +
        &NRSTR(  NAME    ........(R))
      SET &MSK12 = +
        &NRSTR(/*)
      SET &SKM = 12
      END
/* --- XEQ SKELETON - - - - - - - - - - - - - - - - - - - - - - --- */
  ELSE IF &STR(&APARM.) = &STR(XEQ) THEN DO
      SET &MSK1 = +
        &NRSTR(//XEQ     EXEC PGM=........,COND=(4,LT))
      SET &MSK2 = +
        &NRSTR(//STEPLIB   DD DSN=&&GOSET,DISP=(OLD,DELETE,DELETE))
      SET &MSK3 = +
        &NRSTR(//SYSPRINT  DD SYSOUT=*)
      SET &MSK4 = +
        &NRSTR(//SYSUDUMP  DD SYSOUT=*)
      SET &MSK5 = +
        &NRSTR(//SYSIN     DD ...)
      SET &SKM = 5
      END
/* --- CPY SKELETON - - - - - - - - - - - - - - - - - - - - - - --- */
  ELSE IF &STR(&APARM.) = &STR(CPY) THEN DO
      SET &MSK1 = +
        &NRSTR(//COPY    EXEC PGM=IEBCOPY,PARM=(SIZE=100K))
      SET &MSK2 = +
        &NRSTR(//FROM      DD ...FROM DATA-SET...)
      SET &MSK3 = +
        &NRSTR(//TO        DD ...TO DATA-SET...)
      SET &MSK4 = +
        &NRSTR(//SYSUT3    DD UNIT=SYSDA,SPACE=(CYL,(1,1)))
      SET &MSK5 = +
        &NRSTR(//SYSUT4    DD UNIT=(SYSDA,SEP=SYSUT3),)
      SET &MSK6 = +
        &NRSTR(//             SPACE=(CYL,(1,1)),DCB=(KEYLEN=8))
      SET &MSK7 = +
        &NRSTR(//SYSPRINT  DD SYSOUT=*)
      SET &MSK8 = +
        &NRSTR(//SYSIN     DD *)
      SET &MSK9 = +
        &NRSTR(  COPY   OUTDD=TO,INDD=((FROM,R)))
      SET &MSK10 = +
        &NRSTR(  SELECT MEMBER=(...,...) <- IF NOT ALL MEMBERS)
      SET &MSK11 = +
        &NRSTR(/*)
      SET &SKM = 11
      END
/* --- GEN SKELETON - - - - - - - - - - - - - - - - - - - - - - --- */
  ELSE IF &STR(&APARM.) = &STR(GEN) THEN DO
      SET &MSK1 = +
        &NRSTR(//GENER   EXEC PGM=IEBGENER)
      SET &MSK2 = +
        &NRSTR(//SYSUT1    DD ...FROM DATA-SET...)
      SET &MSK3 = +
        &NRSTR(//SYSUT2    DD ...TO DATA-SET...)
      SET &MSK4 = +
        &NRSTR(//SYSPRINT  DD SYSOUT=*)
      SET &MSK5 = +
        &NRSTR(//SYSIN     DD DUMMY <- IF ...TO DATA-SET... IS A PS)
      SET &MSK6 = +
        &NRSTR(//SYSIN     DD *     <- IF ...TO DATA-SET... IS A PO)
      SET &MSK7 = +
        &NRSTR(  GENERATE  MAXNAME=... <- NUMBER OF MEMBERS)
      SET &MSK8 = +
        &NRSTR(  MEMBER    NAME=(...,...))
      SET &MSK9 = +
        &NRSTR(/*)
      SET &SKM = 9
      END
  ELSE DO
    SET ZEDSMSG = INVALID OPERAND
    SET ZEDLMSG = USE HELP OR H TO CHECK VALID OPERAND REQUEST
    ISPEXEC SETMSG MSG(ISRZ000)
    EXIT CODE(12)
    END
  ISREDIT PROCESS DEST
  IF &LASTCC = 0 THEN DO
    ISREDIT LOCATE .ZDEST
    IF &LASTCC = 8 THEN GOTO EMPTY
    ISREDIT SCAN OFF
    ISREDIT MASKLINE = "&MSK1"
    ISREDIT LINE_AFTER .ZDEST = MASKLINE
    IF &SKM > 1 THEN DO
      SET &SKI = 1
      DO WHILE &SKI LT &SKM
        ISREDIT (LINEPTR) = LINENUM .ZDEST
        SET &LINEPTR = &LINEPTR + &SKI
        SET &SKI = &SKI + 1
        SET &MSKN = &NRSTR(&SKC.MSK&SKI)
        ISREDIT MASKLINE = "&MSKN"
        ISREDIT LINE_AFTER &LINEPTR = MASKLINE
        END
      END
    END
  ELSE DO
EMPTY: ISREDIT SCAN OFF
    ISREDIT MASKLINE = "&MSK1"
    ISREDIT LINE_AFTER 0 = MASKLINE
    IF &SKM > 1 THEN DO
      SET &SKI = 1
      DO WHILE &SKI LT &SKM
        ISREDIT (LINEPTR) = LINENUM .ZCSR
        SET &LINEPTR = .ZLAST
        SET &SKW = &SKI + 1
        SET &MSKN = &NRSTR(&SKC.MSK&SKW)
        ISREDIT MASKLINE = "&MSKN"
        ISREDIT LINE_AFTER &SKI = MASKLINE
        SET &SKI = &SKW
        END
      END
    END
  ISREDIT SCAN ON
  ISREDIT MASKLINE = " "
  EXIT CODE(0)
