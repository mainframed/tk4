CCAT     TITLE 'NASA/GSFC -- CONCAT (TSO) COMMAND PROCESSOR.'
CONCAT   START 0
         SPACE 1
* PURPOSE :    THIS IS A TSO COMMAND PROCESSOR WHICH CONCATENATES
* ---------    SPECIFIED FILES.
*              'IKJDAIR' IS INVOKED TO PERFORM THE ACTUAL
*              CONCATENATION.
* ORIGIN :     EXTRACTED FROM TAPE CBT 89 FEB, FILE 300.
* --------     GSFTSOCC, VERSION 1.0, GENE CZARCINSKI, 09JUN73
*              CONCAT, VERSION 1.1, GENE CZARCINSKI, 10APR77 ...
*              - RENAME CSECT FROM 'GSFTSOCC' TO 'CONCAT'
*                (MORE CONSISTANT AND CLEANER CMDLIB)
*              - ADAPTED BY MOINIL P.A.
* ATTRIBUTES : RENT
* ------------
         SPACE 1
CONCAT  AMODE  24
CONCAT  RMODE  24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=@SIZE,SP=1,TYPE=RENT
         LR    R11,R13
         USING @STORAGE,R11        STORAGE BASE REGISTER
         LR    R9,R1
        $TEW$EC REG=R9,MSG=PUTMSG,ERR=QUIT
         USING CPPL,R9             SET CPPL ADDRESSABILITY
         XC    RCODE,RCODE         RC=0 -------------------------------
         XC    ECB,ECB
         LA    R1,PL1
         USING IOPL,R1             INIT FOR TERM I/O
         MVC   IOPLUPT,CPPLUPT
         MVC   IOPLECT,CPPLECT
         LA    R0,ECB
         ST    R0,IOPLECB
         DROP  R1
*------- PARSE THE COMMAND OPERANDS
         LA    R1,PL2
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R0,ECB
         ST    R0,PPLECB
         MVC   PPLPCL,=A($PARSECC)
         LA    R0,APDL
         ST    R0,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         XC    PPLUWA,PPLUWA
         DROP  R1
        CALLTSSR EP=IKJPARS,MF=(E,PL2)
         LTR   R15,R15             CHECK THE RC
         BZ    PARSE1              OK
*------- PARSE ERROR
         LA    R2,MSG0
         B     PUTLNE
*------- PARSE SUCCESSFUL
PARSE1   L     R2,APDL
         USING @PDL,R2
         LA    R3,PL4
         USING DAPB0C,R3
         XC    DAPB0C(DDNAMES-DAPB0C),DAPB0C
         MVI   DA0CCD+1,X'0C'      CONCATENATE CODE
         LA    R4,DDNAMES
         XR    R15,R15             COUNT
         LA    R14,#NAMES
PARSE2   L     R5,0(R14)
         LTR   R5,R5
         BZ    PARSE6              NULL
         LH    R1,4(R14)
         LTR   R1,R1
         BZ    PARSE6              NULL
         MVI   0(R4),C' '          CLEAR DDN SPACE
         MVC   1(7,R4),0(R4)
         BCT   R1,*+L'*+6          FOR EX
         MVC   0(*-*,R4),0(R5)     <<EXECUTED>>
         EX    R1,*-6
         LA    R6,DDNAMES          CHECK FOR DUP DDNAMES
         LTR   R7,R15
         BNP   PARSE5
PARSE3   CLC   0(8,R4),0(R6)
         BNE   PARSE4
         LA    R2,MSG3
         B     PUTLNE
PARSE4   LA    R6,8(R6)
         BCT   R7,PARSE3
PARSE5   LA    R4,8(R4)            BUMP DDN PNTR
         LA    R15,1(R15)          BUMP DDN COUNT
         CH    R15,=H'16'          ONLY SPACE FOR 16
         BH    PARSE7              OUT OF SPACE
PARSE6   CLI   8(R14),X'FF'        END OF LIST?
         BE    PARSE7              YES
         L     R14,8(R14)          NEXT ENTRY
         B     PARSE2
PARSE7   CH    R15,=H'2'           2 OR MORE MUST BE SPEC
         BNL   PARSE8              OK
         LA    R2,MSG2
         B     PUTLNE
PARSE8   STH   R15,DA0CNUMB
*------- CONCATENATE FILES
         LA    R1,PL3
         USING DAPL,R1
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA    R0,ECB
         ST    R0,DAPLECB
         LA    R0,PL4
         ST    R0,DAPLDAPB
         DROP  R1
        CALLTSSR EP=IKJDAIR,MF=(E,PL3)
         LTR   R15,R15
         BZ    DONE1               CONCATENATION SUCCESSFUL
         CVD   R15,WORK            REPORT ERROR
         OI    WORK+7,X'0F'
         MVC   MSGA(MSG1L),MSG1
         UNPK  MSGA+(MSG1A-MSG1)(L'MSG1A),WORK
         UNPK  WORK(5),DA0CDARC(3)
         MVZ   WORK(4),=XL4'0'
         TR    WORK(4),=C'0123456789ABCDEF'
         MVC   MSGA+(MSG1B-MSG1)(L'MSG1B),WORK
         LA    R2,MSGA
*------- OUTPUT ERROR MSG
PUTLNE  PUTLINE PARM=PTPB,OUTPUT=(0(R2),TERM,SINGLE,DATA),             X
               TERMPUT=(EDIT,WAIT,HOLD),MF=(E,PL1)
         MVI   RCODE+1,12          RC=12 ------------------------------
DONE1    CLI   PL2+(PPLANS-PPL),X'FF'
         BE    DONE2               NO CORE TO FREE
         L     R1,PL2+(PPLANS-PPL)
        IKJRLSA (1)
DONE2    CLI   RCODE+1,12
         BNE   DONE3
        STACK  PARM=STPB,DELETE=ALL,MF=(E,PL1)
        TCLEARQ INPUT
DONE3    LH    R2,RCODE
         B     LEAVE
QUIT     LA    R2,12               RC=12 ------------------------------
LEAVE   $XRET  CC=(R2),LV=@SIZE,SP=1,TYPE=RENT
         EJECT
*------- CONSTANTS
         SPACE 1
MSG0     DC    0F'0',Y(MSG0L,0),C'  ERROR PARSING COMMAND'
         DC    C' OPERAND, EXECUTION SUPPRESSED.'
MSG0L    EQU   *-MSG0
MSG1     DC    0F'0',Y(MSG1L,0),C'  CONCATENATION ERROR, DAIR RC='
MSG1A    DC    CL2' ',C', DARC='
MSG1B    DC    CL4' ',C'.'
MSG1L    EQU   *-MSG1
MSG2     DC    0F'0',Y(MSG2L,0),C'  TWO OR MORE FILENAMES MUST'
         DC    C' BE SPECIFIED, EXECUTION SUPPRESSED.'
MSG2L    EQU   *-MSG2
MSG3     DC    0F'0',Y(MSG3L,0),C'  DUPLICATE FILENAMES'
         DC    C' SPECIFIED, EXECUTION SUPPRESSED.'
MSG3L    EQU   *-MSG3
         SPACE 1
        LTORG
         SPACE 1
        PRINT  NOGEN
$PARSECC IKJPARM  DSECT=@PDL
#NAMES   IKJIDENT 'FILENAMES',LIST,MAXLNTH=8,FIRST=ALPHA,              X
               OTHER=ALPHANUM,PROMPT='CONCAT (FILENAME/DDNAME LIST)'
         IKJENDP
        PRINT  GEN
         EJECT
@STORAGE DSECT
         DS    18F                 S.A.
WORK     DS    D
        $TEW$WA
ECB      DS    F
APDL     DS    A
PTPB    PUTLINE MF=L
STPB    STACK  MF=L
PL1      DS    4F                  IOPL
PL2      DS    7F                  PPL
PL3      DS    5F                  DAPL
PL4      DS    36F                 DAPB0C
MSGA     DS    0F,H,H,CL100
RCODE    DS    H
         SPACE 1
@SIZE    EQU   (((*-@STORAGE)+7)/8)*8
         SPACE 1
        PRINT  NOGEN
        $TEW$DS CVT=YES
        IKJIOPL
        IKJPPL
        IKJDAPL
        IKJDAP0C
DDNAMES  EQU   *
        PRINT  GEN
         SPACE 1
         END
