DTSO     TITLE 'DTSO -- DISPLAY SOME CONTROL BLOCKS TSO-COMMAND.'
DTSO     START 0
         SPACE 1
*--------------------------------------------------------------------*
*                              D T S O                               *
*--------------------------------------------------------------------*
* COMMAND :    DTSO                                                  *
* OPERANDS :   UPT ECT PSCB                                          *
*              REQUIRED : NONE                                       *
*              DEFAULT  : UPT IF NONE IS SPECIFIED                   *
* RETURN CODES :  0 - SUCCESSFULL                                    *
*                16 - ERROR                                          *
* ATTRIBUTES : RENT                                                  *
* AUTHOR :     MOINIL P.A.                                           *
*              COMPUTING CENTRE (TP361)                              *
*              J.R.C. - ISPRA ESTABLISHMENT                          *
*              21020 ISPRA (VA), ITALY                               *
*--------------------------------------------------------------------*
         SPACE 1
DTSO     AMODE 24
DTSO     RMODE 24
         PRINT NOGEN
         SPACE 1
        $DEFREG
*------- REGISTERS USE :
*        R8  = COMMAND RETURN CODE
*        R9  = LOCAL LINK RETURN ADDRESS
*        R10 = LOCAL LINK ADDRESS / LOCAL BASE REGISTER
*        R11 = WORK AREAS POINTER
*        R12 = GENERAL BASE REGISTER
*        R13 = SAVE AREA POINTER
*        R14 = LINK RETURN ADDRESS
*        R15 = LINK ADDRESS / RETURN CODE
         SPACE 1
WASIZE   EQU   512            CONTROL BLOCKS WORK-AREA SIZE IN BYTES
*                             (MUST BE LARGE ENOUGH TO HANDLE ALL
*                              DSECT'S THE LIST IS DONE BELOW)
         EJECT
        $XENT  BASE=R12,LV=WKLEN,TYPE=RENT
         LR    R11,R13
         USING WKAREA,R11
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
         MVI   SWREQS,0
        $TEW$PP PARM=PARMLST,REG=R3
         CLC   KWUPT(2),=H'2'
         BNE   *+L'*+4
         OI    SWREQS,RQUPT
         CLC   KWECT(2),=H'2'
         BNE   *+L'*+4
         OI    SWREQS,RQECT
         CLC   KWPSCB(2),=H'2'
         BNE   *+L'*+4
         OI    SWREQS,RQPSCB
         DROP  R3
        IKJRLSA #TSANSW            RELEASE PDL'S SPACE
         CLI   SWREQS,0            ANY REQUESTS ?
         BNE   *+L'*+4             YES
         OI    SWREQS,RQUPT        NO, SET DEFAULT TO UPT
         TM    SWREQS,RQUPT
         BZ    *+L'*+6
         L     R10,=A(PUPTL)
         BASR  R9,R10              LIST UPT
         TM    SWREQS,RQECT
         BZ    *+L'*+6
         L     R10,=A(PECTL)
         BASR  R9,R10              LIST ECT
         TM    SWREQS,RQPSCB
         BZ    *+L'*+6
         L     R10,=A(PPSCBL)
         BASR  R9,R10              LIST ECT
         XR    R8,R8               SET RC=0 ----------------------   0
LEAVE   $XRET  CC=(R8),LV=WKLEN,TYPE=RENT
SETRC    LA    R8,16               SET RC=16 ---------------------  16
         B     LEAVE
         EJECT
*------- OVRFLW - WORK-AREA OVERFLOW ROUTINE.
*              R9 : RETURN ADDRESS.
* ON ENTRY --- R0 : IDENTIFIER NUMBER.
*              R1 : CONTROL BLOCK LENGTH.
         SPACE 1
OVRFLW   LTR   R0,R0
         BM    *+L'*+12
         BZ    *+L'*+14
         CH    R0,TBIDN
         BNH   *+L'*+2
         XR    R0,R0
         SLL   R0,2
         CVD   R1,DBLWRD
         LA    R1,TBID
         ALR   R1,R0
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE(4),0(R1)
         MVC   WKLINE+5(9),=CL9'(LENGTH :'
         MVC   WKLINE+14(8),=XL8'4020202020202120'
         ED    WKLINE+14(8),DBLWRD+4
         MVC   WKLINE+22(26),=CL26') -- WORK-AREA OVERFLOW --'
         LA    R0,48
         LA    R1,WKLINE
GOBACK   BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE ?
         B     0(R9)               +0 - RETURN
         B     SETRC               +4 - ERROR
CHECK    B     *+L'*(R15)          HOW COMPLETE ?
         B     0(R14)              +0 - RETURN
         B     SETRC               +4 - ERROR
         SPACE 1
WALTSZ   DC    A(WASIZE)
TBHEX    DC    CL16'0123456789ABCDEF'
TBID     DC    CL4'????',C'UPT ECT PSCB'
TBIDN    DC    Y(((*-TBID)-4)/4)
         SPACE 1
        LTORG
         EJECT
*------- DISPLAY USER PROFILE TABLE (UPT)
         SPACE 1
         CNOP  0,4
         USING *,R10
PUPTL    LM    R0,R1,=A(1,UPTLEN)  CHECK UPT LENGTH
         CL    R1,WALTSZ
         BH    OVRFLW              WORK-AREA TOO SMALL
         L     R2,#TSCPPL          CPPL ADDRESS
         USING CPPL,R2
         L     R0,CPPLUPT          UPT ADDRESS
         LA    R4,CTLBWA           WORK-AREA ADDRESS
         LR    R5,R1
         MVCL  R4,R0
         L     R3,CPPLPSCB         PSCB ADDRESS
         DROP  R2
         USING PSCB,R3
         XR    R1,R1
         IC    R1,PSCBUSRL         USER-ID LENGTH
         BCT   R1,*+L'*+6
         MVC   WKLINE(*-*),PSCBUSER USER-ID
         EX    R1,*-6
         DROP  R3
         LA    R1,WKLINE+1(R1)
         MVC   0(27,R1),=CL27' USER PROFILE TABLE (UPT) :'
         LA    R1,27(R1)
         LA    R0,WKLINE
         SLR   R1,R0
         XR    R0,R1
         XR    R1,R0
         XR    R0,R1
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         LA    R4,CTLBWA           WORK-AREA ADDRESS
         USING UPT,R4
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         UNPK  WKLINE+2(5),0(3,R4)
         TR    WKLINE+2(4),TBHEX-X'F0'
         MVI   WKLINE+6,C' '
         XR    R15,R15
         LA    R0,L'UPTUSER
         LA    R1,UPTUSER
         LA    R2,WKLINE+7
PUPTP    IC    R15,0(R1)
         STC   R15,1(R2)
         NI    1(R2),X'0F'
         SRL   R15,4
         STC   R15,0(R2)
         LA    R2,2(R2)
         LA    R1,1(R1)
         BCT   R0,PUPTP
         TR    WKLINE+7(2*L'UPTUSER),TBHEX
         UNPK  WKLINE+(2*L'UPTUSER)+8(3),UPTSWS(2)
         TR    WKLINE+(2*L'UPTUSER)+8(2),TBHEX-X'F0'
         MVI   WKLINE+(2*L'UPTUSER)+10,C' '
         UNPK  WKLINE+(2*L'UPTUSER)+11(3),UPTCDEL(2)
         TR    WKLINE+(2*L'UPTUSER)+11(2),TBHEX-X'F0'
         MVI   WKLINE+(2*L'UPTUSER)+13,C' '
         UNPK  WKLINE+(2*L'UPTUSER)+14(3),UPTLDEL(2)
         TR    WKLINE+(2*L'UPTUSER)+14(2),TBHEX-X'F0'
         MVI   WKLINE+(2*L'UPTUSER)+16,C' '
         UNPK  WKLINE+(2*L'UPTUSER)+17(3),UPTLDEL+1(2)
         TR    WKLINE+(2*L'UPTUSER)+17(2),TBHEX-X'F0'
         MVI   WKLINE+(2*L'UPTUSER)+19,C' '
         UNPK  WKLINE+(2*L'UPTUSER)+20((2*L'UPTPREFX)+1),UPTPREFX(L'UPTX
               PREFX+1)
         TR    WKLINE+(2*L'UPTUSER)+20(2*L'UPTPREFX),TBHEX-X'F0'
         MVI   WKLINE+(2*L'UPTUSER)+(2*L'UPTPREFX)+20,C' '
         UNPK  WKLINE+(2*L'UPTUSER)+(2*L'UPTPREFX)+21(3),UPTPREFL(2)
         TR    WKLINE+(2*L'UPTUSER)+(2*L'UPTPREFX)+21(2),TBHEX-X'F0'
         MVI   WKLINE+(2*L'UPTUSER)+(2*L'UPTPREFX)+23,C' '
         LA    R0,L'WKLINE
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(4),=CL4'++++'
         MVC   WKLINE+7(2*L'UPTUSER),=CL20'<<INSTALLATION-USE>>'
         MVC   WKLINE+(2*L'UPTUSER)+8(12),=CL12'SW DC LD ++'
         MVC   WKLINE+(2*L'UPTUSER)+20(17),=CL17'<<<<PREFIX>>>> L'''
         LA    R0,L'WKLINE
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(21),=CL21'(NOTE : + = RESERVED)'
         XR    R1,R1
         IC    R1,UPTPREFL
         LTR   R1,R1
         BP    *+L'*+10
         MVC   WKLINE+40(8),=CL8'NOPREFIX'
         B     PUPTN
         MVC   WKLINE+40(8),=CL8'PREFIX('''
         BCT   R1,*+L'*+6
         MVC   WKLINE+48(*-*),UPTPREFX <<EXECUTED>>
         EX    R1,*-6              PREFIX
         LA    R1,WKLINE+49(R1)
         MVC   0(2,R1),=CL2''')'
PUPTN    LA    R0,L'WKLINE
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(14),=CL14'ENV.SW : BIT-0'
         MVC   WKLINE+26(5),=CL5'BIT-1'
         MVC   WKLINE+41(5),=CL5'BIT-2'
         MVC   WKLINE+56(5),=CL5'BIT-3'
         LA    R0,L'WKLINE
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         TM    UPTSWS,UPTRCVR      EDIT RECOVER ?
         BO    *+L'*+10
         MVC   WKLINE+11(L'KNREC),KNREC
         B     *+L'*+6
         MVC   WKLINE+11(L'KNREC-2),KNREC+2
         TM    UPTSWS,UPTNPRM      NOPROMPT ?
         BO    *+L'*+10
         MVC   WKLINE+26(L'KNPRP-2),KNPRP+2
         B     *+L'*+6
         MVC   WKLINE+26(L'KNPRP),KNPRP
         TM    UPTSWS,UPTMID       MSG-ID ?
         BO    *+L'*+10
         MVC   WKLINE+41(L'KNMSG),KNMSG
         B     *+L'*+6
         MVC   WKLINE+41(L'KNMSG-2),KNMSG+2
         TM    UPTSWS,UPTNCOM      COMMUNICATION ?
         BO    *+L'*+10
         MVC   WKLINE+56(L'KNICM-2),KNICM+2
         B     *+L'*+6
         MVC   WKLINE+56(L'KNICM),KNICM
         LA    R0,L'WKLINE
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+11(5),=CL5'BIT-4'
         MVC   WKLINE+26(5),=CL5'BIT-5'
         MVC   WKLINE+41(5),=CL5'BIT-6'
         MVC   WKLINE+56(5),=CL5'BIT-7'
         LA    R0,L'WKLINE
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         TM    UPTSWS,UPTPAUS      PAUSE ?
         BO    *+L'*+10
         MVC   WKLINE+11(L'KNPSE),KNPSE
         B     *+L'*+6
         MVC   WKLINE+11(L'KNPSE-2),KNPSE+2
         TM    UPTSWS,UPTALD       ATTN ?
         BO    *+L'*+10
         MVC   WKLINE+26(9),=CL9'ATTN=ATTN'
         B     *+L'*+6
         MVC   WKLINE+26(13),=CL13'ATTN=LINE DEL'
         TM    UPTSWS,UPTMODE      MODE ?
         BO    *+L'*+10
         MVC   WKLINE+41(L'KNMDE),KNMDE
         B     *+L'*+6
         MVC   WKLINE+41(L'KNMDE-2),KNMDE+2
         TM    UPTSWS,UPTWTP       WTP ?
         BO    *+L'*+10
         MVC   WKLINE+56(L'KNWTP),KNWTP
         B     *+L'*+6
         MVC   WKLINE+56(L'KNWTP-2),KNWTP+2
         LA    R0,L'WKLINE
         LA    R1,WKLINE
         B     GOBACK
         DROP  R4
         SPACE 1
KNREC    DC    C'NORECOVER'
KNPRP    DC    C'NOPROMPT'
KNMSG    DC    C'NOMSGID'
KNICM    DC    C'NOINTERCOM'
KNPSE    DC    C'NOPAUSE'
KNMDE    DC    C'NOMODE'
KNWTP    DC    C'NOWTPMSG'
         SPACE 1
        LTORG
         SPACE 1
         DROP  R10
         EJECT
*------- DISPLAY ENVIRONMENT CONTROL TABLE (ECT)
         SPACE 1
         CNOP  0,4
         USING *,R10
PECTL    LM    R0,R1,=A(2,ECTLEN)  CHECK ECT LENGTH
         CL    R1,WALTSZ
         BH    OVRFLW              WORK-AREA TOO SMALL
         L     R2,#TSCPPL          CPPL ADDRESS
         USING CPPL,R2
         L     R0,CPPLECT          ECT ADDRESS
         LA    R4,CTLBWA           WORK-AREA ADDRESS
         LR    R5,R1
         MVCL  R4,R0
         L     R3,CPPLPSCB         PSCB ADDRESS
         DROP  R2
         USING PSCB,R3
         XR    R1,R1
         IC    R1,PSCBUSRL         USER-ID LENGTH
         BCT   R1,*+L'*+6
         MVC   WKLINE(*-*),PSCBUSER USER-ID
         EX    R1,*-6
         DROP  R3
         LA    R1,WKLINE+1(R1)
         MVC   0(34,R1),=CL34' ENVIRONMENT CONTROL TABLE (ECT) :'
         LA    R1,34(R1)
         LA    R0,WKLINE
         SLR   R1,R0
         XR    R0,R1
         XR    R1,R0
         XR    R0,R1
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         LA    R4,CTLBWA           WORK-AREA ADDRESS
         USING ECT,R4
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTRCDF :'
         UNPK  WKLINE+11(3),ECTRCDF(2)
         TR    WKLINE+11(2),TBHEX-X'F0'
         MVI   WKLINE+13,C' '
         MVC   WKLINE+42(9),=CL9'ECTRTCD :'
         UNPK  WKLINE+51(7),ECTRTCD(4)
         TR    WKLINE+51(6),TBHEX-X'F0'
         LA    R0,57
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTIOWA :'
         UNPK  WKLINE+11(9),ECTIOWA(5)
         TR    WKLINE+11(8),TBHEX-X'F0'
         MVI   WKLINE+19,C' '
         MVC   WKLINE+42(9),=CL9'ECTMSGF :'
         UNPK  WKLINE+51(3),ECTMSGF(2)
         TR    WKLINE+51(2),TBHEX-X'F0'
         LA    R0,53
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTSMSG :'
         UNPK  WKLINE+11(7),ECTSMSG(4)
         TR    WKLINE+11(6),TBHEX-X'F0'
         MVI   WKLINE+17,C' '
         MVC   WKLINE+42(9),=CL9'ECTPCMD :'
         UNPK  WKLINE+51(9),ECTPCMD(5)
         UNPK  WKLINE+59(9),ECTPCMD+4(5)
         TR    WKLINE+51(16),TBHEX-X'F0'
         CLC   ECTPCMD,=CL8' '
         BNE   *+L'*+8
         MVI   WKLINE+67,C' '
         B     *+L'*+10
         MVI   WKLINE+67,C'='
         MVC   WKLINE+68(8),ECTPCMD
         LA    R0,76
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTSCMD :'
         UNPK  WKLINE+11(9),ECTSCMD(5)
         UNPK  WKLINE+19(9),ECTSCMD+4(5)
         TR    WKLINE+11(16),TBHEX-X'F0'
         CLC   ECTSCMD,=CL8' '
         BNE   *+L'*+8
         MVI   WKLINE+27,C' '
         B     *+L'*+10
         MVI   WKLINE+27,C'='
         MVC   WKLINE+28(8),ECTSCMD
         MVC   WKLINE+42(9),=CL9'ECTSWS  :'
         UNPK  WKLINE+51(3),ECTSWS(2)
         TR    WKLINE+51(2),TBHEX-X'F0'
         LA    R0,53
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTDDNUM:'
         UNPK  WKLINE+11(7),ECTDDNUM(4)
         TR    WKLINE+11(6),TBHEX-X'F0'
         MVI   WKLINE+17,C' '
         MVC   WKLINE+42(9),=CL9'ECTUSER :'
         UNPK  WKLINE+51(9),ECTUSER(5)
         TR    WKLINE+51(8),TBHEX-X'F0'
         LA    R0,59
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTBKPB :'
         UNPK  WKLINE+11(9),ECTBKPB(5)
         TR    WKLINE+11(8),TBHEX-X'F0'
         MVI   WKLINE+19,C' '
         MVC   WKLINE+42(9),=CL9'ECTSWS2 :'
         UNPK  WKLINE+51(3),ECTSWS2(2)
         TR    WKLINE+51(2),TBHEX-X'F0'
         LA    R0,53
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTSWS22:'
         UNPK  WKLINE+11(3),ECTSWS22(4)
         TR    WKLINE+11(2),TBHEX-X'F0'
         MVI   WKLINE+13,C' '
         MVC   WKLINE+42(9),=CL9'RESERVED:'
         UNPK  WKLINE+51(5),42(3,R4)
         TR    WKLINE+51(4),TBHEX-X'F0'
         LA    R0,55
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'ECTHELP :'
         UNPK  WKLINE+11(9),ECTHELP(5)
         TR    WKLINE+11(8),TBHEX-X'F0'
         MVC   WKLINE+19(12),=CL12' (OR ECTNUM)'
         LA    R0,31
         LA    R1,WKLINE
         B     GOBACK
         DROP  R4
         SPACE 1
        LTORG
         SPACE 1
         DROP  R10
         EJECT
*------- DISPLAY PROTECTED STEP CONTROL BLOCK (PSCB)
         SPACE 1
         CNOP  0,4
         USING *,R10
PPSCBL   LM    R0,R1,=A(3,PSCBLEN) CHECK PSCB LENGTH
         CL    R1,WALTSZ
         BH    OVRFLW              WORK-AREA TOO SMALL
         L     R2,#TSCPPL          CPPL ADDRESS
         USING CPPL,R2
         L     R0,CPPLPSCB         PSCB ADDRESS
         DROP  R2
         LA    R4,CTLBWA           WORK-AREA ADDRESS
         LR    R5,R1
         MVCL  R4,R0
         LA    R4,CTLBWA           WORK-AREA ADDRESS
         USING PSCB,R4
         XR    R1,R1
         IC    R1,PSCBUSRL         USER-ID LENGTH
         BCT   R1,*+L'*+6
         MVC   WKLINE(*-*),PSCBUSER USER-ID
         EX    R1,*-6
         LA    R1,WKLINE+1(R1)
         MVC   0(38,R1),=CL38' PROTECTED STEP CONTROL BLOCK (PSCB) :'
         LA    R1,38(R1)
         LA    R0,WKLINE
         SLR   R1,R0
         XR    R0,R1
         XR    R1,R0
         XR    R0,R1
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBUSER:'
         UNPK  WKLINE+11(9),PSCBUSER(5)
         UNPK  WKLINE+19(7),PSCBUSER+4(4)
         TR    WKLINE+11(14),TBHEX-X'F0'
         MVI   WKLINE+25,C'='
         MVC   WKLINE+26(7),PSCBUSER
         MVC   WKLINE+42(9),=CL9'PSCBUSRL:'
         UNPK  WKLINE+51(3),PSCBUSRL(2)
         TR    WKLINE+51(2),TBHEX-X'F0'
         LA    R0,53
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBGPNM:'
         UNPK  WKLINE+11(9),PSCBGPNM(5)
         UNPK  WKLINE+19(9),PSCBGPNM+4(5)
         TR    WKLINE+11(16),TBHEX-X'F0'
         CLC   PSCBGPNM,=CL8' '
         BNE   *+L'*+8
         MVI   WKLINE+27,C' '
         B     *+L'*+10
         MVI   WKLINE+27,C'='
         MVC   WKLINE+28(8),PSCBGPNM
         MVC   WKLINE+42(9),=CL9'PSCBATR1:'
         UNPK  WKLINE+51(5),PSCBATR1(3)
         TR    WKLINE+51(4),TBHEX-X'F0'
         LA    R0,55
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBATR2:'
         UNPK  WKLINE+11(5),PSCBATR2(3)
         TR    WKLINE+11(4),TBHEX-X'F0'
         MVI   WKLINE+15,C' '
         MVC   WKLINE+42(9),=CL9'PSCBLTIM:'
         UNPK  WKLINE+51(9),PSCBLTIM(5)
         UNPK  WKLINE+59(9),PSCBLTI2(5)
         TR    WKLINE+51(16),TBHEX-X'F0'
         LA    R0,67
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBSUBH:'
         UNPK  WKLINE+11(3),PSCBSUBH(2)
         TR    WKLINE+11(2),TBHEX-X'F0'
         XR    R1,R1
         IC    R1,PSCBSUBH
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   WKLINE+13,C' '
         B     *+L'*+8
         MVI   WKLINE+13,C'='
         STC   R1,WKLINE+14
         MVC   WKLINE+42(9),=CL9'PSCBSUBC:'
         UNPK  WKLINE+51(3),PSCBSUBC(2)
         TR    WKLINE+51(2),TBHEX-X'F0'
         XR    R1,R1
         IC    R1,PSCBSUBC
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   WKLINE+53,C' '
         B     *+L'*+8
         MVI   WKLINE+53,C'='
         STC   R1,WKLINE+54
         LA    R0,55
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBSUBM:'
         UNPK  WKLINE+11(3),PSCBSUBM(2)
         TR    WKLINE+11(2),TBHEX-X'F0'
         XR    R1,R1
         IC    R1,PSCBSUBM
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   WKLINE+13,C' '
         B     *+L'*+8
         MVI   WKLINE+13,C'='
         STC   R1,WKLINE+14
         MVC   WKLINE+42(9),=CL9'PSCBSOUT:'
         UNPK  WKLINE+51(3),PSCBSOUT(2)
         TR    WKLINE+51(2),TBHEX-X'F0'
         XR    R1,R1
         IC    R1,PSCBSOUT
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   WKLINE+53,C' '
         B     *+L'*+8
         MVI   WKLINE+53,C'='
         STC   R1,WKLINE+54
         LA    R0,55
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'RESERVED:'
         UNPK  WKLINE+11(3),32(2,R4)
         TR    WKLINE+11(2),TBHEX-X'F0'
         MVI   WKLINE+13,C' '
         MVC   WKLINE+42(9),=CL9'PSCBDRBA:'
         UNPK  WKLINE+51(7),PSCBDRBA(4)
         TR    WKLINE+51(6),TBHEX-X'F0'
         LA    R0,53
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'RESERVED:'
         UNPK  WKLINE+11(9),36(5,R4)
         TR    WKLINE+11(8),TBHEX-X'F0'
         MVI   WKLINE+19,C' '
         MVC   WKLINE+42(9),=CL9'PSCBDEST:'
         UNPK  WKLINE+51(9),PSCBDEST(5)
         UNPK  WKLINE+59(9),PSCBDEST+4(5)
         TR    WKLINE+51(16),TBHEX-X'F0'
         OC    PSCBDEST,PSCBDEST
         BNZ   *+L'*+8
         MVI   WKLINE+67,C' '
         B     *+L'*+10
         MVI   WKLINE+67,C'='
         MVC   WKLINE+68(8),PSCBDEST
         LA    R0,76
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBRLGB:'
         UNPK  WKLINE+11(9),PSCBRLGB(5)
         TR    WKLINE+11(8),TBHEX-X'F0'
         MVI   WKLINE+19,C' '
         MVC   WKLINE+42(9),=CL9'PSCBUPT :'
         UNPK  WKLINE+51(9),PSCBUPT(5)
         TR    WKLINE+51(8),TBHEX-X'F0'
         LA    R0,59
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBUPTL:'
         UNPK  WKLINE+11(5),PSCBUPTL(3)
         TR    WKLINE+11(4),TBHEX-X'F0'
         MVI   WKLINE+15,C' '
         MVC   WKLINE+42(9),=CL9'PSCBCHAR:'
         UNPK  WKLINE+51(3),PSCBCHAR(2)
         TR    WKLINE+51(2),TBHEX-X'F0'
         XR    R1,R1
         IC    R1,PSCBCHAR
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   WKLINE+53,C' '
         B     *+L'*+8
         MVI   WKLINE+53,C'='
         STC   R1,WKLINE+54
         LA    R0,55
         LA    R1,WKLINE
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBLINE:'
         UNPK  WKLINE+11(3),PSCBLINE(2)
         TR    WKLINE+11(2),TBHEX-X'F0'
         XR    R1,R1
         IC    R1,PSCBLINE
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   WKLINE+13,C' '
         B     *+L'*+8
         MVI   WKLINE+13,C'='
         STC   R1,WKLINE+14
         MVC   WKLINE+42(9),=CL9'PSCBRSZ :'
         UNPK  WKLINE+51(9),PSCBRSZ(5)
         TR    WKLINE+51(8),TBHEX-X'F0'
         MVI   WKLINE+59,C'='
         ICM   R1,B'1111',PSCBRSZ
         SLL   R1,1
         CVD   R1,DBLWRD
         MVC   WKEDIT,=XL8'4020202020202120'
         ED    WKEDIT,DBLWRD+4
         LA    R0,L'WKEDIT-1
         LA    R14,WKEDIT+1
         LA    R1,WKLINE+60
         CLI   0(R14),C' '
         BE    *+L'*+10
         MVC   0(1,R1),0(R14)
         LA    R1,1(R1)
         LA    R14,1(R14)
         BCT   R0,*-22
         MVI   0(R1),C'K'
         LA    R0,1(R1)
         LA    R1,WKLINE
         SLR   R0,R1
         BAS   R14,PUTLNE
         BAS   R14,CHECK           SEE HOW COMPLETE
         MVI   WKLINE,C' '
         MVC   WKLINE+1(L'WKLINE-1),WKLINE
         MVC   WKLINE+2(9),=CL9'PSCBU   :'
         UNPK  WKLINE+11(9),PSCBU(5)
         UNPK  WKLINE+19(9),PSCBU+4(5)
         TR    WKLINE+11(16),TBHEX-X'F0'
         MVC   WKLINE+27(32),=CL32' (RESERVED FOR INSTALLATION USE)'
         LA    R0,59
         LA    R1,WKLINE
         B     GOBACK
         DROP  R4
         SPACE 1
        LTORG
         SPACE 1
         DROP  R10
         EJECT
*------- PARSE PARAMETERS LIST
         SPACE 1
PARMLST  IKJPARM  DSECT=IKJPARMD
KWUPT    IKJKEYWD DEFAULT='NOUPT'
         IKJNAME  'NOUPT'
         IKJNAME  'UPT'
KWECT    IKJKEYWD DEFAULT='NOECT'
         IKJNAME  'NOECT'
         IKJNAME  'ECT'
KWPSCB   IKJKEYWD DEFAULT='NOPSCB'
         IKJNAME  'NOPSCB'
         IKJNAME  'PSCB'
         IKJENDP
         EJECT
*------- WORK-AREAS - DSECT'S.
         SPACE 1
WKAREA   DSECT
         DS    18F            FIRST S.A.
DBLWRD   DS    D
         SPACE 1
        $TEW$WA
         SPACE 1
WKEDIT   DS    CL8
WKLINE   DS    CL78
         SPACE 1
SWREQS   DS    XL1'0'         REQUESTS INDICATORS
RQUPT    EQU   X'80'               DISPLAY UPT
RQECT    EQU   X'40'               DISPLAY ECT
RQPSCB   EQU   X'20'               DISPLAY PSCB
         SPACE 1
WALT     EQU   WASIZE+8
WALEN    EQU   ((WALT+7)/8)*8
CTLBWA   DS    0D,(WALEN)X    CONTROL BLOCKS WORK-AREA
         SPACE 1
WKLEN    EQU   (((*-WKAREA)+7)/8)*8
         SPACE 1
        $TEW$DS
        IKJUPT
UPTLEN   EQU   *-UPT
        IKJECT
ECTLEN   EQU   *-ECT
         SPACE 1
         END
