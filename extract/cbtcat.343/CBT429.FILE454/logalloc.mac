UVLA     TITLE 'U V L A -- LOGALLOC TSO-COMMAND.'
LOGALLOC START 0
         SPACE 1
*--------------------------------------------------------------------*
*                          L O G A L L O C                           *
*--------------------------------------------------------------------*
* NAME :       LOGALLOC                                              *
* ATTRIBUTES : RENT                                                  *
* ORIGIN :     EXTRACTED FROM TAPE CBT 89 FEB, FILE 437.             *
*              ADAPTED BY : MOINIL P.A.                              *
* DESCRIPTION :                                                      *
*        THE 'LOGALLOC' COMMAND IS USED IN CONJUNCTION WITH THE      *
*        COMMAND LOGON PROCEDURE.                                    *
*        LOGALLOC MARKS SOME OR ALL DATA-SETS WHICH ARE ALLOCATED    *
*        AT THE TIME IT IS INVOKED AS BEING PERMANENTLY ALLOCATED.   *
*        THIS MEANS THAT THE 'FREE ALL' COMMAND WILL NOT FREE THEM,  *
*        AND ALLOWS A USER TO CONCATENATE HIS OWN HELP, CLIST,       *
*        ETC..., DATA-SETS TO THE NORMAL ONES, EFFECTIVELY CREATING  *
*        A CUSTOMIZED LOGON PROCEDURE.                               *
*        NOTE THAT THE STEPLIB IS NOT PERMITTED TO BE CHANGED BY     *
*        THIS TECHNIQUE, SINCE IT IS OPENED BY THE INTIATOR.         *
*        IF A  FILE(FILE-NAME-LIST) OR DDNAME(FILE-NAME-LIST)        *
*        IS ENTERED, ONLY THE DDNAMES IN THE LIST ARE MARKED         *
*        PERMANENTLY ALLOCATED, OTHERWISE, ALL DYNAMICALLY           *
*        ALLOCATED DATA-SETS ARE MARKED.                             *
*--------------------------------------------------------------------*
         SPACE 1
LOGALLOC AMODE 24
LOGALLOC RMODE 24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=LDYNAMIC,TYPE=RENT
         LR    R9,R13
         USING WRKDSECT,R9
         MVI   FLAG,0              CLEAR FLAG
        $TEW$EC MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
        $TEW$PP PARM=PARMLST,REG=R4,ERR=PARSERR,ATT=PARSERR
         SPACE 1
*------- CHECK FOR A FILE-NAME LIST.
         SPACE 1
         CLI   FILE+1,0            SPECIFIED?
         BE    *+L'*+12            NO
         LA    R4,DDNAMES          GET PLACE
         TM    6(R4),X'80'         EXIST?
         BO    *+L'*+2             YES
         XR    R4,R4               ZERO IT
         DROP  R4
         SPACE 1
*------- FIND THE DSAB CHAIN.
         SPACE 1
         ST    R4,SAVER4
         L     R2,CVTPTR           CVT ADDR
         USING CVTMAP,R2
         L     R2,CVTTCBP          TCB-TCB ADDR (TCBHEAD)
         DROP  R2
         L     R2,4(,R2)           TCB ADDR (CURRENT TCB)
         USING TCB,R2
         L     R2,TCBJSCB          JOB STEP CONTROL BLOCK ADDR
         DROP  R2
         USING IEZJSCB,R2
         L     R2,JSCDSABQ         LOAD ADDRESS OF QDB FOR DSAB CHAIN
         CLC   8(4,R2),=XL4'0'     ZERO DATA-SETS FOR THIS USER
         BNE   DSOK
         LA    R0,L'BADREQ         IF HE DONT HAVE ANY WE CANT PERM EM
         LA    R1,BADREQ           SO SET RC=12
         BAS   R14,PUTLNE
         LA    R2,12               RC=12 -----------------------------
         B     LEAVE
         DROP  R2
DSOK     L     R2,12(R2)           LOAD ADDRESS OF FIRST DSAB
         USING DSAB,R2
         SPACE 1
*------- LOOP THROUGH THE DSABS. IF DYNAMICALLY ALLOCATED, MARK THEM
*              PERMANENTLY ALLOCATED.
*              IF A FILE-NAME LIST WAS SPECIFIED, ONLY MARK THE ONES
*              IN THE LIST.
         SPACE 1
INLOOP   TM    DSABFLG1,DSABDALC   DATA-SET DYNAMICALLY ALLOCATED
         BZ    NOPERM              NO, GO LOOK AT NEXT ONE
         ICM   R4,B'1111',SAVER4
         BZ    PERM                NO PARM, MARK THEM ALL
         L     R15,DSABTIOT        POINT TO TIOT
         USING TIOELNGH,R15
         CLI   TIOEDDNM,C' '       PART OF A CONCAT GROUP?
         BNE   RESETFLG            NO, CHECK THE LIST
         TM    FLAG,FCG            IS THE CONCAT GROUP FLAG ON?
         BO    PERM                YES, GO MARK THIS ONE
RESETFLG NI    FLAG,255-FCG        TURN OFF THE FLAG
DDLOOP   L     R14,0(R4)           PARSED DDNAME
         LH    R1,4(R4)            PARSED LENGTH
         BCT   R1,*+L'*+6          DECREMENT LENGTH
         CLC   0(*-*,R14),TIOEDDNM <<EXECUTED>>
         EX    R1,*-6              IS THIS DDNAME REQUESTED ?
         BE    PERM                YES, GO MARK IT PERM
         CLI   8(R4),X'FF'         END OF LIST?
         BE    NOPERM              YES, GET NEXT DSAB
         L     R4,8(R4)            NO, GET NEXT PARSE PDE
         B     DDLOOP
         DROP  R15
         SPACE 1
*------- MARKS THE DATA-SET PERMANENTLY ALLOCATED.
         SPACE 1
PERM    STAX   DEFER=YES           NO ATTN WHILE WE ARE AUTHORIZED
        ZEROKEY ,                  ENTER KEY ZERO
         NI    DSABFLG1,X'FF'-DSABDALC INDICATE NO DYNAMICALLY ALLOC'D
        RESETKEY ,                 RE-ENTER PROBLEM KEY
        STAX   DEFER=NO
         OI    FLAG,FCG            TURN ON FLAG FOR CONCAT GROUP
NOPERM   L     R2,DSABFCHN         LOAD POINTER TO NEXT DSAB ENTRY
         LTR   R2,R2               END OF CHAIN ?
         BNZ   INLOOP              NO, KEEP LOOKING
         DROP  R2
         SPACE 1
*------- CLEANUP AND EXIT.
         SPACE 1
         XR    R2,R2               RC=0 -------------------------------
         B     LEAVE
PARSERR  LA    R2,8                RC=8 -------------------------------
LEAVE   IKJRLSA #TSANSW            FREE UP CORE
QUIT    $XRET  CC=(R2),LV=LDYNAMIC,TYPE=RENT DIE
SETRC    LA    R2,16               RC=16 ------------------------------
         B     QUIT
         EJECT
*------- CONSTANTS, DSECT'S, ETC...
         SPACE 1
BADREQ   DC    C' -> NO DATA-SETS STATUS, LOGALLOC FAILED'
         SPACE 1
        LTORG
         SPACE 1
        PRINT  NOGEN
PARMLST IKJPARM
FILE    IKJKEYWD
        IKJNAME 'FILE',SUBFLD=DDNAME$,ALIAS=('FI','F')
        IKJNAME 'DDNAME',SUBFLD=DDNAME$,ALIAS=('DDN','DD','D')
DDNAME$ IKJSUBF
DDNAMES IKJIDENT 'DDNAME(S) TO BE MARKED AS PERMANENT',                X
               LIST,                                                   X
               MAXLNTH=8,                                              X
               PROMPT='FILE NAME(S) TO MARK PERMANENT',                X
               HELP='DDNAMES WHICH SHOULD BE PERMANENTLY ALLOCATED',   X
               OTHER=ALPHANUM
        IKJENDP
        PRINT  GEN
         SPACE 1
WRKDSECT DSECT
         DS    18F                 FIRST S.A.
        $TEW$WA
SAVER4   DS    F
WKLINE   DS    CL60
FLAG     DS    X                   SWITCH
FCG      EQU   X'80'
         SPACE 1
LDYNAMIC EQU   (((*-WRKDSECT)+7)/8)*8
         SPACE 1
        PRINT  NOGEN
        $TEW$DS TIOT=YES
        IKJTCB
        IEZJSCB
DSAB    IHADSAB
        PRINT  GEN
         SPACE 1
         END
