         MACRO           CALL CURESTA SUPPLY.
&NAME   $CRS   &ID,&GO,&DEBUG
         LCLA  &Y
         LCLC  &X
.*
.* $CRS MACRO : CALL CURESTA SUPPLY (SEE CURESTA).
.* ------------
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   (N'&SYSLIST GT 3).BAD
&NAME    XR    1,1                 NO PARM. LIST.
         L     15,=V(CURESTA)      LOAD REG. 15 WITH ENTRY.
         BASR  14,15               BRANCH TO ENTRY POINT.
         AIF   (N'&SYSLIST EQ 0).Z
         AIF   ('&GO' EQ '').A2
         AIF   ('&GO'(1,1) EQ '(').A1
         B     &GO
         AGO   .B1
.A1      ANOP
&Y       SETA  K'&GO-2
&X       SETC  '&GO'(2,&Y)
         BR    &X
         AGO   .B1
.A2      ANOP
&Y       SETA  0
         AIF   ('&DEBUG' EQ '').A3
&Y       SETA  6
         AIF   ('&DEBUG' EQ 'DEBUG').A3
         AIF   ('&DEBUG' NE 'D').INV
&Y       SETA  2
.A3      AIF   ('&ID' EQ '').A4
&Y       SETA  &Y+2
.A4      B     *+L'*+&Y
.B1      AIF   ('&DEBUG' EQ '').B3
         AIF   ('&DEBUG' NE 'DEBUG').B2
         DC    AL1(5),CL5'DEBUG'
         AGO   .B3
.B2      AIF   ('&DEBUG' NE 'D').INV
         DC    AL1(1),CL1'D'
.B3      AIF   ('&ID' EQ '').Z
         DC    AL1(255),AL1(&ID)
         MEXIT
.BAD     MNOTE 8,' TOO MANY OPERANDS ******************************** '
         MEXIT
.INV     MNOTE 8,' 3RD OPERAND INVALID (NOT "D" OR "DEBUG") ********* '
.Z       MEND
