         MACRO           SYSCTLG ENTRY FORMATS.
        $CTLGD     &DUMMY
         SPACE 1
*        O.S. CATALOG CONTROL BLOCKS DESCRIPTION.
*        ----------------------------------------
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         SPACE 1
CTLDSECT DSECT  .                  CATALOG ENTRIES DESCRIPTION.
         SPACE 2
*        COMMON DEFINITION TO ALL ENTRIES.
         SPACE 1
ENAME    DS    CL8 .               NAME.
ETTR     DS    XL3 .               ADDRESS.
ETYPE    DS    XL1 .               TYPE.
         SPACE 2
ELENGTH  EQU   *-CTLDSECT .        COMMON ENTRIES LENGTH.
         SPACE 5
         ORG   CTLDSECT .          ALIAS ENTRY (AE).
         SPACE 1
*        AN ALIAS ENTRY DEFINES AN ALTERNATE NAME FOR THE
*        HIGH-LEVEL QUALIFIER OF A DATA SET NAME.
         SPACE 1
         DS    CL8 .               CONTAINS THE ALIAS OF THE HIGH-LEVEL
*                                  INDEX WHOSE RELATIVE TRACK ADDRESS
*                                  IS FOUND AT OFFSET 8 OF THIS ENTRY.
         DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  (TTR) OF THE FIRST BLOCK OF THE
*                                  INDEX NAMED AT OFFSET 12 OF THIS
*                                  ENTRY.
         DC    XL1'04' .           INDICATES THAT THIS IS AN ALIAS
*                                  ENTRY, ALSO THAT FOUR HALFWORDS
*                                  FOLLOW IN THE REMAINDER OF THE
*                                  ENTRY.
ETRUEN   DS    CL8 .               TRUE NAME - CONTAINS THE NAME OF THE
*                                  INDEX WHOSE ALIAS APPEARS AT THE
*                                  BEGINNING OF THIS ENTRY.
         EJECT
         ORG   CTLDSECT .          CONTROL VOLUME POINTER ENTRY (CVPE).
         SPACE 1
*        A CONTROL VOLUME POINTER ENTRY CAN APPEAR ONLY IN
*        VOLUME INDEXES. TWO FORMS ARE POSSIBLE, THE OLD FORM,
*        CREATED PRIOR TO RELEASE 17, AND THE NEW FORM, CREATED
*        SINCE RELEASE 17. BOTH FORMS ARE SHOWN HERE.
         SPACE 1
*                        OLD CVOL POINTER ENTRY.
         DS    CL8 .               CONTAINS A HIGH-LEVEL NAME THAT
*                                  APPEARS IN THE VOLUME INDEX OF THE
*                                  CONTROL VOLUME IDENTIFIED AT OFFSET
*                                  12 OF THIS ENTRY.
         DC    XL3'00' .           ZERO FIELD.
         DC    XL1'03' .           INDICATES THAT THIS IS EITHER AN OLD
*                                  CVOL POINTER ENTRY (CVPE), OR AN
*                                  INDEX CONTROL ENTRY (ICE). AN ICE
*                                  ALWAYS APPEARS AS THE FIRST RECORD
*                                  OF AN INDEX LEVEL, A CVOL POINTER
*                                  ENTRY ALWAYS APPEARS IN THE VOLUME
*                                  INDEX. THIS IS ALSO THE NUMBER OF
*                                  HALFWORDS THAT FOLLOW IN THE
*                                  REMAINDER OF THE ENTRY.
EVOLIDO  DS    CL6 .               SERIAL NUMBER OF THE CONTROL VOLUME
*                                  WHOSE VOLUME INDEX CONTAINS AN ENTRY
*                                  FOR THE NAME FOUND AT THE BEGINNING
*                                  OF THIS ENTRY.
         SPACE 1
         ORG   CTLDSECT .          CONTROL VOLUME POINTER ENTRY (CVPE).
         SPACE 1
*                        NEW CVOL POINTER ENTRY.
         DS    CL8 .               CONTAINS A HIGH-LEVEL NAME THAT
*                                  APPEARS IN THE VOLUME INDEX OF THE
*                                  CONTROL VOLUME IDENTIFIED AT OFFSET
*                                  12 OF THIS ENTRY.
         DC    XL3'00' .           ZERO FIELD.
         DC    XL1'05' .           INDICATES THAT THIS IS A NEW CVOL
*                                  POINTER (CVPE) OR THE VOLUME INDEX
*                                  CONTROL ENTRY. THE VOLUME INDEX
*                                  CONTROL ENTRY ALWAYS APPEARS AS THE
*                                  FIRST ENTRY IN THE FIRST BLOCK OF
*                                  SYSCTLG. A CVOL POINTER NEVER
*                                  APPEARS AS THE FIRST ENTRY OF THE
*                                  FIRST BLOCK. ALSO INDICATES THAT
*                                  FIVE HALFWORDS FOLLOW IN THE
*                                  REMAINDER OF THE ENTRY.
EDEVTYP  DS    XL4 .               CONTAINS THE BINARY DEVICE CODE OF
*                                  THE CONTROL VOLUME WHOSE VOLUME
*                                  INDEX CONTAINS AN ENTRY FOR THE NAME
*                                  FOUND AT THE BEGINNING OF THIS
*                                  ENTRY.
EVOLID   DS    CL6 .               SERIAL NUMBER OF THE CONTROL VOLUME
*                                  WHOSE VOLUME INDEX CONTAINS AN ENTRY
*                                  FOR THE NAME FOUND AT THE BEGINNING
*                                  OF THIS ENTRY.
         EJECT
         ORG   CTLDSECT .          DATA SET POINTER ENTRY (DSPE).
         SPACE 1
*        A DATA SET POINTER ENTRY CAN APPEAR IN ANY INDEX LEVEL.
*        IT CONTAINS THE SIMPLE NAME OF A DATA SET AND FROM
*        ONE TO FIVE 12-BYTE FIELDS THAT EACH IDENTIFY A VOLUME
*        ON WHICH THE NAMED DATA SET RESIDES.
         SPACE 1
         DS    CL8 .               CONTAINS THE SIMPLE NAME OF THE DATA
*                                  SET WHOSE VOLUMES ARE IDENTIFIED AT
*                                  OFFSET 12 OF THIS ENTRY.
EDSCBTTR DS    XL3 .               CONTAINS EITHER BINARY ZERO OR, WHEN
*                                  THE DATA SET RESIDES ON ONLY ONE
*                                  VOLUME, THE RELATIVE TRACK ADDRESS
*                                  (TTR) OF THE DATA SET CONTROL BLOCK
*                                  (DSCB) FOR THIS DATA SET IN THE
*                                  VOLUME TABLE OF CONTENTS (VTOC).
         DC    0XL1'07' .          INDICATES THAT THIS IS A DATA SET
         DC    0XL1'0D' .          POINTER ENTRY (DSPE). ALSO INDICATES
         DC    0XL1'13' .          THE NUMBER OF HALFWORDS THAT FOLLOW
         DC    0XL1'19' .          IN THE REMAINDER OF THIS ENTRY.
         DC    XL1'1F'
EVOLCNT  DS    XL2 .               CONTAINS THE BINARY COUNT OF THE
*                                  NUMBER OF VOLUMES IDENTIFIED
*                                  BEGINNING AT OFFSET 14.
EDATA    DS    5XL12 .             CONTAINS FROM ONE TO FIVE 12-BYTE
*                                  ENTRIES, EACH OF WHICH IDENTIFIES
*                                  ONE VOLUME ON WHICH THE DATA SET
*                                  RESIDES. CATALOG MANAGEMENT NEITHER
*                                  USES NOR CHECKS THE CONTENTS OF THIS
*                                  FIELD.
         EJECT
         ORG   CTLDSECT .          GENERATION INDEX PTR ENTRY (GIPE).
         SPACE 1
*        A GENERATION INDEX POINTER ENTRY CAN APPEAR IN ANY
*        INDEX EXCEPT A GENERATION INDEX. IT CORRESPONDS TO THE
*        SIMPLE NAME USED IN THE RELATIVE NAME FOR A GDG DATA SET.
         SPACE 1
         DS    CL8 .               CONTAINS THE NAME OF THE GENERATION
*                                  INDEX TO WHICH THIS ENTRY POINTS.
         DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE FIRST BLOCK OF THE GENERATION
*                                  INDEX NAMED IN THIS ENTRY, IN THE
*                                  FORM TTR.
         DC    XL1'02' .           INDICATES THAT THIS IS A GENERATION
*                                  INDEX POINTER ENTRY (GIPE). ALSO
*                                  INDICATES THAT TWO HALFWORDS FOLLOW
*                                  IN THE REMAINDER OF THIS ENTRY.
EGFLAGS  DS    XL1 .               CONTAINS THE OPTIONS SPECIFIED BY
*                                  THE CREATOR OF THE GENERATION DATA
*                                  GROUP.
GIPEDEL  EQU   X'02' .             DELETE OPTION.
GIPEEMP  EQU   X'01' .             EMPTY OPTION.
EGMAXSIZ DS    XL1 .               CONTAINS A BINARY NUMBER SPECIFYING
*                                  THE MAXIMUM NUMBER OF GENERATIONS
*                                  ALLOWED IN THE GENERATION INDEX AT
*                                  ONE TIME.
EGCURSIZ DS    XL2 .               CONTAINS THE BINARY NUMBER OF
*                                  GENERATIONS CURRENTLY CATALOGED IN
*                                  THE INDEX.
         EJECT
         ORG   CTLDSECT .          INDEX CONTROL ENTRY (ICE).
         SPACE 1
*        THE INDEX CONTROL ENTRY IS THE FIRST ENTRY IN ALL
*        INDEXES EXCEPT THE VOLUME INDEX.
         SPACE 1
INAME    DS    0XL8 .              LOW VALUE OF BINARY ONE ENSURES THAT
         DC    7X'00',X'01' .      THIS IS THE FIRST ENTRY IN THE
*                                  INDEX.
ILSTBLK  DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE LAST BLOCK ASSIGNED TO THE
*                                  INDEX, IN THE FORM TTR.
ITYPE    DC    XL1'03' .           INDICATES THAT THIS IS EITHER AN ICE
*                                  OR AN OLD CVOL POINTER. AN ICE
*                                  ALWAYS APPEARS AS THE FIRST ENTRY OF
*                                  AN INDEX. AN OLD CVOL POINTER ALWAYS
*                                  APPEARS IN THE VOLUME INDEX. ALSO
*                                  INDICATES THE NUMBER OF HALFWORDS
*                                  THAT FOLLOW IN THE REMAINDER OF THE
*                                  ENTRY.
IFSTBLK  DS    XL3 .               CONTAINS THE RELATIVE ADDRESS OF THE
*                                  BLOCK IN WHICH THIS ENTRY APPEARS,
*                                  IN THE FORM TTR.
ILIASCNT DS    XL1 .               CONTAINS A BINARY COUNT OF ALIASES
*                                  ASSIGNED TO THE INDEX. THIS COUNT IS
*                                  ALWAYS ZERO FOR INDEXES THAT ARE NOT
*                                  HIGH-LEVEL. AN INDEX CANNOT BE
*                                  DELETED IF THIS COUNT IS NON-ZERO.
         DS    XL2 .               RESERVED.
         EJECT
         ORG   CTLDSECT .          INDEX LINK ENTRY (ILE).
         SPACE 1
*        AN INDEX LINK ENTRY IS ALWAYS THE LAST ENTRY IN ANY
*        INDEX BLOCK. IT IS USED TO LINK BLOCKS OF ONE INDEX
*        INTO A CHAIN.
         SPACE 1
         DC    0XL8'00',8X'FF' .   HIGH VALUE (ALL BITS ON) ENSURES
*                                  THAT THIS IS THE LAST ENTRY IN THE
*                                  INDEX.
         DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE NEXT BLOCK OF THE SAME INDEX,
*                                  IF THERE IS ONE, IN THE FORM TTR.
*                                  WHEN THIS IS THE LAST (OR ONLY)
*                                  BLOCK, THIS FIELD CONTAINS BINARY
*                                  ZERO.
         DC    XL1'00' .           INDICATES THAT THIS IS EITHER AN ILE
*                                  OR AN IPE. THE NAME FIELD OF AN ILE
*                                  ALWAYS CONTAINS X'FF...FF', THE NAME
*                                  FIELD OF AN IPE NEVER DOES. ALSO
*                                  INDICATES THAT THERE ARE NO MORE
*                                  HALFWORDS IN THE ENTRY.
         SPACE 2
         ORG   CTLDSECT .          INDEX POINTER ENTRY (IPE).
         SPACE 1
*        THE INDEX POINTER ENTRY CAN APPEAR IN ANY INDEX EXCEPT
*        A GENERATION INDEX. IT POINTS TO A LOWER INDEX.
         SPACE 1
         DS    CL8 .               CONTAINS THE NAME OF THE INDEX TO
*                                  WHICH THIS ENTRY POINTS.
         DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE FIRST BLOCK OF THE INDEX
*                                  NAMED IN THIS ENTRY, IN THE FORM
*                                  TTR.
         DC    XL1'00' .           INDICATES THAT THIS IS EITHER AN IPE
*                                  OR AN ILE. THE NAME FIELD OF AN ILE
*                                  ALWAYS CONTAINS X'FF...FF', THE NAME
*                                  FIELD OF AN IPE NEVER DOES. ALSO
*                                  INDICATES THAT THERE ARE NO MORE
*                                  BYTES IN THE ENTRY.
         EJECT
         ORG   CTLDSECT .          VOLUME CONTROL BLOCK (VCB).
         SPACE 1
*        A VOLUME LIST CAN BE RECORDED IN ONE OR MORE VOLUME
*        CONTROL BLOCKS. EACH VOLUME CONTROL BLOCK IS ONE BLOCK
*        OF THE SYSCTLG DATA SET, AND CAN IDENTIFY UP TO 20
*        VOLUMES ON WHICH ONE DATA SET IS RECORDED.
         SPACE 1
* NOTE - THIS BLOCK IS DIFFERENT FROM OTHER BLOCKS OF SYSCTLG.
*        THE FIRST HALFWORD DOES NOT CONTAIN THE NUMBER OF BYTES
*        USED IN THE BLOCK AS DO OTHER SYSCTLG BLOCKS. THE FIELD
*        VCBVOLCT, SHOWN BELOW, IS THE FIRST HALFWORD OF THE
*        VCB BLOCK.
         SPACE 1
VCBVOLCT DS    XL2 .               CONTAINS THE NUMBER OF VOLUMES
*                                  IDENTIFIED IN THIS AND SUBSEQUENT
*                                  VOLUME CONTROL BLOCKS. THIS NUMBER
*                                  IS REDUCED BY 20 FOR EACH SUBSEQUENT
*                                  VOLUME CONTROL BLOCK. FOR EXAMPLE,
*                                  IF A DATA SET RESIDES ON 61 VOLUMES,
*                                  IT USES FOUR VOLUME CONTROL BLOCKS.
*                                  THIS FIELD OF EACH BLOCK CONTAINS
*                                  61, 41, 21 AND 1, RESPECTIVELY.
VCBVOLS  DS    20XL12 .            CONTAINS FROM 1 TO 20 12-BYTE
*                                  ENTRIES, EACH OF WHICH IDENTIFIES
*                                  ONE OF THE VOLUMES ON WHICH THE DATA
*                                  SET RESIDES. CATALOG MANAGEMENT
*                                  NEITHER USES NOR INSPECTS THE
*                                  CONTENT OF THESE ENTRIES. EACH
*                                  12-BYTE ENTRY CONTAINS A 4-BYTE
*                                  DEVICE CODE, A 6-BYTE VOLUME SERIAL
*                                  NUMBER, AND A 2-BYTE DATA SET
*                                  SEQUENCE NUMBER.
         DC    XL10'00' .          ZERO FIELD.
         DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE NEXT VOLUME CONTROL BLOCK, IF
*                                  THERE IS ONE, IN THE FORM TTR. IF
*                                  THIS IS THE LAST (OR ONLY) BLOCK OF
*                                  THE VOLUME CONTROL BLOCK, THIS FIELD
*                                  CONTAINS BINARY ZERO.
         DC    XL1'00' .           ZERO FIELD.
         EJECT
         ORG   CTLDSECT .          VOLUME CTR BLOCK PTR ENTRY (VCBPE).
         SPACE 1
*        A VOLUME CONTROL BLOCK POINTER ENTRY CAN APPEAR IN ANY
*        INDEX. IT IS USED WHEN A DATA SET RESIDES ON MORE
*        THAN FIVE VOLUMES.
         SPACE 1
         DS    CL8 .               CONTAINS THE SIMPLE NAME OF THE
*                                  DATA SET WHOSE VOLUMES ARE
*                                  IDENTIFIED IN THE VOLUME CONTROL
*                                  BLOCK THAT IS POINTED TO BY THIS
*                                  ENTRY.
         DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE VOLUME CONTROL BLOCK
*                                  IDENTIFIYING THE VOLUMES CONTAINING
*                                  THE DATA SET NAMED IN THIS ENTRY,
*                                  IN THE FORM TTR.
         DC    XL1'01' .           INDICATES THAT THIS IS A VOLUME
*                                  CONTROL BLOCK POINTER ENTRY. ALSO
*                                  INDICATES THAT ONE HALFWORD FOLLOWS
*                                  IN THE REMAINDER OF THIS ENTRY.
         DC    XL2'00' .           ZERO FIELD.
         EJECT
         ORG   CTLDSECT .          VOLUME INDEX CONTROL ENTRY (VICE).
         SPACE 1
*        THE VOLUME INDEX CONTROL ENTRY IS ALWAYS THE FIRST
*        ENTRY IN THE FIRST BLOCK OF DATA SET SYSCTLG.
*        IT IS THE CONTROL RECORD FOR THE ENTIRE DATA SET, AND
*        ACTS AS AN ICE FOR THE VOLUME INDEX.
         SPACE 1
VNAME    DS    0XL8 .              ALWAYS CONTAINS A BINARY ONE TO
         DC    7X'00',X'01' .      ENSURE THAT THIS IS THE FIRST ENTRY
*                                  OF THE VOLUME INDEX.
VLSTBLK  DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE LAST BLOCK OF THE VOLUME
*                                  INDEX, IN THE FORM TTR.
VTYPE    DC    XL1'05' .           INDICATES THAT THIS IS THE VOLUME
*                                  INDEX CONTROL ENTRY OR A NEW CVOL
*                                  POINTER ENTRY. THE VOLUME INDEX
*                                  CONTROL ENTRY IS ALWAYS THE FIRST
*                                  ENTRY OF THE FIRST BLOCK OF SYSCTLG,
*                                  A CVOL POINTER IS NEVER THE FIRST
*                                  ENTRY. ALSO INDICATES THAT FIVE
*                                  HALFWORDS FOLLOW IN THE REMAINDER
*                                  OF THE ENTRY.
VCLSTBLK DS    0XL3 .              CONTAINS THE RELATIVE TRACK ADDRESS
         DS    XL2 .               OF THE LAST BLOCK IN SYSCTLG, IN THE
*                                  FORM TTR.
VHIREC   DS    XL1 .               RECORD NUMBER OF TTR IN VCLSTBLK.
*                                  NOTE THAT THIS FIELD IS THE LAST
*                                  BYTE OF VCLSTBLK (OFFSET 12).
         DC    XL1'00' .           ZERO FIELD.
VFHOLE   DS    XL3 .               CONTAINS THE RELATIVE TRACK ADDRESS
*                                  OF THE FIRST UNUSED BLOCK IN
*                                  SYSCTLG, IN THE FORM TTR.
         DC    XL1'00' .           ZERO FIELD.
         DS    XL2 .               RESERVED.
         SPACE 2
&SYSECT  CSECT  .                  RESTORE ORIGINAL CSECT.
         SPACE 2
         MEND
