OCS      TITLE 'OPERATOR COMMUNICATION SUBROUTINE.'
SETOPC   START 0
         SPACE 2
* CALLING SEQUENCE : CALL SETOPC,(A,B),VL
* ------------------             (A)
*
*        WHERE : A - IS THE ADDRESS OF THE BLOCK INFORMAL TEXT TO
*                    BE DISPLAYED AT THE SYSTEM MASTER CONSOLE FOR
*                    OPERATOR SETUP (OPTIONAL). WHEN THIS ARGUMENT
*                    IS OMITTED, OR THE ADDRESS OF THIS ARGUMENT IS
*                    SET TO ZERO IN THE PARAMETERS LIST, OR THE
*                    PARAMETERS LIST POINTER (REGISTER 1) IS ZERO,
*                    THE DEFAULT REQUEST IS ASSUMED TO BE THE USE
*                    OF THE OPERATING SYSTEM STOP (P) COMMAND ONLY.
*                    BLOCK INFORMAL TEXT (THE MAXIMUM TEXT LENGTH
*                    ALLOWED IS 72 CHARACTERS BY MESSAGE, IF MORE,
*                    IT IS TRUNCATED) FORMAT :
*
*                             DC    AL1(L'TEXT1)
*                    TEXT1    DC    C'...MESSAGE 1...'
*                             DC    AL1(L'TEXT2)
*                    TEXT2    DC    C'...MESSAGE 2...'
*                    ... ... ...
*                             DC    AL1(L'TEXTN)
*                    TEXTN    DC    C'...MESSAGE N...'
*                             DC    AL1(0) OR AL1(225) : END OF LIST.
*
*                B - IS THE RESPONSE INDICATOR (FORMAT FULLWORD - F
*                    BOUNDARY) IF SUPPLIED (OPTIONAL). VALUE IS ALWAYS
*                    PLACED IN REGISTER 15 AND CAN BE :
*
*                     0 - OPERATOR COMMUNICATION IS READY.
*                         THE REGISTER 1 CONTAINS THE ADDRESS OF THE
*                         MESSAGE-ID'S NUMBERS LIST FOR DELETE USE WHEN
*                         THE CALLING PROGRAM DOES NOT TERMINATE BY THE
*                         STOP OR MODIFY (LIKE STOP) OPERATOR COMMAND,
*                         AND THE REGISTER 0 CONTAINS THE LENGTH OF THE
*                         GETMAINED AREA INTO WHICH THE LIST HAS BEEN
*                         BUILDED (FOR FREEMAIN PURPOSE).
*                     1 - CALLING SEQUENCE OR ARGUMENT ALIGNMENT ERROR.
*
*        ALIAS : CALL INIFIN
         SPACE 2
MAX      EQU   64                  MAXIMUM NUMBER OF MESSAGES.
         EJECT
* CALLING SEQUENCE : CALL OPCOM,(A,B),VL
* ------------------            (A)
*
*        WHERE : A - IS THE RESPONSE INDICATOR (FORMAT FULLWORD - F
*                    BOUNDARY). WHEN THE ADDRESS OF THIS ARGUMENT
*                    IS SET TO ZERO IN THE PARAMETERS LIST, OR THE
*                    PARAMETERS LIST POINTER (REGISTER 1) IS ZERO,
*                    THE VALUE IS ALWAYS PLACED IN REGISTER 15 AND
*                    CAN BE :
*
*                     1 - CONTINUE PROCESSING (NOTHING RECEIVED FROM
*                         THE OPERATOR).
*                     2 - STOP (P) COMMAND RECEIVED.
*                     3 - MODIFY (F) COMMAND RECEIVED.
*                     4 - CALLING SEQUENCE, ARGUMENT ALIGNMENT OR
*                         OPERATOR COMMUNICATION NOT INITIALIZED.
*
*                B - IS THE ADDRESS OF AN ARRAY (OPTIONAL) LARGE ENOUGH
*                    (OTHERWISE TRUNCATED) TO RECEIVE THE EVENTUAL
*                    MODIFY COMMAND TEXT TYPED BY THE OPERATOR. WHEN
*                    THIS ARGUMENT IS OMITTED, THE MODIFY COMMAND TEXT
*                    IS NOT AVAILABLE TO THE CALLER AND THE RESPONSE
*                    INDICATOR IS SET TO 2 (AS STOP COMMAND RECEIVED).
*                    BEFORE ANY CALL, THIS RECEIVING TEXT ARRAY MUST BE
*                    INITIALIZED AS BELOW :
*
*                     +0 - LENGTH OF TEXT RECEIVING FIELD (FORMAT H -
*                          HALFWORD BOUNDARY).
*                     +2 - TEXT RECEIVING FIELD (FORMAT CL...).
*
*                    THE TEXT ENTERED BY THE MODIFY COMMAND IS PLACED
*                    IN THE ARRAY AS FOLLOWS :
*
*                     +0 - LENGTH OF TEXT (FORMAT H).
*                     +2 - TEXT (FORMAT CL..., TEXT IS UPPERCASED).
*
*        ALIAS : CALL VERFIN,(A),VL
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
*        MISCELLANEOUS DEFINITIONS.
         SPACE 1
OFF      EQU   X'0F'               NOP OPERATION SWITCH SETTING.
ON       EQU   X'F0'               BRANCH OPERATION SWITCH SETTING.
EL       EQU   X'80'               END OF LIST INDICATOR.
         EJECT
CIBDSECT DSECT
CIB      IEZCIB
         SPACE 1
SETOPC   CSECT                     RESTORE ORIGINAL CSECT.
         SPACE 2
*        ECB BITS DEFINITION.
         SPACE 1
WBIT     EQU   X'80'               WAITING FOR COMPLETION OF AN EVENT.
CBIT     EQU   X'40'               THE EVENT HAS BEEN COMPLETED.
         EJECT
         ENTRY INIFIN
         SPACE 1
        $XENT  BASE=R12
         B     GPARM
         SPACE 1
DISPL    EQU   *-SETOPC
         SPACE 1
INIFIN   LA    R1,DISPL            ENTER AS CALL SETOPC.
         LA    R15,0(R15)
         SLR   R15,R1
         SR    R1,R1
         BR    R15
         SPACE 1
GPARM    LA    R7,1                CONTROL ARGUMENTS.
         SR    R8,R8
         LTR   R6,R1
         BZ    SWIN
         L     R6,0(R1)
         LA    R6,0(R6)
         TM    0(R1),X'80'
         BO    SWIN
         TM    4(R1),X'80'
         BZ    RETURN
         TM    7(R1),X'03'
         BNZ   RETURN
         L     R8,4(R1)
         LA    R8,0(R8)
         SPACE 1
SWIN     NOP   RETURN
         OI    SWIN+1,ON           CLOSE THIS WAY.
        EXTRACT    ANSWER,'S',FIELDS=(TIOT,COMM)
         L     R2,ANSWER           GET TIOT ADDRESS.
         MVC   MSGIN+57(8),0(R2)   MOVE JOB NAME IN MESSAGE.
         OI    SWINI+1,ON          SET INITIALIZATION SWITCH.
         BAS   R9,ANRESP           INITIALIZE COMMUNICATION.
         NI    SWINI+1,OFF         RESET INITIALIZATION SWITCH.
         L     R5,MAXSP
        GETMAIN R,LV=(R5)
         ST    R1,MSGLA
         LR    R4,R1
         LTR   R6,R6
         BZ    STDIN
         TM    0(R6),255
         BZ    STDIN
         BO    STDIN
         SPACE 1
         LA    R14,*
         SR    R15,R15
         MVCL  R4,R14
         LR    R9,R1
         AL    R1,LASTM
         OI    0(R1),EL
         SR    R7,R7
         LA    R5,72               DISPLAY INFORMAL TEXT.
         SR    R2,R2
NEXT     CLI   0(R6),255
         BE    SETEL
         IC    R2,0(R6)
         LTR   R3,R2
         BZ    SETEL
         LR    R4,R9
         CLR   R2,R5
         BNH   *+L'*+2
         LR    R2,R5
         BCTR  R2,0
         EX    R2,MVMSG
        WTO    MF=(E,MSGOP)
         TM    0(R4),EL
         BO    SETEL
         ST    R1,0(R4)
         LA    R9,4(,R4)
         MVI   MSGOP+4,C' '
         MVC   MSGOP+5(71),MSGOP+4
         LA    R6,1(R3,R6)
         B     NEXT
MVMSG    MVC   MSGOP+4(*-*),1(R6)
         SPACE 1
STDIN   WTO    MF=(E,MSGIN)
SETEL    ST    R1,0(R4)
         OI    0(R4),EL
         L     R1,4(R13)
         MVC   20(8,R1),MAXSP      BACK LENGTH AND MSG-ID'S LIST.
         B     RETURN
         SPACE 1
         DROP  R12
         SPACE 2
         ENTRY VERINI
         ENTRY OPCOM
         SPACE 1
        CNOP   2,8
VERINI   OI    0(R1),X'80'         ENTER AS CALL OPCOM,(A),VL.
         BASR  R15,0
         SPACE 1
OPCOM   $XENT  BASE=R12,DT=NO
         SPACE 1
         SR    R6,R6               CONTROL ARGUMENTS.
         LA    R7,4
         LTR   R8,R1
         BZ    ANAL
         TM    3(R1),X'03'
         BNZ   RETURN
         L     R8,0(R1)
         LA    R8,0(R8)
         TM    0(R1),X'80'
         BO    ANAL
         TM    4(R1),X'80'
         BZ    RETURN
         L     R6,4(R1)
         LA    R6,0(R6)
         SPACE 1
ANAL     L     R2,ECBA             GET ADDR. OF ECB FOR COMMUNICATION.
         LTR   R2,R2               TEST IF INITIALIZED.
         BZ    RETURN
         LA    R7,1
         TM    0(R2),CBIT          IS COMMUNICATION ECB POSTED.
         BZ    RETURN
        WAIT   1,ECB=(R2)
         BAS   R9,ANRESP           ANALYZE COMMUNICATION.
         B     RETURN
         B     MODR
STOPR    L     R1,MSGLA
        DOM    MSGLIST=(1)
         LM    R0,R1,MAXSP
        FREEMAIN R,LV=(0),A=(1)
         LA    R7,2                SET STOP RESPONSE.
         B     RETURN
MODR     LTR   R6,R6
         BZ    STOPR
         LA    R7,3                SET MODIFY RESPONSE.
         SPACE 1
         DROP  R12
         SPACE 2
RETURN   BASR  R12,0
         USING *,R12
         LTR   R8,R8
         BZ    EXIT
         ST    R7,0(R8)
EXIT    $XRET  CC=(R7)
         SPACE 1
         DROP  R12
         EJECT
*        ROUTINE TO ANALYZE OPERATOR COMMUNICATION.
*        ------------------------------------------
*
*        BASE REGISTER = R10.
*        LINK REGISTER = R9.
*        MOVE MODIFY TEXT POINTER = R6.
*        WORK REGISTERS = R0, R1, R2, R3, R4, R5.
*
*        RETURN +0 = CONTINUE.
*               +4 = MODIFY COMMAND RECEIVED.
*               +8 = STOP COMMAND RECEIVED.
         SPACE 1
ANRESP   BASR  R10,0               SET LOCAL BASE REGISTER.
         USING *,R10
         L     R3,ANSWER+4         GET ADDR. OF ECB COMMUNICATION AREA.
         L     R4,0(R3)            GET ECB ADDRESS.
         L     R5,4(R3)            GET CIB ADDRESS.
         LA    R5,0(R5)            CLEAR HIGH BYTE.
         LA    R2,4(R3)            SET ADDRESS OF CIB ADDRESS.
         LTR   R5,R5               TEST IF CURRENT CIB SET UP.
         BZ    EMPTY
SWINI    B     SKIP
         USING CIBDSECT,R5
         CLI   CIBVERB,CIBSTOP     IS IT STOP (P) COMMAND.
         BE    8(R9)               YES, RETURN +8.
         CLI   CIBVERB,CIBMODFY    IS IT MODIFY (F) COMMAND.
         BNE   SKIP
         LH    R1,CIBDATLN         GET DATA FIELD LENGTH.
         LTR   R1,R1
         BNP   SKIP
         LTR   R6,R6
         BZ    PURGE
         CH    R1,0(R6)
         BNH   *+L'*+4
         LH    R1,0(R6)
         BCTR  R1,0
         MVI   1(R6),C' '
         EX    R1,CLDATA
         MVC   0(2,R6),CIBDATLN    MOVE LENGTH OF DATA.
         EX    R1,MVDATA           MOVE AND UPPER CASE DATA.
PURGE   QEDIT  ORIGIN=(2),BLOCK=(5)
         B     4(R9)               RETURN +4.
CLDATA   MVC   2(*-*,R6),1(R6)
MVDATA   OC    2(*-*,R6),CIBDATA
         DROP  R5
SKIP    QEDIT  ORIGIN=(2),BLOCK=(5)
         BR    R10
EMPTY   QEDIT  ORIGIN=(2),CIBCTR=2
         ST    R4,ECBA             ADDR. OF ECB FOR COMMUNICATION.
         BR    R9                  RETURN +0.
         SPACE 1
         DROP  R10
         SPACE 2
MAXNM    EQU   (MAX-1)*4
MAXAR    EQU   (((MAX*4)+7)/8)*8
         SPACE 2
*        CONSTANTS AND WORK-AREAS.
         SPACE 1
ANSWER   DC    A(*-*,*-*)
ECBA     DC    A(*-*)
MAXSP    DC    A(MAXAR)            AREA LENGTH.
MSGLA    DC    A(*-*)              START MSG-ID'S LIST.
LASTM    DC    A(MAXNM)
MSGIN   WTO    '-*-*-*-*-*- TO INTERRUPT PROGRAM USE THE COMMAND = P   1
                      ',ROUTCDE=(2),DESC=(2),MF=L
MSGOP   WTO    '                                                       1
                                ',ROUTCDE=(2),DESC=(2),MF=L
         SPACE 2
         END
