         MACRO           MODULE IDENTIFICATION.
&NAME   $IDENT &ID=,&DT=YES
         LCLC  &NCS
         SPACE 1
.*
.* $IDENT MACRO : MODULE IDENTIFICATION.
.* --------------
.*
.*       - GENERATE THE IDENTIFIER NAME.
.*       - GENERATE THE ASSEMBLY DATE AND TIME (DT OPERAND).
.*
.* ID OPERAND         - IDENTIFIER NAME (8 CHARACTERS LENGTH MAXIMUM).
.*                      IF OMITTED, THE LABEL NAME IS USED IF SUPPLIED,
.*                      OR THE CURRENT CONTROL SECTION NAME.
.*
.* DT OPERAND         - CAN BE USED TO SUPPRESS THE ASSEMBLY DATE AND
.*                      TIME GENERATION (I.E. - DT=NO).
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   ('&NAME' EQ '').N1
&NCS     SETC  '&NAME'
         AGO   .N2
.N1      ANOP
&NCS     SETC  '&SYSECT'
.N2      SPACE 1
         AIF   ('&ID' EQ '').N3
         DC    CL8'&ID' IDENTIFIER.
         AGO   .N4
.N3      DC    CL8'&NCS' IDENTIFIER.
.N4      AIF   ('&DT' NE 'YES').N5
         DC    CL8'&SYSDATE' ASSEMBLY DATE (MM/DD/YY).
         DC    C'-'
         DC    CL5'&SYSTIME' ASSEMBLY TIME (HH.MM).
.N5      SPACE 1
         MEND
