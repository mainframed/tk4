XTCL     TITLE 'TSO COMMANDS AND/OR CLISTS INVOKER.'
XTC      START 0
         SPACE 1
* PURPOSE :    ALLOWS A PROGRAM RUNNING IN A TSO ENVIRONMENT TO
* ---------    INVOKE AND THUS EXECUTE TSO COMMANDS AND/OR CLISTS
*              (IMPLICIT FORM ONLY IS ACCEPTED).
*
* CALLING SEQUENCE : CALL XTC,(A,B,C,D),VL
* ------------------          (A,B,C)
*                             (A,B)
*                             (A)
*
*        WHERE : A - IS THE ADDRESS OF THE COMMAND OR CLIST TEXT TO
*                    BE INVOKED (NO ALIGNMENT NEEDED, BUT MAXIMUM 240
*                    CHARACTERS LONG ALLOWED).
*                B - IS THE ADDRESS OF THE COMMAND OR CLIST TEXT
*                    LENGTH (NUMBER OF CHARACTERS - FULLWORD). WHEN
*                    THIS ARGUMENT IS OMITTED, OR THE ADDRESS OF THIS
*                    ARGUMENT IS SET TO ZERO IN THE PARAMETERS LIST,
*                    THE LENGTH IS ASSUMED TO BE IN THE BYTE (FORMAT
*                    XL1) JUST PRECEEDING THE COMMAND OR CLIST TEXT,
*                    AT THE ADDRESS OF A-1.
*                C - IS THE ADDRESS OF A FULLWORD INTO WHICH THE
*                    COMMAND OR CLIST EXECUTION HIGHEST COMPLETION
*                    CODE IS STORED (FORMAT XL4'00SSSUUU', WHERE SSS
*                    IS THE SYSTEM CODE AND UUU IS THE USER CODE),
*                    IF SUPPLIED.
*                    IN ANY CASE THE VALUE IS ALWAYS PLACED IN THE
*                    REGISTER 0.
*                D - IS THE ADDRESS OF A FULLWORD TO STORE THE RETURN
*                    CODE, IF SUPPLIED. VALUE IS ALWAYS PLACED IN
*                    REGISTER 15 AND CAN BE :
*                     0 - ALL PROCESSINGS DONE.
*                     1 - COMMAND FAILED.
*                     2 - INVALID COMMAND TEXT LENGTH.
*                     3 - ARGUMENT ALIGNMENT ERROR.
*                     4 - MISSING COMMAND.
*                     5 - INVALID COMMAND.
*                     6 - COMMAND REJECTED (UNSUPPORTED).
*                     7 - UNABLE TO LOCATE CPPL.
*                     8 - NOT TSO ENVIRONMENT.
*
*        AT ENTRY, REGISTER 0 IS USED AS MESSAGES SWITCH :
*                - ZERO TO SUPPRESS ALL MESSAGES.
*                - POSITIVE TO OBTAIN ERROR MESSAGES ONLY.
*                - NEGATIVE TO OBTAIN ALL MESSAGES.
         EJECT
* OPERATIONAL NEEDS : XTC IS INCLUDED AT PROGRAM LINK-EDIT TIME.
* ------------------- XTCL MUST BE LINK-EDITED IN A LIBRARY DEFINED
*              IN THE LINKLIST (LNKLST00 OF SYS1.PARMLIB) AND THIS
*              LIBRARY IS RACF PROTECTED AGAINST ANY USERS READ OR
*              WRITE (ONLY SYSTEM GROUP MAY ACCESS IT).
*              THIS ALLOWS MODIFICATIONS WITHOUT INVOLVING USERS.
*              REMARK : THE CODE IS NOT 'RENT' NOR 'REUS', IT IS
*                       BASED ON THE FACT THAT THE MODULE IS EACH
*                       TIME RE-LOADED.
*
*              WHEN USED BY A PROGRAM IN BATCH, YOU MUST START WITH
*              THE FOLLOWING JCL STATEMENTS :
*
*                       //IKJ     EXEC PGM=IKJEFT01
*                       //SYSPROC   DD ... TSO COMMANDS PROCEDURES
*                       //...       DD ... PROGRAM DD'S IF NEEDED
*                       //SYSTSPRT  DD SYSOUT=*
*                       //SYSTSIN   DD *
*                       CALL ...PROGRAM...
*                       END
*                       /*
*
* ADVICE : WHEN RUNNING IN A BATCH ENVIRONMENT, THE COMMANDS OR
* ======== PROGRAMS USING TGET/TPUT ARE NOT SUPPORTED.
*          MVS/XA : MUST BE AMODE AND RMODE 24.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        SPLEVEL SET=1
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12
         LR    R5,R1               SAVE PARAMETERS LIST POINTER
         LTR   R0,R0               WANT MESSAGES?
         BP    INIT                YES
         BM    SALL                ALL
         OI    XSWX,NOMSG          SET MSG=NO
         B     INIT
SALL     OI    XSWX,ALLMSG         SET MSG=ALL
         SPACE 1
INIT    EXTRACT ATSO,'S',FIELDS=(TSO,PSB)
         EJECT
*        ARGUMENTS CONTROL.
         SPACE 1
         XR    R1,R1
         XR    R2,R2
         XR    R3,R3
         XR    R4,R4
         LA    R10,MAXERR
         ICM   R4,B'0111',1(R5)    GET ARGUMENTS
         TM    0(R5),X'80'
         BO    ARAN
         ICM   R3,B'0111',5(R5)
         TM    4(R5),X'80'
         BO    ARAN
         ICM   R1,B'0111',9(R5)
         TM    8(R5),X'80'
         BO    ARAN
         ICM   R2,B'0111',13(R5)
ARAN     STM   R1,R2,ACCC
         LTR   R1,R1
         BZ    NOCC
         TM    ACCC+3,X'03'
         BNZ   ERR3
         XC    0(4,R1),0(R1)
NOCC     LTR   R2,R2
         BZ    NORC
         TM    ACRC+3,X'03'
         BNZ   ERR3
         XC    0(4,R2),0(R2)
NORC     LTR   R4,R4
         BZ    ERR4
         LTR   R3,R3               GET LENGTH
         BNZ   ARSL
         LR    R1,R4
         BCTR  R1,0
         IC    R3,0(R1)
         B     ARST
ARSL     TM    7(R5),X'03'
         BNZ   ERR3
         L     R3,0(R3)
ARST     L     R1,ATSO
         TM    0(R1),X'80'
         BZ    *+L'*+4
         OI    XSWX,XFORE
         OC    APSCB,APSCB
         BZ    ERR8
         LTR   R3,R3               VALIDATE ARGUMENTS
         BNP   ERR2
         CH    R3,=H'240'
         BH    ERR2
         AR    R3,R4
         LR    R1,R3
         BCTR  R3,0
         EJECT
*        COMMAND/CLIST CONTROL.
         SPACE 1
         LA    R2,1
LNME     CLI   0(R4),C' '          FIND NAME
         BNE   CNME
FNME     BXLE  R4,R2,LNME
         B     ERR4
CNME     CLI   0(R4),C'%'          CLIST EXECUTION?
         BNE   SNME                NO
         OI    XSWX,CLTSO          INDICATE CLIST
         B     FNME
SNME     SR    R1,R4
         LR    R0,R1
         AH    R0,=H'4'
         STH   R0,LCMD             SET COMMAND BUFFER LENGTH
         BCTR  R1,0
         EX    R1,SNCMD            MOVE COMMAND TEXT
         LR    R1,R4
FENME    BXLE  R4,R2,*+L'*+4
         B     ENME
         CLI   0(R4),C' '
         BNE   FENME
ENME     LR    R5,R4
         SR    R5,R1
         BNP   ERR5
         CH    R5,=H'8'
         BH    ERR5
         BCTR  R5,0
         MVC   TCMD,=CL8' '
         EX    R5,MVCMD
         TM    XSWX,XFORE
         BO    SFORE
         L     R1,=A(TCMDB)        SCAN RESERVED COMMAND (BACKGROUND)
         B     LSCT
SFORE    L     R1,=A(TCMDF)        SCAN RESERVED COMMAND (FOREGROUND)
LSCT     CLI   0(R1),X'FF'         END OF TABLE?
         BE    TXEQ                YES
         CLC   TCMD(L'TCMD),0(R1)  MATCH?
         BE    ERR6                YES, REJECT IT
         LA    R1,L'TCMDB(R1)      BUMP TO NEXT
         B     LSCT
         EJECT
*        TSO ENVIRONMENT CONTROL.
         SPACE 1
         USING PSA,R0
TXEQ     L     R2,PSATOLD          CURRENT TCB ADDRESS
         DROP  R0
         XR    R4,R4
         USING TCB,R2
TSCT     L     R3,TCBRBP           CURRENT RB ADDRESS
         LTR   R3,R3
         BZ    NXTTCB
         USING RBBASIC,R3
         ICM   R4,B'0111',RBCDE1   CURRENT CDE ADDRESS
         BZ    NXTTCB
         USING CDENTRY,R4
         CLC   CDNAME,=CL8'IKJEFT02'
         BE    TFND
NXTTCB   L     R2,TCBOTC           ORIGINATING TCB
         LTR   R2,R2
         BNZ   TSCT
         B     ERR7
         DROP  R3,R4
TFND     L     R3,TCBGRS9          GET TMP WORK-AREA ADDRESS
         DROP  R2
         USING TPL,R3
         L     R4,CPPLPTR
         DROP  R3
         USING CPPL,R4
         L     R2,APSCB
         USING PSCB,R2
         CLC   PSCBUPT,CPPLUPT     UPT ADDRESSES MATCH?
         BNE   ERR7                NO
         CL    R2,CPPLPSCB         CPPL+8 = PSCB ADDRESS?
         BNE   ERR7                NO
         MVC   XCPPL+4(3*4),CPPLUPT     COPY CPPL
         DROP  R2,R4
         EJECT
*        EXECUTE COMMAND/CLIST.
*        (BASED ON Q31 IN FILE 153 OF CBT TAPE)
         SPACE 1
         LA    R2,XCPPL            ADDRESS OF CPPL FOR COMMAND
         USING CPPL,R2
         L     R3,CPPLECT          POINT TO ECT
         USING ECT,R3
         LA    R4,GTPBL            GETLINE PARM BLOCK ADDRESS
         USING GTPB,R4
         MVC   XPCMD,ECTPCMD       SAVE PRIMARY COMMAND NAME
         MVC   XSCMD,ECTSCMD       SAVE SECONDARY COMMAND NAME
         LA    R1,XCSPL            ADDRESS OF IKJSCAN PARM LIST
         USING CSPL,R1
         MVC   CSPLUPT,CPPLUPT     COPY UPT POINTER
         MVC   CSPLECT,CPPLECT     COPY ECT POINTER
         LA    R15,XECB            ADDRESS OF ECB
         ST    R15,CSPLECB
         LA    R15,XTCB            ADDRESS OF FLAGS
         ST    R15,CSPLFLG
         LA    R15,XCSOA           ADDRESS OF OUTPUT AREA
         ST    R15,CSPLOA
         DROP  R1
         LA    R1,XCMD             ADDRESS OF COMMAND
         USING CMDBUF,R1
CSCAN    XC    CMDOFF,CMDOFF       CLEAR OFFSET TO SECOND OPERAND
         DROP  R1
         ST    R1,CPPLCBUF         SET BUFFER CB POINTER
         LA    R1,XCSPL            ADDRESS OF IKJSCAN PARM LIST
         USING CSPL,R1
         MVC   CSPLCBUF,CPPLCBUF   COPY COMMAND BUFFER POINTER
         DROP  R1
         XC    XECB,XECB           CLEAR ECB
         XC    XTCB,XTCB           CLEAR FLAGS
         XC    XCSOA(2*4),XCSOA    CLEAR OUTPUT AREA
        CALLTSSR EP=IKJSCAN        SCAN INPUT BUFFER
         XC    XECB,XECB           CLEAR ECB
         LA    R1,XCSOA            ADDRESS OF OUTPUT AREA
         USING CSOA,R1
         L     R14,CSOACNM         POINTER TO COMMAND NAME
         ICM   R15,B'0011',CSOALNM LENGTH OF NAME
         BZ    NOCMD               NONE, SKIP REST
         TM    CSOAFLG,CSOAQM+CSOANOC   Q.M. OR NO CMD?
         BNZ   NOCMD               YES, SKIP REST
         BCTR  R15,0
         MVC   XBLNM,=CL8' '       FILL WITH BLANKS
         EX    R15,MVBNM           MOVE TO BLDL NAME
         TM    CSOAFLG,CSOABAD     BAD COMMAND NAME?
         BZ    TEXEC               NO
         L     R1,=A(DIAGB)
         MVC   DIAGBD(L'DIAGBD,R1),=CL3'BAD'
         B     SDIAG
TEXEC    CLC   XBLNM(2),=CL2'EX'   EXEC COMMAND?
         BNE   TVALID              NO
         TM    CSOAFLG,CSOAEXEC    IMPLICIT EXEC CMD NAME?
         BZ    INVCMD              NO, INVALID
TVALID   TM    CSOAFLG,CSOAVWP+CSOAVNP  VALID COMMAND?
         BNZ   GOCMD               YES
         DROP  R1
INVCMD   L     R1,=A(DIAGB)
         MVC   DIAGBD(L'DIAGBD,R1),=CL3'INV'
SDIAG    LA    R8,DIAGBS(R1)
         MVI   0(R8),C' '
         MVC   1(L'DIAGBS-1,R8),0(R8)
         L     R15,CPPLCBUF        POINT TO BUFFER
         USING CMDBUF,R15
         LH    R14,CMDLEN          LENGTH
         SH    R14,=H'4'
         CH    R14,DIAGBL
         BNH   *+L'*+4
         LH    R14,DIAGBL
         BCTR  R14,0
         EX    R14,MVMSG
         DROP  R15
         CLC   L'DIAGBS-5(5,R8),=CL8' '
         BE    *+L'*+6
         MVC   L'DIAGBS-5(5,R8),=CL5' ... '
         BAS   R8,SMSG
         B     NOCMD               SKIP REST
GOCMD    TM    XSWX,ALLMSG         ALL MESSAGES REQUEST?
         BZ    SRCS                NO
         L     R1,=A(DIAGA)
         LA    R8,DIAGAS(R1)
         MVI   0(R8),C' '
         MVC   1(L'DIAGAS-1,R8),0(R8)
         L     R15,CPPLCBUF        POINT TO BUFFER
         USING CMDBUF,R15
         LH    R14,CMDLEN          LENGTH
         SH    R14,=H'4'
         CH    R14,DIAGAL
         BNH   *+L'*+4
         LH    R14,DIAGAL
         BCT   R14,*+L'*+L'MVMSG
MVMSG    MVC   0(*-*,R8),CMDTEXT   <<EXECUTED>>
         EX    R14,MVMSG
         DROP  R15
         CLC   L'DIAGAS-5(5,R8),=CL8' '
         BE    *+L'*+6
         MVC   L'DIAGAS-5(5,R8),=CL5' ... '
         BAS   R8,SMSG
SRCS     L     R1,CPPLCBUF         POINT TO BUFFER
         USING CMDBUF,R1
         LA    R15,CMDTEXT         POINT TO COMMAND TEXT
         DROP  R1
SRCL     CLI   0(R15),C' '         SEARCH COMMAND
         BNE   SRCN
         LA    R15,1(R15)
         B     SRCL
SRCN     CLC   0(4,R15),=CL4'END ' END COMMAND?
         BE    NOCMD               YES, SKIP IT
         CLC   0(4,R15),=CL4'TSO ' TSO COMMAND?
         BNE   ANAL                NO
         LA    R15,4(R15)
         B     SRCN
ANAL     TM    XSWX,CLTSO          IS IT A CLIST?
         BO    SCLST               YES
         CLC   XBLNM(2),=CL2'EX'   IMPLICIT EXEC OF CLIST?
         BNE   TNEX                NO
SCLST    NI    XSWX,255-CLTSO
TSEX     OI    XSWX,CLSTX          CLIST IN EXECUTION
         MVC   XBLNM,=CL8'EXEC'    SET MODULE NAME TO ATTACH
         L     R1,CPPLCBUF         POINT TO BUFFER
         USING CMDBUF,R1
         XC    CMDOFF,CMDOFF       CLEAR OFFSET FOR EXEC
         DROP  R1
         B     TATT
TNEX     XC    XBLTTR(L'XBLTTR+L'XBLLK),XBLTTR    CLEAR
        BLDL   0,XBLDL             CHECK FOR MODULE PRESENT
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   TSEX                NONE, MUST BE CLIST
TATT     XC    XECB,XECB           CLEAR ECB
         MVC   ECTPCMD,XBLNM       FAKE PRIMARY COMMAND NAME
         MVC   ECTSCMD,=CL8' '     AND SECONDARY COMMAND NAME
         LR    R1,R2               SET CPPL ADDRESS
        ATTACH EPLOC=XBLNM,ECB=XECB,SF=(E,ATTCH),MF=(E,(1))
         ST    R1,XTCB             SAVE TCB ADDRESS
        WAIT   ECB=XECB            WAIT FOR SUBTASK COMPLETION
         LA    R1,XTCB             POINT TO TCB POINTER
        DETACH (1)
         OC    XECB+1(3),XECB+1    ANY COMPLETION CODE?
         BZ    NOCMD               NO
         CLC   XECB+1(3),XCC+1     COMPLETION CODE TO BE RETAINED?
         BNH   NOSTCC              NO
         MVC   XCC+1(3),XECB+1     RETAIN HIGHEST ENCOUNTERED
NOSTCC   TM    XSWX,NOMSG          DISPLAY RC REQUESTED?
         BO    NOCMD               NO
         L     R1,=A(DIAGC)
         MVC   27(8,R1),XBLNM      GET COMMAND NAME
         MVC   WORK+5(3),XECB+1
         UNPK  WORK(5),WORK+5(3)   GET SYSTEM CODE
         TR    WORK(3),TRTAB-C'0'
         MVC   38(3,R1),WORK
         NI    WORK+6,X'0F'        GET USER CODE
         LH    R0,WORK+6
         CVD   R0,WORK
         UNPK  43(4,R1),WORK+5(3)
         OI    46(R1),C'0'
         BAS   R8,SMSG
NOCMD    TM    XSWX,CLSTX          CLIST IN EXECUTION?
         BZ    DONE                NO, DONE
         ICM   R1,B'1111',GTPBIBUF GETLINE BUFFER
         BZ    GSTCK               NO, SKIP
         USING CMDBUF,R1
         LH    R0,CMDLEN           LENGTH OF BUFFER
         DROP  R1
         ICM   R0,B'1000',=AL1(1)  SUBPOOL 1
        FREEMAIN R,LV=(0),A=(1)    FREE THE BUFFER
GSTCK    XC    XECB,XECB           CLEAR ECB
         LA    R1,XIOPL            GETLINE I/O PARM LIST
         L     R5,CPPLUPT          ADDRESS OF UPT
        GETLINE PARM=(R4),UPT=(R5),ECT=(R3),ECB=XECB,MF=(E,(1))
         CH    R15,=H'16'          END OF INPUT (STACK-CLIST)?
         BE    DONE                YES, DONE
         L     R1,GTPBIBUF         POINT TO INPUT BUFFER
         B     CSCAN
         EJECT
*        END OF PROCESSING.
         SPACE 1
DONE     MVC   ECTPCMD,XPCMD       RESTORE PRIMARY COMMAND NAME
         MVC   ECTSCMD,XSCMD       AND SECONDARY COMMAND NAME
         DROP  R2,R3,R4
         L     R1,ACCC
         LTR   R1,R1
         BZ    *+L'*+6
         MVC   0(4,R1),XCC
         OC    XCC+1(3),XCC+1      ANY FINAL COMPLETION CODE?
         BNZ   CMXC                YES
         XR    R10,R10             ALL OK
         B     LEAVE
CMXC     L     R1,=A(DIAG1)
         MVC   WORK+5(3),XCC+1
         UNPK  WORK(5),WORK+5(3)   GET SYSTEM CODE
         TR    WORK(3),TRTAB-C'0'
         MVC   38(3,R1),WORK
         NI    WORK+6,X'0F'        GET USER CODE
         LH    R0,WORK+6
         CVD   R0,WORK
         UNPK  43(4,R1),WORK+5(3)
         OI    46(R1),C'0'
ERR1     BCTR  R10,0          COMMAND FAILED
ERR2     BCTR  R10,0          INVALID COMMAND TEXT LENGTH
ERR3     BCTR  R10,0          ARGUMENT ALIGNMENT ERROR
ERR4     BCTR  R10,0          MISSING COMMAND
ERR5     BCTR  R10,0          INVALID COMMAND
ERR6     BCTR  R10,0          COMMAND REJECTED (UNSUPPORTED)
ERR7     BCTR  R10,0          UNABLE TO LOCATE CPPL
ERR8     BCTR  R10,0          NOT TSO ENVIRONMENT
MAXERR   EQU   ((*-ERR1)/2)+1
         TM    XSWX,NOMSG
         BO    LEAVE
         LR    R1,R10
         BCTR  R1,0
         SLL   R1,2
         AL    R1,=A(TABD)
         L     R1,0(R1)
         BAS   R8,SMSG
LEAVE    L     R1,4(R13)
         MVC   20(4,R1),XCC        SET R0
         L     R1,ACRC
         LTR   R1,R1
         BZ    *+L'*+4
         ST    R10,0(R1)
        $XRET  CC=(R10)
         EJECT
*        SEND MESSAGES ROUTINE.
*              R1 : WTO MESSAGE ADDRESS.
*              R8 : LINK REGISTER.
         SPACE 1
SMSG     TM    XSWX,XFORE
         BO    STSO
        WTO    MF=(E,(1))
         BR    R8
STSO     LH    R0,0(R1)
         SH    R0,=H'4'
         LA    R1,4(R1)
        TPUT   (1),(0)
         BR    R8
         SPACE 2
SNCMD    MVC   CMND(*-*),0(R4)     << EXECUTED >>
MVCMD    MVC   TCMD(*-*),0(R1)     << EXECUTED >>
MVBNM    MVC   XBLNM(*-*),0(R14)   << EXECUTED >>
         EJECT
*        CONSTANTS AND WORK AREAS.
         SPACE 1
WORK     DC    D'0'
ATSO     DC    A(*-*) -------/
APSCB    DC    A(*-*)        /---> MAINTAIN FIELDS ORDER
ACCC     DC    A(*-*) -------/
ACRC     DC    A(*-*)        /---> MAINTAIN FIELDS ORDER
XCC      DC    F'0'                FINAL COMPLETION CODES
         SPACE 1
GTPBL   GETLINE MF=L
ATTCH   ATTACH SHSPV=78,SF=L
         SPACE 1
XTCB     DC    F'0'                TCB
XECB     DC    F'0'                ECB
XCPPL    DC    4F'0'               CPPL
XIOPL    DC    4F'0'               GETLINE I/O PARM LIST
XCSPL    DC    6F'0'               IKJSCAN PARM LIST
XCSOA    DC    2F'0'               OUTPUT AREA FROM IKJSCAN
         SPACE 1
XBLDL    DS    0XL16 --------/     BLDL LIST
XBLFF    DC    H'1'          /     NUMBER OF ENTRIES IN LIST
XBLLL    DC    H'12'         /     LENGTH OF EACH ENTRY
XBLNM    DC    CL8' '        /     MEMBER NAME
XBLTTR   DC    XL3'0'        /     TTR OF MEMBER START
XBLLK    DC    XL1'0'        /     CONCATENATION NUMBER
*                            /---> MAINTAIN FIELDS ORDER
         SPACE 1
TCMD     DC    CL8' '              TEST COMMAND/CLIST NAME
XPCMD    DC    CL8' '              PRIMARY COMMAND NAME
XSCMD    DC    CL8' '              SECONDARY COMMAND NAME
XCMD     DS    0F -----------/     COMMAND BUFFER
LCMD     DC    H'0'          /
OFFSET   DC    H'0'          /
CMND     DC    CL240' '      /---> MAINTAIN FIELDS ORDER
         SPACE 1
TRTAB    DC    CL16'0123456789ABCDEF'
         SPACE 1
DIAGAL   DC    0H'0',AL2(L'DIAGAS)
DIAGBL   DC    0H'0',AL2(L'DIAGBS)
XSWX     DC    X'0'                PROCESSING SWITCHES
XFORE    EQU   X'80'               FOREGROUND REGION
NOMSG    EQU   X'40'               NO MESSAGES REQUEST
ALLMSG   EQU   X'20'               ALL MESSAGES REQUEST
CLTSO    EQU   X'02'               CLIST EXECUTION REQUEST
CLSTX    EQU   X'01'               CLIST IN EXECUTION
         SPACE 2
        LTORG
         SPACE 2
*- - - - END OF BASE REGISTER ADDRESSING - - - - - - - - - - - - - - -*
         EJECT
*        ERROR MESSAGES.
         SPACE 1
TABD     DC    A(DIAG1,DIAG2,DIAG3,DIAG4,DIAG5,DIAG6,DIAG7,DIAG8)
         SPACE 1
        PRINT  NOGEN
DIAGA   WTO    '   ---> COMMAND :                                      X
                                      ',ROUTCDE=11,DESC=7,MF=L
DIAGAS  EQU    22,60               START OF COMMAND TEXT
DIAGB   WTO    'XTC -     COMMAND :                                    X
                                      ',ROUTCDE=11,DESC=7,MF=L
DIAGBD  EQU    10,3                ERROR TYPE (BAD/INV)
DIAGBS  EQU    24,58               START OF COMMAND TEXT
DIAGC   WTO    'XTC - COMMAND FAILED :          (S   /U    ).',        X
               ROUTCDE=11,DESC=7,MF=L
         SPACE 1
DIAG1   WTO    'XTC - ERR=1 : COMMAND(S) FAILED (S   /U    ).',        X
               ROUTCDE=11,DESC=7,MF=L
DIAG2   WTO    'XTC - ERR=2 : INVALID COMMAND TEXT LENGTH.',           X
               ROUTCDE=11,DESC=7,MF=L
DIAG3   WTO    'XTC - ERR=3 : ARGUMENT ALIGNMENT ERROR.',              X
               ROUTCDE=11,DESC=7,MF=L
DIAG4   WTO    'XTC - ERR=4 : MISSING COMMAND.',                       X
               ROUTCDE=11,DESC=7,MF=L
DIAG5   WTO    'XTC - ERR=5 : INVALID COMMAND.',                       X
               ROUTCDE=11,DESC=7,MF=L
DIAG6   WTO    'XTC - ERR=6 : COMMAND REJECTED (UNSUPPORTED).',        X
               ROUTCDE=11,DESC=7,MF=L
DIAG7   WTO    'XTC - ERR=7 : UNABLE TO LOCATE CPPL.',                 X
               ROUTCDE=11,DESC=7,MF=L
DIAG8   WTO    'XTC - ERR=8 : NOT TSO ENVIRONMENT.',                   X
               ROUTCDE=11,DESC=7,MF=L
        PRINT  GEN
         EJECT
*        THE FOLLOWING TABLES MUST BE IN ALPHABETIC ORDER.
         SPACE 1
TCMDB    DS    0CL8           RESERVED COMMANDS TABLE (BACKGROUND)
         DC    CL8'ACCOUNT'
         DC    CL8'AUTHC'
         DC    CL8'CDSCB'
         DC    CL8'CJSD'
         DC    CL8'DIDOCS'
         DC    CL8'ISPF'
         DC    CL8'LISTENQ'
         DC    CL8'OPER'
         DC    CL8'PDF'            ALIAS OF ISPF
         DC    CL8'PDFINIT'
         DC    CL8'POF'
         DC    CL8'PSWR'
         DC    CL8'PUSE'
         DC    CL8'QSP'
         DC    CL8'RGROUP'
         DC    CL8'RSVENQ'
         DC    CL8'SCHED'
         DC    CL8'SCHEDULE'
         DC    CL8'TEST'
         DC    CL8'TIME'
         DC    CL8'TSODSN'
         DC    CL8'TSOENQ'
         DC    CL8'UJVD'
         DC    CL8'UNIT'
         DC    CL8'UNITS'
         DC    CL8'VCOREZAP'
         DC    CL8'VSM'
         DC    CL8'WAITR'
         DC    CL8'ZAP'
         DC    XL1'FF'             END OF TABLE INDICATOR
         SPACE 1
TCMDF    DS    0CL8           RESERVED COMMANDS TABLE (FOREGROUND)
         DC    CL8'ACCOUNT'
         DC    CL8'AUTHC'
         DC    CL8'CDSCB'
         DC    CL8'OPER'
         DC    CL8'POF'
         DC    CL8'PSWR'
         DC    CL8'PUSE'
         DC    CL8'QSP'
         DC    CL8'TEST'
         DC    CL8'TIME'
         DC    CL8'UNIT'
         DC    CL8'UNITS'
         DC    CL8'VCOREZAP'
         DC    CL8'VSM'
         DC    CL8'ZAP'
         DC    XL1'FF'             END OF TABLE INDICATOR
         EJECT
CMDBUF   DSECT
CMDLEN   DS    H                   LENGTH, INCLUDES HEADER (+4)
CMDOFF   DS    H                   OFFSET TO NON-BLANK PAST COMMAND
CMDTEXT  DS    C                   FIRST TEXT BYTE
         SPACE 2
        PRINT  NOGEN
        CVT    DSECT=YES,LIST=YES
        IHAPSA
        IHARB
        IHACDE
        IKJCPPL
        IKJECT
        IKJPSCB
        IKJGTPB
        IKJCSPL
        IKJCSOA
        IKJTCB
        IKJTMPWA
         SPACE 2
         END
