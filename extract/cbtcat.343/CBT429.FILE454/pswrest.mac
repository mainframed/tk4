PRST     TITLE 'PASSWORD RESTORE ROUTINE.'
PSWREST  START 0
         SPACE 1
* CALLING SEQUENCE : CALL PSWREST,(A,B,C),VL
* ------------------
*
*        WHERE : A - IS THE ADDRESS OF A FIELD (FORMAT CL...) WHICH
*                    CONTAINS THE MASKED PASSWORD.
*
*                B - IS THE ADDRESS OF THE RECEIVING FIELD (FORMAT
*                    CL...) INTO WHICH IS MOVED THE PASSWORD.
*
*                C - IS THE ADDRESS OF A WORD (FORMAT F, FULLWORD
*                    BOUNDARY) CONTAINING THE LENGTH OF THE MASKED
*                    PASSWORD AND THUS OF THE PASSWORD.
*
*        RETURN CODES : VALUE IS SET IN REGISTER 15.
*                       0 - NORMAL.
*                       4 - UNKNOWN CHARACTER(S) DETECTED.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
         AIF   ('&SYSPARM' NE 'TSO').O1S0
         EJECT
WRKDSECT DSECT                     WORK AREAS DESCRIPTION.
         SPACE 1
SVA      DS    18F                 SAVE AREA.
         SPACE 1
WORK     DC    D'0'
WORKR    DC    D'0'
WORKA    DC    D'0'
WORKM    DC    D'0'
RCODE    DC    H'0'
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 1
PSWREST  CSECT                     RESTORE MAIN CSECT.
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         USING WRKDSECT,R13        SET WORK AREAS ADDRESSABILITY.
         AGO   .O1S1
.O1S0    EJECT
        $XENT  BASE=R12
.O1S1    LA    R6,1                INITIAL POINTER.
         L     R7,8(R1)
         L     R5,0(R7)            GET LENGTH.
         XC    RCODE,RCODE
         LTR   R5,R5
         BNP   EXIT
LOOP     XC    WORK,WORK
         XC    WORKR,WORKR
         LA    R4,8
         CR    R4,R5
         BNH   *+L'*+2             BRANCH IF L .GE. 8, LET 8 IN R4.
         LR    R4,R5               L .LT. 8, SET L IN R4.
         LR    R7,R4
         BCTR  R7,0                COMPUTE MACHINE LENGTH.
         LR    R9,R7
         L     R8,0(R1)            GET PASSWORD ADDRESS.
         ALR   R8,R6               + POINTER.
         BCTR  R8,0                -1 TO ADJUST.
         EX    R7,MOVEIN           GET IN WORK.
         MVC   WORKR(1),WORK
         TR    WORKR(1),RESTT
         CLI   WORKR,0
         BE    UNKNOWF
         LTR   R9,R9
         BZ    SBCK
         LA    R10,1
         LA    R14,WORK+1
         LA    R15,WORKR+1
CNXT     IC    R0,0(R14)
         LA    R8,X'0F'
         NR    R0,R8
         LA    R8,RESTT
         AR    R8,R0
         LA    R11,NEL
TNXT     CLI   0(R8),0
         BE    NOCHAR
         MVC   0(1,R15),0(R8)
         MVC   WORKA,WORKR
         MVC   WORKM,WORKA
         LM    R2,R3,WORKA
         SRDL  R2,1
         STM   R2,R3,WORKA
         XC    WORKA,WORKM
         LM    R2,R3,WORKA
         SRDL  R2,3
         STM   R2,R3,WORKA
         XC    WORKA,WORKM
         EX    R10,TEST
         BE    THIS
NOCHAR   LA    R8,L'RESTT(R8)
         BCT   R11,TNXT
UNKNOW   MVI   0(R15),C' '
         LA    R15,1(R15)
         BCT   R9,UNKNOW
         B     SETERR
UNKNOWF  MVI   WORKR,C' '
         MVC   WORKR+1(L'WORKR-1),WORKR
SETERR   MVI   RCODE+1,4
         B     SBCK
TEST     CLC   WORK(*-*),WORKA
THIS     LA    R10,1(R10)
         LA    R14,1(R14)
         LA    R15,1(R15)
         BCT   R9,CNXT
SBCK     L     R8,4(R1)            GET MASKED PASSWORD ADDRESS.
         ALR   R8,R6               + POINTER.
         BCTR  R8,0                -1 TO ADJUST.
         EX    R7,MOVEOU           SET BACK.
         SLR   R5,R4               REMAINING BYTES.
         ALR   R6,R4               NEXT POINTER.
         LTR   R5,R5               ANY BYTES LEFT?
         BP    LOOP                YES, GO TO PROCESS NEXT.
EXIT     LH    R9,RCODE            GET RETURN CODE.
         AIF   ('&SYSPARM' EQ 'TSO').O2S0
        $XRET  CC=(R9)
         AGO   .O2S1
.O2S0   $XRET  CC=(R9),LV=WRKL,TYPE=RENT
.O2S1    SPACE 1
MOVEIN   MVC   WORK(*-*),0(R8)
MOVEOU   MVC   0(*-*,R8),WORKR
         EJECT
         AIF   ('&SYSPARM' EQ 'TSO').O3S0
         SPACE 1
*        CONSTANTS AND WORK AREAS.
         SPACE 1
WORK     DC    D'0'
WORKR    DC    D'0'
WORKA    DC    D'0'
WORKM    DC    D'0'
RCODE    DC    H'0'
.O3S0    SPACE 1
*        RESTORE TABLE.
         SPACE 1
RESTT    DS    0XL16
*                    0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    XL16'00000000000000000000000000000000' 0
         DC    XL16'00000000000000000000000000000000' 1
         DC    XL16'00000000000000000000000000000000' 2
         DC    XL16'00000000000000000000000000000000' 3
         DC    XL16'4D004F4E00004B000000000040000000' 4
         DC    XL16'5E005C5D005B00000000000000000050' 5
         DC    XL16'6B0000006F00006C0000606100000000' 6
         DC    XL16'00007A7B7C7D7E7F0000000000000000' 7
         DC    XL16'00000000000000000000000000000000' 8
         DC    XL16'00000000000000000000000000000000' 9
         DC    XL16'00000000000000000000000000000000' A
         DC    XL16'00000000000000000000000000000000' B
         DC    XL16'D7D6D5D4D3D2D100000000000000D8D9' C
         DC    XL16'C4C5C6C700C1C2C300000000C9C80000' D
         DC    XL16'F1F0F3F2F5F4F7F6F8F9000000000000' E
         DC    XL16'E2E30000E6E7E4E50000E9E800000000' F
NEL      EQU   (*-RESTT)/L'RESTT
         SPACE 2
         END
