         MACRO           CALL RDCHK SUPPLY.
&NAME   $RDCHK      &A,&B,&C,&MF=
         LCLA  &Y,&Z
         LCLB  &W
         LCLC  &X
.*
.* $RDCHK MACRO : CALL RDCHK SUPPLY (SEE RDCHK).
.* --------------
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   (N'&SYSLIST GT 3).BAD
&W       SETB  0
         AIF   ('&MF' EQ '').XI1
         AIF   ('&MF' EQ 'E').XI1
         AIF   ('&MF' NE 'L').XL1
         AIF   (N'&SYSLIST EQ 0).LS11
         AIF   (N'&SYSLIST LT 2).MIS
&Z       SETA  1
         AIF   ('&NAME' EQ '').LS1
&NAME    DS    0F
.LS1     AIF   (N'&SYSLIST EQ &Z).LS10
         AIF   (T'&SYSLIST(&Z) EQ 'O').LS2
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').REG
         DC    A(&SYSLIST(&Z))     P.P. PARAMETER.
         AGO   .LS3
.LS2     DC    A(0)                P.P. PARAMETER.
.LS3     ANOP
&Z       SETA  &Z+1
         AGO   .LS1
.LS10    AIF   (T'&SYSLIST(&Z) EQ 'O').LS12
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').REG
         DC    A(&SYSLIST(&Z)+X'80000000')   LAST P.P. PARAMETER.
         MEXIT
.LS11    ANOP
&NAME    DC    A(0,0)              P.P. PARAMETERS.
.LS12    DC    A(X'80000000')      LAST P.P. PARAMETER.
         MEXIT
.XL1     AIF   ('&MF(1)' NE 'L').XL2
         AIF   ('&MF(2)' EQ '').ERR
&W       SETB  1
         AGO   .XL3
.XL2     AIF   ('&MF(1)' NE 'E').ERR
         AIF   ('&MF(2)' EQ '').XI1
.XL3     AIF   ('&MF(2)'(1,1) EQ '(').XL4
&NAME    LA    1,&MF(2)
         AGO   .XL10
.XL4     AIF   ('&MF(2)' NE '(1)').XL5
         AIF   ('&NAME' EQ '').XL10
&NAME    DS    0H
         AGO   .XL10
.XL5     ANOP
&Y       SETA  K'&MF(2)-2
&X       SETC  '&MF(2)'(2,&Y)
&NAME    LR    1,&X
.XL10    AIF   (N'&SYSLIST EQ 0).XL21
         AIF   (N'&SYSLIST LT 2).MIS
&Y       SETA  0
&Z       SETA  1
.XL11    AIF   (T'&SYSLIST(&Z) EQ 'O').XL14
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').XL12
         LA    15,&SYSLIST(&Z)
         ST    15,&Y.(1)           STORE INTO PARM. LIST.
         AGO   .XL13
.XL12    ST    &SYSLIST(&Z,1),&Y.(1)    STORE INTO PARM. LIST.
.XL13    AIF   (N'&SYSLIST EQ &Z).XL20
         AGO   .XL15
.XL14    AIF   (N'&SYSLIST EQ &Z).XL21
.XL15    ANOP
&Y       SETA  &Y+4
&Z       SETA  &Z+1
         AGO   .XL11
.XL20    OI    &Y.(1),X'80'        SET LAST WORD BIT ON.
.XL21    AIF   (&W).XL22
         L     15,=V(RDCHK)        LOAD REG. 15 WITH ENTRY.
         BASR  14,15               BRANCH TO ENTRY POINT.
.XL22    MEXIT
.XI1     AIF   (N'&SYSLIST LT 2).MIS
         AIF   (T'&A EQ 'O').MIS
         AIF   (T'&B EQ 'O').MIS
&Y       SETA  0
&Z       SETA  1
&X       SETC  'IHB'.'&SYSNDX'
         AIF   ('&NAME' EQ '').XI2
&NAME    DS    0H
.XI2     AIF   (T'&SYSLIST(&Z) EQ 'O').XI3
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').XI4
.XI3     AIF   (N'&SYSLIST EQ &Z).XI7
         AGO   .XI5
.XI4     ST    &SYSLIST(&Z,1),&X+&Y     STORE INTO PARM. LIST.
         AIF   (N'&SYSLIST EQ &Z).XI6
.XI5     ANOP
&Y       SETA  &Y+4
&Z       SETA  &Z+1
         AGO   .XI2
.XI6     OI    &X+&Y,X'80'         SET LAST WORD BIT ON.
.XI7     LA    1,&X                LOAD LIST ADDRESS IN REG. 1.
         B     &X.A                BYPASS LIST.
         CNOP  0,4
&X       DS    0F
&Z       SETA  1
.XI10    AIF   (N'&SYSLIST EQ &Z).XI13
         AIF   (T'&SYSLIST(&Z) EQ 'O').XI11
         AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').XI11
         DC    A(&SYSLIST(&Z))     P.P. PARAMETER.
         AGO   .XI12
.XI11    DC    A(0)                P.P. PARAMETER.
.XI12    ANOP
&Z       SETA  &Z+1
         AGO   .XI10
.XI13    AIF   ('&SYSLIST(&Z)'(1,1) EQ '(').XI15
         AIF   (T'&SYSLIST(&Z) EQ 'O').XI14
         DC    A(&SYSLIST(&Z)+X'80000000')   LAST P.P. PARAMETER.
         AGO   .XI16
.XI14    DC    A(X'80000000')      LAST P.P. PARAMETER.
         AGO   .XI16
.XI15    DC    A(0)                LAST P.P. PARAMETER.
.XI16    ANOP
&X.A     L     15,=V(RDCHK)        LOAD REG. 15 WITH ENTRY.
         BASR  14,15               BRANCH TO ENTRY POINT.
         MEXIT
.MIS     MNOTE 8,' MISSING OPERAND ********************************** '
         MEXIT
.BAD     MNOTE 8,' TOO MANY OPERANDS ******************************** '
         MEXIT
.REG     MNOTE 8,' REGISTER NOTATION INVALID ************************ '
         MEXIT
.ERR     MNOTE 8,' MF= INVALID CODING ******************************* '
         MEND
