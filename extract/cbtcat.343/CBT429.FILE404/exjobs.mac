         PROC 1 JNAME
        /*************************************************************/
        /* THIS CLIST WILL DISPLAY ALL JOBS THAT START WITH A GIVEN  */
        /* CHARACTER STRING. THE STRING IS PASSED TO THE CLIST AS A  */
        /* PARAMETER. THE CLIST WILL ISSUE A OSCMD $DN, AND SCAN THE */
        /* OUTPUT FOR JOBS THAT START WITH THE GIVEN STRING.         */
        /*************************************************************/
        SET CMDRESP = CLIST
        SET MAXCMDOUT = 200
        /*************************************************************/
        /* WE WISH TO TRAP AS MANY LINES OF OUTPUT AS JES WOULD CARE */
        /* TO PROVIDE, THEREFORE, SET MAXCMDOUT TO THE MAX - 200     */
        /*************************************************************/

        SET CMDWAIT = 1
        /*************************************************************/
        /* THE JES COMMAND SHOULD ONLY TAKE 1 SECOND OR LESS, SO     */
        /* SET THE CMDWAIT VARIABLE TO 1.                            */
        /*************************************************************/

        OSCMD $DN
        /*************************************************************/
        /* ISSUE THE $DN JES COMMAND. THIS WILL POPULATE CLIST VARS  */
        /* WITH THE OUTPUT FROM THIS COMMAND.                        */
        /*************************************************************/

        /*************************************************************/
        /* SINCE THE OSCMD COMMAND ALWAYS RETURNS AN ECHO OF THE     */
        /* COMMAND AS THE FIRST LINE, START WITH THE SECOND LINE.    */
        /*************************************************************/
        SET I = 2
        SET COUNT = 0

        /*************************************************************/
        /* WE NEED TO LOOP BETWEEN THE SECOND LINE, AND THE "LAST"   */
        /* LINE, CONVIENTLY STORED IN &CMDOUT VARIABLE.              */
        /*************************************************************/
        DO WHILE (&I <= &CMDOUT - 1)
        /*************************************************************/
        /* NOTE THAT THE $DN RESPONSE INCLUDES THE SPOOL UTILIZATION */
        /* AS THE LAST LINE. WE WILL IGNORE IT.                      */
        /*************************************************************/

        SET LJNAME = &LENGTH(&JNAME)

        /*************************************************************/
        /* THE JOBNAME IS RETURNED IN THE SECOND WORD OF EACH LINE.  */
        /*************************************************************/
        SET JESJOB = &&CMDOUT&I.W2


        IF &SUBSTR(1:&LJNAME,&JESJOB) = &JNAME THEN +
        DO
        /*************************************************************/
        /* WE HAVE FOUND A JOBNAME THAT STARTS WITH THE JOB. WE WILL */
        /* DISPLAY THE ENTIRE LINE.                                  */
        /*************************************************************/
           SET OUT = &STR(&&CMDOUT&I)
           WRITE &STR(&OUT)
           SET COUNT = &COUNT + 1
        END

        SET I = &I + 1
        END

        WRITE &COUNT JOBS FOUND.
        EXIT CODE(0)
