**********************************************************************
* THIS MACRO WILL RETURN THE TSSO COMMUNICIATIONS VECTOR TABLE
* METHOD OF OPERATION:
*  1) SCAN THE SUBSYSTEM VECTOR TABLE FOR TSSO
*  2) IF NOT FOUND, RC = 12 AND EXIT
*  3) IF FOUND, RETURN IN SPECIFIED REGISTER AND EXIT WITH RC = 0
*
*  NOTE THAT THE ADDRESS OF THE TSSO CVT IS STORE IN THE SSCTSUSE
*  FIELD FOR THE TSSO SSUBSYSTEM
*
*
* CHANGE LOG:
*
* 1/3/86 - DELETE USING STATEMENT - TSSO MODULES MUST ISSUE OWN USING
* 2/21/86 - AND SSCT PARAMETER TO GET ADDRESSABILITY TO SSCT IF
*           DESIRED, AND NDSA PARAMETER FOR SAME REASON
**********************************************************************
         MACRO
&LABEL   GETCVT &R,&TSSOCVT=YES,&POINTER=NO,&SSCT=NO,&NDSA=NO
         L     &R,16(0,0)                                       RPS
         L     &R,CVTJESCT-CVT&R                                RPS
         L     &R,JESSSCT-JESCT&R   POINT TO HEAD OF SSCT       RPS
         USING SSCT,&R
SRCH&SYSNDX DS 0H
         L     &R,SSCTSCTA                 POINT TO NEXT ONE
         LTR   &R,&R
         BZ    NOTF&SYSNDX
         CLC   SSCTSNAM-SSCT(4,&R),=CL4'TSSO'   IS THIS ONE TSSO RPS
         BE    FOUND&SYSNDX
         B     SRCH&SYSNDX
NOTF&SYSNDX DS  0H
         XR    R15,R15
         LA    R15,12             SET BAD RETURN CODE
         B     DONE&SYSNDX
FOUND&SYSNDX DS  0H
         AIF   ('&SSCT' EQ 'YES').GOTOSSCT
         AIF   ('&POINTER' EQ 'YES').CVTPTR
         AIF   ('&NDSA' EQ 'YES').GETNDSA
         AIF   ('&TSSOCVT' EQ 'YES').GETCVT
         AGO   .BADPARM
.*
.GETCVT  ANOP
         L     &R,SSCTSUSE        LOAD UP TSSO CVT FIELD
         AGO   .SETRC
.*
.GETNDSA ANOP
         L     &R,SSCTSUS2        LOAD UP TSSO NDSA ...
         AGO   .SETRC
.*
.CVTPTR  ANOP
         LA    &R,SSCTSUSE        POINT TO TSSO CVT FIELD
         AGO   .SETRC
.GOTOSSCT ANOP
.* WE HAVE RETRIVED THE SSCT - LET US EXIT.
         AGO   .SETRC
.SETRC   ANOP
         XR    R15,R15            SET RETURN CODE
         B     DONE&SYSNDX
DONE&SYSNDX DS  0H
         AGO   .MACEND
.BADPARM ANOP
         MNOTE 12,'BAD PARAMETER ON GETCVT MACRO'
.MACEND  ANOP
         MEND
