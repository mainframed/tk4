TSSOPREC CSECT
         EQUATES
**********************************************************************
* THIS MODULE IS THE ESTAE ROUTINE FOR TSSOPCMD. ITS PURPOSE IS TO
* FREE THE RESOURCES OBTAINED BY TSSOPCMD ON BEHALF OF A GIVEN ADDRESS
* SPACE. THE RESOURCES FREED ARE
* 1) THE TSSO SUBSYSTEM CONSOLE.
* 2) THE 40K OF CSA STORAGE IT OBTAINED.
**********************************************************************
*
         LR    R12,R15
         USING TSSOPREC,R12
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR    R7,R14           SAVE RETURN POINT
         CH    R0,=H'12'
         BE    NOSDWA
         LR    R9,R1            SAVE ADDRESS OF THE SDWA.
         USING SDWA,R9
         L     R3,SDWAPARM      OBTAIN PARAMETER. THIS IS THE
*                               SSCON ENTRY FOR THE FAILED TASK
         USING SSCONCB,R3
         WTO   'TSSE999S TSSOPCMD ESTAE IS IN CONTROL',ROUTCDE=(11)
         L     R4,SSCONICB      GET THE OSCMD ICB FOR THE ENTRY
         L     R2,=F'40000'
         FREEMAIN R,LV=(R2),A=(R4),SP=241   *FREE CSA *
*
***********************************************************************
* NOW - TO DEALLOCATE THE SUBSYSTEM CONSOLE.                          *
*                                                                     *
***********************************************************************
         NI    SSCONFLG,255-X'80'    TURN OFF "IN USE" FLAG
         XC    SSCONICB(4),SSCONICB  ZERO OUT THE ICB POINTER
         MVC   SSCONUID(8),=CL8' '   BLANK OUT THE USER ID
         DROP  R3
         SETRP WKAREA=(R9),DUMP=YES,RC=0
         BR    R7
*
NOSDWA   DS    0H
         XR    R15,R15
         BR    R7
*
         IHASDWA
         TSSOCVT
         IEFJSCVT
         IEFJESCT
         CVT  DSECT=YES
         SSCONCB
         SSCONCBH
         OSCMDICB
         END  TSSOPREC
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOPREC(''TSSO V 4.3.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    TSSOPREC'
 PUNCH ' NAME     TSSOPREC(R)'
         END
