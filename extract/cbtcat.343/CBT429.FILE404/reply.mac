REPLY    MENTER 12,EQU,CP=CP,COM=REPLYLOADER
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
********************************************************************
* THIS COMMAND WILL SERVE AS THE LOADING PROGRAM FOR THE REPLY     *
* COMMAND FUNCTION OF TSSO. THIS MODULE WILL LOCATE, AND LOAD THE  *
* REPLY COMMAND MODULE INTO GLOBAL STORAGE, AND THEN ISSUE A LINK  *
* TO IT, AFTER PARSING THE COMMAND USING THE TSO SERVICE ROUTINES  *
*
* METHOD OF OPERATION
*
* 1) OBTAIN COMMAND BUFFER
* 2) USING IKJPARSE TO DETERMINE SYNTAX OF COMMAND: EXPECTED SYNTAX
*    IS:
*
*    REPLY MESSAGE(XXXXXXXX) TEXT(' ')
*    IF THE MESSAGE ID IS THE IMS MESSAGE ID FOR OUTSTANDING WTORS
*    THEN THE COMMAND WILL TAKE ON THE FOLLOWING SYNTAX:
*
*
*    REPLY IMS(IMS COPY) TEXT('IMS COMMAND')
*
*
*
* 3) THE REPLY COMMAND WILL THEN BUILD A PARM LIST TO BE PASSED TO
*    THE REPLYLOA LOAD MODULE. THE PARM LIST IS AS FOLLOWS
*
*    IMS NOT SPECIFIED              IMS SPECIFIED
*
*    MSGNUM DC CL8' '               MSGNUM DC CL8'DFS999I'
*    PARMIMS DC CL9'NONE'           PARMIMS DC CL9' '
*    REPTXT DC CL100' '             REPTXT DC  CL100' '
*
* 4) THE REPLY COMMAND WILL LOAD THE REPLYLOA LOAD MODULE, AND
*    AND BRANCH TO IT, WAITING PATIENTLY UNTIL IT RETURNS.
*    FOR COMMENTS ON HOW THE REPLYLOA MODULE WILL DO ITS MAGIC,
*    READ THE COMMENTS FOR THAT MDOULE.
*
* 5) THE REPLY COMMAND WILL DELETE THE REPLYLOA LOAD MODULE, AND
*    TERMINATE.
*
*
* MODIFIED:
*
*  24AUG99   DHC  BYPASS SUSPECT AUTHORITY CHECKING            *AGCO*
*
*  15OCT92   GLA  INCLUDE MAINTENANCE TO 4.2
*                 A.  ADD RETURN CODE TO ALLOW DETECTION OF NO
*                     OUTSTANDING REPLY
*                        LASTCC = 0    GOOD REPLY
*                                 4    REPLY NOT OUTSTANDING
*                                 8    ERROR IN COMMAND
*
*                 B.  ADD SUPPORT FOR IDMS
*                 C.  CHANGE CHECK FOR JOBNAME TO KEEP BELOW LINE
*                     FOR ESA 4.2
*
********************************************************************
         L     R2,CPPLPSCB
         USING PSCB,R2
         B     PROCEED             SKIP AUTHORITY TESTS        *AGCO*
         TM    PSCBATR2,X'C0'           WELL ?
         BO    PROCEED
         L     R9,X'224'               GET ASCB INTO R9
         USING ASCB,R9
         L     R9,ASCBJBNS         GET STARTED TASK NAME       15OCT92
         CLC   0(4,R9),=CL4'TSSO'  ALLOW TSSO TO REPLY TO ANY  15OCT92
         BE    PROCEED
         B     IMPLEXEC
IMPLEXEC DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR8,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
PROCEED  DS    0H
         SETPARSE  PCL=REPLYPCL
         CALLTSSR EP=IKJPARS
         LTR   R15,R15
         BZ    PARSEOK
         B     PARSERR
         B     ENDPROG
PARSEOK  DS    0H
         L     R9,MYANS                 GET RESULTS OF PARSE
         USING IKJPARMD,R9
         CLC   MSGID,=H'1'              WAS MESSAGE ID RQSTED?  15OCT92
         BE    MSGIDOK                  YES SO HANDLE IT        15OCT92
         BH    TESTIMS                  SEE IF IMS SPECIFIED    15OCT92
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR3,TERM,SINGLE,DATA), 15OCT92X
               MF=(E,IOPLADS)           ISSUE ERROR             15OCT92
         B     PARSERR                                          15OCT92
MSGIDOK  DS    0H
* GO AND MOVE THE MSGID INTO THE REPLYLOA PARM LIST.
         LH    R2,MSGIDFLD+4            GET LENGTH OF MESSAGE ID
         BCTR  R2,0                     SUBTRACT 1 FOR EXECUTE
         L     R3,MSGIDFLD              GET ADDRESS
         EX    R2,MOVEMSG               AND MOVE IT
         MVC   PARMIMS(9),=CL9' '       INDICATE NO IMS SPECIFIED
         B     EX1                      PROCEED
MOVEMSG  MVC   PARMMSG(0),0(R3)         ** EXECUTED **
EX1      DS    0H
         B     GETTEXT                  GET THE TEXT
TESTIMS  DS    0H                                               15OCT92
         CLC   MSGID,=H'2'               WAS IMS SPECIFIED ?    15OCT92
         BH    TESTIDMS                 NO.. TRY IDMS           15OCT92
IMSOK    DS    0H
         L     R3,IMSFLD
         LH    R2,IMSFLD+4              GET LENGTH OF IMS FIELD
         MVI   PARMIMS,C'I'             INDICATE IMS REQUESTED  15OCT92
IMSIDMS  BCTR  R2,0                     GET MACHINE LENGTH      15OCT92
         MVC   PARMIMS+1(8),=CL8' '     CLEAR AREA              15OCT92
         EX    R2,MOVEIMS               MOVE IN DATA            15OCT92
         B     GETTEXT                                          15OCT92
MOVEIMS  MVC   PARMIMS+1(0),0(R3)       **EXECUTED ONLY**       15OCT92
TESTIDMS DS    0H                                               15OCT92
* INSERT OTHER TESTS HERE                                       15OCT92
IDMSOK   L     R3,IDMSFLD               GET ADDRESS             15OCT92
         LH    R2,IDMSFLD+4             GET GET LENGTH          15OCT92
         MVI   PARMIMS,C'D'             INDICATE IDMS REQUESTED 15OCT92
         B     IMSIDMS                                          15OCT92
GETTEXT  DS    0H
* HERE, IMS WAS NOT SPECIFIED  SO WE GET THE REPLY TEXT.
* NOW MOVE IN THE REPLY TEXT
         TM    REPLYFLD+6,X'80'
         BO    REPTXTOK
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR5,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     PARSERR
REPTXTOK DS    0H
         LH    R2,REPLYFLD+4
         L     R3,REPLYFLD
         BCTR  R2,0
         EX    R2,MOVETXT
         B     EX2
MOVETXT  MVC   PARMTEXT(0),0(R3)
EX2      DS    0H
         TESTAUTH FCTN=1
         LTR   R15,R15
         BNZ   NOAPF
         MODESET KEY=ZERO
         LOAD  EP=REPLYLOA,ERRET=NOLOAD    ,GLOBAL=(YES,P)     *DHC*
         L     R1,PARMADDR       LOAD UP PARM ADDRESS
         LR    R15,R0                   GET ADDRESS OF REPLYLOA
         OI    FLAGS,X'80'              SET FLAG TO INDICATE LOAD
*        BALR  R14,R15                  BRANCH TO REPLYLOA
         BASSM R14,R15                  BRANCH TO REPLYLOA      23AUG99
         BASSM R14,0                    RETURN TO 24 BIT MODE   23AUG99
         LTR   R15,R15           WAS REPLY MADE ?
         BZ    REPLIED                  DIAGNOSE RETURN CODE
         MVC   EXITRC,=H'4'         GIVE ERROR                  15OCT92
         CLC   PARMIMS(9),=CL9' '   ATTEMPTING TO REPLY TO IMS ?
         BE    ERRNOIMS             NOPE !
         MVC   ERRLNE1(8),PARMIMS+1
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR1,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
ERRNOIMS DS    0H
         MVC   ERRLNE2+38(8),PARMMSG
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR2,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
REPLIED  DS    0H
         B     ENDPROG
ERRPROG  DS    0H                                               15OCT92
         MVC   EXITRC,=H'8'      SET ERROR OCCURED              15OCT92
ENDPROG  DS    0H
         TM    FLAGS,X'80'       WAS REPLYLOA LOADED ?
         BZ    NODELETE          NO, DONT BOTHER DELETING
         DELETE EP=REPLYLOA
NODELETE DS    0H
         MLEAVE
NOLOAD   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR6,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ERRPROG                                          15OCT92
PARSERR  DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR7,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ERRPROG                                          15OCT92
NOAPF    DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR9,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ERRPROG                                          15OCT92
ERROR1   DC    H'80',H'0'
ERRLNE1  DC    CL80'XXXXXXXX DOES NOT HAVE AN OUTSTANDING REPLY'
ERROR2   DC    H'80',H'0'
ERRLNE2  DC    CL80'THERE ARE NO OUTSTANDING REPLIES FOR "XXXXXXXX"'
ERROR3   DC    H'80',H'0'
ERRLNE3  DC    CL80'YOU MUST SPECIFY EITHER AN IMS COPY, OR A MESSAGE IX
               D TO REPLY TO'
ERROR5   DC    H'80',H'0'
ERRLNE5  DC    CL80'REPLY TEXT MISSING- RE-ENTER THE COMMAND WITH TEXT X
               FOR THE REPLY'
ERROR6   DC    H'80',H'0'
ERRLNE6  DC    CL80'SEVERE ERROR LOADING THE REPLYLOA MODULE- CONTACT  X
               A SYSTEMS PROGRAMMER'
ERROR7   DC    H'80',H'0'
ERRLNE7  DC    CL80'REPLY COMMAND TERMINATING DUE TO PARSE ERROR'
ERROR8   DC    H'80',H'0'
ERRLNE8  DC    CL80'IKJ56500I COMMAND REPLY NOT FOUND'
ERROR9   DC    H'80',H'0'
ERRLNE9  DC    CL80'REPLY WAS CALLED IN AN UNAUTHORIZED ENVIRONMENT- REX
               QUEST ABORTED'
FLAGS    DS    H
* BYTE 1:
* X'80' = REPLYLOA WAS LOADED.
WKA      DS    2D
PRINTOCC DS    F
PARMADDR DC    A(PARMS)
PARMS    DS    0D
PARMMSG  DC    CL8' '
PARMIMS  DC    CL9' '
PARMTEXT DC    CL120' '
REPLYPCL IKJPARM
MSGID    IKJKEYWD
         IKJNAME 'MESSAGE',SUBFLD=MSGIDSUB,                     15OCT92X
               ALIAS=('MSGID','MSG')                            15OCT92
         IKJNAME 'IMS',SUBFLD=IMSSUB                            15OCT92
         IKJNAME 'IDMS',SUBFLD=IDMSSUB                          15OCT92
REPTXT   IKJKEYWD
         IKJNAME 'REPLYTXT',SUBFLD=REPLYSUB
         IKJNAME 'TEXT',SUBFLD=REPLYSUB
MSGIDSUB IKJSUBF
MSGIDFLD IKJIDENT 'MSGID',PROMPT='MESSAGE ID',CHAR,             15OCT92X
               HELP=('MESSAGE NUMBER TO REPLY TO'),             15OCT92X
               MAXLNTH=8                                        15OCT92
REPLYSUB IKJSUBF
REPLYFLD IKJIDENT 'REPLY',PROMPT='REPLY TEXT',FIRST=ALPHANUM,          X
               OTHER=ALPHANUM,HELP=('TEXT OF REPLY'),                  X
               MAXLNTH=120,CHAR
IMSSUB   IKJSUBF
IMSFLD   IKJIDENT 'IMS',FIRST=ALPHANUM,                                X
               OTHER=ALPHANUM,HELP=('IMS COPY TO REPLY TO'),           X
               MAXLNTH=8
IDMSSUB  IKJSUBF ,                                              15OCT92
IDMSFLD  IKJIDENT 'IDMS',FIRST=ALPHANUM,                        15OCT92X
               OTHER=ALPHANUM,HELP=('IDMS CV TO REPLY TO'),     15OCT92X
               MAXLNTH=8                                        15OCT92
         IKJENDP
         CVT DSECT=YES
         IKJIOPL
         IKJPSCB
         IHAASCB
         END REPLY
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY REPLY(''TSSO  4.3 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    REPLY'
 PUNCH ' NAME     REPLY(R)'
         END
