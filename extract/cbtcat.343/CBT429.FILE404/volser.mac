          PRINT GEN
VOLSER    MENTER  12,EQU,COM=VOLSER,CP=CP
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
***********************************************************************
* THIS PROGRAM WILL PERFORM THE IOS LOOKUP TABLE FUNCTION.            *
* IT IS MEANT TO BE EXECUTED UNDER THE TSSO SUBSYSTEM.
* COMMAND SYNTAX-     #VOLSER VOLUME
* IF NO VOLSER IS SPECIFIED, THE SYSTEM RESIDENCE UCB WILL BE
* DISPLAYED.

*         +---------+
*         + CHANGES +
*         +---------+
* 30DEC96 RPS FIXED ADDRESSABILITY ERRORS WITH HLASM
***********************************************************************
         SETPARSE PCL=PARSEPCL
         CALLTSSR EP=IKJPARS
         CH     R15,=H'3'
         BH     TRUB1             RC = 4 OR ABOVE
         L      R4,MYANS
         USING  IKJPARMD,R4
         TM     VOLSERP+6,X'80'       IS VOLSER PRESENT?
         BNO    USERES
         LH     R3,VOLSERP+4          GET LENGTH
         CH     R3,=H'6'              VOLSER IS 6 BYTES !
         BNE    INVALID               GET ADDR OF PARM
         L      R3,VOLSERP            POINT THO THE VOLSER
         MVC    VOLDES(6),0(R3)
         OC     VOLDES(6),=CL6' '     MAKE UPPER CASE
         L      R3,16
         USING  CVTMAP,R3
         XC     WORKAREA(100),WORKAREA     ZERO OUT WORKAREA
         LA     R1,WORKAREA
         ST     R1,PARM1
         LA     R1,DEVCLASS
         ST     R1,PARM2
         LA     R1,UCBADDR
         ST     R1,PARM3
         OI     PARM3,X'80'
SRCHIT   DS     0H
         LA     R1,PARMLIST
         L      R15,CVTUCBSC
         BALR   R14,R15
         LTR    R15,R15
         BNZ    ENDLIST
         B      FORMUCB
USERES   DS     0H
***********************************************************************
* IF NO VOLSER WAS SPECIFIED IN THE COMMAND, THE PROGRAM WILL JUMP HERE
* POINTING TO THE UCB FOR THE SYSTEM RESIDENCE VOLUME.
***********************************************************************
         DROP   R3                                               RPS
         L      R5,16
         USING  CVT,R5
         L      R5,CVTSYSAD
         DROP   R5
         ST     R5,UCBADDR
         MVC    VOLDES(6),28(R5)
         B      FORMUCB
FORMUCB  DS     0H
         FORMAT (UCBADDR,4,XXXXXXXX),TO=UCBECB,WKA=WKA
         L      R5,UCBADDR
         MVC    MSG2+9(3),13(R5)
         MVC    MSG2+21(6),28(R5)
         MVC    MSG2+37(8),UCBECB
         CLC    28(6,R5),VOLDES
         BE     FINIT
CONVEXIT DS     0H
         B      SRCHIT
TRUB1    DS     0H
         WTO    'IKJPARSE FAILURE, CONTACT SUPPORT',ROUTCDE=(2,11)
         B      ENDPROG
INVALID  WTO   'INVALID VOLSER SPECIFIED- REENTER COMMAND',            X
               ROUTCDE=(2,11)
         B      ENDPROG
FINIT    DS     0H
         PUTLINE PARM=PUTBLOK,                                         X
               OUTPUT=(MSG2,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         MVC   OPERMSG+11(3),13(R5)
         PUTLINE PARM=PUTBLOK,                                         X
               OUTPUT=(OPERPUT,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     ENDPROG
ENDLIST  DS     0H
         MVC   MSG4+19(6),VOLDES
         PUTLINE PARM=PUTBLOK,OUTPUT=(MSG4,TERM,SINGLE,DATA),          X
               MF=(E,IOPLADS)
ENDPROG  DS    0H
         MLEAVE
PARSEPCL IKJPARM
VOLSERP  IKJIDENT 'VOLSER',UPPERCASE,MAXLNTH=6,FIRST=ALPHA,            X
               OTHER=ALPHANUM,CHAR,                                    X
               HELP=('VOLSER IN THE FORM XXXXXX')
         IKJENDP
PUTECB   DS     F
MSG2     DC     H'80',H'0'
         DC     CL80'UCB: XXX, VOLSER=YYYYYY, UCBADDR=ZZZZZZZZ'
MSG4     DC     H'80',H'0'
MSG4TEXT DC     CL80'TSSO01I VOLSER XXXXXX IS NOT IN THE SYSTEM'
OPERPUT  DC     H'80',H'0'
OPERMSG  DC     CL80'*COM D U,,,XXX,1'
UCBECB   DC     CL8' '
WKA      DC     CL12' '
UCBADDR  DS     F
VOLDES   DC     CL80' '             VOLUME DESIRED
DEVHEX   DS     F
ADDRECB  DC     CL8' '
CURRADDR DS     F                   STATUS OF SEARCH IN UCBS
PARMLIST DS     0D
PARM1    DS     F
PARM2    DS     F
PARM3    DS     F
WORKAREA DS     XL100
DEVCLASS DC     XL1'00'
NUM      DS     F
COMLIST  DS     0H
         PRINT  NOGEN
         CVT    DSECT=YES
         IKJIOPL
         END VOLSER
 PUNCH ' SETCODE  AC(0)'
 PUNCH ' IDENTIFY VOLSER(''V 1.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    VOLSER'
 PUNCH ' NAME     VOLSER(R)'
         END
