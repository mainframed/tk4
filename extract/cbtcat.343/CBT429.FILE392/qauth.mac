         TITLE '--- QAUTH-- IKJEFTSR INTERFACE ---'
*---------------------------------------------------------------------*
*                                                                     *
*    This module is invoked under a parallel TMP task structure       *
*    set up by the IKJEFTSR call in Q#AF. It should be running        *
*    authorized by virtue of being in the authorized programs         *
*    list (either IKJERFAP or XA related parmlib member).             *
*    The purpose is to set authorization on in the main Q task.       *
*                                                                     *
*  Updates:                                                           *
*    30Jun91 LDW - Use label for qcommon id check location            *
*    17Jun91 LDW - Set return code if didn't get APF auth; QXAPFFL    *
*                     eliminated                                      *
*                - Check QAUTHZ to insure that we are working with    *
*                     a QUEUE module with a synchronized version of   *
*                     QCOMMON so that I don't waste hours debugging   *
*                     this junk when QCOMMON changes                  *
*    14Apr91 LDW - Turn off QXAPFFL upon success instead of setting   *
*                     it on failure                                   *
*    30Jan91 LDW - Rename from ISRAUTH                                *
*                - Miscellaneous cosmetic code changes                *
*                                                                     *
*---------------------------------------------------------------------*
QAUTH    CSECT
         SAVE  (14,12),,QAUTH_&SYSDATE._&SYSTIME
         LR    R12,R15
         USING QAUTH,R12
         L     R2,0(,R1)                -> first parm
*---  Validate that call came from QUEUE
         CLC   =X'0004FFFFFFFF',0(R2)
         BNE   ABEND1
         L     R2,4(,R1)                -> second parm
         L     R11,2(,R2)               -> QCOMMON
         USING QCOMMON,R11
         CLC   =CL11'****QCOMMON',QCMN#ID
         BNE   ABEND2
         CLC   =X'07FE',QAUTHZ+4        look ok?
         BNE   ABEND3                   no
***********************************************************************
*                                                                     *
*  SEE IF MVS THINKS WE ARE AUTHORIZED                                *
*                                                                     *
***********************************************************************
         TESTAUTH  FCTN=1               ask MVS if auth exists
         SPACE 1
         LTR   R15,R15                  are we?
         BZ    QAUTHZ                   yes - go make believe Q
*                                       (set R13 and simulate QSTOP)
***********************************************************************
*                                                                     *
*  AUTHORIZATION ATTEMPT FAILED - RETURN INDICATOR                    *
*                                                                     *
***********************************************************************
         LA    R1,=C'QAUTH not auth'    point at msg
         LA    R0,14                    length
         TPUT  (1),(0),R                go invoke IKJEFTSR
         RETURN (14,12),,RC=16
ABEND1   ABEND 1,DUMP,STEP
ABEND2   ABEND 2,DUMP,STEP
ABEND3   TPUTX '*** QAUTH - Wrong version of QCOMMON ***'
         ABEND 3,DUMP,STEP
         DROP  R12,R11
         LTORG ,
         QCOMMON
         QREGS
