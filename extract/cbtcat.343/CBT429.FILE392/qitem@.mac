         MACRO
&NFS     QITEM &OFF1,&DESC,&OFF2,&TYPE,&NAME,&LEN,&SKIP=,&CAPS=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QITEM - FORMAT A DATA FIELD FOR OUTPUT.  INPUT MAY BE HEX, DEC,   *
.*          CHAR, DATE, or TIME.                                      *
.*                                                                    *
.*  UPDATES:                                                          *
.*     29Nov86  LDW  Change to support DATEFMT subroutine             *
.*     29AUG86  LDW  SUPPORT 'ATTR' ROUTINE                           *
.*                   USE R5 FOR DYNAMICALLY COUNTING FIELDS           *
.*                   ADD 'ADDLINE' FUNCTION TO SET ATTR NUM AND LINK  *
.*                      TO QADDLINE                                   *
.*     11/24/83 LDW  DON'T GENERATE LEADING ZERO IN NAME OF CONSTANT  *
.*                      IF IT'S NUMBER IS > 99 (THIS WAS A BUG)       *
.*                   INCREASE SIZE OF QITEM#T ARRAY                   *
.*     01/17/83 LDW  CHANGE TO STORE HEADERS IN GLOBAL AREA FOR       *
.*                      LATER EXPANSION USING "LC"                    *
.*                   TRANSLATE CHARACTER FIELDS (R7 MUST -> TRTAB)    *
.*     10/25/82 LDW  CHANGE &PREFIX.WORK TO &PREFIX.@WORK             *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
.* NOTE: GENERATED CODE INTENTIONALLY SHIFTED RIGHT 1 COLUMN
         GBLA  &QITEM#I
         GBLC  &QSECT,&QITEM#T(200)
         LCLA  &L,&I
         LCLC  &PREFIX,&Z
         AIF   ('&QSECT' EQ 'JOBINFO').JOBINFO
         AIF   ('&QSECT' EQ 'BLKLIST').BLKLIST
         AIF   ('&QSECT' EQ 'ATTR').ATTR
         MNOTE 12,'QITEM USED IN UNSUPPORTED CSECT - MACRO TERMINATED'
         MEXIT
.JOBINFO ANOP
&PREFIX  SETC  'JI'
         AGO   .START
.BLKLIST ANOP
&PREFIX  SETC  'BL'
         AGO   .START
.ATTR    ANOP
&PREFIX  SETC  'AT'
.START   AIF   ('&OFF1' EQ 'GEN').GEN
         AIF   ('&OFF1' EQ 'ADDLINE').ADDLINE
         AIF   ('&OFF1' NE 'RESET').GO
&QITEM#I SETA  0
         MEXIT
.ADDLINE  STC   R5,QDATRNUM+1            NUMBER OF ATTR BYTES IN LINE
          SR    R5,R5                    CLEAR COUNTER FOR NEXT LINE
          BAL   R14,QADDLINE             DISPLAY LINE
         SPACE 2
         MEXIT
.GO      AIF   ('&DESC' EQ '').NODESC
         AIF   ('&CAPS' EQ 'YES').CAPS
&QITEM#I SETA  &QITEM#I+1               -> ARRAY ELEMENT
&QITEM#T(&QITEM#I) SETC '&DESC'
&Z       SETC  '&PREFIX.#&QITEM#I'      GET NAME OF SYMBOL
         AIF   (&QITEM#I GT 99).ZOK1    &Z NULL IF 3 DIGITS
&Z       SETC  '&PREFIX.#0&QITEM#I'     1 LEADING ZERO IF 2 DIGITS
         AIF   (&QITEM#I GT 9).ZOK1     1 LEADING ZERO IF 2 DIGITS
&Z       SETC  '&PREFIX.#00&QITEM#I'    2 LEADING ZEROES IF 2 DIGITS
.ZOK1    ANOP
&NFS      MVC   &PREFIX.@LINE+&OFF1.(&Z.L),&Z
         AGO   .SKIP
.CAPS    ANOP
&L       SETA  K'&DESC-2
&NFS      MVC   &PREFIX.@LINE+&OFF1.(&L),=C&DESC
.SKIP     LA    R5,2(,R5)                INCREMENT ATTR BYTE COUNT
.NODESC  AIF   ('&TYPE' EQ 'CHAR').CHAR
         AIF   ('&TYPE' EQ 'HEX' ).HEX
         AIF   ('&TYPE' EQ 'TIME').TIME
         AIF   ('&TYPE' EQ 'DATE').DATE
         AIF   ('&TYPE' EQ 'DEC').DEC
         MNOTE 8,'&TYPE IS INVALID TYPE'
         AGO   .DONE
.CHAR     OC    &NAME.(&LEN),&NAME
          BZ    *+16
          MVC   &PREFIX.@LINE+&OFF2.(&LEN),&NAME
          TR    &PREFIX.@LINE+&OFF2.(&LEN),0(R7)
         AGO   .DONE
.HEX      UNPK  &PREFIX.@LINE+&OFF2.(2*&LEN+1),&NAME.(&LEN+1)
          TR    &PREFIX.@LINE+&OFF2.(2*&LEN),QHEXTAB
          MVI   &PREFIX.@LINE+&OFF2+2*&LEN,C' '
         AGO   .DONE
.TIME     L     R1,&NAME
          BAL   R2,&PREFIX.$TIME
          MVC   &PREFIX.@LINE+&OFF2.(8),&PREFIX.@WORK
         AGO   .DONE
.DATE     L     R0,&NAME
          BAL   R2,&PREFIX.$DATE
          B     &SKIP                   FIELD IS ALL ZERO
          MVC   &PREFIX.@LINE+&OFF2.(7),&PREFIX.@WORK
         AGO   .DONE
.DEC     AIF   ('&LEN' EQ '').DEC$4
         AIF   (&LEN EQ 2).DEC$2
         AIF   (&LEN NE 1).DEC$4
          SR    R0,R0
          IC    R0,&NAME
         AGO   .DEC$BAL
.DEC$2    LH    R0,&NAME
         AGO   .DEC$BAL
.DEC$4    L     R0,&NAME
.DEC$BAL  BAL   R2,&PREFIX.$DEC
          MVC   &PREFIX.@LINE+&OFF2.(13),0(R1)
.DONE    SPACE 2
         MEXIT
.GEN     ANOP
&I       SETA  &I+1
&Z       SETC  ''                       NO LEADING ZEROES IF 3 DIGITS
         AIF   (&I GT 99).ZOK2          &Z NULL IF 3 DIGITS
&Z       SETC  '0'                      1 LEADING ZERO IF 2 DIGITS
         AIF   (&I GT 9).ZOK2           1 LEADING ZERO IF 2 DIGITS
&Z       SETC  '00'                     2 LEADING ZEROES IF 2 DIGITS
.ZOK2    ANOP
&PREFIX.#&Z&I  DC  X'1DE8'
         LC    &QITEM#T(&I)
         DC    X'1D60'
&PREFIX.#&Z&I.L EQU *-&PREFIX.#&Z&I
         AIF   (&I LT &QITEM#I).GEN
         MEND
