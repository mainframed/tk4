         TITLE '--- QUEUE--MCS -- SPY ON THE OPERATOR''S CONSOLE ---'
*---------------------------------------------------------------------*
*                                                                     *
*   MCS - Copy contents of an MCS console screen buffer               *
*                                                                     *
*   Entry - QPARM1 = console address (default = last console operand) *
*                                                                     *
*   Note - This subcommand is restricted to privileged users          *
*                                                                     *
*   Updates:                                                          *
*      25Dec89  LDW  Q$xxx -> QZxxx:  CVT, MCS                        *
*      04Jun89  LDW  Use improved AUTHFUNC routine                    *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      04/16/84 LDW  DON'T STACK THIS SUBCOMMAND (TEMP, UNTIL CONS    *
*                       IS RE-INTEGRATED AS A TRUE SUBCOMMAND)        *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*      03/24/83 LDW  AGO AROUND OLD CODE TO AVOID ASM ERRORS          *
*      03/02/83 LDW  CHANGE 'QTILT *' TO USE QERRMSG                  *
*      01/16/83 LDW  CALL APF SUBROUTINE                              *
*      01/07/83 LDW  DO STFSMODE ON AFTER LINKING TO DCMS CONS        *
*      01/05/83 LDW  ADD WORKLEN=                                     *
*      12/22/82 LDW  LINK TO DCMS CONS FOR SP1.3 SUPPORT              *
*      12/20/82 LDW  CHANGE QTILT REPRMPT=YES TO OPTIONS=REPROMPT     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
MCS      QSTART  Q84,WORKLEN=MC@WORKL   ,STACK=X'B0'
         USING WORK,R13
         LA    R1,4                     request apf authorization
         QCALL AUTHFUNC
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
*---  ASSUME WE HAVE TMPMAC AVAILABLE - IF NOT, "CONS" WILL NOT BE
*---  GENERATED IN PARSE'S COMMAND TABLE
         MVC   MCS@CMD,=C'CONS '        SET UP TSO COMMAND
         CLI   QPARM1,C' '              ANY ADDRESS SPECIFIED?
         BE    *+10                     NO - SKIP
         MVC   QUCMNAME,QPARM1          YES - REMEMBER FOR LATER
         MVC   MCS@ADDR,QUCMNAME        GET OPERAND (CONSOLE ADDRESS)
         CLI   MCS@ADDR,C'*'            SWITCH TO MASTER CONSOLE?
         BNE   *+10                     NO - OK
         MVC   MCS@ADDR,QBLANK          YES - SET IT BLANK
         TMPMAC  CMD=(MCS@CMD,8),MF=(E,MCTMPMAC)  DO IT
         STFSMODE ON                    CONS WILL HAVE TURNED THIS OFF
         LA    R1,8                     SET ENTRY CODE = 8
         QCALL AUTHFUNC                 RESET APF IF WE SET IT
         QTILT '=== DONE ==='           KLUDGE UNTIL I FIGURE OUT HOW
*                                          TO DO IT RIGHT...
          AGO   .MC01
***********************************************************************
*                                                                     *
*  THIS CODE RIPPED OFF FROM THE DCMS "CONS" COMMAND                  *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,QBLANK           CLEAR TITLE LINE
         MVI   MCS@FLAG,0               INIT FLAGS
         MVC   QDHLINE(39),=C'SIMULATED CONSOLE USING 3270 CONSOLE AT'
         LA    R1,12                    request key zero
         QCALL AUTHFUNC                 HO HUM
         L     R1,CVTPTR                -> CVT
         L     R1,CVTCUCB-CVT(,R1)      -> UCM
         USING UCM,R1
         CLI   QUCMNAME,C'*'            SWITCH TO MASTER CONSOLE?
         BNE   OLDWAY                   NO - SEARCH FOR REQUESTED CNSL
         SPACE 2
*  FIRST LET'S TRY TO FIND THE MASTER CONSOLE
MCSMSTR  LR    R15,R1                   COPY FOR ADJUSTMENT
         SH    R15,=H'4'                POINT TO MCS PREFIX ADDR
         L     R15,0(,R15)              GET MCS PREFIX
         LTR   R15,R15                  IS THERE ONE?
         BZ    OLDWAY                   NO - SKIP
         USING UCMPRFX,R15
         L     R3,UCMMCENT              GET MASTER CONSOLE ENTRY
         LTR   R3,R3                    IS THERE ONE?
         BZ    OLDWAY                   NO - SKIP
         DROP  R15
         USING UCMLIST,R3
         L     R10,UCMXB                GET DCM
         LTR   R10,R10                  IS THERE ONE?
         BZ    OLDWAY                   SKIP IF NONE/NOT GRAPHICS
         USING DCMTSRT,R10              POINT TO THE DCM
         L     R6,DCMADTRN              GET PAGEABLE DCM (TDCM)
         LTR   R6,R6                    IS THERE ONE?
         BZ    OLDWAY                   NO - SKIP
         DROP  R10
         USING DCMSTRT,R6               A LITTLE
         CLI   DCMIONDX,X'10'           IS IT A 3270 ?
         BE    MCS3270                  YES
         CLI   DCMIONDX,X'04'           IS IT (COUGH) A 3066 ?
         BNE   OLDWAY                   SKIP IF NOT NEITHER
         OI    MCS@FLAG,MCS@3066        REMEMBER 3066 FOR LATER
         SPACE 1
MCS3270  L     R14,UCMUCB               GET UCB FOR THIS CONSOLE
***      CLI   18(R14),X'10'            GRAPHICS?
***      BNE   OLDWAY                   SKIP IF NOT
         TM    UCMATR,UCMUF             IS IT ACTIVE ?
         BO    GOTUCM                   YES - SAVE UCM ADDR
         SPACE 2
         DROP  R3,R6                    DITCH ALL MY REGISTERS
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  COULDN'T FIND MSTCONS, SO NOW WE DO IT THE OLD WAY --              *
*  JUST USE THE FIRST AVAILABLE GRAPHICS CONSOLE.                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
OLDWAY   LM    R3,R5,UCMVEA             R3 -> TO THE FIRST UCME
*                                         R4 CONTAINS UCME LENGTH
*                                           R5 -> TO THE LAST UCME
         DROP  R1
         USING UCMLIST,R3
DID0     L     R1,UCMUCB                SEARCH FOR SOMETHING USEFUL
         MVC   QDHLINE+40(3),13(R1)     SAVE DEVICE ADDRESS
         CLI   QUCMNAME,C'*'            LOOKING FOR SPECIFIC CONS?
         BE    ANY                      NO - SKIP
         CLC   QDHLINE+40(3),QUCMNAME   YES - THIS IT?
         BNE   DID1                     NO - TRY NEXT
***
ANY      TM    UCMATR,UCMUF             IS IT ACTIVE ?
         BZ    DID1                     NO.. UNUSEABLE
         L     R10,UCMXB                IS IT GRAPHICS ?
         LTR   R10,R10                  ...
         BZ    DID1                     NO.. USELESS
         USING DCMTSRT,R10              POINT TO THE DCM
         L     R6,DCMADTRN              FIND THE PAGEABLE DCM (TDCM)
         LTR   R6,R6                    DOES IT EXIST ?
         BZ    DID1                     NO...
         DROP  R10
         USING DCMSTRT,R6               A LITTLE
         NI    MCS@FLAG,255-MCS@3066    ASSUME NOT ANCIENT DEVICE
         CLI   DCMIONDX,X'10'           IS IT A 3270 ?
         BE    GOTUCM                   YES BY DINGLES !
*                                          DINGLES ?
         OI    MCS@FLAG,MCS@3066        ASSUME 3066
         CLI   DCMIONDX,X'04'           IS IT A 3066 ?
         BE    GOTUCM                   YES - ARRGH
DID1     BXLE  R3,R4,DID0               GET THE NEXT ENTRY
         LA    R1,16                    get out of key zero
         QCALL AUTHFUNC                 HO HUM
         CLI   QUCMNAME,C'*'            LOOKING FOR SPECIFIC CONS?
         BNE   NOTFOUND                 YES - DIDN'T FIND IT
         QTILT '*** NO ACCEPTABLE (3270 OR 3066) CONSOLES FOUND ***'
NOTFOUND MVC   QERRMSG,QBLANK           CLEAR THE AREA
         MVC   QERRMSG(3),QUCMNAME      WHAT WE ARE LOOKING FOR
         MVC   QERRMSG+3(24),=C' NOT FOUND OR NOT USABLE'
         QTILT *,OPTIONS=REPROMPT       LEAVE THIS PLACE
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FOR THE USER*
*        R3 ->  UCME                                                  *
*        R6 ->  TDCM                                                  *
*                                                                     *
*---------------------------------------------------------------------*
GOTUCM   MVC   QUCMID,UCMID             SAVE UCM ID
         L     R1,UCMUCB                -> UCB
         MVC   QDHLINE+40(3),13(R1)     GET DEVICE ADDRESS
         TM    UCMDISP1,UCMDISPA        MSTCONS?
         BNO   *+10                     NO - SKIP
         MVC   QDHLINE+50(9),=C'*MSTCONS*'   YES - SHOW WE'RE SMART
         TM    MCS@FLAG,MCS@3066        SHADES OF 2250?
         BNO   *+10                     NO - SKIP
         MVC   QDHLINE+24(4),=C'3066'   YES - IT'S BEEN REINCARNATED...
         LH    R2,DCMMSGAL              NUMBER OF LINES IN MSG AREA
         L     R7,DCMASCRN              POINT TO THE FIRST INPUT LINE
GOT1     MVC   QDMSG(80),0(R7)          MOVE A LINE
         TM    MCS@FLAG,MCS@3066        3066?
         BO    *+10                     YES - IT HAS 80 CHAR LINES
         MVC   QDMSG+78(2),QBLANK       CLEAR THE END OF THE LINE
*        MVI         ,X'F4'             MAKE IT LOW INTENSITY
*        CLI   3(R7),C'*'               SYSTEM ACTION MESSAGE?
*        BE    *+12                     YES - MAKE IT BRIGHT
*        CLI   3(R7),C'@'               USER ACTION MESSAGE?
*        BNE   *+8                      NO - LEAVE AS IS
*        MVI         ,X'F8'             YES - MAKE IT BRIGHT
         LA    R1,16                    get out of key zero
         QCALL AUTHFUNC                 HO HUM
         BAL   R14,QADDLINE             ADD LINE TO DISPLAY
         LA    R7,80(,R7)               -> NEXT CONSOLE BUFFER LINE
         TM    MCS@FLAG,MCS@3066        3066?
         BO    *+8                      YES - OK
         LA    R7,4(,R7)                3270 IS 78 BYTES + 6 CONTROL
         LA    R1,12                    request key zero
         QCALL AUTHFUNC                 HO HUM
         BCT   R2,GOT1                  GET EM ALL
         LA    R1,16                    get out of key zero
         QCALL AUTHFUNC                 HO HUM
         B     QSTOP                    ALL DONE HERE
         DROP  R3,R6
.MC01     ANOP
         LTORG ,
         DROP  R13
         SPACE 3
          AGO   .MC02
         QZCVT
         QZMCS
.MC02     ANOP
         QCOMMON
         SPACE 2
WORK     DSECT
         ORG   WORK+72                  SKIP OVER SAVE AREA
MCS@FLAG DS    X
MCS@3066 EQU   X'80'                    SELECTED CONSOLE IS A 3066
MC@WORKL EQU   *-WORK
MCTMPMAC TMPMAC  MF=L
MCS@CMD  DS    CL5                      'CONS '
MCS@ADDR DS    CL3                      OPERAND FOR ABOVE
