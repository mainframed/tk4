         TITLE '--- QUEUE--LISTDAS -- LIST ALL DAS CONTROL BLOCKS ---'
*---------------------------------------------------------------------*
*                                                                     *
*   LISTDAS - Display information from DAS for all spool volumes      *
*                                                                     *
*   Note - This subcommand is restricted to privileged users          *
*                                                                     *
*   Updates:                                                          *
*      05Jan90  LDW  Written                                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
LISTDAS  QSTART  STACK=X'30'
         QCALL CKPT                     read checkpoint data
         QCALL DISP80                   switch to 3278-2 mode if 3278-5
          AIF   (&QLEVEL GE 7).DL01
         QTILT '*** LISTDAS not implemented for this level of JES2 ***'
          AGO   .DL99
.DL01     ANOP
         MVC   QDHLINE(46),=C'VOLSER XN FL TGnum -Extents- T/G Status  $
                MASK'
         L     R9,QCJQTL                -> master ckpt record
         USNGX $SAVEBEG,R9
         L     R3,QCDASA                -> DAS area
         LH    R4,$SPOLNUM              number of spool volumes allowed
         USNGX DAS,R3
***********************************************************************
*                                                                     *
*        Loop through all DASs, displaying interesting info           *
*                                                                     *
***********************************************************************
DL$LOOP  EQU   *
***      tm    dasflag,dasexsts         does volume exist?
***      bz    dl$skip                  no - skip it
         MVC   DL@VOLID,DASVOLID
         HEX   DL@EXTNO,DASEXTNO,LEN=1,HEXTAB=QHEXTAB
         HEX   DL@FLAG,DASFLAG,LEN=1
         LH    R0,DASNOTGE              number of tg's in extent
         CVD   R0,QNUMWORK
         OI    QNUMWORK+7,X'0F'
         MVC   DL@TGNUM,=X'2020202120'
         ED    DL@TGNUM-1(L'DL@TGNUM+1),QNUMWORK+5
         HEX   DL@LOTRK,DASLOTRK,LEN=2,BYTE=C'-'
         HEX   DL@HITRK,DASUPTRK,LEN=2
         LH    R0,DASNOTGP              number of tracks per group
         CVD   R0,QNUMWORK
         OI    QNUMWORK+7,X'0F'
         MVC   DL@NOTGP,=X'202120'
         ED    DL@NOTGP-1(L'DL@NOTGP+1),QNUMWORK+6
         TM    DASFLAG,DASDRAIN
         BZ    *+10
         MVC   DL@STAT(8),=C'Draining'
         TM    DASFLAG,DASHALT
         BZ    *+10
         MVC   DL@STAT(7),=C'Halting'
         TM    DASFLAG,DASTART
         BZ    *+10
         MVC   DL@STAT(8),=C'Starting'
         TM    DASFLAG,DASEXSTS
         BNZ   *+10
         MVC   DL@STAT(8),=C'nonexist'
         TM    DASFLAG,DASACTIV
         BNO   *+10
         MVC   DL@STAT(8),=C'<Active>'
         TM    DASFLAG,DASAVAIL
         BNO   *+10
         MVC   DL@STAT(7),=C'*Avail*'
         HEX   DL@MASK,DASMASK,LEN=4
         BAL   R14,QADDLINE             display the line
         LA    R3,DASSIZ(,R3)           -> next DAS
         BCT   R4,DL$LOOP               display them all
         B     QSTOP                    all done
***********************************************************************
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
***********************************************************************
         LTORG ,
         SPACE 1
         DROPX R9,R3                    WORK, $SAVEBEG, $DAS
***********************************************************************
*                                                                     *
*        DESCRIBE ALL THE DSECTS NEEDED BY THIS MODULE                *
*                                                                     *
***********************************************************************
         QCOMMON
         ORG   QDMSG
DL@VOLID DS    CL6,C
DL@EXTNO DS    CL2,C
DL@FLAG  DS    CL2,C
DL@TGNUM DS    CL5,C
DL@LOTRK DS    CL4,C
DL@HITRK DS    CL4,C
DL@NOTGP DS    CL3,C
DL@STAT  DS    CL8,C
DL@MASK  DS    CL8,C
         SPACE 3
         Q$HCT
         Q$DAS
.DL99     ANOP
