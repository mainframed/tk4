         MACRO
&NFS     QITEM &OFF1,&DESC,&OFF2,&TYPE,&NAME,&LEN,&SKIP=,&PREFIX=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QITEM - FORMAT A DATA FIELD FOR OUTPUT.  INPUT MAY BE HEX, DEC,   *
.*          CHAR, DATE, or TIME.                                      *
.*                                                                    *
.*  UPDATES:                                                          *
.*     10Jan88  LDW  Eliminate LC macro usage; eliminate CAPS=        *
.*     22Jul87  EMS  Fix SKIP operand with non-numeric OFF2           *
.*      8Jul87  EMS  More reorganization, and function enhancement    *
.*                   Removed r7 requirement (set by reset operand)    *
.*                   Removed routine name restriction.                *
.*                   Clear r5 during RESET processing.                *
.*     22May87  EMS  Fix name field support                           *
.*                   Reorganized to use FMTSUBS routine, r7 must      *
.*                      point to ??#items table                       *
.*     29Nov86  LDW  Change to support DATEFMT subroutine             *
.*     29AUG86  LDW  SUPPORT 'ATTR' ROUTINE                           *
.*                   USE R5 FOR DYNAMICALLY COUNTING FIELDS           *
.*                   ADD 'ADDLINE' FUNCTION TO SET ATTR NUM AND LINK  *
.*                      TO QADDLINE                                   *
.*     11/24/83 LDW  DON'T GENERATE LEADING ZERO IN NAME OF CONSTANT  *
.*                      IF IT'S NUMBER IS > 99 (THIS WAS A BUG)       *
.*                   INCREASE SIZE OF QITEM#T ARRAY                   *
.*     01/17/83 LDW  CHANGE TO STORE HEADERS IN GLOBAL AREA FOR       *
.*                      LATER EXPANSION USING "LC"                    *
.*                   TRANSLATE CHARACTER FIELDS (R7 MUST -> TRTAB)    *
.*     10/25/82 LDW  CHANGE &PREFIX.WORK TO &PREFIX.@WORK             *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
.* NOTE: GENERATED CODE INTENTIONALLY SHIFTED RIGHT 1 COLUMN
         GBLA  &QITEM#I
         GBLC  &QITEM#P,&QITEM#T(200),&QITEMO1(200),&QITEMO2(200)
         GBLA  &QITEM#F(200),&QITEM#L(200),&QITEM#O(200)
         LCLA  &L,&I
         LCLA  &FUNC,&FLAGS
         LCLC  &O1,&O2
         LCLC  &C
         LCLC  &Z
.START   AIF   ('&OFF1' EQ 'GEN').GEN
         AIF   ('&OFF1' EQ 'ADDLINE').ADDLINE
         AIF   ('&OFF1' NE 'RESET').GO
         AIF   ('&PREFIX' NE '').RESET
         MNOTE 12,'RESET option requires &&PREFIX to be specified'
&QITEM#P SETC  '@@'                     set routine prefix name
         MEXIT
.RESET   ANOP  ,
&QITEM#I SETA  0
&QITEM#P SETC  '&PREFIX'                set routine prefix name
         MVC   QCITEMS,=A(&QITEM#P.#ITEMS)  set table addr
         SR    R5,R5                    clear attr byte counter
         MEXIT
.ADDLINE ANOP  ,
&NFS      STC   R5,QDATRNUM+1            NUMBER OF ATTR BYTES IN LINE
          SR    R5,R5                    CLEAR COUNTER FOR NEXT LINE
          BAL   R14,QADDLINE             DISPLAY LINE
         SPACE 2
         MEXIT
.GO      ANOP  ,
&QITEM#I SETA  &QITEM#I+1               -> ARRAY ELEMENT
&Z       SETC  '&QITEM#P.#&QITEM#I'     GET NAME OF SYMBOL
         AIF   (&QITEM#I GT 99).ZOK1    &Z NULL IF 3 DIGITS
&Z       SETC  '&QITEM#P.#0&QITEM#I'    1 LEADING ZERO IF 2 DIGITS
         AIF   (&QITEM#I GT 9).ZOK1     1 LEADING ZERO IF 2 DIGITS
&Z       SETC  '&QITEM#P.#00&QITEM#I'   2 LEADING ZEROES IF 2 DIGITS
.ZOK1    ANOP
&QITEM#T(&QITEM#I) SETC '&DESC'         set description
         AIF   ('&DESC' EQ '').NODESC
&C       SETC  '&OFF1'(1,1)
         AIF   (T'&C EQ 'N').DESCN
&O1      SETC  '&OFF1-QCOMMON'          set to offset
         AGO   .OFF2
.DESCN   ANOP  ,
&O1      SETC  '&QITEM#P.@LINE+&OFF1-QCOMMON' set to offset
         AGO   .OFF2
.NODESC  ANOP  ,
&O1      SETC  '0'                      if none, set to zero
&FLAGS   SETA  128+&FLAGS               indicate no offset 1
.OFF2    AIF   ('&OFF2' EQ '').NODATA
&C       SETC  '&OFF2'(1,1)
         AIF   (T'&C EQ 'N').OFF2N
&O2      SETC  '&OFF2-QCOMMON'          set to offset
         AGO   .SKIPT
.OFF2N   ANOP  ,
&O2      SETC  '&QITEM#P.@LINE+&OFF2-QCOMMON' set to offset
         AGO   .SKIPT
.NODATA  ANOP  ,
&O2      SETC  '0'
&FLAGS   SETA  64+&FLAGS                indicate no offset 2
.SKIPT   ANOP  ,
         AIF   (T'&SKIP EQ 'O').FUNC
&FLAGS   SETA  32+&FLAGS                indicate skip= present
.FUNC    ANOP  ,
&L       SETA  &LEN
         AIF   ('&TYPE' EQ 'CHAR').CHAR
         AIF   ('&TYPE' EQ 'HEX' ).HEX
         AIF   ('&TYPE' EQ 'TIME').TIME
         AIF   ('&TYPE' EQ 'DATE').DATE
         AIF   ('&TYPE' EQ 'DEC').DEC
         MNOTE 8,'&TYPE IS INVALID TYPE'
         AGO   .DONE
.CHAR    ANOP  ,
&FUNC    SETA  &FUNC+0                  char
         AGO   .CONST
.HEX     ANOP  ,
&FUNC    SETA  &FUNC+1                  hex
         AGO   .CONST
.TIME    ANOP  ,
&FUNC    SETA  &FUNC+2                  time
         AGO   .CONST
.DATE    ANOP  ,
&FUNC    SETA  &FUNC+3                  date
         AGO   .CONST
.DEC     ANOP  ,
&FUNC    SETA  &FUNC+4                  decimal
         AIF   (&L EQ 1).DECL
         AIF   (&L EQ 2).DECL
&L       SETA  3
.DECL    ANOP  ,
&L       SETA  &L-1
.CONST   ANOP  ,
&NFS      LA   R1,&NAME                 -> source
          BAL  R14,QITEM                invoke routine
          DC   Y(&QITEM#I)              Table entry for literal
&QITEM#F(&QITEM#I) SETA &FUNC
&QITEM#O(&QITEM#I) SETA &FLAGS
&QITEM#L(&QITEM#I) SETA &L
&QITEMO1(&QITEM#I) SETC '&O1'
&QITEMO2(&QITEM#I) SETC '&O2'
.TYPE2   AIF   (T'&SKIP EQ 'O').DONE
          B    &SKIP
.DONE    SPACE 1
         MEXIT
.GEN     ANOP
         AIF   ('&NFS' EQ '').GENHEAD
         MNOTE 8,'Name field invalid with GEN specification'
.GENHEAD ANOP  ,
&QITEM#P.#ITEMS DC  0H'0'               head of table
.GENLOOP ANOP  ,
&I       SETA  &I+1
&Z       SETC  ''                       NO LEADING ZEROES IF 3 DIGITS
         AIF   (&I GT 99).ZOK2          &Z NULL IF 3 DIGITS
&Z       SETC  '0'                      1 LEADING ZERO IF 2 DIGITS
         AIF   (&I GT 9).ZOK2           1 LEADING ZERO IF 2 DIGITS
&Z       SETC  '00'                     2 LEADING ZEROES IF 2 DIGITS
.ZOK2    ANOP
&QITEM#P.#&Z&I DC  AL1(&QITEM#P.#&Z&I.L) len of item
         DC    AL1(&QITEM#F(&I),&QITEM#O(&I),&QITEM#L(&I))
         DC    AL2(&QITEMO1(&I),&QITEMO2(&I))
         AIF   (K'&QITEM#T(&I) EQ 0).GENLEN
         DC    C&QITEM#T(&I)            def constant
.GENLEN  ANOP  ,
&QITEM#P.#&Z&I.L EQU *-&QITEM#P.#&Z&I
         AIF   (&I LT &QITEM#I).GENLOOP
         MEND
