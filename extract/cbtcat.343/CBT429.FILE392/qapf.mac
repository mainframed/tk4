         MACRO
&NFS     QAPF  &FUNC
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QAPF - INVOKE INSTALLATION SVC TO SET OR RESET APF AUTHORIZATION  *
.*                                                                    *
.*  NOTE - THE EXISTANCE OF THIS SVC IS INDICATED BY THE SETTING OF   *
.*         THE GBLA "&@@SCSVC".  THIS MACRO (QAPF) MAY NEED TO BE     *
.*         EDITTED BY EACH INSTALLATION USING THE APF SVC.            *
.*  NOTE - THE ACTUAL SVC USED IS INTENDED TO BE THE SVC MENTIONED IN *
.*         THE SPF INSTALLATION MANUAL, WHICH SETS OR RESETS JSCBAUTH *
.*         ON REQUEST IF THE CALLER IS IN LPA/CSA.  THIS MEANS THAT   *
.*         IF QUEUE IS TO PASS THE VALIDITY CHECK, IT MUST BE IN      *
.*         LPA OR MLPA.  YOU COULD ALSO MODIFY THE SVC TO WORK IF     *
.*         THE CALLER IS A SYSTEMS PROGRAMMER, EVEN IF NOT IN LPA.    *
.*  NOTE - INSTALLATION OF THE ABOVE MENTIONED SVC RESULTS IN A       *
.*         POSSIBLE INTEGRITY EXPOSURE.  IF YOU EXPECT ME TO DESCRIBE *
.*         IT HERE, YOU'RE CRAZY.                                     *
.*                                                                    *
.*  UPDATES:                                                          *
.*     03/30/86 LDW  IF &@@SCSVC IS ZERO, GENERATE MNOTE              *
.*     12/20/84 LDW  USE &@@SCSVC FOR SPFCOPY SVC NUMBER              *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         GBLA  &@@SCSVC
         AIF   (&@@SCSVC NE 0).OK
         MNOTE 12,'*** LOGIC ERROR IN CONDITIONAL ASSEMBLY -- QAPF MACR$
               O INVOKED WITH &&@@SCSVC=0 ***'
.OOPS    DC    2F'0'                    PATCH SPACE
         MEXIT
.OK      AIF   ('&FUNC' EQ 'SET').SET
         AIF   ('&FUNC' EQ 'RESET').RESET
         MNOTE 12,'&FUNC INVALID'
         AGO   .OOPS
.SET     ANOP
&NFS     LA    R0,1                     INDICATE "SET"
         AGO   .SVC
.RESET   ANOP
&NFS     LA    R0,0                     INDICATE "RESET"
.*SVC    SVC   243
.SVC     SVC   &@@SCSVC
         MEND
