         MACRO
&NFS     QCBFE &NAME,&TYPE,&LEN,&MIN=0,&MAX=999,&L=,&OFFSET=0
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QCBFE - Create a table entry for CBFMT routine.                   *
.*                                                                    *
.*  Updates:                                                          *
.*     01Dec90  LDW  Written                                          *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         GBLA  &QCBFEL,&QCBFEOF,&QLEVEL
         LCLA  &XDIGIT,&XREMAIN,&XDIVISR
         LCLC  &FLAG,&COMMENT,&HEXOFF,&HEXCHAR
         AIF   (&QLEVEL LT &MIN).MEND
         AIF   (&QLEVEL GT &MAX).MEND
&HEXCHAR SETC  '0123456789ABCDEF'
         AIF   ('&L' EQ '').NOL
&QCBFEL  SETA  &L
&QCBFEOF SETA  &OFFSET
&NFS     DC    Y(&L,&OFFSET)            name length, initial offset
.NOL     ANOP
         AIF   ('&NAME&TYPE&LEN' EQ '').MEND
         AIF   ('&TYPE' EQ 'HEX' ).HEX
         AIF   ('&TYPE' EQ 'CHAR').CHAR
         MNOTE 8,'Unknown type -- &TYPE'
.HEX     ANOP
&FLAG    SETC  '000'
         AGO   .TYPESET
.CHAR    ANOP
&FLAG    SETC  '128'
.TYPESET ANOP
&HEXOFF  SETC  ''
&XREMAIN SETA  &QCBFEOF                 offset to convert
&XDIVISR SETA  4096                     first divisor
.HEXLOOP ANOP
&XDIGIT  SETA  &XREMAIN/&XDIVISR
.******  MNOTE *,'XREMAIN=&XREMAIN XDIVISR=&XDIVISR XDIGIT=&XDIGIT'
&XREMAIN SETA  &XREMAIN-&XDIGIT*&XDIVISR
&HEXOFF  SETC  '&HEXOFF'.'&HEXCHAR'(&XDIGIT+1,1)
&XDIVISR SETA  &XDIVISR/16
         AIF   (&XDIVISR NE 0).HEXLOOP
&COMMENT SETC  '    offset = &QCBFEOF (&HEXOFF)'
&NFS     DC    AL1(&LEN,&FLAG),CL&QCBFEL&NAME&COMMENT
&QCBFEOF SETA  &QCBFEOF+&LEN
.MEND    MEND
