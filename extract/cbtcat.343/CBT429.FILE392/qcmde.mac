         MACRO
&ROUTINE QCMDE &CODE,&POSMAX,&CMD,&F=,&KEYS=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QCMDE - GENERATE PARSE TABLE ENTRY FOR A SUBCOMMAND AND ALL OF    *
.*          ITS ALIASES                                               *
.*                                                                    *
.*  UPDATES:                                                          *
.*     12/14/82 LDW  ADD SUPPORT FOR KEYWORD/RESERVED WORD TABLE      *
.*                   ADD SUPPORT FOR MAXIMUM NUMBER OF POSITIONALS    *
.*                   MOVE ROUTINE NAME FROM THIRD OPERAND TO NFS      *
.*                   MOVE &CODE FROM SECOND OPERAND TO FIRST          *
.*                   MAKE &XCODE A SETC (INSTEAD OF SETA)             *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         LCLA  &I,&NUM
         LCLC  &FL,&XCODE
&XCODE   SETC  '001'                    ASSUME CODE NOT SET
&FL      SETC  '000'                    ASSUME FLAGS NOT SET
&NUM     SETA  N'&CMD                   NUMBER OF SUBCMD NAMES
***********************************************************************
         AIF   ('&CODE' EQ '').CODE0    SKIP IF NO CODE GIVEN
&XCODE   SETC  '&CODE'
.CODE0   AIF   ('&F' EQ '').FLAG0       SKIP IF NO FLAGS GIVEN
&FL      SETC  '&F'
.FLAG0   AIF   ('&KEYS' EQ '').NOKEYS
         DC    V(&ROUTINE,&KEYS)        -> ROUTINE; -> KEYWORD TABLE
         AGO   .OKKEYS
.NOKEYS  DC    V(&ROUTINE),A(0)         -> ROUTINE; NO KEYWORD TABLE
.OKKEYS  AIF   ('&POSMAX' EQ 'N').NOCHECK
         DC    AL1(&FL,&POSMAX)         FLAGS; MAX POSIT OPERANDS
         AGO   .POSOK
.NOCHECK DC    AL1(&FL),X'FF'           FLAGS; DON'T VALCK POSITIONALS
.POSOK   DC    AL1(&XCODE,&NUM)         SUBCMD FCTN CODE; # OF NAMES
.LOOP    ANOP
&I       SETA  &I+1
         DC    CL4'&CMD(&I)'            A SUBCOMMAND NAME
         AIF   (&I LT N'&CMD).LOOP      DO ALL SUBCMD NAMES
***********************************************************************
         SPACE 1
         MEND
