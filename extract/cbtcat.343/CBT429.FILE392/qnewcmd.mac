         MACRO
&NAME    QNEWCMD  &P,&MSG=
.*
.*  Updates:
.*    16Jul91  GLA  Add message support for command reissue.  B-16JUL91
.*    31Jan91  LDW  Copy from GLA version, rename from QPRIMCMD to
.*                     QNEWCMD because LINE commands are also handled,
.*                     remove update flags as this is now part of
.*                     the base
.*                  Improve MNOTE text
.*
.*
.*   This macro effects an interrupt to the normal processing
.*   of the 'QUEUE' command by branching to the QQINTER routine
.*   in the main QUEUE module.  This is done to request that
.*   certain types of commands be handled.
.*
.*   Current operand values are:
.*        PRIMARY    indicates to process new primary command
.*        LINE       indicates to process new line command
.*        STACK      indicates to reissue the cmd on the stack
.*        ????       is address of command to run (unused)
.*
.*
         AIF   ('&MSG' EQ '').CLR0                            B-16JUL91
         LA    R0,&MSG                  point at message      B-16JUL91
         AGO   .GR0                                           B-16JUL91
.CLR0    SR    R0,R0                    indicate no msg       B-16JUL91
.GR0     ANOP                                                 B-16JUL91
         AIF   ('&P' EQ '').QPER1
         AIF   ('&P' NE 'PRIMARY').NOTP
&NAME    SR    R15,R15                  show entry from display
         AGO   .DONE
.NOTP    AIF   ('&P' NE 'LINE').SOME
&NAME    LA    R15,4                    go run line command
         AGO   .DONE
.SOME    AIF   ('&P' NE 'STACK').OTHER
&NAME    LA    R15,8                    go rerun stack command
         AGO   .DONE
.OTHER   ANOP                           HANDLE command
         LA    R1,&P                    point to command
         LA    R15,12                   show forced command
.DONE    L     R13,QFRSTSA              get orig save area
         L     R14,12(,R13)             restore registers
         LM    R2,R12,12+16(R13)        reload rest
         BR    R10                      -> interrupt handler in
*                                          QUEUE MAINLINE
         MEXIT
.QPER1   MNOTE 8,'QNEWCMD requires an operand'
&NAME    DS    0H                       define label
         MEND
