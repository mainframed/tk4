         MACRO
&NFS     QNEXT &REG,&LOC,&NONE=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QNEXT - PICK UP JQE OR JOE OFFSET.  THIS MACRO WAS CREATED TO     *
.*          MINIMIZE THE AMOUNT OF CONDITIONAL ASSEMBLY IN OPEN CODE  *
.*          REQUIRED TO IMPLEMENT SP/JES2 SUPPORT.                    *
.*                                                                    *
.*  NOTE - GENERATED CODE INTENTIONALLY SHIFTED RIGHT 1 COLUMN        *
.*                                                                    *
.*  UPDATES:                                                          *
.*     01/04/83 LDW  MACRO CREATED                                    *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         GBLA  &QLEVEL                  LEVEL OF CHECKPOINT SUPPORTED
         LCLC  &H,&MASK
&MASK    SETC  '00FFFFFF'               ASSUME SP
         AIF   (&QLEVEL GE 4).SP        SKIP IF SP (&H WILL BE NULL)
&MASK    SETC  '0000FFFF'               "AND" MASK FOR NON-SP
&H       SETC  'H'                      AND USE "LH" INSTEAD OF "L"
.SP      AIF   ('&LOC' EQ '').NOLOC     SKIP IF ALREADY IN REGISTER
&NFS      L&H   &REG,&LOC
          N     &REG,=X'&MASK'
         AGO   .CHKSLA
.NOLOC   ANOP
&NFS      N     &REG,=X'&MASK'
.CHKSLA  AIF   (&QLEVEL GE 4).CHKNONE
          SLA   &REG,2                   CONVERT OFFSET/4 INTO OFFSET
.CHKNONE AIF   ('&NONE' EQ '').EXIT
          BZ    &NONE
.EXIT    MEND
