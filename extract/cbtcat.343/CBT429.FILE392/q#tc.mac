         TITLE '--- QUEUE--TSOCMD -- ATTACH A TSO COMMAND ---'
*---------------------------------------------------------------------*
*                                                                     *
*   TSOCMD - Execute a TSO command or CLIST without leaving QUEUE     *
*                                                                     *
*   Note - This subcommand may be executed in a QUEUE profile         *
*                                                                     *
*   Updates:                                                          *
*      29Jan91  LDW  Move QCLRSCR from QCOMMON to here; rename it to  *
*                       CLEARSCR                                      *
*                    Move conditional assembly to insure CLEARSCR is  *
*                       assembled                                     *
*      05/01/85 LDW  REMOVE INITIAL=YES TO MAKE SESSION MANAGER HAPPY *
*      06/12/84 LDW  PUT MORE INFO IN 'DONE' MSG                      *
*      04/16/84 LDW  DON'T STACK THIS SUBCOMMAND                      *
*      02/28/84 SDM  SCAN TRAILING BLANKS OFF OF COMMAND              *
*      02/05/84 LDW  MAKE TCAM FAKE SIMATTN 'ASIS'                    *
*      01/20/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*                    IF TCAM, WAIT FOR RESPONSE, TO AVOID OVERWRITING *
*                       LINE OUTPUT                                   *
*      07/25/83 LDW  ADD INITIAL=YES TO STFSMODE, TO ACT LIKE ISPF    *
*      01/14/83 LDW  SET QTSOCMD FOR REPROMPTING                      *
*      01/10/83 LDW  PUT BACK QTILT NOW THAT IT DOESN'T CLEAR SCREEN  *
*      01/06/83 LDW  CHANGE ERROR MESSAGE TEXT                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
TSOCMD   QSTART  Q88      ,STACK=X'B0'
          AIF   (NOT &@@TMPSV).TC01     SKIP IF DON'T HAVE TMPSRV
         USING WORK,R13
         SPACE 1
         MVC   QDPROMPT,QDREPLY         SET WHAT TO REPROMPT WITH
         LH    R4,QOFF1                 OFFSET OF FIRST BYTE OF PARM
         LTR   R4,R4                    DID PARSE FIND ANYTHING?
         BNP   TC$TILT                  NO - ABORT
         MVC   QTSOCMD,QDREPLY          SAVE FOR REPROMPTING
         BAL   R8,TC$CLEAR              ERASE THE SCREEN
         LA    R2,QDREPLY               -> REPLY
         AR    R2,R4                    -> FIRST BYTE OF PARM
         LA    R3,QDREPLY+QDREPLYL-1    -> LAST CHARACTER
         CLI   0(R3),C' '               TRAILING BLANK?
         BNE   *+8                      NO, FOUND END OF COMMAND
         BCT   R3,*-8                   YES, DECREMENT AND LOOP
         LA    R3,1(,R3)                -> LAST CHARACTER + 1
         SR    R3,R2                    LENGTH OF OPERAND
         SPACE 1
         TMPMAC  CMD=((R2),(R3)),MF=(E,TCTMPMAC)  DO IT
         SPACE 1
         STFSMODE  ON                   TELL VTAM WE'RE BACK
         SPACE 1
         TM    QTERMFLG,QTFVTAM         VTAM TERMINAL?
         BO    TC$VTAM                  YES - SKIP
         LA    R1,=C'***'               FAKE THE SIMATTN
         TPUT  (1),3,ASIS               PUT IT OUT
         LA    R1,TCTMPMAC              USE ANY OLD TGET AREA
         LA    R0,1                     LENGTH IS IRRELEVANT
         ICM   R1,B'1000',=X'80'        INDICATE TGET
         TGET  (1),(0),R                WAIT FOR ENTER
         TCLEARQ  INPUT                 AND FLUSH EXTRA INPUT
         SPACE 2
TC$VTAM  MVC   QERRMSG(43),=C'=== TSO COMMAND COMPLETE -- RC=XXXXXXXX =$
               =='
         L     R1,DAPLECT               -> ECT
         HEX   QERRMSG+31,(0,R1),LEN=4,HEXTAB=QHEXTAB  ECTRCDF
         QTILT *
         SPACE 2
TC$TILT  QTILT '*** TSO COMMAND REQUIRED AS OPERAND ***',              $
               OPTIONS=REPROMPT
         SPACE 2
.TC01     ANOP
***********************************************************************
*                                                                     *
*   CLEAR THE SCREEN (TCAM), OR LINES 3 TO END OF SCREEN (VTAM)       *
*                                                                     *
***********************************************************************
CLEARSCR QSTART  TYPE=ENTRY
         BAL   R8,TC$CLEAR              clear the screen
         B     QSTOP                    all done
         SPACE 3
TC$CLEAR TM    QTERMFLG,QTFTTY          FULLSCREEN TERMINAL?
         BOR   R8                       NO - JUST RETURN
         LA    R1,TC#CLEAR              -> MESSAGE
         LA    R0,TC#CLRL               LENGTH
         MVC   QD3270OP,QDERASE         SET ERASE WRITE 3270 OPCODE
         TM    QTERMFLG,QTFVTAM         VTAM TERMINAL?
         BO    TC$CLR2                  YES - DO IT THAT WAY
         ICM   R1,B'1000',=X'03'        SET FULLSCR FLAG
         TPUT  (1),(0),R                CLEAR BOTTOM OF SCREEN
         BR    R8                       RETURN TO CALLER
         SPACE 2
TC$CLR2  ICM   R1,B'1000',=X'0B'        SET FULLSCR & HOLD FLAGS
         TPUT  (1),(0),R                CLEAR BOTTOM OF SCREEN
         SPACE 1
         STLINENO  LINE=3,MODE=OFF      TELL VTAM WE'RE TAKING A BREAK
         SPACE 1
         TCLEARQ  INPUT                 THROW AWAY SPURIOUS RESHOW CODE
         SPACE 1
         BR    R8                       RETURN TO CALLER
         SPACE 1
*  SPF TCAM SEQUENCE TO CLEAR FROM LINE 3 TO END OF SCREEN
TC#CLEAR DC    X'C1'                    WCC: RESET MDT
         DC    X'115D7E'                SPF-TCAM PREFIX -> LAST FULLSCR
*                                       (INCLUDES NEXT SBA)
         DC    X'11C260'                NEXT OUTPUT TO 3RD SCREEN LINE
         DC    X'3C404000'              RA TO BEGINNING OF SCREEN
         DC    X'11C260'                BACK TO 3RD SCREEN LINE
         DC    X'13'                    DROP ANCHOR HERE
TC#CLRL  EQU   *-TC#CLEAR               LENGTH OF THIS ROT
         SPACE 3
         DROP  R13
         SPACE 3
         LTORG ,
         SPACE 3
         QCOMMON
         SPACE 3
WORK     DSECT
         ORG   WORK+72                  SKIP OVER SAVE AREA
TCTMPMAC TMPMAC  MF=L
