         TITLE '--- QUEUE--JESNEWS -- DISPLAY JESNEWS ---'
*---------------------------------------------------------------------*
*                                                                     *
*   JESNEWS - DISPLAY THE CURRENT JESNEWS                             *
*                                                                     *
*   Updates:                                                          *
*       8May87  EMS  Update for 1.3.4 level                           *
*      25Jan87  LDW  Pass READSPC JQE addr = 0 in case of CB ID error *
*      04/10/84 LDW  TEMPORARILY DISABLE FOR SP133                    *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*                    FIX BUG IN HEADER LINE                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
JESNEWS  QSTART  Q82,STACK=X'10'
          AIF   (&QLEVEL LT 5).JN01
          AIF   (&QLEVEL GE 6).JN02
         QTILT '*** JESNEWS SUBCOMMAND NOT YET AVAILABLE FOR THIS VERSI$
               ON OF JES2 ***'
*  $NEWSTTR???
          AGO   .JN04
.JN01     ANOP
         QCALL CKPT                     READ CHECKPOINT DATA
         L     R2,QCJQTL                -> IOAREA FOR CKPT HDR RECORD
         USING $SAVEBEG,R2
         OC    $NEWSIOT,$NEWSIOT        ANY JESNEWS?
         BZ    JN$OOPS                  NO - SAY SORRY
         XC    QPOFFSET,QPOFFSET        CLEAR DISPLAY OFFSET
         MVI   QCODE,0                  TELL LISTDS "INITIAL POSIT'ING"
         L     R3,QCIOTA                -> IOT AREA
         MVC   QCTRAK,$NEWSIOT          SET MTTR TO READ
         DROP  R2
         LR    R1,R3                    GET IOT AREA ADDR FOR READSPC
         MVC   QCCBID,=C'IOT '          SAY WHAT WE WANT
         SR    R0,R0                    NO JQE ADDR   %%%%%%
         QCALL READSPC                  READ THE JESNEWS IOT
         MVC   QJOBNAME,=C'$JESNEWS'    fake for listds
         AGO   .JN03
.JN02     ANOP ,
         MVC   QPARM1,=C'$JESNEWS'      set jobname
         MVC   QLNG1,=H'8'              len of jobname
         QCALL FINDJOB                  find it
.JN03     ANOP ,
         MVC   QPJOBID(6),=C'$$NEWS'    ** FUDGE **
         MVC   QDHJOBID(L'QDHJOBID+1),=C'JESNEWS -----------' SET JOBID
         MVC   QDHDSID,QBLANK           KILL DSID
         QCALL LISTDS                   DISPLAY IT
         B     QSTOP                    LEAVE
         SPACE 1
JN$OOPS  QTILT '*** NO JESNEWS AVAILABLE ***'
         SPACE 2
.JN04     ANOP ,
         LTORG ,
         SPACE 3
         QCOMMON
         SPACE 2
          AIF   (&QLEVEL GE 5).JN05
         Q$HCT
.JN05     ANOP ,
