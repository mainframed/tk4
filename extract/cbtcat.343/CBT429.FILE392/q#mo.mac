         TITLE '--- QUEUE--MODE -- DISPLAY/CHANGE Q MODES ---'
*---------------------------------------------------------------------*
*                                                                     *
*   MODE - Display and change various QUEUE parameters                *
*                                                                     *
*   Entry - QKEYWORDS = mode change (none = display only)             *
*                                                                     *
*   Note - This subcommand may be executed in a QUEUE profile         *
*                                                                     *
*   Updates:                                                          *
*      20Dec91  LDW  Add support for RSCA                             *
*      29Jan91  LDW  Use QVPRNTWA to access most PRINT fields         *
*                    Change BAL QCLRSCR to QCALL CLEARSCR             *
*      31May89  LDW  Change CKPT/NOCKPT to APPLCOPY/NOAPPCPY          *
*                    Fix typo in LINECT= display                      *
*       5May89  EMS  Add support in core CKPT selection               *
*      20Feb88  LDW  Add support for SYSLOG field selection           *
*      21May87  EMS  Update for page/line mode                        *
*                    Add support for LINECT operand                   *
*      21Mar87  LDW  Fix for DSID = 0 (from RSCS)                     *
*      29Nov86  LDW  Add support for 'Jdate/Gdate'                    *
*                    Eliminate LC macro, use real lower case          *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      08/30/84 LDW  ADD SUPPORT FOR 'STACK' OPERAND                  *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*                    ADD SUPPORT FOR 'COLS' SUBCOMMAND                *
*      03/08/83 LDW  FIX BUG IN NOT3270                               *
*      03/02/83 LDW  ADD PFK24                                        *
*                    CHANGE 'QTILT *' TO USE QERRMSG                  *
*                    CHANGE HDR AND SOME DATA TO LOWER CASE           *
*      02/03/83 LDW  ADD YES3270 AND NOT3270                          *
*      01/07/83 LDW  STATUS:  ALSO SET QSTKEY                         *
*      01/05/83 LDW  USE QKEYWORDS INSTEAD OF QPARM1                  *
*                    ADD STATUS, FORM                                 *
*      12/22/82 LDW  DELETE CODE WHICH IS NOW IN NEW RTN "QINNARDS"   *
*                    ADD FCB AND UCS PRINT PARAMETERS                 *
*                    FIX BUG IN PROCESSING "DEST"                     *
*      12/20/82 LDW  CHANGE QTILT REPRMPT=YES TO OPTIONS=REPROMPT     *
*      10/09/82 LDW  FIX BUGS CAUSED BY NOT PICKING UP PSCB ADDR      *
*                       EARLY ENOUGH                                  *
*      09/28/82 EMS  MAKE DEST='CENTRAL' USE BLANKS                   *
*                    ADD 'DESTR' TO SET ROUTE DEFAULT DEST            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
MODE     QSTART  Q85,STACK=X'B0'
         USNGX WORK,R13
         L     R9,QVPRNTWA              -> PRINT workarea
         USNGX QPRNTWA,R9
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
         MVC   QDPROMPT,QDREPLY         SET UP WHAT TO REPROMPT
         CLI   QCODE,4                  IS THIS 'COLS'?
         BNE   MODEMODE                 NO
         OI    QFLAG2,Q2COLIND          ASSUME 'ON'
         CLC   =C'OFF',QKEYWORD         IS IT?
         BNE   *+8                      YES - LEAVE IT ON
         XI    QFLAG2,Q2COLIND          NO - TURN IT OFF
*%%      CLC   QPDSID,=H'0'             IS THERE A VALID DATASET?
         TM    QFLAG2,Q2VALIDS          IS THERE A VALID DATASET?
         BNE   MODE$RP                  YES - GO REPOSITION
         QTILT '=== OK ==='             PUNT
MODE$RP  MVI   QCODE,60                 CODE FOR 'REDISP'
         QCALL REPOS                    DO IT
         B     QSTOP                    LEAVE
         SPACE 3
MODEMODE CLI   QKEYWORD,C' '            DISPLAY REQUEST?
         BE    MODESTAR                 YES
         TM    QFLAG1,Q1PROFOK+Q1IMMED  IMMED COMMAND AFTER INIT PROF?
         BO    MODESTOP                 YES - CONVERT TO NON-IMMED
         SPACE 1
         LA    R5,QKEYWORD              -> FIRST KEYWORD OPERAND
         LA    R6,QKEYLAST              -> PAST LAST ONE
         SPACE 2
MO$LOOP1 LA    R2,MO#KEYS               -> KEYWORDS TABLE
         LR    R3,R2                    COPY
         SPACE 1
MO$LOOP2 CLC   0(8,R2),0(R5)            IS THIS IT?
         BE    MO$FOUND                 YES
         LA    R2,8(,R2)                -> NEXT TABLE ENTRY
         CLI   0(R2),X'FF'              END OF TABLE?
         BNE   MO$LOOP2                 NO - TRY NEXT
         SPACE 1
MODETILT MVC   QERRMSG(25),=C'*** UNKNOWN MODE CHANGE -'
         MVC   QERRMSG+26(8),0(R5)      MOVE IN THE KEYWORD
         MVC   QERRMSG+35(3),=C'***'
         QTILT *,OPTIONS=REPROMPT
         SPACE 2
MO$NEXT  LA    R5,16(,R5)               -> NEXT QKEYWORD
         CR    R5,R6                    PAST END?
         BNL   MODE$SET                 YES - GO DISPLAY RESULTS
         CLI   0(R5),C' '               ANYTHING HERE?
         BE    MODE$SET                 NO - GO DISPLAY RESULTS
         B     MO$LOOP1                 SEE WHAT THIS ONE IS
         SPACE 3
MO$FOUND LR    R15,R2                   COPY TABLE ENTRY ADDRESS
         SR    R15,R3                   COMPUTE OFFSET INTO KEYWORD TBL
         SRL   R15,1                    KEYWORDS ARE 8 BYTES, BR TABLE
*                                       ENTRIES ARE 4 BYTES
         B     *+4(R15)                 SELECT CORRECT ROUTINE
*---  MUST HAVE A ONE-FOR-ONE CORRESPONDENCE BETWEEN TABLE BELOW AND
*---  MO#KEYS
         B     MO$SHWCC                 SHOWCC
         B     MO$NSHCC                 NOSHOWCC
         B     MO$PAGE                  PAGE
         B     MO$LINE                  LINE
         B     MO$FANUM                 FANUM
         B     MO$NFANM                 NOFANUM
         B     MO$BEEP                  BEEP
         B     MO$NBEEP                 NOBEEP
         B     MO$PFK12                 PFK12
         B     MO$PFK24                 PFK24
         B     MO$JDATE                 JDATE
         B     MO$GDATE                 GDATE
         B     MO$VFY                   VERIFY
         B     MO$NOVFY                 NOVERIFY
         B     MO$STACK                 STACK
         B     MO$NSTCK                 NOSTACK
         B     MO$RSCA                  RSCA
         B     MO$NRSCA                 NORSCA
         B     MO$CLASS                 CLASS=
         B     MO$CLASS                 CLS=
         B     MO$UCS                   UCS=
         B     MO$FCB                   FCB=
         B     MO$FORM                  FORM=
         B     MO$DEST                  DEST=
         B     MO$DESTR                 DESTR=
         B     MO$STAT                  STATUS=
         B     MO$Y3270                 YES3270
         B     MO$N3270                 NOT3270
         B     MO$COLS                  COLS
         B     MO$NCOLS                 NOCOLS
         B     MO$LINCT                 linect=
         B     MO$SLOFF                 sloff
         B     MO$SLIND                 slind
         B     MO$SLRTC                 slrtc
         B     MO$SLSID                 slsid
         B     MO$SLDAT                 sldat
         B     MO$SLTIM                 sltim
         B     MO$SLJID                 sljid
         B     MO$SLRFL                 slrfl
          AIF  (&QLEVEL LT 8).MO01
         B     MO$APCY                  ckpt in core
         B     MO$NAPCY                 NOckpt in core
.MO01     ANOP ,
         SPACE 3
MO$SHWCC OI    QMODE1,QM1LCC            REQUEST DISPLAY CARRIAGE CTL
         B     MO$NEXT
         SPACE 1
MO$NSHCC NI    QMODE1,255-QM1LCC        TURN OFF FLAG
         B     MO$NEXT
         SPACE 1
MO$PAGE  TM    QMODE1,QM1PAGE           ALREADY SET?
         BO    MO$NEXT                  YES - SKIP
         OI    QMODE1,QM1PAGE           NO - SET IT
*%%      XC    QPDSID,QPDSID            AND NULLIFY RE-ENTRY TO LISTDS
*ems     NI    QFLAG2,255-Q2VALIDS      NULLIFY VALIDITY FOR LISTDS
         B     MO$NEXT
         SPACE 1
MO$LINE  TM    QMODE1,QM1PAGE           ALREADY NOT SET?
         BNO   MO$NEXT                  YES - SKIP
         NI    QMODE1,255-QM1PAGE       NO - RESET IT
*%%      XC    QPDSID,QPDSID            AND NULLIFY RE-ENTRY TO LISTDS
*ems     NI    QFLAG2,255-Q2VALIDS      NULLIFY VALIDITY FOR LISTDS
         B     MO$NEXT
         SPACE 1
MO$FANUM OI    QMODE1,QM1FANUM          REQUEST REC NUMBERS ON FINDALL
         B     MO$NEXT
         SPACE 1
MO$NFANM NI    QMODE1,255-QM1FANUM      TURN OFF FLAG
         B     MO$NEXT
         SPACE 1
MO$BEEP  OI    QMODE1,QM1BEEP           REQUEST AUDIBLE ALARM ON ERRORS
         B     MO$NEXT
         SPACE 1
MO$NBEEP NI    QMODE1,255-QM1BEEP       TURN OFF FLAG
         B     MO$NEXT
         SPACE 1
MO$PFK12 NI    QMODE1,255-QM1PFK24      MAP PF13-24 TO PF1-12
         B     MO$NEXT
         SPACE 1
MO$PFK24 OI    QMODE1,QM1PFK24          LET PF13-24 BE DIFFERENT
         B     MO$NEXT
         SPACE 1
MO$JDATE OI    QMODE1,QM1JDATE          DISPLAY DATES AS JULIAN
         B     MO$NEXT
         SPACE 1
MO$GDATE NI    QMODE1,255-QM1JDATE      DISPLAY DATES AS GREGORIAN
         B     MO$NEXT
         SPACE 1
MO$VFY   NI    QMODE1,255-QM1NOVFY      TURN OFF "NOVERIFY"
         B     MO$NEXT
         SPACE 1
MO$NOVFY OI    QMODE1,QM1NOVFY          TURN ON FLAG
         B     MO$NEXT
         SPACE 1
MO$STACK OI    QMODE1,QM1STACK          TURN ON FLAG
         B     MO$NEXT
         SPACE 1
MO$NSTCK NI    QMODE1,255-QM1STACK      TURN OFF "STACK"
         B     MO$NEXT
         SPACE 1
MO$RSCA  OI    QMODE2,QM2RSCA           TURN ON FLAG
         B     MO$NEXT
         SPACE 1
MO$NRSCA NI    QMODE2,255-QM2RSCA       TURN OFF "RSCA"
         B     MO$NEXT
         SPACE 1
MO$CLASS MVC   QPCLASS,8(R5)            SAVE REQUESTED PRINT CLASS
         B     MO$NEXT
         SPACE 1
MO$UCS   MVC   QPUCS,8(R5)              SAVE REQUESTED PRINT UCS
         B     MO$NEXT
         SPACE 1
MO$FCB   MVC   QPFCB,8(R5)              SAVE REQUESTED PRINT FCB
         B     MO$NEXT
         SPACE 1
MO$FORM  MVC   QPFORM,8(R5)             SAVE REQUESTED PRINT FORM
         B     MO$NEXT
         SPACE 1
MO$DEST  MVC   QPDEST,8(R5)             SAVE REQUESTED PRINT DEST
         CLC   QPDEST,=CL8'CENTRAL'     USER WISH CENTRAL DEST?
         BNE   MO$NEXT                  NO - CONTINUE
         MVC   QPDEST,QBLANK            YES - USE BLANKS INSTEAD
         B     MO$NEXT
         SPACE 1
MO$DESTR MVC   QDEST,8(R5)              SAVE REQUESTED ROUTE DEST
         CLC   QDEST,=CL8'CENTRAL'      USER WISH CENTRAL DEST?
         BNE   MO$NEXT                  NO - CONTINUE
         MVC   QDEST,QBLANK             YES - USE BLANKS INSTEAD
         B     MO$NEXT
         SPACE 1
MO$STAT  MVC   QSTDEFKY,8(R5)           MOVE IN NEW DEFAULT SEARCH KEY
         CLI   8(R5),C' '               ANY?
         BNE   *+10                     YES - OK
         MVC   QSTDEFKY(7),QLOGON       NO - RESET IT TSO LOGON ID
         MVC   QSTKEY,QSTDEFKY          AND SET CURRENT SEARCH KEY
         B     MO$NEXT
         SPACE 1
MO$Y3270 STLINENO  LINE=1,MODE=ON       TELL VTAM WE'RE BACK
         SPACE 1
         NI    QTERMFLG,255-QTFTTY      RESET TTY FLAG
         B     MO$NEXT
         SPACE 1
MO$N3270 QCALL CLEARSCR                 ERASE THE SCREEN
         OI    QTERMFLG,QTFTTY          SET TTY FLAG
         B     MO$NEXT
         SPACE 3
MO$COLS  OI    QFLAG2,Q2COLIND          TURN ON 'COLS'
         B     MO$NEXT
         SPACE 3
MO$NCOLS NI    QFLAG2,255-Q2COLIND      TURN OFF 'COLS'
         B     MO$NEXT
         SPACE 3
MO$LINCT XR    R1,R1                    clear counter
         LA    R0,3                     max digits
         LA    R14,8(,R5)               -> data
MO$LINLP CLI   0(R14),C' '              end of data?
         BE    MO$LINSV                 yes, store ans
         IC    R15,0(,R14)              get char
         N     R15,=A(X'0F')            -> number
         MH    R1,=H'10'                shift
         AR    R1,R15                        add
         LA    R14,1(,R14)              -> next char
         BCT   R0,MO$LINLP              get it
MO$LINSV CH    R1,=H'10'                >= 10?
         BL    MO$LINIV                 no, invalid
         CH    R1,=H'255'               > 255?
         BH    MO$LINIV                 yes, invalid
         STC   R1,QMLINECT              save final value
         B     MO$NEXT
MO$LINIV QTILT '*** invalid linect range (10-255) are valid ***'
         SPACE 3
MO$SLOFF MVI   QSLFLDS,0                reset all flags
         B     MO$NEXT
         SPACE 1
MO$SLIND OI    QSLFLDS,QSLFLIND         request indicators
         B     MO$NEXT
         SPACE 1
MO$SLRTC OI    QSLFLDS,QSLFLRTC         request route codes
         B     MO$NEXT
         SPACE 1
MO$SLSID OI    QSLFLDS,QSLFLSID         request system id
         B     MO$NEXT
         SPACE 1
MO$SLDAT OI    QSLFLDS,QSLFLDAT         request date
         B     MO$NEXT
         SPACE 1
MO$SLTIM OI    QSLFLDS,QSLFLTIM         request time
         B     MO$NEXT
         SPACE 1
MO$SLJID OI    QSLFLDS,QSLFLJID         request jobid
         B     MO$NEXT
         SPACE 1
MO$SLRFL OI    QSLFLDS,QSLFLRFL         request request_flags
         B     MO$NEXT
         SPACE 1
          AIF   (&QLEVEL LT 8).MO02
MO$APCY  TM    QXAUTH,QXAUTHX           is user authorized?
         BZ    MO$NEXT                  no, skip update
         OI    QMODE2,QM2CKPT           request in core ckpt
         B     MO$NEXT
         SPACE 1
MO$NAPCY TM    QXAUTH,QXAUTHX           is user authorized?
         BZ    MO$NEXT                  no, skip update
         NI    QMODE2,X'FF'-QM2CKPT     reset in core ckpt
         B     MO$NEXT
.MO02     ANOP ,
         SPACE 3
MODE$SET TM    QFLAG1,Q1PROFOK          INIT PROFILE COMPLETE?
         BNO   QSTOP                    NO - DON'T GENERATE DISPLAY
*  "IMMED" WILL HAVE BEEN TURNED OFF THE FIRST TIME IN
         B     MODEDISP                 GO DISPLAY CURRENT OPTIONS
         SPACE 3
MODESTAR TM    QFLAG1,Q1IMMED           ARE WE HERE AS "IMMEDIATE"?
         BNO   MODEDISP                 NO - GO GENERATE DISPLAY
         SPACE 1
MODESTOP NI    QFLAG1,255-Q1IMMED       YES - RESET FLAG
         B     QSTOP                    RETURN TO "DISPLAY" TO BE
*                                       RE-INVOKED BY QUEUE MAINLINE
         SPACE 3
MODEDISP MVC   QDHLINE(22),=C'Current mode settings:'
         BAL   R14,QADDLINE             START WITH BLANK LINE
         SPACE 1
         MVC QDMSG(44),=C'Display Carriage Control -------------->  NO'
         MVC   QDMSG+55(17),=C'(SHOWCC/NOSHOWCC)'
         TM    QMODE1,QM1LCC            WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(46),=C'Handling of output displays ----------->  LIN$
               E'
         MVC   QDMSG+55(11),=C'(PAGE/LINE)'
         TM    QMODE1,QM1PAGE           WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(4),=C'PAGE'     YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(44),=C'Display record numbers for FINDALL ---->  NO'
         MVC   QDMSG+55(15),=C'(FANUM/NOFANUM)'
         TM    QMODE1,QM1FANUM          WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(44),=C'Audible Alarm for error messages ------>  NO'
         MVC   QDMSG+55(13),=C'(BEEP/NOBEEP)'
         TM    QMODE1,QM1BEEP           WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(44),=C'PF13-24 different from PF1-12 --------->  NO'
         MVC   QDMSG+55(13),=C'(PFK12/PFK24)'
         TM    QMODE1,QM1PFK24          WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(48),=C'Format of displayed dates ------------->  Jul$
               ian'
         MVC   QDMSG+55(13),=C'(JDATE/GDATE)'
         TM    QMODE1,QM1JDATE          WELL?
         BO    *+10                     Julian - skip
         MVC   QDMSG+42(9),=C'Gregorian'
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(44),=C'Verify action requests before exec ---->  NO'
         MVC   QDMSG+55(17),=C'(VERIFY/NOVERIFY)'
         TM    QMODE1,QM1NOVFY          WELL?
         BO    *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(44),=C'Support cmd stack via "END" subcmd ---->  NO'
         MVC   QDMSG+55(15),=C'(STACK/NOSTACK)'
         TM    QMODE1,QM1STACK          WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(44),=C'Reissue stacked cmd after action cmd -->  NO'
         MVC   QDMSG+55(13),=C'(RSCA/NORSCA)'
         TM    QMODE2,QM2RSCA           WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC QDMSG(40),=C'Default lines/page -------------------->'
         MVC   QDMSG+55(16),=C'(LINECT=(10-255)'
         XR    R15,R15                  clear
         IC    R15,QMLINECT             get lines/page
         CVD   R15,MO@DWD               convert it
         MVC   QDMSG+42(3),=X'202120'   edit mask
         ED    QDMSG+41(4),MO@DWD+6     number
         BAL   R14,QADDLINE             DISPLAY THIS LINE
          AIF  (&QLEVEL LT 8).MO03
         SPACE 1
         TM    QXAUTH,QXAUTHX           is user authorized?
         BZ    MO$NO$AC                 no, skip display
         MVC QDMSG(44),=C'Use Applcopy Checkpoint --------------->  NO'
         MVC   QDMSG+55(19),=C'(APPLCOPY/NOAPPCPY)'
         TM    QMODE2,QM2CKPT           WELL?
         BNO   *+10                     NO - SKIP
         MVC   QDMSG+42(3),=C'YES'      YES
         BAL   R14,QADDLINE             DISPLAY THIS LINE
MO$NO$AC EQU   *
.MO03     ANOP ,
         BAL   R14,QADDLINE             ADD BLANK LINE
         SPACE 1
         MVC   QDMSG(31),=C'Sysout print defaults  ------->'
         MVC   QDMSG+33(6),=C'CLASS='
         MVC   QDMSG+39(L'QPCLASS),QPCLASS  DISPLAY THE CLASS
         MVC   QDMSG+42(5),=C'DEST='
         MVC   QDMSG+47(L'QPDEST),QPDEST  DISPLAY THE DEST
         CLI   QPDEST,C' '              ANYTHING?
         BH    *+10                     YES - KEEP IT
         MVC   QDMSG+47(8),=CL8'CENTRAL'  NO - IT'S THEREFORE "CENTRAL"
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         MVC   QDMSG+33(4),=C'UCS='
         MVC   QDMSG+37(L'QPUCS),QPUCS  DISPLAY THE UCS
         MVC   QDMSG+43(4),=C'FCB='
         MVC   QDMSG+47(L'QPFCB),QPFCB  DISPLAY THE FCB
         MVC   QDMSG+53(5),=C'FORM='
         MVC   QDMSG+58(L'QPFORM),QPFORM  DISPLAY THE FORM
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC   QDMSG(31),=C'Route default ---------------->'
         MVC   QDMSG+33(6),=C'DESTR='
         MVC   QDMSG+39(L'QDEST),QDEST  DISPLAY DESTINATION
         CLI   QDEST,C' '               ANY SPECIAL LOC?
         BH    *+10                     YES, USE IT
         MVC   QDMSG+39(8),=CL8'CENTRAL' NO - IT'S THEREFORE "CENTRAL"
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         MVC   QDMSG(31),=C'Default "STATUS" search key -->'
         MVC   QDMSG+33(7),=C'STATUS='
         MVC   QDMSG+40(L'QSTDEFKY),QSTDEFKY  DISPLAY DFLT SEARCH KEY
         BAL   R14,QADDLINE             DISPLAY THIS LINE
         SPACE 1
         TM    QXAUTH,QXAUTHX           privileged user?
         BZ    MO$NO$SL                 no - skip
         MVC   QDMSG(17),=C'SYSLOG fields -->'
         LA    R1,QDMSG+19              -> output loc
         LA    R14,MO#SLFLD             -> field descriptor table
MO$SL$LP IC    R15,0(,R14)              get flag byte
         EX    R15,MO$SL$TM             tm qslflds,*-*
         BZ    MO$SL$NO
         MVC   0(3,R1),1(R14)
         LA    R1,5(,R1)
MO$SL$NO LA    R14,1+3(,R14)            -> next table entry
         CLI   0(R14),X'FF'             end of table?
         BNE   MO$SL$LP                 no - continue
         BAL   R14,QADDLINE             DISPLAY THIS LINE
     MVC  QDMSG+33(44),=C'(SLxxx  xxx=OFF/IND/RTC/SID/DAT/TIM/JID/RFL)'
         BAL   R14,QADDLINE             DISPLAY THIS LINE
MO$NO$SL EQU   *
         BAL   R14,QADDLINE             ADD BLANK LINE
         SPACE 1
         B     QSTOP                    ALL DONE
         SPACE 1
MO$SL$TM TM    QSLFLDS,*-*              << executed >>
         SPACE 2
         ENTRY MO#KEYS,COL#KEYS
*---  MUST HAVE A ONE-FOR-ONE CORRESPONDENCE BETWEEN TABLE BELOW AND
*---  BRANCH TABLE IN MO$FOUND
MO#KEYS  DC    CL8'SHOWCC  '
         DC    CL8'NOSHOWCC'
         DC    CL8'PAGE    '
         DC    CL8'LINE    '
         DC    CL8'FANUM   '
         DC    CL8'NOFANUM '
         DC    CL8'BEEP    '
         DC    CL8'NOBEEP  '
         DC    CL8'PFK12   '
         DC    CL8'PFK24   '
         DC    CL8'JDATE   '
         DC    CL8'GDATE   '
         DC    CL8'VERIFY  '
         DC    CL8'NOVERIFY'
         DC    CL8'STACK   '
         DC    CL8'NOSTACK '
         DC    CL8'RSCA    '
         DC    CL8'NORSCA  '
         DC    CL8'CLASS   '
         DC    CL8'CLS     '
         DC    CL8'UCS     '
         DC    CL8'FCB     '
         DC    CL8'FORM    '
         DC    CL8'DEST    '
         DC    CL8'DESTR   '
         DC    CL8'STATUS  '
         DC    CL8'YES3270 '
         DC    CL8'NOT3270 '
         DC    CL8'COLS    '
         DC    CL8'NOCOLS  '
         DC    CL8'LINECT  '
         DC    CL8'SLOFF   '
         DC    CL8'SLIND   '
         DC    CL8'SLRTC   '
         DC    CL8'SLSID   '
         DC    CL8'SLDAT   '
         DC    CL8'SLTIM   '
         DC    CL8'SLJID   '
         DC    CL8'SLRFL   '
         DC    CL8'APPLCOPY'
         DC    CL8'NOAPPCPY'
         DC    X'FF'                    END OF TABLE INDICATOR
         SPACE 2
COL#KEYS DC    CL8'ON      '
         DC    CL8'OFF     '
         DC    X'FF'                    END OF TABLE INDICATOR
         SPACE 2
MO#SLFLD DC    AL1(QSLFLIND),C'Ind'
         DC    AL1(QSLFLRTC),C'Rtc'
         DC    AL1(QSLFLSID),C'Sid'
         DC    AL1(QSLFLDAT),C'Dat'
         DC    AL1(QSLFLTIM),C'Tim'
         DC    AL1(QSLFLJID),C'Jid'
         DC    AL1(QSLFLRFL),C'Rfl'
         DC    X'FF'                    END OF TABLE INDICATOR
         SPACE 3
         DROPX R13,R9                   WORK, QPRNTWA
         SPACE 3
         LTORG ,
         SPACE 3
WORK     DSECT ,
         ORG   WORK+72
MO@DWD   DS    D
         SPACE 3
         QCOMMON
