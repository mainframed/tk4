         MACRO
&NAME    QSORT   &ADDR=,&NUM=,&LEN=,&COMP=,&OFFSET=,&WORK=,&PREFIX=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QSORT  - Invoke QSORT routine                                     *
.*                                                                    *
.*  Updates:                                                          *
.*     10Dec89 LDW  Bypass if &@@OACFS = ''                           *
.*                  Don't use relative value to branch around QTILT   *
.*      3Jun87 EMS  Update to invoke OACFSORT directly                *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         GBLC  &@@OACFS
         LCLC  &IHBNX
&IHBNX   SETC  'IHB'.'&SYSNDX'
         AIF   ('&@@OACFS' NE '').OKSORT
         MNOTE 0,'You really need OACFSORT for QUEUE to function correc$
               tly.'
&NAME    DC    0H'0'                    define name
         MEXIT
.*
.OKSORT  ANOP
&NAME    XC    &PREFIX.@PARM(&PREFIX.@L),&PREFIX.@PARM  clear entry
         MVC   &PREFIX.@RECPT,&ADDR     Address of table start
         MVC   &PREFIX.@RECLN,&LEN      Len of entry
         MVC   &PREFIX.@CMPEP,&COMP     Compare routine addr
         AIF   (T'&WORK EQ 'O').WORK13
         MVC   &PREFIX.@CMPPM,&WORK     Set workarea addr
         AGO   .ENTNUM
.WORK13  ANOP  ,
         ST    R13,&PREFIX.@CMPPM       Default to r13
.ENTNUM  ANOP  ,
         AIF   (T'&NUM EQ 'O').ENTCOMP
         MVC   &PREFIX.@RECNM,&NUM      Set number of entries to sort
         AGO   .SORT
.ENTCOMP ANOP  ,
         LR    R15,&OFFSET              Copy offset addr
         S     R15,&PREFIX.@RECPT       Compute offset
         XR    R14,R14                  clear
         D     R14,&PREFIX.@RECLN       get number of entries
         LA    R15,1(,R15)              add in last entry
         ST    R15,&PREFIX.@RECNM       Save for sort
.SORT    ANOP  ,
         LA    R1,&PREFIX.@SWRK         -> sort workarea
         ST    R1,&PREFIX.@WRKPT        save in parm list
         MVC   &PREFIX.@WRKLN,=A(L'&PREFIX.@SWRK)  len of workarea
         LA    R1,&PREFIX.@PARM         -> parm list
         L     R15,QVSORT               -> sort routine
         BALR  R14,R15                  invoke it
         LTR   R15,R15                  ok?
.*       BZ    *+4+6+4                  yes, continue
         BZ    &IHBNX.A                 yes, continue
*        QTILT '*** Sort failure ***'
         QTILT '*** Sort failure ***'
&IHBNX.A DC    0H'0'
         MEND  ,
