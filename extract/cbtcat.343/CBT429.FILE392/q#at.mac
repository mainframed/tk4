         TITLE '--- QUEUE--ATTR -- DISPLAY SPOOL DS ATTRIBUTES ---'
*---------------------------------------------------------------------*
*                                                                     *
*   ATTR - Display attributes of a spooled dataset                    *
*                                                                     *
*   Entry - QPARM1 = jobid                                            *
*         - QPARM2 = dsid                                             *
*                                                                     *
*   Updates:                                                          *
*      07Feb92  LDW  Integrate GLA fix below:                         *
*      19Dec91  GLA  Add Creator id and Seclabel to display           *
*      08Jun91  LDW  Integrate EMS's SP42 updates below:              *
*     (24Apr91) EMS  Update for SP420                                 *
*      20Dec90  LDW  Correct typo in PSO flag display                 *
*                    Display new flags from 4.1 PDBFLAG1              *
*                    Fix flash count display                          *
*      28Nov90  EMS  Update for SP410                                 *
*      10Oct90  EMS  Fix lines/page display bug                       *
*      31Jul90  EMS  Update for SP313                                 *
*      25Dec89  LDW  QDCBD -> QZDCBD                                  *
*      23Nov89  LDW  Remove unneeded Q$JCT                            *
*      25Apr88  EMS  Update for SP311                                 *
*      10Jan88  LDW  Update for QITEM macro (no more "LC")            *
*       8Jul87  EMS  Update for QITEM macro                           *
*                    Let default forms code be blank                  *
*                    Update to assemble base JES2 code                *
*                    Update for GETPDDB routine                       *
*      22May87  EMS  Fix for JESNEWS                                  *
*                    Fix Line 09 (leave attr bytes alone)             *
*                    Add to description of DCBRECFM (add QDCBD)       *
*                    Update for enhanced QITEM macro                  *
*      21Mar87  LDW  Fix for DSID = 0 (from RSCS)                     *
*      14Dec86  LDW  SP136 updates:  skip PDBCPU                      *
*      28AUG86  LDW  WRITTEN                                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
ATTR     QSTART  Q??,STACK=X'40',WORKLEN=AT@WORKL
         USNGX WORK,R13                 BASE REG FOR LOCAL WORK
         QITEM RESET,PREFIX=AT          RESET GLOBAL ARRAY POINTER
         QCALL FINDJOB                  LOCATE THE JQE, JCT, AND IOT
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE DATASET ID NUMBER FOR USER DSID
*                                                                     *
***********************************************************************
         LH    R1,QLNG2                 GET LENGTH OF DSID OPERAND
         SH    R1,=H'1'                 DATASET ID FIELD ZERO LENGTH?
         BM    AT$USE                   YES - USE CURRENT DSID
         CLC   =C'JL',QPARM2            WANT JOBLOG?
         LA    R15,2                    DSID FOR JOBLOG
         LA    R14,=CL4'JLOG'           GET FULL CHAR ID
         BE    AT$ANYOK                 YES - USE IT
         CLC   =C'JC',QPARM2            WANT JCL?
         LA    R15,3                    DSID FOR JCL?
         LA    R14,=CL4'JCL '           GET FULL CHAR ID
         BE    AT$ANYOK                 YES - USE IT
         CLC   =C'JM',QPARM2            WANT JOBMSG?
         LA    R15,4                    DSID FOR JOBLOG
         LA    R14,=CL4'JMSG'           GET FULL CHAR ID
         BE    AT$ANYOK                 YES - USE IT
         LA    R15,QPARM2               POINT TO PARM
         BAL   R14,QNUMCONV             CONVERT IT
         BNE   AT$TILT1                 QUIT IF NOT NUMERIC
         TM    QXAUTH,QXAUTHX           PRIVILEGED USER?
         BNZ   AT$ANYOK                 YES - SKIP NEXT VALIDITY CHECK
         CH    R15,=H'101'              DATASET ID LESS THAN 101?
         BL    AT$TILT1                 YES - TILT
          AIF  (&QLEVEL GE 9).AT01
AT$ANYOK STH   R15,QPDSID               STORE DATASET ID
          AGO  .AT02
.AT01     ANOP ,
AT$ANYOK STCM  R15,B'1111',QPDSID       store dataset id
.AT02     ANOP ,
         OI    QFLAG2,Q2VALIDS          INDICATE VALIDITY FOR LISTDS
         MVC   QDHDSID,QPARM2           SET DSID
***********************************************************************
*                                                                     *
*   LOCATE PDDB FOR DATASET SPECIFIED IN QPDSID                       *
*                                                                     *
***********************************************************************
*T$USE   OC    QPDSID,QPDSID            ANY DSID?
AT$USE   TM    QFLAG2,Q2VALIDS          ANY DSID?
         BNZ   AT$HAVE
         QTILT '*** NO DSID SELECTED ***'
AT$HAVE  OI    QSCFLAG1,QSC1JOB         PUT THE JOBID IN QDTOP
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
         MVC   QDHLINE(79),=CL79'Attributes for DSID xxxxxxxx'
         MVC   AT@HDSID,QDHDSID         AND FILL IN DSID
         OI    QFLAG1,Q1MVHDR           REQUEST "MOVE HEADER"
         MVI   QGPFLAG,QPFDIR           get specific pddb
         QCALL GETPDDB                  locate pddb entry
         LTR   R6,R1                    copy pddb addr
         BNP   QSTOP                    if none, error msg already out
         USNGX PDBDSECT,R6              BASE REG FOR PDDB
         SPACE 2
*---  LINE 01
         SR    R5,R5                    CLEAR ATTR BYTE COUNTER
         BAL   R14,QADDLINE             DISPLAY A BLANK LINE
         SPACE 2
*---  LINE 02
         QITEM 0+00,'PDBFLAG1',2+10,HEX,PDBFLAG1,1
         QITEM 2+20,'PDBFLAG2',4+30,HEX,PDBFLAG2,1
          AIF   (&QLEVEL LT 4).AT03
         QITEM 4+40,'PDBFLAG3',6+50,HEX,PDBFLAG3,1
         QITEM 6+60,'PDBSTAT',8+70,HEX,PDBSTAT,1
.AT03    ANOP  ,
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
          AIF   (&QLEVEL GT 9).AT04
         TM    PDBFLAG1,PDB1DSID        DSID IN PDBDSID/PDBWTRID FIELD?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+05(4),=C'DSID'
.AT04    ANOP  ,
         TM    PDBFLAG1,PDB1NULL        NULL PDDB?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+10(4),=C'NULL'
         TM    PDBFLAG1,PDB1LOG         HASP JOB LOG?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+15(4),=C'JLOG'
          AIF   (&QLEVEL GE 12).ATX04A  (maybe should check pdbstat??)
         TM    PDBFLAG1,PDB1MDES        FOLLOWED BY PDDBS FOR MULT DEST
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+20(4),=C'MDES'
          AGO   .ATX04B
.ATX04A   ANOP
         TM    PDBFLAG1,PDB1MOC         FOLLOWED BY PDDBS FOR MULT DEST
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+20(4),=C'MOC '
.ATX04B   ANOP
         TM    PDBFLAG1,PDB1NSOT        NOT FOR SYSOUT?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+25(4),=C'NSOT'
         TM    PDBFLAG1,PDB1SPIN        SPIN DATASET?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+30(4),=C'SPIN'
          AIF   (&QLEVEL GE 11).AT05    (maybe should check pdbstat??)
         TM    PDBFLAG1,PDB1HOLD        HELD DATASET?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+35(4),=C'HELD'
         TM    PDBFLAG1,PDB1PSO         ACCESSIBLE BY PSO?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+40(4),=C'PSO '
          AGO   .AT06
.AT05     ANOP  ,
         TM    PDBFLAG1,PDB1NEWS        JESNEWS DATASET?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+35(4),=C'NEWS'
         TM    PDBFLAG1,PDB1USER        user?
         BZ    *+10                     NO - SKIP
         MVC   AT@LINE+40(4),=C'USER'
.AT06     ANOP
         CLC   AT@LINE,QBLANK           ANYTHING HERE?
         BE    *+8                      NO - SKIP
         BAL   R14,QADDLINE             YES - DISPLAY LINE
*---  LINE 03
         MVC   AT@WORK,QBLANK           clear out field
         LA    R1,AT@WORK               -> start of field
         MVI   0(R1),C'U'               start with u
         TM    PDBRECFM,DCBRECU
         BO    AT$RECB                  got it
         MVI   0(R1),C'F'               try f
         TM    PDBRECFM,DCBRECF
         BO    AT$RECB                  got it
         MVI   0(R1),C'V'               must be v
         TM    PDBRECFM,DCBRECV
         BO    AT$RECB                  got it
         MVI   0(R1),C'U'               well, say it's u after all
AT$RECB  LA    R1,1(,R1)                -> next char
         TM    PDBRECFM,DCBRECBR        blocked?
         BZ    AT$RECS                  no
         MVI   0(R1),C'B'                 yes, insert
         LA    R1,1(,R1)
AT$RECS  TM    PDBRECFM,DCBRECSB        spanned/standard?
         BZ    AT$RECC
         MVI   0(R1),C'S'               yes
         LA    R1,1(,R1)
AT$RECC  TM    PDBRECFM,DCBRECCC        control chars
         BZ    AT$REC
         MVI   0(R1),C'A'               assume asa
         TM    PDBRECFM,DCBRECCA
         BO    AT$REC
         MVI   0(R1),C'M'               must be machine
AT$REC   LA    R1,1(,R1)
         MVI   1(R1),C'('               (
         UNPK  2(3,R1),PDBRECFM(2)       ??
         TR    2(2,R1),QHEXTAB             )
         MVI   4(R1),C')'
         QITEM 0+00,'RECFM',2+10,CHAR,AT@WORK,9
         QITEM 2+20,'LRECL',4+30,DEC,PDBLRECL,2
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 04
         QITEM 0+00,'PDBMTTR',2+10,HEX,PDBMTTR,4
          AIF   (&QLEVEL LT 4).AT07
         QITEM 2+20,'PDBMTTRL',4+30,HEX,PDBMTTRL,4
.AT07    ANOP  ,
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 05
         QITEM 0+00,'Class',2+10,CHAR,PDBCLASS,1
         QITEM 2+20,'Copies',4+30,DEC,PDBCOPYS,1
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 06
*%%      MVC   AT@LINE(4+4),AT#DEST     'DESTINATION'
         LA    R1,PDBDEST               POINT TO DESTINATION FIELD
         QCALL FINDRJE                  CONVERT TO RJE ID
*%%      MVC   AT@LINE+2+10(8),0(R1)    MOVE DEST NAME TO DISPLAY LINE
*%%      CLC   AT@LINE+2+10(8),QBLANK   BLANK? (LOCAL)
*%%      BNE   *+10                     NO - KEEP IT
*%%      MVC   AT@LINE+2+10(7),=C'(LOCAL)'  YES - SAY SO
*%%      CLC   AT@DWD,QBLANK            BLANK? (LOCAL)
*%%      BNE   *+10                     NO - KEEP IT
*%%      MVC   AT@DWD(7),=C'(LOCAL)'    YES - SAY SO
*%%      LA    R5,2(,R5)                BUMP NUMBER OF ATTR BYTES
         MVC   AT@DWD,0(R1)             SAVE DEST NAME
         QITEM 0+00,'Dest',2+10,CHAR,AT@DWD,8
          AIF   (&QLEVEL LT 4).AT08
         QITEM 2+20,'Dest Usr',4+30,CHAR,PDBUSER,8
          AIF   (&QLEVEL LT 10).AT08
         QITEM 4+40,'Creator',6+50,CHAR,PDBCRUID,8
         QITEM 6+60,'Seclabel',8+70,CHAR,PDBSECLB,8
.AT08    ANOP  ,
          AIF   (&QLEVEL GE 7).AT09
         QITEM 4+40,'PDBCPU',6+50,HEX,PDBCPU,1
.AT09     ANOP
*  % PDBSEC?
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 07
         QITEM 0+00,'Records',2+10,DEC,PDBRECCT
          AIF   (&QLEVEL LT 6).AT10
         QITEM 2+25,'Pages',4+35,DEC,PDBPGCT
         QITEM 4+50,'Bytes',6+60,DEC,PDBBYTCT
.AT10    ANOP  ,
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 08
          AIF   (&QLEVEL GT 4).AT11
         QITEM 0+00,'Forms',2+10,CHAR,PDBFORMS,4
          AGO   .AT12
.AT11     ANOP
         QITEM 0+00,'Forms',2+10,CHAR,PDBFORMS,8
.AT12     ANOP
         CLC   PDBFORMS,=CL8'&@@QSTDF'  default forms code?
         BNE   *+10                     no, leave as is
         MVC   AT@LINE+10+2(8),QBLANK   yes, clear it out
         QITEM 2+20,'Fcb',4+30,CHAR,PDBFCB,4
         QITEM 4+40,'Ucs',6+50,CHAR,PDBUCS,4
          AIF   (&QLEVEL LT 6).AT13
         QITEM 6+60,'Prmode',8+70,CHAR,PDBPRMD,8
.AT13    ANOP  ,
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 09
          AIF   (&QLEVEL LT 4).AT14
         QITEM 0+00,'DDname',2+10,CHAR,PDBDDNAM,8
         QITEM 2+20,'Proc Step',4+31,CHAR,PDBPNAME,8
         QITEM 4+40,'Step Name',6+51,CHAR,PDBSNAME,8
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
.AT14    ANOP  ,
*---  LINE 10
          AIF   (&QLEVEL GT 9).AT15
         QITEM 0+00,'Dsid/Wtrid',2+30,CHAR,PDBDSID,8
         AGO   .AT16
.AT15    ANOP  ,
         QITEM 0+00,'Wtrid',2+30,CHAR,PDBWTRID,8
.AT16    ANOP  ,
         QITEM 2+40,'PDBRBA',4+50,HEX,PDBRBA,4
         QITEM                   ,,4+59,HEX,PDBRBA+4,4
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 11
         QITEM 0+00,'Chars',2+10,CHAR,PDBCHAR1,4
         MVC   AT@LINE+2+16(4),PDBCHAR2
         MVC   AT@LINE+2+22(4),PDBCHAR3
         MVC   AT@LINE+2+28(4),PDBCHAR4
         QITEM 2+40,'Flash',4+50,CHAR,PDBFLASH,4
         QITEM 4+60,'Modf',6+70,CHAR,PDBMODF,4
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 12
         QITEM 0+00,'Flash Count',2+14,DEC,PDBFLSHC,1
         QITEM 2+20,'TRC',4+30,CHAR,PDBMODFT,1
         QITEM 4+40,'Copyg',6+50,HEX,PDBCOPYG,4
         QITEM             ,,6+59,HEX,PDBCOPYG+4,4
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 13
         QITEM 0+00,'Pages/Ckpt',2+12,DEC,PDBCKPTP,2
         QITEM 2+20,'Lines/Ckpt',4+32,DEC,PDBCKPTL,2
          AIF   (&QLEVEL LT 4).AT17
         TM    PDBFLAG3,PDBLNCTF        lines/page specified?
         BZ    AT$LN12                  no, skip output
         QITEM 2+40,'Lines/Page',4+52,DEC,PDBLINCT,1
.AT17    ANOP  ,
AT$LN12  QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
          AIF   (&QLEVEL LT 4).AT18
*---  LINE 14
         QITEM 0+00,'Output Id',2+13,DEC,PDBID,2
         QITEM 2+20,'Output Name',4+33,CHAR,PDBNAME,8
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
*---  LINE 15
         QITEM 0+00,'PDBCRTME',2+10,HEX,PDBCRTME,4
         QITEM 2+20,'PDBPLIOT',4+30,HEX,PDBPLIOT,4
         QITEM 4+40,'PDBPLOFF',6+50,HEX,PDBPLOFF,4
         QITEM ADDLINE                  SET ATTR NUM AND DISPLAY LINE
         SPACE 2
.AT18    ANOP  ,
         B     QSTOP
         SPACE 3
         DROPX R13,R6                   WORK, PDBDSECT
         EJECT ,
AT$TILT1 MVC   QERRMSG(14),=C'*** DATASET ID '
         MVC   QERRMSG+15(8),QDHDSID    SHOW THE DSID
         MVC   QERRMSG+24(11),=C'INVALID ***'
         QTILT *,OPTIONS=REPROMPT
         SPACE 2
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG ,
         SPACE 1
AT#MASK  DC    X'402020',C',',X'202020',C',',X'202020',C',',X'202120'
AT#MASKL EQU   *-AT#MASK
         SPACE 1
*%%  AT#DEST  DC    X'1DE8'                  OUTPUT, BRIGHT
*%%      DC    C'Dest'
*%%      DC    X'1D60'                  OUTPUT, NORMAL
*%%      SPACE 1
         QITEM GEN                      GEN ALL THE OTHER "LITERALS"
         SPACE 3
WORK     DSECT
         ORG   WORK+72                  SKIP PAST SAVE AREA
AT@DWD   DS    D
AT@WORK  DS    2CL(AT#MASKL)
AT@WORKL EQU   *-WORK
         SPACE 3
         QCOMMON
         ORG   QDMSG
AT@LINE  DS    CL(L'QDMSG)
         ORG   QDHLINE
         DS    C'ATTRIBUTES FOR DSID '
AT@HDSID DS    CL8
         SPACE 3
         QZDCBD
         Q$PDDB
