         MACRO
&NFS     QSTACK  &LVL,&CMD,&TYPE=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QSTACK - ADD CURRENT COMMAND TO CORRECT POSITION IN STACK.        *
.*                                                                    *
.*  UPDATES:                                                          *
.*     08/30/84 LDW  FIX BUG IN XREF DISPLAY                          *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         GBLC  &QSTACKC(50),&QSTACKL(50),&QSTACKN(50)
         GBLA  &QSTACK#
         LCLC  &C,&N,&L
         LCLA  &I
         AIF   ('&TYPE' EQ 'XREF').XREF
&QSTACK# SETA  &QSTACK#+1
         AIF   ('&CMD '(1,1) EQ '''').CQ
&QSTACKC(&QSTACK#)  SETC  '&CMD'
         AGO   .COK
.CQ      ANOP
&QSTACKC(&QSTACK#)  SETC  '''&CMD'''
.COK     ANOP
         AIF   ('&LVL'(1,2) NE 'X''' OR K'&LVL NE 5).LNOTHEX
&L       SETC  '&LVL'(3,2)              EXTRACT 2 HEX CHARACTERS
&QSTACKL(&QSTACK#)  SETC  'X''''&L'''''
&L       SETC  'X''&L'''
         AGO   .LOK
.LNOTHEX ANOP
&L       SETC  '&LVL'                   ASSUME LEVEL NOT HEX
&QSTACKL(&QSTACK#)  SETC  '&LVL'
.LOK     ANOP
&QSTACKN(&QSTACK#)  SETC  '&NFS'
         AIF   ('&NFS' NE '').NFSOK
&QSTACKN(&QSTACK#)  SETC  '&SYSECT'
.NFSOK   ANOP
&NFS      LA    R0,&L
         AIF   ('&CMD' EQ '').NOCMD
          LA    R1,=CL66&CMD
         AGO   .BAL
.NOCMD    LA    R1,QDREPLY
.BAL      BAL   R14,QSTACK
         SPACE 1
         MEXIT
.XREF    SPACE 2
         MNOTE '  LABEL     LEVEL  COMMAND'
         MNOTE '  -----     -----  -------'
&I       SETA  1
.XRLOOP  ANOP
&C       SETC  '&QSTACKC(&I)       '
&L       SETC  '&QSTACKL(&I)    '(1,8)
&N       SETC  '&QSTACKN(&I)       '(1,8)
         MNOTE '  &N  &L  &C'
&I       SETA  &I+1
         AIF   (&I LE &QSTACK#).XRLOOP
         SPACE 2
         MEND
