         TITLE '--- QUEUE--RMTLIST -- LIST RJE/NJE NAMES ---'
*---------------------------------------------------------------------*
*                                                                     *
*   RMTLIST - List symbolic remote destination names, with their      *
*             internal numbers                                        *
*                                                                     *
*   Updates:                                                          *
*      16Jun90  LDW  Remove mode switching since we now run AMODE 31  *
*      03Jun90  LDW  Fix for EMS's $AMODE 24 in QSTOP routine         *
*      25Dec89  LDW  Q$SSCT -> QZSSCT                                 *
*      23Nov89  LDW  Remove conditional assembly around Qxxx macros   *
*                    Eliminate "LC" macro usage                       *
*      31May89  LDW  Fix for non-311                                  *
*      22Mar89  EMS  SP311 changes                                    *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*      03/02/83 LDW  CHANGE 'QTILT *' TO USE QERRMSG                  *
*      01/10/83 LDW  SP SUPPORT                                       *
*                    DISPLAY ROUTE DEST                               *
*                    CHANGE HEADER TO LOWER CASE                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
RMTLIST  QSTART  STACK=X'B0'
         L     R15,QSSCT                -> SSCT OF SELECTED SUBSYSTEM
         LTR   R15,R15                  DEFINED?
         BZ    RL$TILT1                 NO - CAN'T DO THIS SUBCOMMAND
          AIF  (&QLEVEL GE 9).RL12
         L     R15,SSCTSSVT-SSCT(,R15)  -> SSVT
         USING SSVT,R15
          AGO  .RL13
.RL12     ANOP ,
         L     R15,SSCTSUS2-SSCT(,R15)  -> HCCT
         USING HCCT,R15
.RL13     ANOP ,
         LTR   R15,R15                  ACTIVE?
         BZ    RL$TILT2                 NO - CAN'T DO THIS SUBCOMMAND
         L     R3,$SVRDT                -> FIRST RDT
          AIF  (&QLEVEL GE 9).RL14
         L     R5,$SVRDTE               -> LAST RDT
.RL14     ANOP ,
         DROP  R15
         USING RDTDSECT,R3
         LA    R4,RDTSIZ                LENGTH OF RDT (BXLE INCR)
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
         MVC   QDHLINE+10(36),RL#HDR
         TM    QXAUTH,QXAUTHX           PRIVILEGED USER?
         BNZ   *+10                     YES - OK
         MVC   QDHLINE+41(5),QBLANK     REMOVE 'FLAGS' FROM HEADER
*%%%  SHOULD DISPLAY NAME AND NUMBER OF LOCAL NODE...
         SPACE 2
RL$LOOP  MVC   QDMSG+10(8),RDTNAME      DEST ID
          AIF   (&QLEVEL GE 4).RL01
         SR    R0,R0                    CLEAR FOR IC
.RL01     ANOP
         TM    RDTFLAG,RDTFLAGN         USE RDTNODE AS ROUTE BYTE 1?
         BNO   RL$NNODE                 NO - SKIP
         LA    R1,RDTNODE               -> SYSTEM (NODE) NUMBER
         LA    R15,QDMSG+18             -> OUTPUT AREA
         BAL   R8,RL$EDIT               EDIT IN THE NUMBER
         SPACE 1
RL$NNODE TM    RDTFLAG,RDTFLAGR+RDTFLAGU  REMOTE OR UNIT NUMBER?
         BZ    RL$OK                    NO - SKIP
         LA    R1,RDTRMTNO              -> REMOTE OR UNIT NUMBER
         LA    R15,QDMSG+25             ASSUME REMOTE NUMBER
         TM    RDTFLAG,RDTFLAGR         IS IT A REMOTE NUMBER?
         BO    *+8                      YES
         LA    R15,QDMSG+32             NO - UNIT
         BAL   R8,RL$EDIT               EDIT IN THE NUMBER
         SPACE 1
RL$OK    TM    QXAUTH,QXAUTHX           PRIVILEGED USER?
         BZ    RL$ADD                   NO - SKIP
         UNPK  QDMSG+42(2+1),RDTFLAG(1+1)
         TR    QDMSG+42(2),QHEXTAB
         MVI   QDMSG+44,C' '
RL$ADD   BAL   R14,QADDLINE             ADD THIS LINE
          AIF  (&QLEVEL GE 9).RL15
         BXLE  R3,R4,RL$LOOP            FORMAT ALL RDT'S
          AGO  .RL16
.RL15     ANOP ,
         ICM   R3,B'1111',RDTRDT        format
         BNZ   RL$LOOP                        all RDT'S
.RL16     ANOP ,
         B     QSTOP                    LEAVE WHEN DONE
         SPACE 2
RL$TILT1 MVC   QERRMSG(27),=C'*** XXXX IS NOT DEFINED ***'
         MVC   QERRMSG+4(4),QJESNAME
         QTILT *
RL$TILT2 MVC   QERRMSG(22),=C'*** XXXX IS NOT UP ***'
         MVC   QERRMSG+4(4),QJESNAME
         QTILT *
         SPACE 2
*---  RL$EDIT - FORMAT A BINARY NODE OR REMOTE NUMBER              ---*
*---    R1 -> 1 (NON-SP) OR 2 (SP) BYTE BINARY NUMBER (INPUT)      ---*
*---    R8 -> RETURN                                               ---*
*---    R15 -> 6 BYTE OUTPUT AREA                                  ---*
          AIF   (&QLEVEL GE 4).RL08
*---  ASSUME HIGH ORDER 3 BYTES OF R0 STILL CLEAR FROM ABOVE       ---*
RL$EDIT  IC    R0,0(,R1)                GET INPUT NUMBER
          AGO   .RL09
.RL08     ANOP
RL$EDIT  LH    R0,0(,R1)                GET INPUT NUMBER
.RL09     ANOP
         CVD   R0,QNUMWORK
         MVC   1(5,R15),=X'2020202020'  MOVE IN EDIT MASK
         ED    0(6,R15),QNUMWORK+5
         BR    R8                       RETURN TO CALLER
         SPACE 2
         LTORG ,
         SPACE 2
RL#HDR   DC    C'Destid    Node  Remote  Unit   Flags'
         SPACE 3
         QCOMMON
         SPACE 3
         Q$SVT
         Q$HCCT
         Q$RDT
         QZSSCT
