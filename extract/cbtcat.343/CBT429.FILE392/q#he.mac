         TITLE '--- QUEUE--HELP -- DISPLAY HELP ---'
*---------------------------------------------------------------------*
*                                                                     *
*   HELP - Display help                                               *
*                                                                     *
*   Entry - Optional starting screen number in QPARM1                 *
*                                                                     *
*   Updates:                                                          *
*      04Jul91  LDW  Make HELPTEXT a WXTRN                            *
*      23Nov89  LDW  Eliminate "LC" macro usage                       *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*      08/28/83 LDW  CHANGE OFFSETS TO 3 BYTES                        *
*      03/10/83 LDW  ADD SUPPORT FOR "HELP /ALL"                      *
*                    USE OFFSET INSTEAD OF ADCONS TO POINT TO SCREENS *
*      03/08/83 LDW  DON'T DELETE HELP MODULE                         *
*      03/02/83 LDW  CHANGE 'QTILT *' TO USE QERRMSG                  *
*      02/24/83 LDW  CHANGE TO SUPPORT "HELP SUBCMDNAME" ONLY         *
*                    MOVE HELP SCREENS TO SEPARATE LOAD MODULE        *
*      02/10/83 LDW  ADD SUPPORT FOR ATTR BYTES IN HELP LINES         *
*      01/28/83 LDW  DON'T MOVE HEADER LINE UNTIL AFTER FIRST LINE    *
*                       OF A SCREEN IS MOVED (THIS IS TO FIX THE      *
*                       BUG THAT CAUSED THE WRONG HEADING LINE TO     *
*                       BE PRINTED WHEN USING PRINT SCREEN)           *
*      01/05/83 LDW  MOVE ACTUAL SCREEN IMAGES TO NEW COPY MEMBER -   *
*                       "Q#HE#DC" (UPDATES TO SCREENS THEMSELVES WILL *
*                       NO LONGER BE FLAGGED, SO ALL UPDATE NOTES     *
*                       BELOW HAVE BEEN DELETED)                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         WXTRN HELPTEXT
         SPACE 3
HELP     QSTART  Q8,STACK=X'C0'
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
         CLI   QKEYWORD,C'A'            'ALL' KEYWORD GIVEN?
         BE    HE$01                    YES - SKIP
         CLI   QPARM1,C' '              ANY OPERAND?
         BNE   *+10                     YES - USE IT
         MVC   QPARM1,=CL8'COMMANDS'    NO - GIVE SUBCMD NAMES
         SPACE 1
HE$01    L     R8,=A(HELPTEXT)          -> HELP TEXT CSECT
         LTR   R8,R8                    IS IT LINK EDITTED IN?
         BNZ   HE$OK                    YES - USE IT
         L     R8,QVHELP                -> TEXT LOAD MODULE
         LTR   R8,R8                    IS IT LOADED YET?
         BNZ   HE$OK                    YES - USE IT
         SPACE 1
         LOAD  EPLOC==CL8'QHLPTXT',ERRET=HE$OOPS
         SPACE 1
         ST    R0,QVHELP                SAVE ADDR FOR NEXT TIME
         LR    R8,R0                    GET ADDR IN CORRECT REGISTER
         SPACE 2
HE$OK    LM    R3,R5,0(R8)              BXLE REGS FOR KEYWORD TABLE
         CLI   QPARM1,C' '              ANYTHING HERE?
         BE    HELP$GO                  NO - MUST BE "HELP /ALL"
***********************************************************************
*                                                                     *
*   LOCATE REQUESTED HELP SCREEN                                      *
*                                                                     *
***********************************************************************
HE$FIND  CLC   QPARM1,0(R3)             IS THIS IT?
         BE    HELP$GO                  YES - DISPLAY IT
         BXLE  R3,R4,HE$FIND            NO - KEEP LOOKING
         MVC   QERRMSG(34),=C'*** NO HELP FOUND FOR 12345678 ***'
         MVC   QERRMSG+22(8),QPARM1
         QTILT *,OPTIONS=(REPROMPT,SET)
***********************************************************************
*                                                                     *
*   PASS HELP SCREEN TO DISPLAY MODULE                                *
*                                                                     *
***********************************************************************
HELP$GO  MVC   QDHLINE,QBLANK           CLEAR HEADER
         MVC   QDHLINE(6),=C'HELP -'    SET UP HEADER
         MVC   QDHLINE+7(8),0(R3)       SHOW WHICH HELP IS DISPLAYED
         OI    QFLAG1,Q1MVHDR           TELL DISPLAY TO MOVE IT
         SR    R6,R6                    CLEAR FOR ICM
         ICM   R6,B'0111',9(R3)         GET OFFSET OF FIRST LINE
         AR    R6,R8                    -> FIRST LINE (HEADER)
         SR    R7,R7                    CLEAR FOR IC
         IC    R7,0(,R6)                HEADER LINE LENGTH-1
         LR    R1,R7                    COPY HEADER LENGTH
         SH    R1,=H'20'                ADJUST FOR MISSING LEFT SIDE
         BNP   *+8                      SKIP IF NOTHING LEFT
         EX    R1,HE$MVCHD              MVC QDHLINE+20(*-*),20+3(R6)
         LA    R6,3+1(R7,R6)            -> NEXT LINE
         BAL   R14,QADDLINE             START WITH A BLANK LINE
         SPACE 1
HELP$LP  IC    R7,0(,R6)                HELP LINE LENGTH-1
         TM    1(R6),1                  LINE FOR AUTH USERS ONLY?
         BNO   HE$NAUTH                 NO - CONTINUE
         TM    QXAUTH,QXAUTHX           WELL, IS HE AUTHORIZED?
         BZ    HE$SKIP                  NO - SKIP THIS LINE
HE$NAUTH EX    R7,HE$MVC                MVC QDMSG(*-*),3(R6)
         MVC   QDATRNUM+1(1),2(R6)      SET NUMBER OF ATTR BYTES
         BAL   R14,QADDLINE             MOVE LINE TO SCREEN
         SPACE 2
HE$SKIP  LA    R6,3+1(R7,R6)            -> NEXT LINE
         CLI   0(R6),X'FF'              LAST LINE?
         BNE   HELP$LP                  NO - CONTINUE
         BAL   R14,QADDLINE             THROW IN A BLANK LINE
         CLI   QKEYWORD,C'A'            'ALL' KEYWORD GIVEN?
         BNE   QSTOP                    NO - ALL DONE
HE$DUP   AR    R3,R4                    -> NEXT HELP SCREEN DESC
         CLI   0(R3),X'FF'              END OF LIST?
         BE    QSTOP                    YES - ALL DONE
         TM    8(R3),X'80'              THIS SUBCOMMAND AN ALIAS?
         BO    HE$DUP                   YES - SKIP IT
         MVC   QDMSG(HE#NEXTL),HE#NEXT  SAY END OF SCREEN, MORE TO COME
         MVI   QDATRNUM+1,2             NUMBER OF ATTR BYTES IN LINE
         BAL   R14,QADDLINE             ADD MSG TO SCREEN
         QCALL DISPLAY                  DISPLAY THE SCREEN NOW
         B     HELP$GO                  AND DISPLAY THE NEXT SCREEN
         SPACE 1
HE$MVCHD MVC   QDHLINE+20(*-*),20+3(R6) << EXECUTED >>
HE$MVC   MVC   QDMSG(*-*),3(R6)         << EXECUTED >>
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
HE$OOPS  QTILT '*** HELP MODULE UNAVAILABLE -- CONTACT SYSTEMS GROUP **$
               *'
HE#NEXT  DC    X'1DE8'                  OUTPUT, BRIGHT
         DC    C'>>> Press ENTER for next Help screen <<<'
         DC    X'1D60'                  OUTPUT, NORMAL
HE#NEXTL EQU   *-HE#NEXT
         ENTRY HE#KEYS
HE#KEYS  DC    CL8'A',CL8'ALL'
         DC    X'FF'                    END OF HELP KEYS
         LTORG
         SPACE 3
         QCOMMON
