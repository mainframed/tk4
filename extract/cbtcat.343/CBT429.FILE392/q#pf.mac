         TITLE '--- QUEUE--PFKSET -- DISPLAY/CHANGE PFKEYS ---'
*---------------------------------------------------------------------*
*                                                                     *
*   PFKSET - Display / alter user's PFK definitions                   *
*                                                                     *
*   Entry - PFK number in "QPARM1", PFK command starting at "QOFF2"   *
*                                                                     *
*   Exit - PFK table updated if requested                             *
*                                                                     *
*   Note - This subcommand may be executed in a QUEUE profile         *
*                                                                     *
*   Updates:                                                          *
*      23Nov89  LDW  Eliminate "LC" macro usage                       *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      04/17/84 LDW  FIX FOR ENTER AFTER SET KILLING THE PFK JUST SET *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*      03/09/83 LDW  ALLOW FOR 24 PFKEYS IF INDICATED BY "QM1PFK24"   *
*                    CHANGE 'QTILT *' TO USE QERRMSG                  *
*                    CHANGE HEADER TO LOWER CASE                      *
*      12/20/82 LDW  CHANGE QTILT REPRMPT=YES TO OPTIONS=REPROMPT     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
PFKSET   QSTART  STACK=X'B0'
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
         MVC   QDPROMPT,QDREPLY         SET WHAT TO REPROMPT WITH
         TM    QFLAG1,Q1PROFOK+Q1IMMED  IMMED COMMAND AFTER PROFILE?
         BO    PFKSTOP                  YES - TURN OFF IMMED; RECURSE
*  IS THAT WHAT YOU DO WHEN YOU CURSE AT THE PROGRAM MORE THAN ONCE?
         LA    R9,12                    ASSUME LIMIT IS 12
         TM    QMODE1,QM1PFK24          ARE PF13-24 TO BE DIFFERENT?
         BNO   *+8                      NO - SKIP
         LA    R9,24                    YES - LIMIT IS 24
         CLI   QPARM1,C' '              ANY OPERAND?
         BE    PFKSTAR                  NO - JUST DISPLAY
         CLI   QPARM1,C'*'              REQUEST FOR DISPLAY ONLY?
         BE    PFKSTAR                  YES - DO IT
*  CHANGE A PFKEY DEFINITION
         LH    R1,QLNG1                 LENGTH OF QPARM1
         SH    R1,=H'1'                 EX LEN OF QPARM1
         BM    PFKTILT                  NO GOOD
         LA    R15,QPARM1               ADDR OF OPERAND TO CONVERT
         BAL   R14,QNUMCONV             CONVERT PF KEY NUMBER
         BNE   PFKTILT                  BLOW IF NOT NUMERIC
         LTR   R15,R15                  POSITIVE?
         BNP   PFKTILT                  NO
         CR    R15,R9                   KEY TO ALTER IN RANGE?
         BH    PFKTILT                  NO
         BCTR  R15,0                    ADJUST ORIGIN
         SLL   R15,6                    MULTIPLY BY 64
         A     R15,QVPFKEYS             -> APPROPRIATE PF KEY AREA
         MVC   0(64,R15),QBLANK         CLEAR IT
         LA    R1,QDREPLY               -> REPLY AREA
         CLI   QPARM2,C' '              ANY OPERAND?
         BE    PFKNOPER                 NO - SKIP
         AH    R1,QOFF2                 -> OPERAND 2
         LA    R14,QDREPLYL-1           MAX LENGTH-1 OF REPLY
         SH    R14,QOFF2                LENGTH-1 OF OPERAND 2
         EX    R14,PFKMVC               MVC 0(*-*,R15),0(R1)
         MVI   QPARM1,C' '              PREVENT RECURSION
         SPACE 2
PFKNOPER TM    QFLAG1,Q1IMMED           IMMEDIATE SUBCOMMAND?
         BO    QSTOP                    YES - ALL DONE HERE
         B     PFKDISP                  NO - DISP CURRENT DEFINITIONS
         SPACE 3
PFKSTAR  TM    QFLAG1,Q1IMMED           ARE WE HERE AS "IMMEDIATE"?
         BNO   PFKDISP                  NO - GO GENERATE DISPLAY
         SPACE 1
PFKSTOP  NI    QFLAG1,255-Q1IMMED       YES - RESET FLAG
         B     QSTOP                    RETURN TO "DISPLAY" TO BE
*                                       RE-INVOKED BY QUEUE MAINLINE
         SPACE 2
PFKDISP  MVC   QDHLINE(L'PF#HDR),PF#HDR 'CURRENT PFK DEFINITIONS:'
         LA    R2,1                     KEY #
         L     R3,QVPFKEYS              -> FIRST KEY AREA
         BAL   R14,QADDLINE             PUT OUT A BLANK LINE
         SPACE 2
PFKLOOP  CVD   R2,QNUMWORK              CONVERT PFK NUMBER
         MVC   QDMSG+5(3),=X'202020'    MOVE IN EDIT MASK
         ED    QDMSG+4(4),QNUMWORK+6    SHOW PFK NUMBER
         MVC   QDMSG+10(64),0(R3)       PFK TEXT
         BAL   R14,QADDLINE
         LA    R2,1(,R2)                INCR PFK #
         LA    R3,64(,R3)               -> NEXT PFK AREA
         CR    R2,R9                    DONE?
         BNH   PFKLOOP                  NO - SHOW NEXT
         B     QSTOP                    YES - LEAVE
         SPACE 2
PFKTILT  MVC   QERRMSG(7),=C'*** PFK'
         MVC   QERRMSG+8(8),QPARM1
         MVC   QERRMSG+17(11),=C'INVALID ***'
         QTILT *,OPTIONS=REPROMPT
         SPACE 3
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
PFKMVC   MVC   0(*-*,R15),0(R1)         << EXECUTED >>
PF#HDR   DC    C'Current PFK definitions:'
         LTORG
         SPACE 3
         QCOMMON
