        TITLE '--- QUEUE--LISTTEXT -- LIST INTERNAL TEXT FOR A JOB ---'
*---------------------------------------------------------------------*
*                                                                     *
*   LISTTEXT - List internal text for a job                           *
*                                                                     *
*   Updates:                                                          *
*      25Dec89  LDW  Q$TEXT -> QZTEXT                                 *
*      23Nov89  LDW  Remove unneeded Q$JCT, Q$IOT                     *
*                    Eliminate "LC" macro usage                       *
*      26Apr89  EMS  Update for SP311                                 *
*       8Jul87  EMS  Update for GETREC, GETPDDB                       *
*      21May87  EMS  Update for GETREC enhancements                   *
*      21Mar87  LDW  Fix for DSID = 0 (from RSCS)                     *
*      25Jan87  LDW  Pass READSPC the JQE addr in case of CB ID error *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      08/09/84 LDW  UPDATE FOR SP134 (USE IOTPDDB INSTEAD OF         *
*                       QCPDDB1)                                      *
*      04/11/84 LDW  JCTDSECT -> JCTSTART, IOTDSECT -> IOTSTART       *
*                    USING/DROP -> USNGX/DROPX                        *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*      01/06/83 LDW  ROUTINE CREATED - ADAPTED FROM "DDNAME"          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
LISTTEXT QSTART  STACK=X'21'
         MVC   QDHLINE(L'LT#HDR),LT#HDR MOVE IN HEADING
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         QCALL FINDJOB                  FIND THE JOB
         OI    QSCFLAG1,QSC1JOB         PUT JOBID IN QDTOP
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
***********************************************************************
*                                                                     *
*   LOCATE PDDB NUMBER 5                                              *
*                                                                     *
***********************************************************************
          AIF  (&QLEVEL GE 9).LT01
         MVC   QPDSID,=H'5'             req pddb # 5
          AGO  .LT02
.LT01     ANOP ,
         MVC   QPDSID,=F'5'             req pddb # 5
.LT02     ANOP ,
         NI    QFLAG2,255-Q2VALIDS      NULLIFY VALIDITY FOR LISTDS
         MVI   QGPFLAG,QPFDIR+QPFNMSG   req specific pddb
         QCALL GETPDDB                  locate it
         LTR   R2,R1                    set addr
         BP    LT$FOUND                 yes, use it
         USNGX PDBDSECT,R2              BASE REG FOR PDDB
         QTILT '*** JOB DOES NOT HAVE INTERNAL TEXT ***'
***********************************************************************
*                                                                     *
*  SET UP TO USE "GETREC" ROUTINE                                     *
*                                                                     *
***********************************************************************
LT$FOUND MVI   QCRFLAG,QRFINIT          req init processing
         MVC   QCGRMTTR,PDBMTTR         set starting mttr
*LT$FOUND L     R15,QCSTART              -> TABLE AREA
*        MVC   0(4,R15),PDBMTTR         SET DISK ADDR OF FIRST BLOCK
*        SR    R4,R4                    TELL GETREC "POSITION TO TOP"
*        ZAP   QCHREC,=P'0'             ZERO HIGH REC NBR
*        MVC   QCHPTR,QCSTART           BEGIN OF TBL
          AIF  (&QLEVEL GE 9).LT03
         MVC   QPDSID,=H'5'             set dsid for GETREC val check
          AGO  .LT04
.LT03     ANOP ,
         MVC   QPDSID,=F'5'             req pddb # 5
.LT04     ANOP ,
         DROPX R2                       PDBDSECT
***********************************************************************
*                                                                     *
*   PROCESS RECORDS                                                   *
*                                                                     *
***********************************************************************
LT$LOOP1 QCALL GETREC                   GET A TEXT RECORD
         LTR   R4,R4                    END OF DATASET?
         BZ    QSTOP                    YES - LEAVE
         AP    QPREC,=P'1'              BUMP DESIRED NEXT RECORD NUMBER
         MVC   QDMSG(2),=X'1DE8'        SET OUTPUT, BRIGHT
         MVC   QDMSG+10(8),LT#REC       'RECORD #'
         MVC   QDMSG+19(6),=X'20206B202120'
         LA    R1,QDMSG+24              INIT FOR EDMK
         EDMK  QDMSG+18(7),QCCREC+1     FILL IN CURRENT RECORD NUMBER
         MVC   QDMSG+19(7),0(R1)        LEFT JUSTIFY IT
         MVC   QDMSG+30(2),=X'1D60'     SET OUTPUT, NORMAL
         MVI   QDATRNUM+1,2             SET # OF ATTR BYTES IN LINE
         BAL   R14,QADDLINE
         USNGX LRCDSECT,R4
***********************************************************************
*                                                                     *
*   FORMAT THE TEXT RECORD                                            *
*                                                                     *
***********************************************************************
         SR    R0,R0                    ZERO OFFSET
         LR    R1,R4                    ADDRESS
         LA    R2,LRCTEXT-LRCDSECT(,R6) LENGTH TO FORMAT
         TM    LRCFLAG1,LRC1SPAN        IS THIS A SPANNED RECORD SEG?
         BNO   LT$CALL                  NO - OK
         LH    R2,LRCSEGL               YES - GET LENGTH OF SEGMENT
         TM    LRCFLAG1,LRC1SBGN        IS THIS THE FIRST SEGMENT?
         BO    LT$SPAN1                 YES - USE DIFFERENT HDR LENGTH
         LA    R2,LRCSTEXT-LRCDSECT(,R2)  ACCOUNT FOR LONGER PREFIX
         B     LT$CALL
LT$SPAN1 LA    R2,LRCSFTXT-LRCDSECT(,R2)  ACCOUNT FOR LONGER PREFIX
         DROPX R4                       LRCDSECT
         SPACE 2
LT$CALL  QCALL HEXFMT                   DUMP THIS RECORD
         BAL   R14,QADDLINE             ADD A BLANK LINE
         B     LT$LOOP1                 GO READ NEXT RECORD
******** USNGX TEXT-(LRCTEXT-LRCDSECT),R4  THIS SHOULD GET YOU BUT GOOD
         SPACE 2
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
LT#HDR   DC    C'Internal Text Records'
LT#REC   DC    C'Record #'
         SPACE 3
         QCOMMON
*        ORG   QDMSG
*LT@PFX  DS    CL8,C
         SPACE 3
         Q$PDDB
         Q$LRC
         QZTEXT
