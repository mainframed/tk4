         TITLE '--- QUEUE--FINDRJE -- DETERMINE RJE ID ---'
*---------------------------------------------------------------------*
*                                                                     *
*   FINDRJE - CONVERT INTERNAL ROUTE CODE TO DISPLAY FORMAT           *
*                                                                     *
*   ENTRY - R1 = ADDRESS OF 2 BYTE (NON-SP) OR 4 BYTE (SP) ROUTING    *
*                CODE                                                 *
*                                                                     *
*   EXIT - R1 = ADDRESS OF 8 CHAR SYMBOLIC DESTID                     *
*                                                                     *
*   NOTE - DUAL (SP & NON-SP) SUPPORT IS DEPENDENT ON LENGTH ATTRS    *
*          OF CERTAIN DATA ITEMS BEING APPROPRIATE FOR THE SELECTED   *
*          SYSTEM                                                     *
*                                                                     *
*   Updates:                                                          *
*      16Jun90  LDW  Remove mode switching since we now run AMODE 31  *
*      25Dec89  LDW  Q$xxx -> QZxxx:  CVT, JESCT, SSCT                *
*      23Nov89  LDW  Remove conditional assembly around Qxxx macros   *
*      22Mar89  EMS  SP311 updates: no SVT                            *
*      17Jan87  LDW  Misc cleanup                                     *
*      12Jan87  LDW  Check for compatible SVT before using primary's  *
*      04Jan87  LDW  Use CVT instead of CVTDSECT (for XA?)            *
*      23AUG86  LDW  DELETE Q$XECB (DONE BY Q$SVT)                    *
*      12/06/85 LDW  ADD Q$XECB FOR SP133 $SVT                        *
*      06/21/83 LDW  DISPLAY U0 AS BLANK                              *
*      01/27/83 LDW  DISPLAY N2R0 AS JUST N2                          *
*      01/24/83 LDW  VALIDITY CHECK $SVSID IF TRYING TO USE THE       *
*                       PRIMARY SUBSYSTEM'S RDT'S.  THIS IS TO AVOID  *
*                       THE PROBLEM CASE OF MIXED PRIMARY/SECONDARY   *
*                       JES'S (ONE SP, THE OTHER NOT).                *
*      01/03/83 LDW  SP SUPPORT                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
FINDRJE  QSTART  Q81
         USING WORK,R13                 LOCAL WORK AREA
         LR    R6,R1                    PRESERVE INPUT ADDR
         MVC   FR@RJEID,QBLANK          CLEAR RJE NAME
         CLC   QCLOCAL,0(R6)            IS FILE FOR LOCAL?
         BE    FR$LEAVE                 YES, LEAVE WITH BLANK NAME
         CLC   0(L'QCLOCAL,R6),=F'0'    ANYTHING HERE AT ALL????
         BE    FR$LEAVE                 NO - LEAVE WITH BLANK NAME
         MVI   FR@RJEID,C'U'            ASSUME UNNN
         LA    R2,FR@RJEID+1            -> LOCATION FOR NUMBER
         OC    0(L'QCLNODE,R6),0(R6)    NODE = 0?
         BZ    FR$NBR                   YES, GO FORMAT UNIT NUMBER
         MVI   FR@RJEID,C'R'            NO, ASSUME RNNN
         CLC   QCLNODE,0(R6)            IS FILE FOR OUR NODE?
         BE    FR$NBR                   YES, GO FORMAT REMOTE NUMBER
         MVI   FR@RJEID,C'N'            NO, FORMAT NODE NUMBER
         LA    R3,0(,R6)                -> NODE NUMBER
         BAL   R4,FR$CONV               CONVERT TO DISPLAY
         CLC   L'QCLNODE(L'QCLNODE,R6),=H'0'  RMT 0 AT ANOTHER NODE?
         BE    FR$RMT0                  YES - DON'T DISP AS NXXXR0
         MVI   0(R2),C'R'               FORMATTED NODE, NOW REMOTE
         LA    R2,1(,R2)                -> LOCATION FOR NUMBER
FR$NBR   LA    R3,L'QCLNODE(,R6)        -> REMOTE NUMBER
         BAL   R4,FR$CONV               FORMAT NUMBER
         SPACE 1
FR$RMT0  L     R15,QSSCT                -> SSCT OF SELECTED SUBSYSTEM
         LTR   R15,R15                  DEFINED SUBSYSTEM?
         BZ    FR$PRIM                  NO - TRY TO USE PRIMARY
         USING SSCT,R15
          AIF  (&QLEVEL GE 9).FR09
         L     R15,SSCTSSVT             -> SSVT FOR IT
         DROP  R15
         USING SSVT,R15
          AGO  .FR10
.FR09     ANOP ,
         L     R15,SSCTSUS2             -> HCCT FOR IT
         DROP  R15
         USING HCCT,R15
.FR10     ANOP ,
         LTR   R15,R15                  ANY?
         BNZ   FR$SCAN                  YES - SCAN RDT'S
         SPACE 1
* SELECTED SUBSYSTEM IS NOT ACTIVE, TRY TO USE RDT'S FOR PRIMARY SUBSYS
FR$PRIM  L     R15,CVTPTR               -> CVT
         USING CVT,R15
         L     R15,CVTJESCT             -> JESCT
         USING JESCT,R15
         L     R15,JESSSCT              -> SSCT
         USING SSCT,R15
          AIF  (&QLEVEL GE 9).FR11
         L     R15,SSCTSSVT             -> SSVT
         USING SSVT,R15
          AGO  .FR12
.FR11     ANOP ,
         L     R15,SSCTSUS2             -> HCCT FOR IT
         DROP  R15
         USING HCCT,R15
.FR12     ANOP ,
         LTR   R15,R15                  ANY?
         BZ    FR$LEAVE                 NO - PRIMARY SUBSYS IS NOT UP
*  IF NEITHER THE SELECTED SUBSYS OR THE PRIMARY SUBSYS ARE UP, WE WILL
*  LEAVE THE DESTINATION FORMATTED AS NXXX.RXXX
**%%     TM    $SVSID,X'80'             CRUMMY CHECK FOR VALID SID
**%%     BNO   FR$LEAVE                 NOT VALID - MUST BE ADDR
*---  THIS REALLY IS A MISERABLY LOUSY CHECK, BUT I CAN'T THINK OF ---*
*---  ANY BETTER WAY AT THE MOMENT, SO IT'LL HAVE TO DO.  THIS IS  ---*
*---  ONLY A PROBLEM WHEN WORKING WITH A SECONDARY JES OF A THE    ---*
*---  WRONG TYPE, AND WHEN IT IS NOT UP                            ---*
*
*---  Check to see that the primary JES2's $SVT is the same length ---*
*---  as the one that this load module is assembled for.           ---*
         LR    R14,R15                  COPY $SVT PTR
         SH    R14,=H'8'                BACK UP TO PREFIX
          AIF  (&QLEVEL GE 9).FR17
         CLC   =C'SSVT',0(R14)          IDENTIFIER CORRECT?
         BNE   FR$LEAVE                 NO - SOMETHING'S VERY WRONG
         CLC   =A(SVTLEN+8),4(R14)      LENGTH CORRECT?
         BNE   FR$LEAVE                 NO - PRIMARY IS DIFFERENT REL
          AGO  .FR18
.FR17     ANOP ,
         CLC   =C'HCCTU',0(R14)         IDENTIFIER CORRECT?
         BNE   FR$LEAVE                 NO - SOMETHING'S VERY WRONG
*%%      CLC   =A(CCTLEN+8),6(R14)      LENGTH CORRECT?
*%%      BNE   FR$LEAVE                 NO - PRIMARY IS DIFFERENT REL
.FR18     ANOP ,
         SPACE 1
FR$SCAN  L     R3,$SVRDT                -> FIRST RDT
          AIF  (&QLEVEL GE 9).FR13
         L     R5,$SVRDTE               -> LAST RDT
.FR13     ANOP ,
         DROP  R15
         USING RDTDSECT,R3
         LA    R4,RDTSIZ                LENGTH OF RDT (BXLE INCR)
         SPACE 2
*---  SINCE JES2 DOES NOT FILL IN INFORMATION INTO THE RDT WHICH   ---*
*---  WAS NOT EXPLICITLY ON THE DESTID CARD, WE MUST NOW FIGURE    ---*
*---  OUT THE FULL DESTINATION ID BY FACTORING IN THE ASSUMPTIONS  ---*
*---  (IN OTHER WORDS, IF THE NODE NUMBER IS NOT IN THIS RDT,      ---*
*---  USE OUR LOCAL NODE NUMBER.  IF THIS RDT DESCRIBES A UNIT,    ---*
*---  FORCE THE NODE TO ZERO.)                                     ---*
         SPACE 2
FR$LOOP  MVC   FR@RDTID,QCLOCAL         ASSUME DEST=LOCAL
         TM    RDTFLAG,RDTFLAGN         RDT SPECIFY NODE?
         BNO   FR$L01                   NO, USE ASSUMED NODE
         MVC   FR@NODE,RDTNODE          LOOK FOR SPECIFIED NODE
         SPACE 1
FR$L01   TM    RDTFLAG,RDTFLAGR+RDTFLAGU   RDT SPECIFY NUMBER?
         BZ    FR$L02                   NO, USE ASSUMED NUMBER
         MVC   FR@NBR,RDTRMTNO          LOOK FOR SPECIFIED NUMBER
         TM    RDTFLAG,RDTFLAGU         IS NUMBER A UNIT?
         BNO   FR$L02                   NO, NODE IS FINE
         XC    FR@NODE,FR@NODE          FORCE TO DUMMY NODE
         SPACE 1
FR$L02   CLC   FR@RDTID,0(R6)           IS THIS THE RIGHT RDT?
         BNE   FR$BXLE                  NO - SKIP THIS RDT
         MVC   FR@RJEID,RDTNAME         GET THE UNIT NAME
         B     FR$LEAVE                 ALL DONE
         SPACE 1
          AIF  (&QLEVEL GE 9).FR14
FR$BXLE  BXLE  R3,R4,FR$LOOP            SCAN ALL RDT'S
          AGO  .FR15
.FR14     ANOP ,
FR$BXLE  ICM   R3,B'1111',RDTRDT        scan all
         BNZ   FR$LOOP                           RDT's
.FR15     ANOP ,
         SPACE 1
FR$LEAVE LA    R1,FR@RJEID              PASS BACK ADDR OF REMOTE NAME
         B     QSTOP                    BYE
         SPACE 2
* CONVERT NUMBER FOR DISPLAY
* INPUT:   R2 -- -> THREE BYTE OUTPUT AREA
          AIF   (&QLEVEL GE 4).FR01
*          R3 -- -> ONE BYTE BINARY NUMBER
          AGO   .FR02
.FR01     ANOP
*          R3 -- -> TWO BYTE BINARY NUMBER
.FR02     ANOP
*          R4 -- RETURN ADDRESS
* OUTPUT:  R2 -- UPDATED -> OUTPUT AREA
          AIF   (&QLEVEL GE 4).FR03
FR$CONV  SR    R0,R0                    CLEAR FOR IC
         IC    R0,0(,R3)                GET NNN
          AGO   .FR04
.FR03     ANOP
FR$CONV  LH    R0,0(,R3)                GET NNN
.FR04     ANOP
         CVD   R0,FR@CONV               PREPARE TO PRINT
         MVC   FR@CONV(6),=X'402021204040'  EDIT MASK
         LA    R1,FR@CONV+3             INITIALIZE FOR EDMK
         EDMK  FR@CONV(4),FR@CONV+6     EDIT NUMBER, SET R1
         MVC   0(3,R2),0(R1)            MOVE DATA TO OUTPUT
         LA    R0,FR@CONV+4             END OF DATA PLUS ONE
         SR    R0,R1                    LENGTH OF DATA
         AR    R2,R0                    UPDATE DATA POINTER
         BR    R4                       RETURN TO CALLER
         DROP  R13
         SPACE 2
         LTORG
         SPACE 2
         QCOMMON
WORK     DSECT
         ORG   WORK+72                  SKIP OVER SAVE AREA
FR@RJEID DS    CL8                      OUTPUT AREA
         SPACE 1
FR@CONV  DS    0D                       WORK AREA
          AIF   (&QLEVEL GE 4).FR05
FR@RDTID DS    0XL2                     WORKING ID OF RDT
FR@NODE  DS    X                        NODE OF RDTID
FR@NBR   DS    X                        NUMBER OF RDTID
          AGO   .FR06
.FR05     ANOP
FR@RDTID DS    0XL4                     WORKING ID OF RDT
FR@NODE  DS    H                        NODE OF RDTID
FR@NBR   DS    H                        NUMBER OF RDTID
.FR06     ANOP
         SPACE 2
         Q$SVT
         Q$HCCT
         Q$RDT
         QZCVT
         QZJESCT
         QZSSCT
