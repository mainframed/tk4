         TITLE '--- QUEUE--SYSLOG -- PRINT THE SYSTEM LOG DATASET ---'
*---------------------------------------------------------------------*
*                                                                     *
*   SYSLOG - DISPLAY THE SYSTEM LOG                                   *
*                                                                     *
*   Updates:                                                          *
*      17Jan90  LDW  Finish the SP311 Update (with EMS)               *
*      26Apr89  EMS  Update for SP311                                 *
*      20Feb88  LDW  Set Q3SYSLOG                                     *
*                    Change default column number to 0                *
*      21Mar87  LDW  Fix for DSID = 0 (from RSCS)                     *
*      04/11/84 LDW  JCTDSECT -> JCTSTART                             *
*      02/01/84 LDW  CHANGE STACK LEVEL FROM X'10' TO X'30' TO MAKE   *
*                       IT SO THAT FIRST 'END' DOESN'T RETURN TO "SL" *
*      01/19/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*      07/19/83 LDW  SET QDHDSID                                      *
*      03/21/83 LDW  IF FIRST OPERAND IS '*', CHANGE TO BLANKS        *
*      02/01/83 LDW  DON'T SET QSC2SLOG IF SEARCHING BY JOBID         *
*      01/19/83 LDW  USE QVSYSID TO POINT TO SYSTEM ID TABLE          *
*                    SET QPOFFDEF                                     *
*      01/03/83 LDW  SUPPORT M.A.S. SPOOL                             *
*      12/20/82 LDW  CHANGE QTILT REPRMPT=YES TO OPTIONS=REPROMPT     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
SYSLOG   QSTART  Q21,STACK=X'30'
         OI    QSCFLAG2,QSC2CKPT        FORCE CHECKPOINT REFRESH
         L     R10,QVSYSID              -> SYSTEM ID TABLE
         CLC   =CL8'SYSLOG',QPARM1      DID HE SAY 'SYSLOG'
         BE    SL$CURNT                 YES - USE CURRENT SYSTEM ID
         CLI   QPARM1,C'*'              WANT LAST REFERENCED SYSLOG?
         BE    SL$USE                   YES - SEE IF WE CAN DO IT
         CLI   QPARM1,C' '              ANY OPERAND?
         BNE   SL$CHECK                 YES - USE IT
SL$USE   CLC   QJOBNAME,=CL8'SYSLOG'    IS CURRENT JOB 'SYSLOG'?
         BNE   SL$CURNT                 NO - GO SELECT CURRENT SYSLOG
         MVC   QLNG1,=H'5'              YES - FAKE OUT LENGTH AND ...
         MVC   QPARM1(1),QEXTJBID       ... JOBID TO RESELECT THE ...
         MVC   QPARM1+1(4),QEXTJBID+4   ... PREVIOUSLY SELECTED SYSLOG
         B     SL$FINDJ                 AND GO PROCESS IT
SL$CURNT MVC   QPARM1,QBLANK            CLEAR THE PARM
         MVC   QPARM1(4),0(R10)         NO - DEFAULT TO CURRENT SYSTEM
         MVI   QLNG1+1,4                     SET LENGTH
SL$CHECK CLI   QLNG1+1,4                PARM TOO LONG FOR SYSTEM ID?
         BH    SL$FINDJ                 YES - ASSUME IT'S A JOB NUMBER
         LA    R14,L'QSYSID(,R10)       -> SYSTEM ID #1
         LA    R15,7                    NUMBER OF SYSTEM IDS
SL$LOOP  CLC   QPARM1(4),0(R14)         IS THIS IT?
         BE    SL$SIDOK                 YES
         LA    R14,L'QSYSID(,R14)       -> NEXT SYSTEM ID
         BCT   R15,SL$LOOP              KEEP LOOKING
         B     SL$FINDJ                 NOT FOUND - ASSUME IT'S JOBID
SL$SIDOK MVC   QPARM3(4),QPARM1         MOVE THE SYSTEM ID
         MVC   QPARM1,=CL8'SYSLOG'      AND LOOK FOR SYSLOG BY NAME
         MVI   QLNG1+1,6                SET LENGTH
         OI    QSCFLAG2,QSC2SLOG        TELL FINDJOB "SYSLOG BY NAME"
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
SL$FINDJ QCALL FINDJOB                  FIND THE JOB
***********************************************************************
*                                                                     *
*   CHECK JOBNAME EQUAL SYSLOG                                        *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA                ADDR OF IOAREA FOR JCT
         USING JCTSTART,R9              BASE REG FOR JCT
         CLC   JCTJNAME,=CL8'SYSLOG'    IS THIS SYSLOG?
         BNE   SL$TILT                  NO. TILT.
***********************************************************************
*                                                                     *
*   DETERMINE NUMBER OF DATASETS FROM CURRENT DATASET                 *
*                                                                     *
***********************************************************************
         MVC   QDHDSID,QPARM2           SET DSID FOR HEADER LINE
          AIF  (&QLEVEL GE 9).SL01
         LH    R3,JCTPDDBK              HIGHEST DATASET ID NUMBER
          AGO  .SL02
.SL01     ANOP ,
         L     R3,JCTPDDBK              HIGHEST DATASET ID NUMBER
.SL02     ANOP ,
         LH    R1,QLNG2                 LENGTH OF BACKUP PARM
         SH    R1,=H'1'                 IS THE BACKUP PARM ZERO LENGTH?
         BM    SL$CALST                 YES. SKIP.
         CLI   QPARM2,C'-'              IS THERE A MINUS SIGN?
         BNE   *+8                      NO. SKIP.
         MVI   QPARM2,C'0'              CHANGE MINUS TO ZERO
         LA    R15,QPARM2               POINT TO PARM
         BAL   R14,QNUMCONV             CONVERT IT
         BNE   SL$TILT                  QUIT IF NOT NUMERIC
         SR    R3,R15                   BACK UP NUMBER OF DATASETS
         CH    R3,=H'101'               IS THE NUMBER LESS THAN 101?
         BL    SL$TILT                  YES. TILT.
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
          AIF  (&QLEVEL GE 9).SL03
SL$CALST STH   R3,QPDSID                STORE DATASET ID
          AGO  .SL04
.SL03     ANOP ,
SL$CALST STCM  R3,B'1111',QPDSID        STORE DATASET ID
.SL04     ANOP ,
         OI    QFLAG2,Q2VALIDS          INDICATE VALIDITY FOR LISTDS
         OI    QFLAG3,Q3SYSLOG          tell LISTDS it's SYSLOG
*  Q3SYSLOG is reset by FINDJOB
**       MVC   QPOFFSET,=H'5'           PRINT OFFSET (WAS ZERO)
**       MVC   QPOFFDEF,=H'5'           AND SET DEFAULT
         MVC   QPOFFSET,=H'0'           PRINT OFFSET
         MVC   QPOFFDEF,=H'0'           AND SET DEFAULT
         QCALL LISTDS
         B     QSTOP
SL$TILT  QTILT '*** INVALID PARAMETER ***',OPTIONS=REPROMPT
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG ,
         SPACE 3
         DROP  R9                       JCTSTART
         SPACE 3
         QCOMMON
         SPACE 3
         Q$JCT
