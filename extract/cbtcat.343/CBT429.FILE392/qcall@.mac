         MACRO
&NFS     QCALL &ROUTINE,&TYPE=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QCALL - Invoke another QUEUE routine                              *
.*                                                                    *
.*  Note - Generated code intentionally shifted right 1 column        *
.*                                                                    *
.*  Updates:                                                          *
.*     04Jun89  LDW  Change APF to AUTHFUNC                           *
.*     19Jul87  EMS  delete QSORT                                     *
.*     10Jun87  EMS  Change FMTSUBS to QSUBS                          *
.*                   Add GETPDDB                                      *
.*     22May87  EMS  Add FMTSUBS,QSORT                                *
.*                   Update to facilitate adding routines             *
.*                   Fix bugs relating to continued lines             *
.*     29Nov86  LDW  Add DATEFMT                                      *
.*     03/31/86 LDW  CHANGE DISPCHG TO DISP80 & DISP132               *
.*     01/20/84 LDW  FIX BUGS IN 01/13/83 CODE                        *
.*     01/13/83 LDW  HANDLE CASE OF MORE CALLERS THAN FIT ON ONE LINE *
.*     12/13/82 LDW  MOVE CODE FROM QXREF MACRO INTO QCALL.  (QXREF   *
.*                      NOW DELETED.)  ADD TYPE=XREF TO PRINT XREF.   *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
         GBLC  &QNAMES(400),&QSECT
         LCLC  &TO,&FROM,&ENT
         LCLA  &I
         AIF   ('&ROUTINE' EQ '' AND '&TYPE' EQ '').ERR1
         AIF   ('&ROUTINE' NE '' AND '&TYPE' NE '').ERR2
         AIF   ('&ROUTINE' NE '').ROUTINE
         AIF   ('&TYPE' EQ 'XREF').XREF
         MNOTE 8,'TYPE=&TYPE INVALID - MACRO TERMINATED'
         MEXIT
.ERR1    MNOTE 8,'EITHER ROUTINE OR TYPE= MUST BE SPECIFIED'
         MEXIT
.ERR2    MNOTE 8,'ROUTINE AND TYPE MAY NOT BOTH BE SPECIFIED'
         MEXIT
.ROUTINE AIF   ('&ROUTINE' EQ '(R15)').R15
&NFS      L     R15,=V(&ROUTINE)         FIND ROUTINE
          BALR  R14,R11                  INVOKE "QCALL"
.*
.* UPDATE QCALL XREF TABLE FOR PRINTING AT END OF ASSEMBLY
.*
&TO      SETC  '&ROUTINE       '(1,8)
&FROM    SETC  '&QSECT         '(1,8)
         AIF   ('&SYSECT' NE 'QCOMMON').NOTQCMN
&FROM    SETC  'QCOMMON '
.NOTQCMN AIF   ('&QNAMES(1)' NE '').NAMESET  SKIP IF ALREADY SET
&I       SETA  1
&QNAMES(&I) SETC 'ALLOCATE --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'AUTHFUNC --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'CHANGES  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'CKPT     --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'CPU      --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'DATEFMT  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'DDNAME   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'DISPADD  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'DISPADDX --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'DISP132  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'DISP80   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'DISPLAY  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'FINDJOB  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'FINDRJE  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'FORMAT   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'GETPDDB  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'GETREC   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'HELP     --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'HEXBLK   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'HEXFMT   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'INITIAL  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'INITS    --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'JESNEWS  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'JOBINFO  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'LIST     --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'LISTDS   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'LISTTEXT --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'MCS      --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'MODE     --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'OPERCMD  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'PARSE    --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'PFKSET   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'PRINT    --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'QINNARDS --- '
&I       SETA  &I+1
.*  QCOMMON AND QUEUE ARE NOT CALLED
&QNAMES(&I) SETC 'QSUBS    --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'READSPC  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'REPOS    --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'RMTLIST  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'SAVE     --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'SEARCH   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'SYSLIST  --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'SYSLOG   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'SYSOUT   --- '
&I       SETA  &I+1
&QNAMES(&I) SETC 'TSOCMD   --- '
&I       SETA  &I+1
.*   45 Entires used
.NAMESET ANOP
&I       SETA  1
.NAMELP1 ANOP
         AIF   ('&QNAMES(&I)        '(1,8) EQ '&TO').FOUND1
&I       SETA  &I+1
         AIF   ('&QNAMES(&I)        '(1,8) NE '        ').NAMELP1
         MNOTE 0,'"&TO" NOT FOUND IN XREF TABLE - CALLS NOT XREF''D'
         AGO   .MEXIT
.FOUND1  ANOP
&ENT     SETC  '&QNAMES(&I)'
         AIF   (K'&ENT GT 60).TRY2      SET UP A CONTINUATION LINE
&QNAMES(&I) SETC '&ENT&FROM  '
         AGO   .MEXIT
.TRY2    ANOP
&I       SETA  &I+50
         AIF   (&I GT 400).ERR3
         AIF   ('&QNAMES(&I)' NE '').FOUND1   SKIP IF ALREADY SET
&QNAMES(&I) SETC '             '        INDENT CONTINUATION LINE
         AGO   .FOUND1                  NOW GO SAVE THIS CALL
.ERR3    MNOTE 0,'NO SPACE FOR CALL XREF (SEE "QCALL" MACRO)'
         AGO   .MEXIT
.R15     ANOP
&NFS      BALR  R14,R11                  INVOKE "QCALL"
.MEXIT   SPACE 1
         MEXIT
.*
.*  DUMP THE XREF TABLE
.*
.XREF    SPACE 2
         MNOTE '                    *** QUEUE SUBROUTINE CALLEE/CALLER $
               XREF ***'
         SPACE 2
         MNOTE '  ROUTINE      CALLED BY'
         MNOTE '  -------      ---------'
         SPACE 1
.LOOP    ANOP
&I       SETA  &I+1
         AIF   ('&QNAMES(&I)' EQ '').MEND
         MNOTE '  &QNAMES(&I)'
         AIF   ('&QNAMES(&I+50)' EQ '').NOCONT
         MNOTE '  &QNAMES(&I+50)'
         AIF   ('&QNAMES(&I+100)' EQ '').NOCONT
         MNOTE '  &QNAMES(&I+100)'
         AIF   ('&QNAMES(&I+150)' EQ '').NOCONT
         MNOTE '  &QNAMES(&I+150)'
         AIF   ('&QNAMES(&I+200)' EQ '').NOCONT
         MNOTE '  &QNAMES(&I+200)'
         AIF   ('&QNAMES(&I+250)' EQ '').NOCONT
         MNOTE '  &QNAMES(&I+250)'
         AIF   ('&QNAMES(&I+300)' EQ '').NOCONT
         MNOTE '  &QNAMES(&I+300)'
         AIF   ('&QNAMES(&I+350)' EQ '').NOCONT
         MNOTE '  &QNAMES(&I+350)'
.NOCONT  SPACE 1
         AGO   .LOOP
.MEND    MEND
