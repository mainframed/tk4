         TITLE '--- QUEUE--ALLOCATE -- DATASET ALLOCATION ROUTINES ---'
*---------------------------------------------------------------------*
*                                                                     *
*   ALLOCATE - INTERFACE TO DYNAMIC ALLOCATION                        *
*                                                                     *
*   ENTRY - FIELDS FILLED IN IN DAIR BLOCKS IN QCOMMON                *
*                                                                     *
*   Updates:                                                          *
*      17Jan86  LDW  Misc cleanup                                     *
*      10Jan86  LDW  Display DDname and DSName in failure message     *
*      08/30/84 LDW  USE HEX MACRO                                    *
*      03/17/83 LDW  DISPLAY RETURN CODE WHEN ALLOCATION FAILS        *
*      12/21/82 LDW  RE-INSTALL BIT TEST FOR "SAVE"                   *
*      12/10/82 LDW  TEST FOR "SAVEDS" SKIP ROBINSON'S WAY            *
*                       ("SAVEDS" USED TO BE NAMED "SAVE")            *
*      12/08/82 LDW  PUT IN IMPROVED TEST FOR "SAVE"                  *
*      11/11/82 EMS  CORRECT THE TEST FOR CURRENTLY ALLOCATED DSN     *
*                    CORRECT THE TEST FOR CALLER = "SAVE"             *
*      10/21/82 LDW  PUT IN EMS'S FIX FOR CHECKING TO SEE IF DSN IS   *
*                       CURRENTLY ALLOCATED FOR ALL DATASETS, NOT     *
*                       JUST HASPACE                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
ALLOCATE QSTART  Q2
***********************************************************************
*                                                                     *
*   DETERMINE FUNCTION                                                *
*                                                                     *
***********************************************************************
         CLI   DAIRFLAG,X'08'           IS THE FUNCTION ALLOCATE?
         BE    AL$ALLOC                 YES. DO IT.
         CLI   DAIRFLAG,X'18'           IS THE FUNCTION FREE?
         BE    AL$FREE                  YES. DO IT.
         CLI   DAIRFLAG,X'2C'           IS THE FUNCTION SET NOT_IN_USE?
         BE    UNUSE                    YES. DO IT.
         B     AL$FATAL                 INVALID FUNCTION. ABANDON SHIP.
***********************************************************************
*                                                                     *
*   FREE DDNAME(XXXXXXXX)                                             *
*                                                                     *
***********************************************************************
AL$FREE  LA    R1,DA18CD                LOAD ADDR OF FREE PARM LIST
         BAL   R2,AL$CDAIR              CALL DAIR
         B     QSTOP                    RETURN TO CALLER
***********************************************************************
*                                                                     *
*   ALLOC DDNAME(XXXXXXXX) DSNAME(YYYYYYYY) SHR                       *
*                                                                     *
***********************************************************************
AL$ALLOC LM    R4,R5,DA08UNIT           SAVE THE UNIT
         L     R3,DA08PDSN              -> DSN WE ARE ALLOCATING
         LH    R15,0(,R3)               GET LEN OF DATASET BEING ALLOC
         BCTR  R15,0                    -1 FOR EX
         L     R1,X'218'                -> TCB
         L     R1,12(,R1)               -> TIOT
         LA    R1,24(,R1)               -> DEVICE SECTION
ALLOC02  CLI   4(R1),C'A'               VALID DDNAME?
         BL    ALLOC04                  NO - IGNORE THIS ENTRY
         ICM   R14,B'0111',12(R1)       -> JFCB-16 !!!
         EX    R15,ALLOCCLC             THIS THE DSN WE ARE ALLOCATING?
         BNE   ALLOC04                  NO - TRY NEXT
         CH    R15,=Y(44-1)             MAX DSN LEN?
         BE    ALLOC06                  YES, THEN IT MUST OURS
         LA    R6,16+1(R15,R14)         -> END OF DSN
         CLI   0(R6),C' '               ANYTHING ELSE IN DSN?
         BNE   ALLOC04                  YES - THIS AIN'T IT THEN
ALLOC03  CLC   DA08SER(6),QBLANK        ANY VOLSER SPEC?
         BE    ALLOC06                  NO - USE THIS ONE THEN
         CLC   DA08SER(6),16+118(R14)   YES - IS THIS THE RIGHT VOLUME?
         BE    ALLOC06                  YES - USE IT
*                                       NO - TRY NEXT
ALLOC04  SR    R0,R0                    CLEAR FOR IC
         IC    R0,0(,R1)                GET ENTRY LENGTH
         AR    R1,R0                    -> NEXT TIOT ENTRY
         CLC   =F'0',0(R1)              END OF TIOT?
         BNE   ALLOC02                  NO - KEEP LOOKING
         B     ALLOC20                  YES - DO NORMAL ALLOCATE
ALLOCCLC CLC   2(*-*,R3),16+00(R14)     <<  EXECUTED  >>
ALLOC06  MVC   DA08DDN,4(R1)            MOVE DDNAME TO RETURN AREA
         B     QSTOP                    AND PRETEND THAT WE ALLOCATED
ALLOC20  MVC   DA08DDN,QBLANK           BLANK THE DDNAME AREA
         LA    R1,DA08CD                LOAD ADDR OF ALLOC PARM LIST
         BAL   R2,AL$CDAIR              CALL DAIR - ALLOC THE DATASET
         STM   R4,R5,DA08UNIT           RESTORE THE UNIT
         B     QSTOP                    RETURN TO CALLER
***********************************************************************
*                                                                     *
*   MARK DDNAME "NOT IN USE"                                          *
*                                                                     *
***********************************************************************
UNUSE    LA    R1,DA2CCD                LOAD ADDR OF UNUSE PARM LIST
         BAL   R2,AL$CDAIR              CALL DAIR - MARK "NOT IN USE"
         B     QSTOP                    RETURN TO CALLER
***********************************************************************
*                                                                     *
*   CALL DYNAMIC ALLOCATION INTERFACE ROUTINE (DAIR)                  *
*                                                                     *
***********************************************************************
AL$CDAIR ST    R1,DAPLDAPB              SET PARM LIST ADDR IN CALL LIST
         XC    DAIRECB,DAIRECB          CLEAR ECB
         LA    R1,DAPLUPT               LOAD ADDR OF DAIR CALL LIST
         LINK  EP=IKJEFD00              CALL DAIR
         LTR   R15,R15                  IS RETURN CODE ZERO?
         BZR   R2                       YES. RETURN.
         CH    R15,=H'28'               IS DATASET ALREADY FREE?
         BER   R2                       YES. RETURN.
         ST    R15,QNUMWORK
         MVC   QERRMSG(70),=C'*** DATASET ALLOCATION FAILED - R15=XXXXX$
               XXX, DARC=XXXX, CTRC=XXXX ***'
         HEX   QERRMSG+36,QNUMWORK,BYTE=C',',HEXTAB=QHEXTAB
         HEX   QERRMSG+51,DA08DARC,BYTE=C',',LEN=2
         HEX   QERRMSG+62,DA08CTRC,LEN=2
         TM    QSCFLAG2,QSC2ALNA        NO ABEND?
         BNO   AL$FATAL                 NO. ABEND.
         QTILT *
***********************************************************************
*                                                                     *
*   DAIR HAS FAILED. TAKE A PICTURE AND GO HOME.                      *
*                                                                     *
***********************************************************************
AL$FATAL LA    R1,QERRMSG               -> MSG
         LA    R0,L'QERRMSG             LENGTH
         TPUT  (1),(0),R                TELL THEM A SAD STORY
         CLI   DAIRFLAG,X'08'           IS IT ALLOC?
         BNE   AL$ABEND                 NO - NO SECOND MESSAGE
         MVC   QERRMSG,QBLANK           CLEAR THE ERROR MESSAGE AREA
         MVC   QERRMSG(23),=C'*** UNIT=XXXXXXXX  VOL='
         MVC   QERRMSG+09(8),DA08UNIT   FILL IN THE UNIT
         MVC   QERRMSG+23(8),DA08SER    FILL IN THE VOLUME
         LA    R1,QERRMSG               -> MSG
         LA    R0,L'QERRMSG             LENGTH
         TPUT  (1),(0),R                TELL THEM A SAD STORY
         MVC   QERRMSG,QBLANK           CLEAR THE ERROR MESSAGE AREA
         MVC   QERRMSG(25),=C'*** DDNAME=XXXXXXXX  DSN='
         MVC   QERRMSG+11(8),DA08DDN    FILL IN THE DDNAME
         L     R15,DA08PDSN             -> DSN WE ARE ALLOCATING
         LH    R14,0(,R15)              GET LENGTH OF DSNAME
         BCTR  R14,0                    -1 FOR EX
         EX    R14,AL$ERMVC             MVC QERRMSG+25(*-*),2(R14)
         LA    R1,QERRMSG               -> MSG
         LA    R0,L'QERRMSG             LENGTH
         TPUT  (1),(0),R                TELL THEM A SAD STORY
AL$ABEND ABEND 99,DUMP                  ABEND THE JOB. USER CODE 0099.
AL$ERMVC MVC   QERRMSG+25(*-*),2(R15)   << EXECUTED >>
***********************************************************************
*                                                                     *
*   CONSTANTS AND ASSORTED GARBAGE                                    *
*                                                                     *
***********************************************************************
         LTORG ,
         SPACE 3
         QCOMMON
