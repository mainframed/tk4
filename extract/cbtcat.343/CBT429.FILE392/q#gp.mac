    TITLE '--- QUEUE--GETPDDB -- GET A PDDB FROM CURRENT SPOOL IOT ---'
*---------------------------------------------------------------------*
*                                                                     *
*   GETPDDB - GET EITHER NEXT OR A SPECIFIC PDDB FROM THE CURRENTLY   *
*            SELECTED JOB                                             *
*                                                                     *
*   ENTRY - QGPFLAG set bits                                          *
*           QPFINIT init to first pddb (seq processing)               *
*           QPFDIR  get specific pddb id in QPDSID                    *
*           QPFSPIN processing SPIN IOT chain (or start on for init)  *
*           QPFNMSG suppress QTILT on not found                       *
*           QPFSNUL suppress null PDDB's                              *
*           QPFNOSW don't switch IOT chains                           *
*           QPFNIOT just read a new IOT                               *
*                                                                     *
*   EXIT  - R1    -> new current PDDB                                 *
*                                                                     *
*   UPDATES:                                                          *
*     31Jul90  EMS  Update for SP313                                  *
*     17Jul87  EMS  Fix for first IOT in spin IOT chain               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
GETPDDB  QSTART  Q??,WORKLEN=GP@WORKL
         USNGX WORK,R13
         L     R3,QCIOTA                Load base reg
         USNGX IOTSTART,R3              BASE REG FOR IOT
         LR    R5,R3                    IOAREA FOR READ IOT BLOCK
         MVI   GP@FLAGS,0               clear flags
         NI    QGPFLAG,255-QPFNIOT      no new iot
         TM    QGPFLAG,QPFINIT+QPFDIR   Init/Direct REQ?
         BNZ   GP$FIOT                  yes - GO GET A PDDB
         SPACE 1
         LH    R6,QCPDBOFF              CURRENT PDDB OFFSET
         AR    R6,R3                    -> LAST ONE PROCESSED
         LR    R4,R3                    BASE OF IOT
         A     R4,IOTPDDBP              OFFSET BEYOND LAST PDDB
         SH    R4,=Y(BUFSTART-BUFDSECT) ACCOUNT FOR BUFFER PREFIX
         B     GP$DS$NX                 GET NEXT ONE
         SPACE 1
GP$FIOT  MVC   QCCBID,=CL4'IOT'         REQ IOT
         L     R4,QCJCTA                -> jct
         L     R4,JCTIOT-JCTSTART(,R4)  GET MTTR
         TM    QGPFLAG,QPFSPIN          start on spin iots?
         BZ    GP$RIOT                  no, get first iot
         L     R4,QCJCTA                -> jct
         ICM   R4,B'1111',JCTSPIOT-JCTSTART(R4)  Get MTTR
         BZ    GP$TILT2                 if none, exit now
GP$RIOT  BAL   R8,GP$READ               GET FIRST IOT
         OI    QGPFLAG,QPFNIOT          now got new iot
***********************************************************************
*                                                                     *
*   LOCATE PDDB FOR DATASET SPECIFIED IN QPDSID                       *
*                                                                     *
***********************************************************************
GP$NXIOT LR    R4,R3                    BASE OF IOT
         A     R4,IOTPDDBP              OFFSET BEYOND LAST PDDB
         SH    R4,=Y(BUFSTART-BUFDSECT) ACCOUNT FOR BUFFER PREFIX
         LR    R6,R3                    BASE OF IOT
          AIF   (&QLEVEL GE 5).GP01
         A     R6,QCPDDB1               OFFSET TO FIRST PDDB IN IOT
          AGO   .GP02
.GP01     ANOP
         A     R6,IOTPDDB               OFFSET TO FIRST PDDB IN IOT
         SH    R6,=Y(BUFSTART-BUFDSECT) ACCOUNT FOR BUFFER PREFIX
.GP02     ANOP
         USNGX PDBDSECT,R6              BASE REG FOR PDDB
GP$DS$LP TM    QGPFLAG,QPFDIR           direct request?
         BZ    GP$FOUND                 no, return with PDDB addr
         CLC   =C'$JESNEWS',QJOBNAME    IS THIS THE JESNEWS DATASET?
         BE    GP$FOUND                 YES - JUST USE THE FIRST PDDB
         CLC   QPDSID,PDBDSKEY          IS THIS THE DATASET?
         BE    GP$FOUND                 YES. CONTINUE.
GP$DS$NX LA    R6,PDBLENG(,R6)          NO. LOOK AT NEXT PDDB.
         CR    R6,R4                    HAVE WE PAST THE LAST PDDB?
         BL    GP$DS$LP                 NO. TRY AGAIN.
         L     R4,IOTIOTTR              DISK ADDR OF NEXT IOT
GP$SPIN  LTR   R4,R4                    IS THERE ANOTHER IOT?
         BNZ   GP$RIOT                  yes - read it
*  Fall through to search spin iot chain
GP$SPIOT TM    QGPFLAG,QPFSPIN          SPIN IOT SEARCHED YET?
         BO    GP$TILT2                 YES - TILT
         TM    QGPFLAG,QPFNOSW          don't switch iot chains?
         BO    GP$TILT2                 yes - tilt
         OI    QGPFLAG,QPFSPIN          SET SWITCH
         L     R1,QCJCTA                LOAD BASE REG
         L     R4,JCTSPIOT-JCTSTART(,R1)  DISK ADDR OF SPIN IOT
         B     GP$SPIN                  SEARCH THE SPIN IOT CHAIN
         SPACE 1
GP$TILT2 XR    R6,R6                    clear jic
         TM    QGPFLAG,QPFDIR           direct req?
         BZ    GP$EXIT                  no, exit now
         LA    R6,GP@PDDB               -> null pddb
         LNR   R6,R6                    indicate null
         TM    GP@FLAGS,GP@FNULL        did we get one already?
         BO    GP$EXIT                  yes, return with it
         NI    QFLAG2,255-Q2VALIDS      NULLIFY VALIDITY FOR LISTDS
         TM    QGPFLAG,QPFNMSG          suppress msg?
         BO    GP$EXIT                  yes, skip it
         MVC   QERRMSG,QBLANK           CLEAR THE MSG AREA
         MVC   QERRMSG(14),=C'*** DATASET ID'
         MVC   QERRMSG+15(8),QDHDSID    SHOW THE DSID
         MVC   QERRMSG+24(13),=C'NOT FOUND ***'
         QTILT *,OPTIONS=REPROMPT
         SPACE 1
GP$FOUND TM    QGPFLAG,QPFDIR           direct mode?
         BZ    GP$TNULL                 no, exit
         TM    PDBFLAG1,PDB1NULL        null pddb?
         BZ    GP$EXIT                  no, use it
          AIF   (&QLEVEL GE 10).GP03
         MVC   GP@PDDB,PDBDSECT         save null pddb
          AGO  .GP04
.GP03     ANOP
         MVC   GP@PDDB(256),PDBDSECT    save null pddb
         MVC   GP@PDDB+256(L'GP@PDDB-256),PDBDSECT+256  get it all
.GP04     ANOP
         OI    GP@FLAGS,GP@FNULL        indicate we got it
         B     GP$DS$NX                 and try again
GP$TNULL TM    QGPFLAG,QPFSNUL          suppress null pddbs?
         BZ    GP$EXIT                  no, exit
         TM    PDBFLAG1,PDB1NULL        null pddb?
         BZ    GP$EXIT                  no, use it
         B     GP$DS$NX                 yes, try next one
GP$EXIT  NI    QGPFLAG,255-QPFINIT-QPFDIR
         LR    R1,R6                    copy addr for return
         S     R6,QCIOTA                get offset into iot
         STH   R6,QCPDBOFF              save for next pass
         B     QSTOP
         SPACE 3
         DROPX R6,R13                   PDBDSECT,work
         SPACE 2
***********************************************************************
*                                                                     *
*   READ A BLOCK FROM HASPACE                                         *
*                                                                     *
***********************************************************************
GP$READ  ST    R4,QCTRAK                STORE DISK ADDR
         LR    R1,R5                    IOAREA ADDRESS
         QCALL READSPC                  READ HASPACE
         BR    R8                       RETURN TO CALLER
         SPACE 2
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG ,
         SPACE 3
WORK     DSECT ,
         ORG   WORK+72
GP@FLAGS DS    X                        misc flags
GP@FNULL EQU   X'80'                    found a null pddb (direct)
GP@PDDB  DS    XL(PDBLENG)              save area for null pddb
GP@WORKL EQU   *-WORK                   len of workarea
         SPACE 3
         QCOMMON
         SPACE 3
         Q$JCT
         Q$IOT
         Q$PDDB
