 TITLE '--- QUEUE--CHANGES -- DISPLAY RECENT QUEUE COMMAND CHANGES ---'
*---------------------------------------------------------------------*
*                                                                     *
*   Changes - Display recent QUEUE command changes                    *
*                                                                     *
*   Updates:                                                          *
*      04Jul91  LDW  Make CHGSTEXT a WXTRN                            *
*      23Nov89  LDW  Eliminate usage of LC macro                      *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      01/17/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*      06/16/83 LDW  FIX BUG THAT CAUSED 0C4 IF COMMAND RE-ENTERD     *
*                       AFTER SEQUENCING PAST END OF DISPLAY          *
*      02/23/83 LDW  MOVE ACTUAL CHANGES TEXT TO SEPARATE LOAD MODULE *
*      02/10/83 LDW  ADD SUPPORT FOR ATTR BYTES IN CHANGES TEXT LINES *
*      02/02/83 LDW  ONLY DISPLAY LIST OF INTERNAL CHANGES IF THE     *
*                       KEYWORD "INTERNAL" IS ENTERED                 *
*      01/04/83 LDW  NEW ROUTINE                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         WXTRN CHGSTEXT
         SPACE 3
CHANGES  QSTART  STACK=X'C0'
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
         MVC   QDHLINE(L'CH#HDR),CH#HDR MOVE HEADER
         L     R1,=A(CHGSTEXT)          -> CSECT
         LTR   R1,R1                    IS IT LINK EDITTED IN?
         BNZ   CH$GO                    YES - USE IT
         L     R1,QVCHGS                -> TEXT LOAD MODULE
         LTR   R1,R1                    IS IT LOADED YET?
         BNZ   CH$GO                    YES - USE IT
         SPACE 1
         LOAD  EPLOC==CL8'QCHGTXT',ERRET=CH$OOPS
         SPACE 1
         ST    R0,QVCHGS                SAVE ADDR FOR NEXT TIME
         LR    R1,R0                    GET ADDR IN CORRECT REGISTER
         SPACE 2
CH$GO    L     R5,0(,R1)                -> FIRST LINE
         CLI   QKEYWORD,C'I'            WANT LIST OF INTERNAL CHANGES?
         BNE   CH$LOOP                  NO - GO START
         MVC   QDHLINE(L'CH#HDRX),CH#HDRX  MOVE OTHER HEADER
         L     R5,4(,R1)                -> FIRST LINE
         SPACE 1
CH$LOOP  SR    R7,R7                    CLEAR FOR IC
         IC    R7,0(,R5)                GET LINE LENGTH-1
         TM    1(R5),1                  LINE FOR AUTH USERS ONLY?
         BNO   CH$NAUTH                 NO - CONTINUE
         TM    QXAUTH,QXAUTHX           WELL, IS HE AUTHORIZED?
         BZ    CH$SKIP                  NO - SKIP THIS LINE
CH$NAUTH EX    R7,CH$MVC                MVC QDMSG(*-*),3(R5)
         MVC   QDATRNUM+1(1),2(R5)      SET NUMBER OF ATTR BYTES
         BAL   R14,QADDLINE             MOVE LINE TO SCREEN
CH$SKIP  LA    R5,3+1(R7,R5)            -> NEXT LINE
         CLI   0(R5),X'FF'              LAST LINE?
         BNE   CH$LOOP                  NO - CONTINUE
         SPACE 1
         L     R1,=A(CHGSTEXT)          -> CSECT
         LTR   R1,R1                    IS IT LINK EDITTED IN?
         BNZ   QSTOP                    YES - NO DELETE REQUIRED
         SPACE 1
         DELETE  EPLOC==CL8'QCHGTXT'    GET RID OF MODULE
         SPACE 1
         XC    QVCHGS,QVCHGS            INDICATE MODULE NOT LOADED
         B     QSTOP                    BAIL OUT
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
CH$OOPS  QTILT '*** CHANGES MODULE UNAVAILABLE -- CONTACT SYSTEMS GROUP$
                ***'
CH$MVC   MVC   QDMSG(*-*),3(R5)         << EXECUTED >>
         SPACE 1
         LTORG
         SPACE 1
CH#HDR   DC    C'Change history of the QUEUE command:'
CH#HDRX  DC    C'Internal change history of the QUEUE command:'
         SPACE 1
         ENTRY CH#KEYS
CH#KEYS  DC    CL8'INTERNAL'
         DC    X'FF'                    FLAG END OF TABLE
         SPACE 3
         QCOMMON
