         MACRO
&NFS     QTILT &MESSAGE,&LENGTH,&LENFIX=,&OPTIONS=
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QTILT - EXIT CURRENT ROUTINE WITH ERROR MESSAGE                   *
.*                                                                    *
.*  UPDATES:                                                          *
.*     03/09/83 LDW  MOVE ERROR MESSAGE DIRECTLY TO 'QERRMSG'         *
.*     02/25/83 LDW  REMOVE &STOP SUPPORT                             *
.*     01/13/83 LDW  SUPPORT OPTIONS=SET (REQUIRES OPTIONS=REPROMPT)  *
.*     12/22/82 LDW  REMOVE &REPRMPT= OPTION                          *
.*     12/15/82 LDW  PUT IN MNOTE 0 IF REPRMPT SPECIFIED              *
.*     12/13/82 LDW  ADD OPTIONS= KEYWORD TO SUPPORT MULTIPLE OPTIONS *
.*                      LIKE REPROMPT, WHICH WILL BE DISCONTINUED     *
.*     10/28/82 LDW  CHANGE &LENGTH TO &LENFIX AND HANDLE DIFFERENTLY *
.*                      TO KEEP STUPID OLD (VERY OLD) ASMH HAPPY      *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
.* NOTE: GENERATED CODE INTENTIONALLY SHIFTED RIGHT 1 COLUMN
         LCLA  &L,&I
         LCLB  &REPROMT,&SET
         LCLC  &NAME
&NAME    SETC  '&NFS'
.OPTLOOP AIF   (&I GE N'&OPTIONS).OKOPTS
&I       SETA  &I+1
         AIF   ('&OPTIONS(&I)' EQ 'REPROMPT').OPTREPR
         AIF   ('&OPTIONS(&I)' EQ 'SET'     ).OPTSET
         MNOTE 4,'INVALID OPTION - &OPTIONS(&I) - IGNORED'
         AGO   .OPTLOOP
.OPTREPR ANOP
&REPROMT SETB  1
         AGO   .OPTLOOP
.OPTSET  AIF   (&REPROMT).OPTSET2
         MNOTE 4,'OPTIONS=SET REQUIRES OPTIONS=REPROMPT -- SET IGNORED'
         AGO   .OPTLOOP
.OPTSET2 ANOP
&SET     SETB  1
         AGO   .OPTLOOP
.OKOPTS  AIF   ('&MESSAGE' EQ '*').COMMON
         AIF   ('&MESSAGE'(1,1) EQ '''').QUOTE
&NAME     MVC   QERRMSG(&LENGTH),&MESSAGE
&NAME    SETC  ''
         AGO   .COMMON
.QUOTE   ANOP
&L       SETA  K'&MESSAGE-2
         AIF   ('&LENFIX' EQ '').OK
&L       SETA  &L-&LENFIX
         MNOTE 0,'* MESSAGE LENGTH IS &L *'
.OK      ANOP
&NAME     MVC   QERRMSG(&L),=C&MESSAGE
&NAME    SETC  ''
.COMMON  AIF   (NOT &REPROMT).BRANCH
&NAME     OI    QSCFLAG1,QSC1PMPT        REQUEST COMMAND PROMPT
&NAME    SETC  ''
         AIF   (NOT &SET).BRANCH
          MVC   QDPROMPT,QDREPLY         SET WHAT TO REPROMPT
.BRANCH  ANOP
&NAME     B     QTILT                    INVOKE QTILT SUBROUTINE
         SPACE 2
         MEND
