*          DATA SET ACFVIOX    AT LEVEL 008 AS OF 02/15/83
*          DATA SET ACFVIOX    AT LEVEL 006 AS OF 12/08/82
*          DATA SET ACFVIOX    AT LEVEL 001 AS OF 12/03/79
ACFVIOX  TITLE     'ACF2 VIOLATION EXIT  -  INTEL REL 1.0 '
********************************************************************
*        THIS IS A PROGRAM EXIT GIVEN CONTROL BY ACF AFTER A       *
*        SECURITY VIOLATION HAS BEEN DETECTED. INTEL USE OF THIS   *
*        EXIT IS TO ALLOW PHASED IMPLEMENTATION OF ACF 'ABORT'     *
*        MODE SECURITY USING THE FOLLOWING CRITERIA:               *
*                                                                  *
*             PROGRAM VIOLATION;                                   *
*                 A. ALLOW ACCESS, DO NOT LOG THE VIOLATION.       *
*                                                                  *
*             DATASET VIOLATION;                                   *
*                                                                  *
*                 A. IF NO RULE EXISTS:                            *
*                        1. ALLOW ACCESS, LOG, AND WARN VIOLATOR   *
*                 B. IF A RULE EXISTS:                             *
*                        1. IF THERE IS NO USERDATA FIELD THE AC-  *
*                           CESS IS PERMITTED WITHOUT LOGGING.     *
*                        2. IF THE USERDATA FIELD CONTAINS 'WARN'  *
*                           PERMIT THE ACCESS, BUT LOG THE VIOLA-  *
*                           TION AND ISSUE A WTO TO THE USER WARN- *
*                           ING HIM THAT FUTURE ACCESSES MAY BE    *
*                           DIS-ALLOWED                            *
*                        3. IF THE USERDATA FIELD CONTAINS 'LOG'   *
*                           PERMIT THE ACCESS, AND LOG THE VIO-    *
*                           LATION, DO NOT WARN.                   *
*                        4. IF THE USERDATA FIELD CONTAINS 'CNCL'  *
*                           THEN ALLOW ACF TO CONTINUE NORMAL PRO- *
*                           CESSING (USER WILL BE CANCELLED).      *
*                        5. IF THE USERDATA FIELD IS NONE OF THE   *
*                           ABOVE THE ACCESS IS PERMITTED, LOGGED, *
*                           AND THE USER WARNED                    *
*                                                                  *
* MOD1 K TRUE - 18 SEP 82 - SYSTEM WIDE WARN MODE..CHGD WARN MSG   *
* MOD2 A DAVIES 08 DEC 82 - SYSTEM WIDE WARN MODE..CHGD WARN MSG   *
* MOD3 K TRUE - 03 FEB 83 - CHG WARN MSG, ADDED OPS-ANALYST,       *
*                           AND ***PROD JOB SUPPORT                *
*                                                                  *
********************************************************************
*
*             REGISTER USAGE
*
*                R9  = A(ACF$USER)
*                R10 = A(LOGONID RECORD)
*                R11 = A(WORKAREA)
*                R12 = PROGRAM BASE REG
*
*
         EJECT
ACFVIOX  START
         SAVE   (14,12),,*             SAVE THE REGISTERS
         LR     R12,R15                ADDRESSABILITY
         USING  ACFVIOX,R12            R12 = PROGRAM BASE
         LR     R9,R1                  COPY ACF$USER REGISTER
         USING  ACF$USER,R9            R9 = A(ACF$USER)
         LR     R10,R0                 GET LID ADDRESS IN R10
         USING  LIDREC,R10             R10 = A(LOGONID RECORD)
         GETMAIN R,LV=WORKSIZE,SP=241  GET A WORK AREA
         LR     R11,R1                 R11 = A(WORKAREA)
         USING  WORKAREA,R11           ESTABLISH ADDRESSABILITY
         CLI    ACUUFLG3,ACUU3PGM      IS IT A PROG VIOL ?
         BE     RET12                  YES ALLOW, BUT LOG IT
         TM     ACUUFLG1,ACUU1PGM      IS IT A PROG VIOL?
         BO     RET4                   YES ALLOW, BUT DONT LOG
         SPACE  1
         XR     R3,R3                  CLEAR R3
         ICM    R3,7,ACUURADD          GET ADDRESS OF RULE
         BZ     WARN                   NO RULE --> ALLOW, WARN, AND LOG
         SPACE  1
         ACFGDATA (R3),NONE=RET4       GET USERDATA FIELD IF IT EXISTS
         C      R0,=F'4'               RIGHT SIZE FOR WARN OR CNCL ?
         BNE    TRY3                   NOPE, SEE IF RIGHT SIZE FOR LOG
         SPACE  1
         CLC    0(4,R1),=CL4'WARN'     IS USERDATA WARN ?
         BE     WARN                   YES GO TO WARN ROUTINE
         SPACE  1
         CLC    0(4,R1),=CL4'CNCL'     IS USERDATA CNCL ?
         BE     RET0                   CONTINUE PROCESSING(DENY ACCESS)
         SPACE  1
         B      WARN                   UNKNOWN...PERMIT, WARN, LOG IT
         SPACE  1
TRY3     C      R0,=F'3'               RIGHT SIZE FOR LOG?
         BNE    WARN                   NOPE, PERMIT, WARN, AND LOG IT
         SPACE  1
         CLC    0(3,R1),=CL3'LOG'      IS USERDATA LOG ?
         BE     RET12                  OK, PERMIT, LOG, BUT NO WARN
*
         B      WARN                   UNKNOWN...PERMIT, WARN, LOG IT
         SPACE  1
WARN     LA     R4,WTOWARN
         WTO    MF=(E,(R4))            WARN HIM OF VIOLATION
WARNDSN  MVC    WTOWORK(WTOL),WTOPATT  COPY WTO PATTERN TO WORK AREA
         L      R4,ACUUDSN             R4 = A(DSN)
         MVC    WTOWORK+08(44),0(R4)   MOVE DSN TO WTO MSG AREA
         LA     R4,WTOWORK             R4 = A(WTOWORK)
         WTO    MF=(E,(R4))            TELL HIM DSN BEING ACCESSED
         B      RET12                  PERMIT ACCESS BUT LOG VIOLATION
         EJECT
*     RETURN TO ACF FOR NORMAL ACF PROCESSING
         SPACE  1
RET0     LA     R6,0                   PERMIT NORMAL ACF PROCESSING
         L      R1,ACUUCOMM            GET EXIT COMM AREA           NTL
         CLI    0(R1),C'P'             OPS ANALYST ALLOWED?         NTL
         BNE    EXIT                   /NO                          NTL
         TM     LIDI1FLG,LIDOPSAN      IS THIS DUDE AN OPSANALST    NTL
         BNO    EXIT                   /NO.. SPLIT                  NTL
         SPACE  1                                                   NTL
         MVI    1(R1),C'O'             INDICATE OA ACCESS ALLOWED   NTL
         LA     R1,WTOOA                                            NTL
         WTO    MF=(E,(R1))            WARN OA OF ACCESS            NTL
         B      WARNDSN                AND MENTION THE DSNAME       NTL
         SPACE 5
*      RETURN TO ACF TO PERMIT ACCESS WITHOUT LOGGING
         SPACE  1
RET4     LA     R6,4                   PERMIT ACCESS AND NOLOG
         B      EXIT
         SPACE  5
*      RETURN TO ACF TO PERMIT ACCESS WITH LOGGING
         SPACE 1
RET12    LA     R6,12
         B      EXIT                   PERMIT ACCESS AND LOG
         SPACE 5
EXIT     FREEMAIN R,LV=WORKSIZE,A=(R11),SP=241
         LR     R15,R6
         RETURN (14,12),RC=(15)
         SPACE 5
PATCH    DC     10F'0'
         EJECT
*
*      W T O   M E S S A G E   P A T T E R N S
*
WTOPATT  WTO 'DSN=                                            ',       -
               ROUTCDE=(11),DESC=(7),MF=L
WTOL     EQU    *-WTOPATT
         SPACE  5
WTOWARN  WTO 'THIS DS ACCESS WILL ABEND JOB AFTER 03/17/83.  CALL BONI X
               JOHNSTON AT 6-7489.     ',ROUTCDE=(11),DESC=(7),MF=L
WTOOA    WTO 'ACCESS ALLOWED BY OPS-ANALYST PRIVILEGE.  ACCESS HAS BEEN*
                LOGGED.',ROUTCDE=(11),DESC=(7),MF=L                 NTL
         EJECT
         ACFREGS
         EJECT
*                  W O R K   A R E A
*
WORKAREA DSECT
         DS   0F                  ALIGN WTO ON FULL WORD
WTOWORK  DS   CL256               FILLER FOR WTO MESSAGE PATTERN
WORKSIZE EQU  *-WORKAREA
         EJECT
ACFMACS  DSECT
         COPY   ACF$USER
         EJECT
         ACUCB
         EJECT
         ACCVT
         EJECT
         LIDREC
         END   ACFVIOX


