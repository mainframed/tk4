*          DATA SET SMFSEL     AT LEVEL 003 AS OF 11/07/80
*          DATA SET SMFSEL     AT LEVEL 002 AS OF 05/09/80
*          DATA SET SMFSEL     AT LEVEL 001 AS OF 03/16/78
         TITLE 'S M F S E L *** SMF RECORD SELECTOR PROGRAM'
SMFSEL   PGM                        PROGRAM INITIALIZATION
         LA    R10,$$START          ADDRESS START ADDRESS
         LA    R10,4000(R10)        INCREMENT R10 BY 4000 BYTES
         USING $$START+4000,R10     AND ADDRESS IT
         STM   R10,R13,SAVEBASE     SAVE BASE REG 10 &12 IN CASE
         LA    R2,SYSIN             ADDR OF SYSIN DCB
         OI    48(R2),X'08'         SET THAT CONCATENATION OF DATA SETS
         OPEN  (SYSIN,,SYSPRINT,(OUTPUT),SMFTAB,,SYSUT2,(OUTPUT),      X
               SMFDATE,(OUTPUT))    OPEN ALL WORK FILES
         USING RECORD,R11           MAP THE WORKING RECORD   KMT
         MVI   LINE,C'1'            SET SKIP TO TOP OF PAGE
         MVC   LINE+44(46),HEAD1    MOVE IN HEADER 1
         PUT   SYSPRINT,LINE        WRITE A LINE
         MVC   LINE+44(46),LINE+43  BLANK LINE
         MVI   LINE,C'-'            SET TO TRIPLE SPACE
         PUT   SYSPRINT,LINE
         MVI   LINE,C' '            SET SINGLE SPACE
         EJECT
*   C O N T R O L  C A R D  S C A N
*        PROCESS CONTROL CARDS
*         TYPES:
*               DATE - GIVES START & END DATES FOR SELECT
*               TYPE - GIVES RECORD TYPES TO BE SELECTED
*               EXIT - GIVES NAME OF USER EXIT TO LOOK AT EACH OUTPUT
*                      RECORD AND DECIDE IF IT SHOULD BE OUTPUT
REREAD   MVI   REREADSW,0
READCARD GET   SYSIN,CARD
         CLI   REREADSW,1  HAS EOV OCCURRED
         BE    REREAD  YES, RE-ISSUE GET
         MVC   LINE+2(80),CARD
         PUT   SYSPRINT,LINE
         MVC   LINE+2(80),LINE+1
TESTYPE  CLC   CARD(5),=C'TYPE=' REC TYPE CARD?
         BE    TYPEPROS    YES, PROCESS REC TYPE CARD
         CLC   CARD(5),=C'DATE=' DATE CARD?
         BE    DATEPROS    YES, PROCESS DATE
         CLC   CARD(3),=C'END'    END CARD ?
         BE    ENDCARD
         CLC   CARD(5),=C'EXIT='     USER EXIT
         BE    SETEXIT               YES, GO LOAD IT IN
         B     ERROR1  ERROR INVALID CARD
         EJECT
*    R E C O R D  T Y P E  C O N T R O L  S C A N
TYPEPROS MVI   BLNKSW,0    SET SWITCH OFF
         TR    CARD+5(75),LEGAL  GET RID OF ILLEGAL CHARS
         LA    R2,CARD+5  ADDR TO START SCAN
LUP1B    ST    R2,SCANSTRT  SAVE ADDR OF START OF SCAN
LUP1     C     R2,=A(CARD+80)  END OF CARD
         BE    BLNK    TREAT AS BLANK COLUMN IF END OF CARD
         CLI   0(R2),C','   COMMA-END OF TYPE #
         BE    COMMA   BRANCH TO CONVERT ROUTINE
         CLI   0(R2),C' '  BLANK-END OF CARD & TYPE #
         BE    BLNK    GO TO SET BLANK SWITCH & CONVERT
         CLI   0(R2),C'-' DASH-SEQ GROUP OF NUMBERS
         BE    DASH  GO TO GENERATE ALL TYPE#'S INCLUDED IN THE FIELD
*                    JOINED BY DASH 0-5 = 0,1,2,3,4,5
INCR     LA    R2,1(R2)  INCREMENT TO NEXT POSITION
         B     LUP1      RETURN TO SCAN
BLNK     MVI   BLNKSW,1  SET BLANK SWITCH
COMMA    LR    R3,R2
         L     R4,SCANSTRT   ADDR OF FIRST CHAR TO BE PACKED
         SR    R3,R4    GET LENGTH TO BE PACKED
         CH    R3,=H'3'  LENGTH OF 3 IS MAX
         BH    ERROR2    IF HIGHER THEN CARD ERROR
         BCTR  R3,0      DECREMENT LENGTH BY ONE FOR PACK
         EX    R3,PACK   PACK INTO DOUBLE WORD FOR CVB
         CVB   R3,WORKPACK
         CH    R3,=H'255' REC TYPE GT 255
         BH    ERROR2
         CLI   DASHSW,0   IS A DASH FIELD BEING PROCESSED
         BNE   DASHP      YES, BRANCH
         STC   R3,SCANTAB(R3)  STORE IN TABLE TO ENABLE SELECT
         MVC   LINE+9(4),EDWRD2         EDIT PATTERN
         ED    LINE+9(4),WORKPACK+6     EDIT THE REC. TYPE #
         PUT   SYSPRINT,LINE
         MVC   LINE+9(4),LINE+8         BLANK OUTPUT AREA
NEXTPARM CLI   BLNKSW,1  IF BLANK IS NEXT CHAR, END OF CARD
         BE    READCARD  YES, READ NEXT CARD
         LA    R2,1(R2)  INCR TO NEXT POS.
         B     LUP1B     GO SCAN AGAIN
DASH     MVI   DASHSW,1  SET THAT FIRST PARM IS BEING PROCESSED
         B     COMMA
DASHP    CLI   DASHSW,2  2ND PARM
         BE    DASH2     YES, BRANCH
         STC   R3,FROMTYPE    STORE THE STARTING TYPE
         MVI   DASHSW,2       SET THAT 2ND PARM TO BE PROCESSED
         B     NEXTPARM  UPDATE SCAN VALUE
DASH2    MVI   DASHSW,0  SET OFF DASH FIELD INDICATOR
         SR    R4,R4     CLEAR REG FOR COMPARE
         IC    R4,FROMTYPE  LOAD IN START VALUE FOR INCREMENT
LUP2     STC   R4,SCANTAB(R4) STORE IN TABLE TO ENABLE SELECT
         CVD   R4,WORKPACK
         MVC   LINE+9(4),EDWRD2         EDIT PATTERN
         ED    LINE+9(4),WORKPACK+6     EDIT THE REC. TYPE #
         PUT   SYSPRINT,LINE
         MVC   LINE+9(4),LINE+8
         LA    R4,1(R4)       INCREMENT BY 1
         CR    R4,R3     HAS LAST VALUE BEEN STORED
         BNH   LUP2      NO, BRANCH BACK TO STORE
         B     NEXTPARM
         TITLE 'USER EXIT LOAD ROUTINE'
SETEXIT  CLI   EXITSW,1   HAS EXIT CARD ALLREADY BEEN READ
         BE    ERROR1    YES, ERROR
         MVC   EXITNAME(8),CARD+5 MOVE NAME OF EXIT TO PARM FOR LOAD
         LOAD  EPLOC=EXITNAME
         ST    R0,EXITADDR    STORE ADDR OF USER EXIT ENTRY POINT
         MVI   EXITSW,1       SET SW THAT EXIT SHOULD BE TAKEN
         B     READCARD
         TITLE 'DATE CONTROL CARD SCAN'
DATEPROS CLI   DATESW,1  HAS DATE BEEN READ IN
         BE    ERROR1  YES, NO MORE DATE CARDS ACCEPTED
         TR    CARD+5(75),LEGAL  GET RID OF ILLEGAL CHARS
         MVI   DATESW,1  SET THAT DATE READ
         CLI   CARD+10,C'-'  COL 11 A DASH?
         BE    OK1           YES, OK TO PROCESS
         CLI   CARD+10,C','  COL 11 A COMMA?
         BNE   ERROR3        IF NOT A - OR , THEN ISSUE ERROR MSG
OK1      MVC   STRTDATE,CARD+5
         MVC   ENDDATE,CARD+11
         MVC   TRUEDATE(16),CARD   SET UP FOR OUTPUT OF ACTUAL DATES
         CLC   ENDDATE,STRTDATE  END MUST BE GE TO START
         BL    ERROR3    IF NOT PUT OUT ERROR MSG
         PUT   SMFDATE,CARD
         B     READCARD         GO READ ANOTHER CARD
         TITLE '''END'' CARD PROCESSOR'
ENDCARD  MVC   ERR8+21(4),=C'EXIT' CUSTIMIZE ERROR MSG FOR EXIT
         B     EOFCRD
         TITLE 'SMF DATA READ && SELECT ROUTINE'
*
READSMF  GET   SYSUT1           GET A SMF RECORD          LOCATE    KMT
         LR    R11,R1           UPDATE FOR USING,RECORD             KMT
         LA    R6,1(R6)  UPDATE # READ
         CLC   RECLEN(2),=H'18'       IS RECORD LENGTH VALID
         BL    ERROR6    NO, PUT OUT MSG
         IC    R3,RECTYPE  GET RECORD TYPE
         IC    R4,SCANTAB(R3) LOAD UP CORRESPONDING BYTE FORM TABLE
         CR    R4,R3      IF THEY ARE = THEN SELECT RECORD
         BNE   READSMF    NOT EQUAL, READ ANOTHER RECORD
         UNPK  TESTDATE(5),RECDATE  UNPACK RECDATE FOR COMPARE
*                                  THIS IS DONE TO PROTECT AGAINST 0C7
         OI    TESTDATE+4,X'F0'    SET F SIGN
         CLC   TESTDATE,STRTDATE
         BL    READSMF
         CLC   TESTDATE,ENDDATE
         BH    READSMF
         CLI   EXITSW,1
         BNE   OUTPUTOK
         ST    R11,EXITPARM   SAVE RECORD ADDRESS                   KMT
         LA    R1,EXITPARM    LOAD POINTER TO PARM LIST
         L     R15,EXITADDR   ADDR OF EXIT'S ENTRY POINT
         BALR  R14,R15        EXECUTE USER EXIT
         LTR   R15,R15        TEST R15 FOR RETURN CODE
         BZ    OUTPUTOK       IF ZERO OUTPUT RECORD TO SYSUT2
         BP    READSMF  IF POSITIVE IGNORE RECORD
         B     ERROR7   IF NEGITIVE CANCEL
OUTPUTOK LA    R5,1(R5)   ADD 1 TO RECORD COUNT
         LR    R7,R3     PUT REC TYPE # IN REG
         SLL   R7,2      MULTIPLY BY 4
         L     R8,NUMTAB(R7) LOAD COUNTER FOR REC TYPE
         LA    R8,1(R8)      INCREMENT BY 1
         ST    R8,NUMTAB(R7) STORE UPDATED COUNTER
         CLC   TESTDATE,HIDATE     IS DATE GREATER THAN ANY PREVIOUS
         BNH   TSTLO
         MVC   HIDATE,TESTDATE
         B     WRITE
TSTLO    CLC   TESTDATE,LODATE     IS DATE LOWER THAN ANY PREVIOUS
         BNL   WRITE
         MVC   LODATE,TESTDATE
WRITE    PUT   SYSUT2,(R11)        POKE AWAY RECORD                 KMT
         B     READSMF    READ ANOTHER RECORD
         TITLE 'EOJ STATISTICS PRINT ROUTINE'
EOFT     MVI   LINE,C'-'  SET TRIPLE SPACE
         PUT   SYSPRINT,LINE    TRIPLE SPACE
         MVI   LINE,C' '        SET SINGLE SPACE
         MVC   LINE+2(22),HEAD3    PUT OUT # RECORDS READ
         MVC   LINE+28(10),EDWRD1  *                    *
         CVD   R6,WORKPACK         *                    *
         ED    LINE+28(10),WORKPACK+4 *                 *
         PUT   SYSPRINT,LINE       *                    *
         MVC   LINE+2(26),HEAD4          PUT OUT # RECORDS SELECTED
         MVC   LINE+28(10),EDWRD1        *                        *
         CVD   R5,WORKPACK               *                        *
         ED    LINE+28(10),WORKPACK+4    *                        *
         PUT   SYSPRINT,LINE             *                        *
         MVC   LINE+2(36),LINE+1   BLANK LINE
         MVC   TRUEDATE+5(5),LODATE    *SET UP TO OUTPUT ACTUAL DATES
         MVC   TRUEDATE+11(5),HIDATE   *THAT WERE READ FROM SMF
         PUT   SMFDATE,TRUEDATE        *
         MVI   LINE,C' '
         MVC   LINE+1(120),LINE
         MVC   LINE+2(6),=C'ACTUAL'
         MVC   LINE+9(16),TRUEDATE
         PUT   SYSPRINT,LINE
         MVC   LINE+2(23),LINE+1
         MVI   LINE,C'0'  SET DOUBLE SPACE
         MVC   LINE+2(43),HEAD5 HEADER FOR OUTPUT OF REC TYPE COUNTS
         PUT   SYSPRINT,LINE
         MVC   LINE+2(43),LINE+1  BLANK LINE
         MVC   LINE+6(25),HEAD6   HEADER2 FOR # READ BY REC TYPE #
         MVI   LINE,C' '  SET UP FOR 1 SPACE
         PUT   SYSPRINT,LINE
         MVC   LINE+6(25),LINE+5  BLANK LINE
         MVI   LINE,C'0'  SET UP DOUBLE SPACE
         PUT   SYSPRINT,LINE          SPACE 2
         MVI   LINE,C' '   SET SINGLE SPACE
         LA    R2,0   INIT TO REC TYPE = 0
         LA    R3,256         COUNTER OF LOOP
         SR    R4,R4     CLEAR REG FOR CVD
LUP3     IC    R4,SCANTAB(R2)     LOAD REC TYPE FLAG
*        REC TYPE FLAG = REC TYPE ONLY IF REC IS TO BE SELECTED
         CR    R4,R2   IS FLAG = REC TYPE
         BNE   TESTLUP3       NO, GO LOOK AT NEXT TYPE #
         CVD   R4,WORKPACK    COUNVERT TO PACKED DECIMAL FOR EDIT
         MVC   LINE+11(4),EDWRD2   EDIT PATTERN
         ED    LINE+11(4),WORKPACK+6    EDIT REC TYPE #
         UNPK  TYPEFND+5(3),WORKPACK+6(2)  UNPK REC TYPE
         OI    TYPEFND+7,X'F0'   SET SIGN TO F
         LR    R5,R4    LOAD REC TYPE # INTO REG FOR USE AS A
*                       DISPLACEMENT TO # RECORDS READ TABLE
         SLL   R5,2     MULTIPLY BY 4,THE LENGTH OF EACH ENTRY
         L     R6,NUMTAB(R5) LOAD UP THE COUNTER FOR THIS REC TYPE
         CVD   R6,WORKPACK
         MVC   LINE+20(10),EDWRD1   EDIT PATTERN
         ED    LINE+20(10),WORKPACK+4     EDIT THE # SELECTED
         UNPK  TYPEFND+9(7),WORKPACK+4(4)     UNPACK # SELECTED
         OI    TYPEFND+15,X'F0'  SET SIGN TO F
         PUT   SMFDATE,TYPEFND   OUTPUT
         PUT   SYSPRINT,LINE
TESTLUP3 LA   R2,1(R2)  UPDATE TO LOOK AT NEXT ENTRY
         BCT   R3,LUP3   LOOP 256 TIMES
         MVC   LINE+11(19),LINE+10   BLANK LINE
EOJ      MVC   LINE+2(13),HEAD7    END OF JOB MESSAGE
EOJERR   MVI   LINE,C'-'    SPACE 3
         CLI   EXITSW,1
         BNE   NOEXIT
         LA    R1,0      SET EOJ
         L     R15,EXITADDR  ADDR OF USER ENTRY POINT
         BALR  R14,R15
NOEXIT   DS    0H
         PUT   SYSPRINT,LINE
         CLOSE (SYSIN,,SYSPRINT,,SMFTAB,,SYSUT2,,SMFDATE)
         CLI   ERRSW,0    ANY ERRORS
         BNE   SETRC     SET RETURN CODE TO 16
         LA    R15,0
RETN     B     $$EOJ              RETURN TO O.S.
SETRC    LA    R15,16
         B     RETN
         EJECT
*    I N P U T  D D N A M E  S E L E C T I O N  ROUTINE
EOFCRD   CLI   DATESW,1   HAS DATE BEEN READ
         BNE   ERROR4     NO, ERROR MSG
EOFC     SR    R3,R3     CLEAR REG FOR COMPARE
         SR    R4,R4     CLEAR REG FOR TABLE COMPARE
         SR    R5,R5     SET RECORD SELECTED COUNT TO 0
         SR    R6,R6     SET RECORD READ TO 0
         B     READTAB
EOFTAPE EQU *
         CLC   DDNAME(8),TAPE13
         BE    READTAB
* TAPE13 HAS PROBLEMS -- I/O ERROR IN TRAILER LABELS
         CLOSE (SYSUT1)
READTAB  GET   SMFTAB,CARD
         CLI   CARD,C' '      IF FIRST POSITION BLANK IGNORE
         BE    READTAB
         TR    CARD(11),LEGAL MAKE JULIAN DATE LEGAL
         CLI   CARD+5,C','    MUST HAVE COMMA COL 6
         BNE   ERROR5
         CLI   CARD+11,C','   MUST HAVE COMMA COL 12
         BNE   ERROR5
         CLC   CARD+6(5),CARD   END GE START
         BL    ERROR5
         CLC   STRTDATE(5),CARD+6     DOES TAPE END BEFORE START SELECT
         BH    READTAB
         CLC   ENDDATE(5),CARD       DOES TAPE START AFTER END SELECT
         BL    READTAB
         MVC   LINE+2(43),HEAD8    DDNAME TO BE OPENED
         MVC   LINE+9(8),CARD+12   DDNAME
         PUT   SYSPRINT,LINE
         MVC   LINE+2(43),LINE+1   BLANK LINE
         MVC   SYSUT1+40(8),CARD+12  MOVE IN NAME DD CARD TO BE OPENED
         MVC   DDNAME(8),CARD+12
         OPEN  (SYSUT1)            OPEN NEW TAPE
         B     READSMF
         TITLE 'ERROR MESSAGE ROUTINE'
ERROR1   MVC   LINE+14(54),ERR1
         B     ERRPROS
ERROR2   MVC   LINE+14(41),ERR2
         B     ERRPROS
ERROR3   MVC   LINE+14(84),ERR3
         B     ERRPROS
ERROR4   MVC   LINE+14(12),ERR4
         B     ERRPROS
ERROR5   MVC   LINE+14(85),ERR5
         B     ERRPROS
ERROR6   MVC   LINE+2(26),ERR6         ERROR MESSAGE
         MVC   LINE+28(10),EDWRD1  *                    *
         CVD   R6,WORKPACK         *                    *
         ED    LINE+28(10),WORKPACK+4 *                 *
         PUT   SYSPRINT,LINE       *                    *
         MVC   LINE+2(36),LINE+1   BLANK LINE
         B     READSMF
ERROR7   MVC   LINE+14(47),ERR7
         B     ERRPROS
ERROR8   BALR  R9,R0  GET ADDRESSIBILITY IN CASE FROM EXIT
         USING *,R9
         LM    R10,R13,SAVEBASE
         DROP  R9  DROP TEMPORARY ADDRESSABILITY
         MVC   LINE+14(33),ERR8
ERRPROS  MVI   ERRSW,1    SET ERROR
         MVC   LINE+2(11),ERRFLG
         B     EOJERR
         TITLE 'END OF VOLUME ROUTINE'
         USING EOVRTN,R15
EOVRTN   MVI   REREADSW,1  SET THAT EOV OCCURRED AND MUST RE-READ
         BR    R14
REREADSW DS X
         DROP  R15
         TITLE 'CONSTANTS,DCBS,&&DSECTS'
HEAD8    DC    C'DDNAME XXXXXXXX WILL BE OPENED AS SMF INPUT'
DDNAME   DS    CL8
TAPE13   DC    CL8'TAPE13  '
CARD     DS    XL80
BLNKSW   DC    X'0'
LEGAL    DC    64C'0'    ILLEGAL
         DC    C' '      LEGAL  (BLANK)
         DC    31C'0'    ILLEGAL
         DC    C'-'      LEGAL  (DASH)
         DC    10C'0'    ILLEGAL
         DC    C','      LEGAL  (COMMA)
         DC    132C'0'    ILLEGAL
         DC    C'0123456789'   LEGAL (NUMERICS)
         DC    6C'0'     ILLEGAL
DATESW   DC    X'0'
SCANTAB  DC    X'FF'     SPECIAL FLAG TO SET SELECT OF 0 OFF
         DC    255X'0'   FLAGS TO SET SELECT OF 1-255 OFF
FROMTYPE DS    X
DASHSW   DC    X'0'
SAVEAREA DS    18F
STRTDATE DS    CL5
ENDDATE  DS    CL5
HEAD3    DC    C'TOTAL SMF RECORDS READ'
EDWRD1   DC    X'40206B2020206B202120'
LINE     DC    121C' '
HEAD4    DC    C'TOTAL SMF RECORDS SELECTED'
HEAD5    DC    C'NUMBER OF RECORDS SELECTED BY RECORD TYPE #'
HEAD6    DC    C'RECORD TYPE    # SELECTED'
HEAD7    DC    C'END OF SELECT'
EXITNAME DS    CL8
EXITSW   DS    X'0'
TRUEDATE DS    CL16
LODATE   DC    X'FFFFFFFFFF'
HIDATE   DC    X'0000000000'
TESTDATE DS    CL5
ERRSW    DC    X'0'
TYPEFND  DC    CL16'TYPE='
ERR1     DC    C'COL 1-5 NOT ''TYPE='' OR MORE THAN ONE DATE OR EXIT CAX
               RD'
ERR2     DC    C'RECORD TYPE GT 255 OR MORE THAN 3 POS.   '
ERR3     DC    C'DATE FORMAT INCORRECT. COL 1-16  DATE=DATE1-DATE2  DATX
               E2 GE DATE1. BOTH DATES JULIAN'
ERR4     DC    C'NO DATE CARD'
ERR5     DC    C'SMFTAB FORMAT ERROR. FORMAT IS  DATE1,DATE2,DDNAME  BOX
               TH DATES JULIAN, DATE2 GE DATE1'
ERR6     DC    C'BAD RECORD-SYSUT1 RECORD #'
ERR7     DC    C'SELECT UTILITY CANCELED ON REQUEST OF USER EXIT'
ERR8     DC    C'END CARD MISSING FOR MAIN ROUTINE'
ERRFLG   DC    C'***ERROR***'
EDWRD2   DC    X'40202120'
HEAD1    DC    C'S M F  R E C O R D  S E L E C T  U T I L I T Y'
HEAD2    DC    C'RECORD TYPES TO BE SELECTED'
WORKPACK DS    D
SAVEBASE DS    4F
EXITADDR DS    F
SCANSTRT DS    F
NUMTAB   DC    256F'0'
         DC    0F'0'
REOV     DC    X'85'
         DC    AL3(EOVRTN)
EXITPARM DC    A(0)                     RECORD ADDRESS
         DC    A(SYSPRINT)                SYSPRINT DCB
         DC    A(SYSIN)                     SYSIN  DCB
         DC    X'80'                          LAST BANANA
         DC    AL3(SYSUT2)                       OUTPUT DCB
PACK     PACK  WORKPACK(8),0(0,R4)  THIS IS EXECUTED BY AN EX INSTRUC.
         LTORG
SMFTAB   DCB   BFTEK=S,                                                X
               DDNAME=SMFTAB,                                          X
               DSORG=PS,                                               X
               EODAD=EOFT,                                             X
               MACRF=GM
SYSIN    DCB   BFTEK=S,                                                X
               DDNAME=SYSIN,                                           X
               EXLST=REOV,                                             X
               DSORG=PS,                                               X
               EODAD=ERROR8,                                           X
               MACRF=GM
SYSPRINT DCB   BFTEK=S,                                                X
               DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               MACRF=PM
SMFDATE  DCB   BFTEK=S,                                                X
               DDNAME=SMFDATE,                                         X
               DSORG=PS,                                               X
               MACRF=PM
SYSUT1   DCB   BFTEK=A,                                                X
               DDNAME=SYSUT1,                                          X
               DSORG=PS,                                               X
               EODAD=EOFTAPE,                                          X
               EROPT=SKP,                                              X
               LRECL=32760,                                            X
               RECFM=VBS,                                              X
               MACRF=GL
SYSUT2   DCB   DDNAME=SYSUT2,                                          X
               DSORG=PS,                                               X
               MACRF=PM,                                               X
               LRECL=11000,                                            X
               RECFM=VBS
         LTORG
         DS    0D
RECORD   DSECT
RECLEN   DS    XL4
RECRESV  DS    X
RECTYPE  DS    X
RECTIME  DS    XL4
RECDUM   DS    X
RECDATE  DS    PL3
         END
