*          DATA SET NTLM801    AT LEVEL 001 AS OF 06/25/82
PDSRLODR TITLE 'NTLM801 - LOADER FOR THE PDS LOGICAL RECORD READ'
*.....................................................................*
*                                                                     *
* PROGRAM: NTLM801 -                                                  *
*                                                                     *
* PURPOSE: ACT AS TRANSPARENT LOADER FOR NTLM801R SUBROUTINE WHICH IS *
*          THE PDS LOGICAL RECORD READ PROGRAM                        *
*                                                                     *
* KEN TRUE - 6/25/82 - FROM FCIMODS O051A801 PGM BY MIKE FARMER       *
*                                                                     *
*.....................................................................*
         SPACE
         MACRO
         FMXLOAD   &ENTRY,&MODULE,&DELETE
&ENTRY   CSECT
START    EQU   *                  START OF THE PROCESS
R0       EQU   0
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING START,R15
         L     R15,ADDRESS
         BR    R15                AND GO TO THE PROCESSOR
FIRSTIME DS    0F                 FIRST TIME - LOAD THE BUGGER
         USING *,R15              THIS IS NEW TEMPORARY BASE
         STM   R14,R12,12(R13)    NOW SAVE THE REGS....
         L     R12,ASTART         PICK UP TRUE MODULE START
         DROP  R15
         USING START,R12
         LOAD  EPLOC=MODULE       BRING IN THE MODULE
         ST    R0,ADDRESS
         LR    R15,R0             PROPER ADDRESS NOW
         L     R14,12(,R13)       RESET HIS RETURN ADDRESS
         LM    R0,R12,20(R13)     RESTORE ALL THE REST
         BR    R15
*
&DELETE  DS    0H
         ENTRY &DELETE            SECONDARY ENTRY POINT FOR DELETION
         USING *,R15
         STM   R14,R12,12(R13)    HAVE TO SAVE THEM, FIRST
         L     R12,ASTART         ADDRESS THE TRUE BEGINNING
         DROP  R15
         USING START,R12          AND BRAG ABOUT IT
         DELETE EPLOC=MODULE      WIPE OUT THE ONE IN QUESTION
         LA    R0,FIRSTIME        RESET THE POINTER
         ST    R0,ADDRESS         BACK TO START WITH
         LA    R15,0              SIGNAL NORMAL GOOD JOB
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14                BACK TO THE CALLER
*
ADDRESS  DC    A(FIRSTIME)        CHANGED TO EP OF MODULE
ASTART   DC    A(START)
MODULE   DC    CL8'&MODULE'       MODULE TO BE LOADED
         DC    C' &SYSDATE &SYSTIME'
         MEND
         EJECT
         FMXLOAD  NTLM801,NTLM801R,NTLM801D
         END
