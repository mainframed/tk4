*          DATA SET S052A00    AT LEVEL 001 AS OF 05/16/74
         TITLE '(S052A00).  CALENDAR/GREGORIAN DATE CONVERSION ROUTINE'
S052A00  CSECT
         SPACE
         ENTRY R052A00
         SPACE
* S052A00  WILL CHANGE A JULIAN DATE TO A GREGORIAN DATE OR A GREGORIAN
* DATE TO A JULIAN DATE, DEPENDING ON THE INFORMATION SUPPLIED BY THE
* CALLER.
*
* AT ENTRY TO THIS ROUTINE R13 MUST CONTAIN THE ADDRESS OF AN 18 FULL
* WORD SAVEAREA, R14 MUST CONTAIN THE RETURN ADDRESS, AND R15 MUST
* CONTAIN THE ADDRESS OF DATECONV.
*
* R1, AT ENTRY, MUST CONTAIN THE ADDRESS OF A FULLWORD BINARY FIELD
* WHICH, IN TURN, MUST CONTAIN THE ADDRESS OF A LIST OF 5 FULLWORDS.
* THE LIST MUST BE AS SHOWN BELOW:
*
*
*                   WORD          DESCRIPTION
*
*                    ONE     INDICATOR WORD
*                    TWO     BINARY YEAR (LAST TWO DIGITS OF YEAR ONLY)
*                   THREE    BINARY DAY OF YEAR
*                   FOUR     BINARY MONTH
*                   FIVE     BINARY DAY OF MONTH
*
* THE FIRST WORD MUST CONTAIN A 0 IF CONVERSION FROM JULIAN TO
* GREGORIAN IS DESIRED OR A VALUE OTHER THAN 0 IF CONVERSION FROM
* GREGORIAN TO JULIAN IS DESIRED.
*
* IF THE FIRST WORD CONTAINS A 0 THEN THE SECOND AND THIRD WORDS OF THE
* LIST MUST BE FILLED WITH VALID INFORMATION PRIOR TO CALLING DATECONV.
* DATECONV WILL FILL IN THE THIRD AND FOURTH WORDS PRIOR TO RETURNING
* CONTROL TO THE CALLING MODULE.
*
* IF THE FIRST WORD CONTAINS A NON 0 VALUE THEN THE SECOND, FOURTH,
* AND FIFTH WORDS OF THE LIST MUST BE FILLED WITH VALID INFORMATION
* PRIOR TO CALLING DATECONV.  DATECONV WILL FILL IN THE THIRD WORD
* PRIOR TO RETURNING CONTROL TO THE CALLING MODULE.
*
* IF DATECONV SUCCESSFULLY CONVERTS THE DATE GIVEN TO IT IT WILL PLACE
* A 0 IN R15 PRIOR TO RETURNING CONTROL TO THE CALLING MODULE.  IF
* DATECONV CANNOT CONVERT THE DATE GIVEN TO IT (I.E. THE DATE IS
* INVALID) IT WILL PLACE AN 8 IN R15 PRIOR TO RETURNING CONTROL TO
* THE CALLING MODULE.  THE VALUE PLACED IN R15 IS ALSO STORED IN THE
* FIRST WORD OF THE LIST PRIOR TO RETURN TO THE CALLER.
*
* S052A00 IS REENTRANT.
         EJECT
****************************** WARRANTY *******************************
*                                                                     *
* THIS ROUTINE WILL CORRECTLY CONVERT ANY DATE EXCEPT DATES IN YEARS  *
* FALLING ON EVEN CENTURIES.  DATES WITHIN CENTURIES WHICH ARE EVENLY *
* DIVISIBLE BY 400 WILL BE CORRECTLY CONVERTED, HOWEVER.              *
*                                                                     *
* THIS ROUTINE CARRIES NO OTHER WARRANTY EXPRESSED OR IMPLIED.        *
*                                                                     *
***********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12        BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         USING *,R12
         USING DATEDSCT,R1
R052A00  SAVE  (14,12),,*
         LR    R12,R15
         L     R1,0(0,R1)          PICK UP ADDRESS OF LIST.
         L     R3,BIYEAR
         LTR   R3,R3               CHECK FOR VALID YEAR.
         BM    ERROR               BRANCH IF NOT VALID.
         CH    R3,=H'99'
         BH    ERROR               BRANCH IF NOT VALID.
         SR    R2,R2
         D     R2,=F'4'            CHECK FOR LEAP YEAR.
         L     R0,INDWORD
         LTR   R0,R0
         BNZ   GRGTOJUL
         EJECT
* CONVERT JULIAN TO GREGORIAN
         SPACE
         L     R4,BIDAYJUL
         LTR   R4,R4               CHECK FOR VALID JULIAN DAY.
         BNP   ERROR               BRANCH IF NOT VALID.
         CH    R4,=H'366'
         BH    ERROR
         BL    JULOK
         LTR   R2,R2
         BNZ   ERROR
JULOK    EQU   *
         LTR   R2,R2               IS IT A LEAP YEAR?
         BNZ   NOTLEAP             BRANCH IF NOT LEAP YEAR.
         CH    R4,=H'60'           IS THIS FEB 29?
         BL    NOTLEAP
         BE    FEB29JG
         BCTR  R4,0
NOTLEAP  EQU   *
         LA    R11,MONTHS+(2*11)   POINT TO LAST ENTRY IN MONTH TABLE.
         LA    R10,12              INITIALIZE MONTH COUNTER.
MNTHLOOP EQU   *
         CH    R4,0(0,R11)         DETERMINE CORRECT MONTH.
         BH    MNTHFND
         BCTR  R11,0               BACK UP ONE ENTRY.
         BCTR  R11,0
         BCT   R10,MNTHLOOP        DECREMENT MONTH COUNT.
MNTHFND  EQU   *
         SH    R4,0(0,R11)         CALCULATE DAY OF MONTH.
STORGDTE EQU   *
         ST    R10,BIMONTH         PUT MONTH IN CALLER'S LIST.
         ST    R4,BIDAYGRG         PUT DAY OF MONTH IN CALLER'S LIST.
         B     GOODRTRN
FEB29JG  EQU   *
         LA    R10,2
         LA    R4,29
         B     STORGDTE
         EJECT
* CONVERT GREGORIAN TO JULIAN.
         SPACE
GRGTOJUL EQU   *
         L     R10,BIMONTH         PICK UP MONTH.
         LTR   R10,R10             CHECK FOR VALID MONTH.
         BNP   ERROR
         CH    R10,=H'12'
         BH    ERROR
         L     R4,BIDAYGRG         PICK UP DAY OF MONTH.
         LTR   R4,R4               CHECK FOR VALID DAY.
         BNP   ERROR
         SR    R15,R15             CLEAR LEAP YEAR XTRA DAY COUNT.
         LTR   R2,R2               CHECK FOR LEAP YEAR.
         BNZ   EDOFDAY
         CH    R10,=H'2'           IF IT IS LEAP YEAR FIND OUT MONTH.
         BL    EDOFDAY
         BE    LEAPFEB
         LA    R15,1               ADD 1 TO XTRA DAY COUNTER.
EDOFDAY  EQU   *
         SLL   R10,1               MULTIPLY MONTH BY 2.
         CH    R4,MONTHED-2(R10)   COMPARE DAY TO APPROPRIATE ENTRY IN
*                                       EDIT TABLE.
         BH    ERROR
CALCDAY  EQU   *
         AH    R4,MONTHS-2(R10)    ADD IN NUMBER OF DAYS SO FAR THIS
*                                       YEAR.
         AR    R4,R15              ADD POSSIBLE DAY FOR LEAP YEAR.
         ST    R4,BIDAYJUL         GIVE INFO TO CALLER.
         B     GOODRTRN
LEAPFEB  EQU   *
         CH    R4,=H'29'           MAKE SPECIAL EDIT FOR FEB 29
         BH    ERROR
         SLL   R10,1               MULTIPLY MONTH BY 2.
         B     CALCDAY
         SPACE 3
ERROR    EQU   *
         LA    R15,8               GIVE ERROR RETURN
         B     GOBACK
         SPACE
GOODRTRN EQU   *
         SR    R15,R15             GIVE GOOD RETURN
GOBACK   EQU   *
         ST    R15,ERRWORD
         RETURN (0,12)             NOTE: R14 UNDISTURBED.
         EJECT
MONTHS   DS    0CL24
         DC    H'000'
         DC    H'031'
         DC    H'059'
         DC    H'090'
         DC    H'120'
         DC    H'151'
         DC    H'181'
         DC    H'212'
         DC    H'243'
         DC    H'273'
         DC    H'304'
         DC    H'334'
MONTHED  DS    0CL24
         DC    H'31'
         DC    H'28'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         LTORG
DATEDSCT DSECT
INDWORD  DS    0F
ERRWORD  DS    F
BIYEAR   DS    F
BIDAYJUL DS    F
BIMONTH  DS    F
BIDAYGRG DS    F
         END
