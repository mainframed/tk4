*          DATA SET DSINFO     AT LEVEL 003 AS OF 03/09/81
*          DATA SET DSINFO     AT LEVEL 002 AS OF 06/29/78
INFO     TITLE 'RETRIEVE INFORMATION ABOUT DATA SET FOR PL/1 OPTIMIZER'
         MTITL 'RETREIVE DATA SET INFORMATION FOR PL/1 OPTIMIZER',     *
               LEN=50
*              THIS SUBROUTINE IS INTENDED TO FETCH INFORMATION
*              ABOUT A FILE FOR THE USE OF A PL/1 PROGRAM.
*              THE CALLING SEQUENCE IS:
         SPACE 1
*                   CALL DSINFO(DDN,DSINF_STRUCT);
*                   DCL DDN         CHAR(8),
*                       1 DSINF_STRUCT,
*                         2 DSN     CHAR(44),
*                         2 VOLSER  CHAR(6),
*                         2 TYPE    BIT(32),
*                         2 DSTYPE  CHAR(1),
*                         2 DSORG   BIT(16),
*                         2 RECFM   BIT(8),
*                         2 BLKSIZE FIXED BIN(15,0),
*                         2 LRECL   FIXED BIN(15,0);
         SPACE 1
*              PARAMETER  TYPE     CONTENTS
*              DDN        CHAR(8)  DDNAME OF DESIRED DATA SET
*              DSN        CHAR(44) DATA SET NAME FROM JFCB
*              VOLSER     CHAR(6)  VOLUME SERIAL FROM JFCB
*              TYPE       BIT(32)  UNIT TYPE FROM THE UCB
*              DSORG      BIT(16)  DSORG FROM THE DSCB
*              DSTYPE     CHAR(1)  DUMMY='-' TSO='*' DA='D'
*                                   IS='I' JES(SYSIN/SYSOUT)='J'
*                                   MEMBER='M' PDS='P' PS='S'
*                                   TAPE='T'  DDNAME NOT FOUND='U'
*              RECFM      BIT(8)   RECFM FROM THE JSCB OR DSCB
*              LRECL      BIN      LRECL FROM THE JSCB OR DSCB
*              BLKSIZE    BIN      BLKSIZE FROM THE JSCB OR DSCB
         EJECT
DSINFO00 START 0
DSINFO  GETDSA PARM=RX,BASE=RY
         XC    INFMJFCB(256),INFMJFCB
         XC    INFMJFCB+256(SAVEND-INFMJFCB-256),INFMJFCB+256
         MVC   DUMDCB(MODDCBND-MODDCB),MODDCB
         LA    R0,DUMDCB                SET UP OPEN FOR RDJFCB
         ST    R0,DUMOPEN
         MVI   DUMOPEN,X'80'
         LA    R0,EXITLIST              SET UP DCB FOR RDJFCB
         STCM  R0,7,DCBEXLST+1-IHADCB+DUMDCB
         LA    R0,INFMJFCB
         ST    R0,EXITLIST
         MVI   EXITLIST,X'87'
         TM    0(RX),X'80'              EXACTLY TWO ARGUMENTS?
         BO    ARGCNT                    NO - ARGUE WITH THIS
         TM    4(RX),X'80'
         BZ    ARGCNT                    NO - ARGUE WITH THIS
         LM    R1,R2,0(RX)              R1->DDN   R2->STRUCT
         CLC   =H'8',4(R1)              DDN 8 BYTES?
         BNE   NAMELEN                   NO - BYTE THIS
         L     BR,0(R1)
         TM    6(R1),X'80'
         BZ    NOTVAR
         CLC   =H'8',0(BR)
         BNE   NAMELEN
         LA    BR,2(,BR)
NOTVAR   MVC   DCBDDNAM-IHADCB+DUMDCB,0(BR)
         LM    R2,AR,0(R2)              R2=LOCATOR     AR->DESCRIPTOR
         CLC   STRUCTDS,0(AR)
         BNE   BUMPARM
         USING STRUCT,R2
         MVI   DSN,C' '
         MVC   DSN+1(VOLSER+5-DSN),DSN
         XC    TYPE(STRUCTND-TYPE),TYPE
         MVI   DSTYPE,C' '
       DEVTYPE DCBDDNAM-IHADCB+DUMDCB,DEVTYPE
         B     *+4(BR)                  TEST RETURN CODE FROM DEVTYPE
         B     OK
         B     NOTFOUND
         STM   LR,CR,OFLR(DR)           INVALID PARAMETERS TO DEVTYPE!
         DC    H'0'                     FORCE SIGNAL ERROR(0C1)
NOTFOUND MVI   DSTYPE,C'U'              INDICATE DDNAME UNDEFINED
         B     BYBY
         SPACE 1
OK       MVC   TYPE,DEVTYPE
        RDJFCB (DUMDCB,),MF=(E,DUMOPEN)
         MVC   DSN,JFCBDSNM
         MVC   VOLSER,JFCBVOLS
         TM    TYPE,UCB3TAPE
         BO    TAPE
         TM    TYPE,UCB3DACC
         BZ    NOOB
         MVI   DSTYPE,C'D'              ASSUME DA
         MVC   COG(4),MODCOG
         LA    R0,JFCBDSNM
         ST    R0,COG+4
         LA    R0,JFCBVOLS
         ST    R0,COG+8
         LA    R0,DS1FMTID
         ST    R0,COG+12
        OBTAIN COG
         MVC   DSORG(3),DS1DSORG        COPY DSORG AND RECFM
         MVC   BLKSIZE(4),DS1BLKL       COPY BLKSIZE AND LRECL
         TM    DS1DSORG,JFCORGPO        IS THIS PDS?
         BZ    NOOB                      NO - LEAVE DSTYPE ALONE
         MVI   DSTYPE,C'P'              INDICATE THAT DSORG=PO
         TM    JFCBIND1,JFCPDS          WAS MEMBER SPECIFIED?
         BZ    *+8
         MVI   DSTYPE,C'M'
         B     NOOB
TAPE     MVI   DSTYPE,C'T'
*        B     NOOB
NOOB     OC    DSORG,DSORG              MERGE DSORG
         BNZ   *+10
         MVC   DSORG,JFCDSORG
         CLI   RECFM,0                  MERGE RECFM
         BNZ   *+10
         MVC   RECFM,JFCRECFM
         OC    LRECL,LRECL              MERGE LRECL
         BNZ   *+10
         MVC   LRECL,JFCLRECL
         OC    BLKSIZE,BLKSIZE          MERGE BLKSIZE
         BNZ   *+10
         MVC   BLKSIZE,JFCBLKSI
         OC    TYPE(2),TYPE             000000XX OR 000001XXXX?
         BZ    NULL                      YES - DUMMY/TSO/JES
         CLI   DSTYPE,C'M'
         BE    BYBY
         CLI   DSTYPE,C'P'
         BE    BYBY
         CLI   DSTYPE,C'T'
         BE    BYBY
**** DUE TO ERROR CHANGE JFCDSRG1 TO DS1DSORG  FEB 18,1981
*        TM    JFCDSRG1,JFCORGIS
         TM    DS1DSORG,JFCORGIS
         BZ    *+8
         MVI   DSTYPE,C'I'
**** DUE TO ERROR CHANGE JFCDSRG1 TO DS1DSORG  FEB 18,1981
*        TM    JFCDSRG1,JFCORGPS
         TM    DS1DSORG,JFCORGPS
         BZ    *+8
****DUE TO ERROR IN FOLLOWING CHANGED P TO S  FEB 18,1981
*        MVI   DSTYPE,C'P'
         MVI   DSTYPE,C'S'
       FREEDSA
         SPACE 2
***********************************************************************
*                                                                     *
*              DUE TO A BUG IN DEVTYPE, FOR WHICH NO PTF EXISTS,      *
*              THE DIFFERENTIATION BETWEEN TSO, DUMMY, AND JES        *
*              DOES NOT WORK.                                         *
*              SEE OS EWS CD/FR 44E09, P70572.                        *
*                                                                     *
***********************************************************************
         SPACE 2
NULL     MVI   DSTYPE,C'*'              ASSUME TSO
         CLI   TYPE+3,1
         BE    BYBY
         MVI   DSTYPE,C'-'              ASSUME DUMMY
         CLI   DEVTYPE+7,0
         BE    BYBY
         MVI   DSTYPE,C'J'              MUST BE JES
         B     BYBY
ARGCNT   LA    R1,NUMARG
SIG     PLISIG (R1)
ABBY    PLISIG ERR
BYBY   FREEDSA
NAMELEN  LA    R1,#CHAR
         B     SIG
BUMPARM  LA    R1,BUMSTR
         B     SIG
NUMARG PLIICB COND,NUMARGS
#CHAR  PLIICB COND,NUMCHAR
BUMSTR PLIICB COND,INVPARM
ERR    PLIICB ERROR
DOPE     DC    A(DSN-STRUCT)
         DC    Y(44,0)
         DC    A(VOLSER-STRUCT)
         DC    Y(6,0)
         DC    A(TYPE-STRUCT)
         DC    Y(32,0)
         DC    A(DSTYPE-STRUCT)
         DC    Y(1,0)
         DC    A(DSORG-STRUCT)
         DC    Y(16,0)
         DC    A(RECFM-STRUCT)
         DC    Y(8,0)
         DC    A(BLKSIZE-STRUCT)
         DC    A(LRECL-STRUCT)
STRUCTDS EQU   DOPE,*-DOPE
MODDCB   DCB   DDNAME=DDN,DSORG=PS,MACRF=E,EXLST=0
MODDCBND EQU   *
MODCOG  CAMLST SEARCH,*,*,*
         LTORG
         STITL 'LIBRARY CONTROL BLOCK DSECTS AND EQUS'
      IBMBXER
      IBMBXLB
         STITL 'DSA - WORK AREA'
IBMBZDSA DSECT
         ORG   COCL
ICB      DS    0F
ERRNUMB  DS    XL2
ERRONVLD DS    XL2
DCLCB    DS    A
DEVTYPE  DS    2A
DUMOPEN  OPEN  (DUMDCB,INPUT),MF=L
DUMDCB   DCB   DDNAME=DDN,DSORG=PS,MACRF=E,EXLST=EXITLIST
EXITLIST DS    0F,XL1'87',AL3(INFMJFCB)
         EJECT
      IEFJFCBN
         EJECT
COG     CAMLST SEARCH,JFCBDSNM,JFCBVOLS,DS1FMTID
         EJECT
      IECSDSL1 1
SAVEND   EQU   *
         STITL 'DSECT FOR PARAMETER STRUCTURE'
STRUCT   DSECT
DSN      DS    CL44
VOLSER   DS    CL6
TYPE     DS    BL4
DSTYPE   DS    C
         SPACE 1
*              MUST STAY IN THIS ORDER
DSORG    DS    BL2
RECFM    DS    BL1
         SPACE 1
*              MUST STAY IN THIS ORDER
BLKSIZE  DS    H
LRECL    DS    H
STRUCTND EQU   *
         STITL 'COMMUNICATIONS VECTOR TABLE DSECT'
         CVT   SYS=VMS,PREFIX=YES
         STITL 'TASK CONTROL BLOCK DSECT'
        IKJTCB
         STITL 'TASK INPUT/OUTPUT TABLE DSECT'
TIOT     DSECT
      IEFTIOT1
         STITL 'DCB DSECT'
         DCBD  DEVD=DA,DSORG=PS
         STITL 'UNIT CONTROL BLOCK DSECT'
SRTE  IEFUCBOB
         END
