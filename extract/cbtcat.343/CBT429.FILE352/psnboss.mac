*          DATA SET PSNBOSS    AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNBOSS - DRIVER FOR THE PROC VERIFICATION PROCESSORS'
PSNBOSS  PSNMSTRT  T=MAIN,SASIZ=WKLENG
         EJECT
*                    THE DRIVER/BOSS FOR THE PROC EDITTING AND
*                    REFORMATTING SYSTEM - TO CALL THE INIT AND
*                    TERM ROUTINES, AND CALL THE SERIES OF
*                    PROCESSORS NEEDED FOR EACH OF THE MEMBERS
*                    REQUESTED.
*            NAME DEPENDENCIES - THE NAME OF THE WORKAREA WITHIN WORK
         LA    R15,PSWINIT
         LINK  SF=(E,(15))        START OUT THE PROCESS
         LTR   R15,R15            DID IT INITIALIZE PROPERLY???
         BNZ   RETURN             NO - MAY AS WELL LEAVE
*
NEXTONE  EQU   *
         L     R15,PSWSETUP       SO GO GET ANOTHER REQUEST
         BALR  R14,R15
         CH    R15,PSWHALF4       AND BRANCH BASED ON THE RETURN
         BL    PROCESS            SERVICE THE REQUEST
         BE    TERMINAT           END OF THE INPUT - STOP IT ALL
*                    ERROR IN THIS ONE - REPORT AS NON-PROCESSED
         PSNMERR  ERRNOTP,PSWPNAME  ERROR - THIS NOT PROCESSED
         B     NEXTONE            AND BACK FOR STILL ANOTHER
*
PROCESS  EQU   *
         L     R15,PSWTYPE        DETERMINE WHAT TYPE OF CARD IT IS
         BALR  R14,R15            WHAT WAS IT, JOHNNIE
         LA    R0,NUMRTN          CHECK HIS RETURN - JUST IN CASE
         CR    R15,R0             BE SURE IT WILL FIT
         BH    WRONG              OOPS
         SLL   R15,3              TURN INTO A BRANCH INDEX(DOUBLE)
         B     *+4(R15)           GO TO PROPER ROUTINE
BRTABLE  EQU   *
         L     R15,PSWDD          0
         B     CALL
         L     R15,PSWEXEC        1
         B     CALL
         L     R15,PSWPROC        2
         B     CALL
         L     R15,PSWCOMNT       3
         B     CALL
         B     ENDMEMBR           4
         NOP   0
         B     BADERROR           5
         NOP   0
NUMRTN   EQU   (*-BRTABLE)/8      NUMBER OF ENTRIES
*
CALL     EQU   *                  CALL THE PROCESSOR - NORMAL ROUTINES
         BALR  R14,R15
         LTR   R15,R15            WHAT WAS THE PROCESSORS VERDICT
         BZ    PROCESS            OK TO PROCEED - CARRY ON
*
BADERROR EQU   *                  PROCESSOR HAS FOUND BAD ERROR - QUIT
         PSNMERR  ERRSTOPM        TERMINATE ON THIS MEMBER
*
ENDMEMBR EQU   *                  END OF THIS ONE, ON WAY OR OTHER
         L     R15,PSWENDM        AND TO END OF MEMBER CLEANUP
         BALR  R14,R15
         TM    PSWMFLG1,PSWM1REF  REFORMAT REQUESTED FOR THIS ONE
         BZ    NEXTONE            NO - SKIP THE EXCUSES
         L     R15,PSWREFMT       YES - GO TO REFORMAT DIRECTOR
         BALR  R14,R15            AND DONT GET INVOLVED IN DETAILS
         B     NEXTONE
*
*                    AND IF THE PROCESS IS REALLY BAD
WRONG    EQU   *                  INVALID RETURN CODE FROM SCAN
         LR    R10,R15            SAVE THAT RETURN CODE
         PSNMERR   ERRBADRC
         B     PROCESS            BACK FOR THE NEXT CARD
*
*
TERMINAT EQU   *
         LA    R15,PSWTERM
         LINK  SF=(E,(15))         AND STOP THE MUSIC
         B     RETURN
         EJECT
WORKAREA DSECT
         DS    18F
         DS    X                  MINIMUM SIZED WORKAREA
         PSNMBLOK   RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
