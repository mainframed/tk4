*          DATA SET S963A11    AT LEVEL 001 AS OF 05/24/78
R963A11  TITLE  'LOCATE SUBROUTINE - CHECK THE CATALOG FOR A DSNAME'
*.....................................................................*
*                                                                     *
* SUBROUTINE  LOCATE;  CALLING SEQUENCE;                              *
*                                                                     *
*     CALL R963A11(DSNAME,VOLENTS,#ENTRIES,RETURN_CODE);              *
*                                                                     *
*   WHERE: DSNAME = DATASET NAME (CL44)                               *
*          VOLENTS  -  STRUCTURE  (SEE BELOW)                         *
*          RETURN_CODE = RC FROM LOCATE MACRO  (FL4)                  *
*                                                                     *
*   DCL  #ENTRIES    FIXED BIN(15,0),                                 *
*        RETURN_CODE FIXED BIN(31,0);                                 *
*                                                                     *
*   DCL    1 VOLENTS(10),                 MAX OF 10 VOLUMES RETURNED  *
*            2 DEVTYPE  BIT(32),                                      *
*            2 VOLSER   CHAR(6),                                      *
*            2 FSEQ#    FIXED BIN(15,0) UNALIGNED;                    *
*                                                                     *
*   ON RETURN:                                                        *
*                                                                     *
*          VOLENTS IS FILLED IN;   #ENTRIES=0 IF NOT LOCATED          *
*          RETURN_CODE = RETURN CODE FROM LOCATE MACRO. 0=SUCCESS     *
*                                                                     *
*   K TRUE    20 FEB  78.  FOR P963R00 PGM (DASD MONITOR SYSTEM)      *
*                                           V2M0                      *
*.....................................................................*
         SPACE
R963A11  PGM   BASE=R10
         EJECT
START    DS    0H
         LM    R2,R5,0(R1)     PICK UP PARM PTRS
         MVC   LOCDSN,0(R2)    MOVE DSNAME TO LOCATE CAMLST
         MVC   0(2,R4),=XL2'0000'  CLEAR OUT # VOLUMES RETURNED TO
*                                ZEROS
         USING VOLTAB,R3       AND ESTABLISH ADDRESSABILITY
         LOCATE LOCLST         DO THE CATALOG CHECK
         STCM  R15,B'1111',0(R5) SAVE RETURN CODE FOR THEM
         LTR   R15,R15         ANYBODY HOME?
         BNZ   $$EOJ            NO..SOME PROBLEM..RETURN
*
*   NO PROBLEM.....MOVE IN THE LOCATE LIST TO THE USER
*
         LH    R6,BUFFER        GET VOLUME COUNT
         LTR   R6,R6             CHECK COUNT
         BNP   $$EOJ
         CH    R6,=H'10'        MORE THAN 10 ENTRIES?
         BNH   SETUP            NO LEAVE COUNT AS IS
         LA    R6,10            >10..USE 10
SETUP    LA    R7,BUFFER+2      POINT TO FIRST VOLUME ENTRY
         STCM  R6,B'0011',0(R4) SAVE COUNT USED...
DOMOVE   MVC   VOLENTRY,0(R7)   MOVE IT IN...
         LA    R3,VOLLEN(R3)    BUMP BOTH TARGET
         LA    R7,VOLLEN(R7)     AND SOURCE POINTERS
         BCT   R6,DOMOVE           AND GRIND THRU THE MOVES
*
         B     $$EOJ            AND RETURN
         EJECT
*
* DATA CONSTANTS/ STORAGE
*
LOCLST   CAMLST NAME,LOCDSN,,BUFFER
         SPACE  2
LOCDSN   DC   CL44' '
BUFFER   DS   0D
         DC   265X'00'
         ORG  BUFFER+6
LOCVOL   DS   CL6
         ORG
VOLTAB   DSECT
DEVICE   DS   CL4     DEVICE TYPE
VOLSER   DS   CL6     VOLSER
FSEQ#    DS   H       FILE SEQUENCE NUMBER
         ORG  VOLTAB
VOLENTRY DS   XL12
VOLLEN   EQU  *-VOLTAB
         ORG
         END
