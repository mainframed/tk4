*          DATA SET TRAC3850   AT LEVEL 003 AS OF 02/22/79
TRAC3850 TITLE 'MVS-IPO MSS TRACE REDUCTION EXAMPLE'
***********************************************************************
*                                                                     *
*             MODULE NAME = TRAC3850                                  *
*                                                                     *
*             DESCRIPTIVE NAME = IPO SUPPLIED EXAMPLE OF AN MSS TRACE *
*                REDUCTION                                            *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             STATUS = RELEASE 02.0 OF IPO                            *
*                                                                     *
*             FUNCTION =                                              *
*                USE OF REFORMATED TRACE CREATED BY ICGTRINT (SEE MSS *
*                TRACE REPORT LOGIC) TO PRODUCE A COMPLEMENTARY TRACE *
*                REPORT.                                              *
*                THIS PROGRAM CAN BE USED ON A CUMULATED TRACE.       *
*                IF THE TRACE REPORT DATE BEGINNING IN FORMAT         *
*                    MMJJYY    IS NOT SPECIFIED,THE TRACE IS LISTED   *
*                FROM THE BEGINNING OF THE CUMULATED DATA SET         *
*                  EXAMPLE : IF YOU WANT TO SELECT THE TRACE FROM THE *
*                21ST JANUARY OF 1978 ,UP TO THE LAST DAY OF THE      *
*                TRACE,CODE 012178 IN THE PARM FIELD AS LISTED BELOW  *
*                                                                     *
*                // EXEC PGM=TRAC3850,PARM='LONG,011278'              *
*                                                                     *
*    THE PROGRAM CAN BE CALLED BY TWO WAYS UPON VALUE OF THE PARM     *
*                FIELD. TWO VALID PARAMETERS :                        *
*                         - 'LONG'                                    *
*                         - 'SHORT'                                   *
*                SEE BELOW FOR COMPLETE DESCRIPTION                   *
*                                                                     *
*             OPERATION =                                             *
*                GET EACH REFORMATED TRACE RECORD                     *
*                COMPUTE SUMS FOR EACH VALUE AND EDIT CURRENT VALUE   *
*                PRODUCE A LIST OF EACH VALUE                         *
*                AT END OF FILE,PRODUCE A SUMMARY VALUE               *
*                                                                     *
*             NOTES =                                                 *
*                DEPENDENCIES = CHARACTER SET IS EBCDIC.REASSEMBLE    *
*                   IF A DIFFERENT CHARACTER SET IS NEEDED.           *
*                RESTRICTIONS = NONE                                  *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTERS 0 TO 3  = WORK REGISTERS                *
*                   REGISTER  4 TO 5  = UNUSED                        *
*                   REGISTER  6       = WORK REGISTER                 *
*                   REGISTER  7       = UNUSED                        *
*                   REGISTER  8 TO 11 = WORK REGISTERS                *
*                   REGISTER 12       = ADDRESSABILITY TO TRAC3850    *
*                                       CSECT                         *
*                   REGISTER 13       = SAVE AREA REGISTER            *
*                   REGISTERS 14,15   = WORK REGISTERS                *
*                                                                     *
*                PATCH LABEL = NONE                                   *
*                                                                     *
*                MODULE TYPE = PROCEDURE                              *
*                   PROCESSOR = ASM                                   *
*                   MODULE SIZE = < 1K BYTES                          *
*                   ATTRIBUTED = PROBLEM PROGRAM STATE                *
*                                                                     *
*                ENTRY POINT = TRAC3850  (ONLY ENTRY POINT)           *
*                   LINKAGE =                                         *
*                      FROM AN EXEC STATEMENT IN A JCL CARD           *
*                                                                     *
*                INPUT = REGISTER 1 POINTS ON THE PARAMETER LIST      *
*                                                                     *
*                OUTPUT = TRACE REPORT ON SYSPRINT                    *
*                                                                     *
*                EXIT - NORMAL =AT PROGRAM END VIA BRANCH REGISTER 14 *
*                   OUTPUT = TRACE REPORT                             *
*                   RETURN CODE = ZERO                                *
*                                                                     *
*                EXIT - ERROR = NONE                                  *
*                   OUTPUT = NONE                                     *
*                   RETURN CODE = 0                                   *
*                                                                     *
*                EXTERNAL REFERENCES = NONE                           *
*                                                                     *
*                   DATA AREAS = NONE                                 *
*                   CONTROL BLOCKS= DCB                               *
*                                                                     *
*                TABLES = JULIAN DATE TO DAY AND MONTH CONVERSION     *
*                                                                     *
*                MACROS = OPEN,GET,PUT,CLOSE                          *
*                                                                     *
*                CHANGE ACTIVITY = NONE                               *
*                                                                     *
*                MESSAGES = NONE                                      *
*                                                                     *
*                ABEND CODES = NONE                                   *
*                                                                     *
*                OUTPUT DESCRIPTION =                                 *
*                                                                     *
*     IF PARM='LONG' IS SPECIFIED,THE PROGRAM PRODUCES,FOR EACH       *
*  STAGE/DESTAGE REQUEST,A LINE WITH THE FOLLOWING INFORMATIONS :     *
*           - TIME OF DAY THAT REQUEST WAS SCHEDULED                  *
*           - REQUEST TYPE (STAGE/DESTAGE)                            *
*           - NUMBER OF CYLINDERS TO BE STAGED/DESTAGED               *
*           - NUMBER OF CYLINDERS STAGED WITH CYLINDER FAULT          *
*           - NUMBER OF CYLINDERS DESTAGED BY LRU ACTION              *
*           - NUMBER OF PAGES STAGED/DESTAGED                         *
*           - NUMBER OF VTOC STAGE/DESTAGE REQUEST                    *
*           - VOLUME SERIAL NUMBER                                    *
*           - LOCATION LXYZ OF THE MOVED CARTRIDGE                    *
*           - CARTRIDGE SEQUENCE NUMBER (1 OR 2)                      *
*           - SELECTED DRD NUMBER                                     *
*           - TIME IN 1/4 SEC OF SYSTEM BUSY                          *
*                  (TIME END OF REQUEST - TIME SCHEDULED REQUEST)     *
*           - TIME IN 1/4 SEC OF DRD BUSY                             *
*           - TIME IN 1/4 SEC OF STAGING/DESTAGING                    *
*           - TIME IN 1/4 SEC OF CARTRIDGE LOADED                     *
*                                                                     *
*      THEN,ANOTHER REPORT IS GIVEN,WHICH IS THE SUMMARY OF THE       *
*    PREVIOUS ONE DURING THE TIME TRACE HAS BEEN ACTIVE,INCLUDING :   *
*           - THE TOTAL STAGED CYLINDERS NUMBER                       *
*           - THE TOTAL DESTAGED CYLINDERS NUMBER                     *
*           - THE TOTAL NUMBER OF CYLINDERS STAGED WITH CYL FAULT     *
*           - THE TOTAL NUMBER OF CYLINDERS DESTAGED BY LRU ACTION    *
*           - THE TOTAL NUMBER OF ALLOCATED PAGES                     *
*           - THE TOTAL NUMBER OF STAGED/DESTAGED VTOCS               *
*           - THE TOTAL NUMBER OF LOADED CARTRIDGES                   *
*           - THE TOTAL SYSTEM BUSY TIME IN SECONDS                   *
*           - THE TOTAL DRD BUSY TIME IN SECONDS                      *
*           - THE TOTAL STAGING/DESTAGING TIME IN SECONDS             *
*           - THE TOTAL LOAD CARTRIDGE TIME IN SECONDS                *
*           - THE ELAPSED TIME IN SECOND OF TRACE ACTIVITY            *
*                                                                     *
*    IF PARM='SHORT' IS SPECIFIED,ONLY THE SECOND REPORT IS           *
*  FURNISHED.                                                         *
*    AN EXAMPLE OF CALLING THIS PROGRAM IS AS BELOW :                 *
*    (THIS STEP MUST FOLLOW A STEP INVOKING ICGTRINT)                 *
*                                                                     *
* //STEPX   EXEC PGM=TRAC3850,PARM='LONG'  *  ALL REPORTS WANTED *    *
* //TRACE   DD DSN=TRAC2,DISP=(OLD,PASS)   *  REFORMATED TRACE   *    *
* //TRANSIT DD DSN=&&TRANSIT,UNIT=SYSDA,DISP=(,DELETE),               *
* //           DCB=(BLKSIZE=1330,LRECL=133,RECFM=FB),                 *
* //           SPACE=(CYL,(1,1))           *       WORK FILE     *    *
* //SYSPRINT DD SYSOUT=*                   *      OUTPUT FILE    *    *
* //SYSUDUMP DD SYSOUT=*                   *     MAY BE USEFUL   *    *
*                                                                     *
***********************************************************************
         EJECT
TRAC3850 CSECT
         SAVE  (14,12),,TRAC3850_&SYSDATE  SAVE REGISTERS
         LR    R12,R15             INITIALIZE BASE REGISTER
         USING TRAC3850,R12        BASE REGISTER
         B     SAVE1+72            GO TO NEXT EXECUTABLE INSTRUCTION
SAVE1    DS    18F                 SAVE AREA FOR CALLED PROGRAM
         ST    R13,SAVE1+4         SAVE R13 OF CALLING PROGRAM
         LA    R15,SAVE1           R15=SAVE ADDRESS OF CALLED
         ST    R15,8(R13)          PROTECT SAVE AREA OF CALLED PROGRAM
         LR    R13,R15             R13=SAVE AREA ADDR OF CALLED
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         SPACE 1
**********************************************************************
*                    TEST PARAMETER                                  *
**********************************************************************
         SPACE 1
         L     R6,0(R1)            POINT PARAMETER AREA
         LA    R6,2(R6)            JUMP OVER LENGTH
         MVI   SHORTFLG,X'FF'      ASSUME SHORT REPORT
         CLC   0(5,R6),=CL5'SHORT' SHORT REPORT WANTED ?
         BNE   TRYLONG             BRANCH IF NOT
         LA    R6,5(R6)            SKIP OVER PARM
         B     TRYDATE             CONTINUE
TRYLONG  EQU   *                   *
         CLC   0(4,R6),=CL4'LONG'   FULL REPORT WANTED
         BE    SHOWLONG            BRANCH IF YES
         WTO   'INVALID PARAMETER.SHORT REPORT ASSUMED'
         B     NODATE              CONTINUE
SHOWLONG EQU   *                   *
         MVI   SHORTFLG,X'00'      LONG REPORT REQUESTED
         LA    R6,4(R6)            SKIP OVER PARM
         SPACE 2
TRYDATE  EQU   *                   *
         CLI   0(R6),C','          IS BEGINNING DATE SPECIFIED ?
         BNE   NODATE              BRANCH IF NOT
         MVC   EBCDATE(6),1(R6)    MOVE BEGINNING DATE
         CLC   EBCDATE(2),=C'01'   TEST
         BL    DATERROR                FOR
         CLC   EBCDATE(2),=C'12'          VALID
         BH    DATERROR                        MONTH
         CLC   EBCDATE+2(2),=C'01'  TEST
         BL    DATERROR                FOR
         CLC   EBCDATE+2(2),=C'31'          VALID
         BH    DATERROR                          DAY
         CLC   EBCDATE+4(2),=C'00'   TEST
         BL    DATERROR                 FOR
         CLC   EBCDATE+4(2),=C'99'          VALID
         BNH   DATOK                             YEAR
DATERROR WTO   'ERRONEOUS DATE SPECIFIED'
         B     NODATE
DATOK    EQU   *                   *
         MVI   FLAGDATE,X'FF'      SHOW BEGINNING DATE IN USE
         XC    DATE,DATE           CLEAR WORK AREA
         PACK  DATE(2),EBCDATE+4(2) CONVERT YEAR
         LH    R2,DATE             PICK IT UP
         SRL   R2,4                IGNORE SIGN DIGIT
         STC   R2,DATE             STORE DECIMAL VALUE
         MVI   PYEARTAB+4,28       ASSUME NON LEAP YEAR
         TM    DATE,X'01'          TEST
         BO    *+16                     FOR
         TM    DATE,X'12'                    LEAP
         BM    *+8                                YEAR
         MVI   PYEARTAB+4,29       SHOW LEAP YEAR
         XR    R0,R0               CLEAR
         XR    R1,R1                     REGISTERS
         LA    R2,PYEARTAB         POINT ON CONVERSION TABLE
DATLP    EQU   *                   *
         CLC   EBCDATE(2),1(R2)    IS IT THIS MONTH ?
         BE    MONTHFND            BRANCH IF YES
         IC    R0,0(R2)            PICK UP #DAYS IN THIS MONTH
         AR    R1,R0               AND CUMULATE
         LA    R2,4(R2)            NEXT MONTH
         B     DATLP               CONTINUE
MONTHFND XC    PCCWORK,PCCWORK     CLEAR
         PACK  PCCWORK+6(2),EBCDATE+2(2) PACK DAY
         CVB   R2,PCCWORK          CONVERT TO BINARY
         AR    R1,R2               R1 CONTAINS THE JULIAN DAY
         SLL   R1,8                SHIFT
         ST    R1,DATE             STORE JULIAN DAY
         PACK  PCCWORK+6(2),EBCDATE+4(2) PACK YEAR
         CVB   R2,PCCWORK          CONVERT YEAR TO BINARY
         STC   R2,DATE             STORE YEAR
         SPACE 3
NODATE   EQU   *                   *
         SPACE 1
         OPEN  (TRACE,(INPUT))
         CLI   SHORTFLG,X'FF'      SHORT REPORT ?
         BE    NOTRANST            BRANCH IF YES
         OPEN  (TRANSIT,(OUTPUT))
         SPACE 5
NOTRANST EQU   *                   *
         LA    R11,RECORD          PICK UP RECORD ADDRESS
         USING REFTRACE,R11        ESTABLISH RECORD DSECT ADDR.
         SPACE 2
         LA    R10,LIGNE           PICK UP LINE ADDRESS
         USING LINE,R10            ESTABLISH LINE DSECT ADDRESSABILITY
         SPACE 5
         SPACE 1
**********************************************************************
*                    READ THE REFORMATTED TRACE                      *
**********************************************************************
         SPACE 1
READ     EQU   *                   *
         L     R1,=A(LIGNEN)       PICK UP CURRENT LINE ADDRESS
         MVC   LIGNE(133),0(R1)    PREFORMAT CURRENT LINE
         GET   TRACE,RECORD        GET A RECORD FROM REFORMATED TRACE
         CLI   FLAGDATE,X'00'      IS BEGINNING DATE SPECIFIED ?
         BE    NOTEST              BRANCH IF NOT
         CLC   SVEYEAR(3),DATE     IS IT THE GOOD DAY ?
         BL    READ                BRANCH IF NOT
         MVI   FLAGDATE,X'00'      RESET FLAG
NOTEST   EQU   *                   *
         SPACE 1
NOP1     NOP   CURRENT             *****  MODIFIED  *****
         MVI   FLAGREC,X'FF'       SHOW VALID INPUT FOUND
         MVC   LOWERT(4),SVETME    MOVE BEGINNING
         MVC   LOWTIME(4),SVETME                  TRACE TIME
         MVC   DATE1+1(1),SVEYEAR  MOVE THE YEAR
         MVC   DATE1+2(2),SVEJUL   MOVE THE JULIAN DAY
         MVC   LOWDAY(2),SVEJUL    MOVE THE 1ST DAY
         MVI   NOP1+1,X'F0'        ******* ONCE TIME SEQUENCE *******
         SPACE 1
**********************************************************************
*                  PRODUCE THE COMPLETE REPORT                       *
**********************************************************************
         SPACE 1
CURRENT  EQU   *                   *
         MVC   UPERDAY(2),SVEJUL   MOVE THE CURRENT DAY
         MVC   UPERYEAR(1),SVEYEAR MOVE CURRENT YEAR
         CLC   UPERDAY(2),LOWDAY   IS IT THE SAMEDAY
         BE    SAMEDAY             BRANCH IF YES
         L     R1,UPERTIME         PICK UP ENDING DAY TIME
         S     R1,LOWTIME          CALCULATE ELAPSED TIME
         L     R0,ELAPSED          AND ACCUMULATE
         AR    R0,R1                        ELAPSED
         ST    R0,ELAPSED                            TIME
         MVC   LOWTIME,SVETME      RESET LOWER TIME
         MVC   LOWDAY,SVEJUL                        AND DAY
SAMEDAY  EQU   *                   *
         MVC   UPERTIME(4),SVETME  MOVE CURRENT TIME
         MVC   DATE2+1(1),SVEYEAR  MOVE CURRENT YEAR
         MVC   DATE2+2(2),SVEJUL                     AND DAY
         XR    R0,R0               CLEAR
         L     R1,SVETME           PICK UP TRACE RECORD TIME
         D     R0,=F'3600'         OBTAIN HOURS
         LR    R2,R1               PICK UP HOURS
         BAL   R9,EDITIONZ         EDIT
         MVC   TIME(2),EDIT+6      MOVE HOURS
         MVI   TIME+2,C'.'         INSERT SEPARATOR
         LR    R1,R0               PROTECT MINUTES
         XR    R0,R0               CLEAR
         D     R0,=F'60'           OBTAIN MIN AND SECONDS
         LR    R2,R1               PICK UP MINUTES
         BAL   R9,EDITIONZ         EDIT
         MVC   TIME+3(2),EDIT+6    MOVE MINUTES
         MVI   TIME+5,C'.'         INSERT SEPARATOR
         LR    R2,R0               PICK UP SECONDS
         BAL   R9,EDITIONZ         EDIT
         MVC   TIME+6(2),EDIT+6    MOVE SECONDS
         SPACE 2
         LH    R2,DATE2            PICK UP YEAR
         BAL   R9,EDITION          EDIT IT
         LH    R2,DATE2+2          PICK UP JULIAN DAY
         BAL   R9,CONVDATE         CONVERT TO JULIAN DATE
         MVC   CURDAY(2),DAY       MOVE DAY OF TRACE RECORD
         SPACE 2
         ICM   R2,15,STGD          PICK UP STAGED CYL NUMBER
         BE    DESTAGE             TRY NEXT IF NONE
         MVC   TYP(7),=CL7'  STAGE'  INDICATE  STAGING
         MVI   STGFLG,X'FF'                            REQUEST
         LA    R1,CYLSTAG          PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITION          EDIT NUMBER
         MVC   CYL(4),EDIT+4       MOVE CYL NUMBER
CYLFLT   EQU   *                   *
         ICM   R2,15,CFAULT        PICK UP STAGED CYL FAULT NUMBER
         BE    DESTAGE             TRY NEXT IF NONE
         MVC   TYP(7),=CL7'  STAGE'  INDICATE  STAGING
         MVI   STGFLG,X'FF'                            REQUEST
         LA    R1,NCYLFAUT         PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITION          EDIT NUMBER
         MVC   CYLF(4),EDIT+4      MOVE CYLINDERS NUMBER
         B     NEXT1               CONTINUE
DESTAGE  EQU   *                   *
         ICM   R2,15,DSTGD         PICK UP DESTAGED CYL NUMBER
         BE    NEXT1               CONTINUE
         MVC   TYP(7),=CL7'DESTAGE'  INDICATE  STAGING REQUEST
         LA    R1,CYLDSTAG         PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITION          EDIT NUMBER
         MVC   CYL(4),EDIT+4       MOVE DESTAGED CYL NUMBER
CYLLRU   EQU   *                   *
         ICM   R2,15,SALLOC        PICK UP DESTAGED CYL BY LRU NUMBER
         BE    NEXT1               CONTINUE
         MVC   TYP(7),=CL7'DESTAGE'  INDICATE  STAGING REQUEST
         LA    R1,NCYLRU           PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITION          EDIT NUMBER
         MVC   CYLRU(4),EDIT+4     MOVE DESTAGED BY LRU CYL NUMBER
NEXT1    EQU   *                   *
         XR    R2,R2               ERASE R2
         IC    R2,PAGES            PICK UP PAGES NUMBER
         LA    R1,NBPAGE           PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITION          EDIT NUMBER
         MVC   NPAG(3),EDIT+5      MOVE PAGE NUMBER
         IC    R2,VTOCSTG          ASSUME VTOC STAGING REQUEST
         CLI   STGFLG,X'FF'        IS ASSUMPTION CORRECT ?
         BE    *+8                 BRANCH IF YES
         IC    R2,VTOCDSTG         ELSE PICK UP DESTAGE VTOC NUMBER
         LA    R1,NBVSTG           PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITION          EDIT NUMBER
         MVC   VTOCS(2),EDIT+6     MOVE VTOC STAGE/DESTAGE NUMBER
         MVC   VOL(6),VOLID        MOVE VOLUME IDENTIFICATION NUMBER
         IC    R1,CARTPAIR         PICK UP CART NUMBER
         LA    R1,1(R1)            ADD ONE TO IT
         STC   R1,CART             MOVE BINARY NUMBER
         OI    CART,X'F0'          MAKE IT PRINTABLE
         L     R1,NBCART           PICK UP PREVIOUS CART MOUNT NUMB
         LA    R1,1(R1)            PLUS THIS ONE
         ST    R1,NBCART           STORE NEW VALUE
         XR    R2,R2               CLEAR
         IC    R2,READWRIT         PICKUP PHYSICAL DRD ADDRESS
         LA    R2,200(R2)          BUMP FOR 20X  FORMAT
         BAL   R9,EDITIONZ         EDIT
         MVC   DRD(3),EDIT+5       MOVE PHYSICAL DRD ADDRESS
         IC    R2,X                PICK UP X CART LOCATION
         BAL   R9,EDITIONZ         EDIT IT
         MVC   XX(3),EDIT+5        MOVE X VALUE
         IC    R2,Y                PICK UP Y CART LOCATION
         BAL   R9,EDITIONZ         EDIT IT
         MVC   YY(2),EDIT+6        MOVE Y VALUE
         MVC   ZZ(1),Z             MOVE Z VALUE
         OI    ZZ,X'F0'            MAKE IT PRINTABLE
         L     R2,SYSSGTM          PICK UP SYSTEM TIME  ASSUME STAGE
         CLI   STGFLG,X'FF'        IS ASSUMPTION CORRECT ?
         BE    *+8                 BRANCH IF YES
         L     R2,SYSDSTM          ELSE PICK UP DESTAGE SYSTEM TIME
         LA    R1,NBTSYS           PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITIONT         EDIT NUMBER
         MVC   TSYS(5),EDIT+3      MOVE SYSTEM TIME
         L     R2,DEVSGTM          PICK UP DEVICE TIME  ASSUME STAGE
         CLI   STGFLG,X'FF'        IS ASSUMPTION CORRECT ?
         BE    *+8                 BRANCH IF YES
         L     R2,DEVDSTM          ELSE PICK UP DESTAGE DEVICE TIME
         LA    R1,NBTS             PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITIONT         EDIT NUMBER
         MVC   TS(5),EDIT+3        MOVE DEVICE TIME
         L     R2,BUSYTIME         PICK UP DRD ACTIVE TIME
         LA    R1,NBTD             PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITIONT         EDIT NUMBER
         MVC   TD(5),EDIT+3        MOVE DRD ACTIVE TIME
         L     R2,LOADTIME         PICK UP LOAD CARTRIDGE TIME
         LA    R1,NBTL             PICK UP ACCUMULATOR ADDRESS
         BAL   R9,CUMUL            ACCUMULATE
         BAL   R9,EDITIONT         EDIT NUMBER
         MVC   TL(5),EDIT+3        MOVE CART LOAD TIME NUMBER
         CLI   SHORTFLG,X'FF'      SHORT REPORT
         BE    SHORT               BRANCH IF YES
         PUT   TRANSIT,LIGNE       WRITE CURRENT LINE
SHORT    EQU   *                   *
         MVI   STGFLG,X'00'        CLEAR STAGE INDICATOR
         B     READ                AND GET A NEW RECORD
         SPACE 5
EDITION  EQU   *                   EDIT ROUTINE
         CVD   R2,PCCWORK          CONVERT TO DECIMAL
         MVC   EDIT(8),MASKZS      MOVE EDIT MASK
         ED    EDIT(8),PCCWORK+4   EDIT
         BR    R9                  RETURN TO CALLER
         SPACE 5
EDITIONZ EQU   *                   EDIT ROUTINE
         CVD   R2,PCCWORK          CONVERT TO DECIMAL
         MVC   EDIT(8),MASK        MOVE EDIT MASK
         ED    EDIT(8),PCCWORK+4   EDIT
         BR    R9                  RETURN TO CALLER
         SPACE 5
EDITIONT LR    R3,R2               EDIT ROUTINE
         SLR   R2,R2               EDIT ROUTINE
         M     R2,=F'10'           BUMP BY 10 TIMES
         SRL   R3,2                AND DIVIDE BY 4 TO GET .1 SECONDS
         CVD   R3,PCCWORK          CONVERT TO DECIMAL
         MVC   EDIT(8),MASKSEC     MOVE EDIT MASK
         ED    EDIT(8),PCCWORK+5   EDIT
         BR    R9                  RETURN TO CALLER
         SPACE 5
CUMUL    EQU   *                   ACCUMULATE ROUTINE
         L     R3,0(R1)            PICK UP PREVIOUS VALUE
         AR    R3,R2               PLUS THESE ONE
         ST    R3,0(R1)            STORE NEW VALUE
         BR    R9                  RETURN TO CALLER
         SPACE 5
RESULT   EQU   *                   *
         MVC   LIGNE+48(12),EDIT   MOVE EDITED VALUE
         PUT   SYSPRINT,LIGNE      WRITE CURRENT LINE
         MVI   LIGNE,X'40'         BLANK
         MVC   LIGNE+1(132),LIGNE       FOR NEXT LINE
         BR    R9                  RETURN TO CALLER
         SPACE 5
TITRE    EQU   *                   WRITE A TITLE
         L     R2,PAGE             PICK UP CURRENT LISTING PAGE
         ST    R9,SAVR9            PROTECT R9
         BAL   R9,EDITION          EDIT IT
         L     R9,SAVR9            RESTORE R9
         MVC   PAG(4),EDIT+4
         LA    R2,LIGNE0           PICK UP FIRST LINE ADDRESS
         LA    R3,8                NUMBER OF LINES TO PRINT
TLOOP    EQU   *                   *
         PUT   SYSPRINT,0(R2)      WRITE THIS LINE
         LA    R2,133(R2)          GETNEXT LINE
         BCT   R3,TLOOP            LOOP TILL END
         L     R2,PAGE             PICK UP PAGE COUNTER VALUE
         LA    R2,1(R2)            ADD ONE
         ST    R2,PAGE             STORE NEW VALUE
         LA    R8,8                INITIALYZE CURRENT LINE NUMBER
         BR    R9                  RETURN TO CALLER
         SPACE 5
CONVDATE EQU   *                   DATE CONVERSION ROUTINE
         STH   R2,DATE+2           STORE JULIAN DAY TO CONVERT
         MVC   YEAR(2),EDIT+6      MOVE YEAR
         LH    R2,PCCWORK+6        PICK UP DECIMAL YEAR
         SRL   R2,4                SHIFT TO ALIGN DATE ON A BYTE
         STC   R2,DATE             PROTECT THIS VALUE
         MVI   PYEARTAB+4,28       ASSUME NON LEAP YEAR
         TM    DATE,X'01'          TEST
         BO    *+16                    FOR
         TM    DATE,X'12'                  LEAP
         BM    *+8                             YEAR
         MVI   PYEARTAB+4,29       SET UP TABLE FOR LEAP YEAR
         MVC   DAT1(9),DATEMASK    MOVE MASK
         XR    R0,R0               CLEAR
         LH    R1,DATE+2           PICK UP BINARY JULIAN DATE
         LA    R2,PYEARTAB-4       PREPARE TO SCAN CONV TABLE
PRIDATE  SR    R1,R0               CONVERT
         LA    R2,4(R2)             FROM JULIAN
         IC    R0,0(,R2)             DATE TO
         CR    R0,R1                  MONTH
         BL    PRIDATE                 AND DAY
         CVD   R1,PCCWORK          CONVERT TO DECIMAL
         MVO   PCCWORK(2),PCCWORK+6(2)   SHIFT FOR EDIT
         ED    DAT1(3),PCCWORK     EDIT DAY
         MVC   DAY(2),DAT1+1       MOVE DAY
         OI    DAY,X'F0'           FORCE 1ST DIGIT NON BLANK
         MVC   MONTH(2),1(R2)      MOVE MONTH
         BR    R9                  RETURN TO CALLER
         SPACE 5
CFINI    EQU   *                   *
         CLI   FLAGREC,X'FF'       ANY VALID INPUT ?
         BNE   OPEN                BRANCH IF NOT
         CLI   SHORTFLG,X'FF'      SHORT REPORT ?
         BE    NOCLOS1             BRANCH IF YES
         CLOSE (TRANSIT,REWIND)
         SPACE 5
NOCLOS1  EQU   *                   *
         LH    R2,DATE1            PICK UP YEAR
         BAL   R9,EDITION          EDIT YEAR
         LH    R2,DATE1+2          PICK UP JULIAN DAY
         BAL   R9,CONVDATE         CONVERT DATE
         MVC   YEAR1(2),YEAR       MOVE BEGINNING YEAR
         MVC   DAY1(2),DAY                            DAY AND
         MVC   MONTH1(2),MONTH                               MONTH
         SPACE 2
         XR    R0,R0               CLEAR
         L     R1,LOWERT           PICK UP TRACE BEGINNING TIME
         D     R0,=F'3600'         OBTAIN HOURS
         LR    R2,R1               PICK UP HOURS
         BAL   R9,EDITION          EDIT
         MVC   DH(2),EDIT+6        MOVE HOURS
         LR    R1,R0               PROTECT MINUTES
         XR    R0,R0               CLEAR
         D     R0,=F'60'           OBTAIN MIN AND SECONDS
         LR    R2,R1               PICK UP MINUTES
         BAL   R9,EDITION          EDIT
         MVC   DM(2),EDIT+6        MOVE MINUTES
         LR    R2,R0               PICK UP SECONDS
         BAL   R9,EDITION          EDIT
         MVC   DS(2),EDIT+6        MOVE SECONDS
         LH    R2,DATE2            PICK UP ENDING DATE
         BAL   R9,EDITION                 AND
         LH    R2,DATE2+2          PICK UP ENDING JULIAN DAY
         BAL   R9,CONVDATE                   CONVERT IT
         MVC   YEAR2(2),YEAR       MOVE ENDING YEAR
         MVC   DAY2(2),DAY                         DAY AND
         MVC   MONTH2(2),MONTH                             MONTH
         XR    R0,R0               CLEAR
         L     R1,UPERTIME         PICK UP TRACE ENDING TIME
         D     R0,=F'3600'         OBTAIN HOURS
         LR    R2,R1               PICK UP HOURS
         BAL   R9,EDITION          EDIT
         MVC   FH(2),EDIT+6        MOVE HOURS
         LR    R1,R0               PROTECT MINUTES
         XR    R0,R0               CLEAR
         D     R0,=F'60'           OBTAIN MIN AND SECONDS
         LR    R2,R1               PICK UP MINUTES
         BAL   R9,EDITION          EDIT
         MVC   FM(2),EDIT+6        MOVE MINUTES
         LR    R2,R0               PICK UP SECONDS
         BAL   R9,EDITION          EDIT
         MVC   FS(2),EDIT+6        MOVE SECONDS
         SPACE 5
OPEN     OPEN  (SYSPRINT,(OUTPUT))
         CLI   FLAGREC,X'FF'       ANY VALID INPUT ?
         BNE   NOLONG              BRANCH IF NOT
         CLI   SHORTFLG,X'FF'      SHORT REPORT ?
         BE    NOLONG              BRANCH IF YES
         OPEN  (TRANSIT,(INPUT))
         BAL   R9,TITRE            WRITE A TITLE
SLOOP    EQU   *                   *
         GET   TRANSIT,LIGNE
         PUT   SYSPRINT,LIGNE
         LA    R8,1(R8)            ADD ONE TO THE CURRENT LINE COUNTER
         CH    R8,=H'53'           END OF PAGE ?
         BNH   SLOOP               BRANCH IF NOT
         L     R2,=A(LIGNE4)       ADDR OF CLOSING LINE
         PUT   SYSPRINT,0(R2)      ELSE CLOSE CURRENT PAGE
         BAL   R9,TITRE            WRITE A NEW TITLE
         B     SLOOP               AND GET NEXT RECORD
RECAP    EQU   *                   *
         CLOSE TRANSIT             CLOSE TRANSIT FILE
         L     R2,=A(LIGNE4)       ADDR OF CLOSING LINE
         PUT   SYSPRINT,0(R2)      CLOSE THE LAST PAGE
         SPACE 1
**********************************************************************
*                  PRODUCE THE SUMMARY REPORT                        *
**********************************************************************
         SPACE 1
NOLONG   EQU   *                   *
         MVC   LIGNEA+11(8),=CL8'TRAC3850'  PROGRAM NAME
         MVC   LIGNEA+50(13),=C'   SUMMARY   '
         MVI   LIGNEA,C'1'         EJECT
         LA    R2,LIGNEA           PICK UP LINE ADDRESS
         CLI   FLAGREC,X'FF'       ANY VALID INPUT ?
         BE    ALLOK               BRANCH IF YES
         MVC   LIGNEA+20(18),=C'NO INPUT AVAILABLE' MOVE MESSAGE
         PUT   SYSPRINT,LIGNEA     WRITE MESSAGE
         B     CLOSE                            AND EXIT
ALLOK    LA    R3,3                NUMBER OF LINES TO PRINT
TLOOP1   EQU   *                   *
         PUT   SYSPRINT,0(R2)      WRITE CURRENT LINE
         LA    R2,133(R2)          GET NEXT LINE
         BCT   R3,TLOOP1           LOOP TILL END OF TITLE
         SPACE 2
         MVI   LIGNE,X'40'         BLANK
         MVC   LIGNE+1(132),LIGNE        CURRENT LINE
         MVI   LIGNE,C'-'          CAUSE SPACE 3
         MVC   LIGNE+10(37),TEXT1  MOVE APPROPRIATE TEXT
         L     R2,CYLSTAG          PICK UP CYL STAGED NUMBER
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT2  MOVE APPROPRIATE TEXT
         L     R2,CYLDSTAG         PICK UP CYL DESTAGED NUMBER
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT3  MOVE APPROPRIATE TEXT
         L     R2,NCYLFAUT         PICK UP CYL STAGED BY CYL FAULT
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT4  MOVE APPROPRIATE TEXT
         L     R2,NCYLRU           PICK UP CYL DESTAGED BY LRU
         BAL   R9,EDITIONZ         EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT5  MOVE APPROPRIATE TEXT
         L     R2,NBPAGE           PICK UP TOTAL PAGES NUMBER
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT6  MOVE APPROPRIATE TEXT
         L     R2,NBVSTG           PICK UP NUMB OF STAGE/DESTAGE VTOC
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT8  MOVE APPROPRIATE TEXT
         L     R2,NBCART           PICK UP NUMBER OF LOADED CARTRIDGES
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT9  MOVE APPROPRIATE TEXT
         L     R2,NBTSYS           PICK UP TOTAL SYSTEM TIME
         SRL   R2,2                TIME IS NOW IN SECONDS
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT10 MOVE APPROPRIATE TEXT
         L     R2,NBTD             PICK UP TOTAL DRD ACTIVE TIME
         SRL   R2,2                TIME IS NOW IN SECONDS
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT11 MOVE APPROPRIATE TEXT
         L     R2,NBTS             PICK UP TOTAL STAGING TIME
         SRL   R2,2                TIME IS NOW IN SECONDS
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT12 MOVE APPROPRIATE TEXT
         L     R2,NBTL             PICK UP TOTAL LOAD CART TIME
         SRL   R2,2                TIME IS NOW IN SECONDS
         BAL   R9,EDITION          EDIT
         BAL   R9,RESULT           MOVE RESULT
         MVC   LIGNE+10(37),TEXT13 MOVE APPROPRIATE TEXT
         L     R2,UPERTIME         ENDING TRACE TIME
         S     R2,LOWTIME          MINUS BEGINNING TRACE TIME
         A     R2,ELAPSED          PLUS ELAPSED TIME OF OTHER DAYS
         BAL   R9,EDITION          EDIT ELAPSED TIME
         MVI   LIGNE,C'0'          SPACE 2
         BAL   R9,RESULT           MOVE ELAPSED TIME
CLOSE    CLOSE TRACE
         CLOSE SYSPRINT
         SPACE 5
         L     R13,SAVE1+4         RESTORE R13 OF CALLER
         LA    R15,0               SET
         ST    R15,16(R13)             RETURN CODE
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         MVI   12(R13),X'FF'       SHOW THAT WE ARE RETURNED
         BR    R14                 RETURN TO CALLER
         SPACE 5
         LTORG
         SPACE 3
PCCWORK  DS    D                   WORK AREA
TEMP     DS    D                   WORK AREA
LOWERT   DS    CL4                 1ST TIME
LOWDAY   DS    CL2                 1 ST DAY
UPERDAY  DS    CL2                 LAST DAY
YEAR     DS    CL2                 CURRENT YEAR
DAY      DS    CL2                 CURRENT DAY
MONTH    DS    CL2                 CURRENT MONTH
EBCDATE  DS    CL6                 SPECIFIED BEGINNING DATE
FLAGDATE DC    X'00'               IF DATE SPECIFIED
UPERYEAR DS    CL1                 LAST YEAR
ELAPSED  DC    F'0'                ELAPSED TIME OF TRACE ACTIVITY
LOWTIME  DC    F'0'                BEGINNING TRACE TIME
UPERTIME DC    F'0'                ENDING TRACE TIME
DATE     DC    F'0'                TRACE DATE WORK AREA
DATE1    DC    F'0'                BEGINNING TRACE DATE
DATE2    DC    F'0'                ENDING TRACE DATE
CYLSTAG  DC    F'0'                STAGED CYL NUMBER
CYLDSTAG DC    F'0'                DESTAGED CYL NUMB
NCYLFAUT DC    F'0'                CYL DUE TO CYL FAULT NUMB
NCYLRU   DC    F'0'                CYL DUE TO LRU ACTION NUMB
NBPAGE   DC    F'0'                PAGES NUMBER
NBVSTG   DC    F'0'                VTOC STAGE/DESTAGE NUMBER
NBCART   DC    F'0'                CARTRIDGES NUMBER
NBTSYS   DC    F'0'                TOTAL SYSTEM TIME
NBTD     DC    F'0'                TOTAL DRD BUSY TIME
NBTS     DC    F'0'                TOTAL STAGING/DESTAGING TIME
NBTL     DC    F'0'                TOTAL LOAD CARTRIDGE TIME
PAGE     DC    F'1'                LISTING PAGE NUMBER
SAVR9    DS    F                   PROTECT  FOR R9
MASK     DC    XL8'4020202020212020'  EDITION MASK
MASKZS   DC    XL8'4020202020202021'  EDITION MASK
MASKSEC  DC    XL8'4040202021204B20'  EDITION MASK
MASKR    DC    XL8'402021204B20206C'
EDIT     DS    CL8                 EDITION WORK AREA
         DC    CL4' '
STGFLG   DC    X'00'               STAGING INDICATOR
SHORTFLG DC    X'00'               SHORT REPORT INDICATOR
FLAGREC  DC    X'00'               VALID INPUT FOUND
DAT1     DC    CL9' '              WORK FOR DATE TRANSLATE
DATEMASK DC    X'402120204040F1F92120'  MASK FOR DATE TRANSLATE
         SPACE 2
PYEARTAB DC    AL1(31),C'01 '      JULIAN
         DC    AL1(28),C'02 '       DATE
         DC    AL1(31),C'03 '        TO
         DC    AL1(30),C'04 '         DAY
         DC    AL1(31),C'05 '          AND
         DC    AL1(30),C'06 '           MONTH
         DC    AL1(31),C'07 '            CONVERSION
         DC    AL1(31),C'08 '             TABLE
         DC    AL1(30),C'09 '              *
         DC    AL1(31),C'10 '               *
         DC    AL1(30),C'11 '                *
         DC    AL1(255),C'12 '                *
         SPACE 2
         DS    0F
RECORD   DS    CL72                REFORMATED TRACE RECORD
         SPACE 5
TRACE    DCB   DSORG=PS,MACRF=(GM),EODAD=CFINI,DDNAME=TRACE
         SPACE 5
SYSPRINT DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT
         SPACE 5
TRANSIT  DCB   DSORG=PS,MACRF=(PM,GM),EODAD=RECAP,DDNAME=TRANSIT
         SPACE 5
LIGNE    DS    CL133               CURRENT LINE
         SPACE 2
LIGNEREC DS    0CL133
         DC    CL65'1'             EJECT
         DC    CL13'   SUMMARY   '
         DC    CL65' '
         SPACE 2
TEXT1    DC    CL37'TOTAL STAGED CYLINDERS'
TEXT2    DC    CL37'TOTAL DESTAGED CYLINDERS'
TEXT3    DC    CL37'TOTAL STAGED CYLINDERS BY CYL FAULT'
TEXT4    DC    CL37'TOTAL CYLINDERS LRUED           '
TEXT5    DC    CL37'TOTAL NB OF ALLOCATED PAGES'
TEXT6    DC    CL37'TOTAL NB OF STAGED/DESTAGED VTOCS'
TEXT8    DC    CL37'TOTAL NB OF LOADED CARTRIDGES'
TEXT9    DC    CL37'TOTAL SYSTEM TIME IN SECONDS'
TEXT10   DC    CL37'TOTAL DRD BUSY TIME IN SECONDS'
TEXT11   DC    CL37'TOTAL STAGING/DESTAGING TIME IN SECS'
TEXT12   DC    CL37'TOTAL LOAD CART TIME IN SECONDS'
TEXT13   DC    CL37'TIME ELAPSED IN SECONDS'
         SPACE 2
LIGNE0   DS    0CL133
         DC    CL11'1'             EJECT
         DC    CL8'TRAC3850'       PROGRAM NAME
         DC    CL92'  (VERSION 1.1)     IPO/FCI MSS TRACE DATA DISPLAY'
         DC    CL5'PAGE'
PAG      DC    CL4' '              PAGE NUMBER
         DC    CL13' '
         SPACE 2
LIGNEA   DS    0CL133
         DC    CL50'-'             SPACE 3
         DC    CL13'  MSS TRACE  '
         DC    CL70' '
         SPACE 2
LIGNEB   DC    CL50' '
         DC    13C'*'
         DC    CL70' '
         SPACE 2
LIGNEC   DC    CL25'0'             SPACE 2
         DC    C'TRACE FROM '
MONTH1   DC    CL2' '              MONTH
         DC    C'/'
DAY1     DC    CL2' '              DAY
         DC    C'/19'
YEAR1    DC    CL2' '              YEAR
         DC    C' AT '
DH       DC    CL2' '
         DC    C' H '
DM       DC    CL2' '
         DC    C'.'
DS       DC    CL2' '
         DC    C' TO '
MONTH2   DS    CL2' '
         DC    C'/'
DAY2     DC    CL2' '              DAY
         DC    C'/19'
YEAR2    DC    CL2' '              YEAR
         DC    C' AT '
FH       DC    CL2' '
         DC    C' H '
FM       DC    CL2' '
         DC    C'.'
FS       DC    CL2' '
         DC    CL45' '
         SPACE 2
LIGNE1   DC    C'-'                SPACE 3
         DC    130C'-'
         DC    CL2' '
         SPACE 2
LIGNE2   DC    CL7' ×'
         DC    CL11'×'
         DC    CL11'×  STAGE/ ×'
         DC    CL7' CYL  ×'
         DC    CL8' FAULT ×'
         DC    CL7'  LRU ×'
         DC    CL8' PAGES ×'
         DC    CL7' VTOC ×'
         DC    CL9'        ×'
         DC    CL11'          ×'
         DC    CL7'      ×'
         DC    CL6'     ×'
         DC    CL8' TIME  ×'
         DC    CL8' TIME  ×'
         DC    CL8' TIME  ×'
         DC    CL8' TIME  ×'
         DC    CL2' '
         SPACE 2
LIGNE3   DC    CL7' × DAY '
         DC    CL11'×   TIME'
         DC    CL11'× DESTAGE ×'
         DC    CL7'  NB  ×'
         DC    CL8'  CYL  ×'
         DC    CL7'  CYL ×'
         DC    CL8'   NB  ×'
         DC    CL7'  S/D ×'
         DC    CL9' VOLUME ×'
         DC    CL11'   LXYZ   ×'
         DC    CL7' CART ×'
         DC    CL6' DRD ×'
         DC    CL8' SYST  ×'
         DC    CL8'  DRD  ×'
         DC    CL8' STAGE ×'
         DC    CL8'  LOAD ×'
         DC    CL2' '
         SPACE 2
LIGNE4   DC    CL1' '
         DC    130C'-'
         DC    CL2' '
         SPACE 3
LIGNEN   DS    0CL133
         DC    CL7' ×    '
         DC    CL11'×          '
         DC    CL11'×         ×'
         DC    CL7'      ×'
         DC    CL8'       ×'
         DC    CL7'      ×'
         DC    CL8'       ×'
         DC    CL7'      ×'
         DC    CL9'        ×'
         DC    CL11'          ×'
         DC    CL7'      ×'
         DC    CL6'     ×'
         DC    CL8'       ×'
         DC    CL8'       ×'
         DC    CL8'       ×'
         DC    CL8'       ×'
         DC    CL2' '
         SPACE 2
         EJECT
LINE     DSECT                     CURRENT LINE DESCRIPTION DSECT
         DS    CL4
CURDAY   DS    CL2                 CURRENT DAY
         DS    CL3
TIME     DS    CL8                 RECORD TRACE TIME STAMP
         DS    CL3
TYP      DS    CL7                 STAGE / DESTAGE
         DS    CL3
CYL      DS    CL4                 CYL COUNT
         DS    CL4
CYLF     DS    CL4                 STAGED CYL BY CYL FAULT
         DS    CL3
CYLRU    DS    CL4                 DESTAGED CYL BY LRU ACTION
         DS    CL4
NPAG     DS    CL3                 NUMBER OF PAGES
         DS    CL5
VTOCS    DS    CL2                 NUMB OF VTOC REQUESTS
         DS    CL4
VOL      DS    CL6                 VOLUME ID
         DS    CL3
XX       DS    CL3                 X LOCATION
         DS    CL1
YY       DS    CL2                 Y LOCATION
         DS    CL1
ZZ       DS    CL1                 Z LOCATION
         DS    CL4
CART     DS    CL1                 CARTRIDGE NUMBER
         DS    CL5
DRD      DS    CL3                 DRD PHYSICALL ADDRESS
         DS    CL3
TSYS     DS    CL4                 SYSTEM TIME
         DS    CL4
TD       DS    CL4                 DRD TIME
         DS    CL4
TS       DS    CL4                 STAGING TIME
         DS    CL4
TL       DS    CL4                 LOAD CART TIME
         DS    CL4
         EJECT
REFTRACE DSECT                     REFORMATTED TRACE RECORD DSECT
         SPACE 1
STGD     DS    CL4                 NUMB OF CYL STAGED
CFAULT   DS    CL4                 NUMB OF CYL STAGED WITH CYL FAULT
SYSSGTM  DS    CL4                 SYSTEM STAGE TIME
DEVSGTM  DS    CL4                 DEVICE STAGE TIME
DSTGD    DS    CL4                 NUMB OF CYL DESTAGED
SALLOC   DS    CL4                 NUMB OF CYL DESTAGED BY LRU ACTION
SYSDSTM  DS    CL4                 SYSTEM DESTAGE TIME
DEVDSTM  DS    CL4                 DEVICE DESTAGE TIME
BUSYTIME DS    CL4                 DRD BUSY TIME
LOADTIME DS    CL4                 CARTRIDGE LOAD TIME
LOADNUM  DS    CL1                 NUMB OF CART LOADS
SVEYEAR  DS    CL1                 YEAR
SVEJUL   DS    CL2                 JULIAN DAY
SVETME   DS    CL4                 TIME OF DAY IN SECONDS
PAGES    DS    CL1                 NUMBER OF PAGES
MINIMUN  DS    CL1                 NUMB OF MINI-HEADERS
HITNUM   DS    CL1                 NUMB OF TIME THE NEEDED CART WAS
*                                    ALREADY LOADED
STGDNUM  DS    CL1                 NUMB OF STAGE REQUESTS
DSTGDNUM DS    CL1                 NUMB OF DESTAGE REQUESTS
VTOCSTG  DS    CL1                 NUMB OF VTOC STAGINGS
VTOCDSTG DS    CL1                 NUMB OF VTOC DESTAGINGS
LIBRARY  DS    CL1                 MSF NUMBER
DSPORT   DS    CL1                 LOGICAL STAGING ADAPTER NUMBER
DSCU     DS    CL1                 STAGING ADAPTER CONTROL UNIT ADDRESS
Z        DS    CL1                 Z CARTRIDGE COORDINATE
Y        DS    CL1                 Y CARTRIDGE COORDINATE
X        DS    CL1                 X CARTRIDGE COORDINATE
CARTPAIR DS    CL1                 CARTRIDGE PAIR NUMBER (0 OR 1)
READWRIT DS    CL1                 PHYSICAL DRD ADDRESS
VOLID    DS    CL6                 VOLUME IDENTIFICATION NUMBER
CHARDRC  DS    CL1                 LOW-ORDER DIGIT OF DRC IN EBCDIC
         DS    CL2                 RESERVED
         EJECT
         END
