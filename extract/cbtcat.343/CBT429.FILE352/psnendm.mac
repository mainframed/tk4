*          DATA SET PSNENDM    AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNENDM - TERMINATE MEMBER PROCESSING - TOTAL UP'
PSNENDM  PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    THE TERMINATOR FOR A MEMBER - PUT OUT HIS
*                    POINT TOTAL AND ANY OTHER INFORMATION
*                    THOUGHT USEFUL
*
         ZAP   PSWLINUM,PSWPZERO  CLEAR THE LINE NUMBER
         ZAP   PSWIREC#,PSWPZERO  CLEAR THE INPUT RECORD COUNT      NTL
*
         BAL   R14,ENDCOMNT       CHECK FOR ENDING COMMENTS
*
*                    CALL THE END OF PROC EDITOR
         L     R15,PSWEDITP       EDIT PROC
         BALR  R14,R15
         LH    R2,PSWPOINT        HIS ERROR POINT TOTAL
         BAL   R14,COUNT
         CVD   R2,WKDOUBL
         MVC   WKMASK,CONMASK
         ED    WKMASK,WKDOUBL+6
         PSNMERR  ERRPOINT,PSWPNAME,(WKMASK+1,3)
         SR    R15,R15
         B     RETURN
*
CONMASK  DC    X'40202120'        MASK FOR ERROR COUNT
*
         EJECT
*                    TO CHECK FOR AND HANDLE AND END-OF-PROC
*                    COMMENTS THEY MAY HAVE INSERTED
*
ENDCOMNT  PSNMBLOK   ECM,RENT=YES
         L     R1,PSWTHISC        ADDRESS OF THE COMMENTS
         ST    R1,PSWENDCM        INTO THE ENDING COMMENTS POINTER
*
         SR    R1,R1              PREPARE TO CLEAR THE POINTERS AGAIN
         ST    R1,PSWTHISC
         ST    R1,PSWLASTC
*
         B     ECMEXIT
         EJECT
*                    MAINTAIN THE COUNTERS FOR EITHER GOOD
*                    OR CONTAINING ERRORS
*
COUNT    PSNMBLOK  CT,RENT=YES
         LTR   R2,R2              IS THE POINT COUNT ZERO
         BZ    CTGOOD
*                    SOME ERROR ENCOUNTERED
         PSNMCTR  PSWCBAD         ADD TO THE BAD COUNTER
         B     CTEXIT
*
CTGOOD   EQU   *
         PSNMCTR  PSWCGOOD
         B     CTEXIT
         EJECT
WORKAREA DSECT
         DS    18F
WKDOUBL  DS    D
WKMASK   DS    CL4
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
