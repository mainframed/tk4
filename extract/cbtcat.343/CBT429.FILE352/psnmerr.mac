*          DATA SET PSNMERR    AT LEVEL 002 AS OF 10/30/80
         MACRO
&TAG     PSNMERR &ERR,&SAVR=NO
.*                  TO CALL THE ERROR PROCESSOR WITH A MESSAGE
.*                  AND OPTIONALLY UP TO 6 FIELDS TO BE INSERTED
.*                  INTO THE MESSAGE
         LCLC  &WK,&LN,&L         WORK FIELDS FOR SYNTAX-ING
         LCLA  &N,&F              INDEX THROUGH SYSLIST AND ERV'S
&N       SETA  1                  JUST TO START OUT WITH
&L       SETC  'L'''              COMMON USAGE HERE
         AIF   (T'&TAG EQ 'O').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         AIF   ('&SAVR' EQ 'NO').NOSAVR
         STM   R14,R2,PSWERRSV    SAVE KEY POINTERS
.NOSAVR  ANOP
.*
.NEXT    ANOP
&N       SETA  &N+1               ON TO NEXT OF THE LIST
         AIF   (&N GT N'&SYSLIST).LISTDON
         AIF   (T'&SYSLIST(&N) EQ 'O').NEXT   NOTHING IN THIS ONE
&F       SETA  &N-1               SUFFIX OF ERROR POINTER FIELD
&WK      SETC  '&SYSLIST(&N,1)'   ISOLATE FIELD LOCATION
&LN      SETC  '&SYSLIST(&N,2)'   AND ITS LENGTH
         AIF   ('&WK'(1,1) EQ '(').FLDREG    LOCATION IN REG FORM
         LA    R0,&WK
         ST    R0,PSWERV&F
         AIF   ('&LN' EQ '').NRNOL   NO REG, NO LENGTH
         AIF   ('&LN'(1,1) NE '(').ILENLT   LENGTH IS LITERAL
&LN      SETC  '&LN'(2,K'&LN-2)   PLUCK OUT THE REGISTER
         STC   &LN,PSWERV&F
         AGO   .NEXT
.ILENLT  ANOP
         MVI   PSWERV&F,&LN
         AGO   .NEXT
.NRNOL   ANOP
         MVI   PSWERV&F,&L&WK
         AGO   .NEXT
.*
.*                   AND IF THE INPUT IS IN REG FORM
.FLDREG  ANOP
&WK      SETC  '&WK'(2,K'&WK-2)
         ST    &WK,PSWERV&F
         AIF   ('&LN' NE '').IRSETL BIF LENGTH - NEED IT, YOU KNOW
         MNOTE *,'ASSUME LENGTH IN FIRST BYTE OF REGISTER'
         AGO   .NEXT
.IRSETL  ANOP
         AIF   ('&LN'(1,1) NE '(').IRLENLT   LENGTH IS LITERAL
&LN      SETC  '&LN'(2,K'&LN-2)   PLUCK OUT THE REGISTER
         STC   &LN,PSWERV&F
         AGO   .NEXT
.IRLENLT ANOP
         MVI   PSWERV&F,&LN
         AGO   .NEXT
.*
.LISTDON ANOP                   ALMOST DONE WITH THE FIELDS LIST
         AIF   (N'&SYSLIST EQ 1).LISTX CHECK FOR NO VALUES
&F       SETA  N'&SYSLIST-1       ENDING ON PROPER ONE
         OI    PSWERV&F,X'80'
.LISTX   ANOP
         AIF   ('&ERR' EQ '(1)').ERSX POINTER IS PRESET
         AIF   ('&ERR' EQ '(R1)').ERSX POINTER IS PRESET
         AIF   ('&ERR'(1,1) EQ '(').ERSR   REG NOTATION
         LA    R1,&ERR
         AGO   .ERSX
.ERSR    ANOP
&WK      SETC  '&ERR'(2,K'&ERR-2)
         LR    R1,&WK
.ERSX    ANOP
.*
         L     R15,PSWERROR
         BALR  R14,R15
.EXIT    ANOP
         MEND
