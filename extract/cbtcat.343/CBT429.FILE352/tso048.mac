*          DATA SET TSO048     AT LEVEL 004 AS OF 12/03/80
*          DATA SET TSO048     AT LEVEL 003 AS OF 08/22/80
*          DATA SET TSO048     AT LEVEL 002 AS OF 08/21/80
*          DATA SET TSO048     AT LEVEL 001 AS OF 08/12/80
*        DATA SET INTFC1047  AT LEVEL 001 AS OF 11/22/76
*        DATA SET 00PANVIEW  AT LEVEL 004 AS OF 11/16/76
*        DATA SET PAMREAD    AT LEVEL 001 AS OF 01/16/76
*        DATA SET 8PAMREADO  AT LEVEL 006 AS OF 06/13/74
PANV     TITLE 'OS PANVALET/3277 FULL-SCREEN MODULE DISPLAY PROGRAM'
         SPACE 3
         MACRO
&LABEL   SCAN  &F1,&LF1,&F2,&LF2,&NONE,&SQ
         LCLB  &B1,&B2
         LCLC  &C,&MNO
&MNO     SETC  '&SYSNDX'
.*
         SPACE 1                                                    FCI
         AIF   ('&F1'(1,1) NE '(').NRF1
&LABEL   LR    1,&F1(1)            ADDRESS OF FIRST FIELD
         AIF   (T'&LF1 EQ 'O').E1
         AIF   ('&LF1'(1,1) NE '(').NRLF1
         LR    15,&LF1(1)          LENGTH OF FIRST FIELD
         AGO   .FIELD2
.NRLF1   LA    15,&LF1             LENGTH OF FIRST FIELD
         AGO   .FIELD2
.NRF1    ANOP
&LABEL   LA    1,&F1               ADDRESS OF FIRST FIELD
         AIF   (T'&LF1 NE 'O').NLF1O
&C       SETC  'L'''
         LA    15,&C.&F1           LENGTH OF FIRST FIELD
         AGO   .FIELD2
.NLF1O   AIF   ('&LF1'(1,1) NE '(').NRRLF1
         LR    15,&LF1(1)          LENGTH OF FIRST FIELD
         AGO   .FIELD2
.NRRLF1  LA    15,&LF1             LENGTH OF FIRST FIELD
.*
.FIELD2  BALR  14,0                LOOP STARTS HERE
         AIF   ('&F2'(1,1) EQ '''').F2S
         AIF   ('&F2'(1,1) EQ '(').F2R
         AIF   (T'&LF2 NE 'O').LF2P
         CLC   &F2,0(1)            IS IT EQUAL ?
         AGO   .BE
.LF2P    CLC   &F2.(&LF2),0(1)     IS IT EQUAL ?
         AGO   .BE
.F2S     AIF   (K'&F2 NE 3).NCLI
         CLI   0(1),C&F2           IS IT EQUAL ?
         AGO   .BE
.NCLI    AIF   (T'&LF2 NE 'O').E2
         CLC   COMP&MNO,0(1)       IS IT EQUAL ?
&B1      SETB  1
         AGO   .BE
.F2R     AIF   (T'&LF2 EQ 'O').E3
         AIF('&LF2'(1,1) EQ '(').E4
         CLC   0(&LF2,1),0&F2      IS IT EQUAL ?
.*
.BE      AIF   ('&SQ' EQ 'NE').BNE
         BE    EXIT&MNO            YES EXIT
         AGO   .BEND
.BNE     BNE   EXIT&MNO            NO EXIT
.BEND    ANOP
         LA    1,1(1)              NEXT POSITION
         BCTR  15,14               DO IT AGAIN
         AIF   (T'&NONE NE 'O').NONENO
         LA    1,0                 SHOW NOT FOUND
&B2      SETB  1
         AGO   .EXIT
.NONENO  AIF   ('&NONE'(1,1) NE '(').NONENR
         BR    &NONE(1)            NOT FOUND
         AGO   .EXIT
.NONENR  B     &NONE               NOT FOUND
.*
.EXIT    AIF   (NOT &B1).EXIT2
         AIF   (NOT &B2).EXIT1
         B      EXIT&MNO           GO AROUND
         SPACE 1                                                    FCI
.EXIT1   ANOP
COMP&MNO DC    C&F2,0H'0'          COMPARE FIELD
.EXIT2   ANOP
EXIT&MNO EQU   *                   EXIT
         SPACE 1                                                    FCI
         MEXIT
.*
.E1      MNOTE 'MISSING 2ND OPERAND'
         AGO   .E0
.E2      MNOTE 'INVALID 4TH OPERAND'
         AGO   .E0
.E3      MNOTE 'MISSING 4TH OPERAND'
.E0      MNOTE 'EXPANSION ABORTED'
         MEXIT
         MEND
         EJECT
TSO048   CSECT
*****
*)F   FUNCTION -
*         PANVIEW IS DESIGNED TO RUN AS AN INTERACTIVE PROGRAM UNDER
*         IBM'S TSO USING 327X-TYPE TERMINALS WITH 24-LINE SCREENS.
*         USING THE PAN ACCESS METHOD (V10.0) TO AVOID THE NEED FOR
*         PAN RETRIEVAL TO PARTITIONED AND/OR SEQUENTIAL DATA SETS,
*         ANY GROUP OF 23 DATA RECORDS FROM ANY MODULE IN A DISK PAN
*         LIBRARY MAY BE DISPLAYED ON THE SCREEN.  THE TOP LINE IS
*         RESERVED FOR DISPLAY OF THE DISPLAY REQUEST LAST PERFORMED
*         AND FOR SPECIFICATION OF THE NEXT REQUEST.  THE PROGRAM
*         PROVIDES A FAST AND RELATIVELY EFFICIENT MEANS TO EXAMINE
*         DATA RECORDS IN PAN LIBRARIES, PARTICULARLY WHERE THERE
*         ARE MANY SMALL DECKS STORED.
*)X   SYNTAX   -
*     PANVIEW
*)O   OPERANDS -  NONE
*
*         DISPLAY PARAMETERS:
*
*              THE ONLY FIELDS ON THE SCREEN THAT MAY BE MODIFIED
*              ARE THE LOW-INTENSITY CHARACTERS ON THE FIRST LINE.
*              COMMAS AND ASTERISKS SHOULD REMAIN UNCHANGED AS THEY
*              ARE USED AS DELIMITERS IN THE SEARCH ARGUMENTS WITHIN
*              THE PROGRAM ITSELF.
*
*))PANNAMEXX - THE NAME OF THE MODULE IN THE PANVALET LIBRARY TO BE
*              DISPLAYED.
*))/NNNN     - DISPLAY THE DIRECTORY LISTING OF PANVALET LIBRARY
*              SPECIFIED BY THE DDNAME BEGINNING WITH MEMBERS WHOSE
*              NAMES START WITH THE CHARACTERS 'NNNN'.
*))XFERPAN   - THE DDNAME/FILE NAME ASSOCIATED WITH THE PANVALET
*              LIBRARY TO BE OPENED.
*  LIBRARIES AVAILABLE ARE:
*              XFERPAN  - 'INTRO.XFERPAN'
*              PRODPAN  - 'FCI1.PANSORC'
*))00000001  - THE RELATIVE LINE NUMBER (DEFAULT) AT WHICH THE
*              DISPLAY IS TO START.
*))80        - THE NUMBER OF COLUMNS OF STORED DATA TO BE DISPLAYED.
*              IN THE EVENT THAT LESS THAN 73 COLUMNS IS REQUESTED
*              PANVIEW WILL AUTOMATICALLY SEQUENCE THE DISPLAYED
*              LINE NUMBERS (NO CHANGE TO THE MODULE).
*
*****
* NOTE: ABOVE HELP IS FOR PANVIEW CLIST WHICH ALLOCATED XFERPAN AND FCI
*       PRODPAN DDNAMES, AND HAS THE / (DIRECTORY) FEATURE.         FCI
*   PROGRAM IS KNOW AS #PANVIEW                                     FCI
         EJECT  ,                                                   FCI
******************************************************************* FCI
*  THE SCAN MACRO IN PANVIEW PERFORMS THE FUNCTION OF SEARCHING A
*  GIVEN FIELD FOR A CHARACTER STRING.  THE OPERANDS ARE:
*     1.  FIELD TO BE SCANNED
*     2.  LENGTH OF FIELD TO BE SCANNED (ACTUAL LENGTH OR REGISTER)
*     3.  SCAN FIELD (CHARACTER OR POINTER)
*     4.  LENGTH OF SCAN
*     5.  ADDRESS TO BRANCH TO IF FIELD NOT FOUND
*
*  THE ORIGINAL IMPLEMENTATION WAS OS/MVT 360/65 (21.8) WITH 3270'S
*  CONNECTED LOCALLY.
*  AUTHOR:  KIRK HANSEN
*           MANUFACTURER'S LIFE INSURANCE COMPANY
*           200 BLOOR STREET EAST
*           TORONTO, ONTARIO, CANADA
*****
*  UPDATED 20MAY80, MVS/JES2 SE 1.1-(3.8) ACF/VTAM-R2 - AMDAHL 470V/6-I
*    FOR USE IN A SPF/2.2.1 ENVIRONMENT WITH PANVALET/V10
*        WILLIAM SMITH
*        MEMOREX CORPORATION
*        LSSG DATA PROCESSING SERVICES
*        DEPT. 8205, MS/10-35
*        SAN TOMAS AT CENTRAL EXPRESSWAY
*        SANTA CLARA, CALIFORNIA  95052
*        (408)-987-3919
*
*  .  OBTAINED FROM THE PANSOPHIC SYSTEMS EXCHANGE CATALOG - VOL. 3
*     (ENTRY NUMBER P9.0-665-1047)
*  .  INCORPORATED THE 'SCAN' MACRO AS IN-LINE CODE
*  .  REPLACED THE 'REGS' MACRO WITH SIMPLE REGISTER EQUATES
*  .  CHANGED THE STC INSTRUCTION PRIOR TO LABEL 'MVCED' TO AN
*     EXECUTE INSTRUCTION
*  .  CORRECTED THE FOLLOWING CONSTANTS:
*        1. END1PRE -  X'27' --> X'C7'
*        2. BUFPRE  -  X'27' --> X'C7'
*     THIS CORRECTION WAS REQUIRED TO PREVENT INVALID ATTRIBUTE
*     BYTE PROCESSING USING THE TPUT MACRO AT BOTH INITIALIZATION
*     AND TERMINATION OF PANVIEW (IT TERMINATES A USER'S ADDRESS
*     SPACE - A SOFTWARE INDUCED HARDWARE ERROR WILL OCCUR)
*  .  CHANGED THE DEFAULT POSITIONING OF THE CURSOR TO BE THAT OF
*     'PANNAMEXXX' INSTEAD OF 'STLINE2'
*  .  REPLACED AND EXPANDED THE 'MASTER MENU' TO PROVIDE A MORE
*     MEANINGFUL AND SUCCINCT SET OF USE INSTRUCTIONS
*  .  GENERAL COMMENTING, ALIGNMENT, CLEANUP, ADDED TSO HELP INFO
*                                                                   FCI
*  KEN TRUE - FAIRCHILD CAMERA AND INSTRUMENT  11 AUG 80            FCI
*   . ADDED SPF SCREEN SUPPORT (115D7E AND 115D7F)                  FCI
*   . AUTO RESET TO LINE 1 WITH NEW NAME OR END OF DATA             FCI
*   . ADDED NEW DDNS TO HELP/INFO PANEL                             FCI
*   . ADDED / (DIRLIST) FEATURE TO LIST LIBRARY MEMBERS             FCI
*
*****
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,15
BEGIN    STM   14,12,12(13)        STORE REG
         LR    12,13               SAVE CALLERS REG
         CNOP  0,4
         BAL   13,SAVEAREB         SET SAVEAREA ADDRESS
SAVEAREA DC    18A(0)              REGISTER SAVE AREA
SAVEAREB ST    12,4(13)            SET CALLER IN CALLEES
         ST    13,8(12)            CALLEES IN CALLERS
         DROP  15
         USING SAVEAREA,R13        USE R13 AS BASE
         USING SAVEAREA+4000,R12   DEFINE BASE FOR END OF PROGRAM.
         LA    R12,4000(R13)
*  GET PARAMETERS - MODULE NAME, DDNAME, START LINE#, LINE WIDTH.
         L     R1,0(R1)
         LH    R2,0(R1)            LENGTH OF PARM FIELD
         LA    R3,2(R1)            START OF PARM FIELD
         EXTRACT TIOTADDR,FIELDS=(TIOT)
         LTR   R2,R2               IS PARM GIVEN?
         BP    CHKPARM             YES - GO PROCESS IT
         LA    R14,BUF23
         LA    R15,1
         LA    R3,NAMER+78
         B     ERRDISP
         EJECT
*.....................................................................*
* GET THE OPTIONS TO USE BY PARSING THE USER REPLY TO THE TGET        *
*.....................................................................*
         SPACE
GETPARM  EQU   *                   NO, SO GET ONE FROM 3270
         TGET  PARMSTR,L'PARMSTR
         TR    PARMSTR,CAPSONLY
         LA    R2,PARMSTR+L'PARMSTR
         SCAN  PARMSTR,,'*',,NOAST
         LR    R2,R1
NOAST    LA    R3,PARMSTR
         LA    R2,0(R2)
         SR    R2,R3               LENGTH OF PARM STRING
         BNP   RETURN              EXIT IF LENGTH NOT POSITIVE
CHKPARM  EQU   *
         LA    R9,1                DEFAULT STARTING LINE #
         LA    R8,80               DEFAULT LINE WIDTH
         SR    R5,R5
         SCAN  (R3),(R2),',',,NOCOMMA  SEEK FIRST COMMA IN PARM
         LA    R4,1(R1)            POINT TO SECOND PARM ITEM
         LA    R5,0(R2,R3)
         SR    R5,R4               LENGTH REMAINING IN PARM FIELD
         SR    R2,R5
         BCTR  R2,0                LENGTH OF FIRST PARM ITEM
NOCOMMA  LTR   R2,R2
         BNP   NOP1                NO FIRST ITEM
         MVC   NAME1(11),=CL11' '
         MVC   NAME1+11(11),=CL11' '
         MVC   NAME,NAME1
         BCTR  R2,0
         STC   R2,MVC1+1
         STC   R2,MVC1A+1
         CLI   MVC1+1,L'NAME-1
         BNH   MVC1
         MVC   NAME,0(R3)
         LA    R3,NAMER
         B     ERR4SET             PAN MODULE NAME TOO LONG
MVC1     MVC   NAME1(11),0(R3)
MVC1A    MVC   NAME(11),0(R3)
         CLI   NAME,C'/'           DIRECTORY MODE REQUESTED         FCI
         BE    $DIR                YES..PERFORM SONG AND DANCE      FCI
         NI    MODESW,255-DIRMODE  NO..TURN OFF DIRECTORY MODE      FCI
         B     NOP1                                                 FCI
$DIR     MVC   NAME1(10),NAME+1    MOVE IN NAME W/O SLASH           FCI
         OI    MODESW,DIRMODE      AND SET DIRECTORY MODE           FCI
NOP1     LTR   R5,R5
         BNP   ENDPARMS            NO MORE IN PARM FIELD
         SR    R2,R2
         SCAN  (R4),(R5),',',,NOCOMMA2
         LA    R3,1(R1)            START OF NEXT ITEM
         LA    R2,0(R4,R5)
         SR    R2,R3               REMAINING PARM FIELD LENGTH
         SR    R5,R2
         BCTR  R5,0                SECOND PARM ITEM LENGTH
NOCOMMA2 LTR   R5,R5
         BNP   NOP2                NO SECOND PARM ITEM
         MVC   DDNAME,=CL11' '
         BCTR  R5,0
         STC   R5,MVC2+1
         CLI   MVC2+1,L'DDNAME-1
         BNH   MVC2
         MVC   DDNAME,0(R4)
         LA    R3,DDNAME
         B     ERR4SET             DDNAME TOO LONG
MVC2     MVC   DDNAME,0(R4)
NOP2     LTR   R2,R2
         BNP   ENDPARMS            NO MORE PARM ITEMS
         SR    R5,R5
         SCAN  (R3),(R2),',',,NOCOMMA3
         LA    R4,1(R1)            POINT TO FOURTH PARM ITEM
         LA    R5,0(R2,R3)
         SR    R5,R4               REMAINING LENGTH IN PARM FIELD
         SR    R2,R5
         BCTR  R2,0                LENGTH OF THIRD PARM ITEM
NOCOMMA3 LTR   R2,R2
         BNP   NOP3                NO THIRD PARM ITEM
         SR    R9,R9
         SR    R1,R1
         CH    R2,=H'8'
         BNH   P3GET
         LA    R3,STLINEX
ERR4SET  LA    R14,ERR4
         LA    R15,L'ERR4
         B     ERRDISP
P3GET    NI    0(R3),X'0F'
         IC    R1,0(R3)
         MH    R9,=H'10'
         AR    R9,R1
         LA    R3,1(R3)
         BCT   R2,P3GET
NOP3     LTR   R5,R5
         BNP   ENDPARMS            NO FOURTH PARM ITEM
         SR    R8,R8
         SR    R1,R1
         CH    R5,=H'2'
         BNH   P4GET
         LA    R3,LWIDE
         B     ERR4SET             PARM ITEM TOO LONG
P4GET    NI    0(R4),X'0F'
         IC    R1,0(R4)
         MH    R8,=H'10'
         AR    R8,R1
         LA    R4,1(R4)
         BCT   R5,P4GET
         EJECT
*.....................................................................*
*  GOT A SET OF PARMS....SET UP TO DO PROCESSING                      *
*.....................................................................*
         SPACE
ENDPARMS EQU   *
         CVD   R8,TEMP8
         OI    TEMP8+7,X'0F'
         UNPK  LWIDE,TEMP8
         CVD   R9,TEMP8
         OI    TEMP8+7,X'0F'
         UNPK  STLINE,TEMP8
         LA    R15,23(R9)
         CVD   R15,TEMP8
         OI    TEMP8+7,X'0F'
         UNPK  STLINE2,TEMP8
         BCTR  R8,0
         STC   R8,MVC23+1          SET LENGTH FOR RECRD MOVE INTO BUFFR
         CLI   MVC23+1,79
         BH    ERR7SET             RECORD LENGTH GIVEN TOO LARGE
         CLI   DDNAME,C' '
         LA    R3,DDNAME
         BE    ERR9SET
         CLI   NAME1,C' '
         LA    R3,NAMER
         BE    ERR9SET
*  CHECK DDNAME AGAINST LIST IN TIOT
         L     R2,TIOTADDR
         LA    R2,24(R2)           POINT TO FIRST DD ENTRY IN TIOT
CHKDD    CLC   0(4,R2),=F'0'       END OF TIOT?
         BNE   CHKDD1              NO
CHKDD0   LA    R14,ERR8
         LA    R15,L'ERR8
         LA    R3,DDNAME
         B     ERRDISP             GO PUT ERROR MSG
CHKDD1   CLC   4(8,R2),DDNAME
         BE    CHKDDOK             DDNAME FOUND, SO CARRY ON
         SR    R3,R3
         IC    R3,0(R2)
         AR    R2,R3               POINT TO NEXT DD ENTRY
         B     CHKDD
CHKDDOK  EQU   *
         EJECT
*.....................................................................*
*  ALL SETUP ... DO THE OPEN/READS                                    *
*.....................................................................*
         SPACE
         CLI   PARMSTR,C'/'         DIRECTORY WANTED?               FCI
         BE    LISTDIR             YES..GO DO IT                    FCI
         TM    MODESW,DIRMODE      SET BY PRIOR INCARNATION         FCI
         BO    LD#INIT             YEP..CONTINUE WHERE WE LEFT OFF  FCI
         SPACE 1                                                    FCI
         TM    OPENFLAG,1          LIBRARY OPEN ALREADY ?
         BNO   GOOPEN              NO,  SO OPEN
         CLC   LASTDD,DDNAME       YES, SO CHECK FOR CHANGE IN DDNAME
         BNE   GOCLOP              CLOSE, AND OPEN TO NEW FILE
         CLC   LASTNAME,NAME1      CHECK FOR CHANGE IN MODULE NAME
         BNE   GETDIR              GO SEARCH DIRECTORY
         C     R9,LAST1            CHECK START LINE
         BNH   GOCLOP              NEW START LINE BELOW OLD
         C     R9,LAST2            CHECK END LINE
         BNH   GOTOLD1             START LINE IS IN BUFFER
         CLC   DSTMTS,STLINE       CHECK FOR START LINE IN RANGE
         BL    ENDREAD1
         LR    R10,R9              START LINE HAS NOT YET BEEN READ
         S     R10,LAST2
         B     GOTOLD1X
         SPACE 1                                                    FCI
GOTOLD1  LR    R15,R9              PREPARE TO REARRANGE BUFFER
         S     R15,LAST1
         ST    R9,LAST1
         MH    R15,=H'80'          OFFSET TO START LINE IN BUF23
         L     R14,LAST2
         SR    R14,R9
         LA    R14,1(R14)          NO. OF USEFUL LINES IN BUFFER
         LA    R11,23
         SR    R11,R14             NO NO-OF-LINES-TO-READ COUNTER
         LA    R7,BUF23
         SPACE 1                                                    FCI
MOVEOLD1 LA    R1,0(R7,R15)
         MVC   0(80,R7),0(R1)      MOVE A LINE
         LA    R9,1(R9)
         MVI   0(R1),C' '
         MVC   1(79,R1),0(R1)      BLANK OUT OLD LINE
         LA    R7,80(R7)           POINT TO NEXT SLOT IN BUF23
         BCT   R14,MOVEOLD1        GO MOVE NEXT LINE, IF ANY
         SPACE 1                                                    FCI
         LA    R10,1               SET FLAG FOR I/P LOOP
         CLC   DSTMTS,STLINE
         BL    ENDREAD1            START LINE OUT OF RANGE
         CLC   RECORD(2),=C'$*'    CHECK FOR END OF DATA
         BNE   READ
         B     TPUTR0
         SPACE 1                                                    FCI
GOCLOP   BAL   R10,CLOSE           PREPARE TO ACCESS ANOTHER LIBRARY
         SPACE 1                                                    FCI
GOOPEN   BAL   R10,OPEN
         EJECT
*.....................................................................*
* SEARCH DIRECTORY FOR MEMBER NAME                                    *
*.....................................................................*
         SPACE
GETDIR   XC    ACTION,ACTION
         MVC   NAME2,NOENTRY                                        FCI
         MVC   COMMENT,NOENTRY                                      FCI
         MVC   SUBSET,NOENTRY                                       FCI
*  SEARCH
         L     R15,=V(PSRCH)                                        FCI
         LA    R1,@PSRCH                                            FCI
         BALR  R14,R15               INVOKE DIR SEARCH FUNCTION     FCI
         SPACE 1                                                    FCI
         CLC   ACTION,=F'0'        RETURN CODE SHOULD BE ZERO
         BNE   ERRDIR
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES
         BE    ENDDIR
         OC    DSTMTS,=CL8'00000000'
         MVC   LASTNAME,DNAME      SAVE NAME JUST SEARCHED
         BAL   R14,CLRCNTRS                                         FCI
         CLC   DSTMTS,STLINE
         BL    ENDREAD1
         SPACE 1                                                    FCI
         LR    R10,R9
GOTOLD1X LA    R11,23
         LA    R7,BUF23
         BAL   R14,CLRSCRN          CLEAR OUT THE SCREEN BUFFER     FCI
         ST    R9,LAST1            SAVE START LINE NUMBER
         EJECT
*.....................................................................*
*  READ THE MEMBER INTO THE BUFFER                                    *
*.....................................................................*
         SPACE
READ     EQU   *
         XC    ACTION,ACTION
         MVC   NAME(10),DNAME      MOVE SEARCHED NAME TO READ NAME
         MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES FCI
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS FCI
READAGN  L     R15,=V(PREAD)                                        FCI
         LA    R1,@PREAD                                            FCI
         BALR  R14,R15             GO READ A RECORD VIA PAM         FCI
         SPACE 1                                                    FCI
         CLC   ACTION,=F'0'
         BNE   ERROR               RETURN CODE SHOULD BE ZERO
         CLC   RECORD(2),=C'$*'    TEST FOR END OF DATA
         BE    ENDREAD
         SPACE 1                                                    FCI
*PROCESS A RECORD
         BCT   R10,READAGN         SPACE FORWARD TO START LINE #
         LA    R10,1               PREVENT ABOVE BRANCH NOW
         TR    RECORD,TRTAB        BLANK UNPRINTABLES.JI
MVC23    MVC   0(80,R7),RECORD     PUT RECORD IN OUTPUT BUFFER
         CLI   MVC23+1,71
         BH    NONUM               NOT ENOUGH ROOM TO ADD SEQUENCE NUMS
         CVD   R9,TEMP8
         OI    TEMP8+7,X'0F'
         UNPK  72(8,R7),TEMP8      ADD SEQUENCE NUMBER TO RECORD
NONUM    LA    R7,80(R7)           POINT TO NEXT SLOT IN BUFFER
         ST    R9,LAST2            SAVE LAST READ LINE NUMBER
         LA    R9,1(R9)            NEXT LINE #
         BCT   R11,READAGN         GO GET NEXT LINE
         SPACE 2                                                    FCI
         EJECT
*.....................................................................*
* TPUT THE SCREEN AND GO GET NEXT PARM                                *
*.....................................................................*
         SPACE
TPUTR0   LM    R8,R9,TPUTR         GET PARAMETERS FOR TPUT
         SPACE 1                                                    FCI
TPUTR1   TPUT  (R8),(R9),R
         SPACE 1                                                    FCI
         B     NEXTPARM
*
ENDREAD  C     R7,=A(BUF23)
         BH    TPUTR0
ENDREAD1 EQU   *
         LA    R15,L'END2
         LA    R3,STLINEX
         LA    R14,END2
         XC    LASTNAME,LASTNAME    CLEAR OUT LASTNAME SO RESTART   FCI
*                                   AT BEGINNING OF MEMBER.         FCI
         B     ERRDISP
*
ERR9SET  EQU   *
         LA    R14,ERR9
         LA    R15,L'ERR9
         B     ERRDISP
ERROR    BAL   R10,CLOSE
         LA    R14,ERR1
         LA    R15,L'ERR1
         B     ERRDISPX
ERROPEN  BAL   R10,CLOSE
         LA    R14,ERR5
         LA    R15,L'ERR5
         LA    R3,DDNAME
         B     ERRDISP
ERRDIR   BAL   R10,CLOSE
         LA    R15,L'ERR6
         LA    R14,ERR6
         B     ERRDISPX
ENDDIR   EQU   *
         BAL   R10,CLOSE
         LA    R14,END1
         LA    R15,L'END1
         SPACE
ERRDISPX LA    R3,NAMER
         SPACE
         EJECT
*.....................................................................*
* DISPLAY ERROR MESSAGE AND THE HELP GOODIES                          *
*.....................................................................*
         SPACE
ERRDISP  EQU   *                   OUTPUT ERROR MESSAGE
         MVI   BUF23,C' '
         MVC   BUF23+1(239),BUF23
         MVC   BUF23+240(240),MSG123
         MVC   BUF23+480(240),MSG456
         MVC   BUF23+720(240),MSG789
         MVC   BUF23+960(240),MSGABC
         MVC   BUF23+1200(240),MSGDEF                       WJS/27JUN79
         MVC   BUF23+1440(240),BUF23
         MVC   BUF23+1680(160),BUF23
         BCTR  R15,0
*        STC   R15,MVCED+1
MVCED    MVC   BUF23+80(1),0(R14)
         EX    R15,*-6                                      WJS/26JUN79
         MVC   STLINE2,STLINE      DO NOT INCREMENT LINE COUNT
         LA    R15,NAMER-BUF23
         SR    R3,R15
         MVI   1(R3),C'$'
         LM    R8,R9,TPUTR
         TPUT  (R8),(R9),R
         SPACE 2                                                    FCI
NEXTPARM MVI   PARMSTR,C'*'
         MVC   PARMSTR+1(L'PARMSTR-1),PARMSTR   CLEAR AREA
         B     GETPARM             GO GET NEXT PARM FIELD
         SPACE 2                                                    FCI
ERR7SET  LA    R14,ERR7
         LA    R15,L'ERR7
         LA    R3,LWIDE
         B     ERRDISP
         EJECT
*.....................................................................*
* OPEN  PANVALET PROCESSING FOR THIS DDN                              *
*.....................................................................*
         SPACE
OPEN     XC    ACTION,ACTION
         L     R15,=V(POPEN)                                        FCI
         LA    R1,@POPEN                                            FCI
         BALR  R14,R15               INVOKE OPEN PANVALET FILE      FCI
         SPACE 1                                                    FCI
         CLC   ACTION,=F'0'
         BNE   ERROPEN             OPEN ERROR
         MVI   OPENFLAG,1          INDICATE SUCCESSFUL OPEN
         MVC   LASTDD,DDNAME       - AND SAVE DDNAME
         BR    R10                                                  FCI
         EJECT
*.....................................................................*
* CLOSE PANVALET PROCESSING FOR THIS DDN                              *
*.....................................................................*
         SPACE
CLOSE    EQU   *
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)                                       FCI
         LA    R1,@PCLOSE                                           FCI
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE       FCI
**
*  NOTE  UPON RETURN FROM PCLOSE ACTION WILL CONTAIN STANDARD
*        PAN#1 RETURN CODES 0-(SUCCESSFUL),4-(++INCLUDE NOT FOUND)
*        8-(ANY UNSUCCESSFUL ACTION), 16-(HARDWARE ERROR)
**
         XC    LASTNAME,LASTNAME
         MVI   OPENFLAG,0          INDICATE FILE CLOSED
         BR    R10
*
         SPACE 2                                                    FCI
CLRCNTR1 MVC   STLINE2,=C'00000001'                                 FCI
         B     CLRCNT1                                              FCI
CLRCNTRS MVC   STLINE2,=C'00000024'                                 FCI
CLRCNT1  LA    R9,1                RESET TO LINE 1                  FCI
         MVC   STLINE,=C'00000001'                                  FCI
         XC    LAST1,LAST1                                          FCI
         XC    LAST2,LAST2                                          FCI
         BR    R14                                                  FCI
         SPACE 1                                                    FCI
CLRSCRN  MVI   BUF23,C' '
         MVC   BUF23+1(239),BUF23
         MVC   BUF23+240(240),BUF23
         MVC   BUF23+480(240),BUF23
         MVC   BUF23+720(240),BUF23
         MVC   BUF23+960(240),BUF23
         MVC   BUF23+1200(240),BUF23
         MVC   BUF23+1440(240),BUF23
         MVC   BUF23+1680(160),BUF23
         BR    R14                                                  FCI
         EJECT
*.....................................................................*
* DISPLAY DIRECTORY                                                   *
*.....................................................................*
         SPACE
LISTDIR  TM    OPENFLAG,1          LIBRARY OPEN ALREADY ?           FCI
         BNO   LD#OPEN             NO,  SO OPEN                     FCI
         CLC   LASTDD,DDNAME       YES, SO CHECK FOR CHANGE IN DDNAMFCI
         BNE   LD#CLOP             CLOSE, AND OPEN TO NEW FILE      FCI
         CLC   LASTNAME,NAME1      CHECK FOR CHANGE IN START OF LISTFCI
         BE    LD#INIT             GO SEARCH DIRECTORY              FCI
         MVC   LASTNAME,NAME1      SAVE NAME TO START SEARCH        FCI
         B     LD#INIT                                              FCI
LD#CLOP  BAL   R10,CLOSE                                            FCI
LD#OPEN  BAL   R10,OPEN                                             FCI
         SPACE 1                                                    FCI
LD#INIT LA     R7,BUF23+80        LINE TO START WITH                FCI
         LA    R10,11             SAY 11 LOOPS TO DO                FCI
         BAL   R14,CLRSCRN        CLEAR OUT THE SCREEN              FCI
         MVC   BUF23(80),HEAD1    MOVE IN THE HEADING               FCI
         BAL   R14,CLRCNTR1                                         FCI
LD#LOOP  XC    ACTION,ACTION                                        FCI
         MVC   NAME2(10),=CL10'9999999999'   SET TO HIGH WATER MARK FCI
         MVC   COMMENT,BLANKS                WE WANT COMMENTS       FCI
         MVC   SUBSET,NOENTRY                BUT NO SUBSET JUNK     FCI
*  SEARCH
         L     R15,=V(PSRCH)                                        FCI
         LA    R1,@PSRCH                                            FCI
         BALR  R14,R15               INVOKE DIR SEARCH FUNCTION     FCI
         SPACE 1                                                    FCI
         CLC   ACTION,=F'0'        RETURN CODE SHOULD BE ZERO       FCI
         BNE   ERRDIR                                               FCI
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES     FCI
         BE    LD#FIN              THATS ALL..EXIT                  FCI
         TR    DIRENTRY,TRTAB      BLANK UNPRINTABLES.JI            FCI
         BAL   R14,MVCDIR          MOVE DIR FIELDS INTO AREA        FCI
         BAL   R14,NOLDZERO        BLANK OUT LEADING ZEROS          FCI
         MVC   80(80,R7),BLANKS    BLANK OUT TRASH                  FCI
         MVC   100(52,R7),COMMENT  PUT COMMENT AFTER IT             FCI
         LA    R7,160(R7)          BUMP POINTERS                    FCI
         ST    R9,LAST2            SAVE LAST READ LINE NUMBER       FCI
         LA    R9,1(R9)            NEXT LINE #                      FCI
         BCT   R10,LD#LOOP         GO GET NEXT LINE                 FCI
         B     TPUTR0              GO DISPLAY SCREEN                FCI
LD#FIN   XC    LASTNAME,LASTNAME   SET TO REPEAT FROM ORIG START    FCI
         B     TPUTR0                                               FCI
         SPACE
         EJECT
*.....................................................................*
*  MOVE/FORMAT DIRECTORY FIELDS FOR / DIR DISPLAY                     *
*.....................................................................*
         SPACE
         USING MDIR,R7            USING FOR ADDRESSABILITY          FCI
MVCDIR   MVC   MDNAME,DNAME       MOVE IN NAME                      FCI
         MVC   MDLEVEL,DLEVEL     MOVE IN LEVEL                     FCI
         CLC   DUSER,=C'0000'     ANY USER LEVEL?                   FCI
         BE    M#SKP0                                               FCI
         MVC   MDUSER,DUSER       MOVE IN USER LEVEL                FCI
M#SKP0   MVC   MDTYPE,DTYPE       MOVE IN TYPE                      FCI
         CLI   DSECURE,C'0'       NO SECURITY?                      FCI
         BE    M#SKP1                                               FCI
         MVC   MDSECURE,DSECURE   MOVE IN SECURITY LEVEL            FCI
M#SKP1   MVC   MDSTATUS,DSTATUS   MOVE IN STATUS                    FCI
         MVC   MDMAINT,DDATEM     MOVE IN MAINT DATE                FCI
         MVC   MDACCES,DDATEA     MOVE IN ACCESS DATE               FCI
         MVC   MDBLOCKS,DBLOCKS   MOVE IN BLOCK COUNT               FCI
         MVC   MDSTMTS,DSTMTS     MOVE IN STATEMENT COUNT           FCI
         MVC   MDLSTPRD,DLASTACT  MOVE IN PROD FLAG                 FCI
         LA    R15,ACTTABLE       LOAD ADDRESS OF ACTION TABLE      FCI
         MVC   MDLSTACT(3),DLASTACT+1 MOVE IN ABBREVIATION          FCI
ACTLOOP  CLI   0(R15),X'FF'       END OF TABLE                      FCI
         BER   R14                YES..JUST USE ABBREV              FCI
         CLC   0(3,R15),DLASTACT+1  IS OUR EXPANTION THE SAME?      FCI
         BE    MVCDIR1            YES..USE IT                       FCI
         LA    R15,8(R15)         BUMP AND                          FCI
         B     ACTLOOP             GRIND                            FCI
MVCDIR1  MVC   MDLSTACT,0(R15)    MOVE IN EXPANTION                 FCI
         BR    R14                                                  FCI
         EJECT
*.....................................................................*
*  REMOVE LEADING ZEROS IN ALL FIELDS IN DIR LINE                     *
*.....................................................................*
         SPACE
NOLDZERO LA    R15,SCANSTRT             LOAD START OF LD ZERO SCAN  FCI
         LA    R1,SCANLEN               LOAD NUMB OF CHARS TO SCAN  FCI
         NI    MODESW,NOSKIP            TURN OFF SKIP SWITCH        FCI
         SPACE
NOLDZ1   CLI   0(R15),C'0'              IS IT A ZERO?               FCI
         BL    RSTSW                    CHAR < C'0' -> RESET SKIP   FCI
         BH    SETSW                    CHAR > C'0' -> SET SKIP SW  FCI
         TM    MODESW,SKIP              IS SKIP SWITCH SET          FCI
         BO    BUMPZLOP                 YES -> THEN SKIP THE POKE   FCI
         CLI   1(R15),C'0'              HOW ABOUT THE NEXT CHAR?    FCI
         BL    BUMPZLOP                  DONT REMOVE LAST ZERO....  FCI
         MVI   0(R15),C' '              POKE A BLANK INSTEAD...     FCI
         B     BUMPZLOP                 GO INCREMENT                FCI
RSTSW    NI    MODESW,NOSKIP            RESET THE SWITCH            FCI
         B     BUMPZLOP                 GO INCREMENT                FCI
SETSW    OI    MODESW,SKIP              SET THE SWITCH              FCI
BUMPZLOP LA    R15,1(R15)               BUMP POINTER                FCI
         BCT   R1,NOLDZ1                LOOP UNTIL MSG SCANNED...   FCI
         BR    R14                                                  FCI
         DROP  R7                                                   FCI
         SPACE 2
*.....................................................................*
*  HERE WE EXIT STAGE LEFT AFTER CLEANUP                              *
*.....................................................................*
         SPACE
RETURN   EQU   *
         BAL   R10,CLOSE
         LM    R8,R9,TPUTRND
         TPUT  (R8),(R9),R
         SPACE 1                                                    FCI
         L     15,ACTION           LOAD RETURN CODE
         L     R13,4(R13)          GET ORIG SAVE AREA               FCI
         RETURN (14,12),RC=(15)    RETURN NORMALLY                  FCI
         SPACE 1                                                    FCI
ACTION   DC    F'0'         RETURN CODE FROM PAN, SHOULD BE SET TO ZERO
         EJECT
*.....................................................................*
*  LIST FORM OF MACROS                                                *
*.....................................................................*
         SPACE
@POPEN   CALL  ,(ACTION,DDNAME,BACKUP),VL,MF=L                      FCI
         SPACE
@PSRCH   CALL ,(ACTION,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL,MF=L FCI
         SPACE
@PREAD   CALL  ,(ACTION,RECORD,NAME,INCLUDES,COMMENT),VL,MF=L       FCI
         SPACE
@PCLOSE  CALL  ,(ACTION),VL,MF=L                                    FCI
         EJECT
*.....................................................................*
*  CONSTRUCTS FOR PANVALET                                            *
*.....................................................................*
         SPACE
*  POPEN PARAMETERS
BACKUP   DC    CL8'NO-ENTRY'     MUST BE CL8'BACKUP' IF PROTECTION FILE
         SPACE
*  PSRCH PARAMETERS
DIRENTRY DS    0CL80               DIR ENTRY RETURNED IN 0-UP FORMAT
DNAME    DS    CL10                NAME LEFT JUSTIFIED
DLEVEL   DS    CL3                 LEVEL NUMBER
DUSER    DS    CL4                 USER CODE
DSECURE  DS    CL1                 SECURITY CODE
DTYPE    DS    CL5                 LANGUAGE TYPE
DSTATUS  DS    0CL3                3 CHAR STATUS
DPRODT   DS    CL1                 P-PROD  T-TEST
DEORDD   DS    CL1                 E-ENABLE  D-DISABLE
DAORDI   DS    CL1                 A-ACTIVE  I-INACTIVE
DDATEM   DS    CL8                 DATE OF LAST MAINTENANCE MM/DD/YY
DDATEA   DS    CL8                 DATE OF LAST ACCESS  MM/DD/YY
DBLOCKS  DS    CL5                 NO. OF BLOCKS
DSTMTS   DS    CL8                 NO. OF STATEMENTS
DLASTACT DS    CL4                 LAST ACTION, 1ST POS. IS * IF PROD
DBYTES   DS    CL2                 NO. BYTES PER STATMENT
DSUBSET  DS    CL4                 NO. OF SUBSETS(SUPERSETS ONLY)
DNAME2   DS    CL10                NAME RIGHT JUSTIFIED
         DS    CL1                 NOT USED
         DS    CL1                 N=NOFORMAT, T=TSO, ELSE BLANK
DINC     DS    CL1                 I=INCLUDE BEING EXPND (EXIT ONLY)
DVERSION DS    XL2                 VER. #(FOR PROTECTION FILES ONLY)
         EJECT
NAME1    DC    CL22'PAN'           SEARCH NAME1
NAME2    DC    CL11'NO-ENTRY'      SEARCH NAME2
COMMENT  DC    CL52'NO-ENTRY'      NO COMMENTS WANTED, ELSE='COMMENT'
**
* NOTE  COMMENTS WILL BE PROCEDED AND ENDED WITH AN *
**
SUBSET   DC    CL27'NO-ENTRY'      NO SUBSETS WANTED, ELSE='SUBSET'
**
*  NOTE  IF SUBSETS ARE REQUESTED THE FOLLOWING WILL BE A DEFINITION OF
*        THE SUBSET DIRECTORY ENTRY
**
SUBENTRY DS    0CL27
SUBNAME  DS    CL11                FORMAT .XXXXXXXXXX X'S = SUBSET NME
         DS    CL1                 BLANK
SUBDATE  DS    CL8                 DATE ATTACHED IN FORM MM/DD/YY
         DS    CL2                 BLANKS
SUBSTMT  DS    CL5                 # OF STMTS IN SUBSET
         SPACE 2                                                    FCI
*  PREAD PARAMETERS
RECORD   DS    CL80                STATMENT WILL BE RETURNED HERE
INCLUDES DS    CL8
**
* SPECIAL RETURN CODES FROM EACH CALL TO PAM WILL BE RETNED IN ACTION
*   2    NAME NOT ALPHANUMERIC (SAME AS PV002)
*   4    ++INCLUDE NOT FOUND (PREAD ONLY)
*   7    PARAMETER TO LONG (SAME AS PV007)
*   23   NAME NOT FOUND (SAME AS PV023)
*   245  PARAMETER LIST ERROR IN CALL TO PAM
**
         EJECT
*.....................................................................*
*  ERROR MESSAGES TO POKE AT THE UNSUSPECTING USER                    *
*.....................................................................*
         SPACE
ERR1     DC    C'INPUT ERROR IN PAN LIBRARY'
ERR4     DC    C'PARAMETER TOO LONG'
ERR5     DC    C'UNABLE TO ACCESS PAN LIBRARY'
ERR6     DC    C'MODULE NOT FOUND IN PAN LIBRARY'
ERR7     DC    C'ILLEGAL LINE LENGTH GIVEN'
ERR8     DC    C'DDNAME MISSPELLED OR NOT ALLOCATED'
ERR9     DC    C'PARAMETER MAY NOT BEGIN WITH A BLANK'
         SPACE 2                                                    FCI
* HERE FOLLOWS THE MAGIC CLEAR SCREEN UPON EXIT (WITH SPF IN MIND)  FCI
*                                                                   FCI
END1PRE  DC    X'C7115D7E1140401DC8'                KMT     WJS/26JUN79
END1SUFF DC    X'3C404000'
END1END  EQU   *
         SPACE 2                                                    FCI
END1     DC    C'END OF PROCESSING'                                 FCI
END2     DC C'STARTING LINE # OUT OF RANGE (HIT ENTER TO RESTART)'  FCI
         EJECT ,                                                    FCI
*                                                                   FCI
* THIS IS THE HELP/WHAT ME WORRY PANEL                              FCI
*                                                                   FCI
MSG123   DC    CL40'FAIRCHILD             ----------- P A N '       FCI
         DC    CL40'V I E W ----------             FAIRCHILD'       FCI
         DC    CL40'PANVALET/3270         F U L L - S C R E '       FCI
         DC    CL40'E N  D I S P L A Y         3270/PANVALET'       FCI
         DC    CL40'----------------------------------------'
         DC    CL40'----------------------------------------'
MSG456   DC    CL40'* MODIFY ONLY THE FOLLOWING FOUR PARAMET'
         DC    CL40'ERS LEAVING COMMAS/ASTERISKS UNCHANGED *'
         DC    CL40'       PANNAMEXX - NAME OF MODULE IN THE'
         DC    CL40' PANVALET LIBRARY TO BE DISPLAYED       '
         DC    CL40'       XFERPAN   - DDNAME/FILE NAME OF T'       FCI
         DC    CL40'HE PANVALET LIBRARY                     '       FCI
MSG789   DC    CL40'                   (XFERPAN) FOR INTRO.X'       FCI
         DC    CL40'FERPAN                                  '       FCI
         DC    CL40'                   (PRODPAN) FOR FCI1.PA'       FCI
         DC    CL40'NSORC                                   '       FCI
         DC    CL40'       00000001  - RELATIVE LINE NUMBER '
         DC    CL40'AT WHICH THE DISPLAY IS TO START        '
MSGABC   DC    CL40'       80        - NUMBER OF COLUMNS OF '
         DC    CL40'STORED DATA TO BE DISPLAYED             '
         DC    CL40'       (AUTOMATIC DISPLAY SEQUENCING IF '
         DC    CL40'LESS THAN 73 COLUMNS REQUESTED)         '
         DC    CL40'----------------------------------------'
         DC    CL40'----------------------------------------'
MSGDEF   DC    CL40'* TO TERMINATE PANVIEW - ENTER AN ASTERI'
         DC    CL40'SK IN COLUMN 1 OF THE PANNAMEXXX FIELD *'
         DC    CL40'                                        '
         DC    CL40'                                        '
         DC    CL40'* TO LIST NAMES IN LIBRARY USE /SSSS IN '       FCI
         DC    CL40'PANNAMEXX FIELD (SSSS IS STARTING NAME)*'       FCI
         EJECT
*.....................................................................*
*  MISC VARIABLES                                                     *
*.....................................................................*
         SPACE
PARMSTR  DC    0CL78' ',78C'*'
NOENTRY  DC    CL92'NO-ENTRY'            NO-ENTRY FOR PANVALET      FCI
BLANKS   EQU   NOENTRY+8,80              BLANKS FOR US              FCI
HEAD1    DC    CL80'...NAME... LVL TYPE  USER   -S- MAINTNED ACCESSED  X
                 STMTS BLOCKS LAST-ACTION'                          FCI
TIOTADDR DS    F
TEMP8    DS    D
         SPACE 2                                                    FCI
TPUTRND  DC    AL1(3),AL3(END1PRE),A(END1END-END1PRE)
TPUTR    DC    AL1(3),AL3(BUFPRE),A(BEND-BUFPRE)
         SPACE 2                                                    FCI
*THIS IS THE BIG SCREEN DEFINITION, CONTROL CHARS AND ALL           FCI
BUFPRE   DC    X'C7115D7F11C14F1DE8'           KMT/FCI      WJS/26JUN79
BUF23    DC    23CL80' '
BSUFF    DC    X'1140401DC113'
         SPACE 1                                                    FCI
NAME     DC    0CL22' '
NAMER    DC    CL22'PANNAMEXXX'
         DC    C','
DDNAME   DC    CL8'XFERPAN'        DDNAME OF FILE TO BE OPENED      FCI
         DC    C','
STLINEX  DC    CL8'00000001'
         DC    C','
LWIDE    DC    CL2'80'
         DC    CL24'***** FIRST LINE SHOWN #'
STLINE   DC    CL8'00000001'
         DC    3C'*',X'11',C' /'   <==NOTE TRICKY ADDRESSING PUTS THIS
STLINE2  DC    CL8'00000001'      FIELD ON THE SCREEN AS STLINEX...
BEND     EQU   *
BSUFL    EQU   BEND-BSUFF
*END OF SCREEN DEFINITION                                           FCI
         EJECT
*.....................................................................*
* MORE VARIABLES                                                      *
*.....................................................................*
         SPACE
LASTDD   DC    CL8' '              DDNAME OF LIBRARY LAST OPENED
LASTNAME DC    CL10' '
LAST1    DC    F'0'                POINTER TO LAST START LINE
LAST2    DC    F'0'                POINTER TO LAST END LINE
OPENFLAG DC    X'00'             LIBRARY STATUS FLAG =X'00' IF NOT OPEN
MODESW   DC    X'00'             FAIRCHILD MODE SWITCHES            FCI
DIRMODE  EQU   X'80'               DIR LIST MODE                    FCI
SKIP     EQU   X'01'             LEADING ZERO SUPPRESS ROUTINE FLAGSFCI
NOSKIP   EQU   255-SKIP                                             FCI
         SPACE 3
*.....................................................................*
*    LITERAL POOL                                                     *
*.....................................................................*
         SPACE
         LTORG
         EJECT
*.....................................................................*
*    TRANSLATE TABLES                                                 *
*.....................................................................*
         SPACE
TRTAB    DC    CL64' '
         DC    CL10' ',C'Ö.<(+×&&',CL9' ',C'!$*);^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' abcdefghi      '    NOTE: LOWER CASE A-Z
         DC    CL16' jklmnopqr      '          ARE CONSIDERED OK
         DC    CL16'  stuvwxyz      '          FOR DISPLAY....  KT
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         SPACE
CAPSONLY DC    CL64' '
         DC    CL10' ',C'Ö.<(+×&&',CL9' ',C'!$*);^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         SPACE 2                                                    FCI
*.....................................................................*
*  TABLE TO EXPAND ABBREVIATIONS OF LAST ACTION ON MEMBER             *
*.....................................................................*
         SPACE
ACTTABLE DC    CL8'ADDED'                                           FCI
         DC    CL8'UPDATED'                                         FCI
         DC    CL8'COMMENT'                                         FCI
         DC    CL8'RENAMED'                                         FCI
         DC    CL8'LEVEL'                                           FCI
         DC    CL8'RESTORED'                                        FCI
         DC    CL8'REPLACED'                                        FCI
         DC    CL8'STATUS'                                          FCI
         DC    CL8'COPIED'                                          FCI
         DC    CL8'USER'                                            FCI
         DC    CL8'ALLOCATD'                                        FCI
         DC    CL8'ATTACHED'                                        FCI
         DC    CL8'DETACHED'                                        FCI
         DC    XL8'FFFFFFFFFFFFFFFF'   END OF TABLE                 FCI
         EJECT
*.....................................................................*
*  OVERLAY MAPPING OF DIRECTORY LINE FORMAT ON SCREEN                 *
*.....................................................................*
         SPACE
MDIR     DSECT           MAP FOR NICE FORMATTED DIR ENTRY           FCI
MDNAME   DS    CL10,CL1                                             FCI
SCANSTRT EQU   *                                                    FCI
MDLEVEL  DS    CL3,CL1                                              FCI
MDTYPE   DS    CL5,CL1                                              FCI
MDUSER   DS    CL4,CL1                                              FCI
MDSECURE DS    CL1,CL1                                              FCI
MDSTATUS DS    CL3,CL1                                              FCI
MDMAINT  DS    CL8,CL1                                              FCI
MDACCES  DS    CL8,CL1                                              FCI
MDSTMTS  DS    CL8,CL2                                              FCI
MDBLOCKS DS    CL5,CL1                                              FCI
SCANLEN  EQU   *-SCANSTRT                                           FCI
MDLSTPRD DS    CL1                                                  FCI
MDLSTACT DS    CL8                                                  FCI
         END   TSO048
