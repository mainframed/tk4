*          DATA SET PSNCOMNT   AT LEVEL 002 AS OF 10/30/80
         TITLE 'PSNCOMNT - PROCESS A LINE OF COMMENTS'
PSNCOMNT PSNMSTRT  SASIZ=WKLENG
         EJECT
*                    TO PROCESS A LINE OF COMMENTS - PLACE
*                    THE COMMENT AND A DESCRIPTOR BLOCK IN THE
*                    COMMENT GOTTEN-MAIN, AND CHAIN IT TO THE
*                    HEAD OF COMMENT, OR THE PREVIOUS ONE.
*                    THE EXEC PROCESSOR PICKS UP THIS FULL
*                    CHAIN AND REPOINTS IT OFF THE EXEC BLOCK
*                    AS THE HEADER BOX, LATER.
*
         L     R9,PSWACOM         ADDRESS THE COMMENT GOTTEN-MAIN
         LM    R7,R8,PSWRSTRT
         SR    R8,R7              LENGTH OF THE COMMENT
*
*                   BUILD UP THE COMMENT CONTROL
         USING COMMENT,R9
         XC    COMMENT(CMTLNG+2),COMMENT CLEAR THE POINTER AREA
         L     R6,PSWLASTC        PICK UP POINTER TO LAST ONE
         LTR   R6,R6              WAS THERE ONE BEFORE
         BNZ   *+8                BIF NOT FIRST
         LA    R6,PSWTHISC        START OF CHAIN IN PSW
         ST    R9,0(,R6)          CONTINUE OR START THE CHAIN
         ST    R9,PSWLASTC        AND FOR THE NEXT ONE
         L     R1,PSWRNEXT        ADDRESS START OF THE CARD
         CLI   70(R1),C'*'        IS IT ALL THE WAY ACROSS
         BNE   *+8                NO - DO NOT STAR IT
         OI    CMTIND1,CMTI1S71   STAR IN COLUMN 71....
*                    IF ALL **** OR BLANKS, A SPECIAL ROUTINE
         BAL   R14,CHKSPEC        IF RETURN, NOT THE SPECIAL
         LA    R1,CMTLNG+2(R8,R9) POINT TO END OF IT
         SRL   R1,2               ROUNDED TO FULLWORD
         SLL   R1,2               OF COURSE
         C     R1,PSWACOM+4       MATCHED AGAINST END OF AREA
         BNL   NOROMCOM           NO ROOM - MSG AND GO
         ST    R1,PSWACOM         NEW START OF OPEN AREA
         STH   R8,CMTLENGC        SHOW LENGTH OF THIS COMMENT
         BCTR  R8,0               PREPARE TO MOVE THE COMMENT
         EX    R8,MOVEIT
         L     R1,PSWRSTRT        PICK UP START OF THE CARD
         LA    R1,0(,R1)          CLENSE THE ADDRESS, JUST IN CASE
         S     R1,PSWRNEXT        SUBTRACT START OF THE CARD
         STH   R1,CMTCOL          STORE INTO THE COLUMN FIELD
         SR    R15,R15
         B     RETURN
MOVEIT   MVC   CMTTEXT(0),0(R7)   MOVE COMMENT TO HOLDING AREA
*
NOROMCOM EQU   *                  NO ROOM FOR THE COMMENTS
         PSNMERR  ERRTBLFL,(PSWACOM+16,8)
         LA    R15,8              CAN GO LITTLE FURTHER
         B     RETURN
         EJECT
CHKSPEC  PSNMBLOK  CKS,RENT=YES
         EX    R8,CKSBLANK        IS IT ALL BLANK
         BE    CKSISBLK           YES - SPECIAL MARKING IN BLOCK
*                   CHECK FOR LINE OF ASTERISKS
         CLI   0(R7),C'*'
         BNE   CKSEXIT            NOTHING DOING - RETURN
         LR    R1,R8
         SH    R1,PSWHALF2        DOWN FOR +1 AND EX
         EX    R1,CKSMATCH        ARE THEY ALL STARS
         BNE   CKSEXIT
         OI    CMTIND1,CMTI1AST   THEY ARE ALL STARS
*
CKSBLD   EQU   *                  BUILD THE ABBREVIATED BLOCK
         LA    R1,CMTLNG+3        COMPUTE NEW SHORTER LENGTH
         SRL   R1,2
         SLL   R1,2
         LA    R1,0(R9,R1)        POINT TO SHORTER LENGTH OF IT
         ST    R1,PSWACOM         AND MAKE THIS THE START OF NEW AREA
         SR    R15,R15
         B     RETURN             AND DIRECTLY TO END
CKSMATCH CLC   1(0,R7),0(R7)      RIPPLE THE COMPARE DOWN
*
CKSISBLK EQU   *
         OI    CMTIND1,CMTI1BLK   SHOW IT AS BLANK COMMENT LINE
         B     CKSBLD
CKSBLANK CLC   0(0,R7),PSWBLANK   MATCHING ALL BLANKS
         LTORG
         EJECT
         PSNMCMT
*
*
WORKAREA DSECT
         DS    18F
         PSNMBLOK  RENT=REGS
WKLENG   EQU   *-WORKAREA
         EJECT
         PSNMWORK
         END
