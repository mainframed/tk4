*          DATA SET PSNMSTRT   AT LEVEL 002 AS OF 10/30/80
         MACRO
&TAG     PSNMSTRT &BASE=R12,&ZAP=12,&RC=,&T=,&SASIZ=
         LCLA  &SX                ABBREVIATION OF SYSNDX
&SX      SETA  &SYSNDX            TRUNCATE THE ZEROES OFF TAGS
&TAG     CSECT
         B     STR&SX-&TAG.(,R15)
         USING &TAG,&BASE(1)
         USING PSNWORK,R11
         DC    AL1(ZAP&SX-*-1),CL8'&TAG'
         PSNMI002
ZAP&SX   DC    &ZAP.S(*)          ZAP AREA
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RETURN   ST    R13,PSWSAVE        FREE UP THIS SAVEAREA AGAIN
         AIF   (T'&RC EQ 'O').RCSET
         LA    R15,&RC            INSERT PRESET RETURN CODE
.RCSET   ANOP
         L     R13,4(,R13)        PLUCK OUT BACKWARD POINTER
         AIF   (T'&T EQ 'O').SKDELET
         LR    R3,R0              SAVE POSSIBLE REASON CODE
         LR    R2,R15             SAVE THE RETURN CODE FROM DREADED SVC
         DELETE  EPLOC=NLM&SX     WIPE OUT THE WORK AREA
         LR    R15,R2             RECAPTURE RETURN CODE
         LR    R0,R3              RECAPTURE VALUE
.SKDELET ANOP
         L     R14,12(,R13)
         LM    R1,R12,24(R13)
         MVI   12(R13),X'FF'      EXIT INDICATOR
         BR    R14
         AIF   (T'&T EQ 'O').NOMOD
NLM&SX   DC    CL8'PSNWORK'             NAME OF THE WORK AREA
.NOMOD   ANOP
.*                  TO ACCOMMODATE MULTIPLE BASE REGISTERS
         AIF   (N'&BASE EQ 1).BAS1X NO MULTIPLE AFTER ALL
BAS2&SX  DC    A(&TAG+4096)
         AIF   (N'&BASE EQ 2).BAS1X
BAS3&SX  DC    A(&TAG+8192)
.BAS1X   ANOP
.*
STR&SX   STM   R14,R12,12(R13)
         LR    &BASE(1),R15
         AIF   (N'&BASE EQ 1).BAS2X
         L     &BASE(2),BAS2&SX
         USING &TAG+4096,&BASE(2)
         AIF   (N'&BASE EQ 2).BAS2X
         L     &BASE(3),BAS3&SX
         USING &TAG+8192,&BASE(3)
.BAS2X   ANOP
         AIF   (T'&T EQ 'O').GSTDS
         LOAD  EPLOC=NLM&SX       LOAD THE WORKAREA
         LR    R11,R0             ADDRESS THE WORKAREA
.*                   CODE FOR STANDARD SUBROUTINE ENTRY
.GSTDS   ANOP
         L     R15,PSWSAVE        POINTER TO START OF OPEN AREA
         ST    R13,4(,R15)        CROSS-CHAIN THE SAVE-AREAS
         ST    R15,8(,R13)        .
         LR    R13,R15            HAVE THE NEW SAVE-AREA
         LA    R15,&SASIZ+31      REQUESTED WORKAREA SIZE
         SRL   R15,5              ALIGN ON CORE-DUMP BOUNDARY
         SLL   R15,5              .
         LA    R15,0(R15,R13)     POINT TO END OF WORKAREA
         C     R15,PSWSAVE+4      STILL INSIDE WORKAREA
         BNH   CLR&SX+6           SO FAR, SO GOOD
         EX    R0,*               OOPS
CLR&SX   XC    72(0,R13),72(R13)
         ST    R15,PSWSAVE        SHOW NEW END OF SAVEAREA
         LA    R15,&SASIZ-73
         LA    R14,255            OTHER ALTERNATIVE CLEARING SIZE
         CR    R15,R14
         BL    *+6                SELECT THE LESSER OF TWO IEWAALSS
         LR    R15,R14
         EX    R15,CLR&SX
         USING WORKAREA,R13
.*
.EXIT    ANOP
         SPACE 2
         MEND
