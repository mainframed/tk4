*          DATA SET MAPDISK7   AT LEVEL 002 AS OF 08/03/81
*          DATA SET MAPDISK7   AT LEVEL 001 AS OF 05/24/78
FASTMAP7 TITLE 'MSSALLOC SUBROUTINE - DYNAMIC ALLOCATION OF MASS STORAGX
               E VOLUMES FROM LIST IN DATASET  V1M0'
*.....................................................................*
*.                                                                   .*
*.  SUBROUTINE - MSSALLOC                                            .*
*.                                                                   .*
*.  PURPOSE - TO RETURN A MSS VOLUME TO MAPDISK TO BE MAPPED         .*
*.                                                                   .*
*.  ON ENTRY: NORMAL OS CONVENTIONS (R15)=EPA, (R13)=SAP, (R1)=PARM  .*
*.      NOTE:PARM POINTER IS NOT USED                                .*
*.                                                                   .*
*.  ON RETURN:  (R15) =  0 -->  NO (MORE) MSS VOLUMES TO DO          .*
*.                                                                   .*
*.              (R15) >  0 -->  (R15) = ADDR TIOT ENTRY OF VOLUME    .*
*.                              TO BE MAPPED                         .*
*.                                                                   .*
*.                              (R1) =  ADDR OF 8 CHAR MSVGP NAME    .*
*.                                                                   .*
*.  INPUT - MSSVOLS DDNAME                                           .*
*.                                                                   .*
*.       THIS DATASET F(B) 80 CONTAINS :                             .*
*.                                                                   .*
*.     COL 1-6   VOLSER TO MONITOR      (REQUIRED)                   .*
*.     COL 7-14  UNIT TYPE  (REQUIRED)  (E.G. '3330V', '3330-1' ETC) .*
*.     COL 15-22 MSVGP (OPTIONAL)                                    .*
*.                                                                   .*
*.  ERROR OUTPUT-VIA WTO MESSAGES FOR SVC99 ERRORS                   .*
*.                                                                   .*
*.  K TRUE   V1M0   14 FEB 78                                        .*
*.  B WILLIAMS V1M1 08 AUG 81  S99RB FLAG 1 - NO OPER MOUNT XL2'2000'.*
*.                                                                   .*
*.....................................................................*
         SPACE 2
FASTMAP7 START 0
         ENTRY MSSALLOC
*
R0       EQU   0
R1       EQU   1             IF VOLUME FOUND=PTR TO MSVGP NAME
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12            BASE REGISTER
R13      EQU   13            SAVE AREA
R14      EQU   14
R15      EQU   15            ON RETURN=0 OR PTR TO TIOT ENTRY
*
MSSALLOC SAVE  (14,12),,*
         LR    R12,R15                 INIT BASE REG
         USING MSSALLOC,R12
         LA    R15,SAVEAREA            INIT SAVE AREA
         ST    R13,4(R15)              BACKWARD CHAIN
         ST    R15,8(R13)              FORWARD CHAIN
         LR    R13,R15                 POINT TO CURRENT SAVE AREA
         SPACE
         XC    TIOTADDR,TIOTADDR       ASSUME NO MORE ENTRIES
         MVC   MMSVGP,=CL8' '           AND NO MSVGP
*
         TM    SWITCH,FIRSTIME         IS THIS OUR FIRST CALL?
         BNO   GETREC                  NO-> GO GET ANOTHER RECORD
*
         OPEN  MF=(E,OPEN)             DO THE OPEN
         NI    SWITCH,255-FIRSTIME     TURN OFF THE SWITCH
         TM    MSSVOLS+48,X'10'        DID THE FILE OPEN?
         BO    GETREC                  YES-> GO GET A RECORD
         WTO   MF=(E,EMSG1)            NO-> GRIPE AND LEAVE
         B     RETURN
*
         EJECT
*.....................................................................*
*.       READ MSSVOLS AND PERFORM DYNAMIC ALLOCATION OF VOLUME       .*
*.....................................................................*
         SPACE 2
GETREC   GET   MSSVOLS,MREC            GET A RECORD
         SPACE 2
*
         MVC   MSSVOL,MVOLSER           MOVE IN VOLSER TO ALLOC UNIT
         MVC   UNIT,MUNIT               MOVE IN UNIT PARAMETER
         LA    R1,8               SET TO SCAN 8 CHARACTERS
         LA    R2,UNIT            START AT UNIT
BLNKLOOP CLI   0(R2),C' '         IS IT A BLANK
         BE    BLNKFND            YES-> GO USE COUNT
         LA    R2,1(R2)
         BCT   R1,BLNKLOOP        AND LOOP BACK
BLNKFND  LA    R1,UNIT
         SR    R2,R1              (R2) = LENGTH OF UNIT
         STCM  R2,B'0011',UNITLEN SAVE IT FOR ALLOCATION
*
         XC    S99ERC(4),S99ERC   CLEAR OUT ALLOCATE ERC/INFO
         XC    FREERC(4),FREERC   CLEAR OUT FREE     ERC/INFO
         LA    R1,FREEPTR         LOAD ADDR OF SVC99 'FREE'
         DYNALLOC                  AND INVOKE DYNAM ALLOCATION
*
         LA    R1,S99RBPTR        LOAD ADDR OF SVC99 'ALLOCATE'
         DYNALLOC                  GO GET VTOC OF MSSVOL
*
         LTR   R15,R15            ANY PROBLEMS
         BNZ   CANTALLC           YES-> GO REPORT THEM
*                                 NO->  FIND DDCARD IN TIOT
*
         L     R1,16              (R1)= A(CVT)
         L     R1,0(R1)           (R1)= A(NEW/OLD TCB:ASCB)
         L     R1,4(R1)           (R1)= A(MY TCB)
         L     R1,12(R1)          (R1)= A(TIOT)
         LA    R1,24(R1)          SKIP OVER JOBNAMES
*
         SR    R2,R2              CLEAR REGISTER FOR LENGTH USE
LOOPTIOT CLC   =C'ALLOCDD',4(R1)  IS THIS THE ONE?
         BE    FOUNDIT            YES-> GO SAVE ADDRESS
         IC    R2,0(R1)           NO-> GET LENGTH OF ENTRY
         AR    R1,R2                ADD TO BUMP TO NEXT..
         CLI   0(R1),X'00'        IS THAT THE END OF TIOT?
         BNE   LOOPTIOT             NO-> GO TRY AGAIN
ENDTIOT  MVC   M3VOL,MSSVOL       MOVE VOLSER TO MESSAGE
         WTO   MF=(E,EMSG3)
         B     GETREC             GO TRY ANOTHER VOLUME
FOUNDIT  ST    R1,TIOTADDR        SAVE THE TIOT ADDRESS WE FOUND
         B     RETURN              AND RETURN TO THE CALLER
         EJECT
*.....................................................................*
*.       PROCESS EOF ON MSSVOLS                                      .*
*.....................................................................*
         SPACE 3
EOFINPUT DS    0H
         TM    MSSVOLS+48,X'10'     IS THE DCB OPEN?
         BNO   RETURN               NO-> SKIP THE CLOSE
         CLOSE MF=(E,CLOSE)         YES-> DO THE CLOSE
         FREEPOOL MSSVOLS             AND FREE THE POOLS
         B     RETURN               EXIT-> STAGE LEFT
         EJECT
*.....................................................................*
*.       FORMULATE TEXT FOR SVC99 ALLOCATE FAILURE                   .*
*.....................................................................*
         SPACE 3
CANTALLC DS    0H
         MVC   M2VOL,MSSVOL       MOVE VOLSER INTO MESSAGE
         CVD   R15,DBLWORD        CONVERT SVC 99 RETURN CODE
         MVC   M2RC,=X'40202020'   TO NICE PRINTABLE DECIMAL
         ED    M2RC,DBLWORD+6
*
         UNPK  M2ERC(5),S99ERC(3)   CONVERT DYNAM ALLOC ERR CODE
         NC    M2ERC,HEXMASK         TO PRINTABLE HEXADECIMAL
         TR    M2ERC,HEXTAB          AND FIX IT UP PRETTY
         MVI   M2ERC+4,C' '
*
         UNPK  M2INFO(5),S99INFO(3) CONVERT DYNAM ALLOC INFO
         NC    M2INFO,HEXMASK        CODE TO PRNTABLE HEX
         TR    M2INFO,HEXTAB         AND FIX IT UP PRETTY
         MVI   M2INFO+4,C' '
*
         WTO   MF=(E,EMSG2)         WRITE OUT ERROR MESSAGE
         B     GETREC               AND GO GET ANOTHER RECORD
         EJECT
*.....................................................................*
*.       RETURN TO CALLING PROGRAM                                   .*
*.....................................................................*
         SPACE 2
RETURN   LA    R1,MMSVGP               LOAD ADDRESS OF MSVGP
         L     R15,TIOTADDR            LOAD ADDRESS OF TIOT ENTRY
         L     R13,SAVEAREA+4          GET PRIOR SAVE AREA ADDRESS
         L     R14,12(R13)             LOAD RETURN ADDRESS
         LM    R2,R12,28(R13)          RESTORE REGS..LEAVE R1 INTACT
         BR    R14                     AND RETURN
         EJECT
*.....................................................................*
*.       VARIABLES                                                   .*
*.....................................................................*
         SPACE 2
SAVEAREA DC    18F'0'
DBLWORD  DC    D'0'               DOUBLE WORD WORK AREA
TIOTADDR DC    AL4(0)
SWITCH   DC    BL1'10000000'
FIRSTIME EQU   B'10000000'
         EJECT
*.....................................................................*
*.       CONSTANTS                                                   .*
*.....................................................................*
         SPACE 2
HEXTAB   DC    C'0123456789ABCDEF'
HEXMASK  DC    X'0F0F0F0F0F0F0F0F'
         EJECT
*.....................................................................*
*.       MESSAGES                                                    .*
*.....................................................................*
         SPACE 3
EMSG1    WTO   'MAPDISK - MSS OPTION SELECTED..NO MSSVOLS DDCARD',     X
               ROUTCDE=(11),MF=L
         SPACE
EMSG2    WTO   'MAPDISK - DYNAM ALLOC OF VOL=XXXXXX FAILED: R15= XXXX  X
                INFO= XXXX  DARC= XXXX ',                              X
               ROUTCDE=(11),MF=L
         SPACE
         ORG   EMSG2+33
M2VOL    DC    CL6' '
         ORG   EMSG2+53
M2RC     DC    CL4' '
         ORG   EMSG2+65
M2INFO   DC    CL4' '
         ORG   EMSG2+77
M2ERC    DC    CL4' '
         ORG
         SPACE
EMSG3    WTO   'MAPDISK - VOL=XXXXXX: CAN''T LOCATE TIOT ENTRY..VOLUME X
               IS BYPASSED ',ROUTCDE=(11),MF=L
         ORG   EMSG3+18
M3VOL    DS    CL6
         ORG
         SPACE
*
         EJECT
*.....................................................................*
*.       LIST FORM OF MACROS                                         .*
*.....................................................................*
         SPACE 2
OPEN     OPEN  (MSSVOLS,(INPUT)),MF=L
         SPACE 2
CLOSE    CLOSE (MSSVOLS),MF=L
         EJECT
*.....................................................................*
*.       DYNAMIC ALLOCATION REQUEST BLOCK TO ALLOC VTOC OF MSS VOL   .*
*.....................................................................*
         SPACE 3
         DS    0F                 GET FULLWORD BOUNDARY
S99RBPTR DC    X'80',AL3(S99RB)   THE POINTER TO THE MESS..
*
S99RB    DC    FL1'20'            LENGTH OF RB IN BYTES = 20
         DC    XL1'01'            VERB CODE=X'01'..ALLOC BY DSN
         DC    XL2'2000'          FLAGS1..NO OPER MOUNT        BW-V1M1
S99ERC   DC    XL2'0000'          ERROR CODE
S99INFO  DC    XL2'0000'          INFO CODE
         DC    AL4(S99TXPTR)      ADDRESS OF TEXT UNITS
         DC    XL4'00'            RESERVED
         DC    XL4'00'            FLAGS2
         SPACE 2
S99TXPTR DC    AL4(DDTU)          ADDR OF DDNAME TEXT UNIT
         DC    AL4(VOLTU)         ADDR OF VOLSER TEXT UNIT
         DC    AL4(UNITTU)        ADDR OF UNIT NAME TEXT UNIT
         DC    X'80',AL3(DISPTU)  ADDR OF DISP TEXT UNIT (LAST)
         SPACE 2
DDTU     DC    X'0001',X'0001',FL2'7',C'ALLOCDD' DDNAME
VOLTU    DC    X'0010',X'0001',FL2'6'
MSSVOL   DC    CL6' '                            MSS VOLUME SER
UNITTU   DC    X'0015',X'0001'                   MSS UNIT TYPE
UNITLEN  DC    FL2'5'                   LENGTH OF UNIT NAME
UNIT     DC    CL8'3330V'               UNIT NAME AREA
DISPTU   DC    X'0004',X'0001',FL2'1',X'08'      SHR
         SPACE 2
*.....................................................................*
*.       DYNAMIC ALLOCATION REQUEST BLOCK TO FREE DDNAME ALLOCDD     .*
*.....................................................................*
         SPACE 3
         DS    0F                  GET FULLWORD BOUNDARY
FREEPTR  DC    X'80',AL3(FREERB)   THE POINTER TO THE MESS..
*
FREERB   DC    FL1'20'            LENGTH OF RB IN BYTES = 20
         DC    XL1'02'            VERB CODE=X'02'..FREE BY DDN
         DC    AL2(0)             FLAGS1..NO OPTIONS
FREERC   DC    XL2'0000'          ERROR CODE
FREEINFO DC    XL2'0000'          INFO CODE
         DC    AL4(FREETXPT)      ADDRESS OF TEXT UNITS
         DC    XL4'00'            RESERVED
         DC    XL4'00'            FLAGS2
         SPACE 2
FREETXPT DC    AL4(FDDNTU)        ADDR OF DSN TEXT UNIT
         DC    X'80',AL3(UNALCTU) ADDR OF UNALLOC TEXT TU(LAST)
         SPACE 2
FDDNTU   DC    X'0001',X'0001',FL2'7',C'ALLOCDD'  DDNAME
UNALCTU  DC    X'0007',X'0000'        UNALLOC EVEN IF PERM ALLOC
         EJECT
*.....................................................................*
*.       MSSVOLS RECORD FORMAT                                       .*
*.....................................................................*
         SPACE 2
MREC     DS    0F
MVOLSER  DC    CL6' '                    VOLSER TO MONITOR
MUNIT    DC    CL8' '                    ESOTERIC UNIT TYPE TO MONITOR
MMSVGP   DC    CL8' '                    MSVGP OF VOLUME
MFRESPCE DC    CL5' '                    FREESPACE (CYLS) P'ZZZZ9'
MCONTIG  DC    CL5' '                    CONTIG CYLS      P'ZZZZ9'
MEXTENTS DC    CL5' '                    # EXTENTS        P'ZZZZ9'
MDESCR   DC    CL30' '                   DESCRIPTION
MOWNER   DC    CL10' '                   OWNER INFORMATION
         DC    CL3' '       FILLER
MRECLEN  EQU   *-MREC             LENGTH OF RECORD
         EJECT
*.....................................................................*
*.       LITERAL POOL                                                .*
*.....................................................................*
         SPACE 2
         LTORG
         EJECT
*.....................................................................*
*.       DCBS                                                        .*
*.....................................................................*
         SPACE 2
MSSVOLS  DCB   DSORG=PS,MACRF=(GM),DDNAME=MSSVOLS,LRECL=MRECLEN,       X
               EODAD=EOFINPUT,BUFNO=2
         END
