*          DATA SET TSO033     AT LEVEL 002 AS OF 08/10/78
*        DATA SET TSO033     AT LEVEL 001 AS OF 09/09/77
*        DATA SET TSO033     AT LEVEL 001 AS OF 08/12/77
*
* THIS IS THE UPTIME DISPLAYER..SHOWS TRANSACTION TIME FOR
*   THE MASTER SCHEDULER FROM OUCB OF ASID=1.
*
* THIS WORKS FOR OS/VS2 REL 3.7 AND REQUIRES MACROS FROM
*   SYS1.AMODGEN
* KEN TRUE     FAIRCHILD CAMERA AND INSTRUMENT 12 AUG 77
*
* MODIFIED TO WORK AS TSO COMMAND OR OPER STC     K TRUE  12AUG77
*
*
LINKREG  EQU   6                   LINK REGISTER
*
TSO033   PGM                ,PGM TO SHOW EXEC JOBS AND STC
         EXTRACT MF=(E,EXTRACT)
         L     R1,ANSWER            (R1)=ADDR OF TSO FLAG FROM EXTRACT
         MVC   TSOFLAG,0(R1)        MOVE IN THE ANSWER TO OUR FLAG
         SPACE
*
         LA    R0,X'26'          SET SYSEVENT CODE
         ICM   R0,B'1100',=H'1'  SET ASID TO 1
         LA    R1,RTNAREA        -> RETURN AREA
         SVC   95                DO IT
         L     R4,4(,R1)         GET TOTAL TRANSACTIVE TIME
         SRDL  R4,32             MOVE INTO R5 FOR DIVIDE.
         D     R4,=F'1024'       CONVERT TO SECONDS.
         SLR   R4,R4             IGNORE REMAINDER.
         D     R4,=F'3600'       DIVIDE TO GET HOURS.
SAVEHRS  STM   R4,R5,SAVEREGS    SAVE HRS/REM FOR LATER
         SLR   R4,R4
         D     R4,=F'24'         DIVIDE TO GET TOTAL DAYS
         CVD   R5,WRKD           CONVERT DAYS
         MVC   UPDAYS-1(4),=X'40202120' MOVE IN PATTERN
DAYS     ED    UPDAYS-1(4),WRKD+6  AND EDIT THE VALUE
         LR    R5,R4             ESTABLISH NEW HOURS....
HOURS    L     R4,SAVEREGS       GET BACK HOURS REMAINDER
         CVD   R5,WRKD           CONVERT HOURS -
         UNPK  UPHH,WRKD+6(2)      - TO PRINTABLE FORMAT.
         OI    UPHH+1,X'F0'      FIXUP SIGN BYTE.
         MVI   UPHH+2,C':'       SEPARATOR.
         SRDL  R4,32             MOVE REMAINDER OVER FOR DIVIDE.
         D     R4,=F'60'         GET MINUTES.
         CVD   R5,WRKD           CONVERT MINUTES -
         UNPK  UPMM(2),WRKD+6(2)   - TO PRINTABLE FORMAT.
         OI    UPMM+1,X'F0'      FIXUP SIGN BYTE.
         MVI   UPMM+2,C':'       SEPARATOR.
         CVD   R4,WRKD           REMAINDER IS SECONDS.
         UNPK  UPSS(2),WRKD+6(2)   CONVERT SEC TO PRINTABLE FORMAT.
         OI    UPSS+1,X'F0'      FIXUP SIGN.
         SPACE 2
*                                   AND MEMORY STATUS
         LA    R1,MSGWTO
         BAL   LINKREG,PUTMSG     SHOVEL IT OUT
         B     $$EOJ
         EJECT
*.....................................................................*
*        LOCAL SUBROUTINE FOR IO TO OPER/USER                         *
*.....................................................................*
         SPACE
*
* ENTER WITH WTO,WTOR LIST FORM POINTED TO BY R1
*
*    BAL  LINKREG,PUTMSG    OR
*    BAL  LINKREG,PUTGET
*
*  USES R14 FOR WORK
*
*
PUTMSG   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TPUTIT              YES-> GO DO TPUT
         SVC   35                  NO-> USE WTO
         BR    LINKREG
TPUTIT   DS    0H
         LH    R0,0(,R1)           GET LENGTH OF WTO MESSAGE
         S     R0,=F'4'            SUBTRACT OFF HEADER
         LA    R1,4(,R1)           BUMP MSG ADDRESS PAST HDR
         TPUT  (1),(0),R           DO THE TPUT
         BR    LINKREG
         SPACE 2
PUTGET   DS    0H
         TM    TSOFLAG,X'80'       IS IT TSO?
         BO    TSOIT               YES-> GO USE TGET
         LR    R14,R1              NO-> USE WTOR .. SAVE MSG ADDRESS
         SVC   35                  DO THE WTOR...
         L     R1,4(,R14)          GET ADDR OF ECB
         XC    0(4,R1),0(R1)       CLEAN OUT THE ECB....
         LA    R0,1
         SVC   1                   WAIT FOR WTOR TO COMPLETE
         B     PUTRET              RETURN
TSOIT    DS    0H
         LR    R14,R1              SAVE
         LH    R0,8(,R1)           PUT LENGTH
         S     R0,=F'4'
         LA    R1,12(,R1)          PAST HDR
         TPUT  (1),(0),R
         SR    R0,R0
         IC    R0,0(,R14)          GET REPLY LENGTH
         L     R1,0(,R14)          GET REPLY ADRS
         LA    R1,0(,R1)
         ICM   R1,B'1000',=X'80'   INDICATE TGET...
         TGET  (1),(0),R
PUTRET   DS    0H
         L     R1,0(,R14)          GET REPLY ADRS
         XR    R0,R0
         IC    R0,0(,R14)          GET REPLY LENGTH
UPPER    DS    0H
         OI    0(R1),C' '          UPPER CASE
         LA    R1,1(,R1)           NXT
         BCT   R0,UPPER
         BR    LINKREG
         EJECT
*.....................................................................*
*        LIST FORMS OF MACROS                                         *
*.....................................................................*
         SPACE 2
EXTRACT  EXTRACT ANSWER,'S',FIELDS=(TSO),MF=L
         EJECT
*.....................................................................*
*        VARIABLES                                                    *
*.....................................................................*
         SPACE 2
SWITCH   DC    XL1'00'
TSOFLAG  DC    X'00'               ='80' IF TSO. '00' IF NOT TSO
ANSWER   DC    F'0'                ADDRESS POINTER FOR EXTRACT
         SPACE 2
WRKD     DC    D'0'
RMCTPTR  DS    F
SAVEREGS DS    4F
RTNAREA  DS    3F
         EJECT
*.....................................................................*
*        MESSAGE FORMATS                                              *
*.....................................................................*
         SPACE 2
MSGWTO   WTO   'SYSTEM HAS BEEN UP FOR XXX DAYS HH:MM:SS ',            X
               ROUTCDE=(11),MF=L
         ORG   MSGWTO+4
BUFFER   DS    CL72
         ORG   MSGWTO+27
UPDAYS   DS    CL3                JOBNAME
         ORG   MSGWTO+36
UPHH     DS    CL2                FOR UP  TIME HH:MM:SS
         DS    CL1
UPMM     DS    CL2
         DS    CL1
UPSS     DS    CL2
         ORG
         EJECT
*.....................................................................*
*        LITERALS                                                     *
*.....................................................................*
         SPACE 2
         LTORG
         END
