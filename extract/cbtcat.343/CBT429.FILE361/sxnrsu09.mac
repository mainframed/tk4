SXNRSU09 CSECT
         PRINT ON,GEN,DATA
*
*      IDENTIFICATION DIVISION.
*      PROGRAM-ID.               SXNRSU09
*      AUTHOR.                   F.JOHNSTON.
*      DATE-WRITTEN.             30 SEP 69.
*      DATE-COMPILED.            06 NOV 73.
*      SECURITY.                 UNCLASSIFIED.
*      REMARKS.
*          PRODUCT-NO.           NONE.
*          JOB-NO.               7108.
*          TITLE.                DATE ROUTINE.
*          AUTHORITY.            MIDAP 18-2.
*          FREQUENCY.            R
*          LINKAGE.              NONE.
*          PURPOSE.    TO EXTRACT THE CURRENT DATE FROM THE SUPERVISOR.
*          ALIASES.    DATERTNE, TIMERTNE, DATETIME, DATERT, TIMERT,
*                      DATIME.
*      CONSOLE MESSAGES.         NONE
         ENTRY DATERTNE,TIMERTNE,DATETIME,DATERT,TIMERT,DATIME
         USING DATERTNE,15
DATERT   EQU   *
DATERTNE MVI   SWIT,X'40'
         BC    15,GO
         USING TIMERTNE,15
TIMERT   EQU   *
TIMERTNE MVI   SWIT,X'80'
         BC    15,GO
         USING DATETIME,15
DATIME   EQU   *
DATETIME MVI   SWIT,X'C0'
         DROP  15
GO       SAVE  (14,12)
         BALR  12,0
         USING *,12
         LA    11,SAVE
         ST    11,8(13)
         ST    13,SAVE+4
         LR    13,11
         LR    2,1
         LA    11,TAB
         TIME  DEC
         TM    SWIT,X'80'          TIME SWIT
         BC    8,DATE              NO GO TO DATE
         BC    15,TIME
DATE     LM    7,10,0(2)
         ST    1,PACFLD
         OC    SWIT,PACFLD
         UNPK  UNPACFLD+2(3),PACFLD+1(2)
         TM    SWIT,X'01'          YEAR 2000 ?
         BC    1,*+14              YES
         MVC   UNPACFLD(2),=C'19'
         B     *+10
         MVC   UNPACFLD(2),=C'20'
         UNPK  UNPACFLD+4(3),PACFLD+2(2)
         MVC   WORKA(4),UNPACFLD
         MVC   WORKB+7(4),UNPACFLD
         MVC   0(7,7),UNPACFLD
         LR    2,1
         XR    1,1
         SRDL  2,16
         SLL   2,4
         O     2,PLUS
         SRL   3,16
         LR    4,3
         XR    3,3
         STM   1,2,CVBHLD
         CVB   1,CVBHLD
         LA    7,3
         NR    1,7
         BC    4,*+8
         MVI   15(11),X'1D'
         STM   3,4,CVBHLD
         CVB   3,CVBHLD
         XR    5,5
A2       IC    5,7(11)
         SR    3,5
         BC    2,UPDATE
         MVI   0(10),C'A'
         BC    4,ADDBCK
INSER    IC    3,7(11)
         BC    15,*+6
ADDBCK   AR    3,5
         MVC   WORKA+4(2),5(11)
         MVC   WORKB+3(3),1(11)
         CVD   3,CVBHLD
         PACK  CVBHLD+7(0),CVBHLD+7(0)
         MVN   WORKA+6(2),CVBHLD+6
         MVN   WORKB(2),CVBHLD+6
         MVC   0(8,8),WORKA
         MVC   0(11,9),WORKB
QUIT     L     13,SAVE+4
         RETURN (14,12)
UPDATE   C     11,LIMIT
         BC    8,ERROR
         LA    11,8(11)
         BC    15,A2
ERROR    MVI   0(10),C'B'
         BC    15,INSER
TIME     L     6,0(2)
         ST    0,PACFLD
         OI    UNPACFLD+6,X'0F'
         MVO   UNPACFLD,PACFLD
         UNPK  CVBHLD,UNPACFLD
         MVC   WORKC(4),CVBHLD
         MVC   WORKC+5(2),CVBHLD+4
         MVC   WORKC+8(2),CVBHLD+6
         MVC   0(10,6),WORKC
         LA    2,4(2)
         TM    SWIT,X'40'          DATE SWIT
         BC    1,DATE              YES GO TO DATE
         BC    15,QUIT
PACFLD   DS    F
UNPACFLD DS    CL7
WORKA    DC    CL8'      99'
WORKB    DC    CL11'99         '
WORKC    DC    CL10'    /  .  '
SWIT     DC    X'00'
PLUS     DC    F'12'
CVBHLD   DS    D
LIMIT    DC    A(TAB+88)
SAVE     DS    18F
         DS    0D
TAB      DC    CL4' JAN'
         DC    CL3' 01',HL1'31'
         DC    CL4' FEB'
         DC    CL3' 02',HL1'28'
         DC    CL4' MAR'
         DC    CL3' 03',HL1'31'
         DC    CL4' APR'
         DC    CL3' 04',HL1'30'
         DC    CL4' MAY'
         DC    CL3' 05',HL1'31'
         DC    CL4' JUN'
         DC    CL3' 06',HL1'30'
         DC    CL4' JUL'
         DC    CL3' 07',HL1'31'
         DC    CL4' AUG'
         DC    CL3' 08',HL1'31'
         DC    CL4' SEP'
         DC    CL3' 09',HL1'30'
         DC    CL4' OCT'
         DC    CL3' 10',HL1'31'
         DC    CL4' NOV'
         DC    CL3' 11',HL1'30'
         DC    CL4' DEC'
         DC    CL3' 12',HL1'31'
         END
