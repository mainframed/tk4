         TITLE  'Z Z M E S A G E  -  ZZRELINK MESSAGE HANDLING ROUTINE'
ZZMESAGE CSECT
GR0      EQU   0
GR1      EQU   1
GR2      EQU   2
GR3      EQU   3
GR4      EQU   4
GR5      EQU   5
GR6      EQU   6
GR7      EQU   7
GR8      EQU   8
GR9      EQU   9
GR10     EQU   10                 BASE REGISTER 1
GR11     EQU   11                 BASE REGISTER 2
GR12     EQU   12
GR13     EQU   13
GR14     EQU   14
GR15     EQU   15
TRIPLESP EQU   C'-'
SKPSPACE EQU   C' '
C0       EQU   C'0'               EQUATES
C1       EQU   C'1'                 TO SET UP
C2       EQU   C'2'                   RETURN CODE
C9       EQU   C'9'                     IN EOJ MSG
FF       EQU   X'FF'
FIFTYSIX EQU   56
BLNK     EQU   C' '               BLANK CHARACTER
BUFL     EQU   120                LENGTH (MINUS 1) OF MSG BUFFER
CCLEN    EQU   80                 LENGTH OF CONTROL CARD
CHAR0    EQU   C'0'               FOR SETTING PAGE NBR ZONE BITS
COFF     EQU   20                 OFFSET IN BFR FOR CTL CD IMAGE
ZZRMNO   EQU   4                  OFFSET INTO 'ZZR1  I' FOR MSG NBR
LFT2     EQU   2                  FOR USE WITH 'SLL' INSTRUCTION
LINE1    EQU   1                  FOR ADDING 1 TO LINE COUNT
LINES3   EQU   3                  FOR ADDING 3 TO LINE COUNT
LNKBK    EQU   4                  FOR STD LINKAGE USE
LNKTHS   EQU   8                  FOR STD LINKAGE USE
MSGK     EQU   3                  CONSTANT LENGTH OF 3 BYTES WHICH BE-
*                                 GIN EACH MESSAGE 'DC'
M2       EQU   2                  LENGTH OF EACH MSG LIST ENTRY
NAMLEN   EQU   8                  LENGTH OF NAME
NBRLEN   EQU   7                  LENGTH OF NUMBER AND OF 'ZZR1  I' CON
DDNLTH   EQU   8                  LENGTH OF DDNAME
NLEN     EQU   2                  LENGTH OF MSG NBR PUT INTO 'ZZR1  I'
OFF172I  EQU   12                 OFFSET INTO MSG ZZR172I FOR NAME
PGOFF    EQU   116                OFFSET INTO PAGE HEADER FOR PAGE NBR
PNLEN    EQU   4                  LENGTH OF PAGE NUMBER
RCOFF    EQU   25                 OFFSET IN EOJ MSG (WTO) FOR RET CODE
RCOFFP   EQU   16                 OFFSET IN EOJ MSG (NOT WTO) FOR R.C.
SYN1     EQU   62                 LENGTH OF 1ST PART OF SYNADAF MSG
SYN2     EQU   58                 LENGTH OF 2ND PART OF SYNADAF MSG
TXTOFF   EQU   9                  OFFSET IN BFR FOR MSG TEXT
VALOFF   EQU   3                  OFFSET INTO VALIDATION ERR MSG
ZRO      EQU   0                  CONSTANT OF ZERO
L1       EQU   1                  LENGTH OF 1
L3       EQU   3                  LENGTH OF 3
L8       EQU   8                  LENGTH OF 8
P1       EQU   1                  TO BE ADDED TO SOMETHING
P4       EQU   4                  TO ADD 4 TO SOMETHING
X0       EQU   0                  OFFSET OF ZERO
X1       EQU   1                  OFFSET OF ONE
LSYN1    EQU   SYN1+P1
         EJECT
         SAVE  (14,12),,*
         LR    GR10,GR15          ADDRESSIBILITY
         USING ZZMESAGE,GR10
         USING ZZRELCOM,4
         LA    GR12,SAVE
         ST    GR12,LNKTHS(GR13)  STD LINKAGE
         ST    GR13,LNKBK(GR12)   ADDR OF CALLER'S SAVE AREA
         LR    GR13,GR12
         LA    GR2,MSGLIST
         TM    IOEF2,SPRNOPN      I/O ERROR ON SYSPRINT
         BZ    PRNTMSG            NO - PROCEED TO WRITE MSG
         TM    X0(GR2),IOERF      IS I/O ERR MSG IN BUFFER ALREADY
         BO    WTOS               ISSUE SYNADAF MSG AS WTO
         CLI   X1(GR2),BADPRINT   IS SYSPRINT BAD - WTO TO BE ISSUED
         BNE   TESTM53            NO - SEE IF 'END OF JOB' WTO TO
*                                 BE WRITTEN
         L     GR9,ATXT           ADDR OF MESSAGE TEXT CSECT
         USING TXTCSECT,GR9
         MVC   ZZR172I+OFF172I(NAMLEN),NAMEFLD PUT NAME INTO WTO TEXT
*                                 MOVE NAME OF SYSPRINT DD WHICH
*                                 COULD NOT BE OPENED INTO
*                                 WTOLFBAD WTO
         LA    GR1,ZZR172I        POINT TO WTO MESSAGE
         WTO   MF=(E,(1))
TESTM53  CLI   X1(GR2),M53        IS 'END OF JOB' WTO TO BE ISSUED NOW
         BNE   RETURN             IF NOT, RETURN TO CALLER NOW
         LA    GR1,WTOLF5         ADDRESS OF LIST FORM
*                                 'END OF JOB' WTO
         CLI   RCBUF,C9           IS RETURN CODE 10 OR HIGHER
         BH    WTORC12            YES, MOVE IN 12
         MVI   RCOFF-P1(GR1),C0   NO, MOVE IN 0X
         MVC   RCOFF(L1,GR1),RCBUF  MOVE IN RETURN CODE
         B     GOWTO              AND WRITE EOJ WTO MSG
WTORC12  EQU   *
         MVI   RCOFF-P1(GR1),C1   MOVE IN 10
         MVI   RCOFF(GR1),C2      MOVE IN 2
GOWTO    EQU   *
         WTO   MF=(E,(1))         EXECUTE 'END OF JOB' WTO
         B     RETURN             NOW EXIT TO CALLER
PRNTMSG  EQU   *
         SR    GR3,GR3
         IC    GR3,LINECT
         LTR   GR3,GR3            DOES LINE-COUNT NOW = 0
         BZ    PRNTHEAD           IF SO, GO PRINT PAGE HEADER
RDYWRT1  EQU   *
         MVI   MSGBUF,SKPSPACE
RDYWRTHD TM    X0(GR2),IOERF      IS THERE A MSG IN THE BUFFER
         BO    WRTMSG             IF SO, GO WRITE IT
RDYWRT2  EQU   *
         LA    GR6,MSGBUF+P1
         TM    X0(GR2),CTLCD      IS A CONTROL STMT TO BE PRINTED
         BO    ADCC               IF ON, GO PRINT THE CONTROL STMT
         SR    GR11,GR11
         IC    GR11,X1(GR2)       GET MESSAGE CODE
         SLL   GR11,LFT2          MULTIPLY MSG CODE NUMBER BY 4 TO
*                                 USE IT AS AN INDEX TO THE TABLE OF
*                                 POINTERS TO MESSAGE TEXT
         L     GR5,MSGTAB(GR11)   OBTAIN THE POINTER TO THE MESSAGE TXT
         TM    X0(GR2),RCODE      IS A RETURN CODE TO BE PUT INTO MSG
         BZ    ANYPARM            NO- SEE IF ANY PARAMETERS
         CLI   RCBUF,C9           IS RETURN CODE 10 OR HIGHER
         BH    WRTRC12            YES, MOVE IN 12
         MVI   RCOFFP-P1(GR5),C0  NO, MOVE IN 0X
         MVC   RCOFFP(L1,GR5),RCBUF MOVE IN RETURN CODE
         B     ANYPARM            AND WRITE EOJ MSG
WRTRC12  EQU   *
         MVI   RCOFFP-P1(GR5),C1  MOVE IN 10
         MVI   RCOFFP(GR5),C2     MOVE IN 2
ANYPARM  EQU   *
         TM    X0(GR2),PBIT       ARE THERE ANY PARAMETERS
         BZ    NOPARAM            NO - CONTINUE PROCESSING
         TM    PARAMS,NAME        IS THERE A NAME PARAMETER
         BZ    NBRPARM            NO - IS THERE A NBR PARAMETER
         SR    GR8,GR8
         IC    GR8,NAMEDISP       DISPLACEMENT OF NAME FROM BEGINING
*                                 OF MSG IS IN GR8
         AR    GR8,GR5            ADD DISPLACEMENT TO MESSAGE ADDRESS
         LA    GR8,MSGK(GR8)      POINT TO START OF MSG TEXT
         MVC   X0(NAMLEN,GR8),NAMEFLD  PUT NAME INTO MSG TEXT
NBRPARM  TM    PARAMS,NBR         IS THERE A NUMBER PARAMETER
         BZ    DDTEST1            NO - CONTINUE PROCESSING
         SR    GR8,GR8
         IC    GR8,NODISP         DISPLACEMENT OF NAME FROM
*                                 BEGINNING OF MSG IS IN GR8
         AR    GR8,GR5            ADD DISPLACEMENT TO MESSAGE ADDRESS
         LA    GR8,MSGK(GR8)      POINT TO START OF MSG TEXT
         MVC   X0(NBRLEN,GR8),NOFLD    PUT NUMBER INTO MSG TEXT
DDTEST1  TM    PARAMS,DDNM        IS DDNAME PRESENT
         BNO   NOPARAM            NO _ CONTINUE PROCESSING
         SR    GR8,GR8            ZERO R8
         IC    GR8,DDNMDISP       DISPLACEMENT OF DDNAME
         AR    GR8,GR5            POINT AT DDNAME SLOT
         LA    GR8,MSGK(GR8)      POINT AT TRUE SLOT
         MVC   X0(DDNLTH,GR8),DDNMFLD INSERT DDNAME IN MESSAGE
NOPARAM  EQU   *
         CLI   X1(GR2),GENERUAL   IS THIS A 'VALIDATION ERROR' MSG
         BNE   NOTVALID           IF NOT, NO SPECIAL SETUP REQUIRED
         MVC   VALOFF(NAMLEN,GR5),DDVALNM   PUT NAME BEING VALIDATED IN
*                                 GENERVAL MSG
NOTVALID EQU   *
         MVC   X0(NBRLEN,GR6),ZZRCON   SET UP 'ZZR1XXI' IN BUFFER
*                                 BUFFER
         MVC   ZZRMNO(NLEN,GR6),X1(GR5) PUT MSG NBR INTO 'ZZR1  I'
         SR    GR7,GR7
         IC    GR7,X0(GR5)        GET LENGTH OF MSG TEXT (IN BYTES)
         BCTR  GR7,GR0            SUBTR 1 FOR EX
         EX    GR7,MOVEMSG        LENGTH OF MSG IS IN GR7
         B     WRTMSG             GO WRITE THE MSG
ADCC     LA    GR5,CCIMAGE
         MVC   COFF(CCLEN,GR6),X0(GR5) PUT CONTROL CARD IMAGE
*                                 INTO THE BUFFER
WRTMSG   EQU   *
         LA    GR6,MSGBUF
WRTHEAD  EQU   *
         LA    GR7,PRTDCB
         LA    GR8,WTOS           ADDRESS TO BE USED BY SYNAD ROUTINE
*                                 IF I/O ERROR OCCURRS WHILE 'PRINTING'
         PUT   (GR7),(GR6)        PRINT MESSAGE
         TM    IOEF2,SPRNOPN      I/O ERROR ON SYSPRINT
         BZ    NOPUTERR           NO - CONTINUE PROCESSING
WTOS     MVC   WTOLF1+P4(SYN1),MSGBUF+P1 PUT 1ST PART OF SYNADAF
*                                 MESSAGE INTO WTO BUFFER
         MVC   WTOLF2+P4(SYN2),MSGBUF+LSYN1 PUT SECOND PART OF
*                                 SYNADAF MESSAGE INTO WTO BUFFER
         LA    GR1,WTOLF1         ADDRESS OF LIST FORM WTO IN GR1
         WTO   MF=(E,(1))         EXECUTE WTO
         LA    GR1,WTOLF2         ADDRESS OF LIST FORM WTO IN GR1
         WTO   MF=(E,(1))         EXECUTE WTO
*                                 TWO WTO'S MUST BE ISSUED BECAUSE
*                                 IMPLEMENTATION OF MCS DOES NOT
*                                 ALLOW MORE THAN 72 BYTES PER LINE
*                                 ON THE OPERATORS CONSOLE
         B     TESTM53            SEE IF END OF JOB MSG TO BE WRITTEN
NOPUTERR EQU   *
         TM    SWITCH,WRTHDMSG    WAS A HEADER JUST PRINTED
         BO    NISWOFF            YES - DONT CLEAR BUFFER
         MVI   MSGBUF,BLNK        SET MSG BUFFER TO BLANKS AFTER WRITE
         MVC   MSGBUF+P1(BUFL),MSGBUF
         B     NOTHEAD            SINCE MSG ISNT A HEADER, GO TO
*                                 GET THE NEXT MESSAGE
NISWOFF  EQU   *
         NI    SWITCH,FF-WRTHDMSG TURN OFF HEADER SWITCH
         MVI   MSGBUF,TRIPLESP    IF A HEADER WAS PRINTED,
*                                 SET CARRIAGE CONTROL CHARACTER TO
*                                 TRIPLE SPACE
         LA    GR3,LINES3(GR3)    ADD 3 TO LINE COUNT
         B     RDYWRTHD           BRANCH TO SET UP FOR PRINTING THE
*                                 MESSAGE WHICH FOLLOWS THE HEADER
WTOLF1   WTO   '                                                       X
                      ',MF=L      FOR 1ST PART OF SYNADAF MSG
WTOLF2   WTO   '                                                       X
                  ',MF=L          FOR 2ND PART OF SYNADAF MSG
WTOLF5   WTO   'ZZR147I END OF JOB - X WAS HIGHEST SEVERITY CODE',MF=L
         SPACE 1
NOTHEAD  EQU   *
         LA    GR3,LINE1(GR3)     ADD 1 TO LINE COUNT
         STC   GR3,LINECT
         CLI   LINECT,FIFTYSIX    WAS THE 56'TH LINE JUST PRINTED
         BL    RETCAL             IF NOT, SEE IF NECESSARY TO RETURN
*                                 TO CALLER
         MVI   LINECT,ZRO         RESET LINE COUNT
RETCAL   TM    X0(GR2),LASTMSG    IS THIS THE LAST MSG TO HANDLE NOW
         BO    RETURN             IF SO, EXIT TO CALLER
         LA    GR2,M2(GR2)        ELSE POINT TO NXT MSG LIST ENTRY
         CLI   LINECT,ZRO         DOES PAGE HEADER NEED TO BE PRINTED
         BNE   RDYWRT1            IF NOT, GO TO SET UP NEXT MESSAGE
         SR    GR3,GR3
PRNTHEAD OI    SWITCH,WRTHDMSG
         LA    GR6,HEADBUF
         MVC   PGOFF(PNLEN,GR6),PGNO   PUT PAGE NBR INTO HEADER MSG
         PACK  PGCONVRT(L8),PGNO+P1(L3) NOW INCREMENT IT
         CVB   GR8,PGCONVRT       CONVERT PACKED PAGE NUMBER TO
*                                 BINARY
         LA    GR8,X1(GR8)        ADD 1 TO PAGE NUMBER
         CVD   GR8,PGCONVRT       CONVERT BACK TO DECIMAL
         UNPK  PGNO+P1(L3),PGCONVRT
         OI    PGNO+L3,CHAR0      CHANGE SIGN OF LOW ORDER BYTE TO ZONE
         B     WRTHEAD            GO WRITE THE HEADER
RETURN   L     GR13,SAVE+LNKBK
         RETURN  (14,12)
PGCONVRT DC    D'0'               AREA FOR PAGE NBR CONVERSION
ATXT     DC    A(TXTCSECT)        ADDR OF MESSAGE TEXT CSECT
SAVE     DC    18F'0'             REGISTER SAVE AREA
MOVEMSG  MVC   TXTOFF(X0,GR6),MSGK(GR5) EXECUTED TO PUT MSG INTO BUFFER
SWITCH   DC    X'00'              INTERNAL SWITCH
WRTHDMSG EQU   X'80'         ON = A PAGE HEADER IS BEING WRITTEN
*              LOW ORDER 7 BITS NOT USED
ZZRCON   DC    C'ZZR1  I'         MESSAGE NUMBER CONSTANT
* THE FOLLOWING 121 BYTES ARE THE PAGE HEADER TEXT
HEADBUF  DC    C'1'            CARRIAGE CONTROL=EJECT
         DC    40C' '
         DC    C'ZZRELINK MESSAGES AND CONTROL STATEMENTS '
         DC    29C' '
         DC    C'PAGE '
         DC    5C' '
         DS    0F
PATCHLEN EQU   (*-ZZMESAGE)/20      LENGTH OF 5 PER CENT PATCH AREA
PATCH    DC    XL(PATCHLEN)'00'   5 PER CENT PATCH AREA          A41780
         DS    0F
MSGTAB   EQU   *-4                MSGTAB IS A TABLE OF POINTERS TO
*                                 THE MESSAGES. IT MUST START AT THE
*                                 FIRST ENTRY-4 BECAUSE THERE IS NO
*                                 MESSAGE WITH THE NUMBER 0
* NOTE - EACH ADCON IS ACCESSED AS NEEDED BY USING THE MESSAGE CODE AND
*        MULTIPLYING IT BY 4.  IT IS THEREFORE IMPORTANT TO RETAIN THE
*        SAME SEQUENCE OF ADCONS AND MESSAGE CODES.
*        DC    AL4(ZZR100I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(ZZR101I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(ZZR102I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(ZZR103I)       MESSAGE TEXT NOT IN THIS MODULE
         DC    AL4(ZZR104I)
         DC    AL4(ZZR105I)
         DC    AL4(ZZR106I)
         DC    AL4(ZZR107I)
         DC    AL4(ZZR108I)
         DC    AL4(ZZR109I)
         DC    AL4(ZZR110I)
         DC    AL4(ZZR111I)
         DC    AL4(ZZR112I)
         DC    AL4(ZZR113I)
         DC    AL4(ZZR114I)
         DC    AL4(ZZR115I)
         DC    AL4(ZZR116I)
         DC    AL4(ZZR117I)
         DC    AL4(ZZR118I)
         DC    AL4(ZZR119I)
         DC    AL4(ZZR120I)
         DC    AL4(ZZR121I)
         DC    AL4(ZZR122I)
         DC    AL4(ZZR123I)
         DC    AL4(ZZR124I)
         DC    AL4(ZZR125I)
         DC    AL4(ZZR126I)
         DC    AL4(ZZR127I)
         DC    AL4(ZZR128I)
         DC    AL4(ZZR129I)
         DC    AL4(ZZR130I)
         DC    AL4(ZZR131I)
         DC    AL4(ZZR132I)
         DC    AL4(ZZR133I)
         DC    AL4(ZZR134I)
         DC    AL4(ZZR135I)
         DC    AL4(ZZR136I)
         DC    AL4(ZZR137I)
         DC    AL4(ZZR138I)
*        DC    AL4(ZZR139I)       SYNADAF MSG - TEXT NOT IN THIS MODULE
         DC    AL4(ZZR140I)
         DC    AL4(ZZR141I)
         DC    AL4(ZZR142I)
         DC    AL4(ZZR143I)
         DC    AL4(ZZR144I)
         DC    AL4(ZZR145I)
         DC    AL4(ZZR146I)
         DC    AL4(ZZR147I)
         DC    AL4(ZZR148I)
         DC    AL4(ZZR149I)
         DC    AL4(ZZR150I)
         DC    AL4(ZZR151I)
         DC    AL4(ZZR152I)
         DC    AL4(ZZR153I)
         DC    AL4(ZZR154I)
         DC    AL4(ZZR155I)
         DC    AL4(ZZR156I)
         DC    AL4(ZZR157I)
         DC    AL4(ZZR158I)
         DC    AL4(ZZR159I)
         DC    AL4(ZZR160I)
         DC    AL4(ZZR161I)
         DC    AL4(ZZR162I)
         DC    AL4(ZZR163I)
         DC    AL4(ZZR164I)
         DC    AL4(ZZR165I)
         DC    AL4(ZZR166I)
         DC    AL4(ZZR167I)
         DC    AL4(ZZR168I)
         DC    AL4(ZZR169I)
         DC    AL4(ZZR170I)
*        DC    AL4(ZZR171I)       I/O ERROR MSG - TEXT IN ZZRIOE MODULE
         DC    AL4(ZZR172I)
*        DC    AL4(ZZR173I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(ZZR174I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(ZZR175I)       MESSAGE TEXT NOT IN THIS MODULE
*        DC    AL4(ZZR176I)       MESSAGE TEXT NOT IN THIS MODULE
         DC    AL4(ZZR177I)
         DC    AL4(ZZR178I)                                           $
*        DC    AL4(ZZR188I)       MSG TEXT NOT IN THIS MODULE    A38720
         EJECT
ZZRVMTXT CSECT
TXTCSECT EQU   ZZRVMTXT
***********************************************************************
* MESSAGE TEXT FORMAT -
* A 1 BYTE HEXADECIMAL CONSTANT , WHICH IS THE HEXADECIMAL            *
* EQUIVALENT OF THE NUMBER OF BYTES  IN THE MESSAGE TEXT , IS         *
* DEFINED                                                             *
* FOLLOWING THE 1 BYTE HEXADECIMAL CONSTANT IS THE MESSAGE TEXT.      *
* THE MESSAGE TEXT IS A VARIABLE LENGTH CHARACTER DEFINED FIELD       *
* OF WHICH THE FIRST TWO BYTES ARE THE VARIABLE TO BE USED AS THE     *
* MESSAGE NUMBER IN ZZR1  I CONSTANT FIELD
***********************************************************************
*                                                                     *
* NOTE - NOT ALL MESSAGES ISSUED BY THIS UTILITY PROGRAM HAVE THEIR
*        MESSAGE TEXT CONTAINED WITHIN THIS MODULE.  THOSE MESSAGES NOT
* CONTAINED HEREIN ARE NOTED IN THEIR APPROPRIATE LOCATIONS WITHIN THE
*        MESSAGES HERE.
* MESSAGE NUMBERS CURRENTLY IN USE ARE -
*ZZR100I THROUGH ZZR177I AND ZZR188I                             A38720
*                                                                     *
***********************************************************************
* THE FOLLOWING 4 MESSAGES ARE CONTAINED WITHIN ZZRIOE MODULE -
*ZZR100I - I/O ERROR READING MEMBER (MBRNAME)
*ZZR101I - I/O ERROR WRITING MEMBER DATA AT TTR = (TTR) /READBACK CHECK
*ZZR102I MEMBER (NAME) NOT COPIED DUE TO I/O ERROR
*ZZR103I MEMBERS (MBRNAME) THROUGH END OF DATA SET ARE NOT ACCESSIBLE
*        DUE TO I/O ERROR
ZZR104I  DC    X'1A'              MSG LENGTH
         DC    C'04INVALID COMMAND OR KEYWORD'
ZZR105I  DC    X'11'              MSG LENGTH
         DC    C'05PARAMETER INVALID'
ZZR106I  DC    X'13'              MSG LENGTH
         DC    C'06UNEQUAL PARENTHESIS'
ZZR107I  DC    X'14'              MSG LENGTH
         DC    C'07INVALID CONTINUATION'
ZZR108I  DC    X'20'              MSG LENGTH
         DC    C'08MEMBER WITHOUT SELECT OR EXCLUDE'
ZZR109I  DC    X'37'              MSG LENGTH
         DC    C'09NO MIXING OF SELECT AND EXCLUDE MODES IN SAME'
         DC    C' LINK STEP'
ZZR110I  DC    X'19'              MSG LENGTH
         DC    C'10INVALID REPLACE SPECIFIED'
ZZR111I  DC    X'0F'              MSG LENGTH
         DC    C'11NULL PARAMETERS'
ZZR112I  DC    X'20'              MSG LENGTH
         DC    C'12CANNOT RENAME/REPLACE ON EXCLUDE'
ZZR113I  DC    X'1B'              MSG LENGTH
         DC    C'13OUTDD OR INDD NOT SPECIFIED'
ZZR114I  DC    X'1B'              MSG LENGTH
         DC    C'14OUTDD/LIST NOT ON LINK CARD'
ZZR115I  DC    X'14'              MSG LENGTH
         DC    C'15END OF FILE ON SYSIN'
ZZR116I  DC    X'3E'              MSG LENGTH
         DC    C'16MIXING CONTROL STATEMENTS FROM OLD AND NEW '
         DC    C'VERSION OF ZZRELINK'
ZZR117I  DC    AL1(46)            MSG LENGTH
         DC    C'17OVERLAYS NOT SUPPORTED. XXXXXXXX NOT RE-LINKED'
ZZR118I  DC    X'17'              MSG LENGTH
         DC    C'18CONTROL STATEMENT ERROR'
ZZR119I  DC    X'18'              MSG LENGTH
         DC    C'19STATEMENT SEQUENCE ERROR'
ZZR120I  DC    X'19'              MSG LENGTH
         DC    C'20         VALIDATION ERROR'
ZZR121I  DC    AL1(33)            MSG LENGTH
         DC    C'21ERROR ATTEMPTING TO OPEN XXXXXXXX'
ZZR122I  DC    X'1A'              MSG LENGTH
         DC    C'22DSCB COULD NOT BE OBTAINED'
ZZR123I  DC    X'18'              MSG LENGTH
         DC    C'23DATA SET NOT PARTITIONED'
ZZR124I  DC    X'0D'              MSG LENGTH
         DC    C'24INVALID LRECL'
ZZR125I  DC    X'11'              MSG LENGTH
         DC    C'25INVALID BLOCKSIZE'
ZZR126I  DC    X'29'              MSG LENGTH
         DC    C'26         REFERENCES AN UNMOVABLE DATA SET'
ZZR127I  DC    X'12'              MSG LENGTH
         DC    C'27RECFM INCOMPATIBLE'
ZZR128I  DC    AL1(47)            MSG LENGTH
         DC    C'28UNABLE TO GAIN ACCESS TO DIRECTORY FOR XXXXXXXX'
ZZR129I  DC    AL1(64)            MSG LENGTH
         DC    C'29XXXXXXXX DIRECTORY ERROR. ALIAS XXXXXXXX OCCURS '
         DC    C'MORE THAN ONCE.'
ZZR130I  DC    AL1(46)            MESSAGE LENGTH
         DC    C'30IMPLEMENTATION RESTRICTION - ALIAS TABLE FULL.'
ZZR131I  DC    AL1(51)            MSG LENGTH
         DC   C'31UNABLE TO DETERMINE EPA FOR XXXXXXXX. NOT RE-LINKED.'
ZZR132I  DC    AL1(40)            MSG LENGTH
         DC    C'32I/O ERROR READING DIRECTORY FOR XXXXXXXX'
ZZR133I  DC    AL1(83)            MSG LENGTH
         DC    C'33XXXXXXXX NOT RE-LINKED. EXISTS ON OUTPUT DATA SET '
         DC    C'BUT REPLACE OPTION NOT SPECIFIED.'
ZZR134I  DC    X'26'              MSG LENGTH
         DC    C'34CANNOT COMPRESS WITH SELECT OR EXCLUDE'
ZZR135I  DC    AL1(53)            MSG LENGTH
         DC    C'35I/O ERROR ON XXXXXXXX WHILE READING CESD FOR '
         DC    C'XXXXXXXX'
ZZR136I  DC    AL1(77)            MSG LENGTH
         DC    C'36LOAD MODULE XXXXXXXX MARKED ''NOT EDITABLE''. '
         DC    C'IT WAS COPIED TO OUTPUT DATA SET.'
ZZR137I  DC    X'4E'
         DC    C'37CANNOT SPECIFY DUPLICATE MEMBERNAMES FOR SELECT/EXCLX
               UDE/RENAME - NAME=        '
ZZR138I  DC    X'2A'              MSG LENGTH
         DC    C'38CANNOT PROCESS ALL OLD/NEW-NAMES SPECIFIED'
*ZZR139I - SYNADAF MESSAGE TEXT, NOT CONTAINED WITHIN THIS MODULE
ZZR140I  DC    X'29'              MSG LENGTH
         DC    C'40         REFERENCES A NULL INPUT DATA SET'
ZZR141I  DC    X'3E'              MSG LENGTH
         DC    C'41CANNOT RE/DE-BLOCK WITH NOTE-LIST/USER TTRN IN '
         DC    C'MEMBER         '
ZZR142I  DC    X'1F'              MSG LENGTH
         DC    C'42CANNOT CONTINUE TO BUILD CTLTAB'
ZZR143I  DC    X'3E'              MSG LENGTH
         DC    C'43ALL SELECTED MEMBERS LINKED - DID NOT USE ALL '
         DC    C'SPECIFIED INDD''S'
ZZR144I  DC    X'49'              MSG LENGTH
         DC    C'44THERE ARE         UNUSED TRACKS IN OUTPUT DATA SET '
         DC    C'REFERENCED BY         '
ZZR145I  DC    X'27'              MSG LENGTH
         DC    C'45CANNOT COMPRESS TRACK OVERFLOW DATA SET'
ZZR146I  DC    X'23'              MSG LENGTH
         DC    C'46CANNOT COMPRESS WITH RE/DE-BLOCKING'
ZZR147I  DC    X'28'              MSG LENGTH
         DC    C'47END OF JOB - X WAS HIGHEST SEVERITY CODE'
ZZR148I  DC    X'4F'              MSG LENGTH
         DC    C'48NO SPACE IN OUTPUT DIRECTORY FOR DIRECTORY ENTRIES'
         DC    C' FROM INPUT DATA SET         '
ZZR149I  DC    X'3E'              MSG LENGTH
         DC    C'49THERE ARE          UNUSED DIRECTORY BLOCKS IN '
         DC    C'OUTPUT DIRECTORY'
ZZR150I  DC    X'56'              MSG LENGTH                     A36049
         DC    C'50**WARNING** THE OUTPUT DS REF BY XXXXXXXX CONTAINS T*
               OO MANY DIRECTORY BLOCKS PER TRACK'               A36049
ZZR151I  DC    X'20'              MSG LENGTH                     A36047
         DC    C'51JOB HAS TERMINATED WITH ERROR(S)'             A36047
ZZR152I  DC    X'38'              MSG LENGTH
         DC    C'52         COMPRESSED - WAS ALREADY IN PLACE AND NOT '
         DC    C'MOVED'
ZZR153I  DC    X'37'              MSG LENGTH
         DC    C'53ALL MEMBERS COMPRESSED - ALL WERE ORIGINALLY '
         DC    C'COMPRESSED'
ZZR154I  DC    X'25'              MSG LENGTH
         DC    C'54         HAS BEEN SUCCESSFULLY COPIED'
ZZR155I  DC    X'38'              MSG LENGTH
         DC    C'55         HAS BEEN SUCCESSFULLY COPIED AND IS A '
         DC    C'''NEWNAME'''
ZZR156I  DC    X'1C'              MSG LENGTH
         DC    C'56NOT A DIRECT ACCESS DATA SET'
ZZR157I  DC    X'1F'              MSG LENGTH
         DC    C'57XXXXXXXX DD STATEMENT NOT FOUND'
ZZR158I  DC    AL1(41)            MSG LENGTH
         DC    C'58UNIT ALLOCATED TO XXXXXXXX NOT SUPPORTED.'
ZZR159I  DC    X'3C'              MSG LENGTH
         DC    C'59NO MEMBERS LINKED FROM INPUT DATA SET '
         DC    C'REFERENCED BY         '
ZZR160I  DC    X'16'              MSG LENGTH
         DC    C'60CONCATENATED DATA SETS'
ZZR161I  DC    X'2D'              MSG LENGTH
         DC    C'61COMPRESS TO BE DONE USING INDD NAMED         '
ZZR162I  DC    X'4A'              MSG LENGTH
         DC    C'62INPUT DATASET FROM INDD NAMED          NOT SAME AS'
         DC    C' OUTDD - CANNOT COMPRESS'
ZZR163I  DC    X'2F'              MSG LENGTH
         DC    C'63NO MEMBER NAMES FOR PARTIAL LINK, '
         DC    C'WILL NOT LINK'
ZZR164I  DC    X'12'              MSG LENGTH
         DC    C'64TOTAL LINK ASSUMED'
ZZR165I  DC    X'49'              MSG LENGTH
         DC    C'65         ''FOUND'' BUT NOT LINKED, DUE TO I/O ERROR X
               READING INPUT DIRECTORY'
ZZR166I  DC    X'34'              MSG LENGTH
         DC    C'66NO MEMBERS LINKED TO DATA SET REFERENCED BY '
         DC    C'        '
ZZR167I  DC    X'47'              MSG LENGTH
         DC    C'67FOLLOWING MEMBER(S) LINKED FROM INPUT DATA SET REFERX
               ENCED BY          -'
ZZR168I  DC    X'5C'              MSG LENGTH
         DC    C'68**WARNING** DUE TO ERROR, POSSIBLE LOSS OF '
         DC    C'ACCESS TO MEMBER DATA AND/OR INCOMPLETE DIRECTORY'
ZZR169I  DC    X'4A'              MSG LENGTH
         DC    C'69**WARNING** DUE TO I/O ERROR ON SYSUT4, OUTPUT '
         DC    C'DIRECTORY MAY BE INCOMPLETE'
ZZR170I  DC    X'5D'              MSG LENGTH
         DC    C'70**WARNING** DUE TO SYSUT3 I/O ERROR, COMPRESS-IN-PLAX
               CE NOT DONE AND LINK OPERATION TERMINATED'
* THE FOLLOWING MESSAGE IS CONTAINED WITHIN ZZRIOE MODULE -
*ZZR171I **WARNING** DIRECTORY MAY NOT REFLECT VALID LOCATION OF MEM-
*        BER DATA
ZZR172I  WTO   'ZZR172I          COULD NOT BE OPENED',MF=L
*ZZR173I - MESSAGE TEXT FOR THIS MSG IS IN ZZRDV1 MODULE
* THE FOLLOWING 3 MESSAGES ARE CONTAINED WITHIN THE ZZRWSU MODULE -
*ZZR174I ** WARNING ** INPUT RECORD IS A SHORT LENGTH RECORD     A38720
*        -DDNAME=          -OUTPUT TTRN=                         A38720
*ZZR175I ** WARNING ** INPUT RECORD IS GREATER THAN OUTPUT       A38720
*        BLKSIZE  -DDNAME=          -OUTPUT TTRN=                A38720
*ZZR176I MEMBER        IN DATASET REFERENCED BY        HAS MORE  A38720
*        THAN ONE NOTELIST POINTER                               A38720
ZZR177I  DC    X'39'              MSG LENGTH
         DC    C'77         WAS SELECTED BUT NOT FOUND IN ANY INPUT DATX
               A SET'
ZZR178I  DC    AL1(77)            MSG LENGTH                          $
         DC    C'78LOAD MODULE XXXXXXXX MARKED ''PAGE ALIGNED''. '    $
         DC    C'IT WAS COPIED TO OUTPUT DATA SET.'                   $
* THE FOLLOWING MESSAGE IS CONTAINED WITHIN MODULE ZZRWSU -      A38720
*ZZR188I MEMBER        IN DATASET REFERENCED BY        HAS       A38720
*        RECORDS GREATER THAN BLKSIZE                            A38720
         DS    0D
         EJECT
ZZRELCOM   DSECT
         ZZRELCOM
         END
