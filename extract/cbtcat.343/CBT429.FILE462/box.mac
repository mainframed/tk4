ISREDIT MACRO (WIDTH,DEPTH)
        IF &STR(&WIDTH) = HELP THEN +
            DO
            ISPEXEC SELECT PGM(ISPTUTOR) PARM(#BOX)
            EXIT
            END

ISREDIT (ROW,COL) = CURSOR
ISPEXEC CONTROL ERRORS RETURN
IF &WIDTH = THEN SET &WIDTH = 20
IF &DEPTH = THEN SET &DEPTH = 10
SET &DASHS = &STR(****************************************)
SET &DASHS = &STR(&DASHS&DASHS)
SET &TOP = &STR(*&SUBSTR(1:&WIDTH-2,&DASHS)*)
SET &MAX = &DEPTH - 2
SET &COUNT = 1
ISREDIT LINE &ROW               = LINE + < &COL '&TOP'>
DO WHILE(&COUNT LE &MAX)
   ISREDIT LINE &EVAL(&ROW+&COUNT) = +
     LINE + < &COL '*' &EVAL(&COL+&WIDTH-1) '*'>
   SET &COUNT = &COUNT + 1
END
ISREDIT LINE &EVAL(&ROW+&COUNT) = LINE + < &COL '&TOP'>
IF &MAXCC > 0 THEN DO
   SET ZEDSMSG = &STR(INCOMPLETE BOX)
   SET ZEDLMSG = &STR(NOT ENOUGH ROOM TO DRAW A COMPLETE BOX)
   ISPEXEC SETMSG MSG(ISRZ000)
END
SET &COL = &COL + 1
SET &ROW = &ROW + 1
ISREDIT CURSOR = (ROW,COL)
EXIT CODE(0)
