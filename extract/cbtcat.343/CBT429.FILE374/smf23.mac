//E0115DMA JOB EDPCSBS1,'SASD',MSGCLASS=X,CLASS=1,NOTIFY=ED115DM
//*
//SAS     EXEC SAS,OPTIONS=NODATE
//DATLIB  DD DSN=SYS1.MAN1,DISP=SHR
//SYSIN   DD *
TITLE SMF 23;
  DATA;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') ^=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  IF SMFXXRTY = 23;
  INPUT SMF23FIL PIB2.   SMF23POF PIB4. SMF23PLN PIB2.  SMF23PON PIB2.
        SMF23SOF PIB4.   SMF23SLN PIB2. SMF23SON PIB2.  SMF23ROF PIB4.
        SMF23RLN PIB2.   SMF23RON PIB2.
        @;
  PUT SMFXXRTY TIMEOUT SMFXXSID SMF23FIL
               SMF23POF SMF23PLN SMF23PON
               SMF23SOF SMF23SLN SMF23SON
               SMF23ROF SMF23RLN SMF23RON
         ;
   IF SMF23PON > 0 THEN DO;
      DO I = 0 TO (SMF23PON - 1);
        OFF = SMF23POF -3 + I*SMF23PLN;
        INPUT @OFF SMF23TID PIB2. SMF23RVN $CHAR2. SMF23PNM $CHAR8.
        @ ;
        PUT SMF23TID +5 SMF23RVN +5 SMF23PNM
         ;
      END;
   END;
   IF SMF23SON > 0 THEN DO;
      DO I = 0 TO (SMF23SON - 1);
        OFF = SMF23SOF -3 + I*SMF23SLN;
        INPUT @OFF SMF23INT $CHAR6. SMF23RLS $CHAR4.
        @ ;
          PUT SMF23INT +5 SMF23RLS
         ;
      END;
   END;
   IF SMF23RON > 0 THEN DO;
      DO I = 0 TO (SMF23RON - 1);
        OFF = SMF23ROF -3 + I*SMF23RLN;
        INPUT @OFF SMF23BFW PIB4.   SMF23BFQ PIB4.   SMF23SUS PIB4.
                   SMF23RCW PIB4.
        @ ;
          PUT SMF23BFW +5   SMF23BFQ +5   SMF23SUS +5
                   SMF23RCW
         ;
      END;
   END;
