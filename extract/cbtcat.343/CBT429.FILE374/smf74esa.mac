//C4SYSDM4 JOB EDPCSBS1,'RMF74',MSGCLASS=X,CLASS=L,NOTIFY=ED115DM
//** ESA FORMAT
//*
//SAS     EXEC SAS,OPTIONS=NODATE
//*ATLIB  DD DSN=C4H1.PROD.SMFDUMPW.HOLD,DISP=SHR
//DATLIB  DD DSN=SYS1.MAN4,DISP=SHR
//SYSIN   DD *
TITLE 'SMF 74';
DATA T74 (KEEP = TIMEOUT SMFXXSID
        SMF74SSI          SMF74STY        SMF74TRN
        SMF74RV1          SMF74PRS        SMF74PRL
        SMF74PRN          SMF74DCS        SMF74DCL
        SMF74DCN          SMF74DDS        SMF74DDL
        SMF74DDN)
     PRODUCT (KEEP = TIMEOUT
             SMF74MFV          SMF74PRD           SMF74IST
             SMF74DAT          SMF74INT           SMF74MFL
             SMF74SAM          SMF74RV2           SMF74FLA
             SMF74RLS          SMF74CYC           SMF74MVS
             SMF74IML          SMF74PRF           SMF74PTN
             SMF74SRL          SMF74EIT
             )
     DCONTROL (KEEP = TIMEOUT
             SMF74NXT          SMF74TOT           SMF74GEN
             SMF74SUB          SMF74DCF           SMF74DMS
             RES1              SMF74S15           SMF74SRC
             SMF74SRS          SMF74TSR           SMF74CFL
             RES2              SMF74TNM           SMF74TSF
             RES3              SMF74TOK           SMF74TDT
             SMF74TTM          RES4               SMF74MCT
             )
     DEVICE   (KEEP = TIMEOUT
             SMF74NUM          SMF74LCU           SMF74RV3
             SMF74CNF          SMF74SER           SMF74TYP
             SMF74NUX          SMF74SSC           SMF74MEC
             SMF74CNN          SMF74PEN           SMF74ATV
             SMF74DIS          SMF74QUE           SMF74UTL
             SMF74RSV          SMF74DSO           SMF74ALC
             SMF74MTP          SMF74NRD           SMF74COF
             SMF74ICT          SMF74DVB           SMF74CUB
             SMF74CLF          RES5               SMF74SGN
             SMF74NDA          SMF74DEV           SMF74CU
             SMF74DPB          SMF74CNX           RES6
             SMF74MTC
             )
          ;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') ^=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  IF SMFXXRTY = 74;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  INPUT SMF74SSI $CHAR4.  SMF74STY PIB2.  SMF74TRN PIB2.
        SMF74RV1 PIB2.    SMF74PRS PIB4.  SMF74PRL PIB2.
        SMF74PRN PIB2. @;
  IF SMF74STY =1 THEN DO;   * SUB TYPE 1;
  INPUT
        SMF74DCS PIB4.  SMF74DCL PIB2.
        SMF74DCN PIB2.    SMF74DDS PIB4.  SMF74DDL PIB2.
        SMF74DDN PIB2. @;
                     END; ELSE
  IF SMF74STY =2 THEN DO;   * SUB TYPE 2;
                     END;
  OUTPUT T74;
  DO I=1 TO SMF74PRN;
  OFF = V -3 + SMF74PRS + (I-1)*SMF74PRL;
  INPUT @OFF SMF74MFV $CHAR2.  SMF74PRD $CHAR8.   SMF74IST PD4.
             SMF74DAT PD4.     SMF74INT RMFDUR.   SMF74MFL PIB2.
             SMF74SAM PIB4.    SMF74RV2 PIB2.     SMF74FLA PIB2.
             SMF74RLS $CHAR4.  SMF74CYC PD4.      SMF74MVS $CHAR8.
             SMF74IML PIB1.    SMF74PRF PIB3.     SMF74PTN PIB1.
             SMF74SRL PIB1.    SMF74EIT $CHAR8.
  @;
  OUTPUT PRODUCT;
  END;
  DO I=1 TO SMF74DCN;
  OFF = V -3 + SMF74DCS + (I-1)*SMF74DCL;
  INPUT @OFF SMF74NXT PIB2.    SMF74TOT PIB2.     SMF74GEN PIB2.
             SMF74SUB PIB2.    SMF74DCF PIB1.     SMF74DMS PIB1.
             RES1     PIB2.    SMF74S15 PIB4.     SMF74SRC PIB4.
             SMF74SRS PIB4.    SMF74TSR PIB2.     SMF74CFL PIB1.
             RES2     PIB3.    SMF74TNM $CHAR44.  SMF74TSF $CHAR2.
             RES3     PIB2.    SMF74TOK $CHAR16.  SMF74TDT $CHAR8.
             SMF74TTM $CHAR8.  RES4     PIB2.     SMF74MCT PIB4.
  @;
  OUTPUT DCONTROL;
  END;
  DO I=1 TO SMF74DDN;
  OFF = V -3 + SMF74DDS + (I-1)*SMF74DDL;
  INPUT @OFF SMF74NUM PIB2.    SMF74LCU PIB2.     SMF74RV3 PIB1.
             SMF74CNF PIB1.    SMF74SER $CHAR6.   SMF74TYP PIB4.
             SMF74NUX PIB4.    SMF74SSC PIB4.     SMF74MEC PIB4.
             SMF74CNN PIB4.    SMF74PEN PIB4.     SMF74ATV PIB4.
             SMF74DIS PIB4.    SMF74QUE PIB4.     SMF74UTL PIB4.
             SMF74RSV PIB4.    SMF74DSO PIB4.     SMF74ALC PIB4.
             SMF74MTP PIB4.    SMF74NRD PIB4.     SMF74COF PIB2.
             SMF74ICT PIB2.    SMF74DVB PIB4.     SMF74CUB PIB4.
             SMF74CLF PIB1.    RES5     PIB3.     SMF74SGN $CHAR8.
             SMF74NDA PIB4.    SMF74DEV $CHAR8.   SMF74CU  $CHAR8.
             SMF74DPB PIB4.    SMF74CNX PIB1.     RES6     PIB1.
             SMF74MTC PIB2.
  @;
  OUTPUT DEVICE;
  END;
RETURN;
PROC PRINT DATA=T74;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PRODUCT;
FORMAT TIMEOUT DATETIME.;
FORMAT SMF74INT TIME.;
PROC PRINT DATA=DCONTROL;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=DEVICE;
FORMAT TIMEOUT DATETIME.;
