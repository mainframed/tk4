         MACRO
         DEBUG &ID=NO,&DATA=NO,&TEST=YES,&MAXLEN=100,&TYPE=WTO,        *
               &EP=EPLOC,&OPEN=NO,&CLOSE=NO,&PRINT=SYSPRINT
.**********************************************************************
.*       DEBUG Macro Version 2
.**********************************************************************
.*
.*       ID=NO × VALUE
.*             Provides a unique ID for a set of DEBUG statements
.*
.*       (Field,Length,Type)
.*             Coded when ID=value, supplies the "Field" name to
.*             display, its "Length" and its "Type".
.*             Default Type is "X" for hexadecimal, else "C" for
.*             character display
.*
.*       DATA=NO × V × C × G
.*             Generates data constants for V(variable storage)
.*             Generates data constants for C(constant storage)
.*             Generates G(global parameters), used with TYPE & MAXLEN
.*
.*       TEST=YES
.*             Only generates code if SYSPARM(TEST) is used
.*
.*       MAXLEN=100
.*             Maximum amount of data that can be displayed in bytes
.*
.*       TYPE=WTO × LOG × PUT × WTL
.*             How the message is to be issued. WTO or ISPF LOG dataset
.*             WTL or PUT to &PRINT (Which must be open)
.*
.*       EP=EPLOC
.*             Provides the ISPLINK entry point for TYPE=LOG
.*
.*       OPEN=NO × YES
.*             Generate and OPEN the &PRINT DCB for TYPE=PUT
.*
.*       PRINT=SYSPRINT × ddname
.*             DD name of the print output DD and label on the DCB mac
.*
.**********************************************************************
         LCLA  &CNT,&L1,&L2,&L3,&L4,&LENGTH,&N,&MAX
         GBLC  &DEBUGTY,&DEBUGOP,&DEBUGDD
         GBLA  &DEBUGML
         AIF   ('&TEST' NE 'YES').SYSP
         AIF   ('&SYSPARM' NE 'TEST').EXIT
.SYSP    ANOP
         AIF   ('&ID' EQ 'NO').DATA
         STM   R14,R13,DEBUGSAV
         MVC   DEBUGWTO,DEBUGCON
         MVC   DEBUGWTO+4(8),=CL8'&SYSECT'
         MVC   DEBUGWTO+13(8),=CL8'&ID'
         LA    R1,DEBUGWTO+23
         AIF   (N'&SYSLIST EQ 0).MESSAGE                            '
&CNT     SETA  1
&LENGTH  SETA  18
.LOOP    ANOP
&N       SETA  N'&SYSLIST                                           '
         AIF   ('&SYSLIST(&CNT,2)' NE '').GETLENG
&L1      SETA  L'&SYSLIST(&CNT,1)                                   '
         AGO   .GOTLENG
.GETLENG ANOP
&L1      SETA  &SYSLIST(&CNT,2)
.GOTLENG ANOP
&L2      SETA  &L1+1
&L3      SETA  &L1*2
&L4      SETA  &L3+1
&L5      SETA  K'&SYSLIST(&CNT,1)+1                                 '
         MVC   0(&L5,R1),=C'&SYSLIST(&CNT,1)='
         LA    R1,&L5.(R1)
&LENGTH  SETA  &LENGTH+&L5
         AIF   ('&SYSLIST(&CNT,3)' EQ 'X').HEX
         AIF   ('&SYSLIST(&CNT,3)' EQ 'C').CHAR
.HEX     ANOP
         UNPK  DEBUGWK(&L4),&SYSLIST(&CNT,1)(&L2)
         TR    DEBUGWK(&L3),DEBUGTRT-240
         MVC   0(&L3,R1),DEBUGWK
         LA    R1,&L4.(R1)
&LENGTH  SETA  &LENGTH+&L4
         AGO   .DONE
.CHAR    ANOP
         MVC   0(&L1,R1),&SYSLIST(&CNT,1)
         LA    R1,&L2.(R1)
&LENGTH  SETA  &LENGTH+&L2
.DONE    ANOP
&CNT     SETA  &CNT+1
         AIF   (&CNT LE N'&SYSLIST).LOOP                            '
.MESSAGE ANOP
         AIF   (&LENGTH LE &DEBUGML).LOGCHK
         MNOTE 8,'DEBUG WTO BUFFER USED &LENGTH OF &DEBUGML BYTES'
         MNOTE 8,'WTO BUFFER TOO SMALL TO CONTAIN &LENGTH BYTES'
         AGO   .EXIT
.LOGCHK  ANOP
         AIF   ('&DEBUGTY' EQ 'LOG').LOG
         AIF   ('&DEBUGTY' EQ 'WTL').WTL1
         AIF   ('&DEBUGTY' EQ 'PUT').PUT
         WTO   MF=(E,DEBUGWTO)
         LM    R14,R1,DEBUGSAV
         AGO   .EXIT
.WTL1    ANOP
         WTL   MF=(E,DEBUGWTO)
         LM    R14,R1,DEBUGSAV
         AGO   .EXIT
.PUT     ANOP
         PUT   &DEBUGDD,DEBUGWTO+4
         LM    R14,R1,DEBUGSAV
         AGO   .EXIT
.LOG     ANOP
         L     R15,&EP                 SET UP FOR CALL
         CALL  (15),(=CL8'VREPLACE',=CL8'ZEDSMSG',=F'18',              *
               DEBUGWTO+4),VL,MF=(E,DEBCALL)
         L     R15,&EP                 SET UP FOR CALL
         CALL  (15),(=CL8'VREPLACE',=CL8'ZEDLMSG',=F'82',              *
               DEBUGWTO+23),VL,MF=(E,DEBCALL)
         L     R15,&EP                 SET UP FOR CALL
         CALL  (15),(=CL8'LOG',=CL8'ISRZ001'),VL,MF=(E,DEBCALL)
         LM    R14,R1,DEBUGSAV
         AGO   .EXIT
.**********************************************************************
.*       Process the various DATA variables
.**********************************************************************
.DATA    ANOP
         AIF   ('&DATA' EQ 'V').VARS
         AIF   ('&DATA' EQ 'C').CONS
         AIF   ('&DATA' EQ 'G').GBLS
         AGO   .EXIT
.**********************************************************************
.*       DEBUG Globals
.**********************************************************************
.GBLS    ANOP
         AIF   ('&CLOSE' EQ 'YES').CLOSE
&DEBUGTY SETC  '&TYPE'
&DEBUGML SETA  &MAXLEN
&DEBUGOP SETC  '&OPEN'
&DEBUGDD SETC  '&PRINT'
         AIF   ('&DEBUGOP' EQ 'NO').EXIT
         XC    DEBUGOPN,DEBUGOPN       Zero the list
         OI    DEBUGOPN,X'80'          Indicate last in the list
         MVC   &DEBUGDD,DEBUGDCB       Copy the DCB below the line
         MVC   DEBUGDC1,DEBUGDCC       Copy the DCBE below the line
         LA    R1,DEBUGDC1             Address of my DCBE
         ST    R1,&DEBUGDD             Saved in my DCB
         OPEN  (&DEBUGDD,(OUTPUT)),MODE=31,MF=(E,DEBUGOPN)
         AGO   .EXIT
.CLOSE   ANOP
         CLOSE &DEBUGDD,MODE=31,MF=(E,DEBUGOPN)
         AGO   .EXIT
.CONS    ANOP
*---------------------------------------------------------------------*
*        DEBUG Constants                                              *
*---------------------------------------------------------------------*
                                       SPACE
         AIF   ('&DEBUGTY' EQ 'WTL').WTL2
         AIF   ('&DEBUGTY' EQ 'PUT').PUT2
DEBUGCON WTO   '&SYSECT                                                X
                                                                     ',X
               ROUTCDE=(11),MF=L
DEBUGLEN EQU   *-DEBUGCON              Length of WTO params
         AGO   .CONS2
.WTL2    ANOP
DEBUGCON WTL   '&SYSECT                                                X
                                                                     ',X
               MF=L
DEBUGLEN EQU   *-DEBUGCON              Length of WTO params
         AGO   .CONS2
.PUT2    ANOP
&MAX     SETA  &DEBUGML+4
DEBUGCON DS    0F
         DC    CL&MAX' '
DEBUGLEN EQU   *-DEBUGCON              Length of WTO params
.CONS2   ANOP
DEBUGTRT DC    C'0123456789ABCDEF'     Debug Translate Table
         AIF   ('&DEBUGOP' EQ 'NO').EXIT
DEBUGDCB DCB   DDNAME=&DEBUGDD,DSORG=PS,MACRF=PM,RECFM=FB,             *
               DCBE=DEBUGDCC,LRECL=&DEBUGML
DEBUGDCL EQU   *-DEBUGDCB              Length of the DCB
DEBUGDCC DCBE  RMODE31=BUFF
DEBUGDEL EQU   *-DEBUGDCC              Length of the DCBE
         AGO   .EXIT
.VARS    ANOP
*---------------------------------------------------------------------*
*        DEBUG Variables                                              *
*---------------------------------------------------------------------*
                                       SPACE
DEBUGSAV DS    0F                      Register Saver Area
REG14    DC    F'0'                    Save Area for Register 14
REG15    DC    F'0'                    Save Area for Register 15
RC       EQU   REG15+3                 Low order byte of R15
REG00    DC    F'0'                    Save Area for Register 00
REG01    DC    F'0'                    Save Area for Register 01
REG02    DC    F'0'                    Save Area for Register 02
REG03    DC    F'0'                    Save Area for Register 03
REG04    DC    F'0'                    Save Area for Register 04
REG05    DC    F'0'                    Save Area for Register 05
REG06    DC    F'0'                    Save Area for Register 06
REG07    DC    F'0'                    Save Area for Register 07
REG08    DC    F'0'                    Save Area for Register 08
REG09    DC    F'0'                    Save Area for Register 09
REG10    DC    F'0'                    Save Area for Register 10
REG11    DC    F'0'                    Save Area for Register 11
REG12    DC    F'0'                    Save Area for Register 12
REG13    DC    F'0'                    Save Area for Register 13
DEBCALL  DC    5F'0'                   Call Parameter List
DEBUGWK  DC    CL9' '                  Debug Work Area
         DS    0F
DEBUGWTO DC    CL(DEBUGLEN)' '         Changeable WTO params
         DC    D'0'
         AIF   ('&DEBUGOP' EQ 'NO').EXIT
DEBUGOPN DS    D                       Open Exit List
&DEBUGDD DS    CL(DEBUGDCL)            Storage for my DCB
         DS    0F
DEBUGDC1 DS    CL(DEBUGDEL)            Storage for my DCBE
                                       SPACE
.EXIT    ANOP
         MEND
