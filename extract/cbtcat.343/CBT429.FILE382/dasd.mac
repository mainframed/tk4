++ USERMOD(L#FM093) REWORK(1997258)
/*
*
*     Elements:    DASD
*
*     Purpose:     Support the DASD space command
*
*     Reference:   None
*
*     Environment: Zone:  MVS01T   Product:  Inhouse Code
*
*     WARNING:     None
*
*     Writer:      Paul Gillis
*
*     Date:        29/09/97
*
*     Change       Date      Inits  Description
*     History:     ========  =====  ===========
*
*                  29/09/97  PGG    Code redesigned for 4 digit UCB
*
*/ .
++VER(Z038) FMID(LBASE01).
++JCLIN.
//LINK1  EXEC   PGM=HEWL,PARM='LIST,XREF,MAP'
//SYSPRINT  DD SYSOUT=A
//AUSERMOD  DD DSN=SYS1.AUSERMOD,DISP=SHR
//SYSLMOD   DD DSN=MVS01T.LINKLIB,DISP=SHR
//SYSUT1    DD UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLIN    DD *
 INCLUDE AUSERMOD(DASD)
 NAME DASD(R)
++SRC(DASD) DISTLIB(AUSERSRC) DISTMOD(AUSERMOD).
DASD     CSECT
DASD     AMODE 24
DASD     RMODE 24
                                       SPACE 1
***********************************************************************
*                                                                     *
* FUNCTION : THE PROGRAM DASD, ISSUES THE LSPACE SVC FOR EVERY DASD   *
*            DEVICE IN THE SYSTEM AND OUTPUTS THE RESULT TO A FILE    *
*            THE DATA OUTPUT TO THE FILE INCLUDES THE FORMAT 4 DSCB   *
*                                                                     *
* INPUTS   : NONE                                                     *
*                                                                     *
* OUTPUTS  : DDNAME=DASDDATA, NO DCB PARAMETERS REQUIRED, ONE RECORD  *
*            FOR EVERY ONLINE DASD DEVICE. RECORD LAYOUT DESCRIBED AT *
*            MSGLINE TO MSGLENG2.                                     *
*                                                                     *
* ATTRIBUTES : NONE                                                   *
*                                                                     *
* AUTHOR   : P.GILLIS, APRIL 1991                                     *
*                                                                     *
* CHANGES  : P.GILLIS, SEPTEMBER 1997 - SUPPORT 4 DIGIT UCBS          *
*                                                                     *
***********************************************************************
                                       SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
                                       SPACE 1
***********************************************************************
*        INITIALISATION - PROGRAM                                     *
***********************************************************************
                                       SPACE 1
         SAVE  (14,12),,'DASD &SYSDATE &SYSTIME'
         LR    R12,R15                 LOAD BASE REGISTER
         USING DASD,R12
         USING CVTMAP,R11
         USING UCBCMSEG,R10
         LA    R3,SAVEAREA             LOAD ADDR OF MY REG SAVE AREA
         ST    R13,4(R3)               STORE ADDR OF CLG SAVE IN MINE
         ST    R3,8(R13)               STORE ADDR OF MY SAVE IN CLG PGM
         LR    R13,R3                  POINT REG 13 AT MY SAVE AREA
                                       SPACE 1
***********************************************************************
*        INITIALISATION - OPEN FILE , IF NOT PRESENT WE USE TPUTS     *
***********************************************************************
                                       SPACE 1
         MVI   OPENFLAG,0              INDICATE THE DDNAME IS OPEN
         OI    OPENWORD,X'80'          INDICATE ONLY ONE WORD IN LIST
         OPEN  (DASDDATA,(OUTPUT)),MF=(E,OPENWORD)
         TM    DASDDATA+DCBOFLGS-IHADCB,DCBOFOPN OPEN O.K.
         BO    OPENOK                  ===> YES
         MVI   OPENFLAG,1              INDICATE THE DDNAME IS NOT OPEN
OPENOK   DS    0H
         TIME  DEC
         STM   R0,R1,TIMESTMP          SAVE THE TIME STAMP
                                       SPACE 1
***********************************************************************
*        INITIALISATION - UCB SCAN ROUTINE                            *
***********************************************************************
                                       SPACE 1
         L     R11,CVTPTR              A(CVT)
         LA    R15,SCANWORK            A(IOSVSUCB WORK AREA)
         LA    R0,DEVCLASS             A(DEVICE CLASS)
         LA    R1,ADDRUCB              A(A(UCB))
         STM   R15,R1,PARMWA           SAVE THE IOSVSUCB PARAMETERS
         OI    PARMUCB,X'80'           INDICATE LAST ONE
         MVI   DEVCLASS,UCB3DACC       ONLY DASD DEVICES
                                       SPACE 1
***********************************************************************
*        LOOP THROUGH THE UCB CHAIN                                   *
***********************************************************************
                                       SPACE 1
         BAL   R9,SCAN1                GET FIRST UCB ADDRESS
         MVC   FIRSTUCB,UCBNAME        SAVE THE FIRST UCB NAME
         LTR   R15,R15                 TEST THE RETURN CODE
         BNZ   UCBLOOP2                ===> NO DASD UCBS
UCBLOOP1 DS    0H
         BAL   R9,UCBMSG               PROCESS A DASD UCB
         BAL   R9,SCAN2                GET THE NEXT UCB ADDRESS
         LTR   R15,R15                 TEST THE RETURN CODE
         BNZ   UCBLOOP2                ===> NO MORE DASD UCBS
         CLC   FIRSTUCB,UCBNAME        SAME AS THE FIRST UCB NAME
         BNE   UCBLOOP1                ===> NO, PROCESS IT
UCBLOOP2 DS    0H
                                       SPACE 1
***********************************************************************
*        TERMINATE PROGRAM                                            *
***********************************************************************
                                       SPACE 1
         CLOSE ,MF=(E,OPENWORD)
         XR    R15,R15                 SET RC=0
         L     R13,4(R13)              RESTORE CALLERS SAVE AREA
         RETURN (14,12),,RC=(15)       RETURN TO CALLER
                                       SPACE 1
***********************************************************************
*        SUBROUTINE - EXTRACT THE NEXT UCB ADDRESS                    *
***********************************************************************
                                       SPACE 1
SCAN1    DS    0H
         IOCINFO IOCTOKEN=TOKEN        GET CURRENT IOCTOKEN
         XC    SCANWORK,SCANWORK       CLEAR THE WORK AREA
SCAN2    DS    0H
         UCBSCAN UCBAREA=UCBCOPY,WORKAREA=SCANWORK,DYNAMIC=YES,        *
               RANGE=ALL,IOCTOKEN=TOKEN,DEVCLASS=DASD,DCEAREA=DCEAREA, *
               DCELEN=48
         LTR   R15,R15                 TEST THE RETURN CODE
         LA    R10,UCBCOPY             A(UCB)
         BR    R9                      ===> RETURN TO CALLER
                                       SPACE 1
***********************************************************************
*        SUBROUTINE - PROCESS THE DASD UCB                            *
***********************************************************************
                                       SPACE 1
UCBMSG   DS    0H
         MVI   MSGLINE,C' '            CLEAR THE OUTPUT LINE
         MVC   MSGLINE+1(MSGLENG1-1),MSGLINE
*        MVC   MSGUCB,UCBNAME          SAVE THE UCB ADDRESS
         UNPK  MSGUCB(5),UCBCHAN(3)    GET THE 4 DIGIT DEVICE ADDRESS
         TR    MSGUCB(4),TRTABLE-240   CONVERT TO EBCDIC
                                       SPACE 1
         EDTINFO RTNUNIT,DEVTYPE=UCBTYP,OUTUNIT=MSGDEVT
         CLI   UCBTYP+3,X'0E'          DEVICE EXTENSION PRESENT ?
         BL    UCBMSG3                 * NO, USE RESULT OF EDTINFO
         LA    R14,DCEAREA             R14 -> DEVICE CLASS EXTENSION
         USING DCE,R14
         LA    R15,TABLE2              R14 -> UNIT TABLE #2.
                                       SPACE 1
DEVLOOP  DS    0H
         CLI   0(R15),X'FF'            END OF TABLE
         BE    UCBMSG3                 ---> YES
         CLC   DCEOBRDT,8(R15)         IS THIS THE DEVICE
         BE    DEVINDEX                YES, INSERT INTO MESSAGE
         LA    R15,TABLE2LN(R15)       INCREMENT TABLE
         B     DEVLOOP                 LOOP THROUGH DEVICES
                                       SPACE 1
DEVINDEX DS    0H
         MVC   MSGDEVT,0(R15)          COPY THE REAL DEVICE TYPE IN
UCBMSG3  DS    0H
                                       SPACE 1
         TM    UCBVOLI,X'FF'           IS A VOLUME ON THIS ONE
         BZ    UCBMSG99                ===> NO, SKIP IT
         TM    UCBSTAT,UCBONLI         IS THE DEVICE ONLINE
         BZ    UCBMSG99                ===> NO, SKIP IT
         MVC   MSGVOL,UCBVOLI          SAVE THE VOLUME SERIAL
         MVC   MSGSHR,=C'EXC'          EXCLUSIVE USE
         TM    UCBTBYT2,UCBRR          IS IT SHARED
         BZ    UCBMSG02                ===> NO, ITS EXCLUSIVE
         MVC   MSGSHR,=C'SHR'          SHARED USE
UCBMSG02 DS    0H
                                       SPACE 1
         MVC   MSGSTAT1,=C'STORAGE'    MAKE IT STORAGE
         TM    UCBSTAB,UCBBSTR         IS IT STORAGE
         BO    UCBMSG03                ===> YES
         MVC   MSGSTAT1,=C'PUBLIC '    MAKE IT PUBLIC
         TM    UCBSTAB,UCBBPUB         IS IT PUBLIC
         BO    UCBMSG03                ===> YES
         MVC   MSGSTAT1,=C'PRIVATE'    MAKE IT PRIVATE
         TM    UCBSTAB,UCBBPRV         IS IT PRIVATE
         BO    UCBMSG03                ===> YES
         MVC   MSGSTAT1,=C'??????????' MAKE IT UNKNOWN
UCBMSG03 DS    0H
                                       SPACE 1
         TM    UCBSTAT,UCBALOC         IS IT ALLOCATED
         BZ    UCBMSG04                ===> NO
         MVC   MSGSTAT2,=C'ALLOC '     MAKE IT ALLOCATED
UCBMSG04 DS    0H
         TM    UCBSTAT,UCBSYSR         IS IT SYSRES
         BZ    UCBMSG05                ===> NO
         MVC   MSGSTAT2,=C'SYSRES'     MAKE IT SYSRES
UCBMSG05 DS    0H
         TM    UCBFLA,UCBNRY           IS IT READY
         BZ    UCBMSG06                ===> YES
         MVC   MSGSTAT2,=C',READY'     MAKE IT ,READY
UCBMSG06 DS    0H
         TM    UCBSTAT,UCBPGFL         IS IT PAGING
         BZ    UCBMSG07                ===> NO
         MVC   MSGSTAT2,=C'PAGING'     MAKE IT PAGING
UCBMSG07 DS    0H
         CLI   UCBSQC,0                IS IT RESERVED
         BE    UCBMSG08                ===> NO
         MVC   MSGSTAT2,=C'RESRVD'     MAKE IT RESERVED
         B     UCBMSG09                ===> DON'T ISSUE LSPACE SVC
UCBMSG08 DS    0H
                                       SPACE 1
         LA    R6,LSPACF4D             A(F4DSCB RETURNED BY LSPACE)
         USING IECSDSL4,R6
         LA    R7,LSPACDAT             A(DATA RETURNED BY LSPACE)
         USING LSPDRETN,R7
         LA    R8,LSPACLST             A(LSPACE PARAMETER LIST)
         USING LSPAPLID,R8
         XC    LSPAPLID(LSPALEN),LSPAPLID ZERO THE CONTROL BLOCK
         MVC   LSPAPLID,=C'LSPA'       CONTROL BLOCK ID
         MVC   LSPANGTH,=AL2(LSPALEN)  CONTROL BLOCK LENGTH
         LSPACE UCB=(R10),DATA=(R7),F4DSCB=(R6),MF=(E,(R8))
UCBMSG09 DS    0H
                                       SPACE 1
         CLI   OPENFLAG,0              IS THE DDNAME OPEN
         BNE   UCBMSG90                ===> NO, USE THE TPUT MACRO
         PUT   DASDDATA,MSGLINE        OUTPUT A RECORD
         B     UCBMSG99                ===> TERMINATE SUBROUTINE
                                       SPACE 1
UCBMSG90 DS    0H
         LA    R0,MSGLENG1             L'MESSAGE
         LA    R1,MSGLINE              A(MESSAGE)
         TPUT  (1),(0),R               OUTPUT THE MESSAGE TO TSO USER
                                       SPACE 1
UCBMSG99 DS    0H
         BR    R9                      ===> RETURN TO CALLER
                                       SPACE 1
***********************************************************************
*        CONSTANTS AND LITERALS                                       *
***********************************************************************
                                       SPACE 1
TRTABLE  DC    C'0123456789ABCDEF'     HEX/EBCDIC TRANSLATE TABLE
         LTORG ,
         DS    0D                      DOUBLE WORD ALIGNEMENT
                                       SPACE 1
***********************************************************************
*        VARIABLES                                                    *
***********************************************************************
                                       SPACE 1
SAVEAREA DS    18F                     STANDARD SAVE AREA
                                       SPACE 1
IEFEPARM DS    2A                      PARM LIST FOR IEFEB4UV
IEFEFLAG DS    A                       FLAGS
IEFEUNIT DS    2A                      UNIT TABLE
IEFEUNI3 DS    A                       UNIT TABLE
IEFEUNI4 DS    A                       UNIT TABLE
IEFEUNI5 DS    A                       UNIT TABLE
IEFEATTR DS    A                       ATTRIBUTE AREA
IEFEATT2 DS    A                       ATTRIBUTE AREA
IEFEATT3 DS    A                       ATTRIBUTE AREA
IEFELENG EQU   *-IEFEPARM              LENGTH OF IEFEB4UV PARAMETERS
                                       SPACE 1
OPENWORD DS    A                       OPEN WORD FOR DCB
                                       SPACE 1
PARMWA   DS    A                       A(UCB SCAN WORK AREA)
PARMDEVT DS    A                       A(DEVICE TYPE)
PARMUCB  DS    A                       A(A(UCB))
ADDRUCB  DS    A                       A(UCB)
SCANWORK DS    CL100                   UCB SCAN WORK AREA
DEVCLASS DS    X                       DEVICE TYPE
                                       SPACE 1
OPENFLAG DS    X                       DCB OPEN INDICATOR
FIRSTUCB DS    CL3                     FIRST UCB NAME
                                       SPACE 1
MSGLINE  DS    0F                      MSG LINE FOR TPUT ETC.
MSGUCB   DS    CL4                     UNIT ADDRESS
MSGDEVT  DS    CL8                     DEVICE TYPE
         DS    C
MSGVOL   DS    CL6                     VOLUME SERIAL
         DS    C
MSGSHR   DS    CL3                     SHARED OR EXCLUSIVE
         DS    C
MSGSTAT1 DS    CL7                     STORAGE, PUBLIC OR PRIVATE
         DS    C
MSGSTAT2 DS    CL6                     ALLOC SYSRES ,READY PAGING RSVD
MSGLENG1 EQU   *-MSGLINE               MSG LINE LENGTH
                                       SPACE 1
LSPACDAT DS    XL36                    DATA RETURNED BY LSPACE
LSPACF4D DS    XL96                    F4DSCB RETURNED BY LSPACE
TIMESTMP DS    2F                      TIME STAMP
                                       SPACE 1
MSGLENG2 EQU   *-MSGLINE               LOGICAL RECORD LENGTH
BLKSIZE  EQU   (23476/MSGLENG2)*MSGLENG2 BLOCK SIZE
                                       SPACE 1
         DS    0F                      ALIGNEMENT FOR PARAMETER LIST
LSPACLST DS    CL(LSPALEN)             LSPACE PARAMETER LIST
                                       SPACE 1
         PRINT NOGEN
         DS    0F
DASDDATA DCB   DDNAME=DASDDATA,MACRF=PM,RECFM=FB,LRECL=MSGLENG2,       *
               BLKSIZE=BLKSIZE,DSORG=PS
                                       SPACE 1
TOKEN    DC    CL48' '
UCBCOPY  DC    CL48' '
DCEAREA  DC    CL48' '
                                       SPACE 1
TABLE2   DC    CL8'3380-A',X'0E'
TABLE2LN EQU   *-TABLE2
         DC    CL8'3380-D',X'1E'
         DC    CL8'3380-J',X'21'
         DC    CL8'3380-K',X'23'
         DC    CL8'3390-1',X'26'
         DC    CL8'3390-2',X'27'
         DC    CL8'3390-3',X'24'
         DC    CL8'3380-E',X'2E'
         DC    CL8'3390-9',X'32'
         DC    X'FF'
                                       SPACE 1
         DS    0D                      DOUBLE WORD ALIGNEMENT
                                       SPACE 1
***********************************************************************
*        DATA AREA MAPPING MACROS                                     *
***********************************************************************
                                       SPACE 1
         PRINT GEN
                                       SPACE 1
         LSPACE MF=(D,DATA)
                                       SPACE 1
         IECSDSL1 (4)
                                       SPACE 1
         PRINT NOGEN
         LSPACE MF=D
UCBSPACE DSECT
         IEFUCBOB
         CVT   DSECT=YES,LIST=YES
         DCBD  DEVD=DA,DSORG=PS
         IECDDCE
         END
