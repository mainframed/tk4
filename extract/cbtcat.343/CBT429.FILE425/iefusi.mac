         TITLE 'IEFUSI - SMF Step Initiation Exit'
***********************************************************************
*                                                                     *
*             Module Name = IEFUSI                                    *
*                                                                     *
*             Descriptive Name = SMF Step Initiation Exit             *
*                                                                     *
*             Function =                                              *
*                Exit allows installation to control job step         *
*                region size.  A minimum of 4096K will be set if      *
*                anything less has been specified.  Region sizes in   *
*                in excess of 256M will be subject to a RACF check.   *
*                All region requests will have a default increment    *
*                added below the line to set the region limit.        *
*                                                                     *
*                The value of the increment used in this exit is the  *
*                difference between the region limit and the region   *
*                size (below the line) and is set to 128K, i.e. the   *
*                region limit = the region size + 128K                *
*                                                                     *
*                Operation =                                          *
*                   Save registers and establish addressability       *
*                   If subsystem is OMVS, get out without changing    *
*                   anything.                                         *
*                   If region request is <= 4096K, set region         *
*                       size to 4096K below and 32M above             *
*                       Restore registers                             *
*                       Exit                                          *
*                   Get some storage to work in                       *
*                   If region request is = 0 then set the region to   *
*                       the maximum available below and to 32M above  *
*                   If region request is > 4096K and <= the maximum   *
*                       size available below the line, set region     *
*                       size to that requested below and 32M above    *
*                   If region request is > maximum available below    *
*                       the line and <= 32M, set the region to the    *
*                       maximum available below and to 32M above      *
*                   If region request is > 32M and <= 256M, then      *
*                       set the region to the maximum available below *
*                       and to the size requested above               *
*                   If region request is > 256M, issue a RACROUTE for *
*                       read access to RESOURCE=LARGEREG in           *
*                       CLASS DYN$APPL                                *
*                       If user has access then set the region to the *
*                           maximum available below and to the size   *
*                           requested above                           *
*                       otherwise cancel the job                      *
*                   Free the storage                                  *
*                   Restore registers                                 *
*                   Exit                                              *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*              Notes =                                                *
*                                                                     *
*                 Register conventions = standard conventions.        *
*                    Register  0       = not used                     *
*                    Register  1       = Parameter list pointer       *
*                    Register  2       = Work Reg / addr of CVT, GDA  *
*                    Register  3       = Work Reg                     *
*                    Register  4       = Work Reg / VSPL addressabilty*
*                    Register  5       = Work Reg                     *
*                    Registers 6       = not used                     *
*                    Registers 7       = Work Reg                     *
*                    Register  8       = Work Reg / SIPL addressabilty*
*                    Registers 9 - 11  = not used                     *
*                    Register  12      = Base Register                *
*                    Register  13      = Save area pointer            *
*                    Register  14      = Return                       *
*                    Register  15      = Entry point address and      *
*                                        Return code Register         *
*                                                                     *
*             Macros =                                                *
*                SAVE, RETURN, EQUATE, RACROUTE, WTO, STORAGE         *
*                                                                     *
*             Mapping macros used =                                   *
*                CVT, IHAGDA                                          *
*                                                                     *
*             Assembler options = LIST,RENT                           *
*                                                                     *
*             Binder options = LET,LIST,XREF,RENT,REUS,REFR,AC=1      *
*                                                                     *
*                                                                     *
*             Change activity =                                       *
*                06/27/2000  Written.                           MJL.  *
*                                                                     *
***********************************************************************
***********************************************************************
*        Initialization
***********************************************************************
IEFUSI   CSECT ,                       SMF Step Initiation Exit
IEFUSI   AMODE 31
IEFUSI   RMODE ANY
         SAVE  (14,12),,IEFUSI*FAIR*ISAAC*&SYSDATE*&SYSTIME
*                                      Save Registers
         LR    R12,R15                 R12 - Base Register
         USING IEFUSI,R12              Establish addressability
         LR    R8,R1                   Preserve ptr to parms
         USING SIPLBEGN,R8             Establish addressability to SIPL
         SR    R15,R15                 Set default return code = 0
*
*        See what we can do before we get any storage
*
         L     R4,SIPLSSN              Address of the subsytem name
         CLC   0(R4),OMVS              Is it OMVS?
         BE    RETURNQ                 Yes, quick return
         L     R4,SIPLVSM              Address of the VSM parm list
         USING VSPLBEGN,R4
         L     R5,VSPLREGR             Region requested
         LTR   R5,R5                   Zero?
         BZ    GETSTOR                 No, go check further
         C     R5,DFTREGSZ             Compare to installation default
         BH    GETSTOR                 High?  Go check further
*
*        We now know that we are dealing with a region request of
*        less than or equal to the JES default setting of 4096K, so
*        we will set the region to 4096K below and 32M above.
*
         L     R5,DFTREGSZ             Give them the default
         ST    R5,VSPLREGB             Region size...
         A     R5,DFTINCR              Add the LSQA safety factor and
         ST    R5,VSPLGETB             Store it as the region limit
         MVC   VSPLREGA,DFTABOVE       Store default region above 16M
         MVC   VSPLGETA,DFTABOVE       Store default limit above 16M
         OI    VSPLFLAG,X'C0'          Indicate that USI has set values
*                                      and that the system should not
*                                      check for the availability of
*                                      the requested region size.
         B     RETURNQ                 Get out quick.
         DROP  R4
GETSTOR  EQU   *
         LA    R4,WORKLEN
         STORAGE OBTAIN,LENGTH=(R4),LOC=ANY
*                                      GETMAIN dynamic storage
         ST    R13,4(R1)               Store address of prev savearea
*                                      in current savearea
         ST    R1,8(R13)               Store address of current
*                                      savearea in prev savearea
         LR    R13,R1                  R13 = Address of GETMAINed area
         USING WORKAREA,R13            Establish addressability to
*                                      workarea
*
*        This code obtains the value for the maximum region available
*        below the line
*
         L     R2,16                   Address of CVT
         USING CVT,R2
         L     R2,CVTGDA               Address of Global Data Area
         DROP  R2
         USING GDA,R2
         L     R2,GDAPVTSZ             Max storage size below 16M
         DROP  R2
         ST    R2,MAXBELOW             Save it for region limit
         S     R2,DFTINCR              Save room for LSQA
         ST    R2,MAXREGSZ             Save it for region size
*
***********************************************************************
*        Requested region size checking
***********************************************************************
CHKREGN  DS    0H
         L     R4,SIPLVSM              R4 = address if VSM parm list
         USING VSPLBEGN,R4             Establish addressability to
*                                      VSM parameter list DSECT
         OI    VSPLFLAG,X'C0'          Turn on bit 0 TO indicate that
*                                      IEFUSI has set region values
*                                      and bit 1 to indicate that the
*                                      system should not check for the
*                                      availability of the requested
*                                      region size.
*
*        We are checking for REGION=0
*        If found, the region will be set to the maximum available
*        below and to 32M above.
*
         L     R5,VSPLREGR             Geg the requested region size
         LTR   R5,R5                   Check for REGION=0
         BZ    LOADMAX1                Go load default maximums
*
*        We are checking for region requests > the max allowed (256M)
*        If found, a RACROUTE will be issued and either the job will
*        be cancelled or the region will be set to the max available
*        below and to the size requested above the line
*
         C     R5,MAXREQST             Check for > max allowed
         BH    CHEKRACF                Check if RACF will allow
*
*        We are checking for requests <= the maximum available below
*        If found, the region will be set to what was requested below
*        and to 32M above
*
         C     R5,MAXREGSZ             Check to see if req is below
         BNH   LOADREQ1                Go load what was requested
*
*        We are checking for requests <= the default above (32M)
*        If found, the region will be set to the maximum available
*        below and to 32M above
*
         C     R5,DFTABOVE             Check to see if req <= 32M
         BNH   LOADMAX1                Give them max defaults
*
*        The last possiblity is that the request is for an amount
*        > the default above (32M) and <= the installation defined
*        maximum above (256M).  The region will be set to the maximum
*        available below, and to what was requested above
*
         B     LOADREQ2                Must be between 32M and 256M
LOADREQ1 EQU   *
         ST    R5,VSPLREGB             Load requested region below
         A     R5,DFTINCR              Add for LSQA
         ST    R5,VSPLGETB             Load adjusted limit below
         MVC   VSPLGETA,DFTABOVE       Load default limit above
         MVC   VSPLREGA,DFTABOVE       Load default region avove
         B     FINISH
LOADMAX1 EQU   *
         MVC   VSPLGETB,MAXBELOW       Load max limit below
         MVC   VSPLREGB,MAXREGSZ       Load max region below
         MVC   VSPLGETA,DFTABOVE       Load default limit above
         MVC   VSPLREGA,DFTABOVE       Load default region above
         B     FINISH
CHEKRACF EQU   *
         MVC   RACDYN(RACLEN),RACSTAT  Initialize the macro list form
         LA    R7,2                    Set check for read access
         LA    R3,RESOURCE             Point to resource name
         LA    R4,DYNAPPL              Point to class
         RACROUTE ENTITYX=((R3)),CLASS=(R4),LOG=ASIS,REQUEST=AUTH,     +
               WORKA=RACWK,ATTR=(R7),RELEASE=2608,MF=(E,RACDYN)
         LTR   R15,R15                 Check return code.  If not zero
         BNZ   CANJOB                  Cancel job and issue message
LOADREQ2 EQU   *
         L     R4,SIPLVSM              R4 = address of VSM parm list
         MVC   VSPLGETB,MAXBELOW       Load max limit below
         MVC   VSPLREGB,MAXREGSZ       Load max region below
         MVC   VSPLGETA,VSPLREGR       Load requested limit above
         MVC   VSPLREGA,VSPLREGR       Load requested region above
         B     FINISH
***********************************************************************
*        Wrap things up and return
***********************************************************************
CANJOB   EQU   *
         WTO   'JOB CANCELED BY SMF EXIT - IEFUSI.  USER NOT AUTHORIZED+
                BY RACF TO USE LARGE REGION SIZES.',                   +
               ROUTCDE=(11)
         LA    R15,4                   Set return code to 4 to cancel
FINISH   DS    0H
         LR    R4,R15                  R4 - Save the return code
         LR    R7,R13                  R7 - Save address of WORKAREA
         L     R13,4(R13)              Restore Register 13
         LA    R8,WORKLEN              R8 - Length of WORKAREA
         STORAGE RELEASE,ADDR=(R7),LENGTH=(R8)
*                                      Free the dynamic storage
         LR    R15,R4                  R15 - Restore the return code
RETURNQ  EQU   *                       Quick return
         RETURN (14,12),,RC=(15)       Return
***********************************************************************
*        CONSTANTS
***********************************************************************
DFTINCR  DC    A(128*1024)             Default cushion for LSQA, etc.
DFTREGSZ DC    A(4096*1024)            Default region size below 16M
DFTABOVE DC    A(32*1024*1024)         Default region size above 16M
MAXREQST DC    A(256*1024*1024)        Max request size without RACF
OMVS     DC    CL4'OMVS'               OMVS Subsystem name
***********************************************************************
*        Initialization data for portion of GETMAINed area
***********************************************************************
RACSTAT  RACROUTE REQUEST=AUTH,MF=L    Static version of RACROUTE list
RACLEN   EQU   *-RACSTAT               Length of static version
RESOURCE DS    0F
         DC    AL2(0)                  Resource name for ENTITYX format
         DC    AL2(8)                  checking
         DC    CL8'LARGEREG'
DYNAPPL  DS    0F                      Class name
         DC    AL1(8)
         DC    CL8'DYN$APPL'
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
         LTORG
         EQUATE
***********************************************************************
*        IEFUSI input parameter list
***********************************************************************
SIPLBEGN DSECT
SIPLCEPL DS    F                       Addr - Common SMF exit parm addr
SIPLSTEP DS    F                       Addr - 8 byte job step name
SIPLPGM  DS    F                       Addr - 8 byte program name
SIPLACCT DS    F                       Addr - job step accting info
SIPLVSM  DS    F                       Addr - VSM parameter list
SIPLSRM  DS    F                       Addr - performance group number
*                                             and V=R flag
SIPLDSP  DS    F                       Addr - Dataspace, Hiperspace,
*                                             And data sharing defaults
SIPLSSN  DS    F                       Addr - Subsystem name
***********************************************************************
*        VSM parameter list
***********************************************************************
VSPLBEGN DSECT
VSPLFLAG DS    F                       VSM flag word contains:
*
*              BIT 0  ---------------  On - IEFUSI set region values
*                                      Off - region values not set
*              BIT 1  ---------------  On - VSM not to check for avail-
*                                           ability of requested free
*                                           space below 16 Meg line
*                                      Off - VSM is to check for avail-
*                                           ability of requested free
*                                           space below 16 Meg line
*              BIT 2  ---------------  On - VSM not to check for avail-
*                                           ability of requested free
*                                           space above 16 Meg line
*                                      Off - VSM is to check for avail-
*                                           ability of requested free
*                                           space above 16 Meg line
*              BITS 3-31  -----------  Reserved
*
VSPLREGR DS    F                       Region size requested
*
*                                      On entry to IEFUSI the following
*                                      4 words are set to X'FFFFFFFF'
*
VSPLGETB DS    F                       Getmain linit value below 16 Meg
VSPLREGB DS    F                       Region size below 16 Meg
VSPLGETA DS    F                       Getmain limit value above 16 Meg
VSPLREGA DS    F                       Region size above 16 Meg
*
DSPBEGN  DSECT                         Default values for:
DSPDFTSZ DS    F                       Data and Hiper space size in
*                                      Blocks of 4K.  IBM Dft = 956K
*                                      X'000000EF' * 4K
DSPMAXSZ DS    F                       Maximum combined size for all
*                                      user key Data and Hiper spaces.
*                                      IBM default is (2**24)-1 MB.
DSPMAXNO DS    F                       Maximum number of user key Data
*                                      and Hiper spaces.  IBM default
*                                      is (2**31)-1.
DSPMAXSH DS    F                       Maximum number of source and
*                                      target shafed pages usable at
*                                      one time by problem state users
*                                      IBM default is 32.
*
IEFUSI   CSECT
***********************************************************************
*        Dynamic storage area
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     IEFUSI save area
RACWK    DS    CL512                   RACF work area
RACDYN   DS    CL(RACLEN)              Dynamic ver RACHECK list form
MAXBELOW DS    F                       Maximum storage available < 16M
MAXREGSZ DS    F                       Maximum region available < 16M
WORKLEN  EQU   *-WORKAREA
         PRINT NOGEN
         CVT   DSECT=YES
         IHAGDA
         END   IEFUSI
