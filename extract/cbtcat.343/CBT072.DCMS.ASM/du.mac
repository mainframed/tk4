DU       TITLE 'D U - DISPLAY UNITS COMMAND FROM TSO/DCMS'
         SPACE 4
         MACRO
         DX    &HEXTYP,&CHAR
         DC    X'&HEXTYP'
         DC    CL7&CHAR
         MEND
         EJECT
         SPACE 3
*.
* DU  -
*   DISPLAY UNITS COMMAND FROM TSO/DCMS
*   ALL SYSGENNED UNITS ARE DISPLAYED STARTING FROM THE WORD ZERO
*
* PF KEY DEFINITIONS -
*
*    -----
*   × PF4 ×   DISPLAY ALL SYSGENNED UNITS
*   ×     ×
*    -----
*
*    -----
*   × PF5 ×   DISPLAY ONLY ONLINE UNITS
*   ×     ×
*    -----
*
*    -----
*   × PF7 ×   SCROLL UP 20 UNITS
*   ×     ×
*    -----
*
*    -----
*   × PF8 ×   SCROLL DOWN 20 UNITS
*   ×     ×
*    -----
*
*
* REGISTER USAGE
*
*    R2 - POINTER TO CURRENT UCB LOOKUP TABLE ENTRY
*    R3 - POINTER TO BEGINNING OF UCB LOOKUP TABLE
*    R4 - POINTER TO CURRENT SCREEN LINE ENTRY
*    R5 - POINTER TO THE CURRENT UCB
*    R9 - SUBROUTINE LINKAGE REGISTER
*   R12 - BASE
*
*  THIS PROGRAM DOES NOT ATTEMPT TO FIND THE 'OPTCHAN' UNITS SO YOU
*  MAY SEE SOME UNITS APPEARING MORE THAN ONCE.
*
* IF YOU HAVE ANY QUESTIONS PLEASE CALL:
*
*     FRED LUDDY
*     AMDAHL CORP.
*     1250 E ARQUES
*     SUNNYVALE CA.
*   PHONE:
*     (408) 735 4011
*
*.
         EJECT
DU       XSTART ,
         EJECT
         L     R1,CVTPTR           AND IN THE BEGINNING...
         L     R2,CVTILK2-CVT(R1)  GET THE POINTER TO THE UCB WORDS
         LR    R3,R2               SAVE THE POINTER TO THE BEGIN NING
         MVC   SCREEN(79),TITLE    ....
         SR    R5,R5
DU0      EQU   *                   ENTER HERE TO DISPLAY 20 UNITS
*                                   STARTING AT UCB ENTRY POINTED
*                                    TO BY R2
         LA    R8,20               NUMBER OF UNITS TO DISPLAY
         LA    R4,SCREEN           GET ADDRESS OF CURRENT ENTRY
         USING OUTLINE,R4
         XR    R11,R11
         LA    R0,79(R4)           BLANK
         LA    R1,(20*79)           THE
         XR    R14,R14               OUTPUT
         LR    R15,R14                AREA'S
         ICM   R15,B'1000',BLANKS
         MVCL  R0,R14
DU1      EQU   *                   BCT LOOP HERE
         LA    R4,79(,R4)          GET NEXT LINE ADDRESS
         MVI   0(R4),C' '
         MVC   1(78,R4),0(R4)      BLANK THE LINE
         LR    R1,R5
DU101    EQU   *
         LH    R5,0(R2)            SEE IF WE HAVE A LIVE ONE
         LTR   R5,R5               ...
         BM    PUTOUT              END OF OUR ROPES
         BNZ   DU105               THIS UNIT DOES NOT EXIST
DU102    EQU   *
         LA    R2,2(R2)            GET NEXT
         B     DU101
DU105    EQU   *
         CLR   R5,R1               CHECK NEXT UNIT ADDRESS
         BNH   DU102               IF NOT HI IT'S OPTCHAN
         USING UCBOB,R5
         TM    FLAG,FLAGON         ONLINE ONLY ?
         BZ    DU106               NO..
         TM    UCBSTAT,UCBONLI
         BZ    DU102               NOT ONLINE, IGNORE
DU106    EQU   *
         MVC   OUTUNIT(3),UCBNAME  MOVE THE EBCDIC NAME
*
* DETERMINE UNIT TYPE..
*
         CLI   UCBTBYT3,UCB3TAPE   IS THIS A TAPE
         BNE   DU2                 NO, TRY NEXT
         BAL   R9,ITSATAPE         GO DO TAPE UNIT STUFF
         B     DU9                 AND PRESS ON
DU2      EQU   *
         CLI   UCBTBYT3,UCB3COMM   IS THIS A TERMINAL
         BNE   DU3                 NO, TRY NEXT
         BAL   R9,ITSACOMM         GO DO TERMINAL
         B     DU9                 AND PRESS ON
DU3      EQU   *
         CLI   UCBTBYT3,UCB3CTC    IS THIS A CTC
         BNE   DU4                 NO, THANK GOD IF THIS A 470
         BAL   R9,ITSACTC          GO DO CTC STUFF
         B     DU9                 AND PRESS ON
DU4      EQU   *
         CLI   UCBTBYT3,UCB3DACC   IS THIS DASD ?
         BNE   DU5                 NO, WHATS UP DOC ?
         BAL   R9,ITSADASD         GO DO DASDSTUFF
         B     DU9                 AND PRESS ON
DU5      EQU   *
         CLI   UCBTBYT3,UCB3DISP   IS THIS GRAPHICS
         BNE   DU6                 NO.. HOPE FOR UR
         BAL   R9,ITSATUBE         GO DOWN THE TUBES
         B     DU9                 AND PRESS ON
DU6      EQU   *
         CLI   UCBTBYT3,UCB3UREC   IS THIS UNIT RECORD ?
         BNE   DU7                 NO.. LEAVE THE DAMN THING BLA NK
         BAL   R9,ITSAUR           GO DO THE UNIT RECORD STUFF
         B     DU9                 AND PRESS ON
DU7      EQU   *                   SAVE FOR CHAR READER IF YOU LIKE
DU8      EQU   *                   SAVE FOR CHAR READER IF YOU LIKE
DU9      EQU   *
*
* ANALYZE THE DEVICES CURRENT STATUS, ONLINE OR OFF, ALLOCATED, OR NOT
* BUSY OR NOT, ETC...
*
         MVC   OUTSTAT(7),OFFLINE  DEFAULT TO OFFLINE
         TM    UCBSTAT,UCBONLI     IS THIS DEVICE ONLINE ?
         BZ    DU10                NO, PRESS ON
         MVC   OUTSTAT(7),BLANKS   RESET A MINUTE
         MVI   OUTSTAT,C'O'        SHOW IT'S ONLINE
*
* SEE WHATS HAPPENING WITH ONLINE DEVICES
*
         TM    UCBSTAT,UCBALOC    IS IT ALLOCATED
         BZ    *+8                 NO.. SKIP MVI
         MVI   OUTSTAT,C'A'        SHOW ALLOCATED
         TM    UCBFLA,UCBNRY       NOT READY ?
         BZ    *+10                NO..
         MVC   OUTSTAT+1(5),NOTREADY
         TM    UCBFLA,UCBBSY       IS IT BUSY ?
         BZ    DU12                NO..
         MVC   OUTSTAT+1(5),BUSY   SHOW BUSY
*
* ATTEMPT TO FIND OUT WHY
*
         TM    UCBFLA,UCBPSNS      PENDING SENSE ?
         BZ    DU10                NO
         MVC   OUTNOTE,PENDSNSE    SHOW REASON
         B     DU12                GET OUT
DU10     EQU   *
         TM    UCBFLA,UCBCUB       IS THE CONTROL UNIT BUSY ?
         BZ    DU11                NO, SEE IF CHANNEL PROGRAM ACTIVE
         MVC   OUTNOTE,CUB         SHOW CONTROL UNIT BUSY
         B     DU12                AND SPLIT
DU11     EQU   *
         TM    UCBFLA,UCBACTV      CHAN PGM ACTIVE
         BZ    DU12                SORRY.. I GIVE UP
         MVC   OUTNOTE,CHPGMACT    SHOW CHANNEL PROGRAM ACTIVE
DU12     EQU   *
         LR    R1,R5               GET THE ADDRESS OF THE UCB
         BAL   R9,CVTPRT           CONVERT IT
         STCM  R14,B'0011',OUTADDR MOVE THE FIRST PART OF THE ADDRESS
         STCM  R15,B'1111',OUTADDR+2 AND THE REST ALSO
DU13     EQU   *                   INCREMENT AND LEAVE
         LA    R2,2(,R2)           GET PTR TO NEXT LOOK UP
         BCT   R8,DU1              LOOP FOR THE NEXT
PUTOUT   EQU   *
         BAL   R9,WRITE            DOIT JACK !!!
         CLI   IOCBAID,AIDPA1      WELL ?
         BE    END1
         CLI   IOCBAID,C'9'        RESTART ?
         BNE   EYEPATCH            NO..
         LR    R2,R3               SET NEW PTR
         B     DU0                 ADIOS
EYEPATCH EQU   *
         CLI   IOCBAID,C'8'        REQUEST TO GO UP ?
         BE    DU0                 YES.. RESET AND GO UP IN THE  WORLD
         CLI   IOCBAID,C'7'        RETURN UPWARD
         BNE   CHKON               NO.. BEAT IT
*
* COUNT  BACK  UP 40 UNITS, 20 ON DISPLAY AND 20 EARLIER
*
         LA    R8,40
PUT0     EQU   *
         SH    R2,=H'2'            SUBTRACT 2
         CLR   R2,R3               AT THE TOP ?
         LR    R1,R5               SAVE THE UCB POINTER
         LH    R5,0(R2)            GET THE UCB POINTER
         BE    DU0                 START FROM HERE
         LTR   R5,R5               A GENNED UNIT ?
         BZ    PUT0                NO, IT DOESN'T COUNT
         TM    FLAG,FLAGON         ARE WE DOING ONLINE ONLY ?
         BZ    PUT1                NO..
         TM    UCBSTAT,UCBONLI     IS THIS ONE ONLINE ?
         BZ    PUT0                NO.. COUNT AGAIN
PUT1     EQU   *
         BCT   R8,PUT0             COUNT IT
         B     DU0                 GO
CHKON    EQU   *
         CLI   IOCBAID,C'4'        REQUEST ALL ?
         BNE   CHK2                NO..
         MVI   FLAG,0
         LR    R2,R3               RESET.
         L     R10,ADMODE
         MVC   0(6,R10),MODEALL
         B     DU0
CHK2     EQU   *
         CLI   IOCBAID,C'5'        ONLINE ONLY ?
         BNE   END1                NO..
         OI    FLAG,FLAGON         RESET
         LR    R2,R3
         L     R10,ADMODE
         MVC   0(6,R10),MODEONLI
         B     DU0
END1     EQU   *
         L     R13,4(R13)
         RETURN (14,12),RC=0      RETURN TO DUST
         EJECT
CVTPRT   EQU   *
         ST    R1,CVTB
         UNPK  CVTA(9),CVTB(5)
         TR    CVTA(8),HEXTAB
         LM    R14,R15,CVTA
         BR    R9
         SPACE 2
ITSACTC  EQU   *
         MVC   OUTTYPE,CTCTYPE
         BR    R9
         SPACE 2
ITSAUR   EQU   *
         LA    R1,URTYPES          GET DEVICE TYPES ADDRESSES
UR0      EQU   *
         CLC   UCBUNTYP,0(R1)      IS IT THIS ?
         BE    UR1                 SHORE IS
         LA    R1,TYPESIZE(R1)     GET NEXT IF NOT
         CLI   0(R1),255           STOPPER ?
         BNE   UR0
UR1      EQU   *
         MVC   OUTTYPE,1(R1)       MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSACOMM EQU   *
         LA    R1,COMMTYPS         GET DEVICE TYPES ADDRESSES
         MVC   CVTA,UCBTBYT1
         NI    CVTA,15             GET MODEL CODE
CO0      EQU   *
         CLC   CVTA(1),0(R1)       IS IT THIS ?
         BE    CO1                 SHORE IS
         LA    R1,TYPESIZE(R1)     GET NEXT IF NOT
         CLI   0(R1),255           STOPPER ?
         BNE   CO0
         MVC   CVTA,UCBUNTYP
         NI    CVTA,15             GET CONTROL UNIT-TYPE
         LA    R1,COMMCUS
CO00     EQU   *
         CLC   CVTA(1),0(R1)       IS IT THIS ?
         BE    CO1                 SHORE IS
         LA    R1,TYPESIZE(R1)     GET NEXT IF NOT
         CLI   0(R1),255           STOPPER ?
         BNE   CO00
CO1      EQU   *
         MVC   OUTTYPE,1(R1)       MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSATUBE EQU   *
         LA    R1,TUBETYPS         GET DEVICE TYPES ADDRESSES
TU0      EQU   *
         CLC   UCBUNTYP,0(R1)      IS IT THIS ?
         BE    TU1                 SHORE IS
         LA    R1,TYPESIZE(R1)     GET NEXT IF NOT
         CLI   0(R1),255           STOPPER ?
         BNE   TU0
TU1      EQU   *
         MVC   OUTTYPE,1(R1)       MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSATAPE EQU   *
         LA    R1,TAPETYPS         GET DEVICE TYPES ADDRESSES
TA0      EQU   *
         CLC   UCBUNTYP,0(R1)      IS IT THIS ?
         BE    TA1                 SHORE IS
         LA    R1,TYPESIZE(R1)     GET NEXT IF NOT
         CLI   0(R1),255           STOPPER ?
         BNE   TA0
TA1      EQU   *
         MVC   OUTTYPE,1(R1)       MOVE THE DESCRIPTOR
         TM    UCBSTAT,UCBONLI     IS IT ONLINE ?
         BZ    TA10                NO.. MOVE ON
         MVC   OUTVOL(6),UCBVOLI   MOVE THE VOL-SER
         TR    OUTVOL(6),BLANKTAB  MAKE SURE IT IS READABLE
*
* CHECK  OUT THE DENSITYS AVAILABLE
*
TA10     EQU   *
         TM    UCBTBYT2,UCBDUDN1+UCB2OPT1 DUAL  800/1600 ?
         BZ    TA2                 NO..
         MVC   OUTNOTE(9),DD1600
         TM    UCBTBYT2,UCB2OPT1   SUPPORT ASCII ?
         BZ    TA7                 NO..
         MVC   OUTNOTE+8(5),ASCII
         B     TA7
TA2      EQU   *
         TM    UCBTBYT2,UCBDUDN2   1600/6250 ?
         BZ    TA3                 NO..
         MVC   OUTNOTE(9),DD6250
         B     TA7
TA3      EQU   *
         TM    UCBTBYT2,UCB2OPT0   7-TRACK ?
         BZ    TA4                 NO..
         MVC   OUTNOTE(8),TRACK7
         MVC   OUTNOTE+8(9),DD1600
         B     TA7
TA4      EQU   *
         TM    UCBTBYT1,UCBD1600   1600 BPI ?
         BZ    TA5
         MVC   OUTNOTE(4),D1600
         B     TA7
TA5      EQU   *
         TM    UCBTBYT1,UCBD6250   6250 BPI ?
         BZ    TA7                 NO..
         MVC   OUTNOTE(4),D6250
TA7      EQU   *
         BR    R9
         SPACE 2
ITSADASD EQU   *
         TM    UCBWGT,UCBMTPXP     IS THIS THE DOLDRUMS ?
         BZ    DA00                NO..
         TM    UCBUA,15            IS THIS THE BASE EXPOSURE ?
         BNZ   DU102               NOPE.. GOTO NEXT
DA00     EQU   *
         LA    R1,DASDTYPS         GET DEVICE TYPES ADDRESSES
DA0      EQU   *
         CLC   UCBUNTYP,0(R1)      IS IT THIS ?
         BE    DA1                 SHORE IS
         LA    R1,TYPESIZE(R1)     GET NEXT IF NOT
         CLI   0(R1),255           STOPPER ?
         BNE   DA0
DA1      EQU   *
         MVC   OUTTYPE,1(R1)       MOVE THE DESCRIPTOR
         TM    UCBTBYT2,UCBRVDEV   IS THIS VIO
         BZ    DA10                NO..
         MVC   OUTVOL,VIOUNIT      INDICATE SO..
DA10     EQU   *
         TM    UCBSTAT,UCBONLI     IS IT ONLINE ?
         BZR   R9                  NO.. RETURN
         TM    UCBSTAB,UCBPGFL     IS THERE PAGING ON THIS PACK
         BZ    *+10
         MVC   OUTNOTE(13),PAGEPACK TELL THE USER
         MVC   OUTVOL(6),UCBVOLI   MOVE THE VOL-SER
         TR    OUTVOL(6),BLANKTAB  MAKE SURE IT IS READABLE
         TM    UCBSTAT,UCBSYSR     IS THIS THE SYSRES ?
         BZ    DA2                 NO... PRESS ON
         MVC   OUTSTATE(10),SYSRES INDICATE SO
         BR    R9                  AND RETURN
         SPACE
DA2      EQU   *
         TM    UCBSTAB,UCBBPRV     IS THIS VOL PRIVATE ?
         BZ    DA3                 NO..
         MVC   OUTSTATE,PRIVATE    SHOW IT IS SO
DA3      EQU   *
         TM    UCBSTAB,UCBBPUB     IS IT PUBLIC ?
         BZ    DA4                 NO..
         MVC   OUTSTATE,PUBLIC
DA4      EQU   *
         TM    UCBSTAB,UCBBSTR     STORAGE ?
         BZ    DA5                 NO...
         MVC   OUTSTATE,STORAGE
DA5      EQU   *
         TM    UCBSTAT,UCBRESV     IS IT RESERVED ?
         BZ    DA6                 NO..
         MVC   OUTSTAT2,RESERVED
DA6      EQU   *
         TM    UCBSTAT,UCBPRES     PERMANENTLY RESIDENT
         BZ    DA7                 NO..
         MVC   OUTSTAT2,PRES
DA7      EQU   *
         BR    R9
         SPACE 2
WRITE    EQU   *
         TRMIO IOCB,FORMAT=SCR1,IMAGE=SCREEN BEEP=YES
         BR    R9
         TITLE 'D U - DATA AREAS AND THE LIKE'
IOCB     IOCB
SCR1     SFMT
SCREEN   FIELD 79
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,INTEN=LO
         FIELD 79,ALPHA,CURSOR=AFTER
         FIELD 79
         FIELD 79
         SFEND
CVTA     DC    D'0',X'0'               PLUS A GARBAGE BYTE
CVTB     DC    F'0'
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'
CHPGMACT DC    CL35'CHANNEL PROGRAM ACTIVE'
CUB      DC    CL35'CONTROL UNIT BUSY'
PENDSNSE DC    CL35'SENSE PENDING '
OFFLINE  DC    C'OFFLINE'
BUSY     DC    C'-BUSY'
NOTREADY DC    C'-NRDY'
MTP      DC    C'-MTP'
VIOUNIT  DC    CL7'V-I-O'
DD6250   DC    CL9'1600/6250'
DD1600   DC    CL9'800/1600'
D1600    DC    CL4'1600'
D6250    DC    CL4'6250'
ASCII    DC    CL5'ASCII'
TRACK7   DC    CL8'7-TRACK,'
PAGEPACK DC    CL13'<PAGING-PACK>'
MODEALL  DC    CL6'ALL'
MODEONLI DC    CL6'ONLINE'
ADMODE   DC    A(MODE)
BLANKS   DC    CL8' '
FLAG     DC    X'0'                DEFAULT 'ALL'
FLAGON   EQU   X'80'               USE ONLY ONLINE PEOPLE
TITLE    DC    CL79'UNIT TYPE   STATUS  VOLSER  VOLSTATE UCBADDR _______
               ____NOTES_____________'
         LTORG
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
TYPESIZE EQU   8                   SIZE OF A 'DX' ENTRY
PUBLIC   DC    CL4'PUBL'
PRIVATE  DC    CL4'PRIV'
STORAGE  DC    CL4'STRG'
PRES     DC    CL7'/RSDNT'
RESERVED DC    CL7'/RSERV'
SYSRES   DC    CL10'<-SYSRES->'
CTCTYPE  DC    CL7'CTC'
COMMCUS  EQU   *
         DX    1,'2702'
         DX    2,'2701'
         DX    3,'2703'
         DX    4,'2955'
         DX    5,'3704/5'
         DX    FF,'COMM'
TAPETYPS EQU   *
         DX    1,'2400'
         DX    3,'3400'
         DX    FF,'TAPE'
DASDTYPS EQU   *
         DX    6,'2305-1'
         DX    7,'2305-2'
         DX    8,'2314'
         DX    9,'3330'
         DX    A,'3340'
         DX    B,'3350'
         DX    D,'3330-1'
         DX    FF,'DASD'
TUBETYPS EQU   *
         DX    8,'3066'
         DX    9,'3277'
         DX    A,'3284'
         DX    B,'3286'
         DX    FF,'TUBE'
COMMTYPS EQU   *
         DX    1,'TWX'
         DX    3,'2740'
         DX    4,'2740'
         DX    5,'2740C'
         DX    6,'2741P'
         DX    7,'BSC3'
         DX    FF,'COMM'
URTYPES  EQU   *
         DX    1,'2540-R'
         DX    2,'2540-P'
         DX    3,'1442-R'
         DX    4,'2501'
         DX    5,'2520'
         DX    6,'3505'
         DX    8,'1403'
         DX    9,'3211'
         DX    A,'1443'
         DX    C,'3525'
         DX    10,'2671'
         DX    18,'2495'
         DX    1B,'1287'
         DX    1C,'1288'
         DX    20,'1052'
         DX    22,'3210'
         DX    23,'3215'
         DX    30,'3213'
         DX    44,'3540'
         DX    FF,'U/R'
SCREEN   DC    24CL79' '           THIS IS THE MAN
ENTRLNE  EQU   SCREEN+(79*21)      WOOPS
         ORG   SCREEN+(79*22)
         DC   C'PF4-ALL, PF5-ONLINE, PF7-SCROLL UP, PF8-SCROLL DOWN, PF9
               9-RESTART'
         ORG   SCREEN+(79*23)
         DC    C'>MODE = '
MODE     DC    CL60'ALL'
OUTLINE  DSECT ,
OUTUNIT  DS    CL5
OUTTYPE  DS    CL7
OUTSTAT  DS    CL8
OUTVOL   DS    CL7
OUTSTATE DS    CL4
OUTSTAT2 DS    CL7
OUTADDR  DS    CL7
OUTNOTE  DS    CL35
         TITLE 'MVS UCB DEFINITION '
         IEFUCBOB ,
         TITLE 'MVS CVT DEFINITION'
         CVT   DSECT=YES
         END
