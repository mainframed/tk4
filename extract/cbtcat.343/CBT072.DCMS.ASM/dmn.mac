DMN      TITLE 'D M N  - DISPLAY DOMAINS COMMAND FOR DCMS'
         SPACE 2
         MACRO
         EBCDIC &INPUT,&LENGTH,&OFFSET
*
* THIS MACRO IS LOCAL TO THE DMN PROGRAM
*
* VARIABLE SPECIFICATION
*   INPUT  - DEFINES INPUT LOCATION, RX ADDRESS OR (R15) IS VALID
*   LENGTH - DEFINES LENGTH OF INPUT VARIABLE, 1 OR 2 ARE VALID
*   OFFSET - OUTPUT OFFSET OFF OF R4 TO STORE LOW ORDER 3 BYTES
*            OF REGISTER 1.
*
         LCLA  &I
         AIF   ('&INPUT'(1,1) EQ '(').X2
&I       SETA  &LENGTH
         AIF   (&I NE 1).X1
         XR    R15,R15
         IC    R15,&INPUT
         AGO   .X2
.X1      ANOP
         LH    R15,&INPUT
.X2      ANOP
         BAL   R9,CVTEBC
         STCM  R1,B'0111',&OFFSET.(R4)
         MEND
         EJECT
         SPACE 4
*.
*  DMN -
*        THIS IS THE DISPLAY DOMAIN PROCESSOR FOR DCMS / TSO.
*  M/O -
*        THE POINTER TO THE DOMAIN TABLES IS OBTAINED FROM THE
*        RMCT, AND THE TABLES ARE CONVERTED TO SAPIEN-READABLE
*        FORMAT.
*
*  THE FOLLOWING PF KEYS ARE DEFINED -
*
*    .  -----
*      × PF3 ×   EXIT FROM THIS PROGRAM (A LA SPF)
*      ×     ×
*       -----
*
*    .  -----
*      × PA1 ×   EXIT FROM THIS PROGRAM (A LA DCMS)
*      ×     ×
*       -----
*
*  ALL OTHER INTERRUPTS CAUSE THE SCREEN TO BE RE-FRESHED
*
*  IF YOU HAVE ANY QUESTIONS PLEASE CALL:
*
*    FRED LUDDY
*    AMDAHL CORP.
*    1250 E ARQUES
*    SUNNYVALE CA.
*   PHONE
*    (408) 735 4011
*
*.
         EJECT
DMN      XSTART LV=WORKLEN
         USING  WORKAREA,R13
         SPACE 2
DMN0     EQU   *                   SCREEN RE-FRESH LOOP HERE
         L     R1,CVTPTR
         L     R5,CVTOPCTP-CVT(R1) FIND THE RMCT
         LM    R2,R3,RMCTDMDT(R5)  R2 -> FIRST DOMAIN TABLE
*                                   R3 -> LAST DOMAIN TABLE
         BAL   R9,SCRCLEAR         CLEAN UP ANY MESS
         TIMEDIT CVTA,LINE1
         MVC   LINE1+23(L'TITLE1),TITLE1 FIX UP THE COSMETICS
         MVI   LINE2,C'-'
         MVC   LINE2+1(78),LINE2   RIPPLE A BANNER LINE
         MVC   LINE3+10(L'TITLE2),TITLE2 ...
         MVC   LINE4+10(L'TITLE3),TITLE3 ...
         USING DMDT,R2
         LA    R4,LINE1+23         WHERE FOR # OF DOMAINS
         LH    R15,RMCTDMNC(R5)    GET THE NUMBER OF DOMAINS
         EBCDIC (R15),,51
         LA    R4,LINE5+10         SET CURRENT LINE PTR
         SPACE 2
DMN1     EQU   *                   INDIVIDUAL DOMAIN LOOP HERE..
         EBCDIC DMDTNO,1,3         DOMAIN NUMBER
         EBCDIC DMDTWT,1,10        ITS WEIGHT
         EBCDIC DMDTLO,1,14        GET MINIMUM FROM CNSTR
         EBCDIC DMDTHI,1,18        GET MAX FROM CNSTR
         EBCDIC DMDTMPLT,2,25      TARGET MULTI-PROGRAMMING LEVEL
         EBCDIC DMDTCMPL,2,33      CURRENT MULTI-PROGRAMMING LEVEL
         EBCDIC DMDTGOOU,2,38      NUMBER GOING OUT
         EBCDIC DMDTINCU,2,42      NUMBER ON IN-Q
         EBCDIC DMDTOUTU,2,46      NUMBER ON OUT-Q
         EBCDIC DMDTRUA,2,55       AVG. NUMBER OF READY USERS
         LA    R4,79(,R4)          GET NEXT OUTPUT LINE
         LA    R2,DMDTEND-DMDT(R2) GET NEXT DOMAIN TABLE
         CR    R2,R3               ARE WE AT THE END OF THE DOMAINS
         BNH   DMN1                BRANCH IF NOT HALLUCINATING
         BAL   R9,WRITE            WRITE OUT THE SCREEN
         CLI   IOCBAID,AIDPA1
         BE    EXIT                NORMAL DCMS EXIT
         PFSELECT IOCBAID,DMN0,(,,EXIT),DUMMY=DMN0
EXIT     EQU   *
         L     R13,4(,R13)         RETURN TO SENDER
         RETURN (14,12),RC=0       THE END...
         EJECT
*.
*   SCRCLEAR - SCREEN CLEAR
*
*   CALL  THIS  ROUTINE TO CLEAR THE OUTPUT AREA OF A SCREEN
*   REGISTERS USED:
*     R9   - RETURN ADDRESS
*     R0   - WORK
*     R1   - WORK
*     R15  - WORK
*.
SCRCLEAR DS    0F
         LA    R0,LINE1
         LA    R1,LINE24+76        ADDRESS OF THE END OF THE OUTPUT
*                                   AREA.
         SLR   R1,R0               TO OBTAIN THE LENGTH
         LA    R15,C' '            GET THE PAD CHARACTER
         SLL   R15,24              PUT IT WHERE MVCL WANTS IT
         MVCL  R0,R14              AND BLANK THE SCREEN BUFFER
         BR    R9                  RETURN FROM PARADISE
         SPACE 2
*.
*   WRITE -
*
*    THIS ROUTINE CALLS THE TERMIO INTERFACE TO WRITE THE SCREEN
*
*.
WRITE    DS    0F
         TRMIO IOCB,FORMAT=SCR1,IMAGE=LINE1 BEEP=YES
         BR    R9
*.
*   CVTEBC -
*
*      THIS ROUTINE CONVERTS BINARY TO PRINTABLE EBCDIC.
*
*   INPUT  -
*
*      R15 - CONTAINS BINARY INPUT NUMBER
*      R9  - RETURN REGISTER
*
*   OUTPUT -
*
*      R0  - FIRST HALF OF 8 BYTE EBCDIC NUMBER
*      R1  - SECOND HALF OF 8 BYTE EBCDIC NUMBER
*
*.
CVTEBC   EQU   *
         CVD   R15,CVTA            REPENT (NO.. THAT'S CONVERT DUMMY)
         MVC   CVTB,CVTMASK        MOVE IN THE EDIT MASK
         ED    CVTB,CVTA+4
         LM    R0,R1,CVTB
         BR    R9
CVTMASK  DC    X'4020202020202120' A MASK FOR DECEPTION..
*.
*    DEFINE THE SCREEN IMAGE
*.
SCR1     SFMT
LINE1    FIELD 79
LINE2    FIELD 79
LINE3    FIELD 79
LINE4    FIELD 79
LINE5    FIELD 79,INTEN=LO
LINE6    FIELD 79,INTEN=LO
LINE7    FIELD 79,INTEN=LO
LINE8    FIELD 79,INTEN=LO
LINE9    FIELD 79,INTEN=LO
LINE10   FIELD 79,INTEN=LO
LINE11   FIELD 79,INTEN=LO
LINE12   FIELD 79,INTEN=LO
LINE13   FIELD 79,INTEN=LO
LINE14   FIELD 79,INTEN=LO
LINE15   FIELD 79,INTEN=LO
LINE16   FIELD 79,INTEN=LO
LINE17   FIELD 79,INTEN=LO
LINE18   FIELD 79,INTEN=LO
LINE19   FIELD 79,INTEN=LO
LINE20   FIELD 79,INTEN=LO
LINE21   FIELD 79,INTEN=LO
INPUTLNE FIELD 79,ALPHA,CURSOR=AFTER
LINE23   FIELD 79,INTEN=LO
LINE24   FIELD 79,INTEN=LO
         SFEND
RMCTDMDT EQU   180,4,C'F'
RMCTDMNC EQU   188,2,C'H'
*           0....+....1....+....2....+....3....+....4....+....5....+...
TITLE1 DC C'< D_O_M_A_I_N   D_I_S_P_L_A_Y >     # OF DOMAINS ='
TITLE2 DC C'              ×________MPLS________× GOING  ARE    AVERAGE'
TITLE3 DC C'NUMBER WEIGHT MIN MAX TARGET CURRENT  OUT  IN OUT RUNNABLE'
*           0....+....1....+....2....+....3....+....4....+....5....+...
         LTORG
*.
*      DEFINE A DSECT WITH WORK VARIABLES TO MAKE THE PROGRAM
*      RE-ENTRANT. XSTART WILL GETMAIN THIS AREA FOR US IN
*      SUBPOOL ZERO.
*.
WORKAREA DSECT
SAVE     DS    18F
IOCB     IOCB
IOAREA   EQU   *
         DSGEN SCR1
IOLEN    EQU   *-IOAREA
         SPACE 2
TDCBADDR DC    A(0)                USED TO FIND TDCB
CVTA     DC    D'0'
CVTB     DC    D'0'
WORKLEN  EQU   *-WORKAREA
         CVT   DSECT=YES
         IRADMDT ,
         END
