         PRINT NOGEN
***********************************************************************
*                                                                     *
*  NOTE: IF THERE IS A LOT OF DATA TO CONVERT, THIS PROGRAM           *
*        COULD RUN A LONG TIME. IN THE CONVERSION AT G.E.  THIS       *
*        CONVERSION RUN TOOK OVER 5 HOURS. THIS IS DUE TO THE         *
*        DATA BEING IN RANDOM ORDER ON THE OLD JOURNALS.              *
*        G.E. HOLD OVER A YEARS WORTH OF DATA ON DISKWTR, AVERAGING   *
*        ABOUT 3000 JOBS A DAY. (APPROX 1 MILLION ENTRIES HAD TO      *
*        BE CONVERTED)                                                *
*        IF YOU ARE KEEPING 2 TO 4 WEEKS ON DISKWTR, THIS PROGRAM     *
*        WILL ASSIST IN THE CONVERSION,. IF YOU HAVE DATA LIKE G.E.'S *
*        IT MAY BE BETTER TO WRITE A DIFFERENT CONVERSION PROGRAM.    *
*        IE: 1. READ ALL THE OLD JOURNALS FIRST AND CREATE A NEW      *
*               DATASET WITH THE DSN ON EACH JOB.                     *
*            2. SORT BY JOBNAME                                       *
*            3. THEN ADD THE RECORDS                                  *
*                                                                     *
*  THIS IS THE SOUT SUBSYSTEM CONVERSION PROGRAM.                     *
*                                                                     *
*     IT IS USED TO CONVERT THE OLD JOURNAL DATASETS TO               *
*     THE NEW RECORDS ON THE VSAM JOURNAL.                            *
*                                                                     *
*     A ONE CHARACTER PARM IS PASSED TO THE PROGRAM.                  *
*     IT SHOULD BE  "D"  "R"  OR  "O"                                 *
*                                                                     *
*     TO CONVERT:                                                     *
*        1) STOP YOUR OLD DISKWTR                                     *
*        2) START SAVEWTR TO MOVE THE ACTIVE DATA TO YOUR DAILY FILE  *
*        3) PERFORM ONE OR MORE OF THE FOLLOWING:                     *
*                                                                     *
* A > IF YOU ARE CONVERTING TO NEW DATASET NAMES (IE: DIFFERENT       *
*     PREFIX) OR YOU WERE USING TWO(2) PREFIXES IN YOUR OLD SYSTEM    *
*     (IE" SYS2.OPSWTR AND SYS2.OPSTAPE) YOU SHOULD;                  *
*        1) RUN THE OLD SOUTWTR PROGRAM TO PLACE ALL OF YOUR OLD      *
*           SYSOUT REPORTS INTO THE HISTORY PREFIX DATASET NAME.      *
*           (IE: SYS2.OPSTAPE.DATA.ARCHIVE.G----V--)                  *
*        2) RUN THIS CONVERT PROGRAM WITH PARM "O"                    *
*           AND //INPUT  => POINTING TO YOUR ARCHIVE JOURNALS         *
*        3) DELETE ALL OF YOUR OLD ARCHIVE JOURNAL DATASETS           *
*                                                                     *
*        JCL EXAMPLES FOLLOW:                                         *
*                                                                     *
*    //FORMAT   EXEC PGM=SOUTCVT,PARM=O         <==== FORMAT          *
*    //INPUT    DD   DSN=SYS2.OPSTAPE.ARCHIVE,DISP=SHR                *
*    //JOURNAL  DD   DSN=SYS2.OPSWTR.JOURNAL,DISP=SHR,AMP='AMORG'     *
*                                                                     *
*                                                                     *
*      NOTE: IN THE SPF PANEL "CMNFS00" SET:                          *
*            "CMNFSWTR" TO YOUR NEW PREFIX                            *
*            "CMNFSWTO" TO YOUR OLD PREFIX                            *
*                                                                     *
*            WHEN SOUTSUB SEES A "O" IN THE RECORD, IT WILL           *
*            BUILD A DATASET ALLOCATION FOR THE OLD SYSOUT DATASETS   *
*                                                                     *
* B > IF YOU WANT THE DATASET PREFIX TO REMAIN THE SAME,              *
*      (IE:  "SYS2.OPSWTR.DATA")                                      *
*     AND YOU WERE USING ONLY ONE PREFIX IN YOUR OLD SYSTEM           *
*                                                                     *
*        1) RUN THE OLD SOUTWTR PROGRAM TO PLACE ALL OF YOUR DAILY    *
*           SYSOUT REPORTS INTO THE THE ARCHIVE DATASET NAME.         *
*           (IE: SYS2.OPSWTR.DATA.ARCHIVE.G----V--)                   *
*        2) RUN THIS CONVERT PROGRAM WITH PARM "R"                    *
*           AND //INPUT  => POINTING TO YOUR ARCHIVE JOURNALS         *
*        3) DELETE ALL OF YOUR OLD JOURNAL DATASETS                   *
*                                                                     *
*     OR                                                              *
*                                                                     *
*        1) RUN THIS CONVERT PROGRAM WITH PARM "R"                    *
*           AND //INPUT  => POINTING TO YOUR ARCHIVE JOURNALS         *
*        2) RUN THIS CONVERT PROGRAM WITH PARM "D"                    *
*           AND //INPUT  => POINTING TO YOUR DAILY JOURNALS           *
*        3) DELETE ALL OF YOUR OLD JOURNAL DATASETS                   *
*                                                                     *
*      NOTE: IN THE SPF PANEL "CMNFS00" SET:                          *
*            "CMNFSWTR" TO YOUR NEW PREFIX                            *
*            "CMNFSWTO" TO BLANK (WILL NOT BE USED)                   *
*                                                                     *
*        JCL EXAMPLES FOLLOW:                                         *
*                                                                     *
*                                                                     *
*    //FORMAT   EXEC PGM=SOUTCVT,PARM=R         <==== FORMAT          *
*    //INPUT    DD   DSN=SYS2.OPSWTR.ARCHIVE,DISP=SHR                 *
*    //JOURNAL  DD   DSN=SYS2.OPSWTR.JOURNAL,DISP=SHR,AMP='AMORG'     *
*                                                                     *
*    //FORMAT   EXEC PGM=SOUTCVT,PARM=D         <==== FORMAT          *
*    //INPUT    DD   DSN=SYS2.OPSWTR.DAILY,DISP=SHR                   *
*    //JOURNAL  DD   DSN=SYS2.OPSWTR.JOURNAL,DISP=SHR,AMP='AMORG'     *
*                                                                     *
*                                                                     *
*    THIS NEW DISKWTR SYSTEM WILL INTERNALLY ALLOCATE THE OLD         *
*    DATASET GDG'S OR THE NEW DATE/TIME STAMPED DATASETS BASED        *
*    ON THE CHARACTER "A" "D" "R" OR "O" AND THE DATE/TIME STAMP      *
*                                                                     *
*    IF THE CHAR "O" IS FOUND IN THE VSAM JOURNAL, THE OLD PREFIX     *
*    WILL BE USED TO BUILD A DATASET NAME ALONG WITH "ARCHIVE" AND A  *
*    GDG FROM THE LAST 6 CHARS OF THE DATE TIME STAMP.                *
*                                                                     *
*    IF THE OTHER CHARS ARE FOUND, A DATASET NAME WILL BE BUILT       *
*    FROM THE EXISTING PREFIX ALONG WITH ACTIVE, DAILY, OR ARCHIVE,   *
*    DEPENDING ON THE CHARACTER. IF THE PROGRAMS SEES  '99999' IN     *
*    THE FIRST 5 BYTES OF THE DATE/TIME STAMP, A GDG IS BUILT,        *
*    OTHERWISE A DATE/TIME STAMP IS BUILT.                            *
*                                                                     *
*    IF THE DATE/TIME STAMP IS ALL 9'S, NO GDG OR DATE TIME STAMP     *
*    IS BUILT. THIS SHOULD ONLY HAPPEN FOR THE ACTIVE FILE.           *
*                                                                     *
*                                                                     *
*                                                                     *
* ABEND  REASONS:                                                     *
*                                                                     *
*        U0001 - INVALID PARM                                         *
*        U0002 - INPUT DATASET OPEN ERROR                             *
*        U0003 - VSAM JOURNAL OPEN ERROR                              *
*                                                                     *
*   NOTE: IF THERE IS A VSAM UPDATE/ADD ERROR , THIS PROGRAM          *
*         BYPASSES IT. A WTO IS WRITTEN STATING THAT THERE            *
*         IS A DUPLICATE RECORD ERROR. (ONLY ERROR ENCOUNTERED        *
*         WHILE TESTING)                                              *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        INITIALIZATION AND PARM ISOLATION                            *
*                                                                     *
***********************************************************************
SOUTCVT  $PROLOG R12           SPECIFY BASE REGISTERS
         SR    R11,R11         CLEAR RECORD COUNT REGISTER FOR FORMAT
         L     R2,0(R1)        GET PARM POINTER
         LH    R3,0(R2)        GET PARM SIZE
         LTR   R3,R3           ANY PARM?
         BZ    BEGIN           NO, JUST READ THE DATA FOR THE EXIT
         CH    R3,=H'1'        VALID LENGTH?
         BNE   ABEND1          NO, ERROR
         CLC   2(1,R2),=C'O'   VALID REQUEST?
         BE    SETOLD          YES
         CLC   2(1,R2),=C'R'   VALID REQUEST?
         BE    SETARCH         YES
         CLC   2(1,R2),=C'D'   VALID REQUEST?
         BE    SETDALY         YES
         B     ABEND1          ABEND IT
SETOLD   EQU   *
         MVI   HADR,C'O'       SET OLD OPTION
         B     BEGIN           CONTINUE
SETARCH  EQU   *
         MVI   HADR,C'R'       SET ARC OPTION
         B     BEGIN           CONTINUE
SETDALY  EQU   *
         MVI   HADR,C'D'       SET ARC OPTION
         B     BEGIN           CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*        OPEN THE INPUT FILES AND THE VSAM JOURNAL                    *
*                                                                     *
***********************************************************************
BEGIN    EQU   *
         OPEN  (INPUT,(INPUT)) OPEN THE INPUT FILE
         TM    INPUT+48,X'10'  CHECK IF IT OPENED
         BZ    ABEND2          NO, INPUT FAILED TO OPEN
OPENACB  OPEN  JOURNACB        OPEN VSAM FILE
         LTR   R15,R15         OK?
         BNZ   ABEND3          NO - ABEND
READOLD  GET   INPUT,RECORD   GET THE OLD JOURNAL RECORDS
         CLC   RECORD(4),=C'DSN='  IS IT FIRST RECORD
         BE    SETDSN          YES - SET DSN
*  CONVERT OLD RECORD TO NEW
         MVC   HDATE(3),=C'019' SET TO 1900
         CLC   INDATE+6(2),=C'75'  LESS THAN 1975
         BH    MOVEYR              NO - CONTINUE
         MVC   HDATE(3),=C'020' SET TO 2000
MOVEYR   MVC   HDATE+3(2),INDATE+6   MOVE YEAR
         MVC   HDATE+5(2),INDATE     MOVE MONTH
         MVC   HDATE+7(2),INDATE+3   MOVE DAY
         PACK  PDATE,HDATE           PACK DATE
         MVC   JDATE,=P'099999999'   SET TO 9999999
         SP    JDATE,PDATE         REVERSE DATE
*
         MVC   HTIME(1),=C'0'
         MVC   HTIME+1(2),INTIME     MOVE HOUR
         MVC   HTIME+3(2),INTIME+3   MOVE MIN
         MVC   HTIME+5(2),INTIME+6   MOVE DSECONDS
         PACK  PTIME,HTIME          PACK TIME
         MVC   JTIME,=P'0999999'    SET TO 9999999
         SP    JTIME,PTIME          REVERSE TIME
*
         MVC   JJOBN,INJOBN    MOVE JOBNAME
         MVC   JJOB#(5),INJOB#+3   MOVE JOB NUMBER
         MVC   JJOB#+5(3),INJOB#    MOVE JOBTYPE
*
         MVC   JMCLASS,INMCLASS MOVE MSGCLASS
         MVC   JTTR,INTTR       MOVE IN TTR
         MVC   JDAY,INDAY       MOVE IN DAY
         MVC   JLINES,=PL4'0'   ZERO LINE COUNT
         MVC   JDEVT,=C'T'      ASSUME TAPE
         MVC   JADR,HADR        MOVE IN TYPE CODE
         MVC   JDTSTAMP,PSTAMP  MOVE IN DATE/TIME STAMP
*  ADD RECORD TO JOURNAL
         PUT   RPL=JOURNRPL    WRITE OUT NEW JOURNAL RECORD
         LTR   R15,R15         UPDATE OK
         BZ    READOLD         GET NEXT RECORD
         WTO   'SOUTCVT - DUPLICATE RECORD IN CONVERSION'
         B     READOLD         GET NEXT RECORD
         EJECT
SETDSN   EQU   *
         LA    R4,RECORD       SET R4 TO RECORD ADDR
         LA    R4,48(R4)       SET TO END OF DSN
         LA    R5,44
LOOP1    CLI   0(R4),C' '      LOOKING FOR LAST BYTE
         BNE   ENDLOOP
         SH    R4,=H'1'        BACK UP 1
         BCT   R5,LOOP1        TRY AGAIN
ENDLOOP  EQU   *
         SH    R4,=H'8'         BACK UP TO BEGINNING OF GDG
         BNP   NOGDG            TOO FAR - NOT GDG
         CLC   0(2,R4),=C'.G' IS IT .G  ?
         BNE   NOGDG            NOT A GDG
         CLC   6(1,R4),=C'V'    IS IT "V"
         BNE   NOGDG            NOT A GDG
         MVC   HSTAMP+5(4),2(R4)   MOVE IN GDG
         MVC   HSTAMP+10(2),7(R4) MOVE IN VERSION
         B     PACKSTMP
NOGDG    MVC   HSTAMP+5(6),=C'999999' SET TO HIGH
PACKSTMP EQU   *
         MVC   HSTAMP(5),=C'99999' SET TO HIGH VALUE
         PACK  PSTAMP,HSTAMP     PACK IT
         B     READOLD
         EJECT
***********************************************************************
*                                                                     *
*        END OF FILE ON INPUT, INFORM THE EXIT ROUTINE (IF ANY)       *
*        AND CLOSE THE INPUT/OUTPUT FILES(S).                         *
*                                                                     *
***********************************************************************
ENDSOUT  EQU   *
         CLOSE JOURNACB        CLOSE THE JOURNAL VSAM
         CLOSE (INPUT)         CLOSE THE INPUT FILE
         SR    R15,R15         CLEAR RETCODE
EXIT     $EPILOG ,             RETURN TO CALLER
ABEND1   ABEND 1,DUMP
ABEND2   ABEND 2,DUMP
ABEND3   ABEND 3,DUMP
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS AND WORK AREAS                                     *
*                                                                     *
***********************************************************************
         LTORG
HADR     DC    CL1' '
HDATE    DC    CL9' '
HSTAMP   DC    CL11' '
PDATE    DC    PL5'0'
HTIME    DC    CL7' '
PTIME    DC    PL4'0'
PSTAMP   DC    PL6'0'
         DS    D
         DC    CL12'START INPUT='
RECORD   DS    0CL50
INDATE   DS    CL8,CL1
INTIME   DS    CL8,CL1
INJOBN   DS    CL8,CL1
INJOB#   DS    CL8,CL1
INMCLASS DS    CL1,CL1
INTTR    DS    CL3,CL8
INDAY    DS    CL1
         DS    D
*        THE FOLLOWING IS THE JOURNAL RECORD FOR OUTPUT
JOURNREC DS    0CL42              JOURNAL RECORD
JJOBN    DC    CL8' '
JDATE    DC    PL5'0'
JTIME    DC    PL4'0'
JJOB#    DC    CL8' '
JMCLASS  DC    CL1' '
JTTR     DC    XL3'000000'
JDAY     DC    X'FF'
JLINES   DC    PL4'0'
JDEVT    DC    XL1'00'
JADR     DC    CL1' '
JDTSTAMP DC    PL6'0'
         EJECT
JOURNACB ACB   AM=VSAM,DDNAME=JOURNAL,MACRF=(KEY,DIR,OUT)
JOURNRPL RPL   ACB=JOURNACB,AREA=JOURNREC,AREALEN=42,KEYLEN=22,        X
               OPTCD=(KEY,DIR,SYN,NUP,MVE,KEQ,FKS),RECLEN=42,          X
               ARG=JOURNREC
INPUT    DCB   MACRF=(GM),DSORG=PS,DDNAME=INPUT,EODAD=ENDSOUT
OUTPUT   DCB   MACRF=(PM),DSORG=PS,DDNAME=OUTPUT,RECFM=FB,LRECL=42
         END
