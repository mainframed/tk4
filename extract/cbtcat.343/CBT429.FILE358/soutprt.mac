         PRINT OFF
         MACRO
         PROLOG
**********************************************************************
*
*        THIS IS THE SYSOUT BROWSE SUBSYSTEM HARDCOPY UTILITY.
*        IT PROVIDES HARDCOPY SUPPORT TO ALLOW ARCHIVED SYSOUT
*        TO BE LOCATED AND PRINTED (LOCAL OR REMOTE).
*
*        THIS PROGRAM IS CALLED WHEN THE SOUTSYS SYSTEM REQUESTS
*        A REPORT BROWSE IN THE BATCH MODE. THE ISPF PANEL BUILDS
*        THE PROPER PARM TO BE PASSED TO THIS ROUTINE.
*        THE PARM FORMAT FOLLOWS;
*
*        1-8  - JOB NAME
*        9-13 - JOB NUMBER (5 CHARS;  DO NOT INCLUDE STC OR JOB)
*       14-23 - DATE IN FORMAT  MM/DD/YYYY
*       24-31 - TIME IN FORMAT  HH:MM:SS
*       32-39 - DD NAME WHERE OUTPUT WILL PRINT (USUALLY SYSPRINT)
*       40-60 - PREFIX (IE:SYS2.OPSWTR) 21 CHARS MAX
*       61    - AUTHORIZATION BYTE (SET TO X'04')
*       62-71 - DATE LIMIT FOR "LIST" USED IN 4.S ONLY
*       72-96 - OLD SYSTEM PREFIX
*
*
*
*        THE OUTPUT LISTING FOR THE NAMED JOB WILL BE WRITTEN TO THE
*        SYSPRINT FILE.  FOR EACH POSSIBLE RETURN CODE FROM SOUTSUB,
*        A LIKE WTP MESSAGE WILL BE RETURNED.
*
*        EXAMPLE JCL:
*
*
*        //JOBNAME  EXEC PGM=SOUTPRT,PARM='JOBX    '  (8 CHARS REQ'D)
*        //SYSPRINT DD   SYSOUT=A          JJJJJJJJ
*
*
*        //JOBNUM   EXEC PGM=SOUTPRT,PARM='JOBX    324  ' (13 CHARS)
*        //SYSPRINT DD   SYSOUT=A          JJJJJJJJNNNNN
*
*
*        //NAMEDATE EXEC PGM=SOUTPRT,PARM='JOBX    0001 08/25/1984'(23)
*        //SYSPRINT DD   SYSOUT=A          JJJJJJJJNNNNNMM.DD.YYYY
*
*
*   //FULL     EXEC PGM=SOUTPRT,
*   //         PARM='JOBX    324  08/25/8012:08:23SYSPRINTSYSU.OPSTAPE'
*   //SYSPRINT DD   SYSOUT=A      MM.DD.YYHH.MM.SS  DDN   QUALIFIER
*
*   NOTE: USING THE SPF SYSTEM (CMNFS00) AS THE DRIVER, SELECTING THE
*         PRINT OPTION, SPF WILL BUILD THIS PARM IN THE PROPER FORMAT
*         FOR YOU
*
****** MODIFIED BY J MCCLOSKEY - F.W. WOOLWORTH 04/81
*          ADDED SYSPRINT AND QUALIFIER TO VALID PARM INFO TO SUPPORT
*          MULTIPLE QUALIFIERS.
*
*        ATTRIBUTES: NOT REENTRANT AUTHORIZED
*
*        REGISTERS USED:
*            R0 - WORK
*            R1 - WORK
*            R2 - WORK
*            R3 - WORK
*            R4 - ------ (UNUSED)
*            R5 - ------ (UNUSED)
*            R6 - ------ (UNUSED)
*            R7 - ------ (UNUSED)
*            R8 - ------ (UNUSED)
*            R9 - RETURN CODE (INITIALLY CLEARED)
*           R10 - PARM VALUE POINTER (TEMP)
*           R11 - PARM LIST POINTER  (TEMP)
*           R12 - BASE
*           R13 - SAVE AREA
*           R14 - WORK
*           R15 - WORK
*
*        RETURN CODES: 99 = INVALID PARM SPECIFIED (LENGTH ERROR)
*                      SEE SOUTSUB FOR ANY OTHERS
*
**********************************************************************
         MEND
SOUTPRT  $PROLOG
         L     R1,0(R1)           GET PARM POINTER
         LH    R2,0(R1)           PARM SIZE
         LH    R4,0(R1)           PARM SIZE
         LTR   R2,R2              ANY PARM?
         BZ    ERROR              NO, ERROR
         LA    R3,P1              POINT TO FIRST PARM
FILTER1  CLI   2(R1),C','         ARE WE LOOKING AT A COMMA?
         BNE   FILTER2            NO, USE IT
         BCT   R2,FILTER3         DECREMENT ACTUAL LENGTH USED
FILTER2  MVC   0(1,R3),2(R1)      FILL IN ONE BYTE OF PARM
         LA    R3,1(R3)           BUMP TO NEXT VARIABLE POSITION
FILTER3  LA    R1,1(R1)           BUMP TO NEXT INPUT CHAR
         BCT   R4,FILTER1         REPEAT
         CH    R2,=H'8'           VALID PARM?
         BE    PARMOK             YES, CONTINUE
         CH    R2,=H'13'          VALID PARM?
         BE    PARMOK             YES, CONTINUE
         CH    R2,=H'23'          VALID PARM?
         BE    PARMOK             YES, CONTINUE
         CH    R2,=H'31'          VALID PARM?
         BE    PARMOK             YES, CONTINUE
         CH    R2,=H'39'          VALID PARM?
         BE    PARMOK             NO, ERROR
         CH    R2,=H'60'          VALID PARM?
         BE    PARMOK             NO, ERROR
         CH    R2,=H'61'          VALID PARM?
         BE    PARMOK             NO, ERROR
         CH    R2,=H'71'          VALID PARM?
         BE    PARMOK             NO, ERROR
         CH    R2,=H'96'          VALID PARM?
         BE    PARMOK             NO, ERROR
         CH    R2,=H'97'          VALID PARM?
         BL    PARMOK             NO, ERROR
ERROR    WTO   'SOUTPRT - INVALID PARM SPECIFIED',ROUTCDE=11
         LA    R15,99             SET RETURN CODE
         B     EXIT               RETURN
PARMOK   LA    R1,SOUTSUBP        ADDRESS OF SOUTSUB PARM
         LINK EP=SOUTSUB          CALL THE SOUTSUB ROUTINE
EXIT     $EPILOG ,                RETURN TO SYSTEM
         LTORG
SOUTSUBP DC    A(P1,P2,P3,P4,P5,P6,P7,P8,P9)  LIST OF ADDRESSES
P1       DC    CL8' '             JOBNAME
P2       DC    CL5' '             JOBNUMBER
P3       DC    CL10' '             DATE
P4       DC    CL8' '             DATE
P5       DC    CL8'SYSPRINT'      DDNAME
P6       DC    CL21' '            QUALIFIER FOR SOUT DATASETS
P7       DC    X'00'              FLAG BYTE (AUTHORIZED)
P8       DC    CL10' '            DATE LIMIT FOR SEARCH
P9       DC    CL25' '            QUALIFIER FOR OLD SOUT DATASETS
         END
