         PRINT NOGEN
***********************************************************************
*  SOUTDATE WILL TAKE A 5 BYTE NUMERIC FIELD AND BACKUP
* UP THE DATE THE NUMBER OF DAYS SPECIFIED.
* IT WILL RETURN A 10 BYTE DATE IN THE FORMAT MM/DD/YYYY
*
* NO SPECIAL EDITING OF THE DATA PASSED IS DONE.
*
* THIS PROGRAM IS AN EXTRACT OF THE $STCK MACRO.
*
***********************************************************************
SOUTDATE $PROLOG
         L     R10,0(R1)
         L     R9,4(R1)
         USING DATEAREA,R9
         PACK  PACK3,0(5,R10)
         STCK  8(13)              STORE THE TIME-OF-DAY CLOCK
         LM    0,1,8(13)          LOAD INTO WORK REGS
*START TIME
         L     15,16              GET CVT ADDRESS
         A     0,304(15)          ADD LOCAL TIME DEVIATION
         SRDL  0,12               ISOLATE NUMBER OF MICROSECONDS
         D     0,=F'60000000'     DIVIDE BY 60M (R1=MINUTES)
         LR    15,0               COPY REMAINDER OF MICS TO GET SECS
         SR    14,14              CLEAR FOR DIVIDE
         D     14,=F'1000'        DIVIDE TO GET REMAINING MILLISECONDS
         LR    14,15              COPY TO WORK REG
         CVD   15,8(13)           CONVERT SECONDS TO PACKED FORMAT
         CVD   1,8(13)            CONVERT MINUTES TO PACKED FORMAT
         DP    8(8,13),=P'60'     DIVIDE INTO HOURS AND MINUTES
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         DP    8(8,13),=P'24'     DIVIDE INTO DAYS AND HOURS
* END TIME
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         ZAP   16(8,13),8(8,13)   SAVE THE NUMBER OF DAYS
         DP    8(8,13),=P'7'      DIVIDE BY NUMBER OF DAYS IN A WEEK
         ZAP   8(8,13),15(1,13)   FILL DOUBLEW WITH THE REMAINDER
* END DAY
         ZAP   8(8,13),16(8,13)   MOVE # DAYS TO WORK AREA
*
         SP    8(8,13),PACK3      DECREMENT THE DATE
*
         AP    8(8,13),=P'1'      ADD ONE DAY
         DP    8(8,13),=P'1461'   DIVIDE BY # DAYS IN FOUR YEARS
         ZAP   16(8,13),8(5,13)   SAVE # FOUR YEAR PERIODS
         MP    16(8,13),=P'4'     GET TO LAST 4 YEAR PERIOD
         XC    8(5,13),8(13)      CLEAR FOR DIVIDE
         LA    15,YEARTBL         LOAD TABLE ADDRESS FOR NO LEAP
         CP    16(8,13),=P'4'     IS IT PRIOR TO 1904?
         BL    NOLEAP             YES, CAN'T BE LEAP YEAR
         LA    15,LEAPTBL         LOAD TABLE ADDRESS FOR LEAP YEAR
         AP    8(8,13),=P'1'      ADD ONE MORE DAY FOR 1900
         CP    8(8,13),=P'367'    IS IT LEAP YEAR?
         BL    LEAPYR             YES DON'T SUBTRACT
         LA    15,YEARTBL         LOAD TABLE ADDRESS FOR NO LEAP YEAR
         AP    16(8,13),=P'1'     ADD ONE YEAR
         SP    8(8,13),=P'366'    SUBTRACT REMAINING DAYS FOR ONE LEAP
NOLEAP   CP    8(8,13),=P'366'    LESS THAN ONE YEAR REMAINING?
         BL    LEAPYR             YES, GET OUT OF LOOP
         SP    8(8,13),=P'365'    SUBTRACT REMAINING DAYS FOR ONE YEAR
         AP    16(8,13),=P'1'     ADD ONE YEAR
         B     NOLEAP             GO BACK TO COMPARE
LEAPYR   ZAP   24(8,13),=P'1900'  DEFAULT TO 1900'S
         AP    24(8,13),16(8,13)  ADD NUMBER YEARS SINCE 1900
         UNPK  32(5,13),29(3,13)  UNPACK YEAR
         OI    36(13),X'F0'       SET UP FOR PRINTING
         MVC   DATE+6(4),33(13)    MOVE INTO OUTPUT FIELD
         MP    16(8,13),=P'1000'  MAKE ROOM FOR # DAYS
         AP    16(8,13),8(8,13)   ADD IN DAYS TO FORM JULIAN DATE
         ZAP   24(4,13),16(8,13)  MOVE JUL DATE TO WORK AREA
         MVO   14(2,13),25(1,13)  MOVE YEAR PORTION
*        UNPK  DATE+6(2),14(2,13) UNPACK INTO OUTPUT AREA
         LR    14,15              SAVE TABLE IN USE
         ZAP   8(8,13),26(2,13)   MOVE DAY PORTION
         CVB   0,8(13)            CONVERT TO BIN
         SR    1,1                CLEAR REG
         IC    1,0(15)            LOAD CHAR FROM TABLE
         LA    15,1(15)           INDEX TABLE POINTER
         SR    0,1                SUBTRACT FROM WORK VALUE
         BP    *-10               STILL POSITIVE, DO AGAIN
         AR    0,1                ELSE, ADD IT BACK
         CVD   0,8(13)            CONVERT TO DEC
         UNPK  DATE+3(2),14(2,13) UNPACK DAY INTO OUTPUT
         SR    15,14              FIND VALUE OF CURRENT INDEX
         CVD   15,8(13)           CONVERT TO DEC
         UNPK  DATE(2),14(2,13)   UNPACK MONTH INTO OUTPUT
         OI    DATE+1,C'0'        INSURE NUMERICS
         OI    DATE+4,C'0'        INSURE NUMERICS
         OI    DATE+7,C'0'        INSURE NUMERICS
         MVI   DATE+2,C'/'        ADD SLASH
         MVI   DATE+5,C'/'        ADD SLASH
         SR    R15,R15
         $EPILOG
YEARTBL  DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31) MONTH TABLE
LEAPTBL  DC    AL1(31,29,31,30,31,30,31,31,30,31,30,31) LEAP YEAR
PACK3    DC    PL3'0'
         LTORG
DATEAREA DSECT
DATE     DS    CL10
         END
