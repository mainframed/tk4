*********************************************************************
*                                                                   *
*                        HASP EXIT 4                                *
*                                                                   *
*     This exit gets control whenever JES2 encounters a JCL or      *
*     JES2 control statement and performs the following functions:  *
*                                                                   *
*     > Parses any "/*ROUTE XEQ resname" control statements that    *
*       have been specified.  Any Mellon resource name that has     *
*       been specified is then converted to a WLM scheduling        *
*       environment.                                                *
*                                                                   *
*     Entry point: EXIT04                                           *
*                                                                   *
*     Input registers:                                              *
*        R0     Code indicating whether a JES2 control or JCL       *
*               statement is being processed.                       *
*               0 = JES2 control statement                          *
*               4 = JCL statement                                   *
*        R1     Pointer to a three word parameter list formatted    *
*               as follows:                                         *
*               +0  Address of image buffer                         *
*               +4  Address of RDWFLAGX                             *
*               +8  Address of JCTXWRK                              *
*        R2-9   N/A                                                 *
*        R10    JCT address or zero                                 *
*        R11    HCT address                                         *
*        R12    N/A                                                 *
*        R13    PCE address                                         *
*        R14    Return address                                      *
*        R15    Entry address                                       *
*                                                                   *
*     Output registers:                                             *
*        R0-14  Unchanged                                           *
*        R15:   Return code                                         *
*                                                                   *
*     Register usage(internal):                                     *
*        R0-9   Work registers                                      *
*        R10    JCT address                                         *
*        R11    HCT address                                         *
*        R12    Base register                                       *
*        R13    PCE address                                         *
*        R14-15 Work and linkage registers                          *
*                                                                   *
*     Note: This module is for use with JES2 Version 6.0.4          *
*                                                                   *
*     Author: Bob Break                                             *
*             St. Louis, Mo.                                        *
*                                                                   *
*********************************************************************

         COPY  $HASPGBL

MELLON04 $MODULE ENVIRON=JES2,                                         X
               RMODE=ANY,                                              X
               SPLEVEL=NOCHECK,                                        X
               CVT,                                                    X
               PSA,                                                    X
               RPL,                                                    X
               $BUFFER,                                                X
               $CADDR,                                                 X
               $DCT,                                                   X
               $HASPEQU,                                               X
               $HCT,                                                   X
               $HCCT,                                                  X
               $JCT,                                                   X
               $MIT,                                                   X
               $MITETBL,                                               X
               $PADDR,                                                 X
               $PARMLST,                                               X
               $PCE,                                                   X
               $PSV,                                                   X
               $RDRWORK,                                               X
               $SQD,                                                   X
               $TRE,                                                   X
               $USERCBS,                                               X
               $XECB

EXIT04  $ENTRY BASE=R12            Exit routine entry point

*********************************************************************
*                                                                   *
*        Save caller's registers and establish addressabilities.    *
*                                                                   *
*********************************************************************

         USING JCT,R10
EXIT000 $SAVE  NAME=EXIT04         Save caller's registers
         LR    R12,R15             Set base register

*********************************************************************
*                                                                   *
*        Determine if this exit was entered for a JES2 control      *
*        statement.                                                 *
*                                                                   *
*********************************************************************

         LTR   R0,R0               Processing JES2 control statement?
         BNZ   RETURN0             No - branch, return

*********************************************************************
*                                                                   *
*        Determine if a JCT is present.                             *
*                                                                   *
*********************************************************************

         LTR   R10,R10             JCT present?
         BZ    RETURN0             No - branch, return

*********************************************************************
*                                                                   *
*        Determine if a "/*ROUTE XEQ" control statement is being    *
*        processed.                                                 *
*                                                                   *
*********************************************************************

         L     R3,0(,R1)           Get contol statement image address
         CLC   ROUTCARD,0(R3)      "/*ROUTE XEQ" statement?
         BNE   RETURN0             No - branch, return

*********************************************************************
*                                                                   *
*        Isolate the resource name or JES2 destination.             *
*                                                                   *
*********************************************************************

         LA    R3,L'ROUTCARD+1(,R3)  Point to resource name
         TRT   0(9,R3),TRANSTBL     Find end of resource name
         SLR   R1,R3                Calculate resource name length
         LR    R2,R1                Save resource name length

*********************************************************************
*                                                                   *
*        Determine if a valid JES2 destination has been specified.  *
*        If not, process the symbolic as a resource name that is to *
*        be converted to a scheduling environment.                  *
*                                                                   *
*********************************************************************

        $DEST  DEST=(R3),LEN=8,NODE=$OWNNODE,RMTPOOL=NO
         LTR   R15,R15              Valid destination?
         BZ    RETURN0              Yes - branch, return
        $DEST  DEST=(R3),LEN=8,NODE=$OWNNODE,RMTPOOL=NO,EXPLICIT=YES
         LTR   R15,R15              Valid destination?
         BZ    RETURN0              Yes - branch, return

*********************************************************************
*                                                                   *
*        Set the scheduling environment in the JCT.                 *
*                                                                   *
*********************************************************************

         BCTR  R2,0                 Minus 1 for execute
         MVI   JCTSCHEN,C' '        Blank init scheduling environment
         MVC   JCTSCHEN+1(L'JCTSCHEN-1),JCTSCHEN  *
         MVC   JCTSCHEN(0),0(R3)    Executed instruction
         EX    R2,*-6               Set scheduling environment

*********************************************************************
*                                                                   *
*        Convert the "/*ROUTE XEQ" statement to a JCL comment       *
*        statement.  This will prevent an invalid destination from  *
*        causing a jcl error.                                       *
*                                                                   *
*********************************************************************

         B     RETURN8              Branch - set return code = 8

*********************************************************************
*                                                                   *
*        Return to caller.                                          *
*                                                                   *
*********************************************************************

RETURN0  XR    R15,R15              Set return code = 0
         B     RETURN               Branch - continue
RETURN8  L     R15,$F8              Set return code = 8
RETURN  $RETURN RC=(R15)            Return to caller

         LTORG

ROUTCARD DC    C'/*ROUTE XEQ'       "/*ROUTE XEQ" fixed text
TRANSTBL DC    256X'00'             EXIT04 translate table
         ORG   TRANSTBL+C' '
         DC    X'FF'                ' '

         DROP  R10                  JCT

        $MODEND

         END

