AIDSYNC  TITLE 'ACCOUNTING DATA-SET SYNCHRONIZATION.'
AIDSYNC  START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *         ACCOUNTING DATA-SET         * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *          SYNCHRONIZATION            * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*
* PURPOSE :    RE-SYNCHRONIZE THE ACCOUNTING DATA-SET INFORMATION
* ---------    (AUTHORIZATIONS-PROGRAMMERS RECORDS) WITH THE BIT MAP
*              TABLE. THIS MAY BE NECESSARY IF AN UPDATE OF THE
*              DATA-SET HAS BEEN INTERRUPTED, AND SO THE BIT MAP TABLE
*              DOES NOT REFLECT THE TRUE CONTENTS.
*
* CALL OF THE PROGRAM :
* ---------------------
*
*        //SYNCHR EXEC  PGM=AIDSYNC
*        //SYSACC   DD  DSN=...,DISP=OLD OR SHR
*        //SYSACT   DD  DSN=...,DISP=OLD OR SHR
*        //SYSPRINT DD  SYSOUT=A ---> SYNCHRONIZATION LIST
*
* RETURN CODES :   0 = NORMAL COMPLETION
* --------------  16 = ABNORMAL COMPLETION, MESSAGE(S) ON SYSPRINT OR
*                      JOB LOG.
*
* OPTION :     PARM=S TO JUST SIMULATE THE SYNCHRONIZATION PROCESS
* --------            (THIS ALLOWS TO DO A PREVIOUS TEST RUN).
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
         EJECT
        $AIRD  SEC=YES
         SPACE 1
MXSP     EQU   (((NAUTNO+NPRGNO+2)+7)/8)*8
NOOP     EQU   X'00'               NO OPERATION
ADDOP    EQU   X'01'               ADD OPERATION
DELOP    EQU   X'02'               DELETE OPERATION
         EJECT
        $DEFREG
         SPACE 1
*---------------------------------------------------------------------*
*        AUTHORITY LEVELS DEFINITIONS.                                *
*---------------------------------------------------------------------*
         SPACE 1
LV12     EQU   12        ADMINISTRATOR CONTROL LEVEL
         SPACE 1
AUT98   $MDL@IX
         EJECT
        $XENT  BASE=R12
         L     R1,0(R1)            PARM. FIELD ADDRESS
         LH    R2,0(R1)            PARM. FIELD LENGTH
         LTR   R2,R2
         BNP   NOPARM
         CH    R2,=H'1'
         BNE   ERP
         CLI   2(R1),C'S'          'S' SIMULATE?
         BNE   ERP                 NO
         OI    SWITCH,SWS          YES
         B     NOPARM
ERP      LA    R2,PARMERR
         BAL   R5,PRTRTN
         NOP   0
         B     LEAVE
         EJECT
NOPARM  $EACM  REQAUT
         CLI   AUTLVL,LV12
         BL    NOTAUT
         L     R1,=V(YOPEN)
         LA    R14,1
         L     R15,=V(ZOPEN)
         BXLE  R1,R14,*+L'*+4
         B     NOTFND
         CLC   0(8,R1),=CL8'SYSACC'
         BNE   *-14
         MVI   5(R1),C'T'          CHANGE TO 'SYSACT' IN YOPEN
         L     R1,=A(MXSP)
        GETMAIN R,LV=(R1)
         ST    R1,AMXSP
         LR    R6,R1
         LH    R7,=Y(NAUTNO+NPRGNO+2)
         LR    R8,R6
         XR    R9,R9
         MVCL  R6,R8
         XR    R6,R6               NO MSG'S
         XR    R7,R7
         XR    R8,R8
         TM    SWITCH,SWS          SIMULATE?
         BO    STONLY              YES
        CALL   ZOPEN,(IR),VL       OPEN 'SYSACC'
         B     TSTZO
STONLY  CALL   ZOPEN,(IR,XOPT),VL  OPEN 'SYSACC'
TSTZO    LTR   R0,R15              COMPLETION?
         BZ    READ                OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R2,R15
         BAL   R5,PRTRTN
         NOP   0
         B     LEAVE
READ    CALL   ZSEQR,(IR,DATA,ARG),VL   READ DATA 'SYSACC'
         LTR   R0,R15              COMPLETION?
         BM    EOF                 END OF DATA
         BZ    RDSOK               OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R7,R15              RETAIN MSG ADDRESS
         B     ERRZ
RDSOK    CLC   ARG(2),=Y(NAUTNO+NPRGNO+1)    ARE WE OVER?
         BH    EOF                 YES, END OF USEFULL DATA
         LH    R1,ARG              NO
         AL    R1,AMXSP
         MVI   0(R1),DELOP
         B     READ
EOF     CALL   YOPEN,(IR),VL       OPEN 'SYSACT'
         LTR   R0,R15              COMPLETION?
         BZ    STRDR               OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R2,R15
         BAL   R5,PRTRTN
         NOP   0
         B     ERRZ+L'ERRZ
STRDR    LA    R9,1
RDREC    STH   R9,ARG
        CALL   YREAD,(IR,ARG,DATA),VL
         LTR   R0,R15              COMPLETION?
         BZ    *+L'*+8             OK
         CH    R15,=H'15'          ARGUMENT NOT FOUND?
         BNE   ERRDG               NO, ERROR
         LR    R1,R9
         AL    R1,AMXSP
         CLC   DATA(AUTLGTH),DTNULL     CHECK CONTENTS
         BE    CKEND
         LTR   R0,R15              HOW COMPLETION WAS?
         BZ    *+L'*+8             OK
         MVI   0(R1),DELOP
         B     CKEND
         CLI   0(R1),NOOP
         BE    *+L'*+8
         MVI   0(R1),NOOP
         B     CKEND
         MVI   0(R1),ADDOP
CKEND    CH    R9,=Y(NAUTNO+NPRGNO+1)   IS IT THE END?
         BE    EOR                 YES
         LA    R9,1(R9)            NO, NEXT
         CH    R9,=Y(NAUTNO+1)
         BNE   RDREC
         LA    R9,1(R9)
         B     RDREC
EOR      LA    R9,1
NXTRC    LR    R1,R9
         AL    R1,AMXSP
         CLI   0(R1),NOOP
         BE    ENDCK
         STH   R9,ARG
         CLI   0(R1),DELOP
         BE    DELRC
        CALL   YREAD,(IR,ARG,DATA),VL
         LTR   R0,R15              COMPLETION?
         BNZ   ERRDG               ERROR
         TM    SWITCH,SWS          SIMULATE?
         BO    STADD               YES
        CALL   ZWRITE,(IR,ARG,DATA),VL
         LTR   R0,R15              COMPLETION?
         BNZ   ERRDG               ERROR
STADD    MVC   LINE+L'MASK+10(7),=CL7'- ADDED'
         B     PRTOP
DELRC    TM    SWITCH,SWS          SIMULATE?
         BO    STDEL               YES
        CALL   ZDELET,(IR,ARG),VL
         LTR   R0,R15              COMPLETION?
         BNZ   ERRDG               ERROR
STDEL    MVC   LINE+L'MASK+10(9),=CL9'- DELETED'
PRTOP    MVC   LINE+6(3),=CL3'KEY'
         XR    R0,R0               LIST THE ARG. NO.
         ICM   R0,B'0011',ARG
         CVD   R0,WORKD
         MVC   LINE+9(L'MASK),MASK
         ED    LINE+9(L'MASK),WORKD+5
         LA    R2,LINE             DISPLAY KEY OPERATION
         BAL   R5,PRTRTN
         B     ERRY
         MVC   LINE+1(L'LINE-1),LINE
ENDCK    CH    R9,=Y(NAUTNO+NPRGNO+1)   IS IT THE END?
         BE    ALLPR               YES
         LA    R9,1(R9)            NO, NEXT
         CH    R9,=Y(NAUTNO+1)
         BNE   NXTRC
         LA    R9,1(R9)
         B     NXTRC
ALLPR   CALL   YCLOSE,(IR),VL      CLOSE 'SYSACT'
         LTR   R0,R15              COMPLETION?
         BZ    ERRZ+L'ERRZ         OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R7,R15              RETAIN MSG ADDRESS
         B     ERRZ
NOTAUT   LA    R2,AUTMSG
         BAL   R5,PRTRTN
         NOP   0
         OI    SWITCH,SWE          SAY ERROR
         B     LEAVE
NOTFND   LA    R2,SCRMSG
         BAL   R5,PRTRTN
         NOP   0
         OI    SWITCH,SWE          SAY ERROR
         B     LEAVE
ERRDG    XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R6,R15              RETAIN MSG ADDRESS
ERRY    CALL   YCLOSE,(IR),VL      CLOSE 'SYSACT'
         LTR   R0,R15              COMPLETION?
         BZ    ERRZ                OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R7,R15              RETAIN MSG ADDRESS
ERRZ     OI    SWITCH,SWD          ERROR, DISPLAY DATA
        CALL   ZCLOSE,(IR),VL      CLOSE 'SYSACC'
         LTR   R0,R15              COMPLETION?
         BZ    EXIT                OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R8,R15              RETAIN MSG ADDRESS
EXIT     TM    SWITCH,SWE          PRINT ERROR?
         BO    LEAVE               YES
         LTR   R2,R6               PENDING MSG?
         BZ    *+L'*+8             NO
         BAL   R5,PRTRTN
         B     LEAVE
         LTR   R2,R7               PENDING MSG?
         BZ    *+L'*+8             NO
         BAL   R5,PRTRTN
         B     LEAVE
         LTR   R2,R8               PENDING MSG?
         BZ    *+L'*+8             NO
         BAL   R5,PRTRTN
         B     LEAVE
         LA    R2,MEND
         BAL   R5,PRTRTN
         B     LEAVE
         TM    SWITCH,SWD          ERROR?
         BZ    LEAVE               NO
         LA    R2,SNPMSG           YES, DISPLAY DATA
         BAL   R5,PRTRTN
         B     LEAVE
        $SNAP  ARG,LGS
         LTR   R15,R15
         BZ    LEAVE
         OI    SWITCH,SWE          SAY ERROR
LEAVE    ICM   R1,B'1111',AMXSP
         BZ    QUIT
         L     R0,=A(MXSP)
        FREEMAIN R,A=(1),LV=(0)
QUIT    $PCLOSE ,                  CLOSE ALL AND EXIT
         LTR   R15,R15
         BZ    *+L'*+4
         OI    SWITCH,SWE          SAY ERROR
         XR    R10,R10             R.C. = 0
         TM    SWITCH,SWD+SWE
         BZ    *+L'*+4
         LA    R10,16              SET RC = 16
        $XRET  CC=(R10)
         EJECT
PRTRTN  $PRINT (R2)                PRINT ROUTINE (LINK = R5)
         LTR   R15,R15             R2 = LINE POINTER
         BZ    4(R5)               RETURN +4 = PRINT DONE
         OI    SWITCH,SWE          SAY ERROR
         BR    R5                  RETURN +0 = PRINT ERROR
         EJECT
*        CONSTANTS AND WORK AREAS.
*        -------------------------
         SPACE 1
WORKD    DC    D'0'
AMXSP    DC    A(*-*)
IR       DC    F'0'
XOPT     DC    C'ONLY'
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUTLVL)
         SPACE 1
LGS      DC    A(AUTLGTH+L'ARG)
ARG      DC    H'0'
DATA     DC    (AUTLGTH)X'0'
DTNULL   DC    (AUTLGTH)X'0'
         SPACE 1
MASK     DC    XL6'402020202120'
AUTLVL   DC    XL1'0'
SWITCH   DC    XL1'0'
SWE      EQU   X'80'
SWD      EQU   X'40'
SWS      EQU   X'01'
         SPACE 1
         DC    AL1(L'LINE)
LINE     DC    CL121'  '
         DC    AL1(L'MEND)
MEND     DC    C'0 --- SYNCHRONIZATION ENDED ---'
         DC    AL1(L'PARMERR)
PARMERR  DC    C'0 INVALID PARM. FIELD - EXIT-'
         DC    AL1(L'AUTMSG)
AUTMSG   DC    C'0 UNAUTHORIZED ACCESS TO THIS PROGRAM - EXIT -'
         DC    AL1(L'SCRMSG)
SCRMSG   DC    C'0 SCRAMBLE ERROR DETECTED - EXIT -'
         DC    AL1(SNPMSGL)
SNPMSG   DC    C'0 HERE FOLLOWS THE DUMP OF LAST '
         DC    C' ARGUMENT AND DATA :'
SNPMSGL  EQU   *-SNPMSG
         EJECT
        LTORG
         SPACE 2
         END
