B003     TITLE 'CLEAN-UP - ACCOUNTING PROCESS (B003).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B003    @XS
*        ---------------------
*        DATA-SETS ACCOUNTING.
*        ---------------------
*
*        AT ENTRY :   R4 = 0 : START OF VTOC READ.
*                     R4 = + : DSCB'S VTOC READ (DSCB FMT POINTER).
*                     R4 = - : END OF VTOC READ.
         SPACE 1
         USING DDNT,R2
         LTR   R4,R4               WHICH ENTRY CALLED?
         BM    QE0                 END VTOC
         BP    QR0                 DSCB READ
         EJECT
*        START VTOC READ.
*        ----------------
         SPACE 1
         LA    R3,CARDOU
         USING ACDSECT,R3
QSSW     NOP   QS2
         OI    QSSW+1,ON
        $VTOCDR LOCAREA,,MF=(L,QRDR)
        $PUNCH (R3),,DDDATA,MF=(L,QRPU)
         MVI   0(R3),C' '
         MVC   1(L'CARDOU-1,R3),0(R3)
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    QS1                 NO, SKIP THE LIST
         LA    R5,QTITYDD          SET TODAY DATE (YDD)
         LA    R15,TYDD+1
         XR    R0,R0
         ICM   R0,B'0111',0(R15)
         STC   R0,4(R5)
         SRL   R0,4
         STC   R0,3(R5)
         SRL   R0,4
         STC   R0,2(R5)
         SRL   R0,8
         STC   R0,1(R5)
         SRL   R0,4
         STC   R0,0(R5)
         NC    0(5,R5),=X'0F0F0F0F0F'
         TR    0(5,R5),TRTAB
        @LIST  QTIT,,U
QS1      XC    COUNTER,COUNTER
         MVC   ACDATE(L'ACDATE),SYDD    SET DATE OF ACCOUNT
QS2      MVC   QTITUN,DDNVOL
         MVC   QTITUT,DDNUTY
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    QS3                 NO, SKIP THE LIST
        @LIST  QTITU,=PL2'+50'
QS3      MVC   ACVLS,DDNVOL
         XR    R1,R1
         IC    R1,DVTYP+3
         STC   R1,ACTYP+1
         SRL   R1,4
         STC   R1,ACTYP
         NC    ACTYP(L'ACTYP),=XL2'0F0F'
         TR    ACTYP(L'ACTYP),TRTAB
         MVC   SYSUSE,=F'1'       VOLUME LABEL USE TRACK 0
         XC    TOTUSE,TOTUSE
         B     QXIT
         DROP  R3
         EJECT
*        DSCB'S VTOC READ.
*        -----------------
*
*        WHEN PROCESSING THE DATA-SETS DESCRIBED BY A VOLUME VTOC
*        DSCB'S FORMAT 1, THE FOLLOWING ACTIONS ARE TAKEN :
*
*        1. IF TEMPORARY OR UTILITY : BYPASS, IT SHOULD BE SCRATCHED
*           FURTHER BY ANOTHER PROCESSING.
*
*        2. IF G.D.G. MODEL : BYPASS.
         SPACE 1
         USING FMT1DSCB,R4
QR0      CLI   DS1FMTID,C'1'       DSCB'S FORMAT 1?
         BE    QR1                 YES
         LA    R5,DS4VTOCE         FORMAT 4 - VTOC EXTENT
         L     R6,SYSUSE           ADD TO TOTAL SYSTEM USE
         BAS   R14,QR50
         NOP   0
         ST    R6,SYSUSE
         B     QXIT
QR1      CLC   DS1DSNAM(3),=C'SYS' SKIP TEMPORARY DATA-SETS
         BNE   QR2
         CLC   DS1DSNAM+8(2),=C'.T'
         BNE   QR2
         CLC   DS1DSNAM+16(3),=C'.RA'
         BE    QXIT                YES, BYPASS
QR2      CLI   DS1DSNAM,C'*'       SKIP UTILITY DATA-SETS
         BE    QXIT
         CLC   DS1DSNAM(8),=CL8'SYSCTLG'     CATALOG DATA-SET?
         BE    QR3                 YES
         CLI   DS1EXT1,0           GDG MODEL?
         BE    QXIT                YES
QR3      LR    R3,R4               SAVE ORIGINAL WORK AREA
         XR    R6,R6               SPACE COUNTER (TRACKS)
         LA    R5,DS1EXT1          COMPUTE DATA-SET SPACE
         BAS   R14,QR50
         B     QXIT                EMPTY
         LA    R5,DS1EXT2
         BAS   R14,QR50
         B     QR10
         LA    R5,DS1EXT3
         BAS   R14,QR50
         B     QR10
QR4      OC    DS1PTRDS(L'DS1PTRDS),DS1PTRDS ADDITIONAL DSCB?
         BZ    QR10                NO
         TM    DDNALL,DDNXD        VTOC IS OPENED?
         BO    QR5                 YES
        $VTOCDO DDNNME,MF=(E,QRDO) OPEN VTOC FOR DIRECT ACCESS
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR27
         OI    DDNALL,DDNXD        SET OPENED
QR5      MVC   ADDRNB+L'ADDRNB-L'DS1PTRDS(L'DS1PTRDS),DS1PTRDS
         LA    R4,LOCAREA          SET ALTERNATE WORK AREA
        $VTOCDR MF=(E,QRDR)        READ DSCB CONTINUATION
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR28
         CLI   DS3FMTID,C'3'       IS THAT A FORMAT 3 DSCB?
         BNE   QR4                 NO
         LA    R15,4               SET FIRST 4 EXTENTS POINTER
         LA    R5,DS3EXTNT
QR6      BAS   R14,QR50
         B     QR10
         LA    R5,10(R5)
         BCT   R15,QR6
         LA    R15,9               SET LAST 9 EXTENTS POINTER
         LA    R5,DS3ADEXT
QR7      BAS   R14,QR50
         B     QR10
         LA    R5,10(0,R5)
         BCT   R15,QR7
QR10     LR    R4,R3               RESTORE ORIGINAL WORK AREA
         LA    R3,CARDOU           PREPARE ACCOUNTING CARD
         USING ACDSECT,R3
         MVC   ACDSN(L'DS1DSNAM),DS1DSNAM
         ST    R6,TOTTRKS
         CVD   R6,WORKD
         A     R6,TOTUSE
         ST    R6,TOTUSE           ADD TO TOTAL USERS USE
         UNPK  ACTRKS(L'ACTRKS),WORKD+5(3)
         OI    ACTRKS+L'ACTRKS-1,C'0'
QRSW     NOP   QR25
         OI    QRSW+1,ON
         LA    R14,DSNDATA
         LR    R15,R14
         CLI   0(R14),C' '
         BNE   *+L'*+6
         MVC   0(L'USID,R14),USID
         LA    R14,L'USID-1(R14)
         CLI   0(R14),C' '
         BNE   *+L'*+4
         BCT   R14,*-8
         MVC   1(2,R14),=CL2'.D'
         MVC   3(L'TODAYD,R14),TODAYD
         MVC   L'TODAYD+3(2,R14),=CL2'.T'
         MVC   L'TODAYD+5(L'THMS,R14),THMS
         L     R6,DADATA
         USING DYNLIST,R6
         SPACE 1
* NOTE - WHEN THE MASTER CATALOG IS VSAM OR ICF ('STEPCAT' PRESENT)
*        THE "CATLG" AT $DYNALL TIME ENDS WITH R15=04 AND ERC=5708.
*        TO BYPASS THE SITUATION, THE DATA-SET IS FIRST ALLOCATED
*        WITH "KEEP" AND THEN CATALOGED USING CATALOG MACRO (OS-CVOL)
*        OR INVOKING IDCAMS FUNCTION (ALIAS).
         SPACE 1
         L     R14,ATIOT
         USING TIODSECT,R14
         XR    R1,R1
QR11     CLC   TIOELNGH(4),=XL4'0'
         BE    QR14
         CLC   TIOEDDNM,=CL8'STEPCAT'
         BE    QR12
         IC    R1,TIOELNGH
         AR    R14,R1
         B     QR11
         DROP  R14
QR12     MVI   WORKDS,C' '
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
         MVC   WORKDS(9),DSNDATA
         LA    R0,WORKDS
         LA    R1,WORKDS+8
QR13     CLI   0(R1),C'.'
         MVI   0(R1),C' '
         BE    QR15
         BCTR  R1,0
         CLR   R1,R0
         BH    QR13
QR14     MVC   DSNDISP(5),=CL5'CATLG'
         B     QR16
QR15     XC    LOCAREA(256),LOCAREA
         XC    LOCAREA+256(LOCAREAL-256),LOCAREA+256
         LA    R1,WORKDS
         ST    R1,DATACV+4
         LA    R1,LOCAREA
         ST    R1,DATACV+12
        LOCATE DATACV
         LTR   R15,R15
         BNZ   QR14
         CLC   LOCAREA(2),=H'1'
         BNE   QR14
         CLC   WORKDS(9),=CL9'SYSCTLG.V'
         BE    *+L'*+10
         MVC   DATACNME,WORKDS
         B     *+L'*+6
         MVC   DATACVOL(L'DATACVOL),LOCAREA+6
         MVC   DSNDISP(4),=CL4'KEEP'
QR16     MVC   DDNAME(8),DDDATA
         MVC   DSNAME,DSNDATA
         MVC   DSSTATUS(3),=CL3'NEW'
         MVC   DSADISP(6),=CL6'DELETE'
         MVC   DSUNIT(4),=CL4'DISK'
         MVC   DSVOLSER,VADATA
         MVC   DSALLOC(3),=CL3'CYL'
         MVI   DSPRI,C'2'
         MVI   DSSEC,C'1'
         MVC   DSBLKSI(3),=CL3'800'
         MVC   DSORG(2),=CL2'PS'
         MVC   DSLRECL(2),=CL2'80'
         MVI   DSRECFM1,C'F'
         MVI   DSRECFM2,C'B'
        $DYNALL (R6),,MF=(E,DYNPLS)
         LTR   R15,R15             TEST RETURN CODE
         BZ    QR17
         ICM   R1,B'1111',=CL4'ALL.'
         LA    R15,DDNAME
         BAS   R14,DYNFAIL
         B     ERR60
QR17     CLC   DSNDISP(5),=CL5'CATLG'
         BE    QR24
         DROP  R6
         L     R14,ATIOT
         USING TIODSECT,R14
         XR    R1,R1
QR18     CLC   TIOELNGH(4),=XL4'0'
         BE    QR19
         CLC   TIOEDDNM,DDDATA
         BE    QR20
         IC    R1,TIOELNGH
         AR    R14,R1
         B     QR18
QR19    @OUT   QDDNF
         B     QR23
QR20     ICM   R1,B'0111',TIOEFSRT
         DROP  R14
         USING UCBDSECT,R1
         MVC   DATAVOL,UCBVOLI
         MVC   DATATY,UCBTYP
         NI    DATATY+1,255-UCBRR
         DROP  R1
         CLI   DATACNME,C' '
         BNE   QR21
         LA    R1,DSNDATA
         ST    R1,DATACA+4
        CATALOG DATACA
         B     QR22
QR21     LA    R1,DSNDATA
         ST    R1,IDCDSN
         LA    R1,DATACNME
         ST    R1,IDCPLS
         MVI   IDCFUN,C'C'
        @X     C001                INVOKE IDCAMS FUNCTION
QR22     LTR   R15,R15
         BZ    QR24
         MVC   QDIAGT,=CL7'CATALOG'
         STC   R15,QDIAGRC+1
         SRL   R15,4
         STC   R15,QDIAGRC
         NC    QDIAGRC,=XL2'0F0F'
         TR    QDIAGRC,TRTAB
         MVC   QDIAGV,DATAVOL
         MVC   QDIAGN,DSNDATA
        @OUT   QDIAG
         LA    R1,DSNDATA
         ST    R1,DATASC+4
         XR    R0,R0               NOTE - IF NOT 0, SCRATCH USES IT
        SCRATCH DATASC                    AS DEVICE UCB FOR MOUNT.
         LTR   R15,R15
         BZ    QR23
         MVC   QDIAGT,=CL7'SCRATCH'
         STC   R15,QDIAGRC+1
         SRL   R15,4
         STC   R15,QDIAGRC
         NC    QDIAGRC,=XL2'0F0F'
         TR    QDIAGRC,TRTAB
        @OUT   QDIAG
QR23     OI    RUNOPT,RUABORT      ABORT ACCOUNTING PROCESSING
         B     QXIT
QR24     OI    WHAT,WHDATA
QR25    $PUNCH MF=(E,QRPU)
         LTR   R15,R15
         BNZ   ERR60
         OI    WHAT,WHDATA
         L     R1,COUNTER
         LA    R1,1(R1)
         ST    R1,COUNTER
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    QXIT                NO, SKIP THE LIST
         TM    ACCIND,AFFULL       FULL LIST REQUESTED?
         BZ    QXIT                NO, SKIP THE LIST
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,LINE
         USING LQLNE,R5
         MVC   LQDSN,ACDSN
        @FILL  LQDSN
         MVI   LQSP,C'-'
         MVC   LQTOT,ACTRKS
         DROP  R3
         XR    R0,R0
         L     R1,TOTTRKS
         LH    R15,DVTYP+10
         CLR   R1,R15
         BL    QR30
         DR    R0,R15
         CL    R1,LIMIT
         BL    QR30
         MVC   LQCYLS(L'QCYMSK),QCYMSK
         CVD   R1,WORKD
         ED    LQCYLS,WORKD+5
         LTR   R0,R0
         BZ    QR30
         MVC   LQTRKS(L'QTRMSK),QTRMSK
         CVD   R0,WORKD
         ED    LQTRKS,WORKD+6
         DROP  R5
QR30    @LIST  LINE
         B     QXIT
         DROP  R4
         EJECT
*        EXTENT COMPUTE ROUTINE.
*
*              R0  = WORK REGISTER
*              R1  = WORK REGISTER
*              R5  = EXTENT DESCRIPTION OF DSCB
*              R6  = TOTAL SPACE COUNTER
*              R14 = LINK REGISTER
*
*              RETURN +0 = NO EXTENT
*                     +4 = NORMAL
         SPACE 1
QR50     CLI   0(R5),0             EXTENT EXISTS?
         BER   R14                 NO, RETURN +0
         MVC   WORKD(8),2(R5)      MOVE LIMITS
         LH    R0,WORKD            COMPUTE LOWER LIMIT
         MH    R0,DVTYP+10         TRANSFORM CYL. IN TRACKS
         AH    R0,WORKD+2
         LH    R1,WORKD+4          COMPUTE UPPER LIMIT
         MH    R1,DVTYP+10         TRANSFORM CYL. IN TRACKS
         AH    R1,WORKD+6
         AH    R1,=H'1'
         SR    R1,R0               COMPUTE EXTENT SPACE
         AR    R6,R1               ADD TO TOTAL SPACE
         B     4(R14)              RETURN +4
         EJECT
*        END VTOC READ.
*        --------------
         SPACE 1
QE0      TM    DDNALL,DDNXD        VTOC IS OPENED?
         BZ    QE1                 NO
         NI    DDNALL,255-DDNXD    SET CLOSED
        $VTOCDC
         LTR   R15,R15
         BNZ   ERR31
         DROP  R2
QE1      TM    RUNOPT,RUABORT      ACCOUNTING PROCESSING ABORTED?
         BO    QXIT                YES
         L     R1,SYSUSE
         LR    R2,R1
         CVD   R1,WORKD
         MVC   QSYTOT,=XL6'402020202120'
         ED    QSYTOT,WORKD+5      SYSTEM USE
         L     R1,TOTUSE
         AR    R2,R1
         CVD   R1,WORKD
         MVC   QUSTOT,=XL6'402020202120'
         ED    QUSTOT,WORKD+5      USERS USE
         LR    R1,R2
         CVD   R2,WORKD
         MVC   QVLUSE,=XL6'402020202120'
         ED    QVLUSE,WORKD+5      VOLUME USE
         LH    R2,DVTYP+8
         MH    R2,DVTYP+10
         SH    R2,DVALT
         CVD   R2,WORKD
         MVC   QVLTOT,=XL6'402020202120'
         ED    QVLTOT,WORKD+5      VOLUME TOTAL
         M     R0,=F'100'
         DR    R0,R2               COMPUTE PERCENTAGE.
         CVD   R1,WORKD
         MVC   QVLPCT,=XL4'40202120'
         ED    QVLPCT,WORKD+6
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    QXIT                NO, SKIP THE LIST
        @LIST  QTOTM               STATISTICS USE
         SPACE 1
QXIT    @XE
         EJECT
QRDO    $VTOCDO ,MF=L
QRDR    $VTOCDR ,ADDRNB,MF=L
QRPU    $PUNCH ,,,MF=L
         SPACE 1
DATACV  CAMLST NAME,*-*,,*-*
DATACA  CAMLST CAT,*-*,DATACVOL,DATACAT
DATASC  CAMLST SCRATCH,*-*,,DATACAT,,OVRD
DATACNME DC    CL44' ' <---------I
DATACVOL DC    CL6' '            I
DATACAT  DC    H'1'              I----- C001 : PARM.LIST
DATATY   DC    XL4'0'            I
DATAVOL  DC    CL6' ' <----------I
DATARC   DC    H'0'
         EJECT
SYSUSE   DC    F'0'
TOTUSE   DC    F'0'
TOTTRKS  DC    F'0'
LIMIT    DC    F'10'               CYLS LOWER LIMIT SIGNAL
ADDRNB   DC    XL8'0'              NEXT VTOC RECORD ADDRESS (MBBCCHHR)
QCYMSK   DC    0CL10' ',X'402020202120',C'CYL.'
QTRMSK   DC    0CL8' ',X'40202120',C'TRK.'
         SPACE 1
         DC    AL1(QTITL)
QTIT     DC    C'1DATA-SETS ACCOUNTING LIST (DT='
QTITYDD  DC    CL5' ',C') :'
QTITL    EQU   *-QTIT
         DC    AL1(QTITUL)
QTITU    DC    C'0'
QTITUN   DC    CL6' ',C' ('
QTITUT   DC    CL8' ',C') :'
QTITUL   EQU   *-QTITU
         DC    AL1(QTOTML)
QTOTM    DC    CL21' ',C'SYSTEM USE :'
QSYTOT   DC    CL6' ',C' TRACKS, USERS USE :'
QUSTOT   DC    CL6' ',C' TRACKS, VOLUME USE :'
QVLUSE   DC    CL6' ',C' /'
QVLTOT   DC    CL6' ',C' ='
QVLPCT   DC    CL4' ',C' %'
QTOTML   EQU   *-QTOTM
         DC    AL1(L'QDDNF)
QDDNF    DC    C'0 --- ACCOUNT - DD-SYSDATA NOT FOUND IN TIOT.'
         DC    AL1(QDIAGL)
QDIAG    DC    C'0 --- ACCOUNT - '
QDIAGT   DC    CL7' ',C' (RC='
QDIAGRC  DC    CL2' ',C') ON VOLUME '
QDIAGV   DC    CL6' ',C' FAILED FOR DATA-SET : '
QDIAGN   DC    CL44' '
QDIAGL   EQU   *-QDIAG
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
TIODSECT DSECT
        IEFTIOT1
FMT1DSCB DSECT
        IECSDSL1 (1)
         ORG   DS1DSNAM
        IECSDSL1 (3)
         ORG   DS1FMTID
        IECSDSL1 (4)
         EJECT
        PRINT  GEN
         SPACE 1
        #ACD   R=NO
LQLNE    DSECT           ACCOUNT LIST LINE DESCRIPTION. --------------
         SPACE 1
         DS    C ----------------> ASA CONTROL
         DS    CL8
LQDSN    DS    CL44      DATA-SET NAME
         DS    C
LQSP     DS    CL1       '-'
         DS    C
LQTOT    DS    CL5       TOTAL NUMBER OF TRACKS
LQCYLS   DS    CL6       NUMBER OF EQ. CYLS (OVER LIMIT)
         DS    CL4       'CYL.'
LQTRKS   DS    CL4       NUMBER OF EQ. TRKS (REST OF CYLS)
         DS    CL4       'TRK.'
         SPACE 1
LLQLNE   EQU   *-LQLNE             LINE LENGTH
LLQLNEC  EQU   0+L'LINE-LLQLNE     LINE LENGTH CONTROL
         SPACE 2
         END
