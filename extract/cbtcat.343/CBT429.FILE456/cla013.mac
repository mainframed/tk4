A013     TITLE 'CLEAN-UP - SCRATCH/UNCATALOG PROCESSING (A013).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A013    @XS    ALIAS=SCRUNC
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    SUP99               YES
         LA    R1,WORKDS
         ST    R1,CAMSCR+4
         ST    R1,CAMUNC+4
         ST    R1,CAMUNV+4
         ST    R1,CAMCDR+4
         MVC   SCACNAM,USID
         SPACE 1
*        SCRATCH/UNCATALOG DATA-SETS PROCESSING.
         SPACE 1
         TM    RUNIND,RUNOSCR      NOSCRATCH MODE?
         BO    SUP20               YES, BYPASS
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         TM    RUNIND,RUTEST       TEST MODE?
         BZ    SUP1                NO
         MVC   SCRPRTT,=CL16'SIMULATED (TEST)'
SUP1     CLI   0(R2),FMOFF         END OF TABLE?
         BE    SUP20               YES
         TM    RUNIND,RUCATMIS     ALL CATALOGS ARE PRESENT?
         BO    SUP2                NO
         CLI   DSACTION,YS         DATA-SET MUST BE SCRATCHED?
         BE    SUP3                YES
         CLI   DSACTION,YD         SCRATCH DISK?
         BE    SUP3                YES
SUP2     CLI   DSACTION,YT         SCRATCH TEMPORARY?
         BNE   SUP17               NO
SUP3     CLI   DSCAT,XCSI          ICF CATALOG INFO?
         BE    SUP4                YES
SUPSW1   NOP   SUP4
         OI    SUPSW1+1,ON
        @OUT   SCRPRT,=PL2'+40',U
SUP4     TM    STATUS,STBRACF      RACF MUST BE BYPASSED?
         BO    SUP10               YES
         CLI   SCACNAM,C' '        CURRENT USERID?
         BE    SUP10               NONE
         TM    DSFLGS,ZFADD+ZFCHG  ANY DATA-SET (RACF) ACTION?
         BZ    SUP10               NONE
         SPACE 1
*        DELETE FROM RACF DATA-SET DISCRETE PROFILE ANY EVENTUAL
*        PROTECTION REFERENCE AGAINST ME (I WANT SCRATCH).
         SPACE 1
         TM    DSFLGS,ZFTNU        WHICH NAME USE?
         BZ    *+L'*+10
         MVC   SCDSN,DSANME        USE TRUE NAME FOR RACF
         B     *+L'*+6
         MVC   SCDSN,DSNME         USE DATA-SET NAME FOR RACF
         LA    R0,L'SCDSN          LOOK FOR LENGTH OF DSNAME
         LA    R1,SCDSN+L'SCDSN-1
SUP5     CLI   0(R1),C' '
         BNE   *+L'*+6
         BCTR  R1,0
         BCT   R0,SUP5
         STC   R0,SCDSNL           SET LENGTH OF DSNAME
         MVC   SCVOL,DSVOLSN       SET VOLUME SERIAL
         XR    R15,R15             SET IN CASE OF TEST RUN
         TM    RUNIND,RUTEST       TEST MODE?
         BO    SUP7                YES, ONLY TEST RUN
         TM    DSFLGS,ZFADD        ADD SCHEDULED?
         BZ    SUP6                NO
        ICHEINTY ALTER,TYPE='DS',ENTRY=SCDSNL,VOLUME=SCVOL,            C
               ACTIONS=(SCADD)
         LTR   R15,R15
         BZ    SUP10
         B     SUP7
SUP6     TM    DSFLGS,ZFCHG        CHANGE SCHEDULED?
         BZ    SUP7                NO
        ICHEINTY ALTERI,TYPE='DS',ENTRY=SCDSNL,VOLUME=SCVOL,           C
               ACTIONS=(SCCHG)
         LTR   R15,R15
         BZ    SUP10
SUP7     ST    R15,SSCRC
        @SNAPS SN,SSCSN,SCR
         SPACE 1
SUP10    CLI   DSCAT,XCSI          ICF CATALOG INFO?
         BNE   SUP11               NO
        @X     B011                YES, GO GENERATE IDCAMS STATEMENT
         B     SUP17
SUP11    MVC   SCRDS,DSNME         DATA-SET NAME
        @FILL  SCRDS
         MVC   WORKDS,DSNME
         MVC   SCRVOL,DSVOLSN      VOLUME SERIAL
         MVC   SCRSOP,=CL4'O.K.'
         MVI   SCRPRLG,SCRPRLS
         MVC   CAMVOL,DSVOLSN
         MVC   CAMUCBTY,DSUCBTY    UCB TYPE
         XC    CAMRC,CAMRC
         XR    R3,R3               SET IN CASE OF TEST RUN
         XR    R15,R15
         TM    RUNIND,RUTEST       TEST MODE?
         BO    SUP14               YES, ONLY TEST RUN
         XR    R0,R0               NOTE - IF NOT 0, SCRATCH USES IT
*                                         AS DEVICE UCB FOR MOUNT.
        SCRATCH CAMSCR             NO, SCRATCH ...
         LR    R3,R15              SAVE RETURN CODE
         CLI   DSCAT,0             CATALOGED?
         BE    SUP12               NO, BYPASS
         TM    RUNIND,RUCATMIS     YES, ANY CATALOG MISSING?
         BO    SUP12               YES, BYPASS
         TM    RUNIND,RUNOCAT      NO, CATALOG ACTION WANTED?
         BZ    SUP13               YES
SUP12    XR    R15,R15             NO, BYPASS
         B     SUP14
SUP13   CATALOG CAMUNC             UNCATALOG ...
SUP14    LTR   R3,R3
         BNZ   *+L'*+6
         LTR   R15,R15
         BZ    SUP16
         MVC   SCRSOP,=CL4'(RC='
         MVI   SCRPRLG,SCRPRLL
         MVC   SCRRETC,=CL2' '
         MVC   SCRSTC,=CL2' '
         MVC   SCRTXT(SCRTXTL),=C'UNCATALOG (RC=  /LC=  /IX=  ).'
         LTR   R3,R3
         BZ    SUP15
         CVD   R3,WORKD            SCRATCH RETURN CODE
         UNPK  SCRRETC,WORKD+6(2)
         OI    SCRRETC+1,C'0'
         LH    R3,CAMRC
         LTR   R3,R3
         BZ    SUP15
         CVD   R3,WORKD            CAMLST RETURN CODE
         UNPK  SCRSTC,WORKD+6(2)
         OI    SCRSTC+1,C'0'
         CLC   SCRRETC,=CL2'08'
         BNE   SUP15
         CLC   SCRSTC,=CL2'07'
         BNE   SUP15
         MVC   SCRTXT(SCRTXTL),SCRTXT-1
         MVC   SCRTXT(16),=CL16'DATA-SET IN USE.'
         B     SUP16
SUP15    LTR   R15,R15
         BZ    SUP16
         CVD   R15,WORKD           UNCATALOG RETURN CODE
         UNPK  SUNCRC,WORKD+6(2)
         OI    SUNCRC+1,C'0'
         CH    R15,=H'8'
         BNE   SUP16
         LA    R1,0(R1)            CLEAR HI-BYTE
         CVD   R1,WORKD            UNCATALOG/LOCATE RETURN CODE
         UNPK  SUNCLC,WORKD+6(2)
         OI    SUNCLC+1,C'0'
         CVD   R0,WORKD            UNCATALOG/LOCATE NO. INDEXES
         UNPK  SUNCIC,WORKD+6(2)
         OI    SUNCIC+1,C'0'
SUP16   @OUT   SCRPR
SUP17    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     SUP1                LOOP
         DROP  R2
         SPACE 1
*        UNCATALOG DATA-SETS PROCESSING.
         SPACE 1
SUP20    TM    RUNIND,RUCATMIS     ANY CATALOG MISSING?
         BO    SUP90               YES, BYPASS
         TM    RUNIND,RUNOCAT      CATALOG ACTION WANTED?
         BO    SUP90               NO, BYPASS
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         TM    RUNIND,RUTEST       TEST MODE?
         BZ    SUP21               NO
         MVC   UNCPRTT,=CL16'SIMULATED (TEST)'
SUP21    CLI   0(R2),FMOFF         END OF TABLE?
         BE    SUP30               YES
         CLI   DSACTION,YU         TO BE UNCATALOGED?
         BNE   SUP27               NO, SKIP TO NEXT
         CLI   DSCAT,XCSI          ICF CATALOG INFO?
         BNE   SUPSW2              NO
        @X     B011                YES, GO GENERATE IDCAMS STATEMENT
         B     SUP27
SUPSW2   NOP   SUP22
         OI    SUPSW2+1,ON
        @OUT   UNCPRT,=PL2'+40',U
SUP22    MVC   UNCDS,DSNME         DATA-SET NAME
        @FILL  UNCDS
         MVC   UNCOP,=CL4'O.K.'
         MVI   UNCPRLG,UNCPRLS
         MVC   WORKDS,DSNME
         XR    R15,R15             SET IN CASE OF TEST RUN
         TM    RUNIND,RUTEST       TEST MODE?
         BO    SUP23               YES
        CATALOG CAMUNC             UNCATALOG ...
SUP23    LTR   R15,R15
         BZ    SUP26
         CH    R15,=H'8'           MAY BE CVOL UNRELATED
         BNE   SUP25
         CLM   R1,B'0011',=H'8'    LOC. RC IF CVOL MASTER
         BE    SUP24
         LTR   R1,R1
         BNZ   SUP25
         CLM   R0,B'0011',=H'8'    LOC. RC IF ICF OR VSAM MASTER
         BNE   SUP25
SUP24    OC    DSCVOL,DSCVOL
         BZ    SUP25
         MVC   CAMVOL,DSCVOL
        CATALOG CAMUNV             UNCATALOG CVOL ...
         LTR   R15,R15
         BZ    SUP26
         LA    R1,0(R1)            CLEAR HI-BYTE
SUP25    MVC   UUNRC,=CL2' '
         MVC   UUNLC,=CL2' '
         MVC   UUNIC,=CL2' '
         MVC   UNCOP,=CL4'(RC='
         MVI   UNCPRLG,UNCPRLL
         CVD   R15,WORKD           UNCATALOG RETURN CODE
         UNPK  UUNRC,WORKD+6(2)
         OI    UUNRC+1,C'0'
         CH    R15,=H'8'
         BNE   SUP26
         CVD   R1,WORKD            UNCATALOG/LOCATE RETURN CODE
         UNPK  UUNLC,WORKD+6(2)
         OI    UUNLC+1,C'0'
         CVD   R0,WORKD            UNCATALOG/LOCATE NO. INDEXES
         UNPK  UUNIC,WORKD+6(2)
         OI    UUNIC+1,C'0'
SUP26   @OUT   UNCPR
SUP27    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     SUP21               LOOP
         DROP  R2
         SPACE 1
*        CATALOG DATA-SETS RECOVERY PROCESSING.
         SPACE 1
SUP30    L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         TM    RUNIND,RUTEST       TEST MODE?
         BZ    SUP31               NO
         MVC   CDRPRTT,=CL16'SIMULATED (TEST)'
SUP31    CLI   0(R2),FMOFF         END OF TABLE?
         BE    SUP90               YES
         CLI   DSACTION,YC         TO BE CATALOGED?
         BNE   SUP35               NO, SKIP TO NEXT
         CLI   DSCAT,XCSI          ICF CATALOG INFO?
         BNE   SUPSW3              NO
        @X     B011                YES, GO GENERATE IDCAMS STATEMENT
         B     SUP35
SUPSW3   NOP   SUP32
         OI    SUPSW3+1,ON
        @OUT   CDRPRT,=PL2'+40',U
         XC    CAMRC,CAMRC
SUP32    MVC   CDRDS,DSNME         DATA-SET NAME
         MVC   CDRVO,DSVOLSN       VOLUME SERIAL NUMBER
        @FILL  CDRDS
         MVC   CDROP,=CL4'O.K.'
         MVI   CDRPRLG,CDRPRLS
         MVC   WORKDS,DSNME
         MVC   CAMUCBTY,DSUCBTY
         MVC   CAMVOL,DSVOLSN
         XR    R15,R15             SET IN CASE OF TEST RUN
         TM    RUNIND,RUTEST       TEST MODE?
         BO    SUP33               YES
        CATALOG CAMCDR             CATALOG ...
SUP33    LTR   R15,R15
         BZ    SUP34
         MVC   CDRRC,=CL2' '
         MVC   CDRLC,=CL2' '
         MVC   CDRIC,=CL2' '
         MVC   CDROP,=CL4'(RC='
         MVI   CDRPRLG,CDRPRLL
         CVD   R15,WORKD           CATALOG RETURN CODE
         UNPK  CDRRC,WORKD+6(2)
         OI    CDRRC+1,C'0'
         CH    R15,=H'8'
         BNE   SUP34
         LA    R1,0(R1)            CLEAR HI-BYTE
         CVD   R1,WORKD            CATALOG/LOCATE RETURN CODE
         UNPK  CDRLC,WORKD+6(2)
         OI    CDRLC+1,C'0'
         CVD   R0,WORKD            CATALOG/LOCATE NO. INDEXES
         UNPK  CDRIC,WORKD+6(2)
         OI    CDRIC+1,C'0'
SUP34   @OUT   CDRPR
SUP35    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     SUP31               LOOP
         DROP  R2
         SPACE 1
*        EXECUTE IDCAMS IF SOMETHING TO DO.
         SPACE 1
SUP90    TM    RUNIND,RUTEST       TEST MODE?
         BO    SUP91               YES
         TM    WHATS,WHIDIN        IDCAMS INPUT GENERATED?
         BZ    SUP99               NO
         NI    WHATS,255-WHIDIN
         L     R6,DAIDIN
        $UCLOSE (R6)               CLOSE IT
         LTR   R15,R15
         BNZ   ERR71
         B     SUP92
SUP91    ICM   R2,B'1111',AGIDST   IDCAMS ARRAY ACQUIRED?
         BZ    SUP99               NO
         CLI   4(R2),FMON          YES, BUT IS IT EMPTY?
         BE    SUP99               YES
SUP92   @X     B012                NO, GO EXECUTE IDCAMS
         SPACE 1
SUP99   @XE
         EJECT
SCADD   ICHEACTN FIELD=ACL,FLDATA=(11,SCACL),TESTS=SCTUS
SCCHG   ICHEACTN FIELD=ACL,FLDATA=(11,SCACL),TESTS=SCTUSI
SCTUS   ICHETEST FIELD=USERID,FLDATA=(8,SCACNAM),COND=NE
SCTUSI  ICHETEST FIELD=USERID,FLDATA=(8,SCACNAM),COND=EQ
         SPACE 1
SCACL    DS    0CL11               ACCESS LIST ENTRY
SCACNAM  DC    CL8' '
         DC    AL1(ACEEALTR)       ALTER ACCESS
         DC    XL2'0'              ACCESS COUNT
         SPACE 1
CAMSCR  CAMLST SCRATCH,*-*,,VOLIST,,OVRD
CAMUNC  CAMLST UCATDX,*-*
CAMUNV  CAMLST UNCAT,*-*,CAMVOL
CAMCDR  CAMLST CAT,*-*,,VOLIST
         SPACE 1
VOLIST   DC    H'1'
CAMUCBTY DC    XL4'0'
CAMVOL   DC    CL6' '
CAMRC    DC    H'0'
         SPACE 1
SSCSN    DC    A(SSCRC)
         DC    XL1'80',AL3(SSCLG)
SSCRC    DC    F'0'
         DC    XL1'0'
SCDSNL   DC    XL1'0'
SCDSN    DC    CL44' '
SCVOL    DC    CL6' '
SSCLG    EQU   *-SSCRC
         SPACE 1
         DC    AL1(L'SCRPRT+L'SCRPRTT)
SCRPRT   DC    C'0SCRATCH PROCESSING : '
SCRPRTT  DC    CL16' '
         SPACE 1
SCRPRLG  DC    AL1(*-*)
SCRPR    DC    CL6' '
SCRDS    DC    CL44' ',C' - VOLUME : '
SCRVOL   DC    CL6' ',C' - SCRATCH '
SCRSOP   DC    CL4' '
SCRPRLS  EQU   *-SCRPR
SCRRETC  DC    CL2' ',C'/SC='
SCRSTC   DC    CL2' ',C') '
SCRTXT   DC    C'UNCATALOG (RC='
SUNCRC   DC    CL2' ',C'/LC='
SUNCLC   DC    CL2' ',C'/IX='
SUNCIC   DC    CL2' ',C').'
SCRTXTL  EQU   *-SCRTXT
SCRPRLL  EQU   *-SCRPR
         SPACE 1
         DC    AL1(L'UNCPRT+L'UNCPRTT)
UNCPRT   DC    C'0UNCATALOG PROCESSING : '
UNCPRTT  DC    CL16' '
         SPACE 1
UNCPRLG  DC    AL1(*-*)
UNCPR    DC    CL6' '
UNCDS    DC    CL44' ',C' - UNCATALOG '
UNCOP    DC    CL4' '
UNCPRLS  EQU   *-UNCPR
UUNRC    DC    CL2' ',C'/LC='
UUNLC    DC    CL2' ',C'/IX='
UUNIC    DC    CL2' ',C').'
UNCPRLL  EQU   *-UNCPR
         SPACE 1
         DC    AL1(L'CDRPRT+L'CDRPRTT)
CDRPRT   DC    C'0CATALOG PROCESSING : '
CDRPRTT  DC    CL16' '
         SPACE 1
CDRPRLG  DC    AL1(*-*)
CDRPR    DC    CL6' '
CDRDS    DC    CL44' ',C' - VOLUME : '
CDRVO    DC    CL6' ',C' - CATALOG '
CDROP    DC    CL4' '
CDRPRLS  EQU   *-CDRPR
CDRRC    DC    CL2' ',C'/LC='
CDRLC    DC    CL2' ',C'/IX='
CDRIC    DC    CL2' ',C').'
CDRPRLL  EQU   *-CDRPR
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
