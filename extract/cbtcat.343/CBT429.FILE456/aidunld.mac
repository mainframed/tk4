AIDUNLD  TITLE 'ACCOUNTING INFORMATION DATA-SET UNLOAD.'
AIDUNLD  START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *       ACCOUNTING INFORMATION        * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *          DATA-SET UNLOAD            * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*
* PURPOSE :    UNLOAD THE CONTENTS OF THE ACCOUNTING INFORMATION
* ---------    DATA-SET IN A SEQUENTIAL FILE.
*
* CALL OF THE PROGRAM :
* ---------------------
*
*        //UNLOAD EXEC  PGM=AIDUNLD
*        //SYSACC   DD  DSN=...,DISP=OLD OR SHR
*        //SYSPUNCH DD  ... SEQUENTIAL FILE (UNLOADED DATA) ...
*                       NOTE : THE SYSPUNCH-LRECL MUST BE 2-BYTES
*                              LONGER THAN THAT OF SYSACC-LRECL.
*        //SYSPRINT DD  SYSOUT=A ---> LIST OF KEYS
*
* RETURN CODES :   0 = NORMAL COMPLETION
* --------------  16 = ABNORMAL COMPLETION, MESSAGE(S) ON SYSPRINT OR
*                      JOB LOG.
*
* OPTION :     PARM=S UNLOAD ONLY THE AUTHORIZATIONS AND PROGRAMMERS
* --------            NUMBERS KEYS.
*              PARM=U UNLOAD ALL KEYS WHICH ARE NOT AUTHORIZATIONS OR
*                     PROGRAMMERS NUMBERS KEYS.
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
         EJECT
        $AIRD
        $DEFREG
         EJECT
*---------------------------------------------------------------------*
*        AUTHORITY LEVELS DEFINITIONS.                                *
*---------------------------------------------------------------------*
         SPACE 1
LV12     EQU   12        ADMINISTRATOR CONTROL LEVEL
         SPACE 1
AUT98   $MDL@IX
         SPACE 1
        $XENT  BASE=R12
         EJECT
         L     R1,0(R1)            PARM. FIELD ADDRESS
         LH    R2,0(R1)            PARM. FIELD LENGTH
         LTR   R2,R2
         BNP   NOPARM
         CH    R2,=H'1'
         BNE   ERP
         CLI   2(R1),C'S'          'S' UNLOAD?
         BNE   *+L'*+8             NO
         OI    SWITCH,SWS          YES
         B     NOPARM
         CLI   2(R1),C'U'          'U' UNLOAD?
         BNE   ERP                 NO
         OI    SWITCH,SWU          YES
         B     NOPARM
ERP      LA    R2,PARMERR
         BAL   R5,PRTRTN
         NOP   0
         B     LEAVE
NOPARM  $EACM  REQAUT
         CLI   AUTLVL,LV12
         BL    NOTAUT
         XR    R9,R9               COUNT = 0
         XR    R6,R6               NO MSG'S
         XR    R7,R7
         L     R8,PTRI
        CALL   ZOPEN,(IR),VL       OPEN 'SYSACC'
         LTR   R0,R15              COMPLETION?
         BZ    READ                OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R2,R15
         BAL   R5,PRTRTN
         NOP   0
         B     LEAVE
READ    CALL   ZSEQR,(IR,DATA,ARG),VL   READ DATA 'SYSACC'
         LTR   R0,R15              COMPLETION?
         BM    EOF                 END OF DATA
         BZ    PUCH                OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R6,R15              RETAIN MSG ADDRESS
         B     ERR
PUCH     TM    SWITCH,SWS+SWU      'S' OR 'U' UNLOAD?
         BZ    UALL                NO
         TM    SWITCH,SWS          'S' UNLOAD?
         BZ    *+L'*+14            NO
         CLC   ARG,MAXKEY          YES, ARE WE OVER?
         BH    EOF                 YES, END OF USEFULL DATA
         B     UALL                NO
         CLC   ARG,MAXKEY          YES, ARE WE OVER?
         BH    UALL                YES, USEFULL DATA
         B     READ                NO, SKIP AUT. AND PRG.
UALL     LA    R9,1(R9)            +1 COUNT
        $PUNCH ARG                 OUT DATA
         LTR   R15,R15             COMPLETION?
         BNZ   ERR                 BRANCH IF ERROR
         XR    R0,R0               OK, LIST THE ARG. NO.
         ICM   R0,B'0011',ARG
         CVD   R0,WORKD
         MVC   0(L'MASK,R8),MASK
         ED    0(L'MASK,R8),WORKD+5
         LA    R8,L'MASK+2(R8)
         CL    R8,PTRL
         BNH   READ
         LA    R2,LINE             DISPLAY KEYS
         BAL   R5,PRTRTN
         B     CLS
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         L     R8,PTRI
         B     READ
NOTAUT   LA    R2,AUTMSG
         BAL   R5,PRTRTN
         NOP   0
         OI    SWITCH,SWE          SAY ERROR
         B     LEAVE
ERR      OI    SWITCH,SWD          ERROR, DISPLAY DATA
         TM    SWITCH,SWE          PRINT ERROR ?
         BO    CLS                 YES
EOF      CL    R8,PTRI             ANYTHING TO BE PRINTED?
         BE    CLS                 NO
         LA    R2,LINE             DISPLAY LAST KEYS
         BAL   R5,PRTRTN
         NOP   0
CLS     CALL   ZCLOSE,(IR),VL      CLOSE 'SYSACC'
         LTR   R0,R15              COMPLETION?
         BZ    EXIT                OK
         XR    R1,R1               ERROR, GET DIAGNOSTIC
        CALL   ZYDIAG
         LR    R7,R15              RETAIN MSG ADDRESS
EXIT     TM    SWITCH,SWE          PRINT ERROR?
         BO    LEAVE               YES
         LTR   R2,R6               PENDING MSG?
         BZ    *+L'*+8             NO
         BAL   R5,PRTRTN
         B     LEAVE
         LTR   R2,R7               PENDING MSG?
         BZ    *+L'*+8             NO
         BAL   R5,PRTRTN
         B     LEAVE
         CVD   R9,WORKD            DISPLAY COUNT
         ED    MCNT,WORKD+4
         LA    R2,MEND
         BAL   R5,PRTRTN
         B     LEAVE
         TM    SWITCH,SWD          ERROR?
         BZ    LEAVE               NO
         LA    R2,SNPMSG           YES, DISPLAY DATA
         BAL   R5,PRTRTN
         B     LEAVE
        $SNAP  ARG,LGS
         LTR   R15,R15
         BZ    LEAVE
         OI    SWITCH,SWE          SAY ERROR
LEAVE   $UCLOSE ,                  CLOSE ALL AND EXIT
         LTR   R15,R15
         BZ    *+L'*+4
         OI    SWITCH,SWE          SAY ERROR
        $PCLOSE
         LTR   R15,R15
         BZ    *+L'*+4
         OI    SWITCH,SWE          SAY ERROR
         XR    R10,R10             R.C. = 0
         TM    SWITCH,SWD+SWE
         BZ    *+L'*+4
         LA    R10,16              SET RC = 16
        $XRET  CC=(R10)
         EJECT
PRTRTN  $PRINT (R2)                PRINT ROUTINE (LINK = R5)
         LTR   R15,R15             R2 = LINE POINTER
         BZ    4(R5)               RETURN +4 = PRINT DONE
         OI    SWITCH,SWE          SAY ERROR
         BR    R5                  RETURN +0 = PRINT ERROR
         EJECT
*        CONSTANTS AND WORK AREAS.
*        -------------------------
         SPACE 1
WORKD    DC    D'0'
IR       DC    F'0'
PTRI     DC    A(LINE+10)
PTRL     DC    A(LINE+110)
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUTLVL)
         SPACE 1
LGS      DC    A(AUTLGTH+L'ARG)
ARG      DC    H'0'
DATA     DC    (AUTLGTH)X'0'
         SPACE 1
MAXKEY   DC    0H'0',AL2(NAUTNO+NPRGNO+1)
MASK     DC    X'402020202120'
AUTLVL   DC    XL1'0'
SWITCH   DC    XL1'0'
SWS      EQU   X'01'
SWU      EQU   X'02'
SWD      EQU   X'04'
SWE      EQU   X'08'
         SPACE 1
         DC    AL1(L'LINE)
LINE     DC    CL121'0 KEYS :'
         DC    AL1(MENDL)
MEND     DC    C'0 UNLOAD ENDED (COUNT :'
MCNT     DC    X'4020202020202120',C').'
MENDL    EQU   *-MEND
         DC    AL1(L'PARMERR)
PARMERR  DC    C'0 INVALID PARM. FIELD - EXIT -'
         DC    AL1(L'AUTMSG)
AUTMSG   DC    C'0 UNAUTHORIZED ACCESS TO THIS PROGRAM - EXIT -'
         DC    AL1(SNPMSGL)
SNPMSG   DC    C'0 HERE FOLLOWS THE DUMP OF CURRENT '
         DC    C' ARGUMENT AND DATA :'
SNPMSGL  EQU   *-SNPMSG
         SPACE 2
        LTORG
         SPACE 2
         END
