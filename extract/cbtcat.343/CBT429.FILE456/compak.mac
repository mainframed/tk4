CPAK     TITLE 'COMPARE TWO IEAPAK.. VERSIONS.'
COMPAK   START 0
         SPACE 1
COMPAK  AMODE  24
COMPAK  RMODE  24
         SPACE 2
* PURPOSE :    COMPARE TWO VERSIONS OF THE IEAPAK.. AND OBTAIN A
* ---------    LIST OF UNMATCHING NAMES.
*
* MVS/XA :     MUST BE AMODE AND RMODE 24.
* ---------
*
* PROGRAM CALL : //XC     EXEC PGM=COMPAK
* -------------- //SYSUT1   DD DSN=... 1ST IEAPAK.. ...,DISP=SHR
*                //SYSUT2   DD DSN=... 2ND IEAPAK.. ...,DISP=SHR
*                //SYSPRINT DD SYSOUT=A
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
MAX      EQU   4000                MAX. NUMBER OF NAMES
OFF      EQU   X'0F'               NO-OPERATION SWITCH SETTING
ON       EQU   X'F0'               BRANCH SWITCH SETTING
         EJECT
        $XENT  BASE=(R11,R12)
         L     R3,MAXEL
         SLL   R3,3
         LR    R2,R3
         SLL   R2,1
        GETMAIN R,LV=(R2)
         ST    R1,TAUT1
         ALR   R1,R3
         ST    R1,TAUT2
        $GDATE TODAY
         LTR   R10,R15
         BNZ   EXIT
        $PRINT TIT
         LTR   R10,R15
         BNZ   EXIT
        $PRINT UND
         LTR   R10,R15
         BNZ   EXIT
         LA    R8,DCBUT
         USING IHADCB,R8
         LA    R7,JF
         USING JFCDSECT,R7
         MVC   DDNM,FILE1
         MVC   DCBDDNAM,DDNM
        RDJFCB (DCBUT)
         LTR   R10,R15
         BNZ   JFERR
         LA    R1,MSUU
         MVI   0(R1),C' '
         MVC   1(L'MSUU,R1),0(R1)
         MVC   0(L'DDNM,R1),DDNM
         LA    R1,L'DDNM-1(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   2(R1),C':'
         MVC   4(L'JFCBDSNM,R1),JFCBDSNM
         LA    R1,3+L'JFCBDSNM(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         LA    R1,1(R1)
         CLC   JFCBELNM(L'JFCBELNM),=CL8' '
         BE    NOMU1A
         MVI   0(R1),C'('
         MVC   1(L'JFCBELNM,R1),JFCBELNM
         LA    R1,L'JFCBELNM(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   1(R1),C')'
         LA    R1,2(R1)
NOMU1A   CLC   JFCBVOLS(6),=CL8' '
         BE    NOMU1B
         MVC   0(5,R1),=CL5',VOL='
         MVC   5(6,R1),JFCBVOLS
NOMU1B   L     R9,TAUT1            READ SYSUT1 NAMES
         XR    R2,R2
READU1  $READ  CARD,DDNM
         LTR   R10,R15
         BM    EOFU1
         BNZ   EXIT
         BAS   R14,SCAN
         B     READU1
EOFU1    ST    R2,MU1
         CVD   R2,WORK
         MVC   MSUN,=XL6'402020202120'
         ED    MSUN,WORK+5
        $PRINT MSU
         LTR   R10,R15
         BNZ   EXIT
         LTR   R2,R2
         BNP   ERRU
         L     R3,TAUT1            SORT SYSUT1 NAMES
        $SORT  (R3),(R2),8,0,8
         MVC   DDNM,FILE2
         MVC   DCBDDNAM,DDNM
        RDJFCB (DCBUT)
         LTR   R10,R15
         BNZ   JFERR
         LA    R1,MSUU
         MVI   0(R1),C' '
         MVC   1(L'MSUU,R1),0(R1)
         MVC   0(L'DDNM,R1),DDNM
         LA    R1,L'DDNM-1(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   2(R1),C':'
         MVC   4(L'JFCBDSNM,R1),JFCBDSNM
         LA    R1,3+L'JFCBDSNM(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         LA    R1,1(R1)
         CLC   JFCBELNM(L'JFCBELNM),=CL8' '
         BE    NOMU2A
         MVI   0(R1),C'('
         MVC   1(L'JFCBELNM,R1),JFCBELNM
         LA    R1,L'JFCBELNM(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   1(R1),C')'
         LA    R1,2(R1)
NOMU2A   CLC   JFCBVOLS(6),=CL8' '
         BE    NOMU2B
         MVC   0(5,R1),=CL5',VOL='
         MVC   5(6,R1),JFCBVOLS
NOMU2B   L     R9,TAUT2            READ SYSUT2 NAMES
         XR    R2,R2
READU2  $READ  CARD,DDNM
         LTR   R10,R15
         BM    EOFU2
         BNZ   EXIT
         BAS   R14,SCAN
         B     READU2
EOFU2    ST    R2,MU2
         CVD   R2,WORK
         MVC   MSUN,=XL6'402020202120'
         ED    MSUN,WORK+5
        $PRINT MSU
         LTR   R10,R15
         BNZ   EXIT
         LTR   R2,R2
         BNP   ERRU
         L     R3,TAUT2            SORT SYSUT2 NAMES
        $SORT  (R3),(R2),8,0,8
         DROP  R7,R8
         L     R2,TAUT1            COMPARE SYSUT1 AND SYSUT2 NAMES
         XR    R3,R3
TS1      L     R4,TAUT2
         XR    R5,R5
TS2      CLC   0(8,R2),0(R4)
         BNE   TS3
         MVC   0(8,R2),=CL8' '
         MVC   0(8,R4),=CL8' '
         B     TS4
TS3      LA    R4,8(,R4)
         LA    R5,1(,R5)
         CL    R5,MU2
         BL    TS2
TS4      LA    R2,8(,R2)
         LA    R3,1(,R3)
         CL    R3,MU1
         BL    TS1
         L     R2,TAUT1            LIST SYSUT1 NAMES
         XR    R3,R3
         MVC   LINE+2(L'FILE1),FILE1
         MVI   LINE+L'FILE1+3,C':'
         LA    R4,LINE+13
UT1      CLI   0(R2),C' '
         BE    UT2
         MVC   0(8,R4),0(R2)
         LA    R4,10(,R4)
         CL    R4,LMX
         BL    UT2
        $PRINT LINE
         LTR   R10,R15
         BNZ   EXIT
         LA    R4,LINE+13
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
UT2      LA    R2,8(,R2)
         LA    R3,1(,R3)
         CL    R3,MU1
         BL    UT1
         CLI   LINE+13,C' '
         BE    UT3
        $PRINT LINE
         LTR   R10,R15
         BNZ   EXIT
         LA    R4,LINE+13
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
UT3      L     R2,TAUT2            LIST SYSUT2 NAMES
         XR    R3,R3
         MVI   LINE,C'0'
         MVC   LINE+2(L'FILE2),FILE2
         MVI   LINE+L'FILE1+3,C':'
         LA    R4,LINE+13
UT4      CLI   0(R2),C' '
         BE    UT5
         MVC   0(8,R4),0(R2)
         LA    R4,10(,R4)
         CL    R4,LMX
         BL    UT5
        $PRINT LINE
         LTR   R10,R15
         BNZ   EXIT
         LA    R4,LINE+13
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
UT5      LA    R2,8(,R2)
         LA    R3,1(,R3)
         CL    R3,MU2
         BL    UT4
         CLI   LINE+13,C' '
         BE    END
        $PRINT LINE
         LTR   R10,R15
         BNZ   EXIT
END     $PRINT MEND
RERR     LR    R10,R15
EXIT    $RCLOSE
        $PCLOSE
         L     R1,TAUT1
         L     R0,MAXEL
         SLL   R0,4
        FREEMAIN R,A=(1),LV=(0)
        $XRET  CC=(R10)
JFERR    MVC   MJFUU,DDNM
        $PRINT MJFU
         B     EXIT
ERRU     MVC   MERRUU,DDNM
        $PRINT MERRU
         B     RERR
SCOVF    MVC   MOVFUU,DDNM
        $PRINT MOVFU
         B     RERR
         SPACE 2
SCAN     LM    R3,R5,ASCAN         CARD SCAN SUBROUTINE.
         XR    R1,R1
         NI    SCSW+1,OFF
SC0      CLI   0(R3),C' '
         BNE   SCSW
         LTR   R1,R1
         BZ    SC2
         OI    SCSW+1,ON
         B     SC2
SCSW     NOP   SCERR
         CLI   0(R3),C'('
         BE    SC2
         CLI   0(R3),C','
         BNE   SC3
SC2      BXLE  R3,R4,SC0
         BR    R14
SC3      LR    R1,R3
SC4      BXLE  R3,R4,SC5
         LTR   R1,R1
         BZR   R14
         CLI   0(R1),C'('
         BE    SC7
         BR    R14
SC5      CLI   0(R3),C','
         BE    SC6
         CLI   0(R3),C'('
         BE    SCERR
         CLI   0(R3),C' '
         BE    SCERR
         CLI   0(R3),C')'
         BNE   SC4
SC6      LR    R15,R3
         SR    R15,R1
         BNP   SCERR
         CH    R15,=H'8'
         BH    SCERR
         BCTR  R15,0
         LA    R2,1(,R2)
         CL    R2,MAXEL
         BH    SCOVF
         MVC   0(8,R9),=CL8' '
         EX    R15,SCM
         LA    R9,8(,R9)
         B     SC2
SC7      LR    R3,R1
SCERR    LR    R2,R14
        $PRINT DIAG
         LTR   R10,R15
         BNZ   EXIT
         MVI   DIAG,C' '
         MVC   CARD(DIAGL-L'DIAG),DIAG+L'DIAG-1
         MVI   0(R3),C'$'
        $PRINT DIAG
         LTR   R10,R15
         BNZ   EXIT
         MVC   0(12,R3),=CL12'$... ERROR :'
         MVC   13(L'DDNM,R3),DDNM
        $PRINT DIAG
         LTR   R10,R15
         BNZ   EXIT
         MVI   DIAG,C'0'
         BR    R2
SCM      MVC   0(*-*,R9),0(R1)
         EJECT
*        CONSTANTS AND WORK AREAS.
*        -------------------------
         SPACE 1
WORK     DC    D'0'
TAUT1    DC    A(*-*)
TAUT2    DC    A(*-*)
ASCAN    DC    A(CARD,1,CARD+71)
MAXEL    DC    A(MAX)
LMX      DC    A(LINE+110)
MU1      DC    F'0'
MU2      DC    F'0'
FILE1    DC    CL8'SYSUT1'
FILE2    DC    CL8'SYSUT2'
DDNM     DC    CL8' '
         SPACE 1
         DC    AL1(DIAGL)
DIAG     DC    CL6'0 '
CARD     DC    CL80' ',CL20' '
DIAGL    EQU   *-DIAG
         DC    AL1(TITL)
TIT      DC    C'1 IEAPAK.. COMPARE : '
TODAY    DC    CL20' '
TITL     EQU   *-TIT
         DC    AL1(L'UND)
UND      DC    C'  ------------------'
         DC    AL1(L'MEND)
MEND     DC    C'0 - END OF PROCESSING -'
         DC    AL1(L'LINE)
LINE     DC    CL110'0 '
         DC    AL1(MSUL)
MSU      DC    CL6'0 '
MSUN     DC    CL6' ',C' NAMES READ FROM '
MSUU     DC    CL80' '
MSUL     EQU   *-MSU
         DC    AL1(MJFUL)
MJFU     DC    C'0 - '
MJFUU    DC    CL8' ',C' : JFCB READ ERROR - EXIT -'
MJFUL    EQU   *-MJFU
         DC    AL1(MERRUL)
MERRU    DC    C'0 - '
MERRUU   DC    CL8' ',C' : NO NAMES - EXIT -'
MERRUL   EQU   *-MERRU
         DC    AL1(MOVFUL)
MOVFU    DC    C'0 - '
MOVFUU   DC    CL8' ',C' : TABLE OVERFLOW - EXIT -'
MOVFUL   EQU   *-MOVFU
         EJECT
        LTORG
         SPACE 2
        PRINT  NOGEN
DCBUT   DCB    DSORG=PS,DEVD=DA,MACRF=GM,EXLST=JOBFILE,DDNAME=XXXXXXXX
        PRINT  GEN
         SPACE 1
JOBFILE  DC    0F'0',XL1'87',AL3(JF)
JF       DC    0F'0',XL176'0'
         SPACE 2
        PRINT  NOGEN
        DCBD   DSORG=PS,DEVD=DA
JFCDSECT DSECT
        IEFJFCBN LIST=YES
         SPACE 2
         END
