SHUT     TITLE 'SHUTDOWN - OPERATOR GRACEFULL SERVICES TERMINATION.'
SHUTDOWN START 0
         SPACE 1
* PURPOSE : PROGRAM (STARTED TASK) ALLOWING A GRACEFULL SHUTDOWN TO BE
* --------- SCHEDULED FOR A PRE-DETERMINED TIME-OF-DAY BY OPERATOR.
*        THE LOGGED ON TSO AND/OR ACTIVE IMS USERS ARE SENT MESSAGES
*        AT INTERVALS INFORMING THEM OF THE TIME REMAINING TO SHUTDOWN
*        ONE, SOME OR ALL SYSTEM SERVICES (I.E. - TSO, IMS, ... ).
*
* AUTHOR :     MOINIL P.A. - COMPUTING CENTRE
* --------     J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
*
* MESSAGES : THE MESSAGES ARE READ FROM THE DATA-SET INDICATED BY
* ---------- THE DD-NAME 'SYSTMSG' (MUST BE FIXED 80-BYTE RECORDS).
*        THE FORMAT OF THE CARDS CONTAINING THE DESIRED MESSAGES
*        MUST BE AS FOLLOWS :
*        COL.  1   = MUST BE A BLANK
*        COL. 2-66 = THE MESSAGE TEXT (QUOTES NEED NOT BE DOUBLED)
*        THE REMAINING COLUMNS ARE LOST (UNUSED).
*        A CARD CONTAINING '-END' IN COLUMNS 1-4 CAUSES A MESSAGE
*        LINE OF HYPHENS.
*        THE MESSAGES ARE SENDED IN THE SAME ORDER THEY ARE READ.
*
* OPTIONALLY, SPECIAL CARDS MAY BE USED :
* ---------------------------------------
*     1. TO REQUEST THAT, ONCE THE SHUTDOWN MESSAGES HAS BEEN SENT
*        AND THE TIME LIMIT IS EXPIRED, A COMMAND IS ISSUED (THIS
*        ALLOWS THE SHUTDOWN PROCESSING TO START AUTOMATICALLY).
*        COMMAND CARD FORMAT :
*        COL. 1-5  = MUST BE '-COM ' KEYWORD.
*        COL. 6-72 = THE OPERATOR COMMAND TEXT.
*        ONLY ONE COMMAND CARD IS ACCEPTED, IT MAY BE PLACED ANYWHERE
*        (ALL SUCCESSIVES ENCOUNTERED ARE IGNORED).
*     2. TO INDICATE WHERE THE SHUTDOWN MESSAGES MUST BE SENT (USERS
*        DESTINATIONS).
*        COMMAND CARD FORMAT :
*        COL. 1-4  = MUST BE '-TO ' KEYWORD.
*        COL. 5-72 = THE DESTINATION KEYWORDS 'TSO' AND/OR 'IMS'.
*        IF A DESTINATION CARD IS NOT SUPPLIED, THE SHUTDOWN MESSAGES,
*        BY DEFAULT, ARE SENT TO THE LOGGED ON TSO USERS ONLY. SO A
*        '-TO IMS' DESTINATION CARD MEANS TO IMS ACTIVE USERS ONLY.
*
* NOTE : THE INPUT IS COMPATIBLE WITH THAT OF THE 'NOTICE' TSO COMMAND
* ------ BECAUSE THE CARDS ARE SKIPPED WHEN :
*        1. ANY CHARACTER OTHER THAN A BLANK OR MINUS IS FOUND IN
*           COLUMN 1,
*        2. THE CHARACTER MINUS IS FOUND IN COLUMN 1 BUT
*           A. THE CONTENTS OF COLUMNS 2-4 IS NOT 'END', OR
*           B. THE CONTENTS OF COLUMNS 2-5 IS NOT 'COM ', OR
*           C. THE CONTENTS OF COLUMNS 2-4 IS NOT 'TO '.
*        ALL THE CARDS CAN BE LOWER/UPPER CASES.
         EJECT
* PARM FIELD DESCRIPTION : PARM='AT=HH.MM'  (THE TIME-OF-DAY : TOD)
* ------------------------  OR
*                          PARM='IN=MM'     (AN ELAPSED TIME FROM THE
*                                            CURRENT TIME-OF-DAY)
*          WHERE : HH.MM OR H.MM IS THE DESIRED SHUTDOWN TIME-OF-DAY
*                  (FORMAT IS : HOURS.MINUTES). BY DEFAULT (OMITTED),
*                  IT IS ASSUMED 20 MINUTES ELAPSED TIME FROM THE
*                  CURRENT TIME-OF-DAY.
*             OR : MM IS THE DESIRED ELAPSED TIME (IN MINUTES, MINIMUM
*                  IS 2 AND MAXIMUM 60) FROM THE CURRENT TIME-OF-DAY.
*        NOTE - 'AT=' AND 'IN=' KEYWORDS ARE MUTUALLY EXCLUSIVES, BUT
*               PARM='AT=,IN=MM' OR PARM='AT=HH.MM,IN=' IS ACCEPTED.
*               KEYWORDS MUST BE ALWAYS SEPARATED BY A COMMA.
*        WHEN THE DIFFERENCE BETWEEN THE CURRENT TOD AND THE DESIRED
*        SHUTDOWN TOD IS MORE THAN 30 MINUTES, THE START COMMAND IS
*        RE-SCHEDULED TO JES2 AS BELOW
*                  $TA,T=HH.MM,'$VS,''S SHUTDOWN,...'' '
*        WITH HH.MM SET AS WAKE-UP TIME ON ABOUT 30 MINUTES BEFORE THE
*        DESIRED SHUTDOWN TOD.
*
* TIME INTERVALS : THE INTERVAL TIME TO SEND WARNING MESSAGES IS
* ---------------- DETERMINED AUTOMATICALLY BY THE PROGRAM USING
*                  THE REMAINING SHUTDOWN TIME AS FOLLOWS :
*                  - EACH 5 MINUTES FOR A REMAINING TIME FROM 30 TO
*                    10 MINUTES.
*                  - EACH 1 MINUTE FOR A REMAINING TIME FROM 10 TO 5
*                    MINUTES.
*                  - EACH 30 SECONDS FOR A REMAINING TIME OF 5 OR LESS
*                    MINUTES.
*
* ADDITIONAL FACILITIES :
* -----------------------
*        1. WHEN STARTED TASK RUNNING, THE SHUTDOWN PROCESS MAY BE
*           STOPED OR CHANGED AT ANY TIME THROUGH THE STOP (P -NAME-)
*           OR MODIFY (F -NAME-,...) OPERATOR COMMANDS, WHERE :
*              -NAME- IS THE STARTED TASK JOB-NAME, AND
*              ... IS THE DESIRED NEW PARM FIELD VALUE.
*           I.E. - ASSUMING THE STARTED TASK JOB-NAME IS 'SHUTDOWN' :
*                  MODIFY : F SHUTDOWN,AT=17.00
*                  STOP : P SHUTDOWN
*           NOTE - A MODIFY WITH NO TEXT IS LIKE A NULL PARM FIELD
*                  VALUE, AND THUS ALLOWS ANOTHER 20 MINUTES.
*        2. THE PROGRAM ENTER A SIMULATION TEST MODE WHEN NOT STARTED
*           TASK EXECUTING (IT USE THE DD-NAME 'SYSLMSG' AS OUTPUT).
*           THIS ALLOWS OPERATOR TO CHECK A SHUTDOWN PROCESS.
         EJECT
        $DEFREG
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
LVL10    EQU   10        OPERATORS LEVEL
LVL15    EQU   15        SYSTEM LEVEL
         SPACE 1
NOTICE  $MDL@IX ,
         SPACE 2
*        MISCELLANEOUS DEFINITIONS.
         SPACE 1
MXNO     EQU   50                  MAXIMUM NUMBER OF MESSAGES
MSLEN    EQU   110                 MESSAGE TEXT LENTGH
MSW      EQU   MSLEN+1             MESSAGE AREA LENTGH
MSTOT    EQU   MXNO*MSW            TOTAL MESSAGES AREA LENGTH
MLEN     EQU   ((MSTOT+7)/8)*8     GETMAIN AREA LENGTH
         EJECT
*--------------------------------------------------------------------*
*        INITIALIZATION.                                             *
*--------------------------------------------------------------------*
         SPACE 1
        $XENT  BASE=(R11,R12)
         LA    R10,MAXERR
         L     R2,0(R1)            GET PARM FIELD
         LH    R3,0(R2)
         LTR   R3,R3
         BNP   *+L'*+18
         STH   R3,PRMLG
         BCT   R3,*+L'*+6
         MVC   PRMFLD(*-*),2(R2)   << EXECUTED >>
         EX    R3,*-6
         EJECT
*--------------------------------------------------------------------*
*        CONTROL STARTED TASK AND CALLER AUTHORITY.                  *
*--------------------------------------------------------------------*
         SPACE 1
        EXTRACT ANSWER,'S',FIELDS=(TIOT,COMM)
         L     R3,ANSWER+4         COMMUNICATION AREA ADDRESS
         L     R4,4(R3)            CIB ADDRESS
         LA    R4,0(R4)
         LTR   R4,R4               CURRENT CIB SET UP ?
         BZ    TESTRUN             NO
         USING CIBDSECT,R4
         CLI   CIBVERB,CIBSTART    START COMMAND (SYSTEM TASK) ?
         BNE   TESTRUN             NO
         DROP  R4
         OI    SWITCH,SYSTASK      YES, SAY IT
TESTRUN $EACM  REQAUT
         CLI   AUTH,LVL10          OPERATOR LEVEL ?
         BL    ERR1                NO
         EJECT
*--------------------------------------------------------------------*
*        SCAN AND CONTROL PARM. FIELD TEXT.                          *
*        SPECIAL SYSTEM TEST PURPOSES (AUTHORIZED USER ONLY) :       *
*           1. PARM=T FOR BATCH EXECUTION TO SIMULATE MESSAGES BUT   *
*              TEST THE TIMING AND MODIFY PROCESSES.                 *
*           2. PARM=S FOR BATCH EXECUTION TO TEST JUST ONCE THE      *
*              MESSAGES SENDER.                                      *
*         N.B. OPTION T AND S ARE MUTUALLY EXCLUSIVES.               *
*--------------------------------------------------------------------*
         SPACE 1
PA00    TIME   BIN                 GET CURRENT TOD
         LR    R1,R0               R0 = TOD IN 0.01 SECONDS
         XR    R0,R0
         D     R0,=A(60*100)       TRANSFORM TOD IN MINUTES
         LR    R0,R1
         A     R0,=F'20'           COMPUTE DEFAULT SHUTDOWN TOD
         STM   R0,R1,TODREQ
         OC    PRMLG,PRMLG         PARM LENGTH ?
         BZ    CTLSP               IF NO PARM, USE DEFAULT
         LH    R3,PRMLG            PARM. FIELD TEXT LENGTH
         LA    R2,PRMFLD           START OF PARM. FIELD TEXT
PA01     CLI   0(R2),C','          SEPARATOR ?
         BNE   PA04                NO
PA02     LA    R2,1(R2)            YES
         BCT   R3,PA01             LOOP IF MORE
         B     CTLTOD
PA03     CLI   0(R2),C','          SEPARATOR ?
         BE    PA02                YES
         B     PAERR               NO, ERROR
PA04     TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BO    PA07                YES
         CLI   AUTH,LVL15          SYSTEM (HIGHEST) AUTHORITY ?
         BL    PA07                NO
         CLI   0(R2),C'T'          TEST OPTION REQUESTED ?
         BNE   PA05                NO
         TM    SWPRCS,MSTEST       SEND OPTION REQUESTED PREVIOUSLY ?
         BO    PAERR               YES
         OI    SWPRCS,OPTEST
         B     PA06
PA05     CLI   0(R2),C'S'          SEND OPTION REQUESTED ?
         BNE   PA07                NO
         TM    SWPRCS,OPTEST       TEST OPTION REQUESTED PREVIOUSLY ?
         BO    PAERR               YES
         OI    SWPRCS,MSTEST
PA06     LA    R2,1(R2)
         BCT   R3,PA03
         B     CTLTOD
PA07     CH    R3,=H'3'            MAY BE AT=... OR IN=... CODED ?
         BL    PAERR               NO
         CLC   0(3,R2),=CL3'IN='   IN=... CODED ?
         BE    PA20                YES
         CLC   0(3,R2),=CL3'AT='   AT=... CODED ?
         BNE   PAERR               NO
         LA    R2,3(R2)            YES
         SH    R3,=H'3'
         BZ    CTLTOD              NONE, USE DEFAULT
         CLI   0(R2),C','          AT=, CODED ?
         BE    PA02                YES
         TM    SWITCH,KEYWAT+KEYWIN     AT= OR IN= ALREADY HANDLED ?
         BNZ   PAERR               YES
         CH    R3,=H'4'            TOD H.MM OR HH.MM CODED ?
         BL    PAERR               NO, ERROR
         CLI   2(R2),C'.'          HH.MM CODED ?
         BE    PA10                YES
         CLI   1(R2),C'.'          H.MM CODED ?
         BNE   PAERR               NO, SYNTAX ERROR
         MVI   WORKD,C'0'
         MVC   WORKD(1),0(R2)
         LA    R2,2(R2)
         SH    R3,=H'2'
         B     PA11
PA10     MVC   WORKD(2),0(R2)
         LA    R2,3(R2)
         SH    R3,=H'3'
PA11     CH    R3,=H'2'            MM PRESENT ?
         BL    PAERR               NO, ERROR
         MVC   WORKD+2(2),0(R2)
         XC    WORKF,WORKF
         MVZ   WORKF(4),WORKD
         CLC   WORKF(4),=CL4'0000'
         BNE   PAERR               NOT NUMERIC, ERROR
         MVC   WORKF(4),WORKD
         NC    WORKD(4),=XL4'0F0F0F0F'
         TR    WORKD(4),=XL16'00000000000000000000FFFFFFFFFFFF'
         OC    WORKD(4),WORKD
         BNZ   PAERR
         PACK  WORKD,WORKF(2)      TOD HOURS
         CVB   R0,WORKD
         CH    R0,=H'23'
         BH    PAERR               HOURS TOO BIG, ERROR
         PACK  WORKD,WORKF+2(2)    TOD MINUTES
         CVB   R1,WORKD
         CH    R1,=H'59'
         BH    PAERR               MINUTES TOO BIG, ERROR
         MH    R0,=H'60'
         AR    R0,R1
         ST    R0,TODREQ           SHUTDOWN TOD
         LA    R2,2(R2)
         SH    R3,=H'2'
         BZ    CTLTOD              NO MORE
         OI    SWITCH,KEYWAT+KEYWIN     SAY PROCESSED
         B     PA03
PA20     LA    R2,3(R2)
         SH    R3,=H'3'
         BZ    CTLTOD              NONE, USE DEFAULT
         CLI   0(R2),C','          IN=, CODED ?
         BE    PA02                YES
         TM    SWITCH,KEYWAT+KEYWIN     AT= OR IN= ALREADY HANDLED ?
         BNZ   PAERR               YES
         MVI   WORKD,C'0'
         MVC   WORKD+1(1),0(R2)
         LA    R2,1(R2)
         BCT   R3,*+L'*+4
         B     PA21
         CLI   0(R2),C','          SEPARATOR ?
         BE    PA21                YES
         MVC   WORKD(1),WORKD+1
         MVC   WORKD+1(1),0(R2)
         LA    R2,1(R2)
         BCTR  R3,0
PA21     XC    WORKF,WORKF
         MVZ   WORKF(2),WORKD
         CLC   WORKF(2),=CL4'0000'
         BNE   PAERR               NOT NUMERIC, ERROR
         MVC   WORKF(2),WORKD
         NC    WORKD(2),=XL4'0F0F0F0F'
         TR    WORKD(2),=XL16'00000000000000000000FFFFFFFFFFFF'
         OC    WORKD(2),WORKD
         BNZ   PAERR
         PACK  WORKD,WORKF(2)      INTERVAL TIME
         CVB   R0,WORKD
         LTR   R0,R0
         BNP   PAERR
         CH    R0,=H'60'
         BH    PAERR               TOO BIG, ERROR
         A     R0,TODREQ+4
         ST    R0,TODREQ           SHUTDOWN TOD
         LTR   R3,R3
         BZ    CTLTOD              NO MORE
         OI    SWITCH,KEYWAT+KEYWIN     SAY PROCESSED
         B     PA03
PAERR    TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BZ    PAERR0              NO
         TM    SWITCH,PUSHPULL     YES, MODIFY SCAN ERROR ?
         BZ    ERR2                NO
         LA    R1,DIAG2            YES, SAY WHAT ABOUT MODIFY
        WTO    MF=(E,(1))
         LA    R1,MCONT            SAY USE OF DEFAULT
        WTO    MF=(E,(1))
         B     CTLTOD              CONTINUE WITH DEFAULT
PAERR0   LA    R1,PRMFLD           DIAGNOSE (R2 = POINTER)
         LH    R3,PRMLG
         SR    R2,R1
PAERR1   CH    R2,=H'10'
         BNH   PAERR2
         AH    R1,=H'5'
         SH    R2,=H'5'
         SH    R3,=H'5'
         B     PAERR1
PAERR2   CH    R3,=H'10'
         BNH   *+L'*+4
         LH    R3,=H'10'
         MVC   LNEMS(26),=CL26'PARM. FIELD AREA POINTER :'
         BCT   R3,*+L'*+6
         MVC   LNEMS+27(*-*),0(R1) << EXECUTED >>
         EX    R3,*-6
        $PRINT LNE,,DDOUT
         LTR   R15,R15
         BNZ   TSEOF
         MVC   LNEMS(L'LNEMS),LNEMS-1
         LA    R2,LNEMS+27(R2)
         MVI   0(R2),C'$'
        $PRINT LNE,,DDOUT
         LTR   R15,R15
         BNZ   TSEOF
         TM    SWPRCS,OPTEST       SPECIAL TEST OPTION ?
         BZ    ERR2                NO
         TM    SWITCH,PUSHPULL     YES, MODIFY SCAN ERROR ?
         BZ    ERR2                NO
         LA    R2,DIAG2            SAY WHAT ABOUT MODIFY
         BAS   R8,TSCNL
         LA    R2,MCONT            SAY USE OF DEFAULT
         BAS   R8,TSCNL
         EJECT
*--------------------------------------------------------------------*
*        LOOK IF SHUTDOWN MAY BE STARTED.                            *
*--------------------------------------------------------------------*
         SPACE 1
CTLTOD   TM    SWPRCS,MSTEST       SEND OPTION REQUESTED ?
         BO    BEGIN               YES
         LM    R0,R1,TODREQ        REQUESTED TOD / CURRENT TOD
         CLR   R0,R1
         BNL   *+L'*+4
         A     R0,=A(24*60)        ADD 24 HOURS
         SR    R0,R1
         CL    R0,=F'35'
         BH    RESCHDL             TOD IS OVER HALF-HOUR + 5 MINUTES
         MVC   TODWRK(8),TODREQ    SET WORKING TOD
         CL    R0,=F'2'            MINIMUM 2 MINUTES ?
         BNL   CTLOK               YES
         L     R1,TODWRK+4         NO, ADJUST TOD TO SET 2 MINUTES
         L     R0,=F'2'
         AR    R1,R0
         ST    R1,TODWRK
CTLOK    ST    R0,RTIME
CTLSP    TM    SWITCH,SYSTASK      SYSTEM TASK ?
         BO    *+L'*+8             YES
         TM    SWPRCS,OPTEST       NO, TEST ?
         BZ    BEGIN               NO
         TM    SWITCH,PUSHPULL     PARM VALUES MODIFIED ?
         BZ    BEGIN               NO
         NI    SWITCH,255-PUSHPULL
         B     NXTWK
         EJECT
*--------------------------------------------------------------------*
*        RE-SCHEDULE SHUTDOWN FOR LATER.                             *
*--------------------------------------------------------------------*
         SPACE 1
RESCHDL  TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BZ    SMRSC               NO
         L     R3,ANSWER+4         COMMUNICATION AREA ADDRESS
         L     R4,4(R3)            CIB ADDRESS
         LA    R4,0(R4)
         USING CIBDSECT,R4
         LH    R1,CIBDATLN         GET DATA FIELD LENGTH
         LTR   R1,R1
         BNP   ERR3                NONE, UNABLE TO REQUEUE START CMD
         LA    R5,CIBDATA          SET DATA FIELD ADDRESS
         DROP  R4
         LA    R6,RSCIBTX          SET TARGET FIELD ADDRESS
         LA    R0,L'RSCIBTX        HIGH WATER MARK
MTSTC    MVC   0(1,R6),0(R5)       MOVE START COMMAND
         LA    R5,1(R5)
         CLI   0(R6),C''''
         BNE   NOTQU
         BCT   R0,*+L'*+4
         B     ERR3                LONG, UNABLE TO REQUEUE START CMD
         LA    R6,1(R6)
         MVI   0(R6),C''''
NOTQU    LA    R6,1(R6)
         BCT   R1,*+L'*+4
         B     ENDMV
         BCT   R0,MTSTC
         B     ERR3                LONG, UNABLE TO REQUEUE START CMD
ENDMV    MVC   0(4,R6),=CL4''''' '''
         LA    R1,RSCIBTX          COMPUTE
         SLR   R6,R1                 COMMAND
         LA    R6,RSCIBL(R6)           LENGTH
         B     RSCHD
SMRSC    MVC   RSCIBTX(32),=CL32'S SHUTDOWN,... WILL BE GENERATED'
         LA    R6,RSCIBL+28        MSG LENGTH
RSCHD    STH   R6,RSCIB            STORE CMD LENGTH
         L     R1,TODREQ           TOD REQUESTED
         CL    R1,=F'30'
         BNL   *+L'*+4
         A     R1,=A(24*60)        ADD 24 HOURS
         S     R1,=F'30'           TOD FOR WAKE-UP
         LA    R2,RSCIBHM          EDIT TIME
         BAS   R8,EDITTM
         LA    R2,RSCIB            COMMAND ADDRESS
         BAS   R8,CONSL            SEND IT
         B     LEAVE
         EJECT
*--------------------------------------------------------------------*
*        PREPARE MESSAGES FROM INPUT.                                *
*--------------------------------------------------------------------*
         SPACE 1
BEGIN   GETMAIN R,LV=MLEN          GET MESSAGES WORK AREA
         ST    R1,BOTTOM
         LR    R6,R1
         AL    R1,=A(MSTOT)
         ST    R1,TOP
         OI    SWITCH,INOPED       SAY INPUT OPENED
READCD  $READ  CARD,DDIN           READ A CARD IMAGE
         LTR   R15,R15
         BM    EOF
         BNZ   ERR4                ERROR
         CLI   CARD,C' '           MESSAGE TEXT CARD ?
         BE    MSGCD               YES
         OC    CARD,=CL80' '       UPPER CASE
         CLC   CARD(4),=CL4'-END'  HYPHENS REQUEST ?
         BE    HYPCD               YES
         CLC   CARD(4),=CL4'-TO '  DESTINATIONS REQUEST ?
         BE    DESCD               YES
         TM    SWITCH,COMTXT       COMMAND ALREADY HANDLED ?
         BO    READCD              YES, BYPASS CARD
         CLC   CARD(5),=CL5'-COM ' POSSIBLE COMMAND TEXT ?
         BNE   READCD              NO, BYPASS CARD
         CLC   CARD+5(67),=CL80' ' COMMAND TEXT ?
         BNE   CMDCD               YES
         B     READCD              BYPASS CARD
         SPACE 1
MSGCD    LM    R3,R5,MSGSC
         CLI   0(R5),C' '          SEARCH END OF TEXT
         BNE   *+L'*+12
         BCTR  R5,0
         CLR   R5,R3
         BNL   *-12
         B     READCD              NO TEXT, SKIP
         CL    R6,TOP              HIGH WATER MARK ?
         BNL   ERR5                YES, ERROR
         LA    R1,MSLEN            BUILD MESSAGE TEXT
         LA    R2,1(R6)
         LA    R15,65
MSGCD1   BCT   R1,*+L'*+4
         B     ERR8
         MVC   0(1,R2),0(R3)       MOVE TEXT
         BCTR  R15,0
         LA    R2,1(R2)
         CLI   0(R3),C''''
         BNE   MSGCD2
         BCT   R1,*+L'*+4
         B     ERR8
         MVI   0(R2),C''''         DOUBLE QUOTE
         LA    R2,1(R2)
MSGCD2   BXLE  R3,R4,MSGCD1
         LTR   R1,R15
         BNP   MSGCD3+L'MSGCD3
         MVI   0(R2),C' '
         BCT   R1,*+L'*+10
         B     MSGCD3
         MVC   1(*-*,R2),0(R2)     << EXECUTED >>
         BCTR  R1,0
         EX    R1,*-8
MSGCD3   AR    R2,R15
         SH    R2,=H'2'            POINTS JUST BEYOND LAST CHARACTER,
         SLR   R2,R6               SO COMPUTE EXECUTE LENGTH OF
         STC   R2,0(R6)            MESSAGE, AND SET IT IN AREA
         LA    R6,MSW(R6)          NEXT MESSAGE AREA
         B     READCD
         SPACE 1
HYPCD    CL    R6,TOP              HIGH WATER MARK ?
         BNL   ERR5                YES, ERROR
         MVI   0(R6),64            BUILD SEND HYPHENS
         MVI   1(R6),C'-'
         MVC   2(64,R6),1(R6)
         LA    R6,MSW(R6)          NEXT MESSAGE AREA
         B     READCD
         SPACE 1
DESCD    LM    R3,R5,CMDSC
         BCTR  R3,0
DESCD1   CLI   0(R3),C' '          SEARCH DESTINATIONS
         BNE   *+L'*+8
DESCD2   BXLE  R3,R4,*-8
         B     READCD
         LA    R1,2
         XR    R2,R2
         CLC   0(3,R3),=CL3'TSO'
         BNE   *+L'*+8
         LA    R2,SDTSO
         B     DESCD3
         CLC   0(3,R3),=CL3'IMS'
         BNE   DESCD4
         LA    R2,SDIMS
DESCD3   ALR   R3,R1
         BXLE  R3,R4,*+L'*+6
         LNR   R1,R1
         B     DESCD5
         CLI   0(R3),C' '
         BE    DESCD5
DESCD4   BXLE  R3,R4,*+L'*+4
         B     READCD
         CLI   0(R3),C' '
         BNE   DESCD4
         B     DESCD2
DESCD5   OI    SWITCH,DESTRQ
         EX    R2,*+L'*+10
         LTR   R1,R1
         BNM   DESCD2
         B     READCD
         OI    DESTFL,*-*          << EXECUTED >>
         SPACE 1
CMDCD    LM    R3,R5,CMDSC
         CLI   0(R5),C' '          SEARCH END OF COMMAND
         BNE   *+L'*+12
         BCTR  R5,0
         CLR   R5,R3
         BNL   *-12
         B     READCD              NO TEXT, SKIP
         CLI   0(R3),C' '          SEARCH START OF COMMAND
         BNE   *+L'*+4
         BXLE  R3,R4,*-8
         SLR   R5,R3               LENGTH
         EX    R5,MVCMD            BUILD COMMAND BUFFER
         LA    R5,5(R5)
         STH   R5,SHCIB
         OI    SWITCH,COMTXT       SAY COMMAND PROCESSED
         B     READCD
MVCMD    MVC   SHCIBT(*-*),0(R3)   << EXECUTED >>
         SPACE 1
EOF      ST    R6,TOP              SET HIGH LIMIT
        $RCLOSE
         NI    SWITCH,255-INOPED   SAY INPUT NOW CLOSED
         TM    SWITCH,SYSTASK      SYSTEM TASK ?
         BO    *+L'*+8             YES
         TM    SWPRCS,OPTEST       NO, BUT TEST ?
         BZ    NXTWK               NO
         EJECT
*--------------------------------------------------------------------*
*        ESTABLISH OPERATOR COMMUNICATIONS.                          *
*--------------------------------------------------------------------*
         SPACE 1
         L     R1,ANSWER           TIOT ADDRESS
         USING TIODSECT,R1
         MVC   OPMSPN(L'OPMSPN),TIOCNJOB     SET STARTED TASK NAME
         MVC   OPMSSN(L'OPMSSN),TIOCNJOB
         DROP  R1
         LA    R1,OPMSSN+L'OPMSSN-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   0(11,R1),=CL11',AT= OR IN='
        $SETOPC OPMS               ESTABLISH OP.COM.
         LTR   R15,R15             SUCCESSFULL ?
         BNZ   ERR6                NO, ERROR
         STM   R0,R1,MSGLL         SAVE LENGTH AND MSG-ID'S LIST
         EJECT
*--------------------------------------------------------------------*
*        SEND WARNING MESSAGES AT PRE-DEFINED INTERVALS.             *
*--------------------------------------------------------------------*
         SPACE 1
NXTWK    TM    SWPRCS,MSTEST       SEND OPTION REQUESTED ?
         BO    TSOTST              YES
        TIME   BIN                 GET CURRENT TOD
         LR    R1,R0               R0 = TOD IN 0.01 SECONDS
         XR    R0,R0
         D     R0,=A(60*100)       TRANSFORM TOD IN MINUTES
TSTWK    ST    R1,TODWRK+4         SET CURRENT TOD
         L     R0,TODWRK           REQUESTED TOD
         CLR   R0,R1
         BNL   *+L'*+4
         A     R0,=A(24*60)        ADD 24 HOURS
         SR    R0,R1               REMAINING TIME
         CL    R0,RTIME
         BH    QUIT                REQ. TIME EXHAUSTED
         LA    R2,TABT
         CL    R0,0(R2)
         BNH   *+L'*+8
         LA    R2,TABTL(R2)
         B     *-12
         L     R1,4(R2)
         LTR   R1,R1
         BNZ   CINTVL
         LR    R1,R0
         XR    R0,R0
         S     R1,=F'30'
         M     R0,=A(60*100)       TRANSFORM IN 100THS OF SEC.
         ST    R1,TMINTVL
         B     GOWAIT              GO WAIT UNTIL HALF-HOUR
CINTVL   ICM   R3,B'1111',8(R2)
         BZ    APPLY
         SR    R0,R3
         MH    R0,=H'60'           TRANSFORM MIN. TO SEC.
         CLR   R0,R1
         BNL   APPLY
         LR    R1,R0               USE THIS ONE TO ADJUST
APPLY    XR    R0,R0
         M     R0,=F'100'          TRANSFORM IN 100THS OF SEC.
         ST    R1,TMINTVL
         XR    R0,R0
         L     R1,TODWRK
         LA    R2,SHATHM           EDIT SHUTDOWN TIME
         BAS   R8,EDITTM
         L     R1,TODWRK+4
         LA    R2,SHTMHM           EDIT CURRENT TIME
         BAS   R8,EDITTM
         MVC   SHEXIT(L'SHEXIT),SHEXIT-1
         LM    R0,R1,TODWRK
         SR    R0,R1
         S     R0,=F'2'
         BP    *+L'*+6
         MVC   SHEXIT(13),=CL13'* EMERGENCY *'
TSOTST   TM    SWITCH,DESTRQ
         BZ    *+L'*+8
         TM    DESTFL,SDTSO
         BZ    IMSTST
         MVC   MGCRTXT(8),=CL8'SEND ''= '
         L     R5,BOTTOM
TSONXT   CL    R5,TOP
         BNL   TSOFNL
         XR    R1,R1
         IC    R1,0(R5)
         EX    R1,TSOMV
         LA    R1,MGCRTXT+9(R1)
         MVC   0(4,R1),=CL4' ='' '
         LA    R1,4(R1)
         LA    R2,MGCRBUF
         SLR   R1,R2
         STH   R1,MGCRBUF
         BAS   R8,CONSL            SEND TSO WARNING MESSAGES
         LA    R5,MSW(R5)          NEXT MESSAGE AREA
         B     TSONXT
TSOMV    MVC   MGCRTXT+8(*-*),1(R5)     << EXECUTED >>
TSOFNL   TM    SWPRCS,MSTEST       SEND OPTION REQUESTED ?
         BO    *+L'*+12            YES
         MVI   SHXEOM,C' '
         LA    R2,SHMSG
         B     *+L'*+8
         MVI   WRXEOM,C' '
         LA    R2,WRMSG
         MVC   4(5,R2),=CL5'SEND '
         BAS   R8,CONSL            SEND TSO WARNING MESSAGE
IMSTST   TM    SWITCH,DESTRQ
         BZ    GOWAIT
         TM    DESTFL,SDIMS
         BZ    GOWAIT
         LA    R9,1
         LA    R14,IMSRDY          REPLY IDENTIFICATION
         LA    R15,SIMSBM+2        REPLY-ID ADDRESS
         LA    R4,MIMSSK           NOT FOUND MSG
         BAS   R8,SRPIR            FILL REPLY-ID
         B     *+L'*+12            +0 = OK
         TM    DESTFL,SDTSO        +4 = NOT FOUND, ALSO TSO REQUESTED ?
         BO    GOWAIT              YES
         B     QUIT                NO
         LA    R2,SIMSB
         BAS   R8,CONSL            STARTING IMS BROADCAST
         MVC   MGCRTXT(13),=CL13'R ??,''=      '
         L     R5,BOTTOM
IMSNXT   CL    R5,TOP
         BNL   IMSFNL
         TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BO    *+L'*+8             YES
         TM    SWPRCS,MSTEST       SEND OPTION REQUESTED ?
         BZ    IMSMS               NO
         LA    R9,5
IMSRW1   LA    R14,IMSCNT          REPLY IDENTIFICATION
         LA    R15,MGCRTXT+2       REPLY-ID ADDRESS
         LA    R4,MIMSAB           NOT FOUND MSG
         BAS   R8,SRPIR            FILL REPLY-ID
         B     IMSMS               +0 = OK
         BAS   R8,WINTVLS          +4 = NOT FOUND
         BCT   R9,IMSRW1           TRY ONCE MORE UNTIL COUNT EXHAUSTED
         TM    DESTFL,SDTSO        ALSO TSO REQUESTED ?
         BZ    QUIT                NO
         NI    DESTFL,255-SDIMS    YES, DISABLE IMS
         B     GOWAIT
IMSMS    XR    R1,R1
         IC    R1,0(R5)
         EX    R1,IMSMV
         LA    R1,MGCRTXT+14(R1)
         MVC   0(9,R1),=CL9'      ='' '
         LA    R1,9(R1)
         LA    R2,MGCRBUF
         SLR   R1,R2
         STH   R1,MGCRBUF
         BAS   R8,CONSL            SEND IMS WARNING MESSAGES
         LA    R5,MSW(R5)          NEXT MESSAGE AREA
         B     IMSNXT
IMSMV    MVC   MGCRTXT+13(*-*),1(R5)    << EXECUTED >>
IMSFNL   TM    SWPRCS,MSTEST       SEND OPTION REQUESTED ?
         BO    *+L'*+12            YES
         MVI   SHXEOM,C'.'
         LA    R2,SHMSG
         B     *+L'*+8
         MVI   WRXEOM,C'.'
         LA    R2,WRMSG
         MVC   4(5,R2),=CL5'R ??,'
         TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BO    *+L'*+8             YES
         TM    SWPRCS,MSTEST       SEND OPTION REQUESTED ?
         BZ    IMSMSE              NO
         LA    R9,5
IMSRW2   LA    R14,IMSCNT          REPLY IDENTIFICATION
         LA    R15,6(R2)           REPLY-ID ADDRESS
         LA    R4,MIMSAB           NOT FOUND MSG
         BAS   R8,SRPIR            FILL REPLY-ID
         B     IMSMSE              +0 = OK
         BAS   R8,WINTVLS          +4 = NOT FOUND
         BCT   R9,IMSRW2           TRY ONCE MORE UNTIL COUNT EXHAUSTED
         TM    DESTFL,SDTSO        ALSO TSO REQUESTED ?
         BZ    QUIT                NO
         NI    DESTFL,255-SDIMS    YES, DISABLE IMS
         B     GOWAIT
IMSMSE   BAS   R8,CONSL            SEND IMS TIME MESSAGE
GOWAIT   TM    SWPRCS,MSTEST       SEND OPTION REQUESTED ?
         BO    QUIT                YES
         TM    SWITCH,SYSTASK      NO, SYSTEM TASK ?
         BO    *+L'*+8             YES
         TM    SWPRCS,OPTEST       NO, TEST ?
         BZ    TSSET               NO
         LA    R3,TMINTVL
         BAS   R8,WINTVL           WAIT UNTIL NEXT WAKE-UP
         LA    R1,L'PRMFLD         PREPARE TO ANY MODIFY
         STH   R1,PRMLG
        $OPCOM ,PRMLG              LOOK TO OPERATOR
         BCT   R15,TSSTOP
         B     NXTWK               R15 = 1, CONTINUE
TSSTOP   BCT   R15,TSMODF
         XC    MSGLA,MSGLA         R15 = 2, STOP, MSG-ID'S LIST
         B     LEAVE                 IS DELETED
TSMODF   BCT   R15,ERR7            R15 = 4, ERROR
         OI    SWITCH,PUSHPULL     R15 = 3, MODIFY, NEW AT= OR IN=
         NI    SWITCH,255-KEYWAT-KEYWIN               IS SPECIFIED
         B     PA00
TSSET    L     R1,STIME            SIMULATE ELAPSED TIME
         A     R1,TMINTVL
         CL    R1,=A(60*100)
         BL    NOUPDT
         XR    R0,R0
         ST    R0,STIME
         D     R0,=A(60*100)       TRANSFORM IN MINUTES
         A     R1,TODWRK+4         UPDATE CURRENT TOD
         B     TSTWK
NOUPDT   ST    R1,STIME
         L     R1,TODWRK+4         GET CURRENT TOD
         B     TSTWK+L'TSTWK
QUIT     OC    SHCIB,SHCIB         ANY COMMAND TO BE SEND
         BZ    LEAVE               NO
         LA    R2,SHCIB            YES, COMMAND ADDRESS
         BAS   R8,CONSL            SEND IT
         EJECT
*--------------------------------------------------------------------*
*        PROCESS TERMINATED - EXIT.                                  *
*--------------------------------------------------------------------*
         SPACE 1
LEAVE    XR    R10,R10             SET R.C. = 0
RETURN   ICM   R1,B'1111',BOTTOM
         BZ    TSPCL
        FREEMAIN R,A=(1),LV=MLEN   FREE MESSAGES WORK AREA
TSPCL    TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BO    FRDOM               YES
        $PCLOSE
         B     EXIT
FRDOM    L     R1,MSGLA
         LTR   R1,R1
         BZ    EXIT
        DOM    MSGLIST=(1)         DELETE MSG-ID'S LIST
         LM    R0,R1,MSGLL
        FREEMAIN R,LV=(0),A=(1)    FREE AREA
EXIT    $XRET  CC=(R10)
         EJECT
*--------------------------------------------------------------------*
*        EDIT TIME ROUTINE.                                          *
*              R1  - TOD IN MINUTES.                                 *
*              R2  - HOURS-MINUTES (HH.MM) TARGET FIELD ADDRESS.     *
*              R8  - LINK REGISTER.                                  *
*--------------------------------------------------------------------*
         SPACE 1
EDITTM   CL    R1,=F'60'
         BL    *+L'*+10
         XR    R0,R0
         D     R0,=F'60'
         B     *+L'*+4
         LR    R0,R1
         XR    R1,R1
         CVD   R1,WORKD            EDIT HOURS
         UNPK  WORKD(4),WORKD+6(2)
         OI    WORKD+3,C'0'
         MVC   0(2,R2),WORKD+2
         CVD   R0,WORKD            EDIT MINUTES
         UNPK  WORKD(4),WORKD+6(2)
         OI    WORKD+3,C'0'
         MVC   3(2,R2),WORKD+2
         BR    R8                  RETURN
         EJECT
*--------------------------------------------------------------------*
*        CALL SEARCH REPLY-ID PENDING ROUTINE.                       *
*              R2  - ADDRESS OF MESSAGE IF NOT FOUND.                *
*              R8  - LINK REGISTER. RETURN +0 = OK, +4 = NOT FOUND.  *
*              R9  - TRY AGAIN COUNTER.                              *
*              R14 - ADDRESS OF REPLY-ID NUMBER EDIT.                *
*              R15 - ADDRESS OF REPLY MESSAGE IDENTIFICATION TEXT.   *
*--------------------------------------------------------------------*
         SPACE 1
SRPIR   $SRPI  (R14),(R15)
         LTR   R15,R15
         BZR   R8
         CH    R15,=H'4'
         BNE   ERR9
         LA    R8,4(R8)
         TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BO    *+L'*+6             YES
         LR    R2,R4               NO
         B     TSCNL
         CH    R9,=H'1'            LAST TIME ?
         BNER  R8                  NO
         LR    R1,R4               YES
        WTO    MF=(E,(1))
         BR    R8
         EJECT
*--------------------------------------------------------------------*
*        CONSOLE SVC AND WAIT ROUTINE.                               *
*              R2  - COMMAND ADDRESS.                                *
*              R3  - WAIT TIME ADDRESS WHEN ENTERING AT "WINTVL".    *
*              R8  - LINK REGISTER.                                  *
*--------------------------------------------------------------------*
         SPACE 1
CONSL    TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BO    *+L'*+8             YES
         TM    SWPRCS,MSTEST       NO, BUT SEND OPTION REQUESTED ?
         BZ    TSCNL               NO
        ZEROKEY
         XR    R0,R0               CLEAR R0 FOR SVC-34
        MGCR   (R2)
        RESETKEY
WINTVLS  LA    R3,WAITABIT         WAIT A LITTLE
WINTVL  STIMER WAIT,BINTVL=(R3)
         BR    R8                  RETURN
         EJECT
*--------------------------------------------------------------------*
*        PRINT ROUTINE.                                              *
*              R2  - MESSAGE ADDRESS.                                *
*              R8  - LINK REGISTER.                                  *
*--------------------------------------------------------------------*
         SPACE 1
TSCNL    MVC   LNEMS(L'LNEMS),LNEMS-1
         XR    R1,R1
         IC    R1,1(R2)
         SH    R1,=H'5'
         BNPR  R8
         EX    R1,MVCTX
        $PRINT LNE,,DDOUT          PRINT LINE TEXT
         LTR   R15,R15
         BNZ   TSEOF               BRANCH IF ERROR
         BR    R8                  RETURN
MVCTX    MVC   LNEMS(*-*),4(R2)    << EXECUTED >>
         EJECT
*--------------------------------------------------------------------*
*        ERROR DETECTED - EXIT WITH MESSAGE.                         *
*--------------------------------------------------------------------*
         SPACE 1
ERR1     BCTR  R10,0               OPERATOR AUTHORITY REQUIRED
ERR2     BCTR  R10,0               SHUTDOWN TIME LIMIT ERROR
ERR3     BCTR  R10,0               UNABLE TO REQUEUE START CMD
ERR4     BCTR  R10,0               'SYSTMSG' READ ERROR
ERR5     BCTR  R10,0               MAXIMUM NUMBER OF MSGS OVERFLOW
ERR6     BCTR  R10,0               OP.COM. NOT ESTABLISHED
ERR7     BCTR  R10,0               OP.COM. FAILED
ERR8     BCTR  R10,0               TOO MUCH QUOTES IN MSG TEXT
ERR9     BCTR  R10,0               CALL SRCHRPI ERROR
         SPACE 1
MAXERR   EQU   ((*-ERR1)/2)+1
         SPACE 1
         LR    R2,R10              SEND APPROPRIATE MESSAGE
         BCTR  R2,0
         SLL   R2,2
         L     R1,TABD(R2)
         TM    SWITCH,SYSTASK      SYSTEM TASK RUNNING ?
         BO    TSWTT               YES
         LR    R2,R1
         BAS   R8,TSCNL            PRINT ERROR MESSAGE
         B     TSEOF
TSWTT   WTO    MF=(E,(1))
TSEOF    TM    SWITCH,INOPED       INPUT ALREADY CLOSED ?
         BZ    SETRC               YES
        $RCLOSE ,
SETRC    LA    R10,16              SET R.C. = 16
         B     RETURN              EXIT
         EJECT
*--------------------------------------------------------------------*
*        CONSTANTS AND WORK-AREAS.                                   *
*--------------------------------------------------------------------*
         SPACE 1
WORKD    DC    D'0'
WORKF    DC    F'0'
ANSWER   DC    A(*-*,*-*)          A.TIOT / A.COMM.
MSGLL    DC    F'0'                LENGTH OF AREA AND
MSGLA    DC    F'0'                  MSG-ID'S LIST
TODREQ   DC    2F'0'               REQ. TOD / CURR. TOD
TODWRK   DC    2F'0'               REQ. WORK TOD / CURR. WORK TOD
RTIME    DC    F'0'
STIME    DC    F'0'
BOTTOM   DC    A(*-*)
TOP      DC    A(*-*)
MSGSC    DC    A(CARD+1,1,CARD+65)
CMDSC    DC    A(CARD+5,1,CARD+71)
TMINTVL  DC    F'0'                WAIT TIME (100THS OF SEC.)
WAITABIT DC    F'20'               WAIT A LITTLE (100THS OF SEC.)
DDIN     DC    CL8'SYSTMSG '
DDOUT    DC    CL8'SYSLMSG '
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUTH)
AUTH     DC    XL1'0'
         SPACE 1
SWITCH   DC    XL1'0'         INDICATORS
SYSTASK  EQU   X'80'               SYSTEM TASK RUN
PUSHPULL EQU   X'40'               MODIFY PARM VALUES
INOPED   EQU   X'20'               INPUT IS OPENED
COMTXT   EQU   X'10'               COMMAND PROCESSED
KEYWIN   EQU   X'08'               IN= PARM KEYWORD FOUND
KEYWAT   EQU   X'04'               AT= PARM KEYWORD FOUND
DESTRQ   EQU   X'02'               DESTINATION REQUEST FOUND
         SPACE 1
SWPRCS   DC    XL1'0'         PROCESSES
OPTEST   EQU   X'80'               TEST TIMING AND MODIFY
MSTEST   EQU   X'40'               TEST MESSAGES SENDER
         SPACE 1
DESTFL   DC    XL1'0'         DESTINATIONS
SDTSO    EQU   X'80'               LOGGED ON TSO USERS
SDIMS    EQU   X'40'               ACTIVE IMS USERS
         EJECT
CARD     DC    CL80' '
         DC    AL1(L'LNE)
LNE      DS    0CL133
         DC    CL1' '
LNEMS    DC    CL132' '
         SPACE 1
MGCRBUF  DC    0F'0',AL2(*-*,0)    COMMAND LENTGH
MGCRTXT  DC    CL132' '            COMMAND TEXT
SIMSB    DC    0F'0',AL2(SIMSBL,0) COMMAND LENTGH
SIMSBM   DC    C'R ??,''/BRO ACTIVE '' '
SIMSBL   EQU   *-SIMSB
SHMSG    DC    0F'0',AL2(SHMSGL,0) COMMAND LENTGH
SHDEST   DC    CL5' ',C'''===> SCHEDULED SHUTDOWN AT '
SHATHM   DC    CL5'  .  ',C' - CURRENT TIME IS '
SHTMHM   DC    CL5'  .  ',C' '
SHEXIT   DC    CL13' ',C' '
SHXEOM   DC    CL1' ',C''' '
SHMSGL   EQU   *-SHMSG
WRMSG    DC    0F'0',AL2(WRMSGL,0) COMMAND LENTGH
         DC    CL5' ',C'''===> THIS IS A TEST ONLY, '
         DC    C'DON''''T WORRY ABOUT <=== '
WRXEOM   DC    CL1' ',C''' '
WRMSGL   EQU   *-WRMSG
SHCIB    DC    F'0'                COMMAND LENTGH
SHCIBT   DC    CL70' '
RSCIB    DC    F'0'                COMMAND LENTGH
         DC    C'$TA,T='
RSCIBHM  DC    CL5'  .  ',C',''$VS,'''''
RSCIBL   EQU   (*-RSCIB)+4
RSCIBTX  DC    CL100' ',CL4' '
         SPACE 1
IMSRDY   DC    AL1(L'IMSRDYM-1)
IMSRDYM  DC    C'DFS996I *IMS READY* '
IMSCNT   DC    AL1(L'IMSCNTM-1)
IMSCNTM  DC    C'DFS972A *IMS AWAITING MORE INPUT* '
         EJECT
TABT     DC    A(5,30,0)           R.MIN. / W.SEC. / ADJUST VALUE
TABTL    EQU   *-TABT
         DC    A(10,1*60,0)
         DC    A(30,5*60,10)
         DC    A(24*60,0,0)
         SPACE 1
TABD     DC    A(DIAG1,DIAG2,DIAG3,DIAG4,DIAG5)
         DC    A(DIAG6,DIAG7,DIAG8,DIAG9)
        PRINT  NOGEN
MCONT   WTO    'SHUTDOWN : CONTINUE WITH DEFAULT (IN=20 ASSUMED).',    X
               ROUTCDE=2,DESC=6,MF=L
MIMSSK  WTO    'SHUTDOWN : *IMS READY* NOT FOUND - PROCESS SKIP''D.',  X
               ROUTCDE=2,DESC=6,MF=L
MIMSAB  WTO    'SHUTDOWN : *IMS CONTINUE* NOT FOUND - PROCESS ABORT''D.X
               ',ROUTCDE=2,DESC=6,MF=L
DIAG1   WTO    'SHUTDOWN : OPERATOR AUTHORITY IS REQUIRED.',           X
               ROUTCDE=2,DESC=6,MF=L
DIAG2   WTO    'SHUTDOWN : INVALID AT= OR IN= REQUESTED.',             X
               ROUTCDE=2,DESC=6,MF=L
DIAG3   WTO    'SHUTDOWN : UNABLE TO REQUEUE START COMMAND.',          X
               ROUTCDE=2,DESC=6,MF=L
DIAG4   WTO    'SHUTDOWN : ''SYSTMSG'' (INPUT) READ ERROR.',           X
               ROUTCDE=2,DESC=6,MF=L
DIAG5   WTO    'SHUTDOWN : MAXIMUM NUMBER OF MESSAGES OVERFLOW.',      X
               ROUTCDE=2,DESC=6,MF=L
DIAG6   WTO    'SHUTDOWN : OP.COM. NOT ESTABLISHED.',                  X
               ROUTCDE=2,DESC=6,MF=L
DIAG7   WTO    'SHUTDOWN : OP.COM. FAILED.',                           X
               ROUTCDE=2,DESC=6,MF=L
DIAG8   WTO    'SHUTDOWN : TOO MUCH QUOTES IN MESSAGE TEXT.',          X
               ROUTCDE=2,DESC=6,MF=L
DIAG9   WTO    'SHUTDOWN : CALL "SRCHRPI" ERROR DETECTED.',            X
               ROUTCDE=2,DESC=6,MF=L
        PRINT  GEN
         SPACE 1
OPMS     DC    AL1(L'OPMSP+L'OPMSPN)
OPMSP    DC    C' --- SHUTDOWN - TO INTERRUPT USE COMMAND : P '
OPMSPN   DC    CL8' '
         DC    AL1(L'OPMSS+L'OPMSSN+L'OPMSSS)
OPMSS    DC    C'     OR TO CHANGE PARM''S USE : F '
OPMSSN   DC    CL8' '
OPMSSS   DC    CL11' '
         DC    AL1(0)
         SPACE 1
PRMLG    DC    H'0'
PRMFLD   DC    XL100'0'
         EJECT
         LTORG
         EJECT
        PRINT  NOGEN
         SPACE 1
CIBDSECT DSECT
CIB     IEZCIB
TIODSECT DSECT
        IEFTIOT1
         SPACE 2
         END
