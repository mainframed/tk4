AU95     TITLE '-- ROUTINE AUT95 - STATUS SERVICE --'
AUT95    START 0
         SPACE 1
**********************************************************************
*        THIS SUBROUTINE PREPARE THE CURRENT DATA-SET STATUS         *
*        TO BE DISPLAYED BY AUT99 WHEN USING PF-2 KEY.               *
*                                   (MOINIL P.A. - 16 NOVEMBER 1989) *
**********************************************************************
*        CALL AUT95                                                  *
*        FUNCTION : STATUS PREPARE PANEL.                            *
*         AT EXIT : R15 = 0 - NORMAL COMPLETION WITH                 *
*                             R0  = SCREEN IMAGE LENGTH              *
*                             R1  = SCREEN IMAGE ADDRESS             *
*                         4 - ABNORMAL COMPLETION OF 'ZSTATE' CALL   *
*                             (MESSAGE OF SEVERE ERROR SENT)         *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R10
         SPACE 1
        CALL   ZSTATE,(RETCD,STAR),VL  GET STATUS
         LTR   R0,R15                  TEST RETURN CODE
         BZ    OK
         L     R1,=V(AUT99I)
         XC    0(4,R1),0(R1)           NULLIFY PF-2 USE
         L     R1,4(R13)               R1 = AUT99 SAVE AREA
         MVC   SVAR(18*4),0(R1)        SAVE IT AROUND AUT97 USE
         XR    R1,R1
         LM    R2,R3,NZST
         L     R15,=V(AUT97)
         BASR  R14,R15
         L     R1,4(R13)               R1 = AUT99 SAVE AREA
         MVC   0(18*4,R1),SVAR         RESTORE IT
         LA    R15,4                   SET RC=4
         B     LEAVE
OK       LA    R2,STAR                 SET STATUS AREA ADDRESSABILITY
         USING STADSECT,R2
         MVC   EDSNM,STADSNM
         MVC   EVLSR,STAVLSR
         MVC   EDSOR,STADSOR
         LH    R0,STARECL
         BAS   R14,ENRTN
         MVC   ERECL(L'ERECL),ZONE+L'ZONE-L'ERECL
         L     R0,STANAUT
         BAS   R14,ENRTN
         MVC   ENAUT(L'ENAUT),ZONE+L'ZONE-L'ENAUT
         L     R0,STAUAUT
         BAS   R14,ENRTN
         MVC   EUAUT(L'EUAUT),ZONE+L'ZONE-L'EUAUT
         L     R0,STANPRG
         BAS   R14,ENRTN
         MVC   ENPRG(L'ENPRG),ZONE+L'ZONE-L'ENPRG
         L     R0,STAUPRG
         BAS   R14,ENRTN
         MVC   EUPRG(L'EUPRG),ZONE+L'ZONE-L'EUPRG
         L     R0,STANARG
         BAS   R14,ENRTN
         MVC   ENARG(L'ENARG),ZONE+L'ZONE-L'ENARG
         L     R0,STAUARG
         BAS   R14,ENRTN
         MVC   EUARG(L'EUARG),ZONE+L'ZONE-L'EUARG
         L     R0,STANATB
         BAS   R14,ENRTN
         MVC   ENATB(L'ENATB),ZONE+L'ZONE-L'ENATB
         L     R0,STANMTB
         BAS   R14,ENRTN
         MVC   ENMTB(L'ENMTB),ZONE+L'ZONE-L'ENMTB
         L     R0,STATOTL
         BAS   R14,ENRTN
         MVC   ETOTL(L'ETOTL),ZONE+L'ZONE-L'ETOTL
         DROP  R2
         L     R2,4(R13)               SET R0-R1 BACK
         MVC   20(8,R2),PECRAN
LEAVE   $XRET  CC=(R15)                RETURN
         EJECT
*--------------------------------------------------------------------*
*        EDIT NUMBER ROUTINE (LINK = R14 - NUMBER = R0)              *
*--------------------------------------------------------------------*
         SPACE 1
ENRTN    CVD   R0,DSD
         MVC   ZONE,=XL16'40202020202020202020202020202120'
         ED    ZONE,DSD
         BR    R14
         SPACE 2
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
DSD      DC    D'0'
SVAR     DC    18F'0'
RETCD    DC    F'0'
PECRAN   DC    A(LECRAN,ECRAN)
NZST     DC    0F'0',CL8'ZSTATE'
ZONE     DC    XL16'0'
STAR     DC    (STALGTH)X'0' STATUS AREA (SEE STADSECT)
         EJECT
*--------------------------------------------------------------------*
*        ECRAN D'OUTPUT                                              *
*--------------------------------------------------------------------*
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='CURRENT ACCOUNTING DATA-SET STATUS',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='==================================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(6,1),SF=(PROT),TEXT='Data-set name :',MF=L
        $FS    SF=(PROT,INT),MF=L
EDSNM   $FS    TEXT=(' ',44),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(8,1),SF=(PROT),TEXT='Volume serial :',MF=L
        $FS    SF=(PROT,INT),MF=L
EVLSR   $FS    TEXT=(' ',6),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(8,30),SF=(PROT),TEXT='Organization :',MF=L
        $FS    SF=(PROT,INT),MF=L
EDSOR   $FS    TEXT=(' ',2),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(8,55),SF=(PROT),TEXT='Record length :',MF=L
        $FS    SF=(PROT,INT),MF=L
ERECL   $FS    TEXT=(' ',5),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(12,1),SF=(PROT),TEXT='Capacities :',MF=L
        $FS    SBA=(12,20),SF=(PROT,INT),MF=L
ENAUT   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='Aut. records -',MF=L
        $FS    SF=(PROT,INT),MF=L
EUAUT   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT=' in use',MF=L
        $FS    SBA=(13,20),SF=(PROT,INT),MF=L
ENPRG   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='Prg. records -',MF=L
        $FS    SF=(PROT,INT),MF=L
EUPRG   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT=' in use',MF=L
        $FS    SBA=(14,20),SF=(PROT,INT),MF=L
ENARG   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='Arg. records -',MF=L
        $FS    SF=(PROT,INT),MF=L
EUARG   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT=' in use',MF=L
        $FS    SBA=(15,20),SF=(PROT,INT),MF=L
ENATB   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='Arg. Table records',MF=L
        $FS    SBA=(16,20),SF=(PROT,INT),MF=L
ENMTB   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='map Table records',MF=L
        $FS    SBA=(18,2),SF=(PROT),TEXT='T O T A L :',MF=L
        $FS    SBA=(18,20),SF=(PROT,INT),MF=L
ETOTL   $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='records',MF=L
        $FS    SBA=(24,1),SF=(PROT),TEXT='Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to escape',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAN   EQU   *-ECRAN
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         SPACE 1
*--------------------------------------------------------------------*
*        DUMMY SECTIONS                                              *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT GEN
        $AIRD  PRI=NO,STA=YES,R=NO
         SPACE 2
         END
