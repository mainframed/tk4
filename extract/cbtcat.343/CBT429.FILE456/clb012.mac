B012     TITLE 'CLEAN-UP - EXECUTE IDCAMS STATEMENTS (B012).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B012    @XS
         TM    RUNIND,RUTEST       TEST MODE?
         BO    EI80                YES, GO LIST ONLY
         BAS   R14,IDCOAR          NO, ALLOCATE IDCAMS OUTPUT PRINT
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR72
        $VOPEN WORKD,(R6),MF=(L,EIRDOUT)
        $VCLOSE (R6),MF=(L,EICLOUT)
         BAS   R14,XIDCAMS         EXECUTE IDCAMS
         CVD   R15,WORKD
         ED    EIPRTT+4(4),WORKD+6
        @OUT   EIPRT,=PL2'+40',U
        $VOPEN MF=(E,EIRDOUT)
         LTR   R15,R15
         BNZ   ERR72
         OI    WHATS,WHIDOUT       SAY OPENED
         CLC   WORKD(4),=AL4(L'LINE)
         BH    ERR73
        $VREAD LINE,,MF=(L,EIRDOUT)
EI1      MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
        $VREAD MF=(E,EIRDOUT)      READ IDCAMS OUTPUT PRINT
         LTR   R15,R15
         BM    EI50
         BNZ   ERR72
         CLC   LINE(8),=CL8'1IDCAMS'    TITLE LINE?
         BE    EI1                      YES, SKIP IT
         LA    R1,LINE+1
         LA    R2,1
         LA    R3,LINE+L'LINE-1
         CLI   0(R1),C' '
         BNE   EI10
         BXLE  R1,R2,*-8
         B     EI1
EI10     CLC   0(9,R1),=CL9'IDC0001I'   TEST FUNCTION COMPLETED = 0
         BNE   EI11
         CLC   55(3,R1),=CL3' 0'
         BE    EI1
         B     EI40
EI11     CLC   0(9,R1),=CL9'IDC0002I'   TEST IDCAMS COMPLETED = 0
         BNE   EI12
         CLC   63(3,R1),=CL3' 0'
         BE    EI1
         B     EI40
EI12     CLC   0(3,R1),=CL3'IDC'   TEST ANY IDCAMS INFO'S (IDC...)
         BNE   EI1
         CLC   3(16,R1),=CL16'0550I ENTRY (A) '
         BNE   EI40
         MVI   0(R1),C' '
         MVC   1(18,R1),0(R1)
EI40     MVI   LINE,C' '
        @OUT   LINE
         B     EI1+L'EI1
EI50     NI    WHATS,255-WHIDOUT   CLOSE IDCAMS OUTPUT PRINT
        $VCLOSE MF=(E,EICLOUT)
         LTR   R15,R15             TEST RETURN CODE
         BZ    EI99
         B     ERR72
         SPACE 1
EI80     MVC   EIPRTT,=CL16'SIMULATED (TEST)'
        @OUT   EIPRT,=PL2'+40',U
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         L     R2,AGIDST           IDCAMS STARTING ARRAY
EI81     LA    R3,4(R2)
EI82     MVC   LINE+6(80),0(R3)
        @OUT   LINE
         LA    R3,80(R3)
         CLI   0(R3),FMON          FINAL MARKER?
         BNE   EI82                NO
         ICM   R2,B'1111',0(R2)    YES, ANOTHER ARRAY?
         BNZ   EI81                YES, CONTINUE
         SPACE 1
EI99    @XE
         EJECT
EIRDOUT $VREAD ,,MF=L              ALSO USED BY VOPEN
EICLOUT $VCLOSE ,MF=L
         SPACE 1
         DC    AL1(L'EIPRT+L'EIPRTT)
EIPRT    DC    C'0IDCAMS PROCESSING : '
EIPRTT   DC    0CL16' ',CL5'RC = ',XL3'202120',CL8' '
         SPACE 2
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
