NX00     TITLE 'RACF - COMMAND PRE-PROCESSING EXIT (ICHCNX00).'
ICHCNX00 START 0
         SPACE 1
* PURPOSE :    USERS WHO ARE NOT ALLOWED TO USE RACF FOR DATA-SETS
* ---------    PROTECTION MUST BE AVOID FROM ACCESS THE 'ADDSD',
*              'ALTDSD', 'DELDSD' AND 'PERMIT' COMMANDS.
*
* METHOD :     A USER WHO DON'T HAVE THE ADSP ATTRIBUTE OR THE
* --------     INSTALLATION DATA FIELD CONTAINING THE KEYWORD
*              'ALLOWED' WILL BE REJECTED.
*
* AT ENTRY TO THE ICHCNX00 EXIT ROUTINE, THE REGISTER 1 CONTAINS THE
* --------     ADDRESS OF THE PARAMETER LIST AREA (MAPPED BY ICHCNXP).
*
* AT EXIT FROM THE ICHCNX00 EXIT ROUTINE, THE REGISTER 15 SHOULD
* -------      CONTAIN A RETURN CODE FOR PROCESSING CONTINUATION AS
*              BELOW :
*               0 - NORMAL PROCESSING IS TO CONTINUE.
*               4 - THE REQUEST IS NOT ACCEPTED, AND IS TO BE FAILED.
*               8 - THE REQUEST IS NOT ACCEPTED, AND IS TO BE FAILED.
*              12 - EXIT ROUTINE PROCESSING IS COMPLETE, AND THE
*                   REQUEST IS GRANTED.
*
* AUTHOR :     RAVARANI N. - MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
ICHCNX00 AMODE ANY
ICHCNX00 RMODE ANY
         EJECT
        $XENT  BASE=R12,TYPE=RENT
         USING CNXPL,R1
         ICM   R2,B'1111',CNXCALLR GET CALLER ADDRESS
         LA    R3,CMDTBL           COMMANDS TABLE ADDRESS
         LA    R4,L'CMDTBL         COMMANDS TABLE ELEMENT LENGTH
         LA    R5,CMDTBLE          COMMANDS TABLE ENDING ADDRESS
SCANTBL  CLC   0(L'CMDTBL,R3),0(R2)     IS IT THIS FUNCTION CODE?
         BE    GOTEST              YES
         BXLE  R3,R4,SCANTBL       LOOP IF MORE
         B     LETSGO
         USING PSA,R0              ADRESS PSA
GOTEST   L     R2,PSAAOLD          GET ADDRESS OF ASCB
         LTR   R2,R2               VERIFY ADDRESS
         BZ    LETSGO              SOMETHING WRONG
         DROP  R0
         USING ASCB,R2             ESTABLISH ADDRESSABILITY
         CLC   ASCBASCB,=CL4'ASCB' VERIFY CONTROL BLOCK
         BNE   LETSGO              WHAT'S HAPPENING?
         L     R2,ASCBASXB         GET ADDRESS OF ASXB
         LTR   R2,R2               VERIFY ADDRESS
         BZ    LETSGO              SOMETHING WRONG
         DROP  R2
         USING ASXB,R2             ESTABLISH ADDRESSABILITY
         CLC   ASXBASXB,=CL4'ASXB' VERIFY CONTROL BLOCK
         BNE   LETSGO              WHAT'S HAPPENING?
         L     R3,ASXBSENV         GET ACEE ADDRESS
         LTR   R3,R3               VERIFY ADDRESS
         BZ    LETSGO              SOMETHING WRONG
         DROP  R2
         USING ACEE,R3             ESTABLISH ADDRESSABILITY TO ACEE
         TM    ACEEFLG1,ACEEADSP   ADSP ATTRIBUTE?
         BO    LETSGO              YES, USER IS AUTHORIZED
         L     R6,ACEEINST         GET ADDRESS OF INSTALLATION DATA
         DROP  R3
         LTR   R6,R6               ANY INSTALLATION DATA?
         BZ    REJECT              NO
         CLI   0(R6),7             IS INSTALLATION DATA LONG ENOUGH?
         BL    REJECT              NO
         XR    R5,R5
         IC    R5,0(R6)            GET INSTALLATION DATA LENGTH
         LA    R5,0(R5,R6)         POINT TO LAST BYTE
         SH    R5,=H'6'            LAST POSITION TO COMPARE
         LA    R4,1                COMPARE STEP
BUMP     BXLE  R6,R4,SCAN          NEXT IF MORE
REJECT   LA    R15,4               SET REQUEST NOT ACCEPTED -----------
         B     GOBACK
SCAN     CLC   0(7,R6),=CL7'ALLOWED'    IS USER ALLOWED?
         BNE   BUMP                NO, LOOK FURTHER
         DROP  R1
LETSGO   XR    R15,R15             SET ACCEPT RETURN CODE -------------
GOBACK  $XRET  CC=(R15),TYPE=RENT
         EJECT
CMDTBL   DS    0XL1
         DC    XL1'03'             ADDSD
         DC    XL1'04'             ALTDSD
         DC    XL1'05'             DELDSD
         DC    XL1'07'             PERMIT
CMDTBLE  EQU   *-L'CMDTBL          END OF TABLE
         SPACE 2
        LTORG
         SPACE 2
        PRINT  NOGEN
        ICHCNXP
        IHAPSA
        IHAASCB
        IHAASXB
        IHAACEE
         SPACE 2
         END
