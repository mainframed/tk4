LACC     TITLE 'LIST ACCOUNTING CONTROL DATA-SET CONTENTS.'
LTACC    START 0
         SPACE 1
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. -ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
         SPACE 1
*---------------------------------------------------------------------*
*        AUTHORITY LEVELS DEFINITIONS.                                *
*---------------------------------------------------------------------*
         SPACE 1
LV0      EQU   0         REJECT LEVEL
         SPACE 1
AUT98   $MDL@IX
         EJECT
        $AIRD  SEC=YES,STA=YES
*        PROGRAMMERS WORK TABLE ELEMENT DESCRIPTION.
         SPACE 1
PRSDSECT DSECT
PRSPNME  DS    CL40                PROGRAMMER'S NAME
PRSPBOX  DS    CL3                 BOX NUMBER
PRSPNUM  DS    CL5                 PROGRAMMER'S NUMBER
PRSPMTR  DS    CL6                 MATRICULE NUMBER
PRSPBLD  DS    CL6                 BUILDING NUMBER
PRSPPHN  DS    CL9                 PHONE NUMBER
PRSPSRV  DS    CL8                 SERVICE NUMBER
PRSDLG   EQU   *-PRSDSECT          ELEMENT LENGTH
         SPACE 1
LTACC    CSECT ,                   RESTORE ORIGINAL CSECT
         SPACE 2
*        PROGRAMMERS WORK TABLE ELEMENT SORT FIELDS.
         SPACE 1
S1FD     EQU   PRSPNME-PRSDSECT    SORT 1 = PROGRAMMER'S NAME
S1FL     EQU   L'PRSPNME
S2FD     EQU   PRSPBOX-PRSDSECT    SORT 2 = BOX/PROGRAMMER'S NUMBERS
S2FL     EQU   L'PRSPBOX+L'PRSPNUM
         EJECT
        $UJVT
         EJECT
        $XENT  BASE=(R10,R11)
         SPACE 1
*---------------------------------------------------------------------*
*        HANDLE PARM. FIELD IF ANY (ALTERNATE DATA-SET NAME).         *
*---------------------------------------------------------------------*
         SPACE 1
         L     R1,0(R1)            GET PARM. FIELD ADDRESS
         LH    R2,0(R1)            GET PARM. FIELD LENGTH
         LTR   R2,R2
         BNP   NOPARM
         MVI   DYNDSN,C' '
         MVC   DYNDSN(L'DYNDSN-1),DYNDSN
         BCT   R2,*+L'*+6
         MVC   DYNDSN(*-*),2(R1)   SET ACCOUNT DATA-SET NAME
         EX    R2,*-6
         EJECT
NOPARM  $EACM  REQAUT
         CLI   AUTLVL,LV0
         BE    NOTAUT
         CLI   DYNDSN,C' '
         BNE   ALLDSN
         L     R15,=V(SUJVTA)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   UJVTERR
         USING UVTDSECT,R1
         L     R2,ACCNME
         DROP  R1
         MVC   DYNDSN(L'DYNDSN),0(R2)   GET ACC. DATA-SET DEFAULT NAME
         L     R15,=V(DUJVTA)
         BASR  R14,R15
ALLDSN  $DYNALL DYNLIST            DYNAMIC ALLOC OF SYSACC
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERALL               ERROR ALLOCATION
        $GFDATE DATE,TIME
         LTR   R15,R15
         BNZ   ERRDT
         MVC   HOUR(2),TIME
         MVC   HOUR+3(2),TIME+2
         MVC   HOUR+6(2),TIME+4
        CALL   ZOPEN,(RETCD,XOPT),VL
         LTR   R0,R15
         BNZ   ERRZY
         EJECT
*---------------------------------------------------------------------*
*        GET PROGRAMMERS WORK TABLE AREA.                             *
*---------------------------------------------------------------------*
         SPACE 1
        CALL   ZSTATE,(RETCD,STAR),VL  GET STATUS
         LTR   R0,R15
         BNZ   ERRZY
         LA    R2,STAR                 SET STATUS AREA ADDRESSABILITY
         USING STADSECT,R2
         L     R1,STAUPRG
         DROP  R2
         MH    R1,SPWTB+6
         LR    R2,R1
         AH    R1,=H'7'
         SRL   R1,3
         SLL   R1,3
         ST    R1,LPWTB
        GETMAIN R,LV=(1)
         ST    R1,APWTB
         SL    R1,SPWTB+4
         ST    R1,SPWTB
         AR    R1,R2
         ST    R1,SPWTB+8
         SPACE 1
*---------------------------------------------------------------------*
*        GET ALL DATA-SET RECORDS IN SEQUENCE.                        *
*---------------------------------------------------------------------*
         SPACE 1
READ    CALL   ZSEQR,(RETCD,AREA,NAUT),VL
         LTR   R0,R15
         BM    REND
         BNZ   ERRZY
         LA    R9,AREA
         TM    SWITCH,SWP
         BO    NXSCT
         CLC   NAUT,MAXN
         BH    NXSCT
         EJECT
*---------------------------------------------------------------------*
*        AUTHORIZATIONS RECORDS PROCESSING.                           *
*---------------------------------------------------------------------*
         SPACE 1
         USING AUTDSECT,R9
         LA    R1,AUTNPNF
         LA    R2,AUTPNO
         LA    R5,LPG
SCLOOP   LH    R0,0(R2)
         LTR   R0,R0
         BZ    NOST
         STH   R0,0(R5)
         LA    R5,L'LPG(R5)
NOST     LA    R2,L'AUTPNO(R2)
         BCT   R1,SCLOOP
         CLC   NLI,LMX
         BNH   NOSK
         XC    NLI,NLI
         MVC   L1A,DYNDSN
         MVC   L1B,DATE
         MVC   L1C,HOUR
         LH    R0,NPA
         AH    R0,=H'1'
         STH   R0,NPA
         BAS   R14,CNVRT
         MVC   L1D(L'L1D),ZONE+L'ZONE-L'L1D
         LA    R7,L1
         BAS   R8,PRT
         LA    R7,L2
         BAS   R8,PRT
         LA    R7,L3
         BAS   R8,PRT
NOSK     LH    R0,AUTANO
         BAS   R14,CNVNR
         MVC   L4A(L'L4A),ZONE+L'ZONE-L'L4A
         CLI   AUTPI,0
         BNE   *+L'*+8
         MVI   L4B,C'4'
         B     *+L'*+6
         MVC   L4B,AUTPI
         MVC   L4C,AUTACNO
         XR    R0,R0
         ICM   R0,B'0111',AUTVSD
         BAS   R8,EDDTE
         MVC   L4D(L'L4D),ZONE
         XR    R0,R0
         ICM   R0,B'0111',AUTVED
         BAS   R8,EDDTE
         MVC   L4E(L'L4E),ZONE
         LH    R0,AUTPMNO
         BAS   R14,CNVNR
         MVC   L4F(L'L4F),ZONE+L'ZONE-L'L4F
         LA    R6,LPG
         LA    R7,L4G
         BAS   R8,FILL
         LA    R7,L4
         BAS   R8,PRT
         LH    R0,NLI
         AH    R0,=H'2'
         STH   R0,NLI
         CLR   R5,R6
         BE    READ
PLOOP    LA    R7,L5A
         BAS   R8,FILL
         LA    R7,L5
         BAS   R8,PRT
         LH    R0,NLI
         AH    R0,=H'1'
         STH   R0,NLI
         CLR   R5,R6
         BNE   PLOOP
         B     READ
         DROP  R9
         EJECT
*---------------------------------------------------------------------*
*        PROGRAMMERS RECORDS PROCESSING.                              *
*---------------------------------------------------------------------*
         SPACE 1
NXSCT    CLC   NAUT,OVER
         BH    REND
         TM    SWITCH,SWP
         BO    NTFRST
         OI    SWITCH,SWP
         MVC   NLI,MAXN
         MVI   L7A,C'='
         MVC   L7A+1(L'L7A-1),L7A
         MVI   L7B,C'-'
         MVC   L7B+1(L'L7B-1),L7B
         MVI   L7C,C'-'
         MVC   L7C+1(L'L7C-1),L7C
NTFRST   CLC   NLI,GMX
         BNH   NSKP
         XC    NLI,NLI
         MVC   L1A,DYNDSN
         MVC   L1B,DATE
         MVC   L1C,HOUR
         LH    R0,NPA
         AH    R0,=H'1'
         STH   R0,NPA
         BAS   R14,CNVRT
         MVC   L1D(L'L1D),ZONE+L'ZONE-L'L1D
         LA    R7,L1
         BAS   R8,PRT
         LA    R7,L6
         BAS   R8,PRT
         LA    R7,L7
         BAS   R8,PRT
         MVI   L8,C'0'
NSKP     LH    R0,NLI
         AH    R0,=H'1'
         STH   R0,NLI
         USING PRGDSECT,R9
         LH    R0,PRGPNO
         BAS   R14,CNVNR
         MVC   L8A(L'L8A),ZONE+L'ZONE-L'L8A
         MVC   L8B(L'L8B),PRGBXNO
         MVC   L8C(L'L8C),PRGPNME
         MVC   L8D(L'L8D),PRGMNO
         MVC   L8E(L'L8E),PRGBDNO
         MVC   L8F(L'L8F),PRGPHNO
         MVC   L8G(L'L8G),PRGSVNO
         LA    R7,L8
         BAS   R8,PRT
         MVI   L8,C' '
         LM    R15,R1,SPWTB
         BXLE  R15,R0,*+L'*+4
         B     OVRFL
         USING PRSDSECT,R15
         MVC   PRSPNUM(L'PRSPNUM),ZONE+L'ZONE-L'PRSPNUM
         MVC   PRSPBOX,PRGBXNO
         MVC   PRSPNME,PRGPNME
         MVC   PRSPMTR,PRGMNO
         MVC   PRSPBLD,PRGBDNO
         MVC   PRSPPHN,PRGPHNO
         MVC   PRSPSRV,PRGSVNO
         DROP  R15
         ST    R15,SPWTB
         L     R15,NPWTB
         AH    R15,=H'1'
         ST    R15,NPWTB
         B     READ
         DROP  R9
         EJECT
*---------------------------------------------------------------------*
*        LIST BY PROGRAMMERS NAMES IN ASCENDING ALPHA ORDER.          *
*---------------------------------------------------------------------*
         SPACE 1
REND     L     R8,NPWTB
         CH    R8,=H'1'
         BL    EOLJ
         MVC   SPWTB+8(4),SPWTB
         MVC   SPWTB,APWTB
         BE    NSORT
         L     R7,SPWTB
        $SORT  (R7),(R8),PRSDLG,S1FD,S1FL
NSORT    MVC   NLI,MAXN
         MVI   L7A,C'-'
         MVC   L7A+1(L'L7A-1),L7A
         MVI   L7B,C'-'
         MVC   L7B+1(L'L7B-1),L7B
         MVI   L7C,C'='
         MVC   L7C+1(L'L7C-1),L7C
NXTPN    CLC   NLI,GMX
         BNH   NOSKP
         XC    NLI,NLI
         MVC   L1A,DYNDSN
         MVC   L1B,DATE
         MVC   L1C,HOUR
         LH    R0,NPA
         AH    R0,=H'1'
         STH   R0,NPA
         BAS   R14,CNVRT
         MVC   L1D(L'L1D),ZONE+L'ZONE-L'L1D
         LA    R7,L1
         BAS   R8,PRT
         LA    R7,L6
         BAS   R8,PRT
         LA    R7,L7
         BAS   R8,PRT
         MVI   L8,C'0'
NOSKP    LH    R0,NLI
         AH    R0,=H'1'
         STH   R0,NLI
         L     R15,SPWTB
         USING PRSDSECT,R15
         MVC   L8A(L'L8A),PRSPNUM
         MVC   L8B(L'L8B),PRSPBOX
         MVC   L8C(L'L8C),PRSPNME
         MVC   L8D(L'L8D),PRSPMTR
         MVC   L8E(L'L8E),PRSPBLD
         MVC   L8F(L'L8F),PRSPPHN
         MVC   L8G(L'L8G),PRSPSRV
         DROP  R15
         LA    R7,L8
         BAS   R8,PRT
         MVI   L8,C' '
         LM    R15,R1,SPWTB
         BXLE  R15,R0,*+L'*+4
         B     EOLJ
         ST    R15,SPWTB
         B     NXTPN
         EJECT
*---------------------------------------------------------------------*
*        LIST BY BOX NUMBERS IN ASCENDING ORDER.                      *
*---------------------------------------------------------------------*
         SPACE 1
EOLJ     L     R8,NPWTB
         CH    R8,=H'1'
         BL    EOLB
         MVC   SPWTB+8(4),SPWTB
         MVC   SPWTB,APWTB
         BE    SSORT
         L     R7,SPWTB
        $SORT  (R7),(R8),PRSDLG,S2FD,S2FL
SSORT    MVC   NLI,MAXN
         MVI   L7A,C'-'
         MVC   L7A+1(L'L7A-1),L7A
         MVI   L7B,C'='
         MVC   L7B+1(L'L7B-1),L7B
         MVI   L7C,C'-'
         MVC   L7C+1(L'L7C-1),L7C
NXTBN    CLC   NLI,GMX
         BNH   NOTSKP
         XC    NLI,NLI
         MVC   L1A,DYNDSN
         MVC   L1B,DATE
         MVC   L1C,HOUR
         LH    R0,NPA
         AH    R0,=H'1'
         STH   R0,NPA
         BAS   R14,CNVRT
         MVC   L1D(L'L1D),ZONE+L'ZONE-L'L1D
         LA    R7,L1
         BAS   R8,PRT
         LA    R7,L6
         BAS   R8,PRT
         LA    R7,L7
         BAS   R8,PRT
         MVI   L8,C'0'
NOTSKP   LH    R0,NLI
         AH    R0,=H'1'
         STH   R0,NLI
         L     R15,SPWTB
         USING PRSDSECT,R15
         MVC   L8A(L'L8A),PRSPNUM
         MVC   L8B(L'L8B),PRSPBOX
         MVC   L8C(L'L8C),PRSPNME
         MVC   L8D(L'L8D),PRSPMTR
         MVC   L8E(L'L8E),PRSPBLD
         MVC   L8F(L'L8F),PRSPPHN
         MVC   L8G(L'L8G),PRSPSRV
         DROP  R15
         LA    R7,L8
         BAS   R8,PRT
         MVI   L8,C' '
         LM    R15,R1,SPWTB
         BXLE  R15,R0,*+L'*+4
         B     EOLB
         ST    R15,SPWTB
         B     NXTBN
         EJECT
*---------------------------------------------------------------------*
*        END OF LIST - END OF JOB.                                    *
*---------------------------------------------------------------------*
         SPACE 1
EOLB     XC    NLI,NLI
         MVC   L1A,DYNDSN
         MVC   L1B,DATE
         MVC   L1C,HOUR
         LH    R0,NPA
         AH    R0,=H'1'
         STH   R0,NPA
         BAS   R14,CNVRT
         MVC   L1D(L'L1D),ZONE+L'ZONE-L'L1D
         LA    R7,L1
         BAS   R8,PRT
         LA    R7,L9
         BAS   R8,PRT
         LA    R7,L10
         BAS   R8,PRT
         LA    R2,STAR                 SET STATUS AREA ADDRESSABILITY
         USING STADSECT,R2
         MVC   L11A,STADSNM
         LA    R7,L11
         BAS   R8,PRT
         MVC   L12A,STAVLSR
         MVC   L12B,STADSOR
         LH    R0,STARECL
         BAS   R14,CNVRT
         MVC   L12C(L'L12C),ZONE+L'ZONE-L'L12C
         LA    R7,L12
         BAS   R8,PRT
         L     R0,STANAUT
         BAS   R14,CNVRT
         MVC   L13A(L'L13A),ZONE+L'ZONE-L'L13A
         L     R0,STAUAUT
         BAS   R14,CNVRT
         MVC   L13B(L'L13B),ZONE+L'ZONE-L'L13B
         LA    R7,L13
         BAS   R8,PRT
         L     R0,STANPRG
         BAS   R14,CNVRT
         MVC   L14A(L'L14A),ZONE+L'ZONE-L'L14A
         L     R0,STAUPRG
         BAS   R14,CNVRT
         MVC   L14B(L'L14B),ZONE+L'ZONE-L'L14B
         LA    R7,L14
         BAS   R8,PRT
         L     R0,STANARG
         BAS   R14,CNVRT
         MVC   L15A(L'L15A),ZONE+L'ZONE-L'L15A
         L     R0,STAUARG
         BAS   R14,CNVRT
         MVC   L15B(L'L15B),ZONE+L'ZONE-L'L15B
         LA    R7,L15
         BAS   R8,PRT
         L     R0,STANATB
         BAS   R14,CNVRT
         MVC   L16A(L'L16A),ZONE+L'ZONE-L'L16A
         LA    R7,L16
         BAS   R8,PRT
         L     R0,STANMTB
         BAS   R14,CNVRT
         MVC   L17A(L'L17A),ZONE+L'ZONE-L'L17A
         LA    R7,L17
         BAS   R8,PRT
         L     R0,STATOTL
         BAS   R14,CNVRT
         MVC   L18A(L'L18A),ZONE+L'ZONE-L'L18A
         LA    R7,L18
         BAS   R8,PRT
         DROP  R2
        CALL  ZCLOSE,(RETCD),VL
         LTR   R0,R15
         BNZ   ERRCL
         LA    R7,L19
         BAS   R8,PRT
         XC    DYNDSN,DYNDSN
        $DYNALL DYNLIST            DYNAMIC DE-ALLOC OF SYSACC
         XR    R9,R9
EXRET   $PCLOSE
        $XRET  CC=(R9)
         EJECT
*---------------------------------------------------------------------*
*        ERROR MESSAGE - EXIT.                                        *
*---------------------------------------------------------------------*
         SPACE 1
OVRFL    LA    R7,OVFMSG
         BAS   R8,PRT
         B     ERRPR
ERRZY    XR    R1,R1
        CALL  ZYDIAG
         LR    R7,R15
         BAS   R8,PRT
ERRPR   CALL  ZCLOSE,(RETCD),VL
         LTR   R0,R15
         BZ    EXFACC
         TM    SWITCH,SWE
         BO    EXFACC
ERRCL    XR    R1,R1
        CALL  ZYDIAG
         LR    R7,R15
         BAS   R8,PRT
         B     EXFACC
ERRDT    CVD   R15,WORK
         UNPK  WORK(3),WORK+6(2)
         OI    WORK+2,C'0'
         MVC   GFERRRC(2),WORK+1
        $PRINT GFERR
EXFACC   LM    R0,R1,LPWTB
         LTR   R1,R1
         BZ    NOPWTB
        FREEMAIN R,A=(1),LV=(0)
NOPWTB   XC    DYNDSN,DYNDSN
        $DYNALL DYNLIST            DYNAMIC DE-ALLOC OF SYSACC
         B     EXERR
ERALL    STM   R15,R0,WORK
         UNPK  ALLERR15(L'ALLERR15+1),WORK(5)
         TR    ALLERR15(L'ALLERR15),TRTAB-X'F0'
         MVI   ALLERR15+L'ALLERR15,C' '
         UNPK  ALLERR0(L'ALLERR0+1),WORK+4(5)
         TR    ALLERR0(L'ALLERR0),TRTAB-X'F0'
         MVI   ALLERR0+L'ALLERR0,C' '
         PRINT NOGEN
        $PRINT ALLERR
         B     EXERR
UJVTERR $PRINT UJVTMSG
         B     EXERR
NOTAUT  $PRINT AUTMSG
EXERR    LA    R9,16
         B     EXRET
         PRINT GEN
         EJECT
*---------------------------------------------------------------------*
*        UTILITY ROUTINES.                                            *
*---------------------------------------------------------------------*
         SPACE 1
CNVRT    CVD   R0,WORK             CONVERT SUBROUTINE.
         MVC   ZONE,MASK
         ED    ZONE,WORK
         BR    R14
         SPACE 1
CNVNR    LTR   R0,R0               CONVERT NUMBER SUBROUTINE.
         BZ    CNVNRM
         BP    *+L'*+10
         LPR   R0,R0
         IC    R15,=C'-'
         B     *+L'*+4
         IC    R15,=C'+'
         CVD   R0,WORK
         MVC   ZONE,MASK
         LA    R1,ZONE+L'ZONE-1
         EDMK  ZONE,WORK
         BCTR  R1,0
         STC   R15,0(R1)
         BR    R14
CNVNRM   MVC   ZONE+L'ZONE-5(5),=CL5' ****'
         BR    R14
         SPACE 1
EDDTE    CL    R0,=F'99999'        EDIT DATE SUBROUTINE. CHECK DATE?
         BH    *+L'*+4             NEW FORMAT.
         AL    R0,=F'1900000'      OLD FORMAT, ADJUST IT.
         CVD   R0,WORK
         UNPK  ZONE(7),WORK+4(4)
         OI    ZONE+6,C'0'
        $GFDATE ,,WORK,ZONE
         LTR   R0,R15
         BNZ   EDDTEM
         MVC   ZONE(2),WORK+2
         MVI   ZONE+2,C'/'
         MVC   ZONE+3(2),WORK
         MVI   ZONE+5,C'/'
         MVC   ZONE+6(4),WORK+4
         BR    R8
EDDTEM   BAL   R14,CNVRT
         MVC   ZONE(8),=CL8'RC ='
         MVC   ZONE+5(2),ZONE+L'ZONE-2
         BR    R8
         EJECT
FILL     MVI   0(R7),C' '          FILL PRG. NO. SUBROUTINE.
         MVC   1(L4GL-1,R7),0(R7)
         CLR   R5,R6
         BER   R8
         LA    R4,10
FILLNX   LH    R0,0(R6)
         BAS   R14,CNVNR
         MVC   0(5,R7),ZONE+L'ZONE-5
         LA    R7,6(R7)
         LA    R6,L'LPG(R6)
         CLR   R5,R6
         BER   R8
         BCT   R4,FILLNX
         BR    R8
         SPACE 1
PRT     $PRINT (R7)                PRINT SUBROUTINE.
         LTR   R15,R15
         BZR   R8
         OI    SWITCH,SWE
         B     ERRPR               PRINT ERROR ... EXIT.
         EJECT
*---------------------------------------------------------------------*
*        CONSTANTS AND WORK AREAS.                                    *
*---------------------------------------------------------------------*
         SPACE 1
WORK     DC    D'0'
RETCD    DC    F'0'
XOPT     DC    C'ONLY'
NPWTB    DC    F'0'
LPWTB    DC    F'0'
APWTB    DC    A(*-*)
SPWTB    DC    A(*-*,PRSDLG,*-*)
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUTLVL)
ZONE     DC    CL16' '
MASK     DC    XL16'40202020202020202020202020202120'
LPG      DC    (AUTNPNF)H'0'
NAUT     DC    H'0'
MAXN     DC    0H'0',AL2(NAUTNO)
OVER     DC    0H'0',AL2(NAUTNO+NPRGNO+1)
NPA      DC    H'0'
NLI      DC    0H'0',AL2(NAUTNO)
LMX      DC    H'46'
GMX      DC    H'50'
DATE     DC    CL20' '
TIME     DC    CL6' '
HOUR     DC    CL8'  .  .  '
TRTAB    DC    CL16'0123456789ABCDEF'
         SPACE 1
AUTLVL   DC    XL1'0'
SWITCH   DC    XL1'0'
SWP      EQU   X'01'
SWE      EQU   X'02'
         SPACE 1
AREA     DC    0F'0',(AUTLGTH)C' '
STAR     DC    (STALGTH)X'0' STATUS AREA (SEE STADSECT)
         EJECT
*--------------------------------------------------------------------*
*        DYNAMIC ALLOCATION PARAMETER LIST.                          *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0F
DYNLIST  DC    CL8'SYSACC'
DYNDSN   DC    CL44' '
         DC    CL8' '        MEMBER NAME
         DC    CL8' '        PASSWORD
         DC    CL8'SHR'      STATUS
         DC    CL256' '      FILLER
         DC    CL88' '       FILLER
         SPACE 1
*--------------------------------------------------------------------*
*        ERROR MESSAGES.                                             *
*--------------------------------------------------------------------*
         SPACE 1
         DC    AL1(L'AUTMSG)
AUTMSG   DC    C'1     UNAUTHORIZED ACCESS TO THIS PROGRAM - EXIT -'
         DC    AL1(L'UJVTMSG)
UJVTMSG  DC    C'1     UJVTABLE (IEFUJV) NOT FOUND - EXIT -'
         DC    AL1(ALLERRL)
ALLERR   DC    C'1     ACCOUNT FILE ALLOC. ERROR - R15='
ALLERR15 DC    CL8' ',C' - R0='
ALLERR0  DC    CL8' ',C' - EXIT -'
ALLERRL  EQU   *-ALLERR
         DC    AL1(GFERRL)
GFERR    DC    C'1     GFDATE ROUTINE ERROR - RC = '
GFERRRC  DC    CL2' ',C' - EXIT -'
GFERRL   EQU   *-GFERR
         DC    AL1(L'OVFMSG)
OVFMSG   DC    C'1     PROGRAM INTERNAL WORK TABLE OVERFLOW - EXIT -'
         EJECT
*--------------------------------------------------------------------*
*        LIST LINES DEFINITIONS.                                     *
*--------------------------------------------------------------------*
         SPACE 1
         DC    AL1(L1L)
L1       DC    CL22'1     DATA-SET NAME : '
L1A      DC    CL44' ',C' - '
L1B      DC    CL20' ',C' - '
L1C      DC    CL8' ',C' ----- PAGE '
L1D      DC    CL3' ',C'.'
L1L      EQU   *-L1
         DC    AL1(L2L)
L2       DC    C'0    AUT.   P  FICHE PR.     DEBUT       FIN      '
         DC    C'RESP.  PROGRAMMER(S) ...'
L2L      EQU   *-L2
         DC    AL1(L3L)
L3       DC    C'     =====  -  ----------  ---------- ----------  '
         DC    C'-----  '
         DC    ((10*6)-1)C'-'
L3L      EQU   *-L3
         DC    AL1(L4L)
L4       DC    CL5'0 '
L4A      DC    CL5' ',CL2' '
L4B      DC    CL1' ',CL2' '
L4C      DC    CL10' ',CL2' '
L4D      DC    CL10' ',CL1' '
L4E      DC    CL10' ',CL2' '
L4F      DC    CL5' ',CL2' '
L4XL     EQU   *-L4
L4G      DC    ((10*6)-1)C' '
L4GL     EQU   *-L4G
L4L      EQU   *-L4
         DC    AL1(L5L)
L5       DC    (L4XL)C' '
L5A      DC    (L4GL)C' '
L5L      EQU   *-L5
         DC    AL1(L6L)
L6       DC    C'0    PROG.  BOX  NAME',CL38' '
         DC    C'MATR.   BUILD.  PHONE      SERVICE'
L6L      EQU   *-L6
         DC    AL1(L7L)
L7       DC    CL5' '
L7A      DC    CL5' ',CL2'  '
L7B      DC    CL3' ',CL2' '
L7C      DC    CL40' ',CL2' '
         DC    C'------  ------  ---------  --------'
L7L      EQU   *-L7
         DC    AL1(L8L)
L8       DC    CL5'0 '
L8A      DC    CL5' ',CL2' '
L8B      DC    CL3' ',CL2' '
L8C      DC    CL40' ',CL2' '
L8D      DC    CL6' ',CL2' '
L8E      DC    CL6' ',CL2' '
L8F      DC    CL9' ',CL2' '
L8G      DC    CL8' '
L8L      EQU   *-L8
         DC    AL1(L9L)
L9       DC    C'0    ACCOUNTING DATA-SET STATUS'
L9L      EQU   *-L9
         DC    AL1(L10L)
L10      DC    C'     =========================='
L10L     EQU   *-L10
         DC    AL1(L11L)
L11      DC    C'0    DATA-SET NAME : '
L11A     DC    CL44' '
L11L     EQU   *-L11
         DC    AL1(L12L)
L12      DC    C'0    VOLUME SERIAL : '
L12A     DC    CL6' ',C'    ORGANIZATION : '
L12B     DC    CL2' ',C'    RECORD LENGTH : '
L12C     DC    CL5' '
L12L     EQU   *-L12
         DC    AL1(L13L)
L13      DC    C'0    CAPACITIES : '
L13A     DC    CL7' ',C' AUT. RECORDS - '
L13B     DC    CL7' ',C' IN USE'
L13L     EQU   *-L13
         DC    AL1(L14L)
L14      DC    CL18' '
L14A     DC    CL7' ',C' PRG. RECORDS - '
L14B     DC    CL7' ',C' IN USE'
L14L     EQU   *-L14
         DC    AL1(L15L)
L15      DC    CL18' '
L15A     DC    CL7' ',C' ARG. RECORDS - '
L15B     DC    CL7' ',C' IN USE'
L15L     EQU   *-L15
         DC    AL1(L16L)
L16      DC    CL18' '
L16A     DC    CL7' ',C' ARG. TABLE RECORDS'
L16L     EQU   *-L16
         DC    AL1(L17L)
L17      DC    CL18' '
L17A     DC    CL7' ',C' MAP TABLE RECORDS'
L17L     EQU   *-L17
         DC    AL1(L18L)
L18      DC    C'0     T O T A L : '
L18A     DC    CL7' ',C' RECORDS'
L18L     EQU   *-L18
         DC    AL1(L19L)
L19      DC    C'0    --- END OF LIST ---'
L19L     EQU   *-L19
         EJECT
*---------------------------------------------------------------------*
*        LITERAL POOL.                                                *
*---------------------------------------------------------------------*
         SPACE 1
        LTORG
         SPACE 2
         END
