B006     TITLE 'CLEAN-UP - CONNECTS LIST (B006).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B006    @XS
*        RACF CONNECT'S LIST.
         SPACE 1
         LA    R2,LINE
         USING LALNE,R2
         L     R7,WK2A             RACF WORK AREA
         USING WKDS,R7
COSW     NOP   CO0
         OI    COSW+1,ON
         ST    R7,SCOGW
         LA    R1,SVSNAP+4
         ST    R1,SCOGGS
        $GDATE LCLAST+COLJL-L'COLJD,,,WORKD+3,MF=(L,CODTE)
         SPACE 1
*        GET CONNECT'S LIST FROM RACF.
         SPACE 1
CO0      MVC   CONMEL(L'CONMEL+L'CONAME),USNMEL
         XR    R1,R1
         IC    R1,CONMEL           GET LENGTH OF USERID NAME
         BCTR  R1,0                -1 FOR MOVE
         MVC   CORN(L'CORN),CORN-1
         EX    R1,COMO             MOVE USERID NAME
         MVC   CORG(L'CORG),LACN   MOVE CONNECT GROUP NAME
         LA    R14,CONAME+1(R1)    NOW PREPARE NAME TO LOCATE
         MVI   0(R14),0
         LA    R0,LACN
         LA    R15,LACN+L'LACN-1
         CLI   0(R15),C' '
         BNE   *+L'*+4
         BCT   R15,*-8
         SR    R15,R0
         EX    R15,COMV            EXECUTE MOVE
         LA    R1,3(R1,R15)
         STC   R1,CONMEL
         XC    WORKRBA(WORKBSLL),WORKRBA
        ICHEINTY LOCATE,TYPE='CON',ENTRY=CONMEL,WKAREA=(R7),           C
               ACTIONS=(ACNA,ACNLJT,ACNLJD,ACNG,ACNI,ACNF1,ACNF2,      C
               ACNF3,ACNF4,ACNF5,ACNT)
         LA    R14,WORKBS
         A     R14,WORKTL
         STCM  R14,B'0111',SCOGN+1
         XR    R14,R14
         IC    R14,CONMEL
         LA    R14,1(R14)
         ST    R14,SCOGG
        @SNAPS SF,SCOGL,RCO
         LTR   R15,R15             ANY ERROR?
         BZ    CO2                 NO
         CVD   R15,WORKD           RETURN CODE
         UNPK  CORRC,WORKD+6(2)
         OI    CORRC+1,C'0'
         MVI   CORAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   CO1                 NO
         STC   R0,CORAC
         OI    CORAC,C'0'
CO1     @LIST  COR
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    CO99                NO
        @COPY  COR
         B     CO99
COMO     MVC   CORN(*-*),CONAME    << EXECUTED >>
COMV     MVC   1(*-*,R14),LACN     << EXECUTED >>
CO2      OC    WORKTL,WORKTL       DATA RETURNED?
         BZ    CO3                 NO
         CLC   WORKVRA(2),=XL2'0008'    AUTHOR RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+10(2),=XL2'0004' LAST TIME RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+16(2),=XL2'0003' LAST DATE RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+21(2),=XL2'0001' UNIV. AUTH. RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+24(2),=XL2'0002' ACCESS COUNT RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+28(2),=XL2'0001' FLAG1 RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+31(2),=XL2'0001' FLAG2 RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+34(2),=XL2'0001' FLAG3 RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+37(2),=XL2'0001' FLAG4 RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+40(2),=XL2'0001' FLAG5 RECEIVED?
         BNE   CO3                 NO
         CLC   WORKVRA+43(2),=XL2'0001' TERM. UNIV. AUTH. RECEIVED?
         BE    CO10                YES
CO3      MVC   COERN(L'COERN+L'COERG+1),CORN
        @LIST  COERR
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    CO99                NO
        @COPY  COERR
         B     CO99
CO10     MVI   LCSPO,C'('
         MVC   LCOW+1(6),=CL6'......'
         CLI   WORKVRA+2,X'FF'
         BE    *+L'*+6
         MVC   LCOW(L'LCOW),WORKVRA+2   MOVE OWNER OF GROUP
         MVI   LCSPT,C')'
         MVI   LCSPU,C'-'
         MVC   LCAUT(L'LCAUT),=CL22'DEFAULT UNIV. ACCESS :'
         CLI   WORKVRA+23,0        ANY ACCESS?
         BNE   *+L'*+10            NO
         MVC   LCAU+1(6),=CL6'......'
         B     CO11
         CLI   WORKVRA+23,ACEEALTR ALTER ACCESS?
         BNE   *+L'*+10            NO
         MVC   LCAU(5),=CL5'ALTER'
         B     CO11
         CLI   WORKVRA+23,ACEECNTL CONTROL ACCESS?
         BNE   *+L'*+10            NO
         MVC   LCAU(7),=CL7'CONTROL'
         B     CO11
         CLI   WORKVRA+23,ACEEUPDT UPDATE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LCAU(6),=CL6'UPDATE'
         B     CO11
         CLI   WORKVRA+23,ACEEREAD READ ACCESS?
         BNE   *+L'*+10            NO
         MVC   LCAU(4),=CL4'READ'
         B     CO11
         CLI   WORKVRA+23,ACEENONE NONE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LCAU(4),=CL4'NONE'
         B     CO11
         LA    R1,LCAU+1           ANYTHING MIXED OR UNKNOWN
         MVC   0(5,R1),=CL5'X''  '''
         LA    R15,WORKVRA+23
         BAS   R14,DMPRTS+L'DMPRTS
CO11     MVI   LCSPA,C'('
         XR    R0,R0
         ICM   R0,B'0011',WORKVRA+26    ACCESS COUNT
         LTR   R0,R0
         BZ    CO12
         CVD   R0,WORKD
         MVC   LCSI,=XL6'402020202120'
         ED    LCSI,WORKD+5
         B     CO12+L'CO12
CO12     MVC   LCSI,=CL6'-NULL-'
         MVI   LCSPB,C')'
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    CO13                NO
        @COPY  LINE
CO13     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R1,LCFLGS
         LA    R15,WORKVRA+30
         CLI   0(R15),X'80'        FLAG1 - ADSP?
         BNE   CO20
         MVC   0(4,R1),=CL4'ADSP'
         LA    R1,5(R1)
         B     CO21
CO20     CLI   0(R15),0
         BE    CO21
         MVI   0(R1),C'1'
         BAS   R14,DMPRTN
CO21     LA    R15,WORKVRA+33
         CLI   0(R15),X'80'        FLAG2 - SPECIAL?
         BNE   CO22
         MVC   0(7,R1),=CL7'SPECIAL'
         LA    R1,8(R1)
         B     CO23
CO22     CLI   0(R15),0
         BE    CO23
         MVI   0(R1),C'2'
         BAS   R14,DMPRTN
CO23     LA    R15,WORKVRA+36
         CLI   0(R15),X'80'        FLAG3 - OPERATIONS?
         BNE   CO24
         MVC   0(10,R1),=CL10'OPERATIONS'
         LA    R1,11(R1)
         B     CO25
CO24     CLI   0(R15),0
         BE    CO25
         MVI   0(R1),C'3'
         BAS   R14,DMPRTN
CO25     LA    R15,WORKVRA+39
         CLI   0(R15),X'80'        FLAG4 - REVOKE?
         BNE   CO26
         MVC   0(6,R1),=CL6'REVOKE'
         LA    R1,7(R1)
         B     CO27
CO26     CLI   0(R15),0
         BE    CO27
         MVI   0(R1),C'4'
         BAS   R14,DMPRTN
CO27     LA    R15,WORKVRA+42
         CLI   0(R15),X'80'        FLAG5 - GRPACC?
         BNE   CO28
         MVC   0(6,R1),=CL6'GRPACC'
         LA    R1,7(R1)
         B     CO30
CO28     CLI   0(R15),0
         BE    CO30
         MVI   0(R1),C'5'
         BAS   R14,DMPRTN
CO30     CLI   LCFLGS,C' '
         BNE   *+L'*+8
         LA    R1,LCATTR
         B     *+L'*+6
         MVC   LCATTR(L'LCATTR),=CL7'ATTR. :'
         CLI   WORKVRA+45,0        TERMUACC?
         BE    CO31                YES
         MVI   0(R1),C'-'
         LA    R1,2(R1)
         CLI   WORKVRA+45,X'80'    NOTRMUACC?
         BNE   *+L'*+10            NO
         MVC   0(9,R1),=CL9'NOTRMUACC'
         B     CO31
         MVI   0(R1),C'T'          ANYTHING MIXED OR UNKNOWN
         LA    R15,WORKVRA+45
         BAS   R14,DMPRTN
CO31     CLI   LCATTR,C' '
         BE    CO32
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    CO32                NO
        @COPY  LINE
CO32     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         CLC   WORKVRA+18(3),=XL3'FFFFFF'    ANY CONNECT?
         BE    CO99                NO, GET NEXT CONNECT
         MVC   LCLAST(COLJL),COLJ
         UNPK  WORKD,WORKVRA+18(3)
         OI    WORKD+L'WORKD-1,C'0'
        $GDATE MF=(E,CODTE)
         LTR   R15,R15
         BNZ   ERR1
         UNPK  WORKD,WORKVRA+12(4)
         MVC   LCLAST+L'COLJ(2),WORKD+1
         MVC   LCLAST+L'COLJ+3(2),WORKD+3
         MVC   LCLAST+L'COLJ+6(2),WORKD+5
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    CO99                NO
        @COPY  LINE
         B     CO99                GET NEXT CONNECT
         DROP  R2,R7
CO99    @XE
         EJECT
CODTE   $GDATE ,,,,MF=L
ACNA    ICHEACTN FIELD=AUTHOR
ACNLJT  ICHEACTN FIELD=LJTIME
ACNLJD  ICHEACTN FIELD=LJDATE
ACNG    ICHEACTN FIELD=UACC
ACNI    ICHEACTN FIELD=INITCNT
ACNF1   ICHEACTN FIELD=FLAG1
ACNF2   ICHEACTN FIELD=FLAG2
ACNF3   ICHEACTN FIELD=FLAG3
ACNF4   ICHEACTN FIELD=FLAG4
ACNF5   ICHEACTN FIELD=FLAG5
ACNT    ICHEACTN FIELD=NOTRMUAC
         SPACE 1
SCOGL    DC    A(CONMEL)
SCOGG    DC    A(*-*)
SCOGGS   DC    A(*-*)
         DC    A(8)
SCOGW    DC    A(*-*)
SCOGN    DC    XL1'80',AL3(*-*)
         SPACE 1
CONMEL   DC    AL1(L'CONAME+L'CONMGR+1)
CONAME   DC    CL8' ',XL1'0'
CONMGR   DC    CL8' '
COLJ     DC    C'LAST CONNECT AT '
         DC    CL8'  .  .  ',C' OF '
COLJD    DC    CL20' '
COLJL    EQU   *-COLJ
         SPACE 1
         DC    AL1(CORL)
COR      DC    CL3' ',CL3'-- '
CORN     DC    CL8' ',C' '
CORG     DC    CL8' ',C' : RACF READ TYPE=CON (RC='
CORRC    DC    CL2' ',C'/AC='
CORAC    DC    CL1' ',C').'
CORL     EQU   *-COR
         DC    AL1(COERRL)
COERR    DC    CL3' ',CL3'-- '
COERN    DC    CL8' ',C' '
COERG    DC    CL8' ',C' : RACF READ TYPE=CON '
         DC    C'(INVALID WORK AREA RECEIVED BACK).'
COERRL   EQU   *-COERR
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
        #WKDS
         SPACE 2
        #LALNE
         SPACE 2
         END
