UJVD     TITLE 'TSO IEFUJV DISPLAY COMMAND.'
         MACRO
&LAB    PUTIT  &MESS,&EXIT=RETURN
         LCLC  &L
&L       SETC  'L'''
&LAB    TPUT   M&SYSNDX,&L.M&SYSNDX
         B     &EXIT
M&SYSNDX DC    C&MESS
         MEND
         SPACE 2
UJVD     START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *    IEFUJV DISPLAY - TSO COMMAND.    * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
* PURPOSE :    A TSO COMMAND TO OBTAIN THE IEFUJV COMPUTATIONS
* ---------    INFORMATION (SYSTEM-GROUP RESERVED).
*
* COMMAND :    SYNTAX : UJVD  REGION(...) TIME(...) I/O(...) SETUP
* ---------        OR : UJVD  R(...) T(...) I(...) S
*
*               WHERE : R ... : REGION VALUE EXPRESSED IN K.
*                       T ... : CPUTIME EXPRESSED IN MINUTES AND/OR
*                               SECONDS (FORMAT MUST BE MMM,SS OR MMM
*                               OR 0,SS).
*                       I ... : NUMBER OF I/O'S EXPRESSED IN
*                               THOUSANDS.
*                       S     : SETUP COMPUTATIONS.
*              DEFAULT : ALL OPERANDS HAVE DEFAULT VALUES AS DEFINED
*                        IN JES2 OR IEFUJV.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
LV0      EQU   0         REJECT LEVEL.
         SPACE 1
        $MDL@IX
         EJECT
WRKDSECT DSECT                     WORK AREAS DESCRIPTION.
         SPACE 1
SVA      DS    18F'0'              SAVE AREA.
         SPACE 1
CONV     DC    D'0'
         SPACE 1
*        EXTRACT PARAMETER LIST.
         SPACE 1
EXTR    EXTRACT *-*,'S',MF=L
         SPACE 1
LEXTR    EQU   *-EXTR
         SPACE 1
ATSO     DC    A(0)
APSCB    DC    A(0)
UJVTADDR DC    A(0)
         SPACE 1
*        PARSE PARAMETER LIST (PPL).
         SPACE 1
PPLUPT   DC    A(0)
PPLECT   DC    A(0)
PPLECB   DC    A(0)
PPLPCL   DC    A(0)
PPLANS   DC    A(0)
PPLCBUF  DC    A(0)
PPLUWA   DC    A(0)
         SPACE 1
PPLECBS  DC    F'0'
PPLANSS  DC    A(0)
         SPACE 1
LPPL     EQU   *-PPLUPT
         SPACE 1
PLIST    DC    A(0,0,0,0,0,0)
         SPACE 1
REGION   DC    F'0'
NUMIO    DC    F'0'
CPUTM    DC    F'0'
         SPACE 1
TEXT     DC    CL132' '
MRES     DC    CL78' '
         SPACE 1
AUTH     DC    XL1'0'
FLRQ     DC    XL1'0'
SWSU     EQU   X'01'               SETUP COMPUTATIONS.
         SPACE 1
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(*-*)
REQAUTL  EQU   *-REQAUT
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 1
UJVD     CSECT                     RESTORE ORIGINAL CSECT.
         SPACE 2
*        'IEFUJV' COMMUNICATION.
*        -----------------------
         SPACE 1
LDES     DSECT                     LINE DESCRIPTION (D-ARGUMENT).
         SPACE 1
LNE      DS    0CL132,CL5
RGV      DS    CL8,CL2
IOV      DS    CL8,CL3
TMV      DS    CL4,C
TSV      DS    CL2,CL10
PRIO     DS    CL1,CL2
IOX      DS    CL8,CL2
WGV      DS    CL8,CL3
CLV      DS    CL1,CL4
PGV      DS    CL4
         SPACE 1
FILL     EQU   0+L'LNE-(*-LNE)
         DS    (FILL)C
         SPACE 1
UJVD     CSECT                     RESTORE ORIGINAL CSECT.
         SPACE 2
        PRINT  NOGEN
        CVT    DSECT=YES,LIST=YES
        IKJPSCB
        IKJEBECA
UJVD     CSECT                     RESTORE ORIGINAL CSECT.
        PRINT  GEN
         SPACE 1
UJVD     CSECT                     RESTORE ORIGINAL CSECT.
         SPACE 1
        IKJCPPL
         SPACE 1
UJVD     CSECT                     RESTORE ORIGINAL CSECT.
         EJECT
        $UJVT
         EJECT
        $DEFREG
        $XENT  BASE=(R12),LV=WRKL,TYPE=RENT
         EJECT
         LR    R11,R13             SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R11
         LR    R10,R1              SAVE FOR LATER CONTROL.
         MVC   EXTR(LEXTR),EXTRP
         XC    ATSO(L'ATSO+L'APSCB+L'UJVTADDR),ATSO
        EXTRACT ATSO,'S',FIELDS=(TSO,PSB),MF=(E,EXTR)
         L     R2,ATSO
         TM    0(R2),X'80'
         BZ    NOTTSO              NOT TSO ADDRESS SPACE.
         L     R2,APSCB            PSCB ADDRESS.
         LTR   R2,R2
         BZ    NOTTSO              WE ARE'NT IN TSO.
         USING CPPL,R10            SET CPPL ADDRESSABILITY.
         USING PSCB,R2
         CLC   PSCBUPT,CPPLUPT     CPPL+4 = UPT ADDRESS?
         BNE   *+L'*+10            NO.
         DROP  R2
         CLC   APSCB(L'APSCB),CPPLPSCB  CPPL+8 = PSCB ADDRESS?
         BE    OK                  YES, TSO COMMAND.
         LR    R2,R10
         USING IKJEBECA,R2
         L     R10,CAPTTMP         CPPL ADDRESS.
         DROP  R2
         CLC   APSCB(L'APSCB),CPPLPSCB  CPPL+8 = PSCB ADDRESS?
         BNE   ERROR0              NO, NOT TSO SUB-COMMAND.
         EJECT
OK       XC    REQAUT(REQAUTL),REQAUT
         MVC   REQAUT+1(3),=AL3(MDL@IX)
         LA    R1,AUTH
         ST    R1,REQAUT+4
        $EACM  REQAUT
         CLI   AUTH,LV0            AUTHORIZED?
         BE    ERROR1              NO.
         SPACE 1
*        SET PARSE PARAMETER LIST.
         SPACE 1
         XC    PPLUPT(LPPL),PPLUPT
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R1,PPLECBS
         ST    R1,PPLECB
         MVC   PPLPCL,=A(PARMLST)
         LA    R1,PPLANSS
         ST    R1,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         DROP  R10
         SPACE 1
*        LINK TO PARSE.
         SPACE 1
        CALLTSSR EP=IKJPARS,MF=(E,PPLUPT)
         B     *+L'*(R15)          BRANCH ON RETURN CODE.
         B     PARSOK               0 - SUCCESSFULL.
         B     ERROR2               4 - UNABLE TO PROMPT.
         B     RETURN               8 - USER ATTENTION OCCURRED.
         B     ERROR3              12 - PROGRAM BUG.
         B     ERROR4              16 - SPACE UNAVAILABLE IN REGION.
         B     ERROR5              20 - INVALID PARM.
         B     ERROR6              24 - PARM. CONFLICT.
         B     ERROR7              28 - TERMINAL DISCONNECTED.
         SPACE 1
*        HANDLE REQUESTS.
         SPACE 1
PARSOK   L     R3,PPLANSS          CHECK ANSWER.
         LA    R3,0(R3)
         LTR   R3,R3
         BZ    ERROR8
         USING IKJPARMD,R3         SET PDE'S ADDRESSABILITY.
         L     R15,=V(SUJVTA)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   NOTFND
         ST    R1,UJVTADDR
         LR    R6,R1
         USING UVTDSECT,R6
         CLI   PDRG+1,0            REGION SPECIFIED?
         BE    NORG                NO.
         TM    SDRG+6,X'80'        VALUE PRESENT?
         BZ    NORG                NO.
         LH    R14,SDRG+4          GET LENGTH.
         LTR   R14,R14             LENGTH ZERO?
         BNP   NORG                YES.
         L     R1,SDRG             POINT TO VALUE.
         BCTR  R14,0
         EX    R14,GVAL
         CVB   R1,CONV             GET BINARY VALUE.
         B     NORG+L'NORG
NORG     L     R1,DEFREG           GET DEFAULT.
         ST    R1,REGION
         CLI   PDTM+1,0            TIME SPECIFIED?
         BE    NOTM                NO.
         XR    R5,R5
         LA    R2,SDTM             R2 ---> FIRST PDE IN LIST.
         TM    6(R2),X'80'         MINUTES GIVEN?
         BZ    NOMIN               NO.
         LH    R14,4(R2)           GET LENGTH.
         LTR   R14,R14             LENGTH ZERO?
         BNP   NOMIN               YES.
         L     R1,0(R2)            POINT TO VALUE.
         BCTR  R14,0
         EX    R14,GVAL
         CVB   R5,CONV             GET BINARY VALUE.
         MH    R5,=H'60'           CHANGE MINUTES IN SECONDS.
NOMIN    CLI   8(R2),X'FF'         ONLY MINUTES GIVEN?
         BE    NOSEC               YES.
         L     R2,8(R2)            R2 ---> NEXT PDE IN LIST.
         CLI   8(R2),X'FF'         ANY OTHER IN LIST?
         BNE   INVTM               YES, WHAT'S THAT.
         LH    R14,4(R2)           GET LENGTH.
         LTR   R14,R14             LENGTH ZERO?
         BNP   NOSEC               YES.
         L     R1,0(R2)            POINT TO VALUE.
         BCTR  R14,0
         EX    R14,GVAL
         CVB   R1,CONV             GET BINARY VALUE.
         CH    R1,=H'60'           VALID SECONDS SPECIFIED?
         BNL   INVTM               NO, WHAT'S THAT.
         AR    R5,R1               COMPUTE TOTAL TIME IN SECONDS.
NOSEC    LTR   R5,R5               TRUE TIME REQUEST?
         BNP   NOTM                NO.
         C     R5,=A(10*60*60)     OVER MAXIMUM?
         BNH   NOTM+L'NOTM         NO.
         L     R5,=A(10*60*60)     SET MAXIMUM.
         BNH   NOTM+L'NOTM
NOTM     L     R5,DEFTIME          GET DEFAULT.
         ST    R5,CPUTM
         CLI   PDIO+1,0            I/O SPECIFIED?
         BE    NOIO                NO.
         TM    SDIO+6,X'80'        VALUE PRESENT?
         BZ    NOIO                NO.
         LH    R14,SDIO+4          GET LENGTH.
         LTR   R14,R14             LENGTH ZERO?
         BNP   NOIO                YES.
         L     R1,SDIO             POINT TO VALUE.
         BCTR  R14,0
         EX    R14,GVAL
         CVB   R1,CONV             GET BINARY VALUE.
         C     R1,=A(2*60*60)      OVER MAXIMUM?
         BNH   NOIO+L'NOIO         NO.
         L     R1,=A(2*60*60)      SET MAXIMUM.
         B     NOIO+L'NOIO
NOIO     LH    R1,=H'100'          GET DEFAULT.
         ST    R1,NUMIO
         MVI   FLRQ,0
         CLI   PDOP+1,1            SET SETUP OPTION IF SPECIFIED.
         BNE   STLST
         OI    FLRQ,SWSU
         DROP  R3
         SPACE 1
*        SET UP CALL TO IEFUJV.
         SPACE 1
STLST    LA    R1,PLIST
         LA    R2,REGION
         LA    R3,NUMIO
         LA    R4,CPUTM
         LA    R5,TEXT
         LA    R7,FLRQ
         STM   R2,R7,0(R1)
        CALL   IEFUJV
         DROP  R6
         SPACE 1
*        SEND RESULTS TO USER AND RETURN.
         SPACE 1
         LA    R2,TEXT
         USING LDES,R2
         MVI   MRES,C'R'
         MVC   MRES+1(L'RGV),RGV
         MVC   MRES+L'RGV+1(3),=C', I'
         MVC   MRES+L'RGV+4(L'IOV),IOV
         MVC   MRES+L'RGV+L'IOV+4(3),=C', T'
         MVC   MRES+L'RGV+L'IOV+7(L'TMV+L'TSV+1),TMV
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+8(5),=C'/ PR '
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+13(1),PRIO
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+14(5),=C', CL '
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+19(1),CLV
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+20(5),=C', PG '
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+25(L'PGV-1),PGV+1
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+L'PGV+24(4),=C' (JX'
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+L'PGV+28(L'IOX),IOX
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+L'PGV+L'IOX+28(4),=C', JW'
         MVC   MRES+L'RGV+L'IOV+L'TMV+L'TSV+L'PGV+L'IOX+32(L'WGV),WGV
         MVI   MRES+L'RGV+L'IOV+L'TMV+L'TSV+L'PGV+L'IOX+L'WGV+32,C')'
         DROP  R2
         LA    R0,33+L'RGV+L'IOV+L'TMV+L'TSV+L'PGV+L'IOX+L'WGV
         LA    R1,MRES
        TPUT   (1),(0)
RETERR  IKJRLSA PPLANSS
         OC    UJVTADDR,UJVTADDR
         BZ    RETURN
         L     R15,=V(DUJVTA)
         BASR  R14,R15
         SPACE 1
RETURN  $XRET  CC=0,LV=WRKL,TYPE=RENT
         EJECT
        PRINT  NOGEN
ERROR0  PUTIT  'CALL NOT ALLOWED.'
ERROR1  PUTIT  'UNAUTHORIZED COMMAND.'
ERROR2  PUTIT  'INCOMPLETE PARAMETERS AND UNABLE TO PROMPT.'
ERROR3  PUTIT  'PARSE PARAMETER BLOCK INVALID.'
ERROR4  PUTIT  'NOT ENOUGH SPACE IN REGION.'
ERROR5  PUTIT  'INVALID PARAMETERS.'
ERROR6  PUTIT  'PARAMETERS CONFLICT.'
ERROR7  PUTIT  'TERMINAL HAS BEEN DISCONNECTED.'
ERROR8  PUTIT  'PARSE ANSWER ADDRESS ERROR.'
INVTM   PUTIT  'INVALID TIME(MINUTES,SECONDS) SPECIFIED.',EXIT=RETERR
NOTFND  PUTIT  'UJVTABLE (IEFUJV) NOT FOUND.',EXIT=RETERR
NOTTSO  WTO    'NOT ALLOWED OUTSIDE TSO.',ROUTCDE=11,DESC=7
         B     RETURN
        PRINT  GEN
         SPACE 1
GVAL     PACK  CONV,0(0,R1)        << EXECUTED >>
         SPACE 1
         DROP  R11
         EJECT
*        INTERNAL CONSTANTS AND WORK AREAS.
*        ----------------------------------
         SPACE 1
*        EXTRACT PARAMETER LIST.
         SPACE 1
EXTRP   EXTRACT *-*,'S',MF=L
         SPACE 2
        LTORG
         EJECT
        PRINT  NOGEN
         SPACE 1
PARMLST IKJPARM
PDRG    IKJKEYWD
        IKJNAME 'REGION',SUBFLD=SFRG,ALIAS=('R')
PDTM    IKJKEYWD
        IKJNAME 'TIME',SUBFLD=SFTM,ALIAS=('T')
PDIO    IKJKEYWD
        IKJNAME 'I/O',SUBFLD=SFIO,ALIAS=('I')
PDOP    IKJKEYWD
        IKJNAME 'SETUP',ALIAS=('S')
SFRG    IKJSUBF
SDRG    IKJIDENT 'SIZE IN K',FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=7
SFTM    IKJSUBF
SDTM    IKJIDENT 'MINUTES AND/OR SECONDS',LIST,                        X
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=3
SFIO    IKJSUBF
SDIO    IKJIDENT 'I/O IN 1/1000',FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=5
        IKJENDP
         SPACE 2
         END
