B010     TITLE 'CLEAN-UP - READ AN ICF CATALOG CONTENTS (B010).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B010    @XS
*        AT ENTRY :   R3 = CAT. VOL. ELEMENT POINTER
*                     R4 = DDNAME ELEMENT POINTER
         SPACE 1
         USING CVLT,R3
         USING DDNT,R4
IXSW0    NOP   IX1
         OI    IXSW0+1,ON
         L     R6,DACTLG           ALLOCATED CATALOG
         USING DYNLIST,R6
        $ICFSO DDNAME,,,,MF=(L,ICFSOL)
         DROP  R6
        $ICFSR CTYPE,,WORKD,MF=(L,ICFSRL)
        $DVTYUN WORKD,,MF=(L,ICFDV)
IX1      XC    IXMSPW,IXMSPW
        $ICFSO MF=(E,ICFSOL)       OPEN ICF CATALOG
         LTR   R15,R15             TEST RETURN CODE
         BZ    IX3
         CH    R15,=H'2'
         BE    IX2
         CH    R15,=H'9'
         BNE   ERR55
         LTR   R0,R0
         BNZ   IX2
         LTR   R1,R1
         BZ    ERR55
IX2      BAS   R14,IXRR
         B     ERR55
IX3      OI    CVLIND,CVLCSO       SET OPENED
         CLC   CVLVOL(L'CVLVOL+L'CVLNME),IXCVOL   CATALOG REQUESTED?
         BE    IX4                 YES
         MVC   IXRCM1,CVLVOL
         MVC   IXRCM2,CVLNME
        WTO    MF=(E,IXRCM)
         MVC   IXRAM1,IXCVOL
         MVC   IXRAM2,IXCNME
        WTO    MF=(E,IXRCM)
         XR    R15,R15
         B     ERR67
IX4      L     R5,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
         BAS   R6,*+L'*+L'IXUN+8
IXUN     XR    R15,R15
         IC    R15,CTYPE           SET R15 = UNEXPECTED INDICATOR
         B     ERR68
         LA    R7,LINE
         USING LXDSCT,R7
         OI    IXSW1+1,ON
         MVI   IXLSCTR,0
         MVC   IXCINFA,DDNVOL
         MVC   IXCINFB,DDNUTY
         MVC   IXCINFC,CVLNME
        @OUT   IXCINF,=PL2'+50'
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    IX10                NO
         MVC   IXCINITA,IXCINFA
         MVC   IXCINITB,IXCINFB
         MVC   IXCINITC,IXCINFC
         TM    RUNIND,RUCSH        CAT. SHORT LIST REQUESTED?
         BO    IX10                YES
        @LIST  IXCINIT,,U
IX10     MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
IX11    $ICFSR MF=(E,ICFSRL)       READ ICF CATALOG
         LTR   R15,R15             TEST RETURN CODE
         BM    IX90                END OF CATALOG
         BZ    IX12
         CH    R15,=H'1'
         BNE   ERR56
         BAS   R14,IXRR
         B     ERR56
IX12     CLI   CTYPE,255           CONTINUATION?
         BER   R6                  YES
         XC    IXCNME,IXCNME
         XC    IXCPRDSE,IXCPRDSE
IXSW1    B     *+L'*+8
         OI    IXSW1+1,ON
         BAS   R14,IXLS            PRINT
         LA    R1,IXTBTY           SCAN TYPES TABLE
IX13     CLI   0(R1),0             END OF TABLE?
         BE    IXUN                YES, UNEXPECTED TYPE
         CLC   0(1,R1),CTYPE       THIS TYPE?
         BE    *+L'*+8             YES
         LA    R1,L'IXTBTY(R1)     NO,NEXT ENTRY
         B     IX13
         MVC   IXLSCTR(1),5(R1)    GET LIST INDICATOR
         ICM   R15,B'1111',1(R1)   GET ADDRESS TO PROCESS THIS TYPE
         BR    R15
         SPACE 1
IXNVS    XC    ZERODSN,ZERODSN
         MVC   LXTY,=CL8'NON-VSAM'
         MVC   IXCNME,WORKDS       RETAIN DS-NAME
         XC    IXMVCNT,IXMVCNT     RESET MULTI-VOLUME COUNTER
         BAS   R6,IXNVS0
         LH    R0,IXMVCNT
         AH    R0,=H'1'
         STH   R0,IXMVCNT
IXNVS0   BAS   R14,IXUR            GET UNIT NAME
         BAS   R14,IXLS            PRINT
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
IXNVS1   CLI   0(R2),FMOFF         END OF TABLE?
         BE    IXNVS3              YES, NOT IN-LINE DATA-SET
         CLC   DSNME,IXCNME        IS IT THIS ONE?
         BNE   IXNVS2              NO
         CLI   DSTYPE,0            IS IT SAME TYPE?
         BNE   IXNVS2              NO
         CLC   DSVOLSN(L'DSVOLSN),IXCVSN     ON SAME VOLUME?
         BNE   IXNVS2              NO
         BAS   R14,IXNVS10         SET IN TABLE
         B     IX10
IXNVS2   LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     IXNVS1
IXNVS3   BCT   R5,*+L'*+6          CONTROL
         XR    R15,R15             DSNAME'S TABLE OVERFLOW
         B     ERR17
         XC    0(DSTABL,R2),0(R2)  CLEAR
         MVC   DSNME,IXCNME        ENTER IN TABLE
         MVC   DSALTVO(L'DSALTVO),IXCVSN
         BAS   R14,IXNVS10         SET IN TABLE
         LA    R2,DSTABL(R2)
         MVI   0(R2),FMOFF         SET FINAL MARKER
         B     IX10                GET NEXT ONE
IXNVS10  MVI   DSCAT,XCSI          INDICATE READ FROM ICF CATALOG
         XR    R0,R0
         ICM   R0,B'0011',IXCFSN
         BNZ   *+L'*+12
         ICM   R0,B'0011',IXMVCNT
         MVI   IXMVFS,XOM          SAY MULTI-VOL.
         B     *+L'*+4
         MVI   IXMVFS,XOF          SAY FILE SEQ.
         STCM  R0,B'0011',DSMVCNT  SET MULTI-VOLUME COUNT
         LTR   R0,R0
         BNZ   *+L'*+8
         ST    R2,IXCPRDSE         THIS CAN BE START SEQ. 0
         B     IXNVS11+L'IXNVS11
         ICM   R15,B'1111',IXCPRDSE     GET ADDRESS OF START SEQ. 0
         BZ    IXNVS11
         DROP  R2
         USING DSTABDS,R15
         MVC   DSVORG,IXMVFS       SET D.S. VOL. ORG.
         DROP  R15
         XC    IXCPRDSE,IXCPRDSE
         USING DSTABDS,R2
IXNVS11  MVC   DSVORG,IXMVFS       SET D.S. VOL. ORG.
         MVC   DSCVOL,DDNVOL       RETAIN CAT. INFO VOLUME
         BR    R14
         DROP  R2
         SPACE 1
IXGDG    XC    ZERODSN,ZERODSN
         CLC   IXCVSN,=CL6' '
         BE    IXGDG30
         MVC   LXTY,=CL8'GDG D.S.'
         MVC   IXCNME,WORKDS       RETAIN DS-NAME
         LA    R0,L'WORKDS-1       BUILD MODEL GDG NAME
         LA    R1,WORKDS+L'WORKDS-1
IXGDG1   CLI   0(R1),C' '
         BNE   IXGDG2
         BCTR  R1,0
         BCT   R0,IXGDG1
         B     IXGDG4
IXGDG2   CLI   0(R1),C'.'
         MVI   0(R1),C' '
         BE    IXGDG3
         BCTR  R1,0
         BCT   R0,IXGDG2
         B     IXGDG4
IXGDG3   BAS   R14,IXGDG40         SCAN TO FIND MODEL
         NOP   0
IXGDG4   MVC   WORKDS,IXCNME       RESTORE DS-NAME
         XC    IXMVCNT,IXMVCNT     RESET MULTI-VOLUME COUNTER
         BAS   R6,IXGDG10
         LH    R0,IXMVCNT
         AH    R0,=H'1'
         STH   R0,IXMVCNT
IXGDG10  BAS   R14,IXUR            GET UNIT NAME
         BAS   R14,IXLS            PRINT
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
IXGDG11  CLI   0(R2),FMOFF         END OF TABLE?
         BE    IXGDG13             YES, NOT IN-LINE GDG D.S.
         CLC   DSNME,IXCNME        IS IT THIS ONE?
         BNE   IXGDG12             NO
         CLC   DSVOLSN(L'DSVOLSN),IXCVSN     ON SAME VOLUME?
         BNE   IXGDG12             NO
         BAS   R14,IXGDG20         SET IN TABLE
         B     IX10
IXGDG12  LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     IXGDG11
IXGDG13  BCT   R5,*+L'*+6          CONTROL
         XR    R15,R15             DSNAME'S TABLE OVERFLOW
         B     ERR17
         XC    0(DSTABL,R2),0(R2)  CLEAR
         MVC   DSNME,IXCNME        ENTER IN TABLE
         MVC   DSALTVO(L'DSALTVO),IXCVSN
         BAS   R14,IXGDG20         SET IN TABLE
         LA    R2,DSTABL(R2)
         MVI   0(R2),FMOFF         SET FINAL MARKER
         B     IX10                GET NEXT ONE
IXGDG20  MVI   DSCAT,XCSI          INDICATE READ FROM ICF CATALOG
         MVI   DSUSE,XUG           INDICATE GDG D.S.
         XR    R0,R0
         ICM   R0,B'0011',IXCFSN
         BNZ   *+L'*+12
         ICM   R0,B'0011',IXMVCNT
         MVI   IXMVFS,XOM          SAY MULTI-VOL.
         B     *+L'*+4
         MVI   IXMVFS,XOF          SAY FILE SEQ.
         STCM  R0,B'0011',DSMVCNT  SET MULTI-VOLUME COUNT
         LTR   R0,R0
         BNZ   *+L'*+8
         ST    R2,IXCPRDSE         THIS CAN BE START SEQ. 0
         B     IXGDG21+L'IXGDG21
         ICM   R15,B'1111',IXCPRDSE     GET ADDRESS OF START SEQ. 0
         BZ    IXGDG21
         DROP  R2
         USING DSTABDS,R15
         MVC   DSVORG,IXMVFS       SET D.S. VOL. ORG.
         DROP  R15
         USING DSTABDS,R2
         XC    IXCPRDSE,IXCPRDSE
IXGDG21  MVC   DSVORG,IXMVFS       SET D.S. VOL. ORG.
         MVC   DSCVOL,DDNVOL       RETAIN CAT. INFO VOLUME
         BR    R14
IXGDG30  MVC   LXTY,=CL8'NULL GDG'
         MVC   LXNM,WORKDS
         BAS   R14,IXLS            PRINT
         BAS   R14,IXGDG40         SCAN TO FIND MODEL
         B     IXGDG31
         BCT   R5,*+L'*+6          CONTROL
         XR    R15,R15             DSNAME'S TABLE OVERFLOW
         B     ERR17
         XC    0(DSTABL,R2),0(R2)  CLEAR
         MVC   DSNME,WORKDS        ENTER IN TABLE
         MVC   DSALTVO(L'DSALTVO),DDNVOL
         MVI   DSUSE,XUI           INDICATE NULL GDG
         MVI   DSCAT,XCSI          INDICATE READ FROM ICF CATALOG
         LA    R2,DSTABL(R2)
         MVI   0(R2),FMOFF         SET FINAL MARKER
IXGDG31  LA    R6,IXUN
         B     IX10
IXGDG40  L     R2,DSTAB            START OF DSNAME'S TABLE
IXGDG41  CLI   0(R2),FMOFF         END OF TABLE?
         BE    4(R14)              YES, RETURN = +4
         CLI   DSUSE,XUM           NO, MODEL GDG?
         BNE   IXGDG42             NO
         CLC   DSNME,WORKDS        YES, SAME DS-NAME?
         BNE   IXGDG42             NO
         CLC   DSVOLSN,DDNVOL      YES, ON CATALOG VOLUME?
         BNE   IXGDG42             NO
         CLI   DSCAT,0             ALREADY SET?
         BNER  R14                 YES, RETURN = +0
         MVI   DSCAT,XCSI          INDICATE READ FROM ICF CATALOG
         BR    R14                 RETURN = +0
IXGDG42  LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     IXGDG41
         DROP  R2
         SPACE 1
IXCLS    CLI   WORKDS,C' '
         BNE   IXCLS1
         CLI   IXMSPW,0
         BE    IXCLS1
         MVC   CVLPW,IXMSPW
         XC    IXMSPW,IXMSPW
         LA    R14,CVLPW+L'CVLPW-1
         LA    R15,L'CVLPW
         CLI   0(R14),C' '
         BNE   *+L'*+12
         BCTR  R14,0
         BCT   R15,*-10
         XC    CVLPW,CVLPW
         STC   R15,CVLPWL
IXCLS1   MVC   LXTY(7),=CL7'CLUSTER'
         MVC   LXNM,WORKDS
         BAS   R14,IXLS            PRINT
         CLI   WORKDS,C' '
         BNE   *+L'*+10
         XC    ZERODSN,ZERODSN
         B     *+L'*+6
         MVC   ZERODSN,WORKDS      RETAIN TRUENAME
         LA    R6,IXUN
         B     IX10
         SPACE 1
IXALX    MVC   LXTY(3),=CL3'AIX'
         MVC   LXNM,WORKDS
         BAS   R14,IXLS            PRINT
         LA    R6,IXUN
         B     IX10
         SPACE 1
IXDTA    MVC   LXST+2(4),=CL4'DATA'
         B     IXINX+L'IXINX
IXINX    MVC   LXST+1(5),=CL5'INDEX'
         MVC   IXCNME,WORKDS       RETAIN DS-NAME
         XC    IXMVCNT,IXMVCNT     RESET MULTI-VOLUME COUNTER
         BAS   R6,IXDI0
         LH    R0,IXMVCNT
         AH    R0,=H'1'
         STH   R0,IXMVCNT
IXDI0    BAS   R14,IXUR            GET UNIT NAME
         BAS   R14,IXLS            PRINT
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
IXDI1    CLI   0(R2),FMOFF         END OF TABLE?
         BE    IXDI4               YES, NOT IN-LINE DATA-SET
         CLC   DSNME,IXCNME        IS IT THIS ONE?
         BNE   IXDI3               NO
         CLI   DSTYPE,XTV          IS IT SAME TYPE?
         BNE   IXDI3               NO
         CLC   DSVOLSN(L'DSVOLSN),IXCVSN     ON SAME VOLUME?
         BNE   IXDI3               NO
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* NOTE - THE FOLLOWING 6 INSTRUCTIONS ARE NECESSARIES TO SUPPRESS     *
*        THE ABNORMAL SITUATION OF TWICE DEFINED THE VOLUME FOR A     *
*        CLUSTER-INDEX OR REPEATED REFERENCES TO A 'SYS1.VVDS.V...'   *
*        SPACE CLUSTER USED BY VARIOUS ICF CATALOGS.                  *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         CLI   DSCAT,XCSI          ALREADY READ FROM ICF CATALOG?
         BNE   IXDI2               NO
         CLC   DSANME,ZERODSN      IS IT SAME TRUENAME?
         BE    IX10                YES (ABNORMAL CLUSTER-INDEX)
         CLC   IXCNME(11),=CL11'SYS1.VVDS.V' IS IT A SPACE CLUSTER?
         BE    IX10                YES
IXDI2    CLI   DSCAT,0             ALREADY CATALOGED?
         BNE   IXDI3               YES
         BAS   R14,IXDI10          NO, SET IN TABLE
         B     IX10
IXDI3    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     IXDI1
IXDI4    BCT   R5,*+L'*+6          CONTROL
         XR    R15,R15             DSNAME'S TABLE OVERFLOW
         B     ERR17
         XC    0(DSTABL,R2),0(R2)  CLEAR
         MVC   DSNME,IXCNME        ENTER IN TABLE
         MVC   DSALTVO(L'DSALTVO),IXCVSN
         BAS   R14,IXDI10          SET IN TABLE
         LA    R2,DSTABL(R2)
         MVI   0(R2),FMOFF         SET FINAL MARKER
         B     IX10                GET NEXT ONE
IXDI10   MVI   DSCAT,XCSI          INDICATE READ FROM ICF CATALOG
         CLC   DSNME,ZERODSN       TRUENAME IDENTICAL TO DS-NAME?
         BE    *+L'*+6             BRANCH IF YES
         MVC   DSANME,ZERODSN      SET TRUENAME
         XR    R0,R0
         ICM   R0,B'0011',IXCFSN
         BNZ   *+L'*+12
         ICM   R0,B'0011',IXMVCNT
         MVI   IXMVFS,XOM          SAY MULTI-VOL.
         B     *+L'*+4
         MVI   IXMVFS,XOF          SAY FILE SEQ.
         STCM  R0,B'0011',DSMVCNT  SET MULTI-VOLUME COUNT
         LTR   R0,R0
         BNZ   *+L'*+8
         ST    R2,IXCPRDSE         THIS CAN BE START SEQ. 0
         B     IXDI11+L'IXDI11
         ICM   R15,B'1111',IXCPRDSE     GET ADDRESS OF START SEQ. 0
         BZ    IXDI11
         DROP  R2
         USING DSTABDS,R15
         MVC   DSVORG,IXMVFS       SET D.S. VOL. ORG.
         DROP  R15
         XC    IXCPRDSE,IXCPRDSE
         USING DSTABDS,R2
IXDI11   MVC   DSVORG,IXMVFS       SET D.S. VOL. ORG.
         MVC   DSCVOL,DDNVOL       RETAIN CAT. INFO VOLUME
         BR    R14
         DROP  R2
         SPACE 1
IXPTH    XC    ZERODSN,ZERODSN
         MVC   LXTY(4),=CL4'PATH'
         BAS   R14,IXNMS
         NI    IXSW1+1,OFF
         BAS   R6,IX11
         OI    IXSW1+1,ON
         BASR  R6,0
         BAS   R14,IXNME
         BAS   R14,IXLS            PRINT
         B     IX10
         SPACE 1
IXALS    XC    ZERODSN,ZERODSN
         MVC   LXTY(5),=CL5'ALIAS'
         BAS   R14,IXNMS
         NI    IXSW1+1,OFF
         BAS   R6,IX11
         OI    IXSW1+1,ON
         LA    R14,LXNM+1
         LA    R15,LXNM+8
IXALS1   CLI   0(R14),C'.'
         BE    IXALS5
         CLI   0(R14),C' '
         BE    IXALS2
         LA    R14,1(R14)
         CLR   R14,R15
         BNH   IXALS1
         B     IXALS5
IXALS2   LA    R14,WORKDS
         LA    R15,WORKDS+L'WORKDS-L'CVPVOL-2
IXALS3   CLI   0(R14),C' '
         BE    IXALS5
         CLC   0(2,R14),=CL2'.V'
         BE    IXALS4
         LA    R14,1(R14)
         CLR   R14,R15
         BNH   IXALS3
         B     IXALS5
IXALS4   L     R1,TBGVOL           CVOL POINTERS TABLE
         CLI   0(R1),FMOFF         END OF TABLE?
         BE    *+L'*+8             YES
         LA    R1,CVPL(R1)         NO, NEXT ELEMENT
         B     *-12
         LA    R15,CVPL(R1)        FINAL ELEMENT
         CL    R15,TBGVOLE         ANY OVERFLOW?
         BL    *+L'*+6             NO
         XR    R15,R15             YES
         B     ERR64
         USING CVPDS,R1
         MVC   CVPNAME(L'CVPNAME),LXNM
         MVC   CVPVOL(L'CVPVOL),2(R14)
         OI    CVPFL,CVPICF
         DROP  R1
         MVI   0(R15),FMOFF        SET FINAL MARKER
IXALS5   BASR  R6,0
         BAS   R14,IXNME
         BAS   R14,IXLS            PRINT
         B     IX10
         SPACE 1
IXAIX    XC    ZERODSN,ZERODSN
         MVC   LXTY(8),=CL8'TRUENAME'
         MVC   LXST+3(3),=CL3'AIX'
         BAS   R14,IXNMS
         NI    IXSW1+1,OFF
         BAS   R6,IX11
         OI    IXSW1+1,ON
         BASR  R6,0
         BAS   R14,IXNME
         BAS   R14,IXLS            PRINT
         B     IX10
         SPACE 1
IXDTN    XC    ZERODSN,ZERODSN
         MVC   LXTY(8),=CL8'TRUENAME'
         MVC   LXST+2(4),=CL4'DATA'
         BAS   R14,IXNMS
         NI    IXSW1+1,OFF
         BAS   R6,IX11
         OI    IXSW1+1,ON
         BASR  R6,0
         BAS   R14,IXNME
         BAS   R14,IXLS            PRINT
         B     IX10
         SPACE 1
IXIXT    XC    ZERODSN,ZERODSN
         MVC   LXTY(8),=CL8'TRUENAME'
         MVC   LXST+1(5),=CL5'INDEX'
         BAS   R14,IXNMS
         NI    IXSW1+1,OFF
         BAS   R6,IX11
         OI    IXSW1+1,ON
         BASR  R6,0
         BAS   R14,IXNME
         BAS   R14,IXLS            PRINT
         B     IX10
         SPACE 1
IXICN    XC    ZERODSN,ZERODSN
         MVC   LXTY(7),=CL7'CONNECT'
         MVC   LXST+3(3),=CL3'ICF'
         BASR  R6,0
         BAS   R14,IXUR            GET UNIT NAME
         BAS   R14,IXLS            PRINT
         B     IX10
         SPACE 1
IXVCN    XC    ZERODSN,ZERODSN
         MVC   LXTY(7),=CL7'CONNECT'
         MVC   LXST+2(4),=CL4'VSAM'
         BASR  R6,0
         BAS   R14,IXUR            GET UNIT NAME
         BAS   R14,IXLS            PRINT
         B     IX10
         SPACE 1
IXRR     STM   R14,R1,IXSVR        DISPLAY REG. ROUTINE
         UNPK  IXRMR0(9),IXSVR+8(5)
         TR    IXRMR0,TRTAB-X'F0'
         MVI   IXRMR0+L'IXRMR0,C'/'
         UNPK  IXRMR1(9),IXSVR+12(5)
         TR    IXRMR1,TRTAB-X'F0'
         MVI   IXRMR1+L'IXRMR1,C' '
        WTO    MF=(E,IXRMR)
         LM    R14,R1,IXSVR
         BR    R14
         SPACE 1
IXLS     TM    IXLSCTR,IXSHSK      SELECT HOW LIST IS CONTROLED
         BZ    IXLS1
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BOR   R14                 NO
         TM    RUNIND,RUCSH        CAT. SHORT LIST REQUESTED?
         BOR   R14                 YES
         ST    R14,IXSVLS
         B     IXLS2
IXLS1    TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BOR   R14                 NO
         ST    R14,IXSVLS
         TM    RUNIND,RUCSH        CAT. SHORT LIST REQUESTED?
         BZ    IXLS2               NO
IXSW2    NOP   IXLS2
         OI    IXSW2+1,ON
         MVI   IXCINIT,C'0'
        @LIST  IXCINIT,=PL2'+48',U
        @LIST  SPACE
IXLS2   @LIST  LINE
         L     R14,IXSVLS
         BR    R14
         SPACE 1
IXUR     ST    R14,IXSVUR          UNIT NAME ROUTINE
         CLI   WORKDS,C' '
         BE    IXUR1
         MVC   LXNM,WORKDS
        @FILL  LXNM,WORKDS
IXUR1    MVC   LXVL,IXCVSN
         UNPK  LXDT(L'LXDT+1),WORKD(L'LXDT/2+1)
         MVI   LXDT+L'LXDT,C' '
         TR    LXDT(L'LXDT),TRTAB-X'F0'
         CLC   WORKD(4),=XL4'0'
         BE    IXUR2
        $DVTYUN ,LXUN,MF=(E,ICFDV) GET UNIT NAME
         LTR   R15,R15
         BZ    IXUR2
         MVC   LXUN(4),=CL4'?RC='
         CVD   R15,WORKD
         UNPK  LXUN+4(2),WORKD+6(2)
         OI    LXUN+5,C'0'
         MVI   LXUN+6,C'?'
IXUR2    L     R14,IXSVUR
         BR    R14
         SPACE 1
IXNMS    MVC   LXNM,WORKDS         DS-NAME ROUTINE
         LA    R1,LXNM+L'LXNM-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         ST    R1,IXNPTR
         BR    R14
IXNME    L     R1,IXNPTR
         MVI   2(R1),C'/'
         MVC   4(L'WORKDS,R1),WORKDS
         BR    R14
         DROP  R7
         SPACE 1
IX90     ST    R5,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
         LTR   R0,R0
         BNZ   *+L'*+6
         LTR   R1,R1
         BZ    IX91
         BAS   R14,IXRR
         B     ERR56
IX91     NI    CVLIND,255-CVLCSO   SET CLOSED
        $ICFSC MF=(E,ICFSCL)       CLOSE ICF CATALOG
         LTR   R15,R15             TEST RETURN CODE
         BZ    IX92
         CH    R1,=H'2'
         BNE   ERR57
         BAS   R14,IXRR
         B     ERR57
IX92     TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    IX94                NO
         TM    RUNIND,RUCSH        CAT. SHORT LIST REQUESTED?
         BZ    IX93                NO
         TM    IXSW2+1,ON
         BZ    IX94
         NI    IXSW2+1,OFF
IX93    @LIST  IXCENDM
         DROP  R3,R4
IX94     MVI   LINE,C' '           ICF CAT. INF.
         MVC   LINE+1(L'LINE-1),LINE
         LA    R2,LINE
         LA    R3,IXNCTR-L'IXNCTR
         LA    R4,IXNCTRT-L'IXNCTRT
         LA    R5,IXNCTRN
IX95     LA    R3,L'IXNCTR(R3)
         LA    R4,L'IXNCTRT(R4)
         MVC   LINE(1),0(R4)
         MVC   LINE+19(L'IXNCTRT-1),1(R4)
         L     R1,0(R3)
         CVD   R1,WORKD
         CLI   1(R4),C' '
         BE    IX96
         MVC   LINE+11(7),=XL7'20202020202120'
         ED    LINE+10(8),WORKD+4
         MVC   LINE+L'IXNCTRT+19(7),LINE+L'IXNCTRT+18
         B     IX97
IX96     MVC   LINE+L'IXNCTRT+19(7),=XL7'20202020202120'
         ED    LINE+L'IXNCTRT+18(8),WORKD+4
         MVC   LINE+11(7),LINE+10
IX97    @OUT   LINE
         BCT   R5,IX95
         SPACE 1
        @XE
         EJECT
ICFSOL  $ICFSO ,IXDVTY,,ICFSW,MF=L
ICFSRL  $ICFSR ,IXCVSN,,MF=L
ICFSCL  $ICFSC IXNCTR,MF=L
ICFDV   $DVTYUN ,,MF=L
         SPACE 1
IXDVTY   DC    5F'0'
         DC    H'0'
IXCVOL   DC    CL6' '
IXCNME   DC    CL44' '
IXSVR    DC    4F'0'
IXSVUR   DC    F'0'
IXSVLS   DC    F'0'
IXNPTR   DC    F'0'
IXCPRDSE DC    F'0'
IXCVSN   DC    CL6' '
IXCFSN   DC    H'0'
IXMVCNT  DC    H'0'
IXMVFS   DC    XL1'0'
IXLSCTR  DC    XL1'0'
         EJECT
IXRMR   WTO    '--- CONTENT OF REGISTERS 0-1 :                    ',   C
               ROUTCDE=11,DESC=7,MF=L
IXRMR0   EQU   IXRMR+35,8
IXRMR1   EQU   IXRMR+44,8
IXRCM   WTO    '--- REQ. ICF CATALOG :        /                        C
                                     ',ROUTCDE=11,DESC=7,MF=L
IXRCM1   EQU   IXRCM+27,6
IXRCM2   EQU   IXRCM+36,44
IXRAM   WTO    '--- ALL. ICF CATALOG :        /                        C
                                     ',ROUTCDE=11,DESC=7,MF=L
IXRAM1   EQU   IXRAM+27,6
IXRAM2   EQU   IXRAM+36,44
         EJECT
        $ICFSW ICFAUTH+ICFMSPW
IXMSPW   DC    XL8'0'
         SPACE 1
         DC    AL1(IXCINFL)
IXCINF   DC    CL6'0 ',CL7' ICF / '
IXCINFA  DC    CL6' ',C' ('
IXCINFB  DC    CL8'        ',C') : '
IXCINFC  DC    CL44' '
IXCINFL  EQU   *-IXCINF
         DC    AL1(IXCINITL)
IXCINIT  DC    CL8'1 ICF / '
IXCINITA DC    CL6' ',C' ('
IXCINITB DC    CL8' ',C') CATALOG : '
IXCINITC DC    CL44' '
IXCINITL EQU   *-IXCINIT
         DC    AL1(L'IXCENDM)
IXCENDM  DC    C'0    END OF CATALOG LIST.'
         EJECT
*                   ----+----1----+----2----+---
IXNCTRT  DC    CL28' GETS ISSUED AGAINST THE BCS'      1
         DC    CL28' NONVSAM ENTRIES'                  2
         DC    CL28' CLUSTER ENTRIES'                  3
         DC    CL28'   ALTERNATE INDEX ENTRIES :'      4
         DC    CL28'    DATA (CLUSTER) ENTRIES :'      5
         DC    CL28'   INDEX (CLUSTER) ENTRIES :'      6
         DC    CL28' GDG BASE ENTRIES'                 7
         DC    CL28'               GDG ENTRIES :'      8
         DC    CL28' PATH ENTRIES'                     9
         DC    CL28' ALIAS ENTRIES'                   10
         DC    CL28' TRUENAME ENTRIES'                11
         DC    CL28' ICF CONNECTOR ENTRIES'           12
IXNCTRN  EQU   (*-IXNCTRT)/L'IXNCTRT
         SPACE 1
IXNCTR   DC    (IXNCTRN)F'0'
         SPACE 2
IXTBTY   DS    0XL6                TABLE TYPES / PROCESS
         SPACE 1
IXSHSK   EQU   X'80'     DON'T LIST IF CAT. SHORT LIST REQUESTED
         SPACE 1
         DC    AL1(1),AL4(IXNVS),AL1(IXSHSK)      NON-VSAM NAME
         DC    AL1(2),AL4(IXGDG),AL1(0)           GDG NAME
         DC    AL1(3),AL4(IXCLS),AL1(IXSHSK)      CLUSTER NAME
         DC    AL1(4),AL4(IXALX),AL1(IXSHSK)      ALT. INDEX NAME
         DC    AL1(5),AL4(IXDTA),AL1(IXSHSK)      DATA NAME
         DC    AL1(6),AL4(IXINX),AL1(IXSHSK)      INDEX NAME
         DC    AL1(7),AL4(IXPTH),AL1(0)           PATH NAME
         DC    AL1(8),AL4(IXALS),AL1(IXSHSK)      ALIAS NAME
         DC    AL1(9),AL4(IXAIX),AL1(IXSHSK)      AIX TRUE-NAME
         DC    AL1(10),AL4(IXDTN),AL1(IXSHSK)     DATA TRUE-NAME
         DC    AL1(11),AL4(IXIXT),AL1(IXSHSK)     INDEX TRUE-NAME
         DC    AL1(12),AL4(IXICN),AL1(0)          ICF CONNECT NAME
         DC    AL1(13),AL4(IXVCN),AL1(0)          VSAM CONNECT NAME
         DC    AL1(0)              END TABLE INDICATOR
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
         SPACE 1
LXDSCT   DSECT                     LINE DESCRIPTION
LXCC     DS    CL1,CL9             ASA CC
LXTY     DS    CL8,CL2             TYPE
LXST     DS    CL6,CL2             SUB-TYPE
LXNM     DS    CL44,CL2            NAME
LXVL     DS    CL6,CL2             VOLUME SERIAL NUMBER
LXUN     DS    CL8,CL2             UNIT NAME
LXDT     DS    CL8                 DEVICE TYPE
         SPACE 2
         END
