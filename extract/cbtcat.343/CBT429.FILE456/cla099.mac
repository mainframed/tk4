A099     TITLE 'CLEAN-UP - END OF PROCESS (A099).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A099    @XS
*        END OF PROCESS.
         SPACE 1
ENSW1    NOP   EN1
         OI    ENSW1+1,ON
         LA    R1,COPYDS
         ST    R1,COPYCAT+4
         ST    R1,COPYSCR+4
         ST    R1,COPYUCT+4
         ST    R1,COPYUNC+4
         LA    R1,LOCAREA
         ST    R1,COPYCAT+12
         LA    R1,CPYCAT
         ST    R1,COPYSCR+12
         LA    R1,CPYCVOL
         ST    R1,COPYUCT+8
EN1      L     R9,MAXERR           TO COMPUTE ERROR NUMBER
         TM    WHAT,WHIN
         BZ    EN2
         NI    WHAT,255-WHIN-WHCD-WHEOF
        $RCLOSE
EN2      TM    WHAT,WHDATA
         BZ    EN3
         NI    WHAT,255-WHDATA
        $UCLOSE MF=(E,UDDPLS)
EN3      TM    WHAT,WHPDSA+WHPDSO
         BZ    EN11
         TM    WHAT,WHPDSO
         BZ    EN10
         NI    WHAT,255-WHPDSO
        $MCLOSE
EN10     NI    WHAT,255-WHPDSA
EN11     L     R2,TBCVOL           START OF CAT. VOL. TABLE
         USING CVLT,R2
EN12     CLI   0(R2),FMOFF         END OF TABLE?
         BE    EN20                YES
         TM    CVLIND,CVLCSO       NO, CATALOG IS YET OPENED?
         BZ    EN15                NO
         TM    CVLIND,CVLICF       YES, IS IT AN ICF CATALOG?
         BO    EN13                YES
        $CATSC ,                   NO, CLOSE O.S. CATALOG
         B     EN14
EN13    $ICFSC ,                   CLOSE ICF CATALOG
EN14     NI    CVLIND,255-CVLCSO   SAY CLOSED
         DROP  R2
EN15     LA    R2,CVLL(R2)         ADVANCE IN TABLE
         B     EN12                AND LOOP
EN20     L     R2,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R2
EN21     CLI   0(R2),FMOFF         END OF TABLE?
         BE    EN30                YES
         TM    DDNALL,DDNXA
         BZ    EN24
         TM    DDNALL,DDNXQ
         BZ    EN22
         NI    DDNALL,255-DDNXQ
        $VTOCQC
EN22     TM    DDNALL,DDNXD
         BZ    EN23
         NI    DDNALL,255-DDNXD
        $VTOCDC
EN23     NI    DDNALL,255-DDNXA
         MVC   ENDDN(L'DDNNME),DDNNME
        $DAVFR MF=(E,ENPLS)
EN24     LA    R2,DDNL(R2)         ADVANCE IN TABLE
         B     EN21                AND LOOP
         DROP  R2
EN30     TM    WHATS,WHIDIN
         BZ    EN31
         NI    WHATS,255-WHIDIN
         L     R2,DAIDIN
        $UCLOSE (R2)
EN31     TM    WHATS,WHIDOUT
         BZ    EN32
         NI    WHATS,255-WHIDOUT
         L     R2,DAIDOUT
        $VCLOSE (R2)
EN32     LA    R2,DALLS            DE-ALLOCATE ALL
         LA    R3,DALLN
EN33     L     R14,0(R2)
         USING DYNLIST,R14
         CLI   DSNAME,0
         BE    EN34
         CLI   DSNAME,C' '
         BE    EN34
         XC    DSNAME,DSNAME
         DROP  R14
        $DYNALL (R14),,MF=(E,DYNPLS)
EN34     LA    R2,4(R2)
         BCT   R3,EN33
         LM    R3,R4,AGIDST        FREE ANY IDCAMS ARRAY(S)
EN35     OC    AGIDST,AGIDST
         BZ    EN40
         L     R3,0(R3)
        FREEMAIN E,LV=(R4),A=AGIDST,MF=(E,ENFREE)
         ST    R3,AGIDST
         B     EN35
EN40     TM    RUSREQ,RUSSCR       COPY MUST BE SCRATCHED?
         BO    EN41                YES
         TM    WHAT,WHCOPY         COPY HAS BEEN ALLOCATED?
         BZ    EN90                NO
         NI    WHAT,255-WHCOPY
         TM    STATUS,STSCPY       MAY BE COPY MUST BE SCRATCHED?
         BZ    EN60                NO
         NI    STATUS,255-STSCPY
EN41     CLC   CPYVOL,=CL6' '      DATA-SET VOLUME ALREADY KNOWN?
         BNE   ENSW3               YES
ENSW2    NOP   EN90
         XC    LOCAREA(256),LOCAREA
         XC    LOCAREA+256(LOCAREAL-256),LOCAREA+256
        LOCATE COPYCAT
         LTR   R15,R15             TEST COMPLETION CODE
         BZ    EN43
         OI    ENSW2+1,ON
         TM    WHATS,WHPERR
         BO    EN42
        @OUT   LOCFAIL
         B     EN90
EN42    WTO    '--- COPY : LOCATE OF DATA-SET FAILED.',                C
               ROUTCDE=11,DESC=7
         B     EN90
EN43     MVC   CPYTY(L'CPYTY),LOCAREA+2
         MVC   CPYVOL(L'CPYVOL),LOCAREA+6
ENSW3    NOP   EN45
         OI    ENSW3+1,ON
        SCRATCH COPYSCR
         LTR   R15,R15             TEST COMPLETION CODE
         BZ    EN45
         TM    WHATS,WHPERR
         BO    EN44
        @OUT   SCRFAIL
         B     EN45
EN44    WTO    '--- COPY : SCRATCH OF DATA-SET FAILED.',               C
               ROUTCDE=11,DESC=7
EN45     TM    RUSREQ,RUSSCR       COPY TO BE SCRATCHED ONLY?
         BZ    ENSW4               NO, MUST BE ALSO UNCATALOGED
         NI    RUSREQ,255-RUSSCR
         B     EN54
ENSW4    NOP   EN54
         OI    ENSW4+1,ON
         SPACE 1
* NOTE - WHEN THE MASTER CATALOG IS VSAM OR ICF THE DATA-SET IS FIRST
*        SCRATCHED AND THEN UNCATALOGED USING CATALOG MACRO (OS-CVOL)
*        OR INVOKING IDCAMS FUNCTION (ALIAS).
         SPACE 1
         CLI   COPYCNME,C' '       IDCAMS TO BE USED?
         BNE   EN51                YES
         CLC   CPYCVOL,=CL6' '     CATALOG VOLUME NECESSARY?
         BE    EN50                NO
        CATALOG COPYUCT
         B     EN52
EN50    CATALOG COPYUNC
         B     EN52
EN51     LA    R1,COPYDS
         ST    R1,IDCDSN
         LA    R1,COPYCNME
         ST    R1,IDCPLS
         MVI   IDCFUN,C'U'
        @X     C001                INVOKE IDCAMS FUNCTION
EN52     LTR   R15,R15             TEST COMPLETION CODE
         BZ    EN54
         TM    WHATS,WHPERR
         BO    EN53
        @OUT   UNCFAIL
         B     EN90
EN53    WTO    '--- COPY : UNCATALOG OF DATA-SET FAILED.',             C
               ROUTCDE=11,DESC=7
         B     EN90
EN54     TM    WHATS,WHPERR
         BO    EN55
        @OUT   DSDELET
         B     EN90
EN55    WTO    '--- COPY : ERROR - DATA-SET IS DELETED.'               C
               ROUTCDE=11,DESC=7
         B     EN90
EN60     NI    STATUS,255-STCOPY
         CLI   CPYCMD+1,0          ANY START COMMAND?
         BE    EN90                NO
        ZEROKEY
         XR    R0,R0
         XR    R15,R15
        MGCR   CPYCMD              SEND COMMAND
         LR    R2,R15
        RESETKEY
         MVI   CPYCMD+1,0          NULLIFY COMMAND, IT IS SENDED
         LTR   R2,R2               TEST RETURN CODE
         BZ    EN90
        WTO    '--- COPY : START COMMAND FAILED.',                     C
               ROUTCDE=11,DESC=7
EN90     TM    WHATS,WHPERR
         BO    EN99
         CLC   CUST1,=CL8' '
         BE    EN99
         MVC   TITTEST(L'TITTEST),TITTEST-1
         MVC   CUST2A,=CL5'ENDED'
         TM    RUNOPT,RUNOHT       TRAILER SUPPRESS?
         BO    EN99                YES
        @OUT   SKLSPG
        $EDITH MF=(E,CU1PLS)
         MVC   CUST1,=CL8' '
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR45
        $EDITH MF=(E,CU2PLS)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR45
EN99    $PCLOSE
         L     R2,TOTSPCE
         LA    R0,TBEVOL
        FREEMAIN E,LV=(R2),A=(R0)
        @XE
         EJECT
COPYCAT CAMLST NAME,*-*,,*-*
COPYSCR CAMLST SCRATCH,*-*,,*-*,,OVRD
COPYUCT CAMLST UCATDX,*-*,*-*
COPYUNC CAMLST UCATDX,*-*
         SPACE 1
ENFREE  FREEMAIN E,LV=0,A=*-*,MF=L
ENPLS   $DAVFR ENDDNL,MF=L
         SPACE 1
ENDDNL   DC    XL1'8'
ENDDN    DC    CL8' '
         EJECT
         DC    AL1(L'SKLSPG)
SKLSPG   DC    CL2'1 '
         DC    AL1(L'LOCFAIL)
LOCFAIL  DC    C'0--- COPY : LOCATE OF DATA-SET FAILED.'
         DC    AL1(L'SCRFAIL)
SCRFAIL  DC    C'0--- COPY : SCRATCH OF DATA-SET FAILED.'
         DC    AL1(L'UNCFAIL)
UNCFAIL  DC    C'0--- COPY : UNCATALOG OF DATA-SET FAILED.'
         DC    AL1(L'DSDELET)
DSDELET  DC    C'0--- COPY : ERROR - DATA-SET IS DELETED.'
         SPACE 2
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
