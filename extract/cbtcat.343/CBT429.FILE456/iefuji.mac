UJI      TITLE '-- I E F U J I - MVS/SMF JOB INITIATION EXIT --'
         EJECT
IEFUJI   START 0
         SPACE 1
IEFUJI   AMODE 24
IEFUJI   RMODE 24
         SPACE 1
*            -------------------------------------------------
*              S M F   J O B   I N I T I A T I O N   E X I T
*            -------------------------------------------------
         SPACE 1
* AT ENTRY TO THE IEFUJI EXIT ROUTINE, REGISTER 1 POINTS TO A LIST OF
* --------     FOUR-BYTE ADDRESSES AS FOLLOWS :
*
* +0  -  1. THE ADDRESS OF THE COMMON EXIT PARAMETER AREA.
*
* +4  -  2. THE ADDRESS OF A 20-BYTE AREA CONTAINING THE PROGRAMMER'S
*           NAME (IN EBCDIC) FROM THE JOB STATEMENT. THIS AREA IS
*           ALIGNED LEFT AND PADDED WITH BLANKS IF NECESSARY.
*
* +8  -  3. THE ADDRESS OF A ONE-BYTE AREA INDICATING (IN BINARY) THE
*           REQUESTED JOB SELECTION PRIORITY. THIS FIELD EQUALS THE
*           USER-ASSIGNED PRIORITY OF 0 TO 14 (TAKEN FROM THE PRTY
*           PARAMETER ON THE JOB CARD).
*
* +12 -  4. THE ADDRESS OF AN AREA CONTAINING THE ACCOUNTING INFORMA-
*           TION FROM THE JOB STATEMENT.
*           FORMAT : - OFFSET 0, NUMBER OF ACCOUNTING FIELDS (ONE
*                                BYTE, BINARY).
*                    - OFFSET 1, THE ACCOUNTING FIELDS (VARIABLE). EACH
*                                ENTRY FOR AN ACCOUNTING FIELD CONTAINS
*                                THE LENGTH OF THE FIELD (ONE BYTE,
*                                BINARY, A ZERO INDICATES AN OMITTED
*                                FIELD) FOLLOWED BY THE FIELD (EBCDIC).
         SPACE 1
* AT EXIT BEFORE THE IEFUJI EXIT ROUTINE RETURNS CONTROL TO THE
* -------      CONTROL PROGRAM, IT MUST PLACE A RETURN CODE IN
*              REGISTER 15, AS FOLLOWS :
*
*              0 - INDICATES JOB PROCESSING SHOULD BE CONTINUED.
*              4 - INDICATES JOB PROCESSING SHOULD BE CANCELED.
         SPACE 2
* AUTHOR :     A. COLOMBO (AMDAHL CONSULTANT)
* --------     P.A. MOINIL
*              COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
         EJECT
        $CEPA
         EJECT
        PRINT  NOGEN
         SPACE 1
        CVT    DSECT=YES,LIST=YES
        IHAPSA
        IHAASCB
        IKJTCB LIST=YES
        IEFTCT
LCT      DSECT
        IEFALLCT
JCT      DSECT
        IEFAJCTB
         AIF   ('&SYSPARM' NE 'TEST').O2S1
SCT      DSECT
        IEFASCTB
.O2S1    SPACE 1
        PRINT  GEN
         SPACE 2
*              WORK AREAS.
         SPACE 1
AREA     DSECT - - - - - - - - - - START OF DSECT - - - - - - - - - - -
         SPACE 1
SAVAREA  DS    18F           SAVE AREA.
         SPACE 1
         AIF   ('&SYSPARM' EQ 'TEST').O2S2
WORK     DS    D             WORK.
.O2S2    ANOP
SWTO     DC    F'0'                LENGTH & FLAGS.
         AIF   ('&SYSPARM' EQ 'TEST').O2S3
SMSGTX   DC    CL12'--- IEFUJI ('
SMSGTN   DC    CL8' ',CL25') - NEW JOB TIME LIMIT : '
SMSGTH   DC    CL2'  ',CL2'H '      HOURS.
SMSGTM   DC    CL2'  ',CL2'M '      MINUTES.
SMSGTS   DC    CL2'  ',CL2'S.'      SECONDS.
         AGO   .O2S4
.O2S3    ANOP
SMSGTXJ  DC    CL23'--- IEFUJI TEST - JCT ('
SMSGJCT  DC    CL9' ',C' '
SMSGJTM  DC    CL7' '
SMSGTXS  DC    CL6' SCT ('
SMSGSCT  DC    CL9' ',C' '
SMSGSTM  DC    CL7' '
.O2S4    ANOP
LWTO     EQU   *-SWTO              WTO LENGTH.
DCRC     DC    XL4'0'              DESC. & ROUTING CODES.
         SPACE 1
AREAL    EQU   (((*-AREA)+7)/8)*8
         SPACE 2
IEFUJI   CSECT                     RESTORE ORIGINAL CSECT.
         EJECT
         USING *,R11               ADDRESSABILITY.
         SPACE 1
         STM   R14,R12,12(R13)     SAVE REGISTERS.
         LA    R11,0(R15)          SET BASE REGISTER.
         L     R2,0(R1)            GET COMMON EXIT PARM. ADDRESS.
         B     START
         SPACE 1
         USING CEPA,R2
         SPACE 1
         DC    CL8'IEFUJI',C'-'    IDENTIFIER.
         DC    CL8'&SYSDATE',C'('  ASSEMBLY DATE.
         DC    CL5'&SYSTIME',C')'  ASSEMBLY TIME.
         SPACE 1
* PROCESS :
* ---------
*
*        WHEN SUPPLIED, THE SRB TIME LIMIT (NUMBER OF I/O OPERATIONS
*        IN THOUSANDS, PASSED THROUGH THE USER COMMUNICATION FIELD,
*        COMMON EXIT PARAMETER AREA +32, BY THE 'IEFUJV' EXIT) IS SET
*        IN THE ASCB. WHEN ZERO, NO SRB TIMING IS PERFORMED.
         SPACE 1
START    SR    R5,R5
         ICM   R5,B'0011',CEPAUCOM+2    I/O NUMBER LIMIT IF ANY (1000).
         BZ    TSTCPU              IF ZERO, NO I/O NUMBER LIMIT.
         LA    R6,SIDIO            SYSTEM-ID'S/FACTOR TO BE APPLIED.
SCAN     CLC   0(L'CEPASID,R6),CEPASID
         BE    FOUND
         LA    R6,L'SIDIO(R6)
         CLI   0(R6),0             AT END OF TABLE USE DEFAULT.
         BNE   SCAN
FOUND    SR    R4,R4               CLEAR REGISTER.
         M     R4,4(R6)            I/O'S * (SRB-TIME FOR 1000 I/O).
         SLDL  R4,12               TO HAVE ONE MICROSECOND IN BIT 51.
         CH    R4,=H'1'            IF LESS, SET TO ONE (MINIMUM).
         BNL   STOREIT             OTHERWISE STORE THAT TIME.
         LH    R4,=H'1'            GET ONE.
         USING PSA,R0
STOREIT  L     R3,PSAAOLD          PSA OLD ASCB POINTER.
         USING ASCB,R3
         ST    R4,ASCBR15C         SET SRB TIME LIMIT.
         DROP  R0
         SPACE 1
         AIF   ('&SYSPARM' EQ 'TEST').O3S1
         EJECT
* PROCESS :
* ---------
*
*        RE-EVALUATE THE JOB TIME LIMIT (SHOULD BE THE JOB CPU TIME
*        REQUESTED) IF NEEDED.
         SPACE 1
TSTCPU   LA    R6,SIDCPU           SYSTEM-ID'S/FACTOR TO BE APPLIED.
SCNTB    CLC   0(L'CEPASID,R6),CEPASID
         BE    FNDTB
         LA    R6,L'SIDCPU(R6)
         CLI   0(R6),0             END OF TABLE?
         BNE   SCNTB               NO, CONTINUE.
         B     GOBACK              YES, NOTHING TO DO.
FNDTB    LR    R1,R6               RETAIN IT.
         LA    R1,L'SIDCPU(R1)
         CLI   0(R1),0             END OF TABLE?
         BNE   *-8                 NO, CONTINUE UNTIL REACHED.
         CLC   4(4,R1),4(R6)       YES, ANYTHING TO DO?
         BE    GOBACK              NO, NOTHING TO DO.
         AGO   .O3S2
.O3S1    EJECT
* PROCESS :
* ---------
*
*        ONLY WHEN PREDEFINED JOB-NAME MATCHES, DISPLAY THE ADDRESSES
*        OF THE JCT AND SCT, THE JOB TIME LIMIT AND THE MAXIMUM STEP
*        RUNNING TIME (SHOULD BE THAT OF THE FIRST JOB STEP).
         SPACE 1
TSTCPU   CLC   CEPAJOBN,=CL8'SYSSP1$'   IS IT THE TEST JOB-NAME?
         BNE   GOBACK              NO, BYPASS.
        GETMAIN R,LV=AREAL,SP=255
         LR    R9,R1               SET BASE REGISTER FOR AREA.
         USING AREA,R9             ADDRESSABILITY.
         XC    SAVAREA(18*4),SAVAREA
         ST    R13,SAVAREA+4       LINK SAVE AREAS.
         ST    R9,8(R13)
         LR    R13,R9              ACTIVE MY SAVE AREA.
.O3S2    L     R3,CVTPTR           CVT.
         USING CVT,R3
         L     R4,CVTTCBP          TCB WORDS.
         DROP  R3
         L     R3,4(R4)            TCB.
         USING TCB,R3
         SR    R4,R4
         ICM   R4,B'0111',TCBTCTB  TCT.
         DROP  R3
         USING SMFTCT,R4
         L     R3,TCTLCTAD         LCT.
         DROP  R4
         USING LCT,R3
         AIF   ('&SYSPARM' EQ 'TEST').O4S1
         ICM   R4,B'1111',LCTJCTAD JCT.
         BZ    GOBACK              NONE.
         USING JCT,R4
         CLC   JCTJMRTL(L'JCTJMRTL),=AL3(((24*60)-1)*60*100)
         BNL   GOBACK              TIME=1440 OR MAXIMUM.
         XR    R0,R0
         XR    R1,R1
         ICM   R1,B'0111',JCTJMRTL JOB TIME LIMIT (IN HUNDRED OF SEC).
         M     R0,4(R6)            APPLY SYSTEM FACTOR.
         D     R0,=F'100'          REDUCE TO HUNDRED OF SEC.
         LTR   R0,R0
         BZ    *+L'*+6
         AH    R1,=H'1'            ROUND UP.
         XR    R0,R0
         ICM   R0,B'0111',=AL3(((24*60)-1)*60*100)
         CLR   R1,R0               HIGHER THAN MAXIMUM?
         BNH   *+L'*+2             NO.
         LR    R1,R0               YES, GET MAXIMUM.
         STCM  R1,B'0111',JCTJMRTL SET NEW JOB TIME LIMIT.
         DROP  R3,R4
         CL    R1,=A(@OPSGLV*60*100)    MUST BE SIGNALED?
         BNH   GOBACK              NO.
         LR    R3,R1               YES, INFORM OPERATOR.
        GETMAIN R,LV=AREAL,SP=255
         LR    R9,R1               SET BASE REGISTER FOR AREA.
         USING AREA,R9             ADDRESSABILITY.
         XC    SAVAREA(18*4),SAVAREA
         ST    R13,SAVAREA+4       LINK SAVE AREAS.
         ST    R9,8(R13)
         LR    R13,R9              ACTIVE MY SAVE AREA.
.O4S1    MVC   SWTO(4),WTOLST      SET MESSAGE AREA.
         MVI   SWTO+1,LWTO
         MVC   DCRC(4),WTOLST+6
         AIF   ('&SYSPARM' EQ 'TEST').O4S2
         MVC   SMSGTX(12),=CL12'--- IEFUJI ('
         MVC   SMSGTN,CEPAJOBN     JOB-NAME.
         MVC   SMSGTN+L'SMSGTN(25),=CL25') - NEW JOB TIME LIMIT : '
         MVC   SMSGTH(12),=CL12' 0H  0M  0S.'
         XR    R0,R0
         LR    R1,R3
         D     R0,=A(60*100)       COMPUTE SEC./MIN.
         LTR   R0,R0
         BZ    *+L'*+22
         CVD   R0,WORK
         MVC   WORK(4),=XL4'40202021'
         ED    WORK(4),WORK+6
         MVC   SMSGTS(2),WORK+2    SECONDS.
         CL    R1,=F'60'           MORE THAN 1 HOUR?
         BNL   *+L'*+6             YES.
         LR    R0,R1               NO.
         B     STMIN
         XR    R0,R0
         D     R0,=F'60'           COMPUTE MIN./HRS.
         CVD   R1,WORK
         MVC   WORK(4),=XL4'40202021'
         ED    WORK(4),WORK+6
         MVC   SMSGTH(2),WORK+2    HOURS.
         LTR   R0,R0
         BZ    *+L'*+22
STMIN    CVD   R0,WORK
         MVC   WORK(4),=XL4'40202021'
         ED    WORK(4),WORK+6
         MVC   SMSGTM(2),WORK+2    MINUTES.
         AGO   .O4S3
.O4S2    MVC   SMSGTXJ(23),=CL23'--- IEFUJI TEST - JCT ('
         MVC   SMSGTXS(6),=CL6' SCT ('
         UNPK  SMSGJCT(L'SMSGJCT),LCTJCTAD(L'LCTJCTAD+1)
         TR    SMSGJCT(L'SMSGJCT-1),TRTAB-X'F0'
         MVC   SMSGJCT+L'SMSGJCT-1(2),=CL2') '
         ICM   R4,B'1111',LCTJCTAD JCT.
         BNZ   *+L'*+10
         MVC   SMSGJTM(L'SMSGJTM),=CL7'-NONE-,'
         B      *+L'*+16
         USING JCT,R4
         UNPK  SMSGJTM(L'SMSGJTM),JCTJMRTL(L'JCTJMRTL+1)
         TR    SMSGJTM(L'SMSGJTM-1),TRTAB-X'F0'
         MVI   SMSGJTM+L'SMSGJTM-1,C','
         DROP  R4
         UNPK  SMSGSCT(L'SMSGSCT),LCTSCTAD(L'LCTSCTAD+1)
         TR    SMSGSCT(L'SMSGSCT-1),TRTAB-X'F0'
         MVC   SMSGSCT+L'SMSGSCT-1(2),=CL2') '
         ICM   R4,B'1111',LCTSCTAD SCT.
         BNZ   *+L'*+10
         MVC   SMSGSTM(L'SMSGSTM),=CL7'-NONE-.'
         B      *+L'*+16
         USING SCT,R4
         UNPK  SMSGSTM(L'SMSGSTM),SCTSTIME(L'SCTSTIME+1)
         TR    SMSGSTM(L'SMSGSTM-1),TRTAB-X'F0'
         MVI   SMSGSTM+L'SMSGSTM-1,C'.'
         DROP  R3,R4
.O4S3   WTO    MF=(E,SWTO)         SEND MESSAGE TO LOG.
         LR    R1,R9               ADDRESS OF AREA TO BE FREED.
         DROP  R9
         L     R9,4(R13)           PREVIOUS SAVE AREA ADDRESS.
        FREEMAIN R,LV=AREAL,A=(1),SP=255
         LR    R13,R9              RESTORE ORIGINAL SAVE AREA.
         SPACE 1
GOBACK   LM    R14,R12,12(R13)     RESTORE REGISTERS.
         SR    R15,R15             SET RETURN CODE.
         BR    R14                 GO BACK.
         EJECT
*        CONSTANTS.
         SPACE 1
WTOLST   WTO   '  ',MF=L,ROUTCDE=11,DESC=7
         AIF   ('&SYSPARM' NE 'TEST').O8S1
TRTAB    DC    CL16'0123456789ABCDEF'
.O8S1    SPACE 2
         LTORG
         EJECT
        $SIDTB TYPE=I/O
         SPACE 1
         AIF   ('&SYSPARM' EQ 'TEST').O9S1
         EJECT
        $SIDTB TYPE=CPU
.O9S1    SPACE 2
         END
