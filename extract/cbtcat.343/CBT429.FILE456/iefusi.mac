USI      TITLE '-- I E F U S I - MVS/SMF STEP INITIATION EXIT --'
IEFUSI   START 0
         SPACE 1
IEFUSI   AMODE 24
IEFUSI   RMODE 24
         SPACE 1
*           ---------------------------------------------------
*             S M F   S T E P   I N I T I A T I O N   E X I T
*           ---------------------------------------------------
         SPACE 1
* AT ENTRY TO THE IEFUSI EXIT ROUTINE, REGISTER 1 POINTS TO A LIST OF
* --------     FOUR-BYTE ADDRESSES AS FOLLOWS :
*
* +0  -  1. THE ADDRESS OF THE COMMON EXIT PARAMETER AREA.
*
* +4  -  2. THE ADDRESS OF A 8-BYTE AREA CONTAINING THE JOB STEP
*           NAME (IN EBCDIC) FROM THE EXEC STATEMENT. THIS AREA IS
*           ALIGNED LEFT AND PADDED WITH BLANKS IF NECESSARY.
*
* +8  -  3. THE ADDRESS OF A 8-BYTE AREA CONTAINING THE PROGRAM
*           NAME (IN EBCDIC) FROM THE EXEC STATEMENT. THIS AREA IS
*           ALIGNED LEFT AND PADDED WITH BLANKS IF NECESSARY. IF A
*           REFER BACK IS USED, THE AREA CONTAINS 'PGM=*.DD'.
*
* +12 -  4. THE ADDRESS OF AN AREA CONTAINING THE ACCOUNTING INFORMA-
*           TION FROM THE EXEC STATEMENT.
*           FORMAT : - OFFSET 0, NUMBER OF ACCOUNTING FIELDS (ONE
*                                BYTE, BINARY).
*                    - OFFSET 1, THE ACCOUNTING FIELDS (VARIABLE). EACH
*                                ENTRY FOR AN ACCOUNTING FIELD CONTAINS
*                                THE LENGTH OF THE FIELD (ONE BYTE,
*                                BINARY, A ZERO INDICATES AN OMITTED
*                                FIELD) FOLLOWED BY THE FIELD (EBCDIC).
*
* +16 -  5. THE ADDRESS OF A 6-WORD AREA CONTAINING REGION LIMIT
*           VALUES (SEE VSM PARAMETER LIST FOR DESCRIPTION).
*
* +20 -  6. THE ADDRESS OF A WORD CONTAINING A FLAG INDICATING A
*           V=R JOB (BIT 0 : V=R WHEN SET).
         SPACE 1
* AT EXIT BEFORE THE IEFUSI EXIT ROUTINE RETURNS CONTROL TO THE
* -------      CONTROL PROGRAM, IT MUST PLACE A RETURN CODE IN
*              REGISTER 15, AS FOLLOWS :
*
*              0 - INDICATES JOB PROCESSING SHOULD BE CONTINUED.
*              4 - INDICATES JOB PROCESSING SHOULD BE CANCELED.
         EJECT
* VSM PARAMETER LIST :
* --------------------
*
* +0  -  1. VSM FLAG WORD (INITIALIZED TO ZERO) :
*           BIT 0  - ON = IEFUSI HAS SET REGION LIMIT VALUES.
*                  NOTE : IF GETMAIN LIMITS ARE NOT SET BY THE EXIT,
*                  BUT A CHECK FOR CONTIGUOUS FREE SPACE BY VSM SHOULD
*                  BE PERFORMED, BIT 0 OF VSM FLAG WORD MUST BE SET.
*           BIT 1  - ON = VSM IS NOT TO CHECK FOR THE AVAILABILITY OF
*                    THE REQUESTED AMOUNT OF CONTIGUOUS FREE SPACE
*                    BELOW 16 MEGABYTES.
*                  - OFF = VSM IS TO CHECK FOR THE AVAILABILITY OF
*                    THE REQUESTED AMOUNT OF CONTIGUOUS FREE SPACE
*                    BELOW 16 MEGABYTES.
*                  NOTE : VSM TESTS THIS BIT ONLY WHEN THE REGION VALUE
*                  IS LESS THAN 16 MEGABYTES.
*           BIT 2  - ON = VSM IS TO CHECK FOR THE AVAILABILITY OF
*                    THE REQUESTED AMOUNT OF CONTIGUOUS FREE SPACE
*                    ABOVE 16 MEGABYTES.
*                  - OFF = VSM IS NOT TO CHECK FOR THE AVAILABILITY OF
*                    THE REQUESTED AMOUNT OF CONTIGUOUS FREE SPACE
*                    ABOVE 16 MEGABYTES.
*                  NOTE : VSM TESTS THIS BIT ONLY WHEN THE REGION VALUE
*                  IS GREATER THAN 16 MEGABYTES.
*           BIT 3-31 - RESERVED.
*           NOTE : BECAUSE OF COMPATIBILITY CONSIDERATIONS FOR PREVIOUS
*                  MVS/XA RELEASES, THE SETTINGS FOR BITS 1 AND 2 HAVE
*                  OPPOSITE MEANINGS. IN PREVIOUS RELEASES, VSM MADE NO
*                  CHECKS FOR FREE SPACE ABOVE SIXTEEN MEGABYTES. NOW,
*                  IF THE AMOUNT OF CONTIGUOUS FREE SPACE REQUESTED IS
*                  CRITICAL FOR THE STEP TO BE EXECUTED, BIT 2 MUST BE
*                  TURNED ON.
*
* +4  -  2. REGION SIZE REQUESTED.
*
* +8  -  3. GETMAIN LIMIT VALUE BELOW 16 MEGABYTES.
*
* +12 -  4. REGION SIZE BELOW 16 MEGABYTES.
*
* +16 -  5. GETMAIN LIMIT VALUE ABOVE 16 MEGABYTES.
*
* +20 -  6. REGION SIZE ABOVE 16 MEGABYTES.
*
*    NOTE : ON EVERY ENTRY TO IEFUSI, THE LAST FOUR WORDS IN THE VSM
*           PARAMETER LIST ARE SET TO X'FFFFFFFF' (DECIMAL -1).
         SPACE 2
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
         EJECT
        $CEPA
         EJECT
        PRINT  NOGEN
         SPACE 1
        CVT    DSECT=YES,LIST=YES
        IKJTCB LIST=YES
        IEFTCT
LCT      DSECT
        IEFALLCT
         AIF   ('&SYSPARM' NE 'TEST').O2S1
JCT      DSECT
        IEFAJCTB
.O2S1    ANOP
SCT      DSECT
        IEFASCTB
         SPACE 1
        PRINT  GEN
         SPACE 2
*              WORK AREAS.
         SPACE 1
AREA     DSECT - - - - - - - - - - START OF DSECT - - - - - - - - - - -
         SPACE 1
SAVAREA  DS    18F           SAVE AREA.
         SPACE 1
         AIF   ('&SYSPARM' EQ 'TEST').O2S2
WORK     DS    D             WORK.
.O2S2    ANOP
SWTO     DC    F'0'                LENGTH & FLAGS.
         AIF   ('&SYSPARM' EQ 'TEST').O2S3
SMSGTX   DC    CL12'--- IEFUSI ('
SMSGTJ   DC    CL8' ',C'/'
SMSGTN   DC    CL8' ',CL26') - NEW STEP TIME LIMIT : '
SMSGTH   DC    CL2'  ',CL2'H '      HOURS.
SMSGTM   DC    CL2'  ',CL2'M '      MINUTES.
SMSGTS   DC    CL2'  ',CL2'S.'      SECONDS.
         AGO   .O2S4
.O2S3    ANOP
SMSGTXJ  DC    CL23'--- IEFUSI TEST - JCT ('
SMSGJCT  DC    CL9' ',C' '
SMSGJTM  DC    CL7' '
SMSGTXS  DC    CL6' SCT ('
SMSGSCT  DC    CL9' ',C' '
SMSGSTM  DC    CL7' '
.O2S4    ANOP
LWTO     EQU   *-SWTO              WTO LENGTH.
DCRC     DC    XL4'0'              DESC. & ROUTING CODES.
         SPACE 1
AREAL    EQU   (((*-AREA)+7)/8)*8
         SPACE 2
IEFUSI   CSECT                     RESTORE ORIGINAL CSECT.
         EJECT
         USING *,R11               ADDRESSABILITY.
         SPACE 1
         STM   R14,R12,12(R13)     SAVE REGISTERS.
         LA    R11,0(R15)          SET BASE REGISTER.
         L     R2,0(R1)            GET COMMON EXIT PARM. ADDRESS.
         B     START
         SPACE 1
         USING CEPA,R2
         SPACE 1
         DC    CL8'IEFUSI',C'-'    IDENTIFIER.
         DC    CL8'&SYSDATE',C'('  ASSEMBLY DATE.
         DC    CL5'&SYSTIME',C')'  ASSEMBLY TIME.
         SPACE 1
         AIF   ('&SYSPARM' EQ 'TEST').O3S1
* PROCESS :
* ---------
*
*        RE-EVALUATE THE MAXIMUM STEP RUNNING TIME IF NEEDED.
         SPACE 1
START    LR    R8,R1               RETAIN ENTRY R1.
         LA    R6,SIDCPU           SYSTEM-ID'S/FACTOR TO BE APPLIED.
SCNTB    CLC   0(L'CEPASID,R6),CEPASID
         BE    FNDTB
         LA    R6,L'SIDCPU(R6)
         CLI   0(R6),0             END OF TABLE?
         BNE   SCNTB               NO, CONTINUE.
         B     TSTMEM              YES, NOTHING TO DO.
FNDTB    LR    R1,R6               RETAIN IT.
         LA    R1,L'SIDCPU(R1)
         CLI   0(R1),0             END OF TABLE?
         BNE   *-8                 NO, CONTINUE UNTIL REACHED.
         CLC   4(4,R1),4(R6)       YES, ANYTHING TO DO?
         BE    TSTMEM              NO, NOTHING TO DO.
         AGO   .O3S2
.O3S1    ANOP
* PROCESS :
* ---------
*
*        ONLY WHEN PREDEFINED JOB-NAME MATCHES, DISPLAY THE ADDRESSES
*        OF THE JCT AND SCT, THE JOB TIME LIMIT AND THE MAXIMUM STEP
*        RUNNING TIME (SHOULD BE THAT OF THE CURRENT JOB STEP).
         SPACE 1
START    LR    R8,R1               RETAIN ENTRY R1.
         CLC   CEPAJOBN,=CL8'SYSSP1$'   IS IT THE TEST JOB-NAME?
         BNE   TSTMEM              NO, BYPASS.
        GETMAIN R,LV=AREAL,SP=255
         LR    R9,R1               SET BASE REGISTER FOR AREA.
         USING AREA,R9             ADDRESSABILITY.
         XC    SAVAREA(18*4),SAVAREA
         ST    R13,SAVAREA+4       LINK SAVE AREAS.
         ST    R9,8(R13)
         LR    R13,R9              ACTIVE MY SAVE AREA.
.O3S2    L     R3,CVTPTR           CVT.
         USING CVT,R3
         L     R4,CVTTCBP          TCB WORDS.
         DROP  R3
         L     R3,4(R4)            TCB.
         USING TCB,R3
         SR    R4,R4
         ICM   R4,B'0111',TCBTCTB  TCT.
         DROP  R3
         USING SMFTCT,R4
         L     R3,TCTLCTAD         LCT.
         DROP  R4
         USING LCT,R3
         AIF   ('&SYSPARM' EQ 'TEST').O4S1
         ICM   R4,B'1111',LCTSCTAD SCT.
         BZ    TSTMEM              NONE.
         USING SCT,R4
         CLC   SCTSTIME(L'SCTSTIME),=AL3(((24*60)-1)*60*100)
         BNL   TSTMEM              TIME=1440 OR NO TIME SPECIFIED.
         XR    R0,R0
         XR    R1,R1
         ICM   R1,B'0111',SCTSTIME MAX. STEP RUN (IN HUNDRED OF SEC).
         M     R0,4(R6)            APPLY SYSTEM FACTOR.
         D     R0,=F'100'          REDUCE TO HUNDRED OF SEC.
         LTR   R0,R0
         BZ    *+L'*+6
         AH    R1,=H'1'            ROUND UP.
         XR    R0,R0
         ICM   R0,B'0111',=AL3(((24*60)-1)*60*100)
         CLR   R1,R0               HIGHER THAN MAXIMUM?
         BNH   *+L'*+2             NO.
         LR    R1,R0               YES, GET MAXIMUM.
         STCM  R1,B'0111',SCTSTIME SET NEW STEP RUNNING TIME.
         DROP  R3,R4
         CL    R1,=A(@OPSGLV*60*100)    MUST BE SIGNALED?
         BNH   TSTMEM              NO.
         LR    R3,R1               YES, INFORM OPERATOR.
        GETMAIN R,LV=AREAL,SP=255
         LR    R9,R1               SET BASE REGISTER FOR AREA.
         USING AREA,R9             ADDRESSABILITY.
         XC    SAVAREA(18*4),SAVAREA
         ST    R13,SAVAREA+4       LINK SAVE AREAS.
         ST    R9,8(R13)
         LR    R13,R9              ACTIVE MY SAVE AREA.
.O4S1    MVC   SWTO(4),WTOLST      SET MESSAGE AREA.
         MVI   SWTO+1,LWTO
         MVC   DCRC(4),WTOLST+6
         AIF   ('&SYSPARM' EQ 'TEST').O4S2
         MVC   SMSGTX(12),=CL12'--- IEFUSI ('
         MVC   SMSGTJ,CEPAJOBN     JOB-NAME.
         MVI   SMSGTJ+L'SMSGTJ,C'/'
         L     R1,4(R8)
         MVC   SMSGTN(8),0(R1)     STEP-NAME.
         MVC   SMSGTN+L'SMSGTN(26),=CL26') - NEW STEP TIME LIMIT : '
         MVC   SMSGTH(12),=CL12' 0H  0M  0S.'
         XR    R0,R0
         LR    R1,R3
         D     R0,=A(60*100)       COMPUTE SEC./MIN.
         LTR   R0,R0
         BZ    *+L'*+22
         CVD   R0,WORK
         MVC   WORK(4),=XL4'40202021'
         ED    WORK(4),WORK+6
         MVC   SMSGTS(2),WORK+2    SECONDS.
         CL    R1,=F'60'           MORE THAN 1 HOUR?
         BNL   *+L'*+6             YES.
         LR    R0,R1               NO.
         B     STMIN
         XR    R0,R0
         D     R0,=F'60'           COMPUTE MIN./HRS.
         CVD   R1,WORK
         MVC   WORK(4),=XL4'40202021'
         ED    WORK(4),WORK+6
         MVC   SMSGTH(2),WORK+2    HOURS.
         LTR   R0,R0
         BZ    *+L'*+22
STMIN    CVD   R0,WORK
         MVC   WORK(4),=XL4'40202021'
         ED    WORK(4),WORK+6
         MVC   SMSGTM(2),WORK+2    MINUTES.
         AGO   .O4S3
.O4S2    MVC   SMSGTXJ(23),=CL23'--- IEFUSI TEST - JCT ('
         MVC   SMSGTXS(6),=CL6' SCT ('
         UNPK  SMSGJCT(L'SMSGJCT),LCTJCTAD(L'LCTJCTAD+1)
         TR    SMSGJCT(L'SMSGJCT-1),TRTAB-X'F0'
         MVC   SMSGJCT+L'SMSGJCT-1(2),=CL2') '
         ICM   R4,B'1111',LCTJCTAD JCT.
         BNZ   *+L'*+10
         MVC   SMSGJTM(L'SMSGJTM),=CL7'-NONE-,'
         B      *+L'*+16
         USING JCT,R4
         UNPK  SMSGJTM(L'SMSGJTM),JCTJMRTL(L'JCTJMRTL+1)
         TR    SMSGJTM(L'SMSGJTM-1),TRTAB-X'F0'
         MVI   SMSGJTM+L'SMSGJTM-1,C','
         DROP  R4
         UNPK  SMSGSCT(L'SMSGSCT),LCTSCTAD(L'LCTSCTAD+1)
         TR    SMSGSCT(L'SMSGSCT-1),TRTAB-X'F0'
         MVC   SMSGSCT+L'SMSGSCT-1(2),=CL2') '
         ICM   R4,B'1111',LCTSCTAD SCT.
         BNZ   *+L'*+10
         MVC   SMSGSTM(L'SMSGSTM),=CL7'-NONE-.'
         B      *+L'*+16
         USING SCT,R4
         UNPK  SMSGSTM(L'SMSGSTM),SCTSTIME(L'SCTSTIME+1)
         TR    SMSGSTM(L'SMSGSTM-1),TRTAB-X'F0'
         MVI   SMSGSTM+L'SMSGSTM-1,C'.'
         DROP  R3,R4
.O4S3   WTO    MF=(E,SWTO)         SEND MESSAGE TO LOG.
         LR    R1,R9               ADDRESS OF AREA TO BE FREED.
         DROP  R9
         L     R9,4(R13)           PREVIOUS SAVE AREA ADDRESS.
        FREEMAIN R,LV=AREAL,A=(1),SP=255
         LR    R13,R9              RESTORE ORIGINAL SAVE AREA.
         EJECT
* PROCESS :
* ---------
*
*        WHEN THE REGION SIZE REQUESTED INDICATES A VALUE OF EIGHT (8)
*        MEGABYTES OR HIGH AND THE PROGRAM NAME MATCHES ONE OF THOSE
*        DEFINED IN THE INTERNAL TABLE OF 'IEFUSI', THEN THE REGION
*        SIZE FOR BELOW 16 MEGABYTES IS FIXED TO SIX (6) MEGABYTES
*        (THE OTHER VALUES ARE LEFT UNCHANGED : GETMAIN LIMIT VALUE
*        FOR BELOW 16 MEGABYTES, GETMAIN LIMIT VALUE AND REGION SIZE
*        FOR ABOVE 16 MEGABYTES).
         SPACE 1
TSTMEM   L     R3,16(R8)           GET VSM PARAMETER LIST POINTER.
         CLC   4(4,R3),=A(8*1024*1024)  REGION SIZE REQ. = 8MB OR HIGH?
         BL    GOBACK              LOW, LET'S GO.
         L     R4,8(R8)            GET PROGRAM NAME POINTER.
         LM    R5,R7,=A(PGMTB,L'PGMTB,PGMTBE)
LOOP     CLC   0(L'PGMTB,R5),0(R4) IS NAME MATCHING TABLE?
         BE    FOUND               YES.
         BXLE  R5,R6,LOOP          NO, CONTINUE.
         B     GOBACK              NONE, LET'S GO.
FOUND    OI    0(R3),X'80'         IEFUSI SUPPLY REGION LIMIT VALUES.
         MVC   12(4,R3),=A(6*1024*1024) REGION SIZE = 6MB.
         SPACE 1
GOBACK   LM    R14,R12,12(R13)     RESTORE REGISTERS.
         SR    R15,R15             SET CONTINUE PROCESS RETURN CODE.
         BR    R14                 GO BACK.
         EJECT
*        CONSTANTS.
         SPACE 1
WTOLST   WTO   '  ',MF=L,ROUTCDE=11,DESC=7
         AIF   ('&SYSPARM' NE 'TEST').O8S1
TRTAB    DC    CL16'0123456789ABCDEF'
.O8S1    SPACE 2
         LTORG
         EJECT
*        ----------------------
*        PROGRAM'S NAMES TABLE.
*        ----------------------
         SPACE 1
PGMTB    DC    CL8'ASMBLR  '       ASSEMBLER F.
         DC    CL8'IFOX00  '
         DC    CL8'IEUASM  '
         DC    CL8'IEV90   '       ASSEMBLER H.
         DC    CL8'LINKEDIT'       LINKAGE EDITOR.
         DC    CL8'HEWL    '
         DC    CL8'HEWLH096'
         DC    CL8'HEWLF064'
         DC    CL8'IEWL    '
         DC    CL8'IEWLF128'
         DC    CL8'IEWLF440'
         DC    CL8'IEWLF880'
         SPACE 1
PGMTBE   EQU   *-L'PGMTB           LAST PROGRAM NAME TABLE POINTER.
         SPACE 1
         AIF   ('&SYSPARM' EQ 'TEST').O9S1
         EJECT
        $SIDTB TYPE=CPU
.O9S1    SPACE 2
         END
