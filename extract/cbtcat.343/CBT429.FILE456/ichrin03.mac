IN03     TITLE 'RACF - STARTED PROCEDURES TABLE ICHRIN03.'
         PRINT OFF
         MACRO
&NAME    @GEN  &G=NO,&US=SYSPROC,&GR=SYS1,&P=YES
         LCLC  &PN,&UN,&GN,&PF
         AIF   ('&US' EQ '').ERR1
         AIF   ('&US' EQ '&GR').ERR2
         AIF   ('&G' EQ 'YES').A
&PN      SETC  '&NAME'
         AGO   .B
.A       ANOP
&PN      SETC  '*'
.B       AIF   ('&US' NE '@').C
&UN      SETC  '='
         AGO   .D
.C       ANOP
&UN      SETC  '&US'
.D       AIF   ('&GR' NE '').E
&GN      SETC  ' '
         AGO   .G
.E       AIF   ('&GR' NE '@').F
&GN      SETC  '='
         AGO   .G
.F       ANOP
&GN      SETC  '&GR'
.G       AIF   ('&P' NE 'YES').H
&PF      SETC  'PRV'
         AGO   .I
.H       ANOP
&PF      SETC  '0'
.I       DC    CL8'&PN',CL8'&UN',CL8'&GN',AL1(&PF),XL7'0'
         MEXIT
.ERR1    MNOTE 8,' INVALID - USERID NAME MUST BE SPECIFIED ********** '
         MEXIT
.ERR2    MNOTE 8,' INVALID - SAME NAME IN USERID AND GROUP ********** '
         MEND
         PRINT ON
         SPACE 1
ICHRIN03 START 0
         SPACE 1
* PURPOSE :    ASSOCIATE THE NAMES OF STARTED PROCEDURES TO USERIDS
* ---------    AND GROUP NAMES.
*
* DESCRIPTION : THE MODULE SHOULD CONSIST OF A TABLE IN THE FOLLOWING
* ------------- FORMAT :
*        - NUMBER OF ENTRIES IN THE FOLLOWING ARRAY : A HALFWORD OF
*          BINARY DATA, WITH THE HIGH-ORDER BIT TURNED ON TO INDICATE
*          THE NEW FORMAT USED IN RACF VERSION 1 RELEASE 6 OR LATER
*          (USE X'0000' OR X'8000' IF THERE ARE NO ENTRIES).
*        - AN ARRAY : EACH ENTRY CONSIST OF 32-BYTES OF DATA. THE FIRST
*          24 BYTES OF CHARACTER DATA SHOW THE STARTED PROCEDURE NAME
*          AND ITS ASSOCIATED USERID AND GROUP NAME. FORMAT EACH ENTRY
*          AS FOLLOWS :
*             - STARTED PROCEDURE NAME : 8 BYTES OF CHARACTER DATA. THE
*               NAME IS REQUIRED. THE STARTED PROCEDURE NAME MUST BE
*               LEFT JUSTIFIED AND PADDED ON THE RIGHT WITH BLANKS.
*             - USERID : 8 BYTES OF CHARACTER DATA. A USERID IS
*               REQUIRED. THE USERID (OR AN EQUAL SIGN FOR GENERIC
*               ENTRIES) MUST BE LEFT JUSTIFIED AND PADDED ON THE RIGHT
*               WITH BLANKS (THE MAXIMUM LENGTH OF A USERID IS EIGHT
*               CHARACTERS).
*             - GROUP NAME : 8 BYTES OF CHARACTER DATA. THE GROUP NAME
*               IS OPTIONAL. IF A GROUP NAME (OR AN EQUAL SIGN FOR
*               GENERIC ENTRIES) IS USED, IT MUST BE LEFT JUSTIFIED
*               AND PADDED ON THE RIGHT WITH BLANKS. IF A GROUP NAME
*               IS NOT USED, THIS FIELD MUST CONTAIN BLANKS.
*             - FLAGS : 1 BYTE OF BINARY DATA. SETTING BIT 0 ON
*               (X'80') INDICATES THAT THIS ENTRY HAS THE "PRIVILEGED"
*               ATTRIBUTE. THE REMAINING 7 BITS MUST BE ZEROES.
*             - RESERVED : 7 BYTES OF BINARY DATA. THESE 7 BYTES MUST
*               BE BINARY ZEROES.
*
* NOTE : RACF ACCEPTS STARTED PROCEDURES TABLES (INCLUDING THE USE OF
* ------ GENERIC ENTRIES) IN THE FORMAT USED IN RACF RELEASES PRIOR
*        TO RELEASE 6. HOWEVER, YOU CANNOT USE THE OLD FORMAT IF YOU
*        WANT TO SPECIFY THE PRIVILEGED ATTRIBUTE.
         EJECT
* GENERIC ENTRIES : THE STARTED PROCEDURES TABLE CAN CONTAIN ONE
* ----------------- GENERIC ENTRY, INDICATED BY AN ASTERISK (*) IN THE
* PROCEDURE NAME FIELD. THIS ENTRY MUST BE THE LAST ENTRY IN THE TABLE,
* OTHERWISE, IT IS IGNORED. THE CORRESPONDING USERID IN THIS ENTRY CAN
* BE A VALID USERID OR AN EQUAL SIGN (=). THE GROUP NAME SPECIFIED IN
* THE TABLE ENTRY CAN BE EITHER BLANKS, A VALID GROUP NAME, OR AN EQUAL
* SIGN (=).
*        NOTE : YOU CAN USE THE EQUAL SIGN ONLY FOR A GENERIC STARTED
*        PROCEDURES TABLE ENTRY, IT IS INVALID FOR NON-GENERIC ENTRIES.
* WHEN SEARCHING THE TABLE FOR A PROCEDURE NAME MATCH, IF RACF FINDS
* A PROCEDURE NAME OF "*" AS THE LAST ENTRY IN THE TABLE AND THE
* PROCEDURE NAME WAS NOT SPECIFICALLY MATCHED BY ANY OTHER ENTRY IN
* THE TABLE, RACF USES THE "*" ENTRY AS A MATCH FOR THE PROCEDURE NAME.
* IF A USERID IS SPECIFIED FOR THE "*" ENTRY, RACF ASSOCIATES THAT
*        USERID WITH THE STARTED PROCEDURE NAME. IF THE USERID FIELD
*        CONTAINS AN "=", RACF USES THE PROCEDURE NAME THAT WAS MATCHED
*        WITH THE GENERIC ENTRY (*) AS THE USERID.
* IF THE GROUP NAME IS BLANK, THE STARTED PROCEDURE WILL EXECUTE USING
*        THE DEFAULT GROUP IN THE PROFILE RECORD FOR THE SPECIFIED
*        USERID (SPECIFIED ON THE ADDUSER COMMAND). IF THE GROUP NAME
*        FIELD CONTAINS AN "=", RACF USES THE PROCEDURE NAME THAT WAS
*        MATCHED WITH THE GENERIC ENTRY (*) AS THE GROUP NAME.
* IF THE GENERIC ENTRY HAS AN "=" FOR THE USERID (OR GROUP NAME), THE
*        PROCEDURE NAME THAT MATCHES THE EQUAL SIGN MUST BE DEFINED TO
*        RACF AS A USERID (OR GROUP NAME), OTHERWISE THE PROCEDURE RUNS
*        AS AN UNDEFINED RACF USER (USERID = *).
* THE USERID AND THE GROUP NAME CANNOT BOTH CONTAIN VALUES OF "=" IN
* THE "*" PROCEDURE NAME ENTRY OF THE TABLE BECAUSE IT IS NOT POSSIBLE
* TO HAVE A RACF USER AND GROUP WITH THE SAME NAME.
*
* AUTHOR :     RAVARANI N. - MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
PRV      EQU   X'80'               PRIVILEGED ATTRIBUTE
         SPACE 1
         DC    AL2(X'8000'+NENT)   NUMBER OF ENTRIES IN TABLE
         EJECT
ADMPRINT @GEN
APSWPROC @GEN
AUTO     @GEN
CCSP     @GEN
CJSM     @GEN
CM       @GEN
CMIPL    @GEN
CNMPROC  @GEN
CNMPSSI  @GEN
DSPRINT  @GEN
EREP     @GEN
GTF      @GEN
INIT     @GEN
IZAP     @GEN
JES2     @GEN
JES328X  @GEN
KNET     @GEN
MDFWATCH @GEN
MRSD     @GEN
MVSS     @GEN
NET      @GEN
NETVIEW  @GEN
NOTICE   @GEN
OPER     @GEN
RMF      @GEN
R1       @GEN
SAMPLER  @GEN
SCHEDRUN @GEN
SETCLOCK @GEN
SHUTDOWN @GEN
SMIT     @GEN
TPX      @GEN
TSOMON   @GEN
XWTR     @GEN
         SPACE 1
$RV1     @GEN  ,                   RESERVED
$RV2     @GEN  ,                   RESERVED
$RV3     @GEN  ,                   RESERVED
$RV4     @GEN  ,                   RESERVED
         SPACE 1
$LAST    @GEN  G=YES,US=@,GR=,P=NO
NENT     EQU   (*-(ICHRIN03+2))/32
         SPACE 2
         END
