TEST     TITLE 'STCLOK, TIME AND RTIME TEST PROGRAM.'
TEST     START 0
         SPACE 1
        $DEFREG
NRT      EQU   8                   REMAINING TIME CYCLES.
NTT      EQU   9                   CPU + I/O CYCLES.
NS       EQU   100                 NUMBER OF I/O BY CYCLE.
WL       EQU   20                  USED DATA LENGTH.
NSWL     EQU   NS*WL
         EJECT
        $XENT  BASE=R12
         SR    R10,R10
        GETMAIN R,LV=NSWL
         ST    R1,AT
         SPACE 1
         AIF   ('&SYSPARM' NE 'NOGEN').O1S0
        PRINT  NOGEN
.O1S0    EJECT
         LA    R2,1
         LA    R3,NS
CRL      CVD   R2,WORKD
         UNPK  DATA(10),WORKD+2(6)
         OI    DATA+9,C'0'
         CVD   R3,WORKD
         UNPK  DATA+10(10),WORKD+2(6)
         OI    DATA+19,C'0'
        $PUNCH DATA,PL,DDNM
         LTR   R10,R15
         BNZ   ERRU
         LA    R2,1(R2)
         BCT   R3,CRL
        $UCLOSE DDNM
         LA    R8,NRT
BIGLOOP  LA    R9,NTT
         XC    PTR,PTR
         CP    LNCTR,=PL2'+50'
         BNH   DONLY
         BAL   R14,SKPPG
         B     GTCLK
DONLY   $PRINT DOUBLE
         LTR   R10,R15
         BNZ   EXIT
         AP    LNCTR,=PL1'+2'
GTCLK   CALL   STCLOK
LOOP     L     R2,AT
RDL     $READ  DATA,DDNM
         LTR   R10,R15
         BM    EOR
         BNZ   ERRR
         MVC   0(WL,R2),DATA
         LA    R2,WL(R2)
         B     RDL
EOR      SR    R10,R10
        $RCLOSE DDNM
         L     R2,AT
         TM    SWITCH,1
         BO    ALT
        $SORT  (R2),NS,WL,10,10
         B     STD
ALT     $SORT  (R2),NS,WL,0,10
STD      XI    SWITCH,1
         L     R2,AT
         LA    R3,NS
PHL     $PUNCH (R2),PL,DDNM
         LTR   R10,R15
         BNZ   ERRU
         LA    R2,WL(R2)
         BCT   R3,PHL
        $UCLOSE DDNM
        CALL   TIME,(TCPU,TSRB),VL
         MVI   WHAT,0
         BAL   R7,EDIT
         BCT   R9,LOOP
         OC    PTR,PTR
         BZ    NPR
         BAL   R7,EDITF
NPR     CALL   RTIME,(TCPU,TSRB),VL
         MVI   WHAT,1
         BAL   R7,EDIT
         BCT   R8,BIGLOOP
         B     EXIT
         SPACE 1
EDIT     LA    R6,LINE+5
         AL    R6,PTR
         CLI   WHAT,0
         BNE   EDIT1
         MVC   0(3,R6),=CL3'T -'
         B     EDIT2
EDIT1    MVC   0(3,R6),=CL3'R :'
EDIT2    L     R3,TCPU
         BAL   R14,ADJUST
         MVC   5(12,R6),ACC
         L     R3,TSRB
         BAL   R14,ADJUST
         MVC   20(12,R6),ACC
         LA    R6,DSPL+5
         AL    R6,PTR
         MVC   7(2,R6),=CL2'X'''
         UNPK  9(9,R6),TCPU(L'TCPU+1)
         TR    9(8,R6),TRTAB-X'F0'
         MVI   17(R6),C''''
         MVC   22(2,R6),=CL2'X'''
         UNPK  24(9,R6),TSRB(L'TSRB+1)
         TR    24(8,R6),TRTAB-X'F0'
         MVI   32(R6),C''''
         CLI   WHAT,0
         BNE   EDITF
         L     R6,PTR
         CL    R6,=F'70'
         BE    EDITF
         AL    R6,=F'35'
         ST    R6,PTR
         BR    R7
EDITF    CP    LNCTR,=PL2'+50'
         BNH   *+L'*+4
         BAL   R14,SKPPG
        $PRINT LINE
         LTR   R10,R15
         BNZ   EXIT
        $PRINT DSPL
         LTR   R10,R15
         BNZ   EXIT
         AP    LNCTR,=PL1'+2'
         XC    PTR,PTR
         MVC   LINE+1(L'LINE-1),LINE
         MVC   DSPL+1(L'DSPL-1),DSPL
         BR    R7
         SPACE 1
ADJUST   MVC   ACC,=PL12'+0'
         ST    R3,WORK
         SLDL  R2,1
         SR    R2,R2
         SLDL  R2,7
         LR    R1,R2
         SR    R2,R2
         LA    R15,64
ADJUST1  CLR   R1,R15
         BL    ADJUST2
         BE    ADJUST3
         SLDL  R2,4
         BCT   R1,ADJUST1
         B     EXIT
ADJUST2  SRL   R3,4
         LA    R1,1(R1)
         B     ADJUST1
ADJUST3  LTR   R2,R2
         BZ    ADJUST4
         CVD   R2,WORKD
         MVC   ACC(4),WORKD+4
         NI    ACC+3,X'F0'
ADJUST4  LA    R15,HDFCT
         LA    R0,4
ADJUST5  SR    R2,R2
         SLDL  R2,4
         LTR   R2,R2
         BZ    ADJUST7
ADJUST6  AP    ACC+L'ACC-L'HDFCT(L'HDFCT),0(L'HDFCT,R15)
         BCT   R2,ADJUST6
ADJUST7  LA    R15,L'HDFCT(R15)
         BCT   R0,ADJUST5
         NI    ACC+5,X'F0'
         NI    ACC+11,X'0F'
         OC    ACC+5(1),ACC+11
         AP    ACC(6),=PL1'+5'
         MVC   WORKD,=PL8'+0'
         MVO   WORKD,ACC(5)
         MVC   ACC,=XL12'40402020202021206B202020'
         ED    ACC,WORKD+3
         TM    WORK,X'80'
         BZR   R14
         MVI   ACC+1,C'-'
         BR    R14
         SPACE 1
SKPPG    ST    R14,SVR14
         AP    PGCTR,=PL1'+1'
         MVC   TITL1P,=XL6'402020202120'
         ED    TITL1P,PGCTR
        $PRINT TITL1
         LTR   R10,R15
         BNZ   EXIT
        $PRINT TITL2
         LTR   R10,R15
         BNZ   EXIT
        $PRINT DOUBLE
         LTR   R10,R15
         BNZ   EXIT
         ZAP   LNCTR,=PL1'+2'
         L     R14,SVR14
         BR    R14
         SPACE 1
ERRR    $RCLOSE DDNM
         B     EXIT
ERRU    $UCLOSE DDNM
EXIT    $PCLOSE
         SPACE 1
         AIF   ('&SYSPARM' NE 'NOGEN').O2S0
        PRINT  GEN
.O2S0    SPACE 1
        $XRET  CC=(R10)
         EJECT
*        HEXADECIMAL TO DECIMAL FRACTION CONVERSION TABLE.
*        -------------------------------------------------
         SPACE 1
HDFCT    DC    PL9'+0625000000000000'
         DC    PL9'+0039062500000000'
         DC    PL9'+0002441406250000'
         DC    PL9'+0000152587890625'
         SPACE 1
*        CONSTANTS AND WORK AREAS.
*        -------------------------
         SPACE 1
WORKD    DC    D'0'
WORK     DC    F'0'
SVR14    DC    F'0'
AT       DC    A(*-*)
PTR      DC    F'0'
PL       DC    A(WL)
TCPU     DC    F'0'
TSRB     DC    F'0'
ACC      DC    PL12'+0'
DDNM     DC    CL8'SYSTEST'
TRTAB    DC    CL16'0123456789ABCDEF'
LNCTR    DC    PL2'+99'
PGCTR    DC    PL3'+0'
         SPACE 1
SWITCH   DC    XL1'0'
WHAT     DC    XL1'0'
         DC    AL1(LTITL1)
TITL1    DC    C'1          STCLOK, TIME AND RTIME TEST.'
         DC    CL58' ',C'PAGE'
TITL1P   DC    CL6' ',C'.'
LTITL1   EQU   *-TITL1
         DC    AL1(L'TITL2)
TITL2    DC    C'           ----------------------------'
         DC    AL1(L'DOUBLE)
DOUBLE   DC    C'0 '
         DC    AL1(L'DATA)
DATA     DC    CL80' '
         DC    AL1(L'LINE)
LINE     DC    CL121' '
         DC    AL1(L'DSPL)
DSPL     DC    CL121' '
         EJECT
        LTORG
         SPACE 2
         END
