RNMP     TITLE 'RENAME PARTITIONED DATA-SET MEMBERS.'
RENAMP   START 0
         SPACE 1
* PURPOSE :    RENAME ONE OR MORE MEMBERS OF A PARTITIONED DATA-SET.
* ---------    AT EXIT : REGISTER 15 = 0 - RENAME(S) DONE,
*                                      16 - ERROR DETECTED.
*
* FILE NAME :  DD-NAME 'SYSUT0'  - INPUT DATA-SET (DSORG=PO).
* -----------
*
* TSO ACCESS : MAY BE USED IN A CLIST AS DESCRIBED BELOW.
* ------------
*              ALLOC F(SYSUT0) DA(...DATA-SET NAME...)
*              RENAMP &LIST ---> '... PAIR OF NAMES ...' WHERE EACH
*                                PAIR IS THE OLD AND NEW MEMBER NAME
*              WHEN SYSRC(NE 0) ---> ERROR DETECTED
*              ---> MEMBERS RENAMED
         SPACE 1
RENAMP  AMODE  24
RENAMP  RMODE  24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12
         LR    R8,R1               SAVE PARM. FIELD ADDRESS
         XR    R10,R10             RC = 0 -----------------------------
        $TEW$EC NOTTSO=CALLM,NOTCMD=CALLM,MSG=PUTMSG,ERR=SETRC
        $TEW$PP PARM=PARMLST
         TM    LMN+6,X'80'         LIST SPECIFIED?
         BZ    SETRC               NO
         L     R4,LMN              GET POINTER TO LIST
         LH    R3,LMN+4            GET LENGTH
         LTR   R3,R3
         BNP   SETRC
         STH   R3,HW
         B     LOOK
         DROP  R2
         SPACE 1
CALLM    L     R1,0(R8)            PARM. LIST
         LH    R2,0(R1)            PARM. FIELD LENGTH
         LTR   R2,R2               ANY PARM?
         BNP   SETRC               NO, GO BACK
         STH   R2,HW
         LA    R4,2(R1)            START OF PARM. FIELD
LOOK     LR    R2,R4
        OPEN   (UT0,OUTPUT)
         TM    UT0+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    SETRC
         SPACE 1
         LA    R3,1                PARM. POSITION COUNTER
INIZMEMB LA    R5,MEMB
         XR    R2,R2               MEMBER NAME CHARACTERS COUNTER
         MVC   0(8,R5),=CL8' '     SET BLANKS MEMBER NAME
         CH    R3,HW               END OF PARM. FIELD?
         BH    FISK                YES
         CLI   0(R4),C'Z'
         BH    SKIPTHIS
INTNAME  CH    R3,HW
         BH    FISK
         CLI   0(R4),C'$'
         BE    OKTHIS
         CLI   0(R4),C'@'
         BE    OKTHIS
         CLI   0(R4),C'#'
         BE    OKTHIS
         CLI   0(R4),C'A'
         BL    SKIPTHIS
OKTHIS   MVC   0(1,R5),0(R4)       MOVE NAME'S CHARACTER
         LA    R2,1(R2)
         LA    R3,1(R3)
         LA    R4,1(R4)
         LA    R5,1(R5)
         CH    R2,=H'8'
         BNL   SCRAMEMB
         B     INTNAME
FISK     CH    R2,=H'0'
         BE    EODAD
         B     SCRAMEMB
SKIPTHIS LA    R3,1(R3)
         LA    R4,1(R4)
         CH    R2,=H'0'
         BE    INIZMEMB
SCRAMEMB CLC   OLDNAME(8),=CL8' '
         BNE   *+L'*+10
         MVC   OLDNAME,MEMB
         B     INIZMEMB
         CLC   NEWNAME(8),=CL8' '
         BNE   QUIT
         MVC   NEWNAME(8),MEMB
        STOW   UT0,OLDNAME,C
         LTR   R15,R15
         BZ    *+L'*+2
         LR    R10,R15
         MVC   OLDNAME,=CL8' '
         MVC   NEWNAME,=CL8' '
         B     INIZMEMB
         SPACE 1
QUIT     LA    R10,*
EODAD   CLOSE  (UT0)
         LTR   R10,R10
         BZ    LEAVE
SETRC    LA    R10,16              RC = 16 ----------------------------
LEAVE   IKJRLSA #TSANSW
        $XRET  CC=(R10)
         EJECT
*------- CONSTANTS AND WORK-AREAS.
         SPACE 1
        $TEW$WA
         SPACE 1
MEMB     DC    CL8' '              NAME WORK AREA
OLDNAME  DC    CL8' '              NAME OF MEMBER TO CHANGE
NEWNAME  DC    CL8' '              NEW MEMBER NAME
HW       DC    H'0'
         SPACE 1
        LTORG
         SPACE 1
UT0     DCB    DSORG=PO,DDNAME=SYSUT0,MACRF=W
         EJECT
PARMLST IKJPARM
LMN     IKJPOSIT QSTRING,PROMPT='MEMBER-NAMES'
        IKJENDP
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        DCBD   DSORG=PO,DEVD=DA
        PRINT  GEN
         SPACE 1
         END
