AU11     TITLE '-- TSO-CP AUT10 - ROUTINE AUT11 --'
AUT11    START 0
         SPACE 1
**********************************************************************
*        THIS SUBROUTINE IS CALLED BY PROGRAM 'AUT10'.               *
*        I ACCESS THE FILE BY PROGRAMMER NUMBER.                     *
*    NB. I AM NOT ABLE TO MODIFY THE FILE. I DISPLAY ONLY INFO.      *
*                                             M.SACRE  01 OCTOBRE 84 *
*                                        P.A.MOINIL  21 SEPTEMBRE 89 *
**********************************************************************
*        CALL AUT11 OR CALL AUT11,(PARM),VL                          *
*        FUNCTION : SUBROUTINE TO DISPLAY PROGRAMMER'S INFO'S.       *
*        AT ENTRY : R1  = PARM. ADDRESS (WHEN CALLED WITH PARM.)     *
*                         PARM. --->   A(ACCN) WHICH IS THE ADDRESS  *
*                                      OF AN 8-CHAR. FIELD,          *
*                                      FORMAT : CL8'AAAAPPPP'.       *
*              OR : R1 = 0 TO SAY NONE PASSED (WHEN WITHOUT PARM.)   *
*         AT EXIT : R15 = 0 - NORMAL COMPLETION                      *
*                         4 - END REQUESTED                          *
*                         8 - SEVERE ERROR                           *
*                   WHEN PARM. GIVEN AND R15 = 0, THEN               *
*                   R1  = ADDRESS OF REPSONSE FIELD                  *
*                         -1 WHEN PROGRAMMER NOT FOUND               *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R10
         SPACE 1
         MVI   ECRAPM,C' '
         MVC   ECRAPM+1(L'ECRAPM-1),ECRAPM
         LTR   R1,R1                   IS THERE A PARAMETER PASSED?
         BZ    ASKPGM                  IF NOT, ASK PGM NBR
*------- ICI, ON SAIT QU'IL Y A LE PARAMETRE
         L     R2,0(R1)                LOAD ADDRESS OF PARM PASSED
         MVC   RESPONSE(4),4(R2)       MOVE PGM NBR IN RESPONSE
         MVC   SCRFILL(LUSF),USF
         B     OK                      AND BYPASS REQUEST
ASKPGM   MVC   SCRFILL(LPNF),PNF
ASKDEM   LA    R1,PECRAP               ASK PGRMER NUMBER
         L     R15,=V(AUT99)
         BALR  R14,R15
TESTASK  LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   LEAVE                   NO, END (ONLY 4)
         MVI   ECRAPM,C' '
         MVC   ECRAPM+1(L'ECRAPM-1),ECRAPM
         L     R15,=V(AUT99R)          GET RESPONSE
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    QUIT                    IF YES, ESCAPE FROM SUBR.
         LTR   R1,R1                   NO TEXT ?
         BZ    QUIT                    YES
         MVC   RESPONSE(4),=CL7' '
         BCT   R1,*+L'*+6
         MVC   RESPONSE(*-*),3(R15)
         EX    R1,*-6
         LA    R0,4
         LA    R1,RESPONSE+4
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRRESP
         CLI   0(R1),C'9'
         BH    WRRESP
         BCT   R0,*-18
*------- ICI, ON SAIT QUE L'ENTREE EST NUMERIQUE
*        RECHERCHE DU NUMERO DE PROGR. DANS LE FICHIER D'ACCOUNTING
OK       MVC   PGMCHAR(L'PGMCHAR),RESPONSE
         PACK  DSD,RESPONSE(4)         PACK PROGRAMMER NBR
         CVB   R5,DSD
         AH    R5,=Y(NAUTNO+1)         ADD NO. OF AUT. FOR ADDRESS
         STH   R5,ARGUMPRG             STORE IN ARGUMENT
        CALL   ZREAD,(RETCD,ARGUMPRG,AREAP),VL  READ RECORD
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TROUVE                  IF ZERO, PROGR. FOUND
         CH    R15,=H'15'              IF 15, ARGUM NOT FOUND
         BE    NOTFND                  IF EQUAL, NOT FOUND
         LR    R0,R15
         XR    R1,R1
         LM    R2,R3,NZRD
         L     R15,=V(AUT97)
         BALR  R14,R15
         LA    R15,8                   SEVERE ERROR
         B     LEAVE
WRRESP   CLC   RESPONSE(4),=CL7' '     IS RESPONSE BLANK ?
         BE    QUIT                    IF YES, ESCAPE FROM SUBR.
         MVC   ECRAPM(L'MSGPN1),MSGPN1 PGRMER NUMBER NOT NUMERIC
         B     ASKDEM
QUIT     XR    R15,R15                 ESCAPE
LEAVE   $XRET  CC=(R15)
NOTFND   CLI   SCRFILL,C'U'            USERID CALL ?
         BNE   *+L'*+8                 NO
         XR    R1,R1
         BCTR  R1,0
         B     QUIT
         MVC   ECRAPM(L'MSGPN2),MSGPN2 PGRMER NUMBER NOT FOUND
         B     ASKDEM
         SPACE 1
*--------------------------------------------------------------------*
*        NOUS ALLONS AFFICHER LES RENSEIGNEMENTS QUI CONCERNENT      *
*        LE PROGRAMMEUR.                                             *
*--------------------------------------------------------------------*
         SPACE 1
TROUVE   LA    R3,AREAP                SET ADRESSABILITY OF DSECT
         USING PRGDSECT,R3
         LH    R1,PRGPNO               LOAD PROGRAMMER NUMBER
         LTR   R1,R1                   TEST SIGN
         BNM   *+L'*+4                 IF POSITIVE, SKIP NEXT
*------- ICI LE FLAG EST NEGATIF. ON DOIT METTRE I DANS LE CODE
*        AVANT DE FAIRE LE DISPLAY.
         MVI   SCRSI,C'I'              MOVE I IN CODE
         MVC   SCRPGM,PGMCHAR          MOVE PGM NBR FOR DISPLAY
         MVC   SCRNOM,PRGPNME          MOVE NAME
         MVC   SCRMAT,PRGMNO           MOVE MATRICULE
         MVC   SCRBOX,PRGBXNO          MOVE BOX NUMBER
         MVC   SCRBLD,PRGBDNO          MOVE BUILDING
         MVC   SCRTEL,PRGPHNO          MOVE PHONE NUMBER
         MVC   SCRSERV,PRGSVNO         MOVE SERVICE
         DROP  R3
         LA    R1,PECRAN               DISPLAY
         L     R15,=V(AUT99)
         BALR  R14,R15
         CLI   SCRFILL,C'U'            USERID CALL ?
         BNE   TESTASK                 NO
         LTR   R15,R15
         BNZ   LEAVE
         MVC   RESPONSE,=CL7' '
         L     R15,=V(AUT99R)          GET RESPONSE
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    SETARSP                 IF YES, ESCAPE FROM SUBR.
         LTR   R1,R1                   NO TEXT ?
         BZ    SETARSP                 YES
         BCT   R1,*+L'*+6
         MVC   RESPONSE(*-*),3(R15)
         EX    R1,*-6
SETARSP  LA    R1,RESPONSE
         L     R2,4(R13)
         ST    R1,24(R2)               PASS ADDRESS OF RESPONSE
         XR    R15,R15
         B     LEAVE
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
DSD      DC    D'0'
PECRAP   DC    A(LECRAP,ECRAP)
PECRAN   DC    A(LECRAN,ECRAN)
RETCD    DC    F'0'          RETURN CODE
NZRD     DC    0F'0',CL8'ZREAD'
PGMCHAR  DC    CL4' '        PROGR. NBR IN CHAR:
AREAP    DC    (PRGLGTH)X'0' RECORD PROGRAMMER (SEE PRGDSECT)
ARGUMPRG DC    H'0'          PROGR NBR TO POINT IN FILE
RESPONSE DC    CL7' '
         SPACE 1
*--------------------------------------------------------------------*
*        MESSAGES D'ERREURS                                          *
*--------------------------------------------------------------------*
         SPACE 1
MSGPN1   DC    C'Programmer Number NOT Numeric or NOT 4 digits.'
MSGPN2   DC    C'Programmer NOT FOUND in File.'
         SPACE 1
*--------------------------------------------------------------------*
*        VARIABLE SCREEN DATA                                        *
*--------------------------------------------------------------------*
         SPACE 1
PNF     $FS    TEXT='Number (4 numerics)',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='<   ',MF=L
LPNF     EQU   *-PNF
USF     $FS    TEXT='USERID (7 char.max)',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
LUSF     EQU   *-USF         MUST MATCH VALUE OF 'LPNF'
         EJECT
*--------------------------------------------------------------------*
*        ECRANS D'OUTPUT                                             *
*--------------------------------------------------------------------*
         SPACE 1
ECRAP   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='PROGRAMMER    D I S P L A Y',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='===========================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(5,10),SF=(PROT),MF=L
        $FS    TEXT='Enter Programmer Number (4 numerics)',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(6,10),SF=(PROT),TEXT='or you may Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='to escape or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(23,1),SF=(PROT,INT),MF=L
ECRAPM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAP   EQU   *-ECRAP
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='PROGRAMMER    D I S P L A Y ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='===========================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(5,5),SF=(PROT),MF=L
        $FS    TEXT='Programmer is invalidated if I --->',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRSI   $FS    TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(6,9),SF=(PROT),TEXT='Programmer Number :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRPGM  $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(7,11),SF=(PROT),TEXT='Programmer Name :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRNOM  $FS    TEXT=(' ',40),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(9,17),SF=(PROT),TEXT='Matricola :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRMAT  $FS    TEXT=(' ',6),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(11,8),SF=(PROT),TEXT='Listing Box Number :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRBOX  $FS    TEXT=(' ',3),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(13,11),SF=(PROT),TEXT='Building Number :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRBLD  $FS    TEXT=(' ',6),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(15,14),SF=(PROT),TEXT='Phone Number :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRTEL  $FS    TEXT=(' ',9),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(17,19),SF=(PROT),TEXT='Service :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRSERV $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(22,1),SF=(PROT),MF=L
        $FS    TEXT='Enter another Programmer ',MF=L
SCRFILL  DC    (LPNF)X'0'
        $FS    SF=(PROT),MF=L
        $FS    SBA=(23,1),SF=(PROT),TEXT='or you may Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='to escape or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAN   EQU   *-ECRAN
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         SPACE 1
*--------------------------------------------------------------------*
*        DUMMY SECTIONS                                              *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT GEN
        $AIRD  PRI=NO,SEC=YES,R=NO
         SPACE 2
         END
