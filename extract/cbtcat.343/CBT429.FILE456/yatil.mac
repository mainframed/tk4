YATIL    TITLE 'JUST TO KNOW IF A MODULE EXIST IN A PARTITIONED.'
YATIL    START 0
         SPACE 1
* PURPOSE :    LOOK INTO A PARTITIONED DIRECTORY TO FIND A GIVEN
* ---------    MODULE NAME EXISTANCE.
*              AT EXIT : REGISTER 15 = 0 - MODULE FOUND,
*                                      4 - MODULE NOT FOUND,
*                                      8 - NO MODULE NAME SUPPLIED OR
*                                          A PROCESSING ERROR OCCURED.
*
* FILE NAME :  DD-NAME 'SYSUSER'  - INPUT DATA-SET (DSORG=PO).
* -----------
*
* TSO ACCESS : MAY BE USED IN A CLIST AS DESCRIBED BELOW.
* ------------
*              ALLOC F(SYSUSER) DA(...DATA-SET NAME...)
*              YATIL &MEMB
*              WHEN SYSRC(EQ 0) ---> MEMBER EXIST
*              WHEN SYSRC(GE 4) ---> NOT FOUND OR ERROR
         SPACE 1
YATIL   AMODE  24
YATIL   RMODE  24
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=WKLEN,TYPE=RENT
         LR    R8,R1               SAVE PARM. FIELD ADDRESS
         XR    R10,R10             SET C.C. = 0.
         LR    R9,R13              SET WORK-AREAS ADDRESSABILITY
         USING WKAREA,R9
         MVC   MODULE,=CL8' '
        $TEW$EC REG=R8,NOTTSO=CALLM,NOTCMD=CALLM,MSG=PUTMSG,ERR=RET8
        $TEW$PP PARM=PARMLST,REG=R4
         TM    MDL+6,X'80'         MODULE SPECIFIED ?
         BZ    NOMOD               NO
         L     R2,MDL              GET POINTER TO MODULE
         LH    R3,MDL+4            GET LENGTH
         LTR   R3,R3
         BNP   NOMOD
         BCT   R3,*+L'*+6          -1
         OC    MODULE(*-*),0(R2)   <<EXECUTED>>
         EX    R3,*-6              MOVE NAME (WITH UPPERCASE)
         DROP  R4
         BAS   R2,NOMOD+L'NOMOD
NOMOD    XR    R2,R2
        IKJRLSA #TSANSW
         LTR   R2,R2
         BNZ   LOOK
         B     RET8
         SPACE 1
CALLM    L     R2,0(R8)            GET ADDRESS OF PARAMETER
         LH    R3,0(R2)            R3 = PARM LENGTH
         LTR   R3,R3               ANY PARM ?
         BNP   RET8                NO, GO BACK
         CH    R3,=H'8'            OK ?
         BL    *+L'*+4             IF OK, LEAVE
         LA    R3,8                USE ONLY 8
         BCT   R3,*+L'*+6          -1
         OC    MODULE(*-*),2(R2)   <<EXECUTED>>
         EX    R3,*-6              MOVE NAME (WITH UPPERCASE)
         SPACE 1
LOOK     MVC   OPNSR(OPNSKL),OPNSK
         MVC   DCBSR(DCBSKL),DCBSK
        OPEN   (DCBSR,(INPUT)),MF=(E,OPNSR) OPEN
         TM    DCBSR+(DCBOFLGS-IHADCB),DCBOFOPN
         BO    *+L'*+8
         LA    R1,OMSERR           OPEN ERROR
         B     SNDMS
         MVC   BLIST(4),=AL2(1,12) FF,LL
         XC    WKMS,WKMS
        BLDL   DCBSR,BLIST         BLDL
         STM   R15,R0,BRC          SAVE REGISTERS 15-0
         MVC   CLSSR(CLSSKL),CLSSK
        CLOSE  DCBSR,MF=(E,CLSSR)  CLOSE
         LM    R15,R0,BRC          RESTORE REGISTERS 15-0
         LTR   R15,R15             OK?
         BZ    RET0                ALL CORRECT
         CH    R15,=H'4'           IS IT 4 ?
         BE    RET4                YES
         LA    R1,WKMS
         MVC   0(2,R1),=AL2(2+L'DRCDS1+L'BMSERR)
         MVC   2(L'DRCDS1,R1),DRCDS1
         MVC   4(L'BMSERR,R1),BMSERR
         CVD   R0,DBLWD
         MVC   2+L'DRCDS1+L'BMSERR(4,R1),=XL4'40202120'
         ED    2+L'DRCDS1+L'BMSERR(4,R1),DBLWD+L'DBLWD-2
         MVC   6+L'DRCDS1+L'BMSERR(L'DRCDS2),DRCDS2
SNDMS    BAS   R14,PUTMSG
RET8     LA    R10,4(R10)
RET4     LA    R10,4(R10)
RET0    $XRET  CC=(R10),LV=WKLEN,TYPE=RENT
         EJECT
*------- SKELETONS AND CONSTANTS
         SPACE 1
        PRINT  NOGEN
OPNSK   OPEN   (*-*,(INPUT)),MF=L
OPNSKL   EQU   *-OPNSK
CLSSK   CLOSE  *-*,MF=L
CLSSKL   EQU   *-CLSSK
DCBSK   DCB    DDNAME=SYSUSER,MACRF=R,DSORG=PO
DCBSKL   EQU   *-DCBSK
         SPACE 1
        PRINT  GEN
OMSERR  WTO    ' -> BLDL : "SYSUSER" OPEN ERROR',                      X
               ROUTCDE=11,DESC=7,MF=L
DRCDS1   EQU   OMSERR+2,2
DRCDS2   EQU   *-4,4
BMSERR   DC    C' -> BLDL ERROR (RC=8) - REASON CODE :'
         SPACE 1
        LTORG
         SPACE 1
        PRINT  NOGEN
PARMLST IKJPARM
MDL     IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM
        IKJENDP
        PRINT  GEN
         EJECT
*------- WORK-AREAS DESCRIPTION
         SPACE 1
WKAREA   DSECT
         DS    18F                 STANDARD SAVE AREA
DBLWD    DS    D
        $TEW$WA
BRC      DS    2F                  BLDL CODES R15-R0
BLIST    DS    2H                  FF,LL
MODULE   DS    CL8                 NAME OF MODULE TO CHECK
WKMS     DS    XL100
OPNSR    DS    0F,(OPNSKL)X
CLSSR    DS    0F,(CLSSKL)X
DCBSR    DS    0F,(DCBSKL)X
         SPACE 1
WKLEN    EQU   (((*-WKAREA)+7)/8)*8
         SPACE 1
        PRINT  NOGEN
        $TEW$DS
        DCBD   DSORG=PO,DEVD=DA
        PRINT  GEN
         SPACE 1
         END
