HLPL     TITLE 'H E L P L I S T   -   TSO HELP DATA-SET LISTER.'
        PRINT  OFF
         MACRO
&LABEL  @T     &TNR,&TCC,&TXT
.*
.* MACRO : INTERNAL USE TO GENERATE 'MANUAL' TABLE.
.* ------- ARGUMENTS : TNR - PRINT LINE REPEAT NUMBER (DEFAULT = 1).
.*                     TCC - ASA CARRIAGE CONTROL CHARACTER (DEFAULT
.*                           IS SINGLE SPACE).
.*                     TXT - TEXT (DEFAULT IS A BLANK LINE).
.*
         LCLC  &N,&C
&N       SETC  '1'
&C       SETC  ' '
         AIF   (T'&TNR EQ 'O').L01
&N       SETC  '&TNR'
.L01     AIF   (T'&TCC EQ 'O').L02
&C       SETC  '&TCC'
.L02     AIF   (T'&TXT EQ 'O').L03
&LABEL   DC    AL1(&N,L'T&SYSNDX),CL1'&C'
T&SYSNDX DC    C&TXT
         MEXIT
.L03     ANOP
&LABEL   DC    AL1(&N,0),CL1'&C'
         MEND
        PRINT  ON
         SPACE 1
HELPLIST START 0
         SPACE 1
************************************************************
*                                                          *
*              'HELPLIST' PROGRAM AT J.R.C.                *
*                                                          *
************************************************************
         SPACE 1
* PROGRAM :    HELPLIST.
* ORIGIN :     EXTRACTED FROM CBT TAPE 86 FEB., FILE 313.
* AUTHOR :     UNKNOWN - FROM TOM GEE.
* MODIFIOR :   BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION : FEDERAL POWER COMMISSION.
*        DATE-UPDATED IN SOURCE LIBRARY : JANUARY 8 1974.
*        REMARKS : DATE ADDED TO HEADING.
*              SUBCOMMAND NAMES PREFIXED WITH 'SUBCOMMAND - '.
*              INDENTATION CHANGED. CHECK FOR )X SYNTAX ADDED.
*
* MODIFIOR :   JERRY PITTENGER, PLANNING RESEARCH CORPORATION.
*        INSTALLATION : OFFICE OF MANAGEMENT AND BUDGET (OMB).
*        DATE-UPDATED IN SOURCE LIBRARY : AUGUST 17 1977.
*        REMARKS : DATE REMOVED FROM HEADING. MULTIPLE HEADING LINES.
*              CHANGE FROM )O OR )X OR )F HIGHLIGHTED BETTER.
*              ALLOW NO ALIAS TO PRINT BY USING BLDL AGAINST
*              THE SYSHELP LIBRARY/LIBRARIES DIRECTORY. INSERTED
*              SUBCOMMANDS ON SEPARATE PAGES WITH THE SUBCOMMAND
*              NAME IN THE HEADER BLOCK.
*
* MODIFIOR :   MOINIL P.A., J.R.C. COMPUTING CENTRE.
*        INSTALLATION : ISPRA ESTABLISHMENT, 21020 ISPRA (VA), ITALY.
*        DATE-UPDATED IN SOURCE LIBRARY : DECEMBER 2 1986.
*        REMARKS : ALL ADDS ARE DOCUMENTED ON NEXT PAGE (NEW ADDS).
         SPACE 2
* JCL :  //LIST    EXEC PGM=HELPLIST,PARM='...'
*        //SYSHELP   DD DSN=...HELP DATA-SET...,DISP=SHR
*        //             ...CONCATENATED HELP DATA-SET IF NEEDED...
*        //SYSINDEX  DD ...INDEX DATA-SET OUTPUT IF REQUESTED...
*        //SYSPRINT  DD ...LIST DATA-SET OUTPUT...
*        //SYSDIAG   DD ...LIST INPUT IF DESIRED...
*        //SYSIN     DD ...INPUT DATA-SET...
         EJECT
* NEW ADDS : - ADD CHECK FOR )M MESSAGES HIGHLIGHTED.
*            - DATE RESTORED IN HEADING. CONTROL CARDS VARIABLE
*              CODING WITH MULTIPLE MEMBER NAMES (ALIAS NAMES ARE
*              LISTED AS COMMANDS UNLESS IMMEDIATELY FOLLOWED BY
*              '/A' TO PRINT AS ALIAS OF NEXT) AND P=... KEYWORDS
*              TO SET SPECIFIC PAGE NUMBERS (MAY BE 1 UP TO 99999).
*            - ADD *HEAD= (COL.1-6) CONTROL CARDS TO SUPPLY LIST
*              HEADERS (50 CHARACTERS MAXIMUM).
*            - ADD *SKEY= (COL.1-6) CONTROL CARDS TO SELECT KEYWORDS
*              MATCHES INTO HEADERS (*HEAD= CONTROL CARD) AND LIST
*              ONLY THESE COMMANDS SUBGROUPS.
*            - ADD PARM= IN JCL EXEC CARD TO OPTIONALLY SPECIFY :
*                L=... SET THE NUMBER OF LINES PER PAGE (DEFAULT IS
*                      60, MAY BE 20 UP TO 999),
*                I     REQUEST GENERAL INDEX GENERATION (SYSINDEX).
*                M     GENERATE MANUAL HEADER/TRAILER PAGES.
*            - ADD AUTOMATIC USE OF THE HELP MEMBER 'COMMANDS' (SEE
*              NOTE) TO GENERATE :
*              1. A COMPLETE HELP LIST WHEN THE GENERAL INDEX (PARM=I)
*                 IS REQUESTED AND THE SYSIN DD-CARD IS OMITED OR
*                 DUMMY, OR THE SYSIN FILE CONTAINS ONLY A *HEAD=
*                 CONTROL CARD AND/OR *SKEY= CONTROL CARDS (SELECTION
*                 OF SUBGROUPS IN 'COMMANDS' MEMBER).
*              2. A LIST OF THE HELP 'COMMANDS' MEMBER ONLY WHEN THE
*                 GENERAL INDEX IS NOT REQUESTED AND THE SYSIN DD-CARD
*                 IS OMITED OR DUMMY, OR THE SYSIN FILE CONTAINS ONLY
*                 A *HEAD= CONTROL CARD (ANY *SKEY= CONTROL CARDS
*                 HAVE NO EFFECT IN THIS CASE).
*            - ADD THE LIST OF INPUT IF DD-CARD SYSDIAG IS PRESENT.
*
* NOTE - IT IS ASSUMED, AND THUS MANDATORY, THAT THE 'COMMANDS' MEMBER
*        IN THE HELP DATA-SET FOLLOWS THE RULES LISTED BELOW :
*        1. ALL CARDS NOT COMMANDS SUBGROUP TITLES OR COMMANDS NAMES
*           MUST HAVE COLUMNS 1 UP TO 8 BLANKS (THIS ALLOWS THEM TO
*           BE HANDLED AS NOT OPERATIVE CARDS).
*        2. A COMMANDS SUBGROUP TITLE MUST START BETWEEN CARDS COLUMNS
*           1 UP TO 8 OF THE CARD, AND MUST BE FOLLOWED AT LEAST BY
*           ONE NOT OPERATIVE CARD.
*        3. A COMMAND NAME OF A SUBGROUP MUST START IN CARD COLUMN 1,
*           AND HIS DESCRIPTION MUST NOT START BEFORE CARD COLUMN 10
*           AND MUST BE COMPLETE INTO THE CARD COLUMN 72 (DO NOT
*           EXTEND THE DESCRIPTION IN THE FOLLOWING CARD NOR INSERT
*           NOT OPERATIVE CARDS BETWEEN COMMAND NAME CARDS). THEN, AT
*           THE END OF THE COMMAND NAME CARDS, AND BEFORE THE NEXT
*           COMMANDS SUBGROUP TITLE CARD, A NOT OPERATIVE CARD MUST
*           BE PRESENT.
         EJECT
        $DEFREG
*        CURRENT REGISTER USAGE :
*        ------------------------
*        R0-R2   = WORK REGISTERS
*        R3      = CURRENT HELP RECORD POINTER
*        R4      = HELP RECORD LENGTH
*        R5      = HELP BLOCKSIZE
*        R6      = CURRENT CONTROL CARD POINTER
*        R7      = I/O AREA BASE
*        R8      = LOCAL LINKAGE REGISTER
*        R9      = LOCAL ROUTINES BASE REGISTER
*        R10     = EXIT CC
*        R11     = PROGRAM BASE
*        R12     = 2ND PROGRAM BASE
*        R13     = SAVE AREA POINTER
*        R14-R15 = WORK REGISTERS
         SPACE 1
*        MISCELLANEOUS DEFINITIONS.
         SPACE 1
@OFF     EQU   X'0F'               SET SWITCH TO NOP
@ON      EQU   X'F0'               SET SWITCH TO BRANCH
NCP      EQU   300                 NUMBER OF CARDS IMAGE IN INT. POOL
         EJECT
        $XENT  BASE=(R11,R12)
         XR    R7,R7
         L     R2,0(,R1)           REG 2 ---> PARM
         LH    R1,0(,R2)           R3 = LENGTH OF PARM
         LTR   R1,R1               PARM?
         BNP   NOPARM              NO
PRMLOOP  CLI   2(R2),C','          PARMS SEPARATOR ?
         BE    PRMNXT              YES
         CLI   2(R2),C'M'          IS HEADER/TRAILER REQUEST ?
         BE    PRMHT               YES
         CLI   2(R2),C'I'          IS INDEX REQUEST ?
         BE    PRMIX               YES
         CLI   2(R2),C'L'          IS SET LINES/PAGE ?
         BNE   PRMERR              NO
         LR    R3,R2
         LA    R2,1(R2)
         BCT   R1,*+L'*+4
         B     PRMINV
         CLI   2(R2),C'='
         BNE   PRMINV
         LA    R2,1(R2)
         BCT   R1,*+L'*+4
         B     PRMINV
         LR    R0,R1               EXTRACT THE NUMBER SPECIFIED
         LR    R3,R2
PRMLPL   CLI   2(R2),C'0'          SCAN FOR NUMERICS
         BL    PRMINV
         CLI   2(R2),C'9'
         BH    PRMINV
         LA    R2,1(R2)
         BCT   R1,*+L'*+4
         B     *+L'*+8
         CLI   2(R2),C','          SEPARATOR ?
         BNE   PRMLPL              NO, CONTINUE
         LR    R4,R2               COMPUTE FIELD LENGTH
         SR    R4,R3
         CH    R4,=H'3'            LENGTH GREATER THAN 3 ?
         BH    *+L'*+2             YES, INVALID
         BCT   R4,*+L'*+6          LENGTH MINUS 1 FOR EX
         LR    R1,R0               ALSO HERE IF LENGTH WAS 1
         B     PRMINV
         EX    R4,SETNUM
         CP    DBLWRD+6(2),=P'20'
         BL    PRMINV
         MVC   CTRMAX(L'CTRMAX),DBLWRD+L'DBLWRD-L'CTRMAX
         LTR   R1,R1
         BP    PRMNXT
         B     NOPARM
PRMERR   LR    R3,R2
PRMINV   LA    R4,8
         CLR   R1,R4
         BNL   *+L'*+2
         LR    R4,R1
         BCTR  R4,0
         EX    R4,SETINV           SAY WHERE
        WTO    MF=(E,MSGINV)
         LA    R10,4   - - - - - - - - - - - - - - - - - - EXIT CC = 4
         B     FINI
PRMHT    OI    SW2,SWPHT           SET HEADER/TRAILER REQUESTED
         B     PRMNXT
PRMIX    OI    SW1,SWIX+SWPIS      SET INDEX REQUESTED
PRMNXT   LA    R2,1(R2)
         BCT   R1,PRMLOOP
         SPACE 1
NOPARM   XR    R10,R10 - - - - - - - - - - - - - - - - - - EXIT CC = 0
        $GDATE PDATE,,DBLWRD       GET TODAY DATE
         LTR   R15,R15             TEST RETURN CODE
         BZ    DATEOK              0 - GOOD RETURN
         CVD   R15,DBLWRD
         MVC   DTERC,=XL6'402020202120'
         ED    DTERC,DBLWRD+5
        WTO    MF=(E,MSGDTE)
         LA    R10,32  - - - - - - - - - - - - - - - - - - EXIT CC = 32
         B     FINI
DATEOK   TM    SW2,SWPHT           HEADER/TRAILER REQUESTED ?
         BZ    NOHTR
         L     R1,=A(DTDAY)
         MVC   0(L'DTDAY,R1),PDATE
         L     R1,=A(DTDTE)
         MVC   0(2,R1),DBLWRD+2
         MVC   3(2,R1),DBLWRD
         MVC   6(2,R1),DBLWRD+4
NOHTR    MVI   PDATE+L'PDATE-2,C'/'
         MVC   XDATE,PDATE
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         ZAP   CTRMIN,CTRMAX
         SP    CTRMIN,=P'5'
         ZAP   CTRNEW,CTRMIN
         SP    CTRNEW,=P'5'
        EXTRACT ATIOT,'S',FIELDS=(TIOT)
         LA    R2,HELP
         BAS   R8,OPENS
         OI    OPENST+1,@ON
         LA    R2,IN
         BAS   R8,OPENS
         B     *+L'*+4
         OI    SW1,SWINT           NO SYSIN = USE 'COMMANDS' MEMBER
         OI    OPENSW+1,@ON
         LA    R2,DIAG
         BAS   R8,OPENS
         NI    SWDIAG+1,@OFF       SYSDIAG = LIST INPUT
         NI    OPENST+1,@OFF
         LA    R2,OUT
         BAS   R8,OPENS
         TM    SW1,SWIX            INDEX REQUESTED ?
         BZ    *+L'*+8             NO
         LA    R2,NDX
         BAS   R8,OPENS
         XR    R0,R0
         L     R9,=A(HTPAGE)
         BASR  R8,R9               DO HEADER PAGES IF REQUESTED
         LH    R1,HELP+(DCBBLKSI-IHADCB)     BLOCKSIZE
         LA    R0,7(R1)
         SRL   R0,3
         SLL   R0,3
        GETMAIN R,LV=(0)
         LR    R7,R1               SAVE POINTER TO I/O AREA
         XR    R6,R6
GETNXT   TM    SW1,SWINT           INTERNAL READ ?
         BZ    GIN                 NO
STINT    TM    SW1,SWPHM           PROCESSING STARTED ?
         BO    PASSIM              YES
         MVC   REC(8),=CL8'COMMANDS'    SET MEMBER NAME
         TM    SWDIAG+1,@ON
         BO    *+L'*+6
         MVC   DIAGR(L'DIAGR),REC
         TM    SW1,SWIX            IS INDEX REQUESTED ?
         BZ    GETMC               NO, JUST LIST MEMBER
         OI    SW1,SWSCC           START SCAN OF COMMANDS
         L     R0,INTARL
         LR    R3,R0
        GETMAIN R,LV=(0)
         ST    R1,INTARS           SET POINTER TO INT. AREA
         ST    R1,INTARC           SET CURRENT IN INT. AREA
         LR    R2,R1
         AL    R1,INTARL
         ST    R1,INTARX           SET INT. AREA END +1
         LA    R14,*               CLEAR TO ALL BLANKS
         XR    R15,R15
         ICM   R15,B'1000',=CL8' '
         MVCL  R2,R14
         B     GETMC
PASSIM   TM    SW1,SWIX            IS INDEX REQUESTED ?
         BZ    FINI                NO, JUST LIST MEMBER, THUS END
         L     R1,INTARC
         CL    R1,INTARX           HAVE WE PASSED ALL ?
         BH    FINI                YES, END OF PROCESS
         MVC   REC(72),0(R1)       PASS INTERNAL CARD IMAGE
         TM    SWDIAG+1,@ON
         BO    *+L'*+6
         MVC   DIAGR(L'DIAGR),REC
         LA    R1,72(R1)
         ST    R1,INTARC
         CLC   REC(6),=CL6'*HEAD=' HEADER CONTROL CARD ?
         BE    SETHD               YES
KWSW1    B     GETMC               NORMAL WAY
SKWAY    TM    SWDIAG+1,@ON        SKIP WAY
         BO    GETNXT
         MVC   DIAGM+L'DIAGM-10(10),=CL10'SKIPPED : '
         BAS   R8,PDIAG
         B     GETNXT
GIN     GET    IN,REC              READ A CONTROL CARD
SWDIAG   B     *+L'*+6
         MVC   DIAGR(L'DIAGR),REC
         CLC   REC(6),=CL6'*SKEY=' KEYWORDS CONTROL CARD ?
         BNE   NOKWRD              NO
KWRDSW   NOP   KEYINV
         ICM   R1,B'1111',KEYCUR
         BNZ   KEYSC
         L     R0,KEYLGH
         LR    R3,R0
        GETMAIN R,LV=(0)
         ST    R1,KEYSTR           START AREA ADDRESS
         ST    R1,KEYCUR           CURRENT AREA POINTER
         LR    R2,R1
         AL    R1,KEYLGH
         ST    R1,KEYEND           SET END AREA + 1 ADDRESS
         LA    R14,*               CLEAR TO ALL ZEROS
         XR    R15,R15
         MVCL  R2,R14
         L     R1,KEYCUR
         MVI   0(R1),X'FF'         SET TOP
         OI    OP,OPSKEY           SET REQUEST
KEYSC    LA    R3,REC+6            SCAN TO EXTRACT KEYWORDS
         LA    R4,1
         LA    R5,REC+71
         CLI   0(R3),C' '          SEARCH A KEYWORD
         BNE   GETKEY
KEYSRC   BXLE  R3,R4,*-8
         BAS   R8,TPDIAG
         B     GIN
GETKEY   LR    R2,R3               START OF A KEYWORD
         BXLE  R3,R4,*+L'*+4
         B     *+L'*+8
         CLI   0(R3),C' '
         BNE   *-12
         LR    R14,R3              COMPUTE KEYWORD LENGTH
         SR    R14,R2
         LA    R15,1(R1,R14)       ADDRESS OF SPACE NEEDED IN AREA
         CL    R15,KEYEND          MAY BE STORED ?
         BNL   KEYOV               NO, AREA OVERFLOW
         BCTR  R14,0
         STC   R14,0(R1)           STORE KEYWORD EX LENGTH
         EX    R14,MVSKEY          STORE KEYWORD
         LR    R1,R15
         ST    R1,KEYCUR           CURRENT AREA POINTER
         MVI   0(R1),X'FF'         SET TOP
         B     KEYSRC
KEYOV   WTO    MF=(E,MSGKOV)
         LA    R10,28  - - - - - - - - - - - - - - - - - - EXIT CC = 28
         B     FINI
KEYINV   TM    SWDIAG+1,@ON
         BO    KEYIWT
         MVC   DIAGM+L'DIAGM-10(10),=CL10'INVALID : '
         BAS   R8,PDIAG
         B     GIN
KEYIWT  WTO    MF=(E,MSGKIV)
         B     GIN
NOKWRD   CLC   REC(6),=CL6'*HEAD=' HEADER CONTROL CARD ?
         BE    SETHD               YES, PROCESS IT
         OI    KWRDSW+1,@ON        CLOSE KEYWORDS WAY
KWSW2    B     GETMC               NORMAL WAY
         B     SKWAY               SKIP WAY
SETHD    TM    OP,OPSKEY           SELECT KEYWORDS REQUESTED ?
         BZ    SETHD0              NO
         L     R14,KEYSTR          YES, LOOK FOR A MATCH
         XR    R15,R15
KPRSS1   CLI   0(R14),X'FF'        TOP REACHED ?
         BNE   KPRSS2              NO
         NI    KWSW1+1,@OFF        YES, NO MATCH, SO SKIP WAY
         NI    KWSW2+1,@OFF
         B     SKWAY
KPRSS2   IC    R15,0(R14)          NOW SCAN HEADER FOR KEYWORD
         LA    R1,REC+6
         LA    R2,1
         LA    R3,REC+71
         SR    R3,R15
KPRSS3   CLI   0(R1),C' '
         BE    *+L'*+8
         EX    R15,CLKPRS          KEYWORD MATCH ?
         BE    KPRSS4              YES
         BXLE  R1,R2,KPRSS3
         LA    R14,2(R14,R15)      NEXT KEYWORD
         B     KPRSS1
KPRSS4   OI    KWSW1+1,@ON         SET NORMAL WAY
         OI    KWSW2+1,@ON
SETHD0   MVC   XTIT(L'XTIT),XTIT-1
         MVC   PTIT(L'PTIT),PTIT-1
         LA    R1,REC+6
         LA    R3,REC+71
SETHD1   CLI   0(R1),C' '
         BNE   SETHD2
         LA    R1,1(R1)
         CLR   R1,R3
         BNH   SETHD1
HDRIGN   TM    SWDIAG+1,@ON        HEADER IS ALL BLANKS, IGNORE
         BO    GETNXT
         MVC   DIAGM+L'DIAGM-10(10),=CL10'IGNORED : '
         BAS   R8,PDIAG
         B     GETNXT
SETHD2   CLI   0(R3),C' '
         BNE   *+L'*+4
SETHD3   BCT   R3,SETHD2
         LR    R2,R3               COMPUTE HEADER LENGTH
         SR    R2,R1
         CH    R2,=Y(L'PTIT-3)     LENGTH ACCEPTABLE ?
         BNH   SETHD5              YES
SETHD4   BCTR  R3,0
         CLR   R3,R1
         BNH   HDRIGN              HEADER IS TOO LONG, IGNORE
         CLI   0(R3),C' '
         BNE   SETHD4
         B     SETHD3
SETHD5   EX    R2,GETNHD           GET NEW HEADER
         LA    R2,PTIT+L'PTIT-3
         CLI   0(R2),C' '
         BNE   *+L'*+4
         BCT   R2,*-8
         TM    SW1,SWIX
         BZ    SETHD6
         OI    SW1,SWPIS
         MVI   2(R2),C':'
         MVC   XTIT,PTIT
SETHD6   MVC   1(2,R2),=CL2'. '
TPDG     BAS   R8,TPDIAG
         B     GETNXT
GETMC    LA    R6,REC              START OF REQUESTED NAMES
CSCAN    CLI   0(R6),C' '          START OF A NAME ?
         BNE   STNME               YES
CNTRL    CL    R6,=A(REC+71)       END OF CARD ?
         BNL   TPDG                YES
         LA    R6,1(R6)
         B     CSCAN
STNME    LR    R1,R6               START OF NAME POINTER
         LA    R6,1(R6)
         CL    R6,=A(REC+71)       END OF CARD ?
         BNL   *+L'*+8             YES
         CLI   0(R6),C' '          END OF NAME ?
         BNE   STNME+L'STNME       NO
         LR    R2,R6
         SR    R2,R1
         CH    R2,=H'3'            AT LEAST LENGTH IS 3 ?
         BL    TSNAL               NO, MUST BE A NAME
         CLC   0(2,R1),=CL2'P='    IS IT A SET PAGE NUMBER REQUEST ?
         BNE   TSNAL               NO, MUST BE A NAME
         LR    R3,R1               EXTRACT THE NUMBER SPECIFIED
         SH    R2,=H'2'
         LR    R4,R2
         BCTR  R4,0
         CH    R2,=H'5'            OVER MAXIMUM ?
         BH    INVNUM              YES
SCNUM    CLI   2(R1),C'0'          CONTROL FOR NUMERICS
         BL    INVNUM
         CLI   2(R1),C'9'
         BH    INVNUM
         LA    R1,1(R1)
         BCT   R2,SCNUM
         EX    R4,SETPGN           SET NEW PAGE NUMBER
         B     CNTRL
INVNUM   EX    R4,MVPIPN
         CH    R4,=H'7'
         BNH   *+L'*+6
         MVC   PIPN+L'PIPN(3),=CL3'...'
        WTO    MF=(E,MSGIPN)
         LA    R10,12  - - - - - - - - - - - - - - - - - - EXIT CC = 12
         B     FINI
TSNAL    CH    R2,=H'10'           VALID LENGTH ?
         BH    NMERR               NO, TOO LONG
         LA    R3,0(R1,R2)         POINT TO END +1
         SH    R3,=H'2'            BACK 2 POSITIONS
         CLC   0(2,R3),=CL2'/A'    ALIAS OF NEXT ?
         BNE   *+L'*+8             NO
         OI    OP,OPALRP           YES, SET REQUEST
         SH    R2,=H'2'            REDUCE NAME LENGTH
         CH    R2,=H'8'            VALID NAME LENGTH ?
         BNH   SETMNM              YES
NMERR    MVC   PLNG(L'PLNG),0(R1)  SET BEGINNING OF NAME
        WTO    MF=(E,MSGLNG)
         LA    R10,16  - - - - - - - - - - - - - - - - - - EXIT CC = 16
         B     FINI
SETMNM   BCTR  R2,0                MINUS 1 FOR EX
         MVC   MMBR,=CL8' '        RESET TO BLANKS
         MVC   MEMNAME(L'MEMNAME),=CL8' '
         EX    R2,MVMMBR           MOVE TO BLDL AREA
         EX    R2,MVMNME           SAVE DESIRED MEMBER NAME
         STC   R2,MEMLEN           SAVE MEMBER LENGTH FOR EX
         LA    R1,8                LOAD A CONSTANT
         LA    R2,1(R2)            PUT R2 BACK UP
         SR    R1,R2               GET NUMBER OF BLANKS
         STC   R1,OFFSET           OFFSET FOR CENTERING ON HDR
         SRL   R1,1                DIVIDE BY 2
         STC   R1,MEMOFFST         SAVE FOR OFFSETTING
         XC    MTTR(L'BLDLPDS-L'MMBR),MTTR   ZERO OUT BLDL
        BLDL  HELP,BLDLLIST        GO GET IT
         LTR   R15,R15             TEST RETURN CODE
         BZ    MOVE                0 - GOOD MEMBER
         CH    R15,=H'4'
         BE    *+L'*+10            4 - MEMBER NOT FOUND
         MVC   PERT,=CL4'BLDL'     8 - GO ISSUE BLDL I/O ERROR
         B     PERMIO
         MVC   PNFD,MMBR           SET MEMBER NAME
        WTO    MF=(E,MSGNFD)
         B     CNTRL               GO GET THE NEXT MEMBER
MOVE     TM    MINDC,PDS2ALIS      IS IT AN ALIAS ?
         BZ    NOTAL               NO
         TM    OP,OPALRP           IS IT AN ALIAS REQUESTED ?
         BZ    NOTAL               NO, GO LIST AS A COMMAND
         NI    OP,255-OPALRP       RESET REQUEST
         MVI   HDT,C' '            ALIAS PROCESSING, CLEAR LINE
         MVC   HDT+1(L'HDT-1),HDT
         TM    OP,OPALIS           BEEN HERE BEFORE ?
         BZ    *+L'*+8             NO, FORCE HEADING
         MVI   HCC,C'0'            DOUBLE SPACE
         B     INDIC               NO HEADING
         NI    OP,255-OPSTLSC      NOT SUBCMD ANYMORE
         BAS   R8,PHDLNE           HEADER
         MVI   HCC,C'0'            DOUBLE SPACE
         ZAP   CTRLIN,=P'2'        RESET LINE COUNT
INDIC    MVC   HDT+26(27),DASHES   HIGHLITE
        PUT    OUT,HEAD
         MVI   HCC,C' '            SINGLE SPACE
         MVC   HDT+26(27),HDT+25   CLEAR HIGHLITE
         MVC   HDT+29(8),MEMNAME   MOVE IN MEMBER NAME
         MVC   HDT+37(12),=CL12' IS AN ALIAS'
        PUT    OUT,HEAD
         MVI   HDT,C' '            CLEAR HEAD
         MVC   HDT+1(L'HDT-1),HDT
         MVC   HDT+26(27),DASHES   HIGHLITE
        PUT    OUT,HEAD
         MVC   HDT+26(27),HDT+25   CLEAR HIGHLITE
        PUT    OUT,HEAD
         AP    CTRLIN,=P'5'        MAKE LINECT CORRECT
         OI    OP,OPALIS           SAY WE HAVE BEEN HERE
         OI    OP,OPCSEP           PRINT COMMAND NAME ALSO
         B     CNTRL               GO GET NEXT MEMBER
NOTAL    NI    OP,255-OPSTLSC      TELL EM NOT SUBCOMMAND ANYMORE
         NI    OP,255-OPSCSVD      SUBCOMMAND NOT SAVED ANY MORE
        FIND   HELP,MTTR,C         FIND MEMBER
         LTR   R15,R15             TEST RETURN CODE
         BZ    RDSETUP             0 - GOOD RETURN
         CH    R15,=H'4'
         BE    BADNAME             4 - INVALID NAME
         MVC   PERT,=CL4'FIND'     8 - GO ISSUE FIND I/O ERROR
PERMIO   MVC   PERM,MMBR           SAY MEMBER NAME
        WTO    MF=(E,MSGERR)
         LA    R10,20  - - - - - - - - - - - - - - - - - - EXIT CC = 20
         B     FINI
BADNAME  MVC   PBNM,MEMNAME        SET MEMBER NAME
        WTO    MF=(E,MSGBNM)
         B     CNTRL
RDSETUP  NI    OP,255-OPCMDS       TURN OFF CMDS
         OI    SW1,SWCMD           START OF A COMMAND
         CLC   MEMNAME,=CL8'COMMANDS'   COMMANDS ?
         BNE   *+L'*+4             NO, ...
         OI    OP,OPCMDS           YES, INDICATE SUCH
         TM    OP,OPALIS           ALIAS ALREADY CAUSED TOP ?
         BO    READEM              YES, NO HEADER
         ZAP   CTRLIN,=P'0'        FORCE HEADING
READEM   NI    OP,255-OPALIS       TURN OFF ALIAS FLAG
        READ   DECB,SF,HELP,(R7),'S'
        CHECK  DECB
         LH    R5,HELP+(DCBBLKSI-IHADCB)     BLOCK SIZE
         L     R1,DECB+16          A(IOB)
         SH    R5,14(R1)           MINUS RESIDUAL CCW COUNT
         BCTR  R5,0                DOWN ONE
         LR    R3,R7               REC START
         LA    R4,80               REC LENGTH
         LA    R5,0(R3,R5)         REC END
PROCESS  CLI   0(R3),C'*'          COMMENT CARD ?
         BE    BXLE                YES, BYPASS IT
         TM    OP,OPCMDS           CMDS ?
         BZ    NRMWAY              NO, CONTINUE
         MVC   LINEPL(72),0(R3)    YES, COPY IN RECORD
         TM    SW1,SWSCC           MUST BE SCANNED ?
         BZ    LISTER              NO, GO LIST IT
         CLC   0(8,R3),=CL8' '     DATA LINE ?
         BNE   GTHDR               YES
         TM    SW1,SWMMB           BLANK LINE AFTER MEMBERS NAMES ?
         BZ    LISTER              NO, GO LIST IT
         NI    SW1,255-SWHDR-SWMMB YES, RESET
         B     LISTER              GO LIST IT
GTHDR    TM    SW1,SWHDR           HEADER HANDLED ?
         BO    GTMMB               YES
         LA    R1,71(R3)           NO
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         CLI   0(R1),C':'
         BNE   *+L'*+4
         MVI   0(R1),C' '
         L     R1,INTARC
         CLC   0(8,R1),=CL8' '
         BE    *+L'*+4
         LA    R1,72(R1)
         CL    R1,INTARX
         BNL   POOLOV              POOL OVERFLOW
         MVC   0(6,R1),=CL6'*HEAD='     GET HEADER
         MVC   6(66,R1),0(R3)
         LA    R1,72(R1)
         ST    R1,INTARC
         XC    INTARD,INTARD
         OI    SW1,SWHDR
         B     LISTER              GO LIST IT
POOLOV  WTO    MF=(E,MSGPOV)
         LA    R10,24  - - - - - - - - - - - - - - - - - - EXIT CC = 24
         B     FINI
GTMMB    L     R1,INTARC
         LH    R14,INTARD
         CH    R14,=H'70'
         BL    *+L'*+6
         XR    R14,R14
         LA    R1,72(R1)
         CL    R1,INTARX
         BNL   POOLOV              POOL OVERFLOW
         ST    R1,INTARC
         ALR   R1,R14
         LA    R14,10(R14)
         STH   R14,INTARD
         CLI   0(R3),C'%'          GET MEMBER NAME
         BE    *+L'*+10
         MVC   1(8,R1),0(R3)
         B     *+L'*+6
         MVC   1(8,R1),1(R3)
         OI    SW1,SWMMB
         B     LISTER              GO LIST IT
NRMWAY   LA    R1,71(R3)           TEST FOR BLANK LINES
         LA    R0,72
         CLI   0(R1),C' '
         BNE   *+L'*+10            NOT BLANK, PROCESS IT
         BCTR  R1,0
         BCT   R0,*-10
         B     BXLE                BLANK LINE, BYPASS IT
         CLC   0(2,R3),=CL2')F'    FUNCTION
         BE    MAJORF
         CLC   0(2,R3),=CL2')S'    SUBCOMMANDS
         BE    MAJORS
         CLC   0(2,R3),=CL2')X'    SYNTAX
         BE    MAJORX
         CLC   0(2,R3),=CL2')M'    MESSAGES
         BE    MAJORM
         CLC   0(2,R3),=CL2')O'    OPERANDS
         BE    INDENT
         CLI   0(R3),C'='          SUB COMMAND
         BNE   CHEKOP              NO
         OI    OP,OPSBCMD          TELL EM SUBCOMMAND
         NI    OP,255-OPSCSVD      TELL EM NEW SUBCOMMAND
         NI    OP,255-OPSTLSC      NOT CONTINUED SUBCMD
SUBCMD   MVI   HDT,C' '            CLEAR HEAD
         MVC   HDT+1(L'HDT-1),HDT
         TM    OP,OPSTLSC          IS IT STILL SUBCOMMAND ?
         BO    *+L'*+10            YES, LONG MEMBER
         CP    CTRLIN,CTRNEW       ENOUGH ROOM FOR NEW ONE ?
         BL    ADDFV               YES, GO GETEM
         ZAP   CTRLIN,=P'2'        REINITIALIZE LINE COUNT
         BAS   R8,PHDLNE           HEADER
ADDFV    AP    CTRLIN,=P'4'        1 EA. FOR HDR + HIGHLIGHTERS
*                                  THE OTHERS ARE ALREADY TAKEN INTO
*                                  ACCOUNT
         MVI   HCC,C'0'            DOUBLE SPACE
         MVC   HDT+26(27),DASHES   COMMAND HIGHLIGHTER
        PUT    OUT,HEAD            PUT IT
         MVC   HDT+26(27),HDT+25   CLEAR THE DASHES
         MVI   HCC,C' '            SET TO SINGLE SPACE
         MVI   HDT+40,C'-'         PUT IN THE SEPARATOR
         XR    R1,R1               CLEAR IT
         IC    R1,OFFSET           PICK UP THE OFFSET
         LA    R2,HEAD+28(R1)      R2 POINT TO LOCATION + OFFSET
         IC    R1,MEMLEN           PICK UP LENGTH TO MOVE
         EX    R1,MEMMVC           MOVE IN MEMNAME
*                                  PRESUPPOSES THAT SUBCOMMAND HAS
*                                  AN '=' SIGN IN FRONT OF IT
         TM    OP,OPSCSVD          HAS SUBCOMMAND BEEN SAVED ?
         BZ    *+L'*+10            IF NOT GET OFF OF R3
         MVC   HDT+43(8),SCSAVE    HAS BEEN SAVED... GET IT
         B     DONEMOVE            GO AROUND R3 MOVE
         MVC   HDT+43(8),1(R3)     MOVE IN SUBCOMMAND NAME
         MVC   SCSAVE(8),1(R3)     SAVE THE SUBCOMMAND NAME
         OI    OP,OPSCSVD          AND TURN ON THE FLAG
DONEMOVE LA    R14,HDT+43          SET UP FOR '=' CHECK
         LA    R15,8               LENGTH TO CHECK
SUBCX    CLI   0(R14),C'='         IS IT AN '=' SIGN
         BE    *+L'*+8             YES... GO BLANK IT
         LA    R14,1(R14)          BUMP TO NEXT CHAR
         BCT   R15,SUBCX           GO CHECK FURTHER
         MVC   0(9,R14),=CL9' '    MOVE IN BLANKS
         TM    SW1,SWIX            INDEX REQUESTED ?
         BZ    *+L'*+14            NO
         TM    OP,OPSBCMD          LONG MEMBER OR NEW SUBCOMMAND?
         BZ    *+L'*+6             LONG MEMBER... CONTINUATION
         MVC   XLNES(L'XLNES),HDT+43    SET SUBCOMMAND NAME TO INDEX
        PUT    OUT,HEAD            WRITE IT
         MVC   HDT+26(27),DASHES   HIGHLIGHT IT
        PUT    OUT,HEAD            WRITE EM
         MVC   HDT+26(27),HDT+25   BLANK IT
         MVI   CC,C'0'             DOUBLE SPACE
         OI    OP,OPSTLSC          SET SUBCOMMAND HEADER SWITCH
         TM    OP,OPSBCMD          LONG MEMBER OR NEW SUBCOMMAND?
         BZ    REPORTD             LONG MEMBER... LINE PRESENT
         NI    OP,255-OPSBCMD      TURN OFF THE SWITCH
         TM    SW1,SWIX            INDEX REQUESTED ?
         BZ    BXLE                NO
         BAS   R8,PIXLNE           INDEX
         B     BXLE                GO GET SOME MORE
CHEKOP   CLC   0(2,R3),=CL2'))'    OPERAND ?
         BE    NOTDUMB             YES, BRANCH
         CLC   0(2,R3),=CL2')P'    POSITIONAL OPERAND ?
         BE    NOTDUMB             YES, BRANCH
         CLC   0(2,R3),=CL8' '     CHECK FOR LACK OF NEATNESS
         BE    NOTDUMB             YES, VERY GOOD, BRANCH
         MVC   LINEIND(72),0(R3)   SOME DUMMY DIDN'T LEAVE 1ST 2 BLANK
         B     LISTER
NOTDUMB  MVC   LINEIND(70),2(R3)   SET LINE WITH INDENTION
         B     LISTER
MAJORF   LA    R2,=CL14' FUNCTION :'
         B     NORMAL
MAJORS   LA    R2,=CL14' SUBCOMMANDS :'
         B     NORMAL
MAJORX   LA    R2,=CL14' SYNTAX :'
         B     NORMAL
MAJORM   LA    R2,=CL14' MESSAGES :'
         B     NORMAL
INDENT   LA    R2,=CL14' OPERANDS :'
NORMAL   MVI   CC,C'0'             DOUBLE SPACE
         CLC   LINEMAJ(70),2(R3)   ANY DATA ?
         BNE   *+L'*+10            YES
         MVC   LINEMAJ(14),0(R2)   SUPPLY DATA
         B     *+L'*+6
         MVC   LINEMAJ(70),2(R3)   MOVE IN DATA
         MVC   LNT+1(3),=CL3'***'  INSERT HIGHLIGHTER
         CP    CTRLIN,CTRMIN       ENOUGH ROOM FOR NEW ONE ?
         BNL   SKIPPG              YES, GO GETEM
LISTER   CP    CTRLIN,CTRMAX
         BL    *+L'*+6
SKIPPG   ZAP   CTRLIN,=P'0'
         MVI   HDT,C' '            CLEAR IT
         MVC   HDT+1(L'HDT-1),HDT
         TM    OP,OPCSEP           NEED COMMAND NAME ?
         BO    HEADALI             YES... GO DO IT
         CP    CTRLIN,=P'0'
         BNE   REPORTD
         TM    OP,OPSTLSC          IS IT STILL SUBCOMMAND ?
         BO    SUBCMD              YES... GO DO IT THAT WAY
         NI    OP,255-OPSTLSC      NOT SUBCOMMAND ANYMORE
         BAS   R8,PHDLNE           HEADER
         AP    CTRLIN,=P'1'        ADJUST LINE COUNT
HEADALI  AP    CTRLIN,=P'5'        1 EA. FOR HDR + HIGHLIGHTERS
*                                  THE OTHERS ARE ALREADY TAKEN INTO
*                                  ACCOUNT
         NI    OP,255-OPCSEP       TURN OFF COMMAND PRINT SWITCH
         MVI   HCC,C'0'            DOUBLE SPACE
         MVC   HDT+34(10),DASHES   COMMAND HIGHLIGHTER
        PUT    OUT,HEAD            PUT IT
         MVI   HCC,C' '            SET TO SINGLE SPACE
         MVC   HDT+34(10),HDT+33   BLANK OUT HIGHLIGHTER
         XR    R1,R1               ZERO OUT R1
         IC    R1,MEMOFFST         GET THE OFFSET FOR MOVE
         LA    R2,HDT+35(R1)       LOAD R2 W/LOCATION + OFFSET
         IC    R1,MEMLEN           PICK UP LENGTH TO MOVE
         EX    R1,MOVEMEM          MOVE IN THE MEMBER NAME
        PUT    OUT,HEAD            PUT OUT CENTERED NAME
         MVC   HDT+34(10),DASHES   COMMAND HIGHLIGHTER
        PUT    OUT,HEAD            PUT IT
         MVC   HDT+34(10),HDT+33   BLANK OUT HEAD
         MVI   CC,C'0'             DOUBLE SPACE
         TM    SW1,SWIX+SWCMD      INDEX REQUESTED AND START CMD ?
         BNO   REPORTD             NO
         NI    SW1,255-SWCMD       TURN OFF START COMMAND
         MVC   XLNEC,MEMNAME       SET COMMAND NAME
         BAS   R8,PIXLNE           INDEX
REPORTD  CLI   CC,C' '             SINGLE SPACING ?
         BE    SPONE               YES
         CLI   CC,C'0'             DOUBLE SPACING ?
         BNE   SPONE+L'SPONE       NO, BRANCH AROUND AP
         AP    CTRLIN,=P'1'
SPONE    AP    CTRLIN,=P'1'
         TM    OP,OPSBCMD          IS IT SUBCOMMANDS ?
         BZ    *+L'*+10            NO... LIST ASIS
         NI    OP,255-OPSBCMD      TURN OFF SWITCH
         MVC   LNT+2(3),=CL3'***'  INSERT HIGHLIGHTER
        PUT    OUT,LINE
         MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
BXLE     BXLE  R3,R4,PROCESS       SCAN ENTIRE BLOCK
         B     READEM              READ ANOTHER BLOCK
ENDHLP   TM    SW1,SWINT           INTERNAL READ ?
         BZ    CNTRL               NO
         TM    SW1,SWPHM           PROCESS STARTED ?
         BO    CNTRL               YES
         NI    SW1,255-SWSCC-SWHDR-SWMMB RESET
         OI    SW1,SWPHM           SET PROCESS HEADERS-MEMBERS
         MVC   INTARX,INTARC       SET CURRENT AS END
         MVC   INTARC,INTARS       RESTART CURRENT
         B     CNTRL
ENDIN    LTR   R6,R6               HAVE WE PROCESS SOMETHING ?
         BNZ   FINI                YES
         OI    SW1,SWINT           USE 'COMMANDS' MEMBER
         MVI   REC,C' '
         MVC   REC+1(L'REC-1),REC
         B     STINT
FINI     LA    R0,255
         L     R9,=A(HTPAGE)
         BASR  R8,R9               DO TRAILER PAGES IF REQUESTED
         LA    R2,HELP
         BAS   R8,CLOSS
         LA    R2,IN
         BAS   R8,CLOSS
         LA    R2,DIAG
         BAS   R8,CLOSS
         LA    R2,OUT
         BAS   R8,CLOSS
         LA    R2,NDX
         BAS   R8,CLOSS
         ICM   R1,B'1111',KEYSTR
         BZ    FREEIA
         L     R0,KEYLGH
        FREEMAIN R,A=(1),LV=(0)
FREEIA   ICM   R1,B'1111',INTARS
         BZ    FREEAR
         L     R0,INTARL
        FREEMAIN R,A=(1),LV=(0)
FREEAR   LTR   R7,R7
         BZ    QUIT
         LH    R1,HELP+(DCBBLKSI-IHADCB)     BLOCKSIZE
         LA    R0,7(R1)
         SRL   R0,3
         SLL   R0,3
        FREEMAIN R,A=(R7),LV=(0)
QUIT    $XRET  CC=(R10)
         SPACE 1
SETNUM   PACK  DBLWRD,2(*-*,R3)    << EXECUTED >>
SETINV   MVC   PINV(*-*),2(R2)     << EXECUTED >>
MVSKEY   MVC   1(*-*,R1),0(R2)     << EXECUTED >>
CLKPRS   CLC   0(*-*,R1),1(R14)    << EXECUTED >>
GETNHD   MVC   PTIT(*-*),0(R1)     << EXECUTED >>
SETPGN   PACK  CTRPG,2(*-*,R3)     << EXECUTED >>
MVPIPN   MVC   PIPN(*-*),2(R3)     << EXECUTED >>
MVMMBR   MVC   MMBR(*-*),0(R1)     << EXECUTED >>
MVMNME   MVC   MEMNAME(*-*),0(R1)  << EXECUTED >>
MOVEMEM  MVC   0(*-*,R2),MEMNAME   << EXECUTED >>
MEMMVC   MVC   0(*-*,R2),MEMNAME   << EXECUTED >>
         SPACE 1
         USING IHADCB,R2
OPENS    L     R1,ATIOT            TIOT ADDRESS
         USING TIODSECT,R1
         XR    R0,R0
OPENS1   CLC   TIOELNGH(4),=F'0'
         BE    OPENST
         CLC   TIOEDDNM(L'DCBDDNAM),DCBDDNAM
         BE    OPENSW
         IC    R0,TIOELNGH
         ALR   R1,R0
         B     OPENS1
         DROP  R1
OPENSW   NOP   OPENS2
        OPEN   ((R2),INPUT)
         B     OPENS3
OPENS2  OPEN   ((R2),OUTPUT)
OPENS3   TM    DCBOFLGS,DCBOFOPN
         BOR   R8
         MVC   PDDM,=CL10'OPEN ERROR'
         B     OPENS4
OPENST   NOP   4(R8)
         MVC   PDDM,=CL10'DD MISSING'
OPENS4   MVC   PDDN,DCBDDNAM
        WTO    MF=(E,MSGDDN)
         LA    R10,8   - - - - - - - - - - - - - - - - - - EXIT CC = 8
         B     FINI
         DROP  R2
         SPACE 1
         USING IHADCB,R2
CLOSS    TM    DCBOFLGS,DCBOFOPN
         BZR   R8
        CLOSE  ((R2))
         BR    R8
         DROP  R2
         SPACE 1
TPDIAG   TM    SWDIAG+1,@ON
         BOR   R8
PDIAG   PUT    DIAG,DIAGL
         MVI   DIAGL,C' '          CLEAR LINE
         MVC   DIAGL+1(L'DIAGL-1),DIAGL
         BR    R8
         SPACE 1
PHDLNE   MVI   HCC,C'1'            FORCE HEADING (EJECT)
         MVC   PGNBR,=XL6'4B2020202120'
         ED    PGNBR,CTRPG
        PUT    OUT,JRCID           TOP OF PAGE ID
        PUT    OUT,JRCIT           HEADER
         AP    CTRPG,=P'1'
         MVI   HDT,C' '            CLEAR HEAD
         MVC   HDT+1(L'HDT-1),HDT
         BR    R8                  RETURN
         SPACE 1
PIXLNE   MVC   XLNEP,PGNBR         SET WHERE PAGE NUMBER
         LA    R1,XLNEP
         CLI   0(R1),C'.'
         BNE   *+L'*+12
         MVI   0(R1),C' '
         LA    R1,1(R1)
         B     *-16
         MVI   XDASHS,C'.'
         MVC   XDASHS+1(L'XDASHS-1),XDASHS-1
         CP    CTRXLN,=P'0'
         BE    PIXLNE1
         TM    SW1,SWPIS
         BZ    PIXLNE3
         CP    CTRXLN,CTRMIN
         BL    PIXLNE2
PIXLNE1  MVC   XPGNBR,=XL6'4B2020202120'
         ED    XPGNBR,CTRXPG
        PUT    NDX,JRCIX           TOP OF PAGE INDEX
        PUT    NDX,JRCIH
        PUT    NDX,JRCIL
         AP    CTRXPG,=P'1'
         ZAP   CTRXLN,=P'5'
         MVI   XLNE,C'0'           DOUBLE SPACE
         TM    SW1,SWPIS
         BZ    PIXLNE3
PIXLNE2 PUT    NDX,JRCIS
         AP    CTRXLN,=P'2'
         NI    SW1,255-SWPIS
         CLI   XLNE,C'0'           DOUBLE SPACE ALREADY SCHEDULED ?
         BE    PIXLNE3             YES
         MVI   XLNE,C'0'           SET DOUBLE SPACE
         AP    CTRXLN,=P'1'
PIXLNE3 PUT    NDX,XLNE            INDEX LINE
         AP    CTRXLN,=P'1'
         CP    CTRXLN,CTRMAX
         BL    *+L'*+6
         ZAP   CTRXLN,=P'0'
         MVI   XLNE,C' '           BLANK OUT INDEX LINE
         MVC   XLNE+1(L'XLNE-1),XLNE
         BR    R8                  RETURN
         SPACE 1
         CNOP  0,4
         USING IHADCB,R1
DCBEXIT  CLC   DCBLRECL(L'DCBLRECL),=H'79'   CONTROL LRECL
         BL    *+L'*+8
         CLC   DCBLRECL(L'DCBLRECL),=H'133'
         BNHR  R14
         MVC   DCBLRECL(L'DCBLRECL),=H'133'
         MVC   DCBBLKSI(L'DCBBLKSI),=H'133'
         BR    R14
         DROP  R1
         EJECT
DBLWRD   DC    D'0'
SCSAVE   DC    CL8' '              SUBCOMMAND SAVE AREA
MEMNAME  DC    CL8' '
BLDLLIST DC    0F'0',AL2(1,L'BLDLLIST+L'BLDLPDS)  BLDL LIST
BLDLPDS  DC    XL60'0'             PDS DIRECTORY ENTRY
MMBR     EQU   BLDLPDS+(PDS2NAME-PDS2),8
MTTR     EQU   BLDLPDS+(PDS2TTRP-PDS2),3
MINDC    EQU   BLDLPDS+(PDS2INDC-PDS2),1
ATIOT    DC    F'0'
KEYSTR   DC    F'0'                KEYWORDS AREA START ADDRESS
KEYCUR   DC    F'0'                KEYWORDS AREA CURRENT POINTER
KEYEND   DC    F'0'                KEYWORDS AREA END + 1 ADDRESS
KEYLGH   DC    F'1024'             KEYWORDS AREA GETMAINED LENGTH
INTARS   DC    F'0'                INT. AREA START ADDRESS
INTARC   DC    F'0'                INT. AREA CURRENT POINTER
INTARX   DC    F'0'                INT. AREA END +1 ADDRESS
INTARL   DC    A(NCP*72)           INT. AREA GETMAINED LENGTH
INTARD   DC    H'0'                DISPLACEMENT USE
DASHES   DC    27C'-'              SUBCOMMAND HIGHLIGHTER
MEMLEN   DC    XL1'0'
OFFSET   DC    XL1'0'
MEMOFFST DC    XL1'0'
OP       DC    AL1(0)
OPCMDS   EQU   X'80'               COMMAND 'COMMANDS'
OPCSEP   EQU   X'40'               PRINT COMMAND NAME FLAG
OPALIS   EQU   X'20'               ALIAS FLAG
OPALRP   EQU   X'10'               ALIAS REQUEST PROCESSING
OPSTLSC  EQU   X'08'               SUBCOMMAND HEADER FLAG
OPSCSVD  EQU   X'04'               SUBCOMMAND HAS BEEN SAVED FLAG
OPSBCMD  EQU   X'02'               SUBCOMMAND SEPARATION
OPSKEY   EQU   X'01'               KEYWORDS SELECTION
SW1      DC    AL1(0)
SWIX     EQU   X'80'               INDEX REQUESTED
SWPIS    EQU   X'40'               PRINT INDEX SUBTITLE
SWCMD    EQU   X'20'               COMMAND START PROCESSING
SWINT    EQU   X'10'               USE INTERNAL INPUT (COMMANDS)
SWSCC    EQU   X'08'               SCANNING OF COMMANDS MEMBER
SWHDR    EQU   X'04'               SCAN OF COMMANDS / HEADERS
SWMMB    EQU   X'02'               SCAN OF COMMANDS / MEMBERS NAMES
SWPHM    EQU   X'01'               PROCESS HEADERS / MEMBERS NAMES
SW2      DC    AL1(0)
SWPHT    EQU   X'80'               PRINT HEADER/TRAILER PAGES
SWPHTL   EQU   X'40'               HEADER/TRAILER PAGES PROCESS
CTRLIN   DC    PL2'0'
CTRXLN   DC    PL2'0'
CTRNEW   DC    PL2'50'
CTRMIN   DC    PL2'55'
CTRMAX   DC    PL2'60'
CTRPG    DC    PL3'1'
CTRXPG   DC    PL3'1'
         EJECT
REC      DC    CL80' '
DIAGL    DS    0CL133
         DC    CL6' '
DIAGM    DC    CL15' '
DIAGR    DC    CL72' ',CL40' '
LINE     DS    0CL133
CC       DC    CL1'0'
LNT      DC    CL132' '
LINEMAJ  EQU   LNT+5
LINEPL   EQU   LNT+7
LINEIND  EQU   LINEPL
HEAD     DS    0CL133
HCC      DC    CL1'0'
HDT      DC    CL132' '
XLNE     DS    0CL133
         DC    CL5' '
XLNEC    DC    CL8' ',CL3' '
XLNES    DC    CL8' ',CL45' '
XLNEP    DC    CL6' ',CL58' '
XDASHC   EQU   XLNES,50
XDASHS   EQU   XLNES+L'XLNES+4,38
JRCID    DS    0CL133
         DC    CL47'1 J.R.C. - HELP REFERENCE -'
PDATE    DC    CL21' ',CL4'PAGE'
PGNBR    DC    CL6' ',CL55' '
JRCIT    DS    0CL133
         DC    CL11' '
PTIT     DC    CL52'ALL COMMANDS.',CL70' '
JRCIX    DS    0CL133
         DC    CL47'1 J.R.C. - HELP INDEX -'
XDATE    DC    CL21' ',CL4'PAGE'
XPGNBR   DC    CL6' ',CL55' '
JRCIH    DS    0CL133
         DC    CL5'0 ',CL64'COMMAND    SUBCOMMAND '
         DC    CL6' PAGE ',CL58' '
JRCIL    DS    0CL133
         DC    CL5' '
         DC    8C'-',CL3'   '
         DC    10C'-',CL43' ',CL6' -----',CL58' '
JRCIS    DS    0CL133
         DC    CL2'0 '
XTIT     DC    CL52'ALL COMMANDS :',CL79' '
         EJECT
         LTORG
         EJECT
         PRINT NOGEN
         SPACE 1
MSGINV  WTO    '--- INVALID PARM= FIELD (AT :         ).',             1
               ROUTCDE=11,DESC=7,MF=L
PINV     EQU   MSGINV+34,8
MSGDDN  WTO    '---          -           .',ROUTCDE=11,DESC=7,MF=L
PDDN     EQU   MSGDDN+8,8
PDDM     EQU   MSGDDN+19,10
MSGKOV  WTO    '--- KEYWORDS SELECTION AREA OVERFLOW.',                1
               ROUTCDE=11,DESC=7,MF=L
MSGKIV  WTO    '--- *SKEY=... CONTROL CARD MISPLACED - BYPASSED.',     1
               ROUTCDE=11,DESC=7,MF=L
MSGPOV  WTO    '--- INT. POOL OVERFLOW (NCP VALUE TOO SMALL).',        1
               ROUTCDE=11,DESC=7,MF=L
MSGIPN  WTO    '--- SYSIN    - INVALID PAGE NUMBER : P=            ',  1
               ROUTCDE=11,DESC=7,MF=L
PIPN     EQU   MSGIPN+43,8
MSGLNG  WTO    '--- SYSIN    -         ... MEMBER NAME TOO LONG.',     1
               ROUTCDE=11,DESC=7,MF=L
PLNG     EQU   MSGLNG+19,8
MSGDTE  WTO    '--- GET CURRENT DAY ROUTINE ERROR (RC =      ).',      1
               ROUTCDE=11,DESC=7,MF=L
DTERC    EQU   MSGDTE+43,6
MSGERR  WTO    '--- SYSHELP  - .... I/O ERROR / MEMBER :         ',    1
               ROUTCDE=11,DESC=7,MF=L
PERT     EQU   MSGERR+19,4
PERM     EQU   MSGERR+45,8
MSGNFD  WTO    '--- SYSHELP  -          MEMBER NOT FOUND / BLDL.',     1
               ROUTCDE=11,DESC=7,MF=L
PNFD     EQU   MSGNFD+19,8
MSGBNM  WTO    '--- SYSHELP  -          INVALID MEMBER NAME / FIND.',  1
               ROUTCDE=11,DESC=7,MF=L
PBNM     EQU   MSGBNM+19,8
         SPACE 2
HELP    DCB    DSORG=PO,MACRF=R,EODAD=ENDHLP,DDNAME=SYSHELP
IN      DCB    DSORG=PS,MACRF=GM,LRECL=80,EODAD=ENDIN,DDNAME=SYSIN
DIAG    DCB    DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,DDNAME=SYSDIAG
OUT     DCB    DSORG=PS,MACRF=PM,RECFM=FBA,EXLST=PXLST,DDNAME=SYSPRINT
NDX     DCB    DSORG=PS,MACRF=PM,RECFM=FBA,EXLST=PXLST,DDNAME=SYSINDEX
         SPACE 1
PXLST    DC    0F'0',XL1'85',AL3(DCBEXIT)
         EJECT
*        HEADER/TRAILER PAGES ROUTINE
*        ----------------------------
         SPACE 1
        CNOP   0,4
         USING *,R9
HTPAGE   TM    SW2,SWPHT           HEADER/TRAILER REQUESTED ?
         BZR   R8                  NO
         STM   R0,R15,HTSAVE
         LTR   R0,R0
         BNZ   PTRAIL
         TM    SW1,SWIX            INDEX REQUESTED ?
         BZ    PTRAIL              NO
         LA    R5,NDX
         B     PTRAIL+L'PTRAIL
PTRAIL   LA    R5,OUT
         NI    SW2,255-SWPHTL
         USING IHADCB,R5
         TM    DCBOFLGS,DCBOFOPN
         BZ    PMNTX5
         DROP  R5
PMNTX1   L     R2,=A(MANUAL)
         XR    R3,R3
         LR    R4,R3
PMNTX2   ICM   R3,B'0001',0(R2)
         BZ    PMNTX4
         MVC   MTCC(1),2(R2)
         MVI   MTTXT,C' '
         MVC   MTTXT+1(L'MTTXT-1),MTTXT
         ICM   R4,B'0001',1(R2)
         BZ    PMNTX3
         LR    R1,R4
         BCTR  R1,0
         EX    R1,PMNTXMV
PMNTX3  PUT    (R5),MTLINE
         BCT   R3,PMNTX3
         LA    R2,3(R4,R2)
         B     PMNTX2
PMNTX4  PUT    (R5),DTLINE
         TM    SW2,SWPHTL
         BO    PMNTX5
         OI    SW2,SWPHTL
         B     PMNTX1
PMNTX5   LM    R0,R15,HTSAVE
         BR    R8
PMNTXMV  MVC   MTTXT(*-*),3(R2)    << EXECUTED >>
         SPACE 1
HTSAVE   DC    16F'0'
         SPACE 1
MTLINE   DS    0CL133
MTCC     DC    CL6' '
MTTXT    DC    CL70' ',CL57' '
DTLINE   DS    0CL133
         DC    CL13'0     DATE : '
DTDTE    DC    CL8'  /  /  ',CL4' (  '
DTDAY    DC    CL20' ',CL88') '
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
MANUAL   DS    0F
      @T ,1
      @T 3,0
      @T ,,' '
      @T ,,'                                    COMMISSION OF THE'
      @T ,,'               *                   EUROPEAN COMMUNITIES'
      @T ,,'          *         *'
      @T ,,' '
      @T ,,'      *                 *         J       R       C'
      @T ,,'                                   O       E       E'
      @T ,,'                                    I       S       N'
      @T ,,'    *                     *          N       E       T'
      @T ,,'                                      T       A       R'
      @T ,,'                                               R       E'
      @T ,,'      *                 *                       C'
      @T ,,'                                                 H'
      @T ,,'          *         *'
      @T ,,'               *'
      @T ,,'                                  - ISPRA ESTABLISHMENT -'
      @T ,,' '
      @T 3,0
      @T ,,'  TTTTT   SSSS  OOOOO          H   H  EEEEE  L      PPPP'
      @T ,,'   T    S      O   O          H   H  E      L      P   P'
      @T ,,'  T     SSS   O   O          HHHHH  EEEE   L      PPPP  '
      @T ,,' T        S  O   O          H   H  E      L      P      '
      @T ,,'T    SSSS   OOOOO          H   H  EEEEE  LLLLL  P       '
      @T 2,0
         DC    AL1(0)              END INDICATOR
         SPACE 2
TIODSECT DSECT
        IEFTIOT1
        DCBD   DSORG=(PS,PO),DEVD=DA
        IHAPDS
         SPACE 2
         END
