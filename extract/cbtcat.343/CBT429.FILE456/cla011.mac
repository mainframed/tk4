A011     TITLE 'CLEAN-UP - LIST DSNAME''S TABLE (A011).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A011    @XS    ALIAS=LISTDST
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    LD99                YES
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    LD99                NO, SKIP THE LIST
         SPACE 1
*        PRINT DSNAME'S TABLE.
         SPACE 1
        @LIST  PDSNL
         LA    R1,PDSNL
         ST    R1,SUBTLA           SET SUB-TITLE
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         LA    R3,LINE
         USING FPRL,R3
         XR    R5,R5
         MVI   WORKDS,C' '
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
LD1      CLI   0(R2),FMOFF         END OF TABLE?
         BE    LD90                YES
LD2      MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         MVC   ZDSN,DSNME          DATA-SET NAME
        @FILL  ZDSN
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BZ    P1                  NO
         MVC   ZDSV(2),=C'V='
         MVC   ZDSV+2(6),DSVOLSN   VOLUME SERIAL NUMBER
         B     P2
P1       MVC   ZDSV(L'ZDSV),=CL8' ...... '
P2       CLI   DSVORG,0
         BE    P3
         MVC   ZDSO,DSVORG
         B     P3A
P3       OC    DSMVCNT,DSMVCNT
         BZ    P4
P3A      LH    R1,DSMVCNT
         CVD   R1,WORKD
         MVC   WORKD(4),=X'40202120'
         ED    WORKD(4),WORKD+6
         MVC   ZDSM(L'ZDSM),WORKD+4-L'ZDSM
P4       OC    DSALTVO,DSALTVO     CAT. OR RACF VOLUME?
         BZ    P4A                 NO
         MVC   ZAV(2),=C'A='
         MVC   ZAV+2(6),DSALTVO    SET VOLUME SERIAL NUMBER
         B     P5
P4A      MVC   ZAV(L'ZAV),=CL8' ...... '
P5       CLI   DSRACF,XRD
         BNE   P5A
         MVC   ZR,=CL5'P-DIS'
         B     P6
P5A      CLI   DSRACF,XRG
         BNE   P5B
         MVC   ZR,=CL5'P-GEN'
         B     P6
P5B      CLI   DSRACF,0
         BE    P5C
         MVC   ZR+2(L'DSRACF),DSRACF
         B     P6
P5C      MVC   ZR,=CL5'-----'
P6       CLI   DSPROT,XPR
         BNE   P6A
         MVC   ZDSP,=CL3'SET'
         B     P7
P6A      CLI   DSPROT,0
         BE    P6B
         MVC   ZDSP+1(L'DSPROT),DSPROT
         B     P7
P6B      MVC   ZDSP,=CL3'---'
P7       CLI   DSACTION,YS
         BNE   P7A
         MVC   ZACT,=CL9'SCRATCH'
         B     P7X
P7A      CLI   DSACTION,YT
         BNE   P7B
         MVC   ZACT,=CL9'SC. TEMP.'
         B     P7X
P7B      CLI   DSACTION,YD
         BNE   P7C
         MVC   ZACT,=CL9'SC. DISK'
         B     P7X
P7C      CLI   DSACTION,YU
         BNE   P7D
         MVC   ZACT,=CL9'UNCATALOG'
         B     P7X
P7D      CLI   DSACTION,YR
         BNE   P7E
         MVC   ZACT,=CL9'DEL. RACF'
         B     P70
P7E      CLI   DSACTION,YA
         BNE   P7F
         MVC   ZACT,=CL9'ADD RACF'
         B     P70
P7F      CLI   DSACTION,YF
         BNE   P7G
         MVC   ZACT,=CL9'CH. RACF'
         B     P70
P7G      CLI   DSACTION,YN
         BNE   P7H
         MVC   ZACT,=CL9'OFF VTOC'
         B     P70
P7H      CLI   DSACTION,YV
         BNE   P7I
         MVC   ZACT,=CL9'ON VTOC'
         B     P70
P7I      CLI   DSACTION,YW
         BNE   P7J
         MVC   ZACT,=CL9'VTOC+RACF'
         B     P70
P7J      CLI   DSACTION,YC
         BNE   P7K
         MVC   ZACT,=CL9'CATALOG'
         B     P7X
P7K      CLI   DSACTION,YY
         BNE   P7Y
         MVC   ZACT,=CL9'MOD. RACF'
         MVC   ZDSV(2),=C'C='
         MVC   ZDSV+2(6),DSCVOL
         B     P70
P7X      CLI   DSTYPE,0            VSAM INFO?
         BE    P70                 NO
         MVI   DSACTION,YM
         B     P7Y0
P7Y      CLI   DSACTION,YM
         BNE   P7Y1
P7Y0     LA    R1,=CL7'CONTROL'
         B     P7YY
P7Y1     CLI   DSACTION,YE
         BNE   P7Y2
         LA    R1,=CL7'EXEMPT'
         B     P7YY
P7Y2     CLI   DSACTION,YZ
         BNE   P7Z
         LA    R1,=CL7'* ??? *'
P7YY     MVI   ZSEP,C'-'
         CLI   ZACT,C' '
         BNE   *+L'*+6
         MVC   ZACT,=CL9'- - - - -'
         MVC   ZRCVRY(L'ZRCVRY),0(R1)
         B     P8
P7Z      CLI   DSACTION,0
         BE    P70
         MVC   ZACT,=CL9'---- ----'
         MVC   ZACT+4(L'DSACTION),DSACTION
P70      CLI   DSRCVRY,0
         BE    P8
         CLI   DSRCVRY,YRU
         BNE   P70A
         MVI   ZSEP,C'U'
         B     P8
P70A     MVI   ZSEP,C'-'
         CLI   ZACT,C' '
         BNE   *+L'*+6
         MVC   ZACT,=CL9'- - - - -'
         CLI   DSRCVRY,YRR
         BNE   P70B
         MVC   ZRCVRY,=CL7'C. RACF'
         B     P8
P70B     CLI   DSRCVRY,YRA
         BNE   P70C
         MVC   ZRCVRY,=CL7'A. RACF'
         B     P8
P70C     CLI   DSRCVRY,YRM
         BNE   P70D
         MVC   ZRCVRY,=CL7'M-VOL'
         B     P8
P70D     CLI   DSRCVRY,YRS
         BNE   P70E
         MVC   ZRCVRY,=CL7'S-VOL'
         B     P8
P70E     MVC   ZRCVRY,=CL7'--- ---'
         MVC   ZRCVRY+3(L'DSRCVRY),DSRCVRY
P8       CLI   DSCAT,XCSR
         BNE   P8A
         MVC   ZC,=CL5'R-CAT'
         B     P9
P8A      CLI   DSCAT,XCSI
         BNE   P8B
         MVC   ZC,=CL5'I-CAT'
         B     P9
P8B      CLI   DSCAT,XCSL
         BNE   P8C
         MVC   ZC,=CL5'L-CAT'
         B     P9
P8C      CLI   DSCAT,0
         BE    P8D
         MVC   ZC+2(L'DSCAT),DSCAT
         B     P9
P8D      MVC   ZC,=CL5'-----'
P9       CLI   DSTYPE,XTV
         BNE   P9A
         MVC   ZDST,=CL4'VSAM'
         B     P10
P9A      CLI   DSTYPE,XTL
         BNE   P9B
         MVC   ZDST,=CL4'VS-L'
         B     P10
P9B      CLI   DSTYPE,0
         BE    P9C
         MVC   ZDST+1(L'DSTYPE),DSTYPE
         B     P10
P9C      MVC   ZDST,=CL4'----'
P10      CLI   DSRTYPE,XRTV
         BNE   P10A
         MVC   ZRT,=CL5'VSAM'
         B     P11
P10A     CLI   DSRTYPE,0
         BE    P10B
         MVC   ZRT+2(L'DSRTYPE),DSRTYPE
         B     P11
P10B     MVC   ZRT,=CL5'-----'
P11      CLI   DSUSE,XUM
         BNE   P11A
         MVC   ZDSU,=CL3'MDL'
         B     P12
P11A     CLI   DSUSE,XUG
         BNE   P11B
         MVC   ZDSU,=CL3'GDG'
         B     P12
P11B     CLI   DSUSE,XUI
         BNE   P11C
         MVC   ZDSU,=CL3'NGI'
         B     P12
P11C     CLI   DSUSE,0
         BE    P11D
         MVC   ZDSU+1(L'DSUSE),DSUSE
         B     P12
P11D     MVC   ZDSU,=CL3'---'
P12      CLI   DSRUSE,XRUM
         BNE   P12A
         MVC   ZRU,=CL3'MDL'
         B     LD10
P12A     CLI   DSRUSE,0
         BE    P12B
         MVC   ZRU+1(L'DSRUSE),DSRUSE
         B     LD10
P12B     MVC   ZRU,=CL3'---'
         DROP  R3
LD10     TM    RUSREQ,RUSKOFF      SKIP UNPROCESSED VOLUMES?
         BO    PRS1                YES
         TM    RUNIND,RUDSH        SHORT LIST WANTED?
         BZ    LD20                NO, COMPLETE
PRS1     LTR   R5,R5               GROUP TO BE PRINTED?
         BZ    PRS2                NO
         CLC   DSNME(L'DSNME),DSNME-DSTABDS(R5)   SAME DSNAME?
         BE    LD20                YES
PRS2     LR    R5,R2               RETAIN THIS ENTRY
PRS3     TM    RUNIND,RUCATMIS     ALL CATALOGS ARE PRESENT?
         BO    PRS4                NO
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BNZ   PRS4                YES
         TM    RUSREQ,RUSKOFF      SKIP UNPROCESSED VOLUMES?
         BO    PRS6                YES
         CLI   DSUSE,0             MODEL GDG, GDG OR NULL GDG INDEX?
         BE    PRS10               NO, LIST
         TM    RUNOPT,RUGDGL       LIST REQUESTED?
         BO    PRS10               YES, LIST
         B     PRS6
PRS4     CLI   DSVORG,0            MULTI-VOLUME OR FILE SEQ.?
         BNE   PRS10               YES, LIST
         CLI   DSUSE,0             MODEL GDG, GDG OR NULL GDG INDEX?
         BE    PRS5                NO
         TM    RUNOPT,RUGDGL       LIST REQUESTED?
         BO    PRS10               YES, LIST
         B     PRS6
PRS5     TM    RUNIND,RUCATMIS     ALL CATALOGS ARE PRESENT?
         BZ    PRS6                YES
         CLI   DSACTION,YT         SCRATCH TEMPORARY?
         BE    PRS10               YES, LIST
         B     PRS7
PRS6     CLI   DSACTION,0          SOME ACTION ON DATASET?
         BNE   PRS10               YES, LIST
         CLI   DSRCVRY,0           SOME RECOVERY ON DATASET?
         BNE   PRS10               YES, LIST
PRS7     TM    RUSREQ,RUSVSAM      LIST ALL VSAM REQUESTED?
         BZ    PRS8                NO
         CLI   DSTYPE,0            VSAM?
         BNE   PRS10               YES, LIST
         CLI   DSRTYPE,0           VSAM?
         BNE   PRS10               YES, LIST
PRS8     LA    R2,DSTABL(R2)       SEARCH GROUP
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    LD90                YES
         CLC   DSNME(L'DSNME),DSNME-DSTABDS(R5)   SAME DSNAME?
         BE    PRS3                YES
         XR    R5,R5               NO
         B     LD2
PRS10    LR    R2,R5               LIST THIS GROUP
LD20     CLC   WORKDS,DSNME
         BE    LD21
         MVC   WORKDS,DSNME
         B     LD22
LD21     MVI   LINE+ZDSN-FPRL,C' '
         MVC   LINE+ZDSN-FPRL+1(L'ZDSN-1),LINE+ZDSN-FPRL
LD22    @LIST  LINE
         CLI   DSANME,0            IS IT AN ALIAS?
         BE    LD24                NO
         TM    RUNIND,RUDSH        SHORT LIST WANTED?
         BZ    LD23                NO, COMPLETE
         CLC   DSMVCNT,=H'0'       YES, FIRST VOLUME REFERENCE?
         BNE   LD24                NO
LD23     MVI   LINE,C' '           YES, PRINT TRUE NAME
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LINE+11(15),=CL15'ALIAS NAME OF :'
         MVC   LINE+27(L'DSANME),DSANME
        @LIST  LINE
LD24     LA    R2,DSTABL(R2)       ADVANCE IN DSNAME TABLE
         B     LD1                 GO TO PRINT NEXT ONE
         DROP  R2
LD90     XC    SUBTLA,SUBTLA       INHIBIT SUB-TITLE
         L     R1,DSTABNE
         CVD   R1,WORKD
         ED    PDSNEN,WORKD+5
        @LIST  PDSNE
LD99    @XE
         EJECT
         DC    AL1(PDSNLL)
PDSNL    DC    C'1',CL44'<--- DATA-SET NAME ------------------------>'
         DC    C' ',CL32'<--- VTOC + CATALOG ----------->'
         DC    C' ',CL15'<--- RACF ---->'
         DC    C' ',CL8'-VOLUME-'
         DC    C' ',CL11'--ACTIONS--'
         DC    C' ',CL7'INT.REQ'
PDSNLL   EQU   *-PDSNL
         DC    AL1(PDSNEL)
PDSNE    DC    C'0END OF DATA-SET NAMES LIST '
         DC    C'(TOTAL OF DSNAME''S ENTRIES :'
PDSNEN   DC    XL6'402020202120',C').'
PDSNEL   EQU   *-PDSNE
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
        #FPRL
         SPACE 2
         END
