C002     TITLE 'CLEAN-UP - USER DATA-SET PROFILE ACTIONS (C002).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
C002    @XS
* AT ENTRY : R1 ---> ONE BYTE COMMUNICATION TO SELECT ACTION :
*                        0 - CHANGE ALTER TO UPDATE ACCESS.
*                        1 - CHANGE OWNER.
*                        2 - DELETE ALTER ACCESS.
*                        3 - ADD READ ACCESS FOR GROUP.
*                    THEN AT EXIT, SET TO INDICATE :
*                        0 - NORMAL RETURN.
*                        1 - ALTERNATE RETURN.
         SPACE 1
         USING LALNE,R2
         USING WKDS,R7
         LR    R3,R1
         CLI   0(R3),UPMAXN        CONTROL ENTRY
         BH    UP90
         XR    R1,R1
         IC    R1,ZERODS           GET LENGTH OF DSNAME
         BCTR  R1,0                -1 FOR MOVE
         MVI   USADS,C' '          CLEAR BEFORE MOVE
         MVC   USADS+1(L'USADS-1),USADS
         EX    R1,UPMO             MOVE DATA-SET NAME
        @FILL  USADS
         XR    R1,R1
         IC    R1,0(R3)            SELECT ENTRY
         MVI   0(R3),0             NORMAL RETURN
         SLL   R1,2
UPSW     NOP   UPSS(R1)
         OI    UPSW+1,ON
         LA    R15,USER
         STCM  R15,B'1111',ADOW+20
         ST    R15,TUSD+16
         LA    R15,ACL
         STCM  R15,B'1111',ADSF+20
         LA    R15,ACCNAM
         ST    R15,TUS+16
         B     UPSS(R1)
UPMO     MVC   USADS(*-*),ZERODSN  << EXECUTED >>
         SPACE 1
UPSS     B     UP00
         B     UP10
         B     UP20
         B     UP30
UPMAXN   EQU   ((*-UPSS)-L'UPSS)/L'UPSS
         SPACE 1
UP00     MVC   ACCHNM,LAAC         MOVE NAME
         TM    RUNIND,RUNORACF     NO RACF ACTION WANTED?
         BO    UP04                YES, BYPASS
         MVC   USAOP(L'USAOP),=CL25'ALTER ACCESS CHANGED USER'
         TM    RUNIND,RUTEST       TEST RUN?
         BO    UP02                YES
         LA    R0,ZERODS
         LA    R15,VSNERR
        ICHEINTY ALTERI,TYPE='DS',ENTRY=(R0),VOLUME=(R15),             C
               ACTIONS=(ADCH)
         LTR   R15,R15             TEST COMPLETION
         BZ    UP01
         MVC   USALTY(L'USALTY),=CL3'CHG'
         B     UP31
UP01     MVC   USARC(L'USARC),=CL6'O.K.'
         CLI   TUSI+1,0            IS USER ALREADY IN LIST?
         BE    UP02                YES, IT IS CHANGED
         MVC   USARC(L'USARC),=CL6'FAILED'
UP02     TM    STATUS,STUPT
         BO    UP03
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
UP03     MVC   USACHP,ACCHNM
         MVC   USAVOL,VSNERR
        @OUT   USADD
UP04     MVC   LADLM,=CL8'(CHANGE)'
         B     UP99
         SPACE 1
UP10     MVC   USAOP(L'USAOP),=CL25'SELF-OWNER SET - PREVIOUS'
         MVC   USOWNER(L'USOWNER),WORKVRA+2  GET OWNER OF DATA-SET
         TM    RUNIND,RUTEST       TEST RUN?
         BO    UP11                YES
         LA    R0,ZERODS
         LA    R15,VSNERR
        ICHEINTY ALTERI,TYPE='DS',ENTRY=(R0),VOLUME=(R15),             C
               ACTIONS=(ADOW)
         LTR   R15,R15             TEST COMPLETION
         BZ    *+L'*+10
         MVC   USALTY(L'USALTY),=CL3'OWN'
         B     UP31
         MVC   USARC(L'USARC),=CL6'O.K.'
         CLI   TUSO+1,0            IS USER ALREADY IN LIST?
         BE    UP11                YES, IT IS CHANGED
         MVC   USARC(L'USARC),=CL6'FAILED'
UP11     TM    STATUS,STUPT
         BO    UP12
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
UP12     MVC   USACHP,USOWNER
         MVC   USAVOL,VSNERR
        @OUT   USADD
         B     UP99
         SPACE 1
UP20     MVC   USAOP(L'USAOP),=CL25'DELETE ALTER TO SELF-USER'
         TM    RUNIND,RUTEST       TEST RUN?
         BO    UP21                YES
         LA    R0,ZERODS
         LA    R15,VSNERR
        ICHEINTY ALTER,TYPE='DS',ENTRY=(R0),VOLUME=(R15),              C
               ACTIONS=(ADEL)
         LTR   R15,R15             TEST COMPLETION
         BZ    *+L'*+10
         MVC   USALTY(L'USALTY),=CL3'DEL'
         B     UP31
         MVC   USARC(L'USARC),=CL6'O.K.'
         CLI   TUSD+1,0            IS USER ALREADY IN LIST?
         BE    UP21                NO, IT IS DELETED
         MVC   USARC(L'USARC),=CL6'FAILED'
UP21     TM    STATUS,STUPT
         BO    UP22
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
UP22     MVC   USACHP,USER
         MVC   USAVOL,VSNERR
        @OUT   USADD
         B     UP99
         SPACE 1
UP30     MVC   USAOP(L'USAOP),=CL25'ADD READ ACCESS FOR GROUP'
         TM    RUNIND,RUTEST       TEST RUN?
         BO    UP34                YES
         LA    R0,ZERODS
         LA    R15,VSNERR
        ICHEINTY ALTER,TYPE='DS',ENTRY=(R0),VOLUME=(R15),              C
               ACTIONS=(ADSF)
         LTR   R15,R15             TEST COMPLETION
         BZ    UP33
         MVC   USALTY(L'USALTY),=CL3'ADD'
UP31     MVC   USALDS,USADS
         CVD   R15,WORKD           RETURN CODE
         UNPK  USALRC,WORKD+6(2)
         OI    USALRC+1,C'0'
         MVI   USALAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   *+L'*+8             NO
         STC   R0,USALAC
         OI    USALAC,C'0'
         TM    STATUS,STUPT
         BO    UP32
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
UP32     MVC   USALVOL,VSNERR
        @OUT   USAL
UP33     MVC   USARC(L'USARC),=CL6'O.K.'
         CLI   TUS+1,0             WAS USER ALREADY IN LIST?
         BE    UP34                NO, IT IS ADDED
         MVC   USARC(L'USARC),=CL6'FAILED'
UP34     TM    STATUS,STUPT
         BO    UP35
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
UP35     MVC   USACHP,ACCNAM
         MVC   USAVOL,VSNERR
        @OUT   USADD
         EJECT
UP90     MVI   0(R3),1             ALTERNATE RETURN
         DROP  R2,R7
UP99    @XE
         EJECT
ADCH    ICHEACTN FIELD=ACL,FLDATA=(11,ACCH),TESTS=TUSI
ADOW    ICHEACTN FIELD=AUTHOR,FLDATA=(8,*-*),TESTS=TUSO
ADEL    ICHEACTN FIELD=USERID,FLDATA='DEL',TESTS=TUSD
ADSF    ICHEACTN FIELD=ACL,FLDATA=(11,*-*),TESTS=TUS
TUSI    ICHETEST FIELD=USERID,FLDATA=(8,ACCHNM),COND=EQ
TUSO    ICHETEST FIELD=AUTHOR,FLDATA=(8,USOWNER),COND=EQ
TUSD    ICHETEST FIELD=USERID,FLDATA=(8,*-*),COND=EQ
TUS     ICHETEST FIELD=USERID,FLDATA=(8,*-*),COND=NE
         SPACE 1
USOWNER  DC    CL8' '
ACCH     DS    0CL11               ACCESS LIST ENTRY
ACCHNM   DC    CL8' '
         DC    AL1(ACEEUPDT)       UPDATE ACCESS
         DC    XL2'0000'           ACCESS COUNT
         SPACE 1
         DC    AL1(USADDL)
USADD    DC    CL6' '
USADS    DC    CL44' ',C' ('
USAVOL   DC    CL6' ',C') '
USAOP    DC    CL25' ',C' : '
USACHP   DC    CL8' ',C' - '
USARC    DC    CL6'O.K.'
USADDL   EQU   *-USADD
         DC    AL1(USALL)
USAL     DC    CL3' ',CL3'-- '
USALDS   DC    CL44' ',C' ('
USALVOL  DC    CL6' ',C') RACF ALTER '
USALTY   DC    CL3' ',C' (RC='
USALRC   DC    CL2' ',C'/AC='
USALAC   DC    CL1' ',C').'
USALL    EQU   *-USAL
         SPACE 1
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        ICHRRPF
         EJECT
        PRINT  GEN
        #WKDS
         SPACE 2
        #LALNE
         SPACE 2
         END
