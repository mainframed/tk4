B008     TITLE 'CLEAN-UP - GET GROUP DATA-SETS PROFILES (B008).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B008    @XS
*        GET DATA-SET'S PROFILES OF THIS GROUP.
         SPACE 1
         LA    R1,WORKDS
         ST    R1,SDSPDL
         LA    R1,SVSNAP+4
         ST    R1,SDSPDS
         USING LGLNE,R2
         USING WKDS,R7
         MVI   LGSPD,C'-'
         XC    ZERODSN,ZERODSN
         XR    R0,R0
         IC    R0,GRNMEL           GET LENGTH OF GROUP
         XR    R15,R15
         L     R14,TBBYR           BYPASS RACF NAMES TABLE
         USING BYPRDS,R14
GD1      CLI   BYPRLG,FMON         END OF TABLE?
         BE    GD9                 YES
         IC    R15,BYPRLG
         CLR   R0,R15
         BNE   GD2
         LA    R1,BYPRPX(R15)
         CLI   0(R1),C'.'
         BNE   GD2
         LR    R1,R15
         BCTR  R1,0
         EX    R1,GDCL
         BE    GD3
GD2      LA    R14,BYPRPX+1(R15)
         B     GD1
GDCL     CLC   BYPRPX(*-*),GRNAME  << EXECUTED >>
GD3      MVI   LGDS,C'('
         LA    R15,LGDS+1
         ICM   R1,B'0111',BYPRDC
         BP    GD4
         ICM   R1,B'0111',BYPRGC
         BP    GD5
         MVC   0(3,R15),=CL3'NO '
         LA    R15,3(R15)
         B     GD6
GD4      CVD   R1,WORKD
         MVC   0(8,R15),=XL8'4020202020202120'
         ED    0(8,R15),WORKD+4
         LR    R1,R15
         CLI   0(R15),C' '
         BNE   *+L'*+8
         LA    R15,1(R15)
         B     *-12
         MVC   0(1,R1),0(R15)
         LA    R1,1(R1)
         LA    R15,1(R15)
         CLI   0(R15),C' '
         BNE   *-18
         MVC   0(10,R1),=CL10' DISCRETE '
         LA    R15,10(R1)
         ICM   R1,B'0111',BYPRGC
         BNP   GD6
         MVC   0(4,R15),=CL4'AND '
         LA    R15,4(R15)
GD5      CVD   R1,WORKD
         MVC   0(8,R15),=XL8'4020202020202120'
         ED    0(8,R15),WORKD+4
         LR    R1,R15
         CLI   0(R15),C' '
         BNE   *+L'*+8
         LA    R15,1(R15)
         B     *-12
         MVC   0(1,R1),0(R15)
         LA    R1,1(R1)
         LA    R15,1(R15)
         CLI   0(R15),C' '
         BNE   *-18
         MVC   0(9,R1),=CL9' GENERIC '
         LA    R15,9(R1)
GD6      MVC   0(28,R15),=CL28'DATA-SETS PROFILES BYPASSED)'
         B     GD13
         DROP  R14
GD9      LR    R1,R0
         STC   R1,ZERODS           SET LENGTH OF GROUP-ID
         STC   R1,TGRIDL
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,GDMV             EXECUTE MOVE
         MVC   TGRID(L'TGRID),ZERODSN   MOVE FOR TEST OF GROUP-ID
         LA    R1,TGRID+1(R1)
         MVI   0(R1),C'.'
         L     R7,WK2A             GET ALTERNATE RACF WORK AREA
         XC    WORKDDC,WORKDDC
         XC    WORKRBA,WORKRBA
GD10     TM    WORKRBA+6,X'80'
         BO    *+L'*+8
         NI    GDOSW+1,OFF
         B     *+L'*+4
         OI    GDOSW+1,ON
         XC    WORKRBA,WORKRBA
         XC    WORKRES(WORKBSL),WORKRES
         LA    R0,ZERODS
GDOSW    NOP   GDO1
        ICHEINTY ENTRY=(R0),WKAREA=(R7),GENERIC=NO,MF=(E,GDLST)
         B     GDO2
GDO1    ICHEINTY ENTRY=(R0),WKAREA=(R7),GENERIC=UNCOND,MF=(E,GDLST)
GDO2     CH    R15,=H'12'          END FOR GROUP?
         BE    GD12                YES
         LTR   R15,R15             TEST RETURN CODE
         BNZ   *+L'*+14            BRANCH IF ERROR
         XR    R1,R1
         IC    R1,TGRIDL
         EX    R1,GDTG             SAME GROUP?
         BNE   GD12                NO, END FOR GROUP
         LA    R14,WORKBS
         A     R14,WORKTL
         STCM  R14,B'0111',SDSPDN+1
         XR    R14,R14
         IC    R14,ZERODS
         LA    R14,1(R14)
         ST    R14,SDSPDG
         ST    R7,SDSPDW
        @SNAPS SN,SDSPDL,GDS
         LTR   R15,R15             TEST RETURN CODE
         BZ    GD20                O.K.
         CVD   R15,WORKD           RETURN CODE
         UNPK  GRDSRC,WORKD+6(2)
         OI    GRDSRC+1,C'0'
         MVI   GRDSAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   GD11                NO
         STC   R0,GRDSAC
         OI    GRDSAC,C'0'
GD11     MVC   GRDSNM(L'GRDSNM),GRDSNM-1
         XR    R1,R1
         IC    R1,GRNMEL           GET LENGTH OF GROUP
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,GDMD             EXECUTE MOVE
        @LIST  GRDS
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD99                NO
        @COPY  GRDS
         B     GD99
GDMV     MVC   ZERODSN(*-*),GRNAME << EXECUTED >>
GDTG     CLC   ZERODSN(*-*),TGRID  << EXECUTED >>
GDMD     MVC   GRDSNM(*-*),GRNAME  << EXECUTED >>
GD12     CLI   LGSPD,C' '          ANY DS-ENTRY FOR GROUP?
         BE    GD99                YES
         MVC   LGDS(14),=CL14'(NO DATA-SETS)'
GD13    @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD99                NO
        @COPY  LINE
         B     GD99                GET NEXT GROUP
GD20     MVI   GDTSW,0             RESET
         OC    WORKTL,WORKTL       DATA RETURNED?
         BZ    GD21                NO
         CLC   WORKVRA(2),=XL2'0008'    AUTHOR RECEIVED?
         BNE   GD21                NO
         CLC   WORKVRA+10(2),=XL2'0001' FLAG1 RECEIVED?
         BNE   GD21                NO
         CLC   WORKVRA+13(2),=XL2'0008' GROUP NAME RECEIVED?
         BNE   GD21                NO
         XR    R15,R15
         LA    R3,WORKVRA+23
         IC    R15,2(R3)           INST. DATA LENGTH
         LA    R3,3(R15,R3)        SKIP INST. DATA
         ICM   R15,B'0011',0(R3)   GET NUMBER OF VOLUMES
         BNZ   *+L'*+4
         OI    GDTSW,GDTSGF        NO VOLUMES = GENERIC
         LA    R4,2(R15,R3)        SKIP VOLUME(S)
         ICM   R15,B'0011',0(R4)
         LA    R4,2(R15,R4)        SKIP TYPE(S)
         CLC   0(2,R4),=XL2'0001'  UNIVACS RECEIVED?
         BNE   GD21                NO
         CLC   3(2,R4),=XL2'0002'  USERS/GROUPS NO. RECEIVED?
         BE    GD22                YES
GD21     MVC   GRERDSN(L'GRERDSN),GRERDSN-1
         XR    R1,R1
         IC    R1,GRNMEL           GET LENGTH OF GROUP
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,GDMO             EXECUTE MOVE
        @LIST  GRERDS
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD99                NO
        @COPY  GRERDS
         B     GD99                GET NEXT GROUP
GDMO     MVC   GRERDSN(*-*),GRNAME << EXECUTED >>
GDMVD    MVC   LGDS+13(*-*),0(R6)  << EXECUTED >>
GD22     XR    R1,R1
         IC    R1,ZERODS           GET LENGTH OF DSNAME
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,GDMN             MOVE DATA-SET NAME
        @FILL  LGDS
         MVI   LGSPV,C'('
         MVC   LGDSOW(L'LGDSOW),WORKVRA+2    MOVE OWNER OF DATA-SET
         MVI   LGSPW,C')'
         MVC   LGACT(16),=CL16'- UNIV. ACCESS :'
         CLI   2(R4),ACEEALTR      ALTER ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGACT+17(8),=CL8'ALTER'
         B     GD23
         CLI   2(R4),ACEECNTL      CONTROL ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGACT+17(8),=CL8'CONTROL'
         B     GD23
         CLI   2(R4),ACEEUPDT      UPDATE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGACT+17(8),=CL8'UPDATE'
         B     GD23
         CLI   2(R4),ACEEREAD      READ ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGACT+17(8),=CL8'READ'
         B     GD23
         CLI   2(R4),ACEENONE      NONE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGACT+17(8),=CL8'NONE'
         B     GD23
         LA    R1,LGACT+17         ANYTHING MIXED OR UNKNOWN
         LA    R15,2(R4)
         BAS   R14,DMPRTS
GD23     TM    WORKRBA+6,X'80'
         BO    *+L'*+10
         MVC   LGACT+26(5),=CL5'- (D)'
         B     *+L'*+6
         MVC   LGACT+26(5),=CL5'- (G)'
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD24                NO
        @COPY  LINE
GD24     XR    R4,R4
         IC    R4,WORKVRA+25       GET INST. DATA LENGTH
         LTR   R4,R4
         BZ    GD27
         MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LGDS(12),=CL12'INST. DATA :'
         LA    R5,60
         LA    R6,WORKVRA+26       POINT TO INST. DATA OR NEXT
GD25     CLR   R4,R5
         BNL   *+L'*+2
         LR    R5,R4
         BCTR  R5,0
         EX    R5,GDMVD
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD26                NO
        @COPY  LINE
GD26     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,1(R5)
         AR    R6,R5
         SR    R4,R5
         BNZ   GD25
GD27     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LGDS+25(18),=CL18'- CREATION GROUP :'
         MVC   LGDS+44(8),WORKVRA+15    MOVE GROUP NAME OF CREATION
         CLI   WORKVRA+12,X'80'    GROUP ACCESSIBLE?
         BNE   *+L'*+6
         MVC   LGDS+53(24),=CL24'++ GROUP DATA-SET ACCESS'
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD28                NO
        @COPY  LINE
GD28     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LGDS+25(8),=CL8'- VOL. :'
         XR    R4,R4
         ICM   R4,B'0011',0(R3)
         LA    R3,2(R3)
         BZ    GD32+L'GD32         NO VOLUMES
         USING DSPVOLSR,R3
GD29     LA    R5,LGDS+34
         LA    R15,5               NO. OF VOL. SER. BY LINE
GD30     MVC   0(6,R5),DSPVOLSR    SET THIS VOL. SER.
         LA    R5,7(R5)
         LA    R3,6(R3)            NEXT VOL. SER.
         SH    R4,=H'6'            ANY OTHER VOLUME?
         BNP   GD32                NO
         BCT   R15,GD30            YES
         DROP  R3
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD31                NO
        @COPY  LINE
GD31     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         B     GD29
GD32     XR    R4,R4
         ICM   R4,B'0011',0(R3)
         LA    R3,2(R3)
         TM    GDTSW,GDTSGF
         BO    GD34                BRANCH IF GENERIC
         USING DSPTYPE,R3
         TM    DSPTYPE,DSPTP       VSAM DATA-SET?
         BZ    GD33                NO
         MVC   0(6,R5),=CL6'(VSAM)'
         DROP  R3
GD33    @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD34                NO
        @COPY  LINE
GD34     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         AR    R3,R4               NOW SKIP UNIVACS ALREADY DONE
         ICM   R4,B'0011',0(R3)
         LA    R3,2(R3,R4)         START OF VARIABLE FIELDS
         ICM   R4,B'0011',2(R3)    GET NO. OF USERS IN ACCESS LIST
         BNZ   GD35
         LA    R3,8(R3)            NONE, SKIP ACLCNT-USERID-USERACS
         B     GD53+L'GD53
GD35     XR    R5,R5
         ICM   R5,B'0011',4(R3)    GET LENGTH OF NAMES LIST
         LA    R3,6(R3)            POINT TO NAMES LIST
         LA    R5,2(R5,R3)         POINT TO ACCESS CODES
         LTR   R4,R4               AT LEAST ONE?
         BNP   GD53                NO
         MVC   LGDS+25(32),=CL32'- USER''S ACCESS LIST - - - - - -'
GD40     MVC   LGACC(L'LGACC),0(R3)     MOVE NAME
         MVI   LGSPB,C':'
         CLI   0(R5),ACEEALTR      ALTER ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGAU,=CL8'ALTER'
         B     GD51
         CLI   0(R5),ACEECNTL      CONTROL ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGAU,=CL8'CONTROL'
         B     GD51
         CLI   0(R5),ACEEUPDT      UPDATE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGAU,=CL8'UPDATE'
         B     GD51
         CLI   0(R5),ACEEREAD      READ ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGAU,=CL8'READ'
         B     GD51
         CLI   0(R5),ACEENONE      NONE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LGAU,=CL8'NONE'
         B     GD51
         LA    R1,LGAU             ANYTHING MIXED OR UNKNOWN
         LR    R15,R5
         BAS   R14,DMPRTS
GD51    @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD52                NO
        @COPY  LINE
GD52     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R3,8(R3)            ADVANCE IN NAME LIST
         LA    R5,1(R5)            ADVANCE IN ACCESS CODES
         BCT   R4,GD40             LOOP IF ANY MORE
GD53     LR    R3,R5               POINT TO AUDIT FIELD
         CLC   0(2,R3),=XL2'0001'  AUDIT FLAGS RECEIVED?
         BNE   GD10                NO
         LA    R3,2(R3)            POINT TO AUDIT FLAGS
         CLI   0(R3),0             ANYTHING?
         BE    GD55                NO
         CLI   0(R3),X'10'         ONLY NONE?
         BE    GD55                YES
         MVC   LGDS+25(9),=CL9'- AUDIT :'
         LA    R5,LGDS+35
         BAS   R14,AUDITF
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD54                NO
        @COPY  LINE
GD54     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
GD55     LA    R3,1(R3)            POINT TO GLOBAL AUDIT FIELD
         CLC   0(2,R3),=XL2'0001'  GLOBAL AUDIT FLAGS RECEIVED?
         BNE   GD10                NO
         LA    R3,2(R3)            POINT TO GLOBAL AUDIT FLAGS
         CLI   0(R3),0             ANYTHING?
         BE    GD10                NO
         CLI   0(R3),X'10'         ONLY NONE?
         BE    GD10                YES
         MVC   LGDS+25(16),=CL16'- GLOBAL AUDIT :'
         LA    R5,LGDS+42
         BAS   R14,AUDITF
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GD56                NO
        @COPY  LINE
GD56     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         B     GD10
GDMN     MVC   LGDS(*-*),ZERODSN   << EXECUTED >>
         DROP  R2,R7
GD99    @XE
         EJECT
GDLST   ICHEINTY NEXTC,TYPE='DS',                                      C
               ACTIONS=(GDAU,GDF1,GDGR,GDIN,GDAV,GDAT,GDUA,GDSC,GDSL,  C
               GDSR,GDAD,GDGA),MF=L
         SPACE 1
GDAU    ICHEACTN FIELD=AUTHOR
GDF1    ICHEACTN FIELD=FLAG1
GDGR    ICHEACTN FIELD=GROUPNM
GDIN    ICHEACTN FIELD=INSTDATA
GDAV    ICHEACTN FIELD=VOLSER
GDAT    ICHEACTN FIELD=DSTYPE
GDUA    ICHEACTN FIELD=UNIVACS
GDSC    ICHEACTN FIELD=ACLCNT
GDSL    ICHEACTN FIELD=USERID
GDSR    ICHEACTN FIELD=USERACS
GDAD    ICHEACTN FIELD=AUDIT
GDGA    ICHEACTN FIELD=GAUDIT
         EJECT
SDSPDL   DC    A(*-*)
SDSPDG   DC    A(*-*)
SDSPDS   DC    A(*-*)
         DC    A(8)
SDSPDW   DC    A(*-*)
SDSPDN   DC    XL1'80',AL3(*-*)
         SPACE 1
TGRIDL   DC    XL1'0'
TGRID    DC    XL9'0'
GDTSW    DC    XL1'0'              SWITCHES
GDTSGF   EQU   X'01'                    GENERIC PROFILE
         SPACE 1
         DC    AL1(GRDSL)
GRDS     DC    CL3' ',CL3'-- '
GRDSNM   DC    CL8' ',C' : RACF READ TYPE=DS (RC='
GRDSRC   DC    CL2' ',C'/AC='
GRDSAC   DC    CL1' ',C').'
GRDSL    EQU   *-GRDS
         DC    AL1(GRERDSL)
GRERDS   DC    CL3' ',CL3'-- '
GRERDSN  DC    CL8' ',C' : RACF READ TYPE=DS '
         DC    C'(INVALID WORK AREA RECEIVED BACK).'
GRERDSL  EQU   *-GRERDS
         SPACE 2
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        ICHRRPF
         EJECT
        PRINT  GEN
        #BYPRDS
         SPACE 2
        #WKDS
         SPACE 2
        #LGLNE
         SPACE 2
         END
