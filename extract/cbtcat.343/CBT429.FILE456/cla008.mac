A008     TITLE 'CLEAN-UP - LOCATE DATA-SETS (A008).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A008    @XS    ALIAS=LOCATE
*        SCHEDULE N-LOC.VOL. REQUESTS.
         SPACE 1
         LA    R1,WORKDS
         ST    R1,VSAMCAT+4
         ST    R1,VSAMOBT+4
         ST    R1,SLOCL
         ST    R1,SOBTL
         LA    R1,LOCAREA
         ST    R1,VSAMCAT+12
         ST    R1,VSAMBLK+12
         ST    R1,VSAMOBT+12
         ST    R1,SLOCLCL
         ST    R1,SOBTLL
         LA    R1,SVSNAP+4
         ST    R1,SLOCLC
         ST    R1,SOBTLS
         TM    WHAT,WHEOF          END-OF-FILE?
         BO    VSC0                YES
         TM    WHAT,WHCD           CARD ALREADY READ?
         BO    NL1                 YES
NL0      NI    WHAT,255-WHCD
        $READ  MF=(E,RDIPLS)
         LTR   R15,R15             TEST RETURN CODE
         BM    NL91                NO CONTROL STATEMENTS
         BNZ   ERR21
NL1      LM    R3,R5,SCIN          SET TO SCAN INPUT CARD
         CLI   0(R3),C' '          SEARCH START OF A KEYWORD
         BNE   NL3
NL2      BXLE  R3,R4,*-8
         B     NL0
NL3      CLC   0(9,R3),=CL9'N-LOC.VOL'  IS IT 'N-LOC.VOL' KEYWORD?
         BNE   NL90                NO
         LA    R3,8(R3)
         CLI   1(R3),C'.'
         BNE   NLSW1
         LA    R3,1(R3)
NLSW1    NOP   NL4
         OI    NLSW1+1,ON
        @OUT   NLINM,=PL2'+40',U   PRINT HEADER
         L     R6,DALIST
         USING DYNLIST,R6
NL4     @OUT   PRCAR               PRINT INPUT CARD
NL5      BXLE  R3,R4,*+L'*+4
         BAS   R14,NLCN
         CLI   0(R3),C' '          SEARCH START OF ARGUMENT
         BE    NL5
         CLI   0(R3),C'('
         BNE   NLERR
NL6      BXLE  R3,R4,*+L'*+4
         BAS   R14,NLCN
         CLI   0(R3),C' '          SEARCH START OF PO-DSNAME
         BE    NL6
         CLI   0(R3),C')'
         BE    NL2
         CLI   0(R3),C'*'
         BE    NL13
         CLI   0(R3),C'/'
         BNE   NL7
         CLI   DSNAME,C' '
         BNE   NL14                PO-DSNAME AS PREVIOUS
         B     NLERR
NL7      MVI   WORKDS,C' '
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
         LA    R0,WORKDS+L'WORKDS
         LA    R1,WORKDS
NL8      CLI   0(R3),C'/'
         BE    NL9
         CLI   0(R3),C' '
         BE    NL9
         CLR   R1,R0
         BNL   NLERR
         MVC   0(1,R1),0(R3)       GET PO-DSNAME
         LA    R1,1(R1)
         BXLE  R3,R4,NL8
         BAS   R14,NLCN
NL9      CLC   DSNAME,WORKDS       SAME AS PREVIOUS?
         BE    NL12                YES
         TM    WHAT,WHPDSA         FIRST TIME?
         BZ    NL10                YES
         NI    WHAT,255-WHPDSO     NO, CLOSE AND FREE PREVIOUS
        $MCLOSE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR26
         NI    WHAT,255-WHPDSA
         XC    DSNAME,DSNAME
        $DYNALL (R6),,MF=(E,DYNPLS)
         LTR   R15,R15             TEST RETURN CODE
         BZ    NL10
         ICM   R1,B'1111',=CL4'FREE'
         LA    R15,DDNAME
         BAS   R14,DYNFAIL
         B     ERR26
NL10     MVC   DSNAME,WORKDS
         B     NL12
NL11     BAS   R14,NLCN
NL12     CLI   0(R3),C'/'
         BE    NL14
         CLI   0(R3),C' '
         BNE   NLERR
         BXLE  R3,R4,NL12
         B     NL11
NL13     OI    STATUS,STCISI
NL14     BXLE  R3,R4,*+L'*+4
         BAS   R14,NLCN
         CLI   0(R3),C' '          SEARCH START OF VOL. NAME
         BE    NL14
NL15     LA    R0,7
         LR    R1,R3
NL16     CLI   0(R3),C' '
         BE    NL19
         CLI   0(R3),C')'
         BE    NL19
         BCT   R0,NL18
NL17     XR    R15,R15             > 6 CHAR. LONG
         B     ERR51
NL18     BXLE  R3,R4,NL16
NL19     CH    R0,=H'7'
         BE    NL2                 THIS IS THE END OF NAMES
         CH    R0,=H'1'
         BNE   NL17                < 6 CHAR. LONG
         MVC   MBNAME+1(6),0(R1)   DISK VOLSER = MEMBER NAME
         L     R15,TBDDN           START OF DDNAME'S TABLE
         USING DDNT,R15
NL20     CLI   0(R15),FMOFF        END OF TABLE?
         BE    NL21                YES
         CLC   DDNVOL(L'DDNVOL),MBNAME+1     IS IT THIS VOLUME?
         BE    NL23                YES, IT IS ON-LINE
         LA    R15,DDNL(R15)       ADVANCE IN TABLE
         B     NL20                AND LOOP
         DROP  R15
NL21     L     R15,TBDVOL          START OF NOT-D. VOL. TABLE
NL22     CLI   0(R15),FMOFF        END OF TABLE?
         BE    NL25                YES, THIS VOLUME IS NOT MOUNTED
         CLC   0(L'UCBVOLI,R15),MBNAME+1     IS IT THIS VOLUME?
         BE    NL23                YES, IT IS ON-LINE
         LA    R15,TBDVOLL(R15)    ADVANCE IN TABLE
         B     NL22                AND LOOP
NL23     MVC   NLVONME(L'NLVONME),MBNAME+1
        @OUT   NLVON
         TM    STATUS,STCISI       IN-STREAM?
         BZ    NL45                NO
         NI    STATUS,255-STCISI
NL24     CLI   0(R3),C')'          END OF NAMES?
         BE    NL2                 YES
         BXLE  R3,R4,NL24
         BAS   R14,NLCN
         B     NL24
NL25     TM    STATUS,STCISI       IN-STREAM?
         BO    NL50                YES
         TM    WHAT,WHPDSA         ALLOCATED?
         BO    NL31                YES
         MVC   DDNAME(6),=CL6'SYSPDS'
         MVC   DSSTATUS(3),=CL3'SHR'
         MVC   DSNDISP(4),=CL4'KEEP'
         MVC   DSADISP(4),=CL4'KEEP'
        $DYNALL (R6),,MF=(E,DYNPLS)
         LTR   R15,R15             TEST RETURN CODE
         BZ    NL30
         ICM   R1,B'1111',=CL4'ALL.'
         LA    R15,DDNAME
         BAS   R14,DYNFAIL
         B     ERR23
NL30     OI    WHAT,WHPDSA
        $MOPEN MF=(E,MOPPLS)       OPEN PARTITIONNED
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR23
         OI    WHAT,WHPDSO
NL31     MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LINE+9(8),MBNAME    SET MEMBER NAME = VOLSER
         MVI   LINE+18,C':'
        $MFIND MF=(E,NLMF)         FIND MEMBER IN DATA-SET
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR24
NL32    $MREAD MF=(E,MRDPLS)       READ CARD FROM MEMBER
         LTR   R15,R15
         BM    NL45                END OF FILE
         BNZ   ERR25
        @OUT   LINE
         MVC   LINE+9(10),LINE+8
         LA    R1,LINE+20
         LM    R14,R15,LISCAN+4
         CLI   0(R1),C' '          SEARCH START OF DSNAME OR PREFIX
         BNE   NL34
NL33     BXLE  R1,R14,*-8
         B     NL32                GET NEXT CARD
NL34     LR    R0,R1               SET STARTING POSITION
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         B     NL36
NL35     CLI   0(R1),C' '          TEST IF END OF DSNAME?
         BE    NL40                YES
NL36     CLI   0(R1),C'/'          TEST IF '/' PRESENT?
         BE    NL37                YES
         BXLE  R1,R14,NL35         GO FOR NEXT CHARACTER
         B     NL40                ASSUME END OF DSNAME
NL37     ST    R1,LISCAN           SAVE ACTUAL POINTER
         LR    R15,R0              START OF PREFIX POINTER
         SR    R1,R0               LENGTH OF PREFIX
         BNP   NL44                NO PREFIX, ONLY '/', SKIP IT
         CH    R1,=H'44'           OVER MAXIMUM?
         BH    ERR52               YES
         BCTR  R1,0                -1 FOR COMPARE
NL38     CLI   0(R2),FMOFF         END OF TABLE?
         BE    NL44                YES
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BZ    NL39                NO
         EX    R1,NLCL             IS THIS PREFIX?
         BNE   NL39                NO
         MVI   DSACTION,YE         INDICATE EXEMPTED
         MVI   DSRCVRY,0           RESET ANY RECOVERY
NL39     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     NL38
NLCL     CLC   DSNME(*-*),0(R15)   << EXECUTED >>
NL40     ST    R1,LISCAN           SAVE ACTUAL POINTER
         LR    R15,R0              START OF DSNAME POINTER
         SR    R1,R0               LENGTH OF DSNAME
         CH    R1,=H'44'           OVER MAXIMUM?
         BH    ERR52               YES
         BCTR  R1,0                -1 FOR MOVE
         MVI   NLNMDSN,C' '
         MVC   NLNMDSN+1(L'NLNMDSN-1),NLNMDSN
         EX    R1,NLMV             SET DSNAME FOR COMPARE
NL41     CLI   0(R2),FMOFF         END OF TABLE?
         BE    NL43                YES
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BZ    NL42                NO
         CLC   DSNME,NLNMDSN       IS THIS DSNAME?
         BNE   NL42                NO
         MVI   DSACTION,YE         INDICATE EXEMPTED
         MVI   DSRCVRY,0           RESET ANY RECOVERY
         B     NL44                GO CONTINUE CARD SCAN
NL42     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     NL41                GO TEST NEXT ONE
NLMV     MVC   NLNMDSN(*-*),0(R15) << EXECUTED >>
         DROP  R2
NL43    @OUT   NLNMNF              DSNAME NOT FOUND
NL44     L     R1,LISCAN           RESTORE SCAN POINTERS
         LM    R14,R15,LISCAN+4
         CLR   R1,R15              CARD SCAN ENDED?
         BNH   NL33                NO
         B     NL32                TO NEXT MEMBER CARD
NL45     CLR   R3,R5               CARD SCAN ENDED?
         BNH   NL47                NO
NL46     BAS   R14,NLCN
NL47     CLI   0(R3),C')'          END OF NAMES?
         BE    NL2                 YES
         CLI   0(R3),C' '
         BNE   NL15                TO NEXT VOL. NAME
         BXLE  R3,R4,NL47
         B     NL46
         DROP  R6
NL50     NI    STATUS,255-STCISI
NL51     BXLE  R3,R4,*+L'*+4       START SEARCH OF DSNAME OR PREFIX
NL52     BAS   R14,NLCN
         CLI   0(R3),C' '
         BE    NL51
         CLI   0(R3),C')'          END OF NAMES?
         BE    NL2                 YES
         LR    R1,R3               SET STARTING POSITION
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         B     NL54
NL53     CLI   0(R3),C' '          TEST IF END OF DSNAME?
         BE    NL58                YES
         CLI   0(R3),C')'          END OF NAMES?
         BE    NL58                YES
NL54     CLI   0(R3),C'/'          TEST IF '/' PRESENT?
         BE    NL55                YES
         BXLE  R3,R4,NL53          GO FOR NEXT CHARACTER
         B     NL58                ASSUME END OF DSNAME
NL55     LR    R15,R3              COMPUTE LENGTH OF PREFIX
         SR    R15,R1
         BNP   NL62                NO PREFIX, ONLY '/', SKIP IT
         CH    R15,=H'44'          OVER MAXIMUM?
         BH    ERR52               YES
         BCTR  R15,0               -1 FOR COMPARE
NL56     CLI   0(R2),FMOFF         END OF TABLE?
         BE    NL62                YES
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BZ    NL57                NO
         EX    R15,NLRC            IS THIS PREFIX?
         BNE   NL57                NO
         MVI   DSACTION,YE         INDICATE EXEMPTED
         MVI   DSRCVRY,0           RESET ANY RECOVERY
NL57     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     NL56
NLRC     CLC   DSNME(*-*),0(R1)    << EXECUTED >>
NL58     LR    R15,R3              COMPUTE LENGTH OF DSNAME
         SR    R15,R1
         CH    R15,=H'44'          OVER MAXIMUM?
         BH    ERR52               YES
         BCTR  R15,0               -1 FOR MOVE
         MVI   NLNMDSN,C' '
         MVC   NLNMDSN+1(L'NLNMDSN-1),NLNMDSN
         EX    R15,NLMO            SET DSNAME FOR COMPARE
NL59     CLI   0(R2),FMOFF         END OF TABLE?
         BE    NL61                YES
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BZ    NL60                NO
         CLC   DSNME,NLNMDSN       IS THIS DSNAME?
         BNE   NL60                NO
         MVI   DSACTION,YE         INDICATE EXEMPTED
         MVI   DSRCVRY,0           RESET ANY RECOVERY
         B     NL62                GO CONTINUE CARD SCAN
NL60     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     NL59                GO TEST NEXT ONE
NLMO     MVC   NLNMDSN(*-*),0(R1)  << EXECUTED >>
         DROP  R2
NL61    @OUT   NLNMNF              DSNAME NOT FOUND
NL62     CLR   R3,R5               CARD SCAN ENDED?
         BH    NL52                YES
         CLI   0(R3),C')'          END OF NAMES?
         BNE   NL51                NO
         B     NL2
NLCN     ST    R14,SVRETCT         CONTINUATION
        $READ  MF=(E,RDIPLS)       GET NEXT CARD
         LTR   R15,R15
         BM    NLCN1
         BNZ   ERR21
        @OUT   PRCAR               PRINT INPUT CARD
         LM    R3,R5,SCIN          SET TO SCAN INPUT CARD
         L     R14,SVRETCT
         BR    R14                 GO BACK
NLCN1    LA    R1,CTERRMS
         B     NL80
NLERR    MVI   CARDIN,C' '
         MVC   CARDIN+1(L'CARDIN-1),CARDIN
         MVI   0(R3),C'$'          POSITION OF ERROR
        @OUT   PRCAR
         LA    R1,NLTERR           STATEMENT ERROR
NL80    @OUT   (R1)
         XR    R15,R15
         B     ERR22
NL90     OI    WHAT,WHCD           INDICATE "SYSIN" CARD ALREADY READ
         B     VSC0
NL91     OI    WHAT,WHEOF          INDICATE "SYSIN" END-OF-FILE
         SPACE 1
VSC0     TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    VSOB99              YES
         EJECT
*        LOOK  : 1. IF UTILITY DATA-SETS (FORMAT : *...)
*                   OR TEMPORARY DATA-SETS (FORMAT :
*                   SYS-----.T------.RA------) TO BE
*                   SCRATCHED.
*
*                2. IF DATA-SETS NOT ALREADY CATALOGED
*                   CAN BE LOCATED BY MASTER-CATALOG :
*
*                        R15  R0
*                        ---  --
*                          8   8 : NOT CATALOGED
*                          8   X : NOT CATALOGED / ONLY SOME INDEXES
*                          8   0 : GDG L-CAT (VSAM)
*                         12   X : GDG CVOL (R-CAT)
         SPACE 1
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         XC    WORKDS,WORKDS
VSC1     CLI   0(R2),FMOFF         END OF TABLE?
         BE    VSCSW4              YES
         CLI   DSNME,C'*'          IS IT AN UTILITY DSNAME?
         BE    VSC3                YES
         CLC   DSNME,DSNDATA       IS IT MY DAY ACCOUNT DSNAME?
         BE    VSC2                YES
         CLC   DSNME(3),=C'SYS'    IS IT A TEMPORARY DSNAME?
         BNE   VSC4                NO
         CLC   DSNME+8(2),=C'.T'
         BNE   VSC4                NO
         CLC   DSNME+16(3),=C'.RA'
         BNE   VSC4                NO
         CLI   DSNME+22,C'.'
         BNE   VSC4                NO
         XR    R3,R3
         IC    R3,JNMEL
         L     R4,JNMEA
         EX    R3,VSCCL            IS IT MYSELF?
         BNE   VSC3                NO
         LA    R4,DSNME+24(R3)
         CLI   0(R4),C'.'
         BNE   VSC4                NO
VSC2     MVI   DSACTION,YE         LET'S GO (EXEMPTED)
         B     VSC13               GET NEXT
VSC3     MVI   DSACTION,YT         SET SCRATCH TEMPORARY INDICATOR
         B     VSC13               GET NEXT
VSCCL    CLC   DSNME+23(*-*),0(R4) << EXECUTED >>
VSC4     CLC   WORKDS,DSNME        SAME AS PREVIOUS DSNAME?
         BE    VSC13               YES, DON'T LOCATE AGAIN
         MVC   WORKDS,DSNME
         CLI   DSACTION,0          ALREADY ACTION ON THIS DATA-SET?
         BNE   VSC13               YES
         CLI   DSCAT,0             ALREADY CATALOGED?
         BNE   VSC13               YES
         CLI   DSUSE,0             MODEL GDG ,GDG OR NULL GDG INDEX?
         BNE   VSC13               YES
         XR    R15,R15
         LR    R14,R2              RETAIN CURRENT ENTRY
VSC5     OC    DSVOLSN,DSVOLSN     ON A VOLUME?
         BZ    VSC6                NO
         LTR   R15,R15             FIRST TIME?
         BNZ   VSC6                NO
         LR    R15,R2              RETAIN FIRST USEFULL ENTRY
VSC6     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    VSC7                YES
         CLC   WORKDS,DSNME        SAME DATA-SET NAME?
         BNE   VSC7                NO
         CLI   DSCAT,0             ALREADY CATALOGED?
         BE    VSC5                NO, LOOK TO NEXT IF ANY
         B     VSC13
VSC7     LTR   R15,R15             ANY ENTRY RETAINED?
         BZ    VSC8                NO
         LR    R2,R15              GET USEFULL ENTRY
         B     VSC10
VSC8     LR    R2,R14              RESTORE CURRENT ENTRY
         CLI   DSRACF,XRD          IS IT A RACF DICRETE DEFINITION?
         BNE   VSC10               NO, LOCATE IT
         L     R3,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R3
VSC9     CLI   0(R3),FMOFF         END OF TABLE?
         BE    VSC13               YES, VOLUME NOT ANALYZED
         CLC   DDNVOL,DSALTVO      IS IT THIS VOLUME?
         BE    VSC10               YES, O.K.
         LA    R3,DDNL(R3)         ADVANCE IN TABLE
         B     VSC9                AND LOOP
         DROP  R3
VSC10    TM    RUNIND,RUCATMIS     ALL CATALOGS ARE PRESENT?
         BO    VSC13               NO, BYPASS LOCATE
         XC    LOCAREA(256),LOCAREA
         XC    LOCAREA+256(LOCAREAL-256),LOCAREA+256
         TM    RUNIND,RUTEST       TEST RUN?
         BZ    *+L'*+12            NO
         L     R1,CTLN
         AH    R1,=H'1'
         ST    R1,CTLN
        LOCATE VSAMCAT
        @SNAPS SF,SLOCL,LOC
         LTR   R15,R15             TEST LOCATE COMPLETION CODE
         BZ    VSC20
         CH    R15,=H'8'
         BNE   VSC14
         LTR   R0,R0               GDG VSAM?
         BZ    VSC11               YES
         CH    R0,=H'8'            NO, NOTHING CATALOGED?
         BE    VSC13               YES, LET'S GO
         B     VSC15               NO, THERE ARE SOME INDEXES
VSC11    MVI   DSCAT,XCSL          YES, SET CATALOGED BY LOCATE
VSC12    MVI   DSUSE,XUG           SET MODEL GDG
VSC13    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     VSC1
VSC14    CH    R15,=H'12'          GDG CVOL?
         BNE   VSC15               NO
         CLI   DSRACF,XRD          IS IT A RACF DISCRETE DEFINITION?
         BE    VSC13               YES, WILL BE DELETED LATER
         MVI   DSCAT,XCSR          SET AS READ FROM O.S. CATALOG
         B     VSC12
VSC15    CL    R15,=F'999'
         BNH   *+L'*+10
         MVC   LOCR15,=CL4' ---'
         B     *+L'*+16
         CVD   R15,WORKD
         MVC   LOCR15,MASKERR
         ED    LOCR15,WORKD+6
         CL    R0,=F'999'
         BNH   *+L'*+10
         MVC   LOCR0,=CL4' ---'
         B     *+L'*+16
         CVD   R0,WORKD
         MVC   LOCR0,MASKERR
         ED    LOCR0,WORKD+6
         MVC   LOCDSN,DSNME
        @FILL  LOCDSN
VSCSW1   NOP   VSC16
         OI    VSCSW1+1,ON
        @OUT   LOCT,=PL2'+40',U
VSC16   @OUT   LOCA
         B     VSC13
VSC20    OI    VSCSW2+1,ON
         CLC   WORKDS,DSNME        DSNAME CHANGED (ALIAS)?
         BE    VSC21               NO
         MVC   ZERODSN,WORKDS      SAVE THE TRUE NAME
         MVC   WORKDS,DSNME        RESTORE ORIGINAL NAME
         NI    VSCSW2+1,OFF        SET TO PROCESS ALIAS
VSC21    LR    R6,R2               RETAIN CURRENT ENTRY
         OI    VSCSW3+1,ON
         CLC   LOCAREA(2),=H'1'    SINGLE VOLUME?
         BE    VSC22               YES
         NI    VSCSW3+1,OFF        NO, MULTI-VOLUME
VSC22    LH    R3,LOCAREA          NUMBER OF VOLUMES IN LIST
         LA    R4,20               MAX. NUMBER BY WORK AREA
         LA    R5,LOCAREA+2        VOL. ENTRIES START ADDRESS
VSC23    CLC   DSVOLSN(L'DSVOLSN),4(R5) IS IT THIS VOLUME?
         BE    VSC28               YES
         CLC   DSALTVO(L'DSALTVO),4(R5) IS IT THIS ALTERNATE VOLUME?
         BE    VSC28               YES
         LA    R2,DSTABL(R2)       NO, ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    VSC24               YES
         CLC   DSNME,WORKDS        SAME DATA-SET NAME?
         BE    VSC23               YES
VSC24    L     R1,DSTABCT          NO, DSNAME'S TABLE OVERFLOW CONTROL
         BCT   R1,VSC25            CONTROL
         XR    R15,R15             DSNAME'S TABLE OVERFLOW
         B     ERR17
VSC25    ST    R1,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
         NI    VSCSW4+1,OFF
         OC    VSCATST,VSCATST     END OF TABLE ALREADY KNOWN?
         BZ    VSC26               NO
         L     R2,VSCATST          GET IT
         B     VSC27
VSC26    CLI   0(R2),FMOFF         END OF TABLE?
         BE    VSC27               YES
         LA    R2,DSTABL(R2)       NO, ADVANCE IN TABLE
         B     VSC26
VSC27    LA    R1,DSTABL(R2)       COMPUTE NEXT END OF TABLE
         ST    R1,VSCATST          STORE IT
         MVC   DSNME,WORKDS        MOVE NAME TO TABLE
         MVC   DSALTVO(L'DSALTVO),4(R5) MOVE VOLSER TO TABLE
VSC28    MVI   DSCAT,XCSL          SET CATALOGED BY LOCATE
         MVC   DSMVCNT(L'DSMVCNT),10(R5)     SET MULTI-VOL. COUNT
VSCSW2   B     VSCSW3
         MVC   DSANME,ZERODSN      SET TRUE NAME OF ALIAS NAME
VSCSW3   B     VSC29
         MVI   DSVORG,XOM          INDICATE MULTI-VOLUMES
VSC29    LR    R2,R6               RESTORE CURRENT ENTRY
         BCT   R3,VSC30            ANY OTHER VOL. ENTRY IN LIST?
         B     VSC13               NO, END OF LIST
VSC30    BCT   R4,VSC31            END OF WORK AREA?
         MVC   VSCTTR(L'VSCTTR),LOCAREA+252  TTR OF NEXT VCB
         OC    VSCTTR,VSCTTR       VALID TTR?
         BZ    ERR33               NO, WHAT?
         TM    RUNIND,RUTEST       TEST RUN?
         BZ    *+L'*+12            NO
         L     R1,CTLT
         AH    R1,=H'1'
         ST    R1,CTLT
        LOCATE VSAMBLK
        @SNAPS SF,SLOCLC,LOC
         LTR   R15,R15             TEST LOCATE COMPLETION CODE
         BZ    VSC22
         B     ERR33               WHAT?
VSC31    LA    R5,12(R5)           NEXT VOL. ENTRY
         B     VSC23
         DROP  R2
VSCSW4   B     VSOB0
         L     R0,DSTABOV          COMPUTE NO. OF ELEMENTS
         S     R0,DSTABCT
         ST    R0,DSTABNE
        @X     B004                SORT DSNAME'S TABLE
         SPACE 1
*        NOW TRY TO FIX INFORMATION ABOUT VSAM DATA-SETS.
         SPACE 1
VSOB0    TM    RUNIND,RUCATMIS     ALL CATALOGS ARE PRESENT?
         BO    VSOB99              NO, BYPASS OBTAIN
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
VSOB1    CLI   0(R2),FMOFF         END OF TABLE?
         BE    VSOB10              YES
         OC    DSVOLSN,DSVOLSN     ON A VOLUME?
         BNZ   VSOB9               YES, BYPASS
         CLI   DSCAT,XCSL          NO, LOCATE CATALOG INFORMATION?
         BNE   VSOB9               NO, BYPASS
         CLI   DSANME,0            YES, IT IS AN ALIAS?
         BNE   VSOB9               YES, BYPASS
         L     R3,TBDDN            NO, START OF DDNAME'S TABLE
         USING DDNT,R3
VSOB2    CLI   0(R3),FMOFF         END OF TABLE?
         BE    VSOB9               YES, VOLUME NOT ANALYZED
         CLC   DDNVOL,DSALTVO      IS IT THIS VOLUME?
         BE    VSOB3               YES, O.K.
         LA    R3,DDNL(R3)         ADVANCE IN TABLE
         B     VSOB2               AND LOOP
VSOB3    MVC   WORKDS,DSNME        SET DSNAME FOR OBTAIN
         MVC   OBTVOL,DSALTVO      SET VOLUME SERIAL NUMBER
         XC    LOCAREA(140),LOCAREA     CLEAR WORK AREA
         TM    RUNIND,RUTEST       TEST RUN?
         BZ    *+L'*+12            NO
         L     R1,CTOD
         AH    R1,=H'1'
         ST    R1,CTOD
        OBTAIN VSAMOBT
        @SNAPS SF,SOBTL,OBT
         LTR   R15,R15             TEST OBTAIN COMPLETION CODE
         BZ    VSOB7
         CH    R15,=H'8'
         BNE   VSOB5
         LA    R14,L'DSNME         THIS IS PROBABLY A VSAM-CATALOG
         LA    R15,DSNME+L'DSNME-1
VSOB4    CLI   0(R15),C' '
         BNE   VSOB6
         BCTR  R15,0
         BCT   R14,VSOB4
VSOB5    MVI   DSACTION,YZ         WHAT? INDICATE UNKNOWN
         B     VSOB9
VSOB6    LA    R0,L'DSALTVO+1
         SR    R14,R0
         BNP   VSOB5
         SR    R15,R0
         CLC   0(2,R15),=CL2'.V'
         BNE   VSOB5
         CLC   2(L'DSALTVO,R15),DSALTVO
         BNE   VSOB5
         XC    LOCAREA+96(5),LOCAREA+96 FORCE VSAM
VSOB7    MVC   DSDDN,DDNNME        ADJUST
         XC    DSALTVO,DSALTVO
         NI    VSOBSW+1,OFF
         OC    LOCAREA+96(5),LOCAREA+96 IS IT A VSAM?
         BNZ   VSOB8               NO
         MVI   DSTYPE,XTL          YES, INDICATE IT
         B     VSOB9
VSOB8    MVC   DSCBCC+3(5),LOCAREA+96   SET CCHHR OF DSCB
         MVC   DSUCBTY,DDNDTY      SET VOLUME TYPE
         DROP  R3
VSOB9    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     VSOB1               CONTINUE
VSOB10   OC    CTLN(L'CTLN+L'CTLT+L'CTOD),CTLN    ANY NOT ZERO?
         BZ    VSOBSW              BRANCH IF NONE
         L     R1,CTLN             DISPLAY COUNTERS
         CVD   R1,WORKD
         ED    MSTN,WORKD+4
         MVC   MSTM(STIDL),16(R8)  SET MODULE ALIAS NAME
        @OUT   MST,=PL2'+50'
         L     R1,CTLT
         CVD   R1,WORKD
         ED    MSTTN,WORKD+4
        @OUT   MSTT
         L     R1,CTOD
         CVD   R1,WORKD
         ED    MSTON,WORKD+4
        @OUT   MSTO
VSOBSW   B     VSOB99
        @X     B004                SORT DSNAME'S TABLE
VSOB99  @XE
         EJECT
VSAMCAT CAMLST NAME,*-*,,*-*
VSAMBLK CAMLST BLOCK,VSCTTR,,*-*
VSAMOBT CAMLST SEARCH,*-*,OBTVOL,*-*
SVRETCT  DC    F'0'
VSCATST  DC    A(*-*)
         SPACE 1
SLOCL    DC    A(*-*)
         DC    A(L'WORKDS)
SLOCLC   DC    A(*-*)
         DC    A(12)
SLOCLCL  DC    A(*-*)
         DC    XL1'80',AL3(LOCAREAL)
SOBTL    DC    A(*-*)
         DC    A(L'WORKDS)
         DC    A(OBTVOL)
         DC    A(L'OBTVOL)
SOBTLS   DC    A(*-*)
         DC    A(4)
SOBTLL   DC    A(*-*)
         DC    XL1'80',AL3(140)
         SPACE 1
NLMF    $MFIND MBNAME,MF=L
         EJECT
CTLN     DC    F'0'
CTLT     DC    F'0'
CTOD     DC    F'0'
MBNAME   DC    CL8'L '
OBTVOL   DC    CL6' '
VSCTTR   DC    XL3'0'
         SPACE 1
         DC    AL1(L'NLINM)
NLINM    DC    C'0NO-LOCATE REQUESTS :'
         DC    AL1(L'NLTERR)
NLTERR   DC    C' ===== SYNTAX ERROR ====='
         DC    AL1(L'CTERRMS)
CTERRMS  DC    C' ===== CONTINUATION MISSING ====='
         DC    AL1(NLVONL)
NLVON    DC    CL11' '
NLVONME  DC    CL6' ',C' IS ON-LINE.'
NLVONL   EQU   *-NLVON
         DC    AL1(NLNMNFL)
NLNMNF   DC    CL20' '
         DC    C'----- DATA-SET NOT FOUND : '
NLNMDSN  DC    CL44' '
NLNMNFL  EQU   *-NLNMNF
         DC    AL1(L'LOCT)
LOCT     DC    C'0LOCATE BY DSNAME :'
         DC    AL1(LLOCA)
LOCA     DC    CL6' '
LOCDSN   DC    CL44' ',C' - RC ='
LOCR15   DC    CL4' ',C', IX ='
LOCR0    DC    CL4' ',C'.'
LLOCA    EQU   *-LOCA
         DC    AL1(L'MST+L'MSTM+L'MSTN+1)
MST      DC    C'0 COUNTERS ('
MSTM     DC    C'        ) - LOC. BY NAME :'
MSTN     DC    XL8'4020202020202120',C','
         DC    AL1(L'MSTT+L'MSTTN+1)
MSTT     DC    C'                         LOC. BY TTR :'
MSTTN    DC    XL8'4020202020202120',C','
         DC    AL1(L'MSTO+L'MSTON+1)
MSTO     DC    C'                         OBTAIN DSCB :'
MSTON    DC    XL8'4020202020202120',C'.'
         SPACE 2
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
