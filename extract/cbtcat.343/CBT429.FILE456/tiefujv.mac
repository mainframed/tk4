TUJV     TITLE 'TEST IEFUJV SMF EXIT JCL SCAN.'
TUJV     START 0
         SPACE 1
        PRINT  NOGEN
$MAXDA   EQU   253       MAX. NO. OF SPOOL VOLUMES ($HASPEQU) / $JCT
IFGRPL   EQU   0         MVS ACB REQUEST PARAMETER LIST / $BUFFER
        $BUFFER
        $JCT
EUJCTSZ  EQU   JCTEND-JCTSTART
TUJV     CSECT                     RESTORE ORIGINAL CSECT.
        $CEPA
CEPALGTH EQU   CEPAUCOM+L'CEPAUCOM-CEPA
        PRINT  GEN
         SPACE 1
        $DEFREG
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* PURPOSE : JOB TO TEST THE JCL STATEMENTS SCAN PART OF THE IEFUJV    *
* --------- SMF EXIT BEFORE INCLUSION IN THE SYSTEM.                  *
*                                                                     *
* HOW TO CALL THE PROGRAM :                                           *
* -------------------------                                           *
*   //TXEQ    EXEC PGM=TUJV                                           *
*   //SYSACC    DD DSN=...ACC.INF.DATA-SET...,DISP=SHR                *
*   //SYSPRINT  DD SYSOUT=A                                           *
*   //SYSIN     DD *                                                  *
*    ...                                                              *
*    ... JCL STATEMENTS ...                                           *
*    ...                                                              *
*   /*                                                                *
*                                                                     *
* JCL STATEMENTS INPUT DATA :                                         *
* ---------------------------                                         *
*   ALL NORMAL JCL STATEMENTS ARE ACCEPTED AS INPUT TO THE PROGRAM,   *
*   WITH THE MODIFICATION OF THE CARD COLUMN 1 WHICH MUST REFLECT     *
*   THE TYPE OF JCL STATEMENT BEING PASSED TO IEFUJV EXIT. THIS       *
*   INDICATOR MUST BE AS FOLLOWS :                                    *
*        0 - INDICATES A NULL STATEMENT                               *
*        1 - INDICATES A JOB STATEMENT (AND HIS CONTINUATION)         *
*        2 - INDICATES AN EXEC STATEMENT (AND HIS CONTINUATION)       *
*        4 - INDICATES A DD STATEMENT (AND HIS CONTINUATION)          *
*        8 - INDICATES A PROC STATEMENT (AND HIS CONTINUATION)        *
*        C - INDICATES A JCL COMMENT CARD (5785-GAK / CJS)            *
*   A SPECIAL CARD WITH 'Z/ ' IN COLUMNS 1-3 IS USED TO RESET THE     *
*   WORK AREAS OF THE PROGRAM, IT IS NORMALLY PLACED BETWEEN JOBS.    *
*   ALL OTHER CARDS ARE SKIPPED.                                      *
*                                                                     *
* AUTHOR :     MOINIL P.A.                                            *
* --------     COMPUTING CENTRE                                       *
*              J.R.C. - ISPRA ESTABLISHMENT                           *
*              21020 ISPRA (VA), ITALY                                *
*                                                                     *
* NOTE : THIS PROGRAM IS NOT A CLEVER ONE, BUT IT IS ENOUGH TO TEST   *
* ------ THE JCL SCAN PART OF IEFUJV. SEE PROGRAM 'UJVT' TO TEST AND  *
*        GET STATISTICS ON THE COMPUTATIONS PART OF IEFUJV.           *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
        $XENT  BASE=R10
         L     R15,=V(SUJVTA)
         BASR  R14,R15
         LTR   R15,R15
         BZ    *+L'*+8
         LA    R9,256
         B     LEAVE
         L     R2,=V(UJVTADDR)
         ST    R1,0(R2)
INIT     XC    NELLIST(EUNELSZ),NELLIST      ARRAYS INITIALIZATION
         LA    R2,EUJCT
         LR    R6,R2
         LA    R3,EUJCTSZ
         LA    R4,*
         XR    R5,R5
         MVCL  R2,R4
         USING JCTSTART,R6
         LA    R7,CEPAAREA
         USING CEPA,R7
         XC    CEPA(CEPALGTH),CEPA
         MVC   CEPAJOBN(8),=CL8'TIEFUJV '    CEPA JOB NAME
         MVC   CEPASID(4),=CL4'SYSB'
         MVC   CEPAUSER(8),=CL8' '
         MVI   CEPAJC,C'9'
         LA    R1,JCTJMRJN
         ST    R1,NELJMR
         OI    NELOPSWT,NELSMF
         OI    NELPARMO,NELPGMN+NELACCT
         MVC   NELJPRTY(2),=CL2'07'
         MVC   NELTIME(6),=CL6'000500'
         MVC   NELREG(3),=CL3'800'
         MVI   NELCOMDS,C'3'
         MVI   NELLABEL,C'1'
         MVC   JCTID(4),=CL4'JCT '
         MVC   JCTUSER0,=F'10'     I/O FROM /*JOBPARM
         MVI   JCTMCLAS,C'A'       MSGCLASS FROM JOB CARD
         MVI   JCTJCLAS,C'A'       CLASS FROM JOB CARD
         MVI   JCTIPRIO,10         INITIAL JOB SEL. PRIORITY
         MVC   JCTETIME(4),=F'300' TIME IN SEC. FROM JOB CARD.
         MVC   JCTESTLN(4),=F'20000'    LINES FROM /*JOBPARM
         MVC   JCTJMRJN(8),=CL8'TIEFUJV '    JMR JOB NAME
         DROP  R6,R7
         MVC   MSG(3),=CL8' '
         EJECT
READ    $READ  CARD
         LTR   R15,R15
         BM    END
         BP    ER
         CLC   CARD(3),=C'Z/ '
         BNE   SCOM
         MVI   IND,16
         BAS   R8,EXUJV
         MVI   MSG,C'1'
         MVI   MSG+2,C'-'
         BAS   R8,PRMSG
         BAS   R8,PRSNP
         B     INIT
SCOM     CLC   CARD(3),=C'C/*'
         BNE   SJCL
         MVI   IND,128
         B     SETCD
SJCL     CLI   CARD+1,C'/'
         BNE   READ
         LA    R1,SIND
         LA    R0,L'SIND
         CLC   CARD(1),0(R1)
         BE    *+L'*+12
         LA    R1,1(R1)
         BCT   R0,*-14
         B     READ
         MVC   IND(1),CARD
         NI    IND,X'0F'
SETCD    MVI   CARD,C'/'
         MVI   LINE,C'1'
         MVI   LINE+2,C'+'
         BAS   R8,PRLNE
         BAS   R8,EXUJV
         BAS   R8,PRMSG
         MVI   LINE,C' '
         MVI   LINE+2,C' '
         BAS   R8,PRLNE
         BAS   R8,PRSNP
         B     READ
         EJECT
EP       LA    R9,4
         B     EXIT
ER       LA    R9,8
         B     EXIT
ES       LA    R9,12
         B     EXIT
END      CLI   IND,16
         BE    OK
         MVI   IND,16
         BAS   R8,EXUJV
         MVI   MSG,C'1'
         MVI   MSG+2,C'-'
         BAS   R8,PRMSG
         BAS   R8,PRSNP
OK       XR    R9,R9
EXIT     L     R15,=V(DUJVTA)
         BASR  R14,R15
        $RCLOSE
        $PCLOSE
LEAVE   $XRET  CC=(R9)
         EJECT
PRLNE    LA    R2,LINE             PRINT LINE.
         B     EXPRT
PRMSG    LA    R2,MSG              PRINT MESSAGE.
EXPRT   $PRINT (R2)
         LTR   R15,R15
         BNZ   EP
         BR    R8
         SPACE 1
PRSNP   $SNAP  STSNAP,LSNAP        SNAP WORK AREAS.
         LTR   R15,R15
         BNZ   ES
         BR    R8
         SPACE 1
EXUJV   CALL   IEFUJV,(CEPAAREA,CARD,IND,NELPARMO),VL
         STC   R15,MSG+L'MSG-2
         OI    MSG+L'MSG-2,C'0'
         BR    R8
         EJECT
*        CONSTANTS AND WORK-AREAS.
         SPACE 1
LSNAP    DC    A(SNL)
SIND     DC    C'01248'            VALID INDICATORS.
         SPACE 1
        CNOP   6,8
         SPACE 1
IND      DC    XL1'0'
         DC    AL1(L'MSG)
MSG      DC    C'    RC =  .'
         DC    AL1(L'LINE+L'CARD)
LINE     DC    CL4' '
STSNAP   EQU   *
CARD     DC    CL80' '             JCL STATEMENT. - - - - - - - - - - -
         SPACE 1
        CNOP   0,8
         DC    CL8'-CEPA-->'       ID.
CEPAAREA DC    (CEPALGTH)X'0'      COMMON EXIT PARAMETER LIST.  - - - -
         SPACE 1
        CNOP   0,8                 INTERPRETER ENTRANCE LIST. - - - - -
         DC    CL8'-NEL--->'       ID.
        IEFNEL SUBCOM=C
EUNELSZ  EQU   *-NELLIST
         SPACE 1
        CNOP   0,8                 JOB CONTROL TABLE (JES2).  - - - - -
         DC    CL8'-JCT--->'       ID.
EUJCT    DC    (EUJCTSZ)X'0'
         SPACE 1
        CNOP   0,8
SNL      EQU   *-STSNAP
         SPACE 1
        LTORG
         SPACE 1
         END
