A006     TITLE 'CLEAN-UP - READ CATALOGS CONTENTS (A006).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A006    @XS    ALIAS=RCATALS
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    CTN99               YES
        $DYNALL ,RCODES,MF=(L,CATDY)
         L     R3,TBCVOL           START OF CAT. VOL. TABLE
         USING CVLT,R3
CTN1     CLI   0(R3),FMOFF         END OF TABLE?
         BE    CTN90               YES
         TM    CVLIND,CVLMIS       IS VOLUME MISSING?
         BO    CTN20               YES, SKIP TO NEXT
         TM    CVLIND,CVLCTP       IS CATALOG PRESENT?
         BZ    CTN20               NO
         L     R4,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R4
CTN2     CLI   0(R4),FMOFF         END OF TABLE?
         BE    ERR58               YES (MUST NEVER OCCUR)
         CLC   DDNVOL,CVLVOL       IS IT THIS CATALOG VOLUME?
         BE    CTNSW1              YES
         LA    R4,DDNL(R4)         ADVANCE IN DDNAME'S TABLE
         B     CTN2
CTNSW1   NOP   CTN10
         OI    CTNSW1+1,ON
        @OUT   CATINFT,=PL2'+40',U
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    CTN10               NO
         TM    RUNIND,RUCSH        CAT. SHORT LIST REQUESTED?
         BZ    CTN10               NO
        @LIST  CATSHORT
CTN10    MVC   VSNERR,DDNVOL
         L     R6,DACTLG           ALLOCATE THE CATALOG
         USING DYNLIST,R6
         TM    CVLIND,CVLICF       IS IT AN ICF CATALOG?
         BO    CTN11               YES
         MVI   DSNAME,C' '         NO, O.S. CATALOG (SYSCTLG)
         MVC   DSNAME+1(L'DSNAME-1),DSNAME
         MVC   DSNAME(7),CVLNME
         MVC   DSUNIT,DDNUTY
         MVC   DSVOLSER,CVLVOL
         MVC   DDNAME(2),=CL2'OS'
         B     CTN12
CTN11    MVC   DSNAME,CVLNME
         MVC   DSUNIT,=CL8' '
         MVC   DSVOLSER(L'DSVOLSER),=CL8' '
         MVC   DDNAME(2),=CL2'IC'
CTN12    MVC   DDNAME+2(L'CVLVOL),CVLVOL
         MVC   DSSTATUS(3),=CL3'SHR'
        $DYNALL (R6),,MF=(E,CATDY)
         LTR   R15,R15             TEST RETURN CODE
         BZ    CTN13
         ICM   R1,B'1111',=CL4'ALL.'
         LA    R15,DDNAME
         BAS   R14,DYNFAIL
         B     ERR66
         DROP  R6
CTN13    TM    CVLIND,CVLICF       IS IT AN ICF CATALOG?
         BO    CTN14               YES
        @X     B009                GO READ O.S. CATALOG
         B     CTN15
CTN14   @X     B010                GO READ ICF CATALOG
         XC    ZERODSN,ZERODSN
CTN15    L     R6,DACTLG           DE-ALLOCATE THE CATALOG
         USING DYNLIST,R6
         XC    DSNAME,DSNAME
        $DYNALL (R6),,MF=(E,CATDY)
         LTR   R15,R15             TEST RETURN CODE
         BZ    CTN16
         ICM   R1,B'1111',=CL4'FREE'
         LA    R15,DDNAME
         BAS   R14,DYNFAIL
         B     ERR66
         DROP  R6
CTN16    MVC   VSNERR,=CL6' '
CTN20    LA    R3,CVLL(R3)         ADVANCE IN CAT. VOL. TABLE
         B     CTN1
         DROP  R3,R4
         EJECT
CTN90    L     R0,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
         L     R1,DSTABOV          COMPUTE NO. OF ELEMENTS
         SR    R1,R0
         ST    R1,DSTABNE
        @X     B004                SORT DSNAME'S TABLE
         SPACE 1
CTN99   @XE
         SPACE 2
CATDY   $DYNALL MF=L
         SPACE 1
         DC    AL1(L'CATINFT)
CATINFT  DC    C'0CATALOG''S STATUS CHECK LIST : '
         DC    AL1(L'CATSHORT)
CATSHORT DC    C'0WARNING : ONLY CATALOGS SHORT LIST IS REQUESTED.'
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
