B005     TITLE 'CLEAN-UP - LOCATE CATALOG VOLUME (B005).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B005    @XS
*        AT ENTRY :   R3 ------------> DC   AL1(L'NAME)
*                               NAME   DC   CL'...NAME...'
*                        WITH R4 = 0 : RACF USER CALL.
*                                  + : RACF GROUP CALL.
*                     R3 = 0 : CHECK LIST CALL
         SPACE 1
         LTR   R3,R3               CALL FOR WHAT?
         BZ    GCV20               GO TO CHECK LIST
         SPACE 1
*        LOCATE CATALOG VOLUME IF ANY.
*              R2 = A(LALNE) OR A(LGLNE)
         SPACE 1
GCVSW    NOP   GCV01
         OI    GCVSW+1,ON
         LA    R1,WORKDS
         ST    R1,GCVLOC+4
         ST    R1,SGCVL
         LA    R1,LOCAREA
         ST    R1,GCVLOC+12
         ST    R1,SGCVLL
         LA    R1,SVSNAP+4
         ST    R1,SGCVLS
GCV01    MVI   WORKDS,C' '
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
         XR    R1,R1
         IC    R1,0(R3)            GET USER OR GROUP NAME LENGTH
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,GCVMV            SET USER OR GROUP NAME
         L     R1,TBGVOL           CVOL POINTERS TABLE
GCV02    CLI   0(R1),FMOFF         END OF TABLE?
         BE    GCV03               YES
         USING CVPDS,R1
         CLC   CVPNAME(L'CVPNAME),WORKDS     THIS ONE?
         BE    *+L'*+8             YES
         LA    R1,CVPL(R1)         NO, NEXT ELEMENT
         B     GCV02
         OI    CVPFL,CVPRACF       SAY RACF DEFINED
         B     GCV05
         DROP  R1
GCVMV    MVC   WORKDS(*-*),1(R3)   << EXECUTED >>
GCV03    ST    R1,GCTBGPT          SAVE END OF TABLE POINTER
         MVC   WORKD(L'WORKD),WORKDS    AND CVOL POINTER NAME
         XC    LOCAREA(256),LOCAREA
         XC    LOCAREA+256(LOCAREAL-256),LOCAREA+256
        LOCATE GCVLOC
        @SNAPS SN,SGCVL,GCV
         LTR   R15,R15             TEST LOCATE COMPLETION CODE
         BZ    GCV04
         TM    RUNSN,SNGCV         SNAP ALREADY REQUESTED AND DONE?
         BO    GCV99               YES, DON'T DO IT AGAIN
        @SNAPS SN,SGCVL,ECV
         B     GCV99
GCV04    L     R1,GCTBGPT          GET END OF TABLE POINTER
         LA    R15,CVPL(R1)
         CL    R15,TBGVOLE         ANY OVERFLOW?
         BL    *+L'*+6             NO
         XR    R15,R15             YES
         B     ERR64
         USING CVPDS,R1
         MVC   CVPNAME(L'CVPNAME),WORKD ADD IN TABLE
         MVC   CVPVOL(L'CVPVOL),LOCAREA+6    SET CATALOG VOLUME
         OI    CVPFL,CVPRACF       SAY RACF DEFINED
         MVI   0(R15),FMOFF        SET FINAL MARKER
         USING LALNE,R2 <--- IDEM ---> USING LGLNE,R2
GCV05    MVC   LAVAR(25),=CL25'- CVOL OR ALIAS POINTER :'
         MVC   LAVAR+26(L'CVPVOL),CVPVOL     GET CATALOG VOLUME
         DROP  R1,R2
         LTR   R4,R4               WHO CALLED?
         BNZ   GCV10               IT IS RACFGRP
         TM    RUNIND,RUNOLIST     IT IS RACFUSR, LIST REQUESTED?
         BO    GCV11               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    GCV11               YES
GCV10   @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GCV11               NO
        @COPY  LINE
GCV11    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         B     GCV99
         SPACE 1
*        CHECK LIST.
         SPACE 1
GCV20    TM    RUNIND,RUNOLIST     LIST REQUESTED?
         BO    GCV99               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    GCV99               YES
         L     R2,TBGVOL           START OF CVOL PTR. TABLE
         CLI   0(R2),FMOFF         EMPTY?
         BE    GCV99               YES
         XR    R3,R3               RESET TOTAL COUNT
        @LIST  GCVTIT,,U
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GCV21               NO
        @COPY  GCVTIT,,U
GCV21    MVI   LINE,C'0'           CLEAR LINE
         MVI   LINE+1,C' '
         MVC   LINE+2(L'LINE-2),LINE+1
         LA    R4,LINE+6           STARTING ADDRESS IN LINE
         LA    R0,6                MAX. BY LINE
         USING CVPDS,R2
GCV22    L     R2,TBGVOL           START OF CVOL PTR. TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    GCV30               YES
         TM    CVPFL,CVPRACF       NO, RACF DEFINED?
         BO    *+L'*+8             YES
         LA    R2,CVPL(R2)         NO, NEXT ELEMENT
         B     GCV22+L'GCV22
         XR    R5,R5               RESET COUNT
         MVC   0(L'CVPVOL,R4),CVPVOL    GET VOLUME SERIAL
GCV23    A     R5,=F'1'
         MVI   CVPFL,0             SET PROCESSED
GCV24    LA    R2,CVPL(R2)         NEXT ELEMENT
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    GCV25               YES
         CLC   0(L'CVPVOL,R4),CVPVOL    NO, SAME VOLUME SERIAL?
         BE    GCV23               YES
         B     GCV24               NO
GCV25    MVI   L'CVPVOL+1(R4),C':'
         AR    R3,R5               COMPUTE TOTAL COUNT
         CVD   R5,WORKD
         CLC   WORKD(5),=XL5'0'
         BE    GCV26
         MVC   L'CVPVOL+3(5,R4),=CL5'>100K'
         B     GCV27
GCV26    MVC   L'CVPVOL+3(5,R4),=XL5'2020202120'
         ED    L'CVPVOL+2(6,R4),WORKD+5
GCV27    BCT   R0,GCV28
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GCV21               NO
        @COPY  LINE
         B     GCV21
GCV28    LA    R4,L'CVPVOL+12(R4)
         B     GCV22
         DROP  R2
GCV30    CLI   LINE+6,C' '         ANYTHING IN LINE?
         BE    GCV31               NO
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GCV31               NO
        @COPY  LINE
GCV31    CVD   R3,WORKD            TOTAL COUNT
         CLC   WORKD(4),=XL5'0'
         BE    GCV32
         MVC   GCVTOTN+1(8),=CL8'>10M.'
         B     GCV33
GCV32    ED    GCVTOTN,WORKD+4
GCV33   @LIST  GCVTOT
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GCV40               NO
        @COPY  GCVTOT
         USING CVPDS,R2
GCV40    L     R2,TBGVOL           START OF CVOL PTR. TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    GCV90               YES
         TM    CVPFL,CVPOS+CVPICF+CVPLOC     NO, ANY OTHER?
         BNZ   *+L'*+8             YES
         LA    R2,CVPL(R2)         NO, NEXT ELEMENT
         B     GCV40+L'GCV40
        @LIST  GCVOTH,,U
GCV41    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R4,LINE+6           STARTING ADDRESS IN LINE
         LA    R0,4                MAX. BY LINE
GCV42    MVC   0(L'CVPNAME,R4),CVPNAME  CVOL POINTER
         MVI   L'CVPNAME+1(R4),C':'
         MVC   L'CVPNAME+3(L'CVPVOL,R4),CVPVOL    CATALOG VOL. SERIAL
         TM    CVPFL,CVPOS
         BZ    *+L'*+10
         MVC   L'CVPNAME+L'CVPVOL+4(3,R4),=CL3'(O)'
         B     GCV43
         TM    CVPFL,CVPICF
         BZ    *+L'*+10
         MVC   L'CVPNAME+L'CVPVOL+4(3,R4),=CL3'(I)'
         B     GCV43
         MVC   L'CVPNAME+L'CVPVOL+4(3,R4),=CL3'(L)'
GCV43    LA    R2,CVPL(R2)         NEXT ELEMENT
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    GCV45               YES
         TM    CVPFL,CVPOS+CVPICF+CVPLOC     NO, ANY OTHER?
         BZ    GCV43               NO
         BCT   R0,GCV44
        @LIST  LINE
         B     GCV41
GCV44    LA    R4,L'CVPNAME+L'CVPVOL+11(R4)
         B     GCV42
         DROP  R2
GCV45   @LIST  LINE
GCV90    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
GCV99   @XE
         EJECT
GCVLOC  CAMLST NAME,*-*,,*-*
         SPACE 1
SGCVL    DC    A(*-*)
         DC    A(L'WORKDS)
SGCVLS   DC    A(*-*)
         DC    A(12)
SGCVLL   DC    A(*-*)
         DC    XL1'80',AL3(LOCAREAL)
         SPACE 1
GCTBGPT  DC    F'0'
         SPACE 1
         DC    AL1(L'GCVTIT)
GCVTIT   DC    C'1RACF / CVOL OR ALIAS POINTERS STATUS CHECK LIST :'
         DC    AL1(GCVTOTL)
GCVTOT   DC    C'0RACF / TOTAL OF CVOL OR ALIAS POINTERS :'
GCVTOTN  DC    XL8'4020202020202120',C'.'
GCVTOTL  EQU   *-GCVTOT
         DC    AL1(L'GCVOTH)
GCVOTH   DC    C'0LIST OF CVOLS OR ALIASES NOT RACF USER OR GROUP :'
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
        #LALNE
         SPACE 2
         END
