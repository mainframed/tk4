B013     TITLE 'CLEAN-UP - RACF GENERIC PROFILES PROCESS (B013).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B013    @XS
         L     R1,TBRGDS           HLQ NAMES TABLE
         CLI   0(R1),FMOFF         END OF TABLE?
         BE    TPM99               YES, TABLE EMPTY (NO GENERICS)
         ST    R1,TPMRG            SET CURRENT POINTER
         LA    R1,WORKDS+L'WORKDS-1     END OF DS-NAME (TEST)
         ST    R1,TPMMAX
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
TPM1     CLI   DSANME,0            IS THERE A TRUE NAME (ALIAS)?
         BE    *+L'*+10            NO
         MVC   WORKDS,DSANME       YES, USE IT
         B     *+L'*+6
         MVC   WORKDS,DSNME        USE DS-NAME
         LR    R5,R2               RETAIN STARTING GROUP ENTRY
         NI    TPMSW+1,OFF
TPM2     CLI   DSRACF,0            ALREADY DEFINED BY RACF DISCRETE?
         BE    *+L'*+4             NO
         OI    TPMSW+1,ON          YES, SKIP THIS GROUP
         LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    TPMSW               YES
         CLI   DSANME,0            IS THERE A TRUE NAME (ALIAS)?
         BE    *+L'*+10            NO
         CLC   WORKDS,DSANME       YES, USE IT
         B     *+L'*+6
         CLC   WORKDS,DSNME        USE DS-NAME
         BE    TPM2                BRANCH IF EQUAL TO PREVIOUS
TPMSW    NOP   TPM31
         LR    R6,R2               RETAIN ENDING GROUP ENTRY
         LR    R2,R5               RESTORE STARTING GROUP ENTRY
         LA    R4,WORKDS           START OF DS-NAME
         LA    R0,L'WORKD          EXTRACT HIGH-LEVEL INDEX
         LA    R14,WORKD
         MVC   WORKD,=CL8' '
TPM3     MVC   0(1,R14),0(R4)
         LA    R14,1(R14)
         LA    R4,1(R4)
         CLI   0(R4),C' '
         BE    TPM30               NO HIGH-LEVEL NAME, SO NOT FOUND
         CLI   0(R4),C'.'
         BE    *+L'*+8
         BCT   R0,TPM3
         B     ERR75               ERROR, HIGH-LEVEL NAME TOO LONG
         L     R1,TPMRG            HLQ NAMES TABLE CURRENT POINTER
TPM4     CLC   WORKD(8),0(R1)      THIS HLQ NAME?
         BL    TPM30               NOT FOUND
         BE    TPM5                YES
         LA    R1,NGDL(R1)         SEE FORWARD
         CLI   0(R1),FMOFF         END OF TABLE?
         BNE   TPM4                NO, CONTINUE
         B     TPM99               YES, NOTHING MORE
TPM5     CL    R1,TPMRG            CURRENT POINTER CHANGED?
         BE    *+L'*+4             NO
         ST    R1,TPMRG            YES, STORE IT
         ICM   R7,B'1111',8(R1)    GET START OF GENERIC NAMES
         LA    R4,1(R4)            START DS-NAME AFTER HLQ
TPM10    MVI   TPSCDSN,C' '
         MVC   TPSCDSN+1(L'TPSCDSN-1),TPSCDSN
         XR    R1,R1
         IC    R1,0(R7)
         EX    R1,TPMMV
         LA    R7,1(R1,R7)         POINT TO NEXT GENERIC
         LA    R1,TPSCDSN
         LR    R3,R4               START OF DS-NAME SCAN
TPM11    CLC   0(1,R3),0(R1)       TEST UNTIL THEY ARE EQUAL
         BNE   TPM13               START OF A DIFFERENCE
TPM12    LA    R3,1(R3)            TO NEXT CHAR
         CL    R3,TPMMAX           END REACHED?
         BH    TPM20               YES, MATCH FOUND
         LA    R1,1(R1)            TO NEXT CHAR
         B     TPM11
TPMMV    MVC   TPSCDS(*-*),0(R7)   << EXECUTED >>
TPM13    CLI   0(R1),C'%'          THE EVERYTHING CHAR?
         BE    TPM15               YES
         CLI   0(R1),C'*'          THE SOMESTRING CHAR?
         BNE   TPM16               NO, NEXT GENERIC
         CLC   0(2,R1),=C'* '      YES, WHAT FOLLOWS?
         BE    TPM20               EVERYTHING IS GOOD, MATCH FOUND
TPM14    CLI   0(R3),C'.'          SCAN TO FIND A POINT
         BNE   *+L'*+4
         BCT   R3,TPM12            FOUND, BACK ONE CHAR, CONTINUE
         LA    R3,1(R3)            TO NEXT CHAR
         CL    R3,TPMMAX           END REACHED?
         BL    TPM14               NO, CONTINUE SEARCH
         B     TPM16               YES, NO POINT FOUND, NEXT GENERIC
TPM15    CLI   0(R3),C'.'          IS IT A POINT?
         BE    TPM16               YES, INVALID, NEXT GENERIC
         CLI   0(R3),C' '          IS IT A BLANK (END)?
         BNE   TPM12               NO, OK, CONTINUE TO TEST MATCH
TPM16    CLI   0(R7),FMON          END OF GENERICS?
         BNE   TPM10               NO, CONTINUE WITH NEXT GENERIC
         B     TPM30               YES, NO MATCH FOUND
TPM20    MVI   DSRACF,XRG          SET RACF GENERIC PROTECTED
TPM21    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLR   R2,R6               END OF GROUP?
         BE    TPM31               YES
         MVC   DSRACF(L'DSRACF),DSRACF-DSTABDS(R5)     COPY RACF STATUS
         B     TPM21
         DROP  R2
TPM30    LR    R2,R6               SKIP TO END OF GROUP
TPM31    CLI   0(R2),FMOFF         END OF TABLE?
         BNE   TPM1                NO, CONTINUE
TPM99   @XE    ,                   GO BACK
         EJECT
TPMRG    DC    A(*-*)              HLQ NAMES TABLE CURRENT POINTER
TPMMAX   DC    A(*-*)
TPSCDS   DC    AL1(*-*)
TPSCDSN  DC    CL44' '
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
