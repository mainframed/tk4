A012     TITLE 'CLEAN-UP - RACF RECOVERY PROCESSING (A012).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A012    @XS    ALIAS=RACFRCV
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    RRP99               YES
         TM    STATUS,STBRACF      RACF MUST BE BYPASSED?
         BO    RRP99               YES
         SPACE 1
*        RACF (DISCRETE PROFILES ONLY) RECOVERY PROCESSING.
         SPACE 1
         TM    RUNIND,RUCATMIS     ALL CATALOGS ARE PRESENT?
         BO    RRP99               NO
         TM    RUNIND,RUNORACF     NO RACF ACTION WANTED?
         BZ    RRP1                NO, REQUESTED
         TM    RUNOPT,RUNOSET      NO VTOC ACTION WANTED?
         BZ    RRP1                NO, REQUESTED
         TM    RUNIND,RUNOSCR      NOSCRATCH MODE?
         BO    RRP99               YES, BYPASS
RRP1     L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    RRP99               YES
        $VTOCDR VTRDBUF,,MF=(L,RRPWORK)
         L     R7,WK1A             RACF WORK AREA
         USING WKDS,R7
         TM    RUNIND,RUTEST       TEST MODE?
         BZ    RRP2                NO
         MVC   VTUPRTT,=CL16'SIMULATED (TEST)'
RRP2     LR    R6,R2               RETAIN GROUP STARTING ENTRY
RRP3     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    RRP4                YES
         CLC   DSNME(L'DSNME),DSNME-DSTABDS(R6)   SAME DSNAME?
         BE    RRP3                YES, CONTINUE TO SEARCH GROUP
RRP4     LR    R4,R2               RETAIN END OF GROUP
         LR    R2,R6               GET STARTING ENTRY
RRP5     TM    RUNIND,RUNORACF     NO RACF ACTION WANTED?
         BO    RRP6                YES, BYPASS
         CLI   DSACTION,YR         DELETE FROM RACF?
         BE    RRP20               YES
         CLI   DSACTION,YY         MODIFY RACF?
         BE    RRP30               YES
         CLI   DSACTION,YA         ADD TO RACF?
         BE    RRP33               YES
         CLI   DSACTION,YF         CHANGE RACF?
         BE    RRP50               YES
RRP6     TM    RUNIND,RUNOSCR      NOSCRATCH MODE?
         BO    RRP8                YES, BYPASS
         CLI   DSACTION,YS         DATA-SET MUST BE SCRATCHED?
         BE    RRP7                YES
         CLI   DSACTION,YT         SCRATCH TEMPORARY?
         BE    RRP7                YES
         CLI   DSACTION,YD         SCRATCH DISK?
         BNE   RRP8                NO
RRP7     CLI   DSPROT,XPR          IS IT RACF ON (VTOC FLAG)?
         BNE   RRP8                NO
         CLI   DSRACF,XRD          YES, IS IT RACF DISCRETE?
         BNE   RRP10               NO, GO STRIP OFF BIT
RRP8     TM    RUNOPT,RUNOSET      NO VTOC ACTION WANTED?
         BO    RRP90               YES, GO GET NEXT ONE
         CLI   DSACTION,YN         OFF RACF (VTOC FLAG)?
         BE    RRP10               YES
         CLI   DSACTION,YV         ON RACF (VTOC FLAG)?
         BE    RRP10               YES
         CLI   DSACTION,YW         ON RACF (VTOC FLAG) + CHANGE RACF?
         BNE   RRP90               NO, GO GET NEXT ONE
RRP10    L     R5,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R5
RRP11    CLI   0(R5),FMOFF         END OF TABLE?
         BE    ERR63               YES, ERROR
         CLC   DDNNME,DSDDN        IS IT THIS DD-NAME?
         BE    RRP12               YES
         LA    R5,DDNL(R5)         ADVANCE IN TABLE
         B     RRP11               AND LOOP
RRP12    MVC   VSNERR,DDNVOL
        $VTOCDO DDNNME,MF=(E,RRPDO)     OPEN VTOC FOR DIRECT ACCESS
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR27
         OI    DDNALL,DDNXD        SET OPENED
        $VTOCDR ,DSCBCC,MF=(E,RRPWORK)  GET FMT1 DSCB (MBBCCHHR)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR28
         LA    R3,VTRDBUF          ADDRESS VTOC READ BUFFER
         USING FMT1DSCB,R3
         TM    RUNIND,RUTEST       TEST RUN?
         BO    RRP13               YES, ASSUME DSNAME'S O.K.
         CLC   DS1DSNAM,DSNME      RIGHT ONE?
         BE    RRP13               YES, O.K.
        @OUT   VTE                 NO, DSNAME'S ERROR
         UNPK  VTEM1A(9),DSCBCC(5)
         UNPK  VTEM1A+8(9),DSCBCC+4(5)
         MVI   VTEM1A+L'VTEM1A,C','
         TR    VTEM1A,TRTAB-X'F0'
         MVC   VTEM1B,DSVOLSN
        @OUT   VTEM1
         MVC   VTEM2A,=CL6'TABLED'
         MVC   VTEM2B,DSNME
        @OUT   VTEM2
         MVC   VTEM2A,=CL6'DSCB''S'
         MVC   VTEM2B,DS1DSNAM
        @OUT   (R1)
         XR    R15,R15
         B     ERR29
RRP13    CLI   DSACTION,YV         ON RACF (VTOC FLAG) REQUESTED?
         BE    RRP14               YES
         CLI   DSACTION,YA         ADD TO RACF REQUESTED?
         BE    RRP14               YES
         CLI   DSACTION,YW         ON RACF + CHANGE RACF REQUESTED?
         BNE   RRP15               NO
RRP14    OI    DS1DSIND,DS1IND40   YES, SET ON BIT
         MVC   VTUDSP,=CL6'SET.'
         B     RRP16
RRP15    NI    DS1DSIND,255-DS1IND40    STRIP OFF BIT
         MVC   VTUDSP,=CL6'NOSET.'
RRP16    TM    RUNIND,RUTEST       TEST RUN?
         BO    RRPSW               YES
        $VTOCDW ,DSCBCC,MF=(E,RRPWORK)  RE-WRITE ... (MBBCCHHR)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR30
         DROP  R3
RRPSW    NOP   RRP17
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP17    MVC   VTUDS,DSNME         LIST DSNAME
         MVC   VTUDSV,DSVOLSN
        @FILL  VTUDS
        @OUT   VTUPR
         NI    DDNALL,255-DDNXD    SET CLOSED
        $VTOCDC
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR31
         DROP  R5
         MVC   VSNERR,=CL6' '
         TM    RUNIND,RUNORACF     NO RACF ACTION WANTED?
         BO    RRP90               YES, BYPASS
         CLI   DSACTION,YW         ON RACF (VTOC FLAG) + CHANGE RACF?
         BE    RRP50               YES
         B     RRP90
         SPACE 1
RRP20    BAS   R14,RRP70           DELETE DATA-SET FROM RACF
         TM    DSFLGS,ZFTNU
         BO    *+L'*+10
         OC    DSVOLSN,DSVOLSN
         BNZ   *+L'*+10
         MVC   OLDVOL,DSALTVO      SET VOLUME
         B     *+L'*+6
         MVC   OLDVOL,DSVOLSN      SET VOLUME
         MVC   RADEVVL,OLDVOL
         MVC   RADEVOP,=CL4'O.K.'
         MVI   RADEVLG,RADEVLS
         XR    R15,R15             SET IN CASE OF TEST RUN
         TM    RUNIND,RUTEST       TEST RUN?
         BO    RRP21               YES
         LA    R0,ZERODS
        ICHEINTY DELETE,TYPE='DS',ENTRY=(R0),VOLUME=OLDVOL
RRP21    LTR   R15,R15
         BZ    RRP22
         MVC   RADEVRC,=CL2' '
         MVI   RADEVAC,C' '
         MVC   RADEVOP,=CL4'(RC='
         MVI   RADEVLG,RADEVLL
         CVD   R15,WORKD           RETURN CODE
         UNPK  RADEVRC,WORKD+6(2)
         OI    RADEVRC+1,C'0'
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   RRP22               NO
         STC   R0,RADEVAC
         OI    RADEVAC,C'0'
RRP22    MVC   RADEVDS,ZERODSN
        @FILL  RADEVDS
         TM    RRPSW+1,ON
         BO    RRP23
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP23   @OUT   RADEVFPR
         B     RRP90
         SPACE 1
RRP30    BAS   R14,RRP70           MODIFY RACF DATA-SET PROFILE
         MVC   NEWVOL,DSCVOL       SET VOLUMES
         TM    DSFLGS,ZFTNU
         BO    RRP31
         OC    DSALTVO,DSALTVO
         BNZ   RRP31
         MVC   OLDVOL,DSVOLSN
         B     RRP32
RRP31    MVC   OLDVOL,DSALTVO
RRP32    MVC   RAADVVO,=CL6'XCH''D '
         B     RRP40
         SPACE 1
RRP33    BAS   R14,RRP70           ADD DATA-SET TO RACF
         TM    DSFLGS,ZFTNU
         BZ    *+L'*+10
         MVC   NEWVOL,DSCVOL       SET VOLUME TO ADD
         B     *+L'*+6
         MVC   NEWVOL,DSVOLSN      SET VOLUME TO ADD
         LR    R15,R2              RETAIN CURRENT ENTRY
         LR    R2,R6               GET START OF GROUP
RRP34    CLR   R2,R15              MYSELF?
         BE    RRP35               YES
         CLR   R2,R4               END OF GROUP?
         BNL   RRP36               YES, WHAT? GO SIGNAL THIS
         CLI   DSRCVRY,YRU         USE TO ADD RACF?
         BE    RRP38               YES
RRP35    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     RRP34
RRP36    LR    R2,R15              RESTORE CURRENT ENTRY
         MVC   RADERDS,DSNME
        @FILL  RADERDS
         MVC   RADERVL,DSVOLSN
         TM    RRPSW+1,ON
         BO    RRP37
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP37   @OUT   RADERR
         B     RRP90
RRP38    MVC   OLDVOL,DSALTVO      SET VOLUME TO DELETE
         MVI   DSRCVRY,0           CANCEL, IT'S USED NOW
         LR    R2,R15              RESTORE CURRENT ENTRY
         MVC   RAADVVO,=CL6'ADDED '
RRP40    MVC   RAADVVL,NEWVOL
         MVC   RAADVOP,=CL4'O.K.'
         MVI   RAADVLG,RAADVLS
         XR    R15,R15             SET IN CASE OF TEST RUN
         TM    RUNIND,RUTEST       TEST RUN?
         BO    RRP41               YES
         LA    R0,ZERODS
        ICHEINTY ALTERI,TYPE='DS',ENTRY=(R0),VOLUME=OLDVOL,            C
               ACTIONS=(RAM)
RRP41    LTR   R15,R15
         BZ    RRP42
         MVC   RAADVRC,=CL2' '
         MVI   RAADVAC,C' '
         MVC   RAADVOP,=CL4'(RC='
         MVI   RAADVLG,RAADVLL
         CVD   R15,WORKD           RETURN CODE
         UNPK  RAADVRC,WORKD+6(2)
         OI    RAADVRC+1,C'0'
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   RRP42               NO
         STC   R0,RAADVAC
         OI    RAADVAC,C'0'
RRP42    MVC   RAADVDS,ZERODSN
        @FILL  RAADVDS
         TM    RRPSW+1,ON
         BO    RRP43
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP43   @OUT   RAADVFPR
         CLI   DSACTION,YY         WAS MODIFY?
         BE    RRP90               YES
         TM    RUNOPT,RUNOSET      NO VTOC ACTION WANTED?
         BO    RRP90               YES, GO GET NEXT ONE
         CLI   DSTYPE,XTL          VSAM (LOCATE)?
         BE    RRP90               YES, DON'T SET
         CLI   DSPROT,XPR          IS IT RACF ON (VTOC FLAG)?
         BNE   RRP10               NO, GO SET IT
         B     RRP90
         SPACE 1
RRP50    BAS   R14,RRP70           CHANGE RACF FOR A DATA-SET
         TM    DSFLGS,ZFTNU
         BZ    *+L'*+10
         MVC   OLDVOL,DSALTVO      SET VOLUME
         B     *+L'*+6
         MVC   OLDVOL,DSVOLSN      SET VOLUME
         XC    WORKRBA(WORKBSLL),WORKRBA
         LA    R0,ZERODS
        ICHEINTY LOCATE,TYPE='DS',ENTRY=(R0),VOLUME=OLDVOL,            C
               WKAREA=(R7),ACTIONS=(RAL)
         LTR   R15,R15
         BZ    RRP53
         CVD   R15,WORKD           RETURN CODE
         UNPK  RACFLOCR,WORKD+6(2)
         OI    RACFLOCR+1,C'0'
         MVI   RACFLOCA,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   RRP51               NO
         STC   R0,RACFLOCA
         OI    RACFLOCA,C'0'
RRP51    MVC   RACFLOCV,OLDVOL
         MVC   RACFLOCN,ZERODSN
        @FILL  RACFLOCN
         TM    RRPSW+1,ON
         BO    RRP52
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP52   @OUT   RACFLOC
         B     RRP90
RRP53    OC    WORKTL,WORKTL       DATA RETURNED?
         BZ    *+L'*+10            NO
         CLC   WORKVRA(2),=XL2'0001'    DSTYPE RECEIVED BACK?
         BE    RRP55               YES
         MVC   RAERTYV,OLDVOL
         MVC   RAERTYN,ZERODSN
        @FILL  RAERTYN
         TM    RRPSW+1,ON
         BO    RRP54
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP54   @OUT   RAERTY
         B     RRP90
RRP55    MVC   NEWTY(L'NEWTY),WORKVRA+2
         CLI   DSTYPE,0            VSAM DATA-SET?
         BE    RRP56               NO
         CLI   DSRTYPE,0           VSAM RACF DEFINED?
         BNE   RRP57               YES, WHAT? GO SIGNAL THIS
         OI    NEWTY,DSPTP         SET VSAM
         B     RRP60
RRP56    CLI   DSRTYPE,0           VSAM RACF DEFINED?
         BNE   RRP59               YES
RRP57    MVC   RACHRDS,ZERODSN
        @FILL  RACHRDS
         MVC   RACHRVL,OLDVOL
         TM    RRPSW+1,ON
         BO    RRP58
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP58   @OUT   RACHRR
         B     RRP90
RRP59    NI    NEWTY,255-DSPTP     STRIP OFF VSAM
RRP60    MVC   RACHVVL,OLDVOL
         MVC   RACHVOP,=CL4'O.K.'
         MVI   RACHVLG,RACHVLS
         XR    R15,R15             SET IN CASE OF TEST RUN
         TM    RUNIND,RUTEST       TEST RUN?
         BO    RRP61               YES
         LA    R0,ZERODS
        ICHEINTY ALTERI,TYPE='DS',ENTRY=(R0),VOLUME=OLDVOL,            C
               ACTIONS=(RAD)
RRP61    LTR   R15,R15
         BZ    RRP62
         MVC   RACHVRC,=CL2' '
         MVI   RACHVAC,C' '
         MVC   RACHVOP,=CL4'(RC='
         MVI   RACHVLG,RACHVLL
         CVD   R15,WORKD           RETURN CODE
         UNPK  RACHVRC,WORKD+6(2)
         OI    RACHVRC+1,C'0'
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   RRP62               NO
         STC   R0,RACHVAC
         OI    RACHVAC,C'0'
RRP62    MVC   RACHVDS,ZERODSN
        @FILL  RACHVDS
         TM    RRPSW+1,ON
         BO    RRP63
         OI    RRPSW+1,ON
        @OUT   VTUPRT,=PL2'+40',U
RRP63   @OUT   RACHVFPR
         B     RRP90
RRP70    TM    DSFLGS,ZFTNU        BUILD DATA-SET NAME FOR RACF
         BZ    *+L'*+10
         MVC   ZERODSN,DSANME
         B     *+L'*+6
         MVC   ZERODSN,DSNME
         LA    R0,L'ZERODSN        LOOK FOR LENGTH OF DSNAME
         LA    R1,ZERODSN+L'ZERODSN-1
RRP71    CLI   0(R1),C' '
         BNE   RRP72
         BCTR  R1,0
         BCT   R0,RRP71
RRP72    STC   R0,ZERODS           SET LENGTH
         BR    R14                 RETURN
         SPACE 1
RRP90    LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLR   R2,R4               END OF GROUP?
         BL    RRP5                NO, CONTINUE GROUP
         CLI   0(R2),FMOFF         END OF TABLE?
         BNE   RRP2                NO, LOOP
         DROP  R2,R7
RRP99   @XE
         EJECT
RRPDO   $VTOCDO ,MF=L
RRPWORK  DC    2F'0'
         SPACE 1
RAM     ICHEACTN FIELD=VOLSER,FLDATA=(6,NEWVOL),TESTS=RATT
RAL     ICHEACTN FIELD=DSTYPE,TESTS=RATT
RAD     ICHEACTN FIELD=DSTYPE,FLDATA(1,NEWTY),TESTS=RATT
RATT    ICHETEST FIELD=VOLSER,FLDATA=(6,OLDVOL),COND=EQ
         SPACE 1
OLDVOL   DC    CL6' '
NEWVOL   DC    CL6' '
NEWTY    DC    XL1'0'
         EJECT
         DC    AL1(L'VTE)
VTE      DC    C'1===== DIRECT VTOC READ BY MBBCCHHR OBTAIN A FORMAT 1 C
               DSCB WITH A DIFFERENT DATA-SET NAME ====='
         DC    AL1(LVTEM1)
VTEM1    DC    C'0      MBBCCHHR : '
VTEM1A   DC    CL16' ',C', VOLUME : '
VTEM1B   DC    CL6' ',C'.'
LVTEM1   EQU   *-VTEM1
         DC    AL1(LVTEM2)
VTEM2    DC    C'0      '
VTEM2A   DC    CL6' ',C' DSNAME : '
VTEM2B   DC    CL44' '
LVTEM2   EQU   *-VTEM2
         DC    AL1(L'VTUPRT+L'VTUPRTT)
VTUPRT   DC    C'0RACF PROCESSING : '
VTUPRTT  DC    CL16' '
         DC    AL1(VTUPRL)
VTUPR    DC    CL6' '
VTUDS    DC    CL44' ',C' - VOLUME : '
VTUDSV   DC    CL6' ',C' - RACF PROTECTION BIT : '
VTUDSP   DC    CL6' '
VTUPRL   EQU   *-VTUPR
RADEVLG  DC    AL1(*-*)
RADEVFPR DC    CL6' '
RADEVDS  DC    CL44' ',C' - VOLUME : '
RADEVVL  DC    CL6' ',C' - RACF PROFILE DELETED '
RADEVOP  DC    CL4' '
RADEVLS  EQU   *-RADEVFPR
RADEVRC  DC    CL2' ',C'/AC='
RADEVAC  DC    CL1' ',C').'
RADEVLL  EQU   *-RADEVFPR
RAADVLG  DC    AL1(*-*)
RAADVFPR DC    CL6' '
RAADVDS  DC    CL44' ',C' - VOLUME : '
RAADVVL  DC    CL6' ',C' - RACF PROFILE '
RAADVVO  DC    CL6' '
RAADVOP  DC    CL4' '
RAADVLS  EQU   *-RAADVFPR
RAADVRC  DC    CL2' ',C'/AC='
RAADVAC  DC    CL1' ',C').'
RAADVLL  EQU   *-RAADVFPR
         DC    AL1(RADERRL)
RADERR   DC    CL3' ',CL3'-- '
RADERDS  DC    CL44' ',C' - VOLUME : '
RADERVL  DC    CL6' ',C' - RACF PROFILE NOT ADDED '
         DC    C'(USE NOT FOUND). --'
RADERRL  EQU   *-RADERR
         DC    AL1(RACFLOCL)
RACFLOC  DC    CL3' ',CL3'-- '
RACFLOCN DC    CL44' ',C' - VOLUME : '
RACFLOCV DC    CL6' ',C' - RACF LOCATE (RC='
RACFLOCR DC    CL2' ',C'/AC='
RACFLOCA DC    CL1' ',C').'
RACFLOCL EQU   *-RACFLOC
         DC    AL1(RAERTYL)
RAERTY   DC    CL3' ',CL3'-- '
RAERTYN  DC    CL44' ',C' - VOLUME : '
RAERTYV  DC    CL6' ',C' - DSTYPE NOT RECEIVED BACK.'
RAERTYL  EQU   *-RAERTY
RACHVLG  DC    AL1(*-*)
RACHVFPR DC    CL6' '
RACHVDS  DC    CL44' ',C' - VOLUME : '
RACHVVL  DC    CL6' ',C' - RACF PROFILE CHANGED '
RACHVOP  DC    CL4' '
RACHVLS  EQU   *-RACHVFPR
RACHVRC  DC    CL2' ',C'/AC='
RACHVAC  DC    CL1' ',C').'
RACHVLL  EQU   *-RACHVFPR
         DC    AL1(RACHRRL)
RACHRR   DC    CL3' ',CL3'-- '
RACHRDS  DC    CL44' ',C' - VOLUME : '
RACHRVL  DC    CL6' ',C' - RACF PROFILE NOT CHANGED '
         DC    C'(NOT TYPE). --'
RACHRRL  EQU   *-RACHRR
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        ICHRRPF
FMT1DSCB DSECT
        IECSDSL1 (1)
         EJECT
        PRINT  GEN
        #WKDS
         SPACE 2
         END
