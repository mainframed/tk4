B011     TITLE 'CLEAN-UP - GENERATE IDCAMS STATEMENTS (B011).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B011    @XS
*        AT ENTRY :   R2 = DSNAME TABLE ENTRY POINTER
         SPACE 1
GISW1    NOP   GI0
         OI    GISW1+1,ON
        $DVTYUN ,WORKD,MF=(L,GIDV)
         USING DSTABDS,R2
GI0      TM    RUNIND,RUTEST       TEST MODE?
         BO    GI1                 YES
         LA    R3,CARDOU           NO, GENERATE INPUT FILE
         MVI   0(R3),C' '
         MVC   1(L'CARDOU-1,R3),0(R3)
GISW2    NOP   GI10
         OI    GISW2+1,ON
         BAS   R14,IDCIAR          ALLOCATE IDCAMS INPUT
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR71
        $PUNCH (R3),,(R6),MF=(L,GIPU)
         B     GI10
GI1      ICM   R3,B'1111',GIDPTR   ANY CURRENT POINTER?
         BNZ   GI10                YES
         ICM   R3,B'1111',GIDSTR   NO, FIRST ARRAY?
         BNZ   *+L'*+4             NO
         LA    R3,AGIDST           YES
         BAS   R14,GINXAR
         MVI   0(R3),FMON          SET FINAL MARKER
         SPACE 1
GI10     CLI   DSACTION,YS         DATA-SET MUST BE SCRATCHED?
         BE    GI20                YES
         CLI   DSACTION,YD         SCRATCH DISK?
         BE    GI20                YES
         CLI   DSACTION,YT         SCRATCH TEMPORARY?
         BE    GI20                YES
         CLI   DSACTION,YU         UNCATALOG?
         BE    GI20                YES
         CLI   DSACTION,YC         CATALOG?
         BE    GI30                YES
         B     GI99
         SPACE 1
GI20     MVC   1(3,R3),=CL3'DEL'
         MVC   5(L'DSNME,R3),DSNME
         LA    R1,4+L'DSNME(R3)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   2(5,R1),=CL5'NVSAM'
         CLI   DSACTION,YU         UNCATALOG?
         BE    *+L'*+10            YES
         MVC   8(5,R1),=CL5'PRG -' NO, SCRATCH
         B     *+L'*+6
         MVC   8(6,R1),=CL6'NSCR -'
         BAS   R14,GINXCD
         BAS   R14,GICTLG
         B     GI90
         SPACE 1
GI30     BAS   R14,GIDEVT
         MVC   1(3,R3),=CL3'DEF'
         MVC   5(11,R3),=CL11'NVSAM(NAME('
         MVC   16(L'DSNME,R3),DSNME
         LA    R1,15+L'DSNME(R3)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(3,R1),=CL3') -'
         BAS   R14,GINXCD
         MVC   9(5,R3),=CL5'DEVT('
         MVC   14(8,R3),WORKD
         LA    R1,21(R3)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(6,R1),=CL6') VOL('
         MVC   7(L'DSVOLSN,R1),DSVOLSN
         LA    R1,6+L'DSVOLSN(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(4,R1),=CL4')) -'
         BAS   R14,GINXCD
         BAS   R14,GICTLG
         B     GI90
         SPACE 1
GINXAR   L     R0,LGIDST           GET ANOTHER ARRAY SPACE
         ST    R14,GISV2
        GETMAIN EC,LV=(R0),A=(R3)
         LTR   R15,R15             STORAGE AVAILABLE?
         BNZ   ERR69               NO
         L     R3,0(R3)            GET ARRAY START ADDRESS
         ST    R3,GIDSTR
         LR    R4,R3
         A     R4,LGIDST
         SH    R4,=H'4'
         MVI   0(R4),FMON          SET ARRAY FINAL MARKER
         LA    R3,4(R3)            CURRENT POINTER
         LR    R4,R3
         L     R5,LGIDST
         SH    R5,=H'8'
         BASR  R6,0
         XR    R7,R7
         ICM   R7,B'1000',=C' '
         MVCL  R4,R6               BLANKS ALL CARDS
         ST    R3,GIDPTR           SET CURRENT POINTER
         L     R14,GISV2
         BR    R14                 RETURN
         SPACE 1
GINXCD   AP    GINUM,=PL1'+1'      GET NEXT CARD SPACE ROUTINE
         UNPK  72(8,R3),GINUM      R3 = CARD POINTER
         OI    79(R3),C'0'
         MVI   0(R3),C' '
         TM    RUNIND,RUTEST       TEST MODE?
         BO    GINXCD1             YES
         ST    R14,GISV1
        $PUNCH MF=(E,GIPU)
         LTR   R15,R15
         BNZ   ERR71
         MVC   1(L'CARDOU-1,R3),0(R3)
         L     R14,GISV1
         NOPR  R14                 RETURN
         OI    *-1,ON
         OI    WHATS,WHIDIN        SAY OPENED
         BR    R14                 RETURN
GINXCD1  LA    R3,80(R3)
         CLI   0(R3),FMON          ARRAY FINAL MARKER?
         BNER  R14                 NO, RETURN
         L     R3,GIDSTR           YES, SUPPLY ANOTHER ARRAY
         ST    R14,GISV1
         BAS   R14,GINXAR
         L     R14,GISV1
         BR    R14                 RETURN
         SPACE 1
GICTLG   MVC   9(4,R3),=CL4'CAT('  BUILD CATALOG NAME ROUTINE
         L     R15,TBCVOL
         USING CVLT,R15
GICTLG1  CLI   0(R15),FMOFF
         BE    ERR70
         TM    CVLIND,CVLICF
         BZ    GICTLG2
         TM    CVLIND,CVLMIS
         BO    GICTLG2
         TM    CVLIND,CVLCTP
         BZ    GICTLG2
         CLC   CVLVOL,DSCVOL
         BE    GICTLG3
GICTLG2  LA    R15,CVLL(R15)
         B     GICTLG1
GICTLG3  MVC   13(L'CVLNME,R3),CVLNME
         LA    R1,12+L'CVLNME(R3)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         CLI   CVLPWL,0
         BNE   *+L'*+6
         MVI   1(R1),C')'
         BR    R14                 RETURN
         MVI   1(R1),C'/'
         TM    RUNIND,RUTEST       TEST MODE?
         BO    *+L'*+6
         MVC   2(L'CVLPW,R1),CVLPW
         XR    R0,R0
         IC    R0,CVLPWL
         DROP  R15
         AR    R1,R0
         MVI   2(R1),C')'
         BR    R14                 RETURN
         SPACE 1
GIDEVT   ST    R14,GISV1           GET DEVICE TYPE ROUTINE
        $DVTYUN DSUCBTY,,MF=(E,GIDV)
         LTR   R15,R15
         BNZ   *+L'*+6
         L     R14,GISV1
         BR    R14                 RETURN
         LR    R3,R15
GISW3    NOP   GIDEVT1
         OI    GISW3+1,ON
        @LIST  GIERRS,=PL2'+45',U
GIDEVT1  CVD   R3,WORKD
         UNPK  GIERDVT,WORKD+6(2)
         OI    GIERDVT+L'GIERDVT-1,C'0'
         MVC   GIERDVV,DSVOLSN
         MVC   GIERDVN,DSNME
        @FILL  GIERDVN
        @LIST  GIERDV
         B     GI99
         SPACE 1
         DROP  R2
GI90     AP    GINUM,=PL1'+1'
         UNPK  72(8,R3),GINUM
         OI    79(R3),C'0'
         TM    RUNIND,RUTEST       TEST MODE?
         BO    GI91                YES
        $PUNCH MF=(E,GIPU)
         LTR   R15,R15
         BNZ   ERR71
         MVC   1(L'CARDOU-1,R3),0(R3)
         B     GI99
GI91     LA    R3,80(R3)
         CLI   0(R3),FMON          ARRAY FINAL MARKER
         BE    GI92                YES
         MVI   0(R3),FMON          NO, SET FINAL MARKER
         ST    R3,GIDPTR           SAVE CURRENT POINTER
         B     GI99
GI92     XC    GIDPTR,GIDPTR       RESET POINTER
GI99    @XE
         EJECT
GIPU    $PUNCH ,,,MF=L
GIDV    $DVTYUN ,,MF=L
         SPACE 1
GIDSTR   DC    A(*-*)
GIDPTR   DC    A(*-*)
GISV1    DC    F'0'
GISV2    DC    F'0'
GINUM    DC    PL5'+0'
         SPACE 1
         DC    AL1(L'GIERRS)
GIERRS   DC    C'0GENERATE IDCAMS STATEMENTS ERRORS :'
         DC    AL1(GIERDVL)
GIERDV   DC    CL6' ',C'DSN='
GIERDVN  DC    CL44' ',C'  VOL='
GIERDVV  DC    CL6' ',C'  DEVT= ? RC='
GIERDVT  DC    CL2' ',C'.'
GIERDVL  EQU   *-GIERDV
         SPACE 2
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
