AU97     TITLE '-- ROUTINE AUT97 - SEVERE ERROR SERVICE --'
AUT97    START 0
         SPACE 1
**********************************************************************
*        THIS SUBROUTINE DISPLAY SEVERE ERROR DIAGNOSTIC             *
*        AND EXIT FULLSCREEN MODE.                                   *
*                                  (MOINIL P.A. - 21 SEPTEMBER 1989) *
**********************************************************************
*        CALL AUT97,(PARM),VL                                        *
*        FUNCTION : DISPLAY SEVERE ERROR DIAGNOSTIC AND EXIT         *
*                   FULLSCREEN MODE.                                 *
*        AT ENTRY : R1  = ADDRESS OF THE DIAGNOSTIC MESSAGE TEXT.    *
*                         IT POINTS AS BELOW :                       *
*                                    DC  AL1(L'TEXT)                 *
*                         ---> TEXT  DC  C'...MESSAGE...'            *
*                         NOTE : FIRST CHARACTER OF MESSAGE IS THE   *
*                                ASA CONTROL CHARACTER FOR PRINT.    *
*              OR : R1  = 0 AND THEN R0 IS THE ERROR NUMBER TO       *
*                         PASS AT 'ZYDIAG' ROUTINE TO RETRIEVE THE   *
*                         DIAGNOSTIC MESSAGE.                        *
*                 R2-R3 = NAME OF THE ROUTINE CAUSE OF THE ERROR     *
*                      OR R2 = ZERO AND R3 = ADDRESS OF A SECONDARY  *
*                         MESSAGE TEXT, WHICH POINTS AS BELOW :      *
*                                    DC  AL1(L'TEXT)                 *
*                         ---> TEXT  DC  C'...MESSAGE...'            *
*                         NOTE : FIRST CHARACTER OF MESSAGE IS THE   *
*                                ASA CONTROL CHARACTER FOR PRINT.    *
*                      OR NONE (R2 AND R3 = ZERO)                    *
*         AT EXIT : R15 = 0 - ALWAYS                                 *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R10
         SPACE 1
         LTR   R15,R1
         BNZ   SETDIAG                 PARM = ADDRESS DIAGNOSTIC
        CALL   ZYDIAG                  R0 = ERROR NUMBER
SETDIAG  LR    R4,R15                  HANDLE DIAGNOSTIC
         XR    R5,R5
         BCTR  R15,0
         IC    R5,0(R15)
         BCTR  R5,0
         BCT   R5,*+L'*+6
         MVC   ECRANM(*-*),1(R4)
         EX    R5,*-6
         LTR   R2,R2
         BZ    NONAME
         MVC   CTXT,=CL21'Calling routine was :'
         STCM  R2,B'1111',RTNNME       SET NAME OF ROUTINE
         STCM  R3,B'1111',RTNNME+4
         B     BYPASS
NONAME   LTR   R3,R3
         BZ    BYPASS
         LR    R4,R3                   HANDLE 2ND DIAGNOSTIC
         XR    R5,R5
         BCTR  R3,0
         IC    R5,0(R3)
         BCTR  R5,0
         BCT   R5,*+L'*+6
         MVC   ECRANS(*-*),1(R4)
         EX    R5,*-6
BYPASS   L     R4,4(R13)               PREVIOUS SAVE AREA
         SL    R4,=F'32'               POINT TO START OF MODULE
         MVC   MODNME(L'MODNME),10(R4) SET NAME OF MODULE
         MVC   ASSDTE(L'ASSDTE),18(R4) SET DATE-TIME OF MODULE
         LA    R1,PECRAN               DISPLAY DIAGNOSTIC
         L     R15,=V(AUT99)
         BALR  R14,R15
         XR    R1,R1                   EXIT FULLSCREEN
         L     R15,=V(AUT99)
         BALR  R14,R15
         XR    R15,R15
        $XRET  CC=(R15)                RETURN
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
PECRAN   DC    A(LECRAN,ECRAN)
         SPACE 1
*--------------------------------------------------------------------*
*        ECRAN D'OUTPUT                                              *
*--------------------------------------------------------------------*
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,20),SF=(PROT,INT),MF=L
        $FS    TEXT='*************************************',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,20),SF=(PROT,INT),MF=L
        $FS    TEXT='*    S E V E R E       E R R O R    *',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(3,20),SF=(PROT,INT),MF=L
        $FS    TEXT='*************************************',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(7,1),SF=(PROT,INT),RA=(7,80,-),MF=L
        $FS    SBA=(8,1),SF=(PROT),TEXT='Diagnose error :',MF=L
        $FS    SBA=(9,1),SF=(PROT,INT),MF=L
ECRANM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(10,1),SF=(PROT,INT),MF=L
ECRANS  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(11,1),SF=(PROT,INT),RA=(11,80,-),MF=L
        $FS    SBA=(15,1),SF=(PROT),MF=L
        $FS    TEXT='Error has been detected in module :',MF=L
        $FS    SF=(PROT,INT),MF=L
MODNME  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(16,1),SF=(PROT),MF=L
        $FS    TEXT='Module assembly date-time :',MF=L
        $FS    SF=(PROT,INT),MF=L
ASSDTE  $FS    TEXT=(' ',14),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(18,1),SF=(PROT),MF=L
CTXT    $FS    TEXT=(' ',21),MF=L
        $FS    SF=(PROT,INT),MF=L
RTNNME  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(22,1),SF=(PROT),TEXT='Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='to exit - No RECOVERY provided',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAN   EQU   *-ECRAN
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         SPACE 2
         END
