A002     TITLE 'CLEAN-UP - INITIALIZATION CONT''D (A002).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A002    @XS
        $READ  LINE+20,MF=(L,OPARD)
         SPACE 1
*        READ SKIP NAMES.
         SPACE 1
OPS0     LM    R3,R5,SCIN          START SCAN INPUT CARD
         CLI   0(R3),C' '          SEARCH START OF KEYWORD
         BNE   OPS2
OPS1     BXLE  R3,R4,*-8
        $READ  MF=(E,RDIPLS)       GET NEXT CARD
         LTR   R15,R15
         BM    OPECC               END OF CONTROL CARDS
         BZ    OPS0                NEXT
         B     ERR21
OPS2     CLC   0(4,R3),=CL4'SKIP'  SKIP REQUEST?
         BNE   OP2                 NO
        @OUT   PRCAR               PRINT INPUT CARD
         LA    R3,3(R3)            SKIP, SEARCH WHICH NAMES
         BXLE  R3,R4,CSK1
         BAS   R14,OPCONT
CSK1     CLI   0(R3),C' '
         BNE   CSK2
         BXLE  R3,R4,CSK1
         BAS   R14,OPCONT
         B     CSK1
CSK2     CLI   0(R3),C'('
         BNE   OERR
CSK3     BXLE  R3,R4,CSK5
CSK4     BAS   R14,OPCONT
CSK5     CLI   0(R3),C')'
         BE    OPS1                GET NEXT KEYWORD
         CLI   0(R3),C' '
         BE    CSK3
         LR    R1,R3
CSK6     BXLE  R3,R4,CSK7
         B     CSK8
CSK7     CLI   0(R3),C' '
         BE    CSK8
         CLI   0(R3),C')'
         BNE   CSK6
CSK8     LR    R14,R3
         SR    R14,R1
         BNP   ERR48
         CH    R14,=H'8'
         BH    ERR48
         BCTR  R14,0
         L     R15,TBSKP           STORE IN SKIP TABLE
CSK9     CLI   0(R15),FMOFF
         BE    CSK10
         LA    R15,8(R15)
         B     CSK9
CSK10    LA    R0,8(R15)
         CL    R0,TBSKPE
         BL    CSK11
         XR    R15,R15
         B     ERR49
CSK11    MVI   0(R15),C' '
         MVC   1(7,R15),0(R15)
         EX    R14,CSKMV
         MVI   8(R15),FMOFF        SET FINAL MARKER
         CLR   R3,R5
         BNH   CSK5
         B     CSK4
CSKMV    MVC   0(*-*,R15),0(R1)
         SPACE 1
*        READ BYPASS RACF NAMES.
         SPACE 1
OP0      LM    R3,R5,SCIN          START SCAN INPUT CARD
         CLI   0(R3),C' '          SEARCH START OF KEYWORD
         BNE   OP2
OP1      BXLE  R3,R4,*-8
        $READ  MF=(E,RDIPLS)       GET NEXT CARD
         LTR   R15,R15
         BM    OPECC               END OF CONTROL CARDS
         BZ    OP0                 NEXT
         B     ERR21
OP2      CLC   0(7,R3),=CL7'BYPRACF'    BYPRACF REQUEST?
         BNE   OPA2                NO
        @OUT   PRCAR               PRINT INPUT CARD
         LA    R3,6(R3)            SKIP, SEARCH WHICH NAMES
         BXLE  R3,R4,OP3
         BAS   R14,OPCONT
OP3      CLI   0(R3),C' '
         BNE   OP4
         BXLE  R3,R4,OP3
         BAS   R14,OPCONT
         B     OP3
OP4      CLI   0(R3),C'('
         BNE   OERR
OP5      BXLE  R3,R4,OP6+L'OP6
OP6      BAS   R14,OPCONT
         CLI   0(R3),C')'
         BE    OP1                 GET NEXT KEYWORD
         CLI   0(R3),C' '
         BE    OP5
         CLI   0(R3),C'/'
         BE    ERR53               NOT ACCEPTED HERE
OP7      LR    R1,R3
OP8      BXLE  R3,R4,OP9
         B     OP10
OP9      CLI   0(R3),C' '
         BE    OP10
         CLI   0(R3),C'/'
         BE    OP10
         CLI   0(R3),C')'
         BNE   OP8
OP10     LR    R14,R3
         SR    R14,R1
         BNP   ERR53
         CH    R14,=H'44'
         BH    ERR53
         BCTR  R14,0
         L     R15,TBBYR           STORE IN BYPASS RACF TABLE
         USING BYPRDS,R15
         XR    R2,R2
OP11     CLI   BYPRLG,FMON
         BE    OP12
         IC    R2,BYPRLG
         LA    R15,BYPRPX+1(R2)
         B     OP11
OP12     LA    R0,BYPRPX+1(R14)
         CL    R0,TBBYRE
         BL    OP13
         XR    R15,R15
         B     ERR54
OP13     STC   R14,BYPRLG
         XC    BYPRDC(L'BYPRDC+L'BYPRFL+L'BYPRGC),BYPRDC
         EX    R14,OPMV
         LR    R2,R15              RETAIN ENTRY IF NECESSARY
         LR    R15,R0
         MVI   BYPRLG,FMON         SET FINAL MARKER
         CLR   R3,R5
         BH    OP15
OP14     CLI   0(R3),C' '
         BNE   OP16
         BXLE  R3,R4,OP14
OP15     BAS   R14,OPCONT
         B     OP14
OPMV     MVC   BYPRPX(*-*),0(R1)   << EXECUTED >>
         DROP  R15
OP16     CLI   0(R3),C')'
         BE    OP1
         CLI   0(R3),C'/'
         BNE   OP7
OP17     BXLE  R3,R4,*+L'*+4
         BAS   R14,OPCONT
         CLI   0(R3),C' '
         BE    OP17
         CLI   0(R3),C')'
         BE    ERR53               NOT ACCEPTED HERE
         CLI   0(R3),C'/'
         BE    ERR53               NOT ACCEPTED HERE
         CLI   0(R3),C'N'          NOLIST REQUESTED?
         BE    OP18                YES
         CLI   0(R3),C'S'          SKIP REQUESTED?
         BNE   ERR53               NO, SYNTAX ERROR
         USING BYPRDS,R2
         OI    BYPRFL,BYPRS        SET SKIP
         B     OPSC
OP18     OI    BYPRFL,BYPRL        SET NOLIST
         DROP  R2
OPSC     BXLE  R3,R4,*+L'*+4
         B     OP6
         CLI   0(R3),C' '
         BE    OP5
         CLI   0(R3),C')'
         BE    OP1
         CLI   0(R3),C'/'
         BE    ERR53               NOT ACCEPTED HERE
         B     OPSC
         SPACE 1
*        READ ACCOUNT REQUIREMENTS.
         SPACE 1
OPA0     LM    R3,R5,SCIN          SET TO SCAN INPUT CARD
         CLI   0(R3),C' '          SEARCH START OF A KEYWORD
         BNE   OPA2
OPA1     BXLE  R3,R4,*-8
        $READ  MF=(E,RDIPLS)       GET NEXT CARD
         LTR   R15,R15
         BM    OPECC               END OF CONTROL CARDS
         BZ    OPA0                NEXT
         B     ERR21
OPA2     CLC   0(7,R3),=CL7'ACCOUNT'    IS IT 'ACCOUNT' KEYWORD?
         BNE   OPZ90               NO
        @OUT   PRCAR               PRINT INPUT CARD
         LA    R3,6(R3)
OPASW    NOP   OPA3
         OI    OPASW+1,ON
         L     R6,DALIST
         USING DYNLIST,R6
         LA    R1,LINE+20
         ST    R1,LISCAN
OPA3     BXLE  R3,R4,OPA4
         BAS   R14,OPCONT
OPA4     CLI   0(R3),C' '          SEARCH START OF ARGUMENT
         BE    OPA3
         CLI   0(R3),C'*'
         BE    OPB0
         CLI   0(R3),C'('
         BNE   OERR
OPA5     BXLE  R3,R4,OPA6
         BAS   R14,OPCONT
OPA6     CLI   0(R3),C' '          SEARCH START OF PO-DSNAME
         BE    OPA5
         CLI   0(R3),C')'
         BE    OPA1
         CLI   0(R3),C'/'
         BNE   OPA7
         CLI   DSNAME,C' '
         BNE   OPA13               PO-DSNAME AS PREVIOUS
         B     OERR
OPA7     MVI   WORKDS,C' '
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
         LA    R0,WORKDS+L'WORKDS
         LA    R1,WORKDS
OPA8     CLI   0(R3),C'/'
         BE    OPA9
         CLI   0(R3),C' '
         BE    OPA9
         CLR   R1,R0
         BNL   OERR
         MVC   0(1,R1),0(R3)       GET PO-DSNAME
         LA    R1,1(R1)
         BXLE  R3,R4,OPA8
         BAS   R14,OPCONT
OPA9     CLC   DSNAME,WORKDS       SAME AS PREVIOUS?
         BE    OPA12               YES
         TM    WHAT,WHPDSA         FIRST TIME?
         BZ    OPA10               YES
         NI    WHAT,255-WHPDSO     NO, CLOSE AND FREE PREVIOUS
        $MCLOSE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR26
         NI    WHAT,255-WHPDSA
         XC    DSNAME,DSNAME
        $DYNALL (R6),,MF=(E,DYNPLS)
         LTR   R15,R15             TEST RETURN CODE
         BZ    OPA10
         ICM   R1,B'1111',=CL4'FREE'
         LA    R15,DDNAME
         BAS   R14,DYNFAIL
         B     ERR26
OPA10    MVC   DSNAME,WORKDS
         B     OPA12
OPA11    BAS   R14,OPCONT
OPA12    CLI   0(R3),C'/'
         BE    OPA13
         CLI   0(R3),C' '
         BNE   OERR
         BXLE  R3,R4,OPA12
         B     OPA11
OPA13    BXLE  R3,R4,OPA14
         BAS   R14,OPCONT
OPA14    CLI   0(R3),C' '          SEARCH START OF NAME
         BE    OPA13
         OI    OERRSW+1,ON
OPA15    LA    R0,9
         LR    R1,R3
OPA16    CLI   0(R3),C' '
         BE    OPA17
         CLI   0(R3),C')'
         BE    OPA17
         BCT   R0,*+L'*+4
         B     OERRSW              > 8 CHAR. LONG
         BXLE  R3,R4,OPA16
OPA17    LR    R14,R3
         SR    R14,R1
         BZ    OPA1                THAT'S ALL FOLKS
         TM    RUNOPT,RUNOACC      ACCOUNTING PROCESSING SUPPRESS?
         BO    OPA20               YES
         BCTR  R14,0
         STC   R14,MBNMEL
         MVC   MBNME,=CL8' '
         EX    R14,OPAMMV          GET MEMBER NAME
         TM    WHAT,WHPDSA         ALLOCATED?
         BO    OPA18               YES
         MVC   DDNAME(6),=CL6'SYSPDS'
         MVC   DSSTATUS(3),=CL3'SHR'
         MVC   DSNDISP(4),=CL4'KEEP'
         MVC   DSADISP(4),=CL4'KEEP'
        $DYNALL (R6),,MF=(E,DYNPLS)
         LTR   R15,R15             TEST RETURN CODE
         BZ    OPAOK
         ICM   R1,B'1111',=CL4'ALL.'
         LA    R15,DDNAME
         BAS   R14,DYNFAIL
         B     ERR23
OPAMMV   MVC   MBNME(*-*),0(R1)    << EXECUTED >>
OPAOK    OI    WHAT,WHPDSA
        $MOPEN MF=(E,MOPPLS)       OPEN PARTITIONNED
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR23
         OI    WHAT,WHPDSO
OPA18    MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LINE+9(8),MBNME     SET MEMBER NAME
         MVI   LINE+18,C':'
        $MFIND MF=(E,OPAMF)        FIND MEMBER IN DATA-SET
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR24
OPA19   $MREAD MF=(E,MRDPLS)       READ CARD FROM MEMBER
         LTR   R15,R15
         BM    OPA20               END OF FILE?
         BNZ   ERR25
        @OUT   LINE
        @X     B002                ACCOUNTING REQUESTS
         MVC   LINE+1(L'LINE-1),LINE
         B     OPA19               TO NEXT MEMBER CARD
OPA20    CLR   R3,R5               CARD SCAN ENDED?
         BNH   OPA22               NO
OPA21    BAS   R14,OPCONT
OPA22    CLI   0(R3),C')'          END OF NAMES?
         BE    OPA1                YES
         CLI   0(R3),C' '
         BNE   OPA23               TO NEXT NAME IF ANY
         BXLE  R3,R4,OPA22
         B     OPA21
OPA23    NI    OERRSW+1,OFF
         B     OPA15
         DROP  R6
OPB0     MVI   LINE,C' '           ACC. REQ. ARE IN-STREAM
         MVC   LINE+1(L'LINE-1),LINE
OPB1     BXLE  R3,R4,OPB2
         B     OPB4
OPB2     CLI   0(R3),C' '
         BE    OPB1
         LR    R1,R3
         S     R1,SCIN
         LA    R15,LINE+20(R1)
         LA    R14,79
         SR    R14,R1
         EX    R14,OPBMV
         B     OPB7
OPBMV    MVC   0(*-*,R15),0(R3)
OPB3     MVC   LINE+1(L'LINE-1),LINE
OPB4    $READ  MF=(E,OPARD)
         LTR   R15,R15
         BM    OPBERR
         BNZ   ERR21
         LA    R1,LINE+20
         LA    R14,1
         LA    R15,71(R1)
OPB5     CLI   0(R1),C' '          LOOK IF END OF ACC. REQ.
         BNE   OPB6
         BXLE  R1,R14,OPB5
         B     OPB4
OPB6     CLC   0(7,R1),=CL7'ACCOUNT'
         BE    OPB8
         CLC   0(6,R1),=CL6'EL.VOL'
         BE    OPB8
        @OUT   LINE
OPB7    @X     B002                ACCOUNTING REQUESTS
         B     OPB3
OPB8     MVC   CARDIN(L'CARDIN),LINE+20
         LA    R3,LINE+20
         SR    R1,R3
         LA    R3,CARDIN(R1)
         B     OPA2
OPBERR   LA    R1,OPBERRM
         B     OPNERR
OPCONT   ST    R14,SVRETO          CONTINUATION
        $READ  MF=(E,RDIPLS)       READ NEXT CARD
         LTR   R15,R15             TEST RETURN CODE
         BM    OPTERR
         BNZ   ERR21
        @OUT   PRCAR               PRINT INPUT CARD
         LM    R3,R5,SCIN          SET TO SCAN INPUT CARD
         L     R14,SVRETO
         BR    R14                 GO BACK
OPTERR   LA    R1,OPTERRM
         B     OPNERR
OERRSW   B     OERR
        @OUT   PRCAR               REPEAT FOR DIAGNOSE
OERR     MVI   CARDIN,C' '
         MVC   CARDIN+1(L'CARDIN-1),CARDIN
         MVI   0(R3),C'$'          POSITION OF ERROR
        @OUT   PRCAR
         LA    R1,OPERR            STATEMENT ERROR
OPNERR  @OUT   (R1)
         XR    R15,R15
         B     ERR22
OPECC   $RCLOSE
        @OUT   ONINPUT
         NI    WHAT,255-WHIN
         B     ERR21
OPZ90    TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    OPZ99               YES
         TM    RUNOPT,RUNOACC      ACCOUNTING PROCESSING SUPPRESS?
         BO    OPZ99               YES
         TM    ACCIND,AFVOL        ACCOUNTING VOLUME(S)?
         BO    OPZ99               YES
        @OUT   DSACCM
         OI    RUNOPT,RUNOACC      SET NO ACCOUNT
OPZ99   @XE
         EJECT
SVRETO   DC    F'0'
OPAMF   $MFIND MBNME,MF=L
OPARD   $READ  ,MF=L
         SPACE 1
MBNMEL   DC    AL1(*-*)
MBNME    DC    CL8' '
         DC    AL1(L'ONINPUT)
ONINPUT  DC    C'0===== INPUT CONTROL MISSING ====='
         DC    AL1(L'OPERR)
OPERR    DC    C' ===== SYNTAX ERROR ====='
         DC    AL1(L'OPTERRM)
OPTERRM  DC    C' ===== CONTINUATION MISSING ====='
         DC    AL1(L'OPBERRM)
OPBERRM  DC    C' ===== ACCOUNTING REQ. STATEMENT MISSING ====='
         DC    AL1(L'DSACCM)
DSACCM   DC    C' ===== ACCOUNTING SUPPRESSED ====='
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
        #BYPRDS
         SPACE 2
         END
