A004     TITLE 'CLEAN-UP - ALLOCATE VOLUMES (A004).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A004    @XS    ALIAS=ALLOCV
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    CHC99               YES
         SPACE 1
*        LOOK UP UCB'S.
         SPACE 1
         L     R2,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R2
ALL1    $DARSCA MF=(E,ALLPLS)
         LTR   R15,R15             TEST RETURN CODE
         BZ    ALL2                O.K.
         CH    R15,=H'4'           END OF UCB'S?
         BE    ALL14               YES
         B     ERR10               NO, EXIT WITH CODE
         SPACE 1
*        CHECK IF THIS VOLUME WANTED.
         SPACE 1
ALL2     L     R4,TBEVOL           START OF EL. VOL. TABLE
         L     R3,ALLUCB           GET UCB ADDRESS
         USING UCBDSECT,R3
ALL3     XR    R1,R1
         IC    R1,0(R4)            GET LENGTH FOR COMPARE
         EX    R1,ALLCL            IS THIS ONE?
         BE    ALL10               YES
         LA    R4,2(R1,R4)         ADVANCE IN EL. VOL. TABLE
         CLI   0(R4),FMON          END OF TABLE?
         BNE   ALL3                NO, CONTINUE
         L     R1,TBDVOL           START OF NOT-D. VOL. TABLE
ALL4     CLI   0(R1),FMOFF         END OF TABLE?
         BE    ALL5                YES
         CLC   0(L'UCBVOLI,R1),UCBVOLI  IS THIS VOLUME?
         BE    ALL1                YES, DON'T SET TWICE.
         LA    R1,TBDVOLL(R1)
         B     ALL4
ALL5     LA    R15,TBDVOLL(R1)     CONTROL NOT-D. VOL. TABLE
         CL    R15,TBDVOLE         ANY OVERFLOW?
         BL    ALL6                NO
         XR    R15,R15             YES
         B     ERR50
ALL6     MVC   0(L'UCBVOLI,R1),UCBVOLI  INSERT THIS VOLUME
         MVI   0(R15),FMOFF        SET FINAL MARKER
ALLSW    NOP   ALL7
        @OUT   ELMIS1,,U
         OI    ALLSW+1,ON
         LA    R6,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R5,10               MAX. BY LINE
ALL7     MVC   0(L'UCBVOLI,R6),UCBVOLI  MAY BE MISSING VOLUME
         BCT   R5,ALL8
        @OUT   LINE
         LA    R6,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R5,10               MAX. BY LINE
         B     ALL1
ALL8     LA    R6,10(R6)           ADVANCE IN LINE
         B     ALL1
ALLCL    CLC   1(*-*,R4),UCBVOLI   << EXECUTED >>
         SPACE 1
*        BUILD DDNAME TABLE ENTRY.
         SPACE 1
ALL10    L     R15,TBDDN           START OF DDNAME'S TABLE
ALL11    CLR   R15,R2              ANYTHING IN TABLE?
         BNL   ALL12               NO
         CLC   DDNVOL,UCBVOLI      IS THIS VOLSER?
         BE    ALL1                YES, DON'T PROCESS TWICE.
         LA    R15,DDNL(R15)
         B     ALL11
ALL12    LA    R15,DDNL(R2)        CONTROL DDNAME'S TABLE
         CL    R15,TBDDNE          ANY OVERFLOW?
         BL    ALL13               NO
         XR    R15,R15             YES
         B     ERR11
ALL13    MVC   DDNNME(2),=C'VT'    PREFIX FOR DDNAME
         MVC   DDNVOL,UCBVOLI      VOLSER
         MVC   DDNDTY,UCBTYP       UCBTYPE
         MVI   DDNALL,0
        $DAUNTY DDNDTY+3,DDNUTY,MF=(E,ALLWORK)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR12               ERROR, EXIT WITH CODE
         STC   R0,DDNLUT           LENGTH OF DDNUTY
         LA    R2,DDNL(R2)         ADVANCE IN DDNAME'S TABLE
         MVI   0(R2),FMOFF         SET FINAL MARKER
         B     ALL1
         DROP  R2,R3
ALL14    CL    R2,TBDDN            EMPTY DDNAME'S TABLE?
         BH    ALL15               NO
         XR    R15,R15             YES
         B     ERR13
ALL15    TM    ALLSW+1,ON
         BZ    ALL20
         LA    R0,10
         CLR   R0,R5               ANYTHING IN LINE?
         BE    ALL20               NO
        @OUT   LINE
         SPACE 1
*        ALLOCATE VOLUMES.
         SPACE 1
ALL20    L     R2,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R2
ALL21    MVC   ALLDDN(L'DDNNME),DDNNME  DDNAME FOR ALLOCATION
        $DAVALL ALLDDNL,DDNVOL,DDNLUT,MF=(E,ALLWORK)
         LTR   R15,R15             TEST RETURN
         BNZ   ALL22               ERROR
         OI    DDNALL,DDNXA        SET ALLOCATED
         LA    R2,DDNL(R2)         ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BNE   ALL21               NO, CONTINUE
         B     ALL30
         DROP  R2
ALL22    CH    R15,=H'12'          DYNALL ERROR?
         BNE   ERR14               NO, OTHER
        @OUT   (R1)                GET DIAGNOSE MESSAGE
         LH    R15,=H'12'          RESTORE CC
         B     ERR14               STOP, EXIT
         SPACE 1
*        LIST VOLUME'S ALLOCATION TABLE.
         SPACE 1
ALL30   @OUT   TAVOL,,U
         L     R2,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R2
ALL31    LA    R3,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R0,5                MAX. BY LINE
ALL32    CLI   0(R2),FMOFF         LAST ENTRY?
         BE    ALL34               YES
         MVC   0(L'DDNVOL,R3),DDNVOL    VOLSER
         MVC   8(L'DDNUTY,R3),DDNUTY    UNIT TYPE
         LA    R2,DDNL(R2)         ADVANCE IN TABLE
         BCT   R0,ALL33
         B     ALL34
ALL33    LA    R3,20(R3)           ADVANCE IN LINE
         B     ALL32
ALL34    CLI   LINE+6,C' '         ANYTHING IN LINE?
         BE    CHC0                NO
        @OUT   LINE
         CLI   0(R2),FMOFF         LAST ENTRY?
         BNE   ALL31               NO, CONTINUE
         DROP  R2
         SPACE 1
*        CHECK IF ALL CATALOG VOLUMES ARE ONLINE.
         SPACE 1
CHC0     L     R3,TBCVOL           START OF CAT. VOL. TABLE
         USING CVLT,R3
CHC1     CLI   0(R3),FMOFF         END OF TABLE?
         BE    CHC10               YES
         L     R2,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R2
CHC2     CLI   0(R2),FMOFF         END OF TABLE?
         BE    CHCSW               YES
         CLC   CVLVOL,DDNVOL       NO, VOLUME ALLOCATED?
         BE    CHC5                YES, OK
         DROP  R2
         LA    R2,DDNL(R2)         ADVANCE IN DDNAME'S TABLE
         B     CHC2
CHCSW    NOP   CHC3
        @OUT   CATMISS1,=PL2'+50',U
        @OUT   SPACE
         OI    CHCSW+1,ON
         LA    R4,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R0,10               MAX. BY LINE
CHC3     MVC   0(L'CVLVOL,R4),CVLVOL    MISSING VOLUME
         OI    CVLIND,CVLMIS       INDICATE CATALOG VOL. MISSING
         DROP  R3
         BCT   R0,CHC4
        @OUT   LINE
         LA    R4,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R0,10               MAX. BY LINE
         B     CHC5
CHC4     LA    R4,L'CVLVOL+4(R4)   ADVANCE IN LINE
CHC5     LA    R3,CVLL(R3)         ADVANCE IN CAT. VOL. TABLE
         B     CHC1
CHC10    TM    CHCSW+1,ON
         BZ    CHC99
         CLI   LINE+6,C' '         ANYTHING IN LINE?
         BE    CHC11               NO
        @OUT   LINE
CHC11   @OUT   CATMISS2
         OI    RUNIND,RUCATMIS     SET SWITCH
CHC99   @XE
         EJECT
ALLPLS  $DARSCA ALLUCB,MF=L
         SPACE 1
ALLWORK  DC    3F'0'
ALLUCB   DC    F'0'
ALLDDNL  DC    XL1'8'
ALLDDN   DC    CL8' '
         SPACE 1
         DC    AL1(L'ELMIS1)
ELMIS1   DC    C'0NOT DECLARED AS ELIGIBLE VOLUME(S) :'
         DC    AL1(L'TAVOL)
TAVOL    DC    C'0ALLOCATED VOLUMES CHECK LIST :'
         DC    AL1(L'CATMISS1)
CATMISS1 DC    C'0MISSING CATALOG VOLUME(S) :'
         DC    AL1(L'CATMISS2)
CATMISS2 DC    C'0===== PROCESSING HIGHLY REDUCED DUE TO MISSING CATALOC
               G(S) ====='
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
