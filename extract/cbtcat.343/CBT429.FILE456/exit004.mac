X004     TITLE 'JES2 - EXIT 004 - JCL AND JES2 CONTROL STATEMENT SCAN'
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
* MODULE NAME : EXIT004                                               *
* DESCRIPTIVE NAME : JES2 EXIT 4 (HASPRDR)                            *
*                    JCL AND JES2 CONTROL STATEMENT SCAN              *
* PURPOSE :     THIS JES2 EXIT ALLOWS YOU TO SCAN JCL AND JES2        *
*               CONTROL STATEMENTS (NOTE : JOB STATEMENTS AND         *
*               INTERNAL READER CONTROL STATEMENTS SUCH AS /*DEL      *
*               ARE NOT INCLUDED IN THE SCAN).                        *
* INVOCATION POINT : UEXIT004                                         *
* ENVIRONMENT : JES2 MAIN TASK                                        *
* REGISTERS AT ENTRY :                                                *
*               R0 : A CODE INDICATING WHETHER A JES2 CONTROL OR JCL  *
*                    CONTROL STATEMENT IS BEING PROCESSED             *
*                    0 = A JES2 CONTROL STATEMENT                     *
*                    4 = A JCL STATEMENT                              *
*               R1 : POINTER TO A 3-WORD PARAMETER LIST               *
*                    WORD 1 (+0) = ADDRESS OF THE CONTROL STATEMENT   *
*                           IMAGE BUFFER.                             *
*                    WORD 2 (+4) = ADDRESS OF THE EXIT FLAG BYTE,     *
*                           "RDWFLAGX", IN THE PCE.                   *
*                    WORD 3 (+8) = ADDRESS OF THE "JCTXWRK" FIELD     *
*                           IN THE JCT.                               *
*               R2-R9 : N/A                                           *
*               R10 : ADDRESS OF THE JCT                              *
*               R11 : ADDRESS OF THE HCT                              *
*               R12 : N/A                                             *
*               R13 : ADDRESS OF THE PCE                              *
*               R14 : RETURN ADDRESS                                  *
*               R15 : ENTRY ADDRESS                                   *
* REGISTERS ON RETURN :                                               *
*               R0-R1 : N/A                                           *
*               R2-R14 : AS AT ENTRY                                  *
*               R15 : A RETURN CODE :                                 *
*                     =0, CONTINUE USING ANY ADDITIONAL EXITS, THEN   *
*                         PERFORM STANDARD HASPRDR PROCESSING         *
*                     =4, CONTINUE WITHOUT USING ANY ADDITIONAL EXITS *
*                         BUT PERFORM STANDARD HASPRDR PROCESSING     *
*                     =8, FOR JES2 CONTROL STATEMENTS, TELLS JES2 NOT *
*                         TO PERFORM STANDARD PROCESSING, INSTEAD,    *
*                         IMMEDIATELY CONVERT THE STATEMENT TO A      *
*                         COMMENT (//*) WITH THE NULL-ON-INPUT FLAG   *
*                         SET TO ONE AND WRITE THE STATEMENT TO THE   *
*                         JCL DATA-SET. FOR JCL STATEMENTS, TELLS     *
*                         JES2 TO PERFORM STANDARD HASPRDR PROCESSING *
*                     =12, TELLS JES2 TO CANCEL THE JOB BECAUSE AN    *
*                         ILLEGAL CONTROL STATEMENT HAS BEEN DETECTED *
*                         (OUTPUT IS PRODUCED)                        *
*                     =16, TELLS JES2 TO PURGE THE JOB BECAUSE AN     *
*                         ILLEGAL CONTROL STATEMENT HAS BEEN DETECTED *
*                         (NO OUTPUT IS PRODUCED)                     *
* LINKAGE EDITOR INFO :                                               *
*               ATTRIBUTES : REENTRANT, REUSEABLE, REFRESHABLE        *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
* AUTHOR :      MOINIL P.A.                                           *
*               COMPUTING CENTRE                                      *
*               J.R.C. - ISPRA ESTABLISHMENT                          *
*               21020 ISPRA (VA), ITALY                               *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
* JES2PARM INITIALIZATION STATEMENTS :                                *
*               LOAD=J2XIT004                                         *
*               EXIT004 ROUTINE=UEXIT004,DISABLE,TRACE=YES            *
*                                        ENABLE        NO             *
*                                                                     *
* JES2 OPERATOR CONTROL COMMANDS :                                    *
*               $T EXIT004,DISABLE,TRACE=YES                          *
*                          ENABLE        NO                           *
*               $D EXIT004                                            *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
* PURPOSE :     SCAN FOR AND HANDLE JOBIO= OR I= NEW LOCAL PARAMETER  *
*               IN THE JES2 /*JOBPARM CONTROL STATEMENT.              *
*               FORMAT : JOBIO=NNNN OR I=NNNN                         *
*                        WHERE NNNN IS A 1 UP TO 4 NUMERIC DIGITS.    *
*               OBJECT : NNNN IS THE ESTIMATED TOTAL SRB TIME FOR A   *
*                        JOB (EXPRESSED IN SECONDS).                  *
*                        THE VALUE IS STORED IN "JCTUSER0" FIELD.     *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         EJECT
         COPY  $HASPGBL
         SPACE 1
J2XIT004 $MODULE ENVIRON=JES2,TITLE='JES2 - EXIT 004',                 X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP HCT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $RDRWORK,           GENERATE HASP RDRWORK DSECT         X
               RPL                 GENERATE MVS RPL DSECT
         TITLE 'JES2 - EXIT 004 - JCL AND JES2 CONTROL STATEMENT SCAN'
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        EXIT MAIN ENTRY POINT.                                       *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
UEXIT004 $ENTRY BASE=R12
        $SAVE  ,                   SAVE CALLER'S REGISTERS
         LR    R12,R15             ESTABLISH BASE REGISTER
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        SELECT CONTROL STATEMENT PROCESS.                            *
*              THE RETURN CODE 0 TELLS JES2 TO CONTINUE WITH          *
*              STANDARD PROCESSING.                                   *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         LTR   R0,R0
         BNZ   EXIT0               LET'S GO, JCL STATEMENT
         LM    R7,R9,0(R1)         3-WORD'S PARM. ADDRESSES
         LTR   R9,R9               ADDRESS OF JCTXWRK
         BZ    EXIT0               LET'S GO, NOT IN A JOB
         CLC   0(10,R7),=CL10'/*JOBPARM'
         BNE   EXIT0               LET'S GO, NOT /*JOBPARM
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                / * J O B P A R M    S T A T E M E N T               *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        SEARCH JOBIO= OR I= KEYWORD.                                 *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         LA    R4,10(R7)           START OF SCAN
         LA    R3,69(R7)           END OF SCAN
         LA    R2,1                STEP TO SCAN
         CLI   0(R4),C' '          SKIP BLANKS IF ANY BEFORE KEYWORDS
         BNE   *+L'*+8
         BXLE  R4,R2,*-8
         B     EXIT0               NONE, LET'S GO
         LR    R5,R4               RETAIN START OF KEYWORD(S)
SEARCH   CLC   0(6,R4),=CL6'JOBIO=' SCAN TO FIND KEYWORD I NEED
         BE    FOUND
         CLC   0(2,R4),=CL2'I='
         BE    FOUND
         CLI   0(R4),C' '
         BE    EXIT0               NONE, LET'S GO
         BXLE  R4,R2,SEARCH
         B     EXIT0               NONE, LET'S GO
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        PROCESS JOBIO= OR I= VALUE.                                  *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
FOUND    LR    R6,R4               RETAIN START OF THIS KEYWORD
         LA    R3,71(R7)           END OF SCAN
         XR    R0,R0
         XR    R1,R1
         CLI   1(R4),C'='
         BE    *+L'*+4
         LA    R4,4(R4)
         LA    R4,1(R4)
LOOP     BXLE  R4,R2,*+L'*+4       SCAN AND COMPUTE VALUE
         B     VERVAL
         CLI   0(R4),C' '
         BE    VERVAL
         CLI   0(R4),C','
         BE    VERVAL
         CLI   0(R4),C'0'
         BL    NOTNUM              ERROR, NOT NUMERIC DIGIT
         CLI   0(R4),C'9'
         BH    NOTNUM              ERROR, NOT NUMERIC DIGIT
         IC    R0,0(R4)
         N     R0,=A(X'0F')
         LTR   R1,R1
         BNP   *+L'*+4
         MH    R1,=H'10'
         AR    R1,R0
         CH    R1,=H'9999'
         BH    VTLRGE              ERROR, VALUE TOO LARGE
         B     LOOP
VERVAL   LTR   R1,R1
         BNP   VNLL                ERROR, NULL VALUE
         USING JCT,R10
         ST    R1,JCTUSER0         SET REQUESTED SRB TIME
         DROP  R10
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        BUILD NEW /*JOBPARM FOR JES2 IF NEEDED.                      *
*              THE RETURN CODE 8 TELLS JES2 TO SKIP THE STANDARD      *
*              PROCESSING AND, INSTEAD, IMMEDIATELY CONVERT THE       *
*              STATEMENT TO A COMMENT (//*).                          *
*              IF ANY PARAMETER OTHER THAN JOBIO= (OR I=) IS          *
*              PRESENT, A NEW /*JOBPARM IS BUILDED AND PASSED TO      *
*              JES2 AS NEXT CARD IMAGE.                               *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CLR   R5,R6
         BNE   *+L'*+8
         CLI   0(R4),C' '
         BE    EXIT8               ONLY JOBIO= OR I= IN /*JOBPARM
         OI    0(R8),RDWXXSNC      SAY NEXT CARD IMAGE SUPPLIED
         MVC   0(80,R9),0(R7)      COPY /*JOBPARM CARD
         MVI   10(R7),C' '         BUILD /*JOBPARM JOBIO= OR I=
         MVC   11(61,R7),10(R7)
         SLR   R6,R7
         ALR   R6,R9
         SLR   R4,R7
         ALR   R4,R9
         LR    R1,R4
         SLR   R1,R6
         BCT   R1,*+L'*+6
         MVC   0(*-*,R5),0(R6)     <<EXECUTED>>
         EX    R1,*-6
         CLI   55(R7),C' '
         BNE   *+L'*+6
         MVC   55(15,R7),=CL15'-LOCAL-PROCESS-'
         LR    R5,R6               BUILD REMAINING /*JOBPARM
         LR    R3,R4
         BCTR  R3,0
         LA    R2,1
         MVI   0(R5),C' '
         BXLE  R5,R2,*-4
         CLI   0(R4),C' '
         BNE   SHIFT
         BCTR  R6,0
         MVI   0(R6),C' '
         B     EXIT8
SHIFT    LA    R3,71(R9)
         MVI   0(R4),C' '
         BXLE  R4,R2,*+L'*+4
         B     EXIT8
         CLI   0(R4),C' '
         BE    EXIT8
         MVC   0(1,R6),0(R4)
         ALR   R6,R2
         B     SHIFT+L'SHIFT
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        ERROR DETECTED, PREPARE MESSAGE.                             *
*              THE RETURN CODE 12 TELLS JES2 TO CANCEL THE JOB        *
*              WITH THE STATEMENT AS THE LAST IN THE JCL DATA-SET.    *
*              AN ERROR MESSAGE IS BUILDED AND PASSED TO JES2.        *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
NOTNUM   LA    R3,=CL24'= NOT NUMERIC (0-9) ...'
         ALR   R4,R2
         B     SETMSG
VTLRGE   LA    R3,=CL24'= GREATER THAN 9999 ...'
         ALR   R4,R2
         B     SETMSG
VNLL     LA    R3,=CL24'= EQUAL 0 (INVALID) ...'
SETMSG   MVC   0(10,R9),=CL10'  ERROR : ' BUILD DIAGNOSE MESSAGE
         MVC   10(70,R9),9(R9)
         SLR   R4,R6
         BCT   R4,*+L'*+6
         MVC   10(*-*,R9),0(R6)    <<EXECUTED>>
         EX    R4,*-6
         LA    R1,12(R9,R4)
         MVC   0(24,R1),0(R3)
         OI    0(R8),RDWXXSEM      SAY ERROR MESSAGE SUPPLIED
         B     EXIT12
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        GO BYE-BYE.                                                  *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
EXIT0    XR    R15,R15             SET RETURN CODE TO 0
         SPACE 1
LEAVE   $RETURN RC=(R15)           RETURN TO JES2
         SPACE 1
EXIT8    LA    R15,8               SET RETURN CODE TO 8
         B     LEAVE
EXIT12   LA    R15,12              SET RETURN CODE TO 12
         B     LEAVE
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*        LITERAL POOL.                                                *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        LTORG
         SPACE 1
PATCH    DC    128S(*)
         DC    CL8'ENDPATCH'
         SPACE 1
        $MODEND
         SPACE 1
         END
