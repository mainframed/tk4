NQAN     TITLE 'ENQ ANALYSIS PROGRAM.'
ENQANAL  START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* *                                                                 * *
* *           E N Q     A N A L Y S I S     P R O G R A M           * *
* *                                                                 * *
* * PURPOSE :  OBTAIN A MORE READABLE OUTPUT LIST OF THE ENQ'S      * *
* *            (SVC 56) TRACE DATA. IT USES AS INPUT THE NORMAL     * *
* *            GTF OUTPUT LIST RECEIVED FROM A PREVIOUS GTF RUN.    * *
* *                                                                 * *
* * FILES :    SYSIN    - INPUT (GTF LIST)                          * *
* *            SYSPRINT - OUTPUT LIST                               * *
* *                                                                 * *
* * AUTHOR :   MOINIL P.A.                                          * *
* *            COMPUTING CENTER                                     * *
* *            J.R.C. - ISPRA ESTABLISHMENT                         * *
* *            21020 ISPRA (VA), ITALY                              * *
* *                                                                 * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
        $DEFREG
        $XENT  BASE=(R11,R12)
         EJECT
*        EXTRACT DATA FROM GTF LIST
         SPACE 1
         LA    R10,MAXERR
RGTFD   $READ  GTFREC              READ OUTPUT GTF LIST
         LTR   R15,R15             TEST RETURN CODE
         BM    ERR1                END OUTPUT GTF LIST (NO DATE)
         BNZ   QUIT                READ ERROR EXIT
         CLC   GTFREC+1(9),=CL9'*** DATE '
         BNE   RGTFD               LOOP TILL FOUND
         MVC   DAY(L'DAY),GTFREC+5 SET DATE IN TITLE
         MVC   STME(L'STME),GTFREC+49   SET START TIME
         MVC   ETME(L'ETME),GTFREC+49   SET END TIME
         SPACE 1
         LA    R7,LINE
         USING QLINE,R7
RGTFS   $READ  GTFREC              READ OUTPUT GTF LIST
         LTR   R15,R15             TEST RETURN CODE
         BM    ENDRGTF             END OUTPUT GTF LIST
         BNZ   QUIT                READ ERROR EXIT
         CLC   GTFREC+1(9),=CL9'*** DATE '
         BNE   TSVC
         MVC   ETME(L'ETME),GTFREC+49   SET END TIME
         B     RGTFS
TSVC     CLC   GTFREC+1(9),=CL9'SVC   056'
         BNE   RGTFS               LOOP TILL FOUND
         CLC   GTFREC+35(6),=CL6' JOBN '
         BNE   ERR2                NO MATCH
         MVC   JOBN(L'JOBN),GTFREC+41
RGTFM   $READ  GTFREC              READ OUTPUT GTF LIST
         LTR   R15,R15             TEST RETURN CODE
         BM    ERR3                END OUTPUT GTF LIST
         BNZ   QUIT                READ ERROR EXIT
         CLC   GTFREC+111(6),=CL6' PAGE '
         BE    RGTFM               SKIP NEW PAGE
         CLC   GTFREC+52(7),=CL7' MAJOR '
         BNE   ERR4                NO MATCH
         CLC   GTFREC+68(7),=CL7' MINOR '
         BNE   ERR5                NO MATCH
         MVC   MAJN(L'MAJN),GTFREC+59
         MVC   MINN(L'MINN),GTFREC+75
RGTFP   $READ  GTFREC              READ OUTPUT GTF LIST
         LTR   R15,R15             TEST RETURN CODE
         BM    ERR6                END OUTPUT GTF LIST
         BNZ   QUIT                READ ERROR EXIT
         CLC   GTFREC+111(6),=CL6' PAGE '
         BE    RGTFP               SKIP NEW PAGE
         CLC   GTFREC+11(6),=CL6'PLIST '
         BNE   ERR7                NO MATCH
         MVC   PLIST(L'PLIST),GTFREC+26
         NC    GTFREC+26(8),MASK
         TR    GTFREC+26(8),TABIN
         PACK  DATA(L'DATA),GTFREC+26(9)
         TM    DATA+2,X'80'
         BZ    *+L'*+10
         MVC   REQST,=CL4'SHR'
         B     *+L'*+6
         MVC   REQST,=CL4'EXCL'
         TM    DATA+2,X'48'
         BZ    SS1
         BO    SS2
         TM    DATA+2,X'08'
         BZ    SS3
         MVC   SCOPE,=CL7'RESERVE'
         B     SS0
SS1      MVC   SCOPE,=CL7'STEP'
         B     SS0
SS2      MVC   SCOPE,=CL7'SYSTEMS'
         B     SS0
SS3      MVC   SCOPE,=CL7'SYSTEM'
SS0      TM    DATA+2,X'07'
         BZ    RR0
         BO    RR3
         TM    DATA+2,X'03'
         BZ    RR0
         BO    RR2
         TM    DATA+2,X'02'
         BO    RR1
         MVC   RETCR,=CL4'HAVE'
         B     RR0
RR1      MVC   RETCR,=CL4'CHNG'
         B     RR0
RR2      MVC   RETCR,=CL4'USE'
         B     RR0
RR3      MVC   RETCR,=CL4'TEST'
RR0      TM    DATA+2,X'10'
         BZ    *+L'*+6
         MVC   MCFLG,=CL2'MC'
         CLC   SCOPE,=CL7'RESERVE'
         BNE   PR0
         LA    R0,SCOPE+L'SCOPE    POINTS OUT 'RESERVE'
         LA    R1,MAJN-1
FILL     CLI   0(R1),C' '
         BNE   *+L'*+4
         MVI   0(R1),C'.'
         BCTR  R1,0
         CLR   R0,R1
         BNH   FILL
PR0      CP    CNTL,=P'+50'        NEW PAGE ?
         BL    PR1                 NO
         MVC   PGED,=XL6'402020202120'
         ED    PGED,PGNO           SET PAGE NUMBER
         AP    PGNO,=P'+1'         UPDATE PAGES COUNTER
        $PRINT TIT                 SKIP PAGE AND TITLE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   QUIT                PRINT ERROR EXIT
        $PRINT HEAD                HEADER
         LTR   R15,R15             TEST RETURN CODE
         BNZ   QUIT                PRINT ERROR EXIT
        $PRINT UNDR                UNDERLINES IT
         LTR   R15,R15             TEST RETURN CODE
         BNZ   QUIT                PRINT ERROR EXIT
         ZAP   CNTL,=P'+0'         RESET LINES COUNTER
PR1     $PRINT LINE                OUTPUT LINE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   QUIT                PRINT ERROR EXIT
         MVC   CC+1(FHLL),CC       RESET LINE UNLESS MINOR NAME
         AP    CNTL,=P'+1'         UPDATE LINES COUNTER
         SR    R3,R3
         IC    R3,DATA+1           GET MINOR NAME LENGTH
         LR    R4,R3
         BCTR  R3,0
         EX    R3,TST              ANY BINARY INFO ?
         BZ    RGTFS               BRANCH IF NOT
         MVI   MINNSN,C' '
         MVC   MINNSN+1(L'MINNSN-1),MINNSN
         EX    R3,MVP              PROCESS NUMERICS
         EX    R3,NCP
         LA    R2,MINN(R3)         PROCESS ZONES
         SR    R1,R1
PRZNS    IC    R1,0(R2)
         SRL   R1,4
         STC   R1,0(R2)
         BCTR  R2,0
         BCT   R4,PRZNS
         EX    R3,TRP              EDIT ZONES
        $PRINT LINE                OUTPUT LINE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   QUIT                PRINT ERROR EXIT
         MVC   MINN,MINNSN
         EX    R3,TRP              EDIT NUMERICS
        $PRINT LINE                OUTPUT LINE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   QUIT                PRINT ERROR EXIT
         AP    CNTL,=P'+2'         UPDATE LINES COUNTER
         B     RGTFS
         SPACE 1
TST      TRT   MINN(*-*),TABT
MVP      MVC   MINNSN(*-*),MINN
NCP      NC    MINNSN(*-*),MINNMN
TRP      TR    MINN(*-*),TRTAB
         DROP  R7
         SPACE 1
ENDRGTF $PRINT LAST                GTF RUN TIME
QUIT     SR    R10,R10             CC = 0
EXRET   $RCLOSE ,                  CLOSE ALL FILES
        $PCLOSE ,
        $XRET  CC=(R10)            EXIT WITH CC
         EJECT
*        ERRORS PROCESSING / MESSAGES
         SPACE 1
ERR1     BCTR  R10,0               COMPUTE ERROR NUMBER
ERR2     BCTR  R10,0
ERR3     BCTR  R10,0
ERR4     BCTR  R10,0
ERR5     BCTR  R10,0
ERR6     BCTR  R10,0
ERR7     BCTR  R10,0
         SPACE 1
MAXERR   EQU   ((*-ERR1)/2)+1
         SPACE 1
         LR    R2,R10
         BCTR  R2,0
         SLL   R2,2
         L     R1,TABD(R2)
        WTO    MF=(E,(1))          LOG DIAGNOSTIC
         B     EXRET
         SPACE 2
*        TEST TABLE.
         SPACE 1
TABT     DS    0F 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 0
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 1
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 2
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 3
         DC    X'00FFFFFFFFFFFFFFFFFFFF00FF0000FF' 4   .(+
         DC    X'00FFFFFFFFFFFFFFFFFFFF000000FFFF' 5  &$*)
         DC    X'0000FFFFFFFFFFFFFFFFFF0000FFFFFF' 6  -/,%
         DC    X'FFFFFFFFFFFFFFFFFFFFFF0000000000' 7  #@'="
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 8
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 9
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' A
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' B
         DC    X'FF000000000000000000FFFFFFFFFFFF' C  ABCDEFGHI
         DC    X'FF000000000000000000FFFFFFFFFFFF' D  JKLMNOPQR
         DC    X'FFFF0000000000000000FFFFFFFFFFFF' E  STUVWXYZ
         DC    X'00000000000000000000FFFFFFFFFFFF' F  0123456789
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         SPACE 2
*        BINARY TABLE.
         SPACE 1
TABIN    DS    0F 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'000A0B0C0D0E0F000000000000000000' 0
         DC    X'00010203040506070809000000000000' 1
         EJECT
*        ERRORS DIAGNOSTICS
         SPACE 1
TABD     DC    A(DG1,DG2,DG3,DG4,DG5,DG6,DG7)
         SPACE 1
DG1     WTO    'ERR 1 - NO DATA IN GTF OUTPUT LIST.',                  1
               ROUTCDE=11,DESC=7,MF=L
DG2     WTO    'ERR 2 - SVC 056 JOBN KEYWORD NOT FOUND.',              1
               ROUTCDE=11,DESC=7,MF=L
DG3     WTO    'ERR 3 - SVC 056 MAJOR-MINOR LINE NOT RECEIVED (EOF).', 1
               ROUTCDE=11,DESC=7,MF=L
DG4     WTO    'ERR 4 - SVC 056 MAJOR KEYWORD NOT FOUND.',             1
               ROUTCDE=11,DESC=7,MF=L
DG5     WTO    'ERR 5 - SVC 056 MINOR KEYWORD NOT FOUND.',             1
               ROUTCDE=11,DESC=7,MF=L
DG6     WTO    'ERR 6 - SVC 056 PLIST LINE NOT RECIVED (EOF).',        1
               ROUTCDE=11,DESC=7,MF=L
DG7     WTO    'ERR 7 - SVC 056 PLIST KEYWORD NOT FOUND.',             1
               ROUTCDE=11,DESC=7,MF=L
         SPACE 2
*        CONSTANTS AND WORK AREAS
         SPACE 1
TRTAB    DC    CL16'0123456789ABCDEF'
DATA     DC    XL5'0'
CNTL     DC    PL2'+99'
PGNO     DC    PL3'+1'
MASK     DC    8X'1F'
MINNSN   DC    CL52' '
MINNMN   DC    52X'0F'
GTFREC   DC    CL140' '
         DC    AL1(L'LINE)
LINE     DC    CL131' '
         DC    AL1(TITL)
TIT      DC    CL1'1',CL10' '
         DC    C'INFORMATIONAL ENQ''S ANALYSIS OUTPUT FROM GTF '
DAY      DC    CL30' ',CL15' ',C'PAGE'
PGED     DC    CL6' ',C'.'
TITL     EQU   *-TIT
         DC    AL1(HEADL)
HEAD     DC    CL1'0',CL4' '
         DC    CL8'JOB-NAME',CL2' '
         DC    CL8' P.LIST',CL2' '
         DC    CL4'REQ.',CL2' '
         DC    CL7' SCOPE',CL2' '
         DC    CL4'RET',CL6' '
         DC    CL8'MAJOR N.',CL2' '
         DC    CL52'MINOR NAME'
HEADL    EQU   *-HEAD
         DC    AL1(UNDRL)
UNDR     DC    CL1' ',CL4' '
         DC    8C'-',CL2' '
         DC    8C'-',CL2' '
         DC    4C'-',CL2' '
         DC    7C'-',CL2' '
         DC    4C'-',CL6' '
         DC    8C'-',CL2' '
         DC    52C'-'
UNDRL    EQU   *-UNDR
         DC    AL1(LASTL)
LAST     DC    CL1'0',CL10' '
         DC    C'GTF RUN TIME : FROM '
STME     DC    CL8' ',C' UP TO '
ETME     DC    CL8' ',C' - END OF LIST.'
LASTL    EQU   *-LAST
         EJECT
        LTORG
         SPACE 2
QLINE    DSECT                     OUTPUT LINE DESCRIPTION
         SPACE 1
CC       DS    CL1,CL4
JOBN     DS    CL8,CL2             JOB-NAME
PLIST    DS    CL8,CL2             PLIST (HEX)
REQST    DS    CL4,CL2             REQUEST (EXCL/SHR)
SCOPE    DS    CL7,CL2             SCOPE (RESERVE/SYSTEMS/SYSTEM/STEP)
RETCR    DS    CL4,CL2             RET (TEST/USE/HAVE/CHNG)
MCFLG    DS    CL2,CL2             MUST COMPLETE
MAJN     DS    CL8                 MAJOR NAME
FHLL     EQU   *-(CC+1)
         DS    CL2
MINN     DS    CL52                MINOR NAME (MAXIMUM)
         SPACE 2
         END
