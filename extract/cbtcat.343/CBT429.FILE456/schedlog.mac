SCHL     TITLE '--- SCHEDULE/SCHEDRUN LOG LIST ---'
SCHEDLOG START 0
         SPACE 1
* PURPOSE :    LIST THE LOG FILE OUTPUT OF SCHEDRUN PROGRAM EXECUTION.
* ---------
*
* JCL TO EXECUTE IT :  //...     EXEC PGM=SCHEDLOG
* -------------------  //SYSIN     DD DSN=->.OPER.SCHEDLOG,DISP=SHR
*                      //SYSPRINT  DD SYSOUT=A
*
*                  OR  //...     EXEC PGM=SCHEDLOG,PARM=TEST
*                      TO MAP THE LOG FILE RECORDS.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R11
         L     R2,0(R1)
         XR    R0,R0
         ICM   R0,B'0011',0(R2)
         BZ    NPRM
         CH    R0,=H'4'
         BNE   NPRM
         CLC   2(4,R2),=CL4'TEST'
         BNE   NPRM
         OI    SWITCH,TRUN
NPRM    OPEN   (SCDCB,(INPUT))
         TM    SCDCB+(DCBOFLGS-IHADCB),DCBOFOPN
         BO    RVNXS
         LA    R10,100
         B     RVRET
RVNXS   GET    SCDCB
         XR    R2,R2
         ICM   R2,B'0011',0(R1)
         SH    R2,=H'4'
         LA    R3,4(R1)
         TM    SWITCH,TRUN
         BO    *+L'*+12
         LA    R4,108
         NI    SWITCH,255-TSSC
         B     RVNXT+L'RVNXT
         MVC   LNM,=CL4'-S->'
RVNXT    LA    R4,100
         CLR   R2,R4
         BH    RVMVT
         LR    R4,R2
         TM    SWITCH,TRUN
         BZ    RVMVT
         LA    R1,LNT(R4)
         MVC   0(4,R1),=CL4'<-E-'
RVMVT    BCTR  R4,0
         TM    SWITCH,TRUN
         BO    RMVTN
         TM    SWITCH,TSSC
         BZ    *+L'*+8
         EX    R4,SMVC
         B     RMVTN+L'RMVTN
         OI    SWITCH,TSSC
RMVTN    EX    R4,SMVN
         CP    NLN,=PL2'+50'
         BL    PLNE
         ZAP   NLN,=PL1'+0'
         AP    NPG,=PL1'+1'
         MVC   TITP,=XL6'402020202120'
         ED    TITP,NPG
        $PRINT TIT
         LTR   R10,R15
         BNZ   RVEOF
         TM    SWITCH,TRUN
         BO    PSCL
        $PRINT UNL
         B     PTST
PSCL    $PRINT SCL
PTST     LTR   R10,R15
         BNZ   RVEOF
PLNE    $PRINT LNE
         LTR   R10,R15
         BNZ   RVEOF
         AP    NLN,=PL1'+1'
         MVC   LNM(L'LNM+L'LNT),LNE+1
         LA    R4,1(R4)
         ALR   R3,R4
         SR    R2,R4
         BP    RVNXT
         B     RVNXS
SMVN     MVC   LNT(*-*),0(R3)
SMVC     MVC   LNT+8(*-*),0(R3)
RVEOF   CLOSE  SCDCB
        $PCLOSE
         LTR   R10,R10
         BNZ   RVRET
         LR    R10,R15
RVRET   $XRET  CC=(R10)
         EJECT
*        CONSTANTS AND WORK AREAS.
*        -------------------------
         SPACE 1
SWITCH   DC    XL1'0'
TRUN     EQU   X'80'
TSSC     EQU   X'40'
NPG      DC    PL3'+0'
NLN      DC    PL2'+99'
         DC    AL1(LTIT)
TIT      DC    CL6'1 '
         DC    CL100'SCHEDULE/SCHEDRUN LOG LIST'
         DC    CL4'PAGE'
TITP     DC    CL6' '
LTIT     EQU   *-TIT
         DC    AL1(LUNL)
UNL      DC    CL6' '
         DC    C'--------------------------'
LUNL     EQU   *-UNL
         DC    AL1(LSCL)
SCL      DC    CL6' '
         DC    C'----+----1----+----2----+----3----+----4----+----5'
         DC    C'----+----6----+----7----+----8----+----9----+----0'
LSCL     EQU   *-SCL
         DC    AL1(LLNE)
LNE      DC    CL2' '
LNM      DC    CL4' '
LNT      DC    CL110' '
LLNE     EQU   *-LNE
         PRINT NOGEN
SCDCB   DCB    DSORG=PS,DEVD=DA,MACRF=GL,BFALN=D,                      X
               EODAD=RVEOF,DDNAME=SYSIN
         PRINT GEN
         SPACE 2
        LTORG
         SPACE 2
         PRINT NOGEN
        DCBD   DSORG=PS,DEVD=DA
         SPACE 2
         END
