MAIN     TITLE 'CLEAN-UP MAINTENANCE PROCESSING (MAIN).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
MAIN     START 0
         SPACE 1
        $HEDIT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *           C L E A N - U P           * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
* RESTRICTION :    BE CAREFULL WHEN RUNNING THIS PROGRAM NOT ALONE    *
* -------------    IN THE SYSTEM, UNPREDICTABLE RESULTS MAY OCCURS    *
*                  UNLESS ALL ACTIVE JOBS ARE EXECUTION FREEZED.      *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
        @DOC
         EJECT
        $DEFREG
*        REGISTERS USE :
*        ---------------
*
*              R8  = LOCAL FUNCTIONS BASE REGISTER (@X CALL FEATURE).
*              R9  = ERROR NUMBER COUNTER.
*              R10 = CONSTANTS AND WORK-AREAS BASE REGISTER (COMMON).
*              R11 = SERVICES BASE REGISTER.
*              R12 = MAIN BASE REGISTER.
*              R13 = SAVE AREA POINTER.
*              R14 = LINK REGISTER.
         EJECT
*        START PROGRAM ENTRY.
*
*        NOTE - THE MODULE IS NOT RE-ENTERABLE, BUT THE 'RENT' OPTION
*               IN THE $XENT AND $XRET MACROS IS USED TO OBTAIN AND
*               FREE THE STACK SAVE AREAS SPACE.
         SPACE 1
        $XENT  BASE=R12,LV=NSSVL,TYPE=RENT
         ST    R13,SLDBGA          SET SAVE AREAS STACK ADDRESS
         LM    R10,R11,MAINADD
         USING COMMON,R10
         USING SERVICES,R11
         L     R9,MAXERR           TO COMPUTE ERROR NUMBER
         MVC   SVARL(STIDL,R13),8(R12)  SET MAIN IDENTIFIER
         MVC   ABADD+2(2),=S(MAINAB)    FILL ADDRESSES
         MVC   ABEADD+2(2),=S(MAINABE)
         B     MAINGO
         SPACE 1
MAINADD  DC    A(COMMON),V(SERVICES)
         EJECT
MAINGO  @X     A001      INITIALIZATION
        @X     A002      INITIALIZATION (CONT'D)
        @X     A003      READ EL. AND CAT. VOLUMES NAMES
        @X     A004      ALLOCATE VOLUMES
        @X     A005      READ VTOC'S CONTENTS
        @X     A006      READ CATALOGS CONTENTS
        @X     A007      READ RACF (DATA-SETS) CONTENTS
        @X     A008      LOCATE DATA-SETS
        @X     A009      SCHEDULE N-CAT./SPECIAL REQUESTS
        @X     A010      CONTROLS AND DECISIONS
        @X     A011      LIST DSNAME'S TABLE
        @X     A012      RACF RECOVERY PROCESSING
        @X     A013      SCRATCH/UNCATALOG PROCESSING
        @X     A014      USER'S ACCESS LIST AND RECOVERY
        @X     A015      GROUP'S ACCESS LIST
         EJECT
*        PROCESSING TERMINATION.
         SPACE 1
        $GDATE FILLDT,WORKD,MF=(E,MAINDT)
         MVC   MSGENDT(2),WORKD    SET END TIME
         MVC   MSGENDT+3(2),WORKD+2
         MVC   MSGENDT+6(2),WORKD+4
        @OUT   MSGEND              TERMINATED MESSAGE
         XR    R9,R9
         TM    RUNSN,SNDBG         SNAP 'DBG' REQUESTED?
         BZ    MAINRET             NO
         OI    MAINTSW+1,ON        YES, DO IT
         B     MAINTS0
         SPACE 1
MAINAB   TM    RUSREQ,RUSABND
         BZ    MAINTS
         TM    RUNIND,RUTEST       TEST RUN?
         BZ    MAINTS              NO
         STM   R0,R15,MAINSVA      SAVE, DUMP IS REQUESTED
         B     MAINAB0
MAINTS   TM    RUNSN,SNDBG         SNAP 'DBG' REQUESTED?
         BZ    MAINAB0             NO
MAINTS0  STM   R0,R15,MAINSVA      ALL REGISTERS
         BAS   R14,SNAPS
         DC    AL4(SLDBG),XL1'0',CL3'DBG'
         ICM   R14,B'1111',TBEVOL  DATA ARRAYS
         BZ    MAINTSW
         ST    R14,SLDBGD
         L     R14,TOTSPCE
         ST    R14,SLDBGD+4
         OI    SLDBGD+4,X'80'
         L     R14,=V(#SNSW#)
         MVI   0(R14),1
         BAS   R14,SNAPS
         DC    AL4(SLDBGD),XL1'0',CL3'DBG'
         L     R2,AGIDST
         LTR   R2,R2
         BZ    MAINTSW
         MVC   SLDBGD+4,LGIDST
         OI    SLDBGD+4,X'80'
MAINTS1  ST    R2,SLDBGD
         L     R14,=V(#SNSW#)
         MVI   0(R14),1
         BAS   R14,SNAPS
         DC    AL4(SLDBGD),XL1'0',CL3'DBG'
         L     R2,0(R2)
         LTR   R2,R2
         BNZ   MAINTS1
MAINTSW  NOP   MAINRET
MAINAB0  MVC   DIAG3(L'MNAME),MNAME
         CVD   R9,WORKD            DISPLAY ON LOG THE EC AND RC
         MVC   DIAG1,MASKERR
         ED    DIAG1,WORKD+6
         LTR   R15,R15
         BNM   *+L'*+10
         MVC   DIAG2,=CL4'  -1'
         B     *+L'*+16
         CVD   R15,WORKD
         MVC   DIAG2,MASKERR
         ED    DIAG2,WORKD+6
        WTO    MF=(E,DIAG)
         TM    RUSREQ,RUSABND
         BZ    MAINAB1
         TM    RUNIND,RUTEST       TEST RUN?
         BO    MAINA33             YES, ABEND WITH DUMP
MAINAB1  L     R1,MAXERR
         L     R15,ERRTAB
         CL    R1,0(R15)
         BE    MAINAB2
         MVC   DIAGM+22(41),=CL41'ERR.DESC. TABLE ERROR - MSG FLUSHED.'
         B     MAINAB4
MAINAB2  SLL   R9,2
         AR    R9,R15
         XR    R1,R1
         LR    R15,R1
         IC    R1,0(R9)
         ICM   R15,B'0111',1(R9)
         EX    R1,MAINMV
         LA    R15,DIAGM+18(R1)
         LA    R14,1
         LA    R1,DIAGM+21
MAINAB3  BXLE  R1,R14,*+L'*+4
         B     MAINAB4
         CLC   0(6,R1),=CL6'......'
         BNE   MAINAB3
         MVC   0(6,R1),VSNERR
MAINAB4 WTO    MF=(E,DIAGM)
         LA    R9,16
MAINABE  LH    R15,STACKNO         BACKWARD STACK TO TERMINATE
MAINAB5  CH    R15,=H'1'
         BNH   MAINAB6
         L     R13,4(R13)
         SH    R15,=H'1'
         B     MAINAB5
MAINMV   MVC   DIAGM+22(*-*),0(R15)     << EXECUTED >>
MAINAB6  STH   R15,STACKNO
         SPACE 1
MAINRET @X     A099                END OF PROCESS
        $XRET  CC=(R9),LV=NSSVL,TYPE=RENT
         SPACE 1
*        ERROR ABEND.
         SPACE 1
MAINA33  LM    R0,R15,MAINSVA      RESTORE ALL REGISTERS
        ABEND  33,DUMP
         EJECT
        CNOP   0,8
MAINSVA  DC    16F'0'              REG. 0 TO 15 AT ERROR POINT
         SPACE 1
SLDBG    DC    A(MAINSVA,16*4)               REG. 0 - 15
SLDBGA   DC    A(*-*,NSSVL)                  SAVE AREAS STACK
         DC    A(COMMON,CMMNL+X'80000000')   COMMON
SLDBGD   DC    A(*-*,*-*)                    DATA ARRAYS
         SPACE 1
MAINDT  $GDATE ,,MF=L
         SPACE 1
DIAG     WTO    '--- CLEAN ERROR : EC =    , RC =     (        ).',    C
               ROUTCDE=11,DESC=7,MF=L
DIAG1    EQU   DIAG+26,4
DIAG2    EQU   DIAG+36,4
DIAG3    EQU   DIAG+42,8
         SPACE 1
DIAGM   WTO    '                                                       C
                                      ',ROUTCDE=(2,11),DESC=6,MF=L
         EJECT
        LTORG
         SPACE 2
         CNOP  0,8
         SPACE 1
* - * - * - * - * - * - * - * - * - * - * - * - * - * - * - * - * - *
*                                                                   *
*        P A T C H     A R E A     (AT DISPOSAL AND ACCESSIBLE      *
*        ---------------------      THROUGH REGISTER 12)            *
*                                                                   *
PATCH    DC    S(*)                FIRST                            *
         DC    98S(*)                                               *
         DC    S(*)                LAST                             *
*                                                                   *
* - * - * - * - * - * - * - * - * - * - * - * - * - * - * - * - * - *
         EJECT
        @COMM  M=MAIN
         SPACE 1
        @SERV  M=MAIN
         SPACE 2
         END
