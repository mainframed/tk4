AU96     TITLE '-- ROUTINE AUT96 - HARDCOPY SERVICE --'
AUT96    START 0
         SPACE 1
**********************************************************************
*        THIS SUBROUTINE DO THE HARDCOPY SERVICE.                    *
*                                   (MOINIL P.A. - 03 NOVEMBER 1989) *
**********************************************************************
*        CALL AUT96,(PARM),VL                                        *
*        FUNCTION : HARDCOPY SERVICE.                                *
*        AT ENTRY : R1  = ADDRESS OF THE HARDCOPY REQUEST LIST.      *
*                         FORMAT IS AS BELOW :                       *
*                                  DC  AL1(HARDCOPY NUMBER)          *
*                                  DC  AL4(...,...,...   ...)        *
*                                          VARIABLE NO. OF ADDRESSES *
*              OR : R1  = 0 (NO PARM) TO CLOSE AND FREE HARDCOPY.    *
*         AT EXIT : R15 = 0 - NORMAL COMPLETION                      *
*                         4 - ABNORMAL COMPLETION                    *
*                             WITH R1 = POINTER TO MESSAGE TEXT AS   *
*                                       NEEDED BY ROUTINE AUT97.     *
*                             AND EVENTUALLY WITH R0 = POINTER TO A  *
*                                       SECONDARY MESSAGE TEXT AS    *
*                                       NEEDED BY ROUTINE AUT97 (R3) *
*                                 OR NONE (R0 = ZERO).               *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
        $DEFREG
         SPACE 1
FLACC    EQU   X'80'                   ACCOUNT D.S. CHANGED
         SPACE 1
        $XENT  BASE=R10
         SPACE 1
         LTR   R5,R1                   TEST AND RETAIN
         BZ    CLSREQ                  CLOSE REQUEST
         TM    SWITCH,FLST             HARDCOPY STARTED ?
         BO    HCPYPR                  YES, STARTED
         EJECT
*------- ALLOCATE HARDCOPY FILE.
         L     R1,=V(AUT98D)
         MVC   HEAD1U(8),9(R1)
         L     R2,=V(AUT98T)
         TM    2(R2),FLACC             ACCOUNTING IS CHANGED ?
         BZ    SETHCD                  NO
         CLI   8(R1),C'A'              YES, MAY ACCEPT CHANGES
         BE    SETDST
         CLI   8(R1),C' '
         BE    SETDST
         MVC   P99SYSOC(L'P99SYSOC),8(R1)
SETDST   CLC   0(8,R1),=CL8' '
         BE    SETHCD
         CLC   0(8,R1),=CL8'LOCAL'
         BE    LOCDST
         MVC   P99DEST(L'P99DEST),0(R1)  SET REQUESTED DEST=
         B     SCNDST
SETHCD   MVC   P99SYSOC(L'P99SYSOC),3(R2)
         MVC   P99DEST(L'P99DEST),4(R2)
SCNDST   LA    R0,L'P99DEST
         LA    R1,P99DEST+L'P99DEST-1
         CLI   0(R1),C' '
         BNE   *+L'*+6
         BCTR  R1,0
         BCT   R0,*-10
         STCM  R0,B'0011',P99DESTL
         B     ALLHC
LOCDST   MVC   P99TUPL+8(4),P99TUPL+12 OVERRIDE DEST= PARM
ALLHC    LA    R1,P99RBPTR             PARM LIST FOR DYNALLOC
        DYNALLOC
         LTR   R15,R15                 CHECK COMPLETION ?
         BNZ   ALLERR                  ERROR ALLOCATION
         LA    R2,HCPYDCB              OUTPUT DCB ADDRESS
         USING IHADCB,R2
         MVC   DCBDDNAM,=CL8' '
         ICM   R15,B'0011',P99DDNL
         BCT   R15,*+L'*+6
         MVC   DCBDDNAM(*-*),P99DDN
         EX    R15,*-6                 SET DDNAME
*------- OPEN HARDCOPY FILE.
        OPEN   (HCPYDCB,OUTPUT)        OPEN HARDCOPY FILE
         TM    DCBOFLGS,DCBOFOPN       OPEN COMPLETION ?
         DROP  R2
         BZ    OPENERR                 ERROR OPEN
         OI    SWITCH,FLST             SET HARDCOPY STARTED
         ZAP   HPAGE,=PL1'+0'          RESET PAGE NUMBER
         ZAP   HLINE,=PL2'+99'         FORCE NEW PAGE
         SPACE 1
*------- PROCESS REQUEST TO HARDCOPY FILE.
HCPYPR   TM    SWITCH,FLDS             ACCOUNT DS INFO DONE ?
         BO    GOMSG                   YES
         BAL   R9,CHKPG                CHECK PAGE / FORCE NEW PAGE
         DC    PL2'+0'
         MVC   HCLINE(30),=CL30'0Accounting data-set name is :'
         L     R1,=V(AUT98A)
         MVC   HCLINE+31(44),0(R1)     MOVE DSNAME
         BAL   R9,PUTRTN
         OI    SWITCH,FLDS             SET ACCOUNT DS INFO DONE
GOMSG    ICM   R1,B'1111',AMSGT        ANY MESSAGE PENDING ?
         BZ    GOSEL                   NO
         BCTR  R1,0
         XR    R2,R2
         IC    R2,0(R1)
         BCT   R2,*+L'*+6
         MVC   HCLINE(*-*),1(R1)
         EX    R2,*-6
         XC    AMSGT,AMSGT             SAY MESSAGE PROCESSED
         BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+56'
         BAL   R9,PUTRTN               OUTPUT MESSAGE
GOSEL    XR    R1,R1
         IC    R1,0(R5)                GET HARDCOPY NUMBER
         CLI   0(R5),MAXRQ
         BH    INVSEL
         SLL   R1,2
         B     SELRQ(R1)               GO TO PROCESS
SELRQ    B     STMSG                    0
         B     AUTCR                    1
         B     PRGCR                    2
         B     AUTUP                    3
         B     PRGUP                    4
         B     AUTDL                    5
         B     PRGDL                    6
MAXRQ    EQU   ((*-SELRQ)-L'SELRQ)/L'SELRQ
INVSEL   BAL   R9,DECODE
         MVC   MINVSLN(L'MINVSLN),WCL6+L'WCL6-L'MINVSLN
         LA    R1,MINVSL
         B     EXERR
*------- 0 = SET MESSAGE TEXT.
STMSG    ICM   R1,B'1111',1(R5)
         ST    R1,AMSGT
         B     EXOK                    EXIT
*------- 1 = CREATE AUTHORIZATION.
AUTCR    BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+48'
         MVC   HCLINE(23),=CL23'0CREATE Authorization :'
         ICM   R2,B'1111',1(R5)
         LH    R1,0(R2)
         BAL   R9,DECODE
         MVC   HCLINE+24(4),WCL6+L'WCL6-4
         BAL   R9,SETTME
         BAL   R9,PUTRTN
         ICM   R2,B'1111',5(R5)
         BAL   R8,AUTRR
         B     EXOK                    EXIT
*------- 2 = CREATE PROGRAMMER.
PRGCR    BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+52'
         MVC   HCLINE(20),=CL20'0CREATE Programmer :'
         ICM   R2,B'1111',1(R5)
         LH    R1,0(R2)
         BAL   R9,DECODE
         MVC   HCLINE+21(4),WCL6+L'WCL6-4
         BAL   R9,SETTME
         BAL   R9,PUTRTN
         ICM   R2,B'1111',5(R5)
         BAL   R8,PRGRR
         B     EXOK                    EXIT
*------- 3 = UPDATE AUTHORIZATION.
AUTUP    BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+47'
         MVC   HCLINE(23),=CL23'0UPDATE Authorization :'
         ICM   R2,B'1111',1(R5)
         LH    R1,0(R2)
         BAL   R9,DECODE
         MVC   HCLINE+24(4),WCL6+L'WCL6-4
         BAL   R9,SETTME
         BAL   R9,PUTRTN
         MVC   HCLINE+6(14),=CL14'*** BEFORE ***'
         ICM   R2,B'1111',5(R5)
         BAL   R8,AUTRR
         BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+49'
         MVC   HCLINE+6(14),=CL14'*** AFTER ***'
         ICM   R2,B'1111',9(R5)
         BAL   R8,AUTRR
         B     EXOK                    EXIT
*------- 4 = UPDATE PROGRAMMER.
PRGUP    BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+51'
         MVC   HCLINE(20),=CL20'0UPDATE Programmer :'
         ICM   R2,B'1111',1(R5)
         LH    R1,0(R2)
         BAL   R9,DECODE
         MVC   HCLINE+21(4),WCL6+L'WCL6-4
         BAL   R9,SETTME
         BAL   R9,PUTRTN
         MVC   HCLINE+6(14),=CL14'*** BEFORE ***'
         ICM   R2,B'1111',5(R5)
         BAL   R8,PRGRR
         BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+53'
         MVC   HCLINE+6(14),=CL14'*** AFTER ***'
         ICM   R2,B'1111',9(R5)
         BAL   R8,PRGRR
         B     EXOK                    EXIT
*------- 5 = DELETE AUTHORIZATION.
AUTDL    BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+48'
         MVC   HCLINE(23),=CL23'0DELETE Authorization :'
         ICM   R2,B'1111',1(R5)
         LH    R1,0(R2)
         BAL   R9,DECODE
         MVC   HCLINE+24(4),WCL6+L'WCL6-4
         BAL   R9,SETTME
         BAL   R9,PUTRTN
         MVC   HCLINE+6(14),=CL14'*** BEFORE ***'
         ICM   R2,B'1111',5(R5)
         BAL   R8,AUTRR
         B     EXOK                    EXIT
*------- 6 = DELETE PROGRAMMER.
PRGDL    BAL   R9,CHKPG                CHECK PAGE
         DC    PL2'+51'
         MVC   HCLINE(20),=CL20'0DELETE Programmer :'
         ICM   R2,B'1111',1(R5)
         LH    R1,0(R2)
         BAL   R9,DECODE
         MVC   HCLINE+21(4),WCL6+L'WCL6-4
         BAL   R9,SETTME
         BAL   R9,PUTRTN
         MVC   HCLINE+6(14),=CL14'*** BEFORE ***'
         ICM   R2,B'1111',5(R5)
         BAL   R8,PRGRR
         B     EXOK                    EXIT
*------- LOG AN AUT. RECORD ROUTINE (LINK = R8)
         USING AUTDSECT,R2
AUTRR    MVI   HCLINE,C'0'
         CLC   HCLINE+6(8),=CL8' '
         BE    *+L'*+4
         BAL   R9,PUTRTN
         MVC   HCLINE+6(9),=CL9'Aut.No. ='
         LH    R1,AUTANO
         BAL   R9,DECODE
         MVC   HCLINE+16(1),WCL6
         MVC   HCLINE+17(4),WCL6+L'WCL6-4
         MVC   HCLINE+25(17),=CL17'Fiche Programme :'
         MVC   HCLINE+43(L'AUTACNO),AUTACNO
         MVC   HCLINE+60(6),=CL6'Prio ='
         MVC   HCLINE+67(L'AUTPI),AUTPI
         BAL   R9,PUTRTN
         MVC   HCLINE+6(15),=CL15'Date de d!but :'
         XR    R1,R1
         ICM   R1,B'0111',AUTVSD
         BAL   R9,SETDTE
         MVC   HCLINE+22(10),WCL10
         MVC   HCLINE+34(13),=CL13'Date de fin :'
         XR    R1,R1
         ICM   R1,B'0111',AUTVED
         BAL   R9,SETDTE
         MVC   HCLINE+48(10),WCL10
         MVC   HCLINE+60(14),=CL14'Resp. Progr. :'
         LH    R1,AUTPMNO
         BAL   R9,DECODE
         MVC   HCLINE+75(1),WCL6
         MVC   HCLINE+76(4),WCL6+L'WCL6-4
         BAL   R9,PUTRTN
         MVC   HCLINE+6(13),=CL13'Progr''s No. :'
         LA    R3,AUTPNO
         LA    R4,AUTNPNF
AUTRR1   LA    R6,HCLINE+25
         LA    R7,5
AUTRR2   LH    R1,0(R3)
         BAL   R9,DECODE
         MVC   0(1,R6),WCL6
         MVC   1(4,R6),WCL6+L'WCL6-4
         LA    R3,L'AUTPNO(R3)
         BCT   R4,*+L'*+4
         B     AUTRR3
         LA    R6,10(R6)
         BCT   R7,AUTRR2
         BAL   R9,PUTRTN
         B     AUTRR1
AUTRR3   CLC   HCLINE+25(5),=CL8' '
         BER   R8
         BAL   R9,PUTRTN
         BR    R8
         DROP  R2
*------- LOG AN PRG. RECORD ROUTINE (LINK = R8)
         USING PRGDSECT,R2
PRGRR    MVI   HCLINE,C'0'
         CLC   HCLINE+6(8),=CL8' '
         BE    *+L'*+4
         BAL   R9,PUTRTN
         MVC   HCLINE+6(9),=CL9'Prg.No. ='
         LH    R1,PRGPNO
         BAL   R9,DECODE
         MVC   HCLINE+16(1),WCL6
         MVC   HCLINE+17(4),WCL6+L'WCL6-4
         MVC   HCLINE+25(11),=CL11'Matricule :'
         MVC   HCLINE+37(L'PRGMNO),PRGMNO
         MVC   HCLINE+50(5),=CL5'Box ='
         MVC   HCLINE+56(L'PRGBXNO),PRGBXNO
         BAL   R9,PUTRTN
         MVC   HCLINE+6(17),=CL17'Programmer Name :'
         MVC   HCLINE+24(L'PRGPNME),PRGPNME
         BAL   R9,PUTRTN
         MVC   HCLINE+6(10),=CL10'Building :'
         MVC   HCLINE+17(L'PRGBDNO),PRGBDNO
         MVC   HCLINE+30(11),=CL11'Phone No. :'
         MVC   HCLINE+42(L'PRGPHNO),PRGPHNO
         MVC   HCLINE+60(9),=CL9'Service :'
         MVC   HCLINE+70(L'PRGSVNO),PRGSVNO
         BAL   R9,PUTRTN
         BR    R8
         DROP  R2
*------- CHECK PAGE HEADING ROUTINE (LINK = R9)
CHKPG    CP    HLINE,0(2,R9)           EJECT NEEDED ?
         BNH   2(R9)                   NO
         ZAP   HLINE,=PL1'+0'          YES, RESET LINES/PAGE COUNT
         AP    HPAGE,=PL1'+1'          BUMP PAGE COUNT
         MVC   HEAD1P,=XL6'402020202120'
         ED    HEAD1P,HPAGE
        PUT    HCPYDCB,HEAD1           PUT OUT TITLE LINE
         TM    SWITCH,FLPE             PUT ERROR ?
         BO    CLSREQ                  YES
        PUT    HCPYDCB,HEAD2           PUT OUT UNDERLINE
         TM    SWITCH,FLPE             PUT ERROR ?
         BO    CLSREQ                  YES
         B     2(R9)                   NO
*------- SET CURRENT TIME ROUTINE (LINK = R9)
SETTME   MVC   HCLINE+35(6),=CL6'Date :'
         MVC   HCLINE+65(6),=CL6'Time :'
        $GFDATE HCLINE+42,WCL6
         LTR   R15,R15
         BNZ   SETTMEM
         MVC   HCLINE+72(2),WCL6       HHMMSS RECEIVED
         MVI   HCLINE+74,C'.'
         MVC   HCLINE+75(2),WCL6+2
         MVI   HCLINE+77,C'.'
         MVC   HCLINE+78(2),WCL6+4
         BR    R9
SETTMEM  MVC   HCLINE+72(4),=CL4'RC ='
         CVD   R15,DSD
         MVC   WCL6,=XL6'402120202020'
         ED    WCL6,DSD+5
         MVC   HCLINE+77(2),WCL6+L'WCL6-2
         BR    R9
*------- DECODE NUMBER ROUTINE (LINK = R9)
DECODE   LTR   R0,R1
         BZ    DECODEM
         BNM   *+L'*+2
         LPR   R0,R0
         CVD   R0,DSD
         MVC   WCL6,=XL6'402120202020'
         ED    WCL6,DSD+5
         MVI   WCL6,C'+'
         LTR   R1,R1
         BNMR  R9
         MVI   WCL6,C'-'
         BR    R9
DECODEM  MVC   WCL6,=CL6'  None'
         BR    R9
*------- EDIT DATE ROUTINE (LINK = R9)
SETDTE   CL    R1,=F'99999'            WHICH FORMAT?
         BH    *+L'*+4                 NEW FORMAT
         AL    R1,=F'1900000'          OLD FORMAT, ADJUST IT
         CVD   R1,DSD
         UNPK  WCL7,DSD+4(4)
         OI    WCL7+L'WCL7-1,C'0'
        $GFDATE ,,WCL8,WCL7
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   SETDTEM
         MVC   WCL10(2),WCL8+2         DAY
         MVI   WCL10+2,C'/'
         MVC   WCL10+3(2),WCL8         MONTH
         MVI   WCL10+5,C'/'
         MVC   WCL10+6(4),WCL8+4       YEAR
         BR    R9
SETDTEM  CVD   R15,DSD
         MVC   WCL10(L'WCL7),WCL7      YYYYDDD
         UNPK  WCL6(5),DSD+5(3)
         OI    WCL6+4,C'0'
         MVI   WCL10+L'WCL7,C'-'
         MVC   WCL10+L'WCL7+1(2),WCL6+3     RC
         BR    R9
*------- HARDCOPY PUT ROUTINE (LINK = R9)
PUTRTN  PUT    HCPYDCB,HCLINE          PUT OUT LINE
         TM    SWITCH,FLPE             PUT ERROR ?
         BO    CLSREQ                  YES
         AP    HLINE,=PL1'+1'          NO, UPDATE LINES/PAGE COUNT
         CLI   HCLINE,C'0'             WAS DOUBLE SPACE ?
         BNE   *+L'*+6                 NO
         AP    HLINE,=PL1'+1'          YES, UPDATE LINES/PAGE COUNT
         MVI   HCLINE,C' '             RESET LINE
         MVC   HCLINE+1(L'HCLINE-1),HCLINE
         BR    R9
         SPACE 1
*------- CLOSE HARDCOPY FILE.
CLSREQ   TM    SWITCH,FLST             IS HARDCOPY STARTED ?
         BZ    EXOK                    NO, NONE TO DO
        CLOSE  (HCPYDCB)               YES, CLOSE THE FILE
        FREEPOOL HCPYDCB               FREE THE BUFFERS TOO
         TM    SWITCH,FLPE             CLOSE DUE TO PUT ERROR ?
         BO    PUTERRX                 YES
         MVI   SWITCH,0                RESET HARDCOPY FLAGS
EXOK     XR    R15,R15                 SET NORMAL RETURN CODE
LEAVE   $XRET  CC=(R15)                RETURN
*------- FORMULATE TEXT FOR SVC99 ALLOCATE FAILURE.
ALLERR   CLC   P99ERROR,=XL2'046C'     WAS IT 'RMT NOT DEF TO JES2' ?
         BE    BADRMT                  YES
         CVD   R15,DSD                 NO, CONVERT SVC 99 RC
         MVC   MALLRCC,=XL4'40202120'
         ED    MALLRCC,DSD+6
         UNPK  MALLERC(L'MALLERC+1),P99ERROR(L'P99ERROR+1)
         TR    MALLERC(L'MALLERC),HEXTAB
         MVI   MALLERC+L'MALLERC,C' '
         UNPK  MALLINC(L'MALLINC+1),P99INFO(L'P99INFO+1)
         TR    MALLINC(L'MALLINC),HEXTAB
         MVI   MALLINC+L'MALLINC,C' '
         LA    R1,MALLERR
         B     EXERR
BADRMT   MVC   MBDRMTN,P99DEST         REMOTE NAME
         LA    R1,MBDRMT
         B     EXERR
*------- SET DIAGNOSE MESSAGE(S).
OPENERR  LA    R1,MOPERR
EXERR    XR    R0,R0                   SAY NO 2ND MESSAGE
         L     R15,4(R13)
         STM   R0,R1,20(R15)
         LA    R15,4                   SET ERROR RETURN CODE
         B     LEAVE
PUTERRX  LA    R1,MPUTRR
         XR    R0,R0
         CLC   MPUTERR+1(8),=CL8' '
         BE    EXERR+L'EXERR
         LA    R0,MPUTERR
         B     EXERR+L'EXERR
         DROP  R10
         EJECT
*------- HARDCOPY WRITE (PUT) ERROR ROUTINE.
        CNOP   0,8
         USING *,R15
PUTERR   ST    R10,SVREG
         LR    R10,R15
         DROP  R15
         USING PUTERR,R10
        SYNADAF ACSMETH=BSAM
         CLI   69(R1),C'*'             TEST UNSIGNIFICANT MSG
         BE    PUTERR3                 YES
         CLI   8(R1),C' '              TEST IF BINARY INFORMATION
         BE    PUTERR1                 NO
         UNPK  56(7,R1),9(4,R1)
         TR    56(6,R1),HEXTAB
         MVI   62(R1),C' '
         UNPK  63(5,R1),12(3,R1)
         TR    63(4,R1),HEXTAB
         MVI   67(R1),C' '
         B     PUTERR2
PUTERR1  MVC   56(12,R1),8(R1)         MOVE BLANKS
PUTERR2  MVC   MPUTERR+1(L'MPUTERR-1),56(R1)  MOVE MESSAGE
PUTERR3  OI    SWITCH,FLPE             SAY PUT ERROR
        SYNADRLS
         L     R10,SVREG
         BR    R14                     RETURN
         DROP  R10
         EJECT
*--------------------------------------------------------------------*
*        DYNAMIC ALLOCATION PARAMETER LIST.                          *
*--------------------------------------------------------------------*
         SPACE 1
P99RBPTR DC    A(P99RB+X'80000000')    SVC 99 REQUEST BLOCK PTR
P99RB    DS    0F                      SVC 99 REQUEST BLOCK
         DC    AL1(P99RBLN)                 RB LENGTH IN BYTES
         DC    AL1(S99VRBAL)                VERB CODE (DSNAME ALL.)
         DC    AL1(S99NOCNV+S99NOMNT,0)     FLAGS 1
P99ERROR DC    XL2'0'                       ERROR CODE
P99INFO  DC    XL2'0'                       INFO CODE
         DC    A(P99TUPL)                   POINTER TO TEXT UNIT PTRS
         DC    XL4'0'                       RESERVED
         DC    XL4'0'                       FLAGS 2
P99RBLN  EQU   *-P99RB                      RB LENGTH
P99TUPL  DS    0F                      TEXT UNIT POINTERS
         DC    A(P99TUKY1)                  SYSOUT
         DC    A(P99TUKY2)                  FREE AT CLOSE
         DC    A(P99TUKY3)                  DEST=
         DC    A(P99TUKY4+X'80000000')      DDNAME RETURN
         SPACE 1
P99TUKY1 DC    AL2(DALSYSOU),AL2(1),AL2(1)  SYSOUT=
P99SYSOC DC    CL1' '                       CLASS
P99TUKY2 DC    AL2(DALCLOSE),AL2(0)         UNALLOC AT CLOSE
P99TUKY3 DC    AL2(DALSUSER),AL2(1)         DEST=
P99DESTL DC    AL2(*-*)                     LENGTH OF DESTINATION
P99DEST  DC    CL8' '                       DESTINATION
P99TUKY4 DC    AL2(DALRTDDN),AL2(1)         DDNAME RETURN
P99DDNL  DC    AL2(8)                       LENGTH OF DDNAME
P99DDN   DC    CL8' '                       DDNAME
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
DSD      DC    D'0'
SVREG    DC    F'0'
AMSGT    DC    F'0'
WCL10    DC    CL10' '
WCL8     DC    CL8' '
WCL7     DC    CL7' '
WCL6     DC    CL6' '
HPAGE    DC    PL3'+0'                 HARDCOPY PAGE NUMBER
HLINE    DC    PL2'+0'                 HARDCOPY LINES/PAGE NUMBER
SWITCH   DC    XL1'0'                  HARDCOPY PROCESSING SWITCHES
FLST     EQU   X'80'                   HARDCOPY STARTED
FLPE     EQU   X'40'                   HARDCOPY PUT ERROR
FLDS     EQU   X'20'                   HARDCOPY ACCOUNT DS INFO DONE
         SPACE 1
HEAD1    DC    CL39'1Authorizations / Programmers Hardcopy '
         DC    CL20'- Current USER is : '
HEAD1U   DC    CL8' ',CL7' - Page'
HEAD1P   DC    CL6' ',CL1'.'
HEAD1LN  EQU   (*-HEAD1)-81            MUST BE ZERO
HEAD2    DC    CL1' ',80C'-'
HCLINE   DC    CL81' '
         SPACE 1
HEXTAB   EQU   *-X'F0'
         DC    C'0123456789ABCDEF'     HEX TABLE TRANSLATION
         EJECT
*--------------------------------------------------------------------*
*        MESSAGES D'ERREURS.                                         *
*--------------------------------------------------------------------*
         SPACE 1
         DC    AL1(MALLNG)
MALLERR  DC    C' Hardcopy All. Err. : DARC='
MALLERC  DC    CL4' ',C' INFO='
MALLINC  DC    CL4' ',C' and R15 ='
MALLRCC  DC    CL4' ',C' - AUT96'
MALLNG   EQU   *-MALLERR
         DC    AL1(MBDRMTL)
MBDRMT   DC    C' Hardcopy Err. : Remote '
MBDRMTN  DC    CL8' ',C' undefined to JES2 - AUT96'
MBDRMTL  EQU   *-MBDRMT
         DC    AL1(L'MOPERR)
MOPERR   DC    C' Hardcopy OPEN has failed - AUT96'
         DC    AL1(MINVSLL)
MINVSL   DC    C' Hardcopy invalid selection number : '
MINVSLN  DC    CL3' ',C' - AUT96'
MINVSLL  EQU   *-MINVSL
         DC    AL1(L'MPUTRR)
MPUTRR   DC    C' Hardcopy PUT error detected - AUT96'
         DC    AL1(L'MPUTERR)
MPUTERR  DC    CL73' '
         SPACE 1
HCPYDCB DCB    DDNAME=XXXXXXXX,DSORG=PS,DEVD=DA,BFALN=D,MACRF=PM,      1
               SYNAD=PUTERR,RECFM=FA,LRECL=81,BLKSIZE=81
         SPACE 1
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         EJECT
*--------------------------------------------------------------------*
*        DUMMY SECTIONS                                              *
*--------------------------------------------------------------------*
         SPACE 1
        DCBD   DSORG=PS,DEVD=DA
        IEFZB4D0
        IEFZB4D2
         PRINT GEN
        $AIRD  SEC=YES,R=NO
         SPACE 2
         END
