CX01     TITLE 'RACF - RACHECK PRE-PROCESSING EXIT (ICHRCX01).'
ICHRCX01 START 0
         SPACE 1
* PURPOSE :    ESTABLISH FAST PATH AUTHORIZATION CHECKING FOR OWNERS
* ---------    OF DATA-SETS.
*
* METHOD :     IF (CLASS=DATASET) THEN
* --------          IF (ACCESS NE ALTER) THEN
*                        IF (FLQ = USERID) THEN
*                             BYPASS PROFILE CHECK;
*                        END;
*                   END;
*              END;
*
* AT ENTRY TO THE RACHECK PRE-PROCESSING ROUTINE, THE REGISTER 1
* --------     CONTAINS THE ADDRESS OF THE PARAMETER LIST AREA.
*
* AT EXIT FROM THE RACHECK PRE-PROCESSING ROUTINE, THE REGISTER 15
* -------      SHOULD CONTAIN A RETURN CODE FOR PROCESSING
*              CONTINUATION AS BELOW :
*               0 - NORMAL PROCESSING IS TO CONTINUE.
*               4 - REQUEST IS NOT ACCEPTED AND IS TO BE FAILED.
*               8 - REQUEST IS ACCEPTED AND NO MORE PROCESSING IS
*                   PERFORMED.
*              12 - EXIT ROUTINE PROCESSING IS COMPLETE, AND THE
*                   REQUEST IS TO BE GRANTED.
*
* AUTHOR :     RAVARANI N.
* -------      COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
        $DEFREG
ICHRCX01 AMODE ANY
ICHRCX01 RMODE ANY
         EJECT
        $XENT  BASE=R12,TYPE=RENT
         USING RCXPL,R1            ADDRESS PARAMETER LIST
         L     R2,RCXCLASS         GET CLASS ADDRESS
         CLI   0(R2),7             IS IT DATA-SET CLASS LENGTH?
         BNE   LETSGO              NO
         CLC   1(7,R2),=CL7'DATASET'    IS IT DATA-SET CLASS?
         BNE   LETSGO              NO
         L     R2,RCXFLAG2         GET FLAG BYTE 2 ADDRESS
         TM    0(R2),RCXATTAL      ALTER REQUEST?
         BO    LETSGO              LETS RACF DECIDE ABOUT
         L     R2,RCXACEE          ADDRESS OF ACEE ADDRESS
         L     R2,0(R2)            ACEE ADDRESS
         LTR   R2,R2               TEST IF ZERO
         BNZ   ACEEFND             NO, USE THIS ONE
         USING PSA,R0              WE HAVE TO GET IT FROM ASXB
         L     R3,PSAAOLD          GET OUR ASCB ADDRESS
         DROP  R0
         LTR   R3,R3               VERIFY ADDRESS
         BZ    LETSGO              SOMETHING WRONG
         USING ASCB,R3             ESTABLISH ADDRESSABILITY
         CLC   ASCBASCB,=CL4'ASCB'      VERIFY CONTROL BLOCK
         BNE   LETSGO              WHAT'S HAPPENING?
         L     R3,ASCBASXB         GET ASXB ADDRESS
         LTR   R3,R3               VERIFY ADDRESS
         BZ    LETSGO              SOMETHING WRONG
         DROP  R3
         USING ASXB,R3             ESTABLISH ADDRESSABILITY
         CLC   ASXBASXB,=CL4'ASXB'      VERIFY CONTROL BLOCK
         BNE   LETSGO              WHAT'S HAPPENING?
         L     R2,ASXBSENV         GET ACEE ADDRESS
         LTR   R2,R2               VERIFY ADDRESS
         BZ    LETSGO              SOMETHING WRONG
         DROP  R3
         USING ACEE,R2             ESTABLISH ADDRESSABILITY
ACEEFND  L     R3,RCXENORP         GET DATA-SET NAME ADDRESS
         XR    R4,R4               CLEAR REGISTER
         IC    R4,ACEEUSRL         LENGTH OF USERID
         LR    R5,R4
         AR    R5,R3               THIS SHOULD POINT BEHIND FLQ (.)
         CLI   0(R5),C'.'          IS THIS A DOT?
         BNE   LETSGO              NO, DO NORMAL CHECK
         BCTR  R4,0                -1 FOR COMPARE
         EX    R4,COMPUSID         IS USERID EQUAL?
         BNE   LETSGO              NO
         LA    R15,8 ------------- SET NO MORE CHECKS TO BE DONE ------
         B     GOBACK
COMPUSID CLC   0(*-*,R3),ACEEUSRI  COMPARE USERID WITH DSNAME
         DROP  R1,R2
LETSGO   XR    R15,R15 ----------- SET NORMAL PROCESSING TO CONTINUE --
GOBACK  $XRET  CC=(R15),TYPE=RENT
         EJECT
        LTORG
         SPACE 2
        PRINT  NOGEN
        ICHRCXP
        IHAPSA
        IHAASCB
        IHAASXB
        IHAACEE
         SPACE 2
         END
