TSER     TITLE 'EURATOM - TSO SUBMIT EXIT ROUTINE.'
IKJEFF10 START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* *                                                                 * *
* *                      E U R A T O M                              * *
* *                                                                 * *
* *        T S O    S U B M I T    E X I T    R O U T I N E.        * *
* *                                                                 * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
* PURPOSE :    1. WHEN THE JOB STATEMENT IS SUPPLIED, IT INSERTS A
* ---------       CONTINUATION CARD CONTAINING THE LOGON PASSWORD
*                 FOR A RACF DEFINED USER IF THE FOLLOWING CONDITIONS
*                 ARE VERIFIED :
*                 - THE USER=... KEYWORD IS CODED AND REFERS THE
*                   USER HIMSELF,
*                 - THE PASSWORD=... KEYWORD IS NOT CODED.
*              2. WHEN THE JOB STATEMENT IS GENERATED, IT ALLOWS THE
*                 USER TO CHANGE OR ACCEPT SOME PREDEFINED DEFAULTS,
*                 THEN THESE REPLACES THE LAST GENERATED CARD.
*
* NOTE - FOR MORE INFORMATION ABOUT THE SUBMIT EXIT ROUTINE, SEE
*        IBM MANUAL OS/VS2 SYSTEM PROGRAMMING LIBRARY : TSO.
*
* AUTHOR :     RAVARANI N. - MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
        $DEFREG
         EJECT
         USING *,R15
        SAVE   (14,12),,IKJEFF10-&SYSDATE-&SYSTIME
         LR    R12,R15             SET BASE REGISTER
         DROP  R15
         USING IKJEFF10,R12
         SPACE 1
*        SAVE AREAS ARE NOT CHAINED UNTIL AFTER THE GETMAIN.
*        REGISTER 1 POINTS TO POINTER TO PARAMETER LIST.
         SPACE 1
         USING IEDSECTD,R1
         L     R8,IEEXITL          GET ADDRESS OF PARAMETER LIST
         DROP  R1
         USING IEDSECTD,R8
         L     R9,IESUBCTP         GET ADDRESS OF SWITCH BYTES
         USING IESUBCTD,R9         ESTABLISH ADDRESSABILITY
         TM    IESTMTYP,IESJOB     IS IT JOB STATEMENT?
         BZ    EXIT00              NO, OTHER, EXIT
         CLC   IEEXITWD,=F'0'      WORK-AREA ALREADY OBTAINED?
         BE    GETM                NO
         L     R10,IEEXITWD        YES, GET WORK-AREA ADDRESS
         B     SVLNK
GETM     L     R0,=A(WORKL)        OBTAIN A WORK-AREA
        GETMAIN R,LV=(0)
         ST    R1,IEEXITWD         WORK-AREA POINTER
         LR    R10,R1
         USING WORK,R10            SET-WORK AREA ADDRESSABILITY
         SPACE 1
* NOTE - HEREAFTER, IF DESIRED, A JOB STATEMENT PROCESS MAY BE
*        BE DISABLED BY SETTING "ON" THE CORRESPONDING FLAG IN
*        THE "RUNSW" SWITCHES BYTE AS BELOW :
*          SWPSWD - TO SUPPRESS PASSWORD SUPPLY PROCESS.
*          SWGJSD - TO SUPPRESS DEFAULTS CONTINUATION CARD PROCESS.
         SPACE 1
         MVI   RUNSW,0             ENABLE JOB STAT. PROCESSES
         SPACE 1
         XC    RUNIX,RUNIX
         XC    RUNJC,RUNJC
         L     R1,CVTPTR           CVT ADDRESS
         USING CVT,R1
         L     R2,CVTTCBP
         DROP  R1
         L     R1,12(R2)           ASCB ADDRESS
         USING ASCB,R1
         L     R2,ASCBASXB         ASXB ADDRESS
         USING ASXB,R2
         ICM   R3,B'1111',ASXBSENV ACEE ADDRESS
         DROP  R2
         BZ    NOSRCH              NOT A RACF USER
         USING ACEE,R3
         TM    ACEEFLG1,ACEERACF   IS IT A RACF DEFINED USER?
         BZ    NOSRCH              NO
         DROP  R3
         ICM   R2,B'1111',ASCBTSB  TSB ADDRESS
         BZ    NOSRCH              NO TSB
         DROP  R1
         USING TSB,R2
        MODESET KEY=ZERO
         MVC   USPSWD,TSBPSWD      GET PASSWORD FROM TSB
        MODESET KEY=NZERO
         DROP  R2
         OC    USPSWD,USPSWD       IS THERE A LOGON PASSWORD?
         BZ    NOSRCH              NO
         CLC   USPSWD,=CL8' '      LOGON PASSWORD TSO/E PROPAGATION?
         BNE   SVLNK               NO
NOSRCH   OI    RUNSW,SWSPWD        SET DE-ACTIVATED
SVLNK    LA    R1,SVA              BACKWARD-FORWARD SAVE AREAS
         XC    0(18*4,R1),0(R1)
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1              SET NEW SAVE AREA POINTER
         SPACE 1
         ICM   R3,B'1111',IECARDP  GET ADDRESS OF CARD
         BNZ   TSPW                ALL OK
         TM    RUNSW,SWINSC        NO, IS CARD INSERTED?
         BO    INSDN               YES
QUIT     OI    RUNSW,SWGJSD+SWSPWD NONE, SET ALL DE-ACTIVATED
         B     TFRWA               EVERYTHING FREEZED
         EJECT
*        -------------------------------
*        LOOK FOR SUPLIED JOB STATEMENT.
*        -------------------------------
*
*        THIS PART INSERTS A CONTINUATION CARD OF EACH JOB CARD
*        SUBMITTED BY A RACF DEFINED USER. THE CONTINUATION CARD
*        WILL CONTAIN THE LOGON PASSWORD OF THE PERSON SUBMITTING
*        THE JOB. IF THE USER IS NOT RACF DEFINED, OR THE USER=...
*        KEYWORD IS NOT HIMSELF, OR THE PASSWORD=... KEYWORD IS
*        CODED, THE JOB IS PASSED AS IT IS, SO YOU CANNOT GET
*        SOMEONE ELSES PASSWORD.
*
*        NOTE - THE JOB STATEMENT MUST BE MORE THAN ONE CARD CODED,
*               OTHERWISE THE CONTINUATION CARD INSERTION IS NOT
*               ACTIVATED (JOB IS PASSED AS IT IS).
         SPACE 1
TSPW     TM    RUNSW,SWSPWD        SUPPLY PASSWORD DE-ACTIVATED?
         BO    TGJS                YES
         TM    RUNSW,SWFJSC        NO, IS IT FIRST CARD?
         BO    OPRAND              NO, CONTINUE
         TM    IESTMTYP,IESSCON    YES, IS IT ALSO LAST CARD?
         BZ    QUIT                YES, PASS AS IT IS
         OI    RUNSW,SWFJSC        SET FIRST CARD PROCESSED
OPRAND   CLI   IEOPRAND,0          OPERAND COLUMN?
         BE    COPRAND             NO OPERAND IN CARD
         SR    R2,R2
         IC    R2,IEOPRAND         OFFSET OF OPERAND FIELD
         LA    R4,0(R3,R2)         POINT TO OPERAND
         BCTR  R4,0
         LH    R5,=H'71'           IN LOOP, LOOK AT 71 COLUMNS
         SR    R5,R2               LESS FIRST PART OF CARD
         NI    RUNSW,255-SWSSTQ    CLEAR FOR QUOTES SEARCH
SQUOTE   CLI   0(R4),C''''         IS IT A QUOTE MARK?
         BNE   CQUOTE              NO
         TM    RUNSW,SWSSTQ        IS IT THE BEGINNING QUOTE?
         BZ    BQUOTE              YES
         NI    RUNSW,255-SWSSTQ    NO, END OF QUOTE, RESET
         B     NXCOL
BQUOTE   OI    RUNSW,SWSSTQ        SET START OF QUOTE
         B     NXCOL
CQUOTE   TM    RUNSW,SWSSTQ        ARE WE IN A QUOTATION?
         BO    NXCOL               YES, DON'T LOOK FOR KEYWORDS
         CLI   0(R4),C' '          END OF OPERANDS?
         BE    COPRAND             YES
         CLC   0(9,R4),=CL9'PASSWORD='  IS IT PASSWORD=?
         BE    SPOFF               YES
         CLC   0(5,R4),=CL5'USER=' NO, IS IT USER=?
         BNE   NXCOL               NO
         LA    R4,5(R4)
         SH    R5,=H'5'
         BNP   SPOFF
         LR    R2,R4               EXTRACT USER-ID
JUSL     CLI   0(R4),C' '
         BE    JUSF
         CLI   0(R4),C','
         BE    JUSF
         LA    R4,1(R4)
         BCT   R5,JUSL
JUSF     LR    R1,R4
         SR    R1,R2
         BNP   SPOFF
         BCTR  R1,0
         MVC   JBUSR,=CL8' '
         EX    R1,MVUS
         L     R2,IEUSRIDP         POINTER TO USER-ID FIELD
         CLC   JBUSR(L'JBUSR),0(R2)
         BNE   SPOFF
         OI    RUNSW,SWAPWC        SET ADD PASSWORD CARD
         LTR   R5,R5
         BNP   COPRAND
         CLI   0(R4),C' '
         BE    COPRAND
NXCOL    LA    R4,1(R4)            NEXT COLUMN
         BCT   R5,SQUOTE           LOOP IF NOT AT END OF CARD
COPRAND  TM    IESTMTYP,IESOPCON   IS OPERAND TO BE CONTINUED?
         BO    TGJS                YES
         TM    IESTMTYP,IESSCON    NO, IS IT LAST CARD?
         BO    TGJS                NO
         TM    RUNSW,SWAPWC        ADD PASSWORD CARD REQUESTED?
         BZ    TGJS                NO
         ST    R3,SVCARDP          YES
         LA    R3,CRD
         ST    R3,IECARDP          POINTER TO INSERTED CARD
         MVC   CRDS(3),=CL3'// '
         MVC   CRDS+3(L'CRDS-3),CRDS+2
         MVC   CRDKW(L'CRDKW),=CL9'PASSWORD='
         MVI   USPSWD+L'USPSWD,C' '
         MVC   USPSWD+L'USPSWD+1(L'CRDEND+7),USPSWD+L'USPSWD
         MVC   CRDEND(L'PSADD),PSADD    SET '** SUPPLIED BY SUBMIT **'
         LA    R1,USPSWD+L'USPSWD-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   1(R1),C','          SET COMMA AFTER PASSWORD
         MVI   IEOPRAND,16         SET OPERAND OFFSET
         MVC   SVTYP,IESTMTYP      SAVE TYPE SWITCHES
         MVI   IESTMTYP,IESJOB+IESSCON+IESCONTN   SET TYPE
         OI    RUNSW,SWINSC        SET INSERTION DOING
         B     EXIT04              GO INSERT CARD
MVUS     MVC   JBUSR(*-*),0(R2)    <<EXECUTED>>
INSDN    MVC   IECARDP,SVCARDP     RESTORE AND PROCESS LAST CARD
         MVC   IEOPRAND,SVOPOFF
         MVC   IESTMTYP,SVTYP
         B     TFRWA
SPOFF    OI    RUNSW,SWSPWD        SET DE-ACTIVATED
         EJECT
*        ----------------------------------------
*        LOOK FOR SUBMIT GENERATED JOB STATEMENT.
*        ----------------------------------------
*
*        THIS PART IS BASED ON THE FOLLOWING SEQUENCE (GENERATED BY
*        SUBMIT) OF CARDS SKELETON :
*         (1)  //JOBNAME   JOB AAAANNNN,     <--- PRIOR MVS/XA 2.1.7
*         (2)  //             USER-NAME,     **JOB STATEMENT ...
*         (3)  //             NOTIFY=USER-ID,USER=USER-ID,
*         (4)  //             PASSWORD=XXXXXX,
*         (5)  //             MSGLEVEL=...
*        OR :
*         (1)  //JOBNAME   JOB AAAANNNN,     <--- FROM MVS/XA 2.1.7
*         (2)  //             USER-NAME,     **JOB STATEMENT ...
*         (3)  //             NOTIFY=USER-ID,
*         (4)  //             MSGLEVEL=...
*
*        A GENERATED JOB STATEMENT IS DETECTED BY THE COMMENT IN
*        CARD (2). THEN AT THE LAST CARD (CARD (5) OR (4) ABOVE),
*        THE EXIT EXECUTES ACTIONS DIRECTED BY THE RUNNING INDEX
*        VALUE "RUNIX" DESCRIBED BELOW :
*
*              0 - STATEMENT HAS BEEN IDENTIFIED AS A JOB GENERATED,
*                  LAST CARD SHOULD CONTAIN THE TEXT "MSGLEVEL=",
*                  SEND THE FIRST MESSAGE TO THE USER.
*              4 - EXIT RE-ENTERED AFTER WRITE OF FIRST MESSAGE,
*                  SEND THE SECOND MESSAGE WITH REPLY TO THE USER.
*              8 - EXIT RE-ENTERED WITH USER REPLY RESPONSE, WHICH
*                  IS ASSUMED TO BE A CORRECT JCL STATEMENT.
         SPACE 1
TGJS     TM    RUNSW,SWGJSD        GEN. JOB STAT. DE-ACTIVATED?
         BO    TFRWA               YES
         TM    RUNSW,SWGJS         GEN. JOB STAT. ACTIVE?
         BO    LASTC               YES
         CLC   RUNJC,=H'1'         NO, IS IT CARD (2)?
         BNE   TESTLC              NO, CONTINUE
         CLC   30(L'JSGS,R3),JSGS  JOB STATEMENT GENERATED BY SUBMIT?
         BNE   RSJCC               NO
         OI    RUNSW,SWGJS+SWSPWD  SET GEN. JOB STAT. ACTIVE
TESTLC   TM    IESTMTYP,IESSCON    IS IT LAST CARD?
         BZ    FREEWA              YES
BUMP     LH    R15,RUNJC           UPDATE CARD COUNT
         AH    R15,=H'1'
         STH   R15,RUNJC
         B     EXIT00
LASTC    TM    RUNSW,SWGJSC        GEN. JOB STAT. CONTINUE?
         BO    GETRIX              YES
         TM    IESTMTYP,IESSCON    IS IT LAST CARD?
         BO    BUMP                NO
         CLC   15(9,R3),=CL9'MSGLEVEL=' IS IT THE EXPECTED CARD?
         BNE   RSGJS               NO
         CLC   RUNJC,=H'3'         IS IT CARD (4)?
         BE    SETCNT              YES
         CLC   RUNJC,=H'4'         IS IT CARD (5)?
         BNE   RSGJS               NO
SETCNT   OI    RUNSW,SWGJSC        SET GEN. JOB STAT. CONTINUE
         SPACE 1
GETRIX   LH    R4,RUNIX            GET RUNNING INDEX
         B     *+L'*(R4)           GO TO
         B     GENJS        0 ---> LAST CARD?
         B     REENT        4 ---> RE-ENTERED AFTER FIRST MESSAGE?
         B     RRESP        8 ---> REPLY RESPONSE?
         SPACE 1
GENJS    LA    R4,FIRSTMSG
         ST    R4,IEMSGP
         MVC   RUNIX,=H'4'         SET FIRST MESSAGE
         B     EXIT08
REENT    LA    R4,DFLTCARD         DEFAULTS CARD
         ST    R4,IEMSGP
         MVC   0(80,R3),2(R4)      REPLACE THE GEN. STAT. BY DEFAULTS
         MVI   IEOPRAND,4          OFFSET OF OPERAND FIELD
         MVC   RUNIX,=H'8'         SET REPLY MESSAGE
         B     EXIT12              GO GET REPLY
RRESP    ICM   R4,B'1111',IEREPLYP REPLY BUFFER ADDRESS?
         BZ    RSGJS               NO, WHAT'S THAT ..., GET DEFAULTS
         SR    R5,R5
         ICM   R5,B'0011',0(R4)    GET LENGTH OF REPLY
         BZ    RSGJS               IF ZERO LENGTH, GET DEFAULTS
         SPACE 1
*        IF 3270 TYPE SCREEN AND PROFILE MSGID SPECIFIED, OR
*        IF USER HAS MODIFIED THE DISPLAYED LINE, THE RESPONSE
*        CONTAINS THE MESSAGE ID (IKJ56280A), AND EVENTUALLY A
*        PLUS SIGN (+) AT THE END OF THE MESSAGE.
         SPACE 1
         AH    R4,=H'2'            POINTS TO START OF MESSAGE TEXT
         SH    R5,=H'2'            -2 FOR LENGTH FIELD
         BNP   RSGJS               WHAT'S THAT ..., GET DEFAULTS
         CLC   0(10,R4),=CL10'IKJ56280A '
         BNE   SCHPS               NO MSGID, BUT LOOK FOR + SIGN
         AH    R4,=H'10'           SKIP MSGID
         SH    R5,=H'10'           -10 FOR MSG IKJ.....
         BNP   RSGJS               WHAT'S THAT ..., GET DEFAULTS
SCHPS    CLC   0(3,R4),=CL3'// '   SOMEWHAT LIKE A CARD ENTERED?
         BNE   RSGJS               NO, GET DEFAULTS
         LA    R6,0(R4,R5)         POINT END OF TEXT +1
LOOP     BCTR  R6,0                BACKWARD
         CLI   0(R6),C' '          CAN FORGET TRAILING BLANKS
         BNE   *+L'*+8
         BCT   R5,LOOP
         B     RSGJS               WHAT'S THAT ..., GET DEFAULTS
         CLI   0(R6),C'+'          IS IT AN INSERTED + SIGN?
         BNE   *+L'*+8             NO, LET'S GO
         BCT   R5,*+L'*+4          DON'T MOVE THE + SIGN
         B     RSGJS               WHAT'S THAT ..., GET DEFAULTS
         BCT   R5,*+L'*+4          -1 FOR MOVE
         B     RSGJS               WHAT'S THAT ..., GET DEFAULTS
         MVI   0(R3),C' '          CLEAR OLD CARD
         MVC   1(79,R3),0(R3)
         EX    R5,MVCD             REPLACE IT BY THE NEW ONE
         LA    R5,3(R4)            START AFTER '// '
NEXT     CLI   0(R5),C' '          STILL BLANK?
         BNE   SETFO               NO, FIRST OPERAND
         AH    R5,=H'1'            SOME HOPE TO FIND IT REMAIN
         CLR   R5,R6               LAST COLUMN?
         BNH   NEXT                NO, CONTINUE SCAN
SETFO    SLR   R5,R4               RELATIVE OFFSET FROM COLUMN 1
         AH    R5,=H'1'            TRUE OFFSET
         STC   R5,IEOPRAND         STORE NEW OFFSET OF OPERAND FIELD
RSGJS    XC    RUNIX,RUNIX         CLEAR ACTIVITY
         NI    RUNSW,255-SWGJS-SWGJSC
RSJCC    XC    RUNJC,RUNJC
         OI    RUNSW,SWGJSD        SET DE-ACTIVATED
         DROP  R10
TFRWA    TM    IESTMTYP,IESSCON    IS IT LAST CARD?
         BO    EXIT00              NO
         SPACE 2
FREEWA   L     R13,4(R13)          RESTORE PREVIOUS SAVE AREA
         L     R0,=A(WORKL)        YES, FREE WORK-AREA
         LR    R1,R10
        FREEMAIN R,A=(1),LV=(0)
         XC    IEEXITWD,IEEXITWD   RESET WORK-AREA POINTER
         LA    R15,IECONTIN
         B     LEAVE
         SPACE 1
MVCD     MVC   0(*-*,R3),0(R4)     <<EXECUTED>>
         SPACE 1
         DROP  R8,R9
         SPACE 2
*        SET EXIT RETURN CODE.
*        ---------------------
         SPACE 1
EXIT12   LA    R15,IEPROMPT
         B     EXIT
EXIT08   LA    R15,IEMSG
         B     EXIT
EXIT04   LA    R15,IERETURN
         B     EXIT
EXIT00   LA    R15,IECONTIN
EXIT     L     R13,4(R13)          RESTORE PREVIOUS SAVE AREA
LEAVE   RETURN (14,12),RC=(15)
         EJECT
*        C O N S T A N T S
*        -----------------
         SPACE 1
JSGS     DC    C'**JOB STATEMENT GENERATED BY SUBMIT**'
PSADD    DC    C'** SUPPLIED BY SUBMIT **'
         SPACE 1
*        M E S S A G E S
*        ---------------
         SPACE 1
FIRSTMSG DC    0H'0',AL2(FMSGL)
         DC    CL69'-- LAST CARD OF JOB STATEMENT : ALL DEFAULTS MAY BE1
                CHANGED.'
         DC    CL11' '
         DC    C'-- ENTER A NULL LINE TO ACCEPT DEFAULTS.'
FMSGL    EQU   *-FIRSTMSG
         SPACE 1
DFLTCARD DC    0H'0',AL2(DFLTCL)
         DC    CL80'// TIME=(001,00),REGION=2900K,MSGCLASS=X,MSGLEVEL=(1
               2,0)'
DFLTCL   EQU   *-DFLTCARD
         SPACE 2
        LTORG
         EJECT
*        W O R K - A R E A
*        -----------------
         SPACE 1
WORK     DSECT
         SPACE 1
SVA      DS    18F                 SAVE AREA
SVCARDP  DS    A                   SAVE CARD POINTER
CRD      DS    0CL80               CARD INSERTION
CRDS     DS    CL15                     //
CRDKW    DS    CL9                      PASSWORD=
USPSWD   DS    CL8,CL1,CL7              LOGON PASSWORD,
CRDEND   DS    CL40                     ** SUPPLIED BY SUBMIT **
JBUSR    DS    CL8                 JOB USER=...
RUNIX    DS    H                   RUNNING INDEX
RUNJC    DS    H                   RUNNING JOB CARD COUNT
SVOPOFF  DS    XL1                 SAVE OPERAND OFFSET
SVTYP    DS    XL1                 SAVE TYPE SWITCHES
RUNSW    DS    XL1                 RUNNING SWITCH
SWSPWD   EQU   X'80'                    SUPPLY PASSWORD DE-ACTIVATED
SWGJSD   EQU   X'40'                    GEN. JOB STAT. DE-ACTIVATED
SWGJS    EQU   X'20'                    GEN. JOB STAT. ACTIVE
SWGJSC   EQU   X'10'                    GEN. JOB STAT. CONTINUE
SWFJSC   EQU   X'08'                    FIRST JOB STAT. CARD PROCESSED
SWAPWC   EQU   X'04'                    ADD PASSWORD CARD
SWINSC   EQU   X'02'                    INSERT CARD DONE
SWSSTQ   EQU   X'01'                    START-STOP TEXT QUOTES
         SPACE 1
WORKL    EQU   (((*-WORK)+7)/8)*8
         SPACE 2
        PRINT  NOGEN
        CVT    DSECT=YES,LIST=YES
        IHAACEE
        IHAASCB
        IHAASXB
        IKJTSB LIST=YES
        IKJEFFIE IETYPE=SUBMIT
         SPACE 2
         END
