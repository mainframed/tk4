C001     TITLE 'CLEAN-UP - INVOKE IDCAMS FUNCTION (C001).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
C001    @XS
*        AT ENTRY :   IDCPARM = IDCAMS FUNCTION INFORMATION REQUEST
*         AT EXIT :   R15 = IDCAMS RETURN CODE
         SPACE 1
         ST    R15,EFSA            RETAIN PREVIOUS S.A.
         XC    16(4,R15),16(R15)   RESET RETURN CODE
         LM    R2,R3,IDCDSN
         CLI   IDCFUN,C'C'         CATALOG REQUEST?
         BE    IIFC                YES
         CLI   IDCFUN,C'U'         UNCATALOG REQUEST?
         BE    IIFU                YES
         XR    R15,R15             NO
         B     ERR79               INVALID FUNCTION CODE
         USING XPDSCT,R3
         SPACE 1
IIFC     MVI   EFC1L,L'EFCARD-1
         MVI   EFC1,C' '
         MVC   EFC1+1(71),EFC1
         MVC   EFC1+1(15),=CL15'DEF NVSAM(NAME('
         MVC   EFC1+16(44),0(R2)
         LA    R1,EFC1+59
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(3,R1),=CL3') -'
         MVI   EFC2L,L'EFCARD-1
         MVI   EFC2,C' '
         MVC   EFC2+1(71),EFC2
         MVC   EFC2+9(5),=CL5'DEVT('
        $DVTYUN XPTY,EFC2+14,MF=(E,EFDVT)
         LTR   R15,R15
         BNZ   ERR80               INVALID DEVICE TYPE
         LA    R1,EFC2+21
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(6,R1),=CL6') VOL('
         MVC   7(L'XPVOL,R1),XPVOL
         LA    R1,6+L'XPVOL(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(4,R1),=CL4')) -'
         MVI   EFC3L,L'EFCARD-1
         LA    R1,EFC3
         BAS   R14,EFCTLG
         MVI   EFC4L,255
         B     EF0
         SPACE 1
IIFU     MVI   EFC1L,L'EFCARD-1
         MVI   EFC1,C' '
         MVC   EFC1+1(71),EFC1
         MVC   EFC1+1(3),=CL3'DEL'
         MVC   EFC1+5(44),0(R2)
         LA    R1,EFC1+48
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   2(12,R1),=CL12'NVSAM NSCR -'
         MVI   EFC2L,L'EFCARD-1
         LA    R1,EFC2
         BAS   R14,EFCTLG
         MVI   EFC3L,255
         B     EF0
         SPACE 1
EFCTLG   MVI   0(R1),C' '
         MVC   1(71,R1),0(R1)
         MVC   9(4,R1),=CL4'CAT('
         L     R15,TBCVOL
         USING CVLT,R15
EFCTLG1  CLI   0(R15),FMOFF
         BE    EFCTLG3
         CLC   CVLNME,XPCNME
         BNE   EFCTLG2
         CLC   CVLVOL,XPCVOL
         BE    EFCTLG4
EFCTLG2  LA    R15,CVLL(R15)
         B     EFCTLG1
EFCTLG3  OI    EFCTLGSW+1,ON
EFCTLG4  MVC   13(L'XPCNME,R1),XPCNME
         LA    R1,12+L'XPCNME(R1)
         CLI   0(R1),C' '
         BNE   EFCTLGSW
         BCT   R1,*-8
EFCTLGSW NOP   EFCTLG6
         CLI   CVLPWL,0
         BNE   EFCTLG7
EFCTLG5  MVI   1(R1),C')'
         BR    R14
EFCTLG6  NI    EFCTLGSW+1,OFF
         B     EFCTLG5
EFCTLG7  MVI   1(R1),C'/'
         MVC   2(L'CVLPW,R1),CVLPW
         XR    R0,R0
         IC    R0,CVLPWL
         DROP  R15
         AR    R1,R0
         MVI   2(R1),C')'
         BR    R14
         SPACE 1
         DROP  R3
EF0      BAS   R14,IDCIAR          ALLOCATE IDCAMS INPUT
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR71
        $PUNCH ,,(R6),MF=(L,EFPUOUT)
        $UCLOSE (R6),MF=(L,EFCLOUT)
         TM    WHATS,WHIDIN        STILL OPENED?
         BZ    EF1                 NO
         NI    WHATS,255-WHIDIN    YES
        $UCLOSE MF=(E,EFCLOUT)     CLOSE IT, I WANT RE-USE IT
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR71
EF1      NI    EFSW+1,OFF
         LA    R2,EFCARD
EF2     $PUNCH 1(R2),,,MF=(E,EFPUOUT)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR71
EFSW     NOP   EF3
         OI    EFSW+1,ON
         OI    WHATS,WHIDIN        SAY OPENED
EF3      LA    R2,L'EFCARD(R2)     NEXT CARD
         CLI   0(R2),255           END?
         BNE   EF2                 NO, CONTINUE
         NI    WHATS,255-WHIDIN
        $UCLOSE MF=(E,EFCLOUT)     CLOSE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR71
         SPACE 1
         BAS   R14,IDCOAR          ALLOCATE IDCAMS OUTPUT PRINT
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR72
        $VOPEN WORKD,(R6),MF=(L,EFRDOUT)
        $VCLOSE (R6),MF=(L,EFCLOUT)
         TM    WHATS,WHIDOUT       STILL OPENED?
         BZ    EF10                NO
         NI    WHATS,255-WHIDOUT   YES
        $VCLOSE MF=(E,EFCLOUT)     CLOSE IT, I WANT RE-USE IT
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR72
EF10     BAS   R14,XIDCAMS         EXECUTE IDCAMS
         TM    RUNIND,RUTEST       TEST RUN?
         BO    EF11                YES, LIST IDCAMS OUTPUT PRINT
         LTR   R15,R15
         BZ    EF99                BRANCH IF ALL OK
EF11     L     R14,EFSA
         ST    R15,16(R14)         STORE RETURN CODE
         MVI   EFC1,C' '           NOTE - JUST TO USE IT AS CONSTANT
         MVC   EFC1+1(L'EFC1-1),EFC1      IF NEEDED LATER
         SPACE 1
         CVD   R15,WORKD
         ED    EFPRTT+4(4),WORKD+6
         TM    WHATS,WHPERR
         BO    EF12
        @OUT   EFPRT,=PL2'+40',U
         B     EF13
EF12     MVI   EFWTO+4,C' '
         MVC   EFWTO+5(59),EFWTO+4
         MVC   EFWTO+4(L'EFPRT+L'EFPRTT-1),EFPRT+1
        WTO    MF=(E,EFWTO)
EF13    $VOPEN MF=(E,EFRDOUT)
         LTR   R15,R15
         BNZ   ERR72
         OI    WHATS,WHIDOUT       SAY OPENED
         CLC   WORKD(4),=AL4(L'LINE)
         BH    ERR73
        $VREAD LINE,,MF=(L,EFRDOUT)
EF30     MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
        $VREAD MF=(E,EFRDOUT)      READ IDCAMS OUTPUT PRINT
         LTR   R15,R15
         BM    EF50
         BNZ   ERR72
         CLC   LINE(8),=CL8'1IDCAMS'    TITLE LINE?
         BE    EF30                     YES, SKIP IT
         LA    R1,LINE+1
         LA    R2,1
         LA    R3,LINE+L'LINE-1
         CLI   0(R1),C' '
         BNE   EF31
         BXLE  R1,R2,*-8
         B     EF30
EF31     CLC   0(9,R1),=CL9'IDC0001I'   TEST FUNCTION COMPLETED = 0
         BNE   EF32
         CLC   55(3,R1),=CL3' 0'
         BE    EF30
         B     EF40
EF32     CLC   0(9,R1),=CL9'IDC0002I'   TEST IDCAMS COMPLETED = 0
         BNE   EF33
         CLC   63(3,R1),=CL3' 0'
         BE    EF30
         B     EF40
EF33     CLC   0(3,R1),=CL3'IDC'   TEST ANY IDCAMS INFO'S (IDC...)
         BE    EF34 YES
         TM    RUNIND,RUTEST       NO, TEST RUN?
         BO    EF40                YES, LIST IT
         B     EF30
EF34     CLC   3(16,R1),=CL16'0550I ENTRY (A) '
         BNE   EF40
         MVI   0(R1),C' '
         MVC   1(18,R1),0(R1)
EF40     MVI   LINE,C' '
         TM    WHATS,WHPERR
         BO    EF41
        @OUT   LINE
         B     EF30+L'EF30
EF41     MVC   EFWTO+4(60),LINE+1
        WTO    MF=(E,EFWTO)
         CLC   EFC1(60),LINE+61
         BE    EF30+L'EF30
         MVC   EFWTO+4(60),LINE+61
        WTO    MF=(E,EFWTO)
         B     EF30+L'EF30
EF50     NI    WHATS,255-WHIDOUT   CLOSE IDCAMS OUTPUT PRINT
        $VCLOSE MF=(E,EFCLOUT)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR72
         SPACE 1
EF99    @XE
         EJECT
EFDVT   $DVTYUN ,,MF=L
EFPUOUT $PUNCH ,,,MF=L
EFRDOUT $VREAD ,,MF=L              ALSO USED BY VOPEN
EFCLOUT $VCLOSE ,MF=L              ALSO USED BY UCLOSE
         SPACE 1
EFSA     DC    F'0'
EFWTO   WTO    '                                                       C
                    ',ROUTCDE=11,DESC=7,MF=L
         SPACE 1
         DC    AL1(L'EFPRT+L'EFPRTT)
EFPRT    DC    C'0===== IDCAMS : '
EFPRTT   DC    0CL14' ',CL5'RC = ',XL3'202120',CL6' ====='
EFCARD   DS    0CL81               INPUT CARDS GENERATION BUFFER
EFC1L    DC    AL1(0)
EFC1     DC    CL72' ',CL8'GFUN0001'
EFC2L    DC    AL1(0)
EFC2     DC    CL72' ',CL8'GFUN0002'
EFC3L    DC    AL1(0)
EFC3     DC    CL72' ',CL8'GFUN0003'
EFC4L    DC    AL1(0)
EFC4     DC    CL72' ',CL8'GFUN0004'
EFC5L    DC    AL1(0)
EFC5     DC    CL72' ',CL8'GFUN0005'
         DC    AL1(255)
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
         SPACE 1
XPDSCT   DSECT                     IDCAMS PARM.LIST DESCRIPTION
XPCNME   DS    CL44                CATALOG NAME
XPCVOL   DS    CL6                 CATALOG VOLUME SERIAL NUMBER
         DS    H
XPTY     DS    XL4                 D.S. VOLUME DEVICE TYPE
XPVOL    DS    CL6                 D.S. VOLUME SERIAL NUMBER
         SPACE 2
         END
