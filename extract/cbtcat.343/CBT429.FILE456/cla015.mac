A015     TITLE 'CLEAN-UP - GROUP''S ACCESS LIST (A015).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A015    @XS    ALIAS=RACFGRP
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    GRP99               YES
         TM    STATUS,STBRACF      RACF MUST BE BYPASSED?
         BO    GRP99               YES
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    GRP99               NO, SKIP THE LIST
         TM    RUNOPT,RUNLG        SUPPRESS REQUESTED?
         BO    GRP99               YES
         LA    R1,GRNMEL
         ST    R1,SGRPGL
         LA    R1,SVSNAP+4
         ST    R1,SGRPGS
         SPACE 1
*        RACF GROUP'S LIST.
         SPACE 1
         LA    R2,LINE
         USING LGLNE,R2
         L     R7,WK1A             RACF WORK AREA
         USING WKDS,R7
         ST    R7,SGRPGW
         XC    COUNTER,COUNTER
        @LIST  GRPTIT,,U
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP1                NO
        @COPY  GRPTIT,,U
         SPACE 1
*        GET GROUP'S LIST FROM RACF.
*        NOTE : USER AUTHORITY IN GROUP (FROM ICHPTDEF : TEMPLATE
*               DEFINITION) :     X'80' - JOIN
*                                 X'40' - CONNECT
*                                 X'20' - CREATE
*                                 X'10' - USE
         SPACE 1
GRP1     XC    WORKRBA(WORKBSLL),WORKRBA
         LA    R0,GRNMEL
        ICHEINTY NEXTC,TYPE='GRP',ENTRY=(R0),WKAREA=(R7),              C
               ACTIONS=(AGU,AGA,AGG,AGT,AGI,AGM,AGS,AGN,AGC,AGL,AGR)
         CH    R15,=H'12'          END OF FILE?
         BE    GRP90               YES
         LA    R14,WORKBS
         A     R14,WORKTL
         STCM  R14,B'0111',SGRPGN+1
         XR    R14,R14
         IC    R14,GRNMEL
         LA    R14,1(R14)
         ST    R14,SGRPGG
        @SNAPS SF,SGRPGL,RGR
         L     R1,COUNTER
         LA    R1,1(R1)
         ST    R1,COUNTER
         LTR   R15,R15             ANY ERROR?
         BZ    GRP3                NO
         CVD   R15,WORKD           RETURN CODE
         UNPK  GRPRRC,WORKD+6(2)
         OI    GRPRRC+1,C'0'
         MVI   GRPRAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   GRP2                NO
         STC   R0,GRPRAC
         OI    GRPRAC,C'0'
GRP2    @LIST  GRPR
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP1                NO
        @COPY  GRPR
         B     GRP1
GRP3     XR    R1,R1
         IC    R1,GRNMEL           GET LENGTH OF GROUP NAME
         BCTR  R1,0                -1 FOR MOVE
         OC    WORKTL,WORKTL       DATA RETURNED?
         BZ    GRP4                NO
         CLC   WORKVRA(2),=XL2'0008'    SUPGROUP RECEIVED?
         BNE   GRP4                NO
         CLC   WORKVRA+10(2),=XL2'0008' AUTHOR RECEIVED?
         BNE   GRP4                NO
         CLC   WORKVRA+20(2),=XL2'0001' UNIV. GROUP AUTH. RECEIVED?
         BNE   GRP4                NO
         CLC   WORKVRA+23(2),=XL2'0001' TERM. UNIV. AUTH. RECEIVED?
         BE    GRP10               YES
GRP4     MVC   GRPERN(L'GRPERN),GRPERN-1
         EX    R1,GRPMV            EXECUTE MOVE
        @LIST  GRPERR
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP1                NO
        @COPY  GRPERR
         B     GRP1
GRPMV    MVC   GRPERN(*-*),GRNAME  << EXECUTED >>
GRPMD    MVC   LGVAR+15(*-*),0(R3) << EXECUTED >>
GRP10    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         EX    R1,GRPMG            MOVE GROUP NAME
         MVI   LGSPO,C'('
         MVC   LGOW+1(6),=CL6'......'
         CLI   WORKVRA+12,X'FF'
         BE    *+L'*+6
         MVC   LGOW(L'LGOW),WORKVRA+12  MOVE OWNER OF GROUP
         MVI   LGSPT,C')'
         MVI   LGSPS,C':'
         MVC   LGSG(8),=CL8'..NONE..'
         CLI   WORKVRA+2,X'FF'
         BE    *+L'*+6
         MVC   LGSG(L'LGSG),WORKVRA+2   MOVE SUPERIOR GROUP
         CLI   WORKVRA+22,0        UACC?
         BNE   GRP11               YES
         CLI   WORKVRA+25,0        NOTRMUACC?
         BE    GRP22               YES
         LA    R1,LGVAR
         B     GRP18
GRPMG    MVC   LGGN(*-*),GRNAME    << EXECUTED >>
GRP11    MVC   LGVAR(13),=CL13'- AUTHORITY :'
         LA    R1,LGVAR+14
         CLI   WORKVRA+22,ACEEALTR ALTER ACCESS?
         BNE   GRP12               NO
         MVC   0(5,R1),=CL5'ALTER'
         LA    R1,6(R1)
         B     GRP17
GRP12    CLI   WORKVRA+22,ACEECNTL CONTROL ACCESS?
         BNE   GRP13               NO
         MVC   0(7,R1),=CL7'CONTROL'
         LA    R1,8(R1)
         B     GRP17
GRP13    CLI   WORKVRA+22,ACEEUPDT UPDATE ACCESS?
         BNE   GRP14               NO
         MVC   0(6,R1),=CL6'UPDATE'
         LA    R1,7(R1)
         B     GRP17
GRP14    CLI   WORKVRA+22,ACEEREAD READ ACCESS?
         BNE   GRP15               NO
         MVC   0(4,R1),=CL4'READ'
         LA    R1,5(R1)
         B     GRP17
GRP15    CLI   WORKVRA+22,ACEENONE NONE ACCESS?
         BNE   GRP16               NO
         MVC   0(4,R1),=CL4'NONE'
         LA    R1,5(R1)
         B     GRP17
GRP16    MVI   0(R1),C'U'          ANYTHING MIXED OR UNKNOWN
         LA    R15,WORKVRA+22
         BAS   R14,DMPRTN
GRP17    CLI   WORKVRA+25,0        TERMUACC?
         BE    GRP20               YES
GRP18    CLI   WORKVRA+25,X'80'    NOTRMUACC?
         BNE   GRP19               NO
         MVC   0(11,R1),=CL11'- NOTRMUACC'
         B     GRP20
GRP19    MVI   0(R1),C'-'          ANYTHING MIXED OR UNKNOWN
         LA    R1,2(R1)
         MVI   0(R1),C'T'
         LA    R15,WORKVRA+25
         BAS   R14,DMPRTN
GRP20   @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP21               NO
        @COPY  LINE
GRP21    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
GRP22    LA    R3,GRNMEL
         LR    R4,R3               SET RACF GROUP CALL
        @X     B005                LOCATE CATALOG VOLUME IF ANY
         LA    R3,WORKVRA+26       START OF VARIABLE FIELDS
         XR    R4,R4
         IC    R4,2(R3)            GET INST. DATA LENGTH
         LA    R3,3(R3)            POINT TO INST. DATA OR NEXT
         LTR   R4,R4
         BZ    GRP25
         MVC   LGVAR(14),=CL14'- INST. DATA :'
         LA    R5,60
GRP23    CLR   R4,R5
         BNL   *+L'*+2
         LR    R5,R4
         BCTR  R5,0
         EX    R5,GRPMD
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP24               NO
        @COPY  LINE
GRP24    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,1(R5)
         AR    R3,R5
         SR    R4,R5
         BNZ   GRP23
GRP25    XR    R4,R4
         IC    R4,2(R3)            GET MODEL NAME LENGTH
         LA    R3,3(R3)            POINT TO MODEL NAME OR NEXT
         LTR   R4,R4
         BZ    GRP28
         MVC   LGVAR(14),=CL14'- MODEL NAME :'
         LA    R5,60
GRP26    CLR   R4,R5
         BNL   *+L'*+2
         LR    R5,R4
         BCTR  R5,0
         EX    R5,GRPMD
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP27               NO
        @COPY  LINE
GRP27    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,1(R5)
         AR    R3,R5
         SR    R4,R5
         BNZ   GRP26
GRP28    XR    R4,R4
         MVC   LGSPG(16),=CL16'- SGRP(S) : NONE'
         ICM   R4,B'0011',2(R3)    GET NO. OF SUBGROUPS
         LA    R3,6(R3)            POINT TO FIRST NAME IN LIST
         LTR   R4,R4               AT LEAST ONE?
         BNP   GRP33               NO
         LA    R0,NLGSN            NO. OF NAMES BY LINE
         LA    R1,LGSN
GRP30    MVC   0(L'LGSN-1,R1),0(R3)
         LA    R1,L'LGSN(R1)       ADVANCE IN LINE
         BCT   R0,GRP32
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP31               NO
        @COPY  LINE
GRP31    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R0,NLGSN            NO. OF NAMES BY LINE
         LA    R1,LGSN
GRP32    LA    R3,8(R3)            ADVANCE IN NAME LIST
         BCT   R4,GRP30            LOOP IF ANY MORE
         CLI   LGSN,C' '           ANYTHING PENDING?
         BE    GRP35               NO
GRP33   @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP34               NO
        @COPY  LINE
GRP34    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
GRP35    MVC   LGSPU(16),=CL16'- USER(S) : NONE'
         XR    R4,R4
         LR    R5,R4
         ICM   R4,B'0011',2(R3)    GET NO. USERS
         ICM   R5,B'0011',4(R3)    GET LENGTH OF NAMES LIST
         LA    R3,6(R3)            POINT TO NAMES LIST
         LA    R5,2(R5,R3)         POINT TO ACCESS CODES
         LTR   R4,R4               AT LEAST ONE?
         BNP   GRP44               NO
         LA    R0,NLGUN            NO. OF USERS BY LINE
         LA    R1,LGUN
         USING LGUS,R1
GRP36    MVC   LGUS(L'LGUS),0(R3)  MOVE NAME
         MVI   LGSPA,C'-'
         CLI   0(R5),X'80'         JOIN ACCESS?
         BNE   GRP37               NO
         MVC   LGAC,=CL8'JOIN'
         B     GRP41
GRP37    CLI   0(R5),X'40'         CONNECT ACCESS?
         BNE   GRP38               NO
         MVC   LGAC,=CL8'CONNECT'
         B     GRP41
GRP38    CLI   0(R5),X'20'         CREATE ACCESS?
         BNE   GRP39               NO
         MVC   LGAC,=CL8'CREATE'
         B     GRP41
GRP39    CLI   0(R5),X'10'         USE ACCESS?
         BNE   GRP40               NO
         MVC   LGAC,=CL8'USE'
         B     GRP41
GRP40    LR    R15,R5              ANYTHING MIXED OR UNKNOWN
         LR    R5,R1
         LA    R1,LGAC
         BAS   R14,DMPRTS
         LR    R1,R5
         LR    R5,R15
GRP41    LA    R1,L'LGUN(R1)       ADVANCE IN LINE
         BCT   R0,GRP43
         DROP  R1
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP42               NO
        @COPY  LINE
GRP42    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R0,NLGUN            NO. OF USERS BY LINE
         LA    R1,LGUN
GRP43    LA    R3,8(R3)            ADVANCE IN NAME LIST
         LA    R5,1(R5)            ADVANCE IN ACCESS CODES
         BCT   R4,GRP36            LOOP IF ANY MORE
         CLI   LGUN,C' '           ANYTHING PENDING?
         BE    GRP50               NO
GRP44   @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP45               NO
        @COPY  LINE
GRP45    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         SPACE 1
*        GET DATA-SET'S PROFILES OF THIS GROUP.
         SPACE 1
GRP50   @X     B008                GET GROUP DATA-SETS PROFILES
         L     R7,WK1A             RESTORE MAIN RACF WORK AREA
         B     GRP1                GET NEXT GROUP
         DROP  R2,R7
GRP90    L     R1,COUNTER
         CVD   R1,WORKD
         ED    GRPENDN,WORKD+5
        @LIST  GRPEND
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    GRP91               NO
        @COPY  GRPEND
GRP91    TM    STATUS,STGCVL       USERS PROCESSED?
         BZ    GRP99               NO
         XR    R3,R3               SET CHECK LIST CALL
        @X     B005
GRP99   @XE
         EJECT
AGU     ICHEACTN FIELD=SUPGROUP
AGA     ICHEACTN FIELD=AUTHOR
AGG     ICHEACTN FIELD=UACC
AGT     ICHEACTN FIELD=NOTRMUAC
AGI     ICHEACTN FIELD=INSTDATA
AGM     ICHEACTN FIELD=MODELNAM
AGS     ICHEACTN FIELD=SUBGRPCT
AGN     ICHEACTN FIELD=SUBGRPNM
AGC     ICHEACTN FIELD=ACLCNT
AGL     ICHEACTN FIELD=USERID
AGR     ICHEACTN FIELD=USERACS
         SPACE 1
SGRPGL   DC    A(*-*)
SGRPGG   DC    A(*-*)
SGRPGS   DC    A(*-*)
         DC    A(8)
SGRPGW   DC    A(*-*)
SGRPGN   DC    XL1'80',AL3(*-*)
         SPACE 1
         DC    AL1(L'GRPTIT)
GRPTIT   DC    C'1RACF GROUP''S - DATA-SETS ACCESSES LIST :'
         DC    AL1(GRPENDL)
GRPEND   DC    C'0END OF RACF GROUP''S - DATA-SETS ACCESSES LIST '
         DC    C'(GROUP''S TOTAL :'
GRPENDN  DC    XL6'402020202120',C').'
GRPENDL  EQU   *-GRPEND
         DC    AL1(GRPRL)
GRPR     DC    CL3' ',CL3'-- ',C'RACF READ TYPE=GRP (RC='
GRPRRC   DC    CL2' ',C'/AC='
GRPRAC   DC    CL1' ',C').'
GRPRL    EQU   *-GRPR
         DC    AL1(GRPERRL)
GRPERR   DC    CL3' ',CL3'-- '
GRPERN   DC    CL8' ',C' : RACF READ TYPE=GRP '
         DC    C'(INVALID WORK AREA RECEIVED BACK).'
GRPERRL  EQU   *-GRPERR
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
        #WKDS
         EJECT
        #LGLNE
         SPACE 2
         END
