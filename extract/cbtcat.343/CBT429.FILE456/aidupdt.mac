AIDUPDT  TITLE 'ACCOUNTING INFORMATION DATA-SET UPDATE.'
AIDUPDT  START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *       ACCOUNTING INFORMATION        * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *          DATA-SET UPDATE            * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*
* PURPOSE :    UPDATE AN UNLOADED (OUTPUT FROM "AIDUNLD" PROGRAM)
* ---------    ACCOUNTING INFORMATION DATA-SET SEQUENTIAL FILE
*              CONTENTS (OPERATION IS PREPARE NEXT YEAR VALIDITY
*              START-END DATES IN ALL AUTHORIZATION RECORDS).
*
* CALL OF THE PROGRAM :
* ---------------------
*
*        //UPDATE EXEC  PGM=AIDUPDT,PARM='YYYY,S,E'
*                       YYYY = THE DESIRED YEAR (MANDATORY)
*                       'S' = UPDATE START DATES ONLY
*                       'E' = UPDATE END DATES ONLY
*                       IF 'S' AND 'E' NOT SPECIFIED, IT MEANS UPDATE
*                           BOTH START-END DATES.
*        //SYSIN    DD  ... SEQUENTIAL FILE (UNLOADED DATA, THIS IS
*                           THE OUTPUT OF THE "AIDUNLD" PROGRAM) ...
*                       NOTE : THE SYSPUNCH-LRECL MUST BE 2-BYTES
*                              LONGER THAN THAT OF SYSACC-LRECL.
*        //SYSPUNCH DD  ... SEQUENTIAL FILE (UNLOADED DATA, THIS IS
*                           THE NEW UPDATED SEQUENTIAL FILE) ...
*                       NOTE : THE SYSPUNCH-LRECL MUST BE 2-BYTES
*                              LONGER THAN THAT OF SYSACC-LRECL.
*        //SYSPRINT DD  SYSOUT=A ---> LIST OF UPDATED KEYS
*
* RETURN CODES :   0 = NORMAL COMPLETION
* --------------  16 = ABNORMAL COMPLETION, MESSAGE(S) ON SYSPRINT OR
*                      JOB LOG.
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
         EJECT
        $AIRD
        $DEFREG
         EJECT
*---------------------------------------------------------------------*
*        AUTHORITY LEVELS DEFINITIONS.                                *
*---------------------------------------------------------------------*
         SPACE 1
LV12     EQU   12        ADMINISTRATOR CONTROL LEVEL
         SPACE 1
AUT98   $MDL@IX
         SPACE 1
        $XENT  BASE=R12
         LR    R2,R1               PARM.FIELD ADDRESS
         EJECT
        $EACM  REQAUT
         CLI   AUTLVL,LV12
         BNH   NOTAUT
         L     R1,0(R2)            GET DESIRED YEAR AND OPTION(S)
         LH    R0,0(R1)
         LTR   R0,R0
         BNP   NOPARM              YEAR MISSING
         CH    R0,=H'4'
         BL    INPARM              INVALID YEAR
         LA    R1,2(R1)
PARML    CLI   0(R1),C','
         BE    PARMN
         CLI   0(R1),C'S'
         BE    PARMS
         CLI   0(R1),C'E'
         BE    PARME
         CLI   STSDTE,C' '
         BNE   INPARM
         CH    R0,=H'4'
         BL    INPARM
         LR    R14,R1
         LA    R15,4
PARMT    CLI   0(R1),C'0'
         BL    INPARM
         CLI   0(R1),C'9'
         BH    INPARM
         LA    R1,1(R1)
         BCTR  R0,0
         BCT   R15,PARMT
         MVC   STSDTE(4),0(R14)    GET YEAR FOR START-END DATES
         MVC   ENSDTE(4),0(R14)
         LTR   R0,R0
         BP    PARML
         B     CHECK
PARMS    OI    SWITCH,SWS
         B     PARMN
PARME    OI    SWITCH,SWE
PARMN    LA    R1,1(R1)
         BCT   R0,PARML
CHECK    TM    SWITCH,SWS+SWE
         BNZ   *+L'*+4
         OI    SWITCH,SWS+SWE
        $GFDATE ,,WORKD,STSDTE
         LTR   R15,R15
         BNZ   GFDERR
        $GFDATE ,,WORKD,ENSDTE
         LTR   R15,R15
         BZ    LSNDTE
         CH    R15,=H'4'
         BNE   GFDERR
         MVC   ENSDTE+4(3),=CL3'365'
        $GFDATE ,,WORKD,ENSDTE
         LTR   R15,R15
         BNZ   GFDERR
LSNDTE   PACK  WORKD,STSDTE
         CVB   R1,WORKD
         STCM  R1,B'0111',SBDTE    SET BINARY START DATE
         PACK  WORKD,ENSDTE
         CVB   R1,WORKD
         STCM  R1,B'0111',EBDTE    SET BINARY END DATE
         PACK  WORKD,STSDTE(4)     COMPUTE PREVIOUS YEAR FOR CHECKS
         CVB   R1,WORKD
         BCTR  R1,0
         CVD   R1,WORKD
         UNPK  WORKD(5),WORKD+5(3)
         OI    WORKD+4,C'0'
         MVC   PREDTE(4),WORKD+1
        $GFDATE ,,WORKD,PREDTE
         LTR   R15,R15
         BZ    LSPDTE
         CH    R15,=H'4'
         BNE   GFDERR
         MVC   PREDTE+4(3),=CL3'365'
        $GFDATE ,,WORKD,PREDTE
         LTR   R15,R15
         BNZ   GFDERR
LSPDTE   PACK  WORKD,PREDTE
         CVB   R1,WORKD
         STCM  R1,B'0111',PBDTE    SET BINARY PREVIOUS END DATE
         L     R8,PTRI
         XR    R7,R7
         XR    R9,R9
         LA    R6,DATA
         USING AUTDSECT,R6
READ     NI    SWITCH,255-SWL
        $READ  ARG                 READ DATA (SYSIN)
         LTR   R15,R15             COMPLETION?
         BM    EOF                 END OF DATA
         BP    RDRERR              READ ERROR
         XR    R1,R1
         CLC   ARG,=Y(NAUTNO)      IS IT AN AUTHORIZATION RECORD?
         BH    UALL                NO, COPY AS IT IS
         TM    SWITCH,SWS          YES, SET NEW DATES (UPDATE)
         BZ    LTOEDT
         XR    R0,R0               START DATE CHECK
         ICM   R0,B'0111',AUTVSD
         CL    R0,=F'99999'        WHICH FORMAT?
         BH    *+L'*+8             NEW FORMAT
         AL    R0,=F'1900000'      OLD FORMAT, ADJUST IT
         STCM  R0,B'0111',AUTVSD
         CLC   AUTVSD,SBDTE
         BNH   LTOEDT
         MVC   AUTVSD,SBDTE        ADVANCE DATE
         LA    R1,1
         OI    SWITCH,SWL+SWPS
LTOEDT   TM    SWITCH,SWE
         BZ    UALL
         XR    R0,R0               END DATE CHECK
         ICM   R0,B'0111',AUTVED
         CL    R0,=F'99999'        WHICH FORMAT?
         BH    *+L'*+8             NEW FORMAT
         AL    R0,=F'1900000'      OLD FORMAT, ADJUST IT
         STCM  R0,B'0111',AUTVED
         CLC   AUTVED,PBDTE
         BNE   UALL
         MVC   AUTVED,EBDTE        POSTPONE DATE
         LA    R1,1
         OI    SWITCH,SWL+SWPE
UALL     AR    R9,R1
         LA    R7,1(R7)
        $PUNCH ARG                 PUNCH DATA (SYSPUNH)
         LTR   R15,R15             COMPLETION?
         BNZ   PUNERR              PUNCH ERROR
         TM    SWITCH,SWL
         BZ    READ
         XR    R0,R0               LIST THE ARG. NO.
         ICM   R0,B'0011',ARG
         CVD   R0,WORKD
         MVC   0(L'MASK,R8),MASK
         ED    0(L'MASK,R8),WORKD+5
         LA    R8,L'MASK+2(R8)
         CL    R8,PTRL
         BNH   READ
         LA    R2,LINE             DISPLAY KEYS
         BAS   R5,PRTRTN
         B     CLS
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         L     R8,PTRI
         B     READ
         DROP  R6
RDRERR   LA    R2,RDRMSG
         B     PUNERR+L'PUNERR
PUNERR   LA    R2,PUNMSG
         BAS   R5,PRTRTN
         NOP   0
         OI    SWITCH,SWX
EOF      CL    R8,PTRI             ANYTHING TO BE PRINTED?
         BE    CLS                 NO
         LA    R2,LINE             DISPLAY LAST KEYS
         BAS   R5,PRTRTN
         NOP   0
CLS     $RCLOSE ,                  CLOSE READ (SYSIN)
         LTR   R15,R15
         BZ    *+L'*+4
         OI    SWITCH,SWX
        $UCLOSE ,                  CLOSE PUNCH (SYSPUNCH)
         LTR   R15,R15
         BZ    *+L'*+4
         OI    SWITCH,SWX
         TM    SWITCH,SWX
         BO    LEAVE
         TM    SWITCH,SWPS
         BZ    TSTEND
         MVC   INSMSGD,STSDTE
         LA    R2,INSMSG
         BAS   R5,PRTRTN
         B     LEAVE
TSTEND   TM    SWITCH,SWE
         BZ    STPRCS
         MVC   INEMSGT,PREDTE
         MVC   INEMSGD,ENSDTE
         LA    R2,INEMSG
         BAS   R5,PRTRTN
         B     LEAVE
STPRCS   CVD   R9,WORKD            DISPLAY COUNTS
         ED    MCNT,WORKD+4
         CVD   R7,WORKD
         ED    TCNT,WORKD+4
         LA    R2,MEND
         BAS   R5,PRTRTN
         NOP   0
LEAVE   $PCLOSE ,                  CLOSE PRINT (SYSPRINT)
         LTR   R15,R15
         BZ    *+L'*+4
         OI    SWITCH,SWX
         XR    R10,R10             R.C. = 0
         TM    SWITCH,SWX
         BZ    *+L'*+4
         LA    R10,16              SET RC = 16
        $XRET  CC=(R10)
NOTAUT   LA    R2,AUTMSG
         B     SNDMSG
NOPARM   LA    R2,NPMMSG
         B     SNDMSG
INPARM   LA    R2,INVMSG
         B     SNDMSG
GFDERR   STC   R15,GFDMSGR
         OI    GFDMSGR,C'0'
         LA    R2,GFDMSG
SNDMSG   BAS   R5,PRTRTN
         NOP   0
         OI    SWITCH,SWX
         B     LEAVE
         EJECT
PRTRTN  $PRINT (R2)                PRINT ROUTINE (LINK = R5)
         LTR   R15,R15             R2 = LINE POINTER
         BZ    4(R5)               RETURN +4 = PRINT DONE
         OI    SWITCH,SWX          SAY ERROR
         BR    R5                  RETURN +0 = PRINT ERROR
         EJECT
*        CONSTANTS AND WORK AREAS.
*        -------------------------
         SPACE 1
WORKD    DC    D'0'
PTRI     DC    A(LINE+10)
PTRL     DC    A(LINE+110)
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUTLVL)
         SPACE 1
         DC    A(AUTLGTH+L'ARG)
ARG      DC    H'0'
DATA     DC    (AUTLGTH)X'0'
         SPACE 1
STSDTE   DC    CL7'    001'        START SERIAL DATE (YYYYDDD)
ENSDTE   DC    CL7'    366'        END SERIAL DATE (YYYYDDD)
PREDTE   DC    CL7'    366'        PREVIOUS END SERIAL DATE (YYYYDDD)
SBDTE    DC    XL3'0'              START DATE (BINARY)
EBDTE    DC    XL3'0'              END DATE (BINARY)
PBDTE    DC    XL3'0'              PREVIOUS END DATE (BINARY)
MASK     DC    XL6'402020202120'
AUTLVL   DC    XL1'0'
SWITCH   DC    XL1'0'
SWL      EQU   X'80'
SWX      EQU   X'40'
SWPS     EQU   X'08'
SWPE     EQU   X'04'
SWS      EQU   X'02'
SWE      EQU   X'01'
         SPACE 1
         DC    AL1(L'LINE)
LINE     DC    CL121'0 KEYS :'
         DC    AL1(MENDL)
MEND     DC    C'0 KEYS UPDATED :'
MCNT     DC    XL8'4020202020202120',C' - TOTAL KEYS COPIED :'
TCNT     DC    XL8'4020202020202120',C'.'
MENDL    EQU   *-MEND
         DC    AL1(L'AUTMSG)
AUTMSG   DC    C'0 UNAUTHORIZED ACCESS TO THIS PROGRAM - EXIT -'
         DC    AL1(L'NPMMSG)
NPMMSG   DC    C'0 PARM. FIELD MISSING - EXIT -'
         DC    AL1(L'INVMSG)
INVMSG   DC    C'0 INVALID PARM. FIELD - EXIT -'
         DC    AL1(GFDMSGL)
GFDMSG   DC    C'0 GFDATE ROUTINE ERROR (RC='
GFDMSGR  DC    CL1' ',C') - EXIT-'
GFDMSGL  EQU   *-GFDMSG
         DC    AL1(INSMSGL)
INSMSG   DC    C'0 VALIDITY START DATES CHANGED TO '
INSMSGD  DC    CL7' ',C' IN SOME/ALL KEYS RECORDS LISTED ABOVE.'
INSMSGL  EQU   *-INSMSG
         DC    AL1(INEMSGL)
INEMSG   DC    C'0 ONLY VALIDITY END DATES '
INEMSGT  DC    CL7' ',C' CHANGED TO '
INEMSGD  DC    CL7' ',C' IN SOME/ALL KEYS RECORDS LISTED ABOVE.'
INEMSGL  EQU   *-INEMSG
         DC    AL1(L'RDRMSG)
RDRMSG   DC    C'0 READ (SYSIN) ERROR - EXIT -'
         DC    AL1(L'PUNMSG)
PUNMSG   DC    C'0 PUNCH (SYSPUNCH) ERROR - EXIT -'
         SPACE 2
        LTORG
         SPACE 2
         END
