AU33     TITLE '-- TSO-CP AUT30 - ROUTINE AUT33 --'
AUT33    START 0
         SPACE 1
**********************************************************************
*        THIS SUBROUTINE IS CALLED BY PROGRAM 'AUT30'.               *
*        CHECK SYSTEM HIGH-SENSITIVE AUTHORIZATIONS/PROGRAMMERS.     *
*                                             P.A.MOINIL  26 MARS 90 *
**********************************************************************
*        CALL AUT33,(PARM1,PARM2,PARM3),VL                           *
*        FUNCTION : SUBROUTINE TO CHECK SYSTEM HIGH-SENSITIVE        *
*                   AUTHORIZATIONS/PROGRAMMERS NUMBERS.              *
*        AT ENTRY : R1  = PARM. ADDRESS                              *
*                         PARM. --->   A(PARM1,PARM2,PARM3)          *
*                 WHERE : PARM1 = AUTHOR. NUMBER IN BINARY (H) OR    *
*                                 ZERO IF CHECK NOT DESIRED.         *
*                         PARM2 = PROGR. NUMBER IN BINARY (H) OR     *
*                                 ZERO IF CHECK NOT DESIRED.         *
*                         PARM3 = BOX NUMBER IN BINARY (H) OR ZERO   *
*                                 IF CHECK NOT DESIRED.              *
*         AT EXIT : R15 = 0 - ACCEPT COMPLETION                      *
*                         4 - REJECT COMPLETION                      *
*                         8 - SEVERE ERROR                           *
*                         WITH R1 = ADDRESS OF TERMINATION MESSAGE   *
*                         TEXT, IT POINTS AS BELOW :                 *
*                                    DC  AL1(L'TEXT)                 *
*                         ---> TEXT  DC  C'...MESSAGE...'            *
*--------------------------------------------------------------------*
         SPACE 1
*------- MAXIMUM ELEMENTS OF INTERNAL TABLES DEFINITIONS
MXAP     EQU   100                     MAX. AUT.-PRG. ELEMENTS
MXBP     EQU   50                      MAX. BOX-PRG. ELEMENTS
MXIO     EQU   50                      MAX. IO ELEMENTS
MXSP     EQU   50                      MAX. SPECIAL ELEMENTS
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R10
         SPACE 1
         LM    R2,R4,0(R1)             LOAD ADDRESS OF PARAMETERS
         MVC   TAUT(L'TAUT),0(R2)      GET AUT NBR BINARY
         MVC   TPRG(L'TPRG),0(R3)      GET PRG NBR BINARY
         MVC   TBOX(L'TBOX),0(R4)      GET BOX NBR BINARY
         OC    UJVTADDR,UJVTADDR       IEFUJV/UJVTABLE BUILDED?
         BNZ   CHKREQ                  YES
         EJECT
*------- BUILD INTERNAL TABLES
         L     R15,=V(SUJVTA)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   EXERR
         ST    R1,UJVTADDR
         USING UVTDSECT,R1
         L     R2,SYSAUT
         DROP  R1
         LM    R3,R5,=A(CHKAPTB-L'CHKAPTB,L'CHKAPTB,CHKAPTBE)
TAPT     CLC   0(4,R2),=XL4'0'
         BE    EAPT
         LA    R1,2(R2)
NAPT     CLC   0(2,R1),=XL4'0'
         BE    EAPE
         BXLE  R3,R4,*+L'*+4
         B     XOVAP
         MVC   0(2,R3),0(R2)
         MVC   2(2,R3),0(R1)
         LA    R1,2(R1)
         B     NAPT
EAPE     LA    R2,2(R1)
         B     TAPT
EAPT     CL    R3,=A(CHKAPTB-L'CHKAPTB,L'CHKAPTB,CHKAPTBE)
         BE    ZAPT
         LR    R5,R3
         L     R3,=A(CHKAPTB-L'CHKAPTB,L'CHKAPTB,CHKAPTBE)
         ALR   R3,R4
         STM   R3,R5,CHKAPTBS
ZAPT     L     R1,UJVTADDR
         USING UVTDSECT,R1
         L     R2,SYSBOX
         DROP  R1
         LM    R3,R5,=A(CHKBPTB-L'CHKBPTB,L'CHKBPTB,CHKBPTBE)
TBPT     CLC   0(4,R2),=XL4'0'
         BE    EBPT
         LA    R1,2(R2)
NBPT     CLC   0(2,R1),=XL4'0'
         BE    EBPE
         BXLE  R3,R4,*+L'*+4
         B     XOVBP
         MVC   0(2,R3),0(R2)
         MVC   2(2,R3),0(R1)
         LA    R1,2(R1)
         B     NBPT
EBPE     LA    R2,2(R1)
         B     TBPT
EBPT     CL    R3,=A(CHKBPTB-L'CHKBPTB,L'CHKBPTB,CHKBPTBE)
         BE    ZBPT
         LR    R5,R3
         L     R3,=A(CHKBPTB-L'CHKBPTB,L'CHKBPTB,CHKBPTBE)
         ALR   R3,R4
         STM   R3,R5,CHKBPTBS
ZBPT     L     R1,UJVTADDR
         USING UVTDSECT,R1
         L     R2,SYSIOS
         DROP  R1
         LM    R3,R5,=A(CHKIOTB-L'CHKIOTB,L'CHKIOTB,CHKIOTBE)
NIOT     CLC   0(2,R2),=XL4'0'
         BE    EIOT
         BXLE  R3,R4,*+L'*+4
         B     XOVIO
         MVC   0(2,R3),0(R2)
         LA    R2,2(R2)
         B     NIOT
EIOT     CL    R3,=A(CHKIOTB-L'CHKIOTB,L'CHKIOTB,CHKIOTBE)
         BE    ZIOT
         LR    R5,R3
         L     R3,=A(CHKIOTB-L'CHKIOTB,L'CHKIOTB,CHKIOTBE)
         ALR   R3,R4
         STM   R3,R5,CHKIOTBS
ZIOT     L     R1,UJVTADDR
         USING UVTDSECT,R1
         L     R2,SYSPST
         DROP  R1
         LM    R3,R5,=A(CHKSPTB-L'CHKSPTB,L'CHKSPTB,CHKSPTBE)
NSPT     CLC   0(2,R2),=XL4'0'
         BE    ESPT
         BXLE  R3,R4,*+L'*+4
         B     XOVSP
         MVC   0(4,R3),0(R2)
         LA    R2,5(R2)
         B     NSPT
ESPT     CL    R3,=A(CHKSPTB-L'CHKSPTB,L'CHKSPTB,CHKSPTBE)
         BE    ZSPT
         LR    R5,R3
         L     R3,=A(CHKSPTB-L'CHKSPTB,L'CHKSPTB,CHKSPTBE)
         ALR   R3,R4
         STM   R3,R5,CHKSPTBS
ZSPT     L     R15,=V(DUJVTA)
         BASR  R14,R15
         EJECT
*------- CHECK REQUESTS
CHKREQ   XR    R15,R15                 SET RC=0
         OC    TBOX,TBOX
         BNZ   CHKREQ30
         OC    TAUT,TAUT
         BZ    CHKREQ20
         OC    TPRG,TPRG
         BZ    CHKREQ10
         LM    R3,R5,CHKAPTBS          CHECK AUT.-PRG.
         LTR   R3,R3
         BZ    CHKREQ01                NONE
         CLC   0(4,R3),TAUT
         BE    REJCT
         BXLE  R3,R4,*-10
CHKREQ01 LM    R3,R5,CHKSPTBS
         LTR   R3,R3
         BZ    LEAVE                   NONE = ACCEPT
         CLC   0(4,R3),TAUT
         BE    REJCT
         BXLE  R3,R4,*-10
         B     LEAVE
CHKREQ10 LM    R3,R5,CHKAPTBS          CHECK AUT.
         LTR   R3,R3
         BZ    CHKREQ11                NONE
         CLC   0(2,R3),TAUT
         BE    REJCT
         BXLE  R3,R4,*-10
CHKREQ11 LM    R3,R5,CHKIOTBS
         LTR   R3,R3
         BZ    CHKREQ12                NONE
         CLC   0(2,R3),TAUT
         BE    REJCT
         BXLE  R3,R4,*-10
CHKREQ12 LM    R3,R5,CHKSPTBS
         LTR   R3,R3
         BZ    LEAVE                   NONE = ACCEPT
         CLC   0(2,R3),TAUT
         BE    REJCT
         BXLE  R3,R4,*-10
         B     LEAVE
CHKREQ20 OC    TPRG,TPRG
         BZ    INVREQ
         LM    R3,R5,CHKAPTBS          CHECK PRG.
         LTR   R3,R3
         BZ    CHKREQ21                NONE
         CLC   2(2,R3),TPRG
         BE    REJCT
         BXLE  R3,R4,*-10
CHKREQ21 LM    R3,R5,CHKSPTBS
         LTR   R3,R3
         BZ    LEAVE                   NONE = ACCEPT
         CLC   2(2,R3),TPRG
         BE    REJCT
         BXLE  R3,R4,*-10
         B     LEAVE
CHKREQ30 OC    TAUT,TAUT
         BNZ   INVREQ
         OC    TPRG,TPRG
         BZ    INVREQ
         LM    R3,R5,CHKBPTBS          CHECK BOX-PRG.
         LTR   R3,R3
         BZ    LEAVE                   NONE = ACCEPT
         MVC   TBOX+L'TBOX(L'TPRG),TPRG
         CLC   0(4,R3),TBOX
         BE    REJCT
         BXLE  R3,R4,*-10
         B     LEAVE
         EJECT
XOVAP    LA    R1,MSGOVAP
         B     XOVERR
XOVBP    LA    R1,MSGOVBP
         B     XOVERR
XOVIO    LA    R1,MSGOVIO
         B     XOVERR
XOVSP    LA    R1,MSGOVSP
XOVERR   L     R15,=V(DUJVTA)
         BASR  R14,R15
         B     EXMSG
INVREQ   LA    R1,MSGINRQ
         B     EXMSG
EXERR    LA    R1,MSGERR
EXMSG    L     R2,4(R13)
         ST    R1,24(R2)               PASS ADDRESS OF MESSAGE
         LA    R15,8                   SET RC=8
         B     LEAVE
REJCT    LA    R15,4                   SET RC=4
LEAVE   $XRET  CC=(R15)                RETURN
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
UJVTADDR DC    A(*-*)
CHKAPTBS DC    A(*-*,*-*,*-*)
CHKBPTBS DC    A(*-*,*-*,*-*)
CHKIOTBS DC    A(*-*,*-*,*-*)
CHKSPTBS DC    A(*-*,*-*,*-*)
TAUT     DC    H'0'          AUTHORIZATION NUMBER
TPRG     DC    H'0'          PROGRAMMER NUMBER
TBOX     DC    2H'0'         BOX NUMBER
         SPACE 1
*--------------------------------------------------------------------*
*        MESSAGES D'ERREURS                                          *
*--------------------------------------------------------------------*
         SPACE 1
         DC    AL1(L'MSGERR)
MSGERR   DC    C' Unable to locate UJVTABLE (IEFUJV module) -AUT33'
         DC    AL1(L'MSGOVAP)
MSGOVAP  DC    C' Aut.-Prg. internal table overflow (MXAP) - AUT33'
         DC    AL1(L'MSGOVBP)
MSGOVBP  DC    C' Box-Prg. internal table overflow (MXBP) - AUT33'
         DC    AL1(L'MSGOVIO)
MSGOVIO  DC    C' Aut. internal table overflow (MXIO) - AUT33'
         DC    AL1(L'MSGOVSP)
MSGOVSP  DC    C' Aut.-Prg. internal table overflow (MXSP) - AUT33'
         DC    AL1(L'MSGINRQ)
MSGINRQ  DC    C' Invalid check request (Aut/Prg/Box) - AUT33'
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         EJECT
*--------------------------------------------------------------------*
*        DUMMY SECTIONS                                              *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT GEN
        $UJVT
         EJECT
*--------------------------------------------------------------------*
*        INTERNAL TABLES                                             *
*--------------------------------------------------------------------*
         SPACE 1
CHKAPTB  DC    (MXAP-1)F'0'
CHKAPTBE DC    F'0'                    END OF TABLE
CHKBPTB  DC    (MXBP-1)F'0'
CHKBPTBE DC    F'0'                    END OF TABLE
CHKSPTB  DC    (MXSP-1)F'0'
CHKSPTBE DC    F'0'                    END OF TABLE
         SPACE 1
CHKIOTB  DC    (MXIO-1)H'0'
CHKIOTBE DC    H'0'                    END OF TABLE
         SPACE 2
         END
