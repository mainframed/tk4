B001     TITLE 'CLEAN-UP - GET OPTIONS REQUESTS (B001).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B001    @XS
*        READ OPTIONS REQUESTS.
         SPACE 1
         L     R1,=V(#RMSW#)
         MVI   0(R1),1
         OI    WHAT,WHIN
         OI    RUNIND,RUDSH+RUCSH  SET DEFAULT OPTIONS. + + + + + + + +
        $READ  MF=(E,RDIPLS)
         LTR   R15,R15             TEST RETURN CODE
         BZ    CR1
         BM    CR81                CONTROL CARDS MISSING
         CH    R15,=H'1'           DD-CARD?
         BE    CR81                CONTROL CARDS MISSING
         B     ERR21
CR1      XR    R6,R6
CR2      NI    CRSW1+1,OFF
         NI    CRSW3+1,OFF
         LM    R3,R5,SCIN          SET TO SCAN INPUT CARD
         CLI   0(R3),C' '          SEARCH START OF A KEYWORD
         BNE   CRSW1
CR3      BXLE  R3,R4,*-8
        $READ  MF=(E,RDIPLS)       GET NEXT CARD
         LTR   R15,R15
         BM    CR80                END OF CONTROL CARDS
         BZ    CR2                 ANALYZE NEXT CARD
         B     ERR21
CR4      XR    R6,R6
         B     CR3                 GET NEXT KEYWORD
CRSW1    NOP   CRSW2
         OI    CRSW1+1,ON
         CLC   0(6,R3),=CL6'EL.VOL'     TEST IF NEXT GROUP?
         BE    CR99                YES
         CLC   0(7,R3),=CL7'BYPRACF'    BYPRACF REQUEST?
         BE    CR99                YES
         CLC   0(7,R3),=CL7'ACCOUNT'    ACCOUNT REQUEST?
         BE    CR99                YES
         CLC   0(4,R3),=CL4'SKIP'  SKIP REQUEST?
         BNE   CRSW2               NO
         BE    CR99                YES
CRSW2    NOP   CRSW3
         OI    CRSW2+1,ON
        @OUT   INPUT,,U            PRINT HEADER
CRSW3    NOP   CRSW4
         OI    CRSW3+1,ON
        @OUT   PRCAR               PRINT INPUT CARD
CRSW4    B     C1
         OI    CRSW4+1,ON
         CLI   0(R3),C'('          START OF SUPPRESS EXEMPTION?
         BNE   CR20                NO
CR10     BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
         CLI   0(R3),C' '
         BE    CR10
         CLI   0(R3),C')'          END?
         BE    CR4                 YES
         LA    R15,DAYT
CR11     CLC   0(L'DAYT,R3),0(R15)
         BE    CR12
         LA    R15,L'DAYT(R15)
         CLI   0(R15),FMOFF
         BNE   CR11
         B     CRERR
CR12     CLC   0(L'DAYT,R3),TITDAT IS IT TODAY?
         BNE   *+L'*+4             NO
         EX    R6,CRCL
         LA    R3,L'DAYT-1(R3)
         B     CR10
CRCL     NI    RUNOPT,*-*          << EXECUTED >>
CR20     XR    R6,R6
C1       CLC   0(4,R3),=CL4'TEST'  LOOK IF TEST RUN REQUESTED?
         BNE   C2                  NO
         OI    RUNIND,RUTEST       YES, SET SWITCH
         LA    R3,3(R3)
         B     CR4                 GET NEXT KEYWORD
C2       CLC   0(7,R3),=CL7'LISTALL'    COMPLETE LIST REQUIRED?
         BNE   C3                  NO
         NI    RUNIND,255-RUNOLIST-RUDSH-RUCSH    YES, RESET ALL
         NI    RUNOPT,255-RUNLU-RUNLG-RUGDGL
         NI    RUSREQ,255-RUSKOFF-RUSVSAM
         OI    ACCIND,AFFULL
         LA    R3,6(R3)
         B     CR4                 GET NEXT KEYWORD
C3       CLC   0(8,R3),=CL8'DS-SHORT'   DSNAME'S SHORT LIST REQUIRED?
         BNE   C4                  NO
         OI    RUNIND,RUDSH        YES, SET INDICATOR
         NI    RUNIND,255-RUNOLIST ALLOW LIST
         NI    RUNOPT,255-RUGDGL
         NI    RUSREQ,255-RUSVSAM
         LA    R3,7(R3)
         B     CR4                 GET NEXT KEYWORD
C4       CLC   0(8,R3),=CL8'CT-SHORT'   CATALOGS SHORT LIST REQUIRED?
         BNE   C5                  NO
         OI    RUNIND,RUCSH        YES, SET INDICATOR
         NI    RUNIND,255-RUNOLIST ALLOW LIST
         LA    R3,7(R3)
         B     CR4                 GET NEXT KEYWORD
C5       CLC   0(6,R3),=CL6'NOLIST'     LIST SUPPRESS REQUIRED?
         BNE   C6                  NO
         OI    RUNIND,RUNOLIST     YES, SET INDICATOR
         NI    RUNIND,255-RUDSH-RUCSH   RESET
         NI    RUNOPT,255-RUNLU-RUNLG-RUGDGL
         NI    RUSREQ,255-RUSKOFF-RUSVSAM
         NI    ACCIND,255-AFFULL
         NI    STATUS,255-STCOPY
         LA    R3,5(R3)
         B     CR4                 GET NEXT KEYWORD
C6       CLC   0(9,R3),=CL9'NOSCRATCH'  NOSCRATCH REQUESTED?
         BNE   C7                  NO
         OI    RUNIND,RUNOSCR      YES, SET INDICATOR
         LA    R3,8(R3)
         B     CR4                 GET NEXT KEYWORD
C7       CLC   0(9,R3),=CL9'NOCATALOG'  CATALOG ACTION TO BYPASS?
         BNE   C8                  NO
         OI    RUNIND,RUNOCAT      SET INDICATOR
         LA    R3,8(R3)
         B     CR4                 GET NEXT KEYWORD
C8       CLC   0(6,R3),=CL6'NORACF'     BYPASS RACF ACTION?
         BNE   C9                  NO
         OI    RUNIND,RUNORACF     YES, SET INDICATOR
         LA    R3,5(R3)
         B     CR4                 GET NEXT KEYWORD
C9       CLC   0(4,R3),=CL4'SNAP'  SNAP REQUEST?
         BNE   C10                 NO
         LA    R3,3(R3)     YES, SEARCH AT WHICH POINT
         BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
C9A      CLI   0(R3),C' '
         BNE   C9B
         BXLE  R3,R4,C9A
         BAS   R14,CRCNT
         B     C9A
C9B      CLI   0(R3),C'('
         BNE   CRERR
C9C      BXLE  R3,R4,C9D
         BAS   R14,CRCNT
C9D      CLI   0(R3),C')'
         BE    CR4                 GET NEXT KEYWORD
         CLI   0(R3),C' '
         BE    C9C
         CLC   0(3,R3),=CL3'TWA'   AT 'TWA' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFTWA         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'RRD'   AT 'RRD' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFRRD         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'LOC'   AT 'LOC' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFLOC         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'OBT'   AT 'OBT' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFOBT         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'RUS'   AT 'RUS' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFRUS         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'RDS'   AT 'RDS' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFRDS         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'RGR'   AT 'RGR' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFRGR         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'RCO'   AT 'RCO' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSF,SFRCO         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'GDS'   AT 'GDS' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSN,SNGDS         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'RRO'   AT 'RRO' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSN,SNRRO         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'RAE'   AT 'RAE' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSN,SNRAE         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'GCV'   AT 'GCV' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSN,SNGCV         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'ECV'   AT 'ECV' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSN,SNECV         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'SCR'   AT 'SCR' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSN,SNSCR         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'INX'   AT 'INX' POINT?
         BNE   *+L'*+8             NO
         OI    RUNSN,SNINX         YES, SET INDICATOR
         B     C9XIT
         CLC   0(3,R3),=CL3'DBG'   AT 'INX' POINT?
         BNE   CRERR               NO
         OI    RUNSN,SNDBG         YES, SET INDICATOR
C9XIT    LA    R3,2(R3)
         B     C9C
C10      CLC   0(5,R3),=CL5'NOSET' BYPASS VTOC'S ACTION (RACF SET)?
         BNE   C11                 NO
         OI    RUNOPT,RUNOSET      YES, SET INDICATOR
         LA    R3,4(R3)
         B     CR4                 GET NEXT KEYWORD
C11      CLC   0(4,R3),=CL4'TIME'  TIME STAMP REQUEST?
         BNE   C12                 NO
         OI    RUNOPT,RUTMT        YES, SET INDICATOR
         LA    R3,3(R3)
         B     CR4                 GET NEXT KEYWORD
C12      CLC   0(8,R3),=CL8'NL-USERS'   SUPPRESS USERS LIST REQUIRED?
         BNE   C13                 NO
         OI    RUNOPT,RUNLU        YES, SET INDICATOR
         NI    RUNIND,255-RUNOLIST ALLOW LIST
         LA    R3,7(R3)
         LA    R6,255-RUNLU
         NI    CRSW4+1,OFF
         B     CR3
C13      CLC   0(9,R3),=CL9'NL-GROUPS'  SUPPRESS GROUPS LIST REQUIRED?
         BNE   C14                 NO
         OI    RUNOPT,RUNLG        YES, SET INDICATOR
         NI    RUNIND,255-RUNOLIST ALLOW LIST
         LA    R3,8(R3)
         LA    R6,255-RUNLG
         NI    CRSW4+1,OFF
         B     CR3
C14      CLC   0(9,R3),=CL9'NOACCOUNT'  NO ACCOUNT REQUEST?
         BNE   C15                 NO
         OI    RUNOPT,RUNOACC      YES, SET INDICATOR
         LA    R3,8(R3)
         B     CR4                 GET NEXT KEYWORD
C15      CLC   0(8,R3),=CL8'DS-SHGDG'   SHORT WITH GDG'S LIST REQUEST?
         BNE   C16                 NO
         OI    RUNOPT,RUGDGL       YES, SET INDICATOR
         OI    RUNIND,RUCSH
         NI    RUNIND,255-RUNOLIST ALLOW LIST
         NI    RUSREQ,255-RUSVSAM
         LA    R3,7(R3)
         B     CR4                 GET NEXT KEYWORD
C16      CLC   0(5,R3),=CL5'CHECK' CHECK RUN REQUEST?
         BNE   C17                 NO
         OI    WHAT,WHCHECK        YES, SET INDICATOR
         LA    R3,4(R3)
         B     CR4                 GET NEXT KEYWORD
C17      CLC   0(4,R3),=CL4'COPY'  COPY REQUEST?
         BNE   C18                 NO
         CLC   4(3,R3),=CL3'VOL'   YES, COPYVOL INFORMATION?
         BE    C17V                YES
         OI    STATUS,STCOPY       NO, SET INDICATOR
         LA    R3,3(R3)
C17A     BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
         CLI   0(R3),C' '          SEARCH OPERAND
         BE    C17A
         CLI   0(R3),C'('
         BNE   ERR61
C17B     BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
         CLI   0(R3),C' '          SEARCH START OF DSNAME
         BE    C17B
         LR    R1,R3
C17C     CLI   0(R3),C')'
         BE    C17D
         CLI   0(R3),C'/'
         BE    C17D
         BXLE  R3,R4,*+L'*+4
         B     C17D
         CLI   0(R3),C' '
         BNE   C17C
C17D     LR    R14,R3              GET THE DSNAME
         SR    R14,R1
         BNP   ERR61
         CH    R14,=AL2(L'COPYDS)
         BH    ERR61
         BCTR  R14,0
         MVI   COPYDS,C' '
         MVC   COPYDS+1(L'COPYDS-1),COPYDS
         EX    R14,C17MV
         CLR   R3,R5
         BH    C17F
         B     C17G
C17MV    MVC   COPYDS(*-*),0(R1)   << EXECUTED >>
C17E     BXLE  R3,R4,*+L'*+4
C17F     BAS   R14,CRCNT
C17G     CLI   0(R3),C' '
         BE    C17E
         CLI   0(R3),C')'
         BE    CR4                 GET NEXT KEYWORD
         CLI   0(R3),C'/'
         BNE   CRERR
C17H     BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
         CLI   0(R3),C' '
         BE    C17H
         CLI   0(R3),C')'
         BE    CR4                 GET NEXT KEYWORD
         LR    R1,R3               START OF COMMAND
C17I     BXLE  R3,R4,*+L'*+4
         B     C17J
         CLI   0(R3),C')'
         BNE   C17I
C17J     BCTR  R3,0                STRIP LEADING BLANKS
         CLR   R1,R3
         BNH   C17K
         CLI   0(R3),C' '
         BE    C17J
C17K     LR    R14,R3              GET THE COMMAND
         SR    R14,R1
         BM    ERR62
         CH    R14,=AL2(L'CPYCMDT-1)
         BH    ERR62
         MVI   CPYCMD+1,0
         MVI   CPYCMDT,C' '
         MVC   CPYCMDT+1(L'CPYCMDT-1),CPYCMDT
         EX    R14,C17MC
         LA    R14,5(R14)
         STC   R14,CPYCMD+1
C17L     BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
         CLI   0(R3),C')'
         BE    CR4                 GET NEXT KEYWORD
         CLI   0(R3),C' '
         BE    C17L
         B     CRERR
C17MC    MVC   CPYCMDT(*-*),0(R1)  << EXECUTED >>
C17V     LA    R3,6(R3)
C17VA    BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
         CLI   0(R3),C' '          SEARCH OPERAND
         BE    C17VA
         CLI   0(R3),C'('
         BNE   ERR78
C17VB    BXLE  R3,R4,*+L'*+4
         BAS   R14,CRCNT
         CLI   0(R3),C' '          SEARCH START OF VOLUME
         BE    C17VB
         LR    R1,R3
C17VC    CLI   0(R3),C')'
         BE    C17VD
         BXLE  R3,R4,*+L'*+4
         B     C17VD
         CLI   0(R3),C' '
         BNE   C17VC
C17VD    LR    R14,R3              GET THE VOLUME
         SR    R14,R1
         BNP   ERR78
         CH    R14,=H'6'
         BH    ERR78
         BCTR  R14,0
         MVC   CPYVOL,=CL6' '
         EX    R14,C17VMV
         CLR   R3,R5
         BH    C17VF+L'C17VF
C17VE    CLI   0(R3),C' '
         BE    C17VF
         CLI   0(R3),C')'
         BE    CR4                 GET NEXT KEYWORD
         B     ERR78
C17VF    BXLE  R3,R4,C17VE
         BAS   R14,CRCNT
         B     C17VE
C17VMV   MVC   CPYVOL(*-*),0(R1)   << EXECUTED >>
C18      CLC   0(4,R3),=CL4'ONLY'  ONLY REQUEST?
         BNE   C19                 NO
         OI    STATUS,STONLY       YES, SET INDICATOR
         LA    R3,3(R3)
         B     CR4                 GET NEXT KEYWORD
C19      CLC   0(8,R3),=CL8'DS-SKOFF'   SUPPRESS UNPROCESSED VOLUMES?
         BNE   C20                 NO
         OI    RUSREQ,RUSKOFF      YES, SET INDICATOR
         NI    RUNIND,255-RUNOLIST ALLOW LIST
         LA    R3,7(R3)
         B     CR4                 GET NEXT KEYWORD
C20      CLC   0(7,R3),=CL7'DS-VSAM'    SHORT GDG'S+VSAM LIST REQUEST?
         BNE   C30                 NO
         OI    RUNOPT,RUGDGL       YES, SET INDICATOR
         OI    RUNIND,RUCSH
         NI    RUNIND,255-RUNOLIST ALLOW LIST
         OI    RUSREQ,RUSVSAM
         LA    R3,6(R3)
         B     CR4                 GET NEXT KEYWORD
C30      CLC   0(3,R3),=CL3'NEV'   NEV DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNEV
         B     CXIT
         CLC   0(3,R3),=CL3'NCV'   NCV DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNCV
         B     CXIT
         CLC   0(3,R3),=CL3'NNC'   NNC DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNNC
         B     CXIT
         CLC   0(3,R3),=CL3'NGV'   NGV DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNGV
         B     CXIT
         CLC   0(3,R3),=CL3'NDV'   NDV DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNDV
         B     CXIT
         CLC   0(3,R3),=CL3'NDD'   NDD DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNDD
         B     CXIT
         CLC   0(3,R3),=CL3'NIX'   NIX DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNIX
         B     CXIT
         CLC   0(3,R3),=CL3'NRE'   NRE DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNRE
         B     CXIT
         CLC   0(3,R3),=CL3'NSK'   NSK DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNSK
         B     CXIT
         CLC   0(3,R3),=CL3'NGD'   NGD DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNGD
         B     CXIT
         CLC   0(3,R3),=CL3'NVR'   NVR DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNVR
         B     CXIT
         CLC   0(3,R3),=CL3'NW1'   NW1 DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNW1
         B     CXIT
         CLC   0(3,R3),=CL3'NW2'   NW2 DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNW2
         B     CXIT
         CLC   0(3,R3),=CL3'NBR'   NBR DEFINITION?
         BNE   *+L'*+8             NO
         LA    R6,CRNBR
         B     CXIT
         CLC   0(3,R3),=CL3'NWI'   NWI DEFINITION?
         BNE   C90                 NO
         LA    R6,CRNWI
CXIT     LA    R3,2(R3)
         B     CR3                 GET NUMBER
C90      CLI   0(R3),C'0'          NUMBER?
         BL    CRERR               NO
         LTR   R6,R6
         BZ    CRERR
         BAS   R14,GNM             GET THE NUMBER
         BR    R6
CRNEV    LA    R15,TBEVOLL
         BAS   R14,CRSPC           COMPUTE NEW EL.VOL. SPACE
         L     R0,TOTSPCE
         S     R0,TBEVOLT
         ST    R1,TBEVOLT
         S     R0,TBAVOLT          UPDATE ALSO ACC.VOL. SPACE
         ST    R1,TBAVOLT
         AR    R0,R1
         B     CRXIT
CRNCV    LA    R15,CVLL
         BAS   R14,CRSPC           COMPUTE NEW CAT.VOL. SPACE
         L     R0,TOTSPCE
         S     R0,TBCVOLT
         ST    R1,TBCVOLT
         B     CRXIT
CRNNC    LA    R15,TBNVOLL
         BAS   R14,CRSPC           COMPUTE NEW N-CAT.VOL. SPACE
         L     R0,TOTSPCE
         S     R0,TBNVOLT
         ST    R1,TBNVOLT
         B     CRXIT
CRNGV    LA    R15,CVPL
         BAS   R14,CRSPC           COMPUTE NEW CVOL PTR. SPACE
         L     R0,TOTSPCE
         S     R0,TBGVOLT
         ST    R1,TBGVOLT
         B     CRXIT
CRNDV    LA    R15,TBDVOLL
         BAS   R14,CRSPC           COMPUTE NEW NOT-D. VOL. SPACE
         L     R0,TOTSPCE
         S     R0,TBDVOLT
         ST    R1,TBDVOLT
         B     CRXIT
CRNDD    LA    R15,DDNL
         BAS   R14,CRSPC           COMPUTE NEW DDNAME'S SPACE
         L     R0,TOTSPCE
         S     R0,TBDDNT
         ST    R1,TBDDNT
         B     CRXIT
CRNIX    LA    R15,NIXL
         BAS   R14,CRSPC           COMPUTE NEW NULL INDEXES SPACE
         L     R0,TOTSPCE
         S     R0,TBNIXT
         ST    R1,TBNIXT
         B     CRXIT
CRNRE    ST    R1,DSTABOV
         ST    R1,DSTABCT
         LA    R15,DSTABL
         BAS   R14,CRSPC           COMPUTE NEW DSNAME'S SPACE
         L     R0,TOTSPCE
         S     R0,DSTABT
         ST    R1,DSTABT
         B     CRXIT
CRNSK    LA    R15,8
         BAS   R14,CRSPC           COMPUTE NEW SKIP NAMES SPACE
         L     R0,TOTSPCE
         S     R0,TBSKPT
         ST    R1,TBSKPT
         B     CRXIT
CRNGD    LA    R15,12
         BAS   R14,CRSPC           COMPUTE NEW HLQ NAMES SPACE
         L     R0,TOTSPCE
         S     R0,TBRGDST
         ST    R1,TBRGDST
         B     CRXIT
CRNVR    LA    R15,1+L'DSNME
         BAS   R14,CRSPC           COMPUTE NEW GENERIC NAMES SPACE
         L     R0,TOTSPCE
         S     R0,TBVRGNT
         ST    R1,TBVRGNT
         B     CRXIT
CRNW1    LA    R15,1
         BAS   R14,CRSPC           COMPUTE NEW 1ST RACF WA SPACE
         L     R0,TOTSPCE
         S     R0,WK1T
         ST    R1,WK1T
         B     CRXIT
CRNW2    LA    R15,1
         BAS   R14,CRSPC           COMPUTE NEW 2ND RACF WA SPACE
         L     R0,TOTSPCE
         S     R0,WK2T
         ST    R1,WK2T
         B     CRXIT
CRNBR    LA    R15,NBRL
         BAS   R14,CRSPC           COMPUTE NEW BYPASS RACF SPACE
         L     R0,TOTSPCE
         S     R0,TBBYRT
         ST    R1,TBBYRT
CRXIT    AR    R0,R1
         ST    R0,TOTSPCE
         B     CR4
CRNWI    LA    R15,80              COMPUTE NEW IDCAMS ARRAY SPACE
         BCTR  R1,0
         XR    R0,R0
         MR    R0,R15
         LTR   R0,R0
         BNZ   ERR46
         SLDL  R0,8
         LTR   R0,R0
         BNZ   ERR46
         SRL   R1,8
         AH    R1,=H'8'
         ST    R1,LGIDST
         B     CR4
         SPACE 1
GNM      LR    R1,R3               GET NUMBER ROUTINE
         LA    R0,6                5 DIGITS MAX.
GNM1     CLI   0(R3),C'0'
         BL    GNM3
         CLI   0(R3),C'9'
         BH    CRERR
         BCT   R0,GNM2
         B     CRERR
GNM2     BXLE  R3,R4,GNM1
GNM3     LR    R15,R3
         SR    R15,R1
         BNP   CRERR
         BCTR  R15,0
         EX    R15,GNMPK
         CVB   R1,WORKD
         AH    R1,=H'1'            R1 = NUMBER + 1
         BR    R14                 RETURN
GNMPK    PACK  WORKD,0(*-*,R1)     << EXECUTED >>
         SPACE 1
CRSPC    XR    R0,R0               COMPUTE SPACE ROUTINE
         MR    R0,R15              R1 = NUMBER + 1, R15 = LENGTH
         LTR   R0,R0
         BNZ   ERR46
         SLDL  R0,8
         LTR   R0,R0
         BNZ   ERR46
         SRL   R1,8
         AH    R1,=H'7'
         SRL   R1,3
         SLL   R1,3                R1 = SPACE UP ROUNDED
         BR    R14                 RETURN
         SPACE 1
CRCNT    ST    R14,SVRETC          CONTINUATION
        $READ  MF=(E,RDIPLS)       READ NEXT CARD
         LTR   R15,R15             TEST RETURN CODE
         BM    CNTERR
         BNZ   ERR21
        @OUT   PRCAR               PRINT INPUT CARD
         LM    R3,R5,SCIN          SET TO SCAN INPUT CARD
         L     R14,SVRETC
         BR    R14                 GO BACK
CNTERR   LA    R1,CNTERRM
         B     CRNERR
CRERR    MVI   CARDIN,C' '
         MVC   CARDIN+1(L'CARDIN-1),CARDIN
         MVI   0(R3),C'$'          POSITION OF ERROR
        @OUT   PRCAR
         LA    R1,STERR            STATEMENT ERROR
CRNERR  @OUT   (R1)
         XR    R15,R15
         B     ERR22
CR80    $RCLOSE
CR81    @OUT   NOINPUT
         NI    WHAT,255-WHIN
         B     ERR21
CR99    @XE
         EJECT
SVRETC   DC    F'0'
         SPACE 1
         DC    AL1(L'NOINPUT)
NOINPUT  DC    C'0===== INPUT CONTROL MISSING ====='
         DC    AL1(L'INPUT)
INPUT    DC    C'0OPTIONS REQUESTS :'
         DC    AL1(L'STERR)
STERR    DC    C' ===== SYNTAX ERROR ====='
         DC    AL1(L'CNTERRM)
CNTERRM  DC    C' ===== CONTINUATION MISSING ====='
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
