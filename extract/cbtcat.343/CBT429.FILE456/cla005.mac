A005     TITLE 'CLEAN-UP - READ VTOC''S CONTENTS (A005).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A005    @XS    ALIAS=RVTOCS
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    CHR99               YES
        $VTOCQR VTRDBUF,ADDREC,MF=(L,RDVPLS)
         SPACE 1
*        READ (QUICK) VOLUMES VTOC'S CONTENTS.
         SPACE 1
         L     R2,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R2
         L     R3,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R3          ESTABLISH ADDRESSABILITY
         L     R5,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
OPV0     TM    RUNOPT,RUNOACC      ACCOUNTING PROCESSING SUPPRESS?
         BO    OPV3                YES
         XR    R15,R15
         L     R14,TBAVOL          ACC. VOL. TABLE ADDRESS
OPV1     CLI   0(R14),FMON         END OF TABLE?
         BE    OPV3                YES
         IC    R15,0(R14)
         EX    R15,OPVCL           VOLUME MATCH?
         BE    OPV2                YES
         LA    R14,2(R14,R15)      NEXT ENTRY IF ANY
         B     OPV1
OPVCL    CLC   DDNVOL(*-*),1(R14)  << EXECUTED >>
OPV2     NI    RDVSW+1,OFF
         LA    R14,DVTYP
         B     OPV4
OPV3     XR    R14,R14
OPV4     MVC   VSNERR,DDNVOL
        $VTOCQO DDNNME,(R14),MF=(E,OPVPLS)   OPEN VTOC
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR15               ERROR
         OI    DDNALL,DDNXQ        SET OPENED
         NI    RDVSWF4+1,OFF
         XC    IXDSCB(INFLGTH),IXDSCB
RDVSW    B     RDV1
         XR    R4,R4
        @X     B003                ACCOUNTING PROCESS (START)
RDV1     LA    R4,VTRDBUF          ADRRESS VTOC READ BUFFER
         USING DSCDSECT,R4         ESTABLISH ADDRESSABILITY
RDV2    $VTOCQR MF=(E,RDVPLS)      READ RECORD
         LTR   R15,R15             TEST RETURN CODE
         BM    RDV10               LAST RECORD
         BZ    RDV3
         LTR   R0,R0               ERROR, IS IT AN IX VTOC?
         BZ    ERR16               NO
         STM   R15,R0,WORKD        YES, DISPLAY R0 (ERROR INFO)
         UNPK  SUPERRM(9),WORKD+4(5)
         TR    SUPERRM,TRTAB-X'F0'
         MVI   SUPERRM+L'SUPERRM,C'.'
        WTO    MF=(E,SUPERR)
         LM    R15,R0,WORKD
         B     ERR16
RDV3     TM    RDVSW+1,ON
         BO    RDV5
         CLI   DS1FMTID,C'1'       IS THIS A FORMAT 1?
         BE    RDV4                YES
         CLI   DS4IDFMT,C'4'       IS THIS THE FORMAT 4?
         BNE   RDV5                NO, GO FORWARD (NO INTEREST)
RDV4     TM    RUNOPT,RUABORT      ACCOUNTING PROCESSING ABORTED?
         BO    RDV5                YES
        @X     B003                ACCOUNTING PROCESS (READ)
         TM    RUNOPT,RUABORT      ABORT ACCOUNTING PROCESSING?
         BZ    RDV5                NO
         OI    RUNOPT,RUNOACC      YES, SAY SUPPRESSED
RDV5     CLI   DS1FMTID,C'1'       IS THIS A FORMAT 1?
         BE    RDV6                YES
         CLI   DS4IDFMT,C'4'       IS THIS A FORMAT 4?
         BE    RDVSWF4             YES
         CLI   DS2FMTID,C'2'       IS THIS A FORMAT 2?
         BE    RDV2                YES, GET NEXT ONE (NO INTEREST)
         CLI   DS3FMTID,C'3'       IS THIS A FORMAT 3?
         BE    RDV2                YES, GET NEXT ONE (NO INTEREST)
         CLI   DS5FMTID,C'5'       IS THIS A FORMAT 5?
         BE    RDV2                YES, GET NEXT ONE (NO INTEREST)
         CLI   DS6FMTID,C'6'       IS THIS A FORMAT 6?
         BE    RDV2                YES, GET NEXT ONE (NO INTEREST)
         LH    R1,FDSCB            UPDATE FREE DSCB'S COUNT
         AH    R1,=H'1'
         STH   R1,FDSCB
         B     RDV2                GET NEXT ONE
RDVSWF4  NOP   ERR74               MORE THAN ONE FMT 4 ...
         OI    RDVSWF4+1,ON
         MVC   VTIND(L'VTIND),DS4VTOCI  GET VTOC INDICATORS
         MVC   VFDSCB,DS4DSREC     GET VTOC FREE DSCB'S
         B     RDV2                GET NEXT ONE
RDV6     BCT   R5,*+L'*+6          CONTROL
         XR    R15,R15             DSNAME'S TABLE OVERFLOW
         B     ERR17
         XC    0(DSTABL,R3),0(R3)  CLEAR
         MVC   DSNME,DS1DSNAM      GET DSNAME
         MVC   DSCBCC,ADDREC       SET ADDRESS OF DSCB1
         MVC   DSDDN,DDNNME        SET DDNAME + VOLSER
         MVC   DSUCBTY,DDNDTY      SET UCB TYPE
         CLI   DS1EXT1,0           IS MODEL GDG DSCB?
         BNE   SPP1                NO
         MVI   DSUSE,XUM           INDICATE MODEL GDG
         B     SPP2
         SPACE 1
*        SPECIAL PROCESS FOR SYSCTLG DATA-SETS (O.S. CATALOGS),
*        THIS BECAUSE RACF USES THE NAME 'SYSCTLG.V------',
*        WHERE V------ IS THE VOLUME SERIAL NUMBER.
         SPACE 1
SPP1     CLC   DSNME(8),=CL8'SYSCTLG '  O.S. CATALOG DATA-SET?
         BNE   SPP2                NO
         MVC   DSNME+7(2),=C'.V'   CONSTRUCT NAME FOR RACF
         MVC   DSNME+9(6),DSVOLSN
         MVI   DSCAT,XCSL          SET AS L-CAT CATALOGED
         OI    DDNALL,DDNCT        INDICATE O.S. CATALOG PRESENT
         BAS   R14,SPP10           SCAN CAT. VOL. TABLE
SPP2     TM    DS1DSIND,DS1IND40   RACF PROTECTED?
         BZ    *+L'*+4             NO
         MVI   DSPROT,XPR          INDICATE RACF PROTECTED
         TM    DS1DSORG+1,DS1ACBM  VSAM FILE?
         BZ    SPP4                NO
         TM    DS1DSORG,DS1DSGIS+DS1DSGPS+DS1DSGDA+DS1DSGPO
         BZ    SPP3                OK, NO OTHER ORG. BITS
         MVI   DSACTION,YM         WHAT'S THAT - SIGNAL CONTROL
         MVC   VTOEVOL,DSVOLSN
         MVC   VTOEDSN,DS1DSNAM
        @OUT   VTOERR
         B     SPP4
SPP3     MVI   DSTYPE,XTV          INDICATE VSAM DATA-SET
         TM    DS1OPTCD,DS1OPTIC+DS1OPTBC    ICF CATALOG ?
         BNO   SPP4                NO
         CLC   DSNME(9),=CL9'CATINDEX.' IS IT THE INDEX?
         BE    SPP4                YES
         OI    DDNALL,DDNCI        INDICATE ICF CATALOG PRESENT
         BAS   R14,SPP10           SCAN CAT. VOL. TABLE
SPP4     LA    R3,DSTABL(R3)       ADVANCE IN TABLE
         MVI   0(R3),FMOFF         SET FINAL MARKER
         B     RDV2                CONTINUE
         DROP  R4
SPP10    L     R15,TBCVOL          START OF CAT. VOL. TABLE
SPP11    CLI   0(R15),FMOFF        END OF TABLE?
         BER   R14                 YES
         USING CVLT,R15
         TM    CVLIND,CVLCTP       CATALOG ALREADY PRESENT?
         BO    SPP12               YES
         CLC   CVLVOL,DDNVOL       THIS VOLUME?
         BNE   SPP12               NO
         CLC   CVLNME,DSNME        YES, THIS DATA-SET NAME?
         BNE   SPP12               NO
         OI    CVLIND,CVLCTP       INDICATE CATALOG PRESENT
         BR    R14
         DROP  R15
SPP12    LA    R15,CVLL(R15)       ADVANCE IN CAT. VOL. TABLE
         B     SPP11
         DROP  R3
RDV10    LTR   R1,R0               INDEXED VTOC INFO'S?
         BZ    *+L'*+6             NO, NORMAL VTOC
         MVC   IXDSCB(L'IXDSCB+L'IXVIRS),0(R1)    GET IX FREE INFO'S
         TM    RDVSW+1,ON
         BO    RDV11
         OI    RDVSW+1,ON
         XR    R4,R4
         BCTR  R4,0
        @X     B003                ACCOUNTING PROCESS (END)
RDV11    NI    DDNALL,255-DDNXQ    SET CLOSED
        $VTOCQC
         LTR   R15,R15             TEST RETURN CODE
         BNZ   ERR18               ERROR
         MVC   VSNERR,=CL6' '
         TM    RUNIND,RUNOLIST
         BO    RDV22
RDVSWIL  NOP   RDV20
         OI    RDVSWIL+1,ON
        @LIST  VTIL,=PL2'+48',U
RDV20    MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LINE+7(L'DDNVOL),DDNVOL
         MVC   LINE+L'DDNVOL+9(21),=CL21': FREE DSCB''S COUNT ='
         MVC   LINE+L'DDNVOL+30(6),=XL6'402020202120'
         OC    IXDSCB,IXDSCB       INDEXED VTOC INFO'S?
         BNZ   *+L'*+8             YES
         LH    R1,VFDSCB
         B     RDV21
         MVC   LINE+L'DDNVOL+37(19),=CL19'- FREE VIRS COUNT ='
         MVC   LINE+L'DDNVOL+56(6),=XL6'402020202120'
         L     R1,IXVIRS
         CVD   R1,WORKD
         ED    LINE+L'DDNVOL+56(6),WORKD+5
         L     R1,IXDSCB
RDV21    CVD   R1,WORKD
         ED    LINE+L'DDNVOL+30(6),WORKD+5
        @LIST  LINE
RDV22    MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LINE+7(L'DDNVOL),DDNVOL
         MVI   LINE+L'DDNVOL+9,C'-'
         TM    VTIND,DS4DIRF       DIRF BIT ON?
         BZ    RDV24               NO
RDVSWVS  NOP   RDV23
         OI    RDVSWVS+1,ON
        @OUT   VTST,=PL2'+48',U
RDV23    MVC   LINE+L'DDNVOL+11(L'VTDIRF),VTDIRF
        @OUT   LINE
         MVC   LINE+1(L'LINE-1),LINE
RDV24    CLC   FDSCB,VFDSCB
         BNE   RDV25
         OC    IXDSCB,IXDSCB       INDEXED VTOC INFO'S?
         BZ    RDV27               NO
         CLC   FDSCB(L'FDSCB),IXDSCB+L'IXDSCB-L'FDSCB
         BE    RDV27
         MVC   LINE+L'DDNVOL+44(5),=CL5'V-IX='
         UNPK  LINE+L'DDNVOL+49(5),IXDSCB+2(3)
         TR    LINE+L'DDNVOL+49(4),TRTAB-X'F0'
         MVI   LINE+L'DDNVOL+53,C' '
RDV25    MVC   LINE+L'DDNVOL+11(18),=CL18'FREE DSCBS : READ='
         UNPK  LINE+L'DDNVOL+29(5),FDSCB(3)
         TR    LINE+L'DDNVOL+29(4),TRTAB-X'F0'
         MVC   LINE+L'DDNVOL+33(6),=CL6' FMT4='
         UNPK  LINE+L'DDNVOL+39(5),VFDSCB(3)
         TR    LINE+L'DDNVOL+39(4),TRTAB-X'F0'
         MVI   LINE+L'DDNVOL+43,C' '
         TM    RDVSWVS+1,ON
         BO    RDV26
         OI    RDVSWVS+1,ON
        @OUT   VTST,=PL2'+45',U
RDV26   @OUT   LINE
         MVC   LINE+1(L'LINE-1),LINE
RDV27    LA    R2,DDNL(R2)         ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BNE   OPV0                NO, GO TO OPEN NEXT ONE
         DROP  R2
         TM    RDVSWVS+1,ON
         BZ    RDV30
        @OUT   VTSTN
RDV30    ST    R5,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
         L     R1,DSTABOV          COMPUTE NO. OF ELEMENTS
         SR    R1,R5
         ST    R1,DSTABNE
         TM    RUNOPT,RUNOACC      ACCOUNTING PROCESSING SUPPRESS?
         BO    CTG0                YES
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    RDV90               NO, SKIP THE LIST
         L     R1,COUNTER
         CVD   R1,WORKD
         ED    ACCENDN,WORKD+5
        @LIST  ACCEND
RDV90    TM    WHAT,WHDATA         ANY PUNCH DONE?
         BZ    CTG0                NO
         NI    WHAT,255-WHDATA     SET CLOSED
        $UCLOSE MF=(E,UDDPLS)
         MVC   LINE(L'ACCUM),ACCUM
         MVC   LINE+L'ACCUM(L'LINE-L'ACCUM),LINE+L'ACCUM-1
         MVC   LINE+L'ACCUM(L'DSNDATA),DSNDATA
        @OUT   LINE
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         SPACE 1
*        CHECK IF ANY NOT-DECLARED CATALOGS ARE PRESENT.
         SPACE 1
CTG0     L     R2,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R2
CTG1     TM    DDNALL,DDNCT+DDNCI  ANY CATALOG PRESENT?
         BZ    CTG12               NO, CONTINUE
         TM    DDNALL,DDNCT        AN O.S. CATALOG IS PRESENT?
         BZ    CTG3                NO
         OI    CTGSW2+1,ON
         L     R15,TBCVOL          START OF CAT. VOL. TABLE
CTG2     CLI   0(R15),FMOFF        END OF TABLE?
         BE    CTGSW1              YES
         USING CVLT,R15
         TM    CVLIND,CVLICF       IS IT AN O.S. CATALOG?
         BO    *+L'*+10            NO
         CLC   CVLVOL,DDNVOL       THIS VOLUME?
         BE    CTG3                YES
         DROP  R15
         LA    R15,CVLL(R15)       ADVANCE IN CAT. VOL. TABLE
         B     CTG2
CTG3     TM    DDNALL,DDNCI        AN ICF CATALOG IS PRESENT?
         BZ    CTG12               NO
         NI    CTGSW2+1,OFF
         L     R15,TBCVOL          START OF CAT. VOL. TABLE
CTG4     CLI   0(R15),FMOFF        END OF TABLE?
         BE    CTGSW1              YES
         USING CVLT,R15
         TM    CVLIND,CVLICF       IS IT AN ICF CATALOG?
         BZ    *+L'*+10            NO
         CLC   CVLVOL,DDNVOL       THIS VOLUME?
         BE    CTG12               YES
         DROP  R15
         LA    R15,CVLL(R15)       ADVANCE IN CAT. VOL. TABLE
         B     CTG4
CTGSW1   NOP   CTG10
        @OUT   CTLGMIS1,,U
         OI    CTGSW1+1,ON
         LA    R4,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R0,10               MAX. BY LINE
CTG10    MVC   0(L'DDNVOL,R4),DDNVOL    MISSING VOLUME
         TM    CTGSW2+1,ON
         BZ    *+L'*+10
         MVC   L'DDNVOL(3,R4),=CL3'(O)'
         B     *+L'*+6
         MVC   L'DDNVOL(3,R4),=CL3'(I)'
         BCT   R0,CTG11
        @OUT   LINE
         LA    R4,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R0,10               MAX. BY LINE
         B     CTGSW2
CTG11    LA    R4,L'DDNVOL+4(R4)   ADVANCE IN LINE
CTGSW2   B     CTG3
CTG12    LA    R2,DDNL(R2)         ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BNE   CTG1                NO, GO TO CHECK NEXT ONE
         DROP  R2
         TM    CTGSW1+1,ON
         BZ    CHR0
         CLI   LINE+6,C' '         ANYTHING IN LINE?
         BE    CHR0                NO
        @OUT   LINE
         SPACE 1
*        CHECK IF ALL REQUESTED CATALOGS ARE PRESENT.
         SPACE 1
CHR0     L     R2,TBCVOL           START OF CAT. VOL. TABLE
         USING CVLT,R2
CHR1     CLI   0(R2),FMOFF         END OF TABLE?
         BE    CHR4                YES
         TM    CVLIND,CVLCTP       CATALOG IS PRESENT?
         BO    CHR3                YES, CONTINUE
CHRSW    NOP   CHR2
         OI    CHRSW+1,ON
        @OUT   CTRMISS1,,U
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         MVI   LINE+L'CVLVOL+7,C'-'
CHR2     MVC   LINE+6(L'CVLVOL),CVLVOL  SET VOLUME
         MVC   LINE+L'CVLVOL+9(L'CVLNME),CVLNME   SET DS-NAME
         TM    CVLIND,CVLICF       CATALOG TYPE IS ICF?
         BO    *+L'*+6             YES
         MVC   LINE+L'CVLVOL+16(10),LINE+L'CVLVOL+30   NO, O.S.
         DROP  R2
        @OUT   LINE
CHR3     LA    R2,CVLL(R2)         ADVANCE IN TABLE
         B     CHR1                GO TO CHECK NEXT ONE
CHR4     MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
CHR99   @XE
         EJECT
OPVPLS  $VTOCQO ,,MF=L
RDVPLS  $VTOCQR ,,MF=L
         SPACE 1
SUPERR  WTO    '--- IXTOFMT5 - REGISTER 0 = XXXXXXXX.',                C
               ROUTCDE=11,DESC=7,MF=L
SUPERRM  EQU   SUPERR+32,8
         SPACE 1
IXDSCB   DC    F'0'                / ---> LET REMAIN
IXVIRS   DC    F'0'                /        THESE
FDSCB    DC    H'0'                /          FIELDS
VFDSCB   DC    H'0'                /            IN THIS
VTIND    DC    XL1'0'              /              SEQUENCE
INFLGTH  EQU   *-IXDSCB            /                ORDER
         SPACE 1
         DC    AL1(VTOERRL)
VTOERR   DC    C' DS1DSORG ERROR : '
VTOEVOL  DC    CL6' ',C'/'
VTOEDSN  DC    CL44' '
VTOERRL  EQU   *-VTOERR
         DC    AL1(L'VTIL)
VTIL     DC    C'0VTOC''S INFORMATION LIST :'
         DC    AL1(L'VTST)
VTST     DC    C'0VTOC''S STATUS CHECK LIST :'
         DC    AL1(L'VTSTN)
VTSTN    DC    C'0      NOTE : USE PROGRAM "VTOCANAL" TO DEBUG VTOCS.'
VTDIRF   DC    C'VTOC INDICATORS (DS4VTOCI) : DIRF BIT ON (DS4DIRF)'
         DC    AL1(ACCENDL)
ACCEND   DC    C'0END OF DATA-SETS ACCOUNTING LIST '
         DC    C'(ACCOUNT''S TOTAL :'
ACCENDN  DC    XL6'402020202120',C').'
ACCENDL  EQU   *-ACCEND
ACCUM    DC    C'0 ---> ACCOUNTING CARDS ARE STORED IN DATA-SET : '
         DC    AL1(L'CTLGMIS1)
CTLGMIS1 DC    C'0NOT DECLARED CATALOG(S) VOLUME(S) :'
         DC    AL1(L'CTRMISS1)
CTRMISS1 DC    C'0CATALOG(S) NOT FOUND ON DECLARED VOLUME(S) :'
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
DSCDSECT DSECT
        IECSDSL1 (1)
         ORG   DS1DSNAM
        IECSDSL1 (2)
         ORG   DS1DSNAM
        IECSDSL1 (3)
         ORG   DS1FMTID
        IECSDSL1 (4)
         ORG   DS1DSNAM
        IECSDSL1 (5)
         ORG   DS1DSNAM
        IECSDSL1 (6)
         SPACE 2
         END
