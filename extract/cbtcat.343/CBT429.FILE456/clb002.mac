B002     TITLE 'CLEAN-UP - ACCOUNTING REQUESTS (B002).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B002    @XS
*        PROCESS ACCOUNTING REQUIREMENTS.
         SPACE 1
         LM    R3,R5,LISCAN
         CLI   0(R3),C' '
         BNE   *+L'*+8
ACR1     BXLE  R3,R4,*-8
         B     ACR99
         CLI   0(R3),C'V'          VOLUME REQUEST?
         BE    RVL1                YES
         CLI   0(R3),C'L'          FULL LIST REQUEST?
         BE    RLS1                YES
         CLI   0(R3),C'J'          JUMP DAYS REQUEST?
         BE    RJD1                YES
         CLI   0(R3),C'P'          PREFIX REQUEST?
         BE    RPF1                YES
         CLI   0(R3),C'A'          ACCVOL REQUEST?
         BE    RAV1                YES
         B     ACRER
ACRES    BCTR  R3,0
ACRER    MVC   LINE+1(L'LINE-1),LINE    POSITION OF ERROR
         MVC   0(21,R3),=CL20'$... SYNTAX ERROR ...'
        @OUT   LINE
         B     ACR99
         SPACE 1
RVL1     CLI   0(R3),C' '          SEARCH END OF KEYWORD REQUEST
         BE    RVL2
         CLI   0(R3),C'('          START OF OPERAND?
         BE    RVL4                YES
         BXLE  R3,R4,RVL1
         B     ACRES
         CLI   0(R3),C' '          SEARCH START OF OPERAND
         BNE   RVL3
RVL2     BXLE  R3,R4,*-8
         B     ACRES
RVL3     CLI   0(R3),C'('          START OF OPERAND?
         BNE   ACRER               NO, ERROR
RVL4     BXLE  R3,R4,RVL5
         B     ACRES
RVL5     CLI   0(R3),C' '          SEARCH START OF KEY
         BNE   RVL6
         BXLE  R3,R4,RVL5
         B     ACRES
RVL6     CLI   0(R3),C')'          END OF OPERAND?
         BE    ACR1                YES
         LA    R0,7
         LR    R1,R3
RVL7     BXLE  R3,R4,*+L'*+4
         B     ACRES
         CLI   0(R3),C' '
         BE    RVL8
         CLI   0(R3),C')'
         BE    RVL8
         BCT   R0,RVL7
         B     ACRER
RVL8     LR    R14,R3              COMPUTE KEY LENGTH
         SR    R14,R1
         BCTR  R14,0
         L     R6,TBAVOL
         XR    R15,R15
RVL9     CLI   0(R6),FMON
         BE    RVL10
         IC    R15,0(R6)
         LA    R6,2(R6,R15)
         B     RVL9
RVL10    LA    R0,2(R6,R14)
         CL    R0,TBAVOLE
         BNL   ERR59               TABLE OVERFLOW
         STC   R14,0(R6)
         EX    R14,RVLMV           ADD NEW KEY
         LR    R6,R0
         MVI   0(R6),FMON
         OI    ACCIND,AFVOL        SET VOLUME(S) PRESENT
         B     RVL5
RVLMV    MVC   1(*-*,R6),0(R1)     << EXECUTED >>
         SPACE 1
RLS1     OI    ACCIND,AFFULL       SET FULL LIST
RLS2     BXLE  R3,R4,RLS3
         B     ACR99
RLS3     CLI   0(R3),C' '
         BNE   RLS2
         B     ACR1
         SPACE 1
RJD1     CLI   0(R3),C' '          SEARCH END OF KEYWORD REQUEST
         BE    RJD3
         CLI   0(R3),C'('          START OF OPERAND?
         BE    RJD5                YES
         BXLE  R3,R4,RJD1
         B     ACRES
RJD2     CLI   0(R3),C' '          SEARCH START OF OPERAND
         BNE   RJD4
RJD3     BXLE  R3,R4,RJD2
         B     ACRES
RJD4     CLI   0(R3),C'('          START OF OPERAND?
         BNE   ACRER               NO, ERROR
RJD5     BXLE  R3,R4,RJD6
         B     ACRES
RJD6     CLI   0(R3),C' '          SEARCH START OF DAY REQUEST
         BNE   RJD7
         BXLE  R3,R4,RJD6
         B     ACRES
RJD7     CLI   0(R3),C')'          END OF OPERAND?
         BE    ACR1                YES
         LA    R0,7
         LR    R1,R3
RJD8     BXLE  R3,R4,*+L'*+4
         B     ACRES
         CLI   0(R3),C' '
         BE    RJD9
         CLI   0(R3),C')'
         BE    RJD9
         BCT   R0,RJD8
         B     ACRER
RJD9     LR    R14,R3              COMPUTE LENGTH
         SR    R14,R1
         CH    R14,=H'3'           IS IT A DAY NAME LENGTH?
         BE    RJD10               YES
         CH    R14,=H'6'           IS IT A DATE DDMMYY LENGTH?
         BNE   ACRER               NO, ERROR
         CLC   0(L'TODAYA,R1),TODAYA    IS IT TODAY?
         BE    RJD13               YES
         B     RJD6
RJD10    LA    R14,DAYT            CONTROL VALID NAME
RJD11    CLC   0(L'DAYT,R1),0(R14)
         BE    RJD12
         LA    R14,L'DAYT(R14)
         CLI   0(R14),FMOFF        END OF NAMES?
         BNE   RJD11               NO, CONTINUE
         B     ACRES
RJD12    CLC   0(3,R1),TITDAT      IS IT TODAY?
         BNE   RJD6                NO
RJD13    OI    RUNOPT,RUNOACC      SET NO ACCOUNT
         B     RJD6
         SPACE 1
RPF1     CLI   0(R3),C' '          SEARCH END OF KEYWORD REQUEST
         BE    RPF3
         CLI   0(R3),C'('          START OF OPERAND?
         BE    RPF5                YES
         BXLE  R3,R4,RPF1
         B     ACRES
RPF2     CLI   0(R3),C' '          SEARCH START OF OPERAND
         BNE   RPF4
RPF3     BXLE  R3,R4,RPF2
         B     ACRES
RPF4     CLI   0(R3),C'('          START OF OPERAND?
         BNE   ACRER               NO, ERROR
RPF5     BXLE  R3,R4,RPF6
         B     ACRES
RPF6     CLI   0(R3),C' '          SEARCH START OF PREFIX
         BNE   RPF7
         BXLE  R3,R4,RPF6
         B     ACRES
RPF7     CLI   0(R3),C')'          END OF OPERAND?
         BE    ACR1                YES
         LR    R1,R3
RPF8     BXLE  R3,R4,*+L'*+4
         B     ACRES
         CLI   0(R3),C' '
         BE    RPF9
         CLI   0(R3),C')'
         BNE   RPF8
RPF9     LR    R15,R3
         SR    R15,R1
         CH    R15,=H'8'           PREFIX
         BH    ACRER
         MVI   DSNDATA,C' '
         MVC   DSNDATA+1(L'DSNDATA-1),DSNDATA
         BCTR  R15,0
         EX    R15,RPFMV
RPF10    CLI   0(R3),C' '
         BNE   RPF11
         BXLE  R3,R4,RPF10
         B     ACRES
RPF11    CLI   0(R3),C')'          END OF OPERAND?
         BE    ACR1                YES
         B     ACRES
RPFMV    MVC   DSNDATA(*-*),0(R1)
         SPACE 1
RAV1     CLI   0(R3),C' '          SEARCH END OF KEYWORD REQUEST
         BE    RAV3
         CLI   0(R3),C'('          START OF OPERAND?
         BE    RAV5                YES
         BXLE  R3,R4,RAV1
         B     ACRES
RAV2     CLI   0(R3),C' '          SEARCH START OF OPERAND
         BNE   RAV4
RAV3     BXLE  R3,R4,RAV2
         B     ACRES
RAV4     CLI   0(R3),C'('          START OF OPERAND?
         BNE   ACRER               NO, ERROR
RAV5     BXLE  R3,R4,RAV6
         B     ACRES
RAV6     CLI   0(R3),C' '          SEARCH START OF VOL.SER.
         BNE   RAV7
         BXLE  R3,R4,RAV6
         B     ACRES
RAV7     CLI   0(R3),C')'          END OF OPERAND?
         BE    ACR1                YES
         LA    R0,7
         LR    R1,R3
RAV8     BXLE  R3,R4,*+L'*+4
         B     ACRES
         CLI   0(R3),C' '
         BE    RAV9
         CLI   0(R3),C')'
         BE    RAV9
         BCT   R0,RAV8
         B     ACRER
RAV9     LR    R14,R3              COMPUTE VOL.SER. LENGTH
         SR    R14,R1
         BCTR  R14,0
         MVC   VADATA,=CL6' '
         EX    R14,RAVMV           SET DESIRED VOL.SER.
         CLI   0(R3),C')'          WAS END OF OPERAND?
         BE    ACR1                YES
RAV10    BXLE  R3,R4,*+L'*+4
         B     ACRER
         CLI   0(R3),C' '
         BE    RAV10
         CLI   0(R3),C')'          END OF OPERAND?
         BE    ACR1                YES
         B     ACRER
RAVMV    MVC   VADATA(*-*),0(R1)
         SPACE 1
ACR99   @XE
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
