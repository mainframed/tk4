A010     TITLE 'CLEAN-UP - CONTROLS AND DECISIONS (A010).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A010    @XS    ALIAS=DECIDE
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    MS99                YES
         TM    RUNIND,RUCATMIS     ALL CATALOGS ARE PRESENT?
         BO    MS99                NO, BYPASS
         LA    R1,WORKDS
         ST    R1,VSAMINX+4
         ST    R1,SLINX
         LA    R1,LOCAREA
         ST    R1,VSAMINX+12
         ST    R1,SLINXL
         LA    R1,SVSNAP+4
         ST    R1,SLINXS
         SPACE 1
*        LOOK : 1. SET UNCATALOG IF WRONG REFERENCE.
*               2. SET DELETE RACF IF WRONG REFERENCE.
*               3. CONTROL DATA-SET AND RACF DEFINITIONS, AND TRY
*                  TO MAKE RECOVERY.
*
*        NOTE : "VERY CAREFULL ACTION" OR "NO ACTION" IS DECIDED FOR
*               VSAM DATA-SETS BECAUSE THEY MUST BE MANAGED USING THE
*               'IDCAMS' PROGRAM, SO THEY ARE JUST SIGNALED AS MANUAL
*               CONTROL REQUESTED BY 'LISTDST' (SEE A011).
         SPACE 1
         XR    R5,R5
         L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    MS99                YES
MS1      LR    R6,R2               RETAIN GROUP STARTING ENTRY
MS2      LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    MS3                 YES
         CLC   DSNME(L'DSNME),DSNME-DSTABDS(R6)   SAME DSNAME?
         BE    MS2                 YES, CONTINUE TO SEARCH GROUP
MS3      LR    R4,R2               RETAIN END OF GROUP
         LR    R2,R6               GET STARTING ENTRY
MS4      CLI   DSUSE,XUI           IS IT A NULL GDG INDEX?
         BE    MS90                YES
         CLI   DSACTION,YR         DELETE FROM RACF REQ?
         BE    MS5                 YES
         CLI   DSACTION,YS         SCRATCH REQ?
         BE    MS6                 YES
         CLI   DSACTION,YD         SCRATCH DISK REQ?
         BE    MS6                 YES
         CLI   DSACTION,YU         UNCATALOG REQ?
         BE    MS6                 YES
         CLI   DSACTION,0          ANY OTHER ACTION SCHEDULED?
         BNE   MS90                YES, BYPASS
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BNZ   MS40                YES
         SPACE 1
*        PROCESSING WHEN DATA-SET NOT RESIDING ON A VOLUME.
         SPACE 1
         CLI   DSCAT,0             NO, CATALOG DEFINITION ONLY?
         BNE   MS20                YES
MS5      BAS   R14,MSCTR           CONTROL
         B     MS7
MS6      CLI   DSRACF,XRD          RACF DISCRETE?
         BNE   MS90                NO, BYPASS
MS7      LR    R15,R2              RETAIN CURRENT ENTRY
         LR    R2,R6               SET START OF GROUP
MS10     CLR   R2,R15              MYSELF?
         BE    MS11                YES
         CLR   R2,R4               END OF GROUP?
         BNL   MS17                YES
         CLI   DSACTION,0          ALREADY ANY ACTION?
         BNE   MS11                YES
         OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BZ    MS11                NO
         CLI   DSRACF,XRD          ALREADY RACF DISCRETE?
         BNE   MS12                NO
MS11     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     MS10
MS12     CLI   DSTYPE,0            IS IT A VSAM?
         BNE   MS13                YES, VSAM RACF PROFILE
         MVI   DSACTION,YA         INDICATE ADD TO RACF
         LR    R2,R15              RESTORE CURRENT ENTRY
         MVI   DSRCVRY,YRU         INDICATE USE FOR ADD RACF
         B     MS90
MS13     LR    R3,R2               SAVE VOL. ENTRY POINTER
         LR    R2,R15              RESTORE CURRENT ENTRY
         CLI   DSCAT-DSTABDS(R3),XCSI   ICF CATALOG?
         BNE   MS14                NO
         CLC   DSALTVO,DSCVOL-DSTABDS(R3)    VSAM CAT. VOL. MATCHES?
         BE    MS16                YES
         MVC   DSCVOL,DSCVOL-DSTABDS(R3)     SET CATALOG VOLUME
         B     MS15
MS14     BAS   R14,MSLCV           LOCATE CATALOG VOLUME
         CLC   DSALTVO(L'DSALTVO),LOCAREA+6  VSAM CAT. VOL. MATCHES?
         BE    MS16                YES
         MVC   DSCVOL(L'DSCVOL),LOCAREA+6    SET CATALOG VOLUME
MS15     MVI   DSACTION,YY         INDICATE MODIFY RACF
MS16     CLI   DSRACF-DSTABDS(R3),XRD   VOL. ENTRY HAS RACF DIS. ALSO?
         BNE   MS90                NO
         MVI   DSACTION-DSTABDS(R3),YR  DELETE FROM RACF
         B     MS90
MS17     LR    R2,R15              RESTORE CURRENT ENTRY
         CLI   DSACTION,0          ANY ACTION ALREADY SCHEDULED?
         BNE   MS90                YES, BYPASS
         MVI   DSACTION,YR         INDICATE RACF DELETION
         B     MS90
MS20     BAS   R14,MSCTR           CONTROL
         CLI   DSUSE,0             ANYTHING WITH GDG?
         BNE   MS30                YES
         LR    R15,R2              RETAIN CURRENT ENTRY
MS21     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLR   R2,R4               END OF GROUP?
         BNL   MS24                YES
         CLI   DSACTION,0          ALREADY ANY ACTION?
         BE    MS22                NO
         CLI   DSACTION,YS         IS IT SCRATCH REQUESTED?
         BNE   MS24                NO
         B     MS23                GO CHANGE THE ACTION
MS22     OC    DSVOLSN,DSVOLSN     ON VOLUME?
         BZ    MS21                NO, BYPASS
         CLI   DSCAT,0             ALREADY CATALOGED?
         BNE   MS24                YES, O.K.
MS23     MVI   DSACTION,YC         INDICATE CATALOG REQUEST
MS24     LR    R2,R15              RESTORE CURRENT ENTRY
         CLI   DSANME,0            IS THERE A TRUE NAME (ALIAS)?
         BE    MS29                NO
         CLC   DSANME,DSNME        YES, IS TRUE NAME AHEAD IN TABLE?
         BL    MS25                NO
         LR    R15,R2              YES, START FROM THIS POINT
         B     MS27
MS25     L     R15,DSTAB           START OF DSNAME'S TABLE
MS26     CLC   DSNME-DSTABDS(L'DSNME,R15),DSANME  THIS DSNAME?
         BH    MS28                NO, NOT FOUND
         BL    MS27                NO, CONTINUE
         CLC   DSMVCNT-DSTABDS(L'DSMVCNT,R15),DSMVCNT   VOL. COUNT?
         BNE   MS27                NOT EQUALS
         CLC   DSVOLSN-DSTABDS(L'DSVOLSN,R15),DSALTVO   VOL. SER.?
         BE    MS90                EQUALS
MS27     LA    R15,DSTABL(R15)     ADVANCE IN TABLE
         CLI   0(R15),FMOFF        END OF TABLE?
         BNE   MS26                NO, CONTINUE
MS28     MVI   DSACTION,YZ         INDICATE UNKNOWN
         B     MS90
MS29     MVI   DSACTION,YU         INDICATE UNCATALOG
         B     MS90
MS30     MVI   DSACTION,YM         SIGNAL CONTROL
         LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLR   R2,R4               END OF GROUP?
         BL    MS30                NO, CONTINUE GROUP
         CLI   0(R2),FMOFF         END OF TABLE?
         BNE   MS1                 NO, NEXT GROUP
         B     MS99                YES, END
         SPACE 1
*        PROCESSING WHEN DATA-SET EXISTS ON A VOLUME.
         SPACE 1
MS40     CLI   DSRCVRY,0           ALREADY ANY RECOVERY?
         BNE   MS90                YES, DON'T OVERRIDE IT
         CLC   DSNME(9),=CL9'SYSCTLG.V' IS IT AN O.S. CATALOG?
         BNE   MS42                NO
         CLI   DSRACF,XRD          IS IT RACF DISCRETE?
         BE    MS41                YES
         CLI   DSRACF,XRG          IS IT RACF GENERIC?
         BE    MS90                YES
         MVI   DSRCVRY,YRA         INDICATE ADD TO RACF
         B     MS90
MS41     CLI   DSTYPE,XTL          VSAM (LOCATE)?
         BNE   MS44                NO
         CLI   DSRTYPE,XRTV        VSAM (RACF)?
         BE    MS51                YES
         MVI   DSACTION,YF         INDICATE CHANGE RACF
         B     MS51
MS42     CLI   DSUSE,XUM           MODEL GDG?
         BNE   MS43                NO
         CLI   DSCAT,0             YES, CATALOG DEFINED?
         BNE   MS43                YES
         MVI   DSACTION,YM         NO, SIGNAL CONTROL
         B     MS90
MS43     CLI   DSRACF,XRD          RACF DISCRETE?
         BE    MS44                YES
         CLI   DSRACF,XRG          RACF GENERIC?
         BE    MS61                YES
         CLI   DSPROT,0            NO, DATA-SET PROTECTED?
         BE    MS51                NO
         MVI   DSACTION,YN         RESET RACF (VTOC FLAG)
         B     MS51
MS44     CLI   DSPROT,0            DATA-SET PROTECTED?
         BNE   MS46                YES
         CLI   DSTYPE,XTL          NO, VSAM (LOCATE)?
         BE    MS45                YES
         CLI   DSTYPE,XTV          VSAM (VTOC)?
         BE    MS47                YES
         MVI   DSACTION,YV         SET RACF (VTOC FLAG)
         B     MS47
MS45     CLI   DSRTYPE,XRTV        VSAM (RACF)?
         BE    MS48                YES
         B     MS49
MS46     CLI   DSTYPE,XTL          VSAM (LOCATE)?
         BE    MS47                YES
         CLI   DSTYPE,XTV          VSAM (VTOC)?
         BNE   MS47                NO
         MVI   DSACTION,YN         RESET RACF (VTOC FLAG)
MS47     CLC   DSTYPE,DSRTYPE      SAME DATA-SET TYPE?
         BE    MS48                YES
         NI    MSSW2+1,OFF
MS48     CLI   DSRUSE,0            RACF MODEL DATA-SET PROFILE?
         BE    MSSW2               NO
         CLC   DSRUSE,DSUSE        MODEL DEFINITION MATCHES?
         BNE   MS58                NO
MSSW2    B     MS51
         OI    MSSW2+1,ON
MS49     CLI   DSACTION,0          ANY ACTION ALREADY SCHEDULED?
         BE    MS50                NO
         CLI   DSACTION,YN         RESET HAS BEEN REQUESTED?
         BE    MS50                YES
         CLI   DSACTION,YV         IS IT SET RACF (VTOC FLAG)?
         BNE   MS58                NO, WHAT?
         MVI   DSACTION,YW         MODIFY WITH CHANGE RACF ALSO
         B     MS51
MS50     MVI   DSACTION,YF         INDICATE CHANGE RACF
MS51     CLI   DSTYPE,0            VSAM (LOCATE OR VTOC)?
         BNE   MS60                YES
         CLI   DSVORG,XOM          NO, MULTI-VOLUME?
         BE    MS52                YES
         XR    R5,R5
         B     MS90
MS52     CLC   DSMVCNT,=H'0'       FIRST VOLUME?
         BNE   MS53                NO, OTHER
         LR    R5,R2               RETAIN THIS ENTRY
         B     MS90
MS53     LTR   R5,R5               FIRST VOLUME POINTER SET?
         BZ    MS90                NO
         CLC   DSTYPE(L'DSTYPE),DSTYPE-DSTABDS(R5)     CONTROL SEQ.
         BNE   MS58
         CLC   DSRACF(L'DSRACF),DSRACF-DSTABDS(R5)
         BE    MS55
         CLI   DSRACF,0
         BE    MS54
         MVI   DSACTION,YR         DELETE FROM RACF
         B     MS90
MS54     MVI   DSRCVRY,YRR         DEFINE TO RACF
         CLI   DSACTION,YN         RESET HAS BEEN REQUESTED?
         BNE   MS90                NO
         CLI   DSTYPE,0            VSAM (LOCATE OR VTOC)?
         BNE   MS90                YES
         MVI   DSACTION,0          CLEAR IT (NOT NEEDED)
         B     MS90
MS55     CLC   DSPROT(L'DSPROT),DSPROT-DSTABDS(R5)
         BE    MS57
         BL    MS56
         CLI   DSRACF-DSTABDS(R5),0
         BNE   MS57
         MVI   DSACTION,YN         RESET RACF (VTOC FLAG)
         B     MS90
MS56     CLI   DSRACF-DSTABDS(R5),0
         BE    MS57
         CLI   DSTYPE,0            VSAM (LOCATE OR VTOC)?
         BNE   MS90                YES
         MVI   DSACTION,YV         SET RACF (VTOC FLAG)
         B     MS90
MS57     CLC   DSRTYPE(L'DSRTYPE),DSRTYPE-DSTABDS(R5)
         BE    MS90
MS58     LR    R15,R2              RETAIN CURRENT ENTRY
         LR    R2,R6               SET START OF GROUP
MS59     CLR   R2,R4               END OF GROUP?
         BL    *+L'*+6             NO
         LR    R2,R15              YES, RESTORE CURRENT ENTRY
         B     MS90
         MVI   DSACTION,YZ         INDICATE UNKNOWN
         MVI   DSRCVRY,0           CLEAR ANYTHING ELSE
         LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     MS59
MS60     CLI   DSACTION,0          ANY ACTION ALREADY SCHEDULED?
         BNE   MS90                YES
         CLI   DSRACF,0            RACF PROTECTED?
         BE    MS90                NO
         BAS   R14,MSLCV
         CLI   DSTYPE,XTL          VSAM LOCATE?
         BE    *+L'*+10            YES
         CLC   DSCVOL(L'DSCVOL),LOCAREA+6    VSAM CAT. VOL. MATCHES?
         B     *+L'*+6
         CLC   DSVOLSN(L'DSVOLSN),LOCAREA+6  VSAM CAT. VOL. MATCHES?
         BE    MS90                YES
         MVC   DSCVOL(L'DSCVOL),LOCAREA+6    SET CATALOG VOLUME
         MVI   DSACTION,YY         INDICATE MODIFY RACF
         B     MS90
MS61     CLI   DSPROT,0            DATA-SET PROTECTED?
         BE    MS90                NO
         MVI   DSACTION,YN         YES, RESET RACF (VTOC FLAG)
         B     MS90
         SPACE 1
MSCTR    OC    DSALTVO,DSALTVO     ANY VOLUME?
         BZ    MS90                NO, BYPASS
         L     R3,TBDDN            START OF DDNAME'S TABLE
         USING DDNT,R3
MSCTR1   CLI   0(R3),FMOFF         END OF TABLE?
         BE    MS90                YES, VOLUME NOT ANALYZED
         CLC   DDNVOL,DSALTVO      IS IT THIS VOLUME?
         BER   R14                 YES, TAKE ACTION
         LA    R3,DDNL(R3)         ADVANCE IN TABLE
         B     MSCTR1              AND LOOP
         DROP  R3
         SPACE 1
*        NOTE : IN VSAM, THE RACF PROFILE REFERS THE CATALOG
*               VOLUME AND NOT THE DATA-SET VOLUME.
         SPACE 1
MSLCV    ST    R14,SVIXRTN
         XC    LOCAREA(256),LOCAREA
         XC    LOCAREA+256(LOCAREAL-256),LOCAREA+256
         MVI   WORKDS,C' '
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
         TM    DSFLGS,ZFTNU        EXTRACT FIRST LEVEL INDEX
         BZ    *+L'*+10
         MVC   WORKDS(9),DSANME
         B     *+L'*+6
         MVC   WORKDS(9),DSNME
         LA    R0,8
         LA    R14,WORKDS+8
MSLCV1   CLI   0(R14),C'.'
         MVI   0(R14),C' '
         BE    MSLCV2
         BCTR  R14,0
         BCT   R0,MSLCV1
         LA    R15,65              WHAT? WRONG LEVEL INDEX
         SLL   R15,2               (GET ADDRESS OF MSG ERM65)
         AL    R15,ERRTAB
         XR    R0,R0
         XR    R14,R14
         IC    R0,0(R15)
         ICM   R14,B'0111',1(R15)
         ALR   R14,R0
         LA    R0,17
         SLR   R14,R0
         MVC   0(17,R14),DSNME
         B     ERR65
MSLCV2   L     R1,TBGVOL           SEARCH MAIN INDEX (CATALOG VOL.)
         CLI   0(R1),FMOFF         END OF TABLE?
         BE    MSLCV4              YES
         USING CVPDS,R1
         CLC   CVPNAME(L'CVPNAME),WORKDS     THIS ONE?
         BE    MSLCV3              YES
         LA    R1,CVPL(R1)         NO, NEXT ELEMENT
         B     MSLCV2+L'MSLCV2
MSLCV3   MVC   LOCAREA+6(L'CVPVOL),CVPVOL    GET CATALOG VOLUME
         B     MSLCV8
         DROP  R1
MSLCV4   ST    R1,SVTBGPT          SAVE END OF TABLE POINTER
         MVC   WORKD(L'WORKD),WORKDS    SAVE MAIN INDEX NAME
        LOCATE VSAMINX             LOCATE MAIN INDEX (CATALOG VOL.)
        @SNAPS SN,SLINX,INX
         LTR   R15,R15             TEST LOCATE COMPLETION CODE
         BZ    MSLCV7
         CH    R15,=H'8'
         BNE   MSLCV5
         CH    R0,=H'8'
         BNE   MSLCV5
         OC    DSVOLSN,DSVOLSN     NOT FOUND, ON VOLUME?
         BNZ   MS90                YES
         MVI   DSACTION,YM         SIGNAL CONTROL
         MVI   DSACTION-DSTABDS(R3),YM
         B     MS90
MSLCV5   CL    R15,=F'999'
         BNH   *+L'*+10
         MVC   LIXR15,=CL4' ---'
         B     *+L'*+16
         CVD   R15,WORKD
         MVC   LIXR15,MASKERR
         ED    LIXR15,WORKD+6
         CL    R0,=F'999'
         BNH   *+L'*+10
         MVC   LIXR0,=CL4' ---'
         B     *+L'*+16
         CVD   R0,WORKD
         MVC   LIXR0,MASKERR
         ED    LIXR0,WORKD+6
         MVC   LIXDSN,DSNME
        @FILL  LIXDSN
MSSW1    NOP   MSLCV6
         OI    MSSW1+1,ON
        @OUT   LIXT,=PL2'+40',U
MSLCV6  @OUT   LIX
         MVI   DSACTION,YZ         SIGNAL UNKNOWN
         B     MS90
MSLCV7   L     R1,SVTBGPT          RESTORE END OF TABLE POINTER
         LA    R15,CVPL(R1)
         CL    R15,TBGVOLE         ANY OVERFLOW?
         BL    *+L'*+6             NO
         XR    R15,R15             YES
         B     ERR64
         USING CVPDS,R1
         MVC   CVPNAME(L'CVPNAME),WORKD ADD IN TABLE
         MVC   CVPVOL(L'CVPVOL),LOCAREA+6
         OI    CVPFL,CVPLOC
         DROP  R1
         MVI   0(R15),FMOFF        SET FINAL MARKER
MSLCV8   L     R14,SVIXRTN
         BR    R14
         SPACE 1
MS90     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLR   R2,R4               END OF GROUP?
         BL    MS4                 NO, CONTINUE GROUP
         CLI   0(R2),FMOFF         END OF TABLE?
         BNE   MS1                 NO, NEXT GROUP
         DROP  R2
MS99    @XE
         EJECT
VSAMINX CAMLST NAME,*-*,,*-*
         SPACE 1
SLINX    DC    A(*-*)
         DC    A(L'WORKDS)
SLINXS   DC    A(*-*)
         DC    A(12)
SLINXL   DC    A(*-*)
         DC    XL1'80',AL3(LOCAREAL)
         SPACE 1
SVIXRTN  DC    F'0'
SVTBGPT  DC    F'0'
         SPACE 1
         DC    AL1(L'LIXT)
LIXT     DC    C'0LOCATE CATALOG VOLUME :'
         DC    AL1(LLIX)
LIX      DC    CL6' '
LIXDSN   DC    CL44' ',C' - RC ='
LIXR15   DC    CL4' ',C', IX ='
LIXR0    DC    CL4' ',C'.'
LLIX     EQU   *-LIX
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 2
         END
