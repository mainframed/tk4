LMRU     TITLE '--- LOAD MODULES REFERENCES UTILITY ---'
LMRUTIL  START 0
         SPACE 1
         PRINT OFF
         GBLC  &PRS,&PRF
         AIF   ('&SYSPARM' EQ 'FULL').SF
         AIF   ('&SYSPARM' EQ 'SHORT').SS
&PRS     SETC  'NOGEN'
&PRF     SETC  'NOGEN'
         AGO   .ST
.SS      ANOP
&PRS     SETC  'GEN'
&PRF     SETC  'NOGEN'
         AGO   .ST
.SF      ANOP
&PRS     SETC  'GEN'
&PRF     SETC  'GEN'
.ST      PRINT ON
         SPACE 1
* CALL OF THE PROGRAM :
* ---------------------
*
*        //XEQ     EXEC PGM=LMRUTIL,PARM=...(OPTIONAL)...
*        //LML....1  DD DSN=...1ST LOAD LIBRARY...,DISP=SHR
*        //LML....2  DD DSN=...2ND LOAD LIBRARY...,DISP=SHR
*                           ...ANY NUMBER OF DD-CARDS...
*        //LML....N  DD DSN=...NTH LOAD LIBRARY...,DISP=SHR
*        //TITPRINT  DD SYSOUT=*,DCB=(RECFM=FBA,BLKSIZE=800,LRECL=80)
*        //LMLPRINT  DD SYSOUT=*,DCB=(RECFM=FBA,BLKSIZE=800,LRECL=80)
*        //SYSPRINT  DD SYSOUT=*
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE (TP 361)
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         PRINT &PRF
        $DEFREG
*------- DEFAULTS VALUES DEFINITIONS (CAN BE CHANGED THROUGH THE JCL
*              EXEC PARM= FIELD).
         SPACE 1
@LN      EQU   55        MAX. NUMBER OF LINES PER PAGE
@MD      EQU   10        MAX. NUMBER OF LOAD LIBRARY'S DD'S
@MM      EQU   3000      MAX. NUMBER OF DIRECTORIES NAMES (IN ALL LOAD
*                             LIBRARIES)
@RN      EQU   8000      MAX. NUMBER OF REFERENCE ELEMENTS (IN ALL
*                             LOAD LIBRARIES)
*                        NOTE : A REFERENCE ELEMENT IS USED FOR
*                             - THE MODULE NAME OF A LOAD MODULE,
*                             - EACH ALIAS NAME OF A LOAD MODULE,
*                             - EACH ENTRY NAME (UNLESS IT IS MODULE
*                               OR ALIAS NAME) IN A LOAD MODULE, AND
*                             - EACH EXTERNAL NAME IN A LOAD MODULE.
@ZP      EQU   3000      MAX. NUMBER OF HMASPZAP IDR'S (IN ALL LOAD
*                             MODULES OF ALL LOAD LIBRARIES)
@LK      EQU   3000      MAX. NUMBER OF LINKAGE EDITOR IDR'S (IN ALL
*                             LOAD MODULES OF ALL LOAD LIBRARIES)
@US      EQU   3000      MAX. NUMBER OF USER (IDENTIFY) IDR'S (IN ALL
*                             LOAD MODULES OF ALL LIBRARIES)
@CE      EQU   500       MAX. NUMBER OF "CESD" SYMBOLS DEFINED IN A
*                             LOAD MODULE <============== ONLY ONE <===
@TW      EQU   4         MAX. NUMBER OF K (1024 BYTES) FOR TRANSLATOR
*                             IDR'S WORK BUFFER
@UW      EQU   4         MAX. NUMBER OF K (1024 BYTES) FOR USER
*                             (IDENTIFY) IDR'S WORK BUFFER
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                        R   E   M   A   R   K                        *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*  BE CAREFUL IF YOU MODIFY ANYONE OF THE TWO DSECT NAMED "MMENT" AND *
*   "RNENT" BECAUSE THEIR FIELDS ARE INVOLVED IN SOME INTERNAL SORTS. *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                      M A I N     P R O C E S S                      *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*              R10 = LOCAL BASE REGISTER                              *
*              R11 = 1ST MAIN BASE REGISTER                           *
*              R12 = 2ND MAIN BASE REGISTER                           *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        $XENT  BASE=(R11,R12)
         L     R10,=A(INIT0)       INITIALIZATION
         BR    R10
INITX    L     R10,=A(SCAN0)       SCAN ALL DD'S
         BR    R10
SCANX    L     R10,=A(TRCE0)       INITIAL TRACE
         BR    R10
TRCEX    L     R10,=A(GMMN0)       GET DIRECTORIES NAMES
         BR    R10
GMMNX    L     R10,=A(SCLM0)       SCAN LOAD MODULES
         BR    R10
SCLMX    L     R10,=A(PRMR0)       PRINT MODULES REFERENCES
         BR    R10
PRMRX    L     R10,=A(PRNR0)       PRINT NAMES REFERENCES
         BR    R10
PRNRX    L     R10,=A(FTRC0)       FINAL TRACE
         BR    R10
         SPACE 1
*------- THAT'S ALL FOLKS
         SPACE 1
LEAVE   $PCLOSE
         LTR   R2,R15              RC = 0
         BNZ   ERRPCL
         TM    SW2,SWERR
         BZ    QUIT+L'QUIT
QUIT     LA    R2,16               RC = 16
         ICM   R1,B'1111',WADDR
         BZ    EXIT
         L     R0,WSIZE
        FREEMAIN R,A=(1),LV=(0)
EXIT    $XRET  CC=(R2)
         SPACE 1
*------- SEND ERROR DIAGNOTICS
         SPACE 1
ERRPCL   STC   R15,PCLERR+47
         OI    PCLERR+47,C'0'
PCLERR  WTO    '"SYSPRINT" / CLOSE ERROR ($PCLOSE RC =  )',            X
               ROUTCDE=11,DESC=7
         B     QUIT
LSTERR   STC   R15,MERR1+42
         OI    MERR1+42,C'0'
         LA    R1,MERR1
         BAS   R9,PERR
         B     LEAVE
         SPACE 1
*------- SEND MESSAGE ROUTINE
*             R0-R1-R2-R14-R15  = WORK REGISTERS
*             R9 = LINK REGISTER
         SPACE 1
PERR     OI    SW2,SWERR
         TM    SW1,SWPRT
         BZ    PERR1
         MVC   WLNE(6),=CL6'0-M-> '
         MVC   WLNE+6(L'WLNE-6),WLNE+5
         LH    R15,0(R1)
         SH    R15,=H'5'
         EX    R15,PERRMV
         LR    R2,R1               SAVE AROUND PRINT
        $PRINT WLNE
         LTR   R15,R15
         BZR   R9
         NI    SW1,255-SWPRT
         STC   R15,PERRWT+46
         OI    PERRWT+46,C'0'
PERRWT  WTO    '"SYSPRINT" / PRINT ERROR ($PRINT RC =  )',             X
               ROUTCDE=11,DESC=7
         LR    R1,R2               RESTORE
PERR1   WTO    MF=(E,(1))
         BR    R9
PERRMV   MVC   WLNE+6(*-*),4(R1)   <<EXECUTED>>
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                       C O M M O N     D A T A                       *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
WORKD    DC    D'0'                DOUBLE-WORD WORK
ATIOT    DC    A(*-*)              TIOT POINTER
WADDR    DC    F'0'                AREA ADDRESS
WSIZE    DC    F'0'                AREA SIZE
VLLN     DC    A(@LN)              MAX. NO. OF LINES PER PAGE
VLMD     DC    A(@MD)              MAX. NO. OF LOAD LIBRARY'S DD'S
VLMM     DC    A(@MM)              MAX. NO. OF DIRECTORIES NAMES
VLRN     DC    A(@RN)              MAX. NO. OF REFERENCE ELEMENTS
VLZP     DC    A(@ZP)              MAX. NO. OF HMASPZAP IDR'S
VLLK     DC    A(@LK)              MAX. NO. OF LINKAGE EDITOR IDR'S
VLUS     DC    A(@US)              MAX. NO. OF USER (IDENTIFY) IDR'S
VLCE     DC    A(@CE)              MAX. NO. OF CESD SYMBOLS
VLTW     DC    A(@TW)              MAX. NO. OF K FOR TRANSLATOR BUFFER
VLUW     DC    A(@UW)              MAX. NO. OF K FOR USER BUFFER
AMDS     DC    A(*-*)              START OF LOAD'S DD'S
AMDN     DC    F'0'                LOAD'S DD'S USED NUMBER
AMMS     DC    A(*-*)              START OF DIRECTORIES NAMES
AMDE     EQU   AMMS                ALSO END OF 'MD'
ARNS     DC    A(*-*)              START OF REFERENCE ELEMENTS
AMME     EQU   ARNS                ALSO END OF 'MM'
AZPS     DC    A(*-*)              START OF HMASPZAP ELEMENTS
ARNE     EQU   AZPS                ALSO END OF 'RN'
ALKS     DC    A(*-*)              START OF LINKAGE EDITOR ELEMENTS
AZPE     EQU   ALKS                ALSO END OF 'ZP'
AUSS     DC    A(*-*)              START OF USER ELEMENTS
ALKE     EQU   AUSS                ALSO END OF 'LK'
ACES     DC    A(*-*)              START OF CESD SYMBOLS
AUSE     EQU   ACES                ALSO END OF 'US'
ATWS     DC    A(*-*)              START OF TRANSLATOR BUFFER
ACEE     EQU   ATWS                ALSO END OF 'CE'
AUWS     DC    A(*-*)              START OF USER BUFFER
ATWE     EQU   AUWS                ALSO END OF 'TW'
ELDA     DC    A(*-*)              END+1 OF LAST DEFINED AREA
AUWE     EQU   ELDA                ALSO END OF 'UW'
SZMD     DC    F'0'                SIZE OF "MD"
SZMM     DC    F'0'                SIZE OF "MM"
SZRN     DC    F'0'                SIZE OF "RN"
SZZP     DC    F'0'                SIZE OF "ZP"
SZLK     DC    F'0'                SIZE OF "LK"
SZUS     DC    F'0'                SIZE OF "US"
SZCE     DC    F'0'                SIZE OF "CE"
SZTW     DC    F'0'                SIZE OF "TW"
SZUW     DC    F'0'                SIZE OF "UW"
CHKMA    DC    2F'0'
CRNPTR   DC    F'0'
CZPPTR   DC    F'0'
CLKPTR   DC    F'0'
CUSPTR   DC    F'0'
CCEPTR   DC    F'0'
CTWPTR   DC    F'0'
CUWPTR   DC    F'0'
CURCE    DC    F'0'
CURTW    DC    F'0'
CURUW    DC    F'0'
TTLDD    DC    CL8'TITPRINT'
LMLDD    DC    CL8'LMLPRINT'
DDNME    DC    CL8' '
MODNME   DC    CL8' '
TRHEX    DC    CL16'0123456789ABCDEF'
EMDL     DC    Y(MDLGTH)
EMML     DC    Y(MMLGTH)
ERNL     DC    Y(RNLGTH)
EZPL     DC    Y(ZPLGTH)
ELKL     DC    Y(LKLGTH)
EUSL     DC    Y(USLGTH)
ECEL     DC    Y(CELGTH)
MXLN1    DC    PL2'+0'
MXLN2    DC    PL2'+0'
MXLN3    DC    PL2'+0'
MXLN4    DC    PL2'+0'
MRPG     DC    PL3'+0'
MRLN     DC    PL2'+0'
IXPG     DC    PL3'+0'
IXLN     DC    PL2'+0'
NRPG     DC    PL3'+0'
NRLN     DC    PL2'+0'
CTRL     DC    PL3'+0'
CKCODE   DC    XL1'0'
CURTTR   DC    XL3'0'
SW1      DC    XL1'0'
SWPRT    EQU   X'80'               SYSPRINT DD PRESENT
SWTIT    EQU   X'40'               TITPRINT DD PRESENT
SWLML    EQU   X'20'               LMLPRINT DD PRESENT
SWMDD    EQU   X'08'               LML..... DD(S) PRESENT
SW2      DC    XL1'0'
SWERR    EQU   X'80'               ERROR RC=16 TO BE SET
SWTAV    EQU   X'40'               TRACE WORK-AREAS VALUES
SWFTR    EQU   X'20'               FORCE TRACE WORK-AREAS VALUES
SW3      DC    XL1'0'
SWMNP    EQU   X'80'               MODULE NAME PRINT
SWTYP    EQU   X'40'               TYPE NAME/DATE PRINT
SWNRP    EQU   X'20'               NAME REFERENCE PRINT
SWLST    EQU   X'02'               LAST IDR RECORD PROCESSED
SWEQU    EQU   X'01'               PARM.FIELD SCAN USE
         SPACE 1
*------- DATA PRINT LINES
         SPACE 1
         DC    AL1(L'WLNE)
WLNE     DC    CL133' '            PRINT WORK LINE
         SPACE 1
         DC    AL1(L'COV1)
COV1     DS    0CL80
         DC    CL25'1L.M.R.   H A N D B O O K',CL55' '
         DC    AL1(L'COV2)
COV2     DS    0CL80
         DC    CL1'+',24C'_',CL55' '
         DC    AL1(L'COV3)
COV3     DS    0CL80
         DC    CL5' ',CL21' '
         DC    CL32'LOAD MODULES REFERENCES HANDBOOK'
         DC    CL22' '
         DC    AL1(L'COV4)
COV4     DS    0CL80
         DC    CL5' ',CL29' ',CL7'DATE :'
COVDATE  DC    CL10' ',CL29' '
         DC    AL1(L'COV5)
COV5     DC    CL80' '
         SPACE 1
         DC    AL1(L'IXT1)
IXT1     DS    0CL80
         DC    CL41'1L.M.R.   H A N D B O O K - INDEX SECTION'
         DC    CL14' ----- DATE : '
IXT1D    DC    CL10' ',CL9' - PAGE I'
IXT1P    DC    CL6' '
         DC    AL1(L'IXT1U)
IXT1U    DS    0CL80
         DC    CL1'+',79C'_'
         DC    AL1(L'IXT2)
IXT2     DS    0CL80
         DC    CL8'0 '
         DC    CL44'LIBRARY DATA-SET NAME ',CL3' '
         DC    CL6'VOLUME',CL3' '
         DC    CL7'PAGE   ',CL9' '
         DC    AL1(L'IXT2U)
IXT2U    DS    0CL80
         DC    CL8'+ ',44C'_',CL3' '
         DC    CL16'______   _______',CL9' '
         DC    AL1(L'IXT3)
IXT3     DS    0CL80
         DC    CL8' '
IXT3D    DC    CL44' ',CL3' '
IXT3V    DC    CL6' ',CL3' '
IXT3T    DC    CL1' '
IXT3P    DC    CL6' ',CL9' '
         SPACE 1
         DC    AL1(L'MRT1)
MRT1     DS    0CL80
         DC    CL43'1L.M.R.   H A N D B O O K - MODULES SECTION'
         DC    CL12' --- DATE : '
MRT1D    DC    CL10' ',CL9' - PAGE M'
MRT1P    DC    CL6' '
         DC    AL1(L'MRT1U)
MRT1U    DS    0CL80
         DC    CL1'+',79C'_'
         DC    AL1(L'MRT2)
MRT2     DS    0CL80
         DC    CL12'0DATA-SET : '
MRT2N    DC    CL44' ',CL6' ',CL12'ON VOLUME : '
MRT2V    DC    CL6' '
         DC    AL1(L'MRT2U)
MRT2U    DS    0CL80
         DC    CL1'+',10C'_',CL51' ',11C'_',CL7' '
         DC    AL1(L'MRT3)
MRT3     DS    0CL80
         DC    CL3'0',CL8'MODULE  ',CL3' ',CL7'LINK''ED',CL3' '
         DC    CL53'LINKAGE EDITOR INFORMATION ',CL3' '
         DC    AL1(L'MRT3U)
MRT3U    DS    0CL80
         DC    CL3'+',CL8'________',CL3' ',CL7'_______',CL3' '
         DC    53C'_',CL3' '
         DC    AL1(L'MRT3A)
MRT3A    DS    0CL80
         DC    CL14' '
         DC    CL7'TYPE ',CL3' '
         DC    CL53'NAME - DATE COMPILATION/ASSEMBLY ',CL3' '
         DC    AL1(L'MRT3S)
MRT3S    DS    0CL80
         DC    CL14'+ ',CL7'_______',CL3' ',53C'_',CL3' '
         DC    AL1(L'MRT4)
MRT4     DS    0CL80
         DC    CL3' '
MRT4M    DC    CL8' ',CL3' '
MRT4T    DC    CL7' ',CL3' '
NMRT4R   EQU   3                   DUP. NO. OF MRT4R FIELD
MRT4R    DC    (NMRT4R)CL18' ',CL2' '
         SPACE 1
         DC    AL1(L'NRT1)
NRT1     DS    0CL80
         DC    CL41'1L.M.R.   H A N D B O O K - NAMES SECTION'
         DC    CL14' ----- DATE : '
NRT1D    DC    CL10' ',CL9' - PAGE N'
NRT1P    DC    CL6' '
         DC    AL1(L'NRT1U)
NRT1U    DS    0CL80
         DC    CL1'+',79C'_'
         DC    AL1(L'NRT2)
NRT2     DS    0CL80
         DC    CL2'0 ',CL8'NAME ',CL1' ',CL6'TYPE ',CL1' ',CL8'MODULE '
         DC    CL1' ',CL6'DATE  ',CL1' ',CL44'DATA-SET / VOLUME',CL2' '
         DC    AL1(L'NRT2U)
NRT2U    DS    0CL80
         DC    CL2' ',8C'_',CL1' ',6C'_',CL1' ',8C'_'
         DC    CL1' ',6C'_',CL1' ',44C'_',CL2' '
         DC    AL1(L'NRT3)
NRT3     DS    0CL80
         DC    CL2' '
NRT3N    DC    CL8' ',CL1' '
NRT3T    DC    CL6' ',CL1' '
NRT3M    DC    CL8' ',CL1' '
NRT3A    DC    CL6' ',CL1' '
NRT3D    DC    CL44' ',CL2' '
         SPACE 1
*------- DIAGNOTICS MESSAGES
         SPACE 1
         PRINT &PRF
GTMERR  WTO    'GETMAIN : STORAGE NOT AVAILABLE (SIZE=        )',      X
               ROUTCDE=11,DESC=7,MF=L
MERR1   WTO    '"        " / PRINT ERROR ($PRINT RC =  )',             X
               ROUTCDE=11,DESC=7,MF=L
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                     I N I T I A L I Z A T I O N                     *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- GET NEW VALUES IF SUPPLIED BY JCL EXEC PARM= FIELD WHICH
*        MAY BE SPECIFIED AS SHOWN BELOW :
*        1. PARM='LN=..,MD=...,MM=...,RN=...,ZP=...,LK=...,US=...,
*                 CE=...,TW=...,UW=...,T'
*        2. PARM='LN=.. MD=... MM=... RN=... ZP=... LK=... US=...
*                 CE=... TW=... UW=... T'
*        3. PARM='LN .. MD ... MM ... RN ... ZP ... LK ... US ...
*                 CE ... TW ... UW ... T'
         SPACE 1
INIT0    L     R1,0(R1)            PARM.FIELD ADDRESS
         XR    R2,R2
         ICM   R2,B'0011',0(R1)    PARM.FIELD LENGTH
         BZ    NOPARM
         LA    R1,2(R1)            START OF PARM.FIELD TEXT
         AR    R2,R1               END+1 OF PARM.FIELD TEXT
PARM1    CLI   0(R1),C','
         BE    PARM2
         CLI   0(R1),C' '
         BNE   PARM3
PARM2    LA    R1,1(R1)            SEPARATOR
         CLR   R1,R2
         BL    PARM1
         B     NOPARM
PARM3    CLC   0(2,R1),=CL2'TW'
         BE    *+L'*+16
         CLI   0(R1),C'T'
         BNE   *+L'*+8
         OI    SW2,SWTAV
         B     PARM2
         LA    R0,2(R1)
         CLR   R0,R2
         BH    ERRPARM
         CLC   0(2,R1),=CL2'LN'
         BNE   *+L'*+8
         LA    R3,VLLN
         B     PARM4
         CLC   0(2,R1),=CL2'MD'
         BNE   *+L'*+8
         LA    R3,VLMD
         B     PARM4
         CLC   0(2,R1),=CL2'MM'
         BNE   *+L'*+8
         LA    R3,VLMM
         B     PARM4
         CLC   0(2,R1),=CL2'RN'
         BNE   *+L'*+8
         LA    R3,VLRN
         B     PARM4
         CLC   0(2,R1),=CL2'ZP'
         BNE   *+L'*+8
         LA    R3,VLZP
         B     PARM4
         CLC   0(2,R1),=CL2'LK'
         BNE   *+L'*+8
         LA    R3,VLLK
         B     PARM4
         CLC   0(2,R1),=CL2'US'
         BNE   *+L'*+8
         LA    R3,VLUS
         B     PARM4
         CLC   0(2,R1),=CL2'CE'
         BNE   *+L'*+8
         LA    R3,VLCE
         B     PARM4
         CLC   0(2,R1),=CL2'TW'
         BNE   *+L'*+8
         LA    R3,VLTW
         B     PARM4
         CLC   0(2,R1),=CL2'UW'
         BNE   ERRPARM
         LA    R3,VLUW
PARM4    CLR   R0,R2
         BNL   NOPARM              NULL VALUE
         LR    R1,R0
         NI    SW3,255-SWEQU
PARM5    CLI   0(R1),C','
         BE    PARM2               SEPARATOR
         CLI   0(R1),C'='
         BNE   PARM6
         TM    SW3,SWEQU
         BO    ERRPARM
         OI    SW3,SWEQU
         B     *+L'*+8
PARM6    CLI   0(R1),C' '
         BNE   PARM7
         LA    R1,1(R1)
         CLR   R1,R2
         BL    PARM5
         B     NOPARM
PARM7    CLI   0(R1),C'0'
         BL    PARM3
         XR    R15,R15
PARM8    CLI   0(R1),C','
         BE    PARM9               SEPARATOR
         CLI   0(R1),C' '
         BE    PARM9               SEPARATOR
         CLI   0(R1),C'0'
         BL    ERRPARM
         CLI   0(R1),C'9'
         BH    ERRPARM
         XR    R14,R14
         ICM   R14,B'1000',0(R1)
         SLL   R14,4
         SRL   R14,28
         LTR   R15,R15
         BNP   *+L'*+4
         MH    R15,=H'10'
         AR    R15,R14
         LA    R1,1(R1)
         CLR   R1,R2
         BL    PARM8
PARM9    LTR   R15,R15
         BNP   *+L'*+4             NULL VALUE
         ST    R15,0(R3)           SET NEW VALUE
         CLR   R1,R2
         BL    PARM2
         SPACE 1
*------- GET AND INITIALIZE AREAS
         SPACE 1
NOPARM   L     R1,VLLN
         LA    R0,10
         CLR   R1,R0
         BL    ERRMXLN
         LA    R0,99
         CLR   R1,R0
         BH    ERRMXLN
         CVD   R1,WORKD
         SP    WORKD,=PL1'+3'
         ZAP   MXLN1,WORKD+L'WORKD-L'MXLN1(L'MXLN1)
         SP    WORKD,=PL1'+2'
         ZAP   MXLN2,WORKD+L'WORKD-L'MXLN2(L'MXLN2)
         SP    WORKD,=PL1'+2'
         ZAP   MXLN3,WORKD+L'WORKD-L'MXLN3(L'MXLN3)
         SP    WORKD,=PL1'+2'
         ZAP   MXLN4,WORKD+L'WORKD-L'MXLN4(L'MXLN4)
         L     R2,VLMD
         LA    R1,1(R2)
         ST    R1,AMDN
         MH    R2,EMDL
         ST    R2,SZMD
         L     R3,VLMM
         MH    R3,EMML
         ST    R3,SZMM
         AR    R2,R3
         L     R3,VLRN
         MH    R3,ERNL
         ST    R3,SZRN
         AR    R2,R3
         L     R3,VLZP
         MH    R3,EZPL
         ST    R3,SZZP
         AR    R2,R3
         L     R3,VLLK
         MH    R3,ELKL
         ST    R3,SZLK
         AR    R2,R3
         L     R3,VLUS
         MH    R3,EUSL
         ST    R3,SZUS
         AR    R2,R3
         L     R3,VLCE
         MH    R3,ECEL
         ST    R3,SZCE
         AR    R2,R3
         L     R3,VLTW
         MH    R3,=H'1024'         1024 BYTES = 1K
         ST    R3,SZTW
         AR    R2,R3
         L     R3,VLUW
         MH    R3,=H'1024'         1024 BYTES = 1K
         ST    R3,SZUW
         AR    R2,R3
         LA    R0,7(R2)
         SRL   R0,3
         SLL   R0,3
         ST    R0,WSIZE
        GETMAIN RC,LV=(0)
         LTR   R15,R15
         BNZ   ERRGTMN
         ST    R1,WADDR
         ST    R1,AMDS
         AL    R1,SZMD
         ST    R1,AMMS
         AL    R1,SZMM
         ST    R1,ARNS
         AL    R1,SZRN
         ST    R1,AZPS
         AL    R1,SZZP
         ST    R1,ALKS
         AL    R1,SZLK
         ST    R1,AUSS
         AL    R1,SZUS
         ST    R1,ACES
         AL    R1,SZCE
         ST    R1,ATWS
         AL    R1,SZTW
         ST    R1,AUWS
         AL    R1,SZUW
         ST    R1,ELDA
         L     R2,WADDR
         L     R3,WSIZE
         XR    R5,R5
         BASR  R4,0
         MVCL  R2,R4
         B     INITX
ERRPARM  LA    R15,29(R1)
         CLR   R15,R2
         BL    *+L'*+4
         LR    R15,R2
         BCTR  R15,0
         SR    R15,R1
         EX    R15,PARMMV
PARMERR WTO    'PARM= ERROR AT :                                ',     X
               ROUTCDE=11,DESC=7
         B     QUIT
PARMMV   MVC   PARMERR+25(*-*),0(R1)    <<EXECUTED>>
ERRGTMN  XR    R0,R0
         L     R1,WSIZE
         LA    R14,2*L'WSIZE
         LA    R15,GTMERR+42
         SLDL  R0,4
         STC   R0,0(R15)
         LA    R15,1(R15)
         BCT   R14,*-12
         NC    GTMERR+42(8),=XL8'0F0F0F0F0F0F0F0F'
         TR    GTMERR+42(8),TRHEX
         OI    SW2,SWFTR
         B     INITX
ERRMXLN WTO    'PARM= ERROR ON : LN=.. (VALID RANGE IS 10 TO 99)',     X
               ROUTCDE=11,DESC=7
         B     QUIT
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                         S C A N - C H E C K                         *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- SCAN ALL DD'S
         SPACE 1
SCAN0   EXTRACT    ATIOT,'S',FIELDS=(TIOT)
         L     R2,ATIOT
         USING TIODSECT,R2
         TM    SW2,SWFTR
         BO    SCAN1
         L     R3,AMDS
         USING MDENT,R3
         L     R4,AMDN
         XR    R9,R9
SCAN1    CLC   TIOELNGH(4),=XL4'0'
         BE    SCAN10
         TM    SW2,SWFTR
         BO    SCAN4
         CLC   TIOEDDNM(L'TTLDD),TTLDD
         BNE   SCAN2
         TM    SW1,SWTIT
         BO    *+L'*+8
         OI    SW1,SWTIT
         B     SCAN5
         MVC   MESC2+5(L'TTLDD),TTLDD
         LA    R1,MESC2
         BAS   R9,PERR
         B     SCAN5
SCAN2    CLC   TIOEDDNM(L'LMLDD),LMLDD
         BNE   SCAN3
         TM    SW1,SWLML
         BO    *+L'*+8
         OI    SW1,SWLML
         B     SCAN5
         MVC   MESC2+5(L'LMLDD),LMLDD
         LA    R1,MESC2
         BAS   R9,PERR
         B     SCAN5
SCAN3    CLC   TIOEDDNM(3),LMLDD
         BNE   SCAN4
         MVC   MDNME,TIOEDDNM
         BCT   R4,*+L'*+12
         LA    R1,MESC1
         BAS   R9,PERR
         B     LEAVE
         AH    R3,EMDL
         OI    SW1,SWMDD
         B     SCAN5
SCAN4    CLC   TIOEDDNM(8),=CL8'SYSPRINT'
         BNE   SCAN5
         OI    SW1,SWPRT
SCAN5    XR    R1,R1
         IC    R1,TIOELNGH
         ALR   R2,R1
         B     SCAN1
         DROP  R2,R3
SCAN10   TM    SW2,SWFTR
         BZ    SCAN11
         TM    SW1,SWPRT
         BO    SCANX
        WTO    MF=(E,GTMERR)
         B     ERRPRT
SCAN11   TM    SW1,SWPRT+SWTIT+SWLML+SWMDD
         BNO   ERRPRT
         L     R1,AMDN
         SR    R1,R4
         ST    R1,AMDN
         L     R3,AMDS
         USING MDENT,R3
         L     R4,AMDN
         LR    R5,R3
CKDD1    LR    R6,R4
CKDD2    BCT   R6,*+L'*+4
         B     CKDD3
         AH    R5,EMDL
         CLC   MDNME,MDNME-MDENT(R5)
         BNE   CKDD2
         MVC   MESC2+5(L'MDNME),MDNME
         LA    R1,MESC2
         BAS   R9,PERR
         B     CKDD2
CKDD3    BCT   R4,*+L'*+4
         B     *+L'*+8
         AH    R3,EMDL
         B     CKDD1
         DROP  R3
         LTR   R9,R9
         BNZ   LEAVE
         B     SCANX
ERRPRT   TM    SW1,SWPRT
         BO    ERRTIT
         MVC   MESC3+5(8),=CL8'SYSPRINT'
        WTO    MF=(E,MESC3)
         OI    SW2,SWERR
         TM    SW2,SWFTR
         BO    LEAVE
ERRTIT   TM    SW1,SWTIT
         BO    ERRLML
         MVC   MESC3+5(L'TTLDD),TTLDD
         LA    R1,MESC3
         BAS   R9,PERR
ERRLML   TM    SW1,SWLML
         BO    ERRMDD
         MVC   MESC3+5(L'LMLDD),LMLDD
         LA    R1,MESC3
         BAS   R9,PERR
ERRMDD   TM    SW1,SWMDD
         BO    LEAVE
         MVC   MESC4+5(3),LMLDD
         LA    R1,MESC4
         BAS   R9,PERR
         B     LEAVE
         SPACE 1
MESC1   WTO    'TOO MANY "LML....." DD''S ("MD" VALUE OVERFLOW)',      X
               ROUTCDE=11,DESC=7,MF=L
MESC2   WTO    '"        " / JCL ERROR (DD ENCOUNTERED TWICE)',        X
               ROUTCDE=11,DESC=7,MF=L
MESC3   WTO    '"        " DD-CARD MISSING',                           X
               ROUTCDE=11,DESC=7,MF=L
MESC4   WTO    '"   ....." DD-CARD(S) MISSING',                        X
               ROUTCDE=11,DESC=7,MF=L
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                     I N I T I A L     T R A C E                     *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- TRACE WORK-AREAS VALUES IF REQUESTED
         SPACE 1
TRCE0    TM    SW2,SWFTR
         BZ    TRCE1
         LA    R1,GTMERR
         BAS   R9,PERR
         TM    SW1,SWPRT
         BZ    LEAVE
         MVI   TRCM0,C'0'
TRCE1    TM    SW2,SWTAV
         BZ    TRCE5
        $PRINT TRCM0
         LTR   R15,R15
         BNZ   TRCERR
        $PRINT TRCM0U
         LTR   R15,R15
         BZ    TRCE4
TRCERR   STC   R15,PERRWT+46
         OI    PERRWT+46,C'0'
        WTO    MF=(E,PERRWT)
         OI    SW2,SWERR
         B     LEAVE
TRCE2    LR    R1,R2
         XR    R0,R0
         LA    R14,L'TRCM4A
         LA    R15,TRCM4A
         SLDL  R0,4
         STC   R0,0(R15)
         LA    R15,1(R15)
         BCT   R14,*-12
         NC    TRCM4A,=XL8'0F0F0F0F0F0F0F0F'
         TR    TRCM4A,TRHEX
         SR    R3,R2
         LR    R1,R3
         XR    R0,R0
         LA    R14,L'TRCM4B
         LA    R15,TRCM4B
         SLDL  R0,4
         STC   R0,0(R15)
         LA    R15,1(R15)
         BCT   R14,*-12
         NC    TRCM4B,=XL8'0F0F0F0F0F0F0F0F'
         TR    TRCM4B,TRHEX
         MVI   TRCM4C+16,C' '
         MVC   TRCM4C+17(L'TRCM4C-17),TRCM4C+16
         CVD   R3,WORKD
         MVC   TRCM4C(16),=XL16'40202020202020202020202020202120'
         ED    TRCM4C(16),WORKD
         LA    R0,15
         CLI   TRCM4C,C' '
         BNE   *+L'*+14
         MVC   TRCM4C(15),TRCM4C+1
         MVI   TRCM4C+15,C' '
         BCT   R0,*-18
         LA    R1,TRCM4C
         CLI   0(R1),C' '
         BE    *+L'*+8
         LA    R1,1(R1)
         B     *-12
         MVC   1(6,R1),=CL6'BYTES)'
        $PRINT TRCM4
         LTR   R15,R15
         BZR   R9
         B     TRCERR
TRCE3    LR    R1,R2
         XR    R0,R0
         LA    R14,L'TRCM3A
         LA    R15,TRCM3A
         SLDL  R0,4
         STC   R0,0(R15)
         LA    R15,1(R15)
         BCT   R14,*-12
         NC    TRCM3A,=XL8'0F0F0F0F0F0F0F0F'
         TR    TRCM3A,TRHEX
         CVD   R2,WORKD
         MVC   TRCM3B(16),=XL16'40202020202020202020202020202120'
         ED    TRCM3B(16),WORKD
         LA    R0,15
         CLI   TRCM3B,C' '
         BNE   *+L'*+14
         MVC   TRCM3B(15),TRCM3B+1
         MVI   TRCM3B+15,C' '
         BCT   R0,*-18
         LA    R1,TRCM3B
         CLI   0(R1),C' '
         BE    *+L'*+8
         LA    R1,1(R1)
         B     *-12
         MVC   1(6,R1),=CL6'BYTES)'
        $PRINT TRCM3
         LTR   R15,R15
         BZR   R9
         B     TRCERR
TRCE4    L     R0,VLLN
         CVD   R0,WORKD
         MVC   TRCM1V,=XL6'402020202120'
         ED    TRCM1V,WORKD+L'WORKD-3
        $PRINT TRCM1
         LTR   R15,R15
         BNZ   TRCERR
         L     R0,VLMD
         CVD   R0,WORKD
         MVC   TRCM2V,=XL6'402020202120'
         ED    TRCM2V,WORKD+L'WORKD-3
         MVC   TRCM2I,=CL2'MD'
         MVC   TRCM2N(L'TRCM2N),TRCM2N-1
         MVC   TRCM2N(19),=CL19'LOAD LIBRARY''S DD''S'
        $PRINT TRCM2
         LTR   R15,R15
         BNZ   TRCERR
         LH    R2,EMDL
         BAS   R9,TRCE3
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    AMDS,AMDS
         MVC   AMDE,SZMD
         L     R2,AMDS
         L     R3,AMDE
         BAS   R9,TRCE2
         L     R0,VLMM
         CVD   R0,WORKD
         MVC   TRCM2V,=XL6'402020202120'
         ED    TRCM2V,WORKD+L'WORKD-3
         MVC   TRCM2I,=CL2'MM'
         MVC   TRCM2N(L'TRCM2N),TRCM2N-1
         MVC   TRCM2N(17),=CL17'DIRECTORIES NAMES'
        $PRINT TRCM2
         LTR   R15,R15
         BNZ   TRCERR
         LH    R2,EMML
         BAS   R9,TRCE3
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    AMMS,AMMS
         MVC   AMME,SZMM
         L     R2,AMMS
         L     R3,AMME
         BAS   R9,TRCE2
         L     R0,VLRN
         CVD   R0,WORKD
         MVC   TRCM2V,=XL6'402020202120'
         ED    TRCM2V,WORKD+L'WORKD-3
         MVC   TRCM2I,=CL2'RN'
         MVC   TRCM2N(L'TRCM2N),TRCM2N-1
         MVC   TRCM2N(18),=CL18'REFERENCE ELEMENTS'
        $PRINT TRCM2
         LTR   R15,R15
         BNZ   TRCERR
         LH    R2,ERNL
         BAS   R9,TRCE3
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    ARNS,ARNS
         MVC   ARNE,SZRN
         L     R2,ARNS
         L     R3,ARNE
         BAS   R9,TRCE2
         L     R0,VLZP
         CVD   R0,WORKD
         MVC   TRCM2V,=XL6'402020202120'
         ED    TRCM2V,WORKD+L'WORKD-3
         MVC   TRCM2I,=CL2'ZP'
         MVC   TRCM2N(L'TRCM2N),TRCM2N-1
         MVC   TRCM2N(14),=CL14'HMASPZAP IDR''S'
        $PRINT TRCM2
         LTR   R15,R15
         BNZ   TRCERR
         LH    R2,EZPL
         BAS   R9,TRCE3
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    AZPS,AZPS
         MVC   AZPE,SZZP
         L     R2,AZPS
         L     R3,AZPE
         BAS   R9,TRCE2
         L     R0,VLLK
         CVD   R0,WORKD
         MVC   TRCM2V,=XL6'402020202120'
         ED    TRCM2V,WORKD+L'WORKD-3
         MVC   TRCM2I,=CL2'LK'
         MVC   TRCM2N(L'TRCM2N),TRCM2N-1
         MVC   TRCM2N(20),=CL20'LINKAGE EDITOR IDR''S'
        $PRINT TRCM2
         LTR   R15,R15
         BNZ   TRCERR
         LH    R2,ELKL
         BAS   R9,TRCE3
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    ALKS,ALKS
         MVC   ALKE,SZLK
         L     R2,ALKS
         L     R3,ALKE
         BAS   R9,TRCE2
         L     R0,VLUS
         CVD   R0,WORKD
         MVC   TRCM2V,=XL6'402020202120'
         ED    TRCM2V,WORKD+L'WORKD-3
         MVC   TRCM2I,=CL2'US'
         MVC   TRCM2N(L'TRCM2N),TRCM2N-1
         MVC   TRCM2N(21),=CL21'USER (IDENTIFY) IDR''S'
        $PRINT TRCM2
         LTR   R15,R15
         BNZ   TRCERR
         LH    R2,EUSL
         BAS   R9,TRCE3
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    AUSS,AUSS
         MVC   AUSE,SZUS
         L     R2,AUSS
         L     R3,AUSE
         BAS   R9,TRCE2
         L     R0,VLCE
         CVD   R0,WORKD
         MVC   TRCM2V,=XL6'402020202120'
         ED    TRCM2V,WORKD+L'WORKD-3
         MVC   TRCM2I,=CL2'CE'
         MVC   TRCM2N(L'TRCM2N),TRCM2N-1
         MVC   TRCM2N(14),=CL14'"CESD" SYMBOLS'
        $PRINT TRCM2
         LTR   R15,R15
         BNZ   TRCERR
         LH    R2,ECEL
         BAS   R9,TRCE3
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    ACES,ACES
         MVC   ACEE,SZCE
         L     R2,ACES
         L     R3,ACEE
         BAS   R9,TRCE2
         L     R0,VLTW
         CVD   R0,WORKD
         MVC   TRCM5V,=XL6'402020202120'
         ED    TRCM5V,WORKD+L'WORKD-3
         MVC   TRCM5I,=CL2'TW'
        $PRINT TRCM5
         LTR   R15,R15
         BNZ   TRCERR
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    ATWS,ATWS
         MVC   ATWE,SZTW
         L     R2,ATWS
         L     R3,ATWE
         BAS   R9,TRCE2
         L     R0,VLUW
         CVD   R0,WORKD
         MVC   TRCM6V,=XL6'402020202120'
         ED    TRCM6V,WORKD+L'WORKD-3
         MVC   TRCM6I,=CL2'UW'
        $PRINT TRCM6
         LTR   R15,R15
         BNZ   TRCERR
         TM    SW2,SWFTR
         BZ    *+L'*+12
         XC    AUWS,AUWS
         MVC   AUWE,SZUW
         L     R2,AUWS
         L     R3,AUWE
         BAS   R9,TRCE2
         L     R2,WADDR
         L     R3,WSIZE
         AR    R3,R2
         MVI   TRCM4,C'0'
         MVC   TRCM4+6(12),=CL12'===> TOTAL -'
         BAS   R9,TRCE2
TRCE5    TM    SW2,SWFTR
         BO    LEAVE
         B     TRCEX
         SPACE 1
         DC    AL1(TRCM0L)
TRCM0    DC    C'1 CURRENT VALUE OF PARAMETERS AND '
         DC    C'WORK-AREAS SPACE :'
TRCM0L   EQU   *-TRCM0
         DC    AL1(TRCM0UL)
TRCM0U   DC    C'+ ________________________________'
         DC    C'__________________'
TRCM0UL  EQU   *-TRCM0U
         DC    AL1(TRCM1L)
TRCM1    DC    CL10'0     LN :'
TRCM1V   DC    CL6' ',C' - LINES PER PAGE'
TRCM1L   EQU   *-TRCM1
         DC    AL1(TRCM2L)
TRCM2    DC    CL6'0 '
TRCM2I   DC    CL2'  ',CL2' :'
TRCM2V   DC    CL6' ',C' - MAXIMUM NUMBER OF '
TRCM2N   DC    CL25' '
TRCM2L   EQU   *-TRCM2
         DC    AL1(TRCM3L)
TRCM3    DC    CL19' ',C'ELEMENT LENGTH : '
TRCM3A   DC    CL8' ',C' ('
TRCM3B   DC    CL24' '
TRCM3L   EQU   *-TRCM3
         DC    AL1(TRCM4L)
TRCM4    DC    CL19' ',C'AREA ADDRESS : '
TRCM4A   DC    CL8' ',C' LENGTH : '
TRCM4B   DC    CL8' ',C' ('
TRCM4C   DC    CL24' '
TRCM4L   EQU   *-TRCM4
         DC    AL1(TRCM5L)
TRCM5    DC    CL6'0 '
TRCM5I   DC    CL2'  ',CL2' :'
TRCM5V   DC    CL6' ',C' - MAXIMUM NUMBER OF K FOR THE'
         DC    C' TRANSLATOR IDR''S WORK BUFFER'
TRCM5L   EQU   *-TRCM5
         DC    AL1(TRCM6L)
TRCM6    DC    CL6'0 '
TRCM6I   DC    CL2'  ',CL2' :'
TRCM6V   DC    CL6' ',C' - MAXIMUM NUMBER OF K FOR THE'
         DC    C' USER (IDENTIFY) IDR''S WORK BUFFER'
TRCM6L   EQU   *-TRCM6
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*            G E T     D I R E C T O R I E S     N A M E S            *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- FIRST GET TODAY'S DATE
         SPACE 1
GMMN0   $GFDATE ,,WORKD            TODAY DATE
         LTR   R15,R15
         BZ    GFDT1
         STC   R15,MEGFD+42
         OI    MEGFD+42,C'0'
         LA    R1,MEGFD
         BAS   R9,PERR
         B     LEAVE
GFDT1    MVC   COVDATE(2),WORKD+2
         MVI   COVDATE+2,C'/'
         MVC   COVDATE+3(2),WORKD
         MVI   COVDATE+5,C'/'
         MVC   COVDATE+6(4),WORKD+4
         MVC   IXT1D,COVDATE
         MVC   MRT1D,COVDATE
         MVC   NRT1D,COVDATE
         SPACE 1
*------- GET DIRECTORIES NAMES
         SPACE 1
         L     R4,AMMS
         USING MMENT,R4
         L     R5,AMDS
         L     R6,AMDN
         USING MDENT,R5
GMMN1    LA    R3,MDNME
         LA    R2,MDDSN
        $PDOPEN (R2),(R3)
         LTR   R15,R15
         BNZ   *+L'*+8
         ST    R4,MDMMS
         B     GMMN3
         STC   R15,MEGM2+42
         OI    MEGM2+42,C'0'
         MVC   MEGM2+5(L'MDNME),MDNME
         LA    R1,MEGM2
         BAS   R9,PERR
         B     LEAVE
GMMN2    STC   R15,MEGM3+42
         OI    MEGM3+42,C'0'
         MVC   MEGM3+4(L'MDNME),MDNME
         LA    R1,MEGM3
         BAS   R9,PERR
         B     GMMN10
GMMN3   $PDREAD WLNE,(R3)
         LTR   R15,R15
         BP    GMMN2
         BM    GMMN10
         LA    R1,WLNE
         USING PDS2,R1
         IC    R0,PDS2INDC
         SLL   R0,27
         SRL   R0,27
         CL    R0,=F'11'
         BL    GMMN3               NOT LINK-EDITED
         CL    R4,AMME
         BL    *+L'*+12
         LA    R1,MEGM1
         BAS   R9,PERR
         B     GMMN10
         MVC   MMNME,PDS2NAME
         MVC   MMTTR,PDS2TTRP
         MVC   MMEPA,PDS2EPA
         TM    PDS2INDC,PDS2ALIS
         BO    GMMN4
         MVI   MMTYP,MMMO
         MVC   MMONM,=CL8' '
         B     GMMN5
GMMN4    MVI   MMTYP,MMAL
         TM    PDS2ATR1,PDS2SCTR
         BO    *+L'*+4
         SH    R1,=Y(PDSS01LN)
         MVC   MMONM,PDS2MNM
         DROP  R1
GMMN5    ST    R4,MDMME
         AH    R4,EMML
         B     GMMN3
GMMN10  $PDCLOSE (R3)
         LTR   R15,R15
         BZ    GMMN11
         STC   R15,MEGM4+44
         OI    MEGM4+44,C'0'
         MVC   MEGM4+5(L'MDNME),MDNME
         LA    R1,MEGM4
         BAS   R9,PERR
         B     LEAVE
GMMN11   TM    SW2,SWERR
         BO    LEAVE
         L     R3,MDMME
         AH    R3,EMML
         SL    R3,MDMMS
         XR    R2,R2
         XC    WORKD,WORKD
         MVC   WORKD+4-L'EMML(L'EMML),EMML
         D     R2,WORKD
         L     R2,MDMMS
        $SORT  (R2),(R3),MMLGTH,0,L'MMTTR+L'MMTYP
         BCT   R6,*+L'*+4
         B     GMMNX
         AH    R5,EMDL
         CL    R4,AMME
         BL    *+L'*+12
         LA    R1,MEGM1
         BAS   R9,PERR
         B     LEAVE
         AH    R4,EMML
         B     GMMN1
         DROP  R4,R5
         SPACE 1
MEGFD   WTO    'TODAY DATE OBTAIN ERROR ($GFDATE RC =  )',             X
               ROUTCDE=11,DESC=7,MF=L
MEGM1   WTO    'TOO MANY DIRECTORIES NAMES ("MM" VALUE OVERFLOW)',     X
               ROUTCDE=11,DESC=7,MF=L
MEGM2   WTO    '"        " / OPEN ERROR ($PDOPEN RC =  )',             X
               ROUTCDE=11,DESC=7,MF=L
MEGM3   WTO    '"        " / READ ERROR ($PDREAD RC =  )',             X
               ROUTCDE=11,DESC=7,MF=L
MEGM4   WTO    '"        " / CLOSE ERROR ($PDCLOSE RC =  )',           X
               ROUTCDE=11,DESC=7,MF=L
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                S C A N     L O A D     M O D U L E S                *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- SCAN LOAD MODULES
         SPACE 1
SCLM0    L     R5,ARNS
         SH    R5,ERNL
         USING RNENT,R5
         L     R7,AMDS
         USING MDENT,R7
         L     R8,AMDN
SCLM1    AH    R5,ERNL
         CL    R5,ARNE
         BL    *+L'*+12
         LA    R1,MELM1
         BAS   R9,PERR
         B     LEAVE
         ST    R5,MDRNS
         L     R6,MDMMS
         USING MMENT,R6
         MVC   DDNME,MDNME
        $PLOPEN DDNME
         LTR   R15,R15
         BZ    SCLM2
         STC   R15,MELM2+41
         OI    MELM2+41,C'0'
         MVC   MELM2+5(L'DDNME),DDNME
         LA    R1,MELM2
         BAS   R9,PERR
         B     LEAVE
SCLM2    MVC   CURTTR,MMTTR
        $PLPOINT CURTTR,DDNME
         LTR   R15,R15
         BZ    SCLM3
         STC   R15,MELM3+55
         OI    MELM3+55,C'0'
         MVC   MELM3+24(L'MMNME),MMNME
         MVC   MELM3+5(L'DDNME),DDNME
         LA    R1,MELM3
         BAS   R9,PERR
         B     SCLM90
SCLM3    ST    R6,CHKMA
         L     R2,ACES
         L     R3,SZCE
         XR    R15,R15
         BASR  R14,0
         MVCL  R2,R14
         L     R2,ATWS
         L     R3,SZTW
         XR    R15,R15
         BASR  R14,0
         MVCL  R2,R14
         L     R2,AUWS
         L     R3,SZUW
         XR    R15,R15
         BASR  R14,0
         MVCL  R2,R14
         XC    CURCE,CURCE
         XC    CURTW,CURTW
         XC    CURUW,CURUW
         MVC   MODNME,MMNME
         NI    SW3,255-SWLST
         LR    R4,R5
SCLM4    MVC   RNNME,MMNME
         MVC   RNMNM,MODNME
         MVC   RNTYP,MMTYP
         MVC   RNEPA,MMEPA
         STCM  R7,B'1111',RNAMD
         ST    R5,MDRNE
         ST    R6,CHKMA+4
         AH    R6,EMML
         CL    R6,MDMME
         BH    SCLM10
         CLC   CURTTR,MMTTR
         BNE   SCLM10
         AH    R5,ERNL
         CL    R5,ARNE
         BL    SCLM4
SCLM5    LA    R1,MELM1
         BAS   R9,PERR
         B     SCLM80
SCLM10   XC    WORKD,WORKD
         TM    SW3,SWLST
         BO    SCLM40              LAST IDR PROCESSED, SO END
        $PLREAD WORKD,DDNME
         LTR   R15,R15
         BZ    SCLM11
         BM    SCLM40
         STC   R15,MELM4+53
         OI    MELM4+53,C'0'
         MVC   MELM4+23(L'MODNME),MODNME
         MVC   MELM4+5(L'DDNME),DDNME
         LA    R1,MELM4
         BAS   R9,PERR
         B     SCLM90
SCLM11   LM    R2,R3,WORKD
         LTR   R3,R3
         BNP   SCLM10
         TM    0(R2),X'01'
         BO    SCLM40              JUST BEFORE TEXT, SO END
         USING CESDR,R2
         CLI   ESDI,ESDIV
         BNE   SCLM30              NOT CESD RECORD
         ALR   R3,R2               POINT TO END+1 OF RECORD
         LH    R0,ESDCN            GET LENGTH OF ESD DATA
         LA    R2,ESDDT            POINT TO ESD DATA
         DROP  R2
         AR    R0,R2               POINT TO END+1 OF RECORD
         CLR   R0,R3
         BE    SCLM20              CHECK IS OK
         MVC   MELM5+35(3),=CL3'ESD'
SCLM12   MVC   MELM5+23(L'MODNME),MODNME
         MVC   MELM5+5(L'DDNME),DDNME
         LA    R1,MELM5
         BAS   R9,PERR
         B     SCLM90
         USING CESDD,R2
SCLM20   CLR   R3,R2
         BNH   SCLM10              END, NEXT RECORD
         MVC   CKCODE,ESDDTY
         NI    CKCODE,X'0F'
         CLI   CKCODE,ESDDTSD
         BE    *+L'*+8             SECTION DEFINITION (SD)
         CLI   CKCODE,ESDDTLR
         BNE   SCLM24
         LR    R1,R4               LABEL REFERENCE (LR)
SCLM21   CLC   RNNME-RNENT(L'RNNME,R1),ESDDNM
         BNE   *+L'*+10
         CLC   RNEPA-RNENT(L'RNEPA,R1),ESDDAA
         BE    SCLM22              FOUND
         CLR   R1,R5
         BE    SCLM23              NOT FOUND
         AH    R1,ERNL
         B     SCLM21
SCLM22   L     R14,CHKMA           MODULE OR ALIAS IS ENTRY
         CLC   MMNME-MMENT(L'MMNME,R14),RNNME-RNENT(R1)
         BE    *+L'*+16
         CL    R14,CHKMA+4
         BNL   SCLM29
         AH    R14,EMML
         B     *-22
         OI    MMFLG-MMENT(R14),MMFEP
         B     SCLM29
SCLM23   AH    R5,ERNL             ADD ENTRY POINT
         CL    R5,ARNE
         BNL   SCLM5
         MVI   RNTYP,RNEP
         MVC   RNEPA(L'RNEPA),ESDDAA
         B     SCLM25
SCLM24   CLI   CKCODE,ESDDTER
         BNE   SCLM26
         AH    R5,ERNL             EXTERNAL REFERENCE (ER)
         CL    R5,ARNE
         BNL   SCLM5
         MVI   RNTYP,RNEX
SCLM25   MVC   RNNME(L'RNNME),ESDDNM
         MVC   RNMNM,MODNME
         STCM  R7,B'1111',RNAMD
         ST    R5,MDRNE
         B     SCLM29
SCLM26   CLI   CKCODE,ESDDTPC
         BE    SCLM28
         CLI   CKCODE,ESDDTCM
         BE    SCLM29
         CLI   CKCODE,ESDDTPR
         BE    SCLM29
         CLI   CKCODE,ESDDTNL
         BE    SCLM29
         CLI   CKCODE,ESDDTWX
         BE    SCLM29
         MVC   MELM8+16(4),=CL4'CESD'
SCLM27   IC    R1,CKCODE
         STC   R1,MELM8+30
         SRL   R1,4
         STC   R1,MELM8+29
         NC    MELM8+29(2),=XL2'0F0F'
         TR    MELM8+29(2),TRHEX
         LA    R1,MELM8
         BAS   R9,PERR
         B     SCLM80
SCLM28   L     R14,CHKMA
         TM    MMFLG-MMENT(R14),MMFEP
         BO    SCLM29
         CLC   ESDDAA,MMEPA-MMENT(R14)
         BNE   SCLM29
         OI    MMFLG-MMENT(R14),MMFPV
SCLM29   ICM   R1,B'1111',CURCE    ADD CESD SYMBOL IN TABLE
         BNZ   *+L'*+8
         L     R1,ACES
         B     *+L'*+4
         AH    R1,ECEL
         CL    R1,ACEE
         BL    *+L'*+12
         LA    R1,MELM7
         BAS   R9,PERR
         B     SCLM80
         USING CEENT,R1
         MVC   CESYM,ESDDNM
         DROP  R1
         ST    R1,CURCE
         CL    R1,CCEPTR
         BNH   *+L'*+4
         ST    R1,CCEPTR
         LA    R2,CESDDL(R2)       NEXT ESD
         B     SCLM20
         DROP  R2
         USING CIDR,R2
SCLM30   CLI   IDRI,IDRIV
         BNE   SCLM10              NOT CIDR RECORD
         ALR   R3,R2               POINT TO END+1 OF RECORD
         XR    R1,R1
         IC    R1,IDRCN            GET LENGTH OF IDR DATA
         LA    R0,IDRCN(R1)        POINT TO END+1 OF RECORD
         CLR   R0,R3
         BE    *+L'*+10            CHECK IS OK
         MVC   MELM5+35(3),=CL3'IDR'
         B     SCLM12
         MVC   CKCODE,IDRSI
         LA    R2,IDRDT            POINT TO IDR DATA
         DROP  R2
         TM    CKCODE,IDRLST
         BZ    *+L'*+4
         OI    SW3,SWLST
         NI    CKCODE,255-IDRLST
         CLI   CKCODE,IDRHZP
         BE    SCLM31              HMASPZAP DATA
         CLI   CKCODE,IDRLKE
         BE    SCLM33              LINKAGE EDITOR DATA
         CLI   CKCODE,IDRTRS
         BE    SCLM34              TRANSLATOR DATA
         CLI   CKCODE,IDRUSR
         BE    SCLM35              USER (IDENTIFY) DATA
         MVC   MELM8+16(4),=CL4'CIDR'
         B     SCLM27
         USING HZPD,R2
SCLM31   MVC   CKCODE,HZPFC        FLAGS AND COUNT
         DROP  R2
         NI    CKCODE,X'3F'        ISOLATE COUNT
         LA    R2,L'HZPFC(R2)      POINT TO 1ST ENTRY
         XR    R15,R15
         IC    R15,CKCODE
         LTR   R15,R15
         BNP   SCLM10              NONE OR END, NEXT RECORD
         USING HZPDE,R2
SCLM32   XR    R14,R14
         ICM   R14,B'0011',HZPES   ESDID
         BZ    *+L'*+26
         BCT   R14,*+L'*+4
         B     *+L'*+4
         MH    R14,ECEL
         L     R1,ACES
         ALR   R1,R14
         CL    R1,CURCE
         BNH   *+L'*+28
         UNPK  MELM10+19(5),HZPES(L'HZPES+1)
         TR    MELM10+19(4),TRHEX-C'0'
         MVI   MELM10+23,C')'
         LA    R1,MELM10
         BAS   R9,PERR
         B     SCLM80
         USING CEENT,R1
         ICM   R14,B'1111',CZPPTR
         BNZ   *+L'*+8
         L     R14,AZPS
         B     *+L'*+4
         AH    R14,EZPL
         CL    R14,AZPE
         BL    *+L'*+12
         LA    R1,MELM9
         BAS   R9,PERR
         B     SCLM80
         USING ZPENT,R14
         MVI   ZPETY,ZPEID
         MVC   ZPNME,CESYM
         MVC   ZPDTE,HZPDT
         MVC   ZPDTA,HZPDH
         DROP  R1,R14
         ST    R14,CZPPTR
         L     R1,CHKMA
         LA    R1,MMCHA-MMENT(R1)
         L     R0,0(R1)
         LTR   R0,R0
         BZ    *+L'*+6
         LR    R1,R0
         B     *-12
         ST    R14,0(R1)
         BCT   R15,*+L'*+4
         B     SCLM10              END, NEXT RECORD
         LA    R2,HZPDEL(R2)
         CLR   R3,R2
         BH    SCLM32
         LA    R1,MELM11
         BAS   R9,PERR
         B     SCLM80
         DROP  R2
         USING LKED,R2
SCLM33   ICM   R14,B'1111',CLKPTR
         BNZ   *+L'*+8
         L     R14,ALKS
         B     *+L'*+4
         AH    R14,ELKL
         CL    R14,ALKE
         BL    *+L'*+12
         LA    R1,MELM12
         BAS   R9,PERR
         B     SCLM80
         USING LKENT,R14
         MVI   LKETY,LKEID
         MVC   LKDTE,LKEDT
         DROP  R14
         ST    R14,CLKPTR
         L     R1,CHKMA
         LA    R1,MMCHA-MMENT(R1)
         L     R0,0(R1)
         LTR   R0,R0
         BZ    *+L'*+6
         LR    R1,R0
         B     *-12
         ST    R14,0(R1)
         B     SCLM10              END, NEXT RECORD
         DROP  R2
         USING TRSD,R2
SCLM34   SH    R1,=Y(L'IDRCN+L'IDRSI)
         BNP   SCLM10              NONE, NEXT RECORD
         ICM   R14,B'1111',CURTW
         BNZ   *+L'*+4
         L     R14,ATWS
         LA    R15,0(R14,R1)
         CL    R15,ATWE
         BL    *+L'*+12
         LA    R1,MELM15
         BAS   R9,PERR
         B     SCLM80
         BCT   R1,*+L'*+6
         MVC   0(*-*,R14),TRSDES   <<EXECUTED>>
         EX    R1,*-6
         ST    R15,CURTW
         CL    R15,CTWPTR
         BNH   SCLM10              END, NEXT RECORD
         ST    R15,CTWPTR
         B     SCLM10              END, NEXT RECORD
         DROP  R2
         USING USRD,R2
SCLM35   SH    R1,=Y(L'IDRCN+L'IDRSI)
         BNP   SCLM10              NONE, NEXT RECORD
         ICM   R14,B'1111',CURUW
         BNZ   *+L'*+4
         L     R14,AUWS
         LA    R15,0(R14,R1)
         CL    R15,AUWE
         BL    *+L'*+12
         LA    R1,MELM16
         BAS   R9,PERR
         B     SCLM80
         BCT   R1,*+L'*+6
         MVC   0(*-*,R14),USRES    <<EXECUTED>>
         EX    R1,*-6
         ST    R15,CURUW
         CL    R15,CUWPTR
         BNH   SCLM10              END, NEXT RECORD
         ST    R15,CUWPTR
         B     SCLM10              END, NEXT RECORD
         DROP  R2
SCLM40   ICM   R3,B'1111',CURTW
         BZ    SCLM50
         L     R2,ATWS             PROCESS TRANSLATOR IDR'S
         USING TRSD,R2
SCLM41   LR    R1,R2
         TM    TRSDES,TRSLST
         LA    R2,L'TRSDES(R2)
         BO    *+L'*+10
         CLR   R2,R3
         BL    *-14
         B     SCLM44              BUFFER SHORTAGE
         DROP  R2
         CLR   R2,R3
         BNL   SCLM44              BUFFER SHORTAGE
         LA    R0,TRSDSL(R2)
         CLR   R0,R3
         BH    SCLM44              BUFFER SHORTAGE
         USING TRSD,R1
         USING TRSDS,R2
SCLM42   MVC   WORKD(L'TRSDES),TRSDES ESDID
         NI    WORKD,255-TRSLST
         XR    R14,R14
         ICM   R14,B'0011',WORKD
         BZ    *+L'*+26
         BCT   R14,*+L'*+4
         B     *+L'*+4
         MH    R14,ECEL
         L     R15,ACES
         ALR   R15,R14
         CL    R15,CURCE
         BNH   *+L'*+28
         UNPK  MELM17+19(5),TRSDES(L'TRSDES+1)
         TR    MELM17+19(4),TRHEX-C'0'
         MVI   MELM17+23,C')'
         LA    R1,MELM17
         BAS   R9,PERR
         B     SCLM80
         USING CEENT,R15
         CLC   CESYM,=CL8' '
         BE    SCLM43
         CLC   CESYM,=XL8'0'
         BE    SCLM43
         LR    R14,R4
         USING RNENT,R14
         CLC   RNNME,CESYM
         BE    *+L'*+14
         CLR   R14,R5
         BE    SCLM45              NOT FOUND
         AH    R14,ERNL
         B     *-20
         MVC   RNEDT,TRSDSDT
         DROP  R14,R15
SCLM43   TM    TRSDES,TRSLST
         BO    *+L'*+8
         LA    R1,L'TRSDES(R1)
         B     SCLM42
         DROP  R1
         TM    TRSDSID,TRSDSTT
         BZ    *+L'*+4
         LA    R2,TRSDSL-L'TRSDSID(R2)
         LA    R2,TRSDSL(R2)
         CLR   R2,R3
         BL    SCLM41
         BE    SCLM50
         DROP  R2
SCLM44   LA    R1,MELM18           BUFFER SHORTAGE
         BAS   R9,PERR
         B     SCLM80
         USING CEENT,R15
SCLM45   MVC   MELM19+33(L'CESYM),CESYM
         DROP  R15
         LA    R1,MELM19+L'CESYM+32
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(11,R1),=CL11'" NOT FOUND'
         LA    R1,MELM19
         BAS   R9,PERR
         B     SCLM80
SCLM50   OC    CURUW,CURUW
         BZ    SCLM60
         L     R3,CHKMA            PROCESS USER (IDENTIFY) IDR'S
         LA    R3,MMCHA-MMENT(R3)
         L     R0,0(R3)            SEARCH CHAIN ANCHOR
         LTR   R0,R0
         BZ    *+L'*+6
         LR    R3,R0
         B     *-12
         L     R2,AUWS
         USING USRD,R2
SCLM51   LA    R0,USRDF
         CL    R0,CURUW
         BNL   SCLM54              BUFFER SHORTAGE
         XR    R14,R14
         ICM   R14,B'0011',USRES   ESDID
         BZ    *+L'*+26
         BCT   R14,*+L'*+4
         B     *+L'*+4
         MH    R14,ECEL
         L     R1,ACES
         ALR   R1,R14
         CL    R1,CURCE
         BNH   *+L'*+28
         UNPK  MELM13+19(5),USRES(L'USRES+1)
         TR    MELM13+19(4),TRHEX-C'0'
         MVI   MELM13+23,C')'
         LA    R1,MELM13
         BAS   R9,PERR
         B     SCLM80
         USING CEENT,R1
         CLC   CESYM,=CL8' '
         BE    *+L'*+10
         CLC   CESYM,=XL8'0'
         BNE   SCLM53
         CLI   USRCN,L'USDTA
         BNH   *+L'*+28
SCLM52   UNPK  MELM20+13(5),USRES(L'USRES+1)
         TR    MELM20+13(4),TRHEX-C'0'
         MVI   MELM20+17,C' '
         LA    R1,MELM13
         BAS   R9,PERR
         B     SCLM80
         XR    R15,R15
         IC    R15,USRCN
         LTR   R15,R15
         BNP   SCLM52
         LA    R2,USRDF(R15)
         B     SCLM55
SCLM53   ICM   R14,B'1111',CUSPTR
         BNZ   *+L'*+8
         L     R14,AUSS
         B     *+L'*+4
         AH    R14,EUSL
         CL    R14,AUSE
         BL    *+L'*+12
         LA    R1,MELM14
         BAS   R9,PERR
         B     SCLM80
         USING USENT,R14
         MVI   USETY,USEID
         MVC   USNME,CESYM
         MVC   USDTE,USRDT
         CLI   USRCN,L'USDTA
         BH    SCLM52
         XR    R15,R15
         IC    R15,USRCN
         STC   R15,USDTL
         LTR   R15,R15
         BNP   SCLM52
         LA    R0,USRDF(R15)
         CL    R0,CURUW
         BNH   *+L'*+12
SCLM54   LA    R1,MELM21           BUFFER SHORTAGE
         BAS   R9,PERR
         B     SCLM80
         BCT   R15,*+L'*+6
         MVC   USDTA(*-*),USRDF    <<EXECUTED>>
         EX    R15,*-6
         DROP  R1,R14
         ST    R14,CUSPTR
         ST    R14,0(R3)           SET CHAINING
         LR    R3,R14
         LA    R2,USRDF+1(R15)
SCLM55   CL    R2,CURUW
         BL    SCLM51
         DROP  R2
SCLM60   CL    R6,MDMME
         BH    SCLM61
         AH    R5,ERNL
         CL    R5,ARNE
         BL    SCLM2
         B     SCLM5
SCLM61   L     R3,MDRNE
         AH    R3,ERNL
         SL    R3,MDRNS
         XR    R2,R2
         XC    WORKD,WORKD
         MVC   WORKD+4-L'ERNL(L'ERNL),ERNL
         D     R2,WORKD
         L     R2,MDRNS
        $SORT  (R2),(R3),RNLGTH,0,L'RNMNM+L'RNTYP+L'RNNME
         B     SCLM90
SCLM80   MVC   MELM0+39(L'MODNME),MODNME
         MVC   MELM0+14(L'DDNME),DDNME
         LA    R1,MELM0
         BAS   R9,PERR
SCLM90  $PLCLOSE DDNME
         LTR   R15,R15
         BZ    SCLM91
         STC   R15,MELM6+43
         OI    MELM6+43,C'0'
         MVC   MELM6+5(L'DDNME),DDNME
         LA    R1,MELM6
         BAS   R9,PERR
         B     LEAVE
SCLM91   TM    SW2,SWERR
         BO    LEAVE
         AH    R7,EMDL
         BCT   R8,SCLM1
         ST    R5,CRNPTR
         B     SCLMX
         DROP  R5,R6,R7
         SPACE 1
MELM0   WTO    'DD-NAME :          / READ MODULE :          ',         X
               ROUTCDE=11,DESC=7,MF=L
MELM1   WTO    'TOO MANY REFERENCES NAMES ("RN" VALUE OVERFLOW)',      X
               ROUTCDE=11,DESC=7,MF=L
MELM2   WTO    '"        " / OPEN ERROR ($PLOPEN RC =  )',             X
               ROUTCDE=11,DESC=7,MF=L
MELM3   WTO    '"        " / POINT "        " ERROR ($PLPOINT RC =  )',X
               ROUTCDE=11,DESC=7,MF=L
MELM4   WTO    '"        " / READ "        " ERROR ($PLREAD RC =  )',  X
               ROUTCDE=11,DESC=7,MF=L
MELM5   WTO    '"        " / READ "        " /     WRONG LENGTH',      X
               ROUTCDE=11,DESC=7,MF=L
MELM6   WTO    '"        " / CLOSE ERROR ($PLCLOSE RC =  )',           X
               ROUTCDE=11,DESC=7,MF=L
MELM7   WTO    'TOO MANY "CESD" SYMBOLS ("CE" VALUE OVERFLOW)',        X
               ROUTCDE=11,DESC=7,MF=L
MELM8   WTO    'UNEXPECTED "    " TYPE :    (NOT HANDLED BY PROGRAM)', X
               ROUTCDE=11,DESC=7,MF=L
MELM9   WTO    'TOO MANY HMASPZAP IDR''S ("ZP" VALUE OVERFLOW)',       X
               ROUTCDE=11,DESC=7,MF=L
MELM10  WTO    'INVALID ESDID (      IN HMASPZAP IDR ENTRY',           X
               ROUTCDE=11,DESC=7,MF=L
MELM11  WTO    'HMASPZAP IDR NUMBER OF ENTRIES EXCEED RECORD SIZE',    X
               ROUTCDE=11,DESC=7,MF=L
MELM12  WTO    'TOO MANY LINKAGE IDR''S ("LK" VALUE OVERFLOW)',        X
               ROUTCDE=11,DESC=7,MF=L
MELM13  WTO    'INVALID ESDID (      IN USER (IDENTIFY) IDR ENTRY',    X
               ROUTCDE=11,DESC=7,MF=L
MELM14  WTO    'TOO MANY USER (IDENTIFY) IDR''S ("US" VALUE OVERFLOW)',X
               ROUTCDE=11,DESC=7,MF=L
MELM15  WTO    'TRANSLATOR IDR''S WORK BUFFER TOO SHORT ("TW" VALUE OVEX
               RFLOW)',ROUTCDE=11,DESC=7,MF=L
MELM16  WTO    'USER (IDENTIFY) IDR''S WORK BUFFER TOO SHORT ("UW" VALUX
               E OVERFLOW)',ROUTCDE=11,DESC=7,MF=L
MELM17  WTO    'INVALID ESDID (      IN TRANSLATOR IDR ENTRY',         X
               ROUTCDE=11,DESC=7,MF=L
MELM18  WTO    'TRANSLATOR IDR''S WORK BUFFER DATA SHORTAGE',          X
               ROUTCDE=11,DESC=7,MF=L
MELM19  WTO    'TRANSLATOR IDR''S ESDID NAME "                    ',   X
               ROUTCDE=11,DESC=7,MF=L
MELM20  WTO    'AT ESDID      WRONG LENGTH FOR USER (IDENTIFY) DATA FIEX
               LD',ROUTCDE=11,DESC=7,MF=L
MELM21  WTO    'USER (IDENTIFY) IDR''S WORK BUFFER DATA SHORTAGE',     X
               ROUTCDE=11,DESC=7,MF=L
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*         P R I N T     M O D U L E S     R E F E R E N C E S         *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- PRINT COVER (TITLE'S) PAGES
         SPACE 1
PRMR0    L     R3,VLLN
         LA    R1,2
         SR    R3,R1
         XR    R2,R2
         LA    R1,3
         DR    R2,R1
         AR    R2,R3
         BCTR  R3,0
         LA    R4,2                PRINT TWICE THE COVER PAGE
TTLP1   $PRINT COV1,,TTLDD
         LTR   R15,R15
         BZ    *+L'*+10
TTLPERR  MVC   MERR1+5(L'TTLDD),TTLDD
         B     LSTERR
        $PRINT COV2,,TTLDD
         LTR   R15,R15
         BNZ   TTLPERR
         LR    R5,R3
TTLP2   $PRINT COV5,,TTLDD
         LTR   R15,R15
         BNZ   TTLPERR
         BCT   R5,TTLP2
        $PRINT COV3,,TTLDD
         LTR   R15,R15
         BNZ   TTLPERR
         LR    R5,R2
TTLP3   $PRINT COV5,,TTLDD
         LTR   R15,R15
         BNZ   TTLPERR
         BCT   R5,TTLP3
        $PRINT COV4,,TTLDD
         LTR   R15,R15
         BNZ   TTLPERR
         BCT   R4,TTLP1
         SPACE 1
*------- PRINT LOAD MODULES REFERENCES
         SPACE 1
         ZAP   IXLN,=PL2'+99'
         L     R7,AMDS
         USING MDENT,R7
         L     R8,AMDN
PRMR1    L     R3,MDMME
         AH    R3,EMML
         SL    R3,MDMMS
         XR    R2,R2
         XC    WORKD,WORKD
         MVC   WORKD+4-L'EMML(L'EMML),EMML
         D     R2,WORKD
         L     R2,MDMMS
        $SORT  (R2),(R3),MMLGTH,0+L'MMTTR+L'MMTYP,L'MMNME
         ZAP   MRLN,=PL2'+99'
         MVC   MRT2N,MDDSN
         MVC   MRT2V,MDVOL
         ZAP   CTRL,MRPG
         AP    CTRL,=PL1'+1'
         MVC   IXT3D,MDDSN
         MVC   IXT3V,MDVOL
         MVI   IXT3T,C'M'
         MVC   IXT3P,=XL6'402020202120'
         ED    IXT3P,CTRL
         MVI   IXT3P,C'/'
         BAS   R9,IXPRT
         L     R6,MDRNS
         USING RNENT,R6
PRMR2    MVC   MODNME,RNMNM
         MVI   CKCODE,0
         OI    SW3,SWMNP
         MVC   MRT4M,MODNME
         L     R4,MDMMS
         USING MMENT,R4
PRMR3    CLC   RNNME,MMNME
         BE    PRMR4
         BL    *+L'*+12
         AH    R4,EMML
         CL    R4,MDMME
         BNH   PRMR3
         MVC   MEMR1+42(L'RNNME),RNNME MODULE NAME NOT FOUND ?
         LA    R1,MEMR1+41+L'RNNME
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(2,R1),=CL2'")'
         LA    R1,MEMR1
         BAS   R9,PERR
         B     LEAVE
PRMR4    ICM   R1,B'1111',MMCHA
         BZ    PRMR5
         USING LKENT,R1
         CLI   LKETY,LKEID
         BE    *+L'*+12
         ICM   R1,B'1111',LKCHA    OR ZPCHA OR USCHA
         BNZ   *-12
         B     PRMR5
         MVC   MRT4T(6),=XL6'21204B202020'
         ED    MRT4T(6),LKDTE
         DROP  R1
PRMR5    TM    MMFLG,MMFEP
         BZ    PRMR10
         CLI   RNTYP,RNMO
         BNE   *+L'*+14
         MVC   MRT4R(30),=CL30'MODULE NAME IS ALSO ENTRY NAME'
         BAS   R9,MRPRT
         B     PRMR20
         MVC   MRT4R(29),=CL29'ALIAS NAME IS ALSO ENTRY NAME'
         BAS   R9,MRPRT
         B     PRMR14
PRMR10   LR    R5,R6
PRMR11   AH    R5,ERNL
         CL    R5,MDRNE
         BH    PRMR15
         CLC   RNMNM-RNENT(L'RNMNM,R5),MODNME
         BNE   PRMR15
         CLI   RNTYP-RNENT(R5),RNAL
         BE    *+L'*+8
         CLI   RNTYP-RNENT(R5),RNEP
         BNE   PRMR11
         CLC   MMEPA,RNEPA-RNENT(R5)
         BNE   PRMR11
         CLI   RNTYP,RNMO
         BNE   PRMR12
         MVC   MRT4R(23),=CL23'MODULE NAME DEFAULTS TO'
         CLI   RNTYP-RNENT(R5),RNAL
         BNE   *+L'*+10
         MVC   MRT4R+24(5),=CL5'ALIAS'
         B     *+L'*+6
         MVC   MRT4R+24(5),=CL5'ENTRY'
         LA    R1,MRT4R+30
         B     PRMR13
PRMR12   MVC   MRT4R(22),=CL22'ALIAS NAME DEFAULTS TO'
         CLI   RNTYP-RNENT(R5),RNAL
         BNE   *+L'*+10
         MVC   MRT4R+23(5),=CL5'ALIAS'
         B     *+L'*+6
         MVC   MRT4R+23(5),=CL5'ENTRY'
         LA    R1,MRT4R+29
PRMR13   MVI   0(R1),C'"'
         MVC   1(L'RNNME,R1),RNNME-RNENT(R5)
         LA    R1,L'RNNME(R1)
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   1(R1),C'"'
         BAS   R9,MRPRT
         CLI   RNTYP,RNMO
         BE    PRMR20
PRMR14   CLC   RNMNM,MMONM
         BE    PRMR20
         MVC   MRT4R(26),=CL26'ORIGINAL MODULE NAME WAS "'
         MVC   MRT4R+26(L'MMONM),MMONM
         LA    R1,MRT4R+L'MMONM+25
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVI   1(R1),C'"'
         BAS   R9,MRPRT
         B     PRMR20
PRMR15   TM    MMFLG,MMFPV
         BZ    PRMR16
         CLI   RNTYP,RNMO
         BNE   *+L'*+16
         MVC   MRT4R(23),=CL23'MODULE NAME DEFAULTS TO'
         MVC   MRT4R+24(10),=CL10'"$PRIVATE"'
         B     *+L'*+12
         MVC   MRT4R(22),=CL22'ALIAS NAME DEFAULTS TO'
         MVC   MRT4R+23(10),=CL10'"$PRIVATE"'
         BAS   R9,MRPRT
         B     PRMR20
PRMR16   CLI   RNTYP,RNMO
         BNE   PRMR17
         MVC   MEMR2+40(L'MODNME),MODNME
         LA    R1,MEMR2+39+L'MODNME
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(2,R1),=CL2'")'
         LA    R1,MEMR2
         BAS   R9,PERR
         B     LEAVE
PRMR17   MVC   MEMR3+39(L'MODNME),MODNME
         LA    R1,MEMR3+38+L'MODNME
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(2,R1),=CL2'")'
         LA    R1,MEMR3
         BAS   R9,PERR
         B     LEAVE
PRMR20   ICM   R2,B'1111',MMCHA
         DROP  R4
         BZ    PRMR30
         USING ZPENT,R2
PRMR21   CLI   ZPETY,ZPEID
         BNE   PRMR22
         MVC   MRT4R(7),=CL7'CSECT "'
         MVC   MRT4R+7(L'ZPNME),ZPNME
         LA    R1,MRT4R+L'ZPNME+6
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(8,R1),=CL8'" ZAPPED'
         MVC   10(6,R1),=XL6'21204B202020'
         ED    10(6,R1),ZPDTE
         MVC   17(6,R1),=CL6'- ID :'
         MVC   24(L'ZPDTA,R1),ZPDTA
         DROP  R2
         B     PRMR25
         USING LKENT,R2
PRMR22   CLI   LKETY,LKEID
         BE    PRMR25+L'PRMR25
         DROP  R2
         USING USENT,R2
         CLI   USETY,USEID
         BE    PRMR23
         IC    R1,USETY
         STC   R1,MEMR4+34
         SRL   R1,4
         STC   R1,MEMR4+33
         NC    MEMR4+33(2),=XL2'0F0F'
         TR    MEMR4+33(2),TRHEX
         LA    R1,MEMR4
         BAS   R9,PERR
         B     LEAVE
PRMR23   MVC   MRT4R(7),=CL7'CSECT "'
         MVC   MRT4R+7(L'USNME),USNME
         LA    R1,MRT4R+L'ZPNME+6
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(10,R1),=CL10'" IDENTIFY'
         MVC   12(6,R1),=XL6'21204B202020'
         ED    12(6,R1),USDTE
         XR    R3,R3
         IC    R3,USDTL
         LTR   R3,R3
         BNP   PRMR24
         LA    R0,25(R1,R3)
         CL    R0,=A(MRT4R+NMRT4R*L'MRT4R-1)
         BH    PRMR24
         MVC   19(6,R1),=CL6'- ID :'
         BCT   R3,*+L'*+6
         MVC   26(*-*,R1),USDTA    <<EXECUTED>>
         EX    R3,*-6
         XR    R3,R3
PRMR24   BAS   R9,MRPRT
         LTR   R3,R3
         BNP   PRMR25+L'PRMR25
         BCT   R3,*+L'*+6
         MVC   MRT4R+11(*-*),USDTA <<EXECUTED>>
         EX    R3,*-6
         MVC   MRT4R+6(4),=CL4'ID :'
PRMR25   BAS   R9,MRPRT
         ICM   R2,B'1111',USCHA    OR ZPCHA OR LKCHA
         DROP  R2
         BNZ   PRMR21
PRMR30   NI    SW3,255-SWMNP
         OI    SW3,SWTYP
PRMR31   AH    R6,ERNL
         CL    R6,MDRNE
         BH    PRMR40
         CLC   RNMNM,MODNME
         BNE   PRMR33
         CLC   RNTYP,CKCODE
         BE    PRMR32
         CLI   MRT4R,C' '
         BE    *+L'*+4
         BAS   R9,MRPRT
         MVC   CKCODE,RNTYP
         CLI   CKCODE,RNAL
         BNE   *+L'*+10
         MVC   MRT4T,=CL7'ALIAS :'
         B     *+L'*+24
         CLI   CKCODE,RNEP
         BNE   *+L'*+10
         MVC   MRT4T,=CL7'ENTRY :'
         B     *+L'*+6
         MVC   MRT4T,=CL7'EXTRN :'
         LA    R4,MRT4R
         LA    R5,NMRT4R
PRMR32   MVC   0(L'RNNME,R4),RNNME
         TM    RNEDT+L'RNEDT-1,X'0F'
         BNO   *+L'*+12
         MVC   9(6,R4),=XL6'21204B202020'
         ED    9(6,R4),RNEDT
         LA    R4,L'MRT4R(R4)
         BCT   R5,PRMR31
         BAS   R9,MRPRT
         LA    R4,MRT4R
         LA    R5,NMRT4R
         B     PRMR31
PRMR33   CLI   MRT4R,C' '
         BE    *+L'*+4
         BAS   R9,MRPRT
         NI    SW3,255-SWTYP
         B     PRMR2
PRMR40   CLI   MRT4R,C' '
         BE    *+L'*+4
         BAS   R9,MRPRT
         NI    SW3,255-SWTYP
         AH    R7,EMDL
         BCT   R8,PRMR1
         DROP  R6,R7
         MVC   IXT3D(L'IXT3D+L'IXT3V+6),IXT3D-1
         BAS   R9,IXPRT
         MVC   IXT3D(38),=CL38'ALL LOAD MODULES NAMES REFERENCES LIST'
         MVC   IXT3T(7),=CL7'N/    1'
         BAS   R9,IXPRT
         B     PRMRX
         SPACE 1
MRPRT    TM    SW3,SWMNP           MODULES REFERENCES PRINT SUBROUTINE
         BO    *+L'*+10            R0-R1-R14-R15  = WORK REGISTERS
         CP    MRLN,MXLN3          R9 = LINK REGISTER
         B     *+L'*+6
         CP    MRLN,MXLN4
         BL    MRPRT1
         NI    SW3,255-SWMNP
         ZAP   MRLN,=PL1'+0'
         AP    MRPG,=PL1'+1'
         MVC   MRT1P,=XL6'402020202120'
         ED    MRT1P,MRPG
         MVI   MRT1P,C'/'
        $PRINT MRT1,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
        $PRINT MRT1U,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
        $PRINT MRT2,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
        $PRINT MRT2U,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
        $PRINT MRT3,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
        $PRINT MRT3U,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
        $PRINT MRT3A,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
        $PRINT MRT3S,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
         CLI   MRT4M,C' '
         BNE   *+L'*+6
         MVC   MRT4M+2(3),=CL3'" "'
         TM    SW3,SWTYP
         BZ    MRPRT1
         CLI   MRT4T,C' '
         BNE   MRPRT1
         MVC   MRT4T+2(3),=CL3'" "'
MRPRT1   TM    SW3,SWMNP
         BZ    MRPRT2
         MVI   MRT4,C'0'
         NI    SW3,255-SWMNP
         AP    MRLN,=PL1'+1'
MRPRT2  $PRINT MRT4,,LMLDD
         LTR   R15,R15
         BNZ   MRPRTX
         AP    MRLN,=PL1'+1'
         MVI   MRT4,C' '
         MVC   MRT4M(L'MRT4M+L'MRT4T+NMRT4R*L'MRT4R+5),MRT4M-1
         BR    R9
MRPRTX   MVC   MERR1+5(L'LMLDD),LMLDD
         B     LSTERR
         SPACE 1
IXPRT    CP    IXLN,MXLN1          INDEX PRINT SUBROUTINE
         BL    IXPRT1              R0-R1-R14-R15  = WORK REGISTERS
         ZAP   IXLN,=PL1'+0'       R9 = LINK REGISTER
         AP    IXPG,=PL1'+1'
         MVC   IXT1P,=XL6'402020202120'
         ED    IXT1P,IXPG
         MVI   IXT1P,C'/'
        $PRINT IXT1,,TTLDD
         LTR   R15,R15
         BNZ   IXPRTX
        $PRINT IXT1U,,TTLDD
         LTR   R15,R15
         BNZ   IXPRTX
        $PRINT IXT2,,TTLDD
         LTR   R15,R15
         BNZ   IXPRTX
        $PRINT IXT2U,,TTLDD
         LTR   R15,R15
         BNZ   IXPRTX
IXPRT1  $PRINT IXT3,,TTLDD
         LTR   R15,R15
         BNZ   IXPRTX
         AP    IXLN,=PL1'+1'
         MVC   IXT3+1(L'IXT3-1),IXT3
         BR    R9
IXPRTX   MVC   MERR1+5(L'TTLDD),TTLDD
         B     LSTERR
         SPACE 1
MEMR1   WTO    'UNEXPECTED NOT FOUND CONDITION (NAME "           ',    X
               ROUTCDE=11,DESC=7,MF=L
MEMR2   WTO    'MODULE DEFAULT ENTRY FAILURE (NAME "           ',      X
               ROUTCDE=11,DESC=7,MF=L
MEMR3   WTO    'ALIAS DEFAULT ENTRY FAILURE (NAME "           ',       X
               ROUTCDE=11,DESC=7,MF=L
MEMR4   WTO    'UNKNOWN ELEMENT TYPE (ID = X''  '') FAILURE',          X
               ROUTCDE=11,DESC=7,MF=L
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*           P R I N T     N A M E S     R E F E R E N C E S           *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- PRINT ALL NAMES REFERENCES
         SPACE 1
PRNR0    L     R2,ARNS
         USING RNENT,R2
PRNR1    XC    RNMNM,RNNME         ALTERNATE FOR NEXT SORT
         XC    RNNME,RNMNM
         XC    RNMNM,RNNME
         CL    R2,CRNPTR
         BNL   *+L'*+8
         AH    R2,ERNL
         B     PRNR1
         DROP  R2
         L     R3,CRNPTR
         AH    R3,ERNL
         SL    R3,ARNS
         XR    R2,R2
         XC    WORKD,WORKD
         MVC   WORKD+4-L'ERNL(L'ERNL),ERNL
         D     R2,WORKD
         L     R2,ARNS
        $SORT  (R2),(R3),RNLGTH,0,L'RNANME+L'RNTYP+L'RNAMNM
         ZAP   NRLN,=PL2'+99'
         MVI   WLNE,C' '
         MVC   WLNE+1(L'WLNE-1),WLNE
         LA    R3,WLNE
         L     R2,ARNS
         USING RNENT,R2
PRNR2    CLC   RNANME(L'RNANME),RNANME-RNENT(R3)
         BE    PRNR3
         OI    SW3,SWNRP
         MVC   MODNME,RNANME
         MVC   NRT3N,RNANME
PRNR3    CLI   RNTYP,RNMO
         BNE   *+L'*+10
         MVC   NRT3T,=CL6'MODULE'
         B     PRNR4
         CLI   RNTYP,RNAL
         BNE   *+L'*+10
         MVC   NRT3T,=CL6'ALIAS '
         B     PRNR4
         CLI   RNTYP,RNEP
         BNE   *+L'*+10
         MVC   NRT3T,=CL6'ENTRY '
         B     PRNR4
         MVC   NRT3T,=CL6'EXTRN '
PRNR4    TM    RNEDT+L'RNEDT-1,X'0F'
         BNO   *+L'*+12
         MVC   NRT3A,=XL6'21204B202020'
         ED    NRT3A,RNEDT
         TM    SW3,SWNRP
         BO    *+L'*+20
         CLC   RNAMNM(L'RNAMNM),RNAMNM-RNENT(R3)
         BNE   *+L'*+10
         MVC   NRT3M+2(3),=CL3'" "'
         B     *+L'*+6
         MVC   NRT3M(L'RNAMNM),RNAMNM
         TM    SW3,SWNRP
         BO    *+L'*+20
         CLC   RNAMD(L'RNAMD),RNAMD-RNENT(R3)
         BNE   *+L'*+10
         MVC   NRT3D+2(3),=CL3'" "'
         B     PRNR5
         ICM   R4,B'1111',RNAMD
         USING MDENT,R4
         MVC   NRT3D,MDDSN
         LA    R1,NRT3D+L'NRT3D-1
         LR    R0,R1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         SR    R0,R1
         CH    R0,=Y(3+L'MDVOL)
         BL    PRNR5
         MVI   2(R1),C'/'
         MVC   4(L'MDVOL,R1),MDVOL
         DROP  R4
PRNR5    BAS   R9,NRPRT
         CL    R2,CRNPTR
         BNL   PRNRX
         LR    R3,R2
         AH    R2,ERNL
         B     PRNR2
         DROP  R2
         SPACE 1
NRPRT    TM    SW3,SWNRP           NAMES REFERENCES PRINT SUBROUTINE
         BO    *+L'*+10            R0-R1-R14-R15  = WORK REGISTERS
         CP    NRLN,MXLN1          R9 = LINK REGISTER
         B     *+L'*+6
         CP    NRLN,MXLN2
         BL    NRPRT1
         NI    SW3,255-SWNRP
         ZAP   NRLN,=PL1'+0'
         AP    NRPG,=PL1'+1'
         MVC   NRT1P,=XL6'402020202120'
         ED    NRT1P,NRPG
         MVI   NRT1P,C'/'
        $PRINT NRT1,,LMLDD
         LTR   R15,R15
         BNZ   NRPRTX
        $PRINT NRT1U,,LMLDD
         LTR   R15,R15
         BNZ   NRPRTX
        $PRINT NRT2,,LMLDD
         LTR   R15,R15
         BNZ   NRPRTX
        $PRINT NRT2U,,LMLDD
         LTR   R15,R15
         BNZ   NRPRTX
         CLI   NRT3N,C' '
         BNE   *+L'*+6
         MVC   NRT3N+2(3),=CL3'" "'
         CLI   NRT3D,C' '
         BNE   NRPRT1
         MVC   NRT3D+2(3),=CL3'" "'
NRPRT1   TM    SW3,SWNRP
         BZ    *+L'*+14
         MVI   NRT3,C'0'
         NI    SW3,255-SWNRP
         AP    NRLN,=PL1'+1'
        $PRINT NRT3,,LMLDD
         LTR   R15,R15
         BNZ   NRPRTX
         AP    NRLN,=PL1'+1'
         MVI   NRT3,C' '
         MVC   NRT3+1(L'NRT3-1),NRT3
         BR    R9
NRPRTX   MVC   MERR1+5(L'LMLDD),LMLDD
         B     LSTERR
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                       F I N A L     T R A C E                       *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         CNOP  0,8
         USING *,R10
         SPACE 1
*------- TRACE WORK-AREAS USE IF REQUESTED
         SPACE 1
FTRC0    TM    SW2,SWTAV
         BZ    LEAVE
        $PRINT FTRM0
         LTR   R15,R15
         BNZ   FTRERR
        $PRINT FTRM0U
         LTR   R15,R15
         BZ    FTRC2
FTRERR   STC   R15,PERRWT+46
         OI    PERRWT+46,C'0'
        WTO    MF=(E,PERRWT)
         OI    SW2,SWERR
         B     LEAVE
FTRC1    LR    R1,R2
         XR    R0,R0
         LA    R14,L'FTRM1A
         LA    R15,FTRM1A
         SLDL  R0,4
         STC   R0,0(R15)
         LA    R15,1(R15)
         BCT   R14,*-12
         NC    FTRM1A,=XL8'0F0F0F0F0F0F0F0F'
         TR    FTRM1A,TRHEX
         SR    R3,R2
         LR    R1,R3
         XR    R0,R0
         LA    R14,L'FTRM1B
         LA    R15,FTRM1B
         SLDL  R0,4
         STC   R0,0(R15)
         LA    R15,1(R15)
         BCT   R14,*-12
         NC    FTRM1B,=XL8'0F0F0F0F0F0F0F0F'
         TR    FTRM1B,TRHEX
         MVI   FTRM1C+16,C' '
         MVC   FTRM1C+17(L'FTRM1C-17),FTRM1C+16
         CVD   R3,WORKD
         MVC   FTRM1C(16),=XL16'40202020202020202020202020202120'
         ED    FTRM1C(16),WORKD
         LA    R0,15
         CLI   FTRM1C,C' '
         BNE   *+L'*+14
         MVC   FTRM1C(15),FTRM1C+1
         MVI   FTRM1C+15,C' '
         BCT   R0,*-18
         LA    R1,FTRM1C
         CLI   0(R1),C' '
         BE    *+L'*+8
         LA    R1,1(R1)
         B     *-12
         MVC   1(6,R1),=CL6'BYTES)'
        $PRINT FTRM1
         LTR   R15,R15
         BZR   R9
         B     FTRERR
FTRC2    MVC   FTRM1W,=CL2'MD'
         L     R2,AMDS
         L     R3,AMDN
         MH    R3,EMDL
         AR    R3,R2
         LR    R4,R3
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'MM'
         L     R2,AMMS
         SH    R4,EMDL
         USING MDENT,R4
         L     R3,MDMME
         DROP  R4
         AH    R3,EMML
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'RN'
         L     R2,ARNS
         L     R3,CRNPTR
         AH    R3,ERNL
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'ZP'
         L     R2,AZPS
         ICM   R3,B'1111',CZPPTR
         BNZ   *+L'*+6
         LR    R3,R2
         B     *+L'*+4
         AH    R3,EZPL
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'LK'
         L     R2,ALKS
         ICM   R3,B'1111',CLKPTR
         BNZ   *+L'*+6
         LR    R3,R2
         B     *+L'*+4
         AH    R3,ELKL
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'US'
         L     R2,AUSS
         ICM   R3,B'1111',CUSPTR
         BNZ   *+L'*+6
         LR    R3,R2
         B     *+L'*+4
         AH    R3,EUSL
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'CE'
         L     R2,ACES
         ICM   R3,B'1111',CCEPTR
         BNZ   *+L'*+6
         LR    R3,R2
         B     *+L'*+4
         AH    R3,ECEL
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'TW'
         L     R2,ATWS
         ICM   R3,B'1111',CTWPTR
         BNZ   *+L'*+2
         LR    R3,R2
         BAS   R9,FTRC1
         MVC   FTRM1W,=CL2'UW'
         L     R2,AUWS
         ICM   R3,B'1111',CUWPTR
         BNZ   *+L'*+2
         LR    R3,R2
         BAS   R9,FTRC1
         B     LEAVE
         SPACE 1
         DC    AL1(FTRM0L)
FTRM0    DC    C'0 REAL USE OF WORK-AREAS SPACE :'
FTRM0L   EQU   *-FTRM0
         DC    AL1(FTRM0UL)
FTRM0U   DC    C'+ ______________________________'
FTRM0UL  EQU   *-FTRM0U
         DC    AL1(FTRM1L)
FTRM1    DC    CL2'0 '
FTRM1W   DC    CL2' ',C' - AREA ADDRESS : '
FTRM1A   DC    CL8' ',C' LENGTH : '
FTRM1B   DC    CL8' ',C' ('
FTRM1C   DC    CL24' '
FTRM1L   EQU   *-FTRM1
         SPACE 1
*------- LITERAL POOL
         SPACE 1
         PRINT &PRS
        LTORG
         PRINT &PRF
         SPACE 1
         DROP  R10
         EJECT
*------- DSECT'S
         SPACE 1
MDENT    DSECT                     LOAD'S DD'S
MDNME    DS    CL8                 DD NAME
MDMMS    DS    A                   FIRST DIRECTORY NAME POINTER
MDMME    DS    A                   LAST DIRECTORY NAME POINTER
MDRNS    DS    A                   FIRST REFERENCE ELEMENT POINTER
MDRNE    DS    A                   LAST REFERENCE ELEMENT POINTER
MDDSN    DS    CL44                DS-NAME
MDVOL    DS    CL6                 VOLUME NAME
MDLGTH   EQU   (((*-MDENT)+3)/4)*4 ENTRY LENGTH
         SPACE 1
MMENT    DSECT                     DIRECTORIES NAMES
MMTTR    DS    XL3                 TTR OF FIRST BLOCK
MMTYP    DS    XL1                 NAME TYPE
MMMO     EQU   X'00'               NAME IS MODULE
MMAL     EQU   X'01'               NAME IS ALIAS
MMNME    DS    CL8                 NAME
MMONM    DS    CL8                 ORIGINAL NAME (ALIAS ONLY)
MMFLG    DS    XL1                 FLAG BYTE
MMFEP    EQU   X'01'               NAME IS ENTRY
MMFPV    EQU   X'02'               NAME IS $PRIVATE
MMEPA    DS    AL3                 ENTRY POINT ADDRESS
MMCHA    DS    A                   CHAIN ADDRESS (ZERO = NONE)
MMLGTH   EQU   (((*-MMENT)+3)/4)*4 ENTRY LENGTH
         SPACE 1
RNENT    DSECT                     REFERENCE ELEMENT
RNANME   DS    0CL8                ALTERNATE REFERENCE NAME
RNMNM    DS    CL8                 MODULE NAME (TO WHICH IT BELONGS)
RNTYP    DS    XL1                 REFERENCE TYPE
RNMO     EQU   X'00'               NAME IS MODULE
RNAL     EQU   X'01'               NAME IS ALIAS
RNEP     EQU   X'02'               NAME IS ENTRY POINT
RNEX     EQU   X'03'               NAME IS EXTERNAL
RNAMNM   DS    0CL8                ALTERNATE MODULE NAME
RNNME    DS    CL8                 REFERENCE NAME
RNAMD    DS    AL4                 ADDRESS OF "MD" ENTRY
RNEPA    DS    AL3                 ENTRY POINT ADDRESS
RNEDT    DS    PL3                 DATE COMPILATION/ASSEMBLY
RNLGTH   EQU   (((*-RNENT)+3)/4)*4 ENTRY LENGTH
         SPACE 1
ZPENT    DSECT                     ZAP ELEMENT
ZPCHA    DS    A                   CHAIN ADDRESS (ZERO = END OF CHAIN)
ZPETY    DS    XL1                 ZAP ELEMENT TYPE
ZPEID    EQU   X'00'                    ZAP ELEMENT ID
ZPDTE    DS    PL3                 DATE
ZPNME    DS    CL8                 CSECT NAME
ZPDTA    DS    CL8                 DATA
ZPLGTH   EQU   (((*-ZPENT)+3)/4)*4 ENTRY LENGTH
         SPACE 1
LKENT    DSECT                     LINKAGE EDITOR ELEMENT
LKCHA    DS    A                   CHAIN ADDRESS (ZERO = END OF CHAIN)
LKETY    DS    XL1                 LINKAGE EDITOR ELEMENT TYPE
LKEID    EQU   X'01'                    LINKAGE EDITOR ELEMENT ID
LKDTE    DS    PL3                 DATE
LKLGTH   EQU   (((*-LKENT)+3)/4)*4 ENTRY LENGTH
         SPACE 1
USENT    DSECT                     USER (IDENTIFY) ELEMENT
USCHA    DS    A                   CHAIN ADDRESS (ZERO = END OF CHAIN)
USETY    DS    XL1                 USER ELEMENT TYPE
USEID    EQU   X'02'                    USER ELEMENT ID
USDTE    DS    PL3                 DATE
USNME    DS    CL8                 CSECT NAME
USDTL    DS    XL1                 DATA LENGTH
USDTA    DS    CL40                DATA
USLGTH   EQU   (((*-USENT)+3)/4)*4 ENTRY LENGTH
         SPACE 1
CEENT    DSECT                     CESD ELEMENT
CESYM    DS    CL8                 CESD SYMBOL
CELGTH   EQU   (((*-CEENT)+3)/4)*4 ENTRY LENGTH
         EJECT
*------- LINKAGE EDITOR LOAD MODULE RECORD FORMATS
         SPACE 1
CESDR    DSECT ,              CESD RECORD -----------------------------
ESDI     DS    XL1                 IDENTIFICATION
ESDIV    EQU   X'20'               CESD ID VALUE
         DS    XL3                 SPARE (BINARY ZEROS)
ESDID    DS    XL2                 ESDID OF FIRST ESD ITEM
ESDCN    DS    XL2                 COUNT OF ESD DATA
ESDDT    DS    XL1                 ESD DATA (UP TO 240 BYTES)
         SPACE 1
CESDD    DSECT ,              CESD DATA - - - - - - - - - - - - - - - -
ESDDNM   DS    CL8                 EXTERNAL NAME
ESDDTY   DS    XL1                 TYPE
ESDDTSD  EQU   X'00'               SECTION DEFINITION (SD)
ESDDTER  EQU   X'02'               EXTERNAL REFERENCE (ER)
ESDDTLR  EQU   X'03'               LABEL REFERENCE (LR)
ESDDTPC  EQU   X'04'               PRIVATE CODE (PC)
ESDDTCM  EQU   X'05'               COMMON (CM)
ESDDTPR  EQU   X'06'               PSEUDO REGISTER (PR)
ESDDTNL  EQU   X'07'               NULL (NL)
ESDDTWX  EQU   X'0A'               WEAK EXTERNAL REFERENCE (WX)
ESDDAA   DS    AL3                 ASSIGNED ADDRESS OF NAME
ESDDSN   DS    XL1                 SEGMENT NUMBER
ESDDIL   DS    XL3                 ID/LENGTH
CESDDL   EQU   *-CESDD        CESD DATA LENGTH
         SPACE 1
CIDR     DSECT ,              CSECT IDENTIFICATION RECORD -------------
IDRI     DS    XL1                 IDENTIFICATION
IDRIV    EQU   X'80'               IDR VALUE
IDRCN    DS    XL1                 COUNT IN BYTES OF IDR DATA
IDRSI    DS    XL1                 SUB-TYPE INDICATOR (IDR DATA TYPE)
IDRHZP   EQU   X'01'               HMASPZAP DATA
IDRLKE   EQU   X'02'               LINKAGE EDITOR DATA
IDRTRS   EQU   X'04'               TRANSLATOR DATA
IDRUSR   EQU   X'08'               USER (SYSTEM) DATA (FROM IDENTIFY)
IDRLST   EQU   X'80'               LAST IDR OF THIS LOAD MODULE
IDRDT    DS    XL1                 IDR DATA (UP TO 256 BYTES)
         SPACE 1
HZPD     DSECT ,              HMASPZAP DATA - - - - - - - - - - - - - -
HZPFC    DS    XL1                 FLAGS AND COUNT
*                                       BIT 0 - RESERVED
*                                       BIT 1 - CHAIN BIT
*                                       BIT 2-7 - NUMBER OF ENTRIES
HZPDE    DSECT ,              HMASPZAP ENTRY DESCRIPTION  . . . . . . .
HZPES    DS    XL2                 ESDID OF CSECT PROCESSED BY HMASPZAP
HZPDT    DS    PL3                 DATE OF HMASPZAP (YYDDD PACKED)
HZPDH    DS    CL8                 DATA OF HMASPZAP (IDRDATA)
HZPDEL   EQU   *-HZPDE        HMASPZAP DATA LENGTH
         SPACE 1
LKED     DSECT ,              LINKAGE EDITOR DATA - - - - - - - - - - -
LKENM    DS    CL10                PROGRAM NAME OF LINKAGE EDITOR
LKEVM    DS    PL2                 VERSION AND MOD. LEVEL (VVMM PACKED)
LKEDT    DS    PL3                 DATE OF LAST LINK (YYDDD PACKED)
LKEDL    EQU   *-LKED         LINKAGE EDITOR DATA LENGTH
         SPACE 1
TRSD     DSECT ,              TRANSLATOR DATA - - - - - - - - - - - - -
TRSDES   DS    0XL2                ESDID LIST OF CSECT(S)
TRSLST   EQU   X'80'               LAST ESDID IN THE LIST
TRSDS    DSECT ,              TRANSLATOR DESCRIPTION  . . . . . . . . .
TRSDSID  DS    XL1                 INDICATOR
*                                       X'00' = ONLY ONE TRANSLATOR
TRSDSTT  EQU   X'01'                    TWO TRANSLATORS
TRSDSNM  DS    XL10                TRANSLATOR NAME
TRSDSVM  DS    PL2                 VERSION AND MOD. LEVEL (VVMM PACKED)
TRSDSDT  DS    PL3                 DATE OF COMP./ASS. (YYDDD PACKED)
TRSDSL   EQU   *-TRSDS        TRANSLATOR DESCRIPTION LENGTH
         SPACE 1
USRD     DSECT ,              USER (IDENTIFY) DATA  - - - - - - - - - -
USRES    DS    XL2                 ESDID OF CSECT TO WHICH IT APPLIES
USRDT    DS    PL3                 DATE OF IDENTIFY (YYDDD PACKED)
USRCN    DS    XL1                 NUMBER OF CHAR. IN USER DATA FIELD
USRDF    DS    XL1                 USER DATA FIELD (UP TO 40 CHAR.)
         SPACE 1
TIODSECT DSECT
        IEFTIOT1
        DCBD   DSORG=PO,DEVD=DA
        IHAPDS PDSBLDL=NO
         SPACE 1
         END
