AIDD     TITLE 'ACCOUNTING INFORMATION DATA-SET DIAGNOSTICS.'
ZYDIAG   START 0
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *       ACCOUNTING INFORMATION        * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *        DATA-SET DIAGNOSTICS         * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* AUTHOR :     P.A. MOINIL                                            *
* --------     COMPUTING CENTRE                                       *
*              J.R.C. - ISPRA ESTABLISHMENT                           *
*              21020 ISPRA (VA), ITALY                                *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
* PURPOSE :    SUPPLY DIAGNOSTICS TEXT FOR ERROR NUMBER RETURNED BY   *
* ---------    ZPACK AND YPACK ROUTINES PACKAGES. IT IS THEN CALLER   *
*              RESPONSABILITY TO PRINT OUT THE MESSAGE.               *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         EJECT
* CALLING SEQUENCE : CALL ZYDIAG,(A,B,C),VL
* -------------------            (A,B)
*                                (A)
*
*        WHERE : A - IS THE INDICATOR RESPONSE (FORMAT FULLWORD - F
*                    BOUNDARY) RETURNED BY ZPACK OR YAPCK ROUTINES
*                    PACKAGES. IF THE ADDRESS OF THIS ARGUMENT IS
*                    SET TO ZERO IN THE PARAMETERS LIST, OR THE
*                    PARAMETERS LIST POINTER (REGISTER 1) IS ZERO,
*                    THE INDICATOR RESPONSE VALUE IS ASSUMED TO BE
*                    IN THE REGISTER 0. AT EXIT OF THE ROUTINE, IF
*                    THE ARGUMENTS B AND C ARE OMITTED, THE REGISTER
*                    15 POINTS ON THE DIAGNOSTIC TEXT AS BELOW :
*
*                                   DC  AL1(L'TEXT)
*                    R15 ---> TEXT  DC  C'0...DIAGNOSTIC...'
*
*                    NOTE THAT THE FIRST CHARACTER IS THE ASA CONTROL
*                    CHARACTER 0 (ZERO) FOR DOUBLE SPACE, AND THE
*                    LENGTH OF THE DIAGNOSTIC TEXT IS IN THE ONE
*                    BYTE LOCATED AT THE ADDRESS MINUS ONE.
*
*                B - IS THE EVENTUAL ADDRESS OF AN AREA WHERE THE
*                    DIAGNOSTIC TEXT (FORMAT CL...) IS MOVED. IF
*                    THE C ARGUMENT IS OMITTED, THE LENGTH OF THE
*                    DIAGNOSTIC TEXT IS PLACED IN THE ONE BYTE
*                    LOCATED AT THE ADDRESS MINUS ONE OF B ARGUMENT
*                    ADDRESS (ALL MESSAGES ARE LESS THAN 80 CHARACTERS
*                    LONG). AT EXIT OF THE ROUTINE, THE REGISTER 15
*                    IS ZERO.
*
*                C - RECEIVES THE LENGTH OF THE DIAGNOSTIC TEXT
*                    (FORMAT FULLWORD - F BOUNDARY) IF SUPPLIED
*                    (OPTIONAL ARGUMENT).
         EJECT
        $DEFREG
         EJECT
        $XENT  BASE=R12
         SPACE 1
         XR    R6,R6
         XR    R7,R7
         XR    R8,R8
         LTR   R1,R1               CONTROL ARGUMENTS.
         BNZ   *+L'*+6
         LR    R6,R0
         B     GDIAG
         TM    3(R1),X'03'
         BNZ   ERROR
         L     R6,0(R1)
         LA    R6,0(R6)
         LTR   R6,R6
         BNZ   *+L'*+6
         LR    R6,R0
         B     *+L'*+4
         L     R6,0(R6)
         TM    0(R1),X'80'
         BO    GDIAG
         L     R7,4(R1)
         LA    R7,0(R7)
         TM    4(R1),X'80'
         BO    GDIAG
         LTR   R7,R7
         BZ    ERROR
         L     R8,8(R1)
         LA    R8,0(R8)
         SPACE 1
GDIAG    LTR   R6,R6               GET DIAGNOSTIC.
         BNP   ERROR
         CH    R6,MAXERR
         BH    ERROR
         BCTR  R6,0
         SLL   R6,1
         LH    R15,ERRM(R6)
         LA    R15,ERRS(R15)
         B     *+L'*+L'ERROR
ERROR    LA    R15,ERRORM
         SPACE 1
         LTR   R7,R7               RETURN DIAGNOSTIC.
         BZ    EXIT
         BCTR  R15,0
         XR    R2,R2
         IC    R2,0(R15)
         BCTR  R2,0
         EX    R2,MVDIAG
         LA    R2,1(R2)
         LTR   R8,R8
         BZ    *+L'*+14
         ST    R2,0(R8)
         B     *+L'*+12
MVDIAG   MVC   0(*-*,R7),1(R15)
         BCTR  R7,0
         STC   R2,0(R7)
         XR    R15,R15
         SPACE 1
EXIT    $XRET  CC=(R15)
         EJECT
*        MESSAGES TABLE.
         SPACE 1
ERRM    NULL
         DC    AL2(ERR1-ERRS)
         DC    AL2(ERR2-ERRS)
         DC    AL2(ERR3-ERRS)
         DC    AL2(ERR4-ERRS)
         DC    AL2(ERR5-ERRS)
         DC    AL2(ERR6-ERRS)
         DC    AL2(ERR7-ERRS)
         DC    AL2(ERR8-ERRS)
         DC    AL2(ERR9-ERRS)
         DC    AL2(ERR10-ERRS)
         DC    AL2(ERR11-ERRS)
         DC    AL2(ERR12-ERRS)
         DC    AL2(ERR13-ERRS)
         DC    AL2(ERR14-ERRS)
         DC    AL2(ERR15-ERRS)
         DC    AL2(ERR16-ERRS)
         DC    AL2(ERR17-ERRS)
         DC    AL2(ERR18-ERRS)
         DC    AL2(ERR19-ERRS)
         DC    AL2(ERR20-ERRS)
         DC    AL2(ERR21-ERRS)
         DC    AL2(ERR22-ERRS)
         DC    AL2(ERR23-ERRS)
         DC    AL2(ERR24-ERRS)
         DC    AL2(ERR25-ERRS)
         DC    AL2(ERR26-ERRS)
         SPACE 1
ERRL     EQU   (*-ERRM)/2
MAXERR   DC    0H'0',AL2(ERRL)
         SPACE 1
         DC    AL1(L'ERRORM)
ERRORM   DC    C'0 ZYDIAG - CALLING SEQUENCE OR ARGUMENT ERROR.'
         SPACE 1
ERRS     EQU   *
         DC    AL1(L'ERR1)
ERR1     DC    C'0 ROUTINE OR DATA-SET TABLE DESTROYED.'
         DC    AL1(L'ERR2)
ERR2     DC    C'0 SYSACC DD-CARD IS MISSING.'
         DC    AL1(L'ERR3)
ERR3     DC    C'0 SYSACC UNIT IS NOT A DIRECT-ACCESS STORAGE.'
         DC    AL1(L'ERR4)
ERR4     DC    C'0 SYSACC JFCB READ ERROR.'
         DC    AL1(L'ERR5)
ERR5     DC    C'0 SYSACC DD-CARD INCORRECTLY CODED.'
         DC    AL1(L'ERR6)
ERR6     DC    C'0 OBTAIN ERROR WHILE PROCESSING SYSACC VOLUME.'
         DC    AL1(L'ERR7)
ERR7     DC    C'0 SYSACC DSCB FORMAT 1 ERROR DETECTED.'
         DC    AL1(L'ERR8)
ERR8     DC    C'0 SYSACC DATA BLOCK EXCEEDS TRACK CAPACITY.'
         DC    AL1(L'ERR9)
ERR9     DC    C'0 SYSACC DATA-SET SPACE TOO SHORT.'
         DC    AL1(L'ERR10)
ERR10    DC    C'0 SYSACC DATA-SET OPEN ERROR.'
         DC    AL1(L'ERR11)
ERR11    DC    C'0 NO STORAGE AVAILABLE TO READ DATA-SET TABLE.'
         DC    AL1(L'ERR12)
ERR12    DC    C'0 SYSACC DATA-SET I/O ERROR WHILE READING TABLE.'
         DC    AL1(L'ERR13)
ERR13    DC    C'0 SYSACC DATA-SET IS ALREADY OPENED.'
         DC    AL1(L'ERR14)
ERR14    DC    C'0 SYSACC DATA-SET IS NOT OPENED.'
         DC    AL1(L'ERR15)
ERR15    DC    C'0 READ REQUEST - ARGUMENT NOT FOUND.'
         DC    AL1(L'ERR16)
ERR16    DC    C'0 READ/WRITE REQUEST - I/O ERROR.'
         DC    AL1(L'ERR17)
ERR17    DC    C'0 SYSACC DATA-SET FULL, CANNOT ADD RECORD.'
         DC    AL1(L'ERR18)
ERR18    DC    C'0 SYSACC DATA-SET I/O ERROR WHILE WRITING TABLE.'
         DC    AL1(L'ERR19)
ERR19    DC    C'0 ROUTINE CALLING SEQUENCE OR ARGUMENT ERROR.'
         DC    AL1(L'ERR20)
ERR20    DC    C'0 SYSACC DATA-SET WRONGLY OR NOT CATALOGED.'
         DC    AL1(L'ERR21)
ERR21    DC    C'0 SYSACC DATA-SET IN USE FOR UPDATE BY SOMEBODY.'
         DC    AL1(L'ERR22)
ERR22    DC    C'0 ONLY OR BOTH REQUESTED - ZWRITE NOT AVAILABLE.'
         DC    AL1(L'ERR23)
ERR23    DC    C'0 ONLY OR BOTH REQUESTED - ZDELET NOT AVAILABLE.'
         DC    AL1(L'ERR24)
ERR24    DC    C'0 UNAUTHORIZED TO ACCESS THE ACCOUNTING PACKAGES.'
         DC    AL1(L'ERR25)
ERR25    DC    C'0 UNAUTHORIZED TO ACCESS THE ZWRITE FUNCTION.'
         DC    AL1(L'ERR26)
ERR26    DC    C'0 UNAUTHORIZED TO ACCESS THE ZDELET FUNCTION.'
         SPACE 2
         END
