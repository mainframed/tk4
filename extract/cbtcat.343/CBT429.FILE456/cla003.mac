A003     TITLE 'CLEAN-UP - READ EL. AND CAT. VOLUMES NAMES (A003).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A003    @XS    ALIAS=RVNAMES
*        GET ELIGIBLE VOLUMES.
         SPACE 1
         L     R2,TBEVOL           ADDRESS OF EL. VOL. TABLE
         XR    R15,R15
         B     GEV1
GEV0    $READ  MF=(E,RDIPLS)
         LTR   R15,R15
         BM    ERR2                EOF
         BNZ   ERR21               ERROR
GEV1     LM    R3,R5,SCIN          SET TO SCAN INPUT BUFFER
GEVSW1   NOP   GEV5
         CLI   0(R3),C' '          LOOK FOR FIRST NON-BLANK
         BNE   GEVSW2
GEV2     BXLE  R3,R4,*-8
         B     GEV0
GEVSW2   NOP   GEV4
         CLC   0(6,R3),=CL6'EL.VOL'     IS IT 'EL.VOL' KEYWORD?
         BNE   GEVSW3              NO, MAY BE ERROR
         OI    GEVSW3+1,ON
         LA    R3,5(R3)
         CLI   1(R3),C'.'
         BNE   GEV3
         LA    R3,1(R3)
GEV3     OI    GEVSW2+1,ON
         B     GEV2
GEV4     CLI   0(R3),C'('          START OF NAMES?
         BNE   ERR2                NO, ERROR
         OI    GEVSW1+1,ON
         BXLE  R3,R4,GEV5
         B     GEV0
GEVSW3   NOP   GEV12
         B     ERR2
GEV5     CLI   0(R3),C' '          LOOK FOR FIRST NON-BLANK
         BNE   GEV6
         BXLE  R3,R4,GEV5
         B     GEV0
GEV6     LR    R1,R3               SAVE STARTING POINTER
         LA    R0,7                MAX. LENGTH + 1 TO CONTROL
GEV7     CLI   0(R3),C' '          LOOK FOR FIRST BLANK
         BE    GEV9
         CLI   0(R3),C')'          END OF NAMES?
         BE    GEV9                YES
         BCT   R0,GEV8
         XR    R15,R15             > 6 CHAR. LONG
         B     ERR3
GEV8     BXLE  R3,R4,GEV7
GEV9     LR    R15,R3              COMPUTE VOL. SER. LENGTH
         SR    R15,R1
         BZ    GEV11               THIS IS THE END OF NAMES
         BCTR  R15,0               -1 FOR MOVE
         LA    R14,2(R15,R2)       COMPUTE SPACE IN TABLE
         CL    R14,TBEVOLE         ANY OVERFLOW?
         BL    GEV10               NO
         XR    R15,R15             YES
         B     ERR4
GEV10    STC   R15,0(R2)           STORE IN TABLE
         EX    R15,GEVMV           MOVE VOLUME PREFIX TO TABLE
         LR    R2,R14              ADVANCE IN TABLE
         MVI   0(R2),FMON          SET FINAL MARKER
         CLR   R3,R5               CARD SCAN ENDED?
         BH    GEV0                YES
         B     GEV5                GET NEXT VOLUME
GEVMV    MVC   1(*-*,R2),0(R1)     << EXECUTED >>
GEV11    NI    GEVSW1+1,OFF
         NI    GEVSW2+1,OFF
         B     GEV2
GEV12    CL    R2,TBEVOL           EMPTY EL. VOL. TABLE?
         BH    GCV0                NO
         XR    R15,R15             YES
         B     ERR5
         SPACE 1
*        GET CATALOG VOLUMES.
         SPACE 1
GCV0     L     R2,TBCVOL           ADDRESS OF CAT. VOL. TABLE
         XR    R15,R15
         B     GCV2
GCV1    $READ  MF=(E,RDIPLS)
         LTR   R15,R15
         BM    GCV41               EOF
         BNZ   ERR21               OK
GCV2     LM    R3,R5,SCIN          SET TO SCAN INPUT BUFFER
GCVSW1   NOP   GCV6
         CLI   0(R3),C' '          LOOK FOR FIRST NON-BLANK
         BNE   GCVSW2
GCV3     BXLE  R3,R4,*-8
         B     GCV1
GCVSW2   NOP   GCV5
         CLC   0(7,R3),=CL7'CAT.VOL'    IS IT 'CAT.VOL' KEYWORD?
         BNE   GCVSW3              NO, MAY BE ERROR
         NI    GCVSW3+1,OFF
         LA    R3,6(R3)
         CLI   1(R3),C'.'
         BNE   GCV4
         LA    R3,1(R3)
GCV4     OI    GCVSW2+1,ON
         B     GCV3
GCV5     CLI   0(R3),C'('          START OF NAMES?
         BNE   ERR6                NO, ERROR
         OI    GCVSW1+1,ON
         BXLE  R3,R4,GCV6
         B     GCV1
GCVSW3   B     ERR6
         OI    WHAT,WHCD           INDICATE "SYSIN" CARD ALREADY READ
         B     GCV44
GCV6     CLI   0(R3),C' '          LOOK FOR FIRST NON-BLANK
         BNE   GCV7
         BXLE  R3,R4,GCV6
         B     GCV1
GCV7     LR    R1,R3               SAVE STARTING POINTER
         LA    R0,7                MAX. LENGTH + 1 TO CONTROL
GCV8     CLI   0(R3),C' '          LOOK FOR FIRST BLANK?
         BE    GCV11               YES
         CLI   0(R3),C'/'          FOLLOWED BY A DS-NAME?
         BE    GCV11               YES
         CLI   0(R3),C')'          END OF NAMES?
         BE    GCV11               YES
         BCT   R0,GCV10
GCV9     XR    R15,R15             > 6 CHAR. LONG
         B     ERR7
GCV10    BXLE  R3,R4,GCV8
GCV11    CH    R0,=H'7'            END OF NAMES?
         BE    GCV40               YES
         CH    R0,=H'1'            CONTROL VOL. SER. LENGTH
         BNE   GCV9                < 6 CHAR. LONG
         MVC   WORKD(L'CVLVOL),0(R1)    RETAIN CATALOG VOLUME
         MVI   WORKDS,C' '
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
         CLI   0(R3),C' '          WAS IT A BLANK?
         BNE   *+L'*+8             NO
         BXLE  R3,R4,*-8           YES, SEARCH AHEAD
         B     GCV14               THIS IS THE END OF CARD
         CLI   0(R3),C')'          END OF NAMES?
         BE    GCV14               YES
         CLI   0(R3),C'/'          A DS-NAME FOLLOWS?
         BNE   GCV14               NO
         BXLE  R3,R4,*+L'*+4       YES, SEARCH IT
         B     GCV42               END OF CARD ERROR
         CLI   0(R3),C' '          IS IT A BLANK?
         BE    *-12                YES
         CLI   0(R3),C')'          NO, END OF NAMES?
         BE    GCV14               YES
         LR    R1,R3               NO, START OF DS-NAME
GCV12    BXLE  R3,R4,*+L'*+4
         B     GCV13               END OF CARD AND DS-NAME
         CLI   0(R3),C' '          END OF DS-NAME?
         BE    *+L'*+8             YES
         CLI   0(R3),C')'          END OF CARD?
         BNE   GCV12               NO, CONTINUE
GCV13    LR    R14,R3              GET THE DS-NAME
         SR    R14,R1
         BNP   GCV42
         CH    R14,=Y(L'CVLNME)
         BH    GCV42
         BCTR  R14,0
         EX    R14,GCVMV
         B     GCV15
GCVMV    MVC   WORKDS(*-*),0(R1)   << EXECUTED >>
GCV14    MVC   WORKDS(9),=CL9'SYSCTLG.V'     SET O.S. CATALOG DS-NAME
         MVC   WORKDS+9(L'CVLVOL),WORKD
GCV15    LA    R14,CVLL(R2)        COMPUTE SPACE IN TABLE
         CL    R14,TBCVOLE         ANY OVERFLOW?
         BL    GCV30               NO
         XR    R15,R15             YES
         B     ERR8
         USING CVLT,R2
GCV30    MVC   CVLVOL(L'CVLVOL),WORKD   MOVE CATALOG VOLUME TO TABLE
         MVC   CVLNME,WORKDS       MOVE CATALOG DS.NAME
         MVI   CVLIND,0            RESET INDICATORS
         MVI   CVLPWL,0            RESET PASSWORD FIELDS
         XC    CVLPW,CVLPW
         CLC   CVLNME(9),=CL9'SYSCTLG.V'     O.S. CATALOG?
         BE    *+L'*+4             YES
         OI    CVLIND,CVLICF       SAY ICF CATALOG
         DROP  R2
         LR    R2,R14              ADVANCE IN TABLE
         MVI   0(R2),FMOFF         SET FINAL MARKER
         CLR   R3,R5               CARD SCAN ENDED?
         BH    GCV1                YES
         B     GCV6                GO GET NEXT ONE
GCV40    NI    GCVSW1+1,OFF
         NI    GCVSW2+1,OFF
         B     GCV3
GCV41    TM    GCVSW3+1,ON
         BZ    GCV43
GCV42    XR    R15,R15
         B     ERR6
GCV43    OI    WHAT,WHEOF          INDICATE "SYSIN" END-OF-FILE
GCV44    CL    R2,TBCVOL           EMPTY CAT. VOL. TABLE?
         BH    GPV0                NO
         SPACE 1
*        NO CATALOGS TO BE PROCESSED SITUATION MAY BE ACCEPTED
*        WHEN ONLY THE TEMPORARY STORAGES ARE TO BE CLEANED,
*        OR THE ONLY OPTION HAS BEEN REQUESTED.
         SPACE 1
         TM    STATUS,STONLY       ONLY REQUESTED?
         BO    GPV0                YES
         TM    RUNIND,RUNORACF     BYPASS RACF ACTION?
         BZ    GCV19               NO, CAN'T BE ACCEPTED
         TM    RUNOPT,RUNOACC      NO ACCOUNT?
         BZ    GCV19               NO, CAN'T BE ACCEPTED
         L     R4,TBEVOL           START OF EL. VOL. TABLE
         LA    R2,L'TSTOR-1
GCV17    XR    R1,R1
         IC    R1,0(R4)            GET LENGTH OF NAME
         LA    R3,1(R4)            START ADDRESS OF COMPARE
         LA    R4,2(R1,R4)         NEXT EL. VOL. IN TABLE
GCV18    CLR   R1,R2               WHAT ABOUT THE LENGTH?
         BL    GCV19               SHORT, CAN'T BE ACCEPTED
         CLC   TSTOR(L'TSTOR),0(R3)     TEMPORARY STORAGE?
         BE    GCV20               YES
         LA    R3,1(R3)            BUMP COMPARE ADDRESS
         BCT   R1,GCV18            GO TRY AGAIN
GCV19    XR    R15,R15             CAN'T BE ACCEPTED
         B     ERR9
GCV20    CLI   0(R4),FMON          END OF TABLE?
         BNE   GCV17               NO, CONTINUE
         SPACE 1
*        LIST EL. VOL. AND CAT. VOL. TABLES.
         SPACE 1
GPV0    @OUT   VOLCH,,U
         L     R2,TBEVOL
GPV1     LA    R4,LINE+6           STARTING ADDRESS IN LINE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         LA    R0,10               MAX. BY LINE
GPV2     CLI   0(R2),FMON          LAST ENTRY?
         BE    GPV6                YES
         XR    R3,R3               CLEAR REGISTER
         IC    R3,0(R2)            GET LENGTH OF NAME
         EX    R3,GPVMV            MOVE TO PRINT LINE
         LA    R1,5(R4)            REPLACE BLANKS IN LINE
GPV3     CLI   0(R1),C' '          BLANK?
         BNE   GPV4                NO
         MVI   0(R1),C'-'          REPLACE
         BCT   R1,GPV3             AND LOOP
GPV4     LA    R2,2(R2,R3)         ADVANCE IN TABLE
         BCT   R0,GPV5
         B     GPV6
GPVMV    MVC   0(*-*,R4),1(R2)     << EXECUTED >>
GPV5     LA    R4,L'CVLVOL+4(R4)   ADVANCE IN LINE
         B     GPV2
GPV6     CLI   LINE+6,C' '         ANYTHING IN LINE?
         BE    GPV10               NO
        @OUT   LINE
         CLI   0(R2),FMON          LAST ENTRY?
         BNE   GPV1                NO, CONTINUE
GPV10    L     R2,TBCVOL
         CLI   0(R2),FMOFF         EMPTY CAT.VOL. TABLE?
         BE    GPV99               YES
        @OUT   CATTIT,,U
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         MVI   LINE+L'CVLVOL+7,C'-'
         USING CVLT,R2
GPV11    MVC   LINE+6(L'CVLVOL),CVLVOL  MOVE VOLID IN LINE
         MVC   LINE+L'CVLVOL+9(L'CVLNME),CVLNME   MOVE DS-NAME IN LINE
         TM    CVLIND,CVLICF       CATALOG TYPE IS ICF?
         BO    *+L'*+6             YES
         MVC   LINE+L'CVLVOL+16(10),LINE+L'CVLVOL+30   NO, O.S.
         DROP  R2
        @OUT   LINE
         LA    R2,CVLL(R2)         ADVANCE IN TABLE
         CLI   0(R2),FMOFF         LAST ENTRY?
         BNE   GPV11               NO, CONTINUE
         MVC   LINE+1(L'LINE-1),LINE    CLEAR LINE
         SPACE 1
*        PASS TO O.S. EVENTUAL COMMANDS.
         SPACE 1
GPV99    L     R2,ATIOT            LOOK IF COMMANDS INPUT FILE.
         USING TIODSECT,R2
         XR    R1,R1
SCMD1    CLC   TIOELNGH(4),=XL4'0'
         BE    SCMD99
         CLC   TIOEDDNM,DDCMD
         BE    SCMD2
         IC    R1,TIOELNGH
         AR    R2,R1
         B     SCMD1
         DROP  R2
SCMD2   $READ  MF=(E,SCMDPLS)      GET A COMMAND.
         LTR   R15,R15
         BM    SCMD99
         BNZ   ERR40
         CLI   CMND,C'*'           COMMENT?
         BE    SCMD2               YES, BYPASS
        ZEROKEY
         XR    R0,R0               PASS IT TO O.S.
         XR    R15,R15
        MGCR   SCMND
         LR    R2,R15
        RESETKEY
         ORG   *-4                 OVERLAP AUTHORIZATION OFF.
         LTR   R2,R2               RETURN CODE?
         BZ    SCMD2               OK, GET ANOTHER IF ANY.
         CVD   R2,WORKD            DISPLAY R.C. AND TEXT.
         UNPK  CMNDRJ+L'CMNDRJ-6(2),WORKD+6(2)
         OI    CMNDRJ+L'CMNDRJ-5,C'0'
         MVC   CMNDRJT(L'CMNDRJT),CMND
        @OUT   CMNDRJ
         B     SCMD2
SCMD99  @XE
         EJECT
SCMDPLS $READ  CMND,DDCMD,MF=L
DDCMD    DC    CL8'SYSCMD'         COMMANDS INPUT FILE.
SCMND    DC    0F'0',AL2(L'CMND-8,0)
CMND     DC    CL80' '
TSTOR    DC    CL4'STOR'           TEMPORARY STORAGES PREFIX-SUFFIX
         SPACE 1
         DC    AL1(L'VOLCH)
VOLCH    DC    C'0ELIGIBLE VOLUMES CHECK LIST :'
         DC    AL1(CATTITL)
CATTIT   DC    C'0VOLUMES CONTAINING A CATALOG '
         DC    C'(MUST BE PRESENT) CHECK LIST :'
CATTITL  EQU   *-CATTIT
         DC    AL1(L'CMNDRJ+L'CMNDRJT)
CMNDRJ   DC    C'0 --- COMMAND FAILED (RC=  ) : '
CMNDRJT  DC    CL72' '
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
TIODSECT DSECT
        IEFTIOT1
         SPACE 2
         END
