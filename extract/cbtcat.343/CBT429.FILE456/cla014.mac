A014     TITLE 'CLEAN-UP - USER''S ACCESS LIST AND RECOVERY (A014).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A014    @XS    ALIAS=RACFUSR
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    USP99               YES
         TM    STATUS,STBRACF      RACF MUST BE BYPASSED?
         BO    USP99               YES
         LA    R1,USNMEL
         ST    R1,SUSPUL
         LA    R1,SVSNAP+4
         ST    R1,SUSPUG+4
         SPACE 1
*        RACF USER'S DATA-SETS LIST AND RECOVER
*        OF THE GROUP ACCESS FOR TSO COMMANDS.
         SPACE 1
         LA    R2,LINE
         USING LALNE,R2
         L     R7,WK1A             RACF WORK AREA
         USING WKDS,R7
         ST    R7,SUSPUW
         XC    COUNTER,COUNTER
         TM    RUNIND,RUTEST       TEST MODE?
         BZ    USP1                NO
         MVC   USUPTT,=CL16'SIMULATED (TEST)'
USP1     TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP2                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP2                YES
        @LIST  USTIT,,U
         OI    STATUS,STGCVL       INDICATE USERS PROCESSED
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    USP2                NO
        @COPY  USTIT,,U
         SPACE 1
*        GET USER'S LIST FROM RACF.
         SPACE 1
USP2     XC    WORKRBA(WORKBSLL),WORKRBA
         LA    R0,USNMEL
        ICHEINTY NEXTC,TYPE='USR',ENTRY=(R0),WKAREA=(R7),              C
               ACTIONS=(AUSA,AUSF1,AUSF2,AUSF3,AUSF4,AUSF5,AUSD,AUSP,  C
               AUSI,AUSU,AUSF6,AUSM,AUSS,AUSN)
         CH    R15,=H'12'          END OF FILE?
         BE    USP90               YES
         LA    R14,WORKBS
         A     R14,WORKTL
         STCM  R14,B'0111',SUSPUN+1
         XR    R14,R14
         IC    R14,USNMEL
         LA    R14,1(R14)
         ST    R14,SUSPUG
        @SNAPS SF,SUSPUL,RUS
         L     R1,COUNTER
         LA    R1,1(R1)
         ST    R1,COUNTER
         LTR   R15,R15             ANY ERROR?
         BZ    USP5                NO
         CVD   R15,WORKD           RETURN CODE
         UNPK  USUSRRC,WORKD+6(2)
         OI    USUSRRC+1,C'0'
         MVI   USUSRAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   USP3                NO
         STC   R0,USUSRAC
         OI    USUSRAC,C'0'
USP3     TM    STATUS,STUPT
         BO    USP4
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
USP4    @OUT   USUSR
         B     USP2
USP5     XR    R1,R1
         IC    R1,USNMEL           GET LENGTH OF USERID
         BCTR  R1,0                -1 FOR MOVE
         OC    WORKTL,WORKTL       DATA RETURNED?
         BZ    USP50               NO
         CLC   WORKVRA(2),=XL2'0008'    AUTHOR RECEIVED?
         BNE   USP50               NO
         CLC   WORKVRA+10(2),=XL2'0001' FLAG1 RECEIVED?
         BNE   USP50               NO
         CLC   WORKVRA+13(2),=XL2'0001' FLAG2 RECEIVED?
         BNE   USP50               NO
         CLC   WORKVRA+16(2),=XL2'0001' FLAG3 RECEIVED?
         BNE   USP50               NO
         CLC   WORKVRA+19(2),=XL2'0001' FLAG4 RECEIVED?
         BNE   USP50               NO
         CLC   WORKVRA+22(2),=XL2'0001' FLAG5 RECEIVED?
         BNE   USP50               NO
         CLC   WORKVRA+25(2),=XL2'0008' USER DEFAULT GROUP RECEIVED?
         BNE   USP50               NO
         MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         EX    R1,USPMV            MOVE USER
         MVC   USER,LAUS
         MVI   LASPO,C'('
         MVC   LAOW+1(6),=CL6'......'
         CLI   WORKVRA+2,X'FF'
         BE    *+L'*+6
         MVC   LAOW(L'LAOW),WORKVRA+2   MOVE OWNER OF USER
         MVI   LASPT,C')'
         MVI   LADFT,C':'
         MVC   LADF(8),=CL8'..NONE..'
         CLI   WORKVRA+27,X'FF'
         BE    *+L'*+6
         MVC   LADF(L'LADF),WORKVRA+27  MOVE DEFAULT GROUP
         MVC   ACCNAM(L'ACCNAM),WORKVRA+27
         MVI   LASPU,C'-'
         LA    R1,LAFLGS
         LA    R15,WORKVRA+12
         CLI   0(R15),X'80'        FLAG1 - ADSP?
         BNE   USP10
         MVC   0(4,R1),=CL4'ADSP'
         LA    R1,5(R1)
         B     USP11
USP10    CLI   0(R15),0
         BE    USP11
         MVI   0(R1),C'1'
         BAS   R14,DMPRTN
USP11    LA    R15,WORKVRA+15
         CLI   0(R15),X'80'        FLAG2 - SPECIAL?
         BNE   USP12
         MVC   0(7,R1),=CL7'SPECIAL'
         LA    R1,8(R1)
         B     USP13
USP12    CLI   0(R15),0
         BE    USP13
         MVI   0(R1),C'2'
         BAS   R14,DMPRTN
USP13    LA    R15,WORKVRA+18
         CLI   0(R15),X'80'        FLAG3 - OPERATIONS?
         BNE   USP14
         MVC   0(10,R1),=CL10'OPERATIONS'
         LA    R1,11(R1)
         B     USP15
USP14    CLI   0(R15),0
         BE    USP15
         MVI   0(R1),C'3'
         BAS   R14,DMPRTN
USP15    LA    R15,WORKVRA+21
         CLI   0(R15),X'80'        FLAG4 - REVOKE?
         BNE   USP16
         MVC   0(6,R1),=CL6'REVOKE'
         LA    R1,7(R1)
         B     USP17
USP16    CLI   0(R15),0
         BE    USP17
         MVI   0(R1),C'4'
         BAS   R14,DMPRTN
USP17    LA    R15,WORKVRA+24
         CLI   0(R15),X'80'        FLAG5 - GRPACC?
         BNE   USP18
         MVC   0(6,R1),=CL6'GRPACC'
         LA    R1,7(R1)
         B     USP20
USP18    CLI   0(R15),0
         BE    USP19
         MVI   0(R1),C'5'
         BAS   R14,DMPRTN
         B     USP20
USP19    MVC   LANOGRP(L'LANOGRP),=CL22'---> (NO GROUP ACCESS)'
USP20    CLI   LAFLGS,C' '         ANY ATTRIBUTE?
         BE    *+L'*+6             NO
         MVC   LAATTR(L'LAATTR),=CL7'ATTR. :'
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP21               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP21               YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    USP21               NO
        @COPY  LINE
USP21    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R3,USNMEL
         XR    R4,R4               SET RACF USER CALL
        @X     B005                LOCATE CATALOG VOLUME IF ANY
         LA    R3,WORKVRA+35       START OF VARIABLE FIELDS
         XR    R4,R4
         ICM   R4,B'0011',0(R3)    GET USER NAME LENGTH
         LA    R3,2(R3)            POINT TO USER NAME OR NEXT
         LTR   R4,R4
         BZ    USP24
         MVC   LAVAR(13),=CL13'- USER NAME :'
         LA    R5,60
USP22    CLR   R4,R5
         BNL   *+L'*+2
         LR    R5,R4
         BCTR  R5,0
         LA    R14,LAVAR+14
         EX    R5,USPMD
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP23               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP23               YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    USP23               NO
        @COPY  LINE
USP23    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,1(R5)
         AR    R3,R5
         SR    R4,R5
         BNZ   USP22
USP24    XR    R4,R4
         IC    R4,2(R3)            GET INST. DATA LENGTH
         LA    R3,3(R3)            POINT TO INST. DATA OR NEXT
         LTR   R4,R4
         BZ    USP27
         MVC   LAVAR(14),=CL14'- INST. DATA :'
         LA    R5,60
USP25    CLR   R4,R5
         BNL   *+L'*+2
         LR    R5,R4
         BCTR  R5,0
         LA    R14,LAVAR+15
         EX    R5,USPMD
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP26               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP26               YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    USP26               NO
        @COPY  LINE
USP26    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,1(R5)
         AR    R3,R5
         SR    R4,R5
         BNZ   USP25
USP27    CLC   0(2,R3),=XL2'0001'  UAUDIT RECEIVED?
         BNE   USP35               NO, SKIP
         CLC   3(2,R3),=XL2'0001'  FLAG6 RECEIVED?
         BNE   USP34               NO, SKIP
         CLI   5(R3),0             AUDITOR ATTRIBUTE?
         BE    USP33               NO
         MVC   LAVAR(13),=CL13'- LOG ATTR. :'
         LA    R1,LAVAR+14
         CLI   5(R3),X'80'         FLAG6 - AUDITOR?
         BNE   USP28
         MVC   0(7,R1),=CL7'AUDITOR'
         LA    R1,8(R1)
         B     USP29
USP28    MVI   0(R1),C'6'
         LA    R15,5(R3)
         BAS   R14,DMPRTN
USP29    CLI   2(R3),X'80'         UAUDIT?
         BNE   USP30
         MVC   0(6,R1),=CL6'UAUDIT'
         B     USP31
USP30    MVI   0(R1),C'U'
         LA    R15,2(R3)
         BAS   R14,DMPRTN
USP31    TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP32               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP32               YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    USP32               NO
        @COPY  LINE
USP32    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
USP33    LA    R3,3(R3)
USP34    LA    R3,3(R3)
USP35    XR    R4,R4
         IC    R4,2(R3)            GET MODEL NAME LENGTH
         LA    R3,3(R3)            POINT TO MODEL NAME OR NEXT
         LTR   R4,R4
         BZ    USP38
         MVC   LAVAR(14),=CL14'- MODEL NAME :'
         LA    R5,60
USP36    CLR   R4,R5
         BNL   *+L'*+2
         LR    R5,R4
         BCTR  R5,0
         LA    R14,LAVAR+15
         EX    R5,USPMD
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP37               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP37               YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    USP37               NO
        @COPY  LINE
USP37    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,1(R5)
         AR    R3,R5
         SR    R4,R5
         BNZ   USP36
USP38    ICM   R4,B'0011',2(R3)    GET NO. OF CONNECT GROUPS
         LA    R3,6(R3)            POINT TO FIRST NAME IN LIST
         LTR   R4,R4               AT LEAST ONE?
         BNP   USP52               NO
         MVC   LASPG(11),=CL11'- CONNECT :'
USP39    MVC   LACN(L'LACN),0(R3)  MOVE NAME
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP40               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP40               YES
        @X     B006                CONNECTS LIST
USP40    MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R3,8(R3)            ADVANCE IN NAME LIST
         BCT   R4,USP39            LOOP IF ANY MORE
         B     USP52
USPMV    MVC   LAUS(*-*),USNAME    << EXECUTED >>
USPMD    MVC   0(*-*,R14),0(R3)    << EXECUTED >>
USP50    MVC   USERUSRN(L'USERUSRN),USERUSRN-1
         EX    R1,USPMU            EXECUTE MOVE
         TM    STATUS,STUPT
         BO    USP51
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
USP51   @OUT   USERUSR
         B     USP2
USPMU    MVC   USERUSRN(*-*),USNAME     << EXECUTED >>
         SPACE 1
*        GET DATA-SET'S PROFILES OF THIS USER.
         SPACE 1
USP52   @X     B007                GET USER DATA-SETS PROFILES.
         L     R7,WK1A             RESTORE MAIN RACF WORK AREA
         B     USP2
         DROP  R2,R7
USP90    TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    USP99               NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    USP99               YES
         L     R1,COUNTER
         CVD   R1,WORKD
         ED    USENDN,WORKD+5
        @LIST  USEND
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    USP99               NO
        @COPY  USEND
USP99   @XE
         EJECT
AUSA    ICHEACTN FIELD=AUTHOR
AUSF1   ICHEACTN FIELD=FLAG1
AUSF2   ICHEACTN FIELD=FLAG2
AUSF3   ICHEACTN FIELD=FLAG3
AUSF4   ICHEACTN FIELD=FLAG4
AUSF5   ICHEACTN FIELD=FLAG5
AUSD    ICHEACTN FIELD=DFLTGRP
AUSP    ICHEACTN FIELD=PGMRNAME
AUSI    ICHEACTN FIELD=INSTDATA
AUSU    ICHEACTN FIELD=UAUDIT
AUSF6   ICHEACTN FIELD=FLAG6
AUSM    ICHEACTN FIELD=MODELNAM
AUSS    ICHEACTN FIELD=CONGRPCT
AUSN    ICHEACTN FIELD=CONGRPNM
         SPACE 1
SUSPUL   DC    A(*-*)
SUSPUG   DC    A(*-*)
         DC    A(*-*)
         DC    A(8)
SUSPUW   DC    A(*-*)
SUSPUN   DC    XL1'80',AL3(*-*)
         SPACE 1
         DC    AL1(L'USTIT)
USTIT    DC    C'1RACF USER''S - DATA-SETS ACCESSES LIST :'
         DC    AL1(USENDL)
USEND    DC    C'0END OF RACF USER''S - DATA-SETS ACCESSES LIST '
         DC    C'(USER''S TOTAL :'
USENDN   DC    XL6'402020202120',C').'
USENDL   EQU   *-USEND
         DC    AL1(USUSRL)
USUSR    DC    CL3' ',CL3'-- ',C'RACF READ TYPE=USR (RC='
USUSRRC  DC    CL2' ',C'/AC='
USUSRAC  DC    CL1' ',C').'
USUSRL   EQU   *-USUSR
         DC    AL1(USERUSRL)
USERUSR  DC    CL3' ',CL3'-- '
USERUSRN DC    CL8' ',C' : RACF READ TYPE=USR '
         DC    C'(INVALID WORK AREA RECEIVED BACK).'
USERUSRL EQU   *-USERUSR
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        PRINT  GEN
        #WKDS
         EJECT
        #LALNE
         SPACE 2
         END
