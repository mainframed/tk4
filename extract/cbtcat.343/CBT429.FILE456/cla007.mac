A007     TITLE 'CLEAN-UP - READ RACF (DATA-SETS) CONTENTS (A007).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
A007    @XS    ALIAS=RRACFDS
         TM    WHAT,WHCHECK        CHECK RUN REQUESTED?
         BO    LP99                YES
         TM    STATUS,STBRACF      RACF MUST BE BYPASSED?
         BO    LP99                YES
         LA    R1,ZERODS
         ST    R1,SRDSL
         MVC   LPTRG,TBRGDS
         MVC   LPTVR,TBVRGN
         SPACE 1
*        READ RACF DATA-SETS DEFINITIONS.
         SPACE 1
         L     R5,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
         L     R7,WK1A             RACF WORK AREA
         USING WKDS,R7
         ST    R7,SRDSW
         XC    WORKRBA,WORKRBA
         TM    RUNIND,RUTEST       TEST MODE?
         BZ    LP0                 NO
         MVC   LPUPTT,=CL16'SIMULATED (TEST)'
LP0      XC    WORKDDC,WORKDDC
LP1      TM    WORKRBA+6,X'80'
         BO    *+L'*+8
         NI    LPOSW+1,OFF
         B     *+L'*+4
         OI    LPOSW+1,ON
         XC    WORKRBA,WORKRBA
         XC    WORKRES(WORKBSL),WORKRES
         LA    R0,ZERODS
LPOSW    NOP   LPO1
        ICHEINTY ENTRY=(R0),WKAREA=(R7),GENERIC=NO,MF=(E,LPLST)
         B     LPO2
LPO1    ICHEINTY ENTRY=(R0),WKAREA=(R7),GENERIC=UNCOND,MF=(E,LPLST)
LPO2     LTR   R15,R15
         BZ    LP2
         CH    R15,=H'12'          NO SUBSEQUENT ENTRIES?
         BE    LP90                TRUE, IT IS THE END
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   ERR20               NO
         STC   R0,RACFERRE
         OI    RACFERRE,C'0'
        @OUT   RACFERR
         LH    R15,=H'36'
         B     ERR20
LP2      LA    R14,WORKBS
         A     R14,WORKTL
         STCM  R14,B'0111',SRDSN+1
         XR    R14,R14
         IC    R14,ZERODS
         LA    R14,1(R14)
         ST    R14,SRDSG
         OC    WORKTL,WORKTL       DATA RETURNED?
         BZ    LP3                 NO
         CLC   WORKVRA(2),=XL2'0008'    AUTHOR RECEIVED?
         BE    LP4                 YES
LP3     @OUT   LPUSI
         XR    R15,R15
         B     ERR20
LP4      L     R6,TBBYR            DON'T CONTROL TABLE ADDRESS
         USING BYPRDS,R6
LP5      CLI   BYPRLG,FMON         END OF TABLE?
         BE    LP10                YES
         IC    R15,BYPRLG
         EX    R15,LPCL            MATCH?
         BE    LPAL1               YES, DON'T CONTROL IT
         LA    R6,BYPRPX+1(R15)
         B     LP5
LPCL     CLC   ZERODSN(*-*),BYPRPX << EXECUTED >>
         SPACE 1
*        ALTERNATE PROCESS (ONLY DISCRETE PROFILES) :
*        - ADD ALTER ACCESS FOR OWNER IF ABSENT.
*        - CHANGE TO ALTER ACCESS FOR OWNER IF IT WAS DIFFERENT.
         SPACE 1
LPAL1   @SNAPS SN,SRDSL,RRO
         L     R0,LPCB             UPDATE BYPRACF COUNT
         AH    R0,=H'1'
         ST    R0,LPCB
         TM    BYPRFL,BYPRS        SKIP REQUESTED?
         BZ    LPAL3               NO
         LA    R2,LPSKMSN          YES, BYPASS ALL PROCESS
         BAS   R14,LPALS
         TM    LPASW+1,ON
         BO    LPAL2
         OI    LPASW+1,ON
        @OUT   LPUPT,=PL2'+40',U
LPAL2   @OUT   LPSKMS              RACF PROCESS SKIPED
         B     LP0
LPAL3    MVI   DSNIN,C' '
         MVC   DSNIN+1(L'DSNIN-1),DSNIN
         XR    R1,R1
         IC    R1,ZERODS
         BCTR  R1,0
         EX    R1,LPALMV
         LA    R14,BYPRDC          DISCRETE COUNT
         OC    WORKVRA+10(2),WORKVRA+10 ANY VOLUMES?
         BNZ   LPAL4               YES, DISCRETE
         LA    R1,DSNIN(R1)        NO, GENERIC
         CLI   0(R1),C'*'          VERIFIED?
         BNE   LPAL5               NO, WHAT'S THAT ...
         MVI   0(R1),0             ALLOW CHECK
         LA    R14,BYPRGC          GENERIC COUNT
LPAL4    CLC   DSNPV,DSNIN         CHECK SEQUENCE?
         BNH   LPAL10              OK
LPAL5    LA    R2,LPLPMSN          NO, NOW TRY TO BYPASS IT
         BAS   R14,LPALS
         TM    LPASW+1,ON
         BO    LPAL6
         OI    LPASW+1,ON
        @OUT   LPUPT,=PL2'+40',U
LPAL6   @OUT   LPLPMS              PROBABLE RACF LOOP
         B     LP0
LPALMV   MVC   DSNIN(*-*),ZERODSN  << EXECUTED >>
         SPACE 1
LPALS    XR    R1,R1               R2 : TARGET FIELD ADDRESS
         LA    R15,ZERODSN         R14 : LINK REGISTER
LPALS1   CLI   0(R15),C'.'         R1, R15 : WORK REGISTERS
         BE    LPALS2
         LA    R1,1(R1)
         LA    R15,1(R15)
         B     LPALS1
LPALS2   MVI   0(R2),C' '
         MVC   1(L'ZERODSN-1,R2),0(R2)
         EX    R1,LPALSMN
         MVI   0(R15),X'FF'
         LA    R1,1(R1)
         STC   R1,ZERODS
         BR    R14                 RETURN
LPALSMN  MVC   0(*-*,R2),ZERODSN   << EXECUTED >>
         SPACE 1
LPAL10   MVC   DSNPV,DSNIN         UPDATE SEQUENCE
         ICM   R15,B'0111',0(R14)
         LA    R15,1(R15)          UPDATE COUNT
         STCM  R15,B'0111',0(R14)
         MVC   LPACNAM(8),WORKVRA+2     GET OWNER
         XR    R2,R2
         LA    R3,WORKVRA+10
         ICM   R2,B'0011',0(R3)
         BZ    LP1                 NO VOLUMES = GENERIC (LET IT AS IS)
         LA    R3,2(R2,R3)         SKIP VOLUME(S)
         ICM   R2,B'0011',0(R3)
         LA    R3,2(R2,R3)         SKIP TYPE(S)
         ICM   R2,B'0011',0(R3)
         LA    R3,2(R2,R3)         SKIP UNIVERSAL ACCESS
         XR    R4,R4
         LR    R2,R4
         ICM   R4,B'0011',2(R3)    GET NO. OF USERS IN ACCESS LIST
         ICM   R2,B'0011',4(R3)    GET LENGTH OF NAMES LIST
         LA    R3,6(R3)            POINT TO NAMES LIST
         LA    R2,2(R2,R3)         POINT TO ACCESS CODES
         LTR   R4,R4               AT LEAST ONE?
         BNP   LPAL13              NO
LPAL11   CLC   LPACNAM(8),0(R3)    IS IT OWNER?
         BNE   LPAL12              NO
         CLI   0(R2),ACEEALTR      ALTER ACCESS?
         BE    LP1                 YES, O.K.
         B     LPAL20              NO, CHANGE IT
LPAL12   LA    R3,8(R3)            ADVANCE IN NAME LIST
         LA    R2,1(R2)            ADVANCE IN ACCESS CODES
         BCT   R4,LPAL11           LOOP IF ANY MORE
LPAL13   MVI   LPUSAN,C' '
         MVC   LPUSAN+1(L'LPUSAN-1),LPUSAN
         XR    R1,R1
         IC    R1,ZERODS           GET LENGTH OF DSNAME
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,LPALUM           MOVE DATA-SET NAME
        @FILL  LPUSAN
         TM    RUNIND,RUNORACF+RUTEST   NO RACF ACTION OR TEST RUN?
         BNZ   LPAL15              YES
         LA    R0,ZERODS
        ICHEINTY ALTER,TYPE='DS',ENTRY=(R0),ACTIONS=(LPSA)
         LTR   R15,R15             TEST COMPLETION CODE
         BNZ   LPAL30
         CLI   LPTUS+1,0           IS USER ALREADY IN LIST?
         BE    LPAL14              NO, IT IS ADDED
         MVC   LPUSAR(L'LPUSAR),=CL6'FAILED'
         B     LPAL15
LPALUM   MVC   LPUSAN(*-*),ZERODSN << EXECUTED >>
LPAL14   MVC   LPUSAR(L'LPUSAR),=CL6'O.K.'
LPAL15   TM    BYPRFL,BYPRL        NOLIST REQUESTED?
         BO    LP1                 YES
LPASW    NOP   LPAL16
         OI    LPASW+1,ON
        @OUT   LPUPT,=PL2'+40',U
LPAL16   MVC   LPUSAO,LPACNAM      SET OWNER
        @OUT   LPUSA
         B     LP1
LPAL20   MVI   LPUSCN,C' '
         MVC   LPUSCN+1(L'LPUSCN-1),LPUSCN
         XR    R1,R1
         IC    R1,ZERODS           GET LENGTH OF DSNAME
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,LPALUS           MOVE DATA-SET NAME
        @FILL  LPUSCN
         TM    RUNIND,RUNORACF+RUTEST   NO RACF ACTION OR TEST RUN?
         BNZ   LPAL22              YES
         LA    R0,ZERODS
        ICHEINTY ALTERI,TYPE='DS',ENTRY=(R0),ACTIONS=(LPCH)
         LTR   R15,R15             TEST COMPLETION CODE
         BNZ   LPAL40
         CLI   LPTUSI+1,0          IS USER ALREADY IN LIST?
         BE    LPAL21              YES, IT IS CHANGED
         MVC   LPUSCR(L'LPUSCR),=CL6'FAILED'
         B     LPAL22
LPALUS   MVC   LPUSCN(*-*),ZERODSN << EXECUTED >>
LPAL21   MVC   LPUSCR(L'LPUSCR),=CL6'O.K.'
LPAL22   TM    BYPRFL,BYPRL        NOLIST REQUESTED?
         BO    LP1                 YES
         TM    LPASW+1,ON
         BO    LPAL23
         OI    LPASW+1,ON
        @OUT   LPUPT,=PL2'+40',U
LPAL23   MVC   LPUSCO,LPACNAM      SET OWNER
        @OUT   LPUSC
         B     LP1
LPAL30  @SNAPS SN,SRDSL,RAE
         MVC   LPUSEDS,LPUSAN
         CVD   R15,WORKD
         UNPK  LPUSERC,WORKD+6(2)
         OI    LPUSERC+1,C'0'
         MVI   LPUSEAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   LPAL31              NO
         STC   R0,LPUSEAC
         OI    LPUSEAC,C'0'
LPAL31   TM    LPASW+1,ON
         BO    LPAL32
         OI    LPASW+1,ON
        @OUT   LPUPT,=PL2'+40',U
LPAL32  @OUT   LPUSE
         B     LP0
LPAL40  @SNAPS SN,SRDSL,RAE
         MVC   LPUSRDS,LPUSCN
         CVD   R15,WORKD
         UNPK  LPUSRRC,WORKD+6(2)
         OI    LPUSRRC+1,C'0'
         MVI   LPUSRAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   LPAL41              NO
         STC   R0,LPUSRAC
         OI    LPUSRAC,C'0'
LPAL41   TM    LPASW+1,ON
         BO    LPAL42
         OI    LPASW+1,ON
        @OUT   LPUPT,=PL2'+40',U
LPAL42  @OUT   LPUSR
         B     LP0
         DROP  R6
         SPACE 1
*        NORMAL PROCESSING (DISCRETE PROFILE) :
*        LOOK IF DATA-SET DEFINED TO RACF HAS BEEN FOUND ON A VOLUME.
         SPACE 1
LP10    @SNAPS SF,SRDSL,RRD
         LH    R6,WORKVRA+10
         LTR   R6,R6               ANY VOLUME?
         BZ    LP60                NO, GENERIC PROFILE
         L     R0,LPCD             UPDATE DISCRETE COUNT
         AH    R0,=H'1'
         ST    R0,LPCD
         MVI   LPFLGS,0
         LA    R3,WORKVRA+12(R6)   SKIP VOLUME(S)
         XR    R4,R4
         ICM   R4,B'0011',0(R3)
         LA    R3,2(R4,R3)         SKIP TYPE(S)
         ICM   R4,B'0011',0(R3)
         BZ    *+L'*+8
         CLI   2(R3),ACEEALTR      UNIVERSAL ACCESS IS ALTER?
         BE    LP14                YES
         LA    R3,2(R4,R3)         START OF VARIABLE FIELDS
         XR    R1,R1
         ICM   R1,B'0011',2(R3)    GET NO. OF USERS IN ACCESS LIST
         ICM   R4,B'0011',4(R3)    LENGTH OF NAMES LIST
         LA    R3,6(R3)            POINT TO NAMES LIST
         LA    R4,2(R4,R3)         POINT TO ACCESS CODES
         LTR   R1,R1               AT LEAST ONE?
         BNP   LP12                NO
LP11     CLC   0(8,R3),USID        IS IT MYSELF REFERENCED?
         BE    LP13                YES
         LA    R3,8(R3)            NO, ADVANCE IN NAMES LIST
         LA    R4,1(R4)            ADVANCE IN ACCESSS CODES
         BCT   R1,LP11             LOOP IF ANY MORE
LP12     CLC   WORKVRA+2(8),USID   OWNER IS MYSELF?
         BE    LP14                YES
         OI    LPFLGS,ZFADD        NO, SAY ADD NEEDED
         B     LP14
LP13     CLI   0(R4),ACEEALTR      IS IT THE ALTER ACCESS?
         BE    LP14                YES
         OI    LPFLGS,ZFCHG        NO, SAY CHANGE NEEDED
LP14     LA    R4,WORKVRA+12
         LA    R3,2(R6,R4)
         USING DSPTYPE,R3
         USING DSPVOLSR,R4
         MVI   WORKDS,C' '         CLEAR DSNAME
         MVC   WORKDS+1(L'WORKDS-1),WORKDS
         XR    R1,R1
         IC    R1,ZERODS           LENGTH OF DSNAME
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,LPMO
         NI    LPSW+1,OFF
         CH    R6,=H'6'            SINGLE VOLUME RESPONSE?
         BNE   LP20                NO, MULTI-VOLUME
         XR    R6,R6               YES, INDICATE IT
LP20     L     R2,DSTAB            START OF DSNAME'S TABLE
         USING DSTABDS,R2          ESTABLISH ADDRESSABILITY
LP21     CLI   0(R2),FMOFF         END OF TABLE?
         BE    LP40                YES
         CLC   DSNME,WORKDS        IS IT THIS ONE?
         BE    LP23                YES
         CLC   DSANME,WORKDS       IS IT THE TRUENAME?
         BE    LP30                YES
LP22     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         B     LP21                CONTINUE
LP23     CLC   DSVOLSN(L'DSVOLSN),DSPVOLSR   LOOK IF SAME VOLUME?
         BNE   LP25                NO
         LTR   R6,R6               SINGLE VOLUME?
         BNZ   LP24                NO, MULTI-VOLUME
         CLI   DSVORG,XOM          MULTI-VOLUME DEFINED?
         BNE   LP26                NO, SINGLE, O.K.
         MVI   DSRCVRY,YRM         INDICATE MULTI-VOLUME REQ.
         B     LP26
LP24     CLI   DSVORG,XOM          MULTI-VOLUME DEFINED?
         BE    LP26                YES, O.K.
         MVI   DSRCVRY,YRS         INDICATE SINGLE VOLUME REQ.
         B     LP26
LP25     CLC   DSALTVO(L'DSALTVO),DSPVOLSR   SAME AS CAT. VOL.?
         BNE   LP22                NO, GO SEARCH NEXT ONE
LP26     MVI   DSRACF,XRD          INDICATE DATA-SET RACF DISCRETE
         MVC   DSFLGS,LPFLGS       SET DATA-SET ACTION FLAGS
         TM    DSPTYPE,DSPTP       VSAM DATA-SET?
         BZ    LP50                NO
         MVI   DSRTYPE,XRTV        INDICATE IT IN TABLE
         B     LP50
LPMO     MVC   WORKDS(*-*),ZERODSN << EXECUTED >>
LP30     OI    LPSW+1,ON
LP31     MVC   DSFLGS,LPFLGS       SET DATA-SET ACTION FLAGS
         CLC   DSCVOL(L'DSCVOL),DSPVOLSR     SAME AS CAT. VOL.?
         BE    LP32                YES
         MVC   DSALTVO(L'DSALTVO),DSPVOLSR
LP32     MVI   DSRACF,XRD          INDICATE DATA-SET RACF DISCRETE
         OI    DSFLGS,ZFTNU        SET TRUE NAME USE
         TM    DSPTYPE,DSPTP       VSAM DATA-SET?
         BZ    LP33                NO
         MVI   DSRTYPE,XRTV        INDICATE IT IN TABLE
LP33     LA    R2,DSTABL(R2)       ADVANCE IN TABLE
         CLI   0(R2),FMOFF         END OF TABLE?
         BE    LPSW                YES
         CLC   DSANME,WORKDS       IS IT THE TRUENAME?
         BNE   LP33                NO
         B     LP31
         SPACE 1
*        DATA-SET DEFINED TO RACF BUT IT DON'T EXIST ANYWHERE.
         SPACE 1
LPSW     B     LP50
LP40     TM    STATUS,STONLY       ONLY REQUESTED?
         BO    LP1                 YES, GET NEXT RACF DEFINITION
         BCT   R5,*+L'*+6          CONTROL
         XR    R15,R15             DSNAME'S TABLE OVERFLOW
         B     ERR17
         XC    0(DSTABL,R2),0(R2)  CLEAR
         MVC   DSNME,WORKDS        MOVE NAME TO TABLE
         MVC   DSALTVO(L'DSALTVO),DSPVOLSR   MOVE VOLSER TO TABLE
         MVI   DSRACF,XRD          SET DATA-SET RACF DISCRETE
         TM    DSPTYPE,DSPTP       VSAM DATA-SET?
         BZ    *+L'*+4             NO
         MVI   DSRTYPE,XRTV        INDICATE IT IN TABLE
         TM    DSPTYPE,DSPMDL      RACF MODEL DATA-SET PROFILE?
         BZ    *+L'*+4             NO
         MVI   DSRUSE,XRUM         INDICATE IT IN TABLE
         LA    R2,DSTABL(R2)
         MVI   0(R2),FMOFF         SET FINAL MARKER
         SPACE 1
LP50     SH    R6,=H'6'            ANY OTHER VOLUME?
         BNP   LP1                 NO, GET NEXT RACF DEFINITION
         LA    R4,6(R4)            NEXT VOLUME
         B     LP20
         DROP  R2,R3,R4
         SPACE 1
*        NORMAL PROCESSING (GENERIC PROFILE) :
*        BUILD THE HLQ AND GENERIC TABLES ENTRIES.
         SPACE 1
LP60     L     R0,LPCG             UPDATE GENERIC COUNT
         AH    R0,=H'1'
         ST    R0,LPCG
         LA    R0,L'WORKD          EXTRACT HIGH-LEVEL INDEX
         LA    R1,1
         LA    R14,WORKD
         MVC   WORKD,=CL8' '
         LA    R15,ZERODSN
LP61     CLI   0(R15),C'.'
         BE    LP62
         MVC   0(1,R14),0(R15)
         LA    R1,1(R1)
         LA    R14,1(R14)
         LA    R15,1(R15)
         BCT   R0,LP61
         B     ERR75               ERROR, HIGH-LEVEL NAME TOO LONG
LP62     LM    R2,R3,LPTRG         HLQ-GEN. TABLE CURRENT POINTERS
         CLI   0(R2),FMOFF
         BE    LP63
         CLC   WORKD(8),0(R2)
         BE    LP64
         LA    R2,NGDL(R2)
         CL    R2,TBRGDSE
         BNL   ERR76               TABLE OVERFLOW
LP63     MVC   0(8,R2),WORKD       BUILD HLQ ENTRY
         STCM  R3,B'1111',8(R2)
         ST    R2,LPTRG
         LA    R2,NGDL(R2)
         CL    R2,TBRGDSE
         BNL   ERR76               TABLE OVERFLOW
         MVI   0(R2),FMOFF         SET FINAL MARKER
         B     LP64+L'LP64
LP64     BCTR  R3,0
         XR    R14,R14
         IC    R14,ZERODS
         SR    R14,R1
         LA    R2,1(R3,R14)
         CL    R2,TBVRGNE
         BNL   ERR77               TABLE OVERFLOW
         STC   R14,0(R3)           BUILD GENERIC ENTRY
         BCTR  R14,0
         EX    R14,LPMVG
         MVI   0(R2),FMON          SET FINAL MARKER
         LA    R2,1(R2)
         ST    R2,LPTVR
         B     LP1                 GET NEXT RACF DEFINITION
LPMVG    MVC   1(*-*,R3),1(R15)    << EXECUTED >>
         DROP  R7
         SPACE 1
LP90     ST    R5,DSTABCT          DSNAME'S TABLE OVERFLOW CONTROL
         L     R1,DSTABOV          COMPUTE NO. OF ELEMENTS
         SR    R1,R5
         ST    R1,DSTABNE
        @X     B004                SORT DSNAME'S TABLE
         L     R1,LPCD             DISPLAY COUNTERS
         CVD   R1,WORKD
         ED    LPTDN,WORKD+4
         MVC   LPTDM(STIDL),16(R8) SET MODULE ALIAS NAME
        @OUT   LPTD,=PL2'+50'
         L     R1,LPCG
         CVD   R1,WORKD
         ED    LPTGN,WORKD+4
        @OUT   LPTG
         L     R1,LPCB
         CVD   R1,WORKD
         ED    LPTBN,WORKD+4
        @OUT   LPTB
LP99    @XE
         EJECT
LPLST   ICHEINTY NEXTC,TYPE='DS',                                      C
               ACTIONS=(LPAU,LPAV,LPAT,LPUA,LPSC,LPSL,LPSR),MF=L
         SPACE 1
LPAU    ICHEACTN FIELD=AUTHOR
LPAV    ICHEACTN FIELD=VOLSER
LPAT    ICHEACTN FIELD=DSTYPE
LPUA    ICHEACTN FIELD=UNIVACS
LPSC    ICHEACTN FIELD=ACLCNT
LPSL    ICHEACTN FIELD=USERID
LPSR    ICHEACTN FIELD=USERACS
LPSA    ICHEACTN FIELD=ACL,FLDATA=(11,LPACL),TESTS=LPTUS
LPCH    ICHEACTN FIELD=ACL,FLDATA=(11,LPACL),TESTS=LPTUSI
LPTUS   ICHETEST FIELD=USERID,FLDATA=(8,LPACNAM),COND=NE
LPTUSI  ICHETEST FIELD=USERID,FLDATA=(8,LPACNAM),COND=EQ
         SPACE 1
LPCD     DC    F'0'
LPCG     DC    F'0'
LPCB     DC    F'0'
LPTRG    DC    A(*-*)              HLQ TABLE CURRENT POINTER
LPTVR    DC    A(*-*)              GEN. TABLE CURRENT POINTER
         SPACE 1
SRDSL    DC    A(*-*)
SRDSG    DC    A(*-*)
SRDSW    DC    A(*-*)
SRDSN    DC    XL1'80',AL3(*-*)
         SPACE 1
LPACL    DS    0CL11               ACCESS LIST ENTRY
LPACNAM  DC    CL8' '
         DC    AL1(ACEEALTR)       ALTER ACCESS
         DC    XL2'0000'           ACCESS COUNT
DSNIN    DC    CL44' '
DSNPV    DC    CL44' '
LPFLGS   DC    XL1'0'
         SPACE 1
         DC    AL1(RACFERRL)
RACFERR  DC    C'0===== RACF READ ERROR ===== (R0 = '
RACFERRE DC    C' )'
RACFERRL EQU   *-RACFERR
         DC    AL1(L'LPUPT+L'LPUPTT)
LPUPT    DC    C'0RACF OWNER PROCESSING : '
LPUPTT   DC    CL16' '
         DC    AL1(LPUSAL)
LPUSA    DC    CL6' '
LPUSAN   DC    CL44' ',C' - ALTER ACCESS ADDED : '
LPUSAO   DC    CL8' ',C' - '
LPUSAR   DC    CL6'O.K.'
LPUSAL   EQU   *-LPUSA
         DC    AL1(LPUSCL)
LPUSC    DC    CL6' '
LPUSCN   DC    CL44' ',C' - CHANGED TO ALTER ACCESS : '
LPUSCO   DC    CL8' ',C' - '
LPUSCR   DC    CL6'O.K.'
LPUSCL   EQU   *-LPUSC
         DC    AL1(LPUSEL)
LPUSE    DC    CL3' ',CL3'-- '
LPUSEDS  DC    CL44' ',C' - RACF ALTER TYPE=DS (RC='
LPUSERC  DC    CL2' ',C'/AC='
LPUSEAC  DC    CL1' ',C').'
LPUSEL   EQU   *-LPUSE
         DC    AL1(LPUSRL)
LPUSR    DC    CL3' ',CL3'-- '
LPUSRDS  DC    CL44' ',C' - RACF ALTERI TYPE=DS (RC='
LPUSRRC  DC    CL2' ',C'/AC='
LPUSRAC  DC    CL1' ',C').'
LPUSRL   EQU   *-LPUSR
         DC    AL1(LPUSIL)
LPUSI    DC    CL3' ',C'-- RACF READ TYPE=DS '
         DC    C'(INVALID WORK AREA RECEIVED BACK).'
LPUSIL   EQU   *-LPUSI
         DC    AL1(LPLPMSL)
LPLPMS   DC    C' ---- ERROR - PROBABLE RACF LOOP IN : '
LPLPMSN  DC    CL44' '
LPLPMSL  EQU   *-LPLPMS
         DC    AL1(LPSKMSL)
LPSKMS   DC    CL6' ',C'RACF PROCESS SKIP REQUESTED FOR : '
LPSKMSN  DC    CL44' '
LPSKMSL  EQU   *-LPSKMS
         DC    AL1(L'LPTD+L'LPTDM+L'LPTDN+1)
LPTD     DC    C'0 COUNTERS ('
LPTDM    DC    C'        ) - DISCRETE PROFILES :'
LPTDN    DC    XL8'4020202020202120',C','
         DC    AL1(L'LPTG+L'LPTGN+1)
LPTG     DC    C'                         GENERIC PROFILES :'
LPTGN    DC    XL8'4020202020202120',C','
         DC    AL1(L'LPTB+L'LPTBN+1)
LPTB     DC    C'                         BYPRACF PROFILES :'
LPTBN    DC    XL8'4020202020202120',C'.'
         EJECT
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        ICHRRPF
         EJECT
        PRINT  GEN
        #WKDS
         SPACE 2
        #BYPRDS
         SPACE 2
         END
