DEVT     TITLE 'GENERATE DEVICES TABLES - RECORD/TRACK CAPACITIES.'
        PRINT  OFF
         MACRO
&NAME   @TANK  &NT
         LCLA  &N
         LCLC  &CML
         AIF   (T'&NT EQ 'O').MIS
         AIF   (&NT LE 1).INV
&N       SETA  1
&NAME    DC    2CL(PNVALL)' '      LINE 1
.A       ANOP
&N       SETA  &N+1
&CML     SETC  '           &N '
         DC    2CL(PNVALL)' '&CML
         AIF   (&N LT &NT).A
&NAME.E  EQU   *
&NAME.N  EQU   (&NAME.E-&NAME)/(2*PNVALL)
         MEXIT
.MIS     MNOTE 8,' OPERAND MISSING ********************************** '
         MEXIT
.INV     MNOTE 8,' INVALID OPERAND ********************************** '
         MEND
        PRINT  ON
         SPACE 1
DEVTGEN  START 0
         SPACE 1
*        *-----------------------------------------------------------*
*        * GENERATE DEVICES TABLES PROGRAM - RECORD/TRACK CAPACITIES *
*        *-----------------------------------------------------------*
*
* PURPOSE : GENERATE THE RECORD/TRACK CAPACITIES TABLES FOR
* --------- REQUESTED DEVICES AS MEMBERS READY TO BE HANDLED
*           BY THE 'LISTNO' PROGRAM OR AS PANELS READY TO BE
*           PROCESSED BY ISPF HELP (SEE PANEL PAJTBLK).
*
* FILES DESCRIPTION (DD-CARDS) :
* ------------------------------
*
*        1. SYSIN    - INPUT GENERATE REQUESTS ORDERS.
*
*        2. SYSPRINT - EVENTUAL DIAGNOSTICS OR THE GENERATION LIST
*                      (LIST REQUEST).
*
*        3. SYSPUNCH - GENERATED INPUT FOR 'IEBUPDTE' TO LOAD THE
*                      DEVICES TABLES NOTES MEMBERS.
*
*        4. SYSISPF  - GENERATED INPUT FOR 'IEBUPDTE' TO LOAD THE
*                      DEVICES TABLES ISPF PANELS MEMBERS.
*
* GENERATE REQUESTS ORDERS :
* --------------------------
*
*        LIST : LIST DEVICES CHARACTERISTICS AND TABLES (SYSPRINT).
*
*        ADD  : TO GENERATE AN IEBUPDTE ADD MEMBER CARD (SYSPUNCH OR
*               SYSISPF).
*
*        REPL : TO GENERATE AN IEBUPDTE REPLACE MEMBER CARD (SYSPUNCH
*               OR SYSISPF). THIS IS THE DEFAULT WHEN STARTING.
*
*        ...  : ANY DEVICE TYPE AS CODED IN THE UNIT PARAMETER OF
*               A JCL DD STATEMENT FOR THE CURRENT SYSTEM, EVENTUALLY
*               FOLLOWED BY / .. WHICH INDICATES AN ISPF PANEL REQUEST
*               INSTEAD OF A NOTES MEMBER (.. IS A 2-CHARACTERS ID).
*               - IEBUPDTE ADD OR REPLACE CARD FOR NOTES MEMBER NAMES
*                 ARE CODED 'T...' (I.E. T3350 FOR 3350, OR T3330M1
*                 FOR 3330-1).
*               - IEBUPDTE ADD OR REPLACE CARD FOR ISPF PANELS NAMES
*                 ARE CODED 'PAJTB..N' AND 'PAJTK..N'.
         EJECT
* CODING RULES : COLUMNS 1 TO 72 ARE USABLE FOR REQUESTS, EACH ORDER
*                MUST BE SEPARATED FROM THE NEXT BY A COMMA AND ALL
*                SPACES (BLANKS) ARE NEVER CONSIDERED (ELIMINATED).
*                THE LAST ORDER MAY BE FOLLOWED BY A PERIOD TO END
*                THE REQUESTS.
*
* AUTHOR :     P.A. MOINIL
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=(R10,R11,R12)
        CALL   HRDAY,(CD,DATE),VL
         MVC   CD(2),DATE+6
         MVC   DATE+6(2),DATE
         MVC   DATE(2),CD
         EJECT
*        READ GENERATE REQUESTS.
         SPACE 1
RDCD    $READ  CARD
         LTR   R15,R15
         BM    REND
         BNZ   ERRRD               READ ERROR ... EXIT.
         OI    SWPRC,SWPM
         LM    R7,R9,SCAN
NXCD     MVC   CD,=CL8' '
         MVC   CP(L'CP),=CL8' '
         LA    R5,CD
         LA    R6,L'CD+1
TNXC     CLI   0(R7),C' '
         BE    NXSC
         CLI   0(R7),C'/'
         BE    RPNL
         CLI   0(R7),C','
         BE    ENDKEY
         CLI   0(R7),C'.'
         BE    ENDKEY
         BCT   R6,*+L'*+4
         B     ERRDT
         MVC   0(1,R5),0(R7)
         LA    R5,1(R5)
         B     NXSC
RPNL     CLI   CD,C' '
         BE    ERRDM
         CLI   CP,C' '
         BNE   ERRDP
         LA    R5,CP
         LA    R6,L'CP+1
NXSC     BXLE  R7,R8,TNXC
         CLI   CD,C' '
         BE    RDCD
ENDKEY   CLC   CD,=CL8'LIST'
         BNE   *+L'*+8
         OI    SWNTS,SWTR
         B     TEND
         CLC   CD,=CL8'ADD'
         BE    *+L'*+10
         CLC   CD,=CL8'REPL'
         BNE   *+L'*+14
         MVC   BOP(L'BOP),CD
         NI    SWNTS,255-SWTR
         B     TEND
         CLC   CD,=CL8'SET'
         BNE   *+L'*+10
         MVC   P3,=CL4'=/EJ'
         B     TEND
         CLC   CD,=CL8'RESET'
         BNE   COM
         MVC   P3,=CL4'1   '
TEND     CLI   0(R7),C'.'
         BE    REND
         BXLE  R7,R8,NXCD
         B     RDCD
COM      CLI   CD,C' '
         BE    TEND
         OI    SWPRC,SWPR
         MVI   SWPGN,0
         TM    SWNTS,SWTR          LIST REQUESTED?
         BO    *+L'*+12            YES.
         CLI   CP,C' '
         BE    *+L'*+4
         OI    SWPGN,SWGP
         EJECT
* NOW USING THE DEVICE TYPE (I.E. 2314, 3330-1, 3350, ...), SET THE
* DEVICE CHARACTERISTICS :
*
*        NTRPCY : NO. OF TRACKS PER CYLINDER.
*        NBYPTR : NO. OF BYTES PER TRACK.
*        NUNKB  : KEY OVERHEAD.
*        NOVHB  : OVERHEAD BLOCK OR OVERHEAD 1.
*        NOVHLB : OVERHEAD LAST BLOCK OR OVERHEAD 2.
*        NTOLF  : TOLERANCE FACTOR OR MODULO FACTOR.
*        COMPUTATIONAL FLAGS :
*               SWTF : OFF - APPLY TOLERANCE FACTOR.
*                      ON - DO NOT APPLY TOLERANCE FACTOR.
*               SWMD : OFF - NORMAL DEVICE (SEE TOLERANCE FACTOR).
*                      ON - MODULO DEVICE.
         SPACE 1
         XC    NTRPCY,NTRPCY       CLEAR ALL VARIABLES.
         XC    NBYPTR,NBYPTR
         XC    NUNKB,NUNKB
         XC    NOVHB,NOVHB
         XC    NOVHLB,NOVHLB
         XC    NTOLF,NTOLF
         NI    SWNTS,255-SWKL-SWTF-SWMD
         L     R1,=V(DAUTTB)       GET UNIT TYPES TABLE ADDRESS.
LOOP     CLC   2(L'CD,R1),CD       CONTROL DEVICE TYPE REQUESTED.
         BE    UFND
         LA    R1,10(,R1)
         CLI   0(R1),X'FF'
         BE    ERROR1
         B     LOOP
UFND     L     R14,CVTPTR          GET CVT ADDRESS.
         USING CVT,R14
         L     R6,CVTZDTAB         DEV. CONSTANT TABLE ADDRESS.
         DROP  R14
         XR    R14,R14
         LR    R15,R14
         IC    R14,0(R1)           GET DEVICE TYPE.
         STC   R14,UTYPE
         IC    R15,0(R14,R6)       GET DISPLACEMENT OF TABLE.
         LA    R15,0(R15,R6)       COMPUTE ADDRESS OF TABLE.
         CLR   R15,R6
         BE    ERROR2              DEVICE NOT IN 'IECZDTAB' TABLE.
         MVC   NTRPCY+2(2),2(R15)
         MVC   NBYPTR+2(2),4(R15)
         TM    9(R15),BIT3
         BZ    NORMAL
         OI    SWNTS,SWMD
         MVC   NOVHB+2(2),6(R15)
         MVC   NOVHLB+2(2),10(R15)
         MVC   NTOLF+2(2),20(R15)
         B     CMPT
NORMAL   MVC   NUNKB+3(1),8(R15)
         TM    9(R15),BIT4
         BO    ANYB
         MVC   NOVHB+3(1),6(R15)
         MVC   NOVHLB+3(1),7(R15)
         B     TTLF
ANYB     MVC   NOVHB+2(2),6(R15)
         MVC   NOVHLB+2(2),6(R15)
TTLF     TM    9(R15),BIT7
         BZ    NTLF
         MVC   NTOLF+2(2),10(R15)
         B     CMPT
NTLF     OI    SWNTS,SWTF
CMPT     TM    SWNTS,SWTR          LIST REQUESTED?
         BZ    PNCH                NO.
         SPACE 1
*        PRINT THE DEVICE CHARACTERISTICS.
         SPACE 1
         MVC   L1A,CD
         L     R0,NTRPCY
         BAS   R14,CNVRT
         MVC   L1B(L'L1B),ZONE+L'ZONE-L'L1B
         LA    R5,L1
         BAS   R6,PRT
         L     R0,NBYPTR
         BAS   R14,CNVRT
         TM    SWNTS,SWMD
         BZ    LNRM
         MVC   L14A(L'L14A),ZONE+L'ZONE-L'L14A
         L     R0,NTOLF
         BAS   R14,CNVRT
         MVC   L14B(L'L14B),ZONE+L'ZONE-L'L14B
         LA    R5,L14
         BAS   R6,PRT
         L     R0,NOVHB
         BAS   R14,CNVRT
         MVC   L15A(L'L15A),ZONE+L'ZONE-L'L15A
         L     R0,NOVHLB
         BAS   R14,CNVRT
         MVC   L15B(L'L15B),ZONE+L'ZONE-L'L15B
         LA    R5,L15
         BAS   R6,PRT
         B     CONT
LNRM     MVC   L2A(L'L2A),ZONE+L'ZONE-L'L2A
         L     R0,NUNKB
         BAS   R14,CNVRT
         MVC   L2B(L'L2B),ZONE+L'ZONE-L'L2B
         LA    R5,L2
         BAS   R6,PRT
         L     R0,NOVHB
         BAS   R14,CNVRT
         MVC   L3A(L'L3A),ZONE+L'ZONE-L'L3A
         L     R0,NOVHLB
         BAS   R14,CNVRT
         MVC   L3B(L'L3B),ZONE+L'ZONE-L'L3B
         LA    R5,L3
         BAS   R6,PRT
         L     R0,NTOLF
         BAS   R14,CNVRT
         MVC   L4A(L'L4A),ZONE+L'ZONE-L'L4A
         MVC   L4B,=CL4'YES.'
         TM    SWNTS,SWTF
         BZ    *+L'*+6
         MVC   L4B,=CL4'NO.'
         LA    R5,L4
         BAS   R6,PRT
CONT     LA    R5,L5
         BAS   R6,PRT
         LA    R5,L6
         BAS   R6,PRT
         LA    R5,L7
         BAS   R6,PRT
         LA    R5,L8
         BAS   R6,PRT
         LA    R5,L0
         BAS   R6,PRT
         B     CYCLE
         SPACE 1
*        START GENERATED INPUT FOR 'IEBUPDTE'.
         SPACE 1
PNCH     TM    SWPGN,SWGP          ISPF PANELS REQUESTED?
         BO    PPNL                YES.
         LA    R0,1
         ST    R0,LPG
         MVC   CDN,CD
         LA    R1,CDN
         LA    R0,L'CDN
         CLI   0(R1),C'-'
         BNE   *+L'*+4
         MVI   0(R1),C'M'
         LA    R1,1(R1)
         BCT   R0,*-16
         MVC   P1A,BOP
         MVC   P1B,CDN
         LA    R5,P1
         BAS   R6,PCH
         LA    R5,P2
         BAS   R6,PCH
         XC    LLN,LLN
         MVC   P3A,DATE
         LA    R5,P3
         BAS   R6,PCH
         MVC   P4A,CD
         L     R0,LPG
         BAS   R14,CNVRT
         MVC   P4B(L'P4B),ZONE+L'ZONE-L'P4B
         LA    R5,P4
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         MVC   P5A,CD
         L     R0,NTRPCY
         BAS   R14,CNVRT
         MVC   P5B(L'P5B),ZONE+L'ZONE-L'P5B
         LA    R5,P5
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         LA    R5,P6
         BAS   R6,PCH
         LA    R5,P7
         BAS   R6,PCH
         LA    R5,P8
         BAS   R6,PCH
         LA    R5,P9
         BAS   R6,PCH
         LA    R5,P10
         BAS   R6,PCH
         LA    R5,P11
         BAS   R6,PCH
         LA    R5,P12
         BAS   R6,PCH
         B     CYCLE
PPNL     MVC   PN1A,BOP
         MVI   PN1B,C'B'
         MVC   PN1C,CP
         MVI   PN2A,C' '
         MVC   PN2A+1(L'PN2A-1),PN2A
         MVC   PN2A(L'CD),CD
         LA    R1,PN2A+L'CD-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   2(L'PN2TX1,R1),PN2TX1
         LA    R1,L'PN2TX1+3(R1)
         L     R0,NTRPCY
         BAS   R14,CNVRT
         CLI   ZONE+L'ZONE-2,C' '
         BE    *+L'*+10
         MVC   0(1,R1),ZONE+L'ZONE-2
         LA    R1,1(R1)
         MVC   0(1,R1),ZONE+L'ZONE-1
         MVC   2(L'PN2TX2,R1),PN2TX2
         LA    R1,L'PN2TX2+1(R1)
         LA    R14,PN2A+L'PN2A-2
         SR    R14,R1
         BM    *+L'*+10
         MVC   1(*-*,R1),0(R1)
         EX    R14,*-6
         MVC   PN4A,PN4TX1
         MVC   PN6A,PN6TX1
         MVC   PN6B,PN6TX1
         MVI   PN13A,C'B'
         MVC   PN13B,CP
         MVI   PSEQ,0
         XC    PPOS,PPOS
         SPACE 1
*        GENERATE DEVICE TABLE.
*
*        NOTE : UNKEYED RECORDS COMPUTATION STARTS FROM DL=1, AND
*               KEYED RECORDS FROM A FIXED KL=1 AND DL=1.
         SPACE 1
CYCLE    TM    SWNTS,SWKL
         BZ    NOKEY
         MVC   LREC,=F'1'
         MVC   KEYL,=F'1'
         B     RCYCL
NOKEY    MVC   LREC,=F'1'
         MVC   KEYL,=F'0'
RCYCL    NI    SWNTS,255-SWFS-SWLS
NCYCL   TRKCALC FUNCTN=TRKCAP,TYPE=UTYPE,R=1,K=KEYL+3,DD=LREC+2,       X
               REGSAVE=YES
         LTR   R15,R15
         BNZ   CMPR
         ST    R0,NRT
         TM    SWNTS,SWFS
         BO    CM2
         OI    SWNTS,SWFS
CMR      MVC   NRTP,NRT
         L     R1,LREC
         A     R1,KEYL
         ST    R1,MREC
         B     CM3
CM2      CLC   NRT,NRTP
         BNE   CMPRN
CM3      L     R1,LREC
         A     R1,=F'1'
         ST    R1,LREC
         B     NCYCL
CMPR     OI    SWNTS,SWLS
CMPRN    L     R1,LREC
         BCTR  R1,0
         A     R1,KEYL
         ST    R1,NREC
         TM    SWNTS,SWKL
         BO    XCMP
         TM    SWNTS,SWTR
         BZ    CMP1
         L     R0,MREC
         BAS   R14,CNVRT
         MVC   L9A(L'L9A),ZONE+L'ZONE-L'L9A
         L     R0,NREC
         BAS   R14,CNVRT
         MVC   L9B(L'L9B),ZONE+L'ZONE-L'L9B
         L     R0,NRTP
         BAS   R14,CNVRT
         MVC   L9C(L'L9C),ZONE+L'ZONE-L'L9C
         LA    R5,L9
         BAS   R6,PRT
         B     CMP3
CMP1     XR    R2,R2
         L     R3,NRTP
         M     R2,NTRPCY
         ST    R3,NRCY
         TM    SWPGN,SWGP
         BO    PPN1
         CLC   LLN,LMAX
         BNE   CMP2
         L     R1,LPG
         A     R1,=F'1'
         ST    R1,LPG
         XC    LLN,LLN
         LA    R5,P6
         BAS   R6,PCH
         MVC   P3A,DATE
         LA    R5,P3
         BAS   R6,PCH
         MVC   P4A,CD
         L     R0,LPG
         BAS   R14,CNVRT
         MVC   P4B(L'P4B),ZONE+L'ZONE-L'P4B
         LA    R5,P4
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         MVC   P5A,CD
         L     R0,NTRPCY
         BAS   R14,CNVRT
         MVC   P5B(L'P5B),ZONE+L'ZONE-L'P5B
         LA    R5,P5
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         LA    R5,P6
         BAS   R6,PCH
         LA    R5,P7
         BAS   R6,PCH
         LA    R5,P8
         BAS   R6,PCH
         LA    R5,P9
         BAS   R6,PCH
         LA    R5,P10
         BAS   R6,PCH
         LA    R5,P11
         BAS   R6,PCH
         LA    R5,P12
         BAS   R6,PCH
CMP2     L     R1,LLN
         A     R1,=F'1'
         ST    R1,LLN
         L     R0,MREC
         BAS   R14,CNVRT
         MVC   P13A(L'P13A),ZONE+L'ZONE-L'P13A
         L     R0,NREC
         BAS   R14,CNVRT
         MVC   P13B(L'P13B),ZONE+L'ZONE-L'P13B
         L     R0,NRTP
         BAS   R14,CNVRT
         MVC   P13C(L'P13C),ZONE+L'ZONE-L'P13C
         L     R0,NRCY
         BAS   R14,CNVRT
         MVC   P13D(L'P13D),ZONE+L'ZONE-L'P13D
         LA    R5,P13
         BAS   R6,PCH
         B     CMP3
PPN1     ICM   R2,B'1111',PPOS
         BNZ   PPN2
         LA    R2,PNTANK
         B     PPN4
PPN2     CL    R2,=A(PNTANKE)
         BL    PPN4
         TM    SWPGN,SWRS
         BO    *+L'*+12
         OI    SWPGN,SWRS
         LA    R2,PNTANK+PNVALL
         B     PPN4
         MVC   PN1D,PSEQ
         OI    PN1D,C'0'
         LA    R5,PN1
         BAS   R6,PCH
         LA    R5,PN2
         BAS   R6,PCH
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN4
         BAS   R6,PCH
         LA    R5,PN5
         BAS   R6,PCH
         LA    R5,PN6
         BAS   R6,PCH
         LA    R5,PN7
         BAS   R6,PCH
         LA    R5,PN8
         BAS   R6,PCH
         LA    R2,PNTANK
         LA    R5,PN9
         USING PNVAL,R2
PPN3     MVC   PN9A,PNVMIN
         MVC   PN9B,PNVMAX
         MVC   PN9C,PNVTRK
         MVC   PN9D,PNVCYL
         MVI   PNVAL,C' '
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         MVC   PN9E,PNVMIN
         MVC   PN9F,PNVMAX
         MVC   PN9G,PNVTRK
         MVC   PN9H,PNVCYL
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         BAS   R6,PCH
         CL    R2,=A(PNTANKE)
         BL    PPN3
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN10
         BAS   R6,PCH
         LA    R5,PN12
         BAS   R6,PCH
         XR    R2,R2
         IC    R2,PSEQ
         LA    R2,1(R2)
         STC   R2,PSEQ
         MVC   PN13C,PSEQ
         OI    PN13C,C'0'
         LA    R5,PN13
         BAS   R6,PCH
         TM    SWPGN,SWFP
         BO    *+L'*+12
         OI    SWPGN,SWFP
         LA    R5,PN14
         BAS   R6,PCH
         LA    R5,PN15
         BAS   R6,PCH
         NI    SWPGN,255-SWRS
         LA    R2,PNTANK
PPN4     L     R0,MREC
         BAS   R14,CNVRT
         MVC   PNVMIN(L'PNVMIN),ZONE+L'ZONE-L'PNVMIN
         L     R0,NREC
         BAS   R14,CNVRT
         MVC   PNVMAX(L'PNVMAX),ZONE+L'ZONE-L'PNVMAX
         L     R0,NRTP
         BAS   R14,CNVRT
         MVC   PNVTRK(L'PNVTRK),ZONE+L'ZONE-L'PNVTRK
         L     R0,NRCY
         BAS   R14,CNVRT
         MVC   PNVCYL(L'PNVCYL),ZONE+L'ZONE-L'PNVCYL
         DROP  R2
         AL    R2,=A(2*PNVALL)
         ST    R2,PPOS
CMP3     TM    SWNTS,SWLS
         BZ    CMR
         TM    SWNTS,SWTR
         BO    CMF
         TM    SWPGN,SWGP
         BO    PPL1
         LA    R5,P6
         BAS   R6,PCH
CMF      OI    SWNTS,SWKL
         TM    SWNTS,SWTR
         BZ    CMF1
         LA    R5,L10
         BAS   R6,PRT
         LA    R5,L11
         BAS   R6,PRT
         LA    R5,L12
         BAS   R6,PRT
         LA    R5,L13
         BAS   R6,PRT
         LA    R5,L0
         BAS   R6,PRT
         B     CYCLE
CMF1     L     R1,LPG
         A     R1,=F'1'
         ST    R1,LPG
         XC    LLN,LLN
         MVC   P3A,DATE
         LA    R5,P3
         BAS   R6,PCH
         MVC   P4A,CD
         L     R0,LPG
         BAS   R14,CNVRT
         MVC   P4B(L'P4B),ZONE+L'ZONE-L'P4B
         LA    R5,P4
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         MVC   P5A,CD
         L     R0,NTRPCY
         BAS   R14,CNVRT
         MVC   P5B(L'P5B),ZONE+L'ZONE-L'P5B
         LA    R5,P5
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         LA    R5,P6
         BAS   R6,PCH
         LA    R5,P14
         BAS   R6,PCH
         LA    R5,P8
         BAS   R6,PCH
         LA    R5,P15
         BAS   R6,PCH
         LA    R5,P10
         BAS   R6,PCH
         LA    R5,P11
         BAS   R6,PCH
         LA    R5,P12
         BAS   R6,PCH
         B     CYCLE
PPL1     MVC   PN1D,PSEQ
         OI    PN1D,C'0'
         LA    R5,PN1
         BAS   R6,PCH
         LA    R5,PN2
         BAS   R6,PCH
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN4
         BAS   R6,PCH
         LA    R5,PN5
         BAS   R6,PCH
         LA    R5,PN6
         BAS   R6,PCH
         LA    R5,PN7
         BAS   R6,PCH
         LA    R5,PN8
         BAS   R6,PCH
         LA    R2,PNTANK
         LA    R5,PN9
         USING PNVAL,R2
PPL2     MVC   PN9A,PNVMIN
         MVC   PN9B,PNVMAX
         MVC   PN9C,PNVTRK
         MVC   PN9D,PNVCYL
         MVI   PNVAL,C' '
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         MVC   PN9E,PNVMIN
         MVC   PN9F,PNVMAX
         MVC   PN9G,PNVTRK
         MVC   PN9H,PNVCYL
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         BAS   R6,PCH
         CL    R2,=A(PNTANKE)
         BL    PPL2
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN11
         BAS   R6,PCH
         LA    R5,PN12
         BAS   R6,PCH
         LA    R5,PN14
         BAS   R6,PCH
         LA    R5,PN15
         BAS   R6,PCH
         MVI   PN1B,C'K'
         MVC   PN4A,PN4TX2
         MVC   PN6A,PN6TX2
         MVC   PN6B,PN6TX2
         MVI   PN13A,C'K'
         MVI   PSEQ,0
         XC    PPOS,PPOS
         NI    SWPGN,SWGP
         OI    SWNTS,SWKL
         B     CYCLE
XCMP     TM    SWNTS,SWTR
         BZ    XCMP1
         L     R0,MREC
         BAS   R14,CNVRT
         MVC   L9A(L'L9A),ZONE+L'ZONE-L'L9A
         L     R0,NREC
         BAS   R14,CNVRT
         MVC   L9B(L'L9B),ZONE+L'ZONE-L'L9B
         L     R0,NRTP
         BAS   R14,CNVRT
         MVC   L9C(L'L9C),ZONE+L'ZONE-L'L9C
         LA    R5,L9
         BAS   R6,PRT
         B     XCMP3
XCMP1    XR    R2,R2
         L     R3,NRTP
         M     R2,NTRPCY
         ST    R3,NRCY
         TM    SWPGN,SWGP
         BO    XPPN1
         CLC   LLN,LMAX
         BNE   XCMP2
         L     R1,LPG
         A     R1,=F'1'
         ST    R1,LPG
         XC    LLN,LLN
         LA    R5,P6
         BAS   R6,PCH
         MVC   P3A,DATE
         LA    R5,P3
         BAS   R6,PCH
         MVC   P4A,CD
         L     R0,LPG
         BAS   R14,CNVRT
         MVC   P4B(L'P4B),ZONE+L'ZONE-L'P4B
         LA    R5,P4
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         MVC   P5A,CD
         L     R0,NTRPCY
         BAS   R14,CNVRT
         MVC   P5B(L'P5B),ZONE+L'ZONE-L'P5B
         LA    R5,P5
         BAS   R6,PCH
         LA    R5,P0
         BAS   R6,PCH
         LA    R5,P6
         BAS   R6,PCH
         LA    R5,P14
         BAS   R6,PCH
         LA    R5,P8
         BAS   R6,PCH
         LA    R5,P15
         BAS   R6,PCH
         LA    R5,P10
         BAS   R6,PCH
         LA    R5,P11
         BAS   R6,PCH
         LA    R5,P12
         BAS   R6,PCH
XCMP2    L     R1,LLN
         A     R1,=F'1'
         ST    R1,LLN
         L     R0,MREC
         BAS   R14,CNVRT
         MVC   P13A(L'P13A),ZONE+L'ZONE-L'P13A
         L     R0,NREC
         BAS   R14,CNVRT
         MVC   P13B(L'P13B),ZONE+L'ZONE-L'P13B
         L     R0,NRTP
         BAS   R14,CNVRT
         MVC   P13C(L'P13C),ZONE+L'ZONE-L'P13C
         L     R0,NRCY
         BAS   R14,CNVRT
         MVC   P13D(L'P13D),ZONE+L'ZONE-L'P13D
         LA    R5,P13
         BAS   R6,PCH
         B     XCMP3
XPPN1    ICM   R2,B'1111',PPOS
         BNZ   XPPN2
         LA    R2,PNTANK
         B     XPPN4
XPPN2    CL    R2,=A(PNTANKE)
         BL    XPPN4
         TM    SWPGN,SWRS
         BO    *+L'*+12
         OI    SWPGN,SWRS
         LA    R2,PNTANK+PNVALL
         B     XPPN4
         MVC   PN1D,PSEQ
         OI    PN1D,C'0'
         LA    R5,PN1
         BAS   R6,PCH
         LA    R5,PN2
         BAS   R6,PCH
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN4
         BAS   R6,PCH
         LA    R5,PN5
         BAS   R6,PCH
         LA    R5,PN6
         BAS   R6,PCH
         LA    R5,PN7
         BAS   R6,PCH
         LA    R5,PN8
         BAS   R6,PCH
         LA    R2,PNTANK
         LA    R5,PN9
         USING PNVAL,R2
XPPN3    MVC   PN9A,PNVMIN
         MVC   PN9B,PNVMAX
         MVC   PN9C,PNVTRK
         MVC   PN9D,PNVCYL
         MVI   PNVAL,C' '
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         MVC   PN9E,PNVMIN
         MVC   PN9F,PNVMAX
         MVC   PN9G,PNVTRK
         MVC   PN9H,PNVCYL
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         BAS   R6,PCH
         CL    R2,=A(PNTANKE)
         BL    XPPN3
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN10
         BAS   R6,PCH
         LA    R5,PN12
         BAS   R6,PCH
         XR    R2,R2
         IC    R2,PSEQ
         LA    R2,1(R2)
         STC   R2,PSEQ
         MVC   PN13C,PSEQ
         OI    PN13C,C'0'
         LA    R5,PN13
         BAS   R6,PCH
         TM    SWPGN,SWFP
         BO    *+L'*+12
         OI    SWPGN,SWFP
         LA    R5,PN14
         BAS   R6,PCH
         LA    R5,PN15
         BAS   R6,PCH
         NI    SWPGN,255-SWRS
         LA    R2,PNTANK
XPPN4    L     R0,MREC
         BAS   R14,CNVRT
         MVC   PNVMIN(L'PNVMIN),ZONE+L'ZONE-L'PNVMIN
         L     R0,NREC
         BAS   R14,CNVRT
         MVC   PNVMAX(L'PNVMAX),ZONE+L'ZONE-L'PNVMAX
         L     R0,NRTP
         BAS   R14,CNVRT
         MVC   PNVTRK(L'PNVTRK),ZONE+L'ZONE-L'PNVTRK
         L     R0,NRCY
         BAS   R14,CNVRT
         MVC   PNVCYL(L'PNVCYL),ZONE+L'ZONE-L'PNVCYL
         DROP  R2
         AL    R2,=A(2*PNVALL)
         ST    R2,PPOS
XCMP3    TM    SWNTS,SWLS
         BZ    CMR
         TM    SWNTS,SWTR
         BO    TEND
         TM    SWPGN,SWGP
         BO    XPPL1
         LA    R5,P6
         BAS   R6,PCH
         B     TEND
XPPL1    MVC   PN1D,PSEQ
         OI    PN1D,C'0'
         LA    R5,PN1
         BAS   R6,PCH
         LA    R5,PN2
         BAS   R6,PCH
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN4
         BAS   R6,PCH
         LA    R5,PN5
         BAS   R6,PCH
         LA    R5,PN6
         BAS   R6,PCH
         LA    R5,PN7
         BAS   R6,PCH
         LA    R5,PN8
         BAS   R6,PCH
         LA    R2,PNTANK
         LA    R5,PN9
         USING PNVAL,R2
XPPL2    MVC   PN9A,PNVMIN
         MVC   PN9B,PNVMAX
         MVC   PN9C,PNVTRK
         MVC   PN9D,PNVCYL
         MVI   PNVAL,C' '
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         MVC   PN9E,PNVMIN
         MVC   PN9F,PNVMAX
         MVC   PN9G,PNVTRK
         MVC   PN9H,PNVCYL
         MVC   PNVAL+1(PNVALL-1),PNVAL
         LA    R2,PNVALL(R2)
         BAS   R6,PCH
         CL    R2,=A(PNTANKE)
         BL    XPPL2
         LA    R5,PN3
         BAS   R6,PCH
         LA    R5,PN11
         BAS   R6,PCH
         LA    R5,PN12
         BAS   R6,PCH
         LA    R5,PN14
         BAS   R6,PCH
         LA    R5,PN15
         BAS   R6,PCH
         B     TEND
ERROR1   MVC   LM1A,CD
         LA    R5,LM1
         BAS   R6,PRT
         B     EXERR
ERROR2   MVC   LM2A,CD
         LA    R5,LM2
         BAS   R6,PRT
         B     EXERR
ERRDT    CLI   CP,C' '
         BNE   ERRPN
         MVC   LM3A,CP
         MVC   LM3A+L'LM3A(1),0(R7)
         LA    R5,LM3
         BAS   R6,PRT
         B     EXERR
ERRRD    LA    R5,LM4
         BAS   R6,PRT
         B     EXERR
ERRPU    LA    R5,LM5
         BAS   R6,PRT
         B     EXERR
ERRDM    LA    R5,LM6
         BAS   R6,PRT
         B     EXERR
ERRDP    LA    R5,LM7
         BAS   R6,PRT
         B     EXERR
ERRPN    MVC   LM8A,CP
         MVC   LM8A+L'LM8A(1),0(R7)
         LA    R5,LM8
         BAS   R6,PRT
         B     EXERR
REND     TM    SWPRC,SWPM
         BO    *+L'*+12
         LA    R5,LM0
         BAS   R6,PRT
         B     EXERR
         TM    SWPRC,SWPR
         BO    TSPU
         LA    R5,LM9
         BAS   R6,PRT
EXERR    LA    R9,16
         B     EXRET
TSPU     TM    SWPRC,SWPN
         BZ    *+L'*+12
         NI    SWPGN,255-SWGP
         LA    R5,P16
         BAS   R6,PCH
         TM    SWPRC,SWPI
         BZ    *+L'*+12
         OI    SWPGN,SWGP
         LA    R5,P16
         BAS   R6,PCH
         XR    R9,R9
EXRET   $RCLOSE
        $UCLOSE
        $PCLOSE
        $XRET  CC=(R9)
         EJECT
CNVRT    CVD   R0,WORK             CONVERT SUBROUTINE.
         MVC   ZONE,MASK
         ED    ZONE,WORK
         BR    R14
         SPACE 1
PRT     $PRINT (R5)                PRINT SUBROUTINE.
         LTR   R15,R15
         BZR   R6
         B     EXERR               PRINT ERROR ... EXIT.
         SPACE 1
PCH      TM    SWPGN,SWGP          PUNCH SUBROUTINE.
         BO    PCH1
         OI    SWPRC,SWPN
        $PUNCH (R5)
         B     PCH2
PCH1     OI    SWPRC,SWPI
        $PUNCH (R5),,DDNISPF
PCH2     LTR   R15,R15
         BZR   R6
         B     ERRPU               PUNCH ERROR ... EXIT.
         EJECT
*        LITERAL POOL.
         SPACE 1
        LTORG
         EJECT
*        CONSTANTS AND WORK-AREAS.
         SPACE 1
WORK     DC    D'0'
ZONE     DC    CL16' '
MASK     DC    XL16'40202020202020202020202020202120'
NTRPCY   DC    F'0'
NBYPTR   DC    F'0'
NUNKB    DC    F'0'
NOVHB    DC    F'0'
NOVHLB   DC    F'0'
NTOLF    DC    F'0'
LPG      DC    F'0'
LLN      DC    F'0'
LREC     DC    F'0'
KEYL     DC    F'0'
MREC     DC    F'0'
NREC     DC    F'0'
NRT      DC    F'0'
NRTP     DC    F'0'
NRCY     DC    F'0'
PPOS     DC    F'0'
LMAX     DC    F'40'
SCAN     DC    A(CARD,1,CARD+71)
DATE     DC    CL8' '
CD       DC    CL8' '
CDN      DC    CL8' '
DDNISPF  DC    CL8'SYSISPF '
BOP      DC    CL4'REPL'
CP       DC    CL2' '
CARD     DC    CL80' '
UTYPE    DC    XL1'0'
PSEQ     DC    XL1'0'
         SPACE 1
SWPRC    DC    XL1'0'              SWITCHES.
SWPM     EQU   X'01'                    CONTROL CARD READ.
SWPR     EQU   X'02'                    ORDER PROCESSED.
SWPN     EQU   X'04'                    OUTPUT NOTES IS ACTIVE.
SWPI     EQU   X'08'                    OUTPUT ISPF IS ACTIVE.
SWNTS    DC    XL1'0'              SWITCHES.
SWFS     EQU   X'01'                    FIRST COMPUTATION.
SWLS     EQU   X'02'                    LAST COMPUTATION.
SWTR     EQU   X'04'                    LIST REQUESTED.
SWKL     EQU   X'08'                    KEYED RECORD COMPUTATION.
SWTF     EQU   X'10'                    TOLERANCE FACTOR.
SWMD     EQU   X'20'                    MODULO DEVICE.
SWPGN    DC    XL1'0'              SWITCHES.
SWGP     EQU   X'01'                    ISPF PANELS GENERATION.
SWRS     EQU   X'02'                    TANK RIGHT SIDE (PANEL).
SWFP     EQU   X'04'                    FIRST PANEL OUT.
         EJECT
*        OUTPUT PRINT AND ERROR MESSAGES.
         SPACE 1
         DC    AL1(L'L0)
L0       DC    C'  '
         DC    AL1(L1L)
L1       DC    C'1     DEVICE TYPE : '
L1A      DC    CL8' ',C'(1 CYL = '
L1B      DC    CL2' ',C' TRKS),'
L1L      EQU   *-L1
         DC    AL1(L2L)
L2       DC    C'0     BYTES/TRK = '
L2A      DC    CL6' ',C', KEY OVERHEAD = '
L2B      DC    CL6' ',C','
L2L      EQU   *-L2
         DC    AL1(L3L)
L3       DC    C'0     OVERHEAD BLOCK = '
L3A      DC    CL6' ',C', OVERHEAD LAST BLOCK = '
L3B      DC    CL6' ',C','
L3L      EQU   *-L3
         DC    AL1(L4L)
L4       DC    C'0     TOLERANCE FACTOR = '
L4A      DC    CL6' ',C', APPLY INDICATOR = '
L4B      DC    CL4' '
L4L      EQU   *-L4
         DC    AL1(L5L)
L5       DC    CL6'0 '
L5A      DC    C'CAPACITIES WITHOUT KEYS.'
L5L      EQU   *-L5
         DC    AL1(L6L)
L6       DC    CL6'0 '
         DC    (L'L5A)C'*'
L6L      EQU   *-L6
         DC    AL1(L7L)
L7       DC    C'0       MIN.     MAX.     REC.TR'
L7L      EQU   *-L7
         DC    AL1(L8L)
L8       DC    C'        ----     ----     ------'
L8L      EQU   *-L8
         DC    AL1(L9L)
L9       DC    CL6' '
L9A      DC    CL6' ',C' - '
L9B      DC    CL6' ',CL5' '
L9C      DC    CL6' '
L9L      EQU   *-L9
         DC    AL1(L10L)
L10      DC    CL6'0 '
L10A     DC    C'CAPACITIES WITH KEYS.'
L10L     EQU   *-L10
         DC    AL1(L11L)
L11      DC    CL6'0 '
         DC    (L'L10A)C'*'
L11L     EQU   *-L11
         DC    AL1(L12L)
L12      DC    C'0       MIN.     MAX.     REC/TR'
L12L     EQU   *-L12
         DC    AL1(L13L)
L13      DC    C'        ----     ----     ------'
L13L     EQU   *-L13
         DC    AL1(L14L)
L14      DC    C'0     BYTES/TRK = '
L14A     DC    CL6' ',C', MODULO FACTOR = '
L14B     DC    CL6' ',C','
L14L     EQU   *-L14
         DC    AL1(L15L)
L15      DC    C'0     OVERHEAD 1 = '
L15A     DC    CL6' ',C', OVERHEAD 2 = '
L15B     DC    CL6' ',C','
L15L     EQU   *-L15
         DC    AL1(LM0L)
LM0      DC    C'0     SYSIN - CONTROL CARD MISSING.'
LM0L     EQU   *-LM0
         DC    AL1(LM1L)
LM1      DC    C'0     SYSIN - '
LM1A     DC    CL8' ',C' : UNKNOWED DEVICE TYPE.'
LM1L     EQU   *-LM1
         DC    AL1(LM2L)
LM2      DC    C'0     SYSIN - '
LM2A     DC    CL8' ',C' : UNSUPPORTED DEVICE TYPE.'
LM2L     EQU   *-LM2
         DC    AL1(LM3L)
LM3      DC    C'0     SYSIN - '
LM3A     DC    CL8' ',C' ... : INVALID DEVICE TYPE.'
LM3L     EQU   *-LM3
         DC    AL1(LM4L)
LM4      DC    C'0     SYSIN - ERROR.'
LM4L     EQU   *-LM4
         DC    AL1(LM5L)
LM5      DC    C'0     SYSPUNCH - ERROR.'
LM5L     EQU   *-LM5
         DC    AL1(LM6L)
LM6      DC    C'0     SYSIN - PANEL WITH NO DEVICE TYPE.'
LM6L     EQU   *-LM6
         DC    AL1(LM7L)
LM7      DC    C'0     SYSIN - TWICE PANEL NUMBER.'
LM7L     EQU   *-LM7
         DC    AL1(LM8L)
LM8      DC    C'0     SYSIN - '
LM8A     DC    CL2' ',C' ... : INVALID PANEL ID.'
LM8L     EQU   *-LM8
         DC    AL1(LM9L)
LM9      DC    C'0     SYSIN - NO ORDER PROCESSED.'
LM9L     EQU   *-LM9
         SPACE 2
        COPY   DEVTGENP
         EJECT
*        ISPF PANELS VALUES TANK.
         SPACE 1
PNTANK  @TANK  14
         SPACE 1
PNVAL    DSECT
PNVMIN   DS    CL7                 MINIMUM.
PNVMAX   DS    CL7                 MAXIMUM.
PNVTRK   DS    CL5                 TRACK.
PNVCYL   DS    CL8                 CYLINDER.
PNVALL   EQU   *-PNVAL
         SPACE 2
        PRINT  NOGEN
        IEZBITS
        CVT    DSECT=YES,LIST=YES
         SPACE 2
         END
