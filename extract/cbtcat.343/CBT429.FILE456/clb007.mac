B007     TITLE 'CLEAN-UP - GET USER DATA-SETS PROFILES (B007).'
        PRINT  OFF
        COPY   CLMAC
        PRINT  ON
B007    @XS
*        GET DATA-SET'S PROFILES OF THIS USER.
*
*        NORMAL PROCESS :
*        - CHANGE ANY ALTER ACCESS TO UPDATE ACCESS.
*        - CHANGE OWNER IF NOT ITSELF (ALLOWS DMS RESTORE).
*        - DELETE ALTER ACCESS FOR SELF-USER.
*        - ONLY IF GRPACC AND UNIVERSAL ACESS IS NONE, ADD READ ACCESS
*          FOR GROUP IF ABSENT (OBSOLETED, NOW A BYPASS HAS BEEN CODED
*          BELOW, SEE AT INSTRUCTION MARKED WITH --- *1 ---).
         SPACE 1
         LA    R1,WORKDS
         ST    R1,SUSPDL
         LA    R1,SVSNAP+4
         ST    R1,SUSPDS
         USING LALNE,R2
         USING WKDS,R7
         MVI   LASPU,C'-'
         XC    ZERODSN,ZERODSN
         XR    R1,R1
         IC    R1,USNMEL           GET LENGTH OF USERID
         STC   R1,ZERODS           SET LENGTH OF USERID
         STC   R1,TUSIDL
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,UDMV             EXECUTE MOVE
         MVC   TUSID(L'TUSID),ZERODSN   MOVE FOR TEST OF USERID
         LA    R1,TUSID+1(R1)
         MVI   0(R1),C'.'
         L     R7,WK2A             GET ALTERNATE RACF WORK AREA
         ST    R7,SUSPDW
         XC    WORKRBA,WORKRBA
         XC    WORKDDC,WORKDDC
UD1      TM    WORKRBA+6,X'80'
         BO    *+L'*+8
         NI    UDOSW+1,OFF
         B     *+L'*+4
         OI    UDOSW+1,ON
         XC    WORKRBA,WORKRBA
         XC    WORKRES(WORKBSL),WORKRES
         MVC   VSNERR,=CL6' '
         LA    R0,ZERODS
UDOSW    NOP   UDO1
        ICHEINTY ENTRY=(R0),WKAREA=(R7),GENERIC=NO,MF=(E,UDLST)
         B     UDO2
UDO1    ICHEINTY ENTRY=(R0),WKAREA=(R7),GENERIC=UNCOND,MF=(E,UDLST)
UDO2     CH    R15,=H'12'          END FOR USER?
         BE    UD4                 YES
         LTR   R15,R15             TEST RETURN CODE
         BNZ   *+L'*+14            BRANCH IF ERROR
         XR    R1,R1
         IC    R1,TUSIDL
         EX    R1,UDTU             SAME USER?
         BNE   UD4                 NO, END FOR USER
         LA    R14,WORKBS
         A     R14,WORKTL
         STCM  R14,B'0111',SUSPDN+1
         XR    R14,R14
         IC    R14,ZERODS
         LA    R14,1(R14)
         ST    R14,SUSPDG
        @SNAPS SF,SUSPDL,RDS
         LTR   R15,R15             TEST RETURN CODE
         BZ    UD10                O.K.
         CVD   R15,WORKD           RETURN CODE
         UNPK  USDSRC,WORKD+6(2)
         OI    USDSRC+1,C'0'
         MVI   USDSAC,C' '
         CH    R15,=H'36'          BLOCKS ERROR?
         BNE   UD2                 NO
         STC   R0,USDSAC
         OI    USDSAC,C'0'
UD2      MVC   USDSNM(L'USDSNM),USDSNM-1
         XR    R1,R1
         IC    R1,USNMEL           GET LENGTH OF USERID
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,UDMU             EXECUTE MOVE
         TM    STATUS,STUPT
         BO    UD3
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
UD3     @OUT   USDS
         B     UD99
UDMV     MVC   ZERODSN(*-*),USNAME << EXECUTED >>
UDTU     CLC   ZERODSN(*-*),TUSID  << EXECUTED >>
UDMU     MVC   USDSNM(*-*),USNAME  << EXECUTED >>
UD4      CLI   LASPU,C' '          ANY DS-ENTRY FOR USER?
         BE    UD99                YES
         MVC   LADS(14),=CL14'(NO DATA-SETS)'
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD99                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD99                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD99                NO
        @COPY  LINE
         B     UD99                GET NEXT USER
UD10     MVI   UDTSW,0             RESET
         OC    WORKTL,WORKTL       DATA RETURNED?
         BZ    UD11                NO
         CLC   WORKVRA(2),=XL2'0008'    AUTHOR RECEIVED?
         BNE   UD11                NO
         CLC   WORKVRA+10(2),=XL2'0001' FLAG1 RECEIVED?
         BNE   UD11                NO
         CLC   WORKVRA+13(2),=XL2'0008' GROUP NAME RECEIVED?
         BNE   UD11                NO
         XR    R15,R15
         LA    R3,WORKVRA+23
         IC    R15,2(R3)           INST. DATA LENGTH
         LA    R3,3(R15,R3)        SKIP INST. DATA
         ICM   R15,B'0011',0(R3)   GET NUMBER OF VOLUMES
         BNZ   *+L'*+4
         OI    UDTSW,UDTSGF        NO VOLUMES = GENERIC
         LA    R4,2(R15,R3)        SKIP VOLUME(S)
         ICM   R15,B'0011',0(R4)
         LA    R4,2(R15,R4)        SKIP TYPE(S)
         CLC   0(2,R4),=XL2'0001'  UNIVACS RECEIVED?
         BNE   UD11                NO
         CLC   3(2,R4),=XL2'0002'  USERS/GROUPS NO. RECEIVED?
         BE    UD13                YES
UD11     MVC   USERDSN(L'USERDSN),USERDSN-1
         XR    R1,R1
         IC    R1,USNMEL           GET LENGTH OF USERID
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,UDUN             EXECUTE MOVE
         TM    STATUS,STUPT
         BO    UD12
         OI    STATUS,STUPT
        @OUT   USUPT,=PL2'+40',U
UD12    @OUT   USERDS
         B     UD99
UDUN     MVC   USERDSN(*-*),USNAME << EXECUTED >>
UDMO     MVC   LADS(*-*),ZERODSN   << EXECUTED >>
UDMVD    MVC   LADS+13(*-*),0(R6)    << EXECUTED >>
UD13     XR    R1,R1
         IC    R1,ZERODS           GET LENGTH OF DSNAME
         BCTR  R1,0                -1 FOR MOVE
         EX    R1,UDMO             MOVE DATA-SET NAME
        @FILL  LADS
         OI    UDSW1+1,ON
         OI    UDSW2+1,ON
         OI    UDSW3+1,ON
         OI    UDSW4+1,ON
         L     R7,WK1A             RESTORE MAIN RACF WORK AREA
         CLI   WORKVRA+24,X'80'    GROUP ACCESS ATTRIBUTE?
         BNE   *+L'*+4             NO
         NI    UDSW3+1,OFF
         L     R7,WK2A             RESTORE ALTERNATE RACF WORK AREA
         MVI   LASPV,C'('
         MVC   LADSOW(L'LADSOW),WORKVRA+2    MOVE OWNER OF DATA-SET
         MVI   LASPW,C')'
         CLC   LADSOW,USER         OWNER OF DATA-SET IS USER ITSELF?
         BE    *+L'*+4             YES
         NI    UDSW1+1,OFF
         MVC   LAACT(16),=CL16'- UNIV. ACCESS :'
         CLI   2(R4),ACEEALTR      ALTER ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAACT+17(8),=CL8'ALTER'
         B     UD14
         CLI   2(R4),ACEECNTL      CONTROL ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAACT+17(8),=CL8'CONTROL'
         B     UD14
         CLI   2(R4),ACEEUPDT      UPDATE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAACT+17(8),=CL8'UPDATE'
         B     UD14
         CLI   2(R4),ACEEREAD      READ ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAACT+17(8),=CL8'READ'
         B     UD14
         CLI   2(R4),ACEENONE      NONE ACCESS?
         BNE   *+L'*+14            NO
         MVC   LAACT+17(8),=CL8'NONE'
         NI    UDSW4+1,OFF
         B     UD14
         LA    R1,LAACT+17         ANYTHING MIXED OR UNKNOWN
         LA    R15,2(R4)
         BAS   R14,DMPRTS
UD14     TM    WORKRBA+6,X'80'
         BO    *+L'*+10
         MVC   LAACT+26(5),=CL5'- (D)'
         B     *+L'*+6
         MVC   LAACT+26(5),=CL5'- (G)'
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD15                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD15                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD15                NO
        @COPY  LINE
UD15     XR    R4,R4
         IC    R4,WORKVRA+25       GET INST. DATA LENGTH
         LTR   R4,R4
         BZ    UD18
         MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LADS(12),=CL12'INST. DATA :'
         LA    R5,60
         LA    R6,WORKVRA+26       POINT TO INST. DATA OR NEXT
UD16     CLR   R4,R5
         BNL   *+L'*+2
         LR    R5,R4
         BCTR  R5,0
         EX    R5,UDMVD
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD17                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD17                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD17                NO
        @COPY  LINE
UD17     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R5,1(R5)
         AR    R6,R5
         SR    R4,R5
         BNZ   UD16
UD18     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LADS+25(18),=CL18'- CREATION GROUP :'
         MVC   LADS+44(8),WORKVRA+15    MOVE GROUP NAME OF CREATION
         CLI   WORKVRA+12,X'80'    GROUP ACCESSIBLE?
         BNE   *+L'*+6
         MVC   LADS+53(24),=CL24'++ GROUP DATA-SET ACCESS'
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD19                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD19                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD19                NO
        @COPY  LINE
UD19     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         MVC   LADS+25(8),=CL8'- VOL. :'
         XR    R4,R4
         ICM   R4,B'0011',0(R3)
         LA    R3,2(R3)
         BZ    UD23+L'UD23         NO VOLUMES
         USING DSPVOLSR,R3
         MVC   VSNERR(6),DSPVOLSR  SET VOLUME IN CASE ANY ACTION
UD20     LA    R5,LADS+34
         LA    R15,5               NO. OF VOL. SER. BY LINE
UD21     MVC   0(6,R5),DSPVOLSR    SET THIS VOL. SER.
         LA    R5,7(R5)
         LA    R3,6(R3)            NEXT VOL. SER.
         SH    R4,=H'6'            ANY OTHER VOLUME?
         BNP   UD23                NO
         BCT   R15,UD21            YES
         DROP  R3
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD22                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD22                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD22                NO
        @COPY  LINE
UD22     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         B     UD20
UD23     XR    R4,R4
         ICM   R4,B'0011',0(R3)
         LA    R3,2(R3)
         TM    UDTSW,UDTSGF
         BO    UD24                BRANCH IF GENERIC
         USING DSPTYPE,R3
         TM    DSPTYPE,DSPTP       VSAM DATA-SET?
         BZ    *+L'*+6             NO
         MVC   0(6,R5),=CL6'(VSAM)'
         DROP  R3
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD24                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD24                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD24                NO
        @COPY  LINE
UD24     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         AR    R3,R4               NOW SKIP UNIVACS ALREADY DONE
         ICM   R4,B'0011',0(R3)
         LA    R3,2(R3,R4)         START OF VARIABLE FIELDS
         ICM   R4,B'0011',2(R3)    GET NO. OF USERS IN ACCESS LIST
         BNZ   UD25
         LA    R3,8(R3)            NONE, SKIP ACLCNT-USERID-USERACS
         B     UD41+L'UD41
UD25     XR    R5,R5
         ICM   R5,B'0011',4(R3)    GET LENGTH OF NAMES LIST
         LA    R3,6(R3)            POINT TO NAMES LIST
         LA    R5,2(R5,R3)         POINT TO ACCESS CODES
         LTR   R4,R4               AT LEAST ONE?
         BNP   UD41                NO
         MVC   LADS+25(32),=CL32'- USER''S ACCESS LIST - - - - - -'
UD30     MVC   LAAC(L'LAAC),0(R3)  MOVE NAME
         CLC   ACCNAM,LAAC         DEFAULT GROUP DEFINED?
         BNE   *+L'*+4             NO
         OI    UDSW3+1,ON
         MVI   LASPA,C':'
         CLI   0(R5),ACEEALTR      ALTER ACCESS?
         BNE   UD32                NO
         MVC   LAAU,=CL8'ALTER'
         TM    UDTSW,UDTSGF
         BO    UD33                BRANCH IF GENERIC
         CLC   LAAC,USER           ITSELF?
         BE    UD31                YES
         LA    R1,UDCOMM
         MVI   0(R1),0
        @X     C002
         CLI   UDCOMM,0
         BE    UD33
         B     UD1
UD31     NI    UDSW2+1,OFF
         MVC   LADLM,=CL8'(DELETE)'
         B     UD33
UD32     CLI   0(R5),ACEECNTL      CONTROL ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAAU,=CL8'CONTROL'
         B     UD33
         CLI   0(R5),ACEEUPDT      UPDATE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAAU,=CL8'UPDATE'
         B     UD33
         CLI   0(R5),ACEEREAD      READ ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAAU,=CL8'READ'
         B     UD33
         CLI   0(R5),ACEENONE      NONE ACCESS?
         BNE   *+L'*+10            NO
         MVC   LAAU,=CL8'NONE'
         B     UD33
         LA    R1,LAAU             ANYTHING MIXED OR UNKNOWN
         LR    R15,R5
         BAS   R14,DMPRTS
UD33     TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD40                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD40                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD40                NO
        @COPY  LINE
UD40     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
         LA    R3,8(R3)            ADVANCE IN NAME LIST
         LA    R5,1(R5)            ADVANCE IN ACCESS CODES
         BCT   R4,UD30             LOOP IF ANY MORE
UD41     LR    R3,R5               POINT TO AUDIT FIELD
         CLC   0(2,R3),=XL2'0001'  AUDIT FLAGS RECEIVED?
         BNE   UD45                NO
         LA    R3,2(R3)            POINT TO AUDIT FLAGS
         CLI   0(R3),0             ANYTHING?
         BE    UD43                NO
         CLI   0(R3),X'10'         ONLY NONE?
         BE    UD43                YES
         MVC   LADS+25(9),=CL9'- AUDIT :'
         LA    R5,LADS+35
         BAS   R14,AUDITF
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD42                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD42                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD42                NO
        @COPY  LINE
UD42     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
UD43     LA    R3,1(R3)            POINT TO GLOBAL AUDIT FIELD
         CLC   0(2,R3),=XL2'0001'  GLOBAL AUDIT FLAGS RECEIVED?
         BNE   UD45                NO
         LA    R3,2(R3)            POINT TO GLOBAL AUDIT FLAGS
         CLI   0(R3),0             ANYTHING?
         BE    UD45                NO
         CLI   0(R3),X'10'         ONLY NONE?
         BE    UD45                YES
         MVC   LADS+25(16),=CL16'- GLOBAL AUDIT :'
         LA    R5,LADS+42
         BAS   R14,AUDITF
         TM    RUNIND,RUNOLIST     DETAIL LIST REQUESTED?
         BO    UD44                NO, SKIP THE LIST
         TM    RUNOPT,RUNLU        SUPPRESS REQUESTED?
         BO    UD44                YES
        @LIST  LINE
         TM    STATUS,STCOPY       COPY REQUESTED?
         BZ    UD44                NO
        @COPY  LINE
UD44     MVI   LINE,C' '           CLEAR LINE
         MVC   LINE+1(L'LINE-1),LINE
UD45     TM    UDTSW,UDTSGF
         BO    UD1                 BRANCH IF GENERIC
UDSW1    B     UDSW2
         OI    UDSW1+1,ON
         TM    RUNIND,RUNORACF     NO RACF ACTION WANTED?
         BO    UDSW2               YES, BYPASS
         LA    R1,UDCOMM
         MVI   0(R1),1
        @X     C002
         CLI   UDCOMM,0
         BNE   UD1
UDSW2    B     UDSW3
         OI    UDSW2+1,ON
         TM    RUNIND,RUNORACF     NO RACF ACTION WANTED?
         BO    UDSW3               YES, BYPASS
         LA    R1,UDCOMM
         MVI   0(R1),2
        @X     C002
         CLI   UDCOMM,0
         BNE   UD1
UDSW3    B     UD1
UDSW4    B     UD1
         B     UD1                 BYPASS THIS ACTION        --- *1 ---
         TM    RUNIND,RUNORACF     NO RACF ACTION WANTED?
         BO    UD1                 YES, BYPASS
         LA    R1,UDCOMM
         MVI   0(R1),3
        @X     C002
         B     UD1
         DROP  R2,R7
UD99    @XE
         EJECT
UDLST   ICHEINTY NEXTC,TYPE='DS',                                      C
               ACTIONS=(ADAU,ADF1,ADGR,ADIN,ADAV,ADAT,ADUA,ADSC,ADSL,  C
               ADSR,ADAD,ADGA),MF=L
         SPACE 1
ADAU    ICHEACTN FIELD=AUTHOR
ADF1    ICHEACTN FIELD=FLAG1
ADGR    ICHEACTN FIELD=GROUPNM
ADIN    ICHEACTN FIELD=INSTDATA
ADAV    ICHEACTN FIELD=VOLSER
ADAT    ICHEACTN FIELD=DSTYPE
ADUA    ICHEACTN FIELD=UNIVACS
ADSC    ICHEACTN FIELD=ACLCNT
ADSL    ICHEACTN FIELD=USERID
ADSR    ICHEACTN FIELD=USERACS
ADAD    ICHEACTN FIELD=AUDIT
ADGA    ICHEACTN FIELD=GAUDIT
         EJECT
SUSPDL   DC    A(*-*)
SUSPDG   DC    A(*-*)
SUSPDS   DC    A(*-*)
         DC    A(8)
SUSPDW   DC    A(*-*)
SUSPDN   DC    XL1'80',AL3(*-*)
         SPACE 1
UDCOMM   DC    AL1(0)              C002 - COMMUNICATION (ACTION/RETURN)
TUSIDL   DC    XL1'0'
TUSID    DC    XL9'0'
UDTSW    DC    XL1'0'              SWITCHES
UDTSGF   EQU   X'01'                    GENERIC PROFILE
         SPACE 1
         DC    AL1(USDSL)
USDS     DC    CL3' ',CL3'-- '
USDSNM   DC    CL8' ',C' : RACF READ TYPE=DS (RC='
USDSRC   DC    CL2' ',C'/AC='
USDSAC   DC    CL1' ',C').'
USDSL    EQU   *-USDS
         DC    AL1(USERDSL)
USERDS   DC    CL3' ',CL3'-- '
USERDSN  DC    CL8' ',C' : RACF READ TYPE=DS '
         DC    C'(INVALID WORK AREA RECEIVED BACK).'
USERDSL  EQU   *-USERDS
         SPACE 2
        @SERV
         SPACE 1
        @COMM
         SPACE 1
        ICHRRPF
         EJECT
        PRINT  GEN
        #WKDS
         SPACE 2
        #LALNE
         SPACE 2
         END
