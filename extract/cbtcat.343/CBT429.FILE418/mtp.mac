 TITLE 'MTP:  LET TSO USER KNOW ABOUT TAPE AND CART MOUNT PENDINGS'
**********************************************************************
* NAME       -   MTP                                                 *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   ISSUE MESSAGE TO TSO USER ABOUT TAPE AND            *
*                CARTRIDGE MOUNTS PENDING.                           *
*                                                                    *
* INPUT      -   NONE                                                *
*                                                                    *
* OUTPUT     -   VIA TPUT TSO SERVICE ROUTINE                        *
*                                                                    *
* SYSTEM     -   MVS/XA                                              *
*                                                                    *
* DATE       -   11/23/86                                            *
*                                                                    *
* MACROS     -   GETMAIN,FREEMAIN,LOCASCB                            *
*                                                                    *
* DSECTS     -   CVT,IEFUCBOB,IHAASCB,REGISTER                       *
*                                                                    *
*                                                                    *
* REGISTERS  -   R0   - MVS USAGE ONLY REGISTER                      *
*                R1   - MVS USAGE ONLY REGISTER                      *
*                R2   - USED BY TRANSLATE                            *
*                R3   - CVT ADDRESS                                  *
*                R4   - TAPE UCB ADDRESS                             *
*                R5   - ASCB ADDRESS USING TAPE UCB                  *
*                R6   - COUNTER OF HOW MANY TPUTS ISSUED             *
*                R7   - *** UNUSED BY PROGRAM ***                    *
*                R8   - *** UNUSED BY PROGRAM ***                    *
*                R9   - *** UNUSED BY PROGRAM ***                    *
*               R10   - *** UNUSED BY PROGRAM ***                    *
*               R11   - *** UNUSED BY PROGRAM ***                    *
*               R12   - 1ST BASE REGISTER                            *
*               R13   - SYSTEM SAVE AREA/WORK AREA ADDRESS           *
*               R14   - RETURN ADDRESS                               *
*               R15   - ENTRY POINT ADDRESS                          *
*                                                                    *
* LANGUAGE   -    OS/VS ASSEMBLER                                    *
*                                                                    *
* PRIVILEGES -    NONE                                               *
*                                                                    *
* ATTRIBUTES -    PROBLEM PROGRAM KEY 8                              *
**********************************************************************
************************************************************
*   PERFORM REENTRANT HOUSEKEEPING.                        *
************************************************************
MTP      CSECT
         USING MTP,R15             TEMPORARY ADDRESSABILITY
         B     BYPASS              BRANCH AROUND EYECATCHER
         DC    C'MTP'              MTP PROGRAM
         DC    C' '
         DC    C'&SYSDATE'         GREGORIAN DATE
         DC    C' '
         DC    C'&SYSTIME'         GREGORIAN DATE
BYPASS   DS    0H                  RE-ESTABLISH ALIGNMENT
         DROP  R15                 DONE WITH TEMP BASE NOW
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             SET BASE REGISTER
         USING MTP,R12             SET ADDRESSABILITY
************************************************************
*   GET SAVE AND WORK AREA.                                *
************************************************************
         LA    R0,WORKL            ADDRESS OF SAVE/WORK AREA
         GETMAIN R,LV=(0)          STORAGE FOR SAVE AREA
         ST    R13,4(R1)           SYSTEM S/A INTO OUR S/A
         ST    R1,8(R13)           OUR S/A INTO SYSTEM S/A
         LR    R13,R1              OUR S/A ADDRESS IN R13
         L     R1,4(R13)           RELOAD SYSTEM S/A ADDRESS
         LM    R0,R1,20(R1)        RELOAD REGS 0 AND 1
************************************************************
*   INITIALIZE OUR IOSVSUCB WORK AREA AND GET              *
*   ADDRESSABILITY TO THE CVT.                             *
************************************************************
         LR    R10,R13             ADDRESS OF SAVE/WORK AREA
         USING WORKA,R10           SET ADDRESSABILITY TO IT
         TR    UCBWRK,TRTAB        SET TO BINARY ZEROES
         USING CVT,R3              DSECT IT
         L     R3,CVTPTR           GET CVT ADDRESS
************************************************************
*   BUILD OUR PARMLIST FOR THE IOSVSUCB ROUTINE.           *
************************************************************
         LA    R1,UCBWRK           OUR 100-BYTE WORK AREA
         ST    R1,UCBWRKA          SAVE IN PARMLIST
         LA    R1,UCBDEVT          OUR DEVICE CLASS
         ST    R1,UCBDEVTA         SAVE IN PARMLIST
         MVI   UCBDEVT,UCB3TAPE    INDICATE TAPE UCBS ONLY
         LA    R1,UCBADDR          OUR UCB ADDRESS POINTER
         ST    R1,UCBADDRA         SAVE IN PARMLIST
         OI    UCBADDR,X'80'       TURN ON HIGH-ORDER BIT
         MVC   UCBTPUT(NOPENDL),NOPEND  SET NO MOUNT PENDING MESSAGE
*                                  IF NONE ARE LATER FOUND
************************************************************
*   GO EXECUTE THE IOSVSUCB ROUTINE.                       *
************************************************************
TAPEUCB  LA    R1,UCBWRKA          ADDRESS OF PARAMETER LIST
         L     R15,CVTUCBSC        GET SCAN SERVICE ADDRESS
         BALR  R14,R15             EXECUTE IT
         LTR   R15,R15             ANY UCB RETURNED?
         BNZ   FINIS               IF NONE, THEN FINISH
************************************************************
*   WE HAVE GOT A TAPE UCB.  CHECK TO SEE IF TAPE UCB      *
*   IS STANDARD UCB AND NOT UCB EXTENSION.  ALSO CHECK FOR *
*   ONLINE AND A MOUNT PENDING STATUS.                     *
************************************************************
         L     R4,UCBADDR          GET RETURNED TAPE UCB ADDR
         USING UCB,R4              DSECT IT
         CLI   UCBID,X'FF'         A STANDARD UCB ?
         BNE   TAPEUCB                IF NOT, GET ANOTHER UCB
         TM    UCBSTAT,UCBONLI     IS TAPE UNIT ONLINE ?
         BNO   TAPEUCB                IF NOT, GET ANOTHER UCB
         TM    UCBDMCT,UCBMOUNT    IS TAPE MOUNT PENDING ?
         BNO   TAPEUCB                IF NOT, GET ANOTHER UCB
************************************************************
*   GET THE ASID OF THE ADDRESS SPACE USING THIS TAPE      *
*   UCB FROM THE UCB EXTENSIONS AREA.  CHECK THE ASCB      *
*   AND GET THE JOBNAME.                                   *
************************************************************
         L     R1,UCBEXTPT         GET UCB TAPE EXTENSION
         USING UCBCMEXT,R1         SET ADDRESSABILITY
         LH    R1,UCBASID          GET ASID FOR THIS TAPE UCB
         DROP  R1                     DONE WITH REGISTER NOW
         LOCASCB ASID=(R1)         GET ASCB FOR THIS TAPE UCB
         LTR   R1,R1               DO WE HAVE A VALID ASCB?
         BP    GOTASCB               IF SO, GET ITS JOBNAME
         B     TAPEUCB               IF NOT, GET NEXT UCB
GOTASCB  LR    R5,R1               SAVE ASCB ADDRESS
         USING ASCB,R5             SET ADDRESSABILITY
         ICM   R1,15,ASCBJBNI      POINTER TO BATCH JOBNAME
         BZ    NOBATCH              IF 0, MUST BE NON-BATCH
         B     BLDTPUT
NOBATCH  ICM   R1,15,ASCBJBNS      POINTER TO NON-BATCH JOBNAME
         BZ    ABEND                IF 0, MUST ABEND!!!!
ABEND    DC    X'0000'             ** ABEND 0C1 **
************************************************************
*   BEGIN CREATING TPUT MOUNT PENDING MESSAGE              *
************************************************************
BLDTPUT  MVC   UCBTPUT(CHARL),CHAR INITIALIZE WTO AREA
         LA    R15,UCBTPUT         ADDRESS OF WTO AREA
         USING MSGPEND,R15         SET ADDRESSABILITY
         MVC   MSGJOB,0(R1)        MOVE IN JOBNAME
         MVC   MSGCUU,UCBNAME      MOVE IN TAPE UCB
         CLI   UCBVOLI,X'FF'       IS IT A SCRATCH VOLSER
         BNE   SETVOL              MOVE IN THE VOLSER
         MVC   MSGVOL,SCRTCH        INDICATE A SCRATCH VOLSER
         B     TEST3480            CHECK FOR 3480 DEVICE
SETVOL   MVC   MSGVOL,UCBVOLI      MOVE IN TAPE VOLSER
TEST3480 TM    UCBTBYT4,UCB3480    IS IT 3480 DEVICE
         BNO   SET3420
         MVC   MSGTYP,DEV3480      SET TO 3480 DEVICE
         B     ISUTPUT             GO ISSUE TPUT
SET3420  MVC   MSGTYP,DEV3420      SET TO 3420 DEVICE
ISUTPUT  TPUT  UCBTPUT,CHARL       ISSUE TPUT MESSAGE
         B     TAPEUCB             LOOK FOR MORE TAPE UCBS
************************************************************
*   PERFORM REENTRANT EXIT LINKAGE.                        *
************************************************************
FINIS    CLC   UCBTPUT(L'NOMOUNTS),NOMOUNTS   WE FOUND NONE
         BNE   EXIT                  IF AT LEAST 1 FOUND - EXIT
         TPUT  UCBTPUT,NOPENDL     ISSUE NO MOUNTS PENDING MSG
EXIT     LA    R0,WORKL            SAVE AREA LENGTH
         LR    R1,R13              LOAD OUR S/A ADDRESS
         L     R13,4(R13)          RESTORE SYSTEM S/A ADDRESS
         FREEMAIN R,LV=(0),A=(1)   FREE S/A
         LM    R0,R12,20(R13)      RESTORE ALL EXCEPT 14,15
         L     R14,12(R13)         RESTORE RETURN ADDRESS
         SLR   R15,R15             SET GOOD RETURN CODE
         BR    R14                 RETURN TO CALLER
************************************************************
*        C O N S T A N T S                                 *
************************************************************
DEV3420  DC    C'(3420)'
DEV3480  DC    C'(3480)'
SCRTCH   DC    C'SCRTCH'
NOPEND   DS    0C
NOMOUNTS DC    CL17'NO MOUNTS PENDING'
NOPENDL  EQU   *-NOPEND
*
CHAR     DS    0C
         DC    CL17'MOUNT PENDING FOR'
         DC    CL1' '
VOL      DC    CL6'VVVVVV'
         DC    CL1' '
         DC    CL5'UNIT='
CUU      DC    CL3'CUU'
         DC    CL1' '
         DC    CL6'(34XX)'
         DC    CL1' '
         DC    CL8'JOBNAME='
         DC    CL8'JJJJJJJJ'
CHARL    EQU   *-CHAR
*
TRTAB    DC    256X'00'      TRANSLATE TABLE
************************************************************
*        D S E C T S                                       *
************************************************************
WORKA    DSECT
SAVEAREA DS    18F           MY SAVE AREA
UCBWRKA  DS    F             ADDRESS OF UCBWRK 100-BYTE WORK AREA
UCBDEVTA DS    F             ADDRESS OF DEVICE CLASS BYTE
UCBADDRA DS    F             ADDRESS OF WORD TO CONTAIN UCB ADDRESS
*
UCBADDR  DS    F             ADDRESS OF WORD TO CONTAIN UCB ADDRESS
         DS    0D            FORCE DOUBLEWORD ALIGNMENT
UCBWRK   DS    XL100         IOSVSUCB 100-BYTE WORK AREA
UCBDEVT  DS    XL1           DEVICE CLASS BYTE - TAPE
UCBTPUT  DS    XL50          TPUT AREA
WORKL    EQU   *-WORKA
MSGPEND  DSECT
         DS    CL17          MOUNT PENDING FOR
         DS    CL1
MSGVOL   DS    CL6           VVVVVV
         DS    CL1
         DS    CL5           UNIT=
MSGCUU   DS    CL3           CUU
         DS    CL1
MSGTYP   DS    CL6           (34XX)
         DS    CL1
         DS    CL8           JOBNAME=
MSGJOB   DS    CL8           JJJJJJJJ
         CVT DSECT=YES,LIST=NO
UCB      DSECT
         IEFUCBOB LIST=YES,PREFIX=NO
         IHAASCB
         REGISTER
         END   MTP
