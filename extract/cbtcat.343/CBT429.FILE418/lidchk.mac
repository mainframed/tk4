LIDCHK   CSECT
**********************************************************************
* NAME       -   LIDCHK                                              *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTIONS  -   THIS PROGRAM WILL READ THE USER'S IN-STORAGE ACF2   *
*      LOGONID AND COMPARE IN TO AN INPUT PARM CONTAINING A LOGONID  *
*      OR LOGONID MASK.  IN THE INPUT PARAMETER, AN ASTERISK (*) IS  *
*      A MATCH FOR ONE LOGONID POSITION.  A DASH (-) WILL STOP       *
*      FURTHER PARAMETER CHECKING AND CONSIDER IT A MATCH FOR        *
*      ALL REMAINING POSITIONS OF THE IN-STORAGE LOGONID.            *
*                                                                    *
* INPUT      -   PARAMETER GREATER THAN 2 CHARACTERS BUT NOT GREATER *
*                THAN 6 CHARACTERS                                   *
*                                                                    *
* OUTPUT     -   CONDITION CODES PLACED IN REGISTER 15               *
*                                                                    *
*                RETURN CODE             DISPOSITION                 *
*                                                                    *
*                4                       BAD INPUT PARAMETER         *
*                8                       LOGONID MASK DOES NOT MATCH *
*                                                                    *
* SYSTEM     -   ALL MVS SYSTEMS                                     *
*                                                                    *
* DATE       -   06/04/85                                            *
*                                                                    *
* MACROS     -   ACINCVT,ACFGUCB,ACCVT,ACFASVT,LIDREC                *
*                GETMAIN,CVT,IHAPSA,IHAASCB,REGISTER                 *
*                                                                    *
* REGISTERS  -   R0   -   *** UNUSED BY MVS ***                      *
*                R1   -   *** UNUSED BY MVS ***                      *
*                R2   -   *** UNUSED BY MVS ***                      *
*                R3   -   *** UNUSED BY PROGRAM ***                  *
*                R4   -   ADDRESS OF ACF2 CVT AND WORK REGISTER      *
*                R5   -   ADDRESS OF ACF2 UCB AND WORK REGISTER      *
*                R6   -   ADDRESS OF LOGONID RECORD                  *
*                R7   -   WORK REGISTER                              *
*                R8   -   WORK REGISTER                              *
*                R9   -   WORK REGISTER                              *
*               R10   -   *** UNUSED BY PROGRAM ***                  *
*               R11   -   *** UNUSED BY PROGRAM ***                  *
*               R12   -   BASE REGISER                               *
*               R13   -   SYSTEM SAVE AREA ADDRESS                   *
*               R14   -   RETURN ADDRESS                             *
*               R15   -   RETURN CODE                                *
**********************************************************************
         STM   R14,R12,12(R13)      SAVE REGISTERS
         LR    R12,R15              SET BASE REGISTER
         USING LIDCHK,R12           SET ADDRESSABILITY
         GETMAIN R,LV=72            GETMAIN OUR SAVE AREA
         ST    R13,4(R1)            SYSTEM S/A INTO OUR S/A
         ST    R1,8(R13)            OUR S/A INTO SYSTEM S/A
         LR    R13,R1               OUR S/A ADDRESS IN R13
         L     R1,4(R13)            RELOAD SYSTEM S/A ADDRESS
         L     R1,24(R1)            RELOAD REGISTER 1 PARAMETER
         SPACE 1
         L     R3,0(R1)             SAVE PARAMETER REGISTER
         ACFINCVT R4,NONE=MATCHLID  IF NO ACF2 LEAVE EXIT NOW
         USING ACCVT,R4             SET ADDRESSABILITY TO IT
         ACFGUCB (5),NONE=MATCHLID,INLINE=YES,SYS=AOS2
         USING ACUCB,R5             SET ADDRESSABILITY TO IT
         L     R6,ACULRECP          ADDRESS OF LOGONID RECORD
         USING LIDREC,R6            SET ADDRESSABILITY TO IT
         DROP  R4,R5                DONE WITH DSECTS NOW
         LH    R7,0(R3)             GET LENGTH OF PARAMETER
         CH    R7,H6                IS PARAMETER GREATER THAN 6 DIGITS
         BNH   NEXTCHK              IF SO, FAIL THE TEST
         LA    R4,4                 INDICATE BAD INPUT PARAMETER
         B     NOMATCH                 GO FAIL REQUEST
NEXTCHK  CH    R7,H3                MUST BE AT LEAST 3 DIGITS LONG
         BNL   ANYDASH              IF SO CHECK FOR DASH (-)
         LA    R4,4                 INDICATE BAD INPUT PARAMETER
         B     NOMATCH                 GO FAIL REQUEST
ANYDASH  LR    R4,R7                SAVE INPUT PARAMETER LENGTH
         BCTR  R4,0                 SUBTRACT 1 FOR EXECUTE
         EX    R4,EXINS             HAVE WE FOUND A DASH (-)
         BNZ   CHKLNTH              NO, CHECK FOR FULL INPUT PARM
GOTDASH  LA    R1,2(R3)             ADDRESS OF ACTUAL INPUT PARM
         LA    R1,0(R4,R1)          LOOK AT LAST BYTE
         CLC   0(1,R1),DASH         IS IT A DASH (-)
         BE    TESTLID              YES, BEGIN TESTING
CHKLNTH  CH    R7,H6                INPUT PARM MUST BE 6 DIGITS NOW!
         BE    TESTLID              YES, FINALLY TEST THIS LOGONID!
         LA    R4,4                 INDICATE BAD INPUT PARAMETER
         B     NOMATCH                 GO FAIL REQUEST
         SPACE 2
TESTLID  LA    R8,LIDLID            ADDRESS OF IN-STORAGE LOGONID
         LA    R9,2(R3)             ADDRESS OF ACTUAL INPUT PARM
         OC    0(6,R9),UPERCASE     FLIP PARAMETER TO UPPER CASE
CLCLID   CLC   0(1,R9),DASH         IS THIS POSITION MASKED?
         BE    MATCHLID             IF SO, PASS GOOD RETURN CODE NOW!
         CLC   0(1,R9),ASTERISK     IS THIS POSITION MASKED?
         BE    INCRMENT             IF SO, CHECK NEXT POSITION
         CLC   0(0,R8),0(R9)        DO THESE BYTES MATCH?
         BE    INCRMENT
         LA    R4,8                 INDICATE NO MATCH
         B     NOMATCH
         SPACE 1
INCRMENT AH    R8,H1                NEXT PARAMETER POSITION
         AH    R9,H1                NEXT LOGONID POSITION
         BCT   R7,CLCLID            GO COMPARE NEXT POSITION
***********************************************************************
*        IF WE FALL THROUGH THIS LOGIC OUR COMPARISONS ARE FINISHED   *
*        AND A GOOD RETURN CODE IS ISSUED.                            *
***********************************************************************
MATCHLID LR    R1,R13               LOAD OUR S/A ADDRESS
         L     R13,4(R13)           RESTORE SYSTEM S/A ADDRESS
         FREEMAIN R,LV=72,A=(1)     FREE S/A
         LM    R0,R12,20(R13)       RESTORE ALL EXCEPT 14,15
         L     R14,12(R13)          RESTORE RETURN ADDRESS
         SLR   R15,R15              SET GOOD RETURN CODE
         BR    R14                  RETURN TO CALLER
NOMATCH  LR    R1,R13               LOAD OUR S/A ADDRESS
         L     R13,4(R13)           RESTORE SYSTEM S/A ADDRESS
         FREEMAIN R,LV=72,A=(1)     FREE S/A
         LR    R15,R4               SET BAD RETURN CODE
         LM    R0,R12,20(R13)       RESTORE ALL EXCEPT 14,15
         L     R14,12(R13)          RESTORE RETURN ADDRESS
         BR    R14                  RETURN TO CALLER
         LTORG
DASH     DC    C'-'                 WILD CARD MATCH FOR REMAINDER
ASTERISK DC    C'*'                 WILD CARD MATCH FOR 1 DIGIT
H1       DC    H'1'                 INCREMENT VALUE
H3       DC    H'3'
H6       DC    H'6'
UPERCASE DC    XL6'404040404040'
EXINS    TRT   0(0,R4),TRTAB
TRTAB    DC    256X'00'         TRANSLATE TABLE
         ORG   TRTAB+C'-'
         DC    X'FF'
         ORG
***********************************************************************
*        DSECTS USED IN THIS PROGRAM                                  *
***********************************************************************
         TITLE 'LIDCHK: TEST FOR LOGONID MASK - CVT'
         CVT    DSECT=YES
         TITLE 'LIDCHK: TEST FOR LOGONID MASK - IHAPSA'
         IHAPSA DSECT=YES
         TITLE 'LIDCHK: TEST FOR LOGONID MASK - IHAASCB'
         IHAASCB DSECT=YES
         TITLE 'LIDCHK: TEST FOR LOGONID MASK - ACCVT'
         ACCVT  DSECT=YES
         TITLE 'LIDCHK: TEST FOR LOGONID MASK - ACUCB'
         ACUCB  DSECT=YES
         TITLE 'LIDCHK: TEST FOR LOGONID MASK - ACFASVT'
         ACFASVT DSECT=YES
         TITLE 'LIDCHK: TEST FOR LOGONID MASK - LIDREC'
         LIDREC DSECT=YES
         REGISTER
         END    LIDCHK
