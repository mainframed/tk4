OSTAREDC TITLE 'A PROGRAM TO ADD/ CHECK ERROR DETECTION BYTES'
*
*    This software is supplied by Amdahl Corp. for the transmission
* of materials between ObjectStar support and it's customers. Any
* other use of this software is at the user's discretion and is not
* supported in any way by Amdahl Corp.
*
*   The software is supplied as 'freeware' and may be used/modified
* by anyone provided they do not then sell it on for profit or
* expect support from Amdahl Corp.
*
*   Limited support MAY in some cases be available from the author.
*
R15      EQU   15
R14      EQU   14
RSAV     EQU   13
RBASE    EQU   12
RLINK    EQU   11
RWORK    EQU   10
RCOUNT   EQU   9
R8       EQU   8
R7       EQU   7
R6       EQU   6
R5       EQU   5
R4       EQU   4
R3       EQU   3
R2       EQU   2
R1       EQU   1
R0       EQU   0
         TITLE 'INITIALISE PROGRAM'
OSTAREDC CSECT
OSTAREDC AMODE 24
OSTAREDC RMODE 24
         PRINT DATA
         USING OSTAREDC,R15
         B     OSTARED1
         DC    C'OSTAREDC V1.01 &SYSDATE'
*        V2.01 - ADD DISCLAIMER.
OSTARED1 DS    0H
         PRINT NODATA
         PRINT NOGEN
         DROP  R15
         STM   14,12,12(RSAV)
         LR    RBASE,R15
         USING OSTAREDC,RBASE
         L     R15,SAVEADDR
         ST    R15,8(RSAV)
         ST    RSAV,4(R15)
         LR    RSAV,R15
         USING SAVEAREA,RSAV
         L     RWORK,0(R1)               SAVE PARM POINTER
         OPEN  (SYSOUT,OUTPUT)
         LA    R1,SYSOUT
         USING IHADCB,R1
         TM    DCBOFLGS,DCBOFOPN
         DROP  R1
         BNO   ERROR1
         CLC   PARMLEN,0(RWORK)          CORECT PARM LENGTH?
         BNE   ERROR2
         MVC   PARMS,2(RWORK)            SAVE PARMS
         PACK  DWORD,PARMDSNS
         CVB   R1,DWORD
         STH   R1,PARMDSNS
*        MVC   PARMDSNS,=H'1'            DEBUG
         CLI   PARMMODE,C'A'
         BE    ADD
         CLI   PARMMODE,C'U'
         BE    CHECK
         B     ERROR3
         TITLE 'ADD EDC MAINLINE'
ADD      DS    0H
         OPEN  (FB88FILE,OUTPUT)
         LA    R1,FB88FILE
         BAL   RLINK,OPENCHK
         LA    RCOUNT,1                 ALLOW FOR HEADER RECORD
         RDJFCB FB88FILE
         BAL   RLINK,JFCBCHK
         MVC   SAVELEN,=H'80'            SET LRECL FOR OTHER DS
ADDDSLP  DS    0H                        DATASET LOOP
         LH    R1,DSNS
         LA    R1,1(R1)                  ADD 1 TO DATASETS PROCESSED
         STH   R1,DSNS
         CH    R1,PARMDSNS               ARE WE FINISHED?
         BH    ADDEND                    YES - BRANCH
         CVD   R1,DWORD
         UNPK  DWORD(5),DWORD+6(2)
         OI    DWORD+4,C'0'              GET DATASET NUMBER
         LA    R1,FB80FILE
         USING IHADCB,R1
         MVC   DCBDDNAM+5(3),DWORD+2     MOVE TO DDNAME OSTARNNN
         MVC   SAVEDDN,DCBDDNAM          MOVE TO DDNAME OSTARNNN
         OPEN  (FB80FILE,INPUT)          OPEN INPUT FILE
         LA    R1,FB80FILE
         BAL   RLINK,OPENCHK
         RDJFCB FB80FILE
         BAL   RLINK,JFCBCHK
         MVC   FILTOKEN,PARMTOK          INITIALISE FILE EDC
ADDRDLP  DS    0H
         GET   FB80FILE,RECFB80          READ NEXT RECORD
         XC    FILTOKEN,RECFB80          MODIFY FILE EDC
         XC    FILTOKEN,RECFB80+40       MODIFY FILE EDC
         MVC   RECTOKEN,RECFB80          INITIALISE RECORD EDC
         LA    R1,RECFB80+8
         LA    R0,9
ADDEDCLP DS    0H
         XC    RECTOKEN,0(R1)
         LA    R1,8(R1)
         BCT   R0,ADDEDCLP
         PUT   FB88FILE,RECFB88
         LA    RCOUNT,1(RCOUNT)
         B     ADDRDLP
         SPACE
ADDRDLPE DS    0H                        END OF FILE ON READ
         MVC   RECFB88(40),PARMTOK
         MVC   RECFB88+40(40),FILTOKEN
         XC    RECTOKEN,RECTOKEN
         PUT   FB88FILE,RECFB88
         LA    RCOUNT,1(RCOUNT)
         CLOSE (FB80FILE)
         B     ADDDSLP
         SPACE
         TITLE 'CHECK EDC MAINLINE'
CHECK    DS    0H
         OPEN  (FB88FILE,INPUT)
         LA    R1,FB88FILE
         BAL   RLINK,OPENCHK
         RDJFCB FB88FILE
         BAL   RLINK,JFCBCHK
         MVC   SAVELEN,=H'80'            SET LRECL FOR OTHER DS
         GET   FB88FILE,RECFB88          IGNORE HEADER
         LA    RCOUNT,1                  # OF RECORS READ
CHKDSLP  DS    0H                        DATASET LOOP
         LH    R1,DSNS
         LA    R1,1(R1)                  ADD 1 TO DATASETS PROCESSED
         STH   R1,DSNS
         CH    R1,PARMDSNS               ARE WE FINISHED?
         BH    CHKEND                    YES - BRANCH
         CVD   R1,DWORD
         UNPK  DWORD(5),DWORD+6(2)
         OI    DWORD+4,C'0'              GET DATASET NUMBER
         LA    R1,FB80FILE
         USING IHADCB,R1
         MVC   DCBDDNAM+5(3),DWORD+2     MOVE TO DDNAME OSTARNNN
         MVC   SAVEDDN,DCBDDNAM          MOVE TO DDNAME OSTARNNN
         DROP  R1
         OPEN  (FB80FILE,OUTPUT)         OPEN INPUT FILE
         LA    R1,FB80FILE
         BAL   RLINK,OPENCHK
         RDJFCB FB80FILE
         BAL   RLINK,JFCBCHK
         MVC   FILTOKEN,PARMTOK          INITIALISE FILE EDC
CHKRDLP  DS    0H
         GET   FB88FILE,RECFB88          READ NEXT RECORD
         LA    RCOUNT,1(RCOUNT)
         CLC   PARMTOK,RECFB88           END OF INPUT FILE
         BE    CHKRDLPE                  YES BRANCH
         XC    FILTOKEN,RECFB80          MODIFY FILE EDC
         XC    FILTOKEN,RECFB80+40       MODIFY FILE EDC
         MVC   DWORD,RECFB80             INITIALISE RECORD EDC
         LA    R2,RECFB80+8
         LA    R3,9
CHKEDCLP DS    0H
         XC    DWORD,0(R2)
         LA    R2,8(R2)
         BCT   R3,CHKEDCLP
         CLC   DWORD,RECTOKEN
         BNE   CHKEDCER
CHKPUT   DS    0H
         PUT   FB80FILE,RECFB80
         B     CHKRDLP
CHKEDCER DS    0H                        CHECKSUM ERROR
         L     R1,ERRORS
         LA    R1,1(R1)
         ST    R1,ERRORS
         MVC   PLINE(32),=C'CORRUPT DATA DETECTED IN RECORD '
         CVD   RCOUNT,DWORD
         UNPK  PLINE+32(15),DWORD
         OI    PLINE+46,C'0'
         BAL   RLINK,PRINT
         B     CHKPUT
         SPACE
CHKRDLPE DS    0H                        END OF FILE ON READ
         CLC   FILTOKEN,RECFB80+40
         BE    CHRDCLOS
         L     R1,ERRORS
         LA    R1,1(R1)
         ST    R1,ERRORS
         MVC   PLINE(32),=C'CORRUPT FILE DETECTED AT RECORD '
         CVD   RCOUNT,DWORD
         UNPK  PLINE+32(15),DWORD
         OI    PLINE+46,C'0'
         BAL   RLINK,PRINT
CHRDCLOS DS    0H
         CLOSE (FB80FILE)
         B     CHKDSLP
         TITLE 'END OF INPUT IN BOTH MODES'
ADDEND   DS    0H                        END OF JOB
CHKEND   DS    0H                        END OF JOB
         CLOSE (FB88FILE)
         MVC   PLINE(21),=C'Records in XMT file :'
         MVC   PLINE+21(L'EDMASK),EDMASK
         CVD   RCOUNT,DWORD
         ED    PLINE+21(L'EDMASK),DWORD
         BAL   RLINK,PRINT
         MH    RCOUNT,=H'88'
         MVC   PLINE(21),=C'Bytes in XMT file   :'
         MVC   PLINE+21(L'EDMASK),EDMASK
         CVD   RCOUNT,DWORD
         ED    PLINE+21(L'EDMASK),DWORD
         BAL   RLINK,PRINT
         SR    R0,R0
         LR    R1,RCOUNT
         D     R0,=F'1024'
         MVC   PLINE(21),=C'Kbytes in XMT file  :'
         MVC   PLINE+21(L'EDMASK),EDMASK
         CVD   R1,DWORD
         ED    PLINE+21(L'EDMASK),DWORD
         BAL   RLINK,PRINT
         CLOSE (SYSOUT)
         L     R15,ERRORS
         LTR   R15,R15
         BZ    RETURN
         LA    R15,16
RETURN   DS    0H
         L     RSAV,4(RSAV)
         L     14,12(RSAV)
         LM    0,12,20(RSAV)
         BR    R14
         TITLE 'PRINT ERROR MESSAGE, CLOSE ANY OPEN FILES, EXIT RC=16'
ERROR    DS    0H
         BAL   RLINK,PRINT
         LA    R1,FB80FILE
         USING IHADCB,R1
         TM    DCBOFLGS,DCBOFOPN
         DROP  R1
         BNO   ERRCONT1
         CLOSE (FB80FILE)
ERRCONT1 DS    0H
         LA    R1,FB88FILE
         USING IHADCB,R1
         TM    DCBOFLGS,DCBOFOPN
         DROP  R1
         BNO   ERRCONT2
         CLOSE (FB80FILE)
ERRCONT2 DS    0H
         CLOSE (SYSOUT)
         LA    R15,16
         B     RETURN
         TITLE 'SUBROUTINES'
OPENCHK  DS    0H
         USING IHADCB,R1                R1 POINTS TO DCB
         TM    DCBOFLGS,DCBOFOPN
         BNO   ERROROPN
         BR    RLINK
         DROP  R1
JFCBCHK  DS    0H
         LTR   R15,R15
         BNZ   ERRORJFC
         LA    R1,JFCBDATA
         USING JFCBLK,R1
         TM    JFCRECFM,X'80'
         BNO   ERRORRFM
         CLC   SAVELEN,JFCLRECL
         BNE   ERRORRLN
         BR    RLINK
         DROP  R1
PRINT    DS    0H
         PUT   SYSOUT,PLINE-1
         MVC   PLINE,PLINE-1
         BR    RLINK
         TITLE 'ERROR ROUTINES'
ERROR1   DS    0H
         WTO   'ERROR: UNABLE TO OPEN SYSOUT DD.',ROUTCDE=11
         ABEND 0001
         B     ERROR
ERROR2   DS    0H
         MVC   PLINE(L'MSG2),MSG2
         B     ERROR
ERROR3   DS    0H
         MVC   PLINE(L'MSG3),MSG3
         B     ERROR
ERROROPN DS    0H
         MVC   PLINE(L'MSG5),MSG5
         MVC   PLINE+3(8),SAVEDDN
         B     ERROR
ERRORRFM DS    0H
         MVC   PLINE(L'MSG6),MSG6
         MVC   PLINE+3(8),SAVEDDN
         B     ERROR
ERRORRLN DS    0H
         MVC   PLINE(L'MSG7),MSG7
         MVC   PLINE+3(8),SAVEDDN
         B     ERROR
FB88EOF  DS    0H  END OF FILE ENCOUNTERED ON FB88 FILE
         MVC   PLINE(L'MSG8),MSG8
         B     ERROR
ERRORJFC DS    0H
         MVC   PLINE(L'MSG9),MSG9
         MVC   PLINE+3(8),SAVEDDN
         B     ERROR
MSG2     DC    C'ERROR2: INVALID INPUT PARM LENGTH'
MSG3     DC    C'ERROR3: MODE IS NOT ADD OR CHECK.'
MSG5     DC    C'DD XXXXXXXX - OPEN ERROR.'
MSG6     DC    C'DD XXXXXXXX - INVALID RECFM.'
MSG7     DC    C'DD XXXXXXXX - INVALID LRECL.'
MSG8     DC    C'ERROR8: PREMATURE EOF ON INPUT FILE.'
MSG9     DC    C'DD XXXXXXXX - UNABLE TO READ JFCB.'
EDMASK   DC    X'402020206B2020206B2020206B2020206B20202040'
         TITLE ' WORKING STORAGE'
         LTORG
SAVEADDR DC    A(WORKSTOR)
         DROP  RBASE
WORKSTOR CSECT
SAVEAREA DS    18F
ERRORS   DS    F'0'
DWORD    DS    D
SAVEDDN  DC    CL8'OSTARXMT'
SAVELEN  DC    H'88'
DSNS     DC    H'0'
         DS    0H
PARMS    DS    0XL45
PARMDSNS DS    CL3
PARMMODE DS    CL2
PARMTOK  DS    CL40
PARMLEN  DC    H'45'
FILTOKEN DS    XL40
RECFB88  DS    0XL88
RECFB80  DS    XL80
RECTOKEN DS    XL8
         PRINT NOGEN
SYSOUT   DCB   DDNAME=SYSOUT,MACRF=(PM),DSORG=PS
FB80FILE DCB   DDNAME=OSTARXXX,MACRF=(GM,PM),DSORG=PS,EODAD=ADDRDLPE,  X
               LRECL=80,RECFM=FB,EXLST=JFCB1
FB88FILE DCB   DDNAME=OSTARXMT,MACRF=(GM,PM),DSORG=PS,                 X
               LRECL=88,RECFM=FB,EXLST=JFCB1,EODAD=FB88EOF
         DC    CL1' '  BYTE USED TO CLEAR MSG
PLINE    DC    CL80' '
         DC    CL52' '
         DS    0F
JFCB1    DC    X'07',AL3(JFCBDATA)
         DC    X'80',AL3(0)
JFCBDATA DS    XL512
         TITLE 'DSECTS'
         DCBD  DSORG=PS
JFCBLK   DSECT
         IEFJFCBN
         TITLE 'END OF PROGRAM'
         END
