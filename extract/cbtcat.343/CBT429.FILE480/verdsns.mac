VERDSN  CKINICIO
*
         OI    DLIST,X'80'
         LA    R1,PARVDEF
         LINK  EP=ISPLINK
*
         MVC   POR,=P'0'
*
DI_PANEL EQU   *
*
         USING UCBOB,R6
         MVC   TOTUCB,CEROP
CONTUCB  EQU   *
         UCBSCAN COPY,UCBAREA=UCBA,WORKAREA=WORKAREA,                  X
               DEVCLASS=DASD,DYNAMIC=YES
         LTR   R15,R15
         BNZ   SEGUIR
         LA    R6,UCBA
         TM    UCBSTAT,UCBONLI
         BC    8,CONTUCB
         AP    TOTUCB,UNOP
         B     CONTUCB
*
SEGUIR   EQU   *
         UNPK  TVO(4),TOTUCB+0(3)
         OI    TVO+3,X'F0'
*
         OI    DPANEL,X'80'
         LA    R1,PARDISP
         LINK  EP=ISPLINK
         LTR   R15,R15
         BNZ   FINAL
*
         LA    R8,INF
         MVI   FLAG1,X'00'
         MVC   LI1,LISPACE
         MVC   LI2,LISPACE
         MVC   LI3,LISPACE
         MVC   INF,INFSPAC
         MVC   DSNAME,DSN
         MVC   NUMUCB,CEROP
*
*     PROCESO PARA LA LISTA DE UCB'S
*
PROCUCB  EQU   *
         UCBSCAN COPY,UCBAREA=UCBA,WORKAREA=WORKAREA,                  X
               DEVCLASS=DASD,DYNAMIC=YES
         LTR   R15,R15
         BNZ   FINUCB
         LA    R6,UCBA
         TM    UCBSTAT,UCBONLI
         BC    8,PROCUCB
*
         AP    NUMUCB,UNOP
         UNPK  NVO(4),NUMUCB+0(3)
         OI    NVO+3,X'F0'
         MVC   TRANUM,CERO2P
         MVC   TRANUM+4(3),NUMUCB
         MP    TRANUM,CIENP
         DP    TRANUM,TOTUCB
         MVC   POR,TRANUM+1
*
         MVC   LVO,UCBVOLI
         MVC   LDI,UCBNAME
*
         OI    DLOCK,X'80'
         LA    R1,PARCONT
         LINK  EP=ISPLINK
*
         OI    DPANEL,X'80'
         LA    R1,PARDISP
         LINK  EP=ISPLINK
*
         MVC   VOLUME,UCBVOLI
         OBTAIN DSCBDSN
         LTR   R15,R15
         BNZ   PROCUCB
*
         MVI   FLAG1,X'01'
         MVC   LI1,LI11
         MVC   0(6,R8),UCBVOLI
         LA    R8,7(R8)
         B     PROCUCB
*
FINUCB   EQU   *
         CLI   FLAG1,X'01'
         BE    SIHAY
         MVC   LI2,LI21
SIHAY    EQU   *
         MVC   LI3,LI31
         B     DI_PANEL
*
FINAL    CKFINAL RET=0
*
CAMPOCVD DS    D
*
FLAG1    DC    X'00'
TRANUM   DC    P'000000000000'
UNOP     DC    P'1'
CIENP    DC    P'100'
TOTUCB   DC    P'0000'
NUMUCB   DC    P'0000'
CEROP    DC    P'0000'
CERO2P   DC    P'000000000000'
*
PARCONT  DS    0F
         DC    A(CONTROL)
         DC    A(DISPLAY)
DLOCK    DC    A(LOCK)
*
PARDISP  DS    0F
         DC    A(DISPLAY)
DPANEL   DC    A(PANEL00)
*
PARVDEF  DS    0F
         DC    A(VDEFINE)
         DC    A(VARIABL1)
         DC    A(VARIABL2)
         DC    A(VARIABL3)
         DC    A(VARIABL4)
DLIST    DC    A(LIST)
*
DISPLAY  DC    C'DISPLAY '
PANEL00  DC    C'VERDSNP '
CONTROL  DC    C'CONTROL '
LOCK     DC    C'LOCK    '
*
VDEFINE  DC    C'VDEFINE '
VARIABL1 DC    C'(TVO POR LDI LVO NVO DSN LI1 INF LI2 LI3)'
VARIABL2 EQU   *
TVO      DC    C'    '
POR      DC    C'   '
LDI      DC    C'   '
LVO      DC    C'      '
NVO      DC    C'    '
DSN      DC    C'                                            '
LI1      DC    C'                         '
INF      DC    C'                                                  '
LI2      DC    C'                         '
LI3      DC    C'                         '
VARIABL3 EQU   *
         DC    C'CHAR    '
         DC    C'PACK    '
         DC    C'CHAR    '
         DC    C'CHAR    '
         DC    C'CHAR    '
         DC    C'CHAR    '
         DC    C'CHAR    '
         DC    C'CHAR    '
         DC    C'CHAR    '
         DC    C'CHAR    '
VARIABL4 DS    0F
         DC    F'4'
         DC    F'3'
         DC    F'3'
         DC    F'6'
         DC    F'4'
         DC    F'44'
         DC    F'25'
         DC    F'50'
         DC    F'25'
         DC    F'25'
LIST     DC    C'LIST    '
*
DSCBDSN  CAMLST SEARCH,DSNAME,VOLUME,NULLAREA
DSNAME   DC    C'                                            '
VOLUME   DS    CL6
NULLAREA DS    CL140
*
UCBA     DS    CL48
WORKAREA DS    CL100
*
LI11     DC    C'Esta en el/los volumen/es'
LI21     DC    C'No esta en ningun volumen'
LI31     DC    C'Fin de la Busqueda       '
LISPACE  DC    C'                         '
INFSPAC  DC    C'                                                  '
*
UCB      DSECT
         IEFUCBOB LIST=YES
*
         END   VERDSN
