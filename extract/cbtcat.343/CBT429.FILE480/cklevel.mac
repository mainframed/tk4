CKLEVEL  CKINICIO
         LR    R8,R1
         L     R3,0(R8)
         SR    R7,R7
         LH    R7,0(R3)
         S     R7,F1
         LA    R3,2(R3)
         LA    R10,ENTRA
         LA    R4,ENTRA
         LA    R9,DSNAME
         LA    R10,21(R10)
         LA    R1,0
         LINK  EP=IDCAMS
         LTR   R15,R15
         BNZ   DUMP01
         WTO   'ANTES DE OPEN SYSPRINT'
         OPEN  (SYSPRIN,INPUT)
         WTO   'DESPUES DE OPEN SYSPRINT'
         OPEN  (LISTADO,OUTPUT)
LEERI    MVC   ENTRA,BLANCOS
         GET   SYSPRIN,ENTRA
         CLC   ENTRA+5(7),NONVSAM
         BNE   LEERI
         STC   R7,COMPA+1
COMPA    CLC   0(44,R10),0(R3)
         BNE   LEERI
         MVC   W1+2(2),ENTRA+0
         L     R5,W1
         SH    R5,H21
         STC   R5,W1
         MVC   LONDSN+1(1),W1
         MVC   DSNAME,BLANCOS
         STC   R5,MOVER+1
MOVER    MVC   0(44,R9),ENTRA+21
         LA    R1,ALOC
         DYNALLOC
         LTR   R15,R15
         BNZ   DUMP02
         OPEN  (DD1,INPUT)
         MVC   ENTRA,BLANCOS
         MVC   0(44,R4),0(R9)
         PUT   LISTADO,ENTRA
LEERD    MVC   ENTRA,BLANCOS
         GET   DD1,ENTRA
         PUT   LISTADO,ENTRA
         B     LEERD
FINENTRA CLOSE DD1
         LA    R1,ULOC
         DYNALLOC
         LTR   R15,R15
         BNZ   DUMP03
         B     LEERI
CLOPRIN  CLOSE SYSPRIN
         CLOSE LISTADO
         CKFINAL RET=0
         DS    0F
W1       DC    D'0'
F1       DC    F'1'
H21      DC    H'21'
NONVSAM  DC    CL7'NONVSAM'
BLANCOS  DS    0CL140
         DC    140C' '
         DS    0F
*
*  CONTROL SVC 99 FOR TO ALLOC
*
ALOC     DC    0F'0',X'80',AL3(ALOCRB)
ALOCRB   DS    0F
         DC    AL1(20),AL1(01),AL2(0)
ALOCER   DC    AL2(0)
ALOCIN   DC    AL2(0)
         DC    A(ALOCTP)
         DC    A(0)
         DC    A(0)
ALOCTP   CALL  ,(ALOCDDN,ALOCDSP,ALOCDSN),VL,MF=L
ALOCDDN  DC    X'0001'
         DC    X'0001'
         DC    X'0003'
DDNAME   DC    CL3'DD1'
ALOCDSP  DC    X'0004'
         DC    X'0001'
         DC    X'0001'
DISP     DC    X'08'
ALOCDSN  DC    X'0002'
         DC    X'0001'
LONDSN   DC    X'0000'
DSNAME   DC    CL44' '
*
*  CONTROL SVC 99 FOR TO UNALLOC
*
ULOC     DC    0F'0',X'80',AL3(ULOCRB)
ULOCRB   DS    0F
         DC    AL1(20),AL1(02),AL2(0)
ULOCER   DC    AL2(0)
ULOCIN   DC    AL2(0)
         DC    A(ULOCTP)
         DC    A(0)
         DC    A(0)
ULOCTP   CALL  ,(ULOCDDN),VL,MF=L
ULOCDDN  DC    X'0001'
         DC    X'0001'
         DC    X'0003'
UDDNAME  DC    CL3'DD1'
DUMP01   ABEND 0001,DUMP        * FALLO EN EL IDCAMS
DUMP02   ABEND 0002,DUMP        * FALLO AL ASIGNAR
DUMP03   ABEND 0003,DUMP        * FALLO AL DESASIGNAR
DD1      DCB   DDNAME=DD1,DSORG=PS,MACRF=GM,EODAD=FINENTRA
LISTADO  DCB   DDNAME=LISTADO,DSORG=PS,MACRF=PM
SYSPRIN  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=GM,EODAD=CLOPRIN
ENTRA    DC    CL140' '
         END   CKLEVEL
