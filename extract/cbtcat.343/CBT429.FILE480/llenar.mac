ISREDIT MACRO (NUMI NUME INCR)
CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST NOFLUSH NOPROMPT
ISREDIT (LINE,COLU) = CURSOR
IF &COLU = 0 THEN SET &COLU = 9
ISREDIT (NLINZ) = LINENUM .ZLAST
SET &TOLIN = &NUME + &LINE - 1
IF &TOLIN > &NLINZ THEN +
 DO
   SET ZEDLMSG = &STR(**ERROR** NO HAY SUFICIENTES LINEAS ***)
   ISPEXEC SETMSG MSG(ISRZ001)
   EXIT CODE(0)
 END
IF (&LENGTH(&NUMI) = 0) OR +
   (&LENGTH(&NUME) = 0) OR +
   (&LENGTH(&INCR) = 0) THEN +
 DO
   SET ZEDLMSG = &STR(**ERROR** FORMATO *LLENAR NI NE IN* NI=NUMERO IN+
                      ICIAL NE=NUMERO DE LINEAS IN=INCREMENTO)
   ISPEXEC SETMSG MSG(ISRZ001)
   EXIT CODE(0)
 END
 SET &CEROS = &STR(0000000000)
 SET &L1 = &LENGTH(&NUME)
DO WHILE &NUME > 0
 SET &L0 =  &L1 - &LENGTH(&NUMI)
 IF &L0 > 0 THEN SET &NUMI =  &SUBSTR(1:&L0,&CEROS)&NUMI
 ISREDIT LINE &LINE = LINE + < &COLU &NUMI >
 SET &LINE = &LINE + 1
 SET &NUME = &NUME - 1
 SET &NUMI = &NUMI + &INCR
END
EXIT CODE(0)
