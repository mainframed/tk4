CKLOCMEM CKINICIO
         LR    R8,R1
         L     R3,0(R8)
         MVC   MIEMBRO,2(R3)
         LA    R1,0
         LINK  EP=IDCAMS
         LTR   R15,R15
         OPEN  (DCOLLECT,INPUT)
         OPEN  (LISTADO,OUTPUT)
LEER     GET   DCOLLECT
         LA    R9,4(R1)
         USING DCUOUTH,R9
         CLI   DCURCTYP,C'D'
         BNE   LEER
         CLI   DCDDSORG,X'02'
         BNE   LEER
*
*  ESTAS 2 INSTRUCCIONES ES PARA SALTARSE LOS ARCHIVOS HFS
*  SUSTITUIR POR LA SC CORRESPONDIENTE
*
         CLC   DCDSTGCL(6),=C'SCOMVS'
         BE    LEER
*
ASIGNAR  EQU   *
         MVC   DSNAME,BLANCOS
         MVC   DSNAME,DCDDSNAM
         LA    R1,ALOC
         DYNALLOC
         LTR   R15,R15
         BNZ   LEER
         MVC   LINEA,BLANCOS
         OPEN  (DD1,INPUT)
         FIND  DD1,MIEMBRO,D
         CL    R15,=F'0'
         BNE    CLOSDD1
         MVI   LINEA+1,C'*'
CLOSDD1  EQU   *
         MVC   LINEA+4(44),DCDDSNAM
         PUT   LISTADO,LINEA
         CLOSE (DD1)
DESASIG  EQU   *
         LA    R1,ULOC
         DYNALLOC
         LTR   R15,R15
         BNZ   DUMP03
         B     LEER
FINOUT   CLOSE DCOLLECT
         CLOSE LISTADO
         CKFINAL RET=0
BLANCOS  DC    CL133' '
LINEA    DC    CL133' '
DUMP01   ABEND 0001,DUMP        * FALLO EN EL IDCAMS
DUMP02   ABEND 0002,DUMP        * FALLO EN LA ASIGNACION
DUMP03   ABEND 0003,DUMP        * FALLO EN LA DESASIGNACION
MIEMBRO  DS    D
LISTADO  DCB   DDNAME=LISTADO,DSORG=PS,MACRF=PM
DCOLLECT DCB   DDNAME=DCOLLECT,DSORG=PS,MACRF=GL,EODAD=FINOUT
DD1      DCB   DDNAME=DD1,DSORG=PO,MACRF=(R)
*
*  CONTROL SVC 99 FOR TO ALLOC  LIB-MEMBER TO DD1
*
ALOC     DC    0F'0',X'80',AL3(ALOCRB)  RB POINTER
ALOCRB   DS    0F
         DC    AL1(20),AL1(01),AL2(0)  LENGTH,ALLOC_VERB,FLAGS
ALOCER   DC    AL2(0)                  ERROR CODE
ALOCIN   DC    AL2(0)                  INFO CODE
         DC    A(ALOCTP)               TEXT UNIT LIST POINTER
         DC    A(0)                    RESERVED
         DC    A(0)                    FLAGS
ALOCTP   CALL  ,(ALOCDDN,ALOCDSN,ALOCDSP),VL,MF=L
ALOCDDN  DC    X'0001'       FIELD KEY (DDNAME)
         DC    X'0001'       FIX DATA
         DC    X'0003'       LENGTH NEXT FIELD
DDSYSIN  DC    C'DD1'        DDNAME
ALOCDSN  DC    X'0002'       FIELD KEY (DSNAME)
         DC    X'0001'       FIX DATA
         DC    X'002C'       LENGTH NEXT FIELD
DSNAME   DC    CL44' '
ALOCDSP  DC    X'0004'       FIELD KEY  (DISP)
         DC    X'0001'       FIX DATA
         DC    X'0001'       LENGTH NEXT FIELD
DISP     DC    X'08'         DISP=SHR
*
*  CONTROL SVC 99 FOR TO UNALLOC EXPL.CTM213.JOBS
*
ULOC     DC    0F'0',X'80',AL3(ULOCRB)  RB POINTER
ULOCRB   DS    0F
         DC    AL1(20),AL1(02),AL2(0)  LENGTH,ULLOC_VERB,FLAGS
ULOCER   DC    AL2(0)                  ERROR CODE
ULOCIN   DC    AL2(0)                  INFO CODE
         DC    A(ULOCTP)               TEXT UNIT LIST POINTER
         DC    A(0)                    RESERVED
         DC    A(0)                    FLAGS
ULOCTP   CALL  ,(ULOCDDN),VL,MF=L
ULOCDDN  DC    X'0001'       FIELD KEY (DDNAME)
         DC    X'0001'       FIX DATA
         DC    X'0003'       LENGTH NEXT FIELD
         DC    C'DD1'    DDNAME
         IDCDOUT
         END   CKLOCMEM
