CNA0KED1 TITLE ' - PARSE PARAMETERS FOR EDIT BY DDNAME'
***********************************************************************
*                                                                     *
*    BOB ZIMMERMAN                                                    *
*    CNA INSURANCE                                                    *
*    (312) 822-5719                                                   *
*    LAST MODIFIED 8503                                               *
*                                                                     *
***********************************************************************
*    THIS MODULE IS CALLED BY CNA0KED. THIS MODULE WILL PERFORM THE   *
*    PARSE SERVICE AND SUPPLY VALID PARAMETERS TO CNA0KED.            *
***********************************************************************
         SPACE 3
CNA0KED1 CSECT
***********************************************************************
*        STANDARD ENTRY CODE FOR A RE-ENTRANT PROGRAM                 *
*                 REGISTER 12 IS ONLY BASE REGISTER                   *
***********************************************************************
         SPACE 3
         USING *,15                     TEMPORARY BASE REGISTER
         B     PASSLIT                  BRANCH AROUND LITERALS
         DC    C'CNA0KED1'              CSECT NAME
         DC    C'03/12/85 '             DATE WRITTEN
         DC    C'VERSION 1.1 '          VERSION NUMBER
         DC    C'&SYSDATE '             DATE ASSEMBLED
         DC    C'&SYSTIME '             TIME ASSEMBLED
PASSLIT  DS    0H
         STM   14,12,12(13)             SAVE CALLERS REGISTERS
         LR    11,1                     SAVE PARAMETERS PASSED
         LR    12,15                    REGISTER 12 IS BASE REGISTER
         DROP  15                       END TEMPORARY ADDRESSING
         USING CNA0KED1,12              PERMANENT ADDRESSING
         SPACE
         GETMAIN R,LV=WSLEN,SP=0        GET WORKING STORAGE AREA
         SPACE
         ST    1,8(13)                  SET FORWARD CHAIN IN SAVEAREA
         ST    13,4(1)                  SET BACKWARD CHAIN
         LM    13,1,8(13)               RESTORE CLOBBERED REGISTERS
         USING WORKSTOR,13              SETUP WORKING STORAGE
         SPACE 3
         TITLE ' - GET CPPL AND ISSUE REQUEST PARSE'
***********************************************************************
*    ASSIGN BASE REGISTERS FOR THE CPPL, PPL, AND THE PARSE CSECT     *
*    AREA.                                                            *
*                                                                     *
*    AFTER INITALIZING ALL AREAS, CALL THE PARSE ROUTINE.             *
***********************************************************************
         SPACE
         USING CPPL,5                   SET UP CPPL ADDRESABILITY
         USING PPL,6                    SET UP PPL ADDRESABILITY
         USING IKJPARMD,7               SET UP PARSE CSECT ADDRSABLITY
         SPACE
         LR    5,1                      GET CPPL ADDRESS
         LA    6,PPLAREA                GRAB PPL POINTER (BUILT HERE)
         L     7,=A(PARSECT)            GRAP PARSE CSECT ADDRESS
         MVC   PPLCBUF,CPPLCBUF         MOVE CBUF PT FROM CPPL TO PPL
         MVC   PPLUPT,CPPLUPT           MOVE UPT PT FROM CPPL TO PPL
         ST    7,PPLPCL                 SAVE PARSECT ADDRESS IN PPL
         MVC   PPLECT,CPPLECT           MOVE ECT PT FROM CPPL TO PPL
         LA    2,CPECB                  GET ECB ADDRESS
         ST    2,PPLECB                 STORE ADDRESS INTO PPL
         LA    2,CPANS                  GET ANSWER AREA ADDRESS
         ST    2,PPLANS                 STORE ADDRESS INTO PPL
         LA    2,CPUWA                  GET UWA ADDRESS
         ST    2,PPLUWA                 STORE ADDRESS INTO PPL
         SPACE
         CALLTSSR EP=IKJPARS,           CALL THE PARSE ROUTINE         *
               MF=(E,(6))               POINTER TO THE PPL
         SPACE
         LTR   15,15                    IF PARSE ROUTINE FAILED
         BNZ   PARSERR
         DROP  6,5                      FREE REGISTERS
         TITLE ' - MOVE PARSE PARMS INTO LINKAGE SECTION'
***********************************************************************
*    BEGIN GETTING THE DATA FROM PARSE AND RETURN TO LINKAGE AREA     *
*    POINTED TO BY REGISTER 3.                                        *
***********************************************************************
         SPACE
         USING DSNPDE,4                 SET UP PDE ADDRESSABILITY
         L     7,CPANS                  GET POINTER TO PARSE RETURN
*
         LA    4,DSN                    POINT TO DSNAME
         MVC   0(4,3),PDEADDR           GET DSNAME ADDRESS
         MVC   4(2,3),=H'0'             MOVE ZERO TO TOP OF FULLWORD
         MVC   6(2,3),PDEL              GET DSNAME LENGTH
         MVC   8(8,3),SPACES            INIT MEMBER NAME TO SPACES
         LH    2,PDELM                  GET LENGTH OF MEMBER
         LTR   2,2                      WAS A MEMBER SPECIFIED
         BZ    NOMEM                    IF NOT - KEEP GOING
         BCTR  2,0                      BUMP DOWN FOR MVC
         L     14,PDEADDRM              GET ADDRESS OF MEMBER
         MVC   8(0,3),0(14)             DUMMY MVC FOR EX INSTRUCTION
         EX    2,*-6                    MOVE MEMBER NAME
NOMEM    DS    0H                       END MEMBER PROCESSING
*
         LA    4,PROF                   POINT TO PROFILE KEYWORD
         MVC   16(8,3),SPACES           INIT PROFILE TO SPACES
         LH    2,PDEL                   GET LENGTH OF PROFILE
         LTR   2,2                      WAS PROFILE SPECIFIED
         BZ    NOPROF                   IF NOT - KEEP GOING
         BCTR  2,0                      BUMP DOWN FOR MVC INSTRUCTION
         L     14,PDEADDR               GET ADDRESS OF PROFILE
         MVC   16(0,3),0(14)            DUMMY MVC FOR EX
         EX    2,*-6                    MOVE THE PROFILE
NOPROF   DS    0H                       END PROFILE PROCESSING
*
         LA    4,MACRO                  POINT TO MACRO KEYWORD
         MVC   24(8,3),SPACES           INIT MACRO TO SPACES
         LH    2,PDEL                   GET LENGTH OF MACRO
         LTR   2,2                      IF MACRO SPECIFIED
         BZ    NOMACRO                  IF NOT - KEEP GOING
         BCTR  2,0                      BUMP DOWN FOR MVC
         L     14,PDEADDR               GET ADDRESS OF MACRO
         MVC   24(0,3),0(14)            DUMMY MVC FOR EX INSTRUCTION
         EX    2,*-6                    MOVE THE MACRO
NOMACRO  DS    0H                       MACRO PROCESSING DONE
*
         LA    4,KFIRST                 POINT TO FIRST KEYWORD
         MVC   32(2,3),=H'0'            INIT TOP HALF OF FULLWORD
         MVC   34(2,3),0(4)             MOVE THE KEYWORD VALUE
*
         DROP  4                        FREE REGISTER
         TITLE '- EXIT PROGRAM'
         LR    1,13                     GET ADDRESS FOR FREEMAIN
         L     13,WORKSTOR+4            GET CALLERS SAVE AREA
         FREEMAIN R,A=(1),LV=WSLEN,SP=0 FREE WORKING STORAGE
         SPACE
         L     14,12(,13)               RESTORE RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REMAINING REGISTERS
         MVI   12(13),X'FF'             FLAG AS RETURNED FOR SNAP SVC
         BR    14                       RETURN TO CALLER
         TITLE ' - SUBROUTINE PARSERR -  PARSE ENDED WITH NON-ZERO CC'
PARSERR  DS    0H
         TPUTE 'PARSE ENCOUNTED SEVERE ERROR'
         SPACE
         ABEND (15),DUMP
         TITLE ' - WORKING STORAGE'
SPACES   DC    CL80' '                  SPACE LITERAL
         SPACE
WORKSTOR DSECT                          WORKING STORAGE DSECT
SAVEAREA DS    18F                      PROGRAM SAVE AREA
         DS    0D                       DOUBLE WORD ALIGN
PARMLIST DS    F                        PARMS FOR CALL OF CPPLRET
CPECB    DS    F                        ECB FOR THE PPL
CPANS    DS    F                        ANSWER AREA FOR THE PARSE
CPUWA    DS    F                        USER WORK AREA
PPLAREA  DS    0F,CL(PPLLEN)            FULLWORD ALIGN PPL
         DS    0D                       EXTEND TO DOUBLE WORD BOUNDRY
WSLEN    EQU   *-WORKSTOR               LENGTH OF WORKING STORAGE
CNA0KED1 CSECT ,                        RESUME CSECT
         TITLE ' - IKJPARSE CONTROL BLOCKS'
         PRINT NOGEN
PARSECT  IKJPARM
         SPACE
DSN      IKJPOSIT DSNAME,               FIRST FIELD REQUIRED           *
               PROMPT='A DDNAME(MEMBER) - MEMBER IS OPTIONAL',         *
               HELP='A VALID DDNAME - OPTIONALLY ENCLOSE MEMBER',      *
               VALIDCK=DSNEXIT
         SPACE
KPROF    IKJKEYWD
         IKJNAME 'PROFILE',SUBFLD=SPROF
         SPACE
KMACRO   IKJKEYWD
         IKJNAME 'MACRO',SUBFLD=SMACRO
         SPACE
KFIRST   IKJKEYWD
         IKJNAME 'FIRST'
         SPACE
SPROF    IKJSUBF
PROF     IKJIDENT 'EDIT PROFILE NAME',                                 *
               MAXLNTH=8,                                              *
               PROMPT='A VALID EDIT PROFILE NAME',                     *
               FIRST=ALPHA,                                            *
               OTHER=ALPHANUM,                                         *
               HELP='A VALID PDF EDIT PROFILE NAME'
         SPACE
SMACRO   IKJSUBF
MACRO    IKJIDENT 'EDIT MACRO NAME',                                   *
               MAXLNTH=8,                                              *
               PROMPT='A VALID EDIT MACRO',                            *
               FIRST=ALPHA,                                            *
               OTHER=ALPHANUM,                                         *
               HELP='A VALID PDF EDIT MACRO'
         SPACE
         IKJENDP
         PRINT GEN
         TITLE ' - CVT DSECT'
         CVT   DSECT=YES                GENERATE A CVT MAP
         TITLE ' - CPPL AND PPL DSECT'
         DS    0H
         IKJCPPL ,                      GENERATE A CPPL MAP
         SPACE 3
         DS    0F                       FULLWORD ALIGNMENT
         IKJPPL ,                       PPL DSECT
PPLLEN   EQU   *-PPL                    PPL LENGTH
         TITLE ' - PDE DSECTS AND LINKAGE SECTION'
DSNPDE   DSECT                          PDE DSECT
PDEADDR  DS    A                        ADDRESS OF DATA
PDEL     DS    H                        LENGTH OF DATA
PDEFLAGS DS    X                        PDE FLAG
         DS    X                        FILLER BYTE
PDEADDRM DS    A                        ADDRESS OF DATA
PDELM    DS    H                        LENGTH OF DATA
PDEFLAGM DS    X                        PDE FLAG
         DS    X                        FILLER BYTE
         SPACE 3
CNA0KED1 CSECT ,                        RESUME CSECT
DSNEXIT  DS    0H                       PARSE EXIT ROUTINE
         USING DSNEXIT,15               SETUP ADDRESSABILITY
         L     1,0(1)                   GET PDE ADDRESS
         USING DSNPDE,1                 SETUP ADDRESSABILITY
         CLC   PDEL,=H'8'               IS LENGTH > 8 BYTES
         BH    DSNERR                   IF SO - ERROR
         SR    15,15                    CLEAR RETURN CODE
         BR    14                       EXIT
DSNERR   DS    0H                       ERROR ROUTINE
         LA    15,4                     SET RETURN CODE TO 4
         BR    14                       RETURN
         DROP  15,1                     FREE REGISTERS
         END   CNA0KED1
