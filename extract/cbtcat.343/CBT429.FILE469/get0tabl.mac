GET0TABL CSECT
***********************************************************************
*        STANDARD ENTRY CODE FOR A RE-ENTRANT PROGRAM
*                 REGISTER 12 IS ONLY BASE REGISTER
***********************************************************************
         USING *,15                     TEMPORARY BASE REGISTER
         B     PASSLIT                  BRANCH AROUND LITERALS
         DC    C'GET0TABL '             CSECT NAME
         DC    C'05/14/85 '             DATE WRITTEN
         DC    C'VERSION 1.0 '          VERSION NUMBER
         DC    C'&SYSDATE '             DATE ASSEMBLED
         DC    C'&SYSTIME '             TIME ASSEMBLED
PASSLIT  DS    0H
         STM   14,12,12(13)             SAVE CALLERS REGISTERS
         LR    2,1                      SAVE PARAMETERS PASSED
         LR    12,15                    REGISTER 12 IS BASE REGISTER
         DROP  15                       END TEMPORARY ADDRESSING
         USING GET0TABL,12              PERMANENT ADDRESSING
         SPACE
         GETMAIN R,LV=WSLEN,SP=0        GET WORKING STORAGE AREA
         SPACE
         ST    1,8(13)                  SET FORWARD CHAIN IN SAVEAREA
         ST    13,4(1)                  SET BACKWARD CHAIN
         LM    13,1,8(13)               RESTORE CLOBBERED REGISTERS
         USING WORKSTOR,13              SETUP WORKING STORAGE
         SPACE 3
         CALL  RET0TABL,(DDNAME),MF=(E,SPFPARMS)
         SPACE
         ISPF  LOAD                LOAD ISPLINK
         SPACE
         L     2,0(2)              GET VARNAME POINTER
         MVC   VARNAME,=CL10'(        )'
         LH    3,0(2)              GET LENGTH OF VARNAME
         CH    3,=H'0'             IS REGISTER 3 = 0
         BE    BADLEN
         CH    3,=H'8'
         BH    BADLEN
         BCTR  3,0                 BUMP DOWN FOR MVC
         MVC   VARNAME+1(*-*),2(2) DUMMY MVC
         EX    3,*-6               MOVE REST OF VARNAME
         VREPLACE VARNAME,=F'8',DDNAME,MF=E
         SPACE
         VPUT  VARNAME,SHARED,MF=E
         SPACE
         ISPF  DELETE
         SPACE 3
         LR    1,13                     GET ADDRESS FOR FREEMAIN
         L     13,WORKSTOR+4            GET CALLERS SAVE AREA
         FREEMAIN R,A=(1),LV=WSLEN,SP=0 FREE WORKING STORAGE
         SPACE
         SR    15,15                    GET RETURN CODE
         L     14,12(,13)               RESTORE RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REMAINING REGISTERS
         MVI   12(13),X'FF'             FLAG AS RETURNED FOR SNAP SVC
         BR    14                       RETURN TO CALLER
         SPACE 3
BADLEN   DS    0H
         TPUT  =CL79'INVALID PARAMETER LENGTH RECEIVED - EXPECTED 1 - 8*
               BYTES',79
         SPACE 3
         ABEND 20
         EJECT
WORKSTOR DSECT                          WORKING STORAGE DSECT
SAVEAREA DS    18F                      PROGRAM SAVE AREA
         DS    0D                       DOUBLE WORD ALIGN
DDNAME   DS    CL8
VARNAME  DS    CL10
         ISPF  PARMS,MINIMUM=1
         DS    0D
WSLEN    EQU   *-WORKSTOR               LENGTH OF WORKING STORAGE
GET0TABL CSECT ,                        RESUME CSECT
         END   GET0TABL
