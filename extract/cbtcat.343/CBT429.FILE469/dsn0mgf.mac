000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.    DSN0MGF.
000300 AUTHOR.        BOB ZIMMERMAN.
000400 INSTALLATION.  CNA INSURANCE.
000500 DATE-COMPILED.
000600******************************************************************
000700*    BOB ZIMMERMAN                                               *
000800*    CNA INSURANCE                                               *
000900*    (312) 822-5719                                              *
001000*    LAST MODIFIED 8508                                          *
001100******************************************************************
001200*    THIS PROGRAM SUPPORT BOTH IBM'S VS COBOL AND VS COBOL II    *
001300*    PRODUCTS.                                                   *
001400******************************************************************
001500/
001600 ENVIRONMENT DIVISION.
001700 DATA DIVISION.
001800 WORKING-STORAGE SECTION.
001900
002000******************************************************************
002100*    SPF-VARIABLES IS THE WORKING STORAGE AREA USED TO CONTAIN   *
002200*    THE CONTENTS OF THE ISPF VARIABLES USED IN THIS DIALOG.     *
002300*    THESE VARIABLES MAY BE ACCESSED USING EITHER THE VDEFINE    *
002400*    SERVICE OR THE VCOPY SERVICE.                               *
002500******************************************************************
002600
002700 01  SPF-VARIABLES                     SYNC.
002800     05  COMPLETION-CODE               PIC S9(09) COMP.
002900     05  SPF-ZTDTOP                    PIC S9(09) COMP.
003000     05  SPF-CSR                       PIC S9(09) COMP.
003100     05  SPF-ROWNUM                    PIC S9(09) COMP.
003200     05  SPF-NUMROW                    PIC S9(09) COMP.
003300     05  SPF-LSEL                      PIC X(08) VALUE SPACES.
003400     05  SPF-DSNAME                    PIC X(56) VALUE SPACES.
003500     05  SPF-VOLSER                    PIC X(06) VALUE SPACES.
003600     05  SPF-STATS1                    PIC X(01) VALUE SPACES.
003700     05  SPF-STATS2                    PIC X(01) VALUE SPACES.
003800     05  SPF-PROFILE                   PIC X(08) VALUE SPACES.
003900     05  SPF-IMACRO                    PIC X(08) VALUE SPACES.
004000     05  SPF-TBDISPL-PANEL             PIC X(08).
004100     05  SPF-FROM-TABLE                PIC X(08).
004200     05  SPF-TO-TABLE                  PIC X(08).
004300     05  SPF-WORK-AREA.
004400         10  SPF-WORK-AREA-FIRST3      PIC X(03).
004500         10  SPF-WORK-AREA-LAST        PIC X(01).
004600
004700     05  SPF-WORK-NAME.
004800         10  SPF-WORK-USER             PIC X(05).
004900         10  SPF-WORK-ID               PIC X(03).
005000     05  SPF-TEMP-PANEL               PIC X(08).
005100     05  SPF-TEMP-TABLE               PIC X(08).
005200     05  SPF-THE-TABLE                PIC X(08).
005300
005400
005500/
005600******************************************************************
005700*    THE FOLLOWING WORKING STORAGE AREA IS USED IN PROCESSING    *
005800*    AND PARSING THE COMMAND LINE.                               *
005900******************************************************************
006000
006100 01  ZCMD-PROCESS-AREA                 SYNC.
006200     05  ZCMD-COMMAND-LINE             PIC X(80) VALUE SPACES.
006300     05  ZCMD-OVERFLOW-SWITCH          PIC 9(01) VALUE 0.
006400     05  ZCMD-COMMAND                  PIC X(80) VALUE SPACES.
006500     05  ZCMD-PARM-COUNT               PIC S9(04) COMP VALUE +0.
006600     05  ZCMD-PARM-TABLE.
006700         10  ZCMD-PARM                 PIC X(80)
006800                                       OCCURS 20 TIMES
006900                                       INDEXED BY ZCMD-INX.
007000
007100
007200/
007300******************************************************************
007400*    THE FOLLOWING AREA IS USED FOR ISPF DIALOG CALLS. IT        *
007500*    CONTAINS LITERALS, NAMELISTS AND WORKING STORAGE AREAS      *
007600*    THAT THE DIALOG CALLS REQUIRE.                              *
007700******************************************************************
007800
007900 01  PDF-WORK-AREAS                   SYNC.
008000     05  PDF-BROWSE                   PIC X(8) VALUE 'BROWSE'.
008100     05  PDF-CANCEL                   PIC X(8) VALUE 'CANCEL'.
008200     05  PDF-CHAR                     PIC X(8) VALUE 'CHAR'.
008300     05  PDF-CONTROL                  PIC X(8) VALUE 'CONTROL'.
008400     05  PDF-DISPLAY                  PIC X(8) VALUE 'DISPLAY'.
008500     05  PDF-EDIT                     PIC X(8) VALUE 'EDIT'.
008600     05  PDF-ERRORS                   PIC X(8) VALUE 'ERRORS'.
008700     05  PDF-FIXED                    PIC X(8) VALUE 'FIXED'.
008800     05  PDF-LINE                     PIC X(8) VALUE 'LINE'.
008900     05  PDF-MOVE                     PIC X(8) VALUE 'MOVE'.
009000     05  PDF-NEXT                     PIC X(8) VALUE 'NEXT'.
009100     05  PDF-NOWRITE                  PIC X(8) VALUE 'NOWRITE'.
009200     05  PDF-ORDER                    PIC X(8) VALUE 'ORDER'.
009300     05  PDF-PREVIOUS                 PIC X(8) VALUE 'PREVIOUS'.
009400     05  PDF-REPLACE                  PIC X(8) VALUE 'REPLACE'.
009500     05  PDF-RESTORE                  PIC X(8) VALUE 'RESTORE'.
009600     05  PDF-RETURN                   PIC X(8) VALUE 'RETURN'.
009700     05  PDF-SAVE                     PIC X(8) VALUE 'SAVE'.
009800     05  PDF-SELECT                   PIC X(8) VALUE 'SELECT'.
009900     05  PDF-SETMSG                   PIC X(8) VALUE 'SETMSG'.
010000     05  PDF-SHARE                    PIC X(8) VALUE 'SHARE'.
010100     05  PDF-TBADD                    PIC X(8) VALUE 'TBADD'.
010200     05  PDF-TBBOTTOM                 PIC X(8) VALUE 'TBBOTTOM'.
010300     05  PDF-TBCLOSE                  PIC X(8) VALUE 'TBCLOSE'.
010400     05  PDF-TBCREATE                 PIC X(8) VALUE 'TBCREATE'.
010500     05  PDF-TBDELETE                 PIC X(8) VALUE 'TBDELETE'.
010600     05  PDF-TBDISPL                  PIC X(8) VALUE 'TBDISPL'.
010700     05  PDF-TBEND                    PIC X(8) VALUE 'TBEND'.
010800     05  PDF-TBOPEN                   PIC X(8) VALUE 'TBOPEN'.
010900     05  PDF-TBPUT                    PIC X(8) VALUE 'TBPUT'.
011000     05  PDF-TBQUERY                  PIC X(8) VALUE 'TBQUERY'.
011100     05  PDF-TBSARG                   PIC X(8) VALUE 'TBSARG'.
011200     05  PDF-TBSAVE                   PIC X(8) VALUE 'TBSAVE'.
011300     05  PDF-TBSCAN                   PIC X(8) VALUE 'TBSCAN'.
011400     05  PDF-TBSKIP                   PIC X(8) VALUE 'TBSKIP'.
011500     05  PDF-TBSORT                   PIC X(8) VALUE 'TBSORT'.
011600     05  PDF-TBTOP                    PIC X(8) VALUE 'TBTOP'.
011700     05  PDF-TBVCLEAR                 PIC X(8) VALUE 'TBVCLEAR'.
011800     05  PDF-VCOPY                    PIC X(8) VALUE 'VCOPY'.
011900     05  PDF-VDEFINE                  PIC X(8) VALUE 'VDEFINE'.
012000     05  PDF-VRESET                   PIC X(8) VALUE 'VRESET'.
012100     05  PDF-WRITE                    PIC X(8) VALUE 'WRITE'.
012200     05  PDF-DUMMY-PARAMETER          PIC X(8) VALUE '        '.
012300     05  PDF-NAME-LIST                PIC X(256).
012400     05  PDF-COND-LIST                PIC X(80).
012500     05  PDF-DSNAME                   PIC X(56).
012600     05  PDF-VOLSER                   PIC X(6).
012700     05  PDF-FULLWORD                 PIC S9(09) COMP.
012800     05  PDF-RETURN-CODE              PIC S9(09) COMP.
012900     05  WORK-NUM                     PIC S9(09) COMP.
013000     05  WORK-NUM-DISPLAY             PIC Z(08)9.
013100     05  PDF-BROWSE-RETURN-CODE       PIC S9(09) COMP.
013200     05  PDF-TBDISPL-ROW              PIC S9(09) COMP.
013300     05  PDF-SELECT-BUFFER-LENGTH     PIC S9(09) COMP.
013400     05  WS-TABLE-ID.
013500         10  WS-TABLE-ID-PREFACE      PIC X(5).
013600         10  WS-TABLE-SUFFIX          PIC X(3).
013700     05  PDF-TABLE-ID.
013800         10  PDF-TABLE-ID-PREFACE     PIC X(5).
013900         10  PDF-TABLE-SUFFIX         PIC X(3) VALUE 'DSN'.
014000     05  PDF-TBDISPL-PANEL            PIC X(8).
014100     05  PDF-TBDISPL-AUTOSEL          PIC X(8).
014200     05  PDF-TBDISPL-MESSAGE          PIC X(8).
014300     05  PDF-TBDISPL-CURSOR           PIC X(8).
014400     05  PDF-VARIABLE-NAME            PIC X(8).
014500     05  PDF-SHORT-MESSAGE            PIC X(24).
014600     05  PDF-LONG-MESSAGE             PIC X(78).
014700     05  PDF-SELECT-BUFFER            PIC X(256).
014800     05  PDF-LIBRARY                  PIC X(08) VALUE 'DSNMGF'.
014900/
015000 PROCEDURE DIVISION.
015100
015200******************************************************************
015300*    INITALIZE WORKING STORAGE AREAS.                            *
015400*    VDEFINE AND VCOPY ANY ISPF VARIABLES THAT ARE REQUIRED.     *
015500******************************************************************
015600
015700 0100-INIT-AND-VDEFINE.
015800
015900     CALL 'ISPLINK' USING             PDF-CONTROL
016000                                      PDF-ERRORS
016100                                      PDF-CANCEL.
016200
016300     MOVE SPACES TO PDF-SELECT-BUFFER.
016400     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
016500     STRING 'SELECT PGM(EDREC)' DELIMITED BY SIZE
016600         INTO PDF-SELECT-BUFFER
016700         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
016800     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
016900     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
017000                                       PDF-SELECT-BUFFER.
017100
017200     PERFORM 5000-VDEFINE THRU 5999-EXIT.
017300
017400******************************************************************
017500*    BUILD THE TABLE NAME FROM THE FIRST 5 BYTES OF THE USERID   *
017600******************************************************************
017700
017800     MOVE '(ZUSER)' TO PDF-NAME-LIST.
017900     MOVE 8 TO PDF-FULLWORD.
018000     CALL 'ISPLINK' USING              PDF-VCOPY
018100                                       PDF-NAME-LIST
018200                                       PDF-FULLWORD
018300                                       PDF-TABLE-ID
018400                                       PDF-MOVE.
018500     MOVE 'DSN' TO PDF-TABLE-SUFFIX.
018600
018700******************************************************************
018800*    INITALIZE THE TBDISPL PARAMETERS TO SPACES                  *
018900******************************************************************
019000
019100     MOVE SPACES TO PDF-TBDISPL-MESSAGE.
019200     MOVE SPACES TO PDF-TBDISPL-CURSOR.
019300     MOVE ZEROS TO PDF-TBDISPL-ROW.
019400
019500******************************************************************
019600*    BUILD/OPEN THE ISPF TABLE                                   *
019700******************************************************************
019800
019900     PERFORM 2000-BUILD-TABLE THRU 2999-EXIT.
020000
020100******************************************************************
020200*    PROCESS USER INPUT UNTIL END/RETURN ENTERED                 *
020300******************************************************************
020400
020500     MOVE ZEROS TO PDF-RETURN-CODE.
020600     PERFORM 1000-DISPLAY-PANEL THRU 1999-EXIT
020700         UNTIL PDF-RETURN-CODE > 4.
020800
020900 EXIT-PROGRAM.
021000
021100******************************************************************
021200*    IF THIS IS NOT A RECURSIVE INVOCATION (TABLE WAS NOT OPEN)  *
021300*       CLOSE THE ISPF TABLE                                     *
021400******************************************************************
021500
021600     IF SPF-STATS2 NOT = 5
021700         MOVE 50 TO PDF-FULLWORD
021800         CALL 'ISPLINK' USING          PDF-TBCLOSE
021900                                       PDF-TABLE-ID
022000                                       PDF-DUMMY-PARAMETER
022100                                       PDF-DUMMY-PARAMETER
022200                                       PDF-FULLWORD
022300                                       PDF-LIBRARY
022400     ELSE
022500         CALL 'ISPLINK' USING          PDF-TBSAVE
022600                                       PDF-TABLE-ID
022700                                       PDF-DUMMY-PARAMETER
022800                                       PDF-DUMMY-PARAMETER
022900                                       PDF-FULLWORD
023000                                       PDF-LIBRARY.
023100
023200 EXIT-PROGRAM-NOW.
023300
023400******************************************************************
023500*    TO FORCE ISPF TO CLOSE THE DSNMGF FILE, A DUMMY TBSTATS IS  *
023600*    ISSUED.                                                     *
023700******************************************************************
023800
023900     MOVE SPACES TO PDF-SELECT-BUFFER.
024000     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
024100     STRING 'TBSTATS DUMMY LIBRARY(DUMMY)'
024200         DELIMITED BY SIZE
024300         INTO PDF-SELECT-BUFFER
024400         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
024500     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
024600                                       PDF-SELECT-BUFFER.
024700
024800     MOVE SPACES TO PDF-SELECT-BUFFER.
024900     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
025000     STRING 'VPUT (DSMPPANL) PROFILE'
025100         DELIMITED BY SIZE
025200         INTO PDF-SELECT-BUFFER
025300         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
025400     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
025500                                       PDF-SELECT-BUFFER.
025600
025700     CALL 'ISPLINK' USING              PDF-VRESET.
025800     GOBACK.
025900/
026000 1000-DISPLAY-PANEL.
026100
026200******************************************************************
026300*    START AT THE TOP OF THE TABLE, SKIP TO WHERE THE USER LEAVES*
026400*    THE TOP DISPLAY LINE WHEN HE HITS ENTER.                    *
026500******************************************************************
026600
026700     CALL 'ISPLINK' USING              PDF-TBTOP
026800                                       PDF-TABLE-ID.
026900
027000     CALL 'ISPLINK' USING              PDF-TBSKIP
027100                                       PDF-TABLE-ID
027200                                       SPF-ZTDTOP.
027300
027400     MOVE 'ROWNUM' TO PDF-VARIABLE-NAME.
027500     CALL 'ISPLINK' USING              PDF-TBQUERY
027600                                       PDF-TABLE-ID
027700                                       PDF-DUMMY-PARAMETER
027800                                       PDF-DUMMY-PARAMETER
027900                                       PDF-VARIABLE-NAME.
028000
028100******************************************************************
028200*    CHECK FOR AN EMPTY TABLE CONDITION                          *
028300*        IF EMPTY - ADD 1 BLANK ROW                              *
028400******************************************************************
028500
028600     IF SPF-ROWNUM = 0
028700         CALL 'ISPLINK' USING          PDF-TBVCLEAR
028800                                       PDF-TABLE-ID
028900
029000         CALL 'ISPLINK' USING          PDF-TBADD
029100                                       PDF-TABLE-ID.
029200
029300******************************************************************
029400*    IF AN ERROR MESSAGE EXISTS - FORCE AUTOSEL OF THE ROW IN    *
029500*    ERROR                                                       *
029600*    IF NO ERROR MESSAGE EXISTS - NO AUTOSEL IS FORCED           *
029700*    IF NO ERROR MESSAGE AND THE CURSOR IS SET TO A ROW (I, R    *
029800*    LINE COMMANDS) THEN PUT THE CURSOR IN THE LINE COMMAND AREA *
029900******************************************************************
030000
030100     IF PDF-TBDISPL-MESSAGE NOT = SPACES
030200         MOVE 'YES' TO PDF-TBDISPL-AUTOSEL
030300     ELSE
030400         MOVE 'NO' TO PDF-TBDISPL-AUTOSEL
030500         MOVE SPF-CSR TO PDF-TBDISPL-ROW
030600         IF SPF-CSR NOT = 0 AND PDF-TBDISPL-CURSOR = SPACES
030700             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR.
030800
030900     CALL 'ISPLINK' USING              PDF-TBDISPL
031000                                       PDF-TABLE-ID
031100                                       PDF-TBDISPL-PANEL
031200                                       PDF-TBDISPL-MESSAGE
031300                                       PDF-TBDISPL-CURSOR
031400                                       PDF-TBDISPL-ROW
031500                                       PDF-DUMMY-PARAMETER
031600                                       PDF-TBDISPL-AUTOSEL.
031700     MOVE RETURN-CODE TO PDF-RETURN-CODE.
031800     IF PDF-RETURN-CODE > 4
031900         GO TO 1999-EXIT.
032000
032100******************************************************************
032200*    REINIT ALL TBDISPL PARMS TO THEIR DEFAULTS                  *
032300******************************************************************
032400
032500     MOVE SPF-TBDISPL-PANEL TO PDF-TBDISPL-PANEL.
032600     MOVE SPACES TO PDF-TBDISPL-MESSAGE.
032700     MOVE SPACES TO PDF-TBDISPL-CURSOR.
032800     MOVE ZEROS TO PDF-TBDISPL-ROW.
032900     MOVE ZEROS TO SPF-CSR.
033000
033100 1100-VARIABLE-NAME.
033200
033300******************************************************************
033400*    CHECK TO SEE IF A TABLE ROW WAS MODIFIED                    *
033500******************************************************************
033600
033700     MOVE 'CSR' TO PDF-VARIABLE-NAME.
033800     CALL 'ISPLINK' USING              PDF-TBQUERY
033900                                       PDF-TABLE-ID
034000                                       PDF-DUMMY-PARAMETER
034100                                       PDF-DUMMY-PARAMETER
034200                                       PDF-DUMMY-PARAMETER
034300                                       PDF-DUMMY-PARAMETER
034400                                       PDF-DUMMY-PARAMETER
034500                                       PDF-VARIABLE-NAME.
034600
034700     IF SPF-CSR = 0 GO TO 1500-PROCESS-ZCMD.
034800
034900     PERFORM 4000-LINE-COMMAND THRU 4999-EXIT.
035000
035100******************************************************************
035200*    IF A ROW IS IN ERROR - TELL THE USER NOW                    *
035300*                           FORCE THE USER TO FIX IT NOW         *
035400******************************************************************
035500
035600     IF PDF-TBDISPL-MESSAGE NOT = SPACES
035700         GO TO 1999-EXIT.
035800
035900******************************************************************
036000*    IF MORE THAN ONE ROW WAS SELECTED - GO GET ANOTHER ROW      *
036100******************************************************************
036200
036300     IF PDF-RETURN-CODE = 4
036400         MOVE SPACES TO PDF-TBDISPL-PANEL
036500         GO TO 1999-EXIT.
036600/
036700 1500-PROCESS-ZCMD.
036800
036900******************************************************************
037000*    PROCESS THE PRIMARY COMMAND LINE                            *
037100*            FIRST UNSTRING THE PRIMARY COMMAND AREA             *
037200******************************************************************
037300
037400     IF ZCMD-COMMAND-LINE = SPACES
037500         GO TO 1999-EXIT.
037600
037700     MOVE ZEROS TO ZCMD-PARM-COUNT.
037800     MOVE ZEROS TO ZCMD-OVERFLOW-SWITCH.
037900     MOVE SPACES TO ZCMD-COMMAND.
038000     MOVE SPACES TO ZCMD-PARM-TABLE.
038100
038200     UNSTRING ZCMD-COMMAND-LINE
038300              DELIMITED BY ALL SPACES
038400              INTO ZCMD-COMMAND
038500              ZCMD-PARM (1)
038600              ZCMD-PARM (2)
038700              ZCMD-PARM (3)
038800              ZCMD-PARM (4)
038900              ZCMD-PARM (5)
039000              ZCMD-PARM (6)
039100              ZCMD-PARM (7)
039200              ZCMD-PARM (8)
039300              ZCMD-PARM (9)
039400              ZCMD-PARM (10)
039500              ZCMD-PARM (11)
039600              ZCMD-PARM (12)
039700              ZCMD-PARM (13)
039800              ZCMD-PARM (14)
039900              ZCMD-PARM (15)
040000              ZCMD-PARM (16)
040100              ZCMD-PARM (17)
040200              ZCMD-PARM (18)
040300              ZCMD-PARM (19)
040400              ZCMD-PARM (20)
040500              TALLYING ZCMD-PARM-COUNT
040600              ON OVERFLOW
040700                  MOVE +1 TO ZCMD-OVERFLOW-SWITCH.
040800
040900     IF ZCMD-PARM-COUNT > 0
041000         SUBTRACT 1 FROM ZCMD-PARM-COUNT.
041100
041200     IF ZCMD-OVERFLOW-SWITCH NOT = 0
041300         MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
041400         MOVE 'THERE IS A LIMIT OF 20 PARAMETERS TO ANY PRIMARY CO
041500-        'COMMAND' TO PDF-LONG-MESSAGE
041600         MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
041700         MOVE SPACES TO PDF-TBDISPL-PANEL
041800         GO TO 1999-EXIT.
041900
042000 1600-PRIMARY-COMMANDS.
042100
042200******************************************************************
042300*    SORT PRIMAY COMMAND                                         *
042400*         NO VALID OPERANDS                                      *
042500******************************************************************
042600
042700     IF ZCMD-COMMAND = 'SORT'
042800         IF ZCMD-PARM-COUNT > 0
042900             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
043000             MOVE 'THERE ARE NO VALID PARAMETERS FOR THE SORT COMM
043100-            'AND' TO PDF-LONG-MESSAGE
043200             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
043300             MOVE SPACES TO PDF-TBDISPL-PANEL
043400             GO TO 1999-EXIT
043500         ELSE
043600             MOVE SPACES TO PDF-TBDISPL-CURSOR
043700             MOVE 0 TO SPF-CSR
043800             MOVE '(DSNAME,C,A,VOLSER,C,A)' TO PDF-NAME-LIST
043900             CALL 'ISPLINK' USING      PDF-TBSORT
044000                                       PDF-TABLE-ID
044100                                       PDF-NAME-LIST
044200             MOVE SPACES TO ZCMD-COMMAND-LINE
044300             GO TO 1999-EXIT.
044400
044500******************************************************************
044600*    LIST PRIMAY COMMAND                                         *
044700*         NO VALID OPERANDS                                      *
044800******************************************************************
044900
045000     IF ZCMD-COMMAND = 'LIST' OR 'FLIST'
045100         IF ZCMD-PARM-COUNT > 0
045200             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
045300             MOVE 'THERE ARE NO VALID PARAMETERS FOR THE LIST COMM
045400-            'AND' TO PDF-LONG-MESSAGE
045500             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
045600             MOVE SPACES TO PDF-TBDISPL-PANEL
045700             GO TO 1999-EXIT
045800         ELSE
045900             PERFORM 7000-LIST-COMMAND THRU 7099-EXIT
046000             IF PDF-TBDISPL-MESSAGE = SPACES
046100                 GO TO 1500-PROCESS-ZCMD
046200             ELSE
046300                 GO TO 1999-EXIT.
046400
046500******************************************************************
046600*    INIT PRIMAY COMMAND                                         *
046700*         NO VALID OPERANDS                                      *
046800******************************************************************
046900
047000     IF ZCMD-COMMAND = 'INIT'
047100         IF ZCMD-PARM-COUNT > 0
047200             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
047300             MOVE 'THERE ARE NO VALID PARAMETERS FOR THE INIT COMM
047400-            'AND' TO PDF-LONG-MESSAGE
047500             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
047600             MOVE SPACES TO PDF-TBDISPL-PANEL
047700             GO TO 1999-EXIT
047800         ELSE
047900             CALL 'ISPLINK' USING     PDF-TBTOP
048000                                      PDF-TABLE-ID
048100             PERFORM 6100-DEL THRU 6199-EXIT
048200             MOVE SPACES TO ZCMD-COMMAND-LINE
048300             GO TO 1999-EXIT.
048400
048500******************************************************************
048600*    EXPAND PRIMARY COMMAND                                      *
048700*         ONE VALID OPERAND - "YES" OR "NO"                      *
048800******************************************************************
048900
049000     IF ZCMD-COMMAND = 'EXPAND'
049100         IF ZCMD-PARM-COUNT > 1
049200             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
049300             MOVE 'THIS COMMAND ONLY SUPPORTS AN ON OR OFF PARM'
049400             TO PDF-LONG-MESSAGE
049500             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
049600             MOVE SPACES TO PDF-TBDISPL-PANEL
049700             GO TO 1999-EXIT
049800         ELSE
049900         IF ZCMD-PARM (1) NOT = 'ON' AND 'OFF' AND SPACES
050000             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
050100             MOVE 'THIS COMMAND ONLY SUPPORTS AN ON OR OFF PARM'
050200             TO PDF-LONG-MESSAGE
050300             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
050400             MOVE SPACES TO PDF-TBDISPL-PANEL
050500             GO TO 1999-EXIT
050600         ELSE
050700         IF ZCMD-PARM (1) = 'ON' OR SPACES
050800             MOVE SPACES TO PDF-TBDISPL-CURSOR
050900             MOVE 'DSNBMGF1' TO SPF-TBDISPL-PANEL,
051000                                PDF-TBDISPL-PANEL
051100             MOVE SPACES TO ZCMD-COMMAND-LINE
051200             GO TO 1999-EXIT
051300         ELSE
051400             MOVE SPACES TO PDF-TBDISPL-CURSOR
051500             MOVE 'DSNBMGF' TO SPF-TBDISPL-PANEL,
051600                                PDF-TBDISPL-PANEL
051700             MOVE SPACES TO ZCMD-COMMAND-LINE
051800             GO TO 1999-EXIT.
051900
052000******************************************************************
052100*    LOCATE PRIMARY COMMAND                                      *
052200*         ONE REQUIRED OPERAND - SEARCH ARGUMENT                 *
052300******************************************************************
052400
052500     IF ZCMD-COMMAND = 'LOCATE' OR 'LOC' OR 'L'
052600         IF ZCMD-PARM-COUNT > 1
052700             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
052800             MOVE 'THIS COMMAND ONLY SUPPORTS ONE PARAMETER'
052900             TO PDF-LONG-MESSAGE
053000             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
053100             MOVE SPACES TO PDF-TBDISPL-PANEL
053200             GO TO 1999-EXIT
053300         ELSE
053400         IF ZCMD-PARM (1) = SPACES
053500             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
053600             MOVE 'THIS COMMAND REQUIRES ONE PARAMETER'
053700             TO PDF-LONG-MESSAGE
053800             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
053900             MOVE SPACES TO PDF-TBDISPL-PANEL
054000             GO TO 1999-EXIT
054100         ELSE
054200         CALL 'ISPLINK' USING         PDF-TBTOP
054300                                      PDF-TABLE-ID
054400         MOVE SPACES TO SPF-DSNAME
054500         STRING QUOTE
054600             ZCMD-PARM (1)
054700             DELIMITED BY SIZE
054800             INTO SPF-DSNAME
054900         MOVE '(DSNAME)' TO PDF-NAME-LIST
055000         MOVE 'ZTDTOP' TO PDF-VARIABLE-NAME
055100         MOVE '(GE)' TO PDF-COND-LIST
055200         CALL 'ISPLINK' USING          PDF-TBSCAN
055300                                       PDF-TABLE-ID
055400                                       PDF-NAME-LIST
055500                                       PDF-DUMMY-PARAMETER
055600                                       PDF-DUMMY-PARAMETER
055700                                       PDF-DUMMY-PARAMETER
055800                                       PDF-DUMMY-PARAMETER
055900                                       PDF-VARIABLE-NAME
056000                                       PDF-COND-LIST
056100         IF SPF-ZTDTOP NOT = 0
056200             SUBTRACT 1 FROM SPF-ZTDTOP
056300             GO TO 1999-EXIT
056400         ELSE
056500             GO TO 1999-EXIT.
056600
056700******************************************************************
056800*    CREATE PRIMARY COMMAND                                      *
056900*         ONE REQUIRED OPERAND - 3 BYTE DSNAME                   *
057000******************************************************************
057100
057200     IF ZCMD-COMMAND = 'CREATE' OR 'CR'
057300         IF ZCMD-PARM-COUNT > 1
057400             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
057500             MOVE 'THIS COMMAND ONLY SUPPORTS ONE PARAMETER'
057600             TO PDF-LONG-MESSAGE
057700             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
057800             MOVE SPACES TO PDF-TBDISPL-PANEL
057900             GO TO 1999-EXIT
058000         ELSE
058100         IF ZCMD-PARM (1) = SPACES
058200             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
058300             MOVE 'THIS COMMAND REQUIRES ONE PARAMETER'
058400             TO PDF-LONG-MESSAGE
058500             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
058600             MOVE SPACES TO PDF-TBDISPL-PANEL
058700             GO TO 1999-EXIT
058800         ELSE
058900         MOVE ZCMD-PARM (1) TO SPF-WORK-AREA
059000         IF   SPF-WORK-AREA-LAST NOT = SPACES
059100             MOVE 'PARAMETER TOO LONG' TO PDF-SHORT-MESSAGE
059200             MOVE 'THE PARAMETER MUST BE LESS THAN 4 BYTES LONG'
059300             TO PDF-LONG-MESSAGE
059400             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
059500             MOVE SPACES TO PDF-TBDISPL-PANEL
059600             GO TO 1999-EXIT
059700         ELSE
059800         IF SPF-WORK-AREA = 'DSN'
059900             MOVE '"DSN" IS RESERVED' TO PDF-SHORT-MESSAGE
060000             MOVE 'THE "DSN" LABEL IS A RESERVED SLOT AND MAY NOT
060100-            'BE USED'
060200             TO PDF-LONG-MESSAGE
060300             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
060400             MOVE SPACES TO PDF-TBDISPL-PANEL
060500             GO TO 1999-EXIT
060600         ELSE
060700         MOVE PDF-TABLE-ID TO WS-TABLE-ID
060800         MOVE SPF-WORK-AREA TO WS-TABLE-SUFFIX
060900         MOVE WS-TABLE-ID TO SPF-TO-TABLE
061000         MOVE PDF-TABLE-ID TO SPF-FROM-TABLE
061100         MOVE SPACES TO PDF-SELECT-BUFFER
061200         MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
061300         STRING 'TBSTATS ' DELIMITED BY SIZE
061400                SPF-TO-TABLE DELIMITED BY SPACE
061500                ' STATUS1(ST1) LIBRARY(DSNMGF)'
061600                DELIMITED BY SIZE
061700             INTO PDF-SELECT-BUFFER
061800             WITH POINTER PDF-SELECT-BUFFER-LENGTH
061900         SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH
062000         CALL 'ISPEXEC' USING          PDF-SELECT-BUFFER-LENGTH
062100                                       PDF-SELECT-BUFFER
062200         IF SPF-STATS1 = 1
062300             MOVE 'DUPLICATE DSN LIST' TO PDF-SHORT-MESSAGE
062400             MOVE 'YOU ARE ATTEMPTING TO CREATE AN EXISTING LIST'
062500             TO PDF-LONG-MESSAGE
062600             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
062700             MOVE SPACES TO PDF-TBDISPL-PANEL
062800             GO TO 1999-EXIT
062900         ELSE
063000         MOVE '(DSNAME VOLSER COMMENT PROFILE IMACRO)'
063100             TO PDF-NAME-LIST
063200         CALL 'ISPLINK' USING          PDF-TBCREATE
063300                                       SPF-TO-TABLE
063400                                       PDF-DUMMY-PARAMETER
063500                                       PDF-NAME-LIST
063600                                       PDF-WRITE
063700                                       PDF-REPLACE
063800                                       PDF-LIBRARY
063900                                       PDF-SHARE
064000         CALL 'ISPLINK' USING          PDF-TBTOP
064100                                       PDF-TABLE-ID
064200         PERFORM 6000-COPY-TABLE THRU 6099-EXIT
064300         MOVE 50 TO PDF-FULLWORD
064400         CALL 'ISPLINK' USING          PDF-TBCLOSE
064500                                       SPF-TO-TABLE
064600                                       PDF-DUMMY-PARAMETER
064700                                       PDF-DUMMY-PARAMETER
064800                                       PDF-FULLWORD
064900                                       PDF-LIBRARY
065000         MOVE SPACES TO PDF-SHORT-MESSAGE
065100         STRING WS-TABLE-SUFFIX ' HAS BEEN CREATED' DELIMITED BY
065200             SIZE INTO PDF-SHORT-MESSAGE
065300         MOVE 'YOUR CREATE COMMAND HAS COMPLETED SUCCESSFULLY'
065400         TO PDF-LONG-MESSAGE
065500         MOVE 'DTSM001W' TO PDF-TBDISPL-MESSAGE
065600         GO TO 1999-EXIT.
065700
065800
065900******************************************************************
066000*    COPY   PRIMARY COMMAND                                      *
066100*         ONE REQUIRED OPERAND - 3 BYTE DSNAME                   *
066200******************************************************************
066300
066400     IF ZCMD-COMMAND = 'COPY'
066500         IF ZCMD-PARM-COUNT > 1
066600             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
066700             MOVE 'THIS COMMAND ONLY SUPPORTS ONE PARAMETER'
066800             TO PDF-LONG-MESSAGE
066900             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
067000             MOVE SPACES TO PDF-TBDISPL-PANEL
067100             GO TO 1999-EXIT
067200         ELSE
067300         IF ZCMD-PARM (1) = SPACES
067400             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
067500             MOVE 'THIS COMMAND REQUIRES ONE PARAMETER'
067600             TO PDF-LONG-MESSAGE
067700             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
067800             MOVE SPACES TO PDF-TBDISPL-PANEL
067900             GO TO 1999-EXIT
068000         ELSE
068100         MOVE ZCMD-PARM (1) TO SPF-WORK-AREA
068200         IF   SPF-WORK-AREA-LAST NOT = SPACES
068300             MOVE 'PARAMETER TOO LONG' TO PDF-SHORT-MESSAGE
068400             MOVE 'THE PARAMETER MUST BE LESS THAN 4 BYTES LONG'
068500             TO PDF-LONG-MESSAGE
068600             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
068700             MOVE SPACES TO PDF-TBDISPL-PANEL
068800             GO TO 1999-EXIT
068900         ELSE
069000         IF SPF-WORK-AREA = 'DSN'
069100             MOVE '"DSN" IS RESERVED' TO PDF-SHORT-MESSAGE
069200             MOVE 'THE "DSN" LABEL IS A RESERVED SLOT AND MAY NOT
069300-            'BE USED'
069400             TO PDF-LONG-MESSAGE
069500             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
069600             MOVE SPACES TO PDF-TBDISPL-PANEL
069700             GO TO 1999-EXIT
069800         ELSE
069900         MOVE PDF-TABLE-ID TO WS-TABLE-ID
070000         MOVE SPF-WORK-AREA TO WS-TABLE-SUFFIX
070100         MOVE WS-TABLE-ID TO SPF-FROM-TABLE
070200         MOVE PDF-TABLE-ID TO SPF-TO-TABLE
070300         MOVE SPACES TO PDF-SELECT-BUFFER
070400         MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
070500         STRING 'TBSTATS ' DELIMITED BY SIZE
070600                SPF-FROM-TABLE DELIMITED BY SPACE
070700                ' STATUS1(ST1) LIBRARY(DSNMGF)'
070800                DELIMITED BY SIZE
070900             INTO PDF-SELECT-BUFFER
071000             WITH POINTER PDF-SELECT-BUFFER-LENGTH
071100         SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH
071200         CALL 'ISPEXEC' USING          PDF-SELECT-BUFFER-LENGTH
071300                                       PDF-SELECT-BUFFER
071400         IF SPF-STATS1 NOT = 1
071500             MOVE SPACES TO PDF-SHORT-MESSAGE
071600             STRING WS-TABLE-SUFFIX ' NOT FOUND'
071700             DELIMITED BY SIZE INTO PDF-SHORT-MESSAGE
071800             MOVE 'THE DSN LIST YOU REQUESTED DOES NOT EXIST'
071900             TO PDF-LONG-MESSAGE
072000             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
072100             MOVE SPACES TO PDF-TBDISPL-PANEL
072200             GO TO 1999-EXIT
072300         ELSE
072400         MOVE '(DSNAME VOLSER COMMENT PROFILE IMACRO)'
072500             TO PDF-NAME-LIST
072600         CALL 'ISPLINK' USING          PDF-TBEND
072700                                       SPF-TO-TABLE
072800         CALL 'ISPLINK' USING          PDF-TBCREATE
072900                                       SPF-TO-TABLE
073000                                       PDF-DUMMY-PARAMETER
073100                                       PDF-NAME-LIST
073200                                       PDF-WRITE
073300                                       PDF-REPLACE
073400                                       PDF-LIBRARY
073500                                       PDF-SHARE
073600         CALL 'ISPLINK' USING          PDF-TBOPEN
073700                                       SPF-FROM-TABLE
073800                                       PDF-NOWRITE
073900                                       PDF-LIBRARY
074000         CALL 'ISPLINK' USING          PDF-TBTOP
074100                                       SPF-FROM-TABLE
074200         PERFORM 6000-COPY-TABLE THRU 6099-EXIT
074300         CALL 'ISPLINK' USING          PDF-TBEND
074400                                       SPF-FROM-TABLE
074500         MOVE SPACES TO PDF-SHORT-MESSAGE
074600         MOVE +1 TO SPF-ZTDTOP
074700         STRING WS-TABLE-SUFFIX ' HAS BEEN COPIED' DELIMITED BY
074800             SIZE INTO PDF-SHORT-MESSAGE
074900         MOVE 'YOUR COPY COMMAND HAS COMPLETED SUCCESSFULLY'
075000         TO PDF-LONG-MESSAGE
075100         MOVE 'DTSM001W' TO PDF-TBDISPL-MESSAGE
075200         GO TO 1999-EXIT.
075300
075400******************************************************************
075500*    REPLACE PRIMARY COMMAND                                     *
075600*         ONE REQUIRED OPERAND - 3 BYTE DSNAME                   *
075700******************************************************************
075800
075900     IF ZCMD-COMMAND = 'REPLACE' OR 'REP' OR 'REPL'
076000         IF ZCMD-PARM-COUNT > 1
076100             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
076200             MOVE 'THIS COMMAND ONLY SUPPORTS ONE PARAMETER'
076300             TO PDF-LONG-MESSAGE
076400             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
076500             MOVE SPACES TO PDF-TBDISPL-PANEL
076600             GO TO 1999-EXIT
076700         ELSE
076800         IF ZCMD-PARM (1) = SPACES
076900             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
077000             MOVE 'THIS COMMAND REQUIRES ONE PARAMETER'
077100             TO PDF-LONG-MESSAGE
077200             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
077300             MOVE SPACES TO PDF-TBDISPL-PANEL
077400             GO TO 1999-EXIT
077500         ELSE
077600         MOVE ZCMD-PARM (1) TO SPF-WORK-AREA
077700         IF   SPF-WORK-AREA-LAST NOT = SPACES
077800             MOVE 'PARAMETER TOO LONG' TO PDF-SHORT-MESSAGE
077900             MOVE 'THE PARAMETER MUST BE LESS THAN 4 BYTES LONG'
078000             TO PDF-LONG-MESSAGE
078100             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
078200             MOVE SPACES TO PDF-TBDISPL-PANEL
078300             GO TO 1999-EXIT
078400         ELSE
078500         IF SPF-WORK-AREA = 'DSN'
078600             MOVE '"DSN" IS RESERVED' TO PDF-SHORT-MESSAGE
078700             MOVE 'THE "DSN" LABEL IS A RESERVED SLOT AND MAY NOT
078800-            'BE USED'
078900             TO PDF-LONG-MESSAGE
079000             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
079100             MOVE SPACES TO PDF-TBDISPL-PANEL
079200             GO TO 1999-EXIT
079300         ELSE
079400         MOVE PDF-TABLE-ID TO WS-TABLE-ID
079500         MOVE SPF-WORK-AREA TO WS-TABLE-SUFFIX
079600         MOVE WS-TABLE-ID TO SPF-TO-TABLE
079700         MOVE PDF-TABLE-ID TO SPF-FROM-TABLE
079800         MOVE SPACES TO PDF-SELECT-BUFFER
079900         MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
080000         STRING 'TBSTATS ' DELIMITED BY SIZE
080100                SPF-TO-TABLE DELIMITED BY SPACE
080200                ' STATUS1(ST1) LIBRARY(DSNMGF)'
080300                DELIMITED BY SIZE
080400             INTO PDF-SELECT-BUFFER
080500             WITH POINTER PDF-SELECT-BUFFER-LENGTH
080600         SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH
080700         CALL 'ISPEXEC' USING          PDF-SELECT-BUFFER-LENGTH
080800                                       PDF-SELECT-BUFFER
080900         MOVE '(DSNAME VOLSER COMMENT PROFILE IMACRO)'
081000             TO PDF-NAME-LIST
081100         CALL 'ISPLINK' USING          PDF-TBCREATE
081200                                       SPF-TO-TABLE
081300                                       PDF-DUMMY-PARAMETER
081400                                       PDF-NAME-LIST
081500                                       PDF-WRITE
081600                                       PDF-REPLACE
081700                                       PDF-LIBRARY
081800                                       PDF-SHARE
081900         CALL 'ISPLINK' USING          PDF-TBTOP
082000                                       PDF-TABLE-ID
082100         PERFORM 6000-COPY-TABLE THRU 6099-EXIT
082200         MOVE 50 TO PDF-FULLWORD
082300         CALL 'ISPLINK' USING          PDF-TBCLOSE
082400                                       SPF-TO-TABLE
082500                                       PDF-DUMMY-PARAMETER
082600                                       PDF-DUMMY-PARAMETER
082700                                       PDF-FULLWORD
082800                                       PDF-LIBRARY
082900         MOVE SPACES TO PDF-SHORT-MESSAGE
083000         IF SPF-STATS1 NOT = 1
083100         STRING WS-TABLE-SUFFIX ' HAS BEEN CREATED' DELIMITED BY
083200             SIZE INTO PDF-SHORT-MESSAGE
083300         MOVE 'YOUR REPLACE COMMAND HAS COMPLETED SUCCESSFULLY'
083400         TO PDF-LONG-MESSAGE
083500         MOVE 'DTSM001W' TO PDF-TBDISPL-MESSAGE
083600         GO TO 1999-EXIT
083700         ELSE
083800         STRING WS-TABLE-SUFFIX ' HAS BEEN REPLACED' DELIMITED BY
083900             SIZE INTO PDF-SHORT-MESSAGE
084000         MOVE 'YOUR REPLACE COMMAND HAS COMPLETED SUCCESSFULLY'
084100         TO PDF-LONG-MESSAGE
084200         MOVE 'DTSM001W' TO PDF-TBDISPL-MESSAGE
084300         GO TO 1999-EXIT.
084400
084500******************************************************************
084600*    SAVE   PRIMARY COMMAND                                      *
084700*         NO OPERANDS ALLOWED                                    *
084800******************************************************************
084900
085000     IF ZCMD-COMMAND = 'SAVE'
085100         IF ZCMD-PARM-COUNT > 0
085200             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
085300             MOVE 'THERE ARE NO VALID PARAMETERS FOR THIS COMMAND'
085400             TO PDF-LONG-MESSAGE
085500             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
085600             MOVE SPACES TO PDF-TBDISPL-PANEL
085700             GO TO 1999-EXIT
085800         ELSE
085900             MOVE SPACES TO PDF-TBDISPL-CURSOR
086000             MOVE 0 TO SPF-CSR
086100             MOVE 25 TO PDF-FULLWORD
086200             CALL 'ISPLINK' USING      PDF-TBSAVE
086300                                       PDF-TABLE-ID
086400                                       PDF-DUMMY-PARAMETER
086500                                       PDF-DUMMY-PARAMETER
086600                                       PDF-FULLWORD
086700                                       PDF-LIBRARY
086800             MOVE SPACES TO ZCMD-COMMAND-LINE
086900             GO TO 1999-EXIT.
087000
087100******************************************************************
087200*    CANCEL PRIMARY COMMAND                                      *
087300*         NO OPERANDS ALLOWED                                    *
087400******************************************************************
087500
087600     IF ZCMD-COMMAND = 'CANCEL'
087700         IF ZCMD-PARM-COUNT > 0
087800             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
087900             MOVE 'THERE ARE NO VALID PARAMETERS FOR THIS COMMAND'
088000             TO PDF-LONG-MESSAGE
088100             MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
088200             MOVE SPACES TO PDF-TBDISPL-PANEL
088300             GO TO 1999-EXIT
088400         ELSE
088500             MOVE SPACES TO PDF-TBDISPL-CURSOR
088600             MOVE 0 TO SPF-CSR
088700             CALL 'ISPLINK' USING      PDF-TBEND
088800                                       PDF-TABLE-ID
088900
089000* THIS IF STATEMENT SUPPORTS RECURSIVE INVOCATION
089100
089200             IF SPF-STATS2 = 5
089300                CALL 'ISPLINK' USING   PDF-TBOPEN
089400                                       PDF-TABLE-ID
089500                                       PDF-WRITE
089600                                       PDF-LIBRARY
089700                                       PDF-SHARE
089800             GO TO EXIT-PROGRAM-NOW
089900             ELSE GO TO EXIT-PROGRAM-NOW.
090000
090100 1990-INVALID-COMMAND.
090200
090300     MOVE 'ISPZ001' TO PDF-TBDISPL-MESSAGE.
090400     MOVE SPACES TO PDF-TBDISPL-PANEL.
090500
090600 1999-EXIT.
090700     EXIT.
090800/
090900 2000-BUILD-TABLE.
091000
091100******************************************************************
091200*    ISSUE TBSTATS TO DETERMINE THE STATUS OF THE CURRENT TABLE  *
091300******************************************************************
091400
091500     MOVE SPACES TO PDF-SELECT-BUFFER.
091600     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
091700     STRING 'TBSTATS ' DELIMITED BY SIZE
091800            PDF-TABLE-ID DELIMITED BY SPACE
091900            ' STATUS1(ST1) STATUS2(ST2) LIBRARY(DSNMGF)'
092000            DELIMITED BY SIZE
092100         INTO PDF-SELECT-BUFFER
092200         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
092300     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
092400                                       PDF-SELECT-BUFFER.
092500
092600     IF SPF-STATS1 = 3
092700         MOVE SPACES TO PDF-SHORT-MESSAGE
092800         MOVE 'DDNAME "DSNMGF" MUST BE ALLOCATED TO AN ISPF TABLE
092900-        'LIBRARY' TO PDF-LONG-MESSAGE
093000         MOVE 'DTSM001' TO PDF-NAME-LIST
093100         CALL 'ISPLINK' USING          PDF-SETMSG
093200                                       PDF-NAME-LIST
093300         MOVE SPACES TO PDF-SELECT-BUFFER
093400         MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
093500         STRING 'SELECT PGM(ISPTUTOR) PARM(DSNHMGF2)'
093600             DELIMITED BY SIZE
093700             INTO PDF-SELECT-BUFFER
093800             WITH POINTER PDF-SELECT-BUFFER-LENGTH
093900         CALL 'ISPEXEC' USING          PDF-SELECT-BUFFER-LENGTH
094000                                       PDF-SELECT-BUFFER
094100         GO TO EXIT-PROGRAM-NOW.
094200
094300     IF SPF-STATS2 NOT = 5 AND 1
094400         DISPLAY 'TABLE IS OPEN INCORRECTLY'
094500         DISPLAY 'STATUS CODE RETURNED = ' SPF-STATS2
094600         GO TO EXIT-PROGRAM-NOW.
094700
094800     IF SPF-STATS2 = 5
094900         GO TO 2999-EXIT.
095000
095100     IF SPF-STATS1 = 1 AND
095200        SPF-STATS2 NOT = 5
095300        CALL 'ISPLINK' USING           PDF-TBOPEN
095400                                       PDF-TABLE-ID
095500                                       PDF-WRITE
095600                                       PDF-LIBRARY
095700                                       PDF-SHARE
095800        GO TO 2999-EXIT.
095900
096000 2050-CREATE-TABLE.
096100     MOVE '(DSNAME VOLSER COMMENT PROFILE IMACRO)'
096200         TO PDF-NAME-LIST.
096300     CALL 'ISPLINK' USING              PDF-TBCREATE
096400                                       PDF-TABLE-ID
096500                                       PDF-DUMMY-PARAMETER
096600                                       PDF-NAME-LIST
096700                                       PDF-WRITE
096800                                       PDF-REPLACE
096900                                       PDF-LIBRARY
097000                                       PDF-SHARE.
097100
097200     CALL 'ISPLINK' USING              PDF-TBVCLEAR
097300                                       PDF-TABLE-ID.
097400
097500     CALL 'ISPLINK' USING              PDF-TBADD
097600                                       PDF-TABLE-ID.
097700
097800 2999-EXIT.
097900     EXIT.
098000/
098100 4000-LINE-COMMAND.
098200     CALL 'ISPLINK' USING              PDF-TBPUT
098300                                       PDF-TABLE-ID.
098400     IF SPF-LSEL = SPACES
098500         GO TO 4999-EXIT.
098600
098700     IF SPF-LSEL = 'I'
098800         CALL 'ISPLINK' USING          PDF-TBVCLEAR
098900                                       PDF-TABLE-ID
099000         CALL 'ISPLINK' USING          PDF-TBADD
099100                                       PDF-TABLE-ID
099200         MOVE SPACES TO SPF-LSEL
099300         ADD 1 TO SPF-CSR
099400         MOVE 'DSNAME' TO PDF-TBDISPL-CURSOR
099500         GO TO 4999-EXIT.
099600
099700     IF SPF-LSEL = 'R'
099800         CALL 'ISPLINK' USING          PDF-TBADD
099900                                       PDF-TABLE-ID
100000         MOVE SPACES TO SPF-LSEL
100100         ADD 1 TO SPF-CSR
100200         MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
100300         GO TO 4999-EXIT.
100400
100500     IF SPF-LSEL = 'D'
100600         CALL 'ISPLINK' USING          PDF-TBDELETE
100700                                       PDF-TABLE-ID
100800         MOVE SPACES TO SPF-LSEL
100900         SUBTRACT 1 FROM SPF-CSR
101000         GO TO 4999-EXIT.
101100
101200     IF SPF-LSEL = 'B'
101300         CALL 'ISPLINK' USING         PDF-CONTROL
101400                                      PDF-DISPLAY
101500                                      PDF-SAVE
101600         CALL 'ISPLINK' USING         PDF-CONTROL
101700                                      PDF-ERRORS
101800                                      PDF-RETURN
101900         CALL 'ISPLINK' USING         PDF-BROWSE
102000                                      SPF-DSNAME
102100                                      SPF-VOLSER
102200         MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE
102300         CALL 'ISPLINK' USING         PDF-CONTROL
102400                                      PDF-ERRORS
102500                                      PDF-CANCEL
102600         CALL 'ISPLINK' USING         PDF-CONTROL
102700                                      PDF-DISPLAY
102800                                      PDF-RESTORE
102900         IF PDF-BROWSE-RETURN-CODE NOT = 0
103000             MOVE +8 TO PDF-FULLWORD
103100             MOVE '(ZERRMSG)' TO PDF-NAME-LIST
103200             CALL 'ISPLINK' USING     PDF-VCOPY
103300                                      PDF-NAME-LIST
103400                                      PDF-FULLWORD
103500                                      PDF-TBDISPL-MESSAGE
103600                                      PDF-MOVE
103700             MOVE SPACE TO PDF-TBDISPL-PANEL
103800             MOVE SPF-CSR TO PDF-TBDISPL-ROW
103900             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
104000             GO TO 4999-EXIT
104100         ELSE
104200         MOVE SPACES TO SPF-LSEL
104300         GO TO 4999-EXIT.
104400
104500     IF SPF-LSEL = 'E'
104600         CALL 'ISPLINK' USING         PDF-CONTROL
104700                                      PDF-DISPLAY
104800                                      PDF-SAVE
104900         CALL 'ISPLINK' USING         PDF-CONTROL
105000                                      PDF-ERRORS
105100                                      PDF-RETURN
105200         CALL 'ISPLINK' USING         PDF-EDIT
105300                                      SPF-DSNAME
105400                                      SPF-VOLSER
105500                                      PDF-DUMMY-PARAMETER
105600                                      PDF-DUMMY-PARAMETER
105700                                      SPF-IMACRO
105800                                      SPF-PROFILE
105900         MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE
106000         CALL 'ISPLINK' USING         PDF-CONTROL
106100                                      PDF-ERRORS
106200                                      PDF-CANCEL
106300         CALL 'ISPLINK' USING         PDF-CONTROL
106400                                      PDF-DISPLAY
106500                                      PDF-RESTORE
106600         IF PDF-BROWSE-RETURN-CODE > 4
106700             MOVE +8 TO PDF-FULLWORD
106800             MOVE '(ZERRMSG)' TO PDF-NAME-LIST
106900             CALL 'ISPLINK' USING     PDF-VCOPY
107000                                      PDF-NAME-LIST
107100                                      PDF-FULLWORD
107200                                      PDF-TBDISPL-MESSAGE
107300                                      PDF-MOVE
107400             MOVE SPACE TO PDF-TBDISPL-PANEL
107500             MOVE SPF-CSR TO PDF-TBDISPL-ROW
107600             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
107700             GO TO 4999-EXIT
107800         ELSE
107900         MOVE SPACES TO SPF-LSEL
108000         CALL 'ISPLINK' USING          PDF-TBPUT
108100                                       PDF-TABLE-ID
108200             GO TO 4999-EXIT.
108300
108400
108500     MOVE '(ZSEL)' TO PDF-NAME-LIST.
108600     MOVE +256 TO PDF-SELECT-BUFFER-LENGTH
108700     CALL 'ISPLINK' USING             PDF-VCOPY
108800                                      PDF-NAME-LIST
108900                                      PDF-SELECT-BUFFER-LENGTH
109000                                      PDF-SELECT-BUFFER
109100                                      PDF-MOVE.
109200
109300     MOVE +3 TO PDF-FULLWORD.
109400     CALL 'ISPLINK' USING             PDF-CONTROL
109500                                      PDF-DISPLAY
109600                                      PDF-LINE
109700                                      PDF-FULLWORD.
109800     CALL 'ISPLINK' USING             PDF-CONTROL
109900                                      PDF-DISPLAY
110000                                      PDF-SAVE.
110100     CALL 'ISPLINK' USING             PDF-CONTROL
110200                                      PDF-ERRORS
110300                                      PDF-CANCEL.
110400     CALL 'ISPLINK' USING             PDF-SELECT
110500                                      PDF-SELECT-BUFFER-LENGTH
110600                                      PDF-SELECT-BUFFER.
110700     MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE.
110800     CALL 'ISPLINK' USING             PDF-CONTROL
110900                                      PDF-ERRORS
111000                                      PDF-RETURN.
111100     CALL 'ISPLINK' USING             PDF-CONTROL
111200                                      PDF-DISPLAY
111300                                      PDF-RESTORE.
111400
111500     CALL 'ISPLINK' USING              PDF-TBPUT
111600                                       PDF-TABLE-ID.
111700
111800     IF PDF-BROWSE-RETURN-CODE NOT = 0
111900         MOVE SPACES TO PDF-SHORT-MESSAGE
112000         MOVE PDF-BROWSE-RETURN-CODE TO WORK-NUM-DISPLAY
112100         STRING 'RETURN CODE = ' DELIMITED BY SIZE
112200                 WORK-NUM-DISPLAY DELIMITED BY SIZE
112300                 INTO PDF-SHORT-MESSAGE
112400         MOVE 'THE REQUESTED FUNCTION ENDED WITH A NON-ZERO RETURN
112500-        'CODE'
112600         TO PDF-LONG-MESSAGE
112700         MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
112800         MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
112900         MOVE SPACES TO PDF-TBDISPL-PANEL
113000         MOVE SPF-CSR TO PDF-TBDISPL-ROW.
113100
113200 4999-EXIT.
113300     EXIT.
113400/
113500 5000-VDEFINE.
113600
113700******************************************************************
113800*    THE FOLLOWING VDEFINES DO NOT MAKE USE OF VDEFINING         *
113900*    MULTIPLE VARIABLES WITH ONE CALL BECAUSE OF SOME OF THE     *
114000*    RESTRICTIONS PLACED ON A VS COBOL II PROGRAM THAT USES      *
114100*    THE COBOL II OPTIMIZER.                                     *
114200******************************************************************
114300
114400******************************************************************
114500*    ZTDTOP   -  IS THE ISPF VARIABLE FOR TABLE HANDELING        *
114600******************************************************************
114700
114800     MOVE '(ZTDTOP)' TO PDF-NAME-LIST.
114900     MOVE +4 TO PDF-FULLWORD.
115000     CALL 'ISPLINK' USING              PDF-VDEFINE
115100                                       PDF-NAME-LIST
115200                                       SPF-ZTDTOP
115300                                       PDF-FIXED
115400                                       PDF-FULLWORD.
115500
115600******************************************************************
115700*    CSR      -  IS THE ISPF VARIABLE FOR CURRENT CSR POSITION   *
115800******************************************************************
115900
116000     MOVE '(CSR)' TO PDF-NAME-LIST.
116100     MOVE +4 TO PDF-FULLWORD.
116200     CALL 'ISPLINK' USING              PDF-VDEFINE
116300                                       PDF-NAME-LIST
116400                                       SPF-CSR
116500                                       PDF-FIXED
116600                                       PDF-FULLWORD.
116700
116800******************************************************************
116900*    ROWNUM   -  IS THE ISPF VARIABLE FOR CURRENT ROW COUNT      *
117000******************************************************************
117100
117200     MOVE '(ROWNUM)' TO PDF-NAME-LIST.
117300     MOVE +4 TO PDF-FULLWORD.
117400     CALL 'ISPLINK' USING              PDF-VDEFINE
117500                                       PDF-NAME-LIST
117600                                       SPF-ROWNUM
117700                                       PDF-FIXED
117800                                       PDF-FULLWORD.
117900
118000******************************************************************
118100*    NUMROW   -  IS THE ISPF VARIABLE FOR CURRENT ROW COUNT      *
118200******************************************************************
118300
118400     MOVE '(NUMROW)' TO PDF-NAME-LIST.
118500     MOVE +4 TO PDF-FULLWORD.
118600     CALL 'ISPLINK' USING              PDF-VDEFINE
118700                                       PDF-NAME-LIST
118800                                       SPF-NUMROW
118900                                       PDF-FIXED
119000                                       PDF-FULLWORD.
119100
119200******************************************************************
119300*    ZCMD     -  ISPF COMMAND LINE - PRIMARY COMMANDS            *
119400******************************************************************
119500
119600     MOVE '(ZCMD)' TO PDF-NAME-LIST.
119700     MOVE +80 TO PDF-FULLWORD.
119800     CALL 'ISPLINK' USING              PDF-VDEFINE
119900                                       PDF-NAME-LIST
120000                                       ZCMD-COMMAND-LINE
120100                                       PDF-CHAR
120200                                       PDF-FULLWORD.
120300
120400******************************************************************
120500*    ST1      -  FIRST TABLE STATUS VARIABLE (TABLE EXIST?)      *
120600******************************************************************
120700
120800     MOVE '(ST1)' TO PDF-NAME-LIST.
120900     MOVE +1 TO PDF-FULLWORD.
121000     CALL 'ISPLINK' USING              PDF-VDEFINE
121100                                       PDF-NAME-LIST
121200                                       SPF-STATS1
121300                                       PDF-CHAR
121400                                       PDF-FULLWORD.
121500
121600******************************************************************
121700*    ST2      -  2ND TABLE STATUS VARIABLE (TABLE OPEN?)         *
121800******************************************************************
121900
122000     MOVE '(ST2)' TO PDF-NAME-LIST.
122100     MOVE +1 TO PDF-FULLWORD.
122200     CALL 'ISPLINK' USING              PDF-VDEFINE
122300                                       PDF-NAME-LIST
122400                                       SPF-STATS2
122500                                       PDF-CHAR
122600                                       PDF-FULLWORD.
122700
122800******************************************************************
122900*    VOLSER   -  THE VOLSER (PART OF THE TABLE)                  *
123000******************************************************************
123100
123200     MOVE '(VOLSER)' TO PDF-NAME-LIST.
123300     MOVE +6 TO PDF-FULLWORD.
123400     CALL 'ISPLINK' USING              PDF-VDEFINE
123500                                       PDF-NAME-LIST
123600                                       SPF-VOLSER
123700                                       PDF-CHAR
123800                                       PDF-FULLWORD.
123900
124000
124100******************************************************************
124200*    DSNAME   -  THE DSNAME (PART OF THE TABLE)                  *
124300******************************************************************
124400
124500     MOVE '(DSNAME)' TO PDF-NAME-LIST.
124600     MOVE +56 TO PDF-FULLWORD.
124700     CALL 'ISPLINK' USING              PDF-VDEFINE
124800                                       PDF-NAME-LIST
124900                                       SPF-DSNAME
125000                                       PDF-CHAR
125100                                       PDF-FULLWORD.
125200
125300******************************************************************
125400*    PROFILE  -  THE EDIT PROFILE (PART OF THE TABLE)            *
125500******************************************************************
125600
125700     MOVE '(PROFILE)' TO PDF-NAME-LIST.
125800     MOVE +8 TO PDF-FULLWORD.
125900     CALL 'ISPLINK' USING              PDF-VDEFINE
126000                                       PDF-NAME-LIST
126100                                       SPF-PROFILE
126200                                       PDF-CHAR
126300                                       PDF-FULLWORD.
126400
126500******************************************************************
126600*    IMACRO   -  THE EDIT IMACRO  (PART OF THE TABLE)            *
126700******************************************************************
126800
126900     MOVE '(IMACRO)' TO PDF-NAME-LIST.
127000     MOVE +8 TO PDF-FULLWORD.
127100     CALL 'ISPLINK' USING              PDF-VDEFINE
127200                                       PDF-NAME-LIST
127300                                       SPF-IMACRO
127400                                       PDF-CHAR
127500                                       PDF-FULLWORD.
127600
127700******************************************************************
127800*    DSMPPANL -  THE TBDISPL PANEL NAME FOR EXPAND OPTION        *
127900******************************************************************
128000
128100     MOVE '(DSMPPANL)' TO PDF-NAME-LIST.
128200     MOVE +8 TO PDF-FULLWORD.
128300     CALL 'ISPLINK' USING              PDF-VDEFINE
128400                                       PDF-NAME-LIST
128500                                       SPF-TBDISPL-PANEL
128600                                       PDF-CHAR
128700                                       PDF-FULLWORD.
128800
128900     MOVE SPACES TO PDF-SELECT-BUFFER.
129000     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
129100     STRING 'VGET (DSMPPANL) PROFILE'
129200         DELIMITED BY SIZE
129300         INTO PDF-SELECT-BUFFER
129400         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
129500     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
129600                                       PDF-SELECT-BUFFER.
129700
129800     IF SPF-TBDISPL-PANEL = SPACE
129900        MOVE 'DSNBMGF' TO SPF-TBDISPL-PANEL.
130000     MOVE SPF-TBDISPL-PANEL TO PDF-TBDISPL-PANEL.
130100
130200******************************************************************
130300*    LSEL     -  THE LINE COMMAND VARIABLE                       *
130400******************************************************************
130500
130600     MOVE '(LSEL)' TO PDF-NAME-LIST.
130700     MOVE +8 TO PDF-FULLWORD.
130800     CALL 'ISPLINK' USING              PDF-VDEFINE
130900                                       PDF-NAME-LIST
131000                                       SPF-LSEL
131100                                       PDF-CHAR
131200                                       PDF-FULLWORD.
131300
131400******************************************************************
131500*    UMSG1    -  SHORT (UPPER) MESSAGE FOR ERRORS                *
131600******************************************************************
131700
131800     MOVE '(UMSG1)' TO PDF-NAME-LIST.
131900     MOVE +24 TO PDF-FULLWORD.
132000     CALL 'ISPLINK' USING              PDF-VDEFINE
132100                                       PDF-NAME-LIST
132200                                       PDF-SHORT-MESSAGE
132300                                       PDF-CHAR
132400                                       PDF-FULLWORD.
132500
132600******************************************************************
132700*    LMSG1    -  LONG (LOWER) MESSAGE FOR ERRORS                 *
132800******************************************************************
132900
133000     MOVE '(LMSG1)' TO PDF-NAME-LIST.
133100     MOVE +78 TO PDF-FULLWORD.
133200     CALL 'ISPLINK' USING              PDF-VDEFINE
133300                                       PDF-NAME-LIST
133400                                       PDF-LONG-MESSAGE
133500                                       PDF-CHAR
133600                                       PDF-FULLWORD.
133700******************************************************************
133800*    MEMBER   -  FOR THE LIST COMMAND                            *
133900******************************************************************
134000
134100     MOVE '(MEMBER)' TO PDF-NAME-LIST.
134200     MOVE +8 TO PDF-FULLWORD.
134300     CALL 'ISPLINK' USING              PDF-VDEFINE
134400                                       PDF-NAME-LIST
134500                                       SPF-WORK-NAME
134600                                       PDF-CHAR
134700                                       PDF-FULLWORD.
134800     MOVE '(ID)' TO PDF-NAME-LIST.
134900     MOVE +3 TO PDF-FULLWORD.
135000     CALL 'ISPLINK' USING              PDF-VDEFINE
135100                                       PDF-NAME-LIST
135200                                       SPF-WORK-ID
135300                                       PDF-CHAR
135400                                       PDF-FULLWORD.
135500 5999-EXIT.
135600     EXIT.
135700/
135800 6000-COPY-TABLE.
135900     CALL 'ISPLINK' USING             PDF-TBSKIP
136000                                      SPF-FROM-TABLE.
136100     IF RETURN-CODE NOT = 0
136200         GO TO 6099-EXIT.
136300
136400     CALL 'ISPLINK' USING             PDF-TBADD
136500                                      SPF-TO-TABLE.
136600
136700     GO TO 6000-COPY-TABLE.
136800
136900 6099-EXIT.
137000     EXIT.
137100/
137200 6100-DEL.
137300     CALL 'ISPLINK' USING             PDF-TBSKIP
137400                                      PDF-TABLE-ID.
137500     IF RETURN-CODE NOT = 0
137600         GO TO 6199-EXIT.
137700
137800     CALL 'ISPLINK' USING             PDF-TBDELETE
137900                                      PDF-TABLE-ID.
138000
138100     GO TO 6100-DEL.
138200
138300 6199-EXIT.
138400     EXIT.
138500/
138600 7000-LIST-COMMAND.
138700
138800     CALL 'ISPLINK' USING             PDF-CONTROL
138900                                      PDF-DISPLAY
139000                                      PDF-SAVE.
139100
139200     MOVE SPACES TO PDF-SELECT-BUFFER.
139300     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
139400     STRING 'LMINIT DATAID(DSNMGF) DDNAME(DSNMGF)'
139500         DELIMITED BY SIZE
139600         INTO PDF-SELECT-BUFFER
139700         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
139800     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
139900     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
140000                                       PDF-SELECT-BUFFER.
140100
140200     MOVE '(DSNMGF)' TO PDF-NAME-LIST.
140300     MOVE 8 TO PDF-FULLWORD.
140400     CALL 'ISPLINK' USING              PDF-VCOPY
140500                                       PDF-NAME-LIST
140600                                       PDF-FULLWORD
140700                                       SPF-THE-TABLE
140800                                       PDF-MOVE.
140900
141000     MOVE SPACES TO PDF-SELECT-BUFFER.
141100     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
141200     STRING 'LMOPEN DATAID(&DSNMGF)'
141300         DELIMITED BY SIZE
141400         INTO PDF-SELECT-BUFFER
141500         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
141600     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
141700     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
141800                                       PDF-SELECT-BUFFER.
141900
142000     MOVE SPACES TO PDF-SELECT-BUFFER.
142100     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
142200     STRING 'TBCREATE &DSNMGF NAMES(ID TABLEUPD USER NUMROW CDATE'
142300         ' CTIME) NOWRITE REPLACE'
142400         DELIMITED BY SIZE
142500         INTO PDF-SELECT-BUFFER
142600         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
142700     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
142800     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
142900                                       PDF-SELECT-BUFFER.
143000
143100     PERFORM 7100-GEN-TABLE THRU 7199-EXIT.
143200
143300     MOVE SPACES TO PDF-SELECT-BUFFER.
143400     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
143500     STRING 'LMCLOSE DATAID(&DSNMGF)'
143600         DELIMITED BY SIZE
143700         INTO PDF-SELECT-BUFFER
143800         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
143900     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
144000     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
144100                                       PDF-SELECT-BUFFER.
144200
144300     MOVE SPACES TO PDF-SELECT-BUFFER.
144400     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
144500     STRING 'LMFREE DATAID(&DSNMGF)'
144600         DELIMITED BY SIZE
144700         INTO PDF-SELECT-BUFFER
144800         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
144900     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
145000     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
145100                                       PDF-SELECT-BUFFER.
145200
145300     IF PDF-TBDISPL-MESSAGE NOT = SPACES
145400         GO TO 7099-EXIT.
145500
145600     MOVE 'DSNBMGFL' TO SPF-TEMP-PANEL.
145700     MOVE SPF-THE-TABLE TO  SPF-TEMP-TABLE.
145800     PERFORM 7200-TBDISPL THRU 7299-EXIT.
145900 7099-EXIT.
146000     MOVE SPACES TO PDF-SELECT-BUFFER.
146100     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
146200     STRING 'TBEND &DSNMGF'
146300         DELIMITED BY SIZE
146400         INTO PDF-SELECT-BUFFER
146500         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
146600     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
146700     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
146800                                       PDF-SELECT-BUFFER.
146900
147000     CALL 'ISPLINK' USING             PDF-CONTROL
147100                                      PDF-DISPLAY
147200                                      PDF-RESTORE.
147300
147400
147500
147600 7100-GEN-TABLE.
147700
147800     MOVE SPACES TO PDF-SELECT-BUFFER.
147900     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
148000     STRING 'LMMLIST DATAID(&DSNMGF) MEMBER(MEMBER)'
148100         DELIMITED BY SIZE
148200         INTO PDF-SELECT-BUFFER
148300         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
148400     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
148500     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
148600                                       PDF-SELECT-BUFFER.
148700
148800     MOVE RETURN-CODE TO COMPLETION-CODE.
148900     IF COMPLETION-CODE = 4
149000         MOVE 'NO DSNLIST FOUND' TO PDF-SHORT-MESSAGE
149100         MOVE 'THERE ARE NO SAVED DSNAME LISTS AT THIS TIME'
149200         TO PDF-LONG-MESSAGE
149300         MOVE 'DTSM001' TO PDF-TBDISPL-MESSAGE
149400         MOVE SPACES TO PDF-TBDISPL-PANEL
149500         GO TO 7199-EXIT.
149600
149700     IF COMPLETION-CODE = 8
149800         GO TO 7199-EXIT.
149900
150000     IF PDF-TABLE-ID-PREFACE NOT = SPF-WORK-USER
150100         AND ZCMD-COMMAND NOT = 'FLIST'
150200         GO TO 7100-GEN-TABLE.
150300
150400     MOVE SPACES TO PDF-SELECT-BUFFER.
150500     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
150600     STRING 'TBSTATS ' SPF-WORK-NAME
150700            ' CDATE(CDATE) CTIME(CTIME) USER(USER)'
150800            ' ROWCURR(NUMROW) LIBRARY(DSNMGF)'
150900            DELIMITED BY SIZE
151000         INTO PDF-SELECT-BUFFER
151100         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
151200     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
151300     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
151400                                       PDF-SELECT-BUFFER.
151500
151600     MOVE SPACES TO PDF-SELECT-BUFFER.
151700     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
151800     STRING 'TBADD &DSNMGF'
151900         DELIMITED BY SIZE
152000         INTO PDF-SELECT-BUFFER
152100         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
152200     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
152300     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
152400                                       PDF-SELECT-BUFFER.
152500
152600     GO TO 7100-GEN-TABLE.
152700
152800 7199-EXIT.
152900     EXIT.
153000/
153100 7200-TBDISPL.
153200
153300     MOVE SPACES TO PDF-SELECT-BUFFER.
153400     MOVE +1 TO PDF-SELECT-BUFFER-LENGTH.
153500     STRING 'TBTOP &DSNMGF'
153600         DELIMITED BY SIZE
153700         INTO PDF-SELECT-BUFFER
153800         WITH POINTER PDF-SELECT-BUFFER-LENGTH.
153900     SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH.
154000     CALL 'ISPEXEC' USING              PDF-SELECT-BUFFER-LENGTH
154100                                       PDF-SELECT-BUFFER.
154200
154300     CALL 'ISPLINK' USING             PDF-TBDISPL
154400                                      SPF-TEMP-TABLE
154500                                      SPF-TEMP-PANEL.
154600     MOVE RETURN-CODE TO COMPLETION-CODE.
154700     MOVE SPF-THE-TABLE TO SPF-TEMP-TABLE.
154800     MOVE 'DSNBMGFL' TO SPF-TEMP-PANEL.
154900
155000     IF COMPLETION-CODE > 4
155100         GO TO 7299-EXIT.
155200
155300     MOVE 'CSR' TO PDF-VARIABLE-NAME.
155400     CALL 'ISPLINK' USING              PDF-TBQUERY
155500                                       SPF-TEMP-TABLE
155600                                       PDF-DUMMY-PARAMETER
155700                                       PDF-DUMMY-PARAMETER
155800                                       PDF-DUMMY-PARAMETER
155900                                       PDF-DUMMY-PARAMETER
156000                                       PDF-DUMMY-PARAMETER
156100                                       PDF-VARIABLE-NAME.
156200
156300     IF SPF-CSR NOT = 0
156400     IF SPF-LSEL = 'DEL'
156500         MOVE SPACES TO PDF-SELECT-BUFFER
156600         MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
156700         MOVE PDF-TABLE-ID-PREFACE TO SPF-WORK-USER
156800         STRING 'TBERASE ' DELIMITED BY SIZE
156900                SPF-WORK-NAME DELIMITED BY SPACE
157000                ' LIBRARY(DSNMGF)'
157100                DELIMITED BY SIZE
157200             INTO PDF-SELECT-BUFFER
157300             WITH POINTER PDF-SELECT-BUFFER-LENGTH
157400         SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH
157500         CALL 'ISPEXEC' USING          PDF-SELECT-BUFFER-LENGTH
157600                                       PDF-SELECT-BUFFER
157700
157800         MOVE SPACES TO PDF-SELECT-BUFFER
157900         MOVE +1 TO PDF-SELECT-BUFFER-LENGTH
158000         MOVE PDF-TABLE-ID-PREFACE TO SPF-WORK-USER
158100         STRING 'TBDELETE &DSNMGF'
158200                DELIMITED BY SIZE
158300             INTO PDF-SELECT-BUFFER
158400             WITH POINTER PDF-SELECT-BUFFER-LENGTH
158500         SUBTRACT 1 FROM PDF-SELECT-BUFFER-LENGTH
158600         CALL 'ISPEXEC' USING          PDF-SELECT-BUFFER-LENGTH
158700                                       PDF-SELECT-BUFFER
158800         MOVE SPACES TO SPF-LSEL
158900         IF COMPLETION-CODE = 4
159000             MOVE SPACES TO SPF-TEMP-PANEL
159100             GO TO 7200-TBDISPL
159200         ELSE
159300             GO TO 7200-TBDISPL.
159400
159500
159600
159700     IF COMPLETION-CODE = 4
159800         MOVE SPACES TO SPF-TEMP-PANEL
159900         GO TO 7200-TBDISPL.
160000 7299-EXIT.
160100     EXIT.
