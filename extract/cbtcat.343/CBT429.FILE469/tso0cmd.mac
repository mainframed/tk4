TSO0CMD  TITLE ' - INVOKE OPTION 6 FROM THE PRIMARY OPTION PANEL'
***********************************************************************
*    BOB ZIMMERMAN                                                    *
*    CNA INSURANCE                                                    *
*    (312) 822-5719                                                   *
*    LAST MODIFIED 8504                                               *
***********************************************************************
*   THIS COMMAND SUPPORTS THE TSOCMD COMMAND TABLE COMMAND.
*
*   THIS COMMAND ALLOWS A USER TO TYPE TSOCMD FROM ANY SCREEN AND BE
*   PRESENTED THE SAME FUNCTION AS IF HE TYPED OPTION 6 FROM THE PDF
*   PRIMARY OPTION MENU. WHEN THE USER EXITS THE FUNCTION, HE RETURNS
*   TO THE SAME FUNCTION AS WHEN HE TYPED THE TSOCMD COMMAND.
***********************************************************************
* VERSION 2 MODIFICATIONS:                                            *
*    THIS PROGRAM WILL NOW ACCEPT PARMS. TSOCMD CAN BE INVOKED WITH   *
*    THE PASSING OF PARMS. FOR EXAMPLE:                               *
*                                                                     *
*    TSOCMD < A TSO COMMAND >                                         *
*                                                                     *
*    THE TSO COMMAND WILL BE INSERTED ON THE TSO COMMAND LINE OF      *
*    OPTION 6 AND WILL BE INVOKED FOR YOU.                            *
*                                                                     *
* ***NOTE THIS REQUIRES THE NEW TSO COMMAND ON THIS TAPE BE           *
*    INSTALLED.                                                       *
***********************************************************************
         SPACE
TSO0CMD  CSECT
         ISPF  SET,LEVEL=1
***********************************************************************
*        STANDARD ENTRY CODE
***********************************************************************
         USING *,15                     TEMPORARY BASE REGISTER
         B     PASSLIT                  BRANCH AROUND LITERALS
         DC    C'TSO0CMD '              CSECT NAME
         DC    C'02/21/85 '             DATE WRITTEN
         DC    C'VERSION 2.0 '          VERSION
         DC    C'&SYSDATE '             DATE ASSEMBLED
         DC    C'&SYSTIME '             TIME ASSEMBLED
PASSLIT  DS    0H
         STM   14,12,12(13)             SAVE CALLERS REGISTERS
         LR    11,1                     SAVE PARAMETERS PASSED
         LR    12,15                    REGISTER 12 IS BASE REGISTER
         DROP  15                       END TEMPORARY ADDRESSING
         USING TSO0CMD,12               PERMANENT ADDRESSING
         SPACE
         GETMAIN R,LV=WSLEN,SP=0        GET WORKING STORAGE AREA
         SPACE
         ST    1,8(13)                  SET FORWARD CHAIN IN SAVEAREA
         ST    13,4(1)                  SET BACKWARD CHAIN
         LM    13,1,8(13)               RESTORE CLOBBERED REGISTERS
         USING WORKSTOR,13              SETUP WORKING STORAGE
         SPACE 3
***********************************************************************
* INITALIZATION INCLUDES:
*        LOAD ISPLINK
*        SET ISPF ERROR MODE
*        SET ISPEXEC CONTROL NONDISPL END MODE
*        SET ZCMD OPTION TO 6
***********************************************************************
         ISPF  LOAD                     LOAD ISPLINK
         SPACE
         CONTROL ERRORS,                SET ISPF ERROR MODE            *
               CANCEL,                  CANCEL ANY RC OF 12 OR GREATER *
               MF=E                     EXECUTE FORM OF MACRO
         SPACE
         CONTROL NONDISPL,              DONT DISPLAY NEXT SCREEN       *
               END,                     SIMULATE THE END COMMAND       *
               MF=E                     EXECUTE FORM OF MACRO
         SPACE
         VREPLACE '(ZCMD)',             ZCMD IN FUNCTION POOL          *
               1,                       1 BYTE LONG                    *
               =C'6',                   SET TO VALUE OF 6              *
               MF=E                     EXECUTE FORM OF MACRO
         SPACE 3
***********************************************************************
* PROCESSING INCLUDES:
*        DISPLAY PRIMARY OPTION MENU ISR@PRIM
*        THIS PANEL WILL NOT BE DISPLAYED BECAUSE OF PREVIOUS NONDISPL
*        VCOPY THE ZSEL VALUE SET IN PROC SECTION OF ISR@PRIM
*        INVOKE THE COMMAND COPIED
***********************************************************************
         DISPLAY 'ISR@PRIM',            DISPLAY PANEL ISR@PRIM         *
               MF=E                     EXECUTE FORM OF MACRO
         SPACE
         VCOPY '(ZSEL)',                COPY ZSEL VARIABLE             *
               VCOPYLEN,                LENGTH OF COMMAND WILL BE SET  *
               ZSEL,                    ADDRESS OF COMMAND WILL BE SET *
               LOCATE,                  VCOPY LOCATE MODE - NOT MOVE   *
               MF=E                     EXECUTE FORM OF MACRO
         SPACE
         L     2,ZSEL                   GET ADDRESS OF COMMAND
         L     11,0(11)                 GET ANY PARMS PASSED
         LH    10,0(11)                 GET LENGTH OF PARMS
         LTR   10,10                    IF NO PARMS
         BZ    SELIT                    BYPASS THIS
         C     10,=F'255'               TOO MANY PARMS?
         BH    SELIT                    IF SO BYPASS
         ST    10,VREPLEN               SAVE LENGTH
         VREPLACE '(TSOSCMD)',VREPLEN,2(11),MF=E
         SPACE
         VPUT  '(TSOSCMD)',SHARED
         SPACE
SELIT    SELECT VCOPYLEN,               ISPEXEC SELECT THE COMMAND     *
               (2),                     POINTED TO BY REGISTER 2       *
               MF=E                     EXECUTE FORM OF MACRO
         SPACE 3
         LR    11,15                    SAVE RETURN CODE
***********************************************************************
* EXIT CODE INCLUDES:
*        DELETE ISPF VARIABLES
*        DELETE ISPLINK
*        STANDARD EXIT CODE
***********************************************************************
         VRESET MF=E                    DELETE FUNCTION POOL VARIABLES
         SPACE
         ISPF  DELETE                   DELETE ISPLINK MODULE
         SPACE
         LR    1,13                     GET ADDRESS FOR FREEMAIN
         L     13,WORKSTOR+4            GET CALLERS SAVE AREA
         FREEMAIN R,A=(1),LV=WSLEN,SP=0 FREE WORKING STORAGE
         SPACE
         LR    15,11                    GET RETURN CODE
         L     14,12(,13)               RESTORE RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REMAINING REGISTERS
         MVI   12(13),X'FF'             FLAG AS RETURNED FOR SNAP SVC
         BR    14                       RETURN TO CALLER
         TITLE ' - WORKING STORAGE'
WORKSTOR DSECT                          WORKING STORAGE DSECT
SAVEAREA DS    18F                      PROGRAM SAVE AREA
         DS    0D                       DOUBLE WORD ALIGN
         SPACE
VCOPYLEN DS    F                        LENGTH OF OPTION 6 COMMAND
VREPLEN  DS    F                        LENGTH OF OPTION 6 COMMAND
ZSEL     DS    A                        ADDRESS OF OPTION 6 COMMAND
         ISPF  PARMS                    ISPF WORKING STORAGE
         DS    0D
WSLEN    EQU   *-WORKSTOR               LENGTH OF WORKING STORAGE
TSO0CMD  CSECT ,                        RESUME CSECT
         END   TSO0CMD
