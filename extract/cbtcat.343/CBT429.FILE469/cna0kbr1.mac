CNA0KBR1 TITLE ' - PARSE PARAMETERS FOR KBR COMMAND'
***********************************************************************
*    BOB ZIMMERMAN                                                    *
*    CNA INSURANCE                                                    *
*    (312) 822- 5719                                                  *
*    LAST MODIFIED 8503                                               *
*                                                                     *
***********************************************************************
*                                                                     *
*    THIS PROGRAM PASSES THE USERS INPUT TO THE KBR (BROWSE BY DDNAME)*
*    ISPF COMMAND.                                                    *
***********************************************************************
         EJECT
CNA0KBR1 CSECT
***********************************************************************
*        STANDARD ENTRY CODE FOR A RE-ENTRANT PROGRAM
*                 REGISTER 12 IS ONLY BASE REGISTER
***********************************************************************
         USING *,15                     TEMPORARY BASE REGISTER
         B     PASSLIT                  BRANCH AROUND LITERALS
         DC    C'CNA0KBR1 '             CSECT NAME
         DC    C'03/21/85 '             DATE WRITTEN
         DC    C'VERSION 1.1 '          VERSION NUMBER
         DC    C'&SYSDATE '             DATE ASSEMBLED
         DC    C'&SYSTIME '             TIME ASSEMBLED
PASSLIT  DS    0H
         STM   14,12,12(13)             SAVE CALLERS REGISTERS
         LR    11,1                     SAVE PARAMETERS PASSED
         LR    12,15                    REGISTER 12 IS BASE REGISTER
         DROP  15                       END TEMPORARY ADDRESSING
         USING CNA0KBR1,12              PERMANENT ADDRESSING
         SPACE
         GETMAIN R,LV=WSLEN,SP=0        GET WORKING STORAGE AREA
         SPACE
         ST    1,8(13)                  SET FORWARD CHAIN IN SAVEAREA
         ST    13,4(1)                  SET BACKWARD CHAIN
         LM    13,1,8(13)               RESTORE CLOBBERED REGISTERS
         USING WORKSTOR,13              SETUP WORKING STORAGE
         SPACE 3
         USING CPPL,4                   GET REG FOR CPPL
         USING PPL,5                    GET REG FOR PPL
         USING IKJPARMD,6               GET REGISTER FOR PARSE CSECT
         SPACE 3
         TITLE ' - GET CPPL AND ISSUE REQUEST PARSE'
         LR    4,1                      GET CPPL ADDRESS
         LA    5,PPLAREA                GRAB PPL POINTER (BUILT HERE)
         L     6,=A(PARSECT)            GRAP PARSE CSECT ADDRESS
         MVC   PPLCBUF,CPPLCBUF         MOVE CBUF PT FROM CPPL TO PPL
         MVC   PPLUPT,CPPLUPT           MOVE UPT PT FROM CPPL TO PPL
         ST    6,PPLPCL                 SAVE PARSECT ADDRESS IN PPL
         MVC   PPLECT,CPPLECT           MOVE ECT PT FROM CPPL TO PPL
         LA    2,CPECB                  GET ECB ADDRESS
         ST    2,PPLECB                 STORE ADDRESS INTO PPL
         LA    2,CPANS                  GET ANSWER AREA ADDRESS
         ST    2,PPLANS                 STORE ADDRESS INTO PPL
         LA    2,CPUWA                  GET UWA ADDRESS
         ST    2,PPLUWA                 STORE ADDRESS INTO PPL
         SPACE
         CALLTSSR EP=IKJPARS,           CALL THE PARSE ROUTINE         *
               MF=(E,(5))               POINTER TO THE PPL
         SPACE
         LTR   15,15                    IF PARSE ROUTINE FAILED
         BNZ   PARSERR
         DROP  5,4                      FREE REGISTERS
         TITLE ' - MOVE PARSE PARMS INTO LINKAGE SECTION'
         USING DSNPDE,5                 GET PANEL REGISTER
         SPACE
         L     6,CPANS                  GET POINTER TO PARSE RETURN
*
         LA    5,DSN                    POINT TO DSNAME AREA
         MVC   0(4,3),PDEADDR           GET DSN ADDRESS
         MVC   4(2,3),=H'0'             MOVE ZERO TO TOP OF FULLWORD
         MVC   6(2,3),PDEL              GET DSNAME LENGTH
         MVC   8(8,3),SPACES            MOVE SPACES TO MEMBER NAME
         LH    2,PDELM                  GET MEMBER NAME LENGTH
         LTR   2,2                      WAS A MEMBER SPECIFIED
         BZ    NOMEM                    IF NOT - BYPASS MEM LOGIC
         BCTR  2,0                      BUMP DOWN FOR MVC
         L     14,PDEADDRM              GET ADDRESS OF MEMBER
         MVC   8(0,3),0(14)             DUMMY MVC FOR EX INSTRUCTION
         EX    2,*-6                    MOVE THE MEMBER NAME
NOMEM    DS    0H                       END OF DSN CODE
*
         LA    5,KFIRST                 GET POINTER TO FIRST KEYWORD
         MVC   16(2,3),=H'0'            SET FIRST HALFWORD TO 0
         MVC   18(2,3),0(5)             MOVE THE FIRST KEYWORD VALUE
*
         DROP  5                        FREE REGISTER
         SPACE
         LR    1,13                     GET ADDRESS FOR FREEMAIN
         L     13,WORKSTOR+4            GET CALLERS SAVE AREA
         FREEMAIN R,A=(1),LV=WSLEN,SP=0 FREE WORKING STORAGE
         SPACE
         L     14,12(,13)               RESTORE RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REMAINING REGISTERS
         MVI   12(13),X'FF'             FLAG AS RETURNED FOR SNAP SVC
         BR    14                       RETURN TO CALLER
         TITLE ' - SUBROUTINE PARSERR -  PARSE ENDED WITH NON-ZERO CC'
PARSERR  DS    0H
         LR    10,15
         TPUTE 'PARSE ENCOUNTED SEVERE ERROR'
         ABEND (10),DUMP
         TITLE ' - WORKING STORAGE'
SPACES   DC    CL80' '                  SPACE LITERAL
         SPACE
WORKSTOR DSECT                          WORKING STORAGE DSECT
SAVEAREA DS    18F                      PROGRAM SAVE AREA
         DS    0D                       DOUBLE WORD ALIGN
PARMLIST DS    F                        PARMS FOR CALL OF CPPLRET
CPECB    DS    F                        ECB FOR THE PPL
CPANS    DS    F                        ANSWER AREA FOR THE PARSE
CPUWA    DS    F                        USER WORK AREA
PPLAREA  DS    0F,CL(PPLLEN)            FULLWORD ALIGN PPL
         DS    0D                       EXTEND TO DOUBLWORD BOUNDRY
WSLEN    EQU   *-WORKSTOR               LENGTH OF WORKING STORAGE
CNA0KBR1 CSECT ,                        RESUME CSECT
         TITLE ' - IKJPARSE CONTROL BLOCKS'
         PRINT NOGEN
PARSECT  IKJPARM
         SPACE
DSN      IKJPOSIT DSNAME,               FIRST FIELD REQUIRED           *
               PROMPT='A DDNAME(MEMBER) - MEMBER IS OPTIONAL',         *
               HELP='A DDNAME(MEMBER) - MEMBER IS OPTIONAL',           *
               VALIDCK=DSNEXIT
         SPACE
KFIRST   IKJKEYWD
         IKJNAME 'FIRST'
         SPACE
         IKJENDP
         PRINT GEN
         TITLE ' - CVT DSECT'
         CVT   DSECT=YES
         TITLE ' - CPPL AND PPL DSECT'
         DS    0H
         IKJCPPL ,
         SPACE 3
         DS    0F                       FULLWORD ALIGNMENT
         IKJPPL ,                       PPL DSECT
PPLLEN   EQU   *-PPL                    PPL LENGTH
         TITLE ' - PDE DSECTS AND LINKAGE SECTION'
DSNPDE   DSECT                          PDE DSECT
PDEADDR  DS    A                        ADDRESS OF DATA
PDEL     DS    H                        LENGTH OF DATA
PDEFLAGS DS    X                        PDE FLAG
         DS    X                        FILLER BYTE
PDEADDRM DS    A                        ADDRESS OF DATA
PDELM    DS    H                        LENGTH OF DATA
PDEFLAGM DS    X                        PDE FLAG
         DS    X                        FILLER BYTE
         SPACE 3
CNA0KBR1 CSECT ,
DSNEXIT  DS    0H                       PARSE EXIT ROUTINE
         USING DSNEXIT,15               SET UP ADDRESSABILITY
         L     1,0(1)                   GET PDE
         USING DSNPDE,1                 SET UP ADDRESSABILITY
         CLC   PDEL,=H'8'               IS LENGTH > 8
         BH    DSNERR                   IF SO - ERROR
         SR    15,15                    ELSE SET RC TO 0
         BR    14                       EXIT
DSNERR   DS    0H                       ERROR ROUTINE
         LA    15,4                     SET RC TO 4
         BR    14                       EXIT
         DROP  15,1                     FREE REGISTERS
         END   CNA0KBR1
