JOB0LIST TITLE ' - LIST ACTIVE JOBS - ISPF INTERFACE'
*OB0LIST AMODE 31
JOB0LIST CSECT
***********************************************************************
*        STANDARD ENTRY CODE FOR A RE-ENTRANT PROGRAM
*                 REGISTER 12 IS ONLY BASE REGISTER
***********************************************************************
         USING *,15                     TEMPORARY BASE REGISTER
         B     PASSLIT                  BRANCH AROUND LITERALS
         DC    C'JOB0LIST '             CSECT NAME
         DC    C'04/23/85 '             DATE WRITTEN
         DC    C'VERSION 1.0 '          VERSION NUMBER
         DC    C'&SYSDATE '             DATE ASSEMBLED
         DC    C'&SYSTIME '             TIME ASSEMBLED
PASSLIT  DS    0H
         STM   14,12,12(13)             SAVE CALLERS REGISTERS
         LR    11,1                     SAVE PARAMETERS PASSED
         LR    12,15                    REGISTER 12 IS BASE REGISTER
         DROP  15                       END TEMPORARY ADDRESSING
         USING JOB0LIST,12              PERMANENT ADDRESSING
         SPACE
         GETMAIN R,LV=WSLEN,SP=0        GET WORKING STORAGE AREA
         SPACE
         ST    1,8(13)                  SET FORWARD CHAIN IN SAVEAREA
         ST    13,4(1)                  SET BACKWARD CHAIN
         LM    13,1,8(13)               RESTORE CLOBBERED REGISTERS
         USING WORKSTOR,13              SETUP WORKING STORAGE
         SPACE 3
         EJECT
         ISPF  LOAD                LOAD ISPLINK
         SPACE
         CONTROL ERRORS,CANCEL,MF=E
         SPACE
         VDEFINE '(ZTDTOP ZTDROWS ROWID)',ZTDTOP,FIXED,4,MF=E
         SPACE 3
         VDEFINE '(AUTOREQ JOBFTSO OSW)',AUTOREQ,CHAR,1,'(COPY)',MF=E
         SPACE 3
         VDEFINE '(JOBPLMT)',LIMIT,CHAR,8,'(COPY)',MF=E
         SPACE 3
         XC    ZTDTOP,ZTDTOP
         XC    ROWID,ROWID
         MVI   AUTOMODE,C'0'       SET AUTOMODE INITALLY OFF
         MVI   AUTOREQ,C'0'        SET AUTOMODE REQUEST OFF
         MVC   TABLE,=CL8'JOBT'    BEGIN TABLE NAME
         TIME  ,                   GET CURRENT TIME
         SPACE
         ST    0,TABLE+4           BUILD TABLE NAME WITH TIME
         MVC   VCOPYLEN,=F'14'
         VDEFINE '(JOBPSORT)',JOBPSORT,CHAR,14,'(COPY)',MF=E
         SPACE
         CLC   JOBPSORT,=CL14' '
         BNE   LOOP
         MVC   JOBPSORT,=CL14'(JOBNAME,C,A)'
         SPACE 3
LOOP     DS    0H
         TBCREATE TABLE,           BUILD THE TABLE                     *
               NAMES='(JOBNAME STEPNAME PROCNAME JOBHH CPUTIME MEMSTAT1*
               )',REPLACE='REPLACE',MF=E
         SPACE
         TBSORT TABLE,FIELDS=JOBPSORT,MF=E
         SPACE
         CALL  JOB1LIST,(TABLE),MF=(E,SPFPARMS)
         SPACE
         TBTOP TABLE,MF=E
         SPACE
         VREPLACE '(JOBNAME)',=F'8',JOBNAME,MF=E
         SPACE
         TBSCAN TABLE,ARGLIST='(JOBNAME)',MF=E
         SPACE
         CLI   AUTOMODE,C'1'       ARE WE IN AUTOMODE
         BNE   DISPLIT             IF NOT GO TBDISPL
         CONTROL DISPLAY,LOCK      LOCK KEYBOARD
         SPACE
DISPLIT  DS    0H
         TBDISPL TABLE,PANEL='JOBBLIST',MF=E
         SPACE
         LTR   15,15               WAS END COMMAND ENTERED
         BNZ   EXITPGM
         TBTOP TABLE,MF=E
         SPACE
         CLC   ZTDTOP,=F'1'
         BNH   SPJOB
         TBSKIP TABLE,NUMBER=ZTDTOP,MF=E
         SPACE
         MVC   VCOPYLEN,=F'8'
         VCOPY '(JOBNAME)',VCOPYLEN,JOBNAME,MOVE,MF=E
         SPACE
         CLI   OSW,C'1'
         BNE   Y100
         MVI   OSW,C'0'
SPJOB    XC    JOBNAME,JOBNAME
Y100     DS    0H
         CLI   AUTOMODE,C'1'       ARE WE IN AUTOMODE
         BNE   CONTINUE            IF NOT CONTINUE
         CLI   PA1HIT,C'1'         WAS PA1 HIT
         BE    ATTNHIT             IF NOT KEEP DISPLAYING
         TIME  BIN,                TAKE TIME READING
         C     0,TIMELIM           IS AUTOMODE LIMIT UP
         BL    LOOP                IF NOT UP - KEEP GOING
         BAL   14,TURNOFF          TURN OF STAX AND PA1 SWITCH
ATTNHIT  MVI   AUTOMODE,C'0'       SET AUTOMODE OFF
         CONTROL DISPLAY,REFRESH   REFRESH THE SCREEN
         SPACE
         B     LOOP                RETURN
         SPACE
CONTINUE DS    0H                  NOT AUTOMODE
         CLI   AUTOREQ,C'1'        AUTOMODE REQUESTED
         BNE   LOOP                GOTO LOOP IF NOT
         MVI   PA1HIT,C'0'         SET PA1 HIT SWITCH TO OFF
         MVI   AUTOMODE,C'1'       SET AUTOMODE ON
         TIME  BIN                 GET TIME OF DAY
         LR    2,0                 SAVE TIME OF DAY
         A     2,=F'6000'          BUMP BY 1 MINUTE
         ST    2,TIMELIM           SAVE TIME LIMIT OF 1 MINUTE OF AUTO
         STM   11,13,STAXREGS
STAXIT   STAX  ATTNEXIT,                                               *
               REPLACE=YES,                                            *
               IBUF=0,                                                 *
               OBUF=0,                                                 *
               USADDR=STAXREGS,                                        *
               MF=(E,STAXLIST)
         B     LOOP
         SPACE 5
EXITPGM  DS    0H
         TBEND TABLE,MF=E
         SPACE
         VRESET ,
         SPACE
         ISPF  DELETE
         SPACE 5
         LR    1,13                     GET ADDRESS FOR FREEMAIN
         L     13,WORKSTOR+4            GET CALLERS SAVE AREA
         FREEMAIN R,A=(1),LV=WSLEN,SP=0 FREE WORKING STORAGE
         SPACE
         SR    15,15                    GET RETURN CODE
         L     14,12(,13)               RESTORE RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REMAINING REGISTERS
         MVI   12(13),X'FF'             FLAG AS RETURNED FOR SNAP SVC
         BR    14                       RETURN TO CALLER
         TITLE ' - ATTENTION EXIT  '
ATTNEXIT DS    0H
         L     1,8(1)              GET PARMS ADDRESS
         LM    11,13,0(1)          RESTORE BASE REGISTER
TURNOFF  STAX  ,                   TURN OFF STAX EXIT
         SPACE
         MVI   PA1HIT,C'1'         SET PA1 SWITCH ON
         BR    14                  RETURN
         TITLE ' - WORKING STORAGE '
WORKSTOR DSECT                          WORKING STORAGE DSECT
SAVEAREA DS    18F                      PROGRAM SAVE AREA
         DS    0D                       DOUBLE WORD ALIGN
AUTOMODE DS    CL1                 AUTOMODE SET?
AUTOREQ  DS    CL1                 AUTOREQ SWITCH
JOBFTSO  DS    CL1                 TSO USER SWITCH
OSW      DS    CL1                 TSO USER SWITCH
PA1HIT   DS    CL1                 PA1 SWITCH
STAXREGS DS    3F                  BASE REGS FOR STAX EXIT
TIMELIM  DS    F                   TIME LIMIT FOR AUTO MODE
JOBPSORT DS    CL14                SORT KEY
JOBNAME  DS    CL8                 SORT KEY
VCOPYLEN DS    F                   SORT KEY
TABLE    DS    CL8                 ISPF TABLE NAME
LIMIT    DS    CL8                 LIMIT OUTPUT
SLIMIT   DS    CL8                 LIMIT OUTPUT
ZTDTOP   DS    F                   TOP OF SCREEN DISPLAYED
ZTDROWS  DS    F                   TOP OF SCREEN DISPLAYED
ROWID    DS    F                   ROWID
         ISPF  PARMS,MINIMUM=1     WORKING STORAGE
STAXLIST STAX  ATTNEXIT,IBUF=0,OBUF=0,REPLACE=YES,USADDR=0,MF=L
         DS    0D
WSLEN    EQU   *-WORKSTOR               LENGTH OF WORKING STORAGE
JOB0LIST CSECT ,                        RESUME CSECT
         END   JOB0LIST
