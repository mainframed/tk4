         MACRO
         CONCAT &POSIT,&TYPE=,&EXIT=
         GBLA  &CONCATN,&CONCATX
         GBLB  &CONCATB,&CONCATD,&DDDFAIL,&DDDD
         AIF   (&DDDD).ERRDDDD
         AIF   ('&POSIT' EQ 'E').ENDCONC
         AIF   ('&POSIT' NE 'B').ERRPARM
         AIF   ('&TYPE&EXIT' EQ '').CONCB
         MNOTE 4,'TYPE AND EXIT IGNORED ON CONCAT B'
.CONCB   AIF   (&CONCATB).ERRB
&CONCATB SETB  1
&CONCATX SETA  4              INITIAL DISPLACEMENT INTO DDNAME LIST
*
         MNOTE *,'*   BEGIN CONCATENATION'
*
         MEXIT
.ENDCONC AIF   (NOT &CONCATB).ERRE
&CONCATB SETB  0
&CONCATD SETB  0
&CONCATX SETA  &CONCATX/10             INITIAL 4 WILL ROUND TO 0
         AIF   (&CONCATN GE &CONCATX).CONCT
&CONCATN SETA  &CONCATX
.CONCT   AIF   (&CONCATX LE 1).ERRNUMB
         MVC   ALOCONCA(4),=Y(X'01',&CONCATX) CODE, NUMBER OF DDNAMES
         LA    14,ALOCONCA             ADDRESS OF CONCATENATION TU
         AIF   ('&TYPE' EQ '' OR '&TYPE' EQ 'PERM').PERM
         AIF   ('&TYPE' NE 'TEMP').ERRTYPE
         ST    14,ALOBLOCK+20
         OI    ALOBLOCK+20,X'80'       SET END OF TU ADDR LIST
         AGO   .CONCA
.PERM    MVC   ALOBLOCK-8(4),=Y(X'04',0) PERMANENTLY CONCATENATED
         LA    15,ALOBLOCK-8
         STM   14,15,ALOBLOCK+20
         OI    ALOBLOCK+24,X'80'       SET END OF TU ADDR LIST
.CONCA   LA    0,X'0320'               DYNAMIC CONCATENATION
         AIF   ('&EXIT' EQ '').EXITDEF
         AIF   ('&EXIT'(1,1) EQ '(').LREXIT
         LA    15,&EXIT                LOAD ERROR EXIT RTN ADDRESS
         AGO   .BALDDDD
.EXITDEF LA    15,ALODDERR             LOAD ERROR EXIT RTN ADDRESS
&DDDFAIL SETB  1                       IN-LINE EXIT NEEDED
         AGO   .BALDDDD
.LREXIT  LR    15,&EXIT                LOAD ERROR EXIT RTN ADDRESS
.BALDDDD BAL   14,ALOROUT              LINK TO DDDD MACRO
*
         MNOTE *,'*   END CONCATENTATION OF &CONCATX DDNAMES'
*
         MEXIT
.ERRDDDD MNOTE 12,'CONCAT MAY NOT BE ISSUED AFTER DDDD'
         MEXIT
.ERRB    MNOTE 12,'CONCAT E  MUST BE ISSUED BEFORE RE-ISSUING CONCAT B'
         MEXIT
.ERRE    MNOTE 12,'CONCAT E  MAY BE ISSUED ONLY AFTER CONCAT B'
         MEXIT
.ERRNUMB MNOTE 12,'AT LEAST 2 DD''S MUST BE CONCATENATED'
         MEXIT
.ERRTYPE MNOTE 8,'INVALID TYPE, &TYPE.. IGNORED'
         AGO   .PERM
.ERRPARM MNOTE 12,'INVALID POSITIONAL PARAMETER. SPECIFY B OR E'
         MEND
