***********************************************************************
*                                                                     *
*   MACRO DDD0                                                        *
*                                                                     *
*    FUNCTION: MOVE A SINGLE TEXT UNIT INTO THE TEXT UNIT AREA,       *
*      SET ITS ADDRESS INTO A REGISTER.  STORE ADDRESSES INTO THE     *
*      TEXT UNIT ADDRESS ARRAY.                                       *
*                                                                     *
*    OPERANDS: THERE ARE 5 POSITIONAL OPERANDS, P1 THRU P5.           *
*      OPERAND P5 IS ALWAYS A QUOTED STRING THAT DDD0 EXPANDS INTO A  *
*      COMMENT MNOTE. OPERANDS P1-P4 SPECIFY A TEXT UNIT IN ONE OF    *
*      SEVERAL WAYS:                                                  *
*        ADDRESS ONLY: P1 IS AN RX-TYPE ADDRESS OF A TEXT UNIT        *
*          PREFORMATTED BY THE INVOKER. DDD0 DOES NOT COPY THE TEXT   *
*          UNIT IN THIS CASE. THIS MODE IS SELECTED WHEN P2 IS        *
*          OMITTED.                                                   *
*        TWO-PART TEXT UNIT: P1 IS THE LENGTH (NON-RELOCATABLE        *
*          EXPRESSION WHOSE FIRST CHARACTER IS NUMERIC) AND P2 IS     *
*          THE ADDRESS (RS-TYPE) OF THE FIRST PART OF THE TEXT UNIT.  *
*          P3 IS THE LENGTH (NON-RELOCATABLE EXPRESSION) AND P4 IS    *
*          THE ADDRESS (RS-TYPE) OF THE SECOND PART OF THE TEXT       *
*          UNIT. DDD0 COPIES BOTH PARTS TO THE TEXT UNIT AREA. THIS   *
*          MODE IS SELECTED WHEN P2 IS NOT OMITTED, THE FIRST         *
*          CHARACTER OF P1 IS NUMERIC AND P3 IS NOT OMITTED.          *
*        ONE-PART TEXT UNIT: P1 IS THE LENGTH (NON-RELOCATABLE        *
*          EXPRESSION WHOSE FIRST CHARACTER IS NUMERIC) AND P2 IS     *
*          THE ADDRESS (RS-TYPE) OF THE TEXT UNIT.  DDD0 COPIES THE   *
*          TEXT UNIT TO THE TEXT UNIT AREA. THIS MODE IS SELECTED     *
*          WHEN P2 IS NOT OMITTED, THE FIRST CHARACTER OF P1 IS       *
*          NUMERIC AND P3 IS OMITTED.                                 *
*        CHARACTER-STRING VALUE: P1 (A-TYPE ADDRESS CONSTANT WHOSE    *
*          FIRST CHARACTER IS NOT NUMERIC) IS THE KEY AND NUMBER OF   *
*          PARAMETERS (MUST BE 1) OF THE TEXT UNIT.  P2 IS AN         *
*          UNQUOTED STRING USED BY DDD0 AS THE VALUE OF THE FIRST     *
*          PARAMETER. DDD0 WILL EFFECTIVELY REMOVE THE FIRST          *
*          CHARACTER OF THE STRING IF THE FIRST TWO CHARACTERS ARE    *
*          BOTH AMPERSANDS. DDD0 SETS THE LENGTH OF THE FIRST         *
*          PARAMETER TO THE LENGTH OF THE P2 STRING (AFTER AMPERSAND  *
*          REMOVAL, IF ANY). P3 IS AN UPPER LIMIT TO THE LENGTH OF    *
*          P2 (BEFORE AMPERSAND REMOVAL). DDD0 COPIES THE TEXT UNIT   *
*          TO THE TEXT UNIT AREA. THIS MODE IS SELECTED WHEN P2 IS    *
*          NOT OMITTED AND P1'S FIRST CHARACTER IS NOT NUMERIC.       *
*                                                                     *
***********************************************************************
         MACRO
         DDD0  &ID,&CH,&L2,&A2,&COMMENT
         GBLA  &X,&Y,&R
         LCLA  &A,&K
         LCLC  &C
         SPACE 1
         MNOTE *,&COMMENT
         AIF   (T'&CH NE 'O').KEYADDX
         AIF   ('&ID'(K'&ID,1) NE '@').PSREGX
&C       SETC  '&ID'(1,K'&ID-1)
         L     &R,&C
         AGO   .CODEX
.PSREGX  LA    &R,&ID
         AGO   .CODEX
.KEYADDX ANOP
.*
         AIF   ('&ID'(1,1) LT '0').MOVLITX
         AIF   (T'&L2 EQ 'O').MOVVALX
&Y       SETA  (&Y+&L2+1)/2*2
         MVC   ALOBLOCK-&Y.(&L2),&A2
.MOVVALX ANOP
&Y       SETA  (&Y+&ID+1)/2*2
         MVC   ALOBLOCK-&Y.(&ID),&CH
         LA    &R,ALOBLOCK-&Y
         AGO   .CODEX
.MOVLITX ANOP
.*
&K       SETA  K'&CH
         AIF   (&K GT &L2).ERRLEN
&C       SETC  '&CH '(1,2)
&A       SETA  3
         AIF   ('&C' NE '&&').LOOP
&A       SETA  4
&C       SETC  '&CH '(1,3)
.LOOP    AIF   (&A GT &K).SETK
&C       SETC  '&C'',C'''.'&CH   '(&A,4)
&A       SETA  &A+4
         AGO   .LOOP
.SETK    AIF   ('&CH '(1,2) NE '&&').LA
&K       SETA  &K-1
.LA      ANOP
&A       SETA  &K+6
&Y       SETA  (&Y+&A+1)/2*2
         MVC   ALOBLOCK-&Y.(&A),=A(&ID,&K*X'10000'+C'&C')
         LA    &R,ALOBLOCK-&Y
.CODEX   ANOP
.*
         AIF   (&R NE 14).R14X
&R       SETA  15
         MEXIT
.R14X    AIF   (&R NE 15).R15X
&R       SETA  0
         MEXIT
.R15X    AIF   (&R NE 0).R0X
&R       SETA  1
         MEXIT
.R0X     STM   14,1,ALOBLOCK+&X
&X       SETA  &X+16
&R       SETA  14
         MEXIT
.ERRLEN  MNOTE 12,'OPERAND ''&CH'' EXCEEDS &L2 CHARACTERS. OPERAND IGNO*
               RED.'
         MEND
