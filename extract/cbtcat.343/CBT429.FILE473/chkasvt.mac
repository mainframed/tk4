*        DISPLAYS THE MAXIMUM AND AVAILABLE NUMBER OF ASID SLOTS.
*        SYS1.CMDLIB(CHKASVT)
CHKASVT  START
*        REGS
         YREGS
         SAVE  (14,12)
         LR    12,15
         USING CHKASVT,12
         ST    R13,RSAVE+4                SAVE REGISTERS
         LA    R9,RSAVE
         ST    R9,8(R13)
         LR    R13,R9
         L     R13,RSAVE+4
         L     R1,16                      GET ASVT
         USING CVT,R1
         L     R1,CVTASVT
         USING ASVT,R1
         SLR   R2,R2                      GET ASVT HEADER STUFF
         L     R2,ASVTMAXU                MAX ASIDS
         LR    R4,R2                      SAVE MAX ASIDS IN REG4
         CVD   R2,PACKED                  CONVERT TO DECIMAL, AND
         UNPK  WTO+19(5),PACKED+5(3)      DISPLAY
         OI    WTO+23,X'F0'               MAKE LAST DIGIT READABLE
         SLR   R2,R2
         L     R2,ASVTAAV                 GET # AVAILABLE ASIDS
         CVD   R2,PACKED                  CONVERT TO DECIMAL, AND
         UNPK  WTO+44(5),PACKED+5(3)      DISPLAY
         OI    WTO+48,X'F0'               MAKE LAST DIGIT READABLE
         SLR   R2,R2                    CLEAR REG 2(COUNTER)
         LA    R3,ASVTENTY              GO TO 1ST ENTRY ADDR
         LR    R5,R3                 SAVE ADDR FOR COMPARISON
         ICM   R5,8,=X'80'           SET HIGHORDER
         BCTR  R4,0                 REDUCE COUNTER BY ONE
         LA    R6,WTO2+65           ADDRESS OF LAST ZZZZZ IN WTO2
         LA    R9,WTO2+29           ADDRESS OF FRST ZZZZZ IN WTO2
         LA    R7,1(,0)             SET REG7 TO ASID=1
         MVC   WTO2+29(5),=C'     '
         MVC   WTO2+35(5),=C'     '    CLEAR SLOTS IN MSG LINE
         MVC   WTO2+41(5),=C'     '
         MVC   WTO2+47(5),=C'     '
         MVC   WTO2+53(5),=C'     '
         MVC   WTO2+59(5),=C'     '
         MVC   WTO2+65(5),=C'     '
ASVTLOOP C     R5,0(R3)             ENTRY = TO MASTER ASID ENTRY ADDR?
         BNE   PASSCNT              NO, DON'T COUNT IT AS UNAVAILABLE
         CVD   R7,PACKED            THEN PREPARE TO TELL 'EM
         UNPK  0(5,R9),PACKED+5(3)  DISPLAY THE COUNT
         OI    4(R9),X'F0'          MAKE LAST DIGIT READABLE
         LA    R9,6(,R9)            BUMP TO NEXT ZZZZZ IN WTO2
         CR    R9,R6
         BNH   CONTINUE             HAVEN'T FILLED IN ALL ZZZZZ'S YET
         BAL   R10,WTORTN           DISPLAY NON-REUSEABLE ASID NUMBERS
         LA    R9,WTO2+29           REINITIALIZE REG9 TO 1ST ZZZZZ ADDR
         MVC   WTO2+29(5),=C'     '
         MVC   WTO2+35(5),=C'     '
         MVC   WTO2+41(5),=C'     '
         MVC   WTO2+47(5),=C'     '
         MVC   WTO2+53(5),=C'     '
         MVC   WTO2+59(5),=C'     '
         MVC   WTO2+65(5),=C'     '
CONTINUE EQU   *
         LA    R2,1(,R2)                BUMP COUNTER
PASSCNT  LA    R3,4(R3)                GET NEXT ASVT ENTRY
         LA    R7,1(,R7)               BUMP COUNT TO NEXT ASID NUMBER
         BCT   R4,ASVTLOOP             LOOP THRU ENTRIES
         CLC   WTO2+29(5),=C'     '     ANY DATA FILLED IN?
         BE    CONTINU2                 NOPE, BYPASS WTO2
         BAL   R10,WTORTN           DISPLAY NON-REUSEABLE ASID NUMBERS
CONTINU2 CVD   R2,PACKED                 THEN PREPARE TO TELL 'EM
         UNPK  WTO+67(5),PACKED+5(3)     DISPLAY
         OI    WTO+71,X'F0'
WTO      WTO   'ASID MAX = 12345; ASIDS AVAILABLE = 12345; NON-REUSABLEX
               S = 12345',ROUTCDE=11
         RETURN (14,12),RC=0
WTORTN   EQU   *
WTO2     WTO   'NON-REUSEABLE ASIDS: ZZZZZ ZZZZZ ZZZZZ ZZZZZ ZZZZZ ZZZZX
               Z ZZZZZ',ROUTCDE=11
         BR    R10
RSAVE    DC    18F'0'
PACKED   DS    D
         CVT   DSECT=YES
         IHAASVT
         END   CHKASVT
