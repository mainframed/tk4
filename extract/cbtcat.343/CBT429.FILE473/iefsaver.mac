         MACRO
&LABEL   IEFSAVER    &P1,&P2
         LCLA  &SP
         LCLC  &EXTRA
         AIF ('&LABEL' EQ '').A0
&LABEL   DS    0H
.A0      ANOP
         AIF   (T'&P1 NE 'O').NSPZERO
&SP      SETA  0
         AGO   .GETMORE
.NSPZERO ANOP
&SP      SETA  &P1
.GETMORE ANOP
&EXTRA   SETC  '0'
         AIF   (T'&P2 EQ 'O').SETUP
&EXTRA   SETC  '&P2'
.SETUP   ANOP
         GETMAIN R,LV=72+&EXTRA,SP=&SP  GET NEW SAVE AREA
         XC    0(72,1),0(1)             ZERO OUT SAVE AREA
         ST    13,4(0,1)                CHAIN SAVE AREAS TOGETHER
         ST    1,8(0,13)                CHAIN SAVE AREAS TOGETHER
         LR    13,1                     PTR TO SAVE AREA
         AIF   (T'&P2 EQ 'O').MEXIT
         LA    14,72(,13)               ZERO EXTRA AREA
         L     15,IEF&SYSNDX            GET LENGTH TO ZERO OUT
         SLR   0,0
         SLR   1,1
         MVCL  14,0                     ZERO OUT GETMAINED AREA
         B     IEF&SYSNDX+4             BRANCH AROUND EXTRA LENGTH
IEF&SYSNDX DC  A(&EXTRA)                LENGTH OF EXTRA WORK AREA
.MEXIT   ANOP
         MEND
