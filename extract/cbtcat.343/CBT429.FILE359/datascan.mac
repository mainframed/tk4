DATASCAN CSECT     VERSION 1.2
         REGS
         SPACE 2
START    SAVE  (14,12),,DATASCAN_&SYSDATE_&SYSTIME
         LR    R10,R15
         USING START,10,11,12
         LA    R11,4095(R10)
         LA    R11,1(R11)
         LA    R12,4095(R11)
         LA    R12,1(R12)
         ST    R13,SAVEAREA+4
         LR    R4,R13
         LA    R13,SAVEAREA
         ST    R13,8(R4)
         SPACE 2
         LA    R9,LCVTADDR
         SPACE 1
         L     R2,0(R1)
         LH    R3,0(R2)
         SPACE 1
         CH    R3,=H'2'
         BNE   READJFCB
         SPACE 1
         CLC   2(2,R2),=CL2'VS'
         BNE   READJFCB
         SPACE 1
         AP    VSON,=PL1'1'
         MVI   EBR1+1,X'80'
         MVI   EBR2+1,X'80'
         MVI   EBR3+1,X'80'
         B     PHASE1
         EJECT
*   ROUTINE TO GENERATE HEAD LINE 2 AND CENTER THE VOLSER AND DSNAME
*   OF THE PACK AND DATA SET BEING PROCESSED.
         SPACE 2
READJFCB RDJFCB (DRECTORY)
         SPACE 2
BLDHEAD  LA    R2,0
         LA    R3,JFCBDSNM
         LA    R4,44
LOOPJFCB CLI   0(R3),X'40'
         BE    CJFCBNM
         LA    R2,1(R2)
         LA    R3,1(R3)
         BCT   R4,LOOPJFCB
         SPACE 1
CJFCBNM  LA    R2,0(R2)
         A     R2,=F'27'
         CVD   R2,WORK1
         MVC   GM1ADDR1,WORK1+4
         ZAP   WORK1,=P'132'
         SP    WORK1,GM1ADDR1
         ZAP   GM1ADDR2,=P'2'
         DP    WORK1,GM1ADDR2
         MVC   GM1ADDR2(4),WORK1
         ZAP   WORK1,GM1ADDR2
         CVB   R2,WORK1
         SPACE 1
         LA    R3,HEADLN1A
         LA    R3,1(R3)
         AR    R3,R2
         MVC   0(9,R3),=CL9'VOLUME = '
         LA    R3,9(R3)
         MVC   0(6,R3),JFCBVOLS
         LA    R3,8(R3)
         MVC   0(9,R3),=CL9'DSNAME = '
         LA    R3,9(R3)
         MVC   0(44,R3),JFCBDSNM
         MVC   SCTHVOL(6),JFCBVOLS
         MVC   SCTHDSN(44),JFCBDSNM
         SPACE 1
         BR    R9
         EJECT
*   ROUTINE TO DETERMINE DEVICE TYPE OF THE VOLUME BEING PROCESSED.
*   DEVICE TYPE IS THEN MOVED TO THE SCRATCH MEMBER CONTROL CARD
*   SKELETON IN CASE THE DELETE MEMBER OPTION IS TAKEN DURING THE RUN.
         SPACE 1
LCVTADDR L     R2,16          CVT
         L     R3,0(R2)       TCB WORDS
         L     R4,4(R3)       CURRENT TCB
         L     R5,12(R4)      TIOT
         LA    R6,24(R5)      1ST DD ENTRY
         SPACE 2
         SR    R2,R2
         LR    R3,R6
TEST1    IC    R2,0(R3)
         LTR   R2,R2
         BZ    UT1GONE
TEST2    CLC   4(6,R3),=C'SYSUT1'
         BE    FOUNDIT
         AR    R3,R2
         SR    R2,R2
         B     TEST1
         SPACE 2
UT1GONE  B     KILLJOB1
         SPACE 2
FOUNDIT  L     R3,16(R3)
         SR    R5,R5
         IC    R5,19(R3)
         BCTR  R5,0
         CVD   R5,WORK1
         MP    WORK1,=PL4'4'
         CVB   R5,WORK1
         LA    R4,UTABLE
         AR    R4,R5
         MVC   SCTHUNIT(4),0(R4)
         EJECT
PHASE1   EQU   *
         SPACE 1
*        PHASE1 WILL EDIT THE CONTROL CARDS AND DO A GETMAIN TO BUILD
*        AN IN CORE TABLE TO HOLD ALL SCAN PARAMETERS.
         SPACE 2
         MVC   SCBFILL,=CL4'0000'
         SPACE 1
         OPEN  (SYSIN,(INPUT))
         OPEN  (SYSPRINT,(OUTPUT))
         SPACE 2
*        INIT. HEADINGS WITH DATE.
         SPACE 1
         TIME  DEC
         SPACE 2
         ST    R1,GM1ADDR1
         UNPK  PREFIX,GM1ADDR1
         OI    PREFIX+7,X'F0'
         MVC   H1DATE(5),PREFIX+3
         SPACE 1
         BAL   R9,HEADING1
         SPACE 2
GETPCARD GET   SYSIN,PARMCARD
         SPACE 2
         B     EDITPCRD
         SPACE 2
EOFSYSIN CLOSE (SYSIN)
         SPACE 2
         CLI   KILLSW1,C'1'
         BE    KILLJOB1
         SPACE 1
         CP    VSON,=PL1'0'
         BE    PHASE2
         SPACE 1
         CP    VSON,=PL1'4'
         BNE   KILLJOB2
         SPACE 1
         MVC   FINDNAME(4),GM1ADDR1
         MVC   FINDNAME+4(4),GM1ADDR2
         SPACE 1
         BAL   R9,BLDHEAD
         SPACE 1
         MVC   GM1ADDR1(4),FINDNAME
         MVC   GM1ADDR2(4),FINDNAME+4
         SPACE 1
         B     PHASE2
         EJECT
EDITPCRD EQU   *
         SPACE 1
         CLC   CARDID,=C'PREFIX'
         BE    IDPREFIX
         SPACE 1
         CLC   CARDID,=C'SCANS '
         BE    IDSCANS
         SPACE 1
         CLC   CARDID(4),=C'SCAN'
         BE    IDSCAN
         SPACE 1
         CLC   CARDID(4),=C'FROM'
         BE    IDFROM
         SPACE 1
         CLC   CARDID(2),=C'TO'
         BE    IDTO
         SPACE 1
         CLC   CARDID,=C'PUNCH '
         BE    IDPUNCH
         SPACE 1
         CLC   CARDID,=C'DSNAME'
EBR1     NOP   IDDSNAME
         SPACE 1
         CLC   CARDID,=C'UNIT  '
EBR2     NOP   IDUNIT
         SPACE 1
         CLC   CARDID,=C'VOLUME'
EBR3     NOP   IDVOLUME
         SPACE 1
         CLC   CARDID,=C'UPDATE'
         BE    IDUPDATE
         SPACE 3
UNKNOWN  MVC   ELDATA2,ERROR3
         EJECT
RETURN1  EQU   *
         SPACE 1
         MVC   ELDATA1,PARMCARD
         SPACE 2
RETURN1A PUT   SYSPRINT,EDITLINE
         SPACE 2
         MVI   EDITLINE,C' '
         MVC   EDITLINE+1(132),EDITLINE
         AP    LINES,=P'1'
         CLI   SWBRANCH,C'1'
         BE    IDSCANR
         CP    LINES,LINESMAX
         BNH   GETPCARD
         BAL   R9,HEADING1
         B     GETPCARD
         EJECT
IDPREFIX EQU   *
         SPACE 1
         LA    R2,VALUE
         LA    R3,8
         SR    R4,R4
LOOP1    CLI   0(R2),X'40'
         BE    STORE1
         LA    R4,1(R4)
         LA    R2,1(R2)
         BCT   R3,LOOP1
         SPACE 1
STORE1   BCTR  R4,0
         STC   R4,CHKPRFIX+1
         MVC   PREFIX,VALUE
         CLI   SWITCH1,C'1'
         BNE   MOVE1
         MVC   ELDATA2,WARNING1
MOVE1    MVI   SWITCH1,C'1'
         B     RETURN1
         EJECT
IDSCANS  EQU   *
         SPACE 1
         CLC   EF1,=XL2'0000'     CHECK FOR PRIOR SCANS CARD
         BNE   EDITER2
         SPACE 1
         MVZ   EF1(2),VALUE
         CLC   EF1,=XL2'F0F0'     NUMERIC CHECK OF FIELD.
         BE    OK1
         B     EDITER1
         SPACE 1
OK1      MVC   SCANS(2),VALUE
         ZAP   WORK1,=P'0'
         PACK  WORK1,SCANS
         ZAP   SCANS,WORK1
         MP    WORK1,=P'120'      # OF SCANS X SCB LENGTH.
         CVB   R2,WORK1
         ST    R2,GM1LEN
         SPACE 2
         GETMAIN R,LV=(2)
         SPACE 2
         ST    R1,GM1ADDR1        ACTUAL GOTTEN CORE ADDRESS
         S     R1,=F'120'
         ST    R1,GM1ADDR2        CORE ADDRESS - 120 BYTES.
         LR    R8,R1
         B     RETURN1
         EJECT
IDSCAN   EQU   *
         SPACE 1
         CLC   SCBFILL,=CL4'0000'
         BE    IDSCANB
         SPACE 1
         BAL   R9,CSCBFILL
         CLI   OKFILLSW,C'1'
         BE    OK2
         SPACE 1
         CLC   SCBID,=XL2'9587'
         BE    IDSCANB
         MVC   ERROR6+15(2),SCBID
         MVC   ELDATA2,ERROR6
IDSCANER MVI   SWBRANCH,C'1'
         B     RETURN1A
IDSCANR  MVI   SWBRANCH,C'0'
         B     IDSCANB
         SPACE 1
OK2      AP    REALTOT,=P'1'
         CP    REALTOT,SCANS
         BH    IDSCANE7
         SPACE 1
         LA    R8,120(R8)
         LA    R1,120(R8)
         ST    R1,SCBNSCB
         MVC   0(120,R8),SCB      SAVE SCAN CONTROL BLOCK
         MVI   MVD1SCB,C'1'
         B     IDSCANB
         SPACE 1
IDSCANE7 MVC   ERROR7+15(2),SCBID
         MVC   ELDATA2,ERROR7
         B     IDSCANER
         SPACE 2
CSCBFILL EQU   *
         MVI   OKFILLSW,C'0'
         CLC   SCBFILL,=C'1110'
         BE    SETOKSW
         CLC   SCBFILL,=C'1111'
         BE    SETOKSW
         CLC   SCBFILL,=C'2111'
         BE    SETOKSW
         BR    R9
SETOKSW  MVI   OKFILLSW,C'1'
         BR    R9
         EJECT
IDSCANB  EQU   *
         MVI   ELDATA2,X'40'
         MVC   ELDATA2+1(46),ELDATA2
         SPACE 1
         MVI   SCB,X'00'     INIT SCB WORK AREA
         MVC   SCB+1(119),SCB
         SPACE 1
         MVC   SCBID,CARDID+4     MOVE IN SCB ID.
         MVC   EF2,=XL4'00000000'
         MVZ   EF2(4),VALUE
         CLC   EF2,=XL4'F0F0F0F0'
         BNE   EDITER4
         SPACE 1
         ZAP   WORK2,=P'0'
         ZAP   WORK3,=P'0'
         PACK  WORK2,VALUE(2)
         PACK  WORK3,VALUE+2(2)
         AP    WORK2,WORK3
         CP    WORK2,=P'72'
         BH    EDITER5
         SPACE 1
         ZAP   WORK1,=P'0'
         PACK  WORK1,VALUE(2)
         CVB   R2,WORK1
         S     R2,=F'1'
         ST    R2,SCBSCOL
         SPACE 1
         ZAP   WORK1,=P'0'
         PACK  WORK1,VALUE+2(2)
         CVB   R2,WORK1
         ST    R2,SCBSLEN
         SPACE 1
         MVC   SCBACD(1),VALUE+4
         MVI   SCBEXTRA,C' '
         SPACE 1
         MVC   SCBFILL,=CL4'1100'
         CLI   SCBACD,C'C'
         BE    SETACD
SCANBRET B     RETURN1
SETACD   MVC   SCBFILL,=CL4'2100'
         B     RETURN1
         EJECT
IDFROM   EQU   *
         SPACE 1
         CLC   CARDID+4(2),SCBID
         BE    OK3
EDITER8  MVC   ELDATA2,ERROR8
         B     RETURN1
         SPACE 1
OK3      CLI   SCBFILL+2,C'1'
         BNE   OK4
EDITER10 MVC   ELDATA2,ERROR10
         B     RETURN1
         SPACE 1
OK4      CLI   VALUE,C' '
         BE    EDITER9
         CLI   VALUE,X'7D'
         BNE   OK5
         BAL   R9,LITERAL
         B     STORE2
OK5      BAL   R9,NONLITRL
         B     STORE2
EDITER9  MVC   ELDATA2,ERROR9
         B     RETURN1
         SPACE 1
STORE2   EQU   *
         BCTR  R4,0
         ST    R4,SCBFLEN
         MVC   SCBFROM(40),VALUE
         MVI   SCBFILL+2,C'1'
         B     RETURN1
         EJECT
IDTO     EQU   *
         SPACE 1
         CLC   CARDID+2(2),SCBID
         BNE   EDITER8
         SPACE 1
         CLI   SCBFILL+3,C'1'
         BE    EDITER10
         SPACE 1
         CLI   VALUE,C' '
         BE    EDITER9
         CLI   VALUE,X'7D'
         BNE   OK6
         BAL   R9,LITERAL
         B     STORE3
OK6      BAL   R9,NONLITRL
         SPACE 1
STORE3   EQU   *
         BCTR  R4,0
         ST    R4,SCBTLEN
         MVC   SCBTO(40),VALUE
         MVI   SCBFILL+3,C'1'
         B     RETURN1
         SPACE 5
IDPUNCH  EQU   *
         SPACE 1
ALLOPT   MVI   SWITCH2,C'1'
         B     RETURN1
         SPACE 5
IDVOLUME EQU   *
         SPACE 1
         MVC   JFCBVOLS(6),VALUE
         AP    VSON,=PL1'1'
         MVI   EBR3+1,X'00'
         MVC   ELDATA2,WARNING2
         B     RETURN1
         EJECT
IDDSNAME EQU   *
         SPACE 1
         CLI   VALUE,C' '
         BE    DSNERROR
         SPACE 1
         CLI   VALUE+44,C' '
         BNE   DSNERROR
         SPACE 1
         MVC   JFCBDSNM(44),VALUE
         AP    VSON,=PL1'1'
         MVI   EBR1+1,X'00'
         MVC   ELDATA2,WARNING2
         B     RETURN1
         SPACE 1
DSNERROR MVC   ELDATA2,ERROR11
         B     RETURN1
         SPACE 5
IDUNIT   EQU   *
         SPACE 1
         LA    R2,UTABLE
         LA    R3,15
         SPACE 1
UNITCHK  CLC   VALUE(4),0(R2)
         BE    UNITOK
         CLC   0(4,R2),=CL4'XXXX'
         BE    UNITBAD
         LA    R2,4(R2)
         BCT   R3,UNITCHK
         SPACE 1
UNITBAD  MVC   ELDATA2,ERROR12
         B     RETURN1
         SPACE 1
UNITOK   MVC   SCTHUNIT(4),0(R2)
         AP    VSON,=PL1'1'
         MVI   EBR2+1,X'00'
         MVC   ELDATA2,WARNING2
         B     RETURN1
         SPACE 5
IDUPDATE CLI   VALUE,C' '
         BE    UNKNOWN
         CLI   VALUE,C'Z'
         BH    UNKNOWN
         MVC   IEBPRINT(8),VALUE
         MVI   UPDATSW+1,X'00'
         B     RETURN1
         EJECT
* ROUTINE TO COMPUTE THE LENGTH OF AN EDIT STRING.
         SPACE 1
NONLITRL EQU   *
         LA    R2,40
         LA    R3,VALUE
         LA    R4,0
LOOP2    CLI   0(R3),C' '
         BE    EXIT2
         LA    R3,1(R3)
         LA    R4,1(R4)
         BCT   R2,LOOP2
         SPACE 1
EXIT2    BR    R9
         SPACE 5
* ROUTINE TO COMPUTE THE LENGTH OF AN LITERAL EDIT STRING.
         SPACE 1
LITERAL  EQU   *
         LA    R2,39
         LA    R3,VALUE+39
         LA    R4,0
LOOP3    CLI   0(R3),X'7D'
         BE    EXIT3
         BCTR  R3,0
         BCT   R2,LOOP3
         SPACE 1
         B     EDITER9
         SPACE 1
EXIT3    LR    R4,R3
         LA    R3,VALUE+1
         SR    R4,R3
         MVC   CARD(40),VALUE
         MVC   VALUE(39),CARD+1
         BR    R9
         EJECT
*        HEADING ROUTINES.
         SPACE 1
HEADING1 EQU   *
         AP    PAGE#,=P'1'
         UNPK  PAGENO,PAGE#
         OI    PAGENO+2,X'F0'
         SPACE 1
         PUT   SYSPRINT,HEADLN1
         SPACE 1
         PUT   SYSPRINT,HEADLN1A
         SPACE 1
         PUT   SYSPRINT,HEADLN2
         SPACE 1
         ZAP   LINES,=P'5'
         MVI   EDITLINE,C'0'
         BR    R9
         SPACE 2
         EJECT
EDITER1  MVC   ELDATA2,ERROR1
         B     EDITERBR
         SPACE 1
EDITER2  MVC   ELDATA2,ERROR2
         B     RETURN1
         SPACE 1
EDITER4  MVC   ELDATA2,ERROR4
         MVC   SCBID,=XL2'9587'
         B     RETURN1
         SPACE 1
EDITER5  MVC   ELDATA2,ERROR5
         MVC   SCBID,=XL2'9587'
         B     RETURN1
         SPACE 1
EDITERBR MVI   KILLSW1,C'1'
         B     RETURN1
         EJECT
KILLJOB1 EQU   *
         SPACE 1
         LA    R15,8          ERROR RETURN CODE.
         B     EXITPGM2
         SPACE 1
EXITPGM  LA    R15,0          NORMAL RETURN CODE.
EXITPGM2 L     R13,4(R13)
         L     R14,12(13,0)
         LM    0,12,20(13)
         BR    R14            EXIT THE PROGRAM.
         EJECT
*
* PHASE 2 - PHASE 2 WILL INIT THE LAST SCB WITH A TRAILER FLAG,
*           READ THE DIRECTORY OF THE PDS AND SELECT MEMBERS TO
*           PROCESS BASED ON THE PREFIX OPTION TAKEN OR NOT,
*           AND WILL BUILD AN INCORE TABLE OF MEMBERS TO PROCESS.
*
PHASE2   EQU   *
         SPACE 1
         CLI   MVD1SCB,C'1'
         BE    CONTINU1
         CLC   SCBFILL,=CL4'0000'
         BE    KILLJOB2
         BAL   R9,CSCBFILL
         CLI   OKFILLSW,C'1'
         BE    CONTINU1
         SPACE 1
KILLJOB2 EQU   *
         PUT   SYSPRINT,TERMMSG
         CLOSE (SYSPRINT)
         B     KILLJOB1
         SPACE 1
CONTINU1 EQU   *
         BAL   R9,CSCBFILL
         CLI   OKFILLSW,C'1'
         BNE   CONTINU2
         SPACE 1
         AP    REALTOT,=P'1'
         CP    REALTOT,SCANS
         BNH   MOVEITIN
         SPACE 1
         MVC   ERROR7+15(2),SCBID
         MVC   ELDATA2,ERROR7
         PUT   SYSPRINT,EDITLINE
         B     CONTINU2
         SPACE 1
MOVEITIN EQU   *
         MVC   SCBFILL,=C'SEND'
         LA    R8,120(R8)
         MVC   0(120,R8),SCB
         B     CONTINU3
         SPACE 1
CONTINU2 EQU   *
         MVC   116(4,R8),=C'SEND'
         SPACE 1
         EJECT
CONTINU3 EQU   *
         SPACE 1
         OPEN  (DRECTORY,(INPUT))
         OPEN  (SYSUT2,(OUTPUT))
         SPACE 2
GETBLOCK GET   DRECTORY,BLOCK
         SPACE 2
         CLC   BLOCK(2),=XL2'0000' CHECK FOR END OF ENTRIES.
         BE    ENDPDSD             SAFETY VALVE CHECK, SEE GENLIST CODE
         LA    R7,BLOCK+2
         SR    R6,R6
         LH    R6,BLOCK
         SH    R6,=H'2'
         AR    R6,R7               R6 SHOULD HAVE END OF BLOCK.
         LA    R6,0(R6)            CLEAR HIGH ORDER BYTE.
         EJECT
GENLIST  EQU   *
         SPACE 1
         LA    R7,0(R7)
         CR    R7,R6               CHECK FOR END OF BLOCK.
         BE    GETBLOCK
         BH    GETBLOCK
         CLC   0(2,R7),=XL2'FFFF'  CHECK FOR END OF DIRECTORY.
         BE    ENDPDSD
         AP    MEMCNT,=P'1'
         SPACE 1
         TM    11(R7),X'80'        ALIAS CHECK
         BZ    CHECKSW1
         AP    ALIASCNT,=P'1'
         B     COMPUTE1
         SPACE 1
CHECKSW1 CLI   SWITCH1,C'1'        CHECK FOR PREFIX OPTION.
         BNE   MVMEMBER
         SPACE 1
CHKPRFIX CLC   PREFIX(0),0(R7)
         BNE   COMPUTE1
         SPACE 1
MVMEMBER MVC   ENTRY(8),0(R7)
         PUT   SYSUT2,ENTRYREC
         AP    MEMKEPT,=P'1'
         SPACE 1
COMPUTE1 SR    R8,R8
         IC    R8,11(R7)
         N     R8,=XL4'0000001F'
         LA    R8,12(R8,R8)
         AR    R7,R8
         B     GENLIST
         EJECT
ENDPDSD  EQU   *
         SPACE 1
         CLOSE (DRECTORY)
         CLOSE (SYSUT2)
         SPACE 2
         UNPK  MSG1CNT,MEMCNT
         OI    MSG1CNT+4,X'F0'
         UNPK  MSG2CNT,MEMKEPT
         OI    MSG2CNT+4,X'F0'
         UNPK  MSG3CNT,ALIASCNT
         OI    MSG3CNT+4,X'F0'
         SP    MEMCNT,ALIASCNT
         UNPK  MSG4CNT,MEMCNT
         OI    MSG4CNT+4,X'F0'
         SPACE 1
         MVI   HEADLN2,X'40'
         MVC   HEADLN2+1(132),HEADLN2
         BAL   R9,HEADING1
         SPACE 1
         PUT   SYSPRINT,MESSAGE    TOTAL MEMBERS COUNT
         MVC   MESSAGE(33),MESSAGE3
         SPACE 1
         PUT   SYSPRINT,MESSAGE
         MVC   MESSAGE(33),MESSAGE4
         SPACE 1
         PUT   SYSPRINT,MESSAGE
         MVC   MESSAGE(33),MESSAGE2
         SPACE 1
         PUT   SYSPRINT,MESSAGE    SELECTED MEMBERS COUNT
         EJECT
         CP    MEMKEPT,=P'0'
         BE    KILLJOB2
         SPACE 1
         ZAP   WORK1,MEMKEPT
         CVB   R2,WORK1
         ST    R2,GM2CNT
         SPACE 1
         SR    R2,R2
         MP    WORK1,=P'8'
         CVB   R2,WORK1
         ST    R2,GM2LEN           LENGTH FOR SELECTED MEMBER TABLE.
         SPACE 1
         GETMAIN R,LV=(2)
         SPACE 1
         ST    R1,GM2ADDR          ADDRESS OF GET MAIN AREA 2.
         EJECT
*   ROUTINE TO BUILD THE MEMBER TABLE.
         SPACE 2
         OPEN  (SYSUT2,(INPUT))
         SPACE 2
         L     R2,GM2CNT
         L     R3,GM2ADDR
LOOP4    GET   SYSUT2,ENTRYREC
         MVC   0(8,R3),ENTRY
         LA    R3,8(R3)
         BCT   R2,LOOP4
         SPACE 2
NMENTRYS CLOSE (SYSUT2)
         EJECT
*   PHASE3 - PHASE3 WILL READ PDS AND PROCESS CARDS FOR CHANGES TO BE
*            MADE.  IF PUNCH OPTION WAS TAKEN, CARDS WILL BE PUNCHED.
PHASE3   EQU   *
         SPACE 1
         OPEN  (SYSUT1,(INPUT))
         SPACE 1
         SR    R2,R2
         LH    R2,UT1BLKSZ
         ST    R2,GM3LEN
         SPACE 1
         GETMAIN R,LV=(2)
         SPACE 1
         ST    R1,GM3ADDR
         SPACE 1
         L     R2,GM3LEN
         CVD   R2,WORK1
         DP    WORK1,=PL4'80'
         MVC   EF2(4),WORK1
         ZAP   WORK1,EF2
         SR    R2,R2
         CVB   R2,WORK1
         ST    R2,CLOOPCNT
         SPACE 1
         OPEN  (SYSUT2,(OUTPUT))
         OPEN  (SYSPUNCH,(OUTPUT))
         SPACE 1
         MVC   POINTER1,GM2ADDR
         MVC   HEADLN2,HEADLN3
         BAL   R9,HEADING1
         LA    R7,PHASE3S
         EJECT
* MAINLINE FOR PHASE3, ALL IO, AND WORK DONE THRU BAL TO ROUTINES.
         SPACE 1
PHASE3A  BAL   R9,FINDMBR          LOCATE A MEMBER TO PROCESS.
         C     R15,=F'04'
         BE    EOJPHASE
         SPACE 1
PHASE3B  BAL   R9,CLINAREA
         SPACE 1
PHASE3C  BAL   R9,RDABLOCK         READ A BLOCK INTO CORE.
         SPACE 1
PHASE3D  BAL   R9,GETACARD         GET A CARD TO PROCESS.
         C     R15,=F'04'          AREA EMPTY.
         BE    PHASE3D1
         C     R15,=F'08'          END OF PROCEDURE.
         BE    PHASE3E
         B     PHASE3BR
         SPACE 1
PHASE3D1 CLI   EOPSWTCH,C'1'       END OF PROCEDURE.
         BE    PHASE3E
         B     PHASE3B
         SPACE 1
PHASE3E  MVI   EOPSWTCH,C'0'
         LA    R7,0(R7)
         LA    R6,PHASE3S
         LA    R6,0(R6)
         CR    R6,R7
         BE    PHASE3E1
         SPACE 1
         LA    R7,PHASE3S
         MVI   CHANGEON,C'0'
         MVI   LASTSCB,C'0'
         MVI   PUNCHCC,C'0'
         MVI   PUNCHSC,C'0'
         MVI   ACTIONON,C'0'
         B     PHASE3A
         SPACE 1
PHASE3E1 B     PHASE3E2
         SPACE 1
PHASE3BR BR    R7
         EJECT
* ROUTINE TO SCAN PROC AND SEE IF ANY CHANGES WILL BE MADE.
         SPACE 1
PHASE3S  EQU   *
         SPACE 1
         L     R2,GM1ADDR1
         B     SCANONE1
PHASE3S1 L     R2,SCBNSCB
SCANONE1 MVC   SCB(120),0(R2)
         CLC   SCBFILL,=CL4'SEND'
         BNE   PHASE3S2
         MVI   LASTSCB,C'1'
         SPACE 1
PHASE3S2 CLI   SCBACD,C'C'
         BE    PHASE3S3
         CLI   SCBACD,C'D'
         BE    PHASE3S3
         CLI   LASTSCB,C'1'
         BE    PHASE3S8
         B     PHASE3S1
         SPACE 1
PHASE3S3 BAL   R9,REGSETUP
         L     R2,SCBFLEN
         STC   R2,COMPARE1+1
         MVI   ACTIONON,C'1'
         SPACE 1
COMPARE1 CLC   SCBFROM(0),0(R5)
         BNE   BUMP
         CLI   SCBACD,C'D'
         BNE   PHASE3S9
         BAL   R9,DELTOPT
         MVI   PUNCHCC,C'0'
         MVI   LASTSCB,C'0'
         MVI   CHANGEON,C'2'
         B     PHASE3E2
BUMP     LA    R5,1(R5)
         BCT   R6,COMPARE1
         SPACE 1
PHASE3S7 EQU   *
         CLI   LASTSCB,C'1'
         BNE   PHASE3S1
PHASE3S8 MVI   LASTSCB,C'0'
         CLI   ACTIONON,C'1'
         BE    PHASE3D
         B     PHASE3E
         SPACE 1
PHASE3S9 MVI   PUNCHCC,C'0'
         MVI   LASTSCB,C'0'
         MVI   CHANGEON,C'1'
PHASE3E2 LA    R7,PHASE3SC
         SPACE 1
FINDAGIN FIND  SYSUT1,FINDNAME,D
         SPACE 1
         C     R15,=F'00'
         BNE   FINDAGIN
         SPACE 1
         B     PHASE3B
         EJECT
* ROUTINE TO SCAN PROCEDURE AND MAKE REQUESTED CHANGES.
         SPACE 1
PHASE3SC EQU *
         AP    SCANCNT,=P'1'
         SPACE 1
PHASE3SA CLI   CHANGEON,C'0'
         BE    PHASE3SB
         SPACE 1
         CLI   PUNCHCC,C'1'
         BE    PUNCHDC
         MVC   IEBCCNAM,FINDNAME
         SPACE 1
         CLI   PUNCHOFF,C'1'
         BE    PNCHSUT2
         SPACE 1
         PUT   SYSPUNCH,IEBCC
PNCHSUT2 EQU   *
         CLI   CHANGEON,C'2'
         BE    MVPCC1
         PUT   SYSUT2,IEBCC
         SPACE 1
MVPCC1   MVI   PUNCHCC,C'1'
         SPACE 1
PUNCHDC  CLI   PUNCHOFF,C'1'
         BE    PHASE3SB
         SPACE 1
         PUT   SYSPUNCH,CARD
         SPACE 1
         MVI   PUNCHOFF,C'1'
         SPACE 1
PHASE3SB L     R2,GM1ADDR1
         B     SCANONE2
         SPACE 1
NEXTSCAN L     R2,SCBNSCB
SCANONE2 MVC   SCB(120),0(R2)
         CLC   SCBFILL(4),=CL4'SEND'
         BNE   PHASE3SD
         MVI   LASTSCB,C'1'
         EJECT
PHASE3SD BAL   R9,REGSETUP
         L     R2,SCBFLEN
         STC   R2,COMPARE2+1
         LA    R5,0(R5)
         LA    R6,0(R6)
         SPACE 1
COMPARE2 CLC   SCBFROM(0),0(R5)
         BE    PHASE3GM
         LA    R5,1(R5)
COMP2BMP BCT   R6,COMPARE2
         SPACE 1
PHASE3SE CLI   LASTSCB,C'1'
         BNE   NEXTSCAN
         MVI   LASTSCB,C'0'
         CLI   CHANGEON,C'0'
         BE    PHASE3D
         CLI   CHANGEON,C'2'
         BE    PHASE3D
         PUT   SYSUT2,CARD
         B     PHASE3D
         EJECT
PHASE3GM ST    R5,SAVE5
         ST    R6,SAVE6
         CLI   PRINTDC,C'1'
         BE    PHASE3M1
         SPACE 1
         CLI   SCBACD,C'C'
         BNE   PHASE3M1
         SPACE 1
         MVI   SCANLNCC,C'0'
         MVC   SCANLNPN,FINDNAME
         MVC   SCANLND1,CARD
         MVC   SCANLND2,=CL2'  '
         MVC   SCANLND3,=CL9'NO CHANGE'
         MVC   SCANLND4,=CL22'ORIGINAL CARD ********'
         SPACE 1
         PUT   SYSPRINT,SCANLINE
         SPACE 1
         MVI   PRINTDC,C'1'
         MVI   SCANLINE,X'40'
         MVC   SCANLINE+1(132),SCANLINE
         SPACE 1
         PUT   SYSPRINT,SCANLINE
         SPACE 1
         AP    LINES,=P'3'
         CP    LINES,LINESMAX
         BNH   PHASE3M1
         BAL   R9,HEADING1
         SPACE 1
PHASE3M1 CLI   SCBACD,C'C'
         BNE   PHASE3M2
         SPACE 1
         CLI   CHANGEON,C'2'
         BE    PHASE3MD
         SPACE 1
         L     R2,SCBFLEN
         L     R3,SCBTLEN
         LA    R2,0(R2)
         LA    R3,0(R3)
         CR    R2,R3
         BE    R2EQR3
         BH    R2GTR3
         BL    R2LTR3
         EJECT
R2EQR3   STC   R3,MOVE2+1          FROM  =  TO.
MOVE2    MVC   0(0,R5),SCBTO
         B     PHASE3M3
         SPACE 1
R2GTR3   EQU   *                   FROM GREATER THAN TO.
         SPACE 1
*        R2 = SCBFLEN-1
*        R3 = SCBTLEN-1
*        R5    POINTS TO 1ST CHAR OF FIELD TO CHANGE.
         SPACE 1
         MVC   SAVECARD,CARD
         MVC   SAVESEQ(9),CARD+71
         MVC   SAVECARD+71(9),BLANKS
         SPACE 1
         STC   R3,MOVE3+1
MOVE3    MVC   0(0,R5),SCBTO
         AR    R5,R3
         LA    R5,1(R5)
         SR    R2,R3
         SPACE 1
         LA    R4,CARD+79
         SR    R4,R5
         BCTR  R4,0
         STC   R4,MOVE4+1
         STC   R4,MOVE5+1
MOVE4    MVC   0(0,R5),BLANKS
         SPACE 1
         LA    R3,SAVECARD+79
         LA    R4,1(R4)
         SR    R3,R4
         AR    R3,R2
MOVE5    MVC   0(0,R5),0(R3)
         MVC   CARD+71(9),SAVESEQ
         SPACE 1
         B     PHASE3M3
         SPACE 1
         EJECT
R2LTR3   EQU   *                   FROM LESS THAN TO.
         SPACE 1
*        R2 = SCBFLEN-1
*        R3 = SCBTLEN-1
*        R5 POINTS TO 1ST CHAR IN FIELD TO CHANGE.
         SPACE 1
         MVC   SAVECARD,CARD
         MVC   SAVECARD+71(9),BLANKS
         MVC   SAVESEQ(9),CARD+71
         SPACE 1
         LA    R3,0(R3)
         LA    R4,CARD+71
         SR    R4,R5
         LA    R4,0(R4)
         CR    R3,R4
         BH    OVERFLOW
         SPACE 1
         STC   R3,MOVE6+1
MOVE6    MVC   0(0,R5),SCBTO
         AR    R5,R3
         LA    R5,1(R5)
         SR    R3,R2
         SPACE 1
         LA    R4,CARD+79
         SR    R4,R5
         BCTR  R4,0
         STC   R4,MOVE7+1
         STC   R4,MOVE8+1
MOVE7    MVC   0(0,R5),BLANKS
         SPACE 1
         LA    R2,SAVECARD+79
         LA    R4,1(R4)
         SR    R2,R4
         SR    R2,R3
MOVE8    MVC   0(0,R5),0(R2)
         SPACE 1
         CLI   CARD+71,C' '
         BNE   OVERFLOW
         MVC   CARD+71,SAVESEQ
         B     PHASE3M3
OVERFLOW MVC   SCANLND4(22),=CL22'OVERFLOW CONDITION    '
         MVC   CARD,SAVECARD
         MVC   CARD+71(9),SAVESEQ
         B     PHASE3M2
         EJECT
PHASE3M2 MVC   SCANLND3,=CL9'NO CHANGE'
         B     PHASE3M4
PHASE3M3 MVC   SCANLND3,=CL9'*CHANGED*'
         AP    CHANGTOT,=P'1'
         SPACE 1
PHASE3M4 MVC   SCANLND1,CARD
         MVC   SCANLND2,SCBID
         MVC   SCANLNPN,FINDNAME
PHASE3M6 EQU   *
         SPACE 1
         PUT   SYSPRINT,SCANLINE
         SPACE 1
         MVI   SCANLINE,X'40'
         MVC   SCANLINE+1(132),SCANLINE
         AP    LINES,=P'1'
         CP    LINES,LINESMAX
         BNH   PHASE3MR
         BAL   R9,HEADING1
         B     PHASE3MR
         SPACE 2
PHASE3MD MVC   SCANLND4(22),=CL22'DELETE HAS BEEN DONE'
         B     PHASE3M2
         SPACE 2
PHASE3MR L     R5,SAVE5
         L     R6,SAVE6
         CLC   SAVECARD,CARD
         BNE   CHGMADE
         L     R1,SCBFLEN
         B     P3MRX
CHGMADE  L     R1,SCBTLEN
P3MRX    LA    R1,1(R1)
         LA    R1,0(R1)
         AR    R5,R1
         CR    R1,R6
         BH    PHASE3SE
         SR    R6,R1
         B     COMPARE2
         EJECT
* ROUTINE TO GENERATE IEHPROGM SCRATCH CONTROL CARDS FOR DELETE OPTION.
         SPACE 1
DELTOPT  EQU   *
         CLI   PUNCHSC,C'1'
         BE    DELTEXIT
         SPACE 1
         MVC   SCTHMEM,=CL9' '
         MVC   SCTHMEM(8),FINDNAME
         SPACE 1
         LA    R1,9
         LA    R2,SCTHMEM
DELTLOOP CLI   0(R2),C' '
         BE    DELTBLNK
         LA    R2,1(R2)
         BCT   R1,DELTLOOP
         SPACE 1
DELTBLNK MVI   0(R2),C','
         SPACE 1
         PUT   SYSUT2,SCTHCRD1
         SPACE 1
         PUT   SYSUT2,SCTHCRD2
         SPACE 1
         MVI   PUNCHSC,C'1'
         SPACE 1
DELTEXIT EQU   *
         BR    R9
         EJECT
* ROUTINE TO SET UP AND ISSUE THE FIND MACRO FOR A BPAM READ.
         SPACE 1
FINDMBR  EQU   *
         SPACE 1
         CP    LINES,LINESMAX
         BNH   FINDMEM1
         LR    R8,R9
         BAL   R9,HEADING1
         LR    R9,R8
         SPACE 1
FINDMEM1 L     R2,GM2CNT
         C     R2,=F'00'
         BE    FINDMEM3
         SPACE 1
         L     R3,POINTER1         LOAD TABLE POINTER.
         MVC   FINDNAME(8),0(R3)
         LA    R3,8(R3)
         BCTR  R2,0
         ST    R2,GM2CNT
         ST    R3,POINTER1
         SPACE 1
         FIND  SYSUT1,FINDNAME,D   LOCATE FOR READ MACRO.
         SPACE 2
         C     R15,=F'00'
         BE    FINDMEM2
         SPACE 1
         C     R15,=F'04'
         BE    FERROR1
         SPACE 1
         C     R15,=F'08'
         BE    FERROR2
         SPACE 2
FERROR1  MVC   FERROR1M+14(8),FINDNAME
         PUT   SYSPRINT,FERROR1M
         AP    LINES,=P'2'
         B     FINDMBR
         SPACE 1
FERROR2  MVC   FERROR2M+44(8),FINDNAME
         PUT   SYSPRINT,FERROR2M
         AP    LINES,=P'2'
         B     FINDMBR
         SPACE 1
FINDMEM2 EQU   *
         LA    R15,0
         BR    R9
         SPACE 1
FINDMEM3 LA    R15,4
         BR    R9
         EJECT
* ROUTINE TO CLEAR INPUT AREA FOR READ MACRO.
         SPACE 1
CLINAREA EQU   *
         SPACE 1
         L     R2,CLOOPCNT
         L     R3,GM3ADDR
         SPACE 1
INITAREA MVC   0(80,R3),INITFFS
         LA    R3,80(R3)
         BCT   R2,INITAREA
         SPACE 1
         MVC   DEBLKCNT,CLOOPCNT
         MVC   DEBLKPTR,GM3ADDR
         SPACE 1
         BR    R9
         EJECT
* ROUTINE TO READ THE PROCLIB, AND ON FIRST READ FOR A PROC, NOTE.
         SPACE 1
RDABLOCK EQU   *
         SPACE 1
         L     R2,GM3ADDR
         SPACE 1
         READ  READECB,SF,SYSUT1,(2),'S'
         SPACE 1
CHECKRD  CHECK READECB
         SPACE 1
         B     RDABLKBR
ENDOPROC MVI   EOPSWTCH,C'1'
         SPACE 1
RDABLKBR BR    R9
         EJECT
* SYNAD ROUTINE FOR A BAD BRANCH.
         SPACE 1
REOPEN   EQU   *
         WTO   'SYNAD ROUTINE ENTERED'
         CLOSE (SYSUT1)
         OPEN  (SYSUT1,(INPUT))
REFIND   FIND  SYSUT1,FINDNAME,D
         C     R15,=F'00'
         BNE   REFIND
         WTO   'SYNAD ROUTINE COMPLETE'
         B     RDABLOCK
         EJECT
* ROUTINE FOR DEBLOCKING INPUT AREA FROM READ MACRO.
         SPACE 1
GETACARD EQU   *
         SPACE 1
         L     R2,DEBLKCNT
         L     R3,DEBLKPTR
         SPACE 1
         C     R2,=F'00'
         BE    GETCARD1
         CLC   0(2,R3),=XL2'FFFF'
         BE    GETCARD2
         SPACE 1
         MVC   CARD(80),0(R3)
         LA    R3,80(R3)
         BCTR  R2,0
         ST    R3,DEBLKPTR
         ST    R2,DEBLKCNT
         SPACE 1
         LA    R15,0               MOVED A CARD.
         MVI   PRINTDC,C'0'
         MVI   PUNCHOFF,C'1'
         CLI   SWITCH2,C'0'
         BE    GETCARD3
         MVI   PUNCHOFF,C'0'
GETCARD3 BR    R9
         SPACE 1
GETCARD1 LA    R15,4               INPUT AREA EMPTY.
         BR    R9
         SPACE 1
GETCARD2 LA    R15,8               END OF PROCEDURE.
         BR    R9
         EJECT
* ROUTINE TO SETUP REGISTERS FOR A SCAN LOOP.
         SPACE 1
REGSETUP EQU *
         SPACE 1
         LA    R2,0
         L     R4,SCBSCOL
         AR    R2,R4
         L     R4,SCBSLEN
         AR    R2,R4
         L     R4,SCBFLEN
         AR    R2,R4
         LA    R3,0
         C     R2,=F'80'
         BNH   SETUP1
         SPACE 1
         LA    R4,80
         SR    R2,R4
         LR    R3,R2
         SPACE 1
SETUP1   LA    R5,CARD
         L     R2,SCBSCOL
         AR    R5,R2
         L     R6,SCBSLEN
         SR    R6,R3
         SPACE 1
         BR    R9
         EJECT
EOJPHASE EQU   *
         SPACE 1
         MVI   HEADLN2,X'40'
         MVC   HEADLN2+1(132),HEADLN2
         BAL   R9,HEADING1
         SPACE 1
         MVC   MSG1CNT(5),=CL5'     '
         MVC   MESSAGE+1(27),=CL27'TOTAL SCANS PROCESSED.'
         UNPK  MSG1CNT+5(8),REALTOT
         OI    MSG1CNT+12,X'F0'
         PUT   SYSPRINT,MESSAGE
         SPACE 1
         MVC   MESSAGE+1(27),=CL27'TOTAL CARDS PROCESSED.'
         UNPK  MSG1CNT+5(8),SCANCNT
         OI    MSG1CNT+12,X'F0'
         PUT   SYSPRINT,MESSAGE
         SPACE 1
         MVC   MESSAGE+1(27),=CL27'TOTAL CHANGES MADE.'
         UNPK  MSG1CNT+5(8),CHANGTOT
         OI    MSG1CNT+12,X'F0'
         PUT   SYSPRINT,MESSAGE
         EJECT
*  CODE TO LINK IEBUPDTE AND CHANGE PDS.
         SPACE 2
UPDATSW  B     EXITPGM
         SPACE 2
         CLOSE (SYSUT1)
         CLOSE (SYSUT2)
         CLOSE (SYSPRINT)
         CLOSE (SYSPUNCH)
         SPACE 2
         LINK  EP=IEBUPDTE,PARAM=(PARMFLD,DDNAMLST),VL=1
         SPACE 2
         CH    R15,=H'0'
         BNE   KILLJOB1
         B     EXITPGM
         EJECT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=EOFSYSIN,          C
               RECFM=FB,LRECL=80
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      C
               RECFM=FBA,LRECL=133,BLKSIZE=133
         EJECT
DRECTORY DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,EODAD=ENDPDSD,          C
               RECFM=F,LRECL=256,BLKSIZE=256,EXLST=EXIT
         EJECT
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=R,EODAD=ENDOPROC,          C
               SYNAD=REOPEN
         EJECT
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(GM,PM),EODAD=NMENTRYS,    C
               RECFM=FB,LRECL=80,BLKSIZE=800
         EJECT
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=(PM),                    C
               RECFM=FB,LRECL=80,BLKSIZE=80
         EJECT
* SAVEAREA, MISC VARIABLES AND WORK FIELDS.
         SPACE 1
SAVEAREA DC    18F'0'
         SPACE 1
EF1      DC    XL2'0000'
EF2      DC    XL4'00000000'
WORK1    DS    D
SCANS    DC    PL2'0'
PREFIX   DS    CL8
PAGE#    DC    PL3'0'
LINES    DC    PL2'0'
LINESMAX DC    PL2'56'
WORK2    DS    PL2
WORK3    DS    PL2
SCANCNT  DC    PL4'0'
CHANGTOT DC    PL4'0'
REALTOT  DC    PL3'0'
MEMCNT   DC    PL3'0'
MEMKEPT  DC    PL3'0'
ALIASCNT DC    PL3'0'
INITFFS  DC    80XL1'FF'
CARD     DC    CL80' '
SAVECARD DC    CL80' '
BLANKS   DC    80CL1' '
SAVESEQ  DS    CL9
         SPACE 3
*        ADCONS
         DS    0F
GM1LEN   DS    F
GM1ADDR1 DS    F
GM1ADDR2 DS    F
GM2CNT   DS    F
GM2LEN   DS    F
GM2ADDR  DS    F
GM3LEN   DS    F
GM3ADDR  DS    F
DEBLKPTR DS    F
DEBLKCNT DS    F
CLOOPCNT DS    F
POINTER1 DS    F                   MEMBER NAME TABLE POINTER.
SAVE5    DS    F
SAVE6    DS    F
FINDNAME DS    D
UT1BLKSZ EQU   SYSUT1+62
         EJECT
*        SWITCHES
         SPACE 1
KILLSW1  DC    CL1'0'    FAIL JOB BECAUSE OF BAD CONTROL CARDS.
SWBRANCH DC    CL1'0'
SWITCH1  DC    CL1'0'    PEFIX OPTION SWITCH.
SWITCH2  DC    CL1'0'    PUNCH OPTION SWITCH.
OKFILLSW DC    CL1'0'
MVD1SCB  DC    CL1'0'
EOPSWTCH DC    CL1'0'
ACTIONON DC    CL1'0'
LASTSCB  DC    CL1'0'
CHANGEON DC    CL1'0'
PUNCHCC  DC    CL1'0'
PRINTDC  DC    CL1'0'
PUNCHOFF DC    CL1'0'
PUNCHSC  DC    CL1'0'
VSON     DC    PL1'0'
         SPACE 3
         DS    0F
EXIT     DC    X'87',AL3(JFCB)
JFCB     DC    44F'0'
         SPACE 1
JFCBDSNM EQU   JFCB
JFCBVOLS EQU   JFCB+118
         EJECT
*        MESSAGES USED BY PROGRAM
         SPACE 1
ERROR1   DC    CL47'INVALID SCANS CARD, NON NUMERIC VALUE.  *ABORT*'
ERROR2   DC    CL47'MORE THAN ONE SCANS CONTROL CARD.'
ERROR3   DC    CL47'INVALID CONTROL CARD, BYPASSED.'
ERROR4   DC    CL47'INVALID SCANXX CARD, NON NUMERIC VALUE(S).'
ERROR5   DC    CL47'INVALID SCANXX CARD, SC + SL > 72.'
ERROR6   DC    CL47'****NOTE  SCAN XX DELETED,  INCOMPLETE SCB.'
ERROR7   DC    CL47'****NOTE  SCAN XX DELETED, SCANS TOTAL EXCEEDED'
ERROR8   DC    CL47'INVALID SCAN ID, DELETED.'
ERROR9   DC    CL47'INVALID EDIT STRING, DELETED.'
ERROR10  DC    CL47'EXCESS FROM/TO CONTROL CARDS, DELETE THIS ONE.'
ERROR11  DC    CL47'INVALID DSNAME, DELETED.'
ERROR12  DC    CL47'INVALID UNIT TYPE, DELETED.'
WARNING1 DC    CL47'MORE THAN ONE PREFIX CARD, LAST ONE USED'
WARNING2 DC    CL47'CONTROL CARD ACCEPTED, NO MORE WILL BE ACCEPTED'
         SPACE 1
FERROR1M DS    0CL133
         DC    CL1'0'
         DC    CL45'***** MEMBER XXXXXXXX NOT FOUND IN DIRECTORY.'
         DC    CL40'  CONTINUE PROCESSING WITH NEXT MEMBER. '
         DC    CL47' '
         SPACE 1
FERROR2M DS    0CL133
         DC    CL1'0'
         DC    CL43'***** I/O ERROR WHILE SEARCHING FOR MEMBER '
         DC    CL48'XXXXXXXX.  CONTINUE PROCESSING WITH NEXT MEMBER.'
         DC    CL41' '
         SPACE 5
         DS    0F
SCB      DS    0CL120    SCAN CONTROL BLOCK
SCBID    DS    CL2       SCB ID
SCBACD   DS    CL1       SCB ACTION CODE  'D' = DELETE 'C' = CHANGE
SCBEXTRA DS    CL1' '    EXTRA BYTE FOR FUTURE USE IF NEEDED.
SCBSCOL  DS    F         SCB START SCAN COL.
SCBSLEN  DS    F         SCB SCAN LENGTH.
SCBFLEN  DS    F         SCB COMPARE TO LENGTH
SCBTLEN  DS    F         SCB CHANGE TO LENGTH
SCBFROM  DS    CL40      SCB COMPARE TO FIELD
SCBTO    DS    CL40      SCB CHANGE TO FIELD.
SCBNSCB  DS    F         CHAIN POINTER TO NEXT SCB.
SCBFUTUR DS    CL12
SCBFILL  DC    CL4' '    SCB TRAILER FLAG,  SET TO 'SEND' ON LAST SCB.
         EJECT
*        INPUT  AREAS
         SPACE 1
PARMCARD DS    0CL80
CARDID   DS    CL6
         DS    CL1
VALUE    DS    CL73
         SPACE 1
BLOCK    DS    CL256
         SPACE 1
ENTRYREC DS    0CL80
ENTRY    DS    CL8' '
         DC    CL72' '
         SPACE 2
*        PRINT LINES, HEADINGS.
         SPACE 2
EDITLINE DS    0CL133
ELCC     DC    CL1'0'
ELDATA1  DS    CL47
         DC    CL5' '
ELDATA2  DC    CL47' '
         DC    CL33' '
     SPACE 2
SCANLINE DS    0CL133
SCANLNCC DC    CL1' '
         DC    CL1' '
SCANLNPN DS    CL8
         DC    CL1' '
SCANLND1 DS    CL80
         DC    CL5' '
SCANLND2 DS    CL2
         DC    CL2' '
SCANLND3 DS    CL9
         DC    CL2' '
SCANLND4 DC    CL22' '
         EJECT
HEADLN1  DS    0CL133
         DC    CL1'1'
         DC    CL7'DATE = '
H1DATE   DC    CL5' '
         DC    CL28' '
         DC    CL49'         D A T A   S C A N   E D I T O R         '
         DC    CL33' '
         DC    CL7'PAGE = '
PAGENO   DC    CL3' '
         SPACE 2
HEADLN1A DS    0CL133
         DC    CL1'0'
         DC    CL132' '
         SPACE 2
HEADLN2  DS    0CL133
         DC    CL1'0'
         DC    CL47'------ -------- C A R D  I M A G E ------------'
         DC    CL5' '
         DC    CL47'---------- E R R O R S ------------------------'
         DC    CL33' '
         SPACE 2
HEADLN3  DS    0CL133
         DC    CL1'0'
         DC    CL10' --PROC-- '
         DC    CL45'------------------------------ C A R D  I M A'
         DC    CL35' G E ------------------------------'
         DC    CL4' '
         DC    CL5'S.ID '
         DC    CL9' ACTION  '
         DC    CL2' '
         DC    CL22'--- C O M M E N T ---'
         SPACE 1
TERMMSG  DS    0CL133
         DC    CL1'0'
         DC    CL132'***** DATA SCAN EDITOR TERMINATED, TERMINAL ERRORSC
                IN THE PARAMETER CARDS, OR THERE WERE NO MEMBERS SELECTC
               ED TO PROCESS. *****'
         SPACE 2
IEBCC    DS    0CL80
         DC    CL5'./   '
         DC    CL20'ADD   LIST=ALL,NAME='
IEBCCNAM DS    CL8
         DC    CL47' '
         EJECT
MESSAGE  DS    0CL133
         DC    CL1' '
         DC    CL27'TOTAL MEMBERS IN PROCLIB = '
MSG1CNT  DS    CL5
         DC    CL100' '
         SPACE 1
MESSAGE2 DS    0CL33
         DC    CL1' '
         DC    CL27'TOTAL MEMBERS SELECTED   = '
MSG2CNT  DS    CL5
         SPACE 1
MESSAGE3 DS    0CL33
         DC    CL1' '
         DC    CL27'TOTAL ALIAS NAMES FOUND  = '
MSG3CNT  DS    CL5
         SPACE 1
MESSAGE4 DS    0CL33
         DC    CL1' '
         DC    CL27'TOTAL REAL MEMBERS       = '
MSG4CNT  DS    CL5
         EJECT
SCTHCRD1 DS    0CL80
         DC    CL19'      SCRATCH  VOL='
SCTHUNIT DS    CL4
         DC    CL1'='
SCTHVOL  DS    CL6
         DC    CL8',MEMBER='
SCTHMEM  DS    CL9
         DC    CL24' '
         DC    CL1'C'
         DC    CL8'SCTHCRD1'
         SPACE 1
SCTHCRD2 DS    0CL80
         DC    CL15' '
         DC    CL7'DSNAME='
SCTHDSN  DC    CL44' '
         DC    CL6' '
         DC    CL8'SCTHCRD2'
         SPACE 2
UTABLE   DC    CL4'2311' 1
         DC    CL4'2301' 2
         DC    CL4'2303' 3
         DC    CL4'2302' 4
         DC    CL4'2321' 5
         DC    CL4'2305' 6
         DC    CL4'2305' 7
         DC    CL4'2314' 8
         DC    CL4'3330' 9
         DC    CL4'3340' A   FUTURE ENTRY.
         DC    CL4'3350' B
         DC    CL4'XXXX' C   FUTURE ENTRY.
         DC    CL4'3330' D   FUTURE ENTRY.
         DC    CL4'XXXX' E   FUTURE ENTRY.
         DC    CL4'XXXX' F   FUTURE ENTRY.
         SPACE 2
         CNOP  2,4
PARMFLD  DC    H'3'
         DC    CL3'NEW'
         CNOP  2,4
DDNAMLST DC    H'72'
         DC    32X'0'
         DC    CL8'SYSUT2  '       SYSIN    FILE
IEBPRINT DC    CL8'SYSPRINT'
         DC    XL16'0'
         DC    CL8'SYSUT1  '       SYSUT2   FILE
         SPACE 3
         END
