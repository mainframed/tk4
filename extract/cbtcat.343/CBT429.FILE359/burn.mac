BURN     TITLE 'BURN - ABEND A JOB OR ADDRESS SPACE'
BURN     CSECT
         REGS
RSAVE    EQU    13
RBASE    EQU    12
RCVT     EQU    11
RASCB    EQU    10
RASVT    EQU    9
RCNT     EQU    8
RCPPL    EQU    7
RPSCB    EQU    6
         SPACE  5
         SAVE   (14,12),,BURN_&SYSDATE_&SYSTIME
         BALR   RBASE,0
         USING  *,RBASE
         USING  CVT,RCVT
         USING  ASCB,RASCB
         USING  CPPL,RCPPL
         USING  PSCB,RPSCB
         LA     R2,SAVEAREA
         ST     RSAVE,SAVEAREA+4
         ST     R2,8(RSAVE)
         LR     RSAVE,R2
*
*        VALIDATE USER
*
VALIDATE LR    RCPPL,R1        SAVE CPPL POINTER
         L     RPSCB,CPPLPSCB
         TM    PSCBATR1,PSCBCTRL DOES USER HAVE OPERATOR PRIV?
         BNO   RETURN            NO SO RETURN QUIT
*
*        EXAMINE PARAMETER PASSED
*
         L     R2,0(R1)        PARAMETER POINTER
         LH    R1,0(R2)        PARAMETER LENGTH
         SH    R1,=H'4'        PARAMETER FIELD LENGTH
         LH    R3,2(R2)        CMD LENGTH
         SR    R1,R3           SUBTRACT FROM TOTAL
         LA    R2,4(R2)        POINT TO COMMAND
         BCTR  R3,0            REDUCE FOR COMPARE
         EX    R3,CLCCMD
         BNE   ERROR
         AR    R2,R3          SET R2 TO POINT TO ASID PARAMETER
         LA    R2,1(R2)
         LTR   R4,R1           INPUT LENGTH ZERO?
         BZ    ERROR
         CH    R1,K8           LESS THAN 8 BYTES?
         BH    ERROR
         MVC   INNAME,BLANKS   BLANKS JOBNAME/ASID MSG ENTRY
         BCTR  R1,0            DECREMENT FOR MVC
         EX    R1,MOVE         MOVE JOBNAME/ASID TO MESSAGE
*
*       ASID REQUEST TEST
*
         LR     R2,R4
         LA     R3,INNAME       INITIALIZE JOBNAME/ASID POINTER
ASIDTEST DS     0H
         TM     0(R3),X'F0'      NUMERIC CHARACTER
         BNO    JOBNAME         ASSUME JOBNAME
         LA     R3,1(R3)        INCREMENT TO NEXT CHARACTER
         BCT    R2,ASIDTEST
*
*       ASID REQUEST TEST
*
         O     R1,K70      PREPARE PACK EX REG
         EX    R1,PACK
         CVB   R3,PAREA    CONVERT TO BINARY
         STH   R3,ASIDSAVE
         SLL   R3,2        MULTIPLY BY 4
         L     RCVT,16     CVT POINTER
         L     RASVT,CVTASVT    ASVT POINTER
ASCBLOAD L     RASCB,524(R3,RASVT)  ASCB ENTRY
         B     GODOIT
*        ASCB ADDRESS SEARCH
*
JOBNAME  DS    0H
         OC    INNAME,BLANKS     REMOVE ZEROS AND LOWER CASE
         L     RCVT,16
         L     RASVT,CVTASVT
         L     RCNT,516(RASVT)
         LA    RASVT,528(RASVT)
         B     ERROR
GODOIT   DS    0H
         LH    R2,ASCBASID
         CH    R2,=H'2'
         BL    ERROR           DON'T BOMB THE SYSTEM
         CH    R2,ASIDSAVE     BE SURE OF THE RIGHT GUY
         BNE   ERROR
         L     R4,ASCBTSB      CHECK TO SEE IF TSO USER
         LTR   R4,R4
         BZ    ASKIFOK         NO
         L     R4,ASCBJBNS     CHECK TO SEE IF TSO USER IS A STARTING
         LTR   R4,R4
         BZ    BURNOK          YES
ASKIFOK  TPUT  ASKMSG,40
         TGET  REPLY,3
         OI    REPLY,C' '      MAKE IT A CAPITAL Y
         CLI   REPLY,C'Y'
         BNE   ERROR0
BURNOK   DS    0H
         AUTH  ON
         MODESET MODE=SUP,KEY=ZERO
         L     R1,ASCBASXB
         L     R1,4(R1)
         L     R0,136(R1)
         L     R1,=X'88222000'
         SLR   R3,R3
         L     R15,CVTBTERM
         L     R15,44(R15)
         BALR  R14,R15
         MODESET KEY=NZERO,MODE=PROB
         AUTH  OFF
RETURN   DS    0H
         L     RSAVE,SAVEAREA+4
         RETURN (14,12)
ERROR0   DS    0H
         MVC   ERRMSG+24(8),=CL8'BURNED'
ERROR    DS    0H
         MVC   ERRMSG+11(8),INNAME
         TPUT  ERRMSG,50
         B     RETURN
MOVE     MVC   INNAME(8),0(R2)      IF A TSO COMMAND PARAMETER
CLCCMD   CLC   0(0,R2),BURNCMD      IS IT BURN COMMAND?
PACK     PACK  PAREA(0),INNAME(0)
K8       DC    H'8'
PAREA    DC    D'0'
K70      DC    X'00000070'
SAVEAREA DC    18F'0'
BLANKS   DC    CL8' '
INNAME   DC    CL8' '
ASIDSAVE DC    H'0'
BURNCMD  DC    CL5'BURN '
ASKMSG   DC    CL40'NOT A TSO USER STARTING. OK?'
REPLY    DS    CL10
ERRMSG   DS    0CL50
         ORG   ERRMSG
         DC    CL50' '
         ORG   ERRMSG
EMSG     DC    C'BURN000I:           NOT FOUND'
         ORG
         CVT   DSECT=YES,LIST=YES
 EJECT
         IHAASCB
 EJECT
         IKJCPPL
 EJECT
         IKJPSCB
         END   BURN
