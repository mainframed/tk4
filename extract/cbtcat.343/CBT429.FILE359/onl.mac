ONL      TITLE 'ONLINE DISPLAY AND OPERATOR INTERFACE, VERSION 5.2'
         @      ONL    MODULE NAME IN BLOCK LETTERS         *HMD 01/85*
*******************************************************************
**  ONL IS A SYSTEMS OPERATOR INTERFACE PROGRAM THAT ALLOWS      **
**  SYSTEM PROGRAMMERS TO ENTER OPERATOR COMMANDS FROM A TSO     **
**  TERMINAL. THIS PROGRAM ISSUES SVC 34 TO INTERFACE WITH THE   **
**  MASTER SCHEDULER AND MUST BE AUTHORIZED THROUGH THE LINKAGE  **
**  EDITOR AND THE IKJEFTE2 TSO COMMAND TABLE.                   **
**                                                               **
**  VERSION 5.0 - HOWARD M. DEAN - GTE DATA SERVICES             **
**  VERSION 5.1 - HOWARD M. DEAN - GTE DATA SERVICES             **
**  VERSION 5.2 - HOWARD M. DEAN - SYNTEX U.S.A.                 **
*******************************************************************
         EJECT
ON       TSOENTER REGS=YES,PL=(CPPL,IO,CSCAN),WORK=WORKSIZE,SP=1
         TM    0(R2),X'80'    CHECK IF CALLED AS CMD OR WITH CALL OR $
         BO    SSSS           NO CPPL, SO ABEND             *HMD 08/81*
         MVI   FLAGS,0        CLEAR COMMAND FLAGS           *HMD 02/82*
         L     R1,CPPLPSCB    GET PSCB ADDRESS              *HMD 08/81*
         ST    R1,PSCBADDR    SAVE PSCB ADDRESS             *HMD 04/82*
         LUSE  PSCB,R1        ADDRESS THE DSECT             *HMD 08/81*
         TM    PSCBATR1,PSCBCTRL      OPERATOR AUTHORITY?   *HMD 08/81*
         BZ    NOTAUTH        NO, NOT AUTHORIZED            *HMD 08/81*
         LDROP R1                                           *HMD 08/81*
         TESTAUTH FCTN=1      ARE WE APF AUTHORIZED?        *HMD 04/82*
         LTR   R15,R15        WELL, ARE WE?                 *HMD 04/82*
         BNZ   NOTAPF         NO, DO NOT SET FLAG           *HMD 04/82*
         OI    FLAGS,FAUTH    SET APF AUTHORIZED            *HMD 04/82*
NOTAPF   DS    0H                                           *HMD 04/82*
         EJECT
** GET ASID FOR SVC 34
         XC    TJIDLIST,TJIDLIST        CLEAR LIST FORM OF MACRO
         EXTRACT TJIDTEST,FIELDS=(TJID),MF=(E,TJIDLIST)
         OC    TJIDTEST+2(2),TJIDTEST+2 IS IT THERE?
         BZ    CHKBUF                   NO                  *HMD 07/82*
         OI    TJIDTEST+2,X'80'         SET FOR CMD SCHEDULER
** CHECK IF INITIAL COMMAND ISSUED                          *HMD 03/82*
CHKBUF   DS    0H                                           *HMD 03/82*
         LUSE  ECT,R1                   ADDRESS THE DSECT   *HMD 03/82*
         LUSE  IKJEBECA,R9              FOR EDIT ONLY       *HMD 03/82*
         MVC   INPUT,SPACES             CLEAR INPUT BUFFER  *HMD 03/82*
         L     R1,CPPLECT               GET ENVIRON CNTL TBL*HMD 03/82*
         L     R15,CPPLCBUF             POINT TO CMD BUFFER *HMD 03/82*
         CLC   ECTPCMD,=CL8'E'          ARE WE IN EDIT?     *HMD 03/82*
         BE    CHKEDIT                  CHECK FOR EDIT      *HMD 03/82*
         CLC   ECTPCMD,=CL8'EDIT'       ARE WE IN EDIT?     *HMD 03/82*
         BE    CHKEDIT                  NO, NORMAL CHECK    *HMD 03/82*
         CLC   ECTPCMD,=CL8'IKJEBEIN'   WEIRD EDIT MODE?    *HMD 03/82*
         BNE   CHKNEDIT                 NOT IN EDIT         *HMD 03/82*
CHKEDIT  L     R15,CAPTIBFR             GET INPUT BUF ADDR  *HMD 03/82*
         TM    CAPTIBFR,CAOPERND        OPERAND IN BUFFER?  *HMD 03/82*
         BZ    START                    NO, PROMPT USER     *HMD 03/82*
         B     GOTCMD                   YES, PROCESS IT     *HMD 03/82*
CHKNEDIT TM    ECTSWS,ECTNOPD           OPERAND IN BUFFER   *HMD 03/82*
         BO    START                    NO, PROMPT USER     *HMD 03/82*
GOTCMD   OI    FLAGS,FCMD               INDICATE COMMAND    *HMD 03/82*
         LH    R14,2(R15)               GET OFFSET TO CMD   *HMD 03/82*
         LA    R2,4(R14,R15)            POINT TO COMMAND    *HMD 03/82*
         LH    R3,0(R15)                GET LINE LENGTH     *HMD 03/82*
         SR    R3,R14                   GET COMMAND LENGTH  *HMD 03/82*
         SH    R3,=H'5'                 ACCOUNT FOR PREFIX  *HMD 03/82*
         BM    START                    PROMPT IF INVALID   *HMD 03/82*
         CH    R3,=H'126'               LINE TOO LONG?      *HMD 03/82*
         BNH   *+8                      NO, PROCESS         *HMD 03/82*
         LA    R3,126                   YES, ADJUST LENGTH  *HMD 03/82*
         EX    R3,MOVECBUF              MOVE COMMAND        *HMD 03/82*
         B     DOINPUT                  PROCESS COMMAND     *HMD 03/82*
         LDROP R1                                           *HMD 03/82*
         LDROP R9                                           *HMD 03/82*
         EJECT
START    NI    FLAGS,255-FSTART         CLEAR START FLAGS   *HMD 07/82*
         LA    R0,TSOECB
         ST    R0,IOPLECB
         XC    IOPLPTG(16),IOPLPTG
         XC    TSOECB,TSOECB            CLEAR EVENT CNTL BLOCK
         PUTGET  PARM=IOPLPTG,MF=(E,IOPL),                             X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               TERMGET=(EDIT,WAIT),                                    X
               OUTPUT=(OLD,SINGLE,MODE)
         C     R15,=F'4'
         BH    SSSS           ABEND,ERROR
         EJECT
CHKINP   LA    R11,IOPLPTG
         USING PGPB,R11
         L     R1,PGPBIBUF    INPUT BUFFER
         LH    R3,0(R1)       LENGTH INCLUDING 4 BYTE HDR
         CH    R3,=H'4'       IS IT NULL INPUT
         BE    START          YES, START OVER
         SH    R3,=H'5'
         BM    SSSS           ERROR
         C     R3,=F'126'     TOO LONG?                     *HMD 08/81*
         BNH   *+8            NO, OK                        *HMD 08/81*
         LA    R3,126         MOVE ONLY 127 CHARS           *HMD 08/81*
         EX    R3,MOVEIT      MOVE TO MY BUFFER
         LH    R0,0(R1)       LENGTH OF LINE INCLUDING HEADER
         LA    R15,1          SUBPOOL 1
         SLL   R15,24
         OR    R0,R15
         L     R1,PGPBIBUF
         FREEMAIN R,LV=(0),A=(1)      FREE INPUT BUFFR
         DROP  R11
         EJECT
**                                                          *HMD 03/82*
**       ANALYZE INPUT
**                                                          *HMD 03/82*
         SPACE 1
DOINPUT  DS    0H                     SETUP FOR CMD PROCESS *HMD 03/82*
         OC    INPUT,SPACES        TRANSLATE TO CAPS
         MVC   COMMAND,SPACES      CLEAR COMMAND BUFFER
         LA    R2,1(R3)            CMD LENGTH TO R2
         LTR   R2,R2                    TEST LENGTH
         BNP   CKFF                                         *HMD 03/82*
         LA    R15,INPUT                START
CK01     CLI   0(R15),C' '              SUPPRESS BLANKS
         BNE   CK02                     NO
         SH    R2,=H'1'                 SUBTRACT ONE
         BNP   CKFF                     ALL BLANKS          *HMD 03/82*
         LA    R15,1(,R15)              GET NEXT POSITION
         B     CK01                     TRY AGAIN
CK02     LR    R3,R2                    GET CMD LENGTH IN R3
         SH    R3,=H'1'                 DOWN 1 FOR MACH LEN
         BM    CKFF                     COMMAND NOT REALLY THERE
         EX    R3,MOVECMD               MOVE IN COMMAND
         CLC   =C'END',COMMAND          IS THIS THE END?    *HMD 01/78*
         BE    END                      YES, BYE            *HMD 01/78*
         CLC   =C':OFF',COMMAND         TURN PASSWORDS OFF  *HMD 06/79*
         BE    PASSOFF                  YES                 *HMD 01/85*
         CLC   =C':ON',COMMAND          RESTORE PASSWORDS   *HMD 06/79*
         BE    PASSON                   YES                 *HMD 01/85*
         CLC   =C':NOC',COMMAND         SET CANCEL FLAG OFF?*HMD 02/82*
         BE    NONCAN                   YES                 *HMD 02/82*
         CLC   =C':CAN',COMMAND         SET CANCEL FLAG ON? *HMD 02/82*
         BE    CANDO                    YES                 *HMD 02/82*
         CLC   =C':NOT',COMMAND         TURN OF JS TIMING?  *HMD 04/82*
         BE    NOTIME                   YES                 *HMD 04/82*
         CLC   =C':TON',COMMAND         TURN ON JS TIMING?  *HMD 04/82*
         BE    YESTIME                  YES                 *HMD 04/82*
         CLC   =C':AC',COMMAND          TURN ON ACCOUNT     *HMD 07/82*
         BE    ACCOUNT                  YES                 *HMD 07/82*
         CLC   =C':NA',COMMAND          TURN OFF ACCOUNT    *HMD 07/82*
         BE    NACCOUNT                 YES                 *HMD 07/82*
         CLC   =C':MO',COMMAND          TURN ON MOUNT?      *HMD 01/85*
         BE    MOUNT                    YES                 *HMD 01/85*
         CLC   =C':NM',COMMAND          TURN OFF MOUNT?     *HMD 01/85*
         BE    NOMOUNT                  YES                 *HMD 01/85*
         CLC   =C':NOS',COMMAND         MAKE NON-SWAPPABLE? *HMD 01/85*
         BE    NOSWAP                   YES                 *HMD 01/85*
         CLC   =C':SWA',COMMAND         MAKE SWAPPABLE?     *HMD 01/85*
         BE    YESSWAP                  YES                 *HMD 01/85*
         XC    CMDLEN(4),CMDLEN         CLEAR COMMAND LEN   *HMD 01/78*
         LA    R2,4(,R2)                ADD LENGTH
         STH   R2,CMDLEN                SAVE LENGTH OF COMM *HMD 06/79*
         CLC   =H'5',CMDLEN             ONE CHAR. COMMAND?  *HMD 06/79*
         BNE   AROUND                   NO, CONTINUE        *HMD 06/79*
         CLI   COMMAND,C'E'             IS THE COMMAND 'E'? *HMD 06/79*
         BE    END                      MUST BE EXIT REQUEST*HMD 06/79*
AROUND   DS    0H
         XC    CSOUT(12),CSOUT          CLEAR CSOA
*                                                           *HMD 01/85*
* MVS 1.3.3  ALLOW "K" COMMANDS                             *HMD 01/85*
*                                                           *HMD 01/85*
         CLC   =C'LOGON ',COMMAND       LOGON COMMAND?      *HMD 07/82*
         BE    CKFF                     DON'T BE SILLY      *HMD 07/82*
         EJECT                                              *HMD 07/82*
*************************************************************HMD 07/82*
** THE FOLLOWING CODE IS NECESSARY THE ENABLE THIS COMMAND  *HMD 07/82*
** TO WORK UNDER MVS/SP RELEASE 3. THIS IS DUE TO A CHANGE  *HMD 07/82*
** IN THE COMMAND SCHEDULER CSCB CREATION MODULE IEE0803D.  *HMD 07/82*
**                                                          *HMD 07/82*
** IN MVS/SP 3, IF AN ASID IS PRESENT IN THE XSA, THEN THE  *HMD 07/82*
** COMMAND SCHEDULER MOVES YOUR TSO USERID INTO THE NEWLY   *HMD 07/82*
** CREATED CSCB. WHY THIS IS DONE, WHO KNOWS. IT CAUSES,    *HMD 07/82*
** HOWEVER, THE STC INITIALIZATION ROUTINE IEEPRWI2 TO      *HMD 07/82*
** THINK THAT A 'SYSTEM' ADDRESS SPACE IS BEING CREATED     *HMD 07/82*
** DUE TO THE CHKEY FIELD OF THE CSCB CONTAINING YOUR       *HMD 07/82*
** USERID INSTEAD OF ZERO. BECAUSE THIS ADDRESS SPACE       *HMD 07/82*
** INITIALIZATION ROUTINE NAME FOR SYSTEM ADDRESS SPACES    *HMD 07/82*
** IS CONTAINED IN THE CHKEY FIELD, IEEPRWI2 ATTEMPT TO     *HMD 07/82*
** LINK TO YOUR TSO USERID TO INITIALIZE THE STARTED        *HMD 07/82*
** ADDRESS SPACE. THUS AN ABEND806-4 AND A RESULTING SVC    *HMD 07/82*
** DUMP.                                                    *HMD 07/82*
**                                                          *HMD 07/82*
** SINCE RESPONSE AT THE TERMINAL IS NOT REQUIRED FOR       *HMD 07/82*
** START AND MOUNT COMMANDS (AND THAT IS THE ONLY PLACE     *HMD 07/82*
** THIS CRAZY THING HAPPENS) WE WILL ISSUE THOSE COMMANDS   *HMD 07/82*
** WITH REGISTER ZERO EQUAL TO ZERO.                        *HMD 07/82*
*************************************************************HMD 07/82*
         CLC   =C'S ',COMMAND           START COMMAND?      *HMD 07/82*
         BE    SETSTART                 YES..SET FLAG       *HMD 07/82*
         CLC   =C'START ',COMMAND       START COMMAND?      *HMD 07/82*
         BE    SETSTART                                     *HMD 07/82*
         CLC   =C'M ',COMMAND           MOUNT COMMAND       *HMD 07/82*
         BE    SETSTART                                     *HMD 07/82*
         CLC   =C'MOUNT ',COMMAND       MOUNT COMMAND?      *HMD 07/82*
         BNE   CHECKX                                       *HMD 07/82*
SETSTART OI    FLAGS,FSTART             INDICATE START/MOUNT*HMD 07/82*
CHECKX   CLC   =C'X ',COMMAND           EXIT REQUEST?
         BNE   PROCESS                  NO, PROCESS COMMAND
         EJECT
* PROCESS PCF EXIT REQUEST
EXITREQ  XC    CSOUT(12),CSOUT          CLEAR CSOA
         LA    1,CSOUT
         ST    1,CSPLFLG
         LA    1,CSOUT+4                GET CSOA FLAG
         ST    1,CSPLOA                 SAVE OUTPUT AREA
         OI    CSOUT,X'80'              NO SYNTAX CHECK
         LA    1,CMDLEN                 PSUEDO COMMAND BUFFER
         ST    1,CSPLCBUF               SAVE IN COMMAND BUFFER PLACE
         LA    1,TSOECB                 GET ECB ADDR
         ST    1,CSPLECB                STORE ECB ADDR
         XC    TSOECB,TSOECB            CLEAR ECB
         TM    FLAGS,FAUTH              APF AUTHORIZED?     *HMD 04/82*
         BO    MODE01                   YES, DO NOT TURN ON *HMD 04/82*
         RWHOLE  AUTHON                 TURN ON AUTHORITY   *HMD 04/82*
MODE01   MODESET KEY=ZERO               GET INTO KEY ZERO   *HMD 04/82*
         L     R1,PSATOLD-PSA           GET CURRENT TCB     *HMD 04/82*
         L     R1,TCBJSCB-TCB(,R1)      GET CVT POINTER     *HMD 04/82*
         L     R2,JSCBACT-IEZJSCB(,R1)  GET ACTIVE JSCB PTR *HMD 04/82*
         CLC   JSCBPSCB-IEZJSCB(4,R2),PSCBADDR              *HMD 04/82*
         BNE   ABENDXX                  VALIDITY CHECK FAIL *HMD 04/82*
         NI    JSCBOPTS-IEZJSCB(R2),255-JSCBAUTH
*                                 TURN OFF APF BIT TO FOOL PCFSCAN
         L     R1,CVTPTR                GET CVT ADDRESS     *HMD 04/82*
         L     R15,CVTSCAN-CVT(,R1)     GET IKJSCAN ADDRESS *HMD 04/82*
         TM    CVTSCAN-CVT(R1),X'80'    IS SCAN LOADED?     *HMD 04/82*
         BO    GOTSCAN                  NO, THEN LOAD       *HMD 04/82*
         LOAD  EP=IKJSCAN               LOAD IKJSCAN        *HMD 04/82*
         LR    R15,R0                   SAVE SCAN ADDRESS   *HMD 04/82*
GOTSCAN  LA    R1,CSPL                  GET PARM LIST ADDR  *HMD 04/82*
         XC    SYNCHPRM(SYNCHL),SYNCHPRM    CLEAR PARMS     *HMD 07/82*
         SYNCH (15),RESTORE=YES,MF=(E,SYNCHPRM)             *HMD 04/82*
         DELETE  EP=IKJSCAN             SCAN NOT NEEDED NOW *HMD 04/82*
         L     R1,PSATOLD-PSA           GET CURRENT TCB     *HMD 04/82*
         L     R1,TCBJSCB-TCB(,R1)      GET CVT POINTER     *HMD 04/82*
         L     R2,JSCBACT-IEZJSCB(,R1)  GET ACTIVE JSCB PTR *HMD 04/82*
         CLC   JSCBPSCB-IEZJSCB(4,R2),PSCBADDR              *HMD 04/82*
         BNE   ABENDXX                  VALIDITY CHECK FAIL *HMD 04/82*
         OI    JSCBOPTS-IEZJSCB(R2),JSCBAUTH                *HMD 04/82*
*                                       TURN ON AUTH AGAIN  *HMD 04/82*
         MODESET KEY=NZERO              GET BACK TO KEY 8   *HMD 04/82*
         TM    FLAGS,FAUTH              AUTHORIZED ON ENTRY?*HMD 09/82*
         BO    CKFF                     YES, DO NOT USE SVC *HMD 09/82*
         RWHOLE AUTHOFF                 SYSTEM INTERFACE    *HMD 09/82*
         B     CKFF                     OK, ALL DONE        *HMD 09/82*
ABENDXX  MODESET KEY=NZERO,MODE=PROB    TURN OF AUTHORITY   *HMD 04/82*
         TM    FLAGS,FAUTH              AUTHORIZED?         *HMD 04/82*
         BO    ABENDYY                  YES, DO NOT USE SVC *HMD 04/82*
         RWHOLE AUTHOFF                                     *HMD 04/82*
ABENDYY  ABEND 4095,DUMP                                    *HMD 04/82*
         EJECT
* PROCESS COMMAND REQUEST
PROCESS  TM    FLAGS,FAUTH              ALREADY AUTHORIZED? *HMD 04/82*
         BO    SYS01                    YES, DO NOT TURN ON *HMD 04/82*
         RWHOLE  AUTHON                 ALLOW MODESETS      *HMD 01/82*
SYS01    SYSEVENT DONTSWAP              DISALLOW SWAPS      *HMD 10/81*
         MODESET KEY=ZERO,MODE=SUP      GET SUPERVISOR MODE *HMD 07/82*
         SLR   R0,R0                    TEST FOR START      *HMD 07/82*
         TM    FLAGS,FSTART             START/MOUNT?        *HMD 07/82*
         BO    SVC34                    YES, NO RESPONSE    *HMD 07/82*
         L     R0,TJIDTEST              INDICATE TSO        *HMD 10/81*
SVC34    DS    0H                                           *HMD 07/82*
         MGCR  CMDLEN                   ISSUE OPR COMMAND   *HMD 10/81*
         MODESET KEY=NZERO,MODE=PROB
         SYSEVENT OKSWAP                ALLOW SWAPS         *HMD 10/81*
         TM    FLAGS,FAUTH              AUTHORIZED?         *HMD 04/82*
         BO    SYS02                    YES, BYPASS RWHOLE  *HMD 04/82*
         RWHOLE  AUTHOFF                DISALLOW MODESETS
SYS02    STIMER  WAIT,BINTVL=HOLD       HOLD ON A SEC.      *HMD 10/81*
         B     CKFF                                         *HMD 03/82*
         EJECT
**                                                          *HMD 03/82*
**  CHECK FOR INITIAL COMMAND                               *HMD 03/82*
**                                                          *HMD 03/82*
CKFF     DS    0H                                           *HMD 03/82*
         TM    FLAGS,FCMD              INITIAL COMMAND?     *HMD 03/82*
         BZ    START                   NO, REPROMPT USER    *HMD 03/82*
**
**  END OF PROGRAM
**
END      TSOLEAVE ISN=LTORG
         SPACE 2                                            *HMD 03/82*
NOSWAP   RWHOLE   AUTHON                TURN ON AUTH        *HMD 01/85*
         SYSEVENT DONTSWAP              TURN OFF THE SRM    *HMD 01/85*
         RWHOLE   AUTHOFF               AND FURTHER AUTH    *HMD 01/85*
         B        CKFF                  GET ANOTHER CMD     *HMD 01/85*
         SPACE 1                                            *HMD 01/85*
YESSWAP  RWHOLE   AUTHON                                    *HMD 01/85*
         SYSEVENT OKSWAP                GET THE SRM BACK    *HMD 01/85*
         RWHOLE   AUTHON                AND TURN OFF AUTH   *HMD 01/85*
         B        CKFF                  GET ANOTHER CMD     *HMD 01/85*
         SPACE 1                                            *HMD 01/85*
PASSOFF  RWHOLE   PASSOFF               TURN OFF PASSWORDS  *HMD 06/79*
         B        CKFF                  GET NEXT COMMAND    *HMD 03/82*
         SPACE  1                                           *HMD 03/82*
PASSON   RWHOLE   PASSON                TURN ON PASSWORDS   *HMD 06/79*
         B        CKFF                  GET NEXT COMMAND    *HMD 03/82*
         SPACE  1                                           *HMD 03/82*
NONCAN   RWHOLE   NOCAN                 TURN OFF CANCEL FLAG*HMD 02/82*
         B        CKFF                  CHECK FOR END       *HMD 02/82*
         SPACE  1                                           *HMD 03/82*
CANDO    RWHOLE   YESCAN                TURN ON CANCEL FLAG *HMD 02/82*
         B        CKFF                  CHECK FOR END       *HMD 02/82*
         SPACE  1                                           *HMD 03/82*
NOTIME   RWHOLE   NOTIME                SET NO JS TIMING    *HMD 04/82*
         B        CKFF                  CHECK FOR END       *HMD 02/82*
         SPACE  1                                           *HMD 03/82*
YESTIME  RWHOLE   TIME                  SET FOR JS TIMING   *HMD 04/82*
         B        CKFF                  CHECK FOR END       *HMD 04/82*
         SPACE  1                                           *HMD 03/82*
ACCOUNT  DS    0H                     TURN ON ACCOUNT CMD   *HMD 07/82*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED?    *HMD 07/82*
         BO    ACCT01                 YES, DO NOT USE MACRO *HMD 07/82*
         RWHOLE AUTHON                                      *HMD 07/82*
ACCT01   MODESET KEY=ZERO                                   *HMD 07/82*
         L     R1,PSCBADDR            GET PSCB ADDRESS      *HMD 07/82*
         LUSE  PSCB,R1                ADDRESS THE PSCB      *HMD 07/82*
         OI    PSCBATR1,PSCBACCT      TURN ON ACCOUNT BIT   *HMD 07/82*
         LDROP R1                     DROP PSCB BASE        *HMD 07/82*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED     *HMD 07/82*
         BO    CKFF                   YES..DO NOT TURN OFF  *HMD 07/82*
         RWHOLE AUTHOFF               TURN OF AUTHORITY     *HMD 07/82*
         B     CKFF                   GET NEXT COMMAND      *HMD 07/82*
         SPACE 1                                            *HMD 07/82*
NACCOUNT DS    0H                     TURN OFF ACCOUNT CMD  *HMD 07/82*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED?    *HMD 07/82*
         BO    ACCT02                 YES, DO NOT USE MACRO *HMD 07/82*
         RWHOLE AUTHON                TURN ON AUTHORITY     *HMD 07/82*
ACCT02   MODESET KEY=ZERO                                   *HMD 07/82*
         L     R1,PSCBADDR            GET PSCB ADDRESS      *HMD 07/82*
         LUSE  PSCB,R1                ADDRESS THE PSCB      *HMD 07/82*
         NI    PSCBATR1,255-PSCBACCT  TURN OFF ACCOUNT BIT  *HMD 07/82*
         LDROP R1                     DROP PSCB BASE        *HMD 07/82*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED     *HMD 07/82*
         BO    CKFF                   YES..DO NOT TURN OFF  *HMD 07/82*
         RWHOLE AUTHOFF               TURN OFF AUTHORITY    *HMD 07/82*
         B     CKFF                   GET NEXT COMMAND      *HMD 07/82*
         EJECT
MOUNT    DS    0H                     TURN ON MOUNT CMD     *HMD 01/85*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED?    *HMD 01/85*
         BO    MOUNT01                YES, DO NOT USE MACRO *HMD 01/85*
         RWHOLE AUTHON                                      *HMD 01/85*
MOUNT01  MODESET KEY=ZERO                                   *HMD 01/85*
         L     R1,PSCBADDR            GET PSCB ADDRESS      *HMD 01/85*
         LUSE  PSCB,R1                ADDRESS THE PSCB      *HMD 01/85*
         OI    PSCBATR1,PSCBVMNT      TURN ON MOUNT BIT     *HMD 01/85*
         LDROP R1                     DROP PSCB BASE        *HMD 01/85*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED     *HMD 01/85*
         BO    CKFF                   YES..DO NOT TURN OFF  *HMD 01/85*
         RWHOLE AUTHOFF               TURN OF AUTHORITY     *HMD 01/85*
         B     CKFF                   GET NEXT COMMAND      *HMD 01/85*
         SPACE 1                                            *HMD 01/85*
NOMOUNT  DS    0H                     TURN OFF MOUNT CMD    *HMD 01/85*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED?    *HMD 01/85*
         BO    MOUNT02                YES, DO NOT USE MACRO *HMD 01/85*
         RWHOLE AUTHON                TURN ON AUTHORITY     *HMD 01/85*
MOUNT02  MODESET KEY=ZERO                                   *HMD 01/85*
         L     R1,PSCBADDR            GET PSCB ADDRESS      *HMD 01/85*
         LUSE  PSCB,R1                ADDRESS THE PSCB      *HMD 01/85*
         NI    PSCBATR1,255-PSCBVMNT  TURN OFF MOUNT BIT    *HMD 01/85*
         LDROP R1                     DROP PSCB BASE        *HMD 01/85*
         TM    FLAGS,FAUTH            ARE WE AUTHORIZED     *HMD 01/85*
         BO    CKFF                   YES..DO NOT TURN OFF  *HMD 01/85*
         RWHOLE AUTHOFF               TURN OFF AUTHORITY    *HMD 01/85*
         B     CKFF                   GET NEXT COMMAND
         EJECT
NOTAUTH  DS    0H                                           *HMD 08/81*
         LA    R1,TSOECB              GET ECB ADDRESS       *HMD 08/81*
         ST    R1,IOPLECB             SAVE IN IOPL          *HMD 08/81*
         XC    TSOECB,TSOECB          CLEAR ECB             *HMD 08/81*
         PUTLINE PARM=PTPB,OUTPUT=(OLDAUTH,TERM,SINGLE,INFOR),         X
               MF=(E,IOPL)                                  *HMD 08/81*
         B     END
         SPACE 2                                            *HMD 08/81*
SSSS     ABEND 800,DUMP                                     *HMD 08/81*
         EJECT                                              *HMD 08/81*
***                                                         *HMD 03/82*
***      SPECIAL ENTRY FOR 'EDIT' COMMAND                   *HMD 03/82*
***                                                         *HMD 03/82*
         ENTRY EONL                                         *HMD 03/82*
EONL     DS    0H                                           *HMD 03/82*
         USING *,15                                         *HMD 03/82*
         L     15,=A(ON)         ENTRY POINT OF ROUTINE     *HMD 03/82*
         LR    9,1               SAVE COMM AREA POINTER     *HMD 03/82*
         L     1,0(,1)           GET CPPL ADDRESS           *HMD 03/82*
         BR    15                GO TO IT                   *HMD 03/82*
         EJECT                                              *HMD 08/81*
***
***   CONSTANTS
***
*
* SPACES TO TRANSLATE FROM LOWER CASE TO UPPER CASE
*
SPACES   DC    CL127' '                                     *HMD 08/81*
MOVEIT   MVC   INPUT(0),4(R1)
MOVECBUF MVC   INPUT(0),0(R2)        MOVE CMDBUF COMMAND    *HMD 03/82*
MOVECMD  MVC   COMMAND(0),0(R15)
HOLD     DC    F'150'                 HOLD FOR 1 1/2 SECS   *HMD 10/81*
OLDAUTH  DC    A(1),A(MSGAUTH),A(0)   AUTHORIZATION O.L.D.  *HMD 08/81*
OLD      DC    A(1),A(MSG)
*                                                           *HMD 01/85*
MSG      DC    H'41',H'0',C'SYN04542I ENTER REQUEST (VERSION 5.2)'
MSGAUTH  DC    H'36',H'0',C'SYN09943I COMMAND NOT AUTHORIZED'
*                                                           *HMD 01/85*
         EJECT
***
***  DYNAMIC STORAGE AREA
***
TSODSECT DSECT          RESUME USER DSECT
         ORG  TSOUSER
PSCBADDR DS   F             SAVE AREA FOR PSCB              *HMD 04/82*
IOPLPTG  DS   4F
CSOUT    DS   3F
FLAGS    DS   X                                             *HMD 02/82*
FCMD     EQU  X'80'                                         *HMD 02/82*
FAUTH    EQU  X'40'         WE ARE ALREADY AUTHORIZED       *HMD 04/82*
FSTART   EQU  X'20'         START OR MOUNT COMMAND ISSUED   *HMD 07/82*
PTPB     PUTLINE MF=L                                       *HMD 08/81*
CMDLEN   DS   F
COMMAND  DS   CL127                                         *HMD 08/81*
LIDRECP  DS   F                                             *HMD 03/82*
SYNCHPRM SYNCH MF=L                   SYNCH LIST FORM       *HMD 04/82*
SYNCHL   EQU  *-SYNCHPRM                                    *HMD 07/82*
TJIDLIST DS   0CL12
         DS   3A
TJIDTEST DS   F
INPUT    DS   CL127                                         *HMD 08/81*
WORKSIZE EQU  *-TSOUSER
         EJECT
         IKJPGPB
         EJECT
         IKJCSOA
         EJECT
         IKJECT                                             *HMD 03/82*
         EJECT                                              *HMD 03/82*
         IKJPSCB
         EJECT
         IKJEBECA  ,                                        *HMD 03/82*
         EJECT                                              *HMD 03/82*
         CVT   LIST=YES,DSECT=YES                           *HMD 08/81*
         EJECT                                              *HMD 03/82*
         IHAPSA                                             *HMD 03/82*
         EJECT                                              *HMD 03/82*
         IHAASCB                                            *HMD 03/82*
         EJECT                                              *HMD 03/82*
         IEZJSCB                                            *HMD 04/82*
         EJECT                                              *HMD 04/82*
         IKJTCB  LIST=YES,DSECT=YES                         *HMD 04/82*
         END
