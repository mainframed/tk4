         TITLE '$DI -- TSO DISPLAY INITIATORS'
         COPY $HASPGBL
         EJECT
*
*
*
*
*
*
*       DONT KNOW WHICH DSECTS REQUIRE OTHERS BUT THIS SERIES SEEMS
*
*       TO WORK.  MAY HAVE SOME THAT ARE NOT NEEDED BUT I DONT HAVE
*
*       THE TIME TO RESEARCH.  IF ANYONE HAS A LIST OR KNOWS WHERE
*
*       ONE EXISTS THAT TELLS WHICH DSECT REQUIRES ANOTHER AND IN
*
*       WHAT SEQUENCE LET ME KNOW.  THE $MODULE INDICATES THE SEQUENCE
*
*       BUT NOT PRE-REQS.  AFTER ABOUT 5 ASSEMBLIES I JUST PUT
*
*       ALL I EVER USED ANYWHERE ELSE IN HERE AND IT WORKED.
*
*
*             DON
*
*
         EJECT
$DI      $MODULE ENVIRON=USER,NOTICE=NONE,                             *
               CVT,                                                    *
               $CAT,                                                   *
               $MIT,                                                   *
               $PCE,                                                   *
               $JQE,                                                   *
               $SCAT,                                                  *
               $XECB,                                                  *
               $HCT,                                                   *
               $JCT,                                                   *
               $BUFFER,                                                *
               JESCT,                    GEN $JESCT                    *
               SSCT,                     GEN $SSCT                     *
               $SVT,                     GEN $SVT                      *
               $HASPEQU,                 GEN $EQU                      *
               $PIT,                     GEN $PIT                      *
               DEB,                                                    *
               $SJB                      GEN $SJB
DINIT    ENT13
         MVC   PLINE(40),BLANKS
         MVI   IDENTP,C'I'
         MVI   LSW,C' '
         MVC   CLINE(80),BLANKS
         USING CVTDSECT,R1        SETUP CVT BASE
         USING JESCT,R2           SETUP JESCT BASE
         USING SSCT,R3            SETUP SSCT BASE
         USING SSVT,R4            SETUP SSVT BASE
         USING PITDSECT,R5        SETUP PIT BASE
         USING SJBDSECT,R11       SETUP SJB BASE
*-------------------------------------------------------------------*
*        PUT OUT THE HEADING.
*-------------------------------------------------------------------*
         SPACE
         TPUT  MHEAD,L'MHEAD      PRINT MAY & SPEH LINE
         TPUT  SLINE,L'SLINE      PRINT HEADING LINE 1
         TPUT  ALINE,L'ALINE      PRINT HEADING LINE 2
         TPUT  BLANKS,L'BLANKS    PRINT BLANK LINE
         SPACE
         SPACE
         L     R1,16              GET CVT POINTER
         L     R1,0(R1)           OLD/NEW POINTER
         L     R1,4(R1)           OUR TCB POINTER
         L     R15,12(R1)         OUR TIOT POINTER
         L     R1,16              GET CVT
         L     R2,296(R1)         GET JESCT    (JES CONTROL TABLE)
         L     R3,JESSSCT         GET FIRST JES SSCT (SSCVT)
NEXTSSCT EQU   *
         L     R4,SSCTSSVT        GET JES SSVT
         L     R5,$SVPIT          GET PIT ADDR--5.302 JES LOGIC+MAINT
NEXTPIT  EQU   *                  NOW READY TO WRITE
         MVC   IDENT(L'PLINE),BLANKS CLEAR DETAIL LINE
         MVI   IDENTP,C'I'        RESET 'I' IDENTITY
         TM    PITSTAT,PITHOLDA   PIT DRAINED $PI ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'DRAINED '  MOVE IN DRAINED STATUS
         SPACE
         TM    PITSTAT,PITHOLD1   PIT DRAINED $P IN ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'DRAINED '  MOVE IN DRAINED STATUS
         SPACE
         TM    PITSTAT,PITHALTA   PIT HALTED  $Z I ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'HALTED  '  MOVE IN HALTED  STATUS
         SPACE
         TM    PITSTAT,PITHALT1   PIT HALTED  $Z IN ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'HALTED  '  MOVE IN HALTED  STATUS
         SPACE
         TM    PITSTAT,PITINIT    OS INIT EXISTS FOR PIT?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'INACTIVE'  MOVE IN INACTIVE
         SPACE
         TM    PITSTAT,PITBUSY    PIT BUSY ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'ACTIVE  '  MOVE IN ACTIVE
         SPACE
         TM    PITSTAT,PITIDLE    PIT IDLE ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'IDLE    '  MOVE IN IDLE
         SPACE
         CLC   STAT,=8C' '        STATUS NOT AVAILABLE ?
         BNE   *+10               IF NOT CONTINUE
         MVC   STAT,=C'UNAVAIL '  MOVE IN STATUS NOT AVAIL.
         CLI   PITPATID,C' '      NULL 1ST CHAR?
         BNE   IDENTOK            NO, CONTINUE
         MVC   IDENT(1),PITPATID+1 MOVE IN ID---FORMAT CL1
         B     IDENTSKP           SKIP NEXT MOVE
         SPACE
IDENTOK  EQU   *
         MVC   IDENT,PITPATID     MOVE ID IN---FORMAT CL2
         SPACE
IDENTSKP EQU   *
         MVC   INITNAM,PITCLASS   MOVE THE INIT CLASSES TO OUTPUT
         SPACE
*--------------------------------------------------------------------*
*        &MAXCLAS PARAMETER ON GEN LIMITS MAX CLASSES, ABOVE
*        MAY NEED TO BE CHANGED IF OTHER THAN 8 CLASSES.
*--------------------------------------------------------------------*
         SPACE
         TM    PITSTAT,PITBUSY    PIT BUSY ?
         BNO   PRINT              IF NOT CONTINUE
         L     R11,PITSJB         GET SJB ADDRESS
         MVC   JOBNAME,SJBJOBNM   GET JOBNAME FROM JOB CARD
         MVC   UCLASS,SJBJCLAS    GET USED CLASS
         SPACE
PRINT    EQU   *
         CLI   LSW,C' '           CHECK PRINT SW FOR LEFT SIDE
         BE    MOVELEFT           MOVE IN LEFT PRINT AREA
         CLI   LSW,C'1'           CHECK PRINT SW FOR RIGHT SIDE
         BE    MOVERIGT           MOVE IN RIGHT PRINT AREA
         SPACE
ERROR    EQU   *
         TPUT  ELINE,L'ELINE      WRITE ERROR LINE
         B     FINAL              END IF ERROR
         SPACE
PUTIT    EQU   *
         TPUT  CLINE,L'CLINE      WRITE MESSAGE LINE
         MVC   LSW,=C' '          RESET PRINT SWITCH
         B     NEXTIT             GET NEXT PIT
         SPACE
FINAL    EQU   *
         CLI   LSW,C' '           CHECK IF LAST DATA FILLED
         BE    EXIT               IF DONE, DON'T WRITE
         CLI   LSW,C'1'           CHECK IF LEFT ONLY FILLED
         BE    SPRIGHT            GO TO SPACE-RIGHT
         SPACE
LASTWR   EQU   *
         TPUT  CLINE,L'CLINE      WRITE LAST LINE NOW
         SPACE
*--------------------------------------------------------------------*
*        NOW WE CLEANUP AFTER OURSELVES AND GO HOME.
*--------------------------------------------------------------------*
         SPACE
EXIT     EQU   *
         EXT13
MOVELEFT EQU   *
         MVC   LSW,=C'1'          SET LEFT PRINT FLAG
         MVC   CLEFT,PLINE        MOVE IN LEFT SIDE
NEXTIT   EQU   *
         L     R5,PITNEXT         GET NEXT PIT---REFER 5.200 JES2
         LTR   R5,R5              CHK R5 FOR VALID ADDR
         BZ    FINAL              END PROCESS FOR SSCT
         B     NEXTPIT            GO BACK
SPRIGHT  EQU   *
         MVC   CRIGHT,FLINE       MOVE SPACES TO RIGHT
         B     LASTWR             GO TO LAST WRITE
MOVERIGT EQU   *
         MVC   LSW,=C'2'          SET FULL PRINT FLAG
         MVC   CRIGHT,PLINE       MOVE IN RIGHT SIDE
         B     PUTIT              GO TO WRITE LINE
         EJECT
         $MODEND
         EJECT
*--------------------------------------------------------------------*
*        DATA AREA FOLLOWS
*--------------------------------------------------------------------*
         SPACE
MHEAD    DS    0CL80
         DC    C'   '
         DC    C'               M A Y  AND  S P E H  D A T A  C E N T'
         DC    C' E R'
         DC    CL40' '
SLINE    DS    0CL80
         DC    C'                             DISPLAY INI'
         DC    C'TIATORS                                ',CL1' '
ALINE    DS    0CL80
         DC    C'ID  CLASSES  STAT       JOBNAME  C   ',CL3' '
         DC    C'ID  CLASSES  STAT       JOBNAME  C   ',CL3' '
BLANKS   DC    CL80' '
FLINE    DC    CL40' '
ELINE    DC    CL80'ERROR IN MESSAGE ROUTINE'
ILINE    DC    CL80'NO SSVT CONTROL BLOCK FOUND'
JLINE    DC    CL80'NO SSCT CONTROL BLOCK FOUND'
LLINE    DC    CL80'USER NOT AUTHORIZED'
         SPACE
         LTORG
         SPACE
         DS    0F
WORKAREA DS 0D
SAVE     DS    18F
LSW      DS    C                   LINE PRINT SWITCH
*                                  ' ' IS NO FILLED
*                                  '1' IS LEFT FILLED
*                                  '2' IS BOTH FILLED
PLINE    DS    0CL40
IDENTP   DS    CL1
IDENT    DS    CL2,CL1
INITNAM  DS    CL8,CL1
STAT     DS    CL8,CL3
JOBNAME  DS    CL8,CL1
UCLASS   DS    CL1,CL11    (PAD)
CLINE    DS    0CL80
CLEFT    DS    CL40
CRIGHT   DS    CL40
WORKEND  EQU   *
         END
