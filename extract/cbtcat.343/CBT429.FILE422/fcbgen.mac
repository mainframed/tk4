//TSDAMF JOB 01992,DAM,MSGCLASS=H,NOTIFY=TSDAM
// EXEC QASML,NAME=GENFCB,USR=PRDT
GENFCB  ENT13 NO=2
*
*  PROGRAM TO GENERATE FCB FOR 3211 PRINTERS
*
*  NOTHING FANCY BUT IT WORKS
*
*  DON MARQUARDT  MAY & SPEH  3/25/85
*
*  SHARE CODE  MSD
*
*  12/18/85
*
*    UPDATED TO REQUEST FOR CUSTOMER AND REP ID AND ENTERED
*    AS AN IDENTIFY IN LOAD MODULE
*
*
        OPEN  (FCBLIB,INPUT)
        OPEN  (INTRDR,OUTPUT)
        TPUT  HELLO,80
GETNAME DS    0H
        MVC   NAM,BLANKS
        TPUT  NAME,80
        TGET  NAM,4
        CLI   NAM+3,C' '
        BNE   OK4POS
        TPUT  NAMERR,80
        B     GETNAME
OK4POS  DS    0H
        OC    NAM(4),UPPER
BLDL    DS    0H
        BLDL  FCBLIB,FCBNAM
        ST    R15,RETCODE
        CLI   RETCODE+3,X'04'
        BE    NEWONE
REPL    DS    0H
        MVC   REPLAC(4),NAM
        TPUT  REPLAC,80
        TGET  ANS,4
        CLI   ANS,C'Y'
        BE    NEWONE
        CLI   ANS,X'A8'
        BE    NEWONE
        TPUT  ABORT,80
        B     GETOUT
NEWONE  DS    0H
        MVC   NAM1(4),NAM
        TPUT  CUST,80
        TPUT  PERIODS,30
        TGET  CUSTNM,30
        OC    CUSTNM,UPPER
        CLC   CUSTNM,UPPER
        BE    NEWONE
GETREP  TPUT  REP,80
        TPUT  PERIODS,10
        TGET  REPID,10
        OC    REPID,UPPER
        CLC   REPID,UPPER
        BE    GETREP
        TPUT  LINES,80
        TGET  LNS,3
        TPUT  LPI,80
        TGET  LPS,1
        MVC   NOLINES+15(3),LNS
LOOP    DS    0H
        MVC   EXEC+26(4),NAM
        MVC   CSECT+4(4),NAM
        MVC   WTOPARM,NAM
        PUT   INTRDR,JOB
        PUT   INTRDR,EXEC
        PUT   INTRDR,EXEC1
        PUT   INTRDR,PARM
        PUT   INTRDR,CSECT
        PUT   INTRDR,DEFCB
        PUT   INTRDR,NOLINES
        PUT   INTRDR,LIN1
        PUT   INTRDR,CON1
        PUT   INTRDR,ORG1
        PUT   INTRDR,LEN
        CLI   LPS,C'6'
        BE    LINES6
        PUT   INTRDR,LPI8
        B     LINES8
LINES6  DS    0H
        PUT   INTRDR,LPI6
LINES8  DS    0H
        TPUT  TOEND,80
LINELP  DS    0H
        TPUT  CHNNO,80
        TGET  CHNO,2
        CLC   =C'AB',CHNO
        BE    HELLO
        CLI   CHNO,C'N'
        BE    DONE
        CLI   CHNO,X'95'
        BE    DONE
        MVI   CHDC+24,C')'
        MVC   CHDC+22(2),CHNO
        CLI   CHDC+23,C' '
        BNE   PUTCHAN
        MVC   CHDC+23(3),=C')  '
PUTCHAN DS    0H
        TPUT  LINENO,80
        TGET  LNNO,3
        MVC   ORG+22(3),LNNO
        PUT   INTRDR,ORG
        PUT   INTRDR,CHDC
        B     LINELP
DONE    DS    0H
        PUT   INTRDR,END2
        PUT   INTRDR,END3
        PUT   INTRDR,END4
        PUT   INTRDR,END5
        TPUT  ALDONE,80
        TPUT  ALDONE1,80
GETOUT  DS    0H
        CLOSE FCBLIB
        CLOSE INTRDR
        EXT13
RETCODE DC    F'0'
UPPER   DC    CL80' '
PERIODS DC    80C'.'
REP     DC    CL80'ENTER REP ID  10 POSITIONS MAX'
CUST    DC    CL80'ENTER CUSTOMER ID  30 POSITIONS MAX'
HELLO   DC    CL80'GENERATE FCB'
LINES   DC    CL80'NUMBER OF LINES'
NAME    DC    CL80'NAME '
LPI     DC    CL80'LINES PER INCH  6 OR 8'
REPLAC  DC    CL80'      ALREADY EXISTS.....  REPLACE?'
CHNNO   DC    CL80'CHANNEL?'
LINENO  DC    CL80'LINE?'
ALDONE  DC    CL80'JOB SUBMITTED  PLEASE WAIT FOR MESSAGE ON CONSOLE'
ALDONE1 DC    CL80'BEFORE USING'
ABORT   DC    CL80'FCB GENERATE CANCELLED DO TO USER REQUEST'
TOEND   DC    CL80' ****  TO SUBMIT JOB        REPLY  NO  TO CHANNEL?'
NAMERR  DC    CL80'NAME MUST BE 4 POSITIONS LONG'
JOB     DC    CL80'//TMPRTFCB JOB 01990,GENFCB,MSGCLASS=H,NOTIFY=TMPRT'
EXEC    DC    CL80'//FCB EXEC QASML,NAME=FCB2XXXX,USR=SYS1,'
EXEC1   DC    CL80'//   LIB=IMAGELIB,'
PARM    DC    CL80'// PARM.LKED=''LIST,OL,REFR,RENT,XREF'''
CSECT   DC    CL80'FCB2XXX  CSECT'
DEFCB   DC    CL80'         DC      X''A0''   DEFAULT FCB INDICATOR'
NOLINES DC    CL80'NOLINES  EQU     X'
LEN     DC    CL80'LIN      DC      AL1(NOLINES) '
LPI6    DC    CL80'LPIND    DC      AL1(00)   '
LPI8    DC    CL80'LPIND    DC      AL1(16)  '
ORG1    DC    CL80'         ORG     LEN'
LIN1    DC    CL80'LEN      DC      (NOLINES)X''00'''
CON1    DC    CL80'         DC      AL1(16)'
END2    DC    CL80'         END '
END3    DC    CL80'//LKED.SYSIN DD *'
END4    DC    C' IDENTIFY '
        DC    CL4'FCB2'
NAM1    DC    CL4'    '
        DC    X'4D7D'
CUSTNM  DC    CL30' '
REPID   DC    CL10' '
        DC    X'7D5D'
        DC    CL40' '
END5    DC    C'//NOTIFY  EXEC PGM=FCBWTO,COND=(0,NE),PARM='
WTOPARM DC    CL4' '
        DC    CL40' '
INTRDR  DCB   DDNAME=FCBRDR,DSORG=PS,MACRF=PM,BLKSIZE=80
FCBLIB  DCB   DDNAME=FCBLIB,DSORG=PS,MACRF=GM
LNS    DC   CL80' '
FCBNAM DC   H'1',H'14'
       DC   CL4'FCB2'
NAM    DC   CL80' '
LPS    DC   CL80' '
ANS    DC   CL80' '
CHNO   DC   CL80' '
COMCH  DC   CL80' '
LNNO   DC   CL80' '
ORG    DC   CL80'           ORG    LEN+ZZ                  '
CHDC   DC   CL80'           DC     AL1(ZZ)                 '
BLANKS DC   CL80' '
        EQUATE
        END
