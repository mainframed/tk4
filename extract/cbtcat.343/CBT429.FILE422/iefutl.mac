IEFUTL   TITLE 'IPO-SUPPLIED USER TIME LIMIT EXIT'
*/* COMPID: SYSTEM
*/* DOC     THIS IS A SMF EXIT ROUTINE THAT IS INVOKED WHEN A
*/*         CPU TIME LIMIT OR CONTINUOUS WAIT TIME HAS BEEN EXCEEDED.
*
*   AUTHOR:  DON MARQUARDT
*
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFUTL                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  MAY & SPEH   USER TIME LIMIT EXIT   *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             FUNCTION =                                              *
*                ALLOW JOBS TO BE CANCELLLED IF JOB (STEP) TIME       *
*                   LIMIT EXCEEDED.                                   *
*                ALLOW TSO SESSIONS TO BE CANCELLED IF WAIT TIME      *
*                   LIMIT EXCEEDED.                                   *
*                ALLOW SELECTED TSO SESSIONS TO BE EXTENDED ON        *
*                   WAIT TIME EXCEEDED.                               *
*                     ====> ONLY DURING DAY SHIFT <=====
*                     ====> AND ONLY FOR THE GOOD <=====
*                     ====> GUYS LISTED IN THE    <=====
*                     ====> MODULE IEFTBL         <=====
*
*                  THE MODULE IEFTBL CAN BE UPDATED AT ANY
*                  TIME AND TAKES EFFECT AT THE NEXT TIME
*                  IEFUTL IS INVOKED.
*
*                                                                     *
*             NOTES =                                                 *
*                                                                     *
*                                                                     *
*   ===========================================================       *
*                                                                     *
*    THIS ROUTINE ASSUMES THAT IEFTBL IS AVAILABLE PRIOR TO           *
*    THE FIRST CALL.  NO CHECKING IS DONE TO INSURE THAT IT           *
*    IS AVAILABLE AND A 806 ERROR MAY OCCUR IF NOT PRESENT.           *
*                                                                     *
*    PLAN TO ADD A CHECK AT A FUTURE DATE AS TIME PERMITS             *
*                                                                     *
*   ===========================================================       *
*                                                                     *
*                                                                     *
*                                                                     *
*                DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE  *
*                   IF A DIFFERENT CHARACTER SET IS NEEDED.           *
*                                                                     *
*                RESTRICTIONS = NONE                                  *
*                                                                     *
*                REGISTER CONVENTIONS = STANDARD CONVENTIONS.         *
*                   REGISTERS 0 TO 2  = WORK REGISTERS                *
*                   REGISTERS 3 TO 9  = UNUSED                        *
*                   REGISTER  10      = ADDRESS OF GETMAINED AREA     *
*                   REGISTER  11      = ADDRESSABILITY TO COMMON      *
*                                       EXIT PARAMETER AREA           *
*                   REGISTER  12      = ADDRESSABILITY TO IEFUTL      *
*                                       CSECT                         *
*                   REGISTER  13      = SAVE AREA REGISTER            *
*                   REGISTER  14      = RETURN ADDRESS                *
*                   REGISTER  15      = RETURN CODE                   *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                  'ZAP*'S)                                           *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, REENTRANT,             *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
*             ENTRY POINTS = IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM INITIATOR VIA MODULE IEATLEXT                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                THE FIRST OF WHICH POINTS TO THE SMF COMMON          *
*                EXIT PARAMETER AREA MAPPED BY SMFCEPA DSECT          *
*                                                                     *
*             OUTPUT = REGISTER 15 - RETURN CODE                      *
*                REGISTER 1 - TIME LIMIT EXTENSION                    *
*                USER COMMUNICATION FIELD - EXTENSION COUNT           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = IEFTBL                                    *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             TABLES = NONE (IEFTBL IS THE TABLE OF GOOD GUYS)        *
*                                                                     *
*             MACROS = SAVE, TPUT                                     *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THE MESSAGES ISSUED BY THE PROBLEM ARE FOUND AT      *
*                   THE LABELS MESSAGEC, MESSAGEW, AND WTOC.          *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
IEFUTL   CSECT
         SAVE  (14,12),,IEFUTL.IPO.&SYSTIME_&SYSDATE SAVE REGISTERS
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
         USING IEFUTL,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
*
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT         *
*        EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED              *
*                            = 4 - STEP CPU TIME EXCEEDED             *
*                            = 8 - JOB WAIT TIME EXCEEDED             *
*                                                                     *
***********************************************************************
*
         L     R11,D0(R1)              LOAD POINTER TO PARAMETER AREA
         USING SMFEPLA,R11             ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
         C     R0,FULL4                SEE IF JOB/STEP TIME EXCEEDED
         BH    WAITTIME                NO.....GO DO ANALYSIS FOR WAIT
*                                      TIME EXCEEDED
         B     CANCEL                  ALLOW NO EXTENTIONS
*
***********************************************************************
*                                                                     *
*        WAIT TIME EXCEEDED                                           *
*           - IF TSO CHECK LOGON IDS                                  *
*           - ALLOW JOB A 10 MINUTE EXTENSION IF LOGON ID IN IEFTBL   *
*           - ONLY DURING PERIOD OF 7AM THRU 5PM                      *
***********************************************************************
*
WAITTIME DS    0H
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BO    LDTSOTBL                YES...GO CHECK TSO IDS
         B     CANCEL                  NO....GO CANCEL JOB - WAIT TIME
EXTEND   DS    0H
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R1,WAIT600              LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
*
***********************************************************************
*                                                                     *
*        CHECK TSO ID WITH TABLE - EXTEND WAIT TIME IF FOUND          *
*                                                                     *
***********************************************************************
*
LDTSOTBL DS    0H
         TIME
         C     R0,BEFORE7                BEFORE 7 AM
         BL    CANCEL                    YES  DO NOT EXTEND
         C     R0,AFTER5                 AFTER 5 PM
         BH    CANCEL                    YES  DO NOT EXTEND
         LOAD  EP=IEFTBL
         LR    R10,R0                    LOAD TABLE START
CKIDS    LA    R10,8(,R10)               SKIP OVER IDENT
         CLC   SMFJOBN,0(R10)            COMPARE LOGON ID
         BE    FNDID                     EQUAL
         CLI   0(R10),X'FF'              END OF TABLE
         BNE   CKIDS                     NO
         LA    R2,CANCEL                 CANCEL USER
DELETE   DELETE EP=IEFTBL
         BR    R2                        RETURN
FNDID    LA    R2,EXTEND                 GO TO EXTEND ROUTINE
         B     DELETE                    DELETE TABLE
*
***********************************************************************
*                                                                     *
*        ZERO REGISTER 2 TO INDICATE CANCEL                           *
*                                                                     *
***********************************************************************
*
CANCEL   DS    0H
         SLR   R2,R2                   INDICATE RETURN CODE OF ZERO -
*                                      CONTINUE (CANCEL)
*
***********************************************************************
*                                                                     *
*        NORMAL END PROCESSING                                        *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         L     R14,D12(,R13)           LOAD ADDRESS FOR RETURN
         LR    R15,R2                  LOAD RETURN CODE FROM REGISTER 2
         LM    R2,R12,D28(R13)         RESTORE REGISTERS 2 TO 12
         BR    R14                     RETURN TO CALLER
         SPACE 5
**************************** CONSTANTS ********************************
FULL4    DC    F'4'
FULL8    DC    F'8'
WAIT600  DC    F'600'
BEFORE7  DC    X'07000000'
AFTER5   DC    X'17000000'
         SPACE 2
         EJECT
************************ DSECTS (MAPPING MACROS) **********************
SMFEPLA  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFRESV  DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         SPACE 5
********************************* EQUATES *****************************
D0       EQU   0
D4       EQU   4
D12      EQU   12
D28      EQU   28
ONE      EQU   1
WTOSVC   EQU   35
ZERO     EQU   0
         SPACE 5
***************************** REGISTER EQUATES ************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   IEFUTL
