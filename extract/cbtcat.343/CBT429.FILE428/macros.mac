./ ADD NAME=BITSW
         MACRO
&LABEL   BITSW   &BIT0,&BIT1,&BIT2,&BIT3,&BIT4,&BIT5,&BIT6,&BIT7,&DSECT
         LCLC  &#
&#       SETC  '#'
         AIF   ('&BIT0' EQ '').A
&BIT0    EQU   *  .          BIT 0.
&BIT0&#  EQU   128  .        1... ....      BIT POSITION
.A       AIF   ('&BIT1' EQ '').B
&BIT1    EQU   *  .          BIT 1.
&BIT1&#  EQU   64  .         .1.. ....      BIT POSITION
.B       AIF   ('&BIT2' EQ '').C
&BIT2    EQU   *  .          BIT 2.
&BIT2&#  EQU   32  .         ..1. ....      BIT POSITION
.C       AIF   ('&BIT3' EQ '').D
&BIT3    EQU   *  .          BIT 3.
&BIT3&#  EQU   16  .         ...1 ....      BIT POSITION
.D       AIF   ('&BIT4' EQ '').E
&BIT4    EQU   *  .          BIT 4.
&BIT4&#  EQU   8  .          .... 1...      BIT POSITION
.E       AIF   ('&BIT5' EQ '').F
&BIT5    EQU   *  .          BIT 5.
&BIT5&#  EQU   4  .          .... .1..      BIT POSITION
.F       AIF   ('&BIT6' EQ '').G
&BIT6    EQU   *  .          BIT 6.
&BIT6&#  EQU   2  .          .... ..1.      BIT POSITION
.G       AIF   ('&BIT7' EQ '').H
&BIT7    EQU   *  .          BIT 7.
&BIT7&#  EQU   1  .          .... ...1      BIT POSITION
.H       ANOP
         AIF   ('&DSECT' EQ '').I
&LABEL   DS    XL1  .        BIT BYTE.
         SPACE
         MEXIT
.I       ANOP
&LABEL   DC    XL1'0' .      BIT BYTE.
         SPACE
         MEND
./ ADD NAME=IFOFF
         MACRO
&LABEL   IFOFF &BIT,&BROF,&BRON
         LCLB  &BL121,&BL122
         LCLC  &#
&#       SETC  '#'
         AIF   ('&BIT' NE '').A
         MNOTE 1,'OPERAND 1 MISSING - NO GENERATION'
         MEXIT
.A       ANOP
&BL121   SETB  ('&BRON' NE '')
&BL122   SETB  ('&BROF' NE '')
         AIF   (&BL121 OR &BL122).AA
         MNOTE 1,'NO TRANSFER ADDRESS'
         MEXIT
.AA      ANOP
&LABEL   TM    &BIT,&BIT&#  .          TEST FOR BIT.
         AIF   (NOT &BL122).OTHER
         BZ    &BROF  .                BRANCH NOT ON.
         AIF   (&BL121).OTHER
         SPACE
         MEXIT
.OTHER   BO    &BRON  .                BRANCH ON.
         SPACE
         MEND
./ ADD NAME=IFON
         MACRO
&LABEL   IFON  &BIT,&BRYES,&BRNO
         LCLB  &BL121,&BL122
         LCLC  &#
&#       SETC  '#'
         AIF   ('&BIT' NE '').A
         MNOTE 1,'OPERAND 1 MISSING - NO GENERATION'
         MEXIT
.A       ANOP
&BL121   SETB  ('&BRYES' NE '')
&BL122   SETB  ('&BRNO' NE '')
         AIF   (&BL121 OR &BL122).AA
         MNOTE 1,'NO TRANSFER ADDRESS'
         MEXIT
.AA      ANOP
&LABEL   TM    &BIT,&BIT&#  .          TEST FOR BIT.
         AIF   (NOT &BL121).TAKNO
         BO    &BRYES  .               BRANCH ON.
         AIF   (&BL122).TAKNO
         SPACE
         MEXIT
.TAKNO   ANOP
         BZ    &BRNO  .                BRANCH NOT ON.
         SPACE
         MEND
./ ADD NAME=REGEQU   0100-82323-82323-1417-000021-000021-000000-$A1238
         MACRO
         REGEQU
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
***********************************************************************
         MEND
./ ADD NAME=SETOF
         MACRO
&LABEL   SETOF &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L,&M,&N,&O,&P
         LCLA  &NU
         LCLC  &VALUE
         LCLC  &#
&#       SETC  '#'
         AIF   ('&A' NE '').OK
         MNOTE 5,'MISSING OPERANDS - NO GEN'
         MEXIT
.OK      AIF   ('&LABEL' EQ '').B1
&LABEL   EQU   *
.B1      ANOP
&NU      SETA  N'&SYSLIST
.CHK     AIF   ('&NU' NE '0').LOOP
         SPACE
         MEXIT
.LOOP    ANOP
&VALUE   SETC  '&SYSLIST(&NU)'
         NI    &SYSLIST(&NU),255-&VALUE&#
&NU      SETA  &NU-1
         AGO   .CHK
         MEND
./ ADD NAME=SETON
         MACRO
&LABEL   SETON &A,&B,&C,&D,&E,&F,&G,&H,&I,&J,&K,&L,&M,&N,&O,&P
         LCLA  &NU
         LCLC  &VALUE
         LCLC  &#
&#       SETC  '#'
         AIF   ('&A' NE '').OK
         MNOTE 5,'MISSING OPERANDS - NO GEN'
         MEXIT
.OK      AIF   ('&LABEL' EQ '').B1
&LABEL   EQU   *
.B1      ANOP
&NU      SETA  N'&SYSLIST
.CHK     AIF   ('&NU' NE '0').LOOP
         SPACE
         MEXIT
.LOOP    ANOP
&VALUE   SETC  '&SYSLIST(&NU)'
         OI    &SYSLIST(&NU),&VALUE&#
&NU      SETA  &NU-1
         AGO   .CHK
         MEND
./ ADD NAME=XABSM    0100-89168-89168-1749-000019-000019-000000-$A1238
         MACRO
&LAB     XABSM &MODE=24
         AIF   ('&LAB' EQ '').NOLAB CHECK FOR LABEL
&LAB     DS    0H                  MVS/XA SET MODE MACRO
.NOLAB   ANOP
         AIF   ('&MODE' EQ '31').XAON
         AIF   ('&MODE' EQ '24').XAOFF
         MNOTE 8,'MODE &MODE INVALID'
.XAON    ANOP                      GO TO 31 BIT MODE
         CNOP  2,4                 FORCE FULL WORD ALIGNMENT
         LA    R1,*+14 .              POINT TO AMODE = 31 CODE
         O     R1,*+6 .               TURN ON AMODE = 31 BIT
         BSM   0,R1 .                 BRANCH TO AMODE = 31 CODE
         DC    X'80000000'         AMODE = 31 BIT
         AGO   .MACEND
.XAOFF   LA    R1,*+6 .               POINT TO AMODE = 24 CODE
         BSM   0,R1 .                 BRANCH TO AMODE = 24 CODE
.MACEND  ANOP
         MEND
