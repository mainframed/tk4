/* REXX ***************************************************************/
/*                                                                    */
/* DESCRIPTION : ADD A './ ADD MEMBER CARD TO BEGINING OF A MEMBER    */
/*               IN A PARTIONED DATASET.  TO IDENTIFY SEPERATE MEMB.  */
/*               WHEN CREATING A NEW DATASET VIA IEBCOPY.             */
/*                                                                    */
/**********************************************************************/
SAY 'ENTER PDS NAME WITHIN SINGLE QUOTES'
PULL DSN
                                            /* ARG DSN                */
ADDRESS TSO                                 /*                        */
DSN = STRIP(DSN,B,"'")                      /* STRIP INVERTS FROM DSN */
"ALLOC FI(PDS) DA('"DSN"') SHR REUSE ",     /* ALLOCATE FOR DIRECTORY */
       "RECFM(F) DSORG(PS) LRECL(256) BLKSIZE(256)"        /*         */
"EXECIO * DISKR PDS (STEM DIR. FINIS)"      /* READ PDS DIRECTORY     */
"FREE FI(PDS)"                              /* FREE DATASET           */
DO BLK = 1 TO DIR.0                         /* LOOP THRU DIRECTORY    */
 USEDBYTES = C2D(SUBSTR(DIR.BLK,1,2))       /* GET USED BYTE COUNT    */
 INDEX = 3                                  /* SKIP PAST USED BYTES   */
 DO WHILE INDEX < USEDBYTES                 /*                        */
  IF SUBSTR(DIR.BLK,INDEX,8) = 'FFFFFFFFFFFFFFFF'X THEN /* END OF DIR */
   SIGNAL FINISH                            /*                        */
  MEMNAME = STRIP(SUBSTR(DIR.BLK,INDEX,8),B) /* GET MEMBER NAME       */
  INDEX = INDEX + 11                        /* SKIP PAST NAME AND TTR */
  PDS2INDC = SUBSTR(DIR.BLK,INDEX,1)        /*                        */
  LEN = BITAND(PDS2INDC,'1F'X)              /* ISOLATE USER DATA LENG */
  USERLEN = C2D(LEN) * 2                    /* HALFWORDS TO BYTES     */
 USERDATA = SUBSTR(DIR.BLK,INDEX,USERLEN)  /* GET USERDATA           */
  BITS = X2B(C2X(SUBSTR(USERDATA,1,1)))     /*                        */
  IF (SUBSTR(BITS,1,1) = '1') THEN          /* CHECK IF ALIAS         */
   ITERATE                                  /*                        */
  DATALINE = './ ADD NAME='MEMNAME          /* SPECIFY LINE OF DATA   */
  "NEWSTACK"                                /* CREATE NEW DATA STACK  */
  QUEUE DATALINE                            /* ADD LINE TO STACK FIFO */
  "ALLOC FI(MEMIN) DA('"DSN"("MEMNAME")') SHR REUSE" /* ALLOC MEMBER */
  "EXECIO * DISKR MEMIN (FIFO FINIS)"       /* READ LINES OF MEMBER   */
  LINES = QUEUED()                          /* GET TOTAL QUEUE LINES  */
  "EXECIO "LINES" DISKW MEMIN (FINIS)"      /* WRITE NEW MEMBER LINES */
  "DELSTACK"                                /* DELETE NEW DATA STACK  */
  INDEX = INDEX + USERLEN + 1               /* SKIP PAST USER DATA    */
 END                                        /*                        */
END                                         /*                        */
/**********************************************************************/
FINISH:                                     /*                        */
