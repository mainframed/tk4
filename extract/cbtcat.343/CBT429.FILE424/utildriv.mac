*          DATA SET VEUTIL     AT LEVEL 002 AS OF 06/26/85
         TITLE '* * UTILDRIV - UTILITY FRONT END'
         AST
         EJECT
UTILDRIV CSECT
IEBGENER DS    0H
         ENTRY IEBGENER
         SAVE  (14,12),,IEBGENER-75204002
         BALR  R3,0
         USING *,R3                     SAVE
         ST    R13,SAVEAREA+4
         LA    R12,SAVEAREA                  THE
         ST    R12,8(R13)
         LR    R13,R12                           REGISTERS
         SPACE 2
         L     R2,0(R1)            POINT TO PARM
         LH    R4,0(R2)            LENGTH OF PARM
         LTR   R4,R4               PARM LENGTH EQ 0
         BZ    GENRLINK            YES - NO PARM
         SPACE 2
         LA    R5,2(R2)             POINT TO BEGIN OF PARM
         AR    R5,R4                POINT TO END OF PARM
         LA    R4,2(R2)             POINT TO BEGIN OF PARM
         CLC   0(6,R4),=CL6'/ABEND' ABEND REQUESTED
         BNE   GENRLINK             NO
         MVI   ABENDSW,X'FF'        YES,TURN ON ABEND SWITCH
GENRLINK DS    0H
         LINK  EP=IEBGENEX
         CLI   ABENDSW,X'FF'       ABEND REQUESTED?
         BNE   GENRRTN             NO
         CH    R15,=H'8'                RC GT 8
         BH    ABEND                    YES GO TO ABEND
GENRRTN  DS    0H
         L     R13,SAVEAREA+4           THATS ALL
         RETURN (14,12),RC=(15)         GET OUT
ABEND    LR    R1,R15                   PUT RETURN CODE IN R1
         ABEND (1)
         EJECT
*
SAVEAREA DS    18F
ABENDSW  DC    X'00'               ABEND/NOABEND SWITCH
         SPACE 6
R0       EQU   0
R1       EQU   1                        PARM LIST
R2       EQU   2                   WORK
R3       EQU   3                        BASE REGISTER
R4       EQU   4                   WORK
R5       EQU   5                   WORK
R6       EQU   6                        DECIMAL SAVE OF ABEND PARM
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15                       RETURN CODES
         END
