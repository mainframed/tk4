*          DATA SET VETAPEX    AT LEVEL 013 AS OF 06/26/85
*  AUTHOR:  GARY GROGIS
*  FUNCTION   THIS PROGRAM IS TO MIGRATE 3420 TAPES TO 3480 TAPE      *
*
*
VETAPE   CSECT
         USING VETAPE,R15
         STM   R14,R12,R12(R13)
         ST    R13,S+4
         LR    R14,R13
         CNOP  0,4
         BAL   R13,S+72
S        DS    18F
         ST    R13,8(R14)
         DROP  R15
         USING S,R13
         L     R12,*+8
         B     *+8
         DC    A(S+4096)
         USING S+4096,R12
         OPEN  (PRINT,(OUTPUT))         OPEN CONTROL SHEET FILE
         OPENTMC NOTACT=NOTMC           OPEN TMC FILE
         MVC   CARD(L'TITLE),TITLE      MOVE TITLE TO OUTPUT
         PUT   PRINT,CARD               WRITE TITLE
         TIME  DEC                      GET TODAYS DATE
         ST    R1,FULLWORD              STORE DATE
         AP    FULLWORD,=P'3'           ADD 3 DAYS  (FOR OLD TMC REC)
         UNPK  NEWDATE,FULLWORD         PUT IN CHAR FORMAT
         OI    NEWDATE+6,X'F0'          FLIP SIGN
         AP    FULLWORD,=P'27'         ADD 27 MORE (EXPIRES IN 30 DAYS)
         TITLE 'VETAPE   QUESTIONS TO OPERATOR'
GET      DS    0H
         MVC   REPLY(6),=CL6' '            CLEAR REPLY AREA
         MVC   ECB,=F'0'                   CLEAR ECB
         WTOR  'VETAPE   ENTER VOLUME SERIAL OF VOL TO BE MIGRATED   / X
               OR ENTER ''STOP'' TO TERMINATE JOB  ',REPLY,6,ECB,      X
               ROUTCDE=(3)
         WAIT  1,ECB=ECB                   WAIT TILL REPLY IS SATISFIED
         OC    REPLY(6),=XL6'404040404040' TURN TO UPPER CASE
         CLC   REPLY(4),=CL4'STOP'         REPLY = STOP ?
         BE    ENDJOB                      YES, END.
         CLI   REPLY,X'40'                 REPLY = BLANKS
         BE    ENDJOB                      YES, END.
         TITLE ' READ TMC AND EDIT FOR INELGIBLE VOLUMES'
READTMC  DS    0H
VSNTEST  TESTVSN REPLY,BADVOL1A             TEST FOR VALID TMC VOL SER
TMCGET   GETTMC REPLY,TMRECORD,NOTMC,ENQ=NO  READ TMC FOR VOL SER
         LH    R7,TM#DSNBS                  TEST FOR MULTIPLE DSN 'S
         LTR   R7,R7                         ON VOLUME
         BNZ   BADVOL2A                     MORE THAN ONE SKIP THIS VOL
         CLC   TMDEN,=XL1'D3'               STANDARD DEN
         BE    LOADTAB                      YES, CONTINUE
         CLC   TMDEN,=XL1'E3'               STANDARD DEN
         BNE   BADVOL4A                     NO, PUT OUT MSG
LOADTAB  LA    R7,JOBLIST                   GET ADDRESS OF JOB TABLE
         SR    R4,R4                        CLEAR R4 FOR DIVIDE
         LA    R8,8                         LOAD LENGTH IF ELEMENT
         LA    R5,TBLLEN                    LOAD LENGTH OF TABLE
         DR    R4,R8                        CALC NUM OF ENTRIES
LOOP     LTR   R5,R5                        CHECK FOR POSITIVE
         BNP   ENDJOB                       NO GET OUT
         CLC   TMJOBNM,0(R7)                CJOB INELGIBLE ?
         BE    BADVOL3A                     YES, SKIP THIS VOL
         LA    R7,8(R7)                     NEXT TABLE ENTRY
         BCT   R5,LOOP                      LOOP BACK
         CP    FULLWORD,TMEXPDT             EXPIRE IN 30 OR LESS
         BH    BADVOL5                      SKIP IT
         TITLE ' BUILD JCL AND SUBMIT TO INTERNAL READER'
BLDJCL   OPEN  (SYSOUT,(OUTPUT))           OPEN FOR NEW JCL
         MVC   JOBCARD+7(2),REPLY+4        CHANGE JOB NAME
         MVC   JOBCARD+33(6),REPLY         INSERT VOL SER ON JOB CARD
         MVC   JOBCARD+24(7),=CL7'MIGRATE' 3420 TO 3480
         CLC   REPLY+1(1),=CL1'2'          IS INPUT 3480
         BL    BLDJCLA                     NO
         MVC   JOBCARD+24(7),=CL7'REVERSE' YES SPECIFY REVERSE
BLDJCLA  MVC   CARD(L'JOBCARD),JOBCARD     MOVE JOB CARD
         BAL   R4,PUTINTR                  PUT JOB CARD
         MVC   CARD(L'JOBCARD2),JOBCARD2   MOVE JOB CARD
         BAL   R4,PUTINTR                  PUT JOB CARD
         MVC   CARD(L'EXECARD),EXECARD     MOVE EXEC CARD
         BAL   R4,PUTINTR                  PUT EXEC CARD
         MVC   INCARD1+17(4),=CL4'3420'    DEFAULT 3420 INPUT
         CLC   REPLY+1(1),=CL1'2'          IS INPUT 3480
         BL    BLDJCLB                     NO
         MVC   INCARD1+17(4),=CL4'3480'    3480 INPUT
BLDJCLB  MVC   CARD(L'INCARD1),INCARD1     MOVE SYSUT1 CARD
         BAL   R4,PUTINTR                  PUT SYSUT1 CARD
         LOCATE CAMLST1                    CHECK CATALOG
         LTR   R15,R15                     CATALOGED ?
         BNZ   NOTCAT                      NO , CHECK UNCATALOGED
         LA    R7,CATAREA                  GET ADDRESS OF CATAREA
         LH    R5,0(R7)                    GET NUMBER OF VOLS
         LTR   R5,R5                       >0
         BNP   CATERR                      NO, PROBLEM
         LA    R7,6(R7)                    POINT TO FIRST VOL SER
         ZAP   DBLWORD,=P'0'               CLEAR SECONDARY COUNTER
         MVC   OUTCARD+29(14),=C'CATLG,DELETE),'  DEFAULT OUT DISP
         MVC   INCARD3+13(14),=C'UNCATLG,KEEP),'  DEFAULT IN DISP
CATLOOP  CLC   0(6,R7),TMVOLSER            EQUAL TO TMC ?
         BE    BLDJCLB2                    YES, CREATE REST OF JCL
         LA    R7,12(R7)                   NO, POINT TO NEXT VOL SER
         AP    DBLWORD,=P'1'               ADD TO COUNTER
         CLC   DBLWORD,=P'20'              FIRST 20 VOLS CHECKED ?
         BL    CATRET                      NO, CONTINUE LOOP
         CLC   CATAREA+252(3),=C'0'        CHECK FOR MORE VCB'S
         BE    NOTCAT1                     NO MORE TREAT AS NOT CATLGD
         MVC   TTR,CATAREA+252             YES, GET ADDR OF NEXT VCB
         LOCATE CAMLST2                    READ NEXT VCB
         LTR   R15,R15                     GOOD RETURN ?
         BNZ   CATERR                      NO GET OUT
         LA    R7,CATAREA+6                POINT TO VOLSER
CATRET   BCT   R5,CATLOOP                  CONTINUE LOOP
         B     NOTCAT1                     ALL CHECKED VOL NOT CATLG
NOTCAT   C     R15,=F'8'                   NAME NOT CATALOGED ?
         BNE   CATERR                      NO, OTHER PROBLEM GET OUT
NOTCAT1  MVC   OUTCARD+29(14),=C'KEEP,DELETE), '  CHG OUT DISP
         MVC   INCARD2+3(9),=CL9'VOL=SER=('       NEED VOL SERS
         MVC   VOLUME,TMVOLSER                    GET IN ORDER
         CLC   TMVOLSEQ,=XL2'1'                   FIRST VOL ?
         BE    TMCLOOP                            START FOLLOWING CHAIN
         MVC   VOLUME,TMFRSVOL                    USE FIRST VOL SER
TMCLOOP  LA    R7,INCARD2+12                      POINT TO CARD POS
         LA    R5,4                               4 VOLS PER CARD
TMCLOOP1 GETTMC VOLUME,TMRECORD,NOTMC,ENQ=NO      READ TMC
         MVC   0(6,R7),TMVOLSER                   WRITE TO CARD
         LA    R7,6(R7)                           INCREMENT CARD ADDR
         CLC   TMNXTVOL,=XL6'0'                   LAST VOL ?
         BE    PAREN                               YES, FINISH CARD
         MVC   0(1,R7),=C','                 COMMA BETWEEN VOL SERS
         LA    R7,1(R7)                      NEXT ADDRESS ON CARD
         MVC   VOLUME,TMNXTVOL               SET TO READ NEXT VOL
         BCT   R5,TMCLOOP1                   LOOP FOR 4 PER CARD
         BAL   R3,BLDJCLB1                   HAVE 4 , WRITE CARD
         LA    R5,4                          SET FOR 4 MORE
         B     TMCLOOP1                      LOOP BACK
PAREN    MVC   0(2,R7),=C'),'                CLOSE PAREN
         LA    R3,BLDJCLB2                   CHG BAL ADDR
         MVC   INCARD3+13(14),=C'KEEP,KEEP),   '  CHG OUT DISP
BLDJCLB1 MVC   CARD(L'INCARD2),INCARD2          MOVE CARD
         BAL   R4,PUTINTR                  WRITE CARD
         LA    R7,INCARD2+3                     POINT TO BEG OF CARD
         MVC   0(68,R7),=CL68' '                CLEAR FOR MORE VOLS
         BR    R3                           CONT LOOP OR FALL THRU
BLDJCLB2 MVC   CARD(L'INCARD3),INCARD3     PUT OUT DISP
         BAL   R4,PUTINTR                  PUT CARD
         MVC   INCARD4+7(44),TMDSN         MOVE DSN FROM TMC TO SYSUT1
         MVC   CARD(L'INCARD4),INCARD4     WRITE OUT DSN
         BAL   R4,PUTINTR                  PUT CARD
         CLC   TMVOLSEQ,=XL2'1'            FIRST VOL ?
         BNE   MOVFRST                     NEED FIRST FOR TMC SYSIN
         MVC   OUTCARD0+4(6),TMVOLSER      GET READY TO WRITE
         B     BLDJCL2C                    BRANCH AROUND
MOVFRST  MVC   OUTCARD0+4(6),TMFRSVOL      GET FIRST FOR TMC UPD
BLDJCL2C MVC   OUTCARDA+8(44),TMDSN        GET NAME FOR TMC UPD
         MVC   OUTCARD+17(4),=CL4'3480'    SET DEVICE FOR OUTPUT
         CLC   REPLY+1(1),=CL1'2'          IS INPUT 3480
         BNE   BLDJCLC                     NO
         MVC   OUTCARD+17(4),=CL4'3420'    SET DEV FOR INPUT
BLDJCLC  MVC   CARD(L'OUTCARD),OUTCARD     MOVE SYSUT2
         BAL   R4,PUTINTR                  PUT CARD
         UNPK  EXPDT,TMEXPDT               UNPACK EXPDT FROM TMC
         MVC   OUTCARD2+33(5),EXPDT        MOVE EXPDT TO JCL
         MVC   CARD(L'OUTCARD2),OUTCARD2
         BAL   R4,PUTINTR                  PUT CARD
         MVC   OUTCARD3+7(44),TMDSN        MOVE DSN TO SYSUT2
         MVC   CARD(L'OUTCARD3),OUTCARD3
         BAL   R4,PUTINTR                  PUT CARD
         MVC   CARD(L'OUTCARD4),OUTCARD4   MOVE SYSPRINT CARD
         BAL   R4,PUTINTR                  PUT CARD
         MVC   CARD(L'OUTCARD5),OUTCARD5   MOVE SYSUDUMP
         BAL   R4,PUTINTR                  PUT CARD
         MVC   CARD(L'OUTCARD6),OUTCARD6   MOVE SYSIN
         BAL   R4,PUTINTR                  PUT SYSIN
         MVC   CARD(L'OUTCARD7),OUTCARD7   MOVE SYSIN
         BAL   R4,PUTINTR                  PUT SYSIN
         MVC   CARD(L'OUTCARD8),OUTCARD8   MOVE SYSIN
         BAL   R4,PUTINTR                  PUT SYSIN
         MVC   CARD(L'OUTCARD9),OUTCARD9   MOVE SYSIN
         BAL   R4,PUTINTR                  PUT SYSIN
         MVC   CARD(L'OUTCARD0),OUTCARD0   MOVE SYSIN
         BAL   R4,PUTINTR                  PUT SYSIN
         MVC   CARD(L'OUTCARDA),OUTCARDA   MOVE SYSIN
         BAL   R4,PUTINTR                  PUT SYSIN
         MVC   OUTCARDB+10(5),NEWDATE+2
         MVC   CARD(L'OUTCARDB),OUTCARDB   MOVE SYSIN
         BAL   R4,PUTINTR                  PUT SYSIN
         MVC   CARD(L'ENDCARD),ENDCARD     MOVE END CARD
         BAL   R4,PUTINTR                  PUT CARD
         CLOSE SYSOUT                      CLOSE AND SUBMIT JCL
         MVC   CONREC+35(8),=CL8'MIGRATED'
         CLC   REPLY+1(1),=CL1'2'          IS INPUT 3480
         BNE   BLDJCLD                     NO
         MVC   CONREC+35(8),=CL8'REVERSED'
BLDJCLD  MVC   CONREC+27(7),JOBCARD+2     MOVE JOBNAME TO CONTROL SHEET
         MVC   CONREC+5(6),REPLY          MOVE VOL SER TO CONTROL SHEET
         MVC   CARD(L'CONREC),CONREC      MOVE TO OUTPUT
         PUT   PRINT,CARD                 WRITE CONTROL LINE
         MVC   CARD(L'INCARD4),INCARD4     WRITE OUT DSN
         PUT   PRINT,CARD                 WRITE CONTROL LINE
         B     GET                        GO GET NEXT VOL SER
PUTINTR  PUT   SYSOUT,CARD                WRITE ROUTINE
         BR    R4
         TITLE ' ERROR AND BYPASS ROUTINES  '
BADVOL1A MVC   BADVOL1+21(6),REPLY    MOVE VOL SER TO MESSAGE
BADVOL1  WTO   'VETAPE   VOL XXXXXX NOT VALID OR NOT FOUND',           X
               ROUTCDE=(3)            TELL OPERATOR
         MVC   CONREC0+5(6),REPLY     MOVE VOL SER TO CONTROL LIST
         MVC   CARD(L'CONREC0),CONREC0
         PUT   PRINT,CARD              WRITE CONTROL LIST
         B     GET                    GO GET NEXT VOL
BADVOL2A MVC   BADVOL2+8(6),REPLY     MOVE VOL SER TO MESSAGE
BADVOL2  WTO   'XXXXXX CONTAINS MULTIPLE DSNS AND CANNOT BE MIGRATED', X
               ROUTCDE=(3)            TELL OPERATOR
         MVC   CONREC1+5(6),REPLY     MOVE VOL SER TO CONTROL LIST
         MVC   CARD(L'CONREC1),CONREC1
         PUT   PRINT,CARD              WRITE CONTROL LIST
         B     GET                     GO GET NEXT VOL
BADVOL3A MVC   BADVOL3+8(6),REPLY      MOVE VOL SER TO OPERATOR MSG
         MVC   BADVOL3+26(8),TMJOBNM   MOVE CJOB TO OPERATOR MSG
BADVOL3  WTO   'XXXXXX CREATED BY             AND CANNOT BE MIGRATED', X
               ROUTCDE=(3)             TELL OPERATOR VOL INELGIBLE
         MVC   CONREC2+5(6),REPLY      MOVE VOL TO CONTROL LIST
         MVC   CONREC2+24(8),TMJOBNM   MOVE CJOB TO CONTROL LIST
         MVC   CARD(L'CONREC2),CONREC2
         PUT   PRINT,CARD              WRITE CONTROL LIST
         B     GET                     GO GET NEXT VOL
BADVOL4A MVC   BADVOL4+8(6),REPLY      MOVE VOL SER TO OPERATOR MSG
BADVOL4  WTO   'XXXXXX USES NON STANDARD DEN  AND CANNOT BE MIGRATED', X
               ROUTCDE=(3)             TELL OPERATOR VOL INELGIBLE
         MVC   CONREC3+5(6),REPLY      MOVE VOL TO CONTROL LIST
         MVC   CARD(L'CONREC3),CONREC3
         PUT   PRINT,CARD              WRITE CONTROL LIST
         B     GET                     GO GET NEXT VOL
BADVOL5  MVC   CONREC4+5(6),REPLY      MOVE VOL TO CONTROL LIST
         MVC   CARD(L'CONREC4),CONREC4
         PUT   PRINT,CARD              WRITE CONTROL LIST
         B     GET                     GO GET NEXT VOL
CATERR   MVC   CATERR1+8(6),REPLY      MOVE VOL SER TO OPERATOR MSG
CATERR1  WTO   'XXXXXX   ENCOUNTERED CATALOG ERROR',                   X
               ROUTCDE=(3)
         MVC   CONREC5+5(6),REPLY      MOVE VOL TO CONTROL LIST
         MVC   CARD(L'CONREC5),CONREC5
         PUT   PRINT,CARD              WRITE CONTROL LIST
         CLOSE SYSOUT                      CLOSE AND SUBMIT JCL ERROR
         B     GET                     GO GET NEXT VOL
NOTMC    WTO   'VETAPE   TMC NOT ACTIVE UNABLE TO CONTINUE',           X
               ROUTCDE=(3)
ENDJOB   CLOSE PRINT
         WTO   'VETAPE    ENDING TO CONTINUE PROCESSING RESTART JOB',  X
               ROUTCDE=(3)
         L     R13,4(R13)
         RETURN (14,12),RC=0
         TITLE 'REGISTER EQUATES'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'DEFINED STORAGE AND CONSTANTS'
FULLWORD DS    F
NEWDATE  DS    CL7
VOLUME   DS    CL6
TTR      DS    CL3
DBLWORD  DS    D
CARD     DS    CL80
REPLY    DC    CL6' '
EXPDT    DS    CL5
ECB      DC    F'0'
TITLE    DS    0CL133
         DC    CL1'1'
         DC    CL30' '
         DC    CL28'TAPE MIGRATION CONTROL SHEET'
         DC    CL74' '
CONREC   DC    CL80' VOL XXXXXX  SUBMITTED JOB XXXXXXX             '
CONREC0  DC    CL80' VOL XXXXXX  NOT VALID OR NOT FOUND IN TMC     '
CONREC1  DC    CL80' VOL XXXXXX  HAS MUTIPLE DSNS    CANNOT MIGRATE'
CONREC2  DC    CL80' VOL XXXXXX  CREATED BY          CANNOT MIGRATE'
CONREC3  DC    CL80' VOL XXXXXX  NON STANDARD DEN    CANNOT MIGRATE'
CONREC4  DC    CL80' VOL XXXXXX  EXPIRES IN 30 DAYS OR LESS,  SKIP '
CONREC5  DC    CL80' VOL XXXXXX  ENCOUNTERED CATALOG ERROR    SKIP '
         TITLE 'SKELETON JCL   '
JOBCARD  DC    CL80'//?????XX JOB (?,????),''MIGRATE  XXXXXX'','
JOBCARD2 DC    CL80'//     CLASS=?,MSGCLASS=?'
EXECARD  DC    CL80'//STEP1    EXEC  PGM=IEBGENER,PARM=''/ABEND'''
INCARD1  DC    CL80'//SYSUT1 DD UNIT=3420,                        '
INCARD2  DC    CL80'//                                                 X
                                            '
INCARD3  DC    CL80'// DISP=(OLD,                                  '
INCARD4  DC    CL80'// DSN=                                            X
                '
OUTCARD  DC    CL80'//SYSUT2 DD UNIT=3480,DISP=(,CATLG,DELETE),'
OUTCARD2 DC    CL80'// DCB=*.SYSUT1,LABEL=(,SL,EXPDT=XXXXX),VOL=(,,,21)X
               ,'
OUTCARD3 DC    CL80'// DSN=                                            X
                '
OUTCARD4 DC    CL80'//SYSPRINT DD SYSOUT=*,DCB=BUFNO=1'
OUTCARD5 DC    CL80'//SYSUDUMP DD SYSOUT=A,DCB=BUFNO=1'
OUTCARD6 DC    CL80'//SYSIN    DD *'
OUTCARD7 DC    CL80'//STEP2    EXEC PGM=TMSUPDTE,COND=(0,LT,STEP1)'
OUTCARD8 DC    CL80'//TMSRPT   DD SYSOUT=*       '
OUTCARD9 DC    CL80'//SYSIN    DD *              '
OUTCARD0 DC    CL80'VOL XXXXXX                   '
OUTCARDA DC    CL80'VER DSN=                                           X
                 '
OUTCARDB DC    CL80'REP EXPDT=XXXXX'
ENDCARD  DC    CL80'//'
         TITLE 'INELGIBLE JOB TABLE'
         DS    0F
JOBLIST  EQU   *
         DC    C'JOB1    '
         DC    C'JOB2    '
         DC    C'ETC     '
TBLLEN   EQU   *-JOBLIST
         TITLE 'TMS RECORD FORMAT'
         TMRECORD
         TITLE 'CATALOG WORK AREA'
CAMLST1  CAMLST NAME,TMDSN,,CATAREA
CAMLST2  CAMLST BLOCK,TTR,,CATAREA
CATAREA  DS    0D
         DS    265C
         TITLE 'DCBS     '
SYSOUT   DCB   DDNAME=SYSOUT,MACRF=PM,DSORG=PS,RECFM=F,BLKSIZE=80,     X
               BUFNO=1
PRINT    DCB   DDNAME=PRINT,MACRF=PM,DSORG=PS
         END
