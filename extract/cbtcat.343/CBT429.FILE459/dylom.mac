         MACRO           ALIAS(ES) PROCESS DSECT
        @AL    &DUMMY
.* SYNTAX :    @AL  ,
         SPACE 1
*------- ALIAS(ES) TABLE DSECT
         SPACE 1
ALDSECT  DSECT
ALNME    DS    CL8                 NAME
ALANM    DS    CL8                 ALIAS NAME
ALNDD    DS    XL1                 NUMBER OF DD-NAME(S)
ALNUN    DS    XL1                 NUMBER OF UNLESS NAME(S)
ALBSL    EQU   *-ALDSECT           BASE LENGTH
ALDDN    DS    0CL8                DD-NAME(S) ...
ALUNN    DS    0CL8                UNLESS NAME(S) ...
         SPACE 1
         MEND
         MACRO           CALL ROUTINE
&NAME   @CALL  &WHERE,&GO=NO
         LCLC  &NM
.* SYNTAX :    ...  @CALL  ...,GO=YES
.*                         ,
.*             WHERE : ... IS THE LABEL OF WHERE TO GO (RECOGNIZE
.*                     LABELS : PUTLNE, DIAGDD, FREEDS AND PERMAL)
.*                     GO=NO TO USE "BASR"
.*                     GO=YES TO USE "BR"
&NM      SETC  '&NAME'
         AIF   ('&WHERE' EQ '').A11
         AIF   ('&WHERE' EQ 'PUTLNE').A1
         AIF   ('&WHERE' EQ 'DIAGDD').A2
         AIF   ('&WHERE' EQ 'FREEDS').A3
         AIF   ('&WHERE' EQ 'PERMAL').A4
&NM      L     R15,=A(&WHERE)      GET ENTRY POINT
         AGO   .A10
.A1      ANOP
&NM      L     R15,A$PL            GET ENTRY POINT
         AGO   .A10
.A2      ANOP
&NM      L     R15,A$DD            GET ENTRY POINT
         AGO   .A10
.A3      ANOP
&NM      L     R15,A$FD            GET ENTRY POINT
         AGO   .A10
.A4      ANOP
&NM      L     R15,A$PA            GET ENTRY POINT
.A10     ANOP
&NM      SETC  ''
.A11     AIF   ('&GO' EQ 'YES').A12
&NM      BASR  R14,R15             GO TO ROUTINE
         MEXIT
.A12     ANOP
&NM      BR    R15                 GO TO ROUTINE
         MEND
         MACRO           CHECK OPERANDS KEYWORDS NAMES LIST
&NAME   @CHKO  &WHERE=,&NOMATCH=,&MATCH=
         GBLA  &TSON
         GBLC  &TSOP(100)
         LCLA  &L,&N,&T,&X
         LCLB  &IW
         LCLC  &KW,&BW
.* SYNTAX :    ...  @CHKO  ...,...,..., <- KEYWORDS NAMES LIST
.*                         WHERE=...,NOMATCH=...,MATCH=...
&NAME    BAS   R6,&WHERE
         AIF   ('&NOMATCH' NE '').A1
&IW      SETB  1
&BW      SETC  'IHB'.'&SYSNDX'
         B     &BW                 +0 = NO MATCH
         AGO   .A2
.A1      ANOP
         B     &NOMATCH            +0 = NO MATCH
.A2      AIF   ('&MATCH' NE '').B2
         AIF   (&IW).B1
&IW      SETB  1
&BW      SETC  'IHB'.'&SYSNDX'
.B1      B     &BW                 +4 = OK, MATCH
         AGO   .B3
.B2      ANOP
         B     &MATCH              +4 = OK, MATCH
.B3      ANOP
&N       SETA  N'&SYSLIST
         AIF   (&N EQ 0).D1
&L       SETA  1
.C1      ANOP
         AIF   ('&SYSLIST(&L)' EQ '').C9
         AIF   ('&SYSLIST(&L)'(1,1) NE '''').C3
&T       SETA  K'&SYSLIST(&L)
         AIF   ('&SYSLIST(&L)'(&T,1) EQ '''').C2
         MNOTE 8,' KEYWORD NAME &L : FINAL QUOTE MISSING ************ '
         AGO   .END
.C2      ANOP
&T       SETA  &T-2
&KW      SETC  '&SYSLIST(&L)'(2,&T)
         AGO   .C4
.C3      ANOP
&KW      SETC  '&SYSLIST(&L)'
.C4      ANOP
&T       SETA  K'&KW-1
         AIF   (&T LT 50).C5
         MNOTE 8,' KEYWORD NAME &L : TOO LONG (MAX. = 50 CHAR.) ***** '
         AGO   .END
.C5      ANOP
&X       SETA  1
.C6      AIF   ('&TSOP(&X)' EQ '').C7
         AIF   ('&TSOP(&X)' EQ '&KW').C8
&X       SETA  &X+1
         AIF   (&X LE &TSON).C6
         MNOTE 8,' INTERNAL MACRO GLOBAL CHECK TABLE OVERFLOW ******* '
         MNOTE 8,' = INCREMENT DIMENSION OF GLOBAL VARIABLE TSOP    = '
         MNOTE 8,' = AND SET THE GLOBAL VARIABLE TSON EQUAL TO IT.  = '
         AGO   .END
.C7      ANOP
&TSOP(&X) SETC '&KW'
.C8      DC    AL1(&T),C'&KW'
.C9      ANOP
&L       SETA  &L+1
         AIF   (&L LE &N).C1
.D1      DC    AL1(255)            END OF LIST
         AIF   (&IW).D2
         DS    0H                  FORCE HALWORD ALIGNMENT
         AGO   .END
.D2      ANOP
&BW      DS    0H                  FORCE HALWORD ALIGNMENT
.END     MEND
         MACRO           CHECK KEYWORDS LIST
&NAME   @CHKW  &WHERE=,&NOMATCH=,&MATCH=
         LCLA  &L,&N,&T
         LCLB  &IW
         LCLC  &KW,&BW
.* SYNTAX :    ...  @CHKW  ...,...,..., <- KEYWORDS LIST
.*                         WHERE=...,NOMATCH=...,MATCH=...
&NAME    BAS   R6,&WHERE
         AIF   ('&NOMATCH' NE '').A1
&IW      SETB  1
&BW      SETC  'IHB'.'&SYSNDX'
         B     &BW                 +0 = NO MATCH
         AGO   .A2
.A1      ANOP
         B     &NOMATCH            +0 = NO MATCH
.A2      AIF   ('&MATCH' NE '').B2
         AIF   (&IW).B1
&IW      SETB  1
&BW      SETC  'IHB'.'&SYSNDX'
.B1      B     &BW                 +4 = OK, MATCH
         AGO   .B3
.B2      ANOP
         B     &MATCH              +4 = OK, MATCH
.B3      ANOP
&N       SETA  N'&SYSLIST
         AIF   (&N EQ 0).D1
&L       SETA  1
.C1      ANOP
         AIF   ('&SYSLIST(&L)' EQ '').C6
         AIF   ('&SYSLIST(&L)'(1,1) NE '''').C3
&T       SETA  K'&SYSLIST(&L)
         AIF   ('&SYSLIST(&L)'(&T,1) EQ '''').C2
         MNOTE 8,' KEYWORD &L : FINAL QUOTE MISSING ***************** '
         AGO   .END
.C2      ANOP
&T       SETA  &T-2
&KW      SETC  '&SYSLIST(&L)'(2,&T)
         AGO   .C4
.C3      ANOP
&KW      SETC  '&SYSLIST(&L)'
.C4      ANOP
&T       SETA  K'&KW-1
         AIF   (&T LT 50).C5
         MNOTE 8,' KEYWORD &L : TOO LONG (MAX. = 50 CHAR.) ********** '
         AGO   .END
.C5      DC    AL1(&T),C'&KW'
.C6      ANOP
&L       SETA  &L+1
         AIF   (&L LE &N).C1
.D1      DC    AL1(255)            END OF LIST
         AIF   (&IW).D2
         DS    0H                  FORCE HALWORD ALIGNMENT
         AGO   .END
.D2      ANOP
&BW      DS    0H                  FORCE HALWORD ALIGNMENT
.END     MEND
         MACRO           DD-NAME LIBRARIES DSECT
        @DD
.* SYNTAX :    @DD  ,
         SPACE 1
*------- DD-NAME LIBRARIES INFORMATION TABLE DSECT
         SPACE 1
LIBCITD  DSECT
LIBSPA   DS    A                   DD-NAME PRE-ALL. ROUTINE ADDRESS
LIBDDN   DS    CL8                 DD-NAME
LIBLID   DS    CL20                IDENTIFICATION
LIBORG   DS    XL1                 DSORG
LIBRFM   DS    XL1                 RECFM
LIBLRL   DS    H                   LRECL
         DS    0F                  FORCE ALIGNMENT
LIBNDS   DS    0XL(LIB#LDS)        DATA-SET(S) NAME(S) LIST
         SPACE 1
LIB#DS   DSECT ,                   DATA-SET NAME LIST ELEMENT
LIB#DSSA DS    A                   SPECIAL PROCESSING ROUTINE ADDRESS
LIB#DSF$ EQU   X'FFFFFFFF',4       END OF LIST
LIB#DSF  DS    XL1                 FLAGS BYTE
LIB#DSI  DS    XL3                 INDICATORS BYTES
LIB#DSN  DS    CL44                DS-NAME
         DS    0F                  FORCE ALIGNMENT
LIB#LDS  EQU   *-LIB#DS            LENGTH OF ELEMENT
         SPACE 1
         MEND
         MACRO           DEFINE FLAGS
        @DEFF
         GBLC  &FIN1(8)
         LCLA  &L,&N,&T
         LCLC  &FB(8),&IN
.* SYNTAX :    @DEFF  ...,...,... <- LIST OF FLAGS NAME (MAX. = 8,
.*                                        LENGTH OF NAME = 1 CHAR.)
&FB(1)   SETC  '80'
&FB(2)   SETC  '40'
&FB(3)   SETC  '20'
&FB(4)   SETC  '10'
&FB(5)   SETC  '08'
&FB(6)   SETC  '04'
&FB(7)   SETC  '02'
&FB(8)   SETC  '01'
&N       SETA  N'&SYSLIST
         AIF   (&N NE 0).A
         MNOTE 8,' OPERANDS MISSING ********************************* '
         AGO   .END
.A       AIF   (&N LE 8).B
         MNOTE 4,' TOO MANY OPERANDS (8 CHAR. ASSUMED) ************** '
&N       SETA  8
.B       ANOP
&L       SETA  1
         SPACE 1
.C       AIF   ('&SYSLIST(&L)' EQ '').E
&IN      SETC  '&SYSLIST(&L)'
         AIF   (K'&IN EQ 1).C1
         MNOTE 8,' &IN : INVALID (ONLY 1 CHAR. ALLOWED) ************* '
         AGO   .E
.C1      AIF   ('&IN' NE '$').D
         MNOTE 8,' &IN : INVALID (RESERVED : END OF LIST) *********** '
         AGO   .E
.D       ANOP
&T       SETA  1
.D1      AIF   ('&FIN1(&T)' NE '&IN').D2
         MNOTE 8,' &IN : DUPLICATE CHAR. DEFINITION ***************** '
         AGO   .E
.D2      AIF   (&T EQ 8).D3
&T       SETA  &T+1
         AGO   .D1
.D3      ANOP
&T       SETA  &L
         AIF   ('&FIN1(&T)' EQ '').DS1S
         MNOTE 8,' FLAG &IN OVERLAPS &FIN1(&T) ********************** '
         AGO   .E
.DS1S    ANOP
&FIN1(&T) SETC '&IN'
LIB#DSF&IN EQU   X'&FB(&T)'
.E       ANOP
&L       SETA  &L+1
         AIF   (&L LE &N).C
.END     MEND
         MACRO           SAVE AREA'S TANK MANAGEMENT
&NAME   @ENTER &DUMMY
.* SYNTAX :    ... @ENTER  ,   ENTER SUBPROGRAM
        CNOP   0,4
         USING &NAME,R9            SET LOCAL ADDRESSABILITY
&NAME    STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         LR    R9,R15              SET LOCAL BASE REGISTER
         CP    TANKNO,TANKMX       CONTROL TANK AVAILABILITY
         BNL   STOVER              OVERFLOW
         AP    TANKNO,TANKUP       UP TANKS NUMBER
         LA    R15,18*4(R13)       NEXT SAVE AREA
         XC    0(18*4,R15),0(R15)  CLEAR IT
         ST    R15,8(R13)          LINK SAVE AREAS
         ST    R13,4(R15)
         LR    R13,R15             ACTIVATE NEW SAVE AREA
         MEND
         MACRO           SAVE AREA'S TANK MANAGEMENT
&NAME   @EXIT  &DUMMY
.* SYNTAX :    ... @EXIT  ,   EXIT SUBPROGRAM
&NAME    CP    TANKNO,TANKUP       CONTROL TANK AVAILABILITY
         BL    STUNDER             UNDERFLOW
         SP    TANKNO,TANKUP       DOWN TANKS NUMBER
         L     R13,4(R13)          PREVIOUS SAVE AREA
         ST    R15,16(R13)         SET REGISTER 15 (RC)
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGISTERS
         MVI   12(R13),X'FF'       INDICATE RETURN EXECUTED
         BR    R14                 RETURN
         DROP  R9                  KILL LOCAL ADDRESSABILITY
         MEND
         MACRO           GENERATE CHECK KEYWORD ROUTINE
&NAME   @GCHKR  &RP,&RE
.* SYNTAX :    ...  @GCHKR  ..., <- CURRENT TEXT POINTER REGISTER
.*                          ... <- END OF TEXT POINTER REGISTER
&NAME    LA    R15,8(R6)           POINT TO LIST
         XR    R7,R7
&NAME.1  CLI   0(R15),255          END OF LIST ?
         BER   R6                  YES, NO MATCH
         IC    R7,0(R15)           GET KEYWORD COMPARE LENGTH
         LA    R0,0(&RP,R7)
         CLR   R0,&RE              OVER END OF TEXT ?
         BH    *+L'*+8             YES, NO MATCH, SKIP TO NEXT
         EX    R7,*+L'*+12         KEYWORD IN TEXT ?
         BE    4(R6)               YES, OK, MATCH
         LA    R15,2(R15,R7)       POINT TO NEXT
         B     &NAME.1
         CLC   0(*-*,&RP),1(R15)   <<EXECUTED>>
         MEND
         MACRO           TEST KEYWORD
&NAME   @IFKW  &KW,&VK,&GOTO=,&ELSE=
.* SYNTAX :    ...  @IFKW  ..., <- KEYWORD NAME
.*                         ..., <- U = UNSPECIFIED OPTION
.*                                 N = NO OPTION
.*                                 S = OPTION
.*                         GOTO=..., <- BRANCH IF EQUAL
.*                         ELSE=...  <- BRANCH IF NOT EQUAL
         AIF   ('&KW' NE '').A
         MNOTE 8,' KEYWORD NAME MISSING ***************************** '
         AGO   .END
.A       AIF   (K'&KW LE 6).B
         MNOTE 8,' KEYWORD NAME TOO LONG (MAX. CHAR. 6) ************* '
         AGO   .END
.B       AIF   ('&VK' NE '').C
         MNOTE 8,' KEYWORD VALUE MISSING **************************** '
         AGO   .END
.C       AIF   ('&VK' EQ 'U').D
         AIF   ('&VK' EQ 'N').E
         AIF   ('&VK' EQ 'S').F
         MNOTE 8,' KEYWORD VALUE INVALID (NOT U, N OR S) ************ '
         AGO   .END
.D       ANOP
&NAME    CLI   KW&KW,0             UNSPECIFIED OPERAND
         AGO   .G
.E       ANOP
&NAME    CLI   KW&KW,1             NULLIFIED OPERAND
         AGO   .G
.F       ANOP
&NAME    CLI   KW&KW,2             SPECIFIED OPERAND
.G       AIF   ('&GOTO' NE '').H
         AIF   ('&ELSE' NE '').I
         MNOTE 8,' GOTO= OR ELSE= MISSING *************************** '
         AGO   .END
.H       BE    &GOTO
         AIF   ('&ELSE' EQ '').END
         B     &ELSE
         AGO   .END
.I       BNE   &ELSE
.END     MEND
         MACRO           LIST ALL OPERANDS KEYWORDS NAMES
        @LSTO  &DUMMY
         GBLA  &TSON
         GBLC  &PUN,&TSOP(100)
         LCLA  &X
         LCLB  &S
.* SYNTAX :    @LSTO  ,
&S       SETB  0
&X       SETA  1
.A       AIF   ('&TSOP(&X)' EQ '').C
         AIF   (&S).B
&S       SETB  1
         AIF   ('&PUN' EQ 'YES').A1
         MNOTE *,' --- LIST OF GLOBAL DEFINITIONS AS IN "DYLOCOM" MODULX
               E :'
         AGO   .B
.A1      MNOTE *,' --- UPDATE GLOBAL DEFINITIONS IN "DYLOCOM" MODULE ASX
                BELOW :'
.B       AIF   (&X GE 10).B1
         MNOTE *,'     ===> &&TSOP(&X)   SETC  ''&TSOP(&X)'' '
         AIF   ('&PUN' NE 'YES').C
         PUNCH '&&TSOP(&X)   SETC  ''&TSOP(&X)'' '
         AGO   .C
.B1      AIF   (&X GE 100).B2
         MNOTE *,'     ===> &&TSOP(&X)  SETC  ''&TSOP(&X)'' '
         AIF   ('&PUN' NE 'YES').C
         PUNCH '&&TSOP(&X)  SETC  ''&TSOP(&X)'' '
         AGO   .C
.B2      MNOTE *,'     ===> &&TSOP(&X)  SETC ''&TSOP(&X)'' '
         AIF   ('&PUN' NE 'YES').C
         PUNCH '&&TSOP(&X)  SETC ''&TSOP(&X)'' '
.C       ANOP
&X       SETA  &X+1
         AIF   (&X LE &TSON).A
         AIF   (&S).D
         MNOTE 8,'     NO KEYWORDS FOUND ... ************************ '
         AGO   .END
.D       MNOTE *,' --- END OF OPERANDS KEYWORDS LIST ---'
         AIF   ('&PUN' NE 'YES').END
         MNOTE *,' === GBLC CARDS ARE INSERTED IN MEMBER "DYLOOBJ" ==='
         MNOTE 0,' === (AFTER EVENTUAL USE, DELETE FROM "DYLOOBJ") ==='
.END     MEND
         MACRO           OPERANDS KEYWORD-ADDRESS TABLE
&NAME   @OKAT  &KW,&AD
         LCLA  &N
.* SYNTAX :    ...  @OKAT  ..., <- KEYWORD
.*                         ...  <- ADDRESS OF PROCESS
         AIF   ('&NAME' EQ '').A
&NAME    DS    0XL2
.A       ANOP
&N       SETA  K'&KW
         DC    AL1(&N),AL4(&AD),C'&KW='
         MEND
         MACRO           OPERANDS KEYWORD-INDEX TABLE
&NAME   @OKIT  &KW,&IX
         LCLA  &N
.* SYNTAX :    ...  @OKIT  ..., <- KEYWORD
.*                         ...  <- KEYWORD INDEX NAME
         AIF   ('&NAME' EQ '').A
&NAME    DS    0XL2
.A       ANOP
&N       SETA  K'&KW
         DC    AL1(&N,&IX),C'&KW='
         MEND
         MACRO           PASS REGISTER MANAGEMENT
&NAME   @PASS  &REG
.* SYNTAX :    ...  @PASS  ... <- REGISTER
         AIF   ('&REG' EQ '13').A
         AIF   ('&REG' EQ 'R13').A
         AIF   ('&REG' EQ '14').A
         AIF   ('&REG' EQ 'R14').A
         AIF   ('&REG' EQ '15').A
         AIF   ('&REG' NE 'R15').B
.A       MNOTE 8,' &REG : INVALID REGISTER ************************** '
         AGO   .END
.B       ANOP
&NAME    L     R14,4(R13)          GET PREVIOUS SAVE AREA
         ST    &REG,20+(&REG*4)(R14) PASS REGISTER CONTENTS
.END     MEND
         MACRO           SET KEYWORD
&NAME   @SETKW &KW,&VK,&GOTO=
.* SYNTAX :    ...  @SETKW  ..., <- KEYWORD NAME
.*                          ..., <- U = UNSPECIFIED OPTION
.*                                  N = NO OPTION
.*                                  S = OPTION
.*                          GOTO=... <- UNCONDITIONAL BRANCH
         AIF   ('&KW' NE '').A
         MNOTE 8,' KEYWORD MISSING ********************************** '
         AGO   .END
.A       AIF   (K'&KW LE 6).B
         MNOTE 8,' &KW : NAME TOO LONG (MAX.CHAR. 6) **************** '
         AGO   .END
.B       AIF   ('&VK' NE '').C
         MNOTE 8,' KEYWORD VALUE MISSING **************************** '
         AGO   .END
.C       AIF   ('&VK' EQ 'U').D
         AIF   ('&VK' EQ 'N').E
         AIF   ('&VK' EQ 'S').F
         MNOTE 8,' &VK : INVALID KEYWORD VALUE (U, N OR S ONLY) ***** '
         AGO   .END
.D       ANOP
&NAME    MVI   KW&KW,0             SET AS UNSPECIFIED OPERAND
         AGO   .G
.E       ANOP
&NAME    MVI   KW&KW,1             SET AS NULLIFIED OPERAND
         AGO   .G
.F       ANOP
&NAME    MVI   KW&KW,2             SET AS SPECIFIED OPERAND
.G       AIF   ('&GOTO' EQ '').END
         B     &GOTO
.END     MEND
         MACRO           BUILD S99 RB - INITIALIZATION
&NAME   @S99I  &TUPAOV=,&TUAOV=
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @S99I  TUPAOV=...,TUAOV=...
.*             WHERE : TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW TO BE USED AS DEFAULT
.*                             IN ALL FOLLOWING @TU.. MACROS
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                             TO BE USED AS DEFAULT IN ALL FOLLOWING
.*                             @TU.. MACROS
&PAOVE   SETC  ''
&AOVE    SETC  ''
         AIF   ('&TUPAOV' EQ '').A
&PAOVE   SETC  '&TUPAOV'
.A       AIF   ('&TUAOV' EQ '').B
&AOVE    SETC  '&TUAOV'
.B       ANOP
&NAME    XC    RBS99(S99RBEND-S99RB),RBS99
         LM    R0,R1,ALRBTP
         LR    R14,R0
         XR    R15,R15
         MVCL  R0,R14
         LM    R0,R1,ALRBTU
         LR    R14,R0
         XR    R15,R15
         MVCL  R0,R14
         LA    R2,RBS99            BUILD S99 RB PTR
         ST    R2,RBPS99
         OI    RBPS99,S99RBPND     SET LAST POINTER INDICATOR
         USING S99RB,R2
         MVI   S99RBLN,S99RBEND-S99RB BUILD S99 RB
         MVI   S99VERB,S99VRBAL
         OI    S99FLG11,S99NOCNV+S99NOMNT
         L     R3,ALRBTP
         ST    R3,S99TXTPP
         USING S99TUPL,R3
         L     R4,ALRBTU
         USING S99TUNIT,R4
         MEND
         MACRO           BUILD S99 RB - SVC 99 LIST
&NAME   @S99L  &DUMMY
.* SYNTAX :    ...  @S99L  ,
         AIF   ('&NAME' EQ '').NN
&NAME    DS    0F
.NN      ANOP
ALRBTP   DS    3F                  SVC99 POINTERS AREA CONTROL
ALRBTU   DS    3F                  SVC99 TEXT UNITS AREA CONTROL
RBPS99   DS    F                   POINTER TO SVC99 REQUEST BLOCK
RBS99    DS    0F                  SVC 99 REQUEST BLOCK
         DS    XL(S99RBEND-S99RB)
         MEND
         MACRO           BUILD S99 RB - NEXT
&NAME   @S99N  &DUMMY
.* SYNTAX :    ...  @S99N  ,
&NAME    LR    R4,R0
         LA    R3,L'S99TUPTR(R3)
         MEND
         MACRO           BUILD S99 RB - TERMINATION
&NAME   @S99T  &DUMMY
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @S99T  ,
&PAOVE   SETC  ''
&AOVE    SETC  ''
&NAME    OI    S99TUPTR,S99TUPLN   SET LAST TU POINTER IN LIST
         DROP  R2,R3,R4
         MEND
         MACRO           TABLE END MARKER
        @TEM
.* SYNTAX :    @TEM  ,
         DC    AL1(255,255)
         MEND
         MACRO           BUILD S99 RB - TRK/CYL (TU 07/08)
&NAME   @TUTC  &TC,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TUTC  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "TC" = LABEL OF DISPOSITION
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&TC' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY(L'&TC),&TC BUILD TU 07/08 - TRK/CYL ALLOC.
         MVC   S99TUNUM(L'S99TUNUM),=XL2'0'
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - PRIMARY SPACE
&NAME   @TU0A  &PS,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU0A  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "PS" = LABEL OF PRIMARY SPACE
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&PS' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+3(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALPRIME) BUILD TU 0A - PRIMARY SPACE
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'&PS)
         MVC   S99TUPAR(L'&PS),&PS SET FOR NUMBER
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - SECONDARY SPACE
&NAME   @TU0B  &SS,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU0B  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "SS" = LABEL OF SECONDARY SPACE
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&SS' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+3(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALSECND) BUILD TU 0B - SECONDARY SPACE
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'&SS)
         MVC   S99TUPAR(L'&SS),&SS SET FOR NUMBER
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - DIRECTORY BLOCKS
&NAME   @TU0C  &DB,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU0C  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "DB" = LABEL OF DIRECTORY BLOCKS
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&DB' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+3(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALDIR) BUILD TU 0C - DIRECTORY BLOCKS
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'&DB)
         MVC   S99TUPAR(L'&DB),&DB SET FOR # DIR.BLK.
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - DDNAME
&NAME   @TU01  &TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU01  TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LH    R1,WLDD
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG(R1,R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALDDNAM) BUILD TU 01 - DDNAME
         MVC   S99TUNUM,=XL2'1'
         STCM  R1,B'0011',S99TULNG
         BCT   R1,*+L'*+6
         MVC   S99TUPAR(*-*),WDDN  <<EXECUTED>>
         EX    R1,*-6              MOVE IN DDNAME
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - DSNAME
&NAME   @TU02  &TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU02  TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LH    R1,WLDS
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG(R1,R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALDSNAM) BUILD TU 02 - DSNAME
         MVC   S99TUNUM,=XL2'1'
         STCM  R1,B'0011',S99TULNG
         BCT   R1,*+L'*+6
         MVC   S99TUPAR(*-*),WDSN  <<EXECUTED>>
         EX    R1,*-6              MOVE IN DSNAME
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - STATUS
&NAME   @TU04  &ST,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU04  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "ST" IS THE STATUS
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&ST' EQ 'SHR').OK
         AIF   ('&ST' EQ 'NEW').OK
         AIF   ('&ST' EQ 'MOD').OK
         AIF   ('&ST' EQ 'OLD').OK
         MNOTE 8,' STATUS (SHR/NEW/MOD/OLD) MISSING ***************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+L'S99TUPAR(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALSTATS) BUILD TU 04 - STATUS
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(1)
         MVI   S99TUPAR,DA08&ST    SET FOR &ST
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - DISPOSITION
&NAME   @TU05  &DS,&TUPAOV=,&TUAOV=,&LAST=END
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU05  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "DS" IS THE DISPOSITION
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&DS' EQ 'KEEP').OK
         AIF   ('&DS' EQ 'DEL').OK
         AIF   ('&DS' EQ 'CAT').OK
         AIF   ('&DS' EQ 'UCAT').OK
         MNOTE 8,' DISPOSITION (KEEP/DEL/CAT/UCAT) MISSING ********** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+L'S99TUPAR(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALNDISP) BUILD TU 05 - DISPOSITION
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(1)
         MVI   S99TUPAR,DA08&DS    SET FOR &DS
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - DISPOSITION
&NAME   @TU06  &DS,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU06  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "DS" IS THE DISPOSITION
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&DS' EQ 'KEP').OK
         AIF   ('&DS' EQ 'DELE').OK
         AIF   ('&DS' EQ 'CATL').OK
         AIF   ('&DS' EQ 'UNCT').OK
         MNOTE 8,' DISPOSITION (KEP/DELE/CATL/UNCT) MISSING ********* '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+L'S99TUPAR(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALCDISP) BUILD TU 06 - DISPOSITION
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(1)
         MVI   S99TUPAR,DA08&DS    SET FOR &DS
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - BLOCKS
&NAME   @TU09  &BA,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU09  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "BA" = LABEL OF BLOCKS
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&BA' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+3(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALBLKLN) BUILD TU 09 - BLK. ALLOCATION
         MVC   S99TUNUM(L'S99TUNUM),=XL2'1'
         MVC   S99TULNG,=AL2(L'&BA)
         MVC   S99TUPAR(L'&BA),&BA SET BLKSIZE
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - VOLSER
&NAME   @TU10  &VS,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU10  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "VS" = LABEL OF VOLSER
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&VS' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+6(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALVLSER) BUILD TU 10 - VOLSER
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(L'&VS)
         MVC   S99TUPAR(L'&VS),&VS MOVE IN VOL.SER.NUMBER
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - UNIT
&NAME   @TU15  &UN,&TUPAOV=,&TUAOV=,&LAST=END
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU15  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "UN" = LABEL OF UNIT
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&UN' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         XR    R14,R14
         IC    R14,&UN
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG(R14,R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALUNIT) BUILD TU 15 - UNIT
         MVC   S99TUNUM,=XL2'1'
         STCM  R14,B'0011',S99TULNG
         BCT   R14,*+L'*+6
         MVC   S99TUPAR(*-*),&UN+1 <<EXECUTED>>
         EX    R14,*-6             MOVE IN UNIT NAME
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - DCB REFERENCE
&NAME   @TU2C  &DR,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU2C  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "DR" = LABEL OF DCB REFERENCE
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&DR' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         XR    R14,R14
         IC    R14,&DR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG(R4,R14)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALDCBDS) BUILD TU 2C - DCB REFERENCE
         MVC   S99TUNUM,=XL2'1'
         STCM  R14,B'0011',S99TULNG
         BCT   R14,*+L'*+6
         MVC   S99TUPAR(*-*),&DR+1 <<EXECUTED>>
         EX    R14,*-6             MOVE IN DSNAME
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
         LA    R3,L'S99TUPTR(R3)
.END     MEND
         MACRO           BUILD S99 RB - DSORG
&NAME   @TU3C  &DO,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU3C  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "DO" = LABEL OF DSORG
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&DO' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+2(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALDSORG) BUILD TU 3C - DSORG
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(2)
         MVC   S99TUPAR(1),&DO     MOVE IN DSORG
         MVI   S99TUPAR+1,0
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - BLKSIZE
&NAME   @TU30  &SZ,&DFLT,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU30  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "SZ" = LABEL OF BLKSIZE
.*                     DFLT= LABEL OF DEFAULT BLKSIZE (OPTIONAL)
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&SZ' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+2(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALBLKSZ) BUILD TU 30 - BLKSIZE
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(2)
         AIF   ('&DFLT' NE '').G
         MVC   S99TUPAR(L'&SZ),&SZ MOVE IN BLKSIZE
         AGO   .H
.G       OC    &DFLT,&DFLT
         BZ    *+L'*+6
         MVC   &DFLT,&SZ           MOVE IN DEFAULT BLKSIZE
         MVC   S99TUPAR(L'&DFLT),&DFLT MOVE IN BLKSIZE
.H       AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - LRECL
&NAME   @TU42  &LL,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU42  ...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "LL" = LABEL OF LRECL
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&LL' NE '').OK
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+2(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALLRECL) BUILD TU 42 - LRECL
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(2)
         MVC   S99TUPAR(2),&LL     MOVE IN LRECL
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - RECFM
&NAME   @TU49  &RF,&SZ,&LL,&TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU49  ...,...,...,TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : "RF" = LABEL OF RECFM
.*                     "SZ" = LABEL OF BLKSIZE
.*                     "LL" = LABEL OF LRECL
.*                     TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
         AIF   ('&RF' NE '').CN
         MNOTE 8,' 1ST OPERAND MISSING ****************************** '
         AGO   .END
.CN      AIF   ('&SZ' NE '').CL
         MNOTE 8,' 2ND OPERAND MISSING ****************************** '
         AGO   .END
.CL      AIF   ('&LL' NE '').OK
         MNOTE 8,' 3RD OPERAND MISSING ****************************** '
         AGO   .END
.OK      ANOP
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+L'S99TUPAR(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALRECFM) BUILD TU 49 - RECFM
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(1)
         MVC   S99TUPAR(1),&RF     MOVE IN RECFM
         CLC   &SZ,&LL
         BNE   *+L'*+8
         NI    S99TUPAR,255-DS1RECFB SET IT AS RECFM=F
         B     *+L'*+4
         OI    S99TUPAR,DS1RECFB   SET IT AS RECFM=FB
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - PERM. ALL. ATTR.
&NAME   @TU52  &TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU52  TUPAOV=...,TUAOV=....LAST=NO
.*             WHERE : TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALPERMA) BUILD TU 52 - PERM. ALL. ATTR.
         MVC   S99TUNUM(L'S99TUNUM),=XL2'0'
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           BUILD S99 RB - RETURN DDNAME
&NAME   @TU55  &TUPAOV=,&TUAOV=,&LAST=NO
         GBLC  &PAOVE,&AOVE
.* SYNTAX :    ...  @TU55  TUPAOV=...,TUAOV=...,LAST=NO
.*             WHERE : TUPAOV= BRANCH LABEL FOR TEXT UNITS POINTERS
.*                             AREA OVERFLOW
.*                     TUAOV= BRANCH LABEL FOR UNITS AREA OVERFLOW
.*                     LAST=YES TO SKIP @S99N GENERATION (NEXT)
&NAME    CL    R3,ALRBTP+8
         AIF   ('&TUPAOV' NE '').B
         AIF   ('&PAOVE' NE '').A
         MNOTE 8,' TUPAOV= MISSING ********************************** '
         AGO   .END
.A       BNL   &PAOVE
         AGO   .C
.B       BNL   &TUPAOV
.C       ST    R4,S99TUPTR
         LA    R0,L'S99TUKEY+L'S99TUNUM+L'S99TULNG+8(R4)
         CL    R0,ALRBTU+8
         AIF   ('&TUAOV' NE '').E
         AIF   ('&AOVE' NE '').D
         MNOTE 8,' TUAOV= MISSING *********************************** '
         AGO   .END
.D       BNL   &AOVE
         AGO   .F
.E       BNL   &TUAOV
.F       MVC   S99TUKEY,=AL2(DALRTDDN) BUILD TU 55 - RETURN DDNAME
         MVC   S99TUNUM,=XL2'1'
         MVC   S99TULNG,=AL2(8)
         MVC   S99TUPAR(8),=CL8' ' RESET DDNAME RETURN FIELD
         AIF   ('&LAST' EQ 'YES').END
        @S99N  ,
.END     MEND
         MACRO           VECTOR COMMUNICATIONS.
&NAME   @VCOM  &DUMMY
.* SYNTAX :    ...  @VCOM  ,
&NAME    DS    0F
ASRVC    DS    A
AUSD     DS    A
ASOK     DS    A
AWIZ     DS    A
ATLPFX   DS    A
ATLDSN   DS    A
AINDV    DS    A
DDSTART  DS    A
DDEND    DS    A
USERSS   DS    A
USERSE   DS    A
GROUPSS  DS    A
GROUPSE  DS    A
AUTOS    DS    A
LFLTB    DS    A
LFLTB#   EQU   2                   TABLE ELEMENT'S LENGTH
LFLTB$   EQU   1                   NAME'S LENGTH (COMPARE)
SELTB    DS    A
INDTB    DS    A
INDTB#   EQU   7                   TABLE ELEMENT'S LENGTH
INDTB$   EQU   4                   NAME'S LENGTH (COMPARE)
XTSTFM   DS    XL1
XTSTFX   DS    XL1
         DS    AL1
LGNKW    DS    AL1
         DS    0F                  SET FULLWORD ALIGNMENT
VCOML    EQU   *-&NAME             VECTOR COMMUNICATIONS CHECK LENGTH
         MEND
         MACRO           WORK-AREAS COMMUNICATIONS
&NAME   @WAC   &DUMMY
.* SYNTAX :    ...  @WAC  ,
WACOM    DSECT ,
AOCKR    DS    A
ZGRNM    DS    CL8
ZUSID    DS    CL8
ZTDSI    DS    XL3
ZXDSI    DS    XL3
ZSWSTTS  DS    XL1
ZGNKW    DS    XL1 ...
         MEND
         MACRO           QUEUE DESCRIPTOR BLOCK.
&NAME    IHAQDB     &DUMMY
.*
.* PURPOSE :   SUPPLY IBM MACRO IHAQDB I DID'NT FOUND IN SYS1.MACLIB
.* ---------   NOR SYS1.MODGEN.
.*
.* AUTHOR :    P.A. MOINIL
.* --------    COMPUTING CENTRE
.*             J.R.C. - ISPRA ESTABLISHMENT
.*             21020 ISPRA (VA), ITALY
.*
         AIF   ('&NAME' EQ '').A
&NAME    DSECT ,                   QUEUE DESCRIPTOR BLOCK
         AGO   .B
.A       ANOP
QDB      DSECT ,                   QUEUE DESCRIPTOR BLOCK
.B       ANOP
QDBQDB   DS    CL4                 ACRONYM IN EBCDIC 'QDB '
QDBATTR  DS    XL2                 QUEUE ATTRIBUTES
QDBRV001 DS    H                   RESERVED
QDBNELMS DS    F                   NUMBER OF ELEMENTS ON QUEUE
QDBFELMP DS    A                   POINTER TO FIRST ELEMENT
QDBLELMP DS    A                   POINTER TO LAST ELEMENT
QDBFPTDS DS    H                   FORWARD POINTER DISPLACEMENT
QDBBPTDS DS    H                   BACKWARD POINTER DISPLACEMENT
QDBPRSZ  DS    H                   PRIORITY FIELD SIZE
QDBPRDS  DS    H                   PRIORITY FIELD DISPLACEMENT
QDBRV002 DS    A                   RESERVED
         MEND
