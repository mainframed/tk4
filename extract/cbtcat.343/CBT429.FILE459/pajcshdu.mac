PROC     0
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       SHADUP : UPDATE/LINK-EDIT SHADOW PROGRAM TABLE.         (PM)*/
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN GOTO QUIT
         ISPEXEC VGET (ZUSER)
         SET UUTLL = &STR(&ZUSER..LINK.DATA)
         ISPEXEC VPUT (UUTLL) SHARED
         SET UU1 =
         SET UU2 =
         SET UU3 =
         SET UU4 =
         SET UUDSN =
         SET UUDSE =
RSTR:    SET UUCP = &STR(UUDSN)
         SET UUMSG = &Z
         SET UUERR = &Z
         GOTO INIT
INVS:    ISPEXEC SETMSG MSG(PAJU990H)
INIT:    ISPEXEC VPUT (UU1 UU2 UU3 UU4 UUDSN UUDSE) SHARED
         ISPEXEC DISPLAY PANEL(PAJSHDWU) CURSOR(&UUCP)
         SET UUCC = &LASTCC
         IF &UUCC EQ 8 THEN GOTO QUIT
         IF &UUCC NE 0 THEN DO
           SET UU1 = &Z
           SET UU2 = &Z
           SET UU3 = &Z
           SET UU4 = &Z
           SET UUDSN = &Z
           ISPEXEC VPUT (UU1 UU2 UU3 UU4 UUDSN) SHARED
           SET UUOP = 0
           GOTO XERR
           END
         ISPEXEC VGET (UU1 UU2 UU3 UU4 UUDSN) SHARED
         SET UUDSE =
         IF &UUDSN EQ &Z THEN DO
           SET UUDSE = &STR(<= COMPLETE)
           SET UUCP = &STR(UUDSN)
           GOTO INIT
           END
         IF ((&UU1 EQ NO) AND (&UU2 EQ NO) AND (&UU3 EQ NO) AND +
           (&UU4 EQ NO)) THEN GOTO INVS
         IF ((&UU1 EQ NO) AND (&UU2 EQ NO) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ NO) AND (&UU2 EQ NO) AND (&UU3 EQ YES) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ NO) AND (&UU2 EQ YES) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ YES) AND (&UU2 EQ NO) AND (&UU3 EQ NO) AND +
           (&UU4 EQ NO)) THEN GOTO INVS
         IF ((&UU1 EQ YES) AND (&UU2 EQ NO) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ YES) AND (&UU2 EQ YES) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF &UU1 EQ NO THEN DO
           ERROR DO
             ERROR OFF
             SET UUCC = &LASTCC
             GOTO XERR
             END
           SET UUOP = 1
           ALLOC F(UUDATA) DSN('&UUDSN') OLD
           SET UUOP = 100
           ALLOC F(UUFROM) DSN('->.SHADOW.DATA') SHR
           ERROR OFF
           ISPEXEC CONTROL ERRORS RETURN
           ISPEXEC LMINIT DATAID(UUINDT) DDNAME(UUDATA)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 2
             GOTO XERR
             END
           ISPEXEC LMINIT DATAID(UUFRDT) DDNAME(UUFROM)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 3
             GOTO XERR
             END
           ISPEXEC LMCOPY FROMID(&UUFRDT) FROMMEM(PAJSHDU) +
             TODATAID(&UUINDT) TOMEM(PAJSHDT) REPLACE
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 4
             GOTO XERR
             END
           ISPEXEC LMFREE DATAID(&UUFRDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 5
             GOTO XERR
             END
           ISPEXEC LMFREE DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 6
             GOTO XERR
             END
           ISPEXEC CONTROL ERRORS CANCEL
           CONTROL NOMSG
           FREE FILE(UUDATA)
           FREE FILE(UUFROM)
           CONTROL MSG
           END
         IF &UU2 EQ YES THEN DO
           ISPEXEC CONTROL ERRORS RETURN
           ISPEXEC EDIT DATASET('&UUDSN(PAJSHDT)')
           SET UUCC = &LASTCC
           IF &UUCC GT 4 THEN DO
             SET UUOP = 7
             GOTO XERR
             END
           IF &UUCC EQ 4 THEN DO
             ISPEXEC SETMSG(PAJU990R)
             GOTO RSTR
             END
           ISPEXEC LMINIT DATAID(UUINDT) DATASET('&UUDSN') ENQ(EXCLU)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 8
             GOTO XERR
             END
           ISPEXEC LMCOMP DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 9
             GOTO XERR
             END
           ISPEXEC LMFREE DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 10
             GOTO XERR
             END
           END
         IF ((&UU3 EQ YES) OR (&UU4 EQ YES)) THEN DO
           EUSID
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 11
             GOTO XERR
             END
           CONTROL NOMSG
           FREE F(SYSPO SYSSU)
           CONTROL MSG
           ISPEXEC CONTROL ERRORS CANCEL
           ISPEXEC FTOPEN TEMP
           ISPEXEC FTINCL PAJSSHDU
           ISPEXEC FTCLOSE
           ISPEXEC VGET ZTEMPF
           ALLOC F(SYSPO) DA('&ZTEMPF')
           INTRDR SYSSU
           EUSUB
           CONTROL NOMSG
           FREE F(SYSPO SYSSU)
           CONTROL MSG
           ISPEXEC CONTROL ERRORS RETURN
           SET &IOPARM = &STR(I.&EURUSID.T)
           ISPEXEC SELECT PGM(IOFSPF) PARM(&IOPARM) NEWAPPL(IOF)
           END
         IF &UU4 EQ YES THEN ISPEXEC SETMSG MSG(PAJU990Y)
         ELSE ISPEXEC SETMSG MSG(PAJU990T)
         IF &UU4 EQ YES THEN GOTO QUIT
         ELSE GOTO INIT
XERR:    IF &UUOP EQ 0 THEN SET UUMSG = &STR(PANEL DISPLAY ERROR)
         ELSE IF &UUOP EQ 1 THEN +
           SET UUMSG = &STR(FILE ALLOCATION FAILED)
         ELSE IF &UUOP EQ 100 THEN +
           SET UUMSG = &STR(MASTER FILE ALLOCATION FAILED)
         ELSE IF &UUOP EQ 7 THEN +
           SET UUMSG = &STR(EDIT "PAJSHDT" MEMBER FAILED)
         ELSE IF &UUOP EQ 11 THEN +
           SET UUMSG = &STR("EUSID" COMMAND FAILED)
         ELSE SET UUERR = &STR(&ZERRLM)
         IF &UUOP LE 1 THEN ISPEXEC CONTROL ERRORS RETURN
         IF &UUERR NE &Z THEN DO
           ISPEXEC VPUT (UUOP UUERR) SHARED
           ISPEXEC SETMSG MSG(PAJU990C)
           END
         ELSE DO
           ISPEXEC VPUT (UUOP UUCC UUMSG) SHARED
           ISPEXEC SETMSG MSG(PAJU990B)
           END
         IF &UUOP EQ 4 THEN ISPEXEC LMFREE DATAID(&UUFRDT)
         IF (((&UUOP GE 3) AND (&UUOP LE 5)) OR +
           (&UUOP EQ 9)) THEN ISPEXEC LMFREE DATAID(&UUINDT)
         IF ((&UUOP GE 2) AND (&UUOP LE 6)) THEN DO
           FREE FILE(UUDATA)
           FREE FILE(UUFROM)
           END
         ISPEXEC CONTROL ERRORS CANCEL
QUIT:    EXIT CODE(0)
