PROC     0
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       TOPSUP : UPDATE/LINK-EDIT TOPSEC PROGRAM TABLES.        (PM)*/
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN GOTO QUIT
         SET IU = &STR(&&)
         SET TPSMNM =
         SET UU1 =
         SET UU2 =
         SET UU3 =
         SET UU4 =
RSTR:    SET UUMSG = &Z
         SET UUERR = &Z
         GOTO INIT
INVS:    ISPEXEC SETMSG MSG(PAJT900A)
INIT:    ISPEXEC VPUT (TPSMNM UU1 UU2 UU3 UU4) SHARED
         ISPEXEC DISPLAY PANEL(PAJTOPSM)
         SET UUCC = &LASTCC
         IF &UUCC EQ 8 THEN DO
           ISPEXEC SETMSG MSG(PAJT900X)
           GOTO QUIT
           END
         IF &UUCC NE 0 THEN DO
           SET UU1 = &Z
           SET UU2 = &Z
           SET UU3 = &Z
           SET UU4 = &Z
           ISPEXEC VPUT (UU1 UU2 UU3 UU4) SHARED
           SET UUOP = 0
           GOTO XERR
           END
         ISPEXEC VGET (UUCMD TPSMNM UU1 UU2 UU3 UU4) SHARED
         IF &UUCMD EQ E THEN DO
           ISPEXEC SETMSG MSG(PAJT900X)
           GOTO QUIT
           END
         IF ((&UU1 EQ NO) AND (&UU2 EQ NO) AND (&UU3 EQ NO) AND +
           (&UU4 EQ NO)) THEN GOTO INVS
         IF ((&UU1 EQ NO) AND (&UU2 EQ NO) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ NO) AND (&UU2 EQ YES) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ YES) AND (&UU2 EQ NO) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ YES) AND (&UU2 EQ NO) AND (&UU3 EQ YES) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF ((&UU1 EQ YES) AND (&UU2 EQ YES) AND (&UU3 EQ NO) AND +
           (&UU4 EQ YES)) THEN GOTO INVS
         IF &UU1 EQ YES THEN DO
           ERROR DO
             ERROR OFF
             SET UUCC = &LASTCC
             GOTO XERR
             END
           SET UUOP = 1
           ALLOC F(UUDATA) DSN('->.TOPSEC.DATA') OLD
           SET UUOP = 100
           ALLOC F(UUFROM) DSN('->.SHADOW.DATA') SHR
           ERROR OFF
           ISPEXEC CONTROL ERRORS RETURN
           ISPEXEC LMINIT DATAID(UUINDT) DDNAME(UUDATA)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 2
             GOTO XERR
             END
           ISPEXEC LMINIT DATAID(UUFRDT) DDNAME(UUFROM)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 3
             GOTO XERR
             END
           ISPEXEC LMCOPY FROMID(&UUFRDT) FROMMEM(TOPSECU) +
             TODATAID(&UUINDT) TOMEM(&TPSMNM) REPLACE
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 4
             GOTO XERR
             END
           ISPEXEC LMFREE DATAID(&UUFRDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 5
             GOTO XERR
             END
           ISPEXEC LMFREE DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 6
             GOTO XERR
             END
           ISPEXEC CONTROL ERRORS CANCEL
           CONTROL NOMSG
           FREE FILE(UUDATA)
           FREE FILE(UUFROM)
           CONTROL MSG
           END
         IF &UU2 EQ YES THEN DO
           ISPEXEC CONTROL ERRORS RETURN
           ISPEXEC EDIT DATASET('->.TOPSEC.DATA(&TPSMNM)')
           SET UUCC = &LASTCC
           IF &UUCC GT 4 THEN DO
             SET UUOP = 7
             GOTO XERR
             END
           IF &UUCC EQ 4 THEN DO
             ISPEXEC SETMSG MSG(PAJT900R)
             GOTO RSTR
             END
           ISPEXEC LMINIT DATAID(UUINDT) +
             DATASET('->.TOPSEC.DATA') ENQ(EXCLU)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 8
             GOTO XERR
             END
           ISPEXEC LMCOMP DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 9
             GOTO XERR
             END
           ISPEXEC LMFREE DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 10
             GOTO XERR
             END
           END
         IF ((&UU3 EQ YES) OR (&UU4 EQ YES)) THEN DO
           IF &UU4 EQ YES THEN DO
             SET UUTLL = &STR(->.TARGET.CMDLIB)
             SET UUCN =
             SET IX = 1
             DO WHILE &IX LE 10
               SET UUAN&IX = &Z
               SET &IX = &IX +1
               END
             SET UUCP = &STR(UUCN)
             SET UUMSG =
             SET UUERR =
             SET UULC = 0
             SET UULL = 0
             SET UUDSN = &STR(&UUTLL)
             DO WHILE &UULL EQ 0
               SET IX = 1
               DO WHILE &IX LE 10
                 SET UUVB&IX = &STR(&IU.UUAN&IX)
                 SET &IX = &IX +1
                 END
               ISPEXEC VPUT (UUTLL UUCN UUVB1 UUVB2) SHARED
               ISPEXEC VPUT (UUVB3 UUVB4 UUVB5 UUVB6) SHARED
               ISPEXEC VPUT (UUVB7 UUVB8 UUVB9 UUVB10) SHARED
               ISPEXEC DISPLAY PANEL(PAJTOPSL) CURSOR(&UUCP)
               SET UULC = &LASTCC
               SET UUCP = &Z
               IF ((&UULC NE 0) AND (&UULC NE 8)) THEN DO
                 SET UUCC = UULC
                 SET UUOP = 11
                 GOTO XERR
                 END
               ISPEXEC VGET (UUCMD UUTLL UUCN UUAN1 UUAN2) SHARED
               ISPEXEC VGET (UUAN3 UUAN4 UUAN5 UUAN6) SHARED
               ISPEXEC VGET (UUAN7 UUAN8 UUAN9 UUAN10) SHARED
               IF &UUCMD EQ C THEN DO
                 ISPEXEC SETMSG MSG(PAJT900U)
                 GOTO RSTR
                 END
               IF ((&UUCMD EQ E) OR (&UUCMD EQ T)) THEN SET UULC = 8
               SET IX = 1
               DO WHILE &IX LE 10
                 IF &STR(&IU.UUAN&IX) EQ &STR(&UUCN) THEN +
                   SET UUAN&IX = &Z
                 SET IX = &IX + 1
                 END
               SET IX = 1
               DO WHILE &IX LE 9
                 IF &STR(&IU.UUAN&IX) NE &Z THEN DO
                   SET OX = &IX + 1
                   DO WHILE &OX LE 10
                     IF &STR(&IU.UUAN&OX) NE &Z THEN DO
                       IF &STR(&IU.UUAN&IX) EQ &STR(&IU.UUAN&OX) THEN +
                         SET UUAN&OX = &Z
                       END
                     SET OX = &OX + 1
                     END
                   END
                 SET IX = &IX + 1
                 END
               SET IX = 1
               SET OX = 1
               DO WHILE &IX LE 10
                 IF &STR(&IU.UUAN&IX) NE &Z THEN DO
                   IF &IX NE &OX THEN DO
                     SET UUAN&OX = &STR(&IU.UUAN&IX)
                     SET UUAN&IX = &Z
                     END
                   SET OX = &OX + 1
                   END
                 SET IX = &IX + 1
                 END
               SET IX = 1
               DO WHILE &IX LE 9
                 IF &STR(&IU.UUAN&IX) NE &Z THEN DO
                   SET OX = &IX + 1
                   DO WHILE &OX LE 10
                     IF &STR(&IU.UUAN&OX) NE &Z THEN DO
                       IF &STR(&IU.UUAN&OX) LT &STR(&IU.UUAN&IX) THEN +
                         DO
                         SET UUANXC = &STR(&IU.UUAN&IX)
                         SET UUAN&IX = &STR(&IU.UUAN&OX)
                         SET UUAN&OX = &STR(&UUANXC)
                         END
                       END
                     SET OX = &OX + 1
                     END
                   END
                 SET IX = &IX + 1
                 END
               SET IX = 1
               DO WHILE &IX LE 10
                 IF &STR(&IU.UUAN&IX) EQ &Z THEN DO
                   IF &UUCP EQ &Z THEN SET UUCP = &STR(UUAN&IX)
                   END
                 SET IX = &IX + 1
                 END
               IF &UUCP EQ &Z THEN SET UUCP = &STR(UUCN)
               IF &STR(&UUTTL) NE &STR(&UUDSN) THEN DO
                 LISTDSI '&UUTLL'
                 SET UUCC = &LASTCC
                 IF &UUCC NE 0 THEN DO
                   SET UUDSI = &STR(&SYSMSGLVL2)
                   ISPEXEC SETMSG MSG(PAJT900D)
                   END
                 ELSE DO
                   IF &SYSDSORG NE PO THEN DO
                     SET UUCP = &STR(UUTLL)
                     ISPEXEC SETMSG MSG(PAJT900E)
                     END
                   ELSE IF &SYSRECFM NE U THEN DO
                     SET UUCP = &STR(UUTLL)
                     ISPEXEC SETMSG MSG(PAJT900F)
                     END
                   ELSE DO
                     IF &UULC EQ 8 THEN DO
                       IF &UUAN1 NE &Z THEN SET UULL = 4
                       END
                     ELSE SET UUDSN = &STR(&UUTLL)
                     END
                   END
                 END
               ELSE IF &UULC EQ 8 THEN DO
                 IF &UUAN1 NE &Z THEN SET UULL = 4
                 END
               END
             SET UUNME = &STR(&UUCN)
             SET UUAL1 = &STR(&UUAN1)
             SET UUAL2 = &Z
             IF &STR(&UUAN2) NE &Z THEN +
               SET UUAL1 = &STR(&UUAL1,&UUAN2)
             IF &STR(&UUAN3) NE &Z THEN +
               SET UUAL1 = &STR(&UUAL1,&UUAN3)
             IF &STR(&UUAN4) NE &Z THEN +
               SET UUAL1 = &STR(&UUAL1,&UUAN4)
             IF &STR(&UUAN5) NE &Z THEN +
               SET UUAL1 = &STR(&UUAL1,&UUAN5)
             IF &STR(&UUAN6) NE &Z THEN SET UUAL2 = &STR(&UUAN6)
             IF &STR(&UUAN7) NE &Z THEN +
               SET UUAL2 = &STR(&UUAL2,&UUAN7)
             IF &STR(&UUAN8) NE &Z THEN +
               SET UUAL2 = &STR(&UUAL2,&UUAN8)
             IF &STR(&UUAN9) NE &Z THEN +
               SET UUAL2 = &STR(&UUAL2,&UUAN9)
             IF &STR(&UUAN10) NE &Z THEN +
               SET UUAL2 = &STR(&UUAL2,&UUAN10)
             ISPEXEC VPUT (UUNME UUAL1 UUAL2) SHARED
             END
           EUSID
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 12
             GOTO XERR
             END
           CONTROL NOMSG
           FREE F(SYSPO SYSSU)
           CONTROL MSG
           ISPEXEC CONTROL ERRORS CANCEL
           ISPEXEC FTOPEN TEMP
           ISPEXEC FTINCL PAJSTOPS
           ISPEXEC FTCLOSE
           ISPEXEC VGET ZTEMPF
           ALLOC F(SYSPO) DA('&ZTEMPF')
           INTRDR SYSSU
           EUSUB
           CONTROL NOMSG
           FREE F(SYSPO SYSSU)
           CONTROL MSG
           ISPEXEC CONTROL ERRORS RETURN
           SET &IOPARM = &STR(I.&EURUSID.U)
           ISPEXEC SELECT PGM(IOFSPF) PARM(&IOPARM) NEWAPPL(IOF)
           END
         IF &UU4 EQ YES THEN ISPEXEC SETMSG MSG(PAJT900Y)
         ELSE IF &UUCMD EQ T THEN ISPEXEC SETMSG MSG(PAJT900Z)
         ELSE ISPEXEC SETMSG MSG(PAJT900T)
         IF &UU4 EQ YES THEN GOTO QUIT
         ELSE GOTO INIT
XERR:    IF ((&UUOP EQ 0) OR (&UUOP EQ 11)) THEN +
           SET UUMSG = &STR(PANEL DISPLAY ERROR)
         ELSE IF &UUOP EQ 1 THEN +
           SET UUMSG = &STR(FILE ALLOCATION FAILED)
         ELSE IF &UUOP EQ 100 THEN +
           SET UUMSG = &STR(MASTER FILE ALLOCATION FAILED)
         ELSE IF &UUOP EQ 7 THEN +
           SET UUMSG = &STR(EDIT "&TPSMNM" MEMBER FAILED)
         ELSE IF &UUOP EQ 12 THEN +
           SET UUMSG = &STR("EUSID" COMMAND FAILED)
         ELSE SET UUERR = &STR(&ZERRLM)
         IF &UUOP LE 1 THEN ISPEXEC CONTROL ERRORS RETURN
         IF &UUERR NE &Z THEN DO
           ISPEXEC VPUT (UUOP UUERR) SHARED
           ISPEXEC SETMSG MSG(PAJT900C)
           END
         ELSE DO
           ISPEXEC VPUT (UUOP UUCC UUMSG) SHARED
           ISPEXEC SETMSG MSG(PAJT900B)
           END
         IF &UUOP EQ 4 THEN ISPEXEC LMFREE DATAID(&UUFRDT)
         IF (((&UUOP GE 3) AND (&UUOP LE 5)) OR +
           (&UUOP EQ 9)) THEN ISPEXEC LMFREE DATAID(&UUINDT)
         IF ((&UUOP GE 2) AND (&UUOP LE 6)) THEN DO
           FREE FILE(UUDATA)
           FREE FILE(UUFROM)
           END
         ISPEXEC CONTROL ERRORS CANCEL
QUIT:    EXIT CODE(0)
