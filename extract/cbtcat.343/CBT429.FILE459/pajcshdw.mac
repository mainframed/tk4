PROC     0
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       SHADOW : ENCRYPT-DECRYPT DATA-SET (PO/PS).              (PM)*/
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN GOTO QUIT
         SET UUDSN =
         SET UUDSE =
         SET UUNB1 =
         SET UUNB2 =
         SET UUNBE =
RSTR:    SET UUCP = &STR(UUDSN)
         SET UUWHT = &Z
         SET UUMSG = &Z
         SET UUERR = &Z
INIT:    ISPEXEC VPUT (UUDSN UUDSE UUNB1 UUNB2 UUNBE) SHARED
         ISPEXEC DISPLAY PANEL(PAJSHDWI) CURSOR(&UUCP)
         SET UUCC = &LASTCC
         IF &UUCC EQ 8 THEN GOTO QUIT
         IF &UUCC NE 0 THEN DO
           SET UUDSN = &Z
           SET UUNB1 = &Z
           SET UUNB2 = &Z
           ISPEXEC VPUT (UUDSN UUNB1 UUNB2) SHARED
           SET UUOP = 0
           GOTO XERR
           END
         ISPEXEC VGET (UUDSN UUNB1 UUNB2) SHARED
         SET UUDSE =
         SET UUNBE =
         IF &UUDSN EQ &Z THEN DO
           SET UUDSE = &STR(<= COMPLETE)
           SET UUCP = &STR(UUDSN)
           GOTO INIT
           END
         IF ((&UUNB1 EQ &Z) OR (&UUNB2 EQ &Z)) THEN DO
           SET UUNBE = &STR(<= COMPLETE)
           IF &UUNB1 EQ &Z THEN SET UUCP = &STR(UUNB1)
             ELSE SET UUCP = &STR(UUNB2)
           GOTO INIT
           END
         ISPEXEC SELECT PGM(PAJSHDW) PARM(&UUNB1,&UUNB2)
         SET UUCC = &LASTCC
         IF &UUCC NE 0 THEN DO
           SET UUNB1 =
           SET UUNB2 =
           SET UUNBE = &STR(<= BAD DATA)
           SET UUCP = &STR(UUNB1)
           IF &UUCC EQ 101 THEN +
             SET UUMSG = &STR(NO KEYWORDS TABLE)
           ELSE IF &UUCC EQ 102 THEN +
             SET UUMSG = &STR(KEYWORDS TABLE ERROR)
           ELSE IF &UUCC EQ 103 THEN +
             SET UUMSG = &STR(MISSING PARM. FIELD)
           ELSE IF &UUCC EQ 104 THEN +
             SET UUMSG = &STR(ERROR PARM. FIELD)
           ELSE IF &UUCC EQ 105 THEN +
             SET UUMSG = &STR(1ST KEYWORD MISSING)
           ELSE IF &UUCC EQ 106 THEN +
             SET UUMSG = &STR(1ST KEYWORD TOO LONG)
           ELSE IF &UUCC EQ 107 THEN +
             SET UUMSG = &STR(2ND KEYWORD MISSING)
           ELSE IF &UUCC EQ 108 THEN +
             SET UUMSG = &STR(2ND KEYWORD TOO LONG)
           ELSE IF &UUCC EQ 109 THEN +
             SET UUMSG = &STR(1ST CALL SETKEY ERROR)
           ELSE IF &UUCC EQ 110 THEN +
             SET UUMSG = &STR(1ST CALL DECIPH ERROR)
           ELSE IF &UUCC EQ 111 THEN +
             SET UUMSG = &STR(2ND CALL SETKEY ERROR)
           ELSE IF &UUCC EQ 112 THEN +
             SET UUMSG = &STR(2ND CALL DECIPH ERROR)
           ELSE IF &UUCC EQ 113 THEN +
             SET UUMSG = &STR(3RD CALL SETKEY ERROR)
           ELSE IF &UUCC EQ 114 THEN +
             SET UUMSG = &STR(CALL ENCIPH ERROR)
           ELSE IF &UUCC EQ 115 THEN +
             SET UUMSG = &STR(CONTROL CHECK FAILED)
           ELSE SET UUMSG = &STR("PAJSHDW" INVOCATION ERROR)
           ISPEXEC SETMSG MSG(PAJU990D)
           GOTO INIT
           END
         LISTDSI '&UUDSN'
         SET UUCC = &LASTCC
         IF &UUCC GT 4 THEN DO
           SET UUNB1 =
           SET UUNB2 =
           SET UUDSE = &STR(<= PO/PS ? )
           SET UUCP = &STR(UUDSN)
           SET UUMSG = &STR(DATA-SET INFORMATION ERROR)
           ISPEXEC SETMSG MSG(PAJU990D)
           GOTO INIT
           END
         IF &SYSDSORG EQ PS THEN GOTO PSEQ
         IF &SYSDSORG NE PO THEN DO
           SET UUNB1 =
           SET UUNB2 =
           SET UUDSE = &STR(<= PO/PS ? )
           SET UUCP = &STR(UUDSN)
           GOTO INIT
           END
         SET UURSV = 0
RFSH:    ERROR DO
           ERROR OFF
           SET UUCC = &LASTCC
           SET UUOP = 1
           GOTO XERR
           END
         ALLOC F(UUDATA) DSN('&UUDSN') SHR
         ERROR OFF
         ISPEXEC CONTROL ERRORS RETURN
         ISPEXEC LMINIT DATAID(UUINDT) DDNAME(UUDATA)
         SET UUCC = &LASTCC
         IF &UUCC NE 0 THEN DO
           SET UUOP = 2
           GOTO XERR
           END
         ISPEXEC LMOPEN DATAID(&UUINDT)
         SET UUCC = &LASTCC
         IF &UUCC NE 0 THEN DO
           SET UUOP = 3
           GOTO XERR
           END
         ISPEXEC TBCREATE UUTABLE NOWRITE KEYS(UUMEM) +
           NAMES(UUSTS)
         SET UUMBN =
         SET UUSW = 0
         SET UUCC = 0
         DO WHILE &UUCC = 0
           ISPEXEC LMMLIST DATAID(&UUINDT) MEMBER(UUMBN) +
             OPTION(LIST) STATS(YES)
           SET UUCC = &LASTCC
           IF &UUCC EQ 0 THEN DO
             SET UUSW = 1
             SET UUMEM = &STR(&UUMBN)
             SET UUSTS = &STR(&ZLVERS..&ZLMOD  &ZLCDATE +
                              &ZLMDATE &ZLMTIME &ZLCNORC +
                              &ZLINORC &ZLMNORC &ZLUSER)
             ISPEXEC TBADD UUTABLE
             END
           ELSE IF &UUCC NE 8 THEN DO
             SET UUOP = 4
             GOTO XERR
             END
           END
         ISPEXEC LMMLIST DATAID(&UUINDT) OPTION(FREE)
         SET UUCC = &LASTCC
         IF &UUCC NE 0 THEN DO
           SET UUOP = 5
           GOTO XERR
           END
         ISPEXEC LMCLOSE DATAID(&UUINDT)
         SET UUCC = &LASTCC
         IF &UUCC NE 0 THEN DO
           SET UUOP = 6
           GOTO XERR
           END
         ISPEXEC LMFREE DATAID(&UUINDT)
         SET UUCC = &LASTCC
         IF &UUCC NE 0 THEN DO
           SET UUOP = 7
           GOTO XERR
           END
         ISPEXEC CONTROL ERRORS CANCEL
         CONTROL NOMSG
         FREE FILE(UUDATA)
         CONTROL MSG
         IF &UUSW EQ 0 THEN DO
           SET UUOP = 8
           GOTO XERR
           END
         ISPEXEC CONTROL ERRORS RETURN
         ISPEXEC TBTOP UUTABLE
         SET UUSEL = &STR((B/D/E))
         ISPEXEC VPUT (UUSEL) SHARED
         SET ROW = 0
         SET UUROW = &UURSV
LOOP:    SET UUCMD = &Z
         SET UUWHT = &Z
         SET UUMSG = &Z
         SET UUERR = &Z
         ISPEXEC VPUT (UUCMD) SHARED
         SET DONE = NONE
         DO WHILE &DONE = NONE
           IF &ROW NE &UUROW THEN DO
             ISPEXEC TBTOP UUTABLE
             ISPEXEC TBSKIP UUTABLE NOREAD NUMBER(&UUROW) +
               POSITION(ROW)
             END
           ISPEXEC TBDISPL UUTABLE PANEL(PAJSHDWS) POSITION(ROW)
           SET UUCC = &LASTCC
           IF &UUROW NE &ZTDTOP THEN SET UUROW = &ZTDTOP
           IF &UUCC EQ 8 THEN SET DONE = BACK
           ELSE IF &UUCC GT 8 THEN SET DONE = ERROR
           ELSE IF &UUCMD NE &Z THEN DO
             IF &UUCMD EQ L THEN DO
               SET UUMEM = &UUWHT
               ISPEXEC TBTOP UUTABLE
               ISPEXEC TBSCAN UUTABLE ARGLIST(UUMEM) NOREAD +
                 POSITION(UUROW) CONDLIST(GE)
               IF &LASTCC EQ 8 THEN +
                 ISPEXEC TBBOTTOM UUTABLE NOREAD POSITION(UUROW)
               ELSE IF &UUROW NE 0 THEN +
                 ISPEXEC TBSKIP UUTABLE NUMBER(-1) NOREAD +
                 POSITION(UUROW)
               SET ROW = &UUROW
               END
             ELSE IF &UUCMD EQ E THEN SET DONE = BACK
             ELSE SET DONE = CANCEL
             END
           ELSE IF &ZTDSELS GE 1 THEN DO
             IF &S EQ B THEN SET DONE = BROWSE
             ELSE IF &S EQ S THEN SET DONE = BROWSE
             ELSE IF &S EQ D THEN SET DONE = DECRYPT
             ELSE IF &S EQ E THEN SET DONE = ENCRYPT
             ELSE ISPEXEC SETMSG MSG(PAJU990E)
             SET UUROW = &ROW
             END
           END
         IF &DONE EQ ERROR THEN DO
           SET UUOP = 9
           GOTO XERR
           END
         IF ((&DONE EQ CANCEL) OR (&DONE EQ BACK)) THEN GOTO XRSM
         SET UUMBR = &UUMEM
         SET UUSWM = YES
         IF &DONE EQ BROWSE THEN +
           ISPEXEC BROWSE DATASET('&UUDSN(&UUMBR)')
         ELSE IF &DONE EQ ENCRYPT THEN +
           ISPEXEC EDIT DATASET('&UUDSN(&UUMBR)') MACRO(PAJCSHEN)
         ELSE ISPEXEC EDIT DATASET('&UUDSN(&UUMBR)') MACRO(PAJCSHDE)
         SET UUCC = &LASTCC
         IF &UUCC GT 4 THEN DO
           SET UUOP = 10
           GOTO XERR
           END
         IF ((&DONE NE BROWSE) AND (&UUCC EQ 4)) THEN SET UUSWM = NO
         IF ((&DONE NE BROWSE) AND (&UUCC EQ 0)) THEN DO
           ISPEXEC LMINIT DATAID(UUINDT) DATASET('&UUDSN') ENQ(EXCLU)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 11
             GOTO XERR
             END
           ISPEXEC LMCOMP DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 12
             GOTO XERR
             END
           ISPEXEC LMFREE DATAID(&UUINDT)
           SET UUCC = &LASTCC
           IF &UUCC NE 0 THEN DO
             SET UUOP = 13
             GOTO XERR
             END
           END
         ISPEXEC VPUT (UUMEM) SHARED
         IF &UUSWM = YES THEN ISPEXEC SETMSG MSG(PAJU990Z)
           ELSE ISPEXEC SETMSG MSG(PAJU990S)
         IF &DONE EQ BROWSE THEN GOTO LOOP
         SET UURSV = &UUROW
XRSM:    ISPEXEC TBEND UUTABLE
         SET UUCC = &LASTCC
         IF &UUCC NE 0 THEN DO
           SET UUOP = 14
           GOTO XERR
           END
         IF ((&DONE EQ ENCRYPT) OR (&DONE EQ DECRYPT)) THEN GOTO RFSH
         IF &DONE EQ BACK THEN GOTO DJST
         ISPEXEC SETMSG MSG(PAJU990X)
         ISPEXEC CONTROL ERRORS CANCEL
         EXIT CODE(0)
PSEQ:    ISPEXEC CONTROL ERRORS RETURN
         SET UUANS = &Z
QASK:    ISPEXEC VPUT (UUANS) SHARED
         ISPEXEC DISPLAY PANEL(PAJSHDWQ) CURSOR(UUANS)
         SET UUCC = &LASTCC
         IF &UUCC EQ 8 THEN GOTO DJST
         IF &UUCC NE 0 THEN DO
           SET UUOP = 15
           GOTO XERR
           END
         ISPEXEC VGET (UUANS) SHARED
         SET UUSWM = YES
         IF &UUANS EQ BROWSE THEN +
           ISPEXEC BROWSE DATASET('&UUDSN')
         ELSE IF &UUANS EQ ENCRYPT THEN +
           ISPEXEC EDIT DATASET('&UUDSN') MACRO(PAJCSHEN)
         ELSE ISPEXEC EDIT DATASET('&UUDSN') MACRO(PAJCSHDE)
         SET UUCC = &LASTCC
         IF &UUCC GT 4 THEN DO
           SET UUOP = 16
           GOTO XERR
           END
         IF ((&UUANS NE BROWSE) AND (&UUCC EQ 4)) THEN SET UUSWM = NO
         IF &UUSWM = YES THEN ISPEXEC SETMSG MSG(PAJU990F)
           ELSE ISPEXEC SETMSG MSG(PAJU990G)
         IF &UUANS NE BROWSE THEN SET UUANS = &Z
         GOTO QASK
DJST:    SET UUNBE = &STR(<= STILL IN)
         GOTO RSTR
XERR:    IF ((&UUOP EQ 0) OR (&UUOP EQ 15)) THEN +
           SET UUMSG = &STR(PANEL DISPLAY ERROR)
         ELSE IF &UUOP EQ 1 THEN +
           SET UUMSG = &STR(FILE ALLOCATION FAILED)
         ELSE IF &UUOP EQ 8 THEN +
           SET UUMSG = &STR(TABLE EMPTY ERROR)
         ELSE IF &UUOP EQ 9 THEN +
           SET UUMSG = &STR(TABLE DISPLAY ERROR)
         ELSE IF &UUOP EQ 10 THEN DO
           IF &DONE EQ BROWSE THEN +
             SET UUMSG = &STR(BROWSE MEMBER FAILED)
           ELSE IF &DONE EQ ENCRYPT THEN +
             SET UUMSG = &STR(ENCRYPT MEMBER FAILED)
           ELSE SET UUMSG = &STR(DECRYPT MEMBER FAILED)
           END
         ELSE IF &UUOP EQ 16 THEN DO
           IF &UUANS EQ BROWSE THEN +
             SET UUMSG = &STR(BROWSE DATA-SET FAILED)
           ELSE IF &UUANS EQ ENCRYPT THEN +
             SET UUMSG = &STR(ENCRYPT DATA-SET FAILED)
           ELSE SET UUMSG = &STR(DECRYPT DATA-SET FAILED)
           END
         ELSE SET UUERR = &STR(&ZERRLM)
         IF ((&UUOP LE 1) OR (&UUOP EQ 8)) THEN +
           ISPEXEC CONTROL ERRORS RETURN
         IF &UUERR NE &Z THEN DO
           ISPEXEC VPUT (UUOP UUERR) SHARED
           ISPEXEC SETMSG MSG(PAJU990C)
           END
         ELSE IF &UUMSG NE &Z THEN DO
           ISPEXEC VPUT (UUOP UUCC UUMSG) SHARED
           ISPEXEC SETMSG MSG(PAJU990B)
           END
         ELSE DO
           ISPEXEC VPUT (UUOP UUCC UUWHT) SHARED
           ISPEXEC SETMSG MSG(PAJU990A)
           END
         IF &UUOP EQ 4 THEN +
           ISPEXEC LMMLIST DATAID(&UUINDT) OPTION(FREE)
         IF ((&UUOP EQ 4) OR (&UUOP EQ 5)) THEN +
           ISPEXEC LMCLOSE DATAID(&UUINDT)
         IF (((&UUOP GE 3) AND (&UUOP LE 6)) OR (&UUOP EQ 13)) THEN +
           ISPEXEC LMFREE DATAID(&UUINDT)
         IF ((&UUOP GE 2) AND (&UUOP LE 7)) THEN FREE FILE(UUDATA)
         IF ((&UUOP GE 4) AND (&UUOP LE 13)) THEN ISPEXEC TBEND UUTABLE
         ISPEXEC CONTROL ERRORS CANCEL
QUIT:    EXIT CODE(0)
