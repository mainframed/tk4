SHDW     TITLE '* * *   P A J S H D W   * * *'
PAJSHDW  START 0
         SPACE 1
*- - - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                                                                    *
* PURPOSE :    PRIVATE USE ONLY.                                     *
* ---------    (EACH USER MUST DEFINE HIS KEYWORD'S TABLE IN MODULE  *
*              "PAJSHDT" WHICH IS INCLUDED AT LINK-EDIT)             *
*                                                                    *
* AUTHOR :     P.A. MOINIL                                           *
* --------     COMPUTING CENTRE                                      *
*              J.R.C. - ISPRA ESTABLISHMENT                          *
*              21020 ISPRA (VA), ITALY                               *
*                                                                    *
*- - - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R12
         LA    R10,MAXERR
         L     R2,=V(PAJSHDT)
         LTR   R2,R2
         BNP   ERR1
         LM    R7,R9,0(R2)
         CL    R8,=A(L'KEYW)
         BNE   ERR2
         CLR   R7,R9
         BH    ERR2
         L     R2,0(R1)
         LH    R3,0(R2)
         LTR   R3,R3
         BNP   ERR3
         LA    R1,2(R2)
         LA    R3,1(R2,R3)
         LA    R2,1
         LR    R4,R1
         CLI   0(R1),C','
         BE    *+L'*+8
         BXLE  R1,R2,*-8
         B     ERR4
         LR    R5,R1
         SLR   R5,R4
         LTR   R5,R5
         BNP   ERR5
         BCT   R5,*+L'*+6
         OC    KEYW1(*-*),0(R4)
         CH    R5,=H'7'
         BH    ERR6
         EX    R5,*-14
         BXLE  R1,R2,*+L'*+10
         B     ERR7
         OC    KEYW2(*-*),0(R1)
         SLR   R3,R1
         CH    R3,=H'7'
         BH    ERR8
         EX    R3,*-16
LOOP     MVC   KEYW(L'KEYW),0(R7)
         LA    R1,PLST1
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR9
         LA    R1,PLST3
         L     R15,=V(DECIPH)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR10
         LA    R1,PLST2
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR11
         LA    R1,PLST3
         L     R15,=V(DECIPH)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR12
         LA    R1,PLST4
         L     R15,=V(SETKEY)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR13
         MVC   KEYW3(L'KEYW3),PAJSHDW+10
         LA    R1,PLST5
         L     R15,=V(ENCIPH)
         BASR  R14,R15
         LTR   R15,R15
         BNZ   ERR14
         CLC   CONTROL,KEYW3
         BE    LEAVE
         BXLE  R7,R8,LOOP
         B     ERR15
LEAVE    XR    R10,R10
QUIT    $XRET  CC=(R10)
         SPACE 1
ERR1     BCTR  R10,0               NO KEYWORDS TABLE
ERR2     BCTR  R10,0               KEYWORDS TABLE ERROR
ERR3     BCTR  R10,0               MISSING PARM. FIELD
ERR4     BCTR  R10,0               ERROR PARM. FIELD
ERR5     BCTR  R10,0               1ST KEYWORD MISSING
ERR6     BCTR  R10,0               1ST KEYWORD TOO LONG
ERR7     BCTR  R10,0               2ND KEYWORD MISSING
ERR8     BCTR  R10,0               2ND KEYWORD TOO LONG
ERR9     BCTR  R10,0               1ST CALL SETKEY ERROR
ERR10    BCTR  R10,0               1ST CALL DECIPH ERROR
ERR11    BCTR  R10,0               2ND CALL SETKEY ERROR
ERR12    BCTR  R10,0               2ND CALL DECIPH ERROR
ERR13    BCTR  R10,0               3RD CALL SETKEY ERROR
ERR14    BCTR  R10,0               CALL ENCIPH ERROR
ERR15    BCTR  R10,0               CONTROL CHECK FAILED
         SPACE 1
MAXERR   EQU   ((*-ERR1)/2)+1
         SPACE 1
         LA    R10,100(R10)
         B     QUIT
         EJECT
PLST1    DC    A(KEYW2)
PLST2    DC    A(KEYW1)
PLST3    DC    A(KEYW,*+4,L'KEYW)
PLST4    DC    A(KEYW)
PLST5    DC    A(KEYW3,*+4,L'KEYW3)
CONTROL  DC    XL8'EB9C69532FD217DC'
KEYW     DC    XL8'0'
KEYW1    DC    CL8' '
KEYW2    DC    CL8' '
KEYW3    DC    CL8' '
         SPACE 1
        LTORG
         SPACE 2
         END
