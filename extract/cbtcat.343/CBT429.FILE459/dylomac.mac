         MACRO           AUTOMATIC COMMANDS SELECTION
        @AC    &TX
         GBLB  &ACSWS,&ACSWE
         LCLA  &Z
.* SYNTAX :    @AC  '... STATEMENTS TEXT ...'
         AIF   (NOT &ACSWS).CGN
         AIF   (&ACSWE).CGN
         AIF   ('&TX'(1,1) NE '''').ERR1
&Z       SETA  K'&TX
         AIF   ('&TX'(&Z,1) NE '''').ERR2
         DC    C&TX
         AGO   .END
.CGN     MNOTE 8,' CANNOT GENERATE THE DATA HERE ******************** '
         AGO   .END
.ERR1    MNOTE 8,' INITIAL QUOTE MISSING **************************** '
         AGO   .END
.ERR2    MNOTE 8,' FINAL QUOTE MISSING ****************************** '
.END     MEND
         MACRO           AUTOMATIC COMMANDS SELECTION
        @ACS   &DUMMY
         GBLB  &ACSWS
         GBLC  &ACLBL
         LCLC  &Z
.* SYNTAX :    @ACS  ,   START OF TABLE
         AIF   (NOT &ACSWS).A
         MNOTE 8,' REPEATED START OF TABLE (ONLY ONCE ALLOWED) ****** '
         AGO   .END
.A       ANOP
&ACSWS   SETB  1
&Z       SETC  'IHB'.'&SYSNDX'.'S'
&ACLBL   SETC  'IHB'.'&SYSNDX'.'E'
AUTOS    DC    A(&Z,1,&ACLBL)      TABLE SCAN POINTERS
&Z       DC    CL1' '              START OF TABLE
.END     MEND
         MACRO           AUTOMATIC COMMANDS SELECTION
        @ACE   &DUMMY
         GBLB  &ACSWS,&ACSWE
         GBLC  &ACLBL
.* SYNTAX :    @ACE  ,   END OF TABLE
         AIF   (&ACSWS).A
         MNOTE 8,' START OF TABLE HAS NOT BEEN DEFINED ************** '
         AGO   .END
.A       AIF   (&ACSWE).B
&ACSWE   SETB  1
&ACLBL   DC    CL1' '              END OF TABLE
         AGO   .END
.B       MNOTE 8,' REPEATED END OF TABLE (ONLY ONCE ALLOWED) ******** '
.END     MEND
         MACRO           ALIAS(ES) PROCESS INFORMATION
&NAME   @AL    &ALIAS,&DDN=,&UNLESS=
         GBLB  &ALSWSU,&ALSWEU,&ALSWSG,&ALSWEG
         LCLA  &M,&N,&Z
.* SYNTAX :    ...  @AL  ALIAS,DDN=...          ,UNLESS=...
.*                             DDN=(...,...,...),UNLESS=(...,...,...)
         AIF   (NOT &ALSWSU).CHK
         AIF   (NOT &ALSWEU).GCD
.CHK     AIF   (NOT &ALSWSG).CGN
         AIF   (&ALSWEG).CGN
.GCD     ANOP
&M       SETA  1
&N       SETA  1
         AIF   ('&NAME' NE '').A
         MNOTE 8,' NAME MISSING (IT IS MANDATORY) ******************* '
         AGO   .END
.A       AIF   ('&ALIAS' NE '').B
         MNOTE 8,' ALIAS NAME MISSING (IT IS MANDATORY) ************* '
         AGO   .END
.B       AIF   ('&DDN(&M)' NE '').C
         MNOTE 8,' DD-NAME(S) MISSING ( AT LEAST ONE IS MANDATORY) ** '
         AGO   .END
.C       ANOP
&M       SETA  &M+1
         AIF   ('&DDN(&M)' NE '').C
&M       SETA  &M-1
.D       AIF   ('&UNLESS(&N)' EQ '').E
&N       SETA  &N+1
         AGO   .D
.E       ANOP
&N       SETA  &N-1
         DC    CL8'&NAME',CL8'&ALIAS',AL1(&M,&N)
&Z       SETA  1
.F       DC    CL8'&DDN(&Z)'
&Z       SETA  &Z+1
         AIF   (&Z LE &M).F
&Z       SETA  0
.G       AIF   (&Z EQ &N).END
&Z       SETA  &Z+1
         DC    CL8'&UNLESS(&Z)'
         AGO   .G
.CGN     MNOTE 8,' CANNOT GENERATE THE DATA HERE ******************** '
.END     MEND
         MACRO           ALIAS(ES) PROCESS INFORMATION
        @ALE   &DUMMY
         GBLB  &ALSWSU,&ALSWEU,&ALSWSG,&ALSWEG
         GBLC  &ALUNM,&ALGNM
.* SYNTAX :    @ALE  ,   END OF TABLE
         AIF   (&ALSWSU).A
         AIF   (&ALSWSG).B
         MNOTE 8,' NO TABLE STARTED ********************************* '
         AGO   .END
.A       ANOP
&ALSWSU  SETB  0
&ALSWEU  SETB  1
&ALUNM.E DC    XL1'0'              END OF TABLE MARKER
         AGO   .END
.B       ANOP
&ALSWSG  SETB  0
&ALSWEG  SETB  1
&ALGNM.E DC    XL1'0'              END OF TABLE MARKER
.END     SPACE 1
         MEND
         MACRO           ALIAS(ES) PROCESS INFORMATION
&NAME   @ALS   &DUMMY
         GBLB  &ALSWSU,&ALSWEU,&ALSWSG,&ALSWEG
         GBLC  &ALUNM,&ALGNM
.* SYNTAX :    ...  @ALS  ,   START OF TABLE
         AIF   ('&NAME' NE '').A
         MNOTE 8,' NAME MISSING (IT IS MANDATORY) ******************* '
         AGO   .END
.A       AIF   ('&NAME' EQ 'USERS').B
         AIF   ('&NAME' EQ 'GROUPS').D
         MNOTE 8,' INVALID NAME ("USERS" OR "GROUPS" ONLY ALLOWED) ** '
         AGO   .END
.B       AIF   (&ALSWSU).C
         AIF   (&ALSWEU).C
         AIF   (&ALSWSG).F
&ALSWSU  SETB  1
&ALUNM   SETC  '&NAME'
&ALUNM.S DS    0F                  START OF USER-ID TABLE
         AGO   .END
.C       MNOTE 8,' REPEATED NAME ("USERS" IS ALLOWED ONCE ONLY) ***** '
         AGO   .END
.D       AIF   (&ALSWSG).E
         AIF   (&ALSWEG).E
         AIF   (&ALSWSU).G
&ALSWSG  SETB  1
&ALGNM   SETC  '&NAME'
&ALGNM.S DS    0F                  START OF GROUP TABLE
         AGO   .END
.E       MNOTE 8,' REPEATED NAME ("GROUPS" IS ALLOWED ONCE ONLY) **** '
         AGO   .END
.F       MNOTE 8,' PREVIOUS "GROUPS" TABLE IS NOT ENDED ************* '
         AGO   .END
.G       MNOTE 8,' PREVIOUS "USERS" TABLE IS NOT ENDED ************** '
.END     MEND
         MACRO           CHECK INTERNAL KEYWORD=VALUE DEFAULTS
        @CHK   &DUMMY
         GBLC  &DMSVOL,&MXLCIT,&MXALUS,&MXALGR
         GBLC  &PU,&PV,&PA,&PP,&PS,&PD,&PB
         GBLC  &WU,&WA,&WP,&WS,&WD,&WR
         GBLC  &CU,&CA,&CP,&CS,&CB
         GBLC  &EU,&EA,&EP,&ES
         GBLC  &STV
.* SYNTAX :    ...  @CHK  ,
        @CHKV  &DMSVOL,DMSVOL
        @CHKS  &MXLCIT,MXLCIT
&MXLCIT  SETC  '&STV'
        @CHKS  &MXALUS,MXALUS
&MXALUS  SETC  '&STV'
        @CHKS  &MXALGR,MXALGR
&MXALGR  SETC  '&STV'
        @CHKC  &PU,PU,8
        @CHKV  &PV,PV
        @CHKA  &PA,PA
        @CHKN  &PP,PP
        @CHKN  &PS,PS
        @CHKN  &PD,PD
        @CHKN  &PB,PB
        @CHKC  &WU,WU,8
        @CHKA  &WA,WA
        @CHKN  &WP,WP
        @CHKN  &WS,WS
        @CHKN  &WD,WD
        @CHKC  &WR,WR,44
        @CHKC  &CU,CU,8
        @CHKA  &CA,CA
        @CHKN  &CP,CP
        @CHKN  &CS,CS
        @CHKN  &CB,CB
        @CHKC  &EU,EU,8
        @CHKN  &EA,EA
        @CHKN  &EP,EP
        @CHKN  &ES,ES
         MEND
         MACRO           CHECK ALLOC. (USED BY @CHK MACRO ONLY)
        @CHKA  &VV,&VN
         AIF   ('&VV' EQ 'CYL').END
         AIF   ('&VV' EQ 'TRK').END
         PRINT ON
         MNOTE 8,' &VN : NOT CYL OR TRK ***************************** '
         PRINT OFF
.END     MEND
         MACRO           CHECK CHAR. (USED BY @CHK MACRO ONLY)
        @CHKC  &VV,&VN,&VL
         LCLA  &J
&J       SETA  K'&VV
         AIF   (&J LE &VL).END
         PRINT ON
         MNOTE 8,' &VN : MORE THAN &VL CHAR. LONG ******************* '
         PRINT OFF
.END     MEND
         MACRO           CHECK NUMERIC (USED BY @CHK MACRO ONLY)
        @CHKN  &VV,&VN,&S=N
         GBLC  &STV
         LCLA  &J,&M,&L
&J       SETA  K'&VV
         AIF   (&J NE 0).B
         PRINT ON
         MNOTE 8,' &VN : NULL VALUE ********************************* '
         AGO   .SCC
.B       AIF   (&J LE 5).C
         PRINT ON
         MNOTE 8,' &VN : MORE THAN 5 CHAR. LONG ********************* '
         AGO   .SCC
.C       ANOP
&M       SETA  0
&L       SETA  1
.D       AIF   ('&VV'(&L,1) LT '0').ERR
         AIF   ('&VV'(&L,1) GT '9').ERR
         AIF   ('&VV'(&L,1) EQ '0').E
&M       SETA  &M+1
.E       AIF   (&L EQ &J).F
&L       SETA  &L+1
         AGO   .D
.F       AIF   (&M NE 0).END
         PRINT ON
         MNOTE 8,' &VN : ZERO VALUE ********************************* '
         AGO   .SCC
.ERR     PRINT ON
         MNOTE 8,' &VN : NOT NUMERIC ******************************** '
.SCC     PRINT OFF
         AIF   ('&S'  NE 'Y').END
&STV     SETC  '=ERROR='
.END     MEND
         MACRO           CHECK SPACE (USED BY @CHK MACRO ONLY)
        @CHKS  &VV,&VN
         GBLC  &STV
         LCLA  &II
         LCLC  &TT
&II      SETA  K'&VV
         AIF   ('&VV'(&II,1) EQ 'K').A
&STV     SETC  '&VV'
        @CHKN  &VV,&VN,S=Y
         AGO   .END
.A       ANOP
&TT      SETC  '&VV'(1,&II-1)
&STV     SETC  '&TT'.'*1024'
        @CHKN  &TT,&VN,S=Y
.END     MEND
         MACRO           CHECK VOLUME (USED BY @CHK MACRO ONLY)
        @CHKV  &VV,&VN
         LCLA  &J
&J       SETA  K'&VV
         AIF   (&J EQ 6).END
         PRINT ON
         MNOTE 8,' &VN : NOT 6 CHAR. LONG *************************** '
         PRINT OFF
.END     MEND
         MACRO           CHECK OPERANDS KEYWORDS NAMES LIST
&NAME   @CHKO  &NOMATCH=,&MATCH=
         GBLA  &TSON
         GBLC  &TSOP(100)
         LCLA  &L,&N,&T,&X
         LCLB  &IW
         LCLC  &KW,&BW
.* SYNTAX :    ...  @CHKO  ...,...,..., <- KEYWORDS NAMES LIST
.*                         NOMATCH=...,MATCH=...
&NAME    L     R15,AOCKR
         BASR  R6,R15
         AIF   ('&NOMATCH' NE '').A1
&IW      SETB  1
&BW      SETC  'IHB'.'&SYSNDX'
         B     &BW                 +0 = NO MATCH
         AGO   .A2
.A1      ANOP
         B     &NOMATCH            +0 = NO MATCH
.A2      AIF   ('&MATCH' NE '').B2
         AIF   (&IW).B1
&IW      SETB  1
&BW      SETC  'IHB'.'&SYSNDX'
.B1      B     &BW                 +4 = OK, MATCH
         AGO   .B3
.B2      ANOP
         B     &MATCH              +4 = OK, MATCH
.B3      ANOP
&N       SETA  N'&SYSLIST
         AIF   (&N EQ 0).D1
&L       SETA  1
.C1      ANOP
         AIF   ('&SYSLIST(&L)' EQ '').C9
         AIF   ('&SYSLIST(&L)'(1,1) NE '''').C3
&T       SETA  K'&SYSLIST(&L)
         AIF   ('&SYSLIST(&L)'(&T,1) EQ '''').C2
         MNOTE 8,' KEYWORD NAME &L : FINAL QUOTE MISSING ************ '
         AGO   .END
.C2      ANOP
&T       SETA  &T-2
&KW      SETC  '&SYSLIST(&L)'(2,&T)
         AGO   .C4
.C3      ANOP
&KW      SETC  '&SYSLIST(&L)'
.C4      ANOP
&T       SETA  K'&KW-1
         AIF   (&T LT 50).C5
         MNOTE 8,' KEYWORD NAME &L : TOO LONG (MAX. = 50 CHAR.) ***** '
         AGO   .END
.C5      ANOP
&X       SETA  1
.C6      AIF   ('&TSOP(&X)' EQ '').C8
         AIF   ('&TSOP(&X)' NE '&KW').C7
         MNOTE 8,' OPERAND &KW IS A DUPLICATE KEYWORD NAME ********** '
         AGO   .END
.C7      ANOP
&X       SETA  &X+1
         AIF   (&X LE &TSON).C6
         MNOTE 8,' INTERNAL MACRO GLOBAL CHECK TABLE OVERFLOW ******* '
         MNOTE 8,' = INCREMENT DIMENSION OF GLOBAL VARIABLE TSOP    = '
         MNOTE 8,' = AND SET THE GLOBAL VARIABLE TSON EQUAL TO IT.  = '
         AGO   .END
.C8      ANOP
&TSOP(&X) SETC '&KW'
         DC    AL1(&T),C'&KW'
.C9      ANOP
&L       SETA  &L+1
         AIF   (&L LE &N).C1
.D1      DC    AL1(255)            END OF LIST
         AIF   (&IW).D2
         DS    0H                  FORCE HALWORD ALIGNMENT
         AGO   .END
.D2      ANOP
&BW      DS    0H                  FORCE HALWORD ALIGNMENT
.END     MEND
         MACRO           DD-NAME LIBRARIES INFORMATION
&DDN    @DD    &ID=,&DSORG=,&RECFM=,&LRECL=,&SP=
         GBLB  &DDSWS,&DDSWE
         LCLA  &Z
         LCLB  &SW
         LCLC  &UID,&UORG,&URFM,&ULRL,&USPA
.* SYNTAX :    DDN  @DD  ID=...,DSORG=...,RECFM=...,LRECL=...,SP=...
         AIF   (NOT &DDSWS).DGN
         AIF   (NOT &DDSWE).A
.DGN     MNOTE 8,' CANNOT GENERATE THE DATA HERE ******************** '
         AGO   .END
.A       ANOP
&SW      SETB  0
&Z       SETA  K'&DDN
         AIF   (&Z LE 8).B
         MNOTE 8,' DDN : DD-NAME TOO LONG (MAX.CHAR. 8) ************* '
&SW      SETB  1
.B       ANOP
&Z       SETA  K'&ID
         AIF   ('&ID'(1,1) NE '''').B2
         AIF   ('&ID'(&Z,1) NE '''').B1
         AIF   (&Z GT 22).B3
&Z       SETA  &Z-2
&UID     SETC  '&ID'(2,&Z)
         AGO   .C
.B1      MNOTE 8,' ID : LIBRARY-ID INCORRECTLY QUOTED *************** '
&SW      SETB  1
         AGO   .C
.B2      AIF   (&Z GT 20).B3
&UID     SETC  '&ID'
         AGO   .C
.B3      MNOTE 8,' ID : LIBRARY-ID TOO LONG (MAX.CHAR. 20) ********** '
&SW      SETB  1
.C       AIF   ('&DSORG' NE '').C1
&UORG    SETC  '0'
         AGO   .D
.C1      AIF   ('&DSORG' NE 'PO').C2
&UORG    SETC  'DS1DSGPO'
         AGO   .D
.C2      AIF   ('&DSORG' NE 'PS').C3
&UORG    SETC  'DS1DSGPS'
         AGO   .D
.C3      AIF   ('&DSORG' NE 'DA').C4
&UORG    SETC  'DS1DSGDA'
         AGO   .D
.C4      MNOTE 8,' INVALID DSORG SPECIFIED (PO, PS OR DA) *********** '
&SW      SETB  1
.D       AIF   ('&DSORG' EQ 'DA').D1
         AIF   ('&DSORG' NE '').D2
.D1      ANOP
&URFM    SETC  '0'
&ULRL    SETC  '0'
         AIF   ('&RECFM' EQ '').D1A
         MNOTE 0,' RECFM SPECIFIED, BUT WILL BE IGNORED ************* '
.D1A     AIF   ('&LRECL' EQ '').E
         AIF   ('&LRECL' EQ '0').E
         MNOTE 0,' LRECL SPECIFIED, BUT WILL BE IGNORED ************* '
         AGO   .E
.D2      AIF   ('&RECFM' NE '').D2A
         MNOTE 0,' RECFM MISSING (DEFAULT F USED) ******************* '
         AGO   .D2B
.D2A     AIF   ('&RECFM' NE 'F').D3
.D2B     ANOP
&URFM    SETC  'DS1RECFF'
         AIF   ('&LRECL' NE '').D2C
         MNOTE 0,' LRECL MISSING (DEFAULT OF 80 USED) *************** '
&ULRL    SETC  '80'
         AGO   .E
.D2C     AIF   (&LRECL GT 32767).D2D
&ULRL    SETC  '&LRECL'
         AGO   .E
.D2D     MNOTE 8,' INVALID LRECL SPECIFIED (MAX. VALUE 32767) ******* '
&SW      SETB  1
         AGO   .E
.D3      AIF   ('&RECFM' NE 'V').D4
&URFM    SETC  'DS1RECFV'
         AIF   ('&LRECL' NE '').D3A
         MNOTE 0,' LRECL MISSING (DEFAULT OF 255 USED) ************** '
&ULRL    SETC  '255'
         AGO   .E
.D3A     AIF   (&LRECL GT 32767).D3B
&ULRL    SETC  '&LRECL'
         AGO   .E
.D3B     MNOTE 8,' INVALID LRECL SPECIFIED (MAX. VALUE 32767) ******* '
&SW      SETB  1
         AGO   .E
.D4      AIF   ('&RECFM' NE 'U').D5
&URFM    SETC  'DS1RECFU'
         AIF   ('&LRECL' EQ '').D4A
         MNOTE 0,' LRECL SPECIFIED, BUT WILL BE IGNORED ************* '
.D4A     ANOP
&ULRL    SETC  '0'
         AGO   .E
.D5      MNOTE 8,' INVALID RECFM SPECIFIED (F, V OR U) ************** '
&SW      SETB  1
.E       AIF   ('&SP' EQ '').E2
         AIF   ('&SP' EQ 'C').E1
         AIF   ('&SP' EQ 'D').E1
         AIF   ('&SP' EQ 'E').E1
         AIF   ('&SP' EQ 'T').E1
         AIF   ('&SP' EQ 'W').E1
         MNOTE 8,' INVALID SP= SPECIFIED (ONLY C/D/E/T/W) *********** '
         AGO   .END
.E1      ANOP
&USPA    SETC  '&SP'
         AGO   .F
.E2      ANOP
&USPA    SETC  ''
.F       AIF   (&SW).END
         AIF   ('&USPA' NE '').F1
         DC    A(0)
         AGO   .F2
.F1      DC    0F'0',CL4'&USPA'
.F2      DC    CL8'&DDN'
         DC    CL20'&UID'
         DC    AL1(&UORG)
         DC    AL1(&URFM)
         DC    H'&ULRL'
.END     MEND
         MACRO           DD-NAME LIBRARIES INFORMATION
        @DDTE  &DUMMY
         GBLB  &DDSWS,&DDSWE
.* SYNTAX :    @DDTE  ,   END OF TABLE
         AIF   (&DDSWS).A
         MNOTE 8,' START OF TABLE HAS NOT BEEN DEFINED ************** '
         AGO   .END
.A       AIF   (NOT &DDSWE).B
         MNOTE 8,' REPEATED END OF TABLE (ONLY ONCE ALLOWED) ******** '
         AGO   .END
.B       ANOP
&DDSWE   SETB  1
DDEND    EQU   *                   END OF TABLE ADDRESS
.END     MEND
         MACRO           DD-NAME LIBRARIES INFORMATION
        @DDTS  &DUMMY
         GBLB  &DDSWS
.* SYNTAX :    @DDTS  ,   START OF TABLE
         AIF   (NOT &DDSWS).A
         MNOTE 8,' REPEATED START OF TABLE (ONLY ONCE ALLOWED) ****** '
         AGO   .END
.A       ANOP
&DDSWS   SETB  1
DDSTART  DS    0F                  START OF TABLE
LIB#DSF$ EQU   X'FFFFFFFF',4       END OF LIST INDICATOR
.END     MEND
         MACRO           INTERNAL DEFAULTS VALUES.
        @DEFDV &DUMMY
         GBLB  &ITSWT
         GBLC  &ITPFX,&ITDSN
         GBLC  &DMSVOL,&MXLCIT,&MXALUS,&MXALGR
         GBLC  &PU,&PV,&PA,&PP,&PS,&PD,&PB
         GBLC  &WU,&WA,&WP,&WS,&WD,&WR
         GBLC  &CU,&CA,&CP,&CS,&CB
         GBLC  &EU,&EA,&EP,&ES
.* SYNTAX :    @DEFDV  ,
         AIF   (&ITSWT).A
         MNOTE 8,' TABLES LIBRARY DATA-SET NAME MISSING ************* '
         AGO   .END
.A       ANOP
ATLPFX   DC    AL1(L'TLPFX)
TLPFX    DC    C'&ITPFX'
ATLDSN   DC    AL1(L'TLDSN)
TLDSN    DC    C'&ITDSN'
         CNOP  0,4
AINDV    DC    A(&MXLCIT)  - - - - +
         DC    A(&MXALUS)          +
         DC    A(&MXALGR)          +
         DC    AL1(L'IYPUN)        +
IYPUN    DC    C'&PU'              +
         DC    CL6'&PV'            +
         DC    AL2(DAL&PA)         +
         DC    AL3(&PP)            +
         DC    AL3(&PS)            +
         DC    AL3(&PD)            +
         DC    AL2(&PB)            +
         DC    AL1(L'IYWUN)        +
IYWUN    DC    C'&WU'              +
         DC    AL2(DAL&WA)         +
         DC    AL3(&WP)            +
         DC    AL3(&WS)            +
         DC    AL3(&WD)            +
         DC    AL1(L'IYWRN)        +
IYWRN    DC    C'&WR'              +
         DC    AL1(L'IYCUN)        +
IYCUN    DC    C'&CU'              +
         DC    AL2(DAL&CA)         +
         DC    AL3(&CP)            +
         DC    AL3(&CS)            +
         DC    AL2(&CB)            +
         DC    AL1(L'IYEUN)        +
IYEUN    DC    C'&EU'              +
         DC    AL3(&EA)            +
         DC    AL3(&EP)            +
         DC    AL3(&ES)            +
         DC    CL6'&DMSVOL'        + - DEFAULT VALUES
.END     MEND
         MACRO           DEFINE FLAGS
        @DEFF
         GBLC  &FIN1(8)
         LCLA  &L,&N,&T
         LCLC  &FB(8),&IN
.* SYNTAX :    @DEFF  ...,...,... <- LIST OF FLAGS NAME (MAX. = 8,
.*                                        LENGTH OF NAME = 1 CHAR.)
&FB(1)   SETC  '80'
&FB(2)   SETC  '40'
&FB(3)   SETC  '20'
&FB(4)   SETC  '10'
&FB(5)   SETC  '08'
&FB(6)   SETC  '04'
&FB(7)   SETC  '02'
&FB(8)   SETC  '01'
&N       SETA  N'&SYSLIST
         AIF   (&N NE 0).A
         MNOTE 8,' OPERANDS MISSING ********************************* '
         AGO   .END
.A       AIF   (&N LE 8).B
         MNOTE 4,' TOO MANY OPERANDS (8 CHAR. ASSUMED) ************** '
&N       SETA  8
.B       ANOP
&L       SETA  1
         SPACE 1
.C       AIF   ('&SYSLIST(&L)' EQ '').E
&IN      SETC  '&SYSLIST(&L)'
         AIF   (K'&IN EQ 1).C1
         MNOTE 8,' &IN : INVALID (ONLY 1 CHAR. ALLOWED) ************* '
         AGO   .E
.C1      AIF   ('&IN' NE '$').D
         MNOTE 8,' &IN : INVALID (RESERVED : END OF LIST) *********** '
         AGO   .E
.D       ANOP
&T       SETA  1
.D1      AIF   ('&FIN1(&T)' NE '&IN').D2
         MNOTE 8,' &IN : DUPLICATE CHAR. DEFINITION ***************** '
         AGO   .E
.D2      AIF   (&T EQ 8).D3
&T       SETA  &T+1
         AGO   .D1
.D3      ANOP
&T       SETA  &L
         AIF   ('&FIN1(&T)' EQ '').DS1S
         MNOTE 8,' FLAG &IN OVERLAPS &FIN1(&T) ********************** '
         AGO   .E
.DS1S    ANOP
&FIN1(&T) SETC '&IN'
LIB#DSF&IN EQU   X'&FB(&T)'
.E       ANOP
&L       SETA  &L+1
         AIF   (&L LE &N).C
.END     MEND
         MACRO           DEFINE INDICATORS
        @DEFI
         GBLC  &TIN1(8),&TIN2(8),&TIN3(8)
         LCLA  &L,&N,&S,&T
         LCLB  &SW
         LCLC  &FB(8),&IN
.* SYNTAX :    @DEFI  ..., <- INDICATOR BYTE NUMBER (1, 2 OR 3)
.*                    ...,...,... <- LIST OF INDICATORS NAME (MAX. = 8,
.*                                   LENGTH OF NAME = MAX. 4 CHAR.)
&FB(1)   SETC  '80'
&FB(2)   SETC  '40'
&FB(3)   SETC  '20'
&FB(4)   SETC  '10'
&FB(5)   SETC  '08'
&FB(6)   SETC  '04'
&FB(7)   SETC  '02'
&FB(8)   SETC  '01'
&N       SETA  N'&SYSLIST
         AIF   (&N NE 0).A
         MNOTE 8,' OPERANDS MISSING ********************************* '
         AGO   .END
.A       AIF   (&N LE 9).A0
         MNOTE 4,' TOO MANY OPERANDS (IND.NO. + 8 NAMES ASSUMED) **** '
&N       SETA  9
.A0      AIF   ('&SYSLIST(1)' NE '').A1
         MNOTE 8,' INDICATOR BYTE NUMBER MISSING ******************** '
         AGO   .END
.A1      AIF   ('&SYSLIST(1)' EQ '1').A2
         AIF   ('&SYSLIST(1)' EQ '2').A3
         AIF   ('&SYSLIST(1)' EQ '3').A4
         MNOTE 8,' INVALID INDICATOR BYTE NUMBER ******************** '
         AGO   .END
.A2      ANOP
&S       SETA  1
         AGO   .B
.A3      ANOP
&S       SETA  2
         AGO   .B
.A4      ANOP
&S       SETA  3
.B       AIF   (&N EQ 1).ERR
&L       SETA  2
         SPACE 1
.C       AIF   ('&SYSLIST(&L)' EQ '').E
&SW      SETB  1
&IN      SETC  '&SYSLIST(&L)'
         AIF   (K'&IN LE 4).D
         MNOTE 8,' &IN : NAME TOO LONG (MAX.CHAR. 4) **************** '
         AGO   .E
.D       ANOP
&T       SETA  1
.D1      AIF   ('&TIN1(&T)' EQ '&IN').DUP
         AIF   ('&TIN2(&T)' EQ '&IN').DUP
         AIF   ('&TIN3(&T)' NE '&IN').D2
.DUP     MNOTE 8,' &IN : DUPLICATE NAME DEFINITION ****************** '
         AGO   .E
.D2      AIF   (&T EQ 8).D3
&T       SETA  &T+1
         AGO   .D1
.D3      ANOP
&T       SETA  &L-1
         AIF   (&S EQ 3).DS3
         AIF   (&S EQ 2).DS2
         AIF   ('&TIN1(&T)' EQ '').DS1S
         MNOTE 8,' INDICATOR &IN OVERLAPS &TIN1(&T) ***************** '
         AGO   .E
.DS1S    ANOP
&TIN1(&T) SETC '&IN'
         AGO   .DX
.DS2     ANOP
         AIF   ('&TIN2(&T)' EQ '').DS2S
         MNOTE 8,' INDICATOR &IN OVERLAPS &TIN2(&T) ***************** '
         AGO   .E
.DS2S    ANOP
&TIN2(&T) SETC '&IN'
         AGO   .DX
.DS3     ANOP
         AIF   ('&TIN3(&T)' EQ '').DS3S
         MNOTE 8,' INDICATOR &IN OVERLAPS &TIN3(&T) ***************** '
         AGO   .E
.DS3S    ANOP
&TIN3(&T) SETC '&IN'
.DX      ANOP
IND#&IN  EQU   X'&FB(&T)'
.E       ANOP
&L       SETA  &L+1
         AIF   (&L LE &N).C
         AIF   (&SW).END
.ERR     MNOTE 8,' NO DEFINITION NAME(S) SPECIFIED ****************** '
.END     MEND
         MACRO           DD-NAME LIBRARIES INFORMATION
&NAME   @DS    &DSN,&TYPE=,&OPT=,&SP=
         GBLB  &DDSWS,&DDSWE
         GBLC  &TIN1(8),&TIN2(8),&TIN3(8)
         GBLC  &FIN1(8)
         LCLA  &T,&Z
         LCLB  &SW
         LCLC  &UDSN,&UFLG,&UFLW,&UTY1,&UTY2,&UTY3,&USPA
.* SYNTAX :    ...  @DS  DSN=...,TYPE=...,OPT=...,SP=...
         AIF   (NOT &DDSWS).DGN
         AIF   (NOT &DDSWE).GCD
.DGN     MNOTE 8,' CANNOT GENERATE THE DATA HERE ******************** '
         AGO   .END
.GCD     AIF   ('&DSN' NE '').A
&UDSN    SETC  '<T>'
         AGO   .B
.A       ANOP
&Z       SETA  K'&DSN
         AIF   ('&DSN'(1,1) NE '''').A2
         AIF   ('&DSN'(&Z,1) NE '''').A1
         AIF   (&Z GT 46).A3
&Z       SETA  &Z-2
&UDSN    SETC  '&DSN'(2,&Z)
         AGO   .B
.A1      MNOTE 8,' DSN : DSNAME INCORRECTLY QUOTED ****************** '
&SW      SETB  1
         AGO   .B
.A2      AIF   (&Z GT 44).A3
&UDSN    SETC  '&DSN'
         AGO   .B
.A3      MNOTE 8,' DSN : DSNAME TOO LONG (MAX.CHAR. 44) ************* '
&SW      SETB  1
.B       ANOP
&Z       SETA  1
&UFLG    SETC  '0'
.BL      AIF   ('&OPT(&Z)' EQ '').C
         AIF   (K'&OPT(&Z) EQ 1).B1
         MNOTE 8,' OPT : INVALID OPTION (ONLY 1 CHAR. ALLOWED) ****** '
         AGO   .B4
.B1      ANOP
&T       SETA  1
.B2      AIF   ('&OPT(&Z)' EQ '&FIN1(&T)').B5
         AIF   (&T EQ 8).B3
&T       SETA  &T+1
         AGO   .B2
.B3      MNOTE 8,' OPT : UNDEFINED OPTION SPECIFIED ***************** '
.B4      ANOP
&SW      SETB  1
         AGO   .C
.B5      ANOP
&UFLW    SETC  'LIB#DSF'.'&OPT(&Z)'
         ANOP
&Z       SETA  &Z+1
         AIF   ('&UFLG' NE '0').BN
&UFLG    SETC  '&UFLW'
         AGO   .BL
.BN      ANOP
&UFLG    SETC  '&UFLG+&UFLW'
         AGO   .BL
.C       ANOP
&Z       SETA  1
&UTY1    SETC  '0'
&UTY2    SETC  '0'
&UTY3    SETC  '0'
.CL      AIF   ('&TYPE(&Z)' EQ '').D
&T       SETA  1
.C1      AIF   ('&TYPE(&Z)' EQ '&TIN1(&T)').C1S
         AIF   (&T EQ 8).C1N
&T       SETA  &T+1
         AGO   .C1
.C1S     ANOP
&T       SETA  1
         AGO   .C4
.C1N     ANOP
&T       SETA  1
.C2      AIF   ('&TYPE(&Z)' EQ '&TIN2(&T)').C2S
         AIF   (&T EQ 8).C2N
&T       SETA  &T+1
         AGO   .C2
.C2S     ANOP
&T       SETA  2
         AGO   .C4
.C2N     ANOP
&T       SETA  1
.C3      AIF   ('&TYPE(&Z)' EQ '&TIN3(&T)').C3S
         AIF   (&T EQ 8).CERR
&T       SETA  &T+1
         AGO   .C3
.C3S     ANOP
&T       SETA  3
         AGO   .C4
.CERR    MNOTE 8,' TYPE : &TYPE(&Z) INVALID ************************* '
&SW      SETB  1
         AGO   .CT
.C4      AIF   (&T NE 1).C5
         AIF   ('&UTY1' NE '0').C4N
&UTY1    SETC  'IND#&TYPE(&Z)'
         AGO   .CT
.C4N     ANOP
&UTY1    SETC  '&UTY1+IND#&TYPE(&Z)'
         AGO   .CT
.C5      AIF   (&T NE 2).C6
         AIF   ('&UTY2' NE '0').C5N
&UTY2    SETC  'IND#&TYPE(&Z)'
         AGO   .CT
.C5N     ANOP
&UTY2    SETC  '&UTY2+IND#&TYPE(&Z)'
         AGO   .CT
.C6      AIF   ('&UTY3' NE '0').C6N
&UTY3    SETC  'IND#&TYPE(&Z)'
         AGO   .CT
.C6N     ANOP
&UTY3    SETC  '&UTY3+IND#&TYPE(&Z)'
.CT      ANOP
&Z       SETA  &Z+1
         AGO   .CL
.D       AIF   ('&SP' EQ '').D2
         AIF   ('&SP' EQ 'P').D1
         MNOTE 8,' INVALID SP= SPECIFIED (ONLY P) ******************* '
         AGO   .END
.D1      ANOP
&USPA    SETC  '&SP'
         AGO   .E
.D2      ANOP
&USPA    SETC  ''
.E       AIF   (&SW).END
         AIF   ('&USPA' EQ '').E1
&NAME    DC    0F'0',CL4'&USPA'
         AGO   .E2
.E1      ANOP
&NAME    DC    A(0)
.E2      DC    AL1(&UFLG)
         DC    AL1(&UTY1,&UTY2,&UTY3)
         DC    CL44'&UDSN'
.END     MEND
         MACRO           DD-NAME LIBRARIES INFORMATION
&NAME   @DSE   &DUMMY
.* SYNTAX :    ... @DSE  ,   END OF TABLE
&NAME    DC    AL4(LIB#DSF$)       END OF LIST MARKER
         SPACE 1
         MEND
         MACRO           GENERATE PRODUCTS, ... FIELDS FOR CHECK
        @GNKW
         GBLC  &TIN1(8),&TIN2(8)
         LCLA  &Z,&L
         LCLC  &S
.* SYNTAX :    @GNKW  ,
&Z       SETA  1
SGNKW    EQU   *
.A       AIF   ('&TIN1(&Z)' EQ '').B
&S       SETC  '&TIN1(&Z)'
KW&S     DS    XL1                 +
.B       AIF   (&Z EQ 8).C
&Z       SETA  &Z+1
         AGO   .A
.C       ANOP
&Z       SETA  1
.D       AIF   ('&TIN2(&Z)' EQ '').E
&S       SETC  '&TIN2(&Z)'
KW&S     DS    XL1                 +
.E       AIF   (&Z EQ 8).END
&Z       SETA  &Z+1
         AGO   .D
.END     ANOP
LGNKW    EQU   (*-SGNKW)-1
         MEND
         MACRO           TEST KEYWORD/USER/GROUP/TERMINAL
&NAME   @IF    &KEYWORD=,&USER=,&GROUP=,&TERMINAL=,&GOTO=,&ELSE=
         LCLA  &X,&Z
         LCLC  &NM
.* SYNTAX :    ...  @IF  KEYWORD=(... <- KEYWORD NAME
.*                                U), <- UNSPECIFIED OPTION
.*                                N   <- NO OPTION
.*                                S   <- OPTION
.*                  OR : USER=...,
.*                       USER=(...,...,...),
.*                  OR : GROUP=...,
.*                       GROUP=(...,...,...),
.*                  OR : TERMINAL=S, <- SCREEN
.*                                L  <- LINE
.*                                B  <- BACKGROUND
.*                       GOTO=..., <- BRANCH IF EQUAL
.*                       ELSE=...  <- BRANCH IF NOT EQUAL
         AIF   ('&KEYWORD' EQ '').B1
         AIF   ('&USER' EQ '').A1
         MNOTE 8,' USER= CANNOT BE SPECIFED WITH KEYWORD= *********** '
         AGO   .END
.A1      AIF   ('&GROUP' EQ '').A2
         MNOTE 8,' GROUP= CANNOT BE SPECIFED WITH KEYWORD= ********** '
         AGO   .END
.A2      AIF   ('&TERMINAL' EQ '').A3
         MNOTE 8,' TERMINAL= CANNOT BE SPECIFED WITH KEYWORD= ******* '
         AGO   .END
.A3      AIF   (N'&KEYWORD EQ 2).A4
         MNOTE 8,' KEYWORD= INVALID ********************************* '
         AGO   .END
.A4      AIF   ('&KEYWORD(1)' NE '').A5
         MNOTE 8,' KEYWORD NAME MISSING ***************************** '
         AGO   .END
.A5      AIF   (K'&KEYWORD(1) LE 6).A6
         MNOTE 8,' KEYWORD NAME TOO LONG (MAX. CHAR. 6) ************* '
         AGO   .END
.A6      AIF   ('&KEYWORD(2)' NE '').A7
         MNOTE 8,' KEYWORD VALUE MISSING **************************** '
         AGO   .END
.A7      ANOP
&NM      SETC  'KW'.'&KEYWORD(1)'
         AIF   ('&KEYWORD(2)' EQ 'U').A8
         AIF   ('&KEYWORD(2)' EQ 'N').A9
         AIF   ('&KEYWORD(2)' EQ 'S').A10
         MNOTE 8,' KEYWORD VALUE INVALID (NOT U, N OR S) ************ '
         AGO   .END
.A8      ANOP
&NAME    CLI   &NM,0               UNSPECIFIED OPERAND
         AGO   .A11
.A9      ANOP
&NAME    CLI   &NM,1               NULLIFIED OPERAND
         AGO   .A11
.A10     ANOP
&NAME    CLI   &NM,2               SPECIFIED OPERAND
.A11     AIF   ('&GOTO' NE '').A12
         AIF   ('&ELSE' NE '').A13
         MNOTE 8,' GOTO= OR ELSE= MISSING *************************** '
         AGO   .END
.A12     BE    &GOTO
         AIF   ('&ELSE' EQ '').END
         B     &ELSE
         AGO   .END
.A13     BNE   &ELSE
         AGO   .END
.B1      AIF   ('&USER' EQ '').C1
         AIF   ('&GROUP' EQ '').B2
         MNOTE 8,' GROUP= CANNOT BE SPECIFED WITH USER= ************* '
         AGO   .END
.B2      AIF   ('&TERMINAL' EQ '').B3
         MNOTE 8,' TERMINAL= CANNOT BE SPECIFED WITH USER= ********** '
         AGO   .END
.B3      AIF   ('&GOTO' NE '').B4
         AIF   ('&ELSE' NE '').B4
         MNOTE 8,' GOTO= OR ELSE= MISSING *************************** '
         AGO   .END
.B4      AIF   ('&USER'(1,1) EQ '(').B6
&NAME    CLC   USID,=CL8'&USER'
         AIF   ('&GOTO' EQ '').B5
         BE    &GOTO
         AIF   ('&ELSE' EQ '').END
         B     &ELSE
         AGO   .END
.B5      BNE   &ELSE
         AGO   .END
.B6      ANOP
&X       SETA  N'&USER
&Z       SETA  1
&NM      SETC  '&NAME'
.B7      AIF   ('&USER(&Z)' NE '').B8
         MNOTE 8,' USER= (&Z) MISSING ******************************* '
         AGO   .END
.B8      ANOP
&NM      CLC   USID,=CL8'&USER(&Z)'
&NM      SETC  ''
         AIF   (&Z EQ &X).B11
         AIF   ('&GOTO' NE '').B9
         BE    *+L'*+(10*(&X-&Z))
         AGO   .B10
.B9      BE    &GOTO
.B10     ANOP
&Z       SETA  &Z+1
         AGO   .B7
.B11     AIF   ('&GOTO' NE '').B12
         BNE   &ELSE
         AGO   .END
.B12     BE    &GOTO
         AIF   ('&ELSE' EQ '').END
         B     &ELSE
         AGO   .END
.C1      AIF   ('&GROUP' EQ '').D1
         AIF   ('&TERMINAL' EQ '').C2
         MNOTE 8,' TERMINAL= CANNOT BE SPECIFED WITH GROUP= ********* '
         AGO   .END
.C2      AIF   ('&GOTO' NE '').C3
         AIF   ('&ELSE' NE '').C3
         MNOTE 8,' GOTO= OR ELSE= MISSING *************************** '
         AGO   .END
.C3      AIF   ('&GROUP'(1,1) EQ '(').C5
&NAME    CLC   GRNM,=CL8'&GROUP'
         AIF   ('&GOTO' EQ '').C4
         BE    &GOTO
         AIF   ('&ELSE' EQ '').END
         B     &ELSE
         AGO   .END
.C4      BNE   &ELSE
         AGO   .END
.C5      ANOP
&X       SETA  N'&GROUP
&Z       SETA  1
&NM      SETC  '&NAME'
.C6      AIF   ('&GROUP(&Z)' NE '').C7
         MNOTE 8,' GROUP= (&Z) MISSING ****************************** '
         AGO   .END
.C7      ANOP
&NM      CLC   GRNM,=CL8'&GROUP(&Z)'
&NM      SETC  ''
         AIF   (&Z EQ &X).C10
         AIF   ('&GOTO' NE '').C8
         BE    *+L'*+(10*(&X-&Z))
         AGO   .C9
.C8      BE    &GOTO
.C9      ANOP
&Z       SETA  &Z+1
         AGO   .C6
.C10     AIF   ('&GOTO' NE '').C11
         BNE   &ELSE
         AGO   .END
.C11     BE    &GOTO
         AIF   ('&ELSE' EQ '').END
         B     &ELSE
         AGO   .END
.D1      AIF   ('&TERMINAL' NE '').D2
         MNOTE 8,' KEYWORD= , USER= , GROUP= OR TERMINAL= MISSING *** '
         AGO   .END
.D2      ANOP
&NAME    TM    SWSTTS,TDSPLY       TERMINAL TYPE ?
         AIF   ('&GOTO' NE '').D3
         AIF   ('&ELSE' NE '').D7
         MNOTE 8,' GOTO= OR ELSE= MISSING *************************** '
         AGO   .END
.D3      AIF   ('&TERMINAL' NE 'S').D4
         BO    &GOTO               IT IS A SCREEN TERMINAL
         AGO   .D6
.D4      AIF   ('&TERMINAL' EQ 'L').D5
         AIF   ('&TERMINAL' NE 'B').D9
.D5      BZ    &GOTO               IT IS A LINE TERMINAL OR BACKGROUND
.D6      AIF   ('&ELSE' EQ '').END
         B     &ELSE
         AGO   .END
.D7      AIF   ('&TERMINAL' NE 'S').D8
         BZ    &ELSE               IT IS A LINE TERMINAL OR BACKGROUND
         AGO   .END
.D8      AIF   ('&TERMINAL' EQ 'L').D10
         AIF   ('&TERMINAL' EQ 'B').D10
.D9      MNOTE 8,' TERMINAL= INVALID (NOT S, L OR B) **************** '
         AGO   .END
.D10     BO    &ELSE               IT IS A SCREEN TERMINAL
.END     MEND
         MACRO           PASS REGISTER MANAGEMENT
&NAME   @PASS  &REG
.* SYNTAX :    ...  @PASS  ... <- REGISTER
         AIF   ('&REG' EQ '13').A
         AIF   ('&REG' EQ 'R13').A
         AIF   ('&REG' EQ '14').A
         AIF   ('&REG' EQ 'R14').A
         AIF   ('&REG' EQ '15').A
         AIF   ('&REG' NE 'R15').B
.A       MNOTE 8,' &REG : INVALID REGISTER ************************** '
         AGO   .END
.B       ANOP
&NAME    L     R14,4(R13)          GET PREVIOUS SAVE AREA
         ST    &REG,20+(&REG*4)(R14) PASS REGISTER CONTENTS
.END     MEND
         MACRO           SET INDICATORS
&NAME   @SETI  &A,&F,&GOTO=
         GBLC  &TIN1(8),&TIN2(8),&TIN3(8)
         LCLA  &Z
         LCLC  &S
.* SYNTAX :    ...  @SETI  ..., <- TARGET FIELD
.*                         ..., <- INDICATOR NAME
.*                         GOTO=... <- UNCONDITIONAL BRANCH
         AIF   ('&A' NE '').A
         MNOTE 8,' ADDRESS OPERAND MISSING ************************** '
         AGO   .END
.A       AIF   ('&F' NE '').A0
         MNOTE 8,' DEFINITION NAME MISSING ************************** '
         AGO   .END
.A0      ANOP
&Z       SETA  1
&S       SETC  '0'
.A1      AIF   ('&TIN1(&Z)' EQ '&F').D
         AIF   (&Z EQ 8).B
&Z       SETA  &Z+1
         AGO   .A1
.B       ANOP
&Z       SETA  1
&S       SETC  '1'
.B1      AIF   ('&TIN2(&Z)' EQ '&F').D
         AIF   (&Z EQ 8).C
&Z       SETA  &Z+1
         AGO   .B1
.C       ANOP
&Z       SETA  1
&S       SETC  '2'
.C1      AIF   ('&TIN3(&Z)' EQ '&F').D
         AIF   (&Z EQ 8).ERR
&Z       SETA  &Z+1
         AGO   .C1
.ERR     MNOTE 8,' &F : UNDEFINED NAME ****************************** '
         AGO   .END
.D       ANOP
&NAME    OI    &A+&S,IND#&F
         AIF   ('&GOTO' EQ '').END
         B     &GOTO
.END     MEND
         MACRO           SET KEYWORD
&NAME   @SETKW &KW,&VK,&GOTO=
.* SYNTAX :    ...  @SETKW  ..., <- KEYWORD NAME
.*                          ..., <- U = UNSPECIFIED OPTION
.*                                  N = NO OPTION
.*                                  S = OPTION
.*                          GOTO=... <- UNCONDITIONAL BRANCH
         AIF   ('&KW' NE '').A
         MNOTE 8,' KEYWORD MISSING ********************************** '
         AGO   .END
.A       AIF   (K'&KW LE 6).B
         MNOTE 8,' &KW : NAME TOO LONG (MAX.CHAR. 6) **************** '
         AGO   .END
.B       AIF   ('&VK' NE '').C
         MNOTE 8,' KEYWORD VALUE MISSING **************************** '
         AGO   .END
.C       AIF   ('&VK' EQ 'U').D
         AIF   ('&VK' EQ 'N').E
         AIF   ('&VK' EQ 'S').F
         MNOTE 8,' &VK : INVALID KEYWORD VALUE (U, N OR S ONLY) ***** '
         AGO   .END
.D       ANOP
&NAME    MVI   KW&KW,0             SET AS UNSPECIFIED OPERAND
         AGO   .G
.E       ANOP
&NAME    MVI   KW&KW,1             SET AS NULLIFIED OPERAND
         AGO   .G
.F       ANOP
&NAME    MVI   KW&KW,2             SET AS SPECIFIED OPERAND
.G       AIF   ('&GOTO' EQ '').END
         B     &GOTO
.END     MEND
         MACRO           GENERATE FLAGS TABLE
        @TABF  &DUMMY
         GBLC  &FIN1(8)
         LCLA  &Z
         LCLC  &S
.* SYNTAX :    @TABF  ,
LFLTB    DS    0XL2                START OF TABLE
&Z       SETA  1
.A       AIF   ('&FIN1(&Z)' EQ '').B
&S       SETC  '&FIN1(&Z)'
         DC    CL1'&S',AL1(LIB#DSF&S)
.B       AIF   (&Z EQ 8).END
&Z       SETA  &Z+1
         AGO   .A
.END     DC    XL1'0'              END OF TABLE
         MEND
         MACRO           GENERATE INDICATORS TABLE
        @TABI  &DUMMY
         GBLC  &TIN1(8),&TIN2(8),&TIN3(8)
         LCLA  &Z
         LCLC  &S
.* SYNTAX :    @TABI  ,
INDTB    DS    0XL7                START OF TABLE
&Z       SETA  1
.A1      AIF   ('&TIN1(&Z)' EQ '').A2
&S       SETC  '&TIN1(&Z)'
         DC    CL4'&S',AL1(IND#&S,0,0)
.A2      AIF   (&Z EQ 8).B
&Z       SETA  &Z+1
         AGO   .A1
.B       ANOP
&Z       SETA  1
.B1      AIF   ('&TIN2(&Z)' EQ '').B2
&S       SETC  '&TIN2(&Z)'
         DC    CL4'&S',AL1(0,IND#&S,0)
.B2      AIF   (&Z EQ 8).C
&Z       SETA  &Z+1
         AGO   .B1
.C       ANOP
&Z       SETA  1
.C1      AIF   ('&TIN3(&Z)' EQ '').C2
&S       SETC  '&TIN3(&Z)'
         DC    CL4'&S',AL1(0,0,IND#&S)
.C2      AIF   (&Z EQ 8).END
&Z       SETA  &Z+1
         AGO   .C1
.END     DC    XL1'0'              END OF TABLE
         MEND
         MACRO           GENERATE SELECT= TABLE
        @TABS  &DUMMY
         GBLC  &TIN1(8),&TIN2(8),&TIN3(8)
         LCLA  &Z,&L
         LCLC  &S
.* SYNTAX :    @TABS  ,
SELTB    DS    0XL1                START OF TABLE
&Z       SETA  1
.A1      AIF   ('&TIN1(&Z)' EQ '').A2
&S       SETC  '&TIN1(&Z)'
&L       SETA  K'&S-1
         DC    AL1(&L),C'&S',AL1(IND#&S,0,0)
.A2      AIF   (&Z EQ 8).B
&Z       SETA  &Z+1
         AGO   .A1
.B       ANOP
&Z       SETA  1
.B1      AIF   ('&TIN2(&Z)' EQ '').B2
&S       SETC  '&TIN2(&Z)'
&L       SETA  K'&S-1
         DC    AL1(&L),C'&S',AL1(0,IND#&S,0)
.B2      AIF   (&Z EQ 8).C
&Z       SETA  &Z+1
         AGO   .B1
.C       ANOP
&Z       SETA  1
.C1      AIF   ('&TIN3(&Z)' EQ '').C2
&S       SETC  '&TIN3(&Z)'
&L       SETA  K'&S-1
         DC    AL1(&L),C'&S',AL1(0,0,IND#&S)
.C2      AIF   (&Z EQ 8).END
&Z       SETA  &Z+1
         AGO   .C1
.END     DC    AL1(255)            END OF TABLE
         MEND
         MACRO           TEST A FLAG
        @TESTF &F
         GBLC  &FIN1(8)
         LCLA  &Z
         LCLC  &S
.* SYNTAX :    @TESTF  ... <- FLAG NAME
         AIF   ('&F' NE '').A
         MNOTE 8,' OPERAND MISSING ********************************** '
         AGO   .END
.A       AIF   (K'&F EQ 1).B
         MNOTE 8,' OPERAND INVALID (ONLY 1 CHAR. ALLOWED) *********** '
         AGO   .END
.B       ANOP
&Z       SETA  1
.C       AIF   ('&FIN1(&Z)' EQ '&F').E
         AIF   (&Z EQ 8).D
&Z       SETA  &Z+1
         AGO   .C
.D       MNOTE 8,' &F : OPERAND UNDEFINED *************************** '
         AGO   .END
.E       ANOP
&S       SETC  '&FIN1(&Z)'
         DC    AL1(LIB#DSF&S)
.END     MEND
         MACRO           DEFINE DEFAULT LIBRARY DATA-SET NAME.
        @TLDSN &IP=,&RN=
         GBLB  &ITSWT
         GBLC  &ITPFX,&ITDSN
         LCLA  &Z
         LCLC  &P,&R
.* SYNTAX :    @TLDSN  IP=...,RN=...
         AIF   (NOT &ITSWT).A
         MNOTE 8,' DUPLICATE DEFINITION ***************************** '
         AGO   .END
.A       ANOP
&ITSWT   SETB  1
         AIF   ('&IP' NE '').B
         MNOTE 8,' INITIAL PREFIX : IP= MISSING ********************* '
         AGO   .END
.B       AIF   ('&RN' NE '').C
         MNOTE 8,' REST OF NAME : RN= MISSING *********************** '
         AGO   .END
.C       ANOP
&Z       SETA  K'&IP
         AIF   ('&IP'(1,1) NE '''').D
         AIF   ('&IP'(&Z,1) EQ '''').E
         MNOTE 8,' IP= : FINAL QUOTE MISSING ************************ '
         AGO   .END
.D       AIF   ('&IP'(&Z,1) NE '''').F
         MNOTE 8,' IP= : INITIAL QUOTE MISSING ********************** '
         AGO   .END
.E       ANOP
&P       SETC  '&IP'(2,&Z-2)
         AGO   .G
.F       ANOP
&P       SETC  '&IP'
.G       ANOP
&Z       SETA  K'&RN
         AIF   ('&RN'(1,1) NE '''').H
         AIF   ('&RN'(&Z,1) EQ '''').I
         MNOTE 8,' RN= : FINAL QUOTE MISSING ************************ '
         AGO   .END
.H       AIF   ('&RN'(&Z,1) NE '''').J
         MNOTE 8,' RN= : INITIAL QUOTE MISSING ********************** '
         AGO   .END
.I       ANOP
&R       SETC  '&RN'(2,&Z-2)
         AGO   .K
.J       ANOP
&R       SETC  '&RN'
.K       ANOP
&ITPFX   SETC  '&P'                INITIAL PREFIX
&ITDSN   SETC  '.'.'&R'            REST OF THE NAME
.END     MEND
         MACRO           VECTOR COMMUNICATIONS.
        @VCOM  &DUMMY
         LCLC  &Z
.* SYNTAX :    @VCOM  ,
&Z       SETC  'IHB'.'&SYSNDX'
         CNOP  0,8
&Z       DC    A(ASRVC)
         DC    A(AUSD)
         DC    A(ASOK)
         DC    A(AWIZ)
         DC    A(ATLPFX,ATLDSN)
         DC    A(AINDV)
         DC    A(DDSTART,DDEND)
         DC    A(USERSS,USERSE)
         DC    A(GROUPSS,GROUPSE)
         DC    A(AUTOS)
         DC    A(LFLTB)
         DC    A(SELTB)
         DC    A(INDTB)
        @TESTF M
        @TESTF X
         DC    AL1(0)
         DC    AL1(LGNKW)
         DC    A(*-&Z)
ASRVC   $XENT  BASE=R9,ID=DYLOCOM,TYPE=RENT
         L     R3,=A(WKLEN)
         LTR   R1,R1
         BNZ   ASRVCF
        GETMAIN R,LV=(R3)
         LR    R2,R1
         LR    R4,R1
         XR    R5,R5
         MVCL  R2,R4
         B     ASRVCX
ASRVCF  FREEMAIN R,A=(1),LV=(R3)
         XR    R1,R1
ASRVCX  @PASS  R1
        $XRET  TYPE=RENT
         DROP  R9
        LTORG
         MEND
         MACRO           WORK-AREAS COMMUNICATIONS
&NAME   @WAC   &DUMMY
.* SYNTAX :    ...  @WAC  ,
&NAME    DSECT ,
AOCKR    DS    A
GRNM     DS    CL8
USID     DS    CL8
TDSI     DS    XL3
XDSI     DS    XL3
SWSTTS   DS    XL1
TDSPLY   EQU   X'01'               TERMINAL IS A SCREEN
         MEND
         MACRO           DEFINE WIZARD(S).
        @WIZ   &WZUSR,&WZGRP
         LCLA  &I,&N
.* SYNTAX :    ...  @WIZ  ..., <- SINGLE USER OR USERS LIST
.*                        ... <- SINGLE GROUP OR GROUPS LIST
         AIF   ('&WZUSR' NE '').A
         AIF   ('&WZGRP' NE '').F
         MNOTE 8,' USER(S) / GROUP(S) MISSING *********************** '
         AGO   .END
.A       AIF   ('&WZUSR'(1,1) EQ '(').B
AWIZ     DC    AL1(1)
         DC    CL8'&WZUSR'
         AGO   .E
.B       ANOP
&N       SETA  1
.C       ANOP
&N       SETA  &N+1
         AIF   ('&WZUSR(&N)' NE '').C
&N       SETA  &N-1
&I       SETA  1
AWIZ     DC    AL1(&N)
.D       DC    CL8'&WZUSR(&I)'
&I       SETA  &I+1
         AIF   (&I LE &N).D
.E       AIF   ('&WZGRP' NE '').G
         DC    AL1(0)
         AGO   .END
.F       ANOP
AWIZ     DC    AL1(0)
.G       AIF   ('&WZGRP'(1,1) EQ '(').H
         DC    AL1(1)
         DC    CL8'&WZGRP'
         AGO   .END
.H       ANOP
&N       SETA  1
.I       ANOP
&N       SETA  &N+1
         AIF   ('&WZGRP(&N)' NE '').I
&N       SETA  &N-1
&I       SETA  1
         DC    AL1(&N)
.J       DC    CL8'&WZGRP(&I)'
&I       SETA  &I+1
         AIF   (&I LE &N).J
.END     MEND
