VSM8     TITLE 'VSM88000 - DEBUGING AID MODULE.'
         PRINT OFF
         COPY  VSMMAC
         PRINT ON
         SPACE 1
***********************************************************************
*                                                                     *
*    MODULE :           VSM88000                                      *
*                       VERSION : 1.0                                 *
*                       DATE : 12 NOV 1986                            *
*                       NAME : DEBUGING AID MODULE                    *
*                       ATTRIBUTES : REUS                             *
*                                                                     *
*    AUTHOR :           MOINIL P.A. - COMPUTING CENTRE                *
*                       J.R.C. - ISPRA ESTABLISHMENT                  *
*                       21020 ISPRA (VA), ITALY                       *
*                                                                     *
*    FUNCTION :         THIS MODULE TAKES SNAP-DUMPS FOR TEST AND     *
*                       DEBUG OF VSM. THE SNAP-DUMP START-END         *
*                       ADDRESSES ARE PASSED TO VSM88000 VIA THE      *
*                       LINKAGE AREA.                                 *
*                                                                     *
*    NOTE :             THE FOLLOWING ALLOCATION IS NEEDED TO         *
*                       OBTAIN THE SNAP DUMPS :                       *
*                            ALLOC F(SYSSNAP) SYSOUT=A                *
*                       OR : ALLOC F(SYSSNAP) DA(...) OLD             *
*                                                                     *
*    REGS AT ENTRY :    R0   - SNAP-ID NUMBER (0-255)                 *
*                              IF NEGATIVE = CLOSE REQUEST            *
*                       R1   - INTER MODULE LINKAGE                   *
*                       R14  - RETURN ADDRESS                         *
*                       R15  - EPA                                    *
*                                                                     *
*    REGS USAGE :       R0   - WORK REGSITER                          *
*                       R1   - POINTER TO INTER MODULE LINKAGE        *
*                       R2-R6 - WORK REGISTERS                        *
*                       R10  - LINKAGE AREA POINTER                   *
*                       R11  - BASE REGISTER                          *
*                       R12  - 2ND BASE REGISTER (RESERVED)           *
*                       R13  - SAVE AREA POINTER                      *
*                                                                     *
*    REGS AT EXIT :     R14  - RETURN ADDRESS                         *
*                                                                     *
*    LABELS :           SOME OF THE FIELD USAGE IN THIS MODULE CAN    *
*                       BE DETERMINED BY THE FIRST CHARACTER OF THE   *
*                       FIELD. THE KEYS ARE :                         *
*                            @ - FIELD IS IN INTER MODULE LINKAGE     *
*                                AREA                                 *
*                            # - FIELD IS PART OF THE OUTPUT SCREEN   *
*                            $ - DSECT                                *
*                                                                     *
***********************************************************************
         EJECT
VSM88000 START 0
         SPACE 1
        $DEFREG
***********************************************************************
*        STANDARD OS LINKAGE                                          *
***********************************************************************
         SPACE 1
        $XENT  BASE=R11            SAVE CALLER'S REGS
         EJECT
         LR    R10,R1              POINT TO LINKAGE
         USING $LNKSECT,R10        DSECT ADDRESSABILITY
         LTR   R4,R0               TEST AND SAVE SNAP-ID NUMBER
         BM    SNAPCL              BRANCH IF CLOSE REQUESTED
         TM    @SWITCH,@SNPFNA     FUNCTION INACTIVE ?
         BO    RETURN              YES, BYPASS
         LA    R2,SNAPDCB
         USING IHADCB,R2
         TM    @SWITCH,@SNPOPN     DATA-SET ALREADY OPENED ?
         BO    SNO                 YES
         L     R3,@TIOT
         USING TIODSECT,R3
         XR    R1,R1
SNS      CLC   TIOELNGH(4),=F'0'
         BE    SNN
         CLC   TIOEDDNM(L'TIOEDDNM),DCBDDNAM
         BE    SNF
         IC    R1,TIOELNGH
         ALR   R3,R1
         B     SNS
         DROP  R3
SNF     OPEN   (SNAPDCB,OUTPUT)
         TM    DCBOFLGS,DCBOFOPN   SUCCESSFULLY OPENED ?
         BZ    SNN                 NO
         DROP  R2
         OI    @SWITCH,@SNPOPN     INDICATE DATA-SET OPENED
SNO      CH    R4,SNAPMAX          KNOWN ID NUMBER ?
         BL    SNI                 YES
         XR    R4,R4
         LA    R6,SNAPUN
         B     SNG
SNI      LR    R6,R4
         MH    R6,SNAPTLE
         LA    R6,SNAPTB(R6)
         TM    0(R6),X'80'         UNUSED ELEMENT ?
         BZ    RETURN              YES, BYPASS
SNG      LA    R5,@SNPLST
        SNAP   TCB='S',ID=(R4),LIST=(R5),STRHDR=(R6),MF=(E,SNAPLIST)
         LTR   R15,R15             ALL OK ?
         BZ    RETURN              YES
SNN      OI    @SWITCH,@SNPFNA     INACTIVE FUNCTION
RETURN  $XRET  CC=0                B O O G I E
         SPACE 2
SNAPCL   TM    @SWITCH,@SNPOPN     DATA-SET IS OPENED ?
         BZ    RETURN              NO
        CLOSE  (SNAPDCB)
         B     SNN
         EJECT
         PRINT NOGEN
SNAPDCB  DCB   DSORG=PS,RECFM=VBA,MACRF=(W),                           1
               BLKSIZE=1632,LRECL=125,DDNAME=SYSSNAP
SNAPLIST SNAP  DCB=SNAPDCB,MF=L
         PRINT GEN
         SPACE 1
SNAPTB   DS    0F
         DC    XL1'80',AL3(SHD0)
         DC    XL1'80',AL3(SHD1)
         DC    XL1'80',AL3(SHD2)
         DC    XL1'80',AL3(SHD3)
SNAPTBM  EQU   (*-SNAPTB)/L'SNAPTB NUMBER OF ELEMENTS
SNAPUN   DC    XL1'80',AL3(SHDU)
         SPACE 1
SNAPTLE  DC    Y(L'SNAPTB)         ELEMENT LENGTH
SNAPMAX  DC    Y(SNAPTBM)          NUMBER OF ELEMENTS (MAX ID)
SHD0     DC    AL1(L'SHD0T)
SHD0T    DC    C'V S M - REGSITERS 0-15 CONTENTS AT TIME OF ERROR.'
SHD1     DC    AL1(L'SHD1T)
SHD1T    DC    C'V S M - DUMP OF GLOBAL DATA AREA (GDA).'
SHD2     DC    AL1(L'SHD2T)
SHD2T    DC    C'V S M - DUMP OF VSMLIST WORK AREA (SP=SQA).'
SHD3     DC    AL1(L'SHD3T)
SHD3T    DC    C'V S M - DUMP OF VSMLIST WORK AREA (SP=CSA).'
SHDU     DC    AL1(L'SHDUT)
SHDUT    DC    C'V S M - DUMP OF ??? (UNKNOWN).'
         EJECT
         LTORG
         SPACE 2
         $LNKSECT                  LINKAGE SECTION DSECT
         SPACE 2
         PRINT NOGEN
TIODSECT DSECT
         IEFTIOT1
         DCBD  DSORG=PS,DEVD=DA
         SPACE 2
         END
