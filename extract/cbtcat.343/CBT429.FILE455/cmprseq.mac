CMSQ     TITLE 'CMPRSEQ -- SEQUENTIAL DATASET COMPARISON PROGRAM.' -EU-
************************************************************* 04/84 DBC
*                                                           * 04/84 DBC
* MODULE NAME = CMPRSEQ.                                    * 04/84 DBC
*                                                           * 04/84 DBC
* DESCRIPTIVE NAME = SEQUENTIAL DATASET COMPARISON PROGRAM. * 04/84 DBC
*                                                           * 04/84 DBC
* COPYRIGHT = NONE.                                         * 04/84 DBC
*                                                           * 04/84 DBC
* STATUS = RELEASE 84, MODIFICATION LEVEL A.                * 04/84 DBC
*                                                           * 04/84 DBC
* FUNCTION = COMPARE TWO SEQUENTIAL FILES AND REPORT THE    * 04/84 DBC
*            DIFFERENCES. CORRECTLY HANDLE MISSMATCHED      * 04/84 DBC
*            BLOCKS OF INCONSISTANT SIZES.                  * 04/84 DBC
*                                                           * 04/84 DBC
* PROCESSOR = OS/VS ASSEMBLER.                              * 04/84 DBC
*                                                           * 04/84 DBC
* TYPE = PROBLEM PROGRAM.                                   * 04/84 DBC
*                                                           * 04/84 DBC
* DEPENDANCIES = NONE.                                      * 04/84 DBC
*                                                           * 04/84 DBC
* RESTRICTIONS = NONE.                                      * 04/84 DBC
*                                                           * 04/84 DBC
* ATTRIBUTES = NONE.                                        * 04/84 DBC
*                                                           * 04/84 DBC
* AUTHOR = DAVID B. COLE. NOTE, MY APOLOGIES FOR THE LACK   * 04/84 DBC
*          OF COMMENTARY. WHEN I FIRST WROTE THIS PROGRAM,  * 04/84 DBC
*          IT WAS A QUICK-AND-DIRTY ONE INTENDED TO SOLVE   * 04/84 DBC
*          ONLY A VERY SPECIFIC PROBLEM THAT I WAS HAVING   * 04/84 DBC
*          AT THE TIME. EVER SINCE HOWARD GILBERT LET THIS  * 04/84 DBC
*          PROGRAM OUT INTO THE WORLD, I HAVE BEEN          * 04/84 DBC
*          "INTENDING" TO CLEAN IT UP. WELL, I HAVEN'T DONE * 04/84 DBC
*          THAT YET (HOW MANY YEARS HAS IT BEEN NOW?), BUT  * 04/84 DBC
*          I DO, FROM TIME TO TIME, MAKE MISCELLANEOUS      * 04/84 DBC
*          FUNCTIONAL ENHANCEMENTS, AND FROM NOW ON I WILL  * 04/84 DBC
*          AT LEAST BE DOCUMENTING THOSE.                   * 04/84 DBC
*                                                           * 04/84 DBC
************************************************************* 04/84 DBC
         EJECT ,                                              04/84 DBC
************************************************************* 04/84 DBC
*                                                           * 04/84 DBC
* CMPRSEQ VERSION 84A                                       * 04/84 DBC
*                                                           * 04/84 DBC
* LAST CHANGE DATE - APRIL 19, 1984.                        * 04/84 DBC
*                                                           * 04/84 DBC
*                  - I HAVE ADDED COMMENTARY DESCRIBING HOW * 04/84 DBC
*                    TO USE CMPRSEQ.                        * 04/84 DBC
*                                                           * 04/84 DBC
*                  - I AM CHANGING THE WAY IN WHICH THE     * 04/84 DBC
*                    IGNORE FILE IS PROCESSED. NORMALLY,    * 04/84 DBC
*                    WHEN A CARD FROM THE IGNORE FILE IS    * 04/84 DBC
*                    MATCHED WITHIN A MISSMATCHED BLOCK,    * 04/84 DBC
*                    THE FACT THAT THE IGNORED CARD IS      * 04/84 DBC
*                    MATCHED DOES NOT TERMINATE THE         * 04/84 DBC
*                    MISSMATCHED BLOCK. THIS IS STILL TRUE; * 04/84 DBC
*                    HOWEVER, ONCE THE EXTENT OF A          * 04/84 DBC
*                    MISSMATCHED BLOCK HAS BEEN DETERMINED, * 04/84 DBC
*                    THEN THAT BLOCK IS RESCANNED FOR       * 04/84 DBC
*                    IGNORED MATCHES THAT ARE EQUIDISTANT   * 04/84 DBC
*                    INTO THE BLOCK FROM ONE END OR THE     * 04/84 DBC
*                    OTHER. IF ANY ARE FOUND, THEN THE      * 04/84 DBC
*                    MISSMATCHED BLOCK IS SEGMENTED INTO    * 04/84 DBC
*                    SMALLER MISSMATCHED BLOCKS.            * 04/84 DBC
*                                                           * 04/84 DBC
*                  - THE SYSPRINT, NEWLIST, AND OLDLIST     * 04/84 DBC
*                    DATASETS WILL NOW SUPPORT ANY RECFM,   * 04/84 DBC
*                    LRECL, AND BLKSIZE. NOTE, FOR COMPLETE * 04/84 DBC
*                    MESSAGES, THE MINIMUM DATA LENGTH      * 04/84 DBC
*                    SHOULD BE 133 BYTES.                   * 04/84 DBC
*                                                           * 04/84 DBC
************************************************************* 04/84 DBC
         EJECT ,                                              04/84 DBC
************************************************************* 04/84 DBC
*                                                           * 04/84 DBC
* THE CMPRSEQ PROGRAM COMPARES TWO SEQUENTIAL CARD IMAGE    * 04/84 DBC
* FILES AND REPORTS PRECISELY THE DIFFERENCES BETWEEN THE   * 04/84 DBC
* TWO. SUCH DIFFERENCES CAN CONSIST OF INSERTIONS,          * 04/84 DBC
* DELETIONS, AND REPLACEMENTS (OF EITHER EQUAL OR UNEQUAL   * 04/84 DBC
* SIZES).                                                   * 04/84 DBC
*                                                           * 04/84 DBC
* CMPRSEQ ACCOMPLISHES THIS BY READING THE TWO INPUT FILES  * 04/84 DBC
* ALTERNATELY AND COMPARING THE CARD IMAGES AS IT GOES      * 04/84 DBC
* ALONG. WHEN IT ENCOUNTERS TWO CARDS THAT MISMATCH, IT     * 04/84 DBC
* SAVES THEM AND CONTINUES TO READ THE TWO FILES            * 04/84 DBC
* ALTERNATELY. EACH TIME IT READS A CARD FROM ONE FILE, IT  * 04/84 DBC
* COMPARES IT TO ALL CARDS THAT IT HAS READ FROM THE OTHER  * 04/84 DBC
* FILE SINCE THE START OF THE MISMATCH. IF NO MATCH IS      * 04/84 DBC
* FOUND, THEN IT SAVES THAT CARD AND PROCEEDS TO READ THE   * 04/84 DBC
* NEXT CARD FROM THE OTHER FILE. CMPRSEQ CONTINUES          * 04/84 DBC
* ALTERNATING BACK AND FORTH IN THIS MANNER UNTIL IT HAS    * 04/84 DBC
* READ A CARD THAT DOES MATCH ONE OF THE SAVED CARDS FROM   * 04/84 DBC
* THE OTHER FILE. THE MISMATCH THEN CONSISTS OF ALL CARDS   * 04/84 DBC
* IN THE TWO SAVED STACKS THAT ARE BELOW THE TWO MATCHING   * 04/84 DBC
* CARDS.                                                    * 04/84 DBC
*                                                           * 04/84 DBC
* THE MISMATCHED CARDS ARE DUMPED OUT TO LOGGING DATASETS,  * 04/84 DBC
* AND THEN CMPRSEQ PROCEEDS TO LOOK FOR THE NEXT MISMATCHED * 04/84 DBC
* BLOCK.                                                    * 04/84 DBC
*                                                           * 04/84 DBC
* LIMITATIONS                                               * 04/84 DBC
* ===========                                               * 04/84 DBC
*                                                           * 04/84 DBC
* CMPRSEQ WILL COMPARE ONLY CARD IMAGE FILES - I.E., FILES  * 04/84 DBC
* HAVING FIXED LENGTH RECORDS THAT ARE 80 BYTES LONG.       * 04/84 DBC
*                                                           * 04/84 DBC
* CMPRSEQ WILL COMPARE ONLY ONE PAIR OF FILES PER           * 04/84 DBC
* INVOCATION. IT WILL NOT COMPARE AN ENTIRE PDS LIBRARY,    * 04/84 DBC
* ALTHOUGH IT WILL COMPARE A JCL SELECTED PDS LIBRARY       * 04/84 DBC
* MEMBER.                                                   * 04/84 DBC
*                                                           * 04/84 DBC
* IF AN INSERTION BLOCK (FOR EXAMPLE) CONTAINS A CARD WHOSE * 04/84 DBC
* DUPLICATE ALREADY APPEARS COMMONLY THROUGHOUT THE FILES   * 04/84 DBC
* BEING COMPARED (E.G., THE "SPACE 1" ASSEMBLER LANGUAGE    * 04/84 DBC
* STATEMENT, ETC.), THEN THE COMPARISON MAY BECOME, TO A    * 04/84 DBC
* GREATER OR LESSER DEGREE, DESYNCHRONIZED DUE TO THE       * 04/84 DBC
* INSERTED CARD FINDING A MATCH WITH A PRE-EXISTING COPY OF * 04/84 DBC
* THAT CARD IN THE OTHER FILE. CMPRSEQ PROVIDES MECHANISMS  * 04/84 DBC
* TO HELP DEAL WITH SYNCHRONIZATION PROBLEMS. (NOTE,        * 04/84 DBC
* SYNCHRONIZATION PROBLEMS CAN OCCUR, NOT JUST WITH         * 04/84 DBC
* INSERTED BLOCKS, BUT ALSO WITH DELETED BLOCKS AND WITH    * 04/84 DBC
* REPLACED BLOCKS).                                         * 04/84 DBC
         EJECT ,                                                   -EU-
* JCL                                                       * 04/84 DBC
* ===                                                       * 04/84 DBC
*                                                           * 04/84 DBC
* EXEC CARD KEYWORD: REGION=                                * 04/84 DBC
*                                                           * 04/84 DBC
* CMPRSEQ'S MEMORY REQUIREMENTS VARY ACCORDING TO THE       * 04/84 DBC
* AGGREGATE SIZE OF THE LARGEST PAIR OF MISMATCHED BLOCKS   * 04/84 DBC
* ENCOUNTERED. IF A MEMORY SHORTAGE OCCURS, THEN CMPRSEQ    * 04/84 DBC
* TERMINATES IMMEDIATELY; CONSEQUENTLY, IT IS BEST TO       * 04/84 DBC
* PROVIDE A GENEROUS AMOUNT OF AVAILABLE MEMORY. USUALLY,   * 04/84 DBC
* REGION=1024K SHOULD BE ENOUGH.                            * 04/84 DBC
*                                                           * 04/84 DBC
*                                                           * 04/84 DBC
* EXEC CARD KEYWORD: PARM=FULL                              * 04/84 DBC
*                                                           * 04/84 DBC
* BY DEFAULT CMPRSEQ WILL COMPARE TWO CARDS ONLY IN COLUMNS * 04/84 DBC
* 1 THROUGH 72, THUS IGNORING THE SEQUENCE NUMBER FIELD.    * 04/84 DBC
* SPECIFYING PARM=FULL CAUSES CMPRSEQ TO EXAMINE EACH CARD  * 04/84 DBC
* IN ALL 80 COLUMNS.                                        * 04/84 DBC
*                                                           * 04/84 DBC
*                                                           * 04/84 DBC
* DDNAMES: OLD AND NEW                                      * 04/84 DBC
*                                                           * 04/84 DBC
*        ATTRIBUTES                                         * 04/84 DBC
*        ACCESS METHOD   QSAM                               * 04/84 DBC
*        DSORG           PS                                 * 04/84 DBC
*        RECFM           F OR FB                            * 04/84 DBC
*        LRECL           80                                 * 04/84 DBC
*        BLKSIZE         80*N                               * 04/84 DBC
*        DEFAULTS        (PS,F,80,80)                       * 04/84 DBC
*                                                           * 04/84 DBC
* THESE DDNAMES (OLD AND NEW) MUST DESIGNATE THE TWO FILES  * 04/84 DBC
* TO BE COMPARED. THEIR EXISTANCE IS REQUIRED. USUALLY, ONE * 04/84 DBC
* FILE IS AN UPDATED (NEWER) VERSION OF THE OTHER, HENCE    * 04/84 DBC
* NAMES OLD AND NEW.                                        * 04/84 DBC
         EJECT ,                                                   -EU-
* DDNAME: SYSPRINT                                          * 04/84 DBC
*                                                           * 04/84 DBC
*        ATTRIBUTES                                         * 04/84 DBC
*        ACCESS METHOD   QSAM                               * 04/84 DBC
*        DSORG           PS                                 * 04/84 DBC
*        RECFM           UA, VA, VBA, FA, OR FBA            * 04/84 DBC
*        LRECL           133 OR LARGER                      * 04/84 DBC
*        BLKSIZE         133 OR LARGER                      * 04/84 DBC
*        DEFAULTS        (PS,VBA,137,4096)                  * 04/84 DBC
*                                                           * 04/84 DBC
* THE SYSPRINT FILE IS OPTIONAL. IF IT IS AVAILABLE, THEN   * 04/84 DBC
* IT RECEIVES A LOG OF ALL DISCOVERED MISMATCHES. FOR EACH  * 04/84 DBC
* MISMATCHED BLOCK, BOTH THE OLD AND NEW VERSION OF THAT    * 04/84 DBC
* BLOCK IS SHOWN.                                           * 04/84 DBC
*                                                           * 04/84 DBC
*                                                           * 04/84 DBC
* DDNAMES: OLDLIST AND NEWLIST                              * 04/84 DBC
*                                                           * 04/84 DBC
*        ATTRIBUTES                                         * 04/84 DBC
*        ACCESS METHOD   QSAM                               * 04/84 DBC
*        DSORG           PS                                 * 04/84 DBC
*        RECFM           UA, VA, VBA, FA, OR FBA            * 04/84 DBC
*        LRECL           133 OR LARGER                      * 04/84 DBC
*        BLKSIZE         133 OR LARGER                      * 04/84 DBC
*        DEFAULTS        (PS,VBA,137,4096)                  * 04/84 DBC
*                                                           * 04/84 DBC
* BOTH OLDLIST AND NEWLIST ARE OPTIONAL. IF ONE (OR BOTH)   * 04/84 DBC
* ARE AVAILABLE, THEN A COPY OF THE OLD (OR NEW) FILE IS    * 04/84 DBC
* WRITTEN TO IT WITH THE LOCATIONS OF ALL MISMATCHES        * 04/84 DBC
* CLEARLY FLAGGED.                                          * 04/84 DBC
*                                                           * 04/84 DBC
*                                                           * 04/84 DBC
* DDNAME: IGNORE                                            * 04/84 DBC
*                                                           * 04/84 DBC
*        ATTRIBUTES                                         * 04/84 DBC
*        ACCESS METHOD   QSAM                               * 04/84 DBC
*        DSORG           PS                                 * 04/84 DBC
*        RECFM           F OR FB                            * 04/84 DBC
*        LRECL           80                                 * 04/84 DBC
*        BLKSIZE         80*N                               * 04/84 DBC
*        DEFAULTS        (PS,F,80,80)                       * 04/84 DBC
*                                                           * 04/84 DBC
* THE IGNORE FILE IS OPTIONAL. IF IT IS AVAILABLE, THEN IT  * 04/84 DBC
* IS USED TO HELP CONTROL THE POSSIBLE DESYNCHRONIZATION    * 04/84 DBC
* PROBLEMS DISCUSSED EARLIER. THE IGNORE FILE SHOULD        * 04/84 DBC
* CONTAIN COPIES OF CARD IMAGES THAT APPEAR REPEATEDLY      * 04/84 DBC
* THROUGHOUT THE FILES BEING COMPARED, AND ESPECIALLY       * 04/84 DBC
* APPEARING IN AREAS AFFECTED BY INSERTIONS AND DELETIONS.  * 04/84 DBC
* COPIES OF THESE CARDS, WHEN ENCOUNTERED DURING A MISMATCH * 04/84 DBC
* RESOLUTION PROCESS, WILL NOT BE USED TO RESOLVE THE       * 04/84 DBC
* MISMATCH. BY THIS MEANS THE POSSIBILITY OF                * 04/84 DBC
* DESYNCHRONIZATION CAN BE REDUCED.                         * 04/84 DBC
         EJECT ,                                                   -EU-
* DDNAME: SYNC                                              * 04/84 DBC
*                                                           * 04/84 DBC
*        ATTRIBUTES                                         * 04/84 DBC
*        ACCESS METHOD   QSAM                               * 04/84 DBC
*        DSORG           PS                                 * 04/84 DBC
*        RECFM           F OR FB                            * 04/84 DBC
*        LRECL           80                                 * 04/84 DBC
*        BLKSIZE         80*N                               * 04/84 DBC
*        DEFAULTS        (PS,F,80,80)                       * 04/84 DBC
*                                                           * 04/84 DBC
* UNFORTUNATELY, VERY LARGE FILES MAY HAVE TOO MANY         * 04/84 DBC
* COMMONLY REOCCURING CARDS FOR THE IGNORE FILE TO BE       * 04/84 DBC
* WHOLELY EFFECTIVE IN ELIMINATING DESYNCHRONIZATION        * 04/84 DBC
* PROBLEMS. IF THIS IS THE CASE, THEN THE SYNC FILE CAN BE  * 04/84 DBC
* PROVIDED TO FORCE RESYNCHRONIZATION AT PARTICULAR POINTS  * 04/84 DBC
* IN THE FILES BEING COMPARED. THIS FILE SHOULD CONTAIN     * 04/84 DBC
* COPIES OF ONE OR MORE CARDS EACH OF WHICH APPEARS EXACTLY * 04/84 DBC
* ONCE IN BOTH THE OLD AND NEW FILES. THE CARDS IN THE SYNC * 04/84 DBC
* FILE SHOULD APPEAR IN THE SAME ORDER BY WHICH THEY APPEAR * 04/84 DBC
* IN THE OLD AND NEW FILES. THEY SHOULD REPRESENT POINTS IN * 04/84 DBC
* THE OLD AND NEW FILES AT WHICH YOU WISH TO FORCE          * 04/84 DBC
* COMPARISON SYNCHRONIZATION. GENERALLY, SUCH POINTS WILL   * 04/84 DBC
* BE FOLLOWING THOSE AREAS WHERE OTHERWISE UNRECOVERABLE    * 04/84 DBC
* DESYNCHRONIZATION HAS OCCURED.                            * 04/84 DBC
*                                                           * 04/84 DBC
* WHEN CMPRSEQ ENCOUNTERS A RESYNCHRONIZATION POINT (I.E.,  * 04/84 DBC
* WHEN IT HAS READ A CARD FROM ONE OF THE COMPARISON FILES  * 04/84 DBC
* THAT EXACTLY MACTHES THE NEXT CARD FROM THE SYNC FILE),   * 04/84 DBC
* IT WILL NOT READ ANY FURTHER FROM THAT FILE UNTIL IT      * 04/84 DBC
* READS THE IDENTICAL CARD FROM THE OTHER FILE. THIS HAS    * 04/84 DBC
* THE EFFECT OF FORCING THE RESOLUTION OF A CURRENT         * 04/84 DBC
* MISMATCH (IF ANY) AT THE RESINCHRONIZATION POINT.         * 04/84 DBC
*                                                           * 04/84 DBC
* THE SYNC FILE IS OPTIONAL. IF IT IS AVAILABLE, THEN IS    * 04/84 DBC
* MUST BE CORRECTLY FORMED. IF IT IS NOT (I.E., IF IT IS    * 04/84 DBC
* OUT OF SEQUENCE, OR IF IT CONTAINS A CARD THAT DOES NOT   * 04/84 DBC
* APPEAR IN BOTH THE OLD AND NEW FILES), THEN THE CMPRSEQ   * 04/84 DBC
* RUN WILL FAIL.                                            * 04/84 DBC
         EJECT ,                                                   -EU-
* COMPLETION CODES                                          * 04/84 DBC
* ================                                          * 04/84 DBC
*                                                           * 04/84 DBC
*  0 - PROCESSING HAS COMPLETED SUCCESSFULLY. NO MISMATCHES * 04/84 DBC
*      HAVE BEEN FOUND.                                     * 04/84 DBC
*                                                           * 04/84 DBC
*  4 - PROCESSING HAS COMPLETED SUCCESSFULLY. AT LEAST ONE  * 04/84 DBC
*      MISMATCH HAS BEEN FOUND.                             * 04/84 DBC
*                                                           * 04/84 DBC
* 12 - PROCESSING HAS FAILED. A MEMORY SHORTAGE HAS         * 04/84 DBC
*      OCCURED.                                             * 04/84 DBC
*                                                           * 04/84 DBC
* 16 - PROCESSING HAS ABORTED. ONE OF THE COMPARISON FILES  * 04/84 DBC
*      (DDNAME OLD OR NEW) IS NOT AVAILABLE.                * 04/84 DBC
*                                                           * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 2                                              04/84 DBC
         MACRO
&N       #MSG  &MSG
         LCLC  &L,&M
&L       SETC  'L'''
&M       SETC  ''' '.'&MSG'(2,K'&MSG-1)
         DC    AL1(&L&N)
&N       DC    C&M
         MEND
         SPACE 1                                                   -EU-
         PRINT OFF                                                 -EU-
         COPY  CMPRSMAC                                            -EU-
         PRINT ON                                                  -EU-
         EJECT ,
CMPRSEQ  START 0
         SPACE 1
         #REGS R
         SPACE 1
PRTLEN   EQU   133                 PRINTER WIDTH
LINECT   EQU   60                  PAGE HEIGTH
         SPACE 2
         #PUT  SUBAD=PRINTER,MF=INIT
         EJECT ,
         PRINT NOGEN
         SPACE 1
JFCB     DSECT ,                   BASE IS INFMJFCB
         IEFJFCBN LIST=YES         JOB FILE CONTROL BLOCK
         SPACE 1
         DCBD  DSORG=PS            DATA CONTROL BLOCK              -EU-
         SPACE 1
DATA     DSECT
DATDCB   DS    (DCBLNGQS)X                                         -EU-
DATXLIST DS    A
DATSTART DS    A
DATEND   DS    A
DATDIF   DS    A
         SPACE 1
DATLIST  DS    0F
DLSDCB   DS    (DCBLNGQS)X                                         -EU-
DATLCT   DS    H
DATP#    DS    PL3
DATNXCCC DS    C
         DC    AL1(L'DATTIT)       L'TITLE LINE               04/84 DBC
DATTIT   DS    CL(PRTLEN)
DATNAME  EQU   DATTIT+1,3
DATDSN   EQU   DATTIT+7,L'JFCBDSNM+L'JFCBELNM+2
DATTITP# EQU   *-(2*L'DATP#),2*L'DATP#
         DS    CL(L'DATTITP#-2)
DATLEN   EQU   *-DATA              DATA AREA LENGTH           04/84 DBC
         SPACE 1
         PRINT GEN
         EJECT
CMPRSEQ  #ENTER BASES=3
         SPACE 1
         L     R1,0(,R1)
         CLC   2(4,R1),=C'FULL'
         BNE   NOFULL
         MVI   CMPRLEN,79
         SPACE 1
NOFULL   OPEN  (OLDDATA,,                                              *
               NEWDATA,,                                               *
               IGNLIST,,                                               *
               SYNCLIST,,                                              *
               PRTLIST,OUTPUT,                                         *
               OLDLIST,OUTPUT,                                         *
               NEWLIST,OUTPUT)
         SPACE 1
         MVC   OLDTITDS,OLDDSN
         MVC   NEWTITDS,NEWDSN
         SPACE 1
         MVC   CVDWA(1),PRTLIST+(DCBOFLGS-IHADCB)
         OC    CVDWA(1),OLDLIST+(DCBOFLGS-IHADCB)
         OC    CVDWA(1),NEWLIST+(DCBOFLGS-IHADCB)
         TM    CVDWA,DCBOFOPN
         #DIE  Z,'NO OUTPUT FILES AVAILABLE'
         SPACE 1
         LA    R1,MNOOLD-1
         TM    OLDDATA+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    TERM16
         LA    R1,MNONEW-1
         TM    NEWDATA+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    TERM16
         SPACE 1
         GETMAIN VU,LA=GETMQTY,A=GETMADR
         LM    R0,R2,ELBOW
         SR    R2,R0
         ST    R2,GETMADR+4
         AR    R1,R2
         ST    R1,GETMEND
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
         L     R3,GETMEND
         SH    R3,=H'240'
         L     R2,GETMADR
         SPACE 1
         ST    R2,IGNSTART
         TM    IGNLIST+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    IGNNONE
IGNGET2  LA    R1,MIGNCOR-1
         CR    R2,R3
         BH    TERM16
         GET   IGNLIST,(R2)
         LA    R2,80(,R2)
         B     IGNGET2
         SPACE 1
IGNEOD   CLOSE IGNLIST
         SPACE 1
IGNNONE  ST    R2,IGNEND
         SPACE 1
         ST    R2,OLDSTART
         ST    R2,OLDEND
         ST    R2,OLDDIF
         LA    R2,80(,R2)
         ST    R2,NEWSTART
         ST    R2,NEWEND
         ST    R2,NEWDIF
         SPACE 1
         XR    R6,R6
         XR    R7,R7
         TM    SYNCLIST+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    SNCEOD1
         GET   SYNCLIST
         LR    R6,R1
         SPACE 1
SNCEOD1  XR    R4,R4
         LA    R8,OLDDATA
         LA    R9,NEWDATA
         USING DATA,R9
         SPACE 1
GETLOOP  XR    R9,R8
         XR    R8,R9
         XR    R9,R8
         CR    R9,R7
         BE    GETLOOP
         L     R3,DATDIF
         C     R3,DATEND
         BL    NOGET
         TM    DATDCB+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    GETLOOP
         GET   DATDCB
         SPACE 1                                              04/84 DBC
DATEOD1  LA    R0,80(,R3)          RESUME HERE ON EOD         04/84 DBC
         C     R0,GETMEND
         BH    COREOVFL
         MVC   0(80,R3),0(R1)
         LA    R0,160(,R3)
         ST    R0,DATEND
NOGET    L     R2,DATSTART-DATA(,R8)
         IC    R4,CMPRLEN
CMPRLOOP C     R2,DATDIF-DATA(,R8)
         BNL   SAVE
         CLC   0(*-*,R3),0(R2)
         EX    R4,*-6
         BE    SAME
         LA    R2,160(,R2)
         B     CMPRLOOP
SAVE     LA    R0,160(,R3)
         ST    R0,DATDIF
         LTR   R6,R6
         BZ    GETLOOP
         LTR   R7,R7
         BNZ   GETLOOP
         CLC   0(*-*,R3),0(R6)
         EX    R4,*-6
         BNE   GETLOOP
         LR    R7,R9
         B     GETLOOP
         SPACE 1
SAME     CLC   DATDIF,DATSTART     IS THIS A NULL MISMATCH?   04/84 DBC
         BNE   DIFFS3
         ST    R2,DATDIF-DATA(,R8)
         B     NODIFFS
         SPACE 1                                              04/84 DBC
DIFFS3   L     R15,IGNSTART        LOAD THE IGNORE FILE       04/84 DBC
*                                  SCANNER                    04/84 DBC
IGNLP    C     R15,IGNEND
         BNL   DIFFS2
         CLC   0(*-*,R3),0(R15)
         EX    R4,*-6
         BE    SAVE
         LA    R15,80(,R15)
         B     IGNLP
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        THE MISSMATCH HAS BEEN TERMINATED. NOW I WILL SCAN * 04/84 DBC
*        THE MISSMATCHED BLOCK TO SEE IF IT CONTAINS A      * 04/84 DBC
*        MATCHED (BUT IGNORED) CARD THAT IS EQUIDISTANT     * 04/84 DBC
*        FROM THE START OF THE BLOCK. IF SO, THEN I WILL    * 04/84 DBC
*        TERMINATE THE MISSMATCH AT THE FIRST SUCH CARD     * 04/84 DBC
*        FOUND.                                             * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
DIFFS2   L     R14,DATSTART-DATA(,R8) LOAD THE OTHER FILE'S   04/84 DBC
*                                  RESCANNER                  04/84 DBC
         L     R15,DATSTART        LOAD THE CURRENT FILE'S    04/84 DBC
*                                  RESCANNER                  04/84 DBC
         LA    R0,160              LOAD THE RESCAN INCRIMENT  04/84 DBC
         SPACE 1                                              04/84 DBC
         CLC   0(*-*,R14),0(R15)   (EXECUTED)                 04/84 DBC
MTCHFSCN EX    R4,*-6              IS THIS A MACTH THAT IS    04/84 DBC
*                                  EQUIDISTANT FROM THE START 04/84 DBC
*                                  OF THE CURRENT MISMATCH?   04/84 DBC
         BE    MTCHTRNC            YES, TRUNCATE THE MISMATCH 04/84 DBC
         AR    R14,R0              NO, ADVANCE RESCANNER      04/84 DBC
         AR    R15,R0              ADVANCE THE RESCANNER      04/84 DBC
         CLR   R14,R2              BEYOND THE OTHER FILE'S    04/84 DBC
*                                  MISMATCH YET?              04/84 DBC
         BH    MTCHFSCZ            YES, STOP THIS SCAN        04/84 DBC
         CLR   R15,R3              NO, BEYOND THIS FILE'S     04/84 DBC
*                                  MISMATCH YET?              04/84 DBC
         BNH   MTCHFSCN            NO, CONTINUE THE SCAN      04/84 DBC
*                                  YES, STOP THIS SCAN        04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        NO EQUIDISTANT MATCH HAS BEEN FOUND ON THE FORWARD * 04/84 DBC
*        SCAN. SO NO I WILL BACK-SCAN THE BLOCK TO SEE IF I * 04/84 DBC
*        CAN FIND A MATCH THAT IS EQUIDISTANT FROM THE END  * 04/84 DBC
*        OF THE BLOCK. IF SO, THEN I WILL TRUNCATE THE      * 04/84 DBC
*        MISMATCH AT THE EARLIEST SUCH MATCH FOUND (I.E.,   * 04/84 DBC
*        THE ONE FOUND CLOSEST TO THE START OF THE BLOCK).  * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1
MTCHFSCZ LR    R14,R2              SAVE --> OTHER MATCHED CRD 04/84 DBC
         LR    R15,R3              SAVE PTR CRNT MATCHED CARD 04/84 DBC
         SPACE 1                                              04/84 DBC
MTCHBSCN SR    R2,R0               --> PRIOR OTHER CARD       04/84 DBC
         SR    R3,R0               --> PRIOR CRNT CARD        04/84 DBC
         C     R2,DATSTART-DATA(,R8)
         BL    MTCHTRNC            END OF OTHER FILE'S BLOCK  04/84 DBC
         C     R3,DATSTART
         BL    MTCHTRNC            END OF THIS FILE'S BLOCK   04/84 DBC
         CLC   0(*-*,R2),0(R3)
         EX    R4,*-6
         BNE   MTCHBSCN            NOT MATCHED; CONTINUE THE  04/84 DBC
*                                  BACK-SCAN                  04/84 DBC
         LR    R14,R2              AN EQUIDISTANT (FROM THE   04/84 DBC
*                                  END OF THE BLOCK) MATCHED  04/84 DBC
*                                  CARD HAS BEEN FOUND. SAVE  04/84 DBC
*                                  A PTR TO THE OTHER CARD.   04/84 DBC
         LR    R15,R3              SAVE PTR TO THE CRNT CARD  04/84 DBC
         B     MTCHBSCN            CONTINUE THE BACK-SCAN     04/84 DBC
         SPACE 1                                              04/84 DBC
MTCHTRNC LR    R2,R14              --> EARLIEST OTHER MATCHED 04/84 DBC
*                                  CARD THAT IS EQUIDISTANT   04/84 DBC
*                                  FROM THE END OF THE        04/84 DBC
*                                  MISSMATCHED BLOCK.         04/84 DBC
         LR    R3,R15              --> CORRESPONDING CRNT CRD 04/84 DBC
         SPACE 1                                              04/84 DBC
         ST    R2,DATDIF-DATA(,R8)
         ST    R3,DATDIF
         SPACE 1
         BAS   R10,PRTDIF
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        THE NEXT PAIR OF ACCEPTED MATCHED CARDS HAS BEEN   * 04/84 DBC
*        REACHED. IF THESE "CARDS" ARE THE EOD SIGNAL, THEN * 04/84 DBC
*        I AM ALL DONE.                                     * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
NODIFFS  L     R3,DATDIF           --> ONE OF THE MATCHED     04/84 DBC
*                                  CARDS (OF THE PAIR)        04/84 DBC
         CLC   0(80,R3),DATEOD1    IS IT THE EOD SIGNAL?      04/84 DBC
         BE    EOP                 YES, ALL DONE              04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        IF THE MATCHED PAIR MATCHES THE CURRENT SYNC CARD, * 04/84 DBC
*        THEN ADVANCE TO THE NEXT SYNC CARD.                * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
         LTR   R7,R7               HAS THE CURRENT SYNC CARD  04/84 DBC
*                                  BEEN REACHED YET BY ONE OF 04/84 DBC
*                                  THE FILES?                 04/84 DBC
         BZ    SYNCEOD2            NO, DON'T ADVANCE          04/84 DBC
         CLC   0(*-*,R3),0(R6)     (EXECUTED)                 04/84 DBC
         EX    R4,*-6              YES, DOES THE SYNC CARD    04/84 DBC
*                                  MATCH THE MATCHED PAIR?    04/84 DBC
         BNE   SYNCED              NO, DON'T ADVANCE          04/84 DBC
         GET   SYNCLIST            YES, ADVANCE TO THE NEXT   04/84 DBC
*                                  SYNC CARD, IF ANY          04/84 DBC
         LR    R6,R1               POINT TO IT                04/84 DBC
SYNCED   XR    R7,R7               INDICATE THAT NEITHER OF   04/84 DBC
*                                  THE FILES HAS YET REACHED  04/84 DBC
*                                  THE SYNC CARD              04/84 DBC
         SPACE 1                                              04/84 DBC
SYNCEOD2 BAS   R10,DATASHFT                                   04/84 DBC
         XR    R9,R8
         XR    R8,R9
         XR    R9,R8
         BAS   R10,DATASHFT
         B     GETLOOP
         SPACE 1
SNCEOD   CLOSE SYNCLIST
         XR    R6,R6
         LTR   R7,R7
         BZ    SNCEOD1
         XR    R7,R7
         B     SYNCEOD2
         SPACE 1
EOD      CLOSE ((R9))              CLOSE THE ENDED FILE       04/84 DBC
         SPACE 1                                              04/84 DBC
         LTR   R6,R6
         BZ    NOSYNC2
         CLOSE SYNCLIST
         XR    R6,R6
         XR    R7,R7
         SPACE 1                                              04/84 DBC
NOSYNC2  LA    R1,DATEOD1          POINT THE CARD-POINTER TO  04/84 DBC
*                                  THE EOD SIGNAL.            04/84 DBC
         BR    R1                  RESUME MAIN PROCESSING     04/84 DBC
         DROP  R9
         SPACE 2
         USING DATA,R9
DATASHFT L     R2,DATDIF
         TM    DLSDCB+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    DANODLS
         MVC   PRTCARD,0(R2)
         LA    R15,DLSDCB
         #PUT  PRTBUF
DANODLS  L     R1,DATSTART
         ST    R1,DATDIF
SHFTLOOP LA    R2,160(,R2)
         C     R2,DATEND
         BNL   SHFTDONE
         MVC   0(80,R1),0(R2)
         LA    R1,160(,R1)
         B     SHFTLOOP
SHFTDONE ST    R1,DATEND
         BR    R10
         DROP  R9
         EJECT
PRTDIF   MVI   RCD+1,4
         AP    COUNT,=P'1'
         MVC   CNTBUF,=X'40202020202060'
         #TEST SIZE=(L'CNTBUF,EQ,7)
         EDMK  CNTBUF(2*L'COUNT),COUNT
         #TEST SIZE=(L'CNTBUF,GE,2*L'COUNT)
         BCTR  R1,0
         MVI   0(R1),C'-'
         SPACE 1
         MVC   PRTCNT,CNTBUF
         MVC   PRTFRAM1,=C'---'
         MVC   PRTFRAM2,=C'---'
         MVI   NEXTCCC,C'-'
         LA    R3,OLDDATA
         USING DATA,R3
         SPACE 1
PRTLP1   MVC   PRTNAME1,DATNAME
         MVC   PRTNAME2,DATNAME
         L     R5,DATSTART
         SPACE 1
PRTLP2   C     R5,DATDIF
         BNL   PRTDONE2
         MVC   PRTCARD,0(R5)
         LA    R15,PRTLIST
         #PUT  PRTBUF
         MVI   NEXTCCC,C' '
         C     R5,DATSTART
         BNE   PRTNONUM
         MVC   PRTCNT,CNTBUF
PRTNONUM LA    R15,DLSDCB
         #PUT  PRTBUF
         MVC   PRTCNT,CNTBLANK
         LA    R5,160(,R5)
         B     PRTLP2
         SPACE 1
PRTDONE2 LA    R1,NEWDATA
         CR    R3,R1
         BE    PRTDONE
         LR    R3,R1
         MVI   PRTNAME1,C'-'
         MVC   PRTNAME1+1(PRTBUFZ-(PRTNAME1+1)),PRTNAME1
         LA    R15,PRTLIST
         #PUT  PRTBUF
         MVI   NEXTCCC,C' '
         SPACE 1
         MVC   PRTCNT,CNTBUF
         CLC   OLDSTART,OLDDIF
         BNE   NDSHOLD
         MVC   PRTNAME1,OLDNAME
         MVC   PRTNAME2,OLDNAME
         LA    R15,OLDLIST
         #PUT  PRTBUF
NDSHOLD  CLC   NEWSTART,NEWDIF
         BNE   NDSHNEW
         MVC   PRTNAME1,NEWNAME
         MVC   PRTNAME2,NEWNAME
         LA    R15,NEWLIST
         #PUT  PRTBUF
NDSHNEW  MVC   PRTCNT,CNTBLANK
         B     PRTLP1
         DROP  R3
         SPACE 1
PRTDONE  MVI   PRTBUF,C' '
         MVC   PRTBUF+1(PRTBUFLN-1),PRTBUF
         BR    R10
         EJECT ,
TERM16   LR    R2,R1
         MVI   NEXTCCC,C'-'
         LA    R1,MTERMER-1
         BAS   R14,BRODCAST
         MVI   NEXTCCC,C' '
         LR    R1,R2
         BAS   R14,BRODCAST
         MVI   RCD+1,16
         B     TERM
         SPACE 1
COREOVFL BAS   R10,PRTDIF
         LA    R1,MCOROFL-1
         B     TERM12
         SPACE 1
TERM12   MVI   NEXTCCC,C'-'
         BAS   R14,BRODCAST
         MVI   RCD+1,12
         B     TERM
         SPACE 1
EOP      ED    MCE(6),COUNT
         MVI   NEXTCCC,C'-'
         LA    R1,MCE-1
         BAS   R14,BRODCAST
         B     TERM
         SPACE 1
TERM     CLOSE (OLDDATA,,NEWDATA,,IGNLIST,,SYNCLIST,,                  *
               PRTLIST,,OLDLIST,,NEWLIST)
         SPACE 1
         LH    R15,RCD
         #EXIT ((R14,R12)),RC=(R15)
         EJECT ,
BRODCAST STM   R14,R15,SAVBRODC
         LA    R15,PRTLIST
         #PUT  (R1)
         LA    R15,OLDLIST
         #PUT  (R1)
         LA    R15,NEWLIST
         #PUT  (R1)
         LM    R14,R15,SAVBRODC
         BR    R14
         SPACE 2
         USING DATLIST,R15
PRINTER  TM    DLSDCB+(DCBOFLGS-IHADCB),DCBOFOPN
         BZR   R14
         STM   R14,R1,SAVPRINT
         MVC   DATNXCCC,NEXTCCC
         CLI   DATNXCCC,C'1'
         BE    PEJECT
         LA    R14,CCC+L'CCC
         LA    R0,L'CCC
PCCCSCAN BCTR  R14,0
         CLC   DATNXCCC,0(R14)
         BE    PCCCGOTN
         BCT   R0,PCCCSCAN
         MVI   DATNXCCC,C' '
         LA    R0,2
PCCCGOTN BCTR  R0,0
         LNR   R0,R0
         AH    R0,DATLCT
         BNM   PRINT
PEJECT   AP    DATP#,=P'1'
         MVC   DATTITP#+1(L'DATTITP#-1),=(L'DATTITP#-1)X'20'
         EDMK  DATTITP#,DATP#
         #TEST SIZE=(L'DATTITP#,EQ,2*L'DATP#)
         MVC   DATTITP#+1(L'DATTITP#-1),0(R1)
         LA    R1,DATTIT-1         --> TITLE LINE             04/84 DBC
         BAS   R14,PUTIT           GO PRINT IT                04/84 DBC
         DROP  R15
         LM    R14,R1,SAVPRINT
         USING DATLIST,R15
         MVI   DATNXCCC,C'-'
         LA    R0,LINECT-4
PRINT    STH   R0,DATLCT
         MVC   1(1,R1),DATNXCCC
         BAS   R14,PUTIT           PRINT THE MSG              04/84 DBC
         SPACE 1                                              04/84 DBC
         LM    R14,R1,SAVPRINT
         BR    R14
         DROP  R15                 RELEASE DATA AREA BASE     04/84 DBC
         EJECT ,                                                   -EU-
************************************************************* 04/84 DBC
*                                                           * 04/84 DBC
*        PUTIT -- THIS ROUTINE WRITES A LINE TO AN OUTPUT   * 04/84 DBC
*        DATASET. THE DATASET CAN BE ANY RECFM AND ANY      * 04/84 DBC
*        LRECL AND BLKSIZE THAT IS LARGE ENOUGH. NULL       * 04/84 DBC
*        RECORDS ARE EXPANDED TO BLANK LINES. ALL RECORDS   * 04/84 DBC
*        ARE EXPANDED TO A MINIMUM OF 2 BYTES.              * 04/84 DBC
*                                                           * 04/84 DBC
*        INPUTS:                                            * 04/84 DBC
*              - R1 POINTS TO A MESSAGE DESCRIPTOR.         * 04/84 DBC
*              - R14 POINTS TO THE RETURN ADDRESS.          * 04/84 DBC
*              - R15 POINTS TO THE OUTPUT DATA AREA WHICH   * 04/84 DBC
*                INCLUDES A DCB FOR THE OUTPUT DATASET.     * 04/84 DBC
*                                                           * 04/84 DBC
*        RETURN 0(,R14):                                    * 04/84 DBC
*              - THE MESSAGE IS SENT.                       * 04/84 DBC
*              - ALL REGISTERS ARE RESTORED.                * 04/84 DBC
*                                                           * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
         USING DATLIST,R15         DCL DATA AREA BASE         04/84 DBC
PUTIT    STM   R14,R3,SAVPUTIT     SAVE REGISTERS             04/84 DBC
         XR    R3,R3               CLEAR FOR INSERT           04/84 DBC
         IC    R3,0(,R1)           GET L'MSG                  04/84 DBC
         LA    R2,1(,R1)           --> MSG                    04/84 DBC
         SPACE 1                                              04/84 DBC
         TM    DLSDCB+(DCBRECFM-IHADCB),DCBRECU   RECFM=U?    04/84 DBC
         BNM   PUTU                YES, GO HANDLE             04/84 DBC
         TM    DLSDCB+(DCBRECFM-IHADCB),DCBRECF   NO,RECFM=F? 04/84 DBC
         BNZ   PUTF                YES, GO HANDLE             04/84 DBC
*        FALL  THROUGH             NO, RECFM=V                04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        HANDLE RECFM=V                                     * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
PUTV     LA    R0,4(,R3)           GET THE NEEDED LRECL       04/84 DBC
         CH    R0,=H'6'            ABOVE MINIMUMS?            04/84 DBC
         BNL   PUTVLOK1            YES, PROCEED               04/84 DBC
         LA    R0,6                NO, SET MINIMUM            04/84 DBC
         SPACE 1                                              04/84 DBC
PUTVLOK1 LA    R14,8(,R3)          GET MIN BLKSIZE NEEDED     04/84 DBC
         CH    R14,DLSDCB+(DCBBLKSI-IHADCB) RECORD TOO LARGE? 04/84 DBC
         BNH   PUTVLOK2            NO, PROCEED                04/84 DBC
         LH    R0,DLSDCB+(DCBBLKSI-IHADCB)   YES, GET BLKSIZE 04/84 DBC
         SH    R0,=H'4'            SET MAX POSSIBLE LRECL     04/84 DBC
         SPACE 1                                              04/84 DBC
PUTVLOK2 STH   R0,DLSDCB+(DCBLRECL-IHADCB)   LRECL FOR QSAM   04/84 DBC
         PUT   DLSDCB              LOCATE NEXT RECORD BUFFER  04/84 DBC
         DROP  R15                 RELEASE DATA AREA BASE     04/84 DBC
         L     R15,SAVPUTIT+4      RESTORE DATA AREA PTR      04/84 DBC
         USING DATLIST,R15         REDECLARE DATA AREA BASE   04/84 DBC
         SPACE 1                                              04/84 DBC
         MVC   0(2,R1),DLSDCB+(DCBLRECL-IHADCB)   COPY LRECL  04/84 DBC
         XC    2(2,R1),2(R1)       CLEAR REST OF RCW          04/84 DBC
         LA    R0,4(,R1)           --> DATA SECTION OF RECORD 04/84 DBC
         LH    R1,DLSDCB+(DCBLRECL-IHADCB)   GET LRECL        04/84 DBC
         SH    R1,=H'4'            GET L'DATA                 04/84 DBC
         B     PUTMOVE             GO MOVE DATA TO RECORD     04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        HANDLE RECFM=F                                     * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
PUTF     PUT   DLSDCB              LOCATE NEXT RECORD BUFFER  04/84 DBC
         DROP  R15                 RELEASE DATA AREA BASE     04/84 DBC
         L     R15,SAVPUTIT+4      RESTORE DATA AREA PTR      04/84 DBC
         USING DATLIST,R15         REDECLARE DATA AREA BASE   04/84 DBC
         SPACE 1                                              04/84 DBC
         LR    R0,R1               COPY BUFFER PTR            04/84 DBC
         LH    R1,DLSDCB+(DCBLRECL-IHADCB)   GET L'BUFFER     04/84 DBC
         B     PUTMOVE             GO MOVE DATA TO RECORD     04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        HANDLE RECFM=U                                     * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
PUTU     LR    R0,R3               GET DESIRED LRECL          04/84 DBC
         CH    R0,=H'2'            BIG ENOUGH?                04/84 DBC
         BNL   PUTULOK1            YES, PROCEED               04/84 DBC
         LA    R0,2                NO, LOAD MINIMUM           04/84 DBC
         SPACE 1                                              04/84 DBC
PUTULOK1 CH    R0,DLSDCB+(DCBBLKSI-IHADCB)   TOO BIG?         04/84 DBC
         BNH   PUTULOK2            NO, PROCEED                04/84 DBC
         LH    R0,DLSDCB+(DCBBLKSI-IHADCB)   YES, LOAD MAX    04/84 DBC
         SPACE 1                                              04/84 DBC
PUTULOK2 STH   R0,DLSDCB+(DCBLRECL-IHADCB)   STORE BUF LENGTH 04/84 DBC
         PUT   DLSDCB              LOCATE NEXT RECORD BUFFER  04/84 DBC
         DROP  R15                 RELEASE DATA AREA BASE     04/84 DBC
         L     R15,SAVPUTIT+4      RESTORE DATA AREA PTR      04/84 DBC
         USING DATLIST,R15         REDECLARE DATA AREA BASE   04/84 DBC
         SPACE 1                                              04/84 DBC
         LR    R0,R1               COPY BUFFER POINTER        04/84 DBC
         LH    R1,DLSDCB+(DCBLRECL-IHADCB)   GET L'DATA       04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        COPY THE DATA TO THE I/O BUFFER. IF NECESSARY,     * 04/84 DBC
*        THEN TRUNCATE OR PAD (WITH BLANKS).                * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
PUTMOVE  ICM   R3,B'1000',=C' '    GET PAD CHARACTER          04/84 DBC
         MVCL  R0,R2               COPY THE RECORD WITH       04/84 DBC
*                                  TRUNCATION OR PADDING      04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        RETURN TO CALLER                                   * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
         LM    R14,R3,SAVPUTIT     RESTORE REGISTERS          04/84 DBC
         BR    R14                 RETURN TO CALLER           04/84 DBC
         DROP  R15                 RELEASE DATA AREA BASE     04/84 DBC
         SPACE 2                                                   -EU-
         USING DATDCB,R1
OPNEXIT  L     R3,DATDCB+(DCBDEBAD-IHADCB)
         USING INFMJFCB-100,R3
         MVC   DATDSN,DATDSN-1
         MVC   DATDSN(L'JFCBDSNM),JFCBDSNM
         CLI   JFCBELNM,C' '
         BE    DATEXIT
         LA    R2,DATDSN+L'JFCBDSNM
BLNKLP1  BCTR  R2,0
         CLI   0(R2),C' '
         BE    BLNKLP1
         MVI   1(R2),C'('
         MVC   2(L'JFCBELNM,R2),JFCBELNM
         LA    R2,L'JFCBELNM+2(,R2)
BLNKLP2  BCTR  R2,0
         CLI   0(R2),C' '
         BE    BLNKLP2
         MVI   1(R2),C')'
         DROP  R3
         SPACE 1                                              04/84 DBC
DATEXIT  LH    R3,DATDCB+(DCBBLKSI-IHADCB)
         LH    R4,DATDCB+(DCBLRECL-IHADCB)
         XR    R2,R2
         DR    R2,R4
         MR    R2,R4
         CR    R3,R4
         BH    SETBLKSI
         LR    R3,R4
         NI    DATDCB+(DCBRECFM-IHADCB),255-DCBRECBR
SETBLKSI STH   R3,DATDCB+(DCBBLKSI-IHADCB)
         STH   R3,DATDCB+(DCBBUFL-IHADCB)
         BR    R14
         DROP  R1
         EJECT ,                                                   -EU-
************************************************************* 04/84 DBC
*        PRTEXIT -- THIS ROUTINE SETS DEFAULT DCB VALUES    * 04/84 DBC
*        AND ENFORCES CONSTRAINTS FOR THE LISTING FILES.    * 04/84 DBC
*                                                           * 04/84 DBC
*        INPUTS:                                            * 04/84 DBC
*              - R1 POINTS TO THE PRTLIST, OLDLIST, OR      * 04/84 DBC
*                NEWLIST DCB.                               * 04/84 DBC
*              - R14 POINTS TO A RETURN ADDRESS (SVC 3).    * 04/84 DBC
*              - R15 POINTS TO THIS ROUTINE.                * 04/84 DBC
*                                                           * 04/84 DBC
*        RETURN 0(,R14):                                    * 04/84 DBC
*              - NO REGISTERS ARE RESTORED.                 * 04/84 DBC
*              - DCBRECFM MAY BE ADJUSTED.                  * 04/84 DBC
*              - DCBLRECL MAY BE ADJUSTED.                  * 04/84 DBC
*              - DCBBLKSI MAY BE ADJUSTED.                  * 04/84 DBC
*                                                           * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
         USING PRTEXIT,R15         DCL LOCAL BASE             04/84 DBC
         USING IHADCB,R1           DCB BASE                   04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        DETERMINE THE DESIRED RECFM.                       * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
PRTEXIT  NI    DCBRECFM,DCBRECU+DCBRECCA CLEAR POTENTIAL INVA 04/84 DBC
         #TEST SIZE=(DCBRECU,EQ,DCBRECF+DCBRECV)              04/84 DBC
         SPACE 1                                              04/84 DBC
         TM    DCBRECFM,DCBRECU    RECFM=U?                   04/84 DBC
         BO    PXRFMU              YES, GO HANDLE             04/84 DBC
         BM    PXRFMFV             NO, RECFM=F OR =V; GO HAND 04/84 DBC
         OI    DCBRECFM,DCBRECV+DCBRECCA NO, NOT SET; SET     04/84 DBC
*                                  RECFM=VA                   04/84 DBC
         SPACE 1                                              04/84 DBC
PXRFMFV  TM    DCBRECFM,DCBRECF    RECFM=F?                   04/84 DBC
         BNZ   PXRFMF              YES, GO HANDLE             04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        RECFM=V; EXAMINE AND SET RECFM, LRECL, AND         * 04/84 DBC
*        BLKSIZE.                                           * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
         LA    R4,PRTLEN+4         GET REQUIRED LRECL         04/84 DBC
         STH   R4,DCBLRECL         STORE                      04/84 DBC
         LA    R4,4(,R4)           GET MIN REQUIRED BLKSIZE   04/84 DBC
         LH    R3,DCBBLKSI         GET GIVEN BLKSIZE          04/84 DBC
         CR    R3,R4               ARE RECORDS UNBLOCKED?     04/84 DBC
         BER   R14                 YES, LEAVE "UNBLOCKED RCDS 04/84 DBC
         OI    DCBRECFM,DCBRECBR   NO, SET "BLOCKED RCDS" SIG 04/84 DBC
         CR    R3,R4               WAS A VALID BLKSIZE GIVEN? 04/84 DBC
         BHR   R14                 YES, RETURN TO OPEN        04/84 DBC
         MVC   DCBBLKSI,=Y(4096)   NO, SET DEFAULT BLKSIZE    04/84 DBC
         BR    R14                 RETURN TO OPEN             04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        RECFM=F; EXAMINE AND SET RECFM, LRECL, AND         * 04/84 DBC
*        BLKSIZE.                                           * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
PXRFMF   LA    R4,PRTLEN           GET REQUIRED LRECL         04/84 DBC
         STH   R4,DCBLRECL         STORE                      04/84 DBC
         LH    R3,DCBBLKSI         GET GIVEN BLKSIZE          04/84 DBC
         XR    R2,R2               CLEAR FOR DIVIDE           04/84 DBC
         DR    R2,R4               GET BLOCKING FACTOR        04/84 DBC
         MR    R2,R4               GET POSSIBLY VALID BLKSIZE 04/84 DBC
         STH   R3,DCBBLKSI         STORE                      04/84 DBC
         CR    R3,R4               ARE RECORDS UNBLOCKED?     04/84 DBC
         BER   R14                 YES, RETURN TO OPEN        04/84 DBC
         OI    DCBRECFM,DCBRECBR   NO, SET "BLOCKED RECORDS"  04/84 DBC
         CR    R3,R4               WAS A BLKSIZE GIVEN?       04/84 DBC
         BHR   R14                 YES, RETURN TO CALLER      04/84 DBC
         MVC   DCBBLKSI,=Y(4096/PRTLEN*PRTLEN) NO, SET DEFAUL 04/84 DBC
         BR    R14                 RETURN TO CALLER           04/84 DBC
         SPACE 1                                              04/84 DBC
************************************************************* 04/84 DBC
*        RECFM=U; SET LRECL AND BLKSIZE.                    * 04/84 DBC
************************************************************* 04/84 DBC
         SPACE 1                                              04/84 DBC
PXRFMU   LA    R3,PRTLEN           LOAD REQURIED LRECL        04/84 DBC
         STH   R3,DCBLRECL         STORE                      04/84 DBC
         STH   R3,DCBBLKSI         SET BLKSIZE=LRECL          04/84 DBC
         BR    R14                 RETURN TO OPEN             04/84 DBC
         DROP  R1                  RELEASE DCB BASE           04/84 DBC
         DROP  R15                 RELEASE LOCAL BASE         04/84 DBC
         EJECT ,                                                   -EU-
         PRINT NOGEN
         SPACE 1
OLDDATA  DCB   DDNAME=OLD,DSORG=PS,MACRF=GL,RECFM=FB,LRECL=80,         *
               OPTCD=C,EXLST=OLDXLIST,EODAD=EOD,EROPT=ACC
OLDXLIST DC    0F'0',X'85',AL3(OPNEXIT)
OLDSTART DS    A
OLDEND   DS    A
OLDDIF   DS    A
         SPACE 1
OLDLIST  DCB   DDNAME=OLDLIST,                                04/84 DBC*
               DSORG=PS,                                      04/84 DBC*
               MACRF=PL,                                      04/84 DBC*
               OPTCD=C,                                       04/84 DBC*
               EROPT=ACC,                                     04/84 DBC*
               EXLST=PRTXLIST                                 04/84 DBC
OLDLCT   DC    H'0'
OLDP#    DC    PL3'0'
OLDNXCCC DS    C
         DC    AL1(L'OLDTIT)                                  04/84 DBC
OLDTIT   DS    0CL(PRTLEN)
         DC    C'1'
OLDNAME  DC    C'OLD',C' - '
OLDDSN   DC    CL(L'JFCBDSNM+L'JFCBELNM+2)' '
         DC    CL(L'OLDTIT-L'DATTITP#-4-(*-OLDTIT))' ',C'PAGE'
OLDTITP# DC    CL(L'DATTITP#)' '
         #TEST SIZE=(*-OLDTIT,EQ,L'OLDTIT)
         DC    CL(L'OLDTITP#-2)' '
         #TEST SIZE=(*-OLDDATA,EQ,DATLEN)                     04/84 DBC
         EJECT
NEWDATA  DCB   DDNAME=NEW,DSORG=PS,MACRF=GL,RECFM=FB,LRECL=80,         *
               OPTCD=C,EXLST=NEWXLIST,EODAD=EOD,EROPT=ACC
NEWXLIST DC    0F'0',X'85',AL3(OPNEXIT)
NEWSTART DS    A
NEWEND   DS    A
NEWDIF   DS    A
         SPACE 1
NEWLIST  DCB   DDNAME=NEWLIST,                                04/84 DBC*
               DSORG=PS,                                      04/84 DBC*
               MACRF=PL,                                      04/84 DBC*
               OPTCD=C,                                       04/84 DBC*
               EROPT=ACC,                                     04/84 DBC*
               EXLST=PRTXLIST                                 04/84 DBC
NEWLCT   DC    H'0'
NEWP#    DC    PL3'0'
NEWNXCCC DS    C
         DC    AL1(L'NEWTIT)                                  04/84 DBC
NEWTIT   DS    0CL(PRTLEN)
         DC    C'1'
NEWNAME  DC    C'NEW',C' - '
NEWDSN   DC    CL(L'JFCBDSNM+L'JFCBELNM+2)' '
         DC    CL(L'NEWTIT-L'DATTITP#-4-(*-NEWTIT))' ',C'PAGE'
NEWTITP# DC    CL(L'DATTITP#)' '
         #TEST SIZE=(*-NEWTIT,EQ,L'NEWTIT)
         DC    CL(L'NEWTITP#-2)' '
         #TEST SIZE=(*-NEWDATA,EQ,DATLEN)                     04/84 DBC
         EJECT ,
PRTLIST  DCB   DDNAME=SYSPRINT,                               04/84 DBC*
               DSORG=PS,                                      04/84 DBC*
               MACRF=PL,                                      04/84 DBC*
               OPTCD=C,                                       04/84 DBC*
               EROPT=ACC,                                     04/84 DBC*
               EXLST=PRTXLIST                                 04/84 DBC
PRTLCT   DC    H'0'
PRTP#    DC    PL3'0'
PRTNXCCC DS    C
         DC    AL1(L'PRTTIT)                                  04/84 DBC
PRTTIT   DS    0CL(PRTLEN)
         DC    C'1OLD - '
OLDTITDS DC    CL(L'OLDDSN)' ',C' NEW - '
NEWTITDS DC    CL(L'NEWDSN)' '
         DC    CL(L'PRTTIT-L'DATTITP#-4-(*-PRTTIT))' ',C'PAGE'
PRTTITP# DC    CL(L'DATTITP#)' '
         #TEST SIZE=(*-PRTTIT,EQ,L'PRTTIT)
         DC    CL(L'DATTITP#-2)' '
         #TEST SIZE=(*-PRTLIST+(DLSDCB-DATA),EQ,DATLEN)       04/84 DBC
         SPACE 1
IGNLIST  DCB   DDNAME=IGNORE,DSORG=PS,MACRF=GM,RECFM=FB,LRECL=80,      *
               OPTCD=C,EXLST=IGNXLIST,EODAD=IGNEOD,EROPT=ACC
IGNXLIST DC    0F'0',X'85',AL3(DATEXIT)
IGNSTART DS    A
IGNEND   DS    A
         #TEST SIZE=(*-IGNLIST,EQ,DATEND+L'DATEND-DATA)
         SPACE 1
SYNCLIST DCB   DDNAME=SYNC,DSORG=PS,MACRF=GL,RECFM=FB,LRECL=80,        *
               OPTCD=C,EXLST=SNCXLIST,EODAD=SNCEOD,EROPT=ACC
SNCXLIST DC    0F'0',X'85',AL3(DATEXIT)
         #TEST SIZE=(*-SYNCLIST,EQ,DATXLIST+L'DATXLIST-DATA)  04/84 DBC
         SPACE 1
         PRINT GEN
         EJECT
CVDWA    DC    D'0'
RCD      DC    Y(0)
COUNT    DC    PL3'0'
CCC      DC    C'+ 0-'
NEXTCCC  DC    C' '
CMPRLEN  DC    AL1(71)
         SPACE 1
SAVBRODC DC    2A(0)
SAVPRINT DC    4A(0)
SAVPUTIT DC    6A(0)               PUTIT                      04/84 DBC
         SPACE 1
GETMQTY  DC    A(5*1024,X'800000')
ELBOW    DC    A(4*1024)
GETMADR  DC    2A(0)
GETMEND  DS    A
         SPACE 1                                              04/84 DBC
PRTXLIST DS    0F                  PRINT FILES EXIT LIST      04/84 DBC
         DC    B'10000101',AL3(PRTEXIT) DCB OPEN EXIT         04/84 DBC
         SPACE 1
         DC    AL1(PRTBUFLN)
PRTBUF   DS    C
PRTCNT   DS    C'-#####-'
PRTNAME1 DS    C'OLD'
PRTFRAM1 DS    C'---'
PRTCARD  DS    CL80
PRTFRAM2 DS    C'---'
PRTNAME2 DS    C'OLD'
PRTBUFZ  EQU   *
PRTBUFLN EQU   PRTBUFZ-PRTBUF
         SPACE 1
         ORG   PRTBUF
         DC    CL(PRTBUFLN)' '
         SPACE 1
CNTBUF   DS    CL(L'PRTCNT)
CNTBLANK DC    CL(L'PRTCNT)' '
         SPACE 2
         LTORG
         EJECT ,                                                   -EU-
MTERMER  #MSG  '*** TERMINAL ERROR ***'
         SPACE 1
MNOOLD   #MSG  '*** OLD FILE NOT AVAILABLE ***'
         SPACE 1
MNONEW   #MSG  '*** NEW FILE NOT AVAILABLE ***'
         SPACE 1
MIGNCOR  #MSG  '*** INSUFFICIENT MEMORY TO HOLD THE IGNORE FILE ***'
         SPACE 1
MCOROFL  #MSG  '*** MEMORY OVERFLOW - COMPARE ABANDONED ***'
         SPACE 1
MCE      #MSG  ' BLOCKS OF COMPARE ERRORS FOUND'
         SPACE 2
         END   ,                                              04/84 DBC
