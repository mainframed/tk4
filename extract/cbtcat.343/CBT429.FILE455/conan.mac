OCAF     TITLE 'OPERATOR CONSOLE ANALYSIS FACILITY.'
CONAN    START 0
         SPACE 1
* FUNCTION :   PROGRAM TO DISPLAY AT SYSTEM CONSOLE USEFULL FUNCTIONS
* ----------   ANALYSIS FACILITIES (ONLY STARTED TASK RUNNING).
*
*              A FUNCTION RECEIVES CONTROL AS FOLLOWS :
*              R0 : ADDRESS OF TSO FLAG.
*              R1 ---> PARML  DC  H'...L'PARMT...'
*                      PARMT  DC  CL70'...EVENTUAL PARM. TEXT...'
*              R14 : LINK REGISTER.
*              R15 : FUNCTION ENTRY POINT ADDRESS.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
LV10     EQU   10        AUTHORIZED LEVEL.
         SPACE 1
        $MDL@IX
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R10,R13             SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R10
         MVC   EXTR(LEXTR),EXTRP
         XC    ACOMM,ACOMM
        EXTRACT ACOMM,'S',FIELDS=(COMM,TSO),MF=(E,EXTR)
         L     R1,ATSO
         ST    R1,SENDP
         TM    0(R1),X'80'
         BZ    NOTTSO              NOT TSO ADDRESS SPACE.
         XC    REQAUT(REQAUTL),REQAUT
         MVC   REQAUT+1(3),=AL3(MDL@IX)
         LA    R1,AUTH
         ST    R1,REQAUT+4
        $EACM  REQAUT
         CLI   AUTH,LV10           AUTHORIZED?
         BL    NOTALL              NO, NOT AUTHORIZED.
         B     SETUP
NOTTSO   L     R1,ACOMM            COMMUNICATION AREA ADDRESS.
         L     R2,4(R1)            CIB ADDRESS.
         LA    R2,0(R2)            CLEAR.
         LTR   R2,R2               CURRENT CIB SET UP?
         BZ    NOTALL              NO, NOT STARTED TASK.
         USING CIBDSECT,R2
         CLI   CIBVERB,CIBSTART    START COMMAND?
         BE    SETUP               YES, STARTED TASK.
         DROP  R2
NOTALL   LA    R15,NTALLM
         LA    R14,FINISH
         B     SENDL
SETUP    MVC   INQRY(LINQRY),INQRYP     AREAS INITIALIZATION.
         LA    R1,ANSR
         ST    R1,INQRY
         LA    R1,L'ANSR
         STC   R1,INQRY
         LA    R1,ECBR
         ST    R1,INQRY+4
         LA    R1,L'PARMT
         STH   R1,PARML
         LA    R15,INITM           SEND STARTING MESSAGES.
         BAS   R14,SENDL
         LA    R15,INITH
         BAS   R14,SENDL
         SPACE 1
REQST    XC    ECBR,ECBR
         MVC   ANSR,BLANKS
         LA    R15,INQRY
         BAS   R14,SENDL           ENTER A REQUEST.
         LTR   R15,R15             TEST TGET RC.
         BNZ   ERRTG               THERE IS AN ERROR.
         OC    ANSR,BLANKS
         LA    R3,ANSR
         LA    R4,1
         LA    R5,ANSR+L'ANSR-1
SCAN     CLI   0(R3),C' '          SEARCH START OF REQUEST FUNCTION.
         BNE   FOUND
         BXLE  R3,R4,SCAN
         B     REPEAT              JUST REPEAT LAST ONE.
FOUND    LR    R1,R3
         BXLE  R3,R4,*+L'*+4
         B     *+L'*+8
         CLI   0(R3),C' '          SEARCH END OF REQUEST FUNCTION.
         BNE   FOUND+L'FOUND
         LR    R2,R3
         SR    R2,R1
         CH    R2,=H'8'
         BH    INVLD
         MVC   FUNCT(L'FUNCT),BLANKS
         BCT   R2,*+L'*+6
         MVC   FUNCT(*-*),0(R1)
         EX    R2,*-6
         MVC   PARMT(L'PARMT),BLANKS    SEARCH EVENTUAL PARM.
LOOP     BXLE  R3,R4,*+L'*+4
         B     REPEAT              NO PARM.
         CLI   0(R3),C' '
         BE    LOOP
         SR    R5,R3
         MVC   PARMT(*-*),0(R3)
         EX    R5,*-6
REPEAT   L     R2,=A(FUNTB)        SELECT REQUESTED FUNCTION.
         CLC   0(L'FUNCT,R2),FUNCT
         BE    GOTO
         CLC   L'FUNCT(L'ALIAS,R2),FUNCT
         BE    GOTO
         LA    R2,L'FUNTB(R2)
         CLI   0(R2),0
         BNE   REPEAT+L'REPEAT
         LA    R15,UNKFM
         LA    R14,REQST
         B     SENDL
INVLD    LA    R15,INVLDM
         LA    R14,REQST
         B     SENDL
GOTO     L     R15,12(R2)          ENTER FUNCTION.
         LA    R1,PARML
         L     R0,ATSO
        CALL   (15)
         B     REQST
         SPACE 1
XEQ      ST    R1,PARMP            LOAD/EXECUTE/DELETE.
         LR    R0,R2
        LOAD   EPLOC=(0)
         LR    R15,R0
         LA    R1,PARMP
         L     R0,ATSO
        CALL   (15)
         LR    R0,R2
        DELETE EPLOC=(0)
         B     REQST
         SPACE 1
HELP     L     R15,=A(HELP00)      HELP DISPLAYS.
         BAS   R14,SENDL
         L     R15,=A(HELP01)
         BAS   R14,SENDL
         L     R15,=A(HELP02)
         BAS   R14,SENDL
         L     R15,=A(HELP03)
         BAS   R14,SENDL
         L     R15,=A(HELP04)
         BAS   R14,SENDL
         L     R15,=A(HELP90)
         BAS   R14,SENDL
         L     R15,=A(HELP99)
         BAS   R14,SENDL
         B     REQST
         SPACE 1
FINISH  $XRET  CC=0,LV=WRKL,TYPE=RENT
         SPACE 1
ERRTG    MVC   ANSR(22),=CL22'TGET ERROR (R15 : XX).'
         STC   R15,ANSR+19
         SRL   R15,4
         STC   R15,ANSR+18
         NC    ANSR+18(2),=XL2'0F0F'
         TR    ANSR+18(2),=CL16'0123456789ABCDEF'
        TPUT   ANSR,22
         B     FINISH
         SPACE 1
SENDL    LA    R1,SENDP            BUILD PARM. LIST.
         ST    R15,4(R1)
         L     R15,=V(SENDRTN)
         BR    R15                 GO TO DISPLAY THIS LINE.
         DROP  R10
         EJECT
EXTRP   EXTRACT *-*,'S',MF=L       EXTRACT PARM. LIST.
INQRYP  WTOR   'ENTER FUNCTION :',*-*,*-*,*-*,ROUTCDE=2,DESC=4,MF=L
        PRINT  NOGEN
INITM   WTO    '    --- OPERATOR CONSOLE ANALYSIS FACILITY ---',       X
               ROUTCDE=11,DESC=7,MF=L
INITH   WTO    '    ENTER H TO OBTAIN COMMANDS SUMMARY',               X
               ROUTCDE=11,DESC=7,MF=L
NTALLM  WTO    'YOU ARE NOT ALLOWED TO CALL THIS PROGRAM.',            X
               ROUTCDE=11,DESC=7,MF=L
UNKFM   WTO    'UNKNOWN FUNCTION SPECIFIED - REQUEST IGNORED.',        X
               ROUTCDE=2,DESC=4,MF=L
INVLDM  WTO    'INVALID FUNCTION SPECIFIED - REQUEST IGNORED.',        X
               ROUTCDE=2,DESC=4,MF=L
        PRINT  GEN
         SPACE 1
BLANKS   DC    CL72' '
         SPACE 2
        LTORG
         SPACE 2
*--- END OF BASE REGISTERS ADDRESSING TAKE PLACE AT THIS POINT ---
         EJECT
*        -------------------------------------
*        FUNCTIONS TABLE (NAME,ALIAS,ADDRESS).
*        -------------------------------------
         SPACE 1
         DS    0F
FUNTB    DS    0CL16
         DC    CL8'ENQRSV',CL4'R',V(ENQRSV)
         DC    CL8'ENQCNF',CL4'C',V(ENQCNF)
         DC    CL8'USERS',CL4'U',A(XEQ)
         DC    CL8'JOBS',CL4'J',A(XEQ)
         DC    CL8'HELP',CL4'H',A(HELP)
         DC    CL8'END',CL4'E',A(FINISH)
         DC    AL1(0)
         SPACE 2
*        --------------------
*        HELP MESSAGES TABLE.
*        --------------------
         SPACE 1
        PRINT  NOGEN
HELP00  WTO    'COMMANDS SUMMMARY :',ROUTCDE=2,DESC=4,MF=L
HELP01  WTO    '  ENQRSV  /R    - DISPLAY RESERVE RESOURCES ENQ''D',   X
               ROUTCDE=2,DESC=4,MF=L
HELP02  WTO    '  ENQCNF  /C    - DISPLAY DATA-SETS ENQ''S CONFLICTS', X
               ROUTCDE=2,DESC=4,MF=L
HELP03  WTO    '  USERS   /U    - SHOW TSO USERS-ASID/UNIT ADDRESS',   X
               ROUTCDE=2,DESC=4,MF=L
HELP04  WTO    '  JOBS    /J    - SHOW EXEC JOBS AND STARTED TASKS',   X
               ROUTCDE=2,DESC=4,MF=L
HELP90  WTO    '  HELP    /H    - DISPLAY THIS HELP',                  X
               ROUTCDE=2,DESC=4,MF=L
HELP99  WTO    '  END     /E    - TERMINATE',                          X
               ROUTCDE=2,DESC=4,MF=L
        PRINT  GEN
         EJECT
WRKDSECT DSECT                     WORK AREAS DESCRIPTIONS.
         SPACE 1
SVA      DS    18F                 SAVE AREA.
         SPACE 1
EXTR    EXTRACT *-*,'S',MF=L       EXTRACT PARM. LIST.
LEXTR    EQU   *-EXTR
ACOMM    DS    A
ATSO     DS    A
         SPACE 1
INQRY   WTOR   'ENTER FUNCTION :',*-*,*-*,*-*,ROUTCDE=2,DESC=4,MF=L
LINQRY   EQU   *-INQRY
         SPACE 1
ECBR     DS    F
SENDP    DS    2F
ANSR     DS    CL72
PARMP    DS    F
PARML    DS    H
PARMT    DS    CL70
ALIAS    DS    0CL4
FUNCT    DS    CL8
         SPACE 1
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(*-*)
AUTH     DC    XL1'0'
REQAUTL  EQU   *-REQAUT
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 2
        PRINT  NOGEN
CIBDSECT DSECT
CIB     IEZCIB
         SPACE 2
         END
