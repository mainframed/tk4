SNDR     TITLE 'I/O ROUTINE (WTO/WTOR OR TPUT/TGET).'
SENDRTN  START 0
         SPACE 1
* FUNCTION :   ROUTINE TO AUTOMATICALLY TRANSFORM THE WTO/WTOR INTO
* ----------   TPUT/TGET WHEN RUNNING IN TSO ENVIRONMENT.
*
*              AT ENTRY : THE REGISTER 1 MUST POINT TO A PARAMETER
*                         LIST AS FOLLOWS.
*                         R1 ---> +0 : A(ADDRESS TSO FLAG)
*                                 +4 : A(WTO/WTOR PARM. LIST)
*              AT EXIT : R15 CONTAINS TGET RC WHEN SIMULATE WTOR,
*                        IN ALL OTHER CASES IT CONTAINS ZERO.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
         EJECT
        $XENT  BASE=R12,TYPE=RENT
         LM    R2,R3,0(R1)
         SR    R10,R10             RC = 0
         TM    0(R2),X'80'         ARE WE RUNNING IN TSO ?
         BO    STSO                YES.
         CLI   0(R3),0             REPLY?
         BNE   SENDR               YES.
        WTO    MF=(E,(R3))
         B     FINISH
SENDR    L     R4,4(R3)
         MVC   0(4,R4),=F'0'
         SR    R14,R14
         SR    R15,R15
         CLI   8(R3),0             WHICH WTOR LIST VERSION ?
         BE    SWOLD
         IC    R14,8(R3)           NEW, MVS/XA
         L     R15,0(R3)
         B     SWTOR
SWOLD    IC    R14,0(R3)           OLD, MVS/370
         ICM   R15,B'0111',1(R3)
SWTOR    BCTR  R14,0
         MVI   0(R15),C' '
         EX    R14,SCLR
        WTOR   MF=(E,(R3))
        WAIT   ECB=(R4)
         B     FINISH
STSO     CLI   0(R3),0             REPLY?
         BNE   STSOR               YES.
         LH    R0,0(R3)
         SH    R0,=H'4'
         LA    R1,4(R3)
        TPUT   (1),(0)
         B     FINISH
STSOR    SR    R0,R0
         IC    R0,9(R3)
         SH    R0,=H'4'
         LA    R1,12(R3)
        TPUT   (1),(0)
STBK     SR    R14,R14
         SR    R15,R15
         CLI   8(R3),0             WHICH WTOR LIST VERSION ?
         BE    STOLD
         IC    R14,8(R3)           NEW, MVS/XA
         L     R15,0(R3)
         B     STWTR
STOLD    IC    R14,0(R3)           OLD, MVS/370
         ICM   R15,B'0111',1(R3)
STWTR    BCTR  R14,0
         MVI   0(R15),C' '
         EX    R14,SCLR
         LA    R0,1(R14)
         LR    R1,R15
        TGET   (1),(0)
         LTR   R15,R15
         BZ    FINISH
         CH    R15,=H'12'
         BNE   SETCC
        TCLEARQ INPUT
        TPUT   TSRTL,L'TSRTL
         B     STBK
SETCC    LR    R10,R15
FINISH  $XRET  CC=(R10),TYPE=RENT
         SPACE 1
SCLR     MVC   1(*-*,R15),0(R15)   <<EXECUTED>>
         SPACE 2
TSRTL    DC    C'INPUT TOO LONG - RESPECIFY :'
         SPACE 2
        LTORG
         SPACE 2
         END
