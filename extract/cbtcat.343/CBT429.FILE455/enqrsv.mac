RSRV     TITLE 'DISPLAY RESOURCES ENQUE''S RESERVE ROUTINE.'
ENQRSV   START 0
         SPACE 1
* FUNCTION :   ROUTINE TO DISPLAY RESOURCES ENQUE'S RESERVE ON SP1.3
* ----------   AT SYSTEM CONSOLE.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
SIAL     EQU   102400              SCAN INFO AREA LENGTH.
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R5,R13              SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R5
         ST    R0,SENDP            SET TSO FLAG ADDRESS.
         LA    R1,L'WTOLINE+L'LINE
         STH   R1,WTOLINE
         MVC   WTOLINE+2(2),WTOP+2
         MVC   MCSLINE(4),WTOP+8
         MVC   LINE,BLANK
         L     R3,CVTPTR           GET ADDRESS OF CVT.
         USING CVT,R3
         L     R4,CVTSMCA
         DROP  R3
         USING SMCABASE,R4         SET ADDRESSABILITY.
         MVC   LINE(28),=CL28'RESOURCES RESERVES DISPLAY ('
         MVC   LINE+28(4),SMCASID  MOVE SYSTEM ID.
         MVC   CURSYS(4),SMCASID
         DROP  R4
         MVI   LINE+33,C'-'
        TIME  DEC                  R0 : HHMMSSTH
         STM   R0,R1,WORKD         TIME - DATE
         UNPK  LINE+44(7),WORKD(4)
         MVC   LINE+50(2),LINE+48  XXSS
         MVC   LINE+48(1),LINE+47  MXSS
         MVC   LINE+47(1),LINE+46  HHXM MXSS
         MVI   LINE+46,C'.'
         MVI   LINE+49,C'.'        HH.MM.SS
         UNPK  LINE+35(7),WORKD+4(4)
         MVC   LINE+35(2),LINE+37  YY
         MVC   LINE+38(3),LINE+39  DDD
         MVI   LINE+37,C'.'        YY.DDDX
         MVI   LINE+41,C' '        YY.DDD
         MVI   LINE+42,C'-'
         MVI   LINE+52,C')'
         LA    R15,WTOLINE
         BAL   R14,SENDL           TITLE AND TIME.
         XC    TKN,TKN             ZERO OUT TOKEN AREA.
         MVI   SWITCH,0            INITIALIZE SWITCHES.
START    LA    R10,SCANAREA        ADDRESS OF RETURNED DATA.
         LA    R13,SCANSAVE        ADDRESS GQSCAN SAVE AREA.
         XC    0(18*4,R13),0(R13)
         MVC   SCAN(LSCAN),SCANP
        ZEROKEY ,
        GQSCAN AREA=((R10),),TOKEN=TKN,MF=(E,SCAN)
         LR    R13,R5              RESTORE MAIN SAVE AREA.
         LR    R8,R0               SAVE RIB AND RIBE LENGTHS.
         LR    R7,R1               SAVE NUMBER OF RIBS.
         LR    R9,R15              SAVE RETURN CODE.
        RESETKEY ,
         B     *+L'*(R9)           BRANCH TO APPROPRIATE ROUTINE.
         B     CODE00                - ALL OK.
         B     CODE04                - NO RESOURCES MATCHED REQUEST.
         B     CODE08                - GQSCAN AREA TOO SMALL.
         B     CODE0C                - ERROR WHILE PROCESSING.
         B     CODE10                - INVALID SYSNAME.
         SPACE 1
CODE04   LA    R15,CODE04M
         LA    R14,RETURN
         B     SENDL               EXIT, NO RESOURCES.
CODE08   OI    SWITCH,TKNSW        SET TOKEN SWITCH TO CONTINUE.
         B     CODE00              GO CONTINUE PROCESSING.
CODE0C   LA    R15,CODE0CM
         LA    R14,RETURN
         B     SENDL               EXIT, ERROR IN GQSCAN.
CODE10   LA    R15,CODE10M
         LA    R14,RETURN
         B     SENDL               EXIT, INVALID SYSNAME.
         SPACE 1
CODE00   STCM  R8,B'1111',RIBLNGTH STORE RIB AND RIBE LENGTH.
         LTR   R7,R7               TEST NUMBER OF RIBS.
         BZ    RETURN              NO RIB'S SUPPLIED.
         LA    R6,LINE
         USING RIB,R10             RESOURCE INFORMATION BLOCK.
RIBLOOP  L     R9,RIBNRIBE         LOAD THE NO. OF RIBE'S.
         LTR   R9,R9
         BNP   BUMPRIB             NO RIBE'S SUPPLIED.
         LR    R11,R10             GET RIB POINTER.
         AH    R11,RIBLNGTH        POINT TO RIB VARIABLE SECTION.
         USING RIBVAR,R11          RESOURCE INFORMATION BLOCK.
         MVC   LINE,BLANK          CLEAR BEFORE USING.
         USING RNMLINE,R6
         MVC   RNMT,=CL16'RESOURCE NAME :'
         MVC   QNM,RIBQNAME        MOVE QNAME TO PRINT LINE.
         MVI   QRS,C'/'
         SR    R1,R1
         IC    R1,RIBRNMLN         RNAME LENGTH.
         BCT   R1,MVCRNM+L'MVCRNM  MINUS ONE FOR EX INSTRUCTION.
MVCRNM   MVC   RNM(*-*),RIBRNAME   << EXECUTED >>
         EX    R1,MVCRNM           MOVE RNAME TO PRINT LINE.
         DROP  R6,R11
         AH    R11,RIBVLEN         POINT TO FIRST RIBE.
         USING RIBE,R11            RESOURCE INFORMATION BLOCK EXT.
         LR    R0,R9               SAVE IN CASE.
         LR    R1,R11
ANYRSV   TM    RIBERFLG,RIBERESV   ANY RESERVE REQUEST?
         BO    TPHDRS              BRANCH IF YES.
         AH    R11,RIBELENG        POINT TO NEXT RIBE.
         BCT   R9,ANYRSV           DO IT FOR NEXT USER.
         B     BUMPRIB             NO, BUMP TO NEXT RIB.
         SPACE 1
TPHDRS   LR    R9,R0               RESTART FROM FIRST RIBE.
         LR    R11,R1
         LA    R15,SEPRTR
         BAL   R14,SENDL           SEND SEPARATOR.
         LA    R15,WTOLINE
         BAL   R14,SENDL           SEND RESOURCE NAME.
         MVC   LINE,BLANK          CLEAR.
         MVC   LINE(L'FLAGHD),FLAGHD
         LA    R15,WTOLINE
         BAL   R14,SENDL           SEND FLAG HEADER.
         MVC   LINE(L'FLAGHU),FLAGHU
         LA    R15,WTOLINE
         BAL   R14,SENDL           UNDER LINE IT.
         SPACE 1
SEARCH   MVC   LINE,BLANK          CLEAR BEFORE USING.
         OI    SWITCH,ENQSW        SET SWITCH THAT THERE WAS ENQ.
         USING JOBLINE,R6
         MVC   JOBNAME,RIBEJBNM    JOBNAME OF REQUESTOR.
         MVC   SYSTEM(4),RIBESYSN  SYSTEM NAME OF REQUESTOR.
         TM    RIBERFLG,RIBETYPE   IS THIS EXCLUSIVE OR SHARED?
         BO    SHARED              BRANCH IF SHARED.
         MVC   EXCLFLAG,=CL4'EXCL' NO, EXCLUSIVE.
         B     TSMC
SHARED   MVC   EXCLFLAG,=CL4'SHR'
TSMC     TM    RIBERFLG,RIBEMC     IS MUST COMPLETE?
         BO    MUSTC               BRANCH IF YES.
         MVI   MCFLAG,C'N'
         B     TSRR
MUSTC    MVI   MCFLAG,C'Y'
TSRR     TM    RIBERFLG,RIBERESV   IS THIS RESERVE REQUEST?
         BO    RESERVE             BRANCH IF YES.
         MVI   RESVFLAG,C'N'
         B     TSRG
RESERVE  MVI   RESVFLAG,C'Y'
TSRG     TM    RIBERFLG,RIBERESC   IS THIS RESERVE REQUEST GLOBAL?
         BO    GLOBAL              BRANCH IF YES.
         MVI   GLOBFLAG,C'N'
         B     TSGO
GLOBAL   MVI   GLOBFLAG,C'Y'
TSGO     TM    RIBESFLG,RIBESTAT   IS THIS GUY THE OWNER?
         BO    OWNER               BRANCH IF YES.
         MVI   OWNFLAG,C'N'
         MVC   WAITFLAG,=CL3'*W*'  TELL USER WAITING DATA-SET.
         B     SETCUA
OWNER    MVI   OWNFLAG,C'Y'
SETCUA   CLC   CURSYS(4),RIBESYSN  CURRENT SYSTEM NAME?
         BNE   OTHER               BRANCH IF NOT.
         L     R1,RIBEUCB          GET UCB ADDRESS.
         LTR   R1,R1               IS UCB ADDRESS TRUE?
         BZ    WTOIT               BRANCH IF NOT.
         USING UCBDSECT,R1
         MVC   CUA,UCBNAME         SET UCB NAME.
         MVC   UNAME,UCBVOLI       SET VOLUME SERIAL NUMBER.
         DROP  R1
         B     WTOIT
OTHER    MVC   CUA+1(4),=C'--->'   SAY OTHER SYSTEM (BYPASS A 0C4).
         MVC   CUA+6(4),RIBESYSN
         DROP  R6,R11
WTOIT    LA    R15,WTOLINE
         BAL   R14,SENDL           SEND IT.
         AH    R11,RIBELENG        POINT TO NEXT RIBE.
         BCT   R9,SEARCH           DO IT FOR NEXT USER.
BUMPRIB  SR    R3,R3
         L     R4,RIBNRIBE         NUMBER OF RIBE'S.
         LTR   R4,R4
         BNP   NEXTRIB             NO RIBE'S SUPPLIED.
BUMPLOOP AH    R3,RIBELENG         ADD RIBE LENGTH.
         BCT   R4,BUMPLOOP         ADD TIMES NO. OF RIBE'S.
NEXTRIB  AH    R3,RIBLNGTH         ADD LENGTH OF RIB.
         AH    R3,RIBVLEN          ADD LENGTH OF VARIABLE SECTION.
         AR    R10,R3              POINT TO NEXT RIB.
         BCT   R7,RIBLOOP          LOOP UNTIL NO MORE RIBS.
         DROP  R10
RETURN   TM    SWITCH,TKNSW        IS THERE MORE DATA?
         BZ    TEST                NO
         NI    SWITCH,255-TKNSW    YES
         B     START               GO CONTINUE PROCESSING.
TEST     TM    SWITCH,ENQSW        WAS THERE AN ENQ?
         BO    FINISH              YES, EXIT.
         LA    R15,NOENQS
         BAL   R14,SENDL           TELL USER.
         SPACE 1
FINISH  $XRET  CC=0,LV=WRKL,TYPE=RENT
         SPACE 1
SENDL    LA    R1,SENDP            BUILD PARM. LIST.
         ST    R15,4(R1)
         L     R15,=V(SENDRTN)
         BR    R15                 GO TO DISPLAY THIS LINE.
         DROP  R5
         EJECT
SCANP   GQSCAN AREA=(,SIAL),REQLIM=MAX,SCOPE=ALL,RESERVE=YES,MF=L
         SPACE 1
        PRINT  NOGEN
WTOP    WTO    '....',ROUTCDE=2,DESC=4,MF=L
SEPRTR  WTO    '***************',ROUTCDE=2,DESC=4,MF=L
CODE04M WTO    'ERROR ENCOUNTERED CODE 04 - RETRY COMMAND.',           X
               ROUTCDE=2,DESC=4,MF=L
CODE0CM WTO    'ERROR ENCOUNTERED CODE 0C - RETRY COMMAND.',           X
               ROUTCDE=2,DESC=4,MF=L
CODE10M WTO    'ERROR ENCOUNTERED CODE 10 - RETRY COMMAND.',           X
               ROUTCDE=2,DESC=4,MF=L
NOENQS  WTO    'NO RESOURCES ENQUE''S RESERVE EXIST.',                 X
               ROUTCDE=2,DESC=4,MF=L
        PRINT  GEN
         SPACE 1
FLAGHD   DS    0CL55               FLAG HEADER.
         DC    CL14'JOBNAME/USERID'
         DC    CL2' '
         DC    CL4'DISP'
         DC    CL2' '
         DC    CL4'SYS.'
         DC    CL2' '
         DC    CL2'MC'
         DC    CL2' '
         DC    CL2'RS'
         DC    CL2' '
         DC    CL2'GL'
         DC    CL2' '
         DC    CL2'OW'
         DC    CL2' '
         DC    CL3'CUA'
         DC    CL2' '
         DC    CL6'VOLSER'
FLAGHU   DS    0CL55               FLAG HEADER UNDERLINE.
         DC    CL14'--------------'
         DC    CL2' '
         DC    CL4'----'
         DC    CL2' '
         DC    CL4'----'
         DC    CL2' '
         DC    CL2'--'
         DC    CL2' '
         DC    CL2'--'
         DC    CL2' '
         DC    CL2'--'
         DC    CL2' '
         DC    CL2'--'
         DC    CL2' '
         DC    CL3'---'
         DC    CL2' '
         DC    CL6'------'
         SPACE 1
BLANK    DC    CL72' '
         SPACE 2
        LTORG
         SPACE 2
RNMLINE  DSECT                     RESOURCE NAME LINE DESCRIPTION.
RNMT     DC    CL16'RESOURCE NAME :'
QNM      DS    CL8
         DC    CL1' '
QRS      DC    CL1'/'
         DC    CL1' '
RNM      DS    CL44
         SPACE 1
JOBLINE  DSECT                     INFO LINE DESCRIPTION.
JOBNAME  DS    CL8
         DC    C' '
WAITFLAG DS    CL3
         DC    CL2' '
         DC    CL2' '
EXCLFLAG DS    CL4
         DC    CL2' '
SYSTEM   DS    CL4
         DC    CL2' '
         DC    CL1' '
MCFLAG   DS    CL1
         DC    CL2' '
         DC    CL1' '
RESVFLAG DS    CL1
         DC    CL2' '
         DC    CL1' '
GLOBFLAG DS    CL1
         DC    CL2' '
         DC    CL1' '
OWNFLAG  DS    CL1
         DC    CL2' '
CUA      DS    CL3
         DC    CL2' '
UNAME    DS    CL6
         DC    CL17' '
         SPACE 1
WRKDSECT DSECT                     WORK AREAS DESCRIPTIONS.
         SPACE 1
SVA      DS    18F                 SAVE AREA.
SCANSAVE DS    18F                 18 WORDS SAVE AREA REQ FOR GQSCAN.
         SPACE 1
WORKD    DS    D
         SPACE 1
SCAN    GQSCAN AREA=(,SIAL),REQLIM=MAX,SCOPE=ALL,RESERVE=YES,MF=L
LSCAN    EQU   *-SCAN
SWITCH   DS    XL1                 ENQ/TOKEN SWITCHES.
TKNSW    EQU   X'01'                - TOKEN SWITCH.
ENQSW    EQU   X'02'                - ENQ SWITCH.
TKN      DS    F                   TOKEN AREA ADDRESS.
RIBLNGTH DC    H'0'                RIB LENGTH.
RIBELENG DC    H'0'                RIBE LENGTH.
         SPACE 1
CURSYS   DS    CL4                 CURRENT SYSTEM ID.
SENDP    DS    2F
         SPACE 1
WTOLINE  DS    F
LINE     DS    CL72                CONSOLE LINE WORK AREA.
MCSLINE  DS    XL4
         SPACE 1
         DS    0F
SCANAREA DS    (SIAL)X             SCAN INFO AREA.
         SPACE 2
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 2
        PRINT NOGEN
         SPACE 1
        CVT    DSECT=YES,TSO=YES,RPS=YES,LIST=YES
        IEESMCA
        ISGRIB
UCBDSECT DSECT
        IEFUCBOB
CIBDSECT DSECT
CIB     IEZCIB
         SPACE 2
         END
