VSMH     TITLE 'VSMHELP - VIRTUAL STORAGE MONITOR HELP SCREEN.'
         PRINT OFF                                                 -EU-
         COPY  VSMMAC                                              -EU-
         PRINT ON                                                  -EU-
         SPACE 1
***********************************************************************
*                                                                     *
*    MODULE :           VSMHELP                                       *
*                       VERSION : 1.1                                 *
*                       DATE : 9/1/82                                 *
*                       NAME : VIRTUAL STORAGE MONITOR HELP SCREEN    *
*                       ATTRIBUTES : REUS                             *
*                                                                     *
*    AUTHOR :           R.P. SHANNON                                  *
*                       HALLMARK CARDS                                *
*                       ENFIELD DISTRIBUTION CENTER                   *
*                                                                     *
*    FUNCTION :         PROVIDE HELP FOR VSM COMMANDS                 *
*                                                                     *
*    REGS AT ENTRY :    R1   - INTER MODULE LINKAGE                   *
*                       R14  - RETURN ADDRESS                         *
*                       R15  - EPA                                    *
*                                                                     *
*    REGS USAGE :       R0   - WORK REGISTER                          *
*                       R1   - POINTER TO INTER MODULE LINKAGE        *
*                       R10  - LINKAGE AREA POINTER                   *
*                       R11  - BASE REGISTER                          *
*                       R12  - 2ND BASE REGISTER (RESERVED)           *
*                       R13  - SAVE AREA POINTER                      *
*                                                                     *
*    REGS AT EXIT :     R14  - RETURN ADDRESS                         *
*                                                                     *
*    LABELS :           SOME OF THE FIELD USAGE IN THIS MODULE CAN    *
*                       BE DETERMINED BY THE FIRST CHARACTER OF THE   *
*                       FIELD. THE KEYS ARE :                         *
*                            @ - FIELD IS IN INTER MODULE LINKAGE     *
*                                AREA                                 *
*                            # - FIELD IS PART OF THE OUTPUT SCREEN   *
*                            $ - DSECT                                *
*                                                                     *
*    MODS : VERSION 1.0  - BASE                                       *
*           VERSION 1.1  -  REMOVED AUTOMATIC UPSHIFT OF KEYBOARD     *
*                           FOR TERMINALS WITH NUM OPTION             *
*                                                                     *
***********************************************************************
         EJECT
VSMHELP  START 0
         SPACE 1
        $DEFREG ,                                                  -EU-
***********************************************************************
*        STANDARD OS LINKAGE                                          *
***********************************************************************
         SPACE 1
        $XENT  BASE=R11            SAVE CALLER'S REGS              -EU-
         EJECT
***********************************************************************
*        GET LINKAGE ADDRESSABILITY                                   *
***********************************************************************
         SPACE 1
         LR    R10,R1              LOAD INTER MOD LNKG ADDR R10
         USING $LNKSECT,R10        DSECT ADDRESSABILITY
P000200  MVC   #MSG1,=CL26' '      CLEAR
         MVC   #MSG2,=CL26' '        MESSAGES
         CLI   @BADCMND,C'1'       WAS BAD COMMAND ENTERED ?
         BE    P000210             BRANCH IF YES                   -EU-
         CLI   @BADCMND,C'2'       WAS HARDCOPY HAS FAILED ?       -EU-
         BNE   P000400             BRANCH IF NO
         MVC   #MSG1,MSG1B                                         -EU-
         B     P000300             BRANCH IF NO                    -EU-
P000210  MVC   #MSG1,MSG1A                                         -EU-
P000300  MVC   #MSG2,MSG2                                          -EU-
         SPACE 1
***********************************************************************
*        OUTPUT THE SCREEN                                            *
***********************************************************************
         SPACE 1
P000400  MVI   @BADCMND,C' '       MOVE BLANK TO BAD COMMAND IND
         MVI   @RSPONSE,C' '       MOVE BLANK TO RESPONSE
         LA    R0,#SCREEN          LOAD SCREEN ADDRESS
         ST    R0,@PUTAREA         STORE IN LINKAGE AREA
         LA    R0,#SCRNLEN         LOAD SCREEN LENGTH
         ST    R0,@PUTLEN          STORE IN LINKAGE AREA
         LR    R1,R10              POINT R1 TO LINKAGE AREA
         L     R15,@IOMOD          LOAD ADDRESS OF TERMINAL I/O MOD
         BASR  R14,R15             GO DO TERMINAL IO
         CLI   @RSPONSE,C'R'       REFRESH SCREEN ?
         BE    P000200             BRANCH IF YES
        $XRET  CC=0                B O O G I E                     -EU-
         EJECT
MSG1A    DC    CL26'  COMMAND NOT RECOGNIZED  '
MSG1B    DC    CL26' HARDCOPY OPEN HAS FAILED '
MSG2     DC    CL26'PLEASE ENTER VALID COMMAND'
         SPACE 1
#SCREEN  DS    0F
         $FS   WCC=(KBR,RMDT),MF=L                                 -EU-
         $FS   SBA=(1,1),RA=(1,1,00),MF=L                          -EU-
         $FS   SBA=(1,1),SF=(PROT,INT),MF=L                        -EU-
         $FS   TEXT='SELECT OPTION =>',MF=L                        -EU-
         $FS   SF=NORMAL,TEXT=' ',MF=L                             -EU-
         $FS   SF=(PROT),MF=L                                      -EU-
         $FS   SBA=(3,35),SF=(PROT,INT),MF=L                       -EU-
         $FS   TEXT='HELP MENU',MF=L                               -EU-
         $FS   SF=(PROT),MF=L                                      -EU-
         $FS   SBA=(5,19),TEXT='THE VIRTUAL STORAGE ',MF=L         -EU-
         $FS   TEXT='MONITOR WAS DESIGNED TO',MF=L                 -EU-
         $FS   SBA=(6,19),TEXT='ASSIST IN SOLVING ',MF=L           -EU-
         $FS   TEXT='VIRTUAL STORAGE PROBLEMS.',MF=L               -EU-
         $FS   SBA=(8,19),TEXT='THE FOLLOWING COMMANDS ',MF=L      -EU-
         $FS   TEXT='MAY BE ENTERED FROM',MF=L                     -EU-
         $FS   SBA=(9,19),TEXT='ANY SCREEN :',MF=L                 -EU-
         $FS   SBA=(11,19),SF=(PROT,INT),TEXT='PFK-10/22',MF=L     -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='1',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> VIRTUAL STORAGE MAPPER',MF=L     -EU-
         $FS   SBA=(12,19),SF=(PROT,INT),TEXT='PFK-11/23',MF=L     -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='2',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> SUBPOOL MONITOR',MF=L            -EU-
         $FS   SBA=(13,19),SF=(PROT,INT),TEXT='PFK-12/24',MF=L     -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='3',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> PROTECT KEY MONITOR',MF=L        -EU-
         $FS   SBA=(14,20),SF=(PROT,INT),TEXT='PFK-1/13',MF=L      -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='H',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> HELP SCREEN',MF=L                -EU-
         $FS   SBA=(15,20),SF=(PROT,INT),TEXT='PFK-2/14',MF=L      -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='M',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> PRIMARY MENU',MF=L               -EU-
         $FS   SBA=(16,23),SF=(PROT,INT),TEXT='ENTER',MF=L         -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='R',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> REFRESH THE SCREEN',MF=L         -EU-
         $FS   SBA=(17,20),SF=(PROT,INT),TEXT='PFK-7/19',MF=L      -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='S',MF=L                         -EU-
         $FS   SF=(PROT),MF=L                                      -EU-
         $FS   TEXT='=> START HARDCOPY OF 1, 2 AND 3',MF=L         -EU-
         $FS   SBA=(18,20),SF=(PROT,INT),TEXT='PFK-8/20',MF=L      -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='P',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> STOP HARDCOPY',MF=L              -EU-
         $FS   SBA=(19,20),SF=(PROT,INT),TEXT='PFK-3/15',MF=L      -EU-
         $FS   SF=(PROT),TEXT='OR',MF=L                            -EU-
         $FS   SF=(PROT,INT),TEXT='Z',MF=L                         -EU-
         $FS   SF=(PROT),TEXT='=> EXIT VSM',MF=L                   -EU-
         $FS   SBA=(21,27),SF=(PROT,INT),MF=L                      -EU-
#MSG1    $FS   TEXT=(' ',26),MF=L                                  -EU-
         $FS   SF=(PROT),MF=L                                      -EU-
         $FS   SBA=(22,27),SF=(PROT,INT),MF=L                      -EU-
#MSG2    $FS   TEXT=(' ',26),MF=L                                  -EU-
         $FS   SF=(PROT),MF=L                                      -EU-
         $FS   SBA=(1,1),SF=(PT,IC),MF=L                           -EU-
#SCRNLEN EQU   *-#SCREEN
         SPACE 2
         LTORG
         EJECT
         $LNKSECT
         SPACE 2
         END
