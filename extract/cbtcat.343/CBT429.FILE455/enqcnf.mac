CNFL     TITLE 'DISPLAY DATA-SETS ENQUE''S CONFLICTS ROUTINE.'
ENQCNF   START 0
         SPACE 1
* FUNCTION :   ROUTINE TO DISPLAY DATA-SETS ENQUE'S CONFLICTS ON SP1.3
* ----------   AT SYSTEM CONSOLE.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 2
        $DEFREG
SIAL     EQU   102400              SCAN INFO AREA LENGTH.
         EJECT
        $XENT  BASE=R12,LV=WRKL,TYPE=RENT
         LR    R5,R13              SET WORK AREAS ADDRESSABILITY.
         USING WRKDSECT,R5
         ST    R0,SENDP            SET TSO FLAG ADDRESS.
         LA    R1,L'WTOLINE+L'LINE
         STH   R1,WTOLINE
         MVC   WTOLINE+2(2),WTOP+2
         MVC   MCSLINE(4),WTOP+8
         MVC   LINE,BLANK
         L     R3,CVTPTR           GET ADDRESS OF CVT.
         USING CVT,R3
         L     R4,CVTSMCA
         DROP  R3
         USING SMCABASE,R4         SET ADDRESSABILITY.
         MVC   LINE(28),=CL28'DATA-SET CONFLICTS DISPLAY ('
         MVC   LINE+28(4),SMCASID  MOVE SYSTEM ID.
         DROP  R4
         MVI   LINE+33,C'-'
        TIME  DEC                  R0 : HHMMSSTH
         STM   R0,R1,WORKD         TIME - DATE
         UNPK  LINE+44(7),WORKD(4)
         MVC   LINE+50(2),LINE+48  XXSS
         MVC   LINE+48(1),LINE+47  MXSS
         MVC   LINE+47(1),LINE+46  HHXM MXSS
         MVI   LINE+46,C'.'
         MVI   LINE+49,C'.'        HH.MM.SS
         UNPK  LINE+35(7),WORKD+4(4)
         MVC   LINE+35(2),LINE+37  YY
         MVC   LINE+38(3),LINE+39  DDD
         MVI   LINE+37,C'.'        YY.DDDX
         MVI   LINE+41,C' '        YY.DDD
         MVI   LINE+42,C'-'
         MVI   LINE+52,C')'
         LA    R15,WTOLINE
         BAL   R14,SENDL           TITLE AND TIME.
         XC    TKN,TKN             ZERO OUT TOKEN AREA.
         MVI   SWITCH,0            INITIALIZE SWITCHES.
START    LA    R8,SCANAREA         ADDRESS OF RETURNED DATA.
         LA    R9,SYSDSN           ADDRESS OF SYSDSN MAJOR NAME.
         MVC   SYSDSN,=CL8'SYSDSN'
         LA    R13,SCANSAVE        ADDRESS GQSCAN SAVE AREA.
         XC    0(18*4,R13),0(R13)
         MVC   SCAN(LSCAN),SCANP
        GQSCAN AREA=((R8),),RESNAME=((R9)),TOKEN=TKN,MF=(E,SCAN)
         LR    R13,R5              RESTORE MAIN SAVE AREA.
         B     *+L'*(R15)          BRANCH TO APPROPRIATE ROUTINE.
         B     CODE00                - ALL OK.
         B     CODE04                - NO RESOURCES MATCHED REQUEST.
         B     CODE08                - GQSCAN AREA TOO SMALL.
         B     CODE0C                - ERROR WHILE PROCESSING.
         B     CODE10                - INVALID SYSNAME.
         SPACE 1
CODE04   LA    R15,CODE04M
         LA    R14,RETURN
         B     SENDL               EXIT, DSNAME NOT FOUND.
CODE08   OI    SWITCH,TKNSW        SET TOKEN SWITCH TO CONTINUE.
         B     CODE00              GO CONTINUE PROCESSING.
CODE0C   LA    R15,CODE0CM
         LA    R14,RETURN
         B     SENDL               EXIT, ERROR IN GQSCAN.
CODE10   LA    R15,CODE10M   INVALID SYSNAME.
         LA    R14,RETURN
         B     SENDL               EXIT, INVALID SYSNAME.
         SPACE 1
CODE00   STCM  R0,B'1111',RIBLNGTH STORE RIB AND RIBE LENGTH.
         LTR   R7,R1               SAVE NUMBER OF RIBS.
         BZ    RETURN              NO RIB'S SUPPLIED.
         LA    R6,LINE
         LA    R10,SCANAREA        ADDRESS OF GQSCAN AREA (1ST RIB).
         USING RIB,R10             RESOURCE INFORMATION BLOCK.
RIBLOOP  L     R9,RIBNRIBE         LOAD THE NO. OF RIBE'S.
         LTR   R9,R9
         BNP   BUMPRIB             NO RIBE'S SUPPLIED.
         LR    R11,R10             GET RIB POINTER.
         AH    R11,RIBLNGTH        POINT TO RIB VARIABLE SECTION.
         USING RIBVAR,R11          RESOURCE INFORMATION BLOCK.
         MVC   LINE,BLANK          CLEAR BEFORE USING.
         USING DSNLINE,R6
         MVC   DSNT,=CL16'DATA-SET NAME :'
         SR    R1,R1
         IC    R1,RIBRNMLN         RNAME LENGTH.
         BCT   R1,MVCDSN+L'MVCDSN  MINUS ONE FOR EX INSTRUCTION.
MVCDSN   MVC   DSN(*-*),RIBRNAME   << EXECUTED >>
         EX    R1,MVCDSN           MOVE DSNAME TO PRINT LINE.
         DROP  R11
         AH    R11,RIBVLEN         POINT TO FIRST RIBE.
         USING RIBE,R11            RESOURCE INFORMATION BLOCK EXT.
         CLC   RIBNTWE,=F'0'       ANY WAITING FOR EXCLUSIVE?
         BNE   TPHDRS              YES, PROCESS.
         CLC   RIBNTWS,=F'0'       ANY WAITING FOR SHARE?
         BNE   TPHDRS              YES, PROCESS.
         B     BUMPRIB             NO, BUMP TO NEXT RIB.
         DROP  R6
         SPACE 1
TPHDRS   LA    R15,SEPRTR
         BAL   R14,SENDL           SEND SEPARATOR.
         LA    R15,WTOLINE
         BAL   R14,SENDL           SEND DATA-SET NAME.
         MVC   LINE,BLANK
         MVC   LINE(L'FLAGHD),FLAGHD
         LA    R15,WTOLINE
         BAL   R14,SENDL           SEND FLAG HEADER.
         MVC   LINE(L'FLAGHU),FLAGHU
         LA    R15,WTOLINE
         BAL   R14,SENDL           UNDER LINE IT.
         SPACE 1
SEARCH   MVC   LINE,BLANK          CLEAR BEFORE USING.
         OI    SWITCH,ENQSW        SET SWITCH THAT THERE WAS ENQ.
         USING JOBLINE,R6
         MVC   JOBNAME,RIBEJBNM    JOBNAME OF REQUESTOR.
         MVC   SYSTEM(4),RIBESYSN  SYSTEM NAME OF REQUESTOR.
         TM    RIBERFLG,RIBETYPE   IS THIS EXCLUSIVE OR SHARED?
         BO    SHARED              BRANCH IF SHARED.
         MVC   EXCLFLAG,=CL4'EXCL' NO, EXCLUSIVE.
         B     TSMC
SHARED   MVC   EXCLFLAG,=CL4'SHR'
TSMC     TM    RIBERFLG,RIBEMC     IS MUST COMPLETE?
         BO    MUSTC               BRANCH IF YES.
         MVI   MCFLAG,C'N'
         B     TSRR
MUSTC    MVI   MCFLAG,C'Y'
TSRR     TM    RIBERFLG,RIBERESV   IS THIS RESERVE REQUEST?
         BO    RESERVE             BRANCH IF YES.
         MVI   RESVFLAG,C'N'
         B     TSRG
RESERVE  MVI   RESVFLAG,C'Y'
TSRG     TM    RIBERFLG,RIBERESC   IS THIS RESERVE REQUEST GLOBAL?
         BO    GLOBAL              BRANCH IF YES.
         MVI   GLOBFLAG,C'N'
         B     TSGO
GLOBAL   MVI   GLOBFLAG,C'Y'
TSGO     TM    RIBESFLG,RIBESTAT   IS THIS GUY THE OWNER?
         BO    OWNER               BRANCH IF YES.
         MVI   OWNFLAG,C'N'
         MVC   WAITFLAG,=CL9'*WAITING*' TELL USER WAITING DATA-SET.
         B     WTOIT
OWNER    MVI   OWNFLAG,C'Y'
         DROP  R6,R11
WTOIT    LA    R15,WTOLINE
         BAL   R14,SENDL           SEND IT.
         AH    R11,RIBELENG        POINT TO NEXT RIBE.
         BCT   R9,SEARCH           DO IT FOR NEXT USER.
BUMPRIB  SR    R3,R3
         L     R4,RIBNRIBE         NUMBER OF RIBE'S.
         LTR   R4,R4
         BNP   NEXTRIB             NO RIBE'S SUPPLIED.
BUMPLOOP AH    R3,RIBELENG         ADD RIBE LENGTH.
         BCT   R4,BUMPLOOP         ADD TIMES NO. OF RIBE'S.
NEXTRIB  AH    R3,RIBLNGTH         ADD LENGTH OF RIB.
         AH    R3,RIBVLEN          ADD LENGTH OF VARIABLE SECTION.
         AR    R10,R3              POINT TO NEXT RIB.
         BCT   R7,RIBLOOP          LOOP UNTIL NO MORE RIBS.
         DROP  R10
RETURN   TM    SWITCH,TKNSW        IS THERE MORE DATA?
         BZ    TEST                NO
         NI    SWITCH,255-TKNSW    YES
         B     START               GO CONTINUE PROCESSING.
TEST     TM    SWITCH,ENQSW        WAS THERE AN ENQ?
         BO    FINISH              YES, EXIT.
         LA    R15,NOENQ
         BAL   R14,SENDL           TELL USER.
         SPACE 1
FINISH  $XRET  CC=0,LV=WRKL,TYPE=RENT
         SPACE 1
SENDL    LA    R1,SENDP            BUILD PARM. LIST.
         ST    R15,4(R1)
         L     R15,=V(SENDRTN)
         BR    R15                 GO TO DISPLAY THIS LINE.
         DROP  R5
         EJECT
SCANP   GQSCAN AREA=(,SIAL),REQLIM=MAX,SCOPE=ALL,MF=L
        PRINT  NOGEN
WTOP    WTO    '....',ROUTCDE=2,DESC=4,MF=L
SEPRTR  WTO    '***************',ROUTCDE=2,DESC=4,MF=L
CODE04M WTO    'ERROR ENCOUNTERED CODE 04 - RETRY COMMAND.',           X
               ROUTCDE=2,DESC=4,MF=L
CODE0CM WTO    'ERROR ENCOUNTERED CODE 0C - RETRY COMMAND.',           X
               ROUTCDE=2,DESC=4,MF=L
CODE10M WTO    'ERROR ENCOUNTERED CODE 10 - RETRY COMMAND.',           X
               ROUTCDE=2,DESC=4,MF=L
NOENQ   WTO    'NO DATA-SETS ENQUE''S CONFLICTS EXIST.',               X
               ROUTCDE=2,DESC=4,MF=L
        PRINT  GEN
         SPACE 1
FLAGHD   DS    0CL72               FLAG HEADER.
         DC    CL14'JOBNAME/USERID'
         DC    CL5' '
         DC    CL4'DISP'
         DC    CL2' '
         DC    CL8' SYSTEM '
         DC    CL2' '
         DC    CL13'MUST COMPLETE'
         DC    CL2' '
         DC    CL7'RESERVE'
         DC    CL2' '
         DC    CL6'GLOBAL'
         DC    CL2' '
         DC    CL5'OWNER'
FLAGHU   DS    0CL72               FLAG HEADER UNDERLINE.
         DC    CL14'--------------'
         DC    CL5' '
         DC    CL4'----'
         DC    CL2' '
         DC    CL8'--------'
         DC    CL2' '
         DC    CL13'-------------'
         DC    CL2' '
         DC    CL7'-------'
         DC    CL2' '
         DC    CL6'------'
         DC    CL2' '
         DC    CL5'-----'
         SPACE 1
BLANK    DC    CL72' '
         SPACE 2
        LTORG
         SPACE 2
DSNLINE  DSECT                     DATA-SET NAME LINE DESCRIPTION.
DSNT     DC    CL16'DATA-SET NAME :'
DSN      DS    CL44
         SPACE 1
JOBLINE  DSECT                     INFO LINE DESCRIPTION.
JOBNAME  DS    CL8
         DC    C' '
WAITFLAG DS    CL9' '
         DC    C' '
EXCLFLAG DS    CL4
         DC    CL4' '
SYSTEM   DS    CL4
         DC    CL4' '
         DC    CL6' '
MCFLAG   DS    CL1
         DC    CL6' '
         DC    CL2' '
         DC    CL3' '
RESVFLAG DS    CL1
         DC    CL3' '
         DC    CL2' '
         DC    CL2' '
GLOBFLAG DS    CL1
         DC    CL3' '
         DC    CL2' '
         DC    CL2' '
OWNFLAG  DS    CL1
         DC    CL2' '
         SPACE 1
WRKDSECT DSECT                     WORK AREAS DESCRIPTIONS.
         SPACE 1
SVA      DS    18F                 SAVE AREA.
SCANSAVE DS    18F                 18 WORDS SAVE AREA REQ FOR GQSCAN.
         SPACE 1
WORKD    DS    D
         SPACE 1
SCAN    GQSCAN AREA=(,SIAL),REQLIM=MAX,SCOPE=ALL,MF=L
LSCAN    EQU   *-SCAN
SWITCH   DS    XL1                 ENQ/TOKEN SWITCHES.
TKNSW    EQU   X'01'                - TOKEN SWITCH.
ENQSW    EQU   X'02'                - ENQ SWITCH.
TKN      DS    F                   TOKEN AREA ADDRESS.
RIBLNGTH DC    H'0'                RIB LENGTH.
RIBELENG DC    H'0'                RIBE LENGTH.
SYSDSN   DS    CL8                 QNAME FOR GQSCAN.
         SPACE 1
SENDP    DS    2F
         SPACE 1
WTOLINE  DS    F
LINE     DS    CL72                CONSOLE LINE WORK AREA.
MCSLINE  DS    XL4
         SPACE 1
         DS    0F
SCANAREA DS    (SIAL)X             SCAN INFO AREA.
         SPACE 2
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         SPACE 2
        PRINT NOGEN
         SPACE 1
        CVT    DSECT=YES,TSO=YES,RPS=YES,LIST=YES
        IEESMCA
        ISGRIB
         SPACE 2
         END
