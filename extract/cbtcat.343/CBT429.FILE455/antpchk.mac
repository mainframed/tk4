COVR     TITLE 'IEBANTP - CHECK OPERATION VALIDITY ROUTINE.'
ANTPCHK  START 0
         SPACE 1
* PURPOSE :    VALIDITY CHECK OF IEBANTP OPERATION REQUESTED AGAINST
* ---------    ANY MAGNETIC TAPE OR 3480 DATA CARTRIDGE CONTENTS
*              VIOLATION FROM AN UNAUTHORIZED USER.
*      NOTES - VALIDITY CHECK IS SKIPPED BY SETTING 'CHCKSW' TO 'N'.
*            - THE CHECK VALIDITY TABLES (ANTPCTB) ARE DYNAMICALLY
*              LOADED BY THE ROUTINE.
*            - THIS ROUTINE MAY BE USED BY OTHER PROGRAMS IF DESIRED,
*              IT MAY CONTROL UP TO 9 REQUESTS.
*
* AUTHOR :     MOINIL P.A.
* --------     COMPUTING CENTRE
*              J.R.C. - ISPRA ESTABLISHMENT
*              21020 ISPRA (VA), ITALY
         SPACE 1
*        VALIDITY CHECK CONTROL TABLES - MISCELLANEOUS DEFINITIONS
         SPACE 1
ACCEPT   EQU   0                   ACCEPT ACCESS
REJECT   EQU   1                   REJECT ACCESS
READ     EQU   2                   READ ONLY ACCESS
UPDATE   EQU   3                   READ-WRITE ACCESS
ENDTB    EQU   255                 TABLE END MARKER
         EJECT
        $DEFREG
        $XENT  BASE=R12
         EJECT
         XR    R10,R10             SET OK RC ---------------------- 00
         CLI   CHCKSW,C'N'         BYPASS?
         BE    EXIT                YES
         L     R2,0(R1)            PARM-FIELD ADDRESS
         LH    R1,0(R2)
         LA    R3,2(R2)            POINT TO CONTROL(S) REQUESTED
         USING CHREQ,R3
         LTR   R1,R1
         BNP   SCC04
         CLI   CHCKNO,C'1'
         BL    SCC04
         CLI   CHCKNO,C'9'
         BH    SCC04
         XR    R8,R8
         LA    R0,15
         IC    R8,CHCKNO
         NR    R8,R0
         LR    R2,R8
         MH    R2,=Y(CHCKTL)
         LA    R2,1(R2)
         CLR   R1,R2
         BNE   SCC04
         LA    R9,CHCKLV
         DROP  R3
         MVI   RQAU,0              CALLER
         MVC   USERID,=CL8' '        USER-ID AND
         MVC   GROUP,=CL8' '           GROUP
        $EACM  RQUS                      REQUEST
         CLC   USERID,=CL8' '
         BNE   LDTBLS
         CLC   GROUP,=CL8' '
         BE    SCC08
LDTBLS  BLDL   0,EPENTRY
         LTR   R15,R15
         BNZ   SCC12
        LOAD   DE=EPNAME,LOADPT=TBADDR
         L     R1,TBADDR
         CLC   0(8,R1),=CL8'ANTPCTB'
         BNE   SCC16
         LA    R1,3*8(R1)
         ST    R1,TBADDR
        EXTRACT ATIOT,'S',FIELDS=(TIOT)
VERIFY   BAS   R14,SCTBR
         B     SCC28
         AH    R9,=Y(CHCKTL)
         BCT   R8,VERIFY
         B     EXIT
SCC36    LA    R10,36              UNKNOWN DEVICE ADDRESS --------- 36
         B     EXIT
SCC32    LA    R10,32              DD-NAME NOT FOUND -------------- 32
         B     EXIT
SCC28    LA    R10,28              ACCESS REJECTED ---------------- 28
         B     EXIT
SCC24    LA    R10,24              UNKNOWN UNIT-TYPE -------------- 24
         B     EXIT
SCC20    LA    R10,20              INVALID LEVEL ------------------ 20
         B     EXIT
SCC16    LA    R10,16              INVALID TABLE-ID --------------- 16
         B     EXIT
SCC12    LA    R10,12              NO CHECK TABLES ---------------- 12
         B     LEAVE
SCC08    LA    R10,8               NO USER-ID NOR GROUP ----------- 08
         B     LEAVE
SCC04    LA    R10,4               INVALID PARM-FIELD ------------- 04
         B     LEAVE
EXIT    DELETE DE=EPNAME
LEAVE   $XRET  CC=(R10)
         EJECT
*        SCAN TABLES ROUTINE.
*              R0-R4 = WORK REGISTER
*              R9 = CONTROL FIELDS POINTER
*              R14 = LINK REGISTER
*                    RETURN +0 = REJECTED
*                           +4 = ACCEPTED
         SPACE 1
         USING CHCKLV,R9
SCTBR    CLI   CHCKLV,C'R'
         BE    SCTBR0
         CLI   CHCKLV,C'W'
         BNE   SCC20
SCTBR0   L     R3,TBADDR
SCTBR1   CLC   CHCKUT(L'CHCKUT),0(R3)
         BE    SCTBR10
         LA    R3,8(R3)
         CLI   0(R3),ENDTB
         BNE   SCTBR1
         B     SCC24
SCTBR10  TM    4(R3),X'80'         IS IT POINTER TO SUB-TABLE?
         BZ    SCTBR20             NO
         L     R1,ATIOT            YES, TIOT ADDRESS
         USING TIODSECT,R1
         XR    R0,R0
SCTBR11  CLC   TIOELNGH(4),=XL4'0' TIOT END?
         BE    SCC32               YES, DD-NAME NOT FOUND
         CLC   TIOEDDNM,CHCKDD
         BE    SCTBR12
         IC    R0,TIOELNGH         GET IN R0 DD ENTRY LENGTH
         AR    R1,R0               R1 POINTS TO NEXT DD ENTRY
         B     SCTBR11
SCTBR12  ICM   R2,B'0111',TIOEFSRT GET UCB ADDRESS
         DROP  R1
         USING UCBOB,R2
         MVC   DEVADD,UCBNAME      GET DEVICE ADDRESS (CUU)
         DROP  R2
         ICM   R1,B'1111',4(R3)    GET SUB-TABLE DEVICES ADDRESSES
         LA    R1,0(R1)            CLEAR HIGH-ORDER BIT
SCTBR13  CLI   0(R1),ENDTB
         BE    SCC36               UNKNOWN DEVICE ADDRESS
         CLC   DEVADD(L'DEVADD),0(R1)
         BL    SCTBR14
         BE    SCTBR15
         CLI   3(R1),C' '
         BE    SCTBR14
         CLC   DEVADD(L'DEVADD),3(R1)
         BNH   SCTBR15
SCTBR14  LA    R1,10(R1)
         B     SCTBR13
SCTBR15  ICM   R2,B'1111',6(R1)    SEARCH SERIAL NUMBERS TABLE
         B     SCTBR20+L'SCTBR20
SCTBR20  ICM   R2,B'1111',4(R3)    SEARCH SERIAL NUMBERS TABLE
         LA    R3,1(R2)            BUMP AND RETAIN GLOBAL DEFAULT
SCTBR21  CLI   0(R3),ENDTB
         BE    SCTBR40
         CLC   CHCKVS(L'CHCKVS),0(R3)
         BL    SCTBR22
         BE    SCTBR30
         CLI   6(R3),C' '
         BE    SCTBR22
         CLC   CHCKVS(L'CHCKVS),6(R3)
         BNH   SCTBR30
SCTBR22  LA    R3,16(R3)
         B     SCTBR21
SCTBR30  ICM   R2,B'1111',12(R3)   SEARCH USER-ID/GROUP ACCESS TABLE
         LA    R4,1(R2)            BUMP AND RETAIN LOCAL DEFAULT
SCTBR31  CLI   0(R4),ENDTB
         BE    SCTBR40
         CLI   0(R4),UPDATE
         BE    SCTBR32
         CLI   0(R4),READ
         BNE   SCTBR33
SCTBR32  LR    R0,R4
         LA    R4,1(R4)
SCTBR33  CLC   USERID(8),0(R4)
         BE    SCTBR34
         CLC   GROUP(8),0(R4)
         BE    SCTBR34
         LA    R4,8(R4)
         B     SCTBR31
SCTBR34  LR    R2,R0
SCTBR40  CLI   0(R2),REJECT
         BER   R14
         CLI   CHCKLV,C'R'
         BE    4(R14)
         CLI   0(R2),READ
         BNE   4(R14)
         BR    R14
         DROP  R9
         EJECT
*        CONSTANTS AND WORK-AREAS.
*        -------------------------
         SPACE 1
ATIOT    DC    A(*-*)
TBADDR   DC    A(*-*)
EPENTRY  DC    Y(1,58)
EPNAME   DC    CL8'ANTPCTB'
         DC    XL58'0'
RQUS     DC    0F'0',BL1'00010000',AL3(0),AL4(RQAU)
CHCKSW   DC    CL1'Y'              BYPASS SWITCH
RQAU     DC    AL1(0)
USERID   DC    CL8' '
GROUP    DC    CL8' '
DEVADD   DC    CL3' '
         SPACE 2
        LTORG
         SPACE 2
*        CONTROLS REQUESTED - PARM-FIELD DESCRIPTION.
*        --------------------------------------------
         SPACE 1
CHREQ    DSECT
CHCKNO   DS    CL1                 NUMBER OF CONTROLS (1 UP TO 9)
*        1 UP TO 9 REPEATED CONTROL FIELDS.
CHCKLV   DS    CL1                 CHECK LEVEL (R OR W)
CHCKUT   DS    CL4                 UNIT-TYPE
CHCKVS   DS    CL6                 VOL. SER.
CHCKDD   DS    CL8                 DD-NAME.
CHCKTL   EQU   L'CHCKLV+L'CHCKUT+L'CHCKVS+L'CHCKDD
         SPACE 2
         PRINT NOGEN
TIODSECT DSECT
        IEFTIOT1
        IEFUCBOB
         SPACE 2
         END
