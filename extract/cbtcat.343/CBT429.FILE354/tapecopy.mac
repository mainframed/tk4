//TAPECOPY JOB
/*ROUTE  PRINT  R7
//STEP0   EXEC HLASMCL,CLASS='*',
//             PARM.ASM='LINECOUNT(59),NODECK,OBJ,XREF(FULL),ESD'
//SYSIN    DD  *
         EJECT                                                      RH
***********************************************************************
*                                                                     *
*                         A T T E N T I O N                           *
*                                                                     *
***********************************************************************
*                                                                     *
*         THIS PROGRAM MATERIAL IS THE PROPRIETORY PROPERTY OF        *
*                                                                     *
*               RANDY HALL,                                           *
*               DIVERSIFIED DEVELOPMENTS,                             *
*               1151 PALMER ROAD,                                     *
*               VICTORIA, B.C., V8P-2H5                               *
*                                                                     *
*         AND MAY NOT BE USED OR REPRODUCED IN ANY MEDIUM             *
*         WITHOUT PRIOR WRITTEN CONSENT                               *
*                                                                     *
***********************************************************************
         EJECT                                                      RH
   TITLE  'TAPECOPY - A MASTER TO MULTI OUTPUT TAPE COPYING PROGRAM '
*              RANDY HALL, DIVERSIFIED DEVELOPMENTS, 1980'
*              1151 PALMER ROAD,
*              VICTORIA, B.C. CANADA
*              604-386-1061
*              LARRY SMITH.  NOV. 1969'
         MACRO
&LAB     EQUREGS
         LCLA      &N
         AIF       ('&LAB' EQ '').NOLAB
&LAB     DC        0H'0'
.NOLAB   ANOP
R&N      EQU       &N
&N       SETA      &N+1
         AIF       (&N LE 15).NOLAB
         MEND
         SPACE
         MACRO
         IECDSECT
***********************************************************************
*
*        VOLUME LABEL AREA - SEE 'DATA SET LABELS- FL1 AND FL2'
*
***********************************************************************
         SPACE 1
DXLBL    DS    0CL80
VOLLABI  DS    CL3                 LABEL IDENTIFIER
VOLNO    DS    CL1                 VOLUME LABEL NUMBER
VOLSERNO DS    CL6
VOLSEC   DS    CL1
         DS    0CL10               RESERVED
VOLVTOC  DS    CL5
         DS    CL5
         DS    CL10                RESERVED
         DS    CL10                RESERVED
VOLOWNER DS    CL10                OWNER NAME AND ADDRESS CODE
         DS    CL29                RESERVED
         SPACE 1
***********************************************************************
*
*        ANSI VOLUME LABEL AREA
*        (ANSI AND IBM STANDARD LABEL DIFFERENCES)
*
***********************************************************************
         SPACE 1
         ORG   DXLBL+37
AVOLOWNR DS    CL14                     OWNER IDENTIFICATION
         DS    CL28                     RESERVED
LABSTAND DS    CL1                      LABEL STANDARD LEVEL
         SPACE 1
***********************************************************************
*
*        FILE LABEL 1 AREA - SEE 'DATA SET LABEL -- FL1'
*
***********************************************************************
         SPACE 1
         ORG   DXLBL
FL1LABI  DS    CL3                 LABEL IDENTIFIER
FL1NO    DS    CL1                 FILE LABEL NUMBER
FL1ID    DS    CL17                FILE IDENTIFIER
FL1FILSR DS    CL6                 FILE SERIAL NUMBER
FL1VOLSQ DS    CL4                 VOLUME SEQUENCE NUMBER
FL1FILSQ DS    CL4                 FILE SEQUENCE NUMBER
FL1GNO   DS    CL4                 GENERATION NUMBER
FL1VNG   DS    CL2                 VERSION NUMBER OF GENERATION
FL1CREDT DS    CL6                 CREATION DATE
FL1EXPDT DS    CL6                 EXPIRATION DATE
FL1FSEC  DC    C'0'                FILE SECURITY INDICATOR
FL1BLKCT DS    CL6                 BLOCK COUNT
FL1SYSCD DS    CL13                SYSTEM CODE
FL1RES   DS    0CL7                RESERVED FOR FUTURE USE
         DS    CL1
FL1RES1  DS    CL6
         SPACE 1
***********************************************************************
*
*        FILE LABEL 2 AREA - SEE 'DATA SET LABEL -- FL2'
*
***********************************************************************
         SPACE 1
         ORG   FL1ID
FL2RECFM DS    CL1                 RECORD FORMAT
FL2BLKL  DS    CL5                 BLOCK LENGTH
FL2LRECL DS    CL5                 BLOCKING FACTOR/RECORD LENGTH
FL2DEN   DS    CL1                 DENSITY
FL2FILP  DS    CL1                 FILE POSITION
FL2JSID  DS    0CL17               JOB/STEP IDENTIFICATION
FL2JOBD  DS    CL8                 JOB IDENTIFICATION
FL2JSSP  DC    C'/'                SLASH
FL2STEPD DS    CL8                 STEP IDENTIFICATION
FL2TRTCH DS    CL2                 TAPE RECORDING TECHNIQUE
FL2CNTRL DS    CL1                 CARRAIGE CONTROL CHARACTER
         DS    CL1                     RESERVED FOR FUTURE USE
FL2BLKA  DS    CL1                     BLOCK ATTRIBUTE
         DS    CL1                 RESERVED                      99223
FL2DRID  DS    0CL7                TAPE DRIVE ID                 99223
         DS    CL2                 RESERVED                      99223
FL2ID    DS    CL5                 ID OF CREATING DRIVE          99223
FL2DSIND DS    CL1                 DATA SET INDICATOR FIELD      Y02083
FL2RES   DS    CL32                RESERVED FOR FUTURE USE       Y02083
         SPACE 1
***********************************************************************
*
*        ANSI FILE LABEL 2 AREA
*        (ANSI AND IBM STANDARD FILE LABEL 2 DIFFERENCES)
*
***********************************************************************
         SPACE 1
         ORG   DXLBL+50
FL2BUFOF DS    CL2                      BUFFER OFFSET
         SPACE 1
         MEND
         SPACE
         MACRO
&NAME    RESTORE   &LIST
         AIF       ('&LIST' EQ '').E1
&NAME    IHBINNRA  &LIST               LOAD REG 1
         SVC       17                  ISSUE SVC FOR RESTORE
         MEXIT
.E1      IHBERMAC  01,150              LIST ADDR MISSING
         MEND
         SPACE
         MACRO
&NAME    IOBS      &ECBS
         GBLA      &MAXTAPE
         LCLA      &N
         LCLC      &NM
&NM      SETC      '&NAME'
.LOOP    ANOP
&NM      DS        0FL8                                             RH
         DC        X'03',XL3'00'  SAY DON'T USE SYSTEM I/O ROUTINES RH
&NM      SETC      '        '
         DC        A(&ECBS+4*&N)
         DC        2F'0'
         DC        A(WRITECCW)
         DC        A(DCBS+DCBLEN*&N)
         DC        2F'0'
&N       SETA      &N+1
         AIF       (&N LT &MAXTAPE).LOOP
         MEND
         SPACE
         MACRO
&LABEL   DCBS      &IOBS
         GBLA      &MAXTAPE
         LCLC      &LAB
         LCLA      &N
&LAB     SETC      '&LABEL'
.LOOP    ANOP
         DC        D'0'
&LAB     DCB   MACRF=PL,DDNAME=XXXXXXXX,DEVD=DA,EXLST=JFCBADD,      RH X
               DSORG=PS,BUFNO=1                 OUTPUT DCB RH
         AIF   (&N GT 0).SKIP1                                      RH
DCBLEN   EQU   *-&LABEL+8         LENGTH OF EXCP DCB                RH
.SKIP1   ANOP
&LAB     SETC      '        '
&N       SETA      &N+1
         AIF       (&N LT &MAXTAPE).LOOP
         MEND
         SPACE
         MACRO
&LABEL   OUTPUT    &MSG
&LABEL   LA        R1,&MSG             LOAD WTO PARM LIST ADDRESS
         BAL       R7,OUTPUT           LINK TO OUTPUT ROUTINE
         MEND
         SPACE     2
         MACRO
&LABEL   ECBLIST   &ECB
         GBLA      &MAXTAPE
         LCLA      &N
         LCLC      &LAB
&LAB     SETC      '&LABEL'
.LOOP    AIF       (&N EQ &MAXTAPE-1).LAST
&LAB     DC        A(&ECB+4*&N)
&LAB     SETC      ''
&N       SETA      &N+1
         AGO       .LOOP
.LAST    ANOP
&LAB     DC        X'80',AL3(&ECB+4*&N)
         MEND
         MACRO                                                      RH
&LABEL   VOLS
         GBLA  &MAXTAPE                                             RH
         LCLC  &LAB                                                 RH
         LCLA  &N                                                   RH
&LAB     SETC  '&LABEL'                                             RH
VOLPTR   DC    F'0'                                                 RH
.LOOP    ANOP                                                       RH
&LAB     DC    CL6' '                                               RH
&LAB     SETC  '        '                                           RH
&N       SETA  &N+1                                                 RH
         AIF   (&N LT &MAXTAPE).LOOP                                RH
         MEND                                                       RH
         EJECT                                                      RH
********************************************************************RH
*                                                                  *RH
*                 T A P E C O P Y                                  *RH
*                                                                  *RH
*   TAPECOPY CAN BE USED FOR THE FOLLOWING PURPOSES:               *RH
*                                                                  *RH
*   1) IT CAN BE USED TO PRINT INFORMATION ABOUT DATA SETS ON      *RH
*      A TAPE, OR,                                                 *RH
*   2) IT CAN BE USED TO PRINT INFORMATION AND TO COPY DATA        *RH
*      SETS FROM ONE TAPE TO 1 OR MORE OUTPUT TAPES, OR,           *RH
*   3) IT CAN BE USED TO COPY DATA SETS FROM AN INPUT TAPE         *RH
*      TO 1 OR MORE OUTPUT TAPES WITHOUT PRINTING ANY              *RH
*      INFORMATION, OR,                                            *RH
*   4) IT CAN BE USED TO RESET THE BLOCK COUNT IN THE TRAILER      *RH
*      LABEL OF A TAPE THAT IS CAUSING 237 ABENDS. THIS IS         *RH
*      AUTOMATICALLY DONE WHEN THE TAPE IS COPIED TO A             *RH
*      NEW TAPE. A MESSAGE WILL BE PRINTED SHOWING THE OLD         *RH
*      BLOCK COUNT AND THE NEW COUNT, OR,                          *RH
*   5) IT CAN  BE USED TO PRINT RECORDS FROM EACH FILE ON          *RH
*      THE TAPE IN A HEXADECIMAL OR A STANDARD PRINT FORMAT        *RH
*                                                                  *RH
*   TAPECOPY CAN BE EXECUTED WITH THE FOLLOWING JCL:               *RH
*                                                                  *RH
*   //JOBNAME  JOB (PIN,CHG),PROG....ETC                           *RH
*   //COPY    EXEC PGM=TAPECOPY,PARM='PARAMETERS'                  *RH
*   //STEPLIB   DD DSN=HALL.LINKLIB,DISP=SHR                       *RH
*   //SYSPRINT  DD SYSOUT=A                                        *RH
*   //MASTER    DD UNIT=TAPE,DISP=OLD,DSN=FIRST DATA SET NAME,     *RH
*   //             VOL=SER=IF NOT CATALOGED                        *RH
*                                                                  *RH
*   THE ABOVE JCL IS ALL THAT IS NECESSARY IF PARM='DEBE'          *RH
*   IS CODED.                                                      *RH
*                                                                  *RH
*   IF PARM='DUMP' IS CODED, THE FOLLOWING JCL MUST BE ADDED       *RH
*   TO THAT ABOVE:                                                 *RH
*                                                                  *RH
*   //TAPE1     DD DSN=ANY VALID DATA SET NAME,                    *RH
*   //             UNIT=TAPE,DISP=(NEW,CATLG OR KEEP)              *RH
*                                                                  *RH
*   IF 2 COPIES OF THE MASTER TAPE ARE REQUIRED, ADD THE           *RH
*   FOLLOWING JCL:                                                 *RH
*                                                                  *RH
*   //TAPE2     DD DSN=ANY VALID DATA SET NAME,                    *RH
*   //             UNIT=TAPE,DISP=(NEW,CATLG OR KEEP)              *RH
*                                                                  *RH
*   A VALID DATA SET NAME MUST BE SUPPLIED ON THE TAPE1 AND        *RH
*   TAPE2 DD STATEMENTS IF LABEL=SL (THE DEFAULT) IS USED,         *RH
*   OTHERWISE THE SYSTEM WILL NOT KEEP THE TAPE.                   *RH
*                                                                  *RH
*   NOTE....FOR SOFTWARE USE ONLY.......                           *RH
*   IF THE DATASET NAME FOR THE OUTPUT TAPE IS 'NOLABEL',          *RH
*   THE OUTPUT TAPE WILL BYPASS TLMS PROTECTION AND WILL           *RH
*   CREATE A NO LABEL OUTPUT TAPE                                  *RH
*                                                                  *RH
*   NOTE2...AN NL TAPE CAN ALSO BE CREATED BY SPECIFYING           *RH
*   LABEL=(,BLP) ON THE OUTPUT TAPES!                              *RH
*                                                                  *RH
*   IF DSN= IS CODED ON THE MASTER JCL STATEMENT, THE JOB          *RH
*   CAN BE RUN IN CLASS=A                                          *RH
*   IF THE DSN OF THE FIRST DATA SET YOU WANT IS NOT KNOWN,        *RH
*   LABEL=(,BLP) MUST BE ADDED TO THE //MASTER JCL AND THE         *RH
*   JOB MUST BE RUN IN CLASS=Z                                     *RH
*                                                                  *RH
*   NOTE ...                                                       *RH
*   UNLESS OTHERWIZE SPECIFIED, DCB INFORMATION FOR NL AND BLP     *RH
*   MASTER TAPES DEFAULTS TO :                                     *RH
*   DCB=(DSORG=PS,RECFM=FB,LRECL=80,BLKSIZE=32000)                 *RH
*                                                                  *RH
*   THE FOLLOWING PARAMETERS CAN BE CODED IN THE PARM= FIELD:      *RH
*                                                                  *RH
*     DEBE - THIS CAUSES INFORMATION ONLY TO BE PRINTED            *RH
*                                                                  *RH
*     DUMP - THIS CAUSES INFORMATION TO BE PRINTED AND THE          RH
*            TAPE TO BE COPIED                                     *RH
*                                                                  *RH
*  NEWVOL= - THE SPECIFIED 6 CHARACTER VOLUME SERIAL NUMBER        *RH
*            WILL BE PUT ON THE OUTPUT TAPES                       *RH
*                                                                  *RH
*    COPY= - INDICATES WHICH FILES ARE TO BE PROCESSED FROM        *RH
*            THE INPUT MASTER TAPE.                                *RH
*            IF COPY=X IS CODED, FILES 1 THROUGH X WILL BE         *RH
*            PROCESSED.                                            *RH
*            IF COPY=(X,Y) IS CODED, FILES X THROUGH Y WILL        *RH
*            BE PROCESSED.                                         *RH
*                                                                  *RH
*  NEWSEQ= - IF THE FILES ARE TO BE COPIED TO THE OUTPUT           *RH
*            TAPES, THE STARTING OUTPUT FILE SEQUENCE NUMBER       *RH
*            CAN BE SPECIFIED WITH NEWSEQ=.                        *RH
*            A MAXIMUM OF 4 CHARACTERS CAN BE SPECIFIED.           *RH
*            THE DEFAULT STARTING SEQUENCE WILL BE THE             *RH
*            SEQUENCE NUMBER SPECIFIED IN THE LABEL=               *RH
*            PARAMETER ON THE //TAPE1 JCL STATEMENT.               *RH
*                                                                  *RH
*   NODUMP - IF NODUMP IS SPECIFIED, THE COMPLETE MASTER TAPE      *RH
*            WILL BE COPIED TO THE OUTPUT TAPES AND NO             *RH
*            INFORMATION WILL BE PRINTED.                          *RH
*            NO FILE SEQUENCE CHECKING WILL BE DONE.               *RH
*            ALL INPUT FILES WILL BE COPIED FROM THE INPUT TAPE    *RH
*            TO ALL OUTPUT TAPES.                                  *RH
*                                                                  *RH
* IGNOREOF - IF THE MASTER TAPE HAS BEEN WRITTEN OVER, IGNOREOF    *RH
*            WILL CAUSE TAPECOPY TO IGNORE THE FIRST END OF        *RH
*            FILE IT ENCOUNTERS AND TO CONTINUE READING THE TAPE.  *RH
*                                                                  *RH
*            *** NOTE ***                                          *RH
*            IF THERE IS ONLY 1 EOF ON THE TAPE, IGNOREOF WILL     *RH
*            CAUSE TAPECOPY TO READ OFF THE END OF THE REEL.       *RH
*                                                                  *RH
*                                                                  *RH
*     TEST - WILL CAUSE THE FOLLOWING EVENTS TO OCCUR :            *RH
*                                                                  *RH
*            THE MESSAGE 'TAPE MARK READ' TO BE PRINTED            *RH
*            WHEN A TAPE MARK IS READ ON THE INPUT TAPE.           *RH
*                                                                  *RH
*            THE MESSAGE 'TAPE MARK WRITTEN AFTER .... RECORDS'    *RH
*            TO BE PRINTED WHEN A TAPE MARK IS WRITTEN ON          *RH
*            THE OUTPUT TAPES.                                     *RH
*                                                                  *RH
*            IF THE PRINT= PARM IS SUPPLIED, LABEL RECORDS         *RH
*            WILL BE PRINTED.                                      *RH
*                                                                  *RH
*   RENAME - INDICATES THAT THE DATASET NAME ON THE OUTPUT         *RH
*            FILE IS TO BE CHANGED. THE NEW FILE NAME WILL         *RH
*            BE OBTAINED FROM 1 OF 2 SOURCES...                    *RH
*              1) IF THE MASTER TAPE CONTAINS ONLY 1 FILE,         *RH
*                 THE NEW DATASET NAME MAY BE OBTAINED FROM        *RH
*                 THE DSN SPECIFIED ON THE JCL FOR THE TAPE1       *RH
*                 OR THE TAPE2 DD STATEMENT.                       *RH
*                 IF TAPE1 AND TAPE2 DD'S ARE USED, THE DSNAME     *RH
*                 FROM TAPE2 WILL BE USED ON BOTH OUTPUT TAPES     *RH
*              2) IF THE MASTER IS A MULTI-FILE TAPE OR A SINGLE   *RH
*                 FILE TAPE, THE SLPARMS FILE DESCRIBED            *RH
*                 IN THE 'SL' PARM BELOW WILL BE USED FOR THE      *RH
*                 DATASET NAMES. FOR THE RENAME PARM, ONLY THE     *RH
*                 FIRST 17 CHARACTERS OF EACH SLPARMS RECORD       *RH
*                 ARE REQUIRED.                                    *RH
*                                                                  *RH
*       SL - INDICATES CREATION OF A STANDARD LABELLED             *RH
*            OUTPUT TAPE. ALL DCB PARAMETERS MUST BE SPECIFIED     *RH
*            THROUGH THE SLPARMS FILE. THE SLPARMS RECORDS         *RH
*            MUST BE CODED AS FOLLOWS :                            *RH
*                                                                  *RH
*             1-17 = LAST 17 CHARACTERS OF THE DSN                 *RH
*               18 = BLANK                                         *RH
*            19-22 = FILE SEQUENCE NBR -EG- 0001                   *RH
*               23 = BLANK                                         *RH
*            24-25 = RECFM -EG- FB (FR FOR FBS)                    *RH
*               26 = BLANK                                         *RH
*            27-31 = BLOCK SIZE -EG- 04000                         *RH
*               32 = BLANK                                         *RH
*            33-37 = RECORD LENGTH -EG- 00080                      *RH
*               38 = BLANK                                         *RH
*            38-80 = BLANK                                         *RH
*                                                                  *RH
*            THERE MUST BE 1 SLPARMS RECORD FOR EACH FILE          *RH
*            TO BE COPIED. IF THERE IS NOT, THE TAPECOPY           *RH
*            JOB WILL BE TERMINATED.                               *RH
*                                                                  *RH
*   PRINT= - INDICATES NUMBER OF RECORDS TO BE PRINTED             *RH
*            FROM EACH FILE. PRINT=ALL WILL CAUSE ALL              *RH
*            RECORDS OF EACH FILE TO BE PRINTED.                   *RH
*            A MAXIMUM OF 4 CHARACTERS CAN BE SPECIFIED.           *RH
*                                                                  *RH
*          NOTE ...                                                *RH
*            IF THE RECORDS ARE LONGER THAN 132 CHARACTERS,        *RH
*            THEY WILL BE PRINTED IN 132 BYTE CHUNKS.              *RH
*                                                                  *RH
*            IF SL HAS BEEN SPECIFIED, THE PRINT OPTION WILL       *RH
*            BE TURNED OFF, AS THEY ARE MUTUALLY EXCLUSIVE.        *RH
*                                                                  *RH
*      HEX - CAUSES RECORDS PRINTED TO BE PRINTED IN A             *RH
*            HEX DUMP FORMAT.                                      *RH
*          - IF THERE IS A //HEXPRINT DD SYSOUT=A DD STATEMENT     *RH
*            IN THE JOB STREAM, THE HEX LISTING WILL BE PRINTED    *RH
*            THERE RATHER THAN ON SYSPRINT WITH THE DEBE           *RH
*            INFORMATION.                                          *RH
*                                                                  *RH
*                                                                  *RH
*   TAPECOPY CAN PRODUCE THE FOLLOWING ABEND CODES:                *RH
*                                                                  *RH
*    U500 - SYSPRINT  COULD NOT BE OPENED. ENSURE THAT THE         *RH
*           SYSPRINT JCL IS CORRECT.                               *RH
*                                                                  *RH
*   U1000 - AN I/O ERROR OCCURRED READING THE MASTER FILE.         *RH
*                                                                  *RH
*   U1111 - AN ERROR OCCURRED PROCESSING A TAPE LABEL              *RH
*                                                                  *RH
*   U1500 - AN I/O ERROR OCCURRED WHEN WRITING TO SYSPRINT.        *RH
*                                                                  *RH
*   U2000 - AN I/O ERROR OCCURRED ON UNIT XXX                      *RH
*                                                                  *RH
*   U3000 - AN ERROR OCCURRED WHEN WRITING TO AN OUTPUT TAPE.      *RH
*                                                                  *RH
*   U4000 - THE MASTER TAPE FILE COULD NOT BE OPENED. VERIFY       *RH
*           THAT THE MASTER JCL IS CORRECT.                        *RH
*                                                                  *RH
*   IF YOU HAVE ANY QUESTIONS OR PROBLEMS CONCERNING TAPECOPY,     *RH
*   PLEASE CONTACT RANDY HALL AT 387-1671                          *RH
*                                                                  *RH
********************************************************************RH
         EJECT
***********************************************************************
*                  REGISTER USAGE
*        R0 - UNUSED
*        R1 - GENERAL CONTROL BLOCK LOOKUP REG. (E.G. TIOT,UCB,ETC).
*        R2 - CONTAINS NUMBER OF OUTPUT TAPES TAKEN FROM TIOT.
*        R3 - SAVE REGISTER FOR R2 FOR USE IN BCT LOOPS.
*        R4 - CONTAINS ADDRESS OF CURRENT DCB
*        R5 - CONTAINS ADDRESS OF CURRENT IOB
*        R6 - CONTAINS ADDRESS OF CURRENT ECB
*        R7 - WORK REGISTER AND LINKAGE REGISTER TO OUTPUT ROUTINES
*        R8 - WORK REGISTER
*        R9 - CONTAINS NUMBER OF 'GOOD' TAPES LEFT. (NO I/O ERRORS)
*       R10 - CONTAINS RETURN ADDRESS FOR ROUTINE AFTER WRITE ROUTINE.
*       R11 - POINTS TO GETMAINED BUFFER
*       R12 - BASE REGISTER
*       R13 - SAVE AREA REGISTER
*       R14 - O/S LINKAGE REGISTER
*       R15 - O/S LINKAGE REGISTER
*
*   NOTE THAT FOR REGISTERS R4,R5,R6 THAT THESE DO NOT NECESSARILY HOLD
*   THE CORRECT ADDRESS FOR THE MASTER DCB,IOB,ECB AS THESE CAN AND ARE
*   USUALLY ADDRESSED DIRECTLY.  BUT FOR THE OUTPUT TAPES, THESE DO
*   CONTAIN THE CORRECT INFORMATION.
***********************************************************************
         EJECT
         GBLA      &MAXTAPE            SPECIFY GLOBAL PARAMETER
&MAXTAPE SETA  2                       MAX NUMBER OF OUTPUT TAPES
TAPECOPY START     0                   START THINGS OFF
*        PRINT     NOGEN
         EQUREGS                       DEFINE SYMBOLIC REGISTERS
         SAVE      (14,12),,*          SAVE REGISTERS
         LR        R12,R15             GET ENTRY POINT ADDRESS
         USING     TAPECOPY,R12        ADDRESSABILITY
         LA        R15,SAVE            CHAIN
         ST        R15,8(R13)           THE
         ST        R13,4(R15)            SAVE
         LR        R13,R15                AREAS
         B     CONTINUE            CONTINUE PROCESSING
SAVE     DC    25F'0'              SAVE AREA
CONTINUE DS    0H
         DROP  R12                 DROP R12 AS BASE
         USING SAVE,R13,R12,R11    SET R13, R12, R11 AS BASE REGS
         LA    R12,4095(0,R13)     LOAD BASE REG R12
         LA    R12,1(0,R12)        *
         LA    R11,4095(0,R12)     LOAD BASE REG R11
         LA    R11,1(0,R11)        *
*
*  NOW PICK UP THE PARM FIELD INFO.
*  ALLOWABLE MODES ARE:
*        1) DUMP - THIS DUMPS INFORMATION ABOUT EACH FILE ON THE
*                  MASTER TAPE OUT ONTO THE SYSPRINT FILE AS EACH
*                  FILE IS COPIED. THE INFORMATION GIVEN CONSISTS
*                  OF SUCH THINGS AS RECORD FORMAT, BLOCKSIZE,ETC.
*
*        2) DEBE - THIS IS THE SAME AS THE DUMP OPTION EXCEPT THAT
*                  NO COPYING IS DONE.  ITS USE IS TO PERFORM
*                  ONE OF THE MAIN USES OF THE DEBE PROGRAM, NAMELY
*                  THAT OF FINDING OUT WHAT IS ON AN UNKNOWN TAPE.
*
*   THE DEFAULT MODE IS DUMP. TO RECEIVE NO FILE INFORMATION
*   WHILE COPYING, CODE 'PARM=NODUMP' ON THE EXEC CARD OF YOUR
*   PROCEDURE.
*   TO RUN IN DEBE MODE, CODE 'PARM=DEBE' AS ABOVE.
*
*   TO CHANGE THE VOLUME SERIAL ON THE OUTPUT VOLUME(S) WHILE
*   COPYING A TAPE, CODE EITHER PARM='NODUMP,XXXXXX' IF YOU
*   WANT THE LABEL CHANGED BUT NO FILE INFORMATION OUTPUTTED,
*   OR ELSE CODE PARM=',XXXXXX' IF DUMPING IS DESIRED.  IN EITHER
*   CASE, THE OUTPUT TAPES WILL HAVE A NEW VOLUME SERIAL OF 'XXXXXX'.
*   (N.B. NOT THAT IT REALLY NEEDS MENTIONING, BUT IF YOU CODE
*   PARM=',      ', THEN THE VOLSER WILL NOT BE CHANGED SINCE
*   THE PROGRAM CHECKS A SIX BYTE FIELD (LABELED 'NEWVOLID') FOR
*   BEING SOMTHING DIFFERENT FROM SIX BLANKS AND IN THAT CASE USES
*   THE NONBLANK VALUE FOR THE NEW VOLID.  SEE, I SAID IT WASN'T
*   WORTH MENTIONING.)
*
         L         R1,0(R1)            GET PARM FIELD ADDRESS       RH
         LH        R2,0(R1)            GET PARM FIELD LENGTH        RH
         LTR       R2,R2               LENGTH OF ZERO               RH
         BZ        TRYPRT              YES, SO NO PARM FIELD        RH
         LA    R1,2(0,R1)          R1 -> TO PARMS                   RH
         LA    R8,0(R2,R1)         R8 -> TO END OF PARMS            RH
         SPACE 1                                                    RH
A10NODMP DS    0H                                                   RH
         CLC   0(6,R1),NODUMP1     PARM = 'NODUMP' ?                RH
         BNE   A11HEX              NO, LOOK FOR HEX                 RH
         NI    SW1,255-$DUMP       YES, TURN OFF DUMP SWITCH        RH
         LA    R1,7(0,R1)          R1 -> TO NEXT PARM               RH
         B     A80TEST             SEE IF END OF PARMS              RH
         SPACE 1                                                    RH
A11HEX   DS    0H                                                   RH
         CLC   0(3,R1),HEX         PARM = 'HEX' ?                   RH
         BNE   A20DEBE             NO, LOOK FOR DEBE                RH
         OI    SW2,X'04'           YES, TURN ON HEX SWITCH          RH
         MVC   FHEXHD,HEXHEAD     PUT RECNO AND LRECL IN HEADING    RH
         LA    R1,4(0,R1)          R1 -> TO NEXT PARM               RH
         B     A80TEST             SEE IF END OF PARMS              RH
         SPACE 1                                                    RH
A20DEBE  DS    0H                                                   RH
         CLC   0(4,R1),DEBE        PARM = 'DEBE' ?                  RH
         BNE   A30NEWVL            NO, LOOK FOR NEWVOL              RH
         NI    SW1,255-$DUMP       YES, TURN OFF DUMP SWITCH        RH
         OI    SW1,$DEBE           YES, TURN ON DEBE SWITCH         RH
         LA    R1,5(0,R1)          R1 -> TO NEXT PARM               RH
         B     A80TEST             SEE IF AT END OF PARMS           RH
         SPACE 1                                                    RH
A30NEWVL       DS                  0H                               RH
         CLC   0(7,R1),NEWVOL      PARM = 'NEWVOL=' ?               RH
         BNE   A40COPY             NO, LOOK FOR COPY PARM           RH
         MVC   NEWVOLID,7(R1)      SAVE NEW VOL ID                  RH
         LA    R1,14(0,R1)         R1 -> TO NEXT PARM               RH
         B     A80TEST             SEE IF AT END OF PARMS           RH
         SPACE 1                                                    RH
A40COPY  DS    0H                                                   RH
         CLC   0(5,R1),COPY        PARM = 'COPY=' ?                 RH
         BNE   A50DUMP             NO, SEE IF DUMP                  RH
         OI    CPYFLG,X'80'        SAY COPY PARM SUPPLIED           RH
         LA    R1,5(0,R1)          R1 -> TO COPY PARM               RH
         CLI   0(R1),C'('          COPY START,STOP ?                RH
         BNE   A43STOP             NO, JUST STOP                    RH
         MVI   CPARM,C'0'          SET CPARM TO 0                   RH
         MVC   CPARM+1(7),CPARM    *                                RH
         LA    R1,1(0,R1)          R1 -> TO START PARM              RH
         LR    R3,R1               R3 -> TO START PARM              RH
         LA    R4,CPARM+4          R4 -> TO WHERE START PARM GOES   RH
         SPACE 1                                                    RH
A41LOOP  DS    0H                                                   RH
         CR    R3,R8               AT END OF PARMS ?                RH
         BE    A42S1               YES, END OF LOOP                 RH
         CLI   0(R3),C','          END OF START PARM ?              RH
         BE    A42S1               YES                              RH
         MVC   0(1,R4),0(R3)       MOVE START BYTE TO CPARM         RH
         LA    R4,1(0,R4)          R4 -> TO NEXT START BYTE         RH
         LA    R3,1(0,R3)          R3 -> TO NEXT START CHAR         RH
         B     A41LOOP             SEE IF END                       RH
         SPACE 1                                                    RH
A42S1    DS    0H                                                   RH
         SR    R3,R1               R3 = LTH OF START PARM           RH
         LA    R4,CPARM(R3)        R4 -> TO 4 BYTE START PARM       RH
         MVC   STARTCPY,0(R4)      SAVE START PARM                  RH
         LA    R1,1(R3,R1)         R1 -> TO STOP PARM               RH
         SPACE 1                                                    RH
A43STOP  DS    0H                                                   RH
         LR    R3,R1               R3 -> TO STOP PARM               RH
         MVI   CPARM,C'0'          ZERO CPARM                       RH
         MVC   CPARM+1(7),CPARM    *                                RH
         LA    R4,CPARM+4          R4 -> TO WHERE STOP PARM GOES    RH
         SPACE 1                                                    RH
A44LOOP  DS    0H                                                   RH
         CR    R3,R8               AT END OF PARMS ?                RH
         BE    A45S1               YES, END OF LOOP                 RH
         CLI   0(R3),C','          END OF STOP PARM ?               RH
         BE    A45S1               YES                              RH
         CLI   0(R3),C')'          END OF START / STOP ?            RH
         BE    A45S1               YES                              RH
         MVC   0(1,R4),0(R3)       SAVE STOP DIGIT                  RH
         LA    R4,1(0,R4)          R4 -> TO NEXT SAVE AREA          RH
         LA    R3,1(0,R3)          R3 -> TO NEXT STOP CHAR          RH
         B     A44LOOP             SEE IF END                       RH
         SPACE 1                                                    RH
A45S1    DS    0H                                                   RH
         SR    R3,R1               R3 = LTH OF STOP PARM            RH
         LA    R4,CPARM(R3)        R4 -> TO 4 BYTE STOP PARM        RH
         MVC   STOPCPY,0(R4)       SAVE STOP PARM                   RH
         LA    R1,1(R3,R1)         R1 -> TO NEXT PARM               RH
         CLI   0(R3),C','          IS IT A COMMA ?                  RH
         BNE   A80TEST             NO, GO SEE IF AT END OF PARMS    RH
         LA    R1,1(0,R1)          YES, R1 -> TO NEXT PARM          RH
         B     A80TEST             SEE IF AT END OF PARMS           RH
         SPACE 1                                                    RH
A50DUMP  DS    0H                                                   RH
         CLC   0(4,R1),DUMP        PARM = 'DUMP' ?                  RH
         BNE   A60SEQ              NO, SEE IF NEWSEQ                RH
         OI    SW1,$DUMP           TURN DUMP SWITCH ON              RH
         LA    R1,5(0,R1)          R1 -> TO NEXT PARM               RH
         B     A80TEST            SEE IF AT END OF PARM             RH
         SPACE 1                                                    RH
A60SEQ   DS    0H                                                   RH
         CLC   0(7,R1),NEWSEQ     PARM = 'NEWSEQ=' ?                RH
         BNE   A70EOF             NO, SEE IF IGNOREOF               RH
         LA    R1,7(0,R1)         R1 -> TO NEW SEQ NBR              RH
         MVI   CPARM,C'0'          SET CPARM TO 0                   RH
         MVC   CPARM+1(7),CPARM    *                                RH
         LR    R3,R1               R3 -> TO SEQ PARM                RH
         LA    R4,CPARM+4          R4 -> TO WHERE SEQ PARM GOES     RH
         LA    R5,4                R5 = MAX NBR CHARS               RH
         SPACE 1                                                    RH
A61LOOP  DS    0H                                                   RH
         CR    R3,R8               AT END OF PARMS ?                RH
         BE    A62S1               YES, END OF LOOP                 RH
         CLI   0(R3),C','          END OF SEQ PARM ?                RH
         BE    A62S1               YES                              RH
         BCT   R5,A61LOOP2        IF NOT MAX, MOVE TO CPARM         RH
         B     PARMERR            BAD PARM                          RH
         SPACE 1                                                    RH
A61LOOP2 DS    0H                                                   RH
         MVC   0(1,R4),0(R3)       MOVE SEQ BYTE TO CPARM           RH
         LA    R4,1(0,R4)          R4 -> TO NEXT SEQ BYTE           RH
         LA    R3,1(0,R3)          R3 -> TO NEXT SEQ CHAR           RH
         B     A61LOOP            GO GET NEXT CHAR                  RH
         SPACE 1                                                    RH
A62S1    DS    0H                                                   RH
         SR    R3,R1               R3 = LTH OF SEQ PARM             RH
         LA    R4,CPARM(R3)        R4 -> TO 4 BYTE SEQ PARM         RH
         PACK  STARTSEQ,0(4,R4)   CONVERT NEW SEQ NBR TO DECIMAL    RH
         LA    R1,1(R3,R1)         R1 -> TO NEXT PARM               RH
         B     A80TEST             SEE IF AT END OF PARMS           RH
         SPACE 1                                                    RH
A70EOF   DS    0H                                                   RH
         CLC   0(8,R1),IGNOREOF   PARM = 'IGNOREOF' ?               RH
         BNE   A71TEST            NO, SEE IF TEST                   RH
         OI    SW2,X'40'          YES, SET UP TAPECOPY TO           RH
*                                 IGNORE 1ST DOUBLE TAPE MARK AND   RH
*                                 ECB/IOB ERRORS                    RH
         LA    R1,9(0,R1)         R1 -> TO NEXT PARM                RH
         B     A80TEST            SEE IF AT END OF PARMS            RH
         SPACE 1                                                    RH
A71TEST  DS    0H                                                   RH
         CLC   0(4,R1),TEST       PARM = 'TEST' ?                   RH
         BNE   A72SL              NO, SEE IF SL                     RH
         MVI   TAPEMSG+1,X'00'    SAY WE WANT TAPE MARK MESSAGES    RH
         MVI   TAPEMSG2+1,X'00'   SAY WE WANT TAPE MARK MESSAGES    RH
         MVI   PRTHDR+1,X'F0'     SAY WE WANT LABEL RECORDS PRINTED RH
         LA    R1,5(0,R1)         R1 -> TO NEXT PARM                RH
         B     A80TEST            SEE IF AT END OF PARM             RH
         SPACE 1                                                    RH
A72SL    DS    0H                                                   RH
         CLC   0(2,R1),SL         PARM = 'SL' ?                     RH
         BNE   A74PRINT           NO, SEE IF PRINT PARM             RH
         OI    SW2,X'20'          SAY CREATE SL OUTPUT TAPE         RH
         LA    R1,3(0,R1)         R1 -> TO NEXT PARM                RH
         B     A80TEST            SEE IF AT END OF PARM             RH
         SPACE 1                                                    RH
A74PRINT DS    0H                                                   RH
         CLC   0(6,R1),PRINTEQ    IS IT PRINT= PARM ?               RH
         BNE   A77RENAM           NO, SEE IF RENAME PARM            RH
         LA    R1,6(0,R1)         R1 -> TO PRINT KEYWORD            RH
         OI    SW2,X'10'          SAY PRINT PARM SUPPLIED           RH
         CLC   0(3,R1),ALL        PRINT ALL RECORDS ?               RH
         BNE   A75                NO, GO SET UP PRINT PARM          RH
         MVC   PRECS,PALL         YES, SET PARM TO SAY PRINT ALL    RH
         LA    R1,4(0,R1)         R1 -> TO NEXT PARM                RH
         B     A80TEST            SEE IF AT END OF PARM             RH
         SPACE 1                                                    RH
A75      DS    0H                                                   RH
         LR    R3,R1               R3 -> TO PRINT PARM              RH
         LA    R4,CPARM+4          R4 -> TO WHERE PRINT PARM GOES   RH
         LA    R5,4                R5 = MAX NBR CHARS               RH
         SPACE 1                                                    RH
A75LOOP  DS    0H                                                   RH
         CR    R3,R8               AT END OF PARMS ?                RH
         BE    A75S1               YES, END OF LOOP                 RH
         CLI   0(R3),C','          END OF PRINT PARM ?              RH
         BE    A75S1               YES                              RH
         BCT   R5,A75LOOP2        IF NOT MAX, MOVE CHAR TO CPARM    RH
         B     PARMERR            SAY BAD PARM                      RH
         SPACE 1                                                    RH
A75LOOP2 DS    0H                                                   RH
         MVC   0(1,R4),0(R3)       MOVE PRINT BYTE TO DWORD         RH
         LA    R4,1(0,R4)          R4 -> TO NEXT PRINT BYTE         RH
         LA    R3,1(0,R3)          R3 -> TO NEXT PRINT CHAR         RH
         B     A75LOOP            GO GET NEXT CHAR                  RH
         SPACE 1                                                    RH
A75S1    DS    0H                                                   RH
         SR    R3,R1               R3 = LTH OF PRINT PARM           RH
         LA    R4,CPARM(R3)        R4 -> TO 4 BYTE PRINT PARM       RH
         PACK  DWORD,0(4,R4)      CONVERT PRINT NBR TO DECIMAL      RH
         CVB   R5,DWORD           CONVERT TO BINARY                 RH
*        BCTR  R5,0               -1 BECAUSE FOR SOME STRANGE       RH
*                                 REASON WE PRINT PRECS+1 RECS      RH
         ST    R5,PRECS           SAVE NBR OF RECORDS TO PRINT      RH
         LA    R1,1(R3,R1)         R1 -> TO NEXT PARM               RH
         B     A80TEST             SEE IF AT END OF PARMS           RH
         SPACE 1                                                    RH
A77RENAM DS    0H                                                   RH
         CLC   0(6,R1),=C'RENAME' PARM = 'RENAME' ?                 RH
         BNE   PARMERR            NO, PARM ERROR                    RH
         OI    SW2,X'02'          SAY RENAME SL OUTPUT TAPE         RH
         LA    R1,7(0,R1)         R1 -> TO NEXT PARM                RH
         B     A80TEST            SEE IF AT END OF PARM             RH
         SPACE 1                                                    RH
A80TEST  DS    0H                                                   RH
         CR    R1,R8               AT END OF PARM ?                 RH
         BL    A10NODMP            NO, GO FIND NEXT PARM            RH
         TM    SW2,X'20'+X'02'    YES, SHOULD WE OPEN SLPARMS FILE? RH
         BZ    TRYPRT             NO, OPEN SYSPRINT                 RH
         NI    SW2,255-X'10'      IF HE ALSO SPECIFIED PRINT=,      RH
*        BZ    TRYPRT             TURN IT OFF, AS IT DOESN'T WORK   RH
         OPEN  (SLPARMS,(INPUT))  OPEN SLPARMS                      RH
         TM    SLPARMS+48,X'10'   DID IT OPEN ?                     RH
         BO    TRYPRT             YES                               RH
         NI    SW2,255-X'20'      NO, TURN OFF SL FLAG              RH
         B     TRYPRT             OPEN SYSPRINT                     RH
         SPACE 1                                                    RH
PARMERR  OI        SW1,$PARMERR        SIGNAL PARAMETER ERROR
*
*   NOW TRY TO OPEN THE PRINTER IF EITHER THE DUMP OR DEBE
*   OPTIONS WERE SPECIFIED.
*
TRYPRT   DS    0H                                                   RH
*                                                                   RH
*              AS WE ARE OUT OF   BASE REGS, RESET THE REGS         RH
*              FOR THE REST OF    THE PROGRAM                       RH
*                                                                   RH
         DROP  R13,R12,R11        DROP OLD BASE REGS                RH
         BALR  R1,R0              SET UP TEMP BASE REG              RH
         USING *,R1               SET BASE
         L     R13,4(0,R13)       R13 -> TO OS SAVE AREA            RH
         LA    R15,SAVE2          R15 -> TO NEW SAVE AREA           RH
         ST        R15,8(R13)     CHAIN THE NEW                     RH
         ST        R13,4(R15)            SAVE                       RH
         LR        R13,R15                AREAS                     RH
         B     CONTIN2             CONTINUE PROCESSING              RH
SAVE2    DC    25F'0'              SAVE AREA                        RH
CONTIN2  DS    0H                                                   RH
         DROP  R1                  DROP TEMP BASE REG               RH
         USING SAVE2,R13,R12,R10   SET R13, R12, R10 BASE REGS      RH
         LA    R12,4095(0,R13)     LOAD BASE REG R12                RH
         LA    R12,1(0,R12)        *                                RH
         LA    R10,4095(0,R12)     LOAD BASE REG R10                RH
         LA    R10,1(0,R10)        *                                RH
         SPACE 2                   *                                RH
TRYPRT2  DS    0H
         TM    SW2,X'04'          WAS HEX PRINT WANTED ?            RH
         BZ    TRYPRT3            NO, SO DON'T TRY TO OPEN
         USING     IHADCB,R4           ADDRESSABILITY OF DCB DSECT
         LA    R4,HEXPRT          R4 -> TO HEX PRINT DCB
         OPEN      ((R4),OUTPUT)       TRY TO OPEN
         TM        DCBOFLGS,X'10'      DID IT OPEN
         BNO   TRYPRT3            NO, TOO BAD
         ST    R4,HEXPRTFL        PASS PRINTER ADDRESS TO HEX PRINT RH
         MVI   HEXPRTFL,X'80'     TELL PRINTRTN WE HAVE HEX FILE    RH
         SPACE 1                   *                                RH
TRYPRT3  DS    0H
         TM        SW1,$DUMP+$DEBE     DO WE NEED A PRINTER
         BZ        CHEKERR             NO, SO DON'T TRY TO OPEN
         USING     IHADCB,R4           ADDRESSABILITY OF DCB DSECT
         LA        R4,PRT              A(PRINTER DCB)
         OPEN      ((R4),OUTPUT)       TRY TO OPEN
         TM        DCBOFLGS,X'10'      DID IT OPEN
         BNO       BADPRT              NO
         OI        SW1,$PRT            WE HAVE A PRINTER
         MVC   PRTBUF(133),FHEADING MOVE HEADING TO PRINT LINE      RH
         BAL   R7,PRINT           PRINT IT                          RH
         AP    FPAGECTR,=P'1'     INC PAGE COUNT                    RH
         MVC   FPNUM(4),=X'40202020'  EDIT IT                       RH
         ED    FPNUM(4),FPAGECTR      EDIT PAGE NO TO PRINT         RH
         MVI   PRTBUF,C'-'        TRIPLE SPACE NEXT TIME            RH
         AP    LINES,=P'2'        UPDATE LINE COUNT                 RH
         B         CHEKERR             AND ON WE GO
         SPACE 1                                                    RH
BADPRT   TM        SW1,$DEBE           DO WE REALLY NEED IT
         BO        BADDEBE             YES INDEED
         NI        SW1,255-$DUMP       TURN OFF DUMP SWITCH
         WTO       'UNABLE TO OPEN SYSPRINT. DUMP OPTION CANCELLED',   X
               ROUTCDE=11
         B         CHEKTIOT            GO CHECK THROUGH TIOT
CHEKERR  TM        SW1,$PARMERR        DID WE HAVE A PARAMETER ERROR
         BNO       CHEKTIOT            NO, SO GO ON OK
         NI        SW1,255-$PARMERR    TURN OFF ERROR FLAG
         OUTPUT    MSG1                AND OUTPUT ERROR MESSAGE
         B     FINISH             CANCEL TAPECOPY                   RH
         EJECT                                                      RH
*
*  NOW WE SEARCH THROUGH THE TIOT TO FIND ALL DDNAMES STARTING
*  WITH THE LETTERS 'TAPE'. THE NUMBER OF THESE IS COUNTED TO
*  FIND OUT HOW MANY COPIES OF THE MASTER ARE DESIRED.
*  THE TIOT IS ALSO CHECKED FOR THE PRESENCE OF A DDNAME OF 'MASTER'.
*  THIS, OF COURSE, WILL BE THE MASTER TAPE.
*
CHEKTIOT L         R1,16               A(CVT)
         L         R1,0(R1)            A(TCB ADDRESSES)
         L         R1,4(R1)            A(OUT TCB)
         L         R1,12(R1)           A(TIOT)
         SR        R2,R2               NUMBER OF TAPES
         LA        R3,24               OFFSET OF FIRST DDNAME ENTRY
         LA        R4,DCBS             A(FIRST DCB)
*
*   NOW CHECK THE NEXT ENTRY IN THE TIOT. AN ENTRY LENGTH OF
*   ZERO SIGNALS THE END.
*
TIOLOOP  AR        R1,R3               GO ON TO NEXT ENTRY
         IC        R3,0(R1)            LENGTH OF ENRTY
         LTR       R3,R3               LENGTH OF ZERO?
         BZ        TIOEND              YES, SO MUST BE END OF TABLE
         CLC       4(8,R1),=CL8'MASTER'  DOES MASTER TAPE EXIST?
         BNE       *+12                NOT YET
         OI        SW1,$MASTER         WE'VE FOUND IT
         B         TIOLOOP             GO CHECK FOR TAPES
         CLC       4(4,R1),=C'TAPE'    NEW TAPE ENTRY?
         BNE       TIOLOOP             NO, SO BACK FOR NEXT ONE
         LA        R2,1(R2)            TAPE#=TAPE#+1
         CH        R2,=H'&MAXTAPE'     TOO MANY TAPES?
         BH        TOOMANY             YES
*
*   WE'VE FOUND A DDTAPE ENTRY, SO PLACE THE FULL DDNAME INTO
*   THE DDNAME FIELD OF THE CURRENT DCB AND THEN INCREMENT THE
*   POINTER TO GO ON TO THE NEXT DCB FOR NEXT TIME.
*
         MVC       DCBDDNAM,4(R1)      MOVE DDNAME INTO DCB
         LA        R4,DCBLEN(R4)       ON TO NEXT DCB
         B         TIOLOOP             BACK FOR MORE
TOOMANY  OUTPUT    MSG2                OUTPUT MESSAGE
         LA        R2,&MAXTAPE         FAKE TO &MAXTAPE COPIES
         EJECT
***********************************************************************
*   WE HAVE NOW SEARCHED THE TIOT AND PULLED OUT ANY DDTAPE NAMES THAT
*   ARE THERE, AND HAVE ALSO TRIED TO FIND A DDMASTER CARD.  WE NOW
*   CHECK TO MAKE SURE THAT A DDMASTER CARD WAS INDEED THERE (I.E. WE
*   REALLY DO HAVE A MASTER TAPE), AND THAT THERE WERE SOME DDTAPE
*   CARDS (I.E. SOME JOKER IS TRYING TO GIVE US A MASTER TAPE BUT
*   NOTHING TO COPY IT ON TO).  IF EITHER TEST FAILS, THE GUY IS BOOTED
*   OFF WITH AN ERROR MESSAGE.  ASSUMING EVERYTHING WENT OK, THE MASTER
*   TAPE DCB IS OPENED, AND THEN I LOOP OPENING THE OUTPUT TAPES.  IF
*   ANY TAPE DOES NOT OPEN, AN ERROR IS GIVEN, GIVING THE DDNAME THAT
*   WOULD NOT OPEN, AND THEN SET THE ECB ADDRESS IN THE CORRESPONDING
*   IOB TO F'0' TO INDICATE THAT EITHER AN I/O ERROR HAD OCCURRED ON
*   THIS UNIT, OR IT WOULD NOT OPEN, AND IN EITHER CASE, IGNORE THIS
*   TAPE DURING EXECUTION, UNLESS IT WAS THE MASTER TAPE, IN WHICH CASE
*   THE PROGRAM CAN GO NO FURTHER WITH A BUM MASTER, SO THE PROGRAM
*   ABENDS AND GIVES A DUMP IN CASE ANYONE IS THRILLED BY CORE DUMPS.
*   (P.S.   BREATHE NOW.)
***********************************************************************
         SPACE
TIOEND   TM        SW1,$MASTER         WAS MASTER TAPE FOUND
         BNO       NOMASTER            NO, SO GO ABORT
         NI        SW1,255-$MASTER     MAKE SWITCH AVAILABLE
         LTR       R2,R2               ANY COPIES TO BE MADE
         BNZ       OK4                 YES, SO FINE
         TM        SW1,$DEBE           0 COPIES ALLOWED IN DEBE MODE
         BNO       NOCOPIES            BUT NO, SO GIVE AN ERROR
         B         OK5                 YES, WE'RE FINE
OK4      TM        SW1,$DEBE           OUTPUT TAPES AND DEBE MODE
         BNO       OK5                 NO, SO FINE
         OUTPUT    MSG3                OUTPUT MESSAGE
         NI        SW1,255-$DEBE       TURN OFF FLAG
         OI        SW1,$DUMP           SIGNAL DUMP OPTION
OK5      LR        R9,R2               R9=NUMBER OF GOOD TAPES LEFT
         LA        R4,MASTER           A(MASTER TAPE DCB)
*                                                                   RH
*             IN ORDER TO AVOID CLOBBERING ANY INFORMATION          RH
*             IN THE TLMS DATABASE RECORDS FOR THE TAPE             RH
*             BEING PROCESSED, DO A RDJFCB, TURN ON THE             RH
*             JFCB BIT SAYING DON'T WRITE THE JFCB BACK,            RH
*             THEN DO AN OPEN TYPE=J.                               RH
*                                                                   RH
         RDJFCB ((4))             READ MASTER JFCB                  RH
         OI    JFCB+52,X'08'      DON'T WRITE JFCB BACK             RH
*        TM    JFCBLTYP,X'11'     OPEN AS BLP OR NL ?               RH
*        BZ    OK6                NO, ASSUME BLKSIZE IS OKAY        RH
*        SETUP DCB JFCB FIELDS  IF NOT SET                          RH
*        OC    JFCB+98(2),JFCB+98 JFCB DSORG SET?                   RH
*        BNZ   *+4+6              YES, DON'T CHANGE IT              RH
*        MVC   JFCB+98(2),=C'PS'  NO, JFCB DSORG = PS               RH
*        CLI   JFCB+100,X'00'     JFCB RECFM SET ?                  RH
*        BNE   *+4+6              YES, DON'T CHANGE IT              RH
*        MVI   JFCB+100,X'90'     NO, JFCB RECFM = FB               RH
*        OC    JFCB+102(2),JFCB+102 JFCB BLKSIZE = 0 ?              RH
*        BNZ   *+4+6              NO, DON'T CHANGE IT               RH
*        MVC   JFCB+102(2),=H'32000' YES, JFCB BLKSIZE = 32000      RH
*        OC    JFCB+104(2),JFCB+104  JFCB LRECL = 0 ?               RH
*        BNZ   *+4+6              NO, DON'T CHANGE IT               RH
*        MVC   JFCB+104(2),=H'80' YES, JFCB LRECL = 80              RH
*        SPACE 1                                                    RH
*K6      DS    0H                                                   RH
         OPEN  TYPE=J,((R4),INPUT) OPEN MASTER TAPE                 RH
*        OPEN  ((R4),INPUT) OPEN MASTER TAPE                        RH
         TM        DCBOFLGS,X'10'      DID DCB OPEN OK
         BNO       MASTERR             NO, SO GO ABORT
*        RDJFCB ((4))             READ MASTER JFCB                  RH
*                                 AFTER OPEN TO GET HDR             RH
*                                 INFORMATION                       RH
         LA    R8,DIOBLK1         PUT A(DIOBLK1)                    RH
         ST    R8,28(0,R4)        INTO MASTER DCB                   RH
         MVC   42(2,R4),=X'D008'  SET MACRF = E                     RH
         TM    JFCBLTYP,X'10'     OPENED AS BLP ?                   RH
         BO    B15                YES, DON'T TRY TO BACKSPACE       RH
         TM    JFCBLTYP,X'01'     OPENED AS NL ?                    RH
         BO    B14                YES, DON'T TRY TO BACKSPACE       RH
         NI    SW2,255-X'20'      MAKE SURE SL PARM FLAG IS OFF     RH
         LA    R8,DJMPMARK        STORE CCW TO BACKSPACE            RH
         ST    R8,CHANADD1        PAST TAPE MARK                    RH
         BAL   R8,DEXCPROG        EXECUTE BACKSPACE                 RH
         LA    R8,DRDBACK         STORE CCW TO READ LABELS          RH
         ST    R8,CHANADD1        BACKWARD TO VOL1                  RH
         CLI   JFCBFLSQ+1,X'01'   DID WE START AT LABEL = 1?        RH
         BH    B11                NO, WE CAN'T GET TO VOL1 THEN     RH
         SPACE 1                                                    RH
B10      DS    0H                                                   RH
         BAL   R8,DEXCPROG        EXECUTE READ                      RH
         CLC   DENDDATA(4),=CL4'VOL1' ARE WE AT VOL1 ?              RH
         BNE   B10                NO, READ BACKWARD NEXT 80 BYTES   RH
         B     B20                BRANCH AROUND SETTING OF FLAG     RH
         SPACE 1                                                    RH
B11      DS    0H                                                   RH
         OI    CPYFLG,X'10'       SAY MASTER LABEL= > 1             RH
         SPACE 1                                                    RH
B12      DS    0H                                                   RH
         BAL   R8,DEXCPROG        EXECUTE READ                      RH
         CLC   DENDDATA(4),=CL4'HDR1' ARE WE AT HDR1 ?              RH
         BNE   B12                NO, READ BACKWARD NEXT 80 BYTES   RH
         SPACE 1                                                    RH
         B     B20                BRANCH AROUND SETTING OF FLAG     RH
         SPACE 1                                                    RH
B14      DS    0H                                                   RH
         OI    CPYFLG,X'04'       SAY MASTER IS NL                  RH
         B     B20                CONTINUE                          RH
         SPACE 1                                                    RH
B15      DS    0H                                                   RH
         OI    CPYFLG,X'40'       SAY MASTER IS BLP                 RH
         SPACE 1                                                    RH
*18      DS    0H                                                   RH
*        LH    R8,JFCLRECL        R8 = JFCB LRECL                   RH
*        LTR   R8,R8              WAS AN LRECL SUPPLIED ON DD ?     RH
*        BZ    B20                NO, USE DEFAULT OF 80             RH
*        CVD   R8,DWORD           CONVERT TO DECIMAL                RH
*        UNPK  PFLRECL,DWORD+5(3) UNPACK FOR PRINT PARM             RH
*        MVZ   PFLRECL+4(1),PFLRECL+3 FIX ZONE                      RH
         SPACE 1                                                    RH
B20      DS    0H                                                   RH
         MVC   FAKEVSER,JFCBVOLS  MOVE VOLSER TO FAKE VOL1 RECORD   RH
         LA    R8,MASTIOB         PUT A(MASTIOB)                    RH
         ST    R8,28(0,R4)        INTO MASTER DCB                   RH
*        CLI   JFCBFLSQ+1,X'01'   DID WE START AT LABEL = 1?        RH
*        BE    OK1                YES, DON'T SET FLAG THEN          RH
*        OI    CPYFLG,X'10'       SAY MASTER LABEL= > 1             RH
         NI    SW2,255-X'01'      MAKE SURE OPTCD=Q FLAG IS OFF     RH
         TM    JFCOPTCD,JFCOPTQ   OPENED AS ASCII TAPE ?            RH
         BZ    OK1                NO, LEAVE FLAG OFF                RH
         OI    SW2,X'01'          TURN OPTCD=Q PARM FLAG ON         RH
         SPACE 1                                                    RH
OK1      DS    0H
         TM        SW1,$DEBE           DO WE NEED OUTPUT TAPES
         BO        GETBUF              NO, SO DON'T TRY TO OPEN THEM
         LR        R3,R2               SAVE NUMBER OF TAPES
         LA        R4,DCBS             A(FIRST DCB)
*
*   NOW TRY TO OPEN THE OUTPUT TAPES
*
OPEN     DS    0H
         MVC   36(1,R4),MASTER+36 OUTPUT RECFM = MASTER RECFM       RH
         MVC   62(2,R4),MASTER+62 OUTPUT LRECL = MASTER LRECL       RH
         MVC   82(2,R4),MASTER+82 OUTPUT BLKSIZE = MASTER BLKSIZE   RH
         SPACE 1                                                    RH
         OPEN      ((R4),OUTPUT)       OPEN COPY TAPE
         TM        DCBOFLGS,X'10'      IS DCB OPEN
         BNO       OPENERR             NO, GIVE ERROR MESSAGE
         ST    R4,DCBADD1         PUT A(OUTPUT DCB) IN IOB          RH
         LA    R8,DIOBLK1         PUT A(DIOBLK1) IOB                RH
         ST    R8,28(0,R4)        IN TAPE DCB                       RH
         MVC   42(2,R4),=X'D008'  SET MACRF = E                     RH
         RDJFCB ((4))             READ OUTPUT FILE JFCB             RH
         TM    JFCOPTCD,JFCOPTQ   OPENED AS ASCII TAPE ?            RH
         BZ    B23                NO, CHECK MASTER                  RH
         TM    SW2,X'01'          IS MASTER TAPE OPTCD=Q?           RH
         BZ    QERROR             NO,  ERROR                        RH
         B     B25                YES, TAPES ARE OKAY               RH
         SPACE 1                                                    RH
B23      DS    0H
         TM    SW2,X'01'          IS MASTER TAPE OPTCD=Q?           RH
         BO    QERROR             YES, ERROR                        RH
         SPACE 1                                                    RH
B25      DS    0H                                                   RH
         TM    JFCBLTYP,X'10'     TAPE FILE OPENED AS BLP ?         RH
         BO    B35                YES, DON'T TRY TO BACKSPACE       RH
         TM    JFCBLTYP,X'01'     TAPE FILE OPENED AS NL ?          RH
         BO    B34                YES, DON'T TRY TO BACKSPACE       RH
         SPACE 1                                                    RH
         TM    CPYFLG,X'04'       MASTER TAPE NL ?                  RH
         BZ    B26                NO, EVERYTHING IS OKAY SO FAR     RH
         TM    SW2,X'20'          YES, DID WE OPEN SLPARMS FILE?    RH
         BZ    NLERROR            NO, ISSUE MSG AND KILL TAPECOPY   RH
         SPACE 1                                                    RH
B26      NOP   B27                B AFTER FIRST PASS                RH
         MVI   B26+1,X'F0'        CHANGE NOP TO B                   RH
         CLI   STARTSEQ+2,X'1C'   WAS NEWSEQ SPECIFIED ?            RH
         BNE   B27                YES, DON'T CHANGE IT              RH
         LH    R8,JFCBFLSQ        R8 = SEQ NBR FROM TAPE1           RH
         CVD   R8,DWORD           CONVERT TO DECIMAL                RH
         ZAP   STARTSEQ,DWORD+5(3) MOVE IT TO STARTSEQ              RH
         SPACE 1                                                    RH
B27      DS    0H                                                   RH
         LA    R8,DJMPMARK        STORE CCW TO BACKSPACE            RH
         ST    R8,CHANADD1        PAST TAPE MARK                    RH
         BAL   R8,DEXCPROG        EXECUTE BACKSPACE                 RH
         LA    R8,DRDBACK         STORE CCW TO READ LABELS          RH
         ST    R8,CHANADD1        BACKWARD TO HDR1                  RH
         SPACE 1                                                    RH
B30      DS    0H                                                   RH
         BAL   R8,DEXCPROG        EXECUTE READ                      RH
         CLC   DENDDATA(4),=CL4'HDR1' ARE WE AT HDR1 ?              RH
         BNE   B30                NO, READ BACKWARD NEXT 80 BYTES   RH
         SPACE 1                                                    RH
B32      DS    0H                                                   RH
         CLC   JFCBDSNM(7),=C'NOLABEL' IS DSN = 'NOLABEL' ?         RH
         BNE   B325               NO, DON'T OVERWRITE VOL1          RH
         TM    CPYFLG,X'20'       IS IT A BLP TAPE ?
         BZ    B325               NO, DON'T BUGGER BCSC SL TAPE
         OI    CPYFLG,X'02'       SAY COPY IS NL                    RH
         MVC   NEWVOLID,=CL6' '   MAKE SURE NO NEW VOLID            RH
         B     B33                GO READ VOL1 RECORD               RH
         SPACE 1                                                    RH
B325     DS    0H                                                   RH
         CLI   NEWVOLID,C' '      NEWVOLID SPECIFIED?               RH
         BE    B40                NO, STAY WHERE WE ARE             RH
         SPACE 1                                                    RH
B33      DS    0H                                                   RH
         CLI   JFCBFLSQ+1,X'01'   DID WE START AT LABEL = 1?        RH
         BE    B335               YES, GO READ VOL1 THEN            RH
         OUTPUT INVLABEL          SAY LABEL NOT 1                   RH
         B     FINISH             NO, WE CAN'T GET TO VOL1 THEN     RH
         SPACE 1                                                    RH
B335     DS    0H                                                   RH
*        LA    R8,DJMPMARK        STORE CCW TO BACKSPACE            RH
*        ST    R8,CHANADD1        PAST TAPE MARK                    RH
*        BAL   R8,DEXCPROG        EXECUTE BACKSPACE                 RH
*        LA    R8,DRDBACK         STORE CCW TO READ LABELS          RH
*        ST    R8,CHANADD1        BACKWARD TO HDR1                  RH
         BAL   R8,DEXCPROG        READ BACK TO VOL1                 RH
         CLC   DENDDATA(4),=CL4'VOL1' ARE WE AT VOL1 ?              RH
         BE    B40              YES, BRANCH AROUND SETTING OF FLAG  RH
         OUTPUT NOVOL1            SAY VOL1 NOT FOUND                RH
         B     FINISH             END OF TAPECOPY                   RH
         SPACE 1                                                    RH
B34      DS    0H                                                   RH
*        MVC   NEWVOLID,=CL6' '   MAKE SURE NO NEW VOLID            RH
         OI    CPYFLG,X'02'       SAY COPY IS NL                    RH
         B     B40                CONTINUE                          RH
*        B     B36                B36 IF CAN CREATE ONLY 1 OUTPUT   RH
         SPACE 1                                                    RH
B35      DS    0H                                                   RH
         CLI   JFCBFLSQ+1,X'01'   IS IT 1,BLP ?                     RH
         BNE   B352               NO, IT'S OKAY                     RH
         OUTPUT BLPMSG            SAY MUST NOT BE 1,BLP             RH
*        B     FINISH             END OF TAPECOPY                   RH
*              REMOVE THE ASTERISK  FROM THE ABOVE CARD             RH
*              TO PREVENT 1,BLP  OUTPUT TAPES, AND CHANGE           RH
*              THE BLPMSG.                                          RH
         SPACE 1                                                    RH
B352     DS    0H                                                   RH
         OI    CPYFLG,X'20'       SAY TAPE IS BLP                   RH
         B     B32                SEE IF NEW VOL REQUIRED           RH
         SPACE 1                                                    RH
B36      DS    0H                                                   RH
         LA    R2,1               CAN ONLY COPY TO 1 TAPE           RH
         LR    R3,R2              *                                 RH
         CLI   NEWVOLID,C' '      IS A NEWVOLID SPECIFIED?          RH
         BE    B40                YES, USE WHATEVER IT IS           RH
         MVC   NEWVOLID,JFCBVOLS  NO, USE TAPE VOLSER               RH
         SPACE 1                                                    RH
B40      DS    0H                                                   RH
         L     R7,IOBPTR          R7 = IOB OFFSET                   RH
         LA    R8,IOBS(R7)        R8 -> TO IOB FOR THIS TAPE        RH
         ST    R8,28(0,R4)        STORE IOBAD IN TAPE DCB           RH
         LA    R7,28(0,R7)        R7 = OFFSET TO NEXT IOB           RH
         ST    R7,IOBPTR          SAVE NEXT IOB OFFSET              RH
         OI        SW1,$OPEN           SHOW AT LEAST ONE OPENED OK
         L     R7,VOLPTR          R7 = OFFSET IN VOL TABLE          RH
         LA    R8,VOLTAB(R7)      R8 -> TO VOL TABLE ENTRY          RH
         MVC   0(6,R8),JFCBVOLS   MOVE VOL TO TABLE                 RH
         LA    R7,6(0,R7)         R7 = OFFSET TO NEXT TABLE ENTRY   RH
         ST    R7,VOLPTR          SAVE NEXT VOL TAB OFFSET          RH
         SPACE 1                                                    RH
NEXTDCB  LA        R4,DCBLEN(R4)       NEXT DCB
         BCT       R3,OPEN             GO OPEN NEXT ONE
         LA    R4,JFCBDSNM        R4 -> TO JFCB DSN                 RH
         LA    R3,JFCBDSNM+43     R3 -> TO END OF JFCB DSN          RH
JFCDSN0  CLI   0(R3),X'40'        END OF DSN ?                      RH
         BNE   JFCDSN1            YES                               RH
         BCTR  R3,0               R3 -> TO NEXT PREV CHAR           RH
         B    JFCDSN0             GO CHECK IT                       RH
         SPACE 1                                                    RH
JFCDSN1  DS    0H                                                   RH
         SH    R3,=H'16'          R3 -> TO LAST 17 CHARS OF DSN     RH
         C     R3,R4              STILL SOME DSN LEFT ?             RH
         BH    JFCDSN2            YES, USE LAST 17 CHARS OF IT      RH
         LA    R3,4(0,R4)         USE WHAT THERE IS AFTER 'JFCB'    RH
         SPACE 1                                                    RH
JFCDSN2  DS    0H                                                   RH
         MVC   NEWDSN,0(R3)       SAVE LAST JFCB DSN IN CASE RENAME RH
         TM        SW1,$OPEN           DID ANY OPEN AT ALL
         BNO       ALLERR              SOMETHING'S SCREWY
         NI        SW1,255-$OPEN       FREE SWITCH
         TIME  DEC                GET DATE                          RH
         STCM  R1,7,JFCBCRDT      PUT DATE IN JFCB                  RH
         UNPK  HDR1CRED+1(5),JFCBCRDT  PUT CREATION DATE IN HDR1    RH
*        MVZ   HDR1CRED+4(1),HDR1CRED+3 FIX ZONE                    RH
         B     GETBUF             GO GET INPUT BUFFER               RH
         SPACE 1                                                    RH
         SPACE 1                                                    RH
OPENERR  MVC       MSG9+26(8),DCBDDNAM DDNAME TO MESSAGE
ERMES1   OUTPUT    MSG9                OUTPUT MESSAGE
         L         R5,DCBIOBAD         A(IOB)
         L         R6,4(R5)            A(ECB)
         MVI       0(R6),X'7F'         FAKE NORMAL COMPLETION
         XC        4(4,R5),4(R5)       SHOW I/O ERROR OR SOMETHING
         B         NEXTDCB             BACK TO TRY MORE
         EJECT
***********************************************************************
*   CORE IS NOW REQUESTED FOR AN INPUT/OUTPUT BUFFER OF 32K BYTES.
*   THE ADDRESS RETURNED FROM GETMAIN IS PLACED INTO THE READ AND WRITE
*   CCWS (AND SINCE THERE IS NO 'STORE ADDRESS' INSTRUCTION ON THIS
*   MACHINE, THE READ AND WRITE OPCODE MUST ALSO BE REPLACED).  THE
*   MASTER TAPE IS THEN READ, THE ECB CHECKED TO SEE IF ANY ERRORS WERE
*   FOUND DURING THE READ, AND AN ERROR ROUTINE ENTERED IF THERE WAS.
*   IF WE ARE IN EITHER DEBE OR DUMP MODE, THE RECORD READ IS CHECKED
*   TO SEE IF IT WAS A VOLUME OR STANDARD HEADER RECORD. IF THIS IS
*   THE CASE, THE INFORMATION IS FORMATTED AND PRINTED OUT. IF WE
*   ARE NOT IN DEBE MODE (I.E. WE DO WANT TO COPY TAPES), THEN NEXT
*   THE LENGTH OF THE RECORD READ IS CALCULATED FROM THE RESIDUAL COUNT
*   FROM THE CSW IN THE IOB,AND THIS LENGTH PLACED IN THE LENGTH FIELD
*   OF THE OUTPUT CCW. ALSO, IF THERE WERE NO I/O ERRORS, THIS MEANS
*   THAT THE LAST THING READ WAS NOT A TAPE MARK, SO A SWITCH IS TURNED
*   OFF TO INDICATE THIS. (N.B. THE PROGRAM ASSUMES THE TAPE TO BE
*   COPIED ENDS WITH TWO TAPE MARKS IN A ROW.)
***********************************************************************
         SPACE
GETBUF   DS    0H                                                   RH
GETBUF2  DS    0H                                                   RH
         GETMAIN   R,LV=32768,SP=2     GET CORE FOR BUFFER
         LR        R11,R1              SAVE BUFFER ADDRESS
         USING     LABELS,R11          ADDRESSABILITY FOR DSECT
         ST        R1,READCCW          INTO READ CCW ADDRESS
         ST        R1,WRITECCW         INTO WRITE CCW ADDRESS
         MVI       READCCW,X'02'       MOVE IN READ OPCODE
         MVI       WRITECCW,X'01'      MOVE IN WRITE OPCODE
*        LA        R10,READ            PROVIDE RETURN ADDRESS
         LA    R1,MASTER           RESET IOB DCB TO READ            RH
         ST    R1,DCBADD1          SENSE BYTES OF MASTER DCB        RH
         LA    R1,READSNS          RESET IOB CCW ADDR TO            RH
         ST    R1,CHANADD1         POINT TO READSNS                 RH
         TM    CPYFLG,X'10'       MASTER LABEL >1 ?                 RH
         BO    GETFAKE            YES, USE FAKE VOL1 RECORD         RH
         TM    CPYFLG,X'04'       IS MASTER TAPE NL ?               RH
         BZ    READ               NO, GO READ VOL1 RECORD           RH
GETFAKE  DS    0H                                                   RH
         MVC   0(80,R11),FAKEVOL1 YES, SET UP FAKE VOL1 RECORD      RH
         SP    #RECORDS,=P'1'     SUBT 1 FROM RECORD COUNT          RH
*                                 TO ACCOUNT FOR FAKE VOL1          RH
         MVC   MASTIOB+14(2),=X'7FAF' SET RESIDUAL CNT TO MAX-80    RH
         B     PVOL1              GO PRINT FAKE VOL1 INFO           RH
         SPACE 1                                                    RH
READ     DS    0H                                                   RH
         LA    R8,10              R8 = NBR OF RETRIES               RH
         SPACE 1                                                    RH
READ2    DS    0H                                                   RH
         XC    MASTECB,MASTECB    RESET ECB                         RH
         EXCP      MASTIOB             READ THE MASTER TAPE
         WAIT      ECB=MASTECB         WAIT FOR IT TO FINISH
         CLI   MASTECB,X'44'      WAS READ INTERCEPTED?             RH
         BNE   READOK             NO, WAS IT READ OKAY ?            RH
         BCT   R8,READ2           NO, RETRY READ UP TO 10 TIMES     RH
         SPACE 1                                                    RH
READOK   DS    0H                                                   RH
         TM        MASTECB,X'7F'       ANY ERRORS
         BNO       READERR             YES
         SPACE 1                                                    RH
PVOL1    DS    0H                                                   RH
*
*   COMPUTE LENGTH OF RECORD JUST READ AND STORE IN OUTPUT CCW
*
         LA        R5,MASTIOB          A(MASTER IOB)
         LH        R7,14(R5)           RESIDUAL COUNT FROM CSW
         LH        R8,=H'32767'        LENGTH OF MAX READ
         SR        R8,R7               GET LENGTH OF DATA READ
         ST        R8,WRITECCW+4       INTO OUTPUT LENGTH
         MVI       WRITECCW+4,$SLI     TURN ON SLI BIT IN CCW
         C         R8,MAXBLOCK         COMPARE BLOCK LENGTHS
         BNH   TESTNLSL           IF LESS OR SAME, BRANCH           RH
*        BNH   TESTRITE           IF LESS OR SAME, BRANCH           RH
         ST        R8,MAXBLOCK         SAVE THIS LARGER VALUE
         SPACE 1                                                    RH
TESTNLSL DS    0H                                                   RH
*                                 FOLLOWING COMMENTED OUT CODE      RH
*                                 DOESN'T WORK.                     RH
*                                 IF IT EVER DOES, COMMENT OUT THE  RH
*                                 GET SLPARMS AFTER LABEL OK9.      RH
*                                                                   RH
*        TM    SW1,$HEADER        ARE WE IN HEADER MODE             RH
*        BNO   TESTRITE           NO, SEE IF WE WANT TO PRINT       RH
*        TM    CPYFLG,X'01'       COPY THIS FILE?                   RH
*        BO    TESTRITE           NO, DON'T READ LABEL FILE         RH
*        TM    SW2,X'20'          DO WE HAVE TO CREATE LABELS?      RH
*        BZ    TESTRITE           NO, CONTINUE                      RH
*        GET   SLPARMS,FILEDATA   READ SLPARMS DATA                 RH
*                                                                   RH
         SPACE 1                                                    RH
TESTRITE DS    0H                                                   RH
         TM    SW2,X'10'          PRINT RECORDS ?                   RH
         BZ    NOPRINT            NO                                RH
         SPACE 1                                                    RH
PRTHDR   NOP   PRTHDR1            B IF LABELS TO BE PRINTED         RH
         CLC   FL1LABI(3),=C'HDR' HEADER RECORDS ?                  RH
         BE    NOPRINT            YES, DON'T PRINT THEM             RH
         CLC   FL1LABI(3),=C'EOF' TRAILER RECORDS ?                 RH
         BE    NOPRINT            YES, DON'T PRINT THEM             RH
         CLC   FL1LABI(3),=C'EOV' VOLUME RECORDS ?                  RH
         BE    NOPRINT            YES, DON'T PRINT THEM             RH
         SPACE 1                                                    RH
PRTHDR1  DS    0H                 LABELS TO BE PRINTED              RH
         TM    SW1,$HEADER        ARE WE IN HEADER MODE             RH
         BNO   GETTYPE            NO, FIND FILE TYPE                RH
         XC    RECSPRTD,RECSPRTD  CLEAR OUT RECORDS PRINTED FIELD   RH
         TM    CPYFLG,X'04'       IS MASTER TAPE NL?                RH
         BZ    CHKHDR11           NO, PROCESS NORMALLY              RH
         SPACE 1                                                    RH
NLVOL1   NOP   NLVOL2             B AFTER 1ST TIME THRU             RH
         MVI   NLVOL1+1,X'F0'     CHANGE NOP TO B                   RH
         B     CHKHDR11           ON WE GO                          RH
NLVOL2   DS    0H                                                   RH
*              THE ABOVE CODE WAS  PUT IN BECAUSE WE DON'T WANT TO  RH
*              SAY FILE NUMBER 1  ON THE VOLUME LABEL               RH
         SPACE 1                                                    RH
*        NI    SW1,255-$HEADER    ALSO NO HEADERS ON NL TAPES       RH
*                                 YES, BUT IF YOU WANT HDR LABELS   RH
*                                 ON THE OUTPUT TAPE WHEN YOU COPY  RH
*                                 TO AN SL TAPE, LEAVE THE $HEADER  RH
*                                 FLAG ON!!!                        RH
         BAL   R7,PRINT           PRINT A BLANK LINE AS A DELIMITER RH
         UNPK  PRTBUF+102(4),STARTSEQ UNPACK SEQ NBR                RH
         MVZ   PRTBUF+105(1),PRTBUF+104 FIX ZONE                    RH
         AP    STARTSEQ,ONE       ADD 1 TO GET NEXT SEQ NBR         RH
         TM    CPYFLG,X'80'        WAS COPY PARM SUPPLIED ?         RH
         BZ    COPYALL2            NO, COPY EVERYTHING              RH
         NI    CPYFLG,255-X'01'    TURN ON COPY RECORD FLAG         RH
         CLC   STARTCPY,PRTBUF+104 COPY THIS FILE ?                 RH
         BNH   STOPARM2           YES, SEE IF OVER STOP PARM        RH
         OI    CPYFLG,X'01'        NO, TURN OFF COPY FLAG           RH
         B     OK9                TURN OFF HEADER MODE              RH
*        B     READ                READ NEXT RECORD                 RH
         SPACE 1                                                    RH
STOPARM2 DS    0H                                                   RH
         CLC   PRTBUF+102(4),STOPCPY END OF COPY ?                  RH
         BH    DEOR                YES, END OF TAPECOPY             RH
         SPACE 1                                                    RH
COPYALL2 DS    0H                                                   RH
         MVI   PRTBUF+30,C'-'     SET UP AN EYE CATCHER             RH
         MVC   PRTBUF+31(59),PRTBUF+30  ''                          RH
         MVC   PRTBUF+90(11),=C'FILE NUMBER'                        RH
         BAL       R7,PRINT            PRINT IT                     RH
*        B     CHKHDR11           PRINT THESE RECORDS               RH
*        B     OK9                TURN OFF HEADER MODE              RH
         SPACE 1                                                    RH
CHKHDR11 DS    0H                                                   RH
         PACK  DWORD,LRECL80      YES, SET LRECL TO 80              RH
         ST    R9,SAVE9           SAVE NBR OF //TAPE DD'S           RH
         XC    RECSPRTD,RECSPRTD  CLEAR OUT RECORDS PRINTED FIELD   RH
         B     DWORDSET           GO PRINT IT                       RH
         SPACE 1                                                    RH
GETTYPE  DS    0H                                                   RH
         CLC   RECSPRTD,PRECS     PRINTED ALL WE NEED ?             RH
         BNL   NOPRINT            YES                               RH
         ST    R9,SAVE9           SAVE R9                           RH
         CLI   PFTYPE,C'F'        FIXED FILE ?                      RH
         BNE   NOTFIXED           NO                                RH
         PACK  DWORD,PFLRECL(5)   PACK LRECL FROM HDR2 OR DEFAULT   RH
         TM    SW1,$DEBE          IN DEBE MODE ?                    RH
         BO    DWORDSET           YES, NO SLPARMS LRECL             RH
         TM    CPYFLG,X'04'       IS MASTER TAPE NL ?               RH
         BZ    DWORDSET           NO, DWORD IS OKAY                 RH
         PACK  DWORD,FILERECL(5)  YES, PACK LRECL FROM SLPARMS REC  RH
         SPACE 1                                                    RH
DWORDSET DS    0H                                                   RH
         CVB   R5,DWORD           CONVERT TO BINARY                 RH
         SR    R6,R6              CLEAR EVEN REG                    RH
         LR    R7,R8              PUT BLKSIZE IN ODD REG            RH
         DR    R6,R5              DIVIDE BLKSIZE BY LRECL           RH
*        LTR   R7,R7              ANY RECORDS PER BLOCK ?           RH
*        BP    *+4+4              YES                               RH
*        LA    R7,1               NO, SAY ONLY 1                    RH
         ST    R7,RECSPBLK        SAVE RECORDS PER BLOCK            RH
         LR    R9,R7              R9 = RECORDS PER BLOCK            RH
         LR    R8,R5              SAVE LRECL IN R8                  RH
         LR    R3,R11             R3 -> TO BUFFER                   RH
         L     R6,PRECS           R6 = RECORDS TO PRINT             RH
         S     R6,RECSPRTD        R6 = RECORDS LEFT TO BE PRINTED   RH
         BZ    RESET9             JUST IN CASE WE GET 0 HERE        RH
         SPACE 1                                                    RH
PRT1     DS    0H                                                   RH
         TM    SW2,X'04'          PRINT IN HEX ?                    RH
         BZ    NOTHEX1            NO, ON WE GO                      RH
         MVC   FPARMS(4),RECSPRTD SET UP RECORD NUMBER              RH
         ST    R5,FPARMS+4        SET RECORD LENGTH                 RH
         BCTR  R5,0               R5 = HEX LTH FOR PRT3 CODE        RH
*        MVC   FPARMS+6(2),H132   SET RECORD LENGTH                 RH
         LR    R7,R3              R7 -> TO RECORD                   RH
*        LA    R7,PRTBUF          R7 -> TO RECORD                   RH
*        ST    R7,FPARMS+8        SET IT UP FOR PRINTRTN            RH
         LA    R14,HEX2           R14 -> TO RETURN ADDRESS          RH
*        LA    R14,HEX1           R14 -> TO RETURN ADDRESS          RH
         B     CALLPRNT           GO TO HEX PRINT ROUTINE           RH
*        CALL  PRINTRTN,(PRT,FHEADING,FPARMS)                       RH
*        B     HEX1               GET NEXT PART OF RECORD           RH
         SPACE 1                                                    RH
NOTHEX1  DS    0H                                                   RH
         CH    R5,H132            IS LTH OVER 132 ?                 RH
         BNH   PRT15              NO, GO PRINT IT                   RH
         LA    R7,131             R7 = HEX LTH TO PRINT             RH
         EX    R7,MVCDATA         MOVE 132 CHARS TO PRINT LINE      RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
HEX1     DS    0H                                                   RH
         LA    R3,132(R3,0)       R3 -> TO NEXT 132 BYTES OF RECORD RH
H132     EQU   *-2                HALFWORD OF 132                   RH
         SH    R5,H132            R5 = LTH OF REST OF DATA          RH
         B     NOTHEX1            GO PRINT IT                       RH
*        B     PRT1               GO PRINT IT                       RH
         SPACE 1                                                    RH
PRT15    DS    0H                                                   RH
         BCTR  R5,0               R5 = HEX LTH TO PRINT             RH
         EX    R5,MVCDATA         MOVE DATA TO BUFFER               RH
*MVCDATA MVC   PRTBUF+1(0),0(R3)  *                                 RH
*        TM    SW2,X'04'          PRINT IN HEX ?                    RH
*        BZ    NOTHEX2            NO, ON WE GO                      RH
*        MVC   FPARMS(4),RECSPRTD SET UP RECORD NUMBER              RH
*        LA    R7,1(0,R5)         R7 = REAL LTH                     RH
*        ST    R7,FPARMS+4        SET RECORD LENGTH                 RH
*        LA    R7,PRTBUF          R7 -> TO RECORD                   RH
*        ST    R7,FPARMS+8        SET IT UP FOR PRINTRTN            RH
*        LA    R14,HEX2           R14 -> TO RETURN ADDRESS          RH
*        B     CALLPRNT           GO TO HEX PRINT ROUTINE           RH
*        CALL  PRINTRTN,(PRT,FHEADING,FPARMS)                       RH
*        B     HEX2               GET NEXT PART OF RECORD           RH
         SPACE 1                                                    RH
NOTHEX2  DS    0H                                                   RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
HEX2     DS    0H                                                   RH
         LA    R7,1               ADD 1 TO RECORDS PRINTED          RH
         A     R7,RECSPRTD        ''                                RH
         ST    R7,RECSPRTD        ''                                RH
         C     R7,PRECS           PRINTED ALL WE NEED?              RH
         BNL   RESET9             YES, WE DON'T NEED ANY MORE       RH
         BCT   R6,PRT2            PRINT ALL WE NEED                 RH
         BAL   R7,PRINT           PUT IN A BLANK LINE               RH
         B     RESET9             WE DON'T NEED ANY MORE, RESET R9  RH
         SPACE 1                                                    RH
PRT2     DS    0H                                                   RH
         BCT   R9,PRT3            PRINT ALL RECORDS IN BUFFER       RH
*        L     R9,RECSPBLK        NO MORE, ADD TO TOTAL             RH
*        A     R9,RECSPRTD        ''                                RH
*        ST    R9,RECSPRTD        ''                                RH
         B     RESET9             RESET R9                          RH
         SPACE 1                                                    RH
PRT3     DS    0H                                                   RH
         LA    R3,1(R5,R3)        R3 -> TO NEXT RECORD IN BUFFER    RH
         LR    R5,R8              R5 = LRECL                        RH
         B     PRT1               GO PRINT IT                       RH
         SPACE 1                                                    RH
NOTFIXED DS    0H                                                   RH
         CLI   PFTYPE,C'V'        VARIABLE FILE ?                   RH
         BNE   NOTVAR             NO, PRINT WHATEVER IT IS          RH
*        BNE   RESET9             NO, CAN'T HANDLE NOW              RH
         LR    R9,R11             R9 -> TO START OF BUFFER          RH
         AH    R9,0(0,R11)        R9 -> TO END OF BUFFER            RH
         LA    R3,8(0,R11)        R3 -> TO FIRST VARIABLE LTH DATA  RH
         LA    R4,4(0,R11)        R4 -> TO FIRST VARIABLE LTH REC   RH
         LH    R5,0(0,R4)         R5 = LTH OF 1ST VARIABLE RECORD   RH
         L     R6,PRECS           R6 = RECORDS TO PRINT             RH
         S     R6,RECSPRTD        R6 = RECORDS LEFT TO BE PRINTED   RH
         SPACE 1                                                    RH
PRT4     DS    0H                                                   RH
         SH    R5,=H'4'           R5 = LTH OF JUST DATA             RH
         SPACE 1                                                    RH
PRT42    DS    0H                                                   RH
         TM    SW2,X'04'          PRINT IN HEX ?                    RH
         BZ    NOTHEX3            NO, ON WE GO                      RH
         MVC   FPARMS(4),RECSPRTD SET UP RECORD NUMBER              RH
         ST    R5,FPARMS+4        SET RECORD LENGTH                 RH
*        MVC   FPARMS+6(2),H132   SET RECORD LENGTH                 RH
         LR    R7,R3              R7 -> TO RECORD                   RH
*        LA    R7,PRTBUF          R7 -> TO RECORD                   RH
*        ST    R7,FPARMS+8        SET IT UP FOR PRINTRTN            RH
         LA    R14,HEX4           R14 -> TO RETURN ADDRESS          RH
*        LA    R14,HEX3           R14 -> TO RETURN ADDRESS          RH
         B     CALLPRNT           GO TO HEX PRINT ROUTINE           RH
*        CALL  PRINTRTN,(PRT,FHEADING,FPARMS)                       RH
*        B     HEX3               GET NEXT PART OF RECORD           RH
         SPACE 1                                                    RH
NOTHEX3  DS    0H                                                   RH
         CH    R5,H132            IS LTH OVER 132 ?                 RH
         BNH   PRT45              NO, GO PRINT IT                   RH
         LA    R7,132             R7 = HEX LTH TO PRINT             RH
         EX    R7,MVCDATA         MOVE 132 CHARS TO PRINT LINE      RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
HEX3     DS    0H                                                   RH
         LA    R3,132(R3,0)       R3 -> TO NEXT 132 BYTES OF RECORD RH
         SH    R5,H132            R5 = LTH OF REST OF DATA          RH
         B     NOTHEX3            GO PRINT IT                       RH
*        B     PRT42              GO PRINT IT                       RH
         SPACE 1                                                    RH
PRT45    DS    0H                                                   RH
         BCTR  R5,0               R5 = HEX LTH TO PRINT             RH
         EX    R5,MVCDATA         MOVE DATA TO BUFFER               RH
*        TM    SW2,X'04'          PRINT IN HEX ?                    RH
*        BZ    NOTHEX4            NO, ON WE GO                      RH
*        MVC   FPARMS(4),RECSPRTD SET UP RECORD NUMBER              RH
*        LA    R7,1(0,R5)         R7 = REAL LTH                     RH
*        ST    R7,FPARMS+4        SET RECORD LENGTH                 RH
*        LA    R7,PRTBUF          R7 -> TO RECORD                   RH
*        ST    R7,FPARMS+8        SET IT UP FOR PRINTRTN            RH
*        LA    R14,HEX4           R14 -> TO RETURN ADDRESS          RH
*        B     CALLPRNT           GO TO HEX PRINT ROUTINE           RH
*        CALL  PRINTRTN,(PRT,FHEADING,FPARMS)                       RH
*        B     HEX4               GET NEXT PART OF RECORD           RH
         SPACE 1                                                    RH
NOTHEX4  DS    0H                                                   RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
HEX4     DS    0H                                                   RH
         LA    R7,1               ADD 1 TO RECORDS PRINTED          RH
         A     R7,RECSPRTD        ''                                RH
         ST    R7,RECSPRTD        ''                                RH
         C     R7,PRECS           PRINTED ALL WE NEED?              RH
         BNL   RESET9             YES, WE DON'T NEED ANY MORE       RH
         BCT   R6,PRT5            PRINT ALL WE NEED                 RH
         BAL   R7,PRINT           PRINT A BLANK LINE                RH
         B     RESET9             WE DON'T NEED ANY MORE, RESET R9  RH
         SPACE 1                                                    RH
PRT5     DS    0H                                                   RH
         CR    R4,R9              PRINT ALL RECORDS IN BUFFER       RH
         BNL   RESET9             NO MORE, END OF THIS PRINT        RH
         AH    R4,0(0,R4)         R4 -> TO NEXT VARIABLE RECORD     RH
         LA    R3,4(0,R4)         R3 -> TO NEXT VARIABLE DATA       RH
         LH    R5,0(0,R4)         R5 = LTH OF NEXT VARIABLE REC     RH
         B     PRT4               GO PRINT IT                       RH
         SPACE 1                                                    RH
NOTVAR   DS    0H                                                   RH
         L     R7,RECSPRTD        R7 = RECORDS PRINTED              RH
         C     R7,PRECS           PRINTED ALL WE NEED?              RH
         BNL   RESET9             YES, WE DON'T NEED ANY MORE       RH
         TM    SW2,X'04'          PRINT IN HEX ?                    RH
         BZ    NOTHEX5            NO, ON WE GO                      RH
         ST    R7,FPARMS          SET UP RECORD NUMBER              RH
         ST    R8,FPARMS+4        SET RECORD LENGTH                 RH
         LA    R14,HEX5           R14 -> TO RETURN ADDRESS          RH
         LR    R7,R11             R7 -> TO BUFFER                   RH
         SPACE 1                                                    RH
CALLPRNT DS    0H                                                   RH
         ST    R7,FPARMS+8        SET UP RECORD ADDRESS             RH
         ST    R14,0(0,R13)       SAVE RETURN ADDRESS               RH
         L     R15,HEXPRTFL       R15 -> TO PRINT FILE FOR HEX      RH
         CALL  PRINTRTN,((15),FHEADING,FPARMS)                      RH
*        CALL  PRINTRTN,(PRT,FHEADING,FPARMS)                       RH
         MVI   PRTBUF,C' '        CLEAR PRINT LINE                  RH
         MVC   PRTBUF+1(132),PRTBUF ''                              RH
         L     R14,0(0,R13)       GET RETURN ADDRESS                RH
         BR    R14                ON WE GO                          RH
*        B     HEX5               ON WE GO                          RH
         SPACE 1                                                    RH
NOTHEX5  DS    0H                                                   RH
         LR    R5,R8              R5 = LTH OF RECORD                RH
         LR    R3,R11             R3 -> TO RECORD IN BUFFER         RH
         SPACE 1                                                    RH
PRT52    DS    0H                                                   RH
         CH    R5,H132            IS LTH OVER 132 ?                 RH
         BNH   PRT55              NO, GO PRINT IT                   RH
         LA    R7,131             R7 = HEX LTH TO PRINT             RH
         EX    R7,MVCDATA         MOVE 132 CHARS TO PRINT LINE      RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
         LA    R3,132(R3,0)       R3 -> TO NEXT 132 BYTES OF RECORD RH
         SH    R5,H132            R5 = LTH OF REST OF DATA          RH
         B     PRT52              GO PRINT IT                       RH
         SPACE 1                                                    RH
PRT55    DS    0H                                                   RH
         BCTR  R5,0               R5 = HEX LTH TO PRINT             RH
         EX    R5,MVCDATA         MOVE DATA TO BUFFER               RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
HEX5     DS    0H                                                   RH
         LA    R7,1               ADD 1 TO RECORDS PRINTED          RH
         A     R7,RECSPRTD        ''                                RH
         ST    R7,RECSPRTD        ''                                RH
         SPACE 1                                                    RH
RESET9   DS    0H                                                   RH
         L     R9,SAVE9           RELOAD R9                         RH
         SPACE 1                                                    RH
PRNTBLNK DS    0H                                                   RH
*        BAL   R7,PRINT           PRINT A BLANK LINE AS A DELIMITER RH
         SPACE 3                                                    RH
NOPRINT  DS    0H                                                   RH
         NI        SW1,255-$TM         SHOW NO TM READ
*        TM        SW1,$DUMP+$DEBE     WAS EITHER OPTION SPECFIED
*        NOP       OK3                 NO, SO SKIP CODE FOLLOWING   RH
*        BZ        OK3                 NO, SO SKIP CODE FOLLOWING   RH
         AP        #RECORDS,=P'1'      ONE MORE RECORD READ
         TM        SW1,$HEADER         ARE WE IN HEADER MODE
         BNO       OK3                 NO
         EJECT
***********************************************************************
*   THE NEXT SECTION OF CODE ANALYSES THE HEADER LABELS ON THE FILE
*   IF THEY EXIST AND WE ARE IN EITHER DUMP OR DEBE MODE. THE
*   INFORMATION IS FORMATTED AND PRINTED OUT ON THE PRINTER.
***********************************************************************
         SPACE
*
*   CHECK FOR VOLUME LABEL
*
         TM    SW2,X'80'          HAS VOL1 RECORD BEEN PROCESSED ?  RH
         BO    CHKHDR1            YES, SEE IF IT'S A HDR RECORD     RH
         TM    CPYFLG,X'54'       MASTER TAPE NL BLP OR LABEL > 1 ? RH
         BM    FAKEID             YES, SAY WHAT IT USED TO BE       RH
         CLC   VOLLABI(4),=C'VOL1' IS IT VOLID RECORD
         BNE   CHKHDR1            NO, SO TRY FOR HDR1
*
*   MOVE IN VOLUME ID AND CHANGE VOLID ON OUTPUT TAPE IF DESIRED
*
FAKEID   DS    0H                                                   RH
         OI    SW2,X'80'          SAY VOL1 RECORD WAS PROCESSED     RH
         MVC       PRTBUF+1(10),=C'VOLUME ID='
         MVC       PRTBUF+11(6),VOLSERNO  MOVE IN VOLID
         CLC       NEWVOLID,=CL6' '    NEW VOLUME ID WANTED
         BE    OLDVOLS            NO, SO PRINT OLD VOLSERS          RH
         MVC       VOLSERNO,NEWVOLID   MOVE IN NEW VOLUME ID
         MVC       PRTBUF+46(14),=C'NEW VOLUME ID='
         MVC       PRTBUF+60(6),NEWVOLID  TELL HIM
         MVC   PRTBUF+66(17),=C'  OLD VOLUME WAS ' TELL OF OLD VOL  RH
         LA    R8,PRTBUF+84       R8 -> TO WHERE OLD VOLS GO        RH
         B     POLDVOL            PUT OLD VOLS IN LINE              RH
         SPACE 1                                                    RH
OLDVOLS  DS    0H                                                   RH
         TM    SW1,$DEBE          IN DEBE MODE ?                    RH
         BO    OWNER              YES, DON'T PUT IN COPY MSG        RH
         MVC   PRTBUF+46(15),=C'COPIED TO TAPE '                    RH
         LA    R8,PRTBUF+61       R8 -> TO WHERE VOLS GO            RH
POLDVOL  DS    0H                                                   RH
         LA    R7,&MAXTAPE        R7 = MAX ENTRIES                  RH
         LA    R14,VOLTAB         R14 -> TO VOL TABLE               RH
         SPACE 1                                                    RH
VOLLOOP  DS    0H                                                   RH
         CLI   0(R14),X'40'       ANY VOL ?                         RH
         BE    ENDVOLS            NO, END OF TABLE                  RH
         MVC   0(6,R8),0(R14)     MOVE VOL TO PRINT                 RH
         LA    R14,6(0,R14)       R14 -> TO NEXT TABLE ENTRY        RH
         MVI   6(R8),C','         PUT COMMA BESIDE VOL              RH
         LA    R8,7(0,R8)         R8 -> TO WHERE NEXT VOL GOES      RH
         BCT   R7,VOLLOOP         GET NEXT VOL, IF ANY              RH
         SPACE 1                                                    RH
ENDVOLS  DS    0H                                                   RH
         BCTR  R8,0               R8 -> TO LAST COMMA               RH
         MVI   0(R8),C' '         BLANK IT OUT                      RH
         SPACE 1                                                    RH
*
*   MOVE IN OWNER FIELD
*
OWNER    DS    0H                                                   RH
         CLC   VOLOWNER(6),=CL6' ' IS OWNER FIELD BLANK ?           RH
         BE    NOWNER             YES, SKIP MSG                     RH
         SPACE 1                                                    RH
         MVC       PRTBUF+25(6),=C'OWNER='
         MVC       PRTBUF+31(10),VOLOWNER  MOVE IN VOLOWNER FIELD
         SPACE 1                                                    RH
NOWNER   DS    0H                                                   RH
         BAL       R7,PRINT            GO PRINT LINE
         MVI       PRTBUF,C'-'         TRIPLE SPACE NEXT TIME
         AP        LINES,=P'2'         UPDATE LINES/PAGE COUNT
*        TM    CPYFLG,X'02'       OUTPUT TAPE NL ?                  RH
*        BO    READ               YES, WE DONE. GET NEXT RECORD     RH
*        TM    CPYFLG,X'04'       MASTER TAPE NL ?                  RH
*        BO    WRITETM            YES, WRITE TM AFTER VOL1 RECORD   RH
         CLI   NEWVOLID,X'40'     NEW VOLID SPECIFIED ?             RH
         BE    READ               NO, DON'T WRITE VOL1 RECORD       RH
         B         OK2                 ON FOR NEXT RECORD
         EJECT
*
*   NOW CHECK FOR FIRST HEADER RECORD
*
CHKHDR1  DS    0H                                                   RH
         CLC       FL1LABI(4),=C'HDR1' IS IT A HDR1 RECORD
         BNE       CHKHDR2             NO, SO TRY FOR HDR2
         TM    CPYFLG,X'80'        WAS COPY PARM SUPPLIED ?         RH
         BZ    COPYALL             NO, COPY EVERYTHING              RH
         NI    CPYFLG,255-X'01'    TURN ON COPY RECORD FLAG         RH
         CLC   STARTCPY,FL1FILSQ   COPY THIS FILE?                  RH
         BNH   STOPARM             YES, SEE IF OVER STOP PARM       RH
         OI    CPYFLG,X'01'        NO, TURN OFF COPY FLAG           RH
         B     READ                READ NEXT RECORD                 RH
         SPACE 1                                                    RH
STOPARM  DS    0H                                                   RH
         CLC   FL1FILSQ,STOPCPY    END OF COPY ?                    RH
         BH    DEOR                YES, END OF TAPECOPY             RH
         SPACE 1                                                    RH
COPYALL  DS    0H                                                   RH
*
*   PICK UP DSNAME
*
         MVC       PRTBUF+1(7),=C'DSNAME='
         MVC       PRTBUF+8(17),FL1ID  MOVE IN DSNAME
*
*   PICK UP FILE SEQUENCE NUMBER
*
         MVI   PRTBUF+30,C'-'     SET UP AN EYE CATCHER             RH
         MVC   PRTBUF+31(59),PRTBUF+30  ''                          RH
         MVC       PRTBUF+90(11),=C'FILE NUMBER'
         TM    SW1,$DEBE          ARE WE IN DEBE MODE ?             RH
         BO    NOCHANGE           YES, LEAVE SEQ NBR AS IS          RH
         UNPK  FL1FILSQ,STARTSEQ  UNPACK NEW SEQ NBR                RH
         MVZ   FL1FILSQ+3(1),FL1FILSQ+2  FIX ZONE                   RH
         AP    STARTSEQ,ONE       ADD 1 TO GET NEXT SEQ NBR         RH
*                                                                   RH
         TM    SW2,X'02'          RENAME PARM SPECIFIED?            RH
         BZ    NOCHANGE           NO, LEAVE DSN AS IS               RH
         TM    SLPARMS+48,X'10'   SLPARMS FILE OPEN?                RH
         BO    GETPARM            YES, GO READ FILE NAME            RH
         MVC   FL1ID,NEWDSN       MOVE JFCB DSN TO HDR1             RH
         B     SAYNEW             ON WE GO                          RH
         SPACE 1                                                    RH
GETPARM  DS    0H                                                   RH
         GET   SLPARMS,FILEDATA   READ SLPARMS DATA                 RH
         MVC   FL1ID,FILEDSN      MOVE NEW  DSN TO HDR1             RH
         SPACE 1                                                    RH
SAYNEW   DS    0H                                                   RH
         MVC       PRTBUF+35(7),=C'NEWDSN='
         MVC       PRTBUF+42(17),FL1ID  MOVE IN DSNAME
         SPACE 1                                                    RH
NOCHANGE DS    0H                                                   RH
         MVC       PRTBUF+102(4),FL1FILSQ  FILE SEQUENCE NUMBER
         BAL       R7,PRINT            PRINT IT
*
*   SEE IF THE FILE BELONGS TO A GENERATION DATA GROUP
*
         CLC       FL1GNO(6),=CL6' '   ANY GENERATION NAMES
         BE        OK7                 NO
         MVC       PRTBUF+1(49),=C'THIS FILE IS A MEMBER OF A GENERATIO*
               N DATA GROUP.'
         MVC       PRTBUF+51(18),=C'GENERATION NUMBER='
         MVC       PRTBUF+69(4),FL1GNO MOVE IN GENARATION NUMBER
         MVC       PRTBUF+80(15),=C'VERSION NUMBER='
         MVC       PRTBUF+95(2),FL1VNG MOVE IN VERSION NUMBER
         BAL       R7,PRINT            GIVE HIM THE LOW DOWN
*
*   PICK UP CREATION AND EXPIRY DATES
*
OK7      MVC       PRTBUF+1(14),=C'CREATION DATE='
         MVC       PRTBUF+15(5),FL1CREDT+1  FILL IN CREATION DATE
         MVC       PRTBUF+30(12),=C'EXPIRY DATE='
         MVC       PRTBUF+42(5),FL1EXPDT+1  FILL IN EXPIRY DATE
         BAL       R7,PRINT            OUT IT GOES
*
*   CHECK FOR PASSWORD PROTECTION
*
         CLI       FL1FSEC,X'F0'       IS DATA SET PROTECTED
         BE        OK2                 NO
         MVC       PRTBUF+1(30),=C'DATA SET IS PASSWORD PROTECTED'  RH
         BAL       R7,PRINT           LOTS OF THESE BAL'S, AREN'T THERE
         B         OK2                 BRANCH AROUND ALTERNATIVE
         EJECT
*
*   NOW CHECK FOR A HEADER 2 RECORD
*
CHKHDR2  CLC       FL1LABI(4),=C'HDR2' IS IT A HEADER 2 RECORD
         BNE   CHKEOF1            NO, SEE IF EOF1                   RH
         TM    CPYFLG,X'01'       ARE WE COPYING THIS FILE?         RH
         BO    OK9                NO, GO READ NEXT RECORD           RH
*
*   PICK UP RECFM,BLKSIZE, AND LRECL
*
         MVC       PRTBUF+1(14),=C'RECORD FORMAT='
         MVC       PRTBUF+15(1),FL2RECFM  MOVE IN RECORD FORMAT
         CLI   FL2BLKA,C' '       IS FILE BLOCKED OR SPANNED ?      RH
         BE    RECFMOK            NO, IT IS UNBLOCKED, UNSPANNED    RH
         MVI   PRTBUF+16,C'S'     SET RECFM TO SPANNED              RH
         CLI   FL2BLKA,C'S'       IS FILE SPANNED ?                 RH
         BE    RECFMOK            YES                               RH
         MVI   PRTBUF+16,C'B'     SET RECFM TO BLOCKED              RH
         CLI   FL2BLKA,C'B'       IS FILE ONLY BLOCKED?             RH
         BE    RECFMOK            YES                               RH
         MVI   PRTBUF+17,C'S'     NO, SO RECFM = SPANNED ALSO       RH
         SPACE 1                                                    RH
RECFMOK  DS    0H                                                   RH
         MVC   PFTYPE,PRTBUF+15   SAVE RECFM FOR PRINT COMMAND      RH
         MVC       PRTBUF+30(8),=C'BLKSIZE='
         MVC       PRTBUF+38(5),FL2BLKL  MOVE IN BLKSIZE
         MVC       PRTBUF+48(14),=C'RECORD LENGTH='
         MVC       PRTBUF+62(5),FL2LRECL MOVE IN LRECL
         BAL       R7,PRINT          I REFUSE TO COMMENT THESE ANY MORE
         MVC   PFLRECL,FL2LRECL   SAVE LRECL FOR PRINT COMMAND      RH
*
*   PICK UP RECORDING DENSITY
*
         TM    SW1,$DEBE          ARE WE IN DEBE MODE ?             RH
         BO    GETDEN             YES, USE DEN AS IS                RH
         TM    CPYFLG,X'01'       COPY THIS FILE ?                  RH
         BO    GETDEN             NO, DON'T CHANGE DEN THEN         RH
         MVI   FL2DEN,C'4'        DEFAULT TO 6250 BPI               RH
         CLI   DCBS+18,X'D3'      IS IT 6250 ?                      RH
         BE    GETDEN             YES, OUTPUT DENSITY SET THEN      RH
         MVI   FL2DEN,C'3'        DEFAULT TO 1600 BPI               RH
         CLI   DCBS+18,X'C3'      IS IT 1600 ?                      RH
         BE    GETDEN             YES, OUTPUT DENSITY SET THEN      RH
         MVI   FL2DEN,C'2'        DEFAULT TO 800  BPI               RH
         SPACE 1                                                    RH
GETDEN   DS    0H                                                   RH
         MVC       PRTBUF+1(8),=C'DENSITY='
         CLI       FL2DEN,C'4'         IS IT 6250 BPI              RH
         BE        D6250               YES, MOVE IN 6250           RH
         CLI       FL2DEN,C'3'         IS IT 1600 BPI              RH
         BE        D1600               YES, MOVE IN 1600           RH
         CLI       FL2DEN,C'2'         IS IT 800 BPI               RH
         BE        D800                YES, MOVE IN 800            RH
         CLI       FL2DEN,C'1'         IS IT 556 BPI               RH
         BE        D556                YES, MOVE IN 556            RH
         MVC    PRTBUF+9(7),=C'200 BPI'  SAY 200 BPI               RH
         B      OK11                     PRINT IT                  RH
D6250    DS     0H                                                 RH
         MVC    PRTBUF+9(8),=C'6250 BPI'  SAY 6250 BPI             RH
         B      OK11                     PRINT IT                  RH
D1600    DS     0H                                                 RH
         MVC    PRTBUF+9(8),=C'1600 BPI'  SAY 1600 BPI             RH
         B      OK11                     PRINT IT                  RH
D800     DS     0H                                                 RH
         MVC    PRTBUF+9(7),=C'800 BPI'  SAY 800 BPI               RH
         B      OK11                     PRINT IT                  RH
D556     DS     0H                                                 RH
         MVC    PRTBUF+9(7),=C'556 BPI'  SAY 556 BPI               RH
OK11     BAL       R7,PRINT            ***COMMENT***
*
*   PICK UP CARRIAGE CONTROL CHARACTER
*
         CLI       FL2CNTRL,C' '       ANY CARRIAGE CONTROL CHARACTERS
         BE        NOCNTRL                NO
         CLI       FL2CNTRL,C'A'       ASA CONTROL
         BE        ASA                 YES
         MVC       PRTBUF+11(7),=C'MACHINE'   MOVE IN TYPE
         MVC       PRTBUF+19(18),=C'CONTROL CHARACTERS'
         B         OK10                GO TURN OFF HEADER MODE
ASA      MVC       PRTBUF+11(3),=C'ASA'  MOVE IN MODE
         MVC       PRTBUF+15(18),=C'CONTROL CHARACTERS'
         B         OK10                SAME AS ABOVE
NOCNTRL  MVC       PRTBUF+11(2),=C'NO' MOVE IN TYPE
         MVC       PRTBUF+14(18),=C'CONTROL CHARACTERS'
OK10     MVC       PRTBUF+1(9),=C'FILE USES'  PREFIX
         BAL       R7,PRINT            THIS IS NOT A COMMENT
         SPACE 1                                                    RH
CHKEOF1  DS    0H                                                   RH
         CLC   FL1LABI(4),=C'EOF1' IS IT AN EOF1 RECORD ?           RH
         BNE   CHKEOV             NO, SEE IF IT IS AN EOV RECORD    RH
         SPACE 1                                                    RH
CHKEOF12 DS    0H                                                   RH
*        UNPK  DWORD(7),#RECORDS  CONVERT # RECORDS TO DISPLAY      RH
*        OI    DWORD+6,X'F0'      FIX SIGN                          RH
*        CLC   FL1BLKCT,DWORD+1   BLOCKS READ AND EOF COUNT MATCH ? RH
         CLC   FL1BLKCT,EOF1BLKC  BLOCKS READ AND EOF COUNT MATCH ? RH
         BE    OK2                YES, ON WE GO                     RH
*        BE    OK9                YES, ON WE GO                     RH
         BAL   R7,PRINT           PRINT A BLANK LINE                RH
         MVC   PRTBUF+1(LBMSG),BLKCTMSG MOVE MSG TO PRINT           RH
         MVC   PRTBUF+1+BLKCTIN(6),FL1BLKCT MOVE OLD COUNT TO MSG   RH
         MVC   PRTBUF+1+BLKCTOUT(6),EOF1BLKC MOVE NEW COUNT TO MSG  RH
         TM    SW1,$DEBE          ARE WE IN DEBE MODE ?             RH
         BO    CHKEOF13           YES, SAY EOF1 S/B CHANGED         RH
         TM    CPYFLG,X'01'       COPY THIS FILE?                   RH
         BZ    CHKEOF15           YES, SAY EOF1 WAS CHANGED         RH
         SPACE 1                                                    RH
CHKEOF13 DS    0H                                                   RH
         MVC   PRTBUF+1+BLKCTSHB(3),=C'S/B' CHANGE MSG TO S/B       RH
         SPACE 1                                                    RH
CHKEOF15 DS    0H                                                   RH
         BAL   R7,PRINT           SAY WE CHANGED EOF BLOCK COUNT    RH
         BAL   R7,PRINT           PRINT A BLANK LINE                RH
         MVC   FL1BLKCT,EOF1BLKC  CHANGE BLOCK COUNT IN EOF1        RH
         B     OK2                ON WE GO                          RH
         SPACE 1                                                    RH
CHKEOV   DS    0H                                                   RH
         CLC   FL1LABI(3),=C'EOF' IS IT AN EOF RECORD ?             RH
         BE    OK2                YES, ON WE GO                     RH
         CLC   FL1LABI(3),=C'UTL' IS IT A UTL RECORD ?              RH
         BE    OK2                YES, ON WE GO                     RH
         CLC   FL1LABI(3),=C'UHL' IS IT A UHL RECORD ?              RH
         BE    OK2                YES, ON WE GO                     RH
         CLC   FL1LABI(3),=C'EOV' IS IT AN EOV RECORD ?             RH
         BNE   OK9                NO, ON WE GO                      RH
         OI    SW1,$EOF           YES, FORCE END OF FILE            RH
         CLC   FL1LABI(4),=C'EOV1' IS IT EOV1 ?                     RH
         BE    CHKEOF12           YES, CHECK BLOCK COUNT            RH
         SPACE 1                                                    RH
OK9      DS    0H                                                   RH
         NI        SW1,255-$HEADER     TURN OFF HEADER MODE
         XC    RECSPRTD,RECSPRTD  CLEAR OUT RECORDS PRINTED FIELD   RH
         TM    CPYFLG,X'01'       COPY THIS FILE?                   RH
         BO    READ               NO, DON'T CREATE LABELS           RH
         TM    SW2,X'20'          DO WE HAVE TO CREATE LABELS?      RH
         BZ    OK3                NO, CONTINUE                      RH
         GET   SLPARMS,FILEDATA   READ SLPARMS DATA                 RH
         MVC   HDR1ID,=C'HDR1'    SAY IT'S A HDR 1                  RH
         MVC   HDR1DSN,FILEDSN    MOVE DSN TO HDR1                  RH
         MVC   HDR1SEQN,FILESEQN  MOVE SEQ NBR TO HDR1              RH
         MVC   EOF1BLKC,HDR1SEC   SET BLOCK COUNT TO 0 FOR HDR1     RH
         LA    R1,80              R1 = LTH OF HDR1                  RH
         ST    R1,WRITECCW+4      PUT IT IN WRITE CCW               RH
         MVI   WRITECCW+4,$SLI    TURN ON SLI BIT                   RH
         LA    R1,HDR1REC         PUT A(HDR1 RECORD)                RH
         STCM  R1,7,WRITECCW+1    INTO WRITE CCW                    RH
         BAL   R7,RITELOOP        WRITE HDR1'S                      RH
         SPACE 1                                                    RH
         MVC   HDR2ID,=C'HDR2'    SAY IT'S A HDR2 RECORD            RH
         MVC   HDR2RCFM,FILERCFM  MOVE RECFM TO HDR2                RH
         MVC   HDR2BLKS,FILEBLKS  MOVE BLKSIZE TO HDR2 REC          RH
         MVC   HDR2RECL,FILERECL  MOVE RECORD LTH TO HDR2 REC       RH
         MVC   HDR2ATTR,FILEATTR  MOVE BLK ATTR TO HSR2 REC         RH
         LA    R1,HDR2REC         PUT A(HDR2 RECORD) -              RH
         STCM  R1,7,WRITECCW+1    INTO WRITE CCW                    RH
         BAL   R7,RITELOOP        WRITE HDR2 RECORDS                RH
         MVI       WRITECCW,X'1F'      WRITE TM CCW OPCODE
         OI        WRITECCW+4,$CC      TURN ON COMMAND CHAIN BIT
         BAL       R7,RITELOOP         GO WRITE TM
         NI    WRITECCW+4,255-$CC TURN OFF COMMAND CHAIN BIT        RH
         MVI       WRITECCW,X'01'      BACK TO WRITE TAPE OPCODE
*        LA    R10,READ           RESET R10                         RH
         STCM  R11,7,WRITECCW+1   PUT A(BUFFER) BACK INTO WRITE CCW RH
         SPACE 1                                                    RH
OK2      DS    0H                                                   RH
         TM    CPYFLG,X'22'       COPY TO NL OR BLP TAPE ?          RH
         BNZ   READ               YES, DON'T WRITE LABELS           RH
OK3      DS    0H                                                   RH
         TM    CPYFLG,X'01'        COPY THIS RECORD ?               RH
         BO    READ                NO, READ NEXT RECORD             RH
         TM        SW1,$DEBE           IN DEBE MODE                 RH
         BO        READ                YES, SO BYPASS THE WRITE     RH
         BAL   R7,RITELOOP        WRITE DATA RECORDS                RH
         B     READ                READ NEXT RECORD                 RH
         EJECT
***********************************************************************
*   THIS IS THE WRITE LOOP TO WRITE EITHER THE LAST RECORD READ OUT
*   ONTO THE OUTPUT TAPE, OR TO WRITE A TAPE MARK.  BEFORE THE EXCP
*   IS ISSUED ON THE IOB, THE ECB ADDRESS IN THE IOB IS CHECKED TO SEE
*   IF IT IS ZERO.  IF SO, THEN EITHER THE DCB COULD NOT BE OPENED, OR
*   A PERMANENT I/O ERROR HAD OCCURRED PREVIOUSLY ON THAT UNIT.  IN
*   EITHER CASE, THAT IOB IS IGNORED, AND THE LOOP CONTINUES WRITING
*   ON ANY TAPES THAT ARE LEFT.  AFTER THE WRITE LOOP HAS BEEN
*   COMPLETED, A MULTIPLE WAIT IS DONE ON AN ECBLIST UNTIL ALL VALID
*   TAPES HAVE FINISHED WRITING.  THE ECBS ARE THEN CHECKED FOR I/O
*   ERRORS, AND IF THERE ARE ANY, A BRANCH IS MADE TO WRITEERR TO WRITE
*   OUT AN ERROR MESSAGE TELLING WHICH UNIT THE ERROR OCCURRED ON
*   AND ZEROING THE ECB ADDRESS IN THE IOB TO INDICATE THE I/O ERROR SO
*   THIS TAPE WILL NOT BE TRIED TO BE WRITTEN ON AGAIN.
***********************************************************************
         SPACE
RITELOOP DS    0H                                                   RH
         TM    CPYFLG,X'01'       WRITE THIS RECORD ?               RH
         BOR   R7                 NO, GO BACK TO CALLER             RH
         TM    SW1,$HEADER        ARE WE IN HEADER MODE ?           RH
         BZ    WRITE2             NO, GO WRITE RECORD               RH
         TM    CPYFLG,X'02'       OUTPUT TAPE NL ?                  RH
         BOR   R7                 YES, DON'T WRITE HDR/TLR RECORDS  RH
         SPACE 1                                                    RH
WRITE2   DS    0H                                                   RH
         LR        R3,R2               SAVE NUMBER OF TAPES
         LA        R5,IOBS             A(OUTPUT IOBS)
OPENCHK  OC        4(4,R5),4(R5)       IS THE ECB ADDRESS ZERO
         BZ        GOBACK              NO, SO TAPE HAD I/O ERROR
         EXCP      (R5)                WRITE A BLOCK
GOBACK   LA        R5,IOBLEN(R5)       ON TO NEXT IOB
         BCT       R3,OPENCHK          GO CHECK AND WRITE NEXT TAPE
         WAIT      (R9),ECBLIST=ECBLIST  WAIT FOR THINGS TO FINISH
         LR        R3,R2               SAVE NUMBER OF TAPES
         LA        R5,IOBS             POINT TO IOB LIST
         LA        R6,ECBS             CHECK ECB COMPLETION CODES
NEXTCHK  EQU       *
         TM        0(R6),X'7F'         ANY ERRORS
         BNO       WRITEERR            YES
NEXTECB  LA        R6,4(R6)            CHECK NEXT ECB
         LA        R5,IOBLEN(,R5)      POINT TO NEXT IOB
         BCT       R3,NEXTCHK          GO CHECK THE NEXT ECB
         BR        R7                  GO BACK
         EJECT
***********************************************************************
*   WE COME HERE IF WE HAVE HAD AN INPUT ERROR ON THE MASTER TAPE.
*   THE ECB COMPLETION CODE IS CHECKED.  IF THE COMPLETION CODE WAS
*   A X'41' SIGNIFYING A PERMANENT I/O ERROR, THE CSW IN THE IOB IS
*   CHECKED TO SEE IF THE ERROR WAS DUE TO READING A TAPE MARK.
*   IF THIS WAS NOT THE CASE, A TRUE PERMANENT ERROR IS ASSUMED TO
*   HAVE OCCURRED, AN ERROR MESSAGE IS PUT OUT AND THE PROGRAM
*   ABENDS.  IF A TM WAS INDEED READ, A CHECK IS MADE TO SEE IF THE
*   LAST READ ALSO WAS OF ONE, AND IF SO A SWITCH IS SET TO INDICATE
*   THAT END OF FILE WAS REACHED.  OTHERWISE THE SWITCH IS SET TO SHOW
*   THAT ONE WAS READ ANYWAY IN CASE THE NEXT READ WOULD INDICATE EOF.
*   SINCE AN I/O ERROR HAS INDEED OCCURRED, HOWEVER, THE REQUEST
*   QUEUE ELEMENT (RQE) FOR THIS I/O REQUEST HAS BEEN PURGED FROM
*   ITS QUEUE BY THE I/O SUPERVISOR AND MUST BE RESTORED IN ORDER TO
*   CONTINUE PROCESSING. THE RESTORE MACRO IS USED TO DO THIS.
*   A TAPE MARK IS THEN WRITTEN ONTO EACH OF THE OUTPUT TAPES. THE
*   END OF FILE SWITCH IS THEN EXAMINED AND THE PROGRAM TERMINATES
*   IF IT WAS ON.  OTHERWISE WE GO BACK TO READ THE MASTER TAPE AGAIN.
***********************************************************************
         SPACE
READERR  DS    0H                                                   RH
         CLI       MASTECB,X'41'       DO CSW CONTENTS APPLY
         BNE       PRMRDERR            ASSUME PERMANENT READ ERROR
         TM        MASTIOB+12,X'01'    UNIT EXCEPTION BIT ON IN CSW
         BNO       PRMRDERR            NO, SO TM NOT READ
         SPACE 1                                                    RH
*        TM    SW1,$HEADER        ARE WE IN HEADER MODE ?           RH
*        BO    READERR2           YES, LEAVE PFTYPE ALONE           RH
*        MVI   PFTYPE,C'F'        NEXT RECORDS COULD BE LABELS,     RH
*                                 SO SET PFTYPE TO F                RH
         SPACE 1                                                    RH
READERR2 DS    0H                                                   RH
         TM        SW1,$TM             WAS LAST READ A READ OF A TM
         BNO    CHKRTYPE               NO, SO NOT END OF FILE       RH
*                                 TWO TM'S IN A ROW -> END-OF-FILE  RH
*                                 IF WE HAVE 2 TAPE MARKS WITH      RH
*                                 NO DATA IN BETWEEN, TRY TO READ   RH
*                                 ANOTHER FILE AS WE COULD HAVE     RH
*                                 AN EMPTY DATA SET                 RH
         CLC   FL1LABI,=C'HDR'    WERE LAST RECORDS HDR RECORDS     RH
         BE    SETSW2             YES, WE HAVE AN EMPTY DATASET     RH
         CLC   FL1LABI,=C'UHL'    WERE LAST RECORDS UHL RECORDS     RH
         BE    SETSW2             YES, IT COULD STILL BE EMPTY      RH
         SPACE 1                                                    RH
DEOR     DS    0H                                                   RH
         OI        SW1,$EOF            SHOW EOF REACHED
*
*   IGNORE IF LAST THING READ WAS A HEADER OR TRAILER RECORD
*
CHKRTYPE DS     0H                                                  RH
         OI        SW1,$TM             SHOW TM READ
TAPEMSG2 B     NOTAPMS            SKIP PRINT COMMAND IF NOT WANTED  RH
         OUTPUT TMMSG2            SAY TAPE MARK READ          DEBUG RH
         BAL   R7,PRINT           PRINT A BLANK LINE                RH
         SPACE 1                                                    RH
NOTAPMS  DS    0H                                                   RH
         TM    CPYFLG,X'01'       DID WE COPY THIS FILE ?           RH
         BO    ZAP                NO, DON'T PRINT BLOCK COUNT       RH
         MVI   TMMSG+31,C' '      CLEAR LAST BYTE OF 'DATA'         RH
         MVC   TMMSG+28(3),=C'EOF' SET UP TAPE MARK MSG             RH
         CLC       FL1LABI,=C'EOF'     WERE LAST RECORDS TRAILERS
         BE        ZAP                 YES
         MVC   TMMSG+28(3),=C'HDR' SET UP TAPE MARK MSG             RH
         CLC       FL1LABI,=C'HDR'     HEADER RECORDS MAYBE
         BE    TESTSW2                 SO THEY WERE                 RH
         MVC   TMMSG+28(3),=C'EOV' SET UP TAPE MARK MSG             RH
         CLC       FL1LABI,=C'EOV'     EOV TRAILER
         BE        ZAP                 SAME
         MVC   TMMSG+28(3),=C'UTL' SET UP TAPE MARK MSG             RH
         CLC       FL1LABI,=C'UTL'     USER TRAILER LABEL
         BE        ZAP                 YUP
         MVC   TMMSG+28(3),=C'UHL' SET UP TAPE MARK MSG             RH
         CLC       FL1LABI,=C'UHL'     USER HEADER LABEL
         BE    TESTSW2            YES, SEE IF EMPTY FILE            RH
         MVC   TMMSG+28(4),=C'DATA' SET UP TAPE MARK MSG            RH
PTOT     DS    0H
         TM    SW1,$EOF           AT END OF TAPE ?                  RH
         BO    ZAP                YES, DON'T PRINT MSG              RH
         SPACE 1                                                    RH
         MVC       PRTBUF+1(18),=C'NUMBER OF BLOCKS ='
         UNPK      PRTBUF+20(7),#RECORDS  PUT IN NUMBER OF RECORDS
         OI        PRTBUF+26,X'F0'     FIX UP SIGN
*        MVI   EOF1BLKC,C'0'      MOVE BLK COUNT TO EOF1 RECORD     RH
         MVC   EOF1BLKC,PRTBUF+21 IN CASE WE WRITE                  RH
*                                 TRAILER RECORDS                   RH
*                                 MOVE BLK COUNT TO DCB             RH
*                                                                   RH
*        TO PUT A BLOCK COUNT IN THE DCB, UNCOMMENT                 RH
*        THE FOLLOWING 3 LINES                                      RH
*                                                                   RH
*        MVC   DWORD+4(4),#RECORDS PUT RECORD COUNT IN DWORD        RH
*        CVB   R7,DWORD           CONVERT TO BINARY                 RH
*        ST    R7,DCBBLKCT        PUT BLOCK COUNT IN DCB            RH
         SPACE 1                                                    RH
         MVC       PRTBUF+28(17),=C'MAXIMUM LENGTH = '     MOVE MSG
         L         R7,MAXBLOCK         LOAD THE MAX(<=65536)
         CVD       R7,MAXBLOCK         STORE (F'0',P'0065536')
         UNPK      PRTBUF+45(5),MAXBLOCK+5(3)    UNPACK THE LENGTH
         OI        PRTBUF+49,C'0'      FIX THE LAST DIGIT
         BAL       R7,PRINT            THOUGHT THESE WERE DONE, EH?
         CLI       PRTBUF,C' '         DON''T TRIPLE SPACE IF
         BNE       ZAP                 NEXT LINE IS NOT SINGLE-SPACE
         MVI       PRTBUF,C'-'         AND SKIP 3 LINES
         AP        LINES,=P'2'         UPDATE LINES/PAGE COUNT
         BAL   R7,CPLINES         AT END OF PAGE ?                  RH
         SPACE 1                                                    RH
ZAP      DS    0H
         ZAP       #RECORDS,=P'0'      RESET RECORD COUNT
*        SPACE 1                                                    RH
         LA        R4,MASTER           A(MASTER DCB)
         L         R1,DCBDEBAD         A(MASTER DEB)
         LA        R1,16(,R1)          A(USER PURGE LIST)
         NI        DCBIFLGS,X'3F'      TURN OFF ERROR FLAGS
         RESTORE   (1)                 RESTORE RQE TO CHAIN
         CLC   TMMSG+28(3),=C'HDR' DID WE JUST READ HDR RECORDS     RH
         BE    NOTHDR             YES, DON'T SET TO HEADING MODE    RH
         OI        SW1,$HEADER         SET TO HEADING MODE
         MVI   PFTYPE,C'F'        LABEL RECORDS ARE FIXED           RH
         SPACE 1                                                    RH
NOTHDR   DS    0H                                                   RH
         TM        SW1,$DEBE           IN DEBE MODE
         BO        TESTEOF             YES, SO DON'T WRITE TAPE MARKS
         TM    CPYFLG,X'22'        OUTPUT TAPE NL OR BLP ?          RH
         BZ    WRITETM             NO, GO WRITE TAPE MARK           RH
         TM    SW1,$EOF            END OF TAPE ?                    RH
         BO    WRITETM             YES, WRITE TAPE MARK             RH
         CLC   TMMSG+28(4),=C'DATA' DID WE JUST WRITE DATA RECORDS  RH
         BNE   TESTEOF             NO, DON'T WRITE TAPE MARK        RH
         SPACE 1                                                    RH
WRITETM  DS    0H                                                   RH
         MVI       WRITECCW,X'1F'      WRITE TM CCW OPCODE
         OI        WRITECCW+4,$CC      TURN ON COMMAND CHAIN BIT
         BAL       R7,RITELOOP         GO WRITE TM
         SPACE 1                                                    RH
TAPEMSG  B     NOTAPEMS           SKIP PRINT COMMAND IF NOT WANTED  RH
         OUTPUT TMMSG             SAY TAPE MARK WAS WRITTEN   DEBUG RH
         BAL   R7,PRINT           PRINT A BLANK LINE                RH
         SPACE 1                                                    RH
NOTAPEMS DS    0H                                                   RH
         NI    WRITECCW+4,255-$CC TURN OFF COMMAND CHAIN BIT        RH
TESTEOF  TM        SW1,$EOF            HAD WE REACHED EOF
         BO        TESTIGNR       YES, SEE IF IGNORE FIRST EOF      RH
         TM    CPYFLG,X'01'       DID WE COPY THIS FILE ?           RH
         BO    NEXTEOF            NO, DON'T CREATE TRAILER LABELS   RH
         TM    SW2,X'20'          CREATE TRAILER LABELS ?           RH
         BZ    NEXTEOF            NO, GO TO GET NEXT FILE           RH
         SPACE 1                                                    RH
         MVC   HDR1ID,=C'EOF1'    SAY IT'S AN EOF 1 RECORD          RH
         MVC   HDR1DSN,FILEDSN    MOVE DSN TO HDR1                  RH
         LA    R1,80              R1 = LTH OF HDR1                  RH
         ST    R1,WRITECCW+4      PUT IT IN WRITE CCW               RH
         MVI   WRITECCW+4,$SLI    TURN ON SLI BIT                   RH
         LA    R1,EOF1REC         PUT A(EOF1 RECORD)                RH
         STCM  R1,7,WRITECCW+1    INTO WRITE CCW                    RH
         MVI   WRITECCW,X'01'     BACK TO WRITE TAPE OPCODE         RH
         BAL   R7,RITELOOP        WRITE HDR1'S                      RH
         SPACE 1                                                    RH
         MVC   HDR2ID,=C'EOF2'    SAY IT'S AN EOF2 RECORD           RH
         LA    R1,EOF2REC         PUT A(EOF2 RECORD) -              RH
         STCM  R1,7,WRITECCW+1    INTO WRITE CCW                    RH
         BAL   R7,RITELOOP        WRITE EOF2 RECORDS                RH
         MVI   WRITECCW,X'1F'     WRITE TM CCW OPCODE               RH
         OI    WRITECCW+4,$CC     TURN ON COMMAND CHAIN BIT         RH
         BAL   R7,RITELOOP        GO WRITE TM                       RH
         STCM  R11,7,WRITECCW+1   PUT A(BUFFER) BACK INTO WRITE CCW RH
         SPACE 1                                                    RH
NEXTEOF  DS    0H                                                   RH
         NI    WRITECCW+4,255-$CC TURN OFF COMMAND CHAIN BIT        RH
         MVI       WRITECCW,X'01'      BACK TO WRITE TAPE OPCODE
*        LA        R10,READ            FIX UP RETURN REGISTER
*        BR        R10                 GO READ SOME MORE
         B     READ                    GO READ SOME MORE            RH
         SPACE 1                                                    RH
SETSW2   DS    0H                                                   RH
         OI    SW2,$HEADER        TURN ON HEADER SWITCH             RH
         B     CHKRTYPE           GO CHECK RECORD TYPE              RH
         SPACE 1                                                    RH
TESTSW2  DS    0H                                                   RH
         TM    SW2,$HEADER        WAS IT AN EMPTY FILE?             RH
         BZ    ZAP                NO, CONTINUE                      RH
         NI    SW2,255-$HEADER    YES, TURN SW2 OFF                 RH
         B     PTOT               SAY 0 RECORDS                     RH
         SPACE 1                                                    RH
TESTIGNR DS    0H                                                   RH
         TM    SW2,X'40'          IGNORE FIRST EOF ?                RH
         BZ    FINISH             NO, END OF TAPECOPY               RH
         NI    SW2,255-X'40'      YES, TURN SWITCH OFF              RH
         NI    SW1,255-($EOF+$TM) TURN OFF EOF & TAPE MARK SWITCH   RH
         OUTPUT EOFMSG            SAY WE IGNORED EOF HERE           RH
********************************************************************RH
*        THE FOLLOWING CODE HAS BEEN COMMENTED OUT. IT CAN BE       RH
*        BE USED FOR DEBUGGING PURPOSES TO COUNT THE NUMBER OF      RH
*        TIMES THIS ROUTINE HAS BEEN ENTERED. IT SHOULD BE          RH
*        ENABLED IF THE IGNOREOF SWITCH IS NOT TURNED OFF SO        RH
*        THE WTOR DOES NOT GET SENT TO THE OPERATOR MORE THAN       RH
*        ONCE.                                                      RH
*                                                                   RH
*        L     R8,EOFCTR          R8 = NBR OF TIMES IN HERE         RH
*        LA    R8,1(0,R8)         + 1 FOR THIS TIME                 RH
*        ST    R8,EOFCTR          SAVE NBR OF TIMES IN HERE         RH
*        C     R8,=F'1'           FIRST TIME IN HERE?               RH
*        BE    WTOR2              YES, SEND MSG                     RH
*        C     R8,=F'7'           IN HERE 7 TIMES ALREADY?          RH
*        BH    FINISH             YES, LETS QUIT THEN               RH
*        B     NEXTEOF            NO, READ TO NEXT EOF THEN         RH
*                                                                   RH
********************************************************************RH
         SPACE 1                                                    RH
WTOR2    DS    0H                                                   RH
         L     R8,MASTER+44       R8 -> TO DEB                      RH
         L     R8,32(0,R8)        R8 -> TO UCB                      RH
         MVC   WTOR1+21(3),13(R8) MOVE DRIVE NBR TO MSG             RH
*                                 SEND A MSG TO THE OPERATOR TO     RH
*                                 TELL HIM TAPE MAY RUN OFF END OF  RH
*                                 REEL                              RH
         XC    WTORECB,WTORECB    CLEAR WTOR ECB                    RH
         MVI   WTOREP,C' '        CLEAR ANSWER AREA FOR HELL OF IT  RH
WTOR1    WTOR  'UNIT XXX MAY RUN  OFF END OF REEL. REPLY C TO CONTINUE +
                OR R TO RESHOW MSG',WTOREP,1,WTORECB,ROUTCDE=(3,11) RH
         WAIT  ECB=WTORECB        WAIT FOR REPLY                    RH
         CLI   WTOREP,C'R'        DOES HE WANT TO SEE IT AGAIN ?    RH
         BE    WTOR1              YES, GO SEND IT AGAIN             RH
         TM    SW1,$DEBE          IN DEBE MODE                      RH
         BO    NEXTEOF            YES, SO DON'T UNWRITE TAPE MARKS  RH
*
*                                 NO BACKSPACE THE OUTPUT TAPE      RH
*                                 BACK OVER THE 2 TAPE MARKS        RH
*                                 WE HAVE JUST WRITTEN SO THAT      RH
*                                 WE WILL NOT HAVE AN EOF IN THE    RH
*                                 MIDDLE OF THE TAPE                RH
*                                                                   RH
         MVC   SAVECCW,WRITECCW   SAVE OUTPUT TAPE WRITE CCW        RH
         MVC   WRITECCW,DJMPMARK  PUT BACKSPACE CCW IN WRITE CCW    RH
         BAL   R7,RITELOOP        EXECUTE BACKSPACE ONE TAPE MARK   RH
         BAL   R7,RITELOOP        EXECUTE BACKSPACE ONE TAPE MARK   RH
         MVC   WRITECCW,SAVECCW   PUT WRITE CCW BACK                RH
         SPACE 1                                                    RH
         B     NEXTEOF            GO READ TO NEXT EOF               RH
         EJECT
***********************************************************************
*   THIS IS THE PRINT ROUTINE THAT PRINTS THE DUMP/DEBE INFO ON
*   THE PRINTER.  IT ALSO CONTAINS THE EXIT LIST ROUTINE FOR THE
*   PRINTER TO FILL IN THE DEFAULT BLKSIZE FOR THE DCB IF NECESSARY.
***********************************************************************
         SPACE
PRINT    DS    0H                                                   RH
         TM    PRT+48,X'10'       IS PUT FILE OPEN?                 RH
         BZR   R7                 NO, RETURN IMMEDIATELY            RH
         SPACE
PRINT1   DS    0H                                                   RH
         PUT       PRT,PRTBUF          OUT IT GOES
         MVI       PRTBUF,C' '         RESET FOR SINGLE SPACING
         MVC       PRTBUF+1(132),PRTBUF  CLEAR OUTPUT LINE
         AP        LINES,=P'1'         LINES=LINES+1
CPLINES  DS    0H                                                   RH
         CP    LINES,=P'63'       PAGE OVERFLOW ?                   RH
         BLR   R7                 NOT YET                           RH
*        MVC   PRTBUF(133),FHEADING MOVE HEADING TO PRINT LINE      RH
         PUT   PRT,FHEADING       PRINT HEADING                     RH
         AP    FPAGECTR,=P'1'     INC PAGE COUNT                    RH
         MVC   FPNUM(4),=X'40202020'  EDIT IT                       RH
         ED    FPNUM(4),FPAGECTR      EDIT PAGE NO TO PRINT         RH
*        MVI       PRTBUF,C' '         RESET FOR SINGLE SPACING
*        MVC       PRTBUF+1(132),PRTBUF  CLEAR OUTPUT LINE
*        BAL   R7,PRINT           PRINT IT                          RH
         MVI   PRTBUF,C'-'        TRIPLE SPACE NEXT TIME            RH
         ZAP   LINES,=P'2'        UPDATE LINE COUNT                 RH
*        MVI       PRTBUF,C'1'         SKIP TO CHANNEL 1 NEXT TIME
*        ZAP       LINES,=P'0'         RESET LINE COUNT
         BR        R7                  BACK TO CALLER
         SPACE     2                                               RH
         DROP      R4                  SO R1 CAN BE ESTABLISHED    RH
         USING     DCBEXIT,R15         ADDRESSABILITY FOR EXIT ROUTINE
         USING     IHADCB,R1           ADDRESSABILITY FOR DCB DSECT
DCBEXIT  OC        DCBBLKSI,DCBBLKSI   WAS A BLKSIZE SUPPLIED ON DDCARD
         BCR       7,R14               YES, SO LEAVE IT            RH
         MVC       DCBBLKSI,DEFBLKSI   NO, SO BY DEFAULT,
         BR        R14                 BLKSIZE=32000, AND RETURN   RH
DEFBLKSI DC        H'32000'            DEFAULT SIZE FOR MASTER     RH
         DROP      R15,R1              DROP TEMPORARY BASE REGISTERS
         USING     IHADCB,R4           GET BACK DSECT ADDRESSABILITY
         SPACE 2                                                   RH
         USING DCBABEND,R15        SET ABEND CODE BASE             RH
DCBABEND DS    0H                                                  RH
         CLI   0(R1),X'81'         813 ABEND ?                     RH
         BNE   DCBRETN             NO, LET IT BOMB                 RH
         MVI   3(R1),12            YES, RECOVER AND IGNORE         RH
*                                  EG - WHO CARES IF NAMES DON'T   RH
*                                  MATCH                           RH
DCBRETN  BR    R14                 RETURN TO OPEN                  RH
         DROP  R15                 DROP ABEND CODE BASE            RH
         SPACE 2                                                    RH
         DROP  R4                 DROP DCBDSECT BASE                RH
         USING TRAP237,R15        SET TRAP 237 CODE BASE            RH
         USING IHADCB,R1          SET DCBDSECT BASE FOR 237 CODE    RH
         SPACE 1                                                    RH
TRAP237  DS    0H                                                   RH
         SR    R15,R15            SAY ABEND FOR NOW                 RH
         BR    R14                GO BACK TO EOV PROCESSING         RH
         DROP  R15,R1             DROP TRAP237 BASE REGS            RH
         USING IHADCB,R4          GET BACK TO REAL DCBDSECT BASE    RH
         EJECT
***********************************************************************
*   THE FOLLOWING IS THE ERROR OUTPUT ROUTINE. IT IS ATTEMPTED TO
*   WRITE THE MESSAGES ON THE PRINTER. HOWEVER, IF WE HAVE NO PRINTER,
*   THEN A WRITE TO OPERATOR IS DONE INSTEAD .
***********************************************************************
         SPACE
OUTPUT   TM        SW1,$PRT            DO WE HAVE A PRINTER
         BNO       WTO                 NO, SO WRITE ON CONSOLE
         MVC       PRTBUF+1(14),=C'***TAPECOPY***'  PREFIX
         LH        R15,0(R1)           GET WRT PARM LIST LENGTH
         SH        R15,=H'5'           L-5-1
         EX        R15,MVC             MOVE INTO PRINT BUFFER
         B         PRINT               GO ENTER PRINT ROUTINE
WTO      WTO       MF=(E,(1)),ROUTCDE=11 TELL THE PROGRAMMER
         BR        R7                  AND RETURN
MVC      MVC       PRTBUF+16(*-*),4(R1) EX'D ABOVE
         EJECT
*   THE FOLLOWING ROUTINE EXECUTES THE CHANNEL PROGRAM TO READ THE RH
*   SENSE BYTES OF THE TAPE DRIVE THE MASTER FILE IS ON TO SEE IF  RH
*   THE ERROR CONDITION WAS CAUSED BY THE HARDWARE SENSING AN END  RH
*   OF TAPE CONDITION.                                             RH
*                                                                  RH
DEXCPROG DS    0H                                                  RH
         LA    R7,5                     SET MAX. RETRY COUNT       RH
DEXCPTRY EXCP  DIOBLK1                  READ/SPACE                 RH
         WAIT  1,ECB=ECB                WAIT FOR ACTION TO FINISH  RH
         CLI   ECB,X'44'                IF READ WAS NOT INTERCEPTEDRH
         BNE   DEXCPOK                  GO TO TEST ITS SUCCESS     RH
         BCT   R7,DEXCPTRY              TRY AGAIN UP TO 5 TIMES    RH
DEXCPOK  CLI   ECB,X'7F'           IF READ WAS NOT OKAY            RH
         BNE   DABEND              THEN GO TO ABEND                RH
         BR    R8                  ELSE RETURN
DABEND   ABEND 1111,DUMP ERROR IN OPEN OR CHANNEL PROGRAM - RERUN  RH
         EJECT
***********************************************************************
*   THE FOLLOWING ARE THE ERROR ROUTINES. MOST ARE SELF EXPLANATORY.
*   A FEW COMMENTS CAN BE MADE THOUGH.  THE 'UNABLE TO OPEN UNIT
*   XXX' MESSAGE IS THEN COMPLETED BY FILLING IN THE UNIT ADDRESS
*   OF THE UNIT IN QUESTION WHICH IS FOUND BY GOING TO THE UCB AND
*   PICKING THE NAME OUT OF THERE TO BE MOVED INTO THE MESSAGE.
*   ALSO IN THESE ROUTINES, THE ADDRESS OF THE ECB IN THE IOB IS ZEROED
*   TO INDICATE THAT THIS TAPE EITHER WOULD NOT OPEN, OR ELSE HAD AN
*   I/O ERROR.  A BLANK ECB ADDRESS IN THE IOB IS CHECKED FOR BEFORE A
*   WRITE IS MADE ON THAT UNIT AND IF IT IS ZERO, THEN THE WRITE IS
*   BYPASSED.  ALSO, SINCE WE HAVE ONE LESS 'GOOD' TAPE, REGISTER 9 IS
*   DECREMENTED SO WE CAN TELL IF WE HAVE RUN OUT OF TAPES TO COPY.
***********************************************************************
ALLERR   OUTPUT    MSG5                OUTPUT MESSAGE
         LA        R1,MASTIOB          POINT TO MASTER IOB
         BAL       R8,UVICIOER         CALL UVIC I/O ERROR ROUTINE
         ABEND     3000,DUMP           TALK ABOUT BAD LUCK
         SPACE 1                                                    RH
PRMRDERR OUTPUT    MSG11               OUTPUT MESSAGE
         MVC   PRTBUF+1(11),=C'MASTER ECB='                         RH
         UNPK  PRTBUF+12(9),MASTECB(5)   UNPACK ECB                 RH
         TR    PRTBUF+12(8),HEXTAB-240   TRANSLATE TO DISPLAY       RH
         MVI   PRTBUF+20,C' '     CLEAR GARBAGE BYTE                RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
         MVC   PRTBUF+1(14),=C'MASTER IOB(3)='                      RH
         UNPK  PRTBUF+15(7),MASTIOB(4)   UNPACK 1ST 3 BYTES OF IOB  RH
         TR    PRTBUF+15(6),HEXTAB-240   TRANSLATE TO DISPLAY       RH
         MVI   PRTBUF+21,C' '     CLEAR GARBAGE BYTE                RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
         MVC   PRTBUF+1(16),=C'MASTER CSW+1(7)='                    RH
         UNPK  PRTBUF+17(15),MASTIOB+9(8)  UNPK LOW 7 BYTES OF CSW  RH
         TR    PRTBUF+17(14),HEXTAB-240   TRANSLATE TO DISPLAY      RH
         MVI   PRTBUF+31,C' '     CLEAR GARBAGE BYTE                RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
         MVC   PRTBUF+1(4),=C'SW1='                                 RH
         UNPK  PRTBUF+5(3),SW1(2)  UNPACK SW1                       RH
         TR    PRTBUF+5(2),HEXTAB-240   TRANSLATE TO DISPLAY        RH
         MVI   PRTBUF+7,C' '      CLEAR GARBAGE BYTE                RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
         MVC   PRTBUF+1(4),=C'SW2='                                 RH
         UNPK  PRTBUF+5(3),SW2(2) UNPACK SW2                        RH
         TR    PRTBUF+5(2),HEXTAB-240   TRANSLATE TO DISPLAY        RH
         MVI   PRTBUF+7,C' '      CLEAR GARBAGE BYTE                RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
         BAL   R8,DEXCPROG         READ SENSE BYTES                 RH
         MVC   PRTBUF+1(12),=C'SENSE BYTES='                        RH
         UNPK  PRTBUF+13(13),SNSBYTES(7) UNPACK SENSE BYTES         RH
         TR    PRTBUF+13(12),HEXTAB-240   TRANSLATE TO DISPLAY      RH
         MVI   PRTBUF+25,C' '     CLEAR GARBAGE BYTE                RH
         BAL   R7,PRINT           PRINT IT                          RH
         SPACE 1                                                    RH
         TM     SNSBYTES+4,X'20'         ARE WE AT END OF REEL ?    RH
         BO     DEOR                     YES, TREAT SAME AS EOF     RH
*        THE  ABOVE TEST DOES NOT  WORK ????                        RH
         SPACE 1                                                    RH
         TM    MASTIOB+12,X'0E'   CE, DE, UC BITS ON IN CSW         RH
         BO    BACKMARK           YES, TREAT SAME AS END OF FILE    RH
         SPACE 1                                                    RH
         TM    SW2,X'40'          IGNORE FIRST EOF AND ERRORS?      RH
         BO    READERR2           YES , BYPASS DUMP AND CONTINUE    RH
         SPACE 1                                                    RH
         ABEND     1000,DUMP           GIVE HIM A DUMP
         SPACE 1                                                    RH
*                                                                   RH
*              IF WE GET DOWN TO  HERE, WE HAVE PROBABLY            RH
*              READ PAST THE REFLECTIVE MARKER AT THE END           RH
*              OF THE TAPE.  TRY  TO BACKSPACE BACK TO THE          RH
*              LAST TAPE MARK TO  HOPEFULLY AVOID HAVING            RH
*              THE TAPE RUN OFF  THE END OF THE REEL.               RH
*                                                                   RH
         SPACE 1                                                    RH
BACKMARK DS    0H
         LA    R8,DJMPMARK        STORE CCW TO BACKSPACE            RH
         ST    R8,CHANADD1        PAST TAPE MARK                    RH
         BAL   R8,DEXCPROG        EXECUTE BACKSPACE                 RH
         B     DEOR               TREAT SAME AS END OF FILE         RH
         SPACE 1                                                    RH
WRITEERR L         R4,20(,R5)          GET A(DCB) FROM IOB
         L         R1,DCBDEBAD         A(DEB) FROM DCB
         L         R1,32(R1)      A(UCB) FROM DEB                   RH
         MVC       MSG6+35(3),13(R1)   UNIT NAME FROM UCB           RH
ERMES2   OUTPUT    MSG6                OUTPUT THE MESSAGE
         LR        R1,R5               POINT TO OUTPUT IOB
         BAL       R8,UVICIOER         CALL UVIC I/O ERROR ROUTINE
         L     R6,4(R5)           R6 -> TO ECB                      RH
         XC        4(4,R5),4(R5)       SHOW I/O ERROR OCCURRED
         MVI       0(R6),X'7F'         FAKE NORMAL COMPLETION
         BCT       R9,NEXTECB          IF ANY GOOD TAPES LEFT
         ABEND     2000,DUMP           TOO BAD
NOMASTER OUTPUT    MSG7                OUTPUT MESSAGE
FINISH   OUTPUT    MSG12               SAY WE'RE DONE
         TM    SLPARMS+48,X'10'   DID WE OPEN SLPARMS DCB ?         RH
         BNO   CLOSEPRT           NO, TRY SYSPRINT                  RH
         CLOSE (SLPARMS)          YES, THEN CLOSE IT                RH
         SPACE 1                                                    RH
CLOSEPRT DS    0H                                                   RH
         TM        SW1,$PRT            TEST FOR PRINTER
         BNO       NOCLOSE             IF NONE,
         CLOSE     (PRT)               THEN NO CLOSE
NOCLOSE  EQU       *                   ALMOST DONE
         CLOSE (MASTER)           CLOSE MASTER FILE                 RH
         TM    SW1,$DEBE          DID WE OPEN ANY OUTPUT TAPES?     RH
         BO    FINISH2            NO, SO DON'T TRY TO CLOSE THEM    RH
         TM    SW1,$OPEN          DID ANY GET OPENED?               RH
         BNO   FINISH2            NO, DON'T TRY A CLOSE HERE EITHER RH
         LA    R5,DCBS            R5 -> TO OUTPUT DCBS              RH
         SPACE 1                                                    RH
CLOSLOOP DS    0H                                                   RH
         CLOSE ((R5))             CLOSE OUTPUT FILES                RH
         LA    R5,DCBLEN(R5)      R5 -> TO NEXT OUTPUT DCB          RH
         BCT   R2,CLOSLOOP        CLOSE ALL OUTPUT FILES            RH
         SPACE 1                                                    RH
FINISH2  DS    0H                                                   RH
         L         R13,4(R13)          RESTORE SAVE AREA
         MVI       12(R13),X'FF'       POST SAVE AREA
         RETURN    (14,12),RC=0        BACK TO O/S
         SPACE 1                                                    RH
NOCOPIES OUTPUT    MSG8                OUTPUT MESSAGE
         B         FINISH              GO FINISH UP
         SPACE 1                                                    RH
NLERROR  OUTPUT NLMSG             OUTPUT MESSAGE                    RH
         B         FINISH              GO FINISH UP
         SPACE 1                                                    RH
QERROR   OUTPUT  QMSG             OUTPUT MESSAGE                    RH
         B         FINISH              GO FINISH UP
         SPACE 1                                                    RH
SLERROR  OUTPUT SLMSG             OUTPUT MESSAGE                    RH
         B         FINISH              GO FINISH UP
         SPACE 1                                                    RH
MASTERR  OUTPUT    MSG10               OUTPUT MESSAGE
         ABEND     4000,DUMP           BETTER LUCK NEXT TIME
BADDEBE  WTO       'UNABLE TO OPEN SYSPRINT FOR DEBE MODE',ROUTCDE=11
         ABEND     500,DUMP            SLOPPY
PRTERR   ST        R14,SAVE2+96
         WTO       'PERMANENT ERROR ON SYSPRINT',ROUTCDE=11
         TM        SW1,$DEBE           WAS IT DEBE
         BNO       NODUMP              NO, SO WE CAN CONTINUE
         ABEND     1500,DUMP           CAN'T WIN 'EM ALL
NODUMP   NI        SW1,255-$DUMP       TURN OFF DUMP OPTION
         L     R14,SAVE2+96
         BR        R14
         SPACE     4
UVICIOER DS    0H                                                   RH
         STM   R14,R12,12(R13)    SAVE REGS                         RH
         SYNADAF   ACSMETH=EXCP        CALL THE I/O ERROR ROUTINE
         MVC       MSG13+4(78),50(1)   MOVE THE MESSAGE TEXT
*        SYNADRLS  ,               RESTORE                          RH
*        SYNADRLS DOESN'T WANT TO WORK SO I WON'T DO IT             RH
         L     R13,4(0,R13)       POINT R13 BACK TO MY SAVE AREA    RH
         LM    R14,R12,12(R13)    LOAD REGS                         RH
         OUTPUT    MSG13               PRINT THE SYNAD TEXT
         BR        R8                  RETURN
         EJECT
***********************************************************************
*                  CONSTANT SECTION
*
*   WHAT CAN YOU SAY ABOUT A CONSTANT SECTION OTHER THAN IT
*   CONTAINS THE MASTER TAPE DCB,IOB, AND ECB, AND ALSO THE
*   ABOVE CONTROL BLOCKS FOR EACH OF THE OUTPUT TAPES, A BUNCH
*   OF EQUS (EQUS???), A READ AND A WRITE CCW, A FEW SWITCHES,
*   THE LITERAL POOL, AND A DSECT TO OVERLAY MY DCBS. NOTHING I GUESS,
*   SO I WON'T BOTHER.
***********************************************************************
         SPACE
         DC        D'0'
MASTIOB  DS    0FL8               MASTER TAPE IOB                    RH
         DC    X'03',XL3'00'      DON'T USE ANY SYSTEM I/O ROUTINES  RH
         DC        A(MASTECB)          MASTER ECB ADDRESS
         DC        2F'0'               CSW SAVE AREA
         DC        A(READCCW)          CCW ADDRESS
         DC        A(MASTER)           DCB ADDRESS
         DC        2F'0'               REST OF IOB
IOBS     IOBS      ECBS                OUTPUT IOBS
IOBPTR   DC    F'0'               IOB OFFSET
MASTECB  DC        F'0'                MASTER ECB
ECBS     DC        6F'0'               OUPUT TAPE ECBS
ECBLIST  ECBLIST   ECBS                DEFINE ECBLIST
IOBLEN   EQU       32                  LENGTH OF IOB
$CC      EQU       X'40'               COMMAND CCW CHAINING MASK
$SLI     EQU       X'20'               SUPPRESS INCORRECT LENGTH MASK
*                                 THE FOLLOWING FIELDS MUST STAY    RH
*                                 IN THE PROPER SEQUENCE AND IF     RH
*                                 THEY ARE CHANGED, THE
CPARM    DC    CL8'00000000'                                        RH
STARTCPY DC    CL4'0001'                                            RH
STOPCPY  DC    CL4'9999'                                            RH
COPY     DC    C'COPY='                                             RH
DUMP     DC    C'DUMP'                                              RH
NEWVOL   DC    C'NEWVOL='                                           RH
NODUMP1  DC    C'NODUMP'          PARM FIELD PARAMETER              RH
DEBE     DC    C'DEBE'            DEBE OPTION                       RH
NEWSEQ   DC    C'NEWSEQ='         NEW STARTING SEQUENCE NBR         RH
IGNOREOF DC    C'IGNOREOF'        IGNORE 1ST SET OF TM'S            RH
TEST     DC    C'TEST'            PRINT TAPE MARK MSG'S             RH
HEX      DC    C'HEX'             PRINT RECORDS IN HEX              RH
SL       DC    C'SL'              CREATE SL OUTPUT TAPES            RH
PRINTEQ  DC    C'PRINT='          PRINT RECORDS                     RH
ALL      DC    C'ALL'             ALL KEYWORD                       RH
WTOREP   DC    C' '               WTOR REPLY                        RH
STARTSEQ DC    PL3'1'             DEFAULT START IS 1                RH
WTORECB  DC    F'0'               WTOR ECB                          RH
ONE      DC    P'1'               DECIMAL 1                         RH
COMMA    DC    C','               PARM FOR NEWVOLID ONLY            RH
CPYFLG   DC    X'00'              FLAG TO DETERMINE COPY            RH
*              X'80'              COPY PARM SUPPLIED                RH
*              X'40'              MASTER TAPE IS BLP                RH
*              X'20'              COPY TAPE IS BLP                  RH
*              X'10'              MASTER LABEL= > 1                 RH
*              X'08'              NOT SET                           RH
*              X'04'              MASTER TAPE IS NL                 RH
*              X'02'              COPY TAPE IS NL                   RH
*              X'01'              DON'T COPY RECORDS                RH
SW1      DC        X'0C'               SWITCHES (PRESET TO HEADER,DUMP)
$MASTER  EQU       X'80'               MASTER TAPE FOUND
$TM      EQU       X'40'               TM WAS LAST THING READ
$EOF     EQU       X'20'               2 TMS IN A ROW -> EOF
$PARMERR EQU       X'20'               PARAMETER ERROR FLAG
$OPEN    EQU       X'10'               AT LEAST SOMETHING OPENED
$HEADER  EQU       X'08'               IN HEADER MODE
$DUMP    EQU       X'04'               DUMP MODE
$DEBE    EQU       X'02'               DEBE MODE
$PRT     EQU       X'01'               WE HAVE A PRINTER
SW2      DC    X'00'              SWITCH FOR EMPTY DATA SET         RH
*              X'80'              VOL1 RECORD HAS BEEN PROCESSED    RH
*              X'40'              IGNORE FIRST EOF AND ERRORS       RH
*              X'20'              CREATE SL OUTPUT TAPE             RH
*              X'10'              PRINT XX RECORDS PER FILE         RH
*              X'08'              HEADER LABELS HAVE BEEN READ      RH
*              X'04'              PRINT RECORDS IN HEX              RH
*              X'02'              RENAME OUTPUT FILES               RH
*              X'01'                                                RH
HEXTAB   DC    C'0123456789ABCDEF'                                  RH
HEXPRTFL DC    A(PRT)             DEFAULT PRINTER FOR HEX STUFF     RH
MAXBLOCK DC        D'0'                MAXIMUM BLOCK LENGTH
DWORD    DC    D'0'               WORK DOUBLE WORD                  RH
PRECS    DC    F'0'                                                 RH
PALL     DC    XL4'7FFFFFFF'      PRINT ALL LINES                   RH
RECSPRTD DC    F'0'                                                 RH
RECSPBLK DC    F'0'                                                 RH
SAVE9    DC    F'0'                                                 RH
PFTYPE   DC    CL3'FB'            DEFAULT RECFM = FB                RH
PFLRECL  DC    CL5'00080'         DEFAULT LRECL = 80                RH
LRECL80  DC    CL2'80'            HEADER LRECL = 80                 RH
MVCDATA  MVC   PRTBUF+1(0),0(R3)  MOVE DATA TO BUFFER               RH
PRTBUF   DC    CL133'1'           PRINT BUFFER                      RH
BLKCTMSG DC    C'BLOCK COUNT IN TRAILER LABEL '                     RH
BLKCTSHB EQU   *-BLKCTMSG                                           RH
         DC    C'WAS '                                              RH
         DC    C'CHANGED FROM '                                     RH
BLKCTIN  EQU   *-BLKCTMSG                                           RH
         DC    CL6' '                                               RH
         DC    C' TO '                                              RH
BLKCTOUT EQU   *-BLKCTMSG                                           RH
         DC    CL6' '                                               RH
LBMSG    EQU   *-BLKCTMSG                                           RH
         SPACE 2                                                    RH
MSG1     WTO       'INVALID PARAMETER ON EXEC CARD',MF=L,ROUTCDE=11
MSG2     WTO       'MORE THAN &MAXTAPE COPIES REQUESTED. ONLY FIRST &MA*
               XTAPE TAPES WILL BE CREATED',MF=L,ROUTCDE=11
MSG3     WTO       'DEBE MODE CONVERTED TO DUMP MODE',MF=L,ROUTCDE=11
MSG4     WTO       'UNABLE TO OPEN ANY OUTPUT TAPES',MF=L
MSG5     WTO   'PERMANENT ERROR ON ALL OUTPUT TAPES',MF=L,ROUTCDE=11
MSG6     WTO       'PERMANENT OUTPUT ERROR ON UNIT XXX',MF=L,ROUTCDE=11
MSG7     WTO       'NO MASTER TAPE WAS FOUND ',MF=L,ROUTCDE=11
MSG8     WTO       'NO OUTPUT TAPES WERE FOUND ',MF=L,ROUTCDE=11
MSG9     WTO       'UNABLE TO OPEN DDNAME XXXXXXXX',MF=L,ROUTCDE=11
MSG10    WTO   'UNABLE TO OPEN MASTER TAPE',MF=L,ROUTCDE=11
MSG11    WTO      'PERMANENT READ ERROR ON MASTER TAPE',MF=L,ROUTCDE=11
MSG12    WTO       'END OF JOB',MF=L,ROUTCDE=11
MSG13    WTO       '123456789012345678901234567890123456789012345678901X
               234567890123456789012345678',MF=L,ROUTCDE=11
TMMSG    WTO   'TAPE MARK WRITTEN AFTER DATA RECORDS',MF=L,ROUTCDE=11
TMMSG2   WTO   'TAPE MARK READ',MF=L,ROUTCDE=11                     RH
EOFMSG   WTO   '        END OF FILE HERE WAS IGNORED',MF=L,ROUTCDE=11
SLMSG    WTO   '        MORE FILES THAN SLPARMS. TAPECOPY ENDED.',     +
               MF=L,ROUTCDE=11
NLMSG    WTO   '        NL TO SL REQUIRES SLPARMS. TAPECOPY ENDED.',   +
               MF=L,ROUTCDE=11
QMSG     WTO   '        OPTCD NOT EQUAL ON TAPES.  TAPECOPY ENDED.',   +
               MF=L,ROUTCDE=11
BLPMSG   WTO   '        1,BLP ON OUTPUT TAPE MAY CREATE NL TAPE',      +
               MF=L,ROUTCDE=11
*LPMSG   WTO   '        OUTPUT TAPE MUST BE GREATER THAN 1,BLP',       +
               MF=L,ROUTCDE=11
NOVOL1   WTO   '        VOL1 RECORD NOT FOUND ON MASTER',              +
               MF=L,ROUTCDE=11
INVLABEL WTO   '        LABEL= MUST BE 1 TO READ VOL1 RECORD',         +
               MF=L,ROUTCDE=11
READCCW  CCW       2,*-*,$SLI,32767    READ TAPE CCW
WRITECCW CCW       1,*-*,$SLI,*-*      WRITE TAPE CCW
*
*   THE SYSTEM PROGRAMMER'S GUIDE MANUAL SUGGESTS COMMAND CHAINING
*   A NOP CCW ON TO THE END OF A CCW CHAIN IN WHICH THE LAST OR
*   ONLY CCW IN THE CHAIN IS A WRITE TAPE MARK OR ERASE LONG GAP.
*   THIS IS DONE TO AID ERROR RECOVERY PROCEDURES IN CASE AN I/O
*   ERROR OCCURS.  THIS IS THE REASON FOR THE FOLLOWING CCW.
*
NOP      CCW       3,0,0,1             NOP CCW FOR ERROR RECOVERY
SAVECCW  CCW   1,*-*,$SLI,*-*     SAVE  WRITE TAPE CCW              RH
READSNS  CCW   4,SNSBYTES,32,6                                      RH
DJMPMARK CCW   47,0,32,1                                            RH
FJMPMARK CCW   63,0,32,1                                            RH
*                                  BYTE WILL BE                     RH
DRDBACK  CCW   12,RDBKADDR,32,80   READ BACKWARDS FOR TAPE LABELS   RH
DSTRDATA EQU   *+41                     **                          RH
SNSBYTES DS    CL6                      *** STORAGE AREA FOR CCW    RH
         DS    CL74                     **  COMMAND                 RH
DENDDATA DC    CL80' '                  *                           RH
RDBKADDR EQU   *-1                                                  RH
         DC    CL80' '    MAYBE TAKE THIS OUT IF THIS VERSION WORKS RH
DIOBLK1  DS    0FL8                                                 RH
         DC    X'03'                                                RH
         DC    XL3'00'                                              RH
ECBADD1  DC    A(ECB)                                               RH
FLAG31   DC    X'00'                                                RH
CSW1     DC    XL7'00'                                              RH
CHANADD1 DC    A(READSNS)                                           RH
DCBADD1  DC    A(MASTER)                MASTER TAPE FILE            RH
         DC    F'0'                                                 RH
BLKINC   DC    H'0'                                                 RH
         DC    H'0'                                                 RH
ECB      DC    F'0'                                                 RH
EOFCTR   DC    F'0'               NBR OF TIMES IN TO READERR        RH
         LTORG
#RECORDS DC        PL4'0'              # RECORDS IN FILE
LINES    DC        PL2'0'              LINES/PAGE ACCUMULATOR (7090 ?)
NEWVOLID DC        CL6' '              SAVE AREA FOR NEW VOLUME ID
VOLTAB   VOLS                     OUTPUT VOL SER TABLE              RH
         SPACE 1                                                    RH
FAKEVOL1 DS    0CL80              VOL1 RECORD IF MASTER LABEL= >1   RH
         DC    CL4'VOL1'                                            RH
FAKEVSER DC    CL6' '             MASTER VOLSER                     RH
         DC    CL70' '                                              RH
         SPACE 1                                                    RH
NEWDSN   DS    CL17               NEW DSNAME                        RH
         SPACE 1                                                    RH
FILEDATA DS    0CL80                                                RH
FILEDSN  DS    CL17               DSNAME                            RH
         DS    C                                                    RH
FILESEQN DS    CL4                FILE SEQ NBR                      RH
         DS    C                                                    RH
FILERCFM DS    C                  F,V,U                             RH
FILEATTR DS    C                  B,S,R(BS)                         RH
         DS    C                                                    RH
FILEBLKS DS    CL5                BLKSIZE                           RH
         DS    C                                                    RH
FILERECL DS    CL5                RECORD LTH                        RH
         DS    CL(80-(*-FILEDSN))                                   RH
         SPACE 1                                                    RH
HDR1REC  DS    0CL80                                                RH
EOF1REC  DS    0C                                                   RH
HDR1ID   DC    CL4'HDR1'                                            RH
HDR1DSN  DC    CL17' '                                              RH
HDR1VOL  DC    CL6'000001'                                          RH
HDR1VSEQ DC    CL4'0001'                                            RH
HDR1SEQN DC    CL4'0001'                                            RH
         DC    CL4' '                                               RH
         DC    CL2' '                                               RH
HDR1CRED DC    CL6' '             CREATION DATE (JFCB)              RH
HDR1EXPD DC    CL6' '                                               RH
HDR1SEC  DC    C'0'                                                 RH
EOF1BLKC DC    CL6'000000'        ZERO FOR HDR1                     RH
         DC    CL13'TAPECOPY HALL'                                  RH
         DC    CL7'DIVDEV'                                          RH
         SPACE 1                                                    RH
HDR2REC  DS    0CL80                                                RH
EOF2REC  DS    0C                                                   RH
HDR2ID   DC    CL4'HDR2'                                            RH
HDR2RCFM DC    C'F'                                                 RH
HDR2BLKS DC    CL5'04000'                                           RH
HDR2RECL DC    CL5'00080'                                           RH
HDR2DENS DC    C'4'                                                 RH
         DC    CL1'0'                                               RH
         DC    CL17'TAPECOPY JOB'                                   RH
         DC    CL2' '                                               RH
         DC    CL1' '                                               RH
         DC    C' '                                                 RH
HDR2ATTR DC    C'B'                                                 RH
         DC    CL8' '                                               RH
         DC    CL1' '                                               RH
         DC    CL15'DIVDEV'                                         RH
FPARMS   DC    3F'0'                    PARM ADDRESSES
FFLAG1   DC    C'N'                     THESE FLAGS MUST BE RIGHT   RH1
FFLAG2   DC    C'D'                     AFTER FPARMS. FFLAG1 IS AN  RH1
*                                       INDICATOR FOR HEX OR DEC    RH1
*                                       OFFSETS. FFLAG2 IS AN       RH1
*                                       INDICATOR FOR THE COPY      RH1
*                                       FACILITY                    RH1
FHEADING DS    0CL133
         DC    CL13'1'
FTITLE   EQU   *
         DC    CL24' '
         DC    CL72' '
         ORG   *-72
         DC    C'DIVERSIFIED DEVELOPMENTS TAPE UTILITY'
         DC    C' PROGRAM, VERSION 1.1'
         ORG
         DC    CL4'PAGE'
FPNUM    DC    CL7'   1   '
FHEXHD   DC    CL13' '
*              FOVFLOW AND FPAGECTR MUST BE RIGHT AFTER FHEXHD
FOVFLOW  DC    CL1' '                   ISAM OVERFLOW RECORD FLAG
FPAGECTR DC    PL2'0'                   PAGE COUNT
*
FREADPAT DC    XL10'40206B2020206B202120'
HEXHEAD  DC    CL13'RECNO   LRECL'
         SPACE
         DS    0F                 ALIGN EXITS                       RH
PRTEXLST DC    X'85',AL3(DCBEXIT)  DCB EXIT LIST                    RH
MASTEXIT DC    X'11',AL3(DCBABEND) TRY TO TRAP 813                  RH
         DC    X'0B',AL3(TRAP237) TRY TO TRAP 237 ABENDS            RH
         DC    X'05',AL3(DCBEXIT) IN CASE NO BLKSIZE ON MASTER      RH
JFCBADD  DS    0F                 JFCB EXLST CB                     RH
         DC    X'87'                                                RH
         DC    AL3(JFCB)          A(JFCB)                           RH
         DC    C'JFCB'            SO I CAN FIND IT EASY             RH
JFCB     DS    0CL176                                               RH
JFCBDSNM DS    CL44               DATA SET NAME                     RH
JFCBELNM DS    CL8                ELEMENT NAME OR VERSION           RH
         DS    CL14                                                 RH
JFCBLTYP DS    CL1                LABEL TYPE                        RH
         DS    C                                                    RH
JFCBFLSQ DS    XL2                FILE SEQ NBR                      RH
JFCBVLSQ DS    XL2                VOL SEQ NBR                       RH
         DS    CL8                                                  RH
JFCBCRDT DS    CL3                CREATION DATE                     RH
JFCBXPDT DS    CL3                EXPIRATION DATE                   RH
         DS    CL18                                                 RH
JFCLRECL DS    CL2                                                  RH
         DS    CL12                                                 RH
JFCBVOLS DS    CL30               5 VOL SER NBRS                    RH
         DS    CL28                                                 RH
JFCOPTCD EQU   JFCB+101           JFCB OPTCDS                       RH
JFCOPTQ  EQU   X'08'              OPTCD=Q                           RH
SLPARMS  DCB   MACRF=GM,DDNAME=SLPARMS,                             RH X
               DSORG=PS,EODAD=SLERROR                               RH
PRT      DCB   DSORG=PS,MACRF=PMC,LRECL=133,BLKSIZE=3990,RECFM=FBA, RH *
               DDNAME=SYSPRINT,EXLST=PRTEXLST,SYNAD=PRTERR          RH
HEXPRT   DCB   DSORG=PS,MACRF=PMC,LRECL=133,BLKSIZE=3990,RECFM=FBA, RH *
               DDNAME=HEXPRINT,EXLST=PRTEXLST,SYNAD=PRTERR          RH
MASTER   DCB     MACRF=GL,DDNAME=MASTER,DEVD=DA,BUFNO=1,            RH X
               DSORG=PS,EODAD=READOK,EXLST=MASTEXIT                 RH
         DS    CL156              FOR ISAM FILES ??                 RH
DCBS     DCBS      IOBS                DCBS FOR OUTPUT TAPES
         LTORG
         EJECT
         TITLE 'MODULE TO GIVE HEX/CHARACTER PRINT FORMAT'
*        PARAMETERS ON INPUT ARE
*
*             1) DCB OF OPEN PRINT FILE (ADDR, MOVE MODE)
*             2) HEADING LINE (133 CHAR)
*             3) 3-WORD AREA CONTAINING 1- RECORD COUNT
*                                       2- LENGTH OF RECORD
*                                       3- START ADDR OF RECORD
*
PRINTRTN CSECT
         SAVE  (14,12),T,*
         BALR  R12,R0
         USING *,R12                    SET BASE REG TO 12
         ST    R13,HSAVE+4              CHAIN SAVE AREAS
         LR    R11,R13
         LA    R13,HSAVE
         ST    R13,8(R11)
         SPACE 3
         LM    R6,R8,0(R1)              STORE PARAMETERS
         ST    R8,HSAVE           SAVE ADDRESS OF PRINT LINE        RH
PSET     NOP   PNOSET                                               RH1
*        WHILE R8 IS POINTING TO FPARMS, ZAP HSETMOVE ONCE          RH1
*        FROM A BRANCH TO A NO OP IF USER WANTS HEX OFFSETS         RH1
         MVI   PSET+1,X'F0'             CHANGE NOP TO B             RH1
*        CLI   12(R8),C'H'              DOES HE WANT HEX OFFSETS?   RH1
*        BNE   PNOSET                   NO, DON'T ZAP HSETMOVE      RH1
         MVI   HSETMOVE+1,X'00'         YES, CHANGE B TO NOP        RH1
         MVI   PK1,X'0C'          CHANGE OFFSET STARTER TO 0        RH1
         CLI   0(R1),X'80'        USING HEXPRINT DD ?
         BNE   PNOSET             NO
         MVI   HEXPRT2+1,X'00'    YES, TURN ON NEW FILE TEST
         SPACE 1                                                    RH1
PNOSET   DS    0H                                                   RH1
*                                       R2 - NOT USED
*                                       R3 - LENGTH FOR HEXIT RTN
*                                       R4 - CHAR ADDR FOR HEXIT RTN
         LA    R5,HEXAREA               R5 - HEX AREA FOR HEXIT RTN
*                                       R6 - PRINT DCB ADDR
*                                       R7 - HEADING LINE
         LM    R8,R10,0(R8)             R8 - RECORD COUNT
*                                       R9 - LENGTH OF RECORD
*                                       R10- START OF RECORD
         LA    R11,32(0,0)              R11- CONSTANT 32
*
         MVI   HCARCTL,C'0'        MOVE DOUBLE SPACE TO PRINT
         NI    HCLEAR+1,X'0F'      BYPASS BRANCH TO CLEAR AREA
         AP    HLINCTR,=P'1'       INCR LINE-CTR FOR BLANK LINE
         ZAP   HCOUNTER,PK1             RESET OFFSET COUNTER        RH1
         LA    R8,1(0,R8)         +1 CAUSE COUNTER STARTS AT 0      RH
         SPACE 1
HEXPRT2  B     NOTFIRST           NOP IF USING HEXPRINT DD
         CH    R8,H1              FIRST RECORD OF BUFFER ?
         BH    NOTFIRST           NO
         ZAP   HLINCTR,=P'50'     YES, SET PAGE CTR TO MAX
         SPACE 1
NOTFIRST DS    0H
         CVD   R8,HDBLWORK
         MVC   HRECNO,HCTRPAT
         ED    HRECNO,HDBLWORK+4   MOVE RECORD NUMBER TO PRINT
         MVC   HOVFLOW,133(R7)          MOVE ISAM OVERFLOW FLAG TO PRNT
         CVD   R9,HDBLWORK
         MVC   HRECLN,HCTRPAT
         ED    HRECLN,HDBLWORK+4   MOVE RECORD LENGTH TO PRINT
         EJECT
HPROCESS EQU   *
         LTR   R9,R9               ANY DATA TO MOVE
         BZ    HENDJOB             IF NO GO TO END OF MODULE
         CR    R9,R11              IS THERE MORE THAN 1 LINE TO MOVE
         BNH   HSETLEN             IF NO, BRANCH
         LR    R3,R11              SET LENGTH TO 32
         SR    R9,R11              SET R9 TO REDUCED LENGTH
         B     HSETMOVE
HSETLEN  EQU   *
         LR    R3,R9               SET R3 TO LENGTH
         SR    R9,R3               SET R9 TO ZERO
HSETMOVE B     HNOHEX                   NOP IF HEX OFFSETS          RH1
         ZAP   HDBLWORK,HCOUNTER        MOVE OFFSET TO DBLE WORD    RH1
         CVB   R4,HDBLWORK              CONVERT TO BINARY           RH1
         STM   R3,R5,HEX3SAVE           SAVE REGS 3, 4, 5           RH1
         SR    R3,R3                    TELL HEXIT IT'S AN OFFSET   RH1
         LA    R4,HEX3SAVE+5            R4 -> TO HEX NBR            RH1
         LA    R5,HLINMARK              R5 -> TO OFFSET PRINT LOC   RH1
         CALL  HEXIT                    PUT IT IN PRINT LINE        RH1
         LM    R3,R5,HEX3SAVE           RELOAD REGS 3, 4, 5         RH1
         B     HHEX                     DECODE REST OF LINE         RH1
         SPACE 1                                                    RH1
HNOHEX   DS    0H                                                   RH1
         MVC   HLINMARK(6),HLINPTRN     PRINT BYTE COUNTER
         ED    HLINMARK(6),HCOUNTER       IN LEFT MARGIN
         SPACE 1                                                    RH1
HHEX     DS    0H                                                   RH1
         LR    R4,R10                   SET CHAR FLD ADDR.
         MVI   HCHARS+1,C' '            CLEAR
         MVC   HCHARS+2(31),HCHARS+1      PRINTAREA
         LTR   R2,R3                    MOVE LENGTH TO WORK REG
         BP    HHEX2              OVER 0, ON WE GO
         LA    R2,32              SET R2 TO 32
         SPACE 1                                                    RH1
HHEX2    DS    0H                                                   RH1
         BCTR  R2,0                     REDUCE TO MACHINE LEN
         EX    R2,HMOVCHAR              MOVE CHARS TO PRINT
         TR    HCHARS+1(32),HPRINTAB    CONVERT NON-PRINT TO PERIOD
         CALL  HEXIT                    FORMAT HEX AREA.
         AP    HLINCTR,=P'1'            INC LINE COUNT
         AP    HCOUNTER,=P'32'          INC BYTE COUNT
         CP    HLINCTR,=P'50'           END OF PAGE TEST
         BL    HPRINT                     IF NO BYPASS HEADINGS
         AP    134(2,R7),=P'1'          INC PAGE COUNT
         MVC   113(4,R7),HLINPTRN
         ED    113(4,R7),134(R7)        EDIT PAGE NO TO PRINT
         MVI   0(R7),C'1'               SKIP TO NEW PAGE
         MVI   1(R7),C' '
         PUT   (6),(7)                  PRINT HEADING
         MVI   HCARCTL,C'0'             DOUBLE SPACE AFTER HEADING
         SP    HLINCTR,HLINCTR          ZERO LINECOUNT
HPRINT   EQU   *
         PUT   (6),HPRNTLIN             PRINT DATA-LINE
         MVI   HCARCTL,C' '             SET CARR-CTL TO SINGLE SPACE
         LA    R10,32(0,R10)       SET PTR TO NEXT AREA
HCLEAR   NOP   HPROCESS            BYPASS CLEAR FUNCTION
         OI    HCLEAR+1,X'F0'      SET BRANCH TO BYPASS NEXT FILE
         MVI   HRECNO,C' '              CLEAR
         MVC   HRECNO+1(16),HRECNO        LAST PART OF PRINT-LINE
         B     HPROCESS
         SPACE 2
HMOVCHAR MVC   HCHARS+1(0),0(R10)     EXECUTE MOVE CHARS TO PRINT
         SPACE 2
HENDJOB  DS    0H
*        L     R8,HSAVE           R8 -> TO TAPECOPY PRINT LINE      RH
*        MVI   0(R8),C' '         CLEAR IT OUT                      RH
*        MVC   1(132,R8),0(R8)    ''                                RH
         L     R13,HSAVE+4
         RETURN (14,12)
         DS    0D
HSAVE    DC    18F'0'
HEX3SAVE DC    3F'0'
PK1      DC    P'1'               START OF OFFSET. 0 FOR HEX        RH1
H1       DC    H'1'               HALFWORD OF 1                     RH1
HDBLWORK DC    D'0'
*
HPRNTLIN DS    0CL133
HCARCTL  DC    CL1' '
HLINMARK DC    CL7' '
HEXAREA  DC    CL74' '
HCHARS   DC    34CL1'*'
HRECNO   DC    CL8' '
HOVFLOW  DC    CL1' '                   ISAM OVERFLOW RECORD FLAG
HRECLN   DC    CL8' '
*
HLINPTRN DC    XL6'402020202120'        PRINT
HCTRPAT  DC    XL8'4020202020202021'      PATTERNS
HLINCTR  DC    PL2'02' WAS 24           LINECOUNT
HCOUNTER DC    PL3'1'                   BYTE COUNT PER RECORD/LINE
HPRINTAB DS    0CL256              TABLE TO CONVERT NO-PRINT CHARS
         DC    64CL1'.'
         DC    C' '
*        DC    128CL1'.'
         DC    9CL1'.'
         DC    C'.<(+|&&'        MUST USE && FOR &
         DC    9CL1'.'
         DC    C'!$*);^-/'
         DC    8CL1'.'
         DC    C',%_>?'
         DC    10CL1'.'
         DC    C':#@''="'         MUST USE '' FOR '
         DC    64CL1'.'
         DC    C'{ABCDEFGHI'
*        DC    7CL1'.'
         DC    6CL1'.'
         DC    C'}JKLMNOPQR'
         DC    8CL1'.'
         DC    C'STUVWXYZ'
         DC    6CL1'.'
         DC    C'0123456789'
         DC    6CL1'.'
         LTORG
         TITLE 'CHARACTER TO HEX MAX 32 CHARS'
*        ON ENTRY TO THIS ROUTINE, REC 3 TO 5 CONTAIN
*             R3 - LENGTH OF CHARACTER FIELD
*             R4 - ADDRESS OF CHARACTER FIELD
*             R5 - ADDRESS OF HEX FIELD
*
HEXIT    CSECT
BEGIN    SAVE  (14,12),T,*
         BALR  12,0
         USING *,12
         MVI   TRANSLAT+1,X'47'         DEFAULT TRANSLATE LTH = 72  RH1
         LTR   R3,R3                    ARE WE PRINTING HEX OFFSET  RH1
         BP    NOTHOFF                  NO, WE'RE HEXING DATA       RH1
         LA    R3,3           YES, SET R3 TO LTH OF HEX OFFSET      RH1
         MVI   TRANSLAT+1,X'05'         CHANGE TRANSLATE LTH TO 6   RH1
         SPACE 1                                                    RH1
         SPACE 1                                                    RH1
NOTHOFF  DS    0H                                                   RH1
         SR    R1,R1               R1 - INDEX REG
         LA    R2,1(0,0)           R2 - INDEX VALUE OF ONE
         BCTR  R3,R0               R3 - MACHINE CODE LEN OF CHAR FLD
*                                  R4 - ADDR OF CHAR FLD
*                                  R5 - ADDR OF HEX FLD
         LR    R6,R4               R6 - CURR BYTE IN CHAR FLD
         LR    R7,R5               R7 - CURR BYTES IN HEX FLD
         LA    R8,2(0,0)           R8 - INDEX VALUE OF TWO
         SR    R9,R9               R9 - WORK REG
         LA    R10,4(0,0)          SET LOOP OF FOUR INDEX REG
LOOP     EQU   *
         IC    R9,0(0,R6)          PLACE CHAR IN WORK REG
         STC   R9,1(0,R7)          SET UP 2ND HALF OF BYTE          RH1
         NI    1(R7),X'0F'         STRIP ZONE FROM CHAR
         SRL   R9,4(R0)            MOVE ZONE TO LOW-ORDER
         STC   R9,0(0,R7)          STORE 1ST HALF OF CHAR INTO HEX FLD
         AR    R6,R2               INCR TO NEXT CHAR FLD BYTE
         AR    R7,R8               INCR TO NEXT HEX FLD BYTES
         BCT   R10,NOINSRT        IF NO INSERT CONTINUE
         MVI   0(R7),X'10'         INSERT SPACE AFTER 8 HEX POSITIONS
         LA    R7,1(R0,R7)         INCREMENT HEX BYTE POINTER       S
         LA    R10,4(0,0)          RESET INDEX REG
NOINSRT  EQU   *
         BXLE  R1,R2,LOOP          REPEAT NO OF BYTES TIMES
         SPACE 2
         CLI   TRANSLAT+1,X'05'    ARE WE TRANSLATING HEX OFFSET    RH1
         BE    TRANSLAT                 YES, GO TRANSLATE IT        RH1
         SPACE 1                                                    RH1
         C     R3,=F'31'           ANY BYTES TO PAD?
         BE    TRANSLAT            IF NO GO TO TRANSLATE
         MVI   0(R7),X'10'         PAD 1ST BYTE
         LA    R9,71(R0,R5)        ADDRESS END OF HEX FLD
         SR    R9,R7              COUNT BYTES TO PAD
         EX    R9,PADMOVE          MOVE IN PAD CHARACTERS
         SPACE 2
TRANSLAT EQU   *
         TR    0(71,R5),HEXTABLE   CONVERT HEX FIELD TO PRINT CHARS
         RETURN (14,12)
         SPACE 2
PADMOVE  MVC   1(0,R7),0(R7)       PROPAGATE PADDING CHAR
         SPACE 2
HEXTABLE DC    CL17'0123456789ABCDEF '
         SPACE 2
         LTORG
LABELS   DSECT                         DSECT FOR LABEL FIELDS
*
*   THE FOLLOWING MACRO IS ASSUMED TO BE IN THE INSTALLATION MACRO
*   LIBRARY.  IF IT IS NOT AVAILABLE THERE, THEN IT SHOULD BE EITHER
*   INCLUDED IN THE MACRO LIBRARY, OR ELSE PUNCHED OUT AND INSERTED
*   WITH THE REST OF THE MACROS AT THE BEGINNING OF THE PROGRAM.  THE
*   MACRO DEFINITION CAN BE OBTAINED FROM EITHER THE 'TAPE LABELS'
*   MANUAL OR THE 'SYSTEM PROGRAMMER''S GUIDE' MANUAL.  IF THE MACRO IS
*   PUNCHED UP, THEN ONLY THE FIRST THREE SECTIONS OF THE MACRO (I.E.
*   THE VOLUME LABEL, THE FILE LABEL 1, AND THE FILE LABEL 2 SECTIONS)
*   NEED BE INCLUDED SINCE THE PROGRAM ACCESSES ONLY THESE PARTS.  ALSO
*   THE DUMMY IEFJFCBN MACRO AT THE BEGINNING OF THE PROGRAM CAN BE
*   REMOVED.
*
         IECDSECT                      DEFINE LABEL FIELDS
*
*   DEFINE DCB DUMMY SECTION
*
         PRINT NOGEN
         DCBD      DSORG=(XE,QS),DEVD=(DA,TA)   DCB DSECT
         PRINT GEN
         END       TAPECOPY            THAT'S ALL FOLKS
/*
//LKED.SYSLMOD DD DSN=HALL.LINKLIB(TAPECOPY),DISP=SHR
//
//*
//*         SAMPLE RUN OF TAPECOPY
//*
//STEP1 EXEC PGM=IEBGENER
//SYSPRINT DD SYSOUT=*
//SYSUT2 DD DSN=&&TAPECOPY,DISP=(,PASS),
//        UNIT=TAPE,LABEL=(1,SL,RETPD=0),
//        VOL=(,RETAIN),
//        DCB=(RECFM=FB,LRECL=80,BLKSIZE=400,DSORG=PS)
//SYSIN  DD DUMMY
//SYSUT1 DD *
LINE 1
LINE 2
LINE 3
LINE 4
LINE 5
LINE 6
LINE 7
LINE 8
LINE 9
LINE 10
LINE 11
LINE 12 IS EXACTLY 32 BYTES LONG
/*
//STEP2 EXEC PGM=IEBGENER
//SYSPRINT DD SYSOUT=*
//SYSUT2 DD DSN=&&TAPECOPY,DISP=(,PASS),
//        UNIT=TAPE,LABEL=(2,SL,RETPD=0),
//        VOL=(,RETAIN,REF=*.STEP1.SYSUT2),
//        DCB=(RECFM=FB,LRECL=80,BLKSIZE=400,DSORG=PS)
//SYSIN  DD DUMMY
//SYSUT1 DD *
LINE 1
LINE 2
LINE 3
LINE 4
LINE 5
LINE 6
LINE 7
LINE 8
LINE 9
LINE 10
LINE 11
LINE 12 IS EXACTLY 32 BYTES LONG
/*
//STEP8 EXEC PGM=TAPECOPY,PARM='DUMP,TEST,PRINT=3'
//STEPLIB DD DSN=*.STEP0.LKED.SYSLMOD,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSABEND DD SYSOUT=*
//MASTER DD DISP=(OLD,PASS),UNIT=TAPE,
//       VOL=(,RETAIN,REF=*.STEP1.SYSUT2),
//   DSN=*.STEP1.SYSUT2
//TAPE1  DD DSN=&&NEWTAPE,DISP=(,PASS),
//        UNIT=TAPE,LABEL=(1,SL,RETPD=0),
//        VOL=(,RETAIN),
//        DCB=(RECFM=FB,LRECL=80,BLKSIZE=400,DSORG=PS)
//SLPARMS DD *
THISIS.FILE1
THISIS.FILE2
/*
//STEP9 EXEC PGM=TAPECOP1,PARM='DEBE,TEST,PRINT=ALL,HEX'
//STEPLIB DD DSN=*.STEP0.LKED.SYSLMOD,DISP=SHR
//SYSPRINT DD SYSOUT=*
//HEXPRINT DD SYSOUT=*
//SYSABEND DD SYSOUT=*
//MASTER DD DISP=(OLD,DELETE),UNIT=TAPE,VOL=(REF=*.STEP8.TAPE1),
//   DSN=TAPECOPY.TAPECOPY
//*  DSN=*.STEP8.TAPE1
//
//
