//GENQ     JOB
/*ROUTE  PRINT R7
//* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
//* $  CBT.SOURCE(GENQ)
//* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
//S1        EXEC  HLASMCL,CLASS='*'
//ASM.SYSIN DD  *
*        MODIFICATIONS.....
*              JUL 11/96...ADDED  CODE TO PRINT 1ST 32 CHARS OF
*                          QNAME IN HEX IF IT CONTAINS UNPRINTABLES
*
*
*
*              THIS VERSION IS FOR SP3 SYSTEMS
*              NOTE..FIXUP CODE AT GQSCAN MACRO FOR 'BAD' GEN'D CODE
*                    TO AVOID NEEDING 2 GQSCANS, AS RESNAME PARM
*                    CAUSES GEN'D CODE TO DEFAULT TO QNAME LTH & FLAG
*
ENQ      CSECT
         TITLE 'COMMAND TO LIST ENQUEUED DATASET NAMES'
         USING *,R15
         B     AROUND             BRANCH AROUND DUMP TITLE
         DC    C'ENQ     '        PROGRAM NAME
         DC    C'&SYSDATE'        DATE WRITTEN
         DC    C'&SYSTIME'        TIME WRITTEN
         SPACE 1
AROUND   DS    0H
         DROP  R15
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING ENQ,R12
         ST    R13,SAVE+4
         LR    R11,R13
         LA    R13,SAVE
         ST    R13,8(,R11)
         L     R1,0(,R1)        GET ADDR OF PARM LIST
         LA    R10,2(,R1)       SAVE PARM STRING ADDR FOR LATER
         LH    R11,0(,R1)       GET LENGTH OF PARM STRING
         EJECT
         L    R4,GQLTH             LOAD LENGTH OF REQUEST FOR GETMAIN
*
*    ROUND THE LENGTH UP TO A DOUBLEWORD BOUNDARY.
*
         A    R4,=F'7'             ADD 7 BYTES
         N    R4,=XL4'FFFFFFF8'    ROUND TO DOUBLEWORD BOUNDARY
         ST   R4,GQLTH             STORE UPDATED LENGTH
*
*    ISSUE THE REQUEST FOR VIRTUAL STORAGE.
*
         GETMAIN RC,LV=(R4)
*
*    DETERMINE IF VIRTUAL STORAGE WAS ALLOCATED.  A RETURN CODE OF 0
*    INDICATES SUCCESS.
*
         LTR  R15,R15              RETURN CODE = 0 ???
         BNZ  CFATAL               NO, SO SET THE RETURN CODE TO 4.
*
*    STORE THE ADDRESS OF THE VIRTUAL STORAGE.
*
         ST   R1,GQCORE
         SPACE 1                                                    RH
DECODE   DS    0H                                                   RH
         LTR   R11,R11          CHECK FOR NO PARMS
         BZ    HELP             GIVE HELP MSG
         LA    R7,7               R7 = MAX QNAME LTH                RH
         CLC   0(2,R10),=C'Q='    IS IT Q= PARM?                    RH
         BNE   NOQ                NO, USE DEFAULT OF SYSDSN         RH
         MVC   QNAME,OLDNAME      MAKE SURE QNAME IS BLANK          RH
         LA    R10,2(0,R10)       R10 -> TO QNAME                   RH
         LR    R7,R10             R7 -> TO QNAME                    RH
         BCTR  R11,0              R11 = LTH OF REST OF PARM         RH
         BCTR  R11,0              ''                                RH
         SPACE 1                                                    RH
QLOOP    DS    0H                                                   RH
         CLI   0(R7),C','         FOUND COMMA ?                     RH
         BE    COMMAFND           YES, SET UP QNAME                 RH
         LA    R7,1(0,R7)         R7 -> TO NEXT BYTE                RH
         BCT   R11,QLOOP          LOOK AT IT                        RH
         TPUT  MISSPARM,LMISS     SAY MISSING PARM                  RH
         B     GETANS             GO GET NEXT REPLY                 RH
         SPACE 1                                                    RH
COMMAFND DS    0H                                                   RH
         BCTR  R11,0              R11 = LTH OF REST 0F PARM         RH
         SR    R7,R10             R7 = LTH OF QNAME                 RH
         BCTR  R7,0               R7 = HEX LTH                      RH
         EX    R7,MVCQ            MOVE QNAME                        RH
         LA    R10,2(R7,R10)      R10 -> TO NEXT PARM               RH
         SPACE 1                                                    RH
NOQ      DS    0H                                                   RH
         CLC   0(2,R10),=C'*J'  LIST DSNS BY JOBNAME?
         BNE   NOJOBN           NO, DON'T GET JOBNAME LENGTH
         LR    R9,R11           GET LENGTH OF PARM STRING
         S     R9,=F'3'         SUBTRACT 1 FOR EX, 2 FOR '*J'
NOJOBN   BCTR  R11,R0           DECR FOR EXECUTE LATER
         SPACE 2
QTEST    DS    0H                                                   RH
         SPACE 2
***********************************************************************
*                                                                     *
*     GQSCAN  QNAME=ALL OR QNAME,                                     *
*             RNAME=ALL,                                              *
*             SCOPE=ALL                                               *
*                                                                     *
***********************************************************************
         SPACE 3
         L     R1,GQCORE           R1 -> TO CORE FOR GQSCAN
         L     R4,GQLTH            R4 = LTH OF CORE
         LA    R5,0                R5 = 0 IF Q=ALL
*                                                                   RH
*              SINCE YOU CAN'T TELL GQSCAN THAT THERE IS A QNAME,   RH
*              RATHER THAN HAVE 2  OR MORE GQSCAN'S, FUDGE THE      RH
*              GQSCAN PARM LIST.                                    RH
*                                                                   RH
*                                                                   RH
         LA    R2,SCANALL4+12      R2 -> TO WHERE BAL R1 S/B        RH
         CLC   0(2,R2),=X'4510'    IS IT HERE?                      RH
         BE    QTEST2              YES                              RH
         LA    R2,2(0,R2)          NO, CNOP MUST BE HERE, GO AROUND RH
         SPACE 2
QTEST2   DS    0H                                                   RH
         LA    R2,4(0,R2)          GO AROUND BAL INST               RH
*                                                                   RH
*              SET THE 'QNAME LTH SPECIFIED' BIT IN THE PARM OFF,   RH
*              AND SET THE 'QNAME LTH' TO 0, IN CASE THE QNAME IS   RH
*              'ALL'.                                               RH
*                                                                   RH
         NI    45(R2),255-X'02'    DEFAULT TO NO QNAME SPECIFIED    RH
         MVI   47(R2),X'00'        DEFAULT LTH IS 0                 RH
*        MVI   SCANALL4+63,X'08'   FIX BAD GQSCAN GEN               RH
*        MVI   SCANALL4+65,X'00'   FIX BAD GQSCAN GEN               RH
         CLC   QNAME,=CL8'ALL'     Q=ALL SPECIFIED ?
         BE    SCANALL4            YES, R5 IS SET
*                                                                   RH
*              A SPECIFIC QNAME IS WANTED, SO, ......               RH
*              SET THE 'QNAME LTH SPECIFIED' BIT IN THE PARM ON,    RH
*              AND SET THE 'QNAME LTH' TO 8.                        RH
*                                                                   RH
         OI    45(R2),X'02'        SET TO QNAME IS SPECIFIED        RH
         MVI   47(R2),X'08'        SET LTH TO 8                     RH
*        MVI   SCANALL4+63,X'0A'   FIX BAD GQSCAN GEN               RH
*        MVI   SCANALL4+65,X'08'   FIX BAD GQSCAN GEN               RH
         LA    R5,QNAME            NO, R5 -> TO QNAME
SCANALL4 GQSCAN AREA=((1),(4)),                                        *
               SCOPE=ALL,RESNAME=((5)),                                *
               TOKEN=GQTOKEN
         SPACE 2
***********************************************************************
*                                                                     *
*     PROCESS THE GQSCAN RETURN CODE.                                 *
*                                                                     *
***********************************************************************
         SPACE 3
*
*    GQSCAN REGISTER VALUES ARE:
*
*       R0 - UPPER HALF CONTAINS THE RIB LENGTH.  THE LOWER HALF
*            CONTAINS THE RIBE LENGTH.
*
*       R1 - INDICATES THE NUMBER OF RIB'S IN THE GQSCAN BUFFER.
*
*      R15 -  0 = ALL THE DATA HAS BEEN COPIED INTO THE GQSCAN BUFFER.
*             4 = NO RESOURCES MATCHED THE CALLER'S REQUEST.
*             8 = NOT ALL OF THE DATA WILL FIT INTO THE GQSCAN BUFFER.
*            12 = ABNORMAL SITUATION ENCOUNTERED.
*            16 = INVALID 'SYSNAME' SPECIFIED.  SHOULD NOT OCCUR.
*
CHECKGQ  EQU   *
         ST    R1,RIBNBR           STORE NO. OF RIB'S IN BUFFER
         ST    R1,RIBNBR2          STORE NO. OF RIB'S FOR DEBUGGING
         SRDL  R0,16               ISOLATE RIB LENGTH
         ST    R0,RIBLTH           STORE RIB LENGTH
         SRL   R1,16               ISOLATE RIBE LENGTH
         ST    R1,RIBELTH          STORE RIBE LENGTH
         C     R15,=F'0'           ALL RIB'S RETURNED IN BUFFER ???
         BE    NORMAL              YES
         C     R15,=F'4'           NO RIB'S RETURNED ???
         BE    NORIBS              YES
         C     R15,=F'8'           WAS BUFFER FILLED ???
         BE    NORMAL              YES, NORMAL FOR NOW
*        BE    FATAL1              YES
         C     R15,=F'12'          ABNORMAL SITUATION ???
         BE    FATAL2              YES
         C     R15,=F'16'          INVALID 'SYSNAME' SPECIFIED ???
         BE    FATAL3              YES
         BE    FATAL4              UNKNOWN RETURN CODE
         SPACE 2
***********************************************************************
*                                                                     *
*     PROCESS FATAL ERRORS.                                           *
*                                                                     *
***********************************************************************
         SPACE 3
*
*    FATAL ERROR 1.  THE GQSCAN MACRO'S RETURN CODE WAS 8.  THIS MEANS
*    THE GQSCAN BUFFER WAS FILLED.
*
FATAL1   EQU   *
*        LA    R01,PFATAL1         ADDRESS OF PARAMETER LIST
         B     CFATAL              GO CALL DQFATAL
*
*    FATAL ERROR 2.  THE GQSCAN MACRO'S RETURN CODE WAS 12.  THIS MEANS
*    AN ABORMAL SITUATION OCCURRED WHILE PROCESSING THE GQSCAN MACRO.
*
FATAL2   EQU   *
*        LA    R01,PFATAL2         ADDRESS OF PARAMETER LIST
         B     CFATAL              GO CALL DQFATAL
*
*    FATAL ERROR 3.  THE GQSCAN MACRO'S RETURN CODE WAS 16.  THIS MEANS
*    AN INVALID 'SYSNAME' WAS SPECIFIED.  THIS SHOULD NOT OCCUR AS THE
*    'SYSNAME' PARAMETER IS NOT USED.
*
FATAL3   EQU   *
*        LA    R01,PFATAL3         ADDRESS OF PARAMETER LIST
         B     CFATAL              GO CALL DQFATAL
*
*    FATAL ERROR 4.  THE GQSCAN MACRO'S RETURN CODE WAS NOT DOCUMENTED.
*
FATAL4   EQU   *
*        LA    R01,PFATAL4          ADDRESS OF PARAMETER LIST
*
*    ENTER HERE IF THE RETURN CODE IS NOT DOCUMENTED.
*
CFATAL   EQU   *
         DC    H'0'
*        L     R15,=V(DQFATAL)      ADDRESS OF ERROR ROUTINE
*        BALR  R14,R15              CALL DQFATAL
*        B     ABNORMAL             SET RETURN CODE TO 4
         SPACE 2
***********************************************************************
*                                                                     *
*    END ROUTINE.                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
*
*    NORMAL TERMINATION.
*
NORMAL   EQU   *
         L     R4,GQCORE           R4 -> TO GQSCAN RIBS
         LR    R2,R4               R2 -> TO GQSCAN RIBS
         B     FOUNDIT             GQSCAN GIVES US THE QNAME
*                                  WE WANT
         SPACE 1                                                    RH
NORIBS   DS    0H                 NO RIBS, NO QCB                   RH
         CLC   20(4,R2),24(R2)    DID WE LOSE SOME RIBES ?
         BH    LOSTRIBE           YES, END FOR NOW
         ICM   R15,15,GQTOKEN      ARE THERE MORE RIBS IN GRS ?
         BNZ   QTEST               YES, GO GET EM
         SPACE 1                                                    RH
QNFOUND  NOP   RETURN             B IF QNAME FOUND                  RH
LOSTRIBE MVC   QMSGNAM,QNAME      MOVE QNAME TO MSG                 RH
         TPUT  QMSG,LQMSG         PUT MSG TO TERMINAL               RH
         SPACE 2
RETURN   DS    0H               COMMON RETURN POINT
         L     R4,GQCORE        R4 -> TO GOTTEN CORE
         L     R5,GQLTH         R5 = LTH OF GOTTEN CORE
         FREEMAIN RU,A=(4),LV=(5)  FREE GOTTEN CORE
         L     R13,SAVE+4       COMMON RETURN POINT
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
         SPACE 2
MVCQ     MVC   QNAME(0),0(R10)    MOVE QNAME TO SAVE AREA           RH
         SPACE 2
FOUNDIT  DS    0H
         L     R3,24(0,R2)         R3 = NBR OF RIBES IN RIB
         ST    R3,NBRRIBES         SAVE IT
         LA    R3,0(0,R2)          R3 -> TO RIB
         A     R3,RIBLTH           R3 -> TO MINOR RNAME
         MVI   QNFOUND+1,X'F0'    CHANGE NOP TO B                   RH
         MVC   QMSGNAM,32(R2)     MOVE QNAME TO MSG                 RH
         SPACE 1                                                    RH
MINORLP  CLI   0(R10),C'*'      DO ALL DSNAMES?
         BE    SKIPCHEK            YES
         EX    R11,DSNCOMP      IS IT OUR DSN?
         BNE   NEXTMIN          CHECK NEXT MINOR QCB
         SPACE 1                                                    RH
SKIPCHEK DS    0H
         MVC   SCOPE,=CL7'SYSTEM ' DEFAULT SCOPE TO SYTEM
         TM    30(R2),X'80'        IS SCOPE SYSTEM ?
         BO    SCOPESET            YES
         MVC   SCOPE,=CL7'SYSTEMS' SET SCOPE TO SYTEMS
         TM    30(R2),X'40'        IS SCOPE SYSTEMS?
         BO    SCOPESET            YES
         MVC   SCOPE,=CL7'STEP   ' SET SCOPE TO STEP
         TM    30(R2),X'40'        IS SCOPE STEP ?
         BO    SCOPESET            YES
         MVC   SCOPE,=CL7'GLOBAL ' SET SCOPE TO GLOBAL
         TM    30(R2),X'40'        IS SCOPE GLOBAL ?
         BO    SCOPESET            YES
         MVC   SCOPE,=CL7'LOCAL  ' IT MUST BE LOCAL THEN
         SPACE 1                                                    RH
SCOPESET DS    0H
*        SLR   R8,R8
*        IC    R8,30(,R2)         GET SCOPE FLAG BYTE
*        STC   R8,HEXSCOPE+1      SET UP BYTE 2                     RH
*        SRL   R8,4               DROP LOW ORDER 4 BITS             RH
*        STC   R8,HEXSCOPE        SET UP BYTE 1                     RH
*        MVZ   HEXSCOPE(1),=X'F0'  FIX ZONE                         RH
*        MVZ   HEXSCOPE+1(1),=X'F0' FIX ZONE                        RH
*
         SLR   R8,R8
         IC    R8,31(,R2)       GET LENGTH OF DSNAME
         CVD   R8,DWORD           CONVERT TO DECIMAL                RH
         UNPK  QLTH(3),DWORD+6(2) MOVE LTH TO MSG                   RH
         MVI   QLTH,C' '           BLANK FIRST CHAR                 RH
         MVZ   QLTH+2(1),QLTH+1    FIX ZONE                         RH
         CH    R8,=H'52'          IS LTH OKAY?                      RH
         BNH   *+4+4              YES                               RH
         LH    R8,=H'52'          NO, SET TO MAX                    RH
         BCTR  R8,R0            MINUS ONE FOR EXECUTE
         MVI   DSNBUFF,C' '     CLEAR FIELD TO BLANKS FIRST
         MVC   DSNBUFF+1(51),DSNBUFF
         EX    R8,DSNMOVE       MOVE DSN TO OUR BUFF
         LH    R4,28(0,R2)         R4 = LTH OF RIBVAR
         LA    R4,40(R4,R2)        R4 -> TO RIBE
QELLP    MVC   TYPE(3),=CL3'SHR'
         TM    30(R4),X'80'     IS IT ENQUEUED SHARE?
         BO    *+10             YES
         MVC   TYPE(3),=CL3'OLD'
*        LH    R5,28(,R4)       GET ASID ASSOCIATED WITH QEL
*        SLL   R5,2             MULT BY 4 TO INDEX TABLE
*        LA    R5,0(R5,R6)      GET ASSOCIATED ASCB ADDR.
*        ICM   R5,7,1(R5)
*        BZ    PUTMSG
*        CLC   0(4,R5),=C'ASCB'
*        BNE   PUTMSG
*        ICM   R1,7,173(R5)     GET JOBNAME PTR
*        BZ    TRYSTC
         MVC   JOBNAME(8),0(R4) MOVE IN JOBNAME
         B     PUTMSG
*RYSTC   ICM   R1,7,177(R5)     GET STC NAME PTR
*        BZ    PUTMSG
*        MVC   JOBNAME(8),0(R1) MOVE IN STC NAME
PUTMSG   DS    0H
         CLC   =C'*E',0(R10)      LIST ONLY EXCLUSIVE REQUESTS?
         BNE   DOALL
         CLC   TYPE(3),=CL3'OLD'
         BNE   NEXTQEL
DOALL    CLC   0(2,R10),=C'*J'  LIST A PARTICULAR JOBNAME?
         BNE   SKIPJOB          NO, LIST ALL JOBS
         EX    R9,JOBCOMP       DO JOBNAMES MATCH?
         BNE   NEXTQEL
SKIPJOB  CLC   DSNBUFF(52),OLDNAME
         BNE   TPUT
         MVI   DSNBUFF,C' '
         MVC   DSNBUFF+1(51),DSNBUFF
         MVI   DSNBUFF+4,C'"'
TPUT     DS    0H
         CLC   GQNAME,QMSGNAM      STILL SAME QNAME ?
         BE    TPUT2               YES, ON WE GO
         MVC   GQNAME,QMSGNAM      NO, SAVE NEW QNAME               RH
         TPUT  QMSG,14             SEND QNAME MSG                   RH
         SPACE 1                                                    RH
TPUT2    DS    0H
         TPUT  DSNBUFF,80          PRINT ENQ LINE
         CLI   DSNBUFF,C' '
         BE    TPUT3
         MVC   OLDNAME(52),DSNBUFF SAVE DSNAME
*
TPUT3    DS    0H
*
* SEE IF ANY UNPRINTABLE CHARS IN QNAME...
*
         ST    R2,SAVE2           SAVE R2 OVER TRT
         EX    R8,TRANS1          LOOK FOR ANY UNPRINTABLE CHAR
         L     R2,SAVE2           RELOAD R2
         BZ    BUFOKAY            NONE
         SPACE 1
CHKLTH   DS    0H
         LR    R1,R8              R1 = LTH
         CH    R8,=H'31'          MORE THAN 33 CHARS?
         BL    UNDER8             NO
         LA    R1,31              YES, HEX 1ST 32 BYTES
         SPACE 1
UNDER8   DS    0H                 CLEAR SOME FIELDS
         MVI   DUMPLINE,C' '
         MVC   DUMPLINE+1(79),DUMPLINE
         MVC   DUMPLINE+4(4),=C'HEX='
         MVI   DUMPDATA,C' '
         MVC   DUMPDATA+1(79),DUMPDATA
         MVI   DUMPDALO,C' '
         MVC   DUMPDALO+1(79),DUMPDALO
*
* DUMP THE DATA FROM THE SPECIFIED STORAGE AREA
*
*
DUMPRINT EQU   *
         EX    R1,MVC1   DUMPDATA(00),DSNBUFF    GET DATA TO TRANSLATE
         EX    R1,MVC2   DUMPDALO(00),DSNBUFF    GET DATA TO TRNASLATE
         EX    R1,TR1    DUMPDATA(00),DUMPTBHI   CONVERT HO ORDER HEX
         EX    R1,TR2    DUMPDALO(00),DUMPTBLO   CONVERT LO ORDER HEX
         MVC   DUMPLINE+8(64),DUMPSELD DIGITS TO SELECT WAS 72
         TR    DUMPLINE+8(64),DUMPDATA DATA => O/P LINE
DUMPNXT1 EQU   *
         TPUT  DUMPLINE,80         PRINT ENQ LINE
DUMPEND  DS    0H
         SPACE 1
BUFOKAY  DS    0H
*
NEXTQEL  DS    0H
         L     R15,NBRRIBES        R15 = NBR RIBES
         SH    R15,=H'1'           -1
         BZ    NEXTMIN             NO RIBES LEFT, GET NEXT RIB
         ST    R15,NBRRIBES        SAVE NBR RIBES LEFT
         A     R4,RIBELTH          R4 -> TO NEXT RIBE
         B     QELLP               GO PROCESS IT
         SPACE 2
NEXTMIN  DS    0H
         L     R15,RIBNBR          R15 = NBR OF RIBS LEFT
         SH    R15,=H'1'           - 1
         BZ    NORIBS              IF NONE LEFT, END OF GENQ
         ST    R15,RIBNBR          SAVE NBR OF RIBS LEFT
         SPACE 1
         LA    R4,40(0,R2)         R4 -> TO END OF FIXED RIB
         LH    R15,28(0,R2)        R15 = LTH OF RIBVAR
         AR    R4,R15              R4 NOW -> TO FIRST RIBE IN RIB
         L     R3,24(0,R2)         R3 = NBR OF RIBES IN RIB
         MH    R3,RIBELTH+2        R3 = LTH OF ALL RIBES
         AR    R4,R3               R4 SHOULD NOW -> TO NEXT RIB
         LR    R2,R4               SO SHOULD R2
         B     FOUNDIT             GO CHECK IT OUT
         SPACE 2
HELP     TPUT  HELP1,L'HELP1
         TPUT  HELP2,L'HELP2
         TPUT  HELP3,L'HELP3
         TPUT  HELP4,L'HELP4
         TPUT  HELP5,L'HELP5
         TPUT  HELP6,L'HELP6
         TPUT  HELP7,L'HELP7
         TPUT  HELP8,L'HELP8      PUT ENTER MSG                     RH
         SPACE 1                                                    RH
GETANS   DS    0H                                                   RH
         TGET  REPLY,80,EDIT,WAIT GET INPUT                         RH
         LTR   R15,R15            WAS TGET OKAY ?                   RH
         BNZ   RETURN             NO, RETURN                        RH
         LR    R11,R1             PUT LTH IN R11                    RH
         LA    R10,REPLY          R10 -> TO REPLY                   RH
         OC    REPLY,OLDNAME      MAKE SURE ANSWER IS UPPER CASE    RH
         B     DECODE             GO DECODE ANSWER                  RH
         EJECT
MVC1     MVC   DUMPDATA(00),DSNBUFF    GET DATA TO TRANSLATE
MVC2     MVC   DUMPDALO(00),DSNBUFF    GET DATA TO TRNASLATE
TR1      TR    DUMPDATA(00),DUMPTBHI   CONVERT HO ORDER HEX
TR2      TR    DUMPDALO(00),DUMPTBLO   CONVERT LO ORDER HEX
DSNMOVE  MVC   DSNBUFF(0),0(R3)    COPY DSN FROM MINOR QCB
DSNCOMP  CLC   0(0,R10),0(R3)      DO THE DSNS MATCH?
JOBCOMP  CLC   2(0,R10),JOBNAME DO JOBNAMES MATCH?
TRANS1   TRT   DSNBUFF(0),TRTAB   LOOK FOR ANY UNPRINTABLE CHAR
TRTAB    DC    256X'FF'           UNPRINTABLE
         ORG   TRTAB+64
         DC    XL1'00'            BLANK IS OKAY
         ORG   TRTAB+X'4B'
         DC    XL1'00'            PERIOD IS OKAY
         ORG   TRTAB+C'"'
         DC    XL1'00'            DOUBLE QUOTE IS OKAY
         ORG   TRTAB+C'#'
         DC    XL1'00'            POUND SIGN IS OKAY
         ORG   TRTAB+C'A'
         DC    9X'00'             ABCDEFGHI IS OKAY
         ORG   TRTAB+C'J'
         DC    9X'00'             JKLMNOPQR IS OKAY
         ORG   TRTAB+C'S'
         DC    8X'00'             STUVWXYZ IS OKAY
         ORG   TRTAB+C'0'
         DC    10X'00'            0123456789 IS OKAY
         ORG
         SPACE 1
WORKU    DC    CL52' '
         SPACE 2
SAVE2    DC    F'-1'
SAVE     DC    18F'-1'
DWORD    DC    D'0'
OLDNAME  DC    CL52' '
DSNBUFF  DC    CL52' '
         DC    CL1' '
JOBNAME  DC    CL8' '
         DC    CL1' '
TYPE     DC    CL3' '
QLTH     DC    CL3' '
         DC    CL1' '
SCOPE    DC    CL7' '
         DC    CL1' '
HEXSCOPE DC    CL2' '
         DC    CL1' '
         SPACE 1
HELP1    DC    C'PARM FIELD OPTIONS:'
HELP2    DC    C'Q=ENQ NAME(ABBREV) AS 1ST PARM. DEFAULT = SYSDSN'  RH
HELP3    DC    C'     *       ..... LIST ALL DSNAMES IN USE'
HELP4    DC    C'     *E      ..... LIST ALL EXCLUSIVE REQUESTS'
HELP5    DC    C'     XXXX    ..... LIST ALL DSNS STARTING WITH XXXX'
HELP6    DC    C'                   (CAN BE ANY LENGTH UP TO 44)'
HELP7    DC    C'     *JXXXX  ..... LIST ALL DSNS FOR JOBNAME XXXX'
HELP8    DC    C'ENTER PARM -EG- Q=SPF,*E'                          RH
QNAME    DC    CL8'SYSDSN'                                          RH
QMSG     DC    C'QNAME='                                            RH
QMSGNAM  DC    CL8' '                                               RH
         DC    C' NOT FOUND'                                        RH
LQMSG    EQU   *-QMSG                                               RH
GQNAME   DC    CL8' '                                               RH
REPLY    DC    CL80' '            TGET BUFFER                       RH
MISSPARM DC    C'MISSING PARAMETER, RE-ENTER'                       RH
LMISS    EQU   *-MISSPARM                                           RH
         SPACE 2
GQCORE   DS    F
GQLTH    DC    F'32000'
GQTOKEN  DS    F
RIBNBR   DS    F
RIBNBR2  DS    F
RIBLTH   DS    F
RIBELTH  DS    F
NBRRIBES DS    F
DUMPDATA DS    8F
DUMPDALO DS    8F
DUMPBLK2 DC    CL8' '             LEAVE AFTER DUMPDALO
DUMPBLNK DC    CL80' '
DUMPTBHI DC    16X'F0'
         DC    16X'F1'
         DC    16X'F2'
         DC    16X'F3'
         DC    16X'F4'
         DC    16X'F5'
         DC    16X'F6'
         DC    16X'F7'
         DC    16X'F8'
         DC    16X'F9'
         DC    16X'C1'
         DC    16X'C2'
         DC    16X'C3'
         DC    16X'C4'
         DC    16X'C5'
         DC    16X'C6'
DUMPTBLO DC    16X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
DUMPSELR DC    X'00400141024203438004440545064607'
         DC    X'4780084809490A4A0B4B800C4C0D4D0E'
         DC    X'4E0F4F80801050115112521353801454'
         DC    X'15551656175780185819591A5A1B5B80'
         DC    X'1C5C1D5D1E5E1F5F'
DUMPSELD DC    X'00200121022203234004240525062607'
         DC    X'2740082809290A2A0B2B400C2C0D2D0E'
         DC    X'2E0F2F40401030113112321333401434'
         DC    X'15351636173740183819391A3A1B3B40'
         DC    X'1C3C1D3D1E3E1F3F'
DUMPSELA DC    X'010502060307'
DUMPLINE DC    C' '
         DC    CL79' '
*        DC    CL132' '
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   ENQ
//LKED.SYSLMOD DD DSN=SYS9.LOADLIB(GENQ),DISP=SHR
//*KED.SYSLMOD DD DSN=HALL.LINKLIB(GENQ1),DISP=SHR
//
