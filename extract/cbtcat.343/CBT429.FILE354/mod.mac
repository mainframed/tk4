         MACRO
*
*        THIS  MACRO SIMULATES THE PLI MOD FUNCTION
*
&NAME    MOD   &X,&Y,&ANS
         AIF   ('&X(1,1)' NE '(').NOPAREN
         LR    R14,&X             PUT NUMBER IN R14
         AGO   .X1
.NOPAREN ANOP
         L     R14,&X             PUT NUMBER IN R14
.X1      ANOP
         SRDA  R14,32             SHIFT IT TO R15
.*
         AIF   ('&Y(1,1)' NE '(').NOPARN2
         LR    R1,&Y              PUT DIVISOR IN R14
         AGO   .Y1
.NOPARN2 ANOP
         L     R1,&Y              PUT DIVISOR IN R14
.Y1      ANOP
         DR    R14,R1             DIVIDE
         LTR   R14,R14            IS REMAINDER POSITIVE?
         BNM   *+4+4              YES
         LPR   R1,R1              TWEEK DIVISOR
         AR    R14,R1             ADD IT TO REMAINDER
.*
         AIF   ('&ANS(1,1)' NE '(').NOPARN3
         LR    &ANS,R14           PUT ANSWER IN HIS REG
         AGO   .ANS1
.NOPARN3 ANOP
         ST    R14,&ANS           PUT ANSWER IN HIS ANSWER FIELD
.ANS1    ANOP
         MEND
