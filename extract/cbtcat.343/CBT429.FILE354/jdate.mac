//JDATE    JOB
/*ROUTE PRINT R7
//JDATE     EXEC  HLASMCL,
//             PARMC='NOALIGN,NORLD',CLASS='*',COND.LKED=(7,LT,ASM)
//ASM.SYSIN DD  *
*                                                                   Y2
*              MODIFIED JAN 06/97 TO ACCEPT A 7 CHAR JULIAN DATE    Y2
*              TO BE YEAR 2000 COMPLIANT                            Y2
*              CHANGES FOR YEAR 2000 HAVE Y2 IN COLS 69-70          Y2
*
*              MODIFIED JUL24/91  TO ACCEPT A JULIAN DATE
*              THAT WILL BE CONVERTED TO DAY/DATE, AND USE PUTLINE
*              NOT TPUT SO OUTPUT CAN BE TRAPPED IN A CLIST IF
*              JDATE IS CALLED AS A TSO COMMAND....
*
*
JDATE    CSECT      DISPLAY TODAYS JULIAN DATE, ETC.
         USING *,R15              SET TEMP BASE
         B     AROUND             BRANCH AROUND DUMP TITLE
         DC    AL1(EYELTH)
EYECATCH DC    C'JDATE '          PROGRAM NAME
         DC    C'VERSION 3.0 '    VERSION                           Y2
         DC    C'&SYSDATE'        DATE WRITTEN
         DC    C' '               BLANK
         DC    C'&SYSTIME'        TIME WRITTEN
         DC    C' '               BLANK
         DC    C'ACCEPT 7 CHAR PARM FOR YEAR 2000'                  Y2
*        DC    C'VERSION 2. ACCEPT PARM'
EYELTH   EQU   *-EYECATCH         LTH OF EYECATCHER
         SPACE 1
AROUND   DS    0H
         LA    R1,0(0,R1)         CLEAR HI-ORDER BYTE FOR 31 BIT
         LA    R13,0(0,R13)       CLEAR HI-ORDER BYTE FOR 31 BIT
         BSM   R14,0              SAVE CURRENT AMODE
         SPACE 1
         STM   R14,R12,12(R13)
         LR    R12,R15
         DROP  R15
         USING JDATE,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         ST    R1,SAVE            SAVE R1 (ADDR INPUT PARM)
         SPACE 1
*        XC    CONID,CONID
*        EXTRACT ANSWER,FIELDS=(TSO,TIOT)
*        L     R2,TSO             TSO USER?
*        TM    0(R2),X'80'
*        BZ    PARM0              NO
**       L     R2,TIOT            GET USERID
**       MVC   USERID,0(R2)       FROM TIOT
**       MVC   JOBSAVE,=CL8' '    CLEAR JOB NAME SAVE FIELD
         SPACE 2
*        UNDER TSO. INVOKED AS A  COMMAND PROCESSOR.
*        SET UP THE IOPL FOR PUTLINE.
*
         L     R2,SAVE            R2 -> TO INPUT PARM
         USING CPPL,R2
*
*        WE MAY BE RUNNING UNDER TSO, BUT DO WE HAVE A CPPL
*        OR WAS THIS A CALL TO JDATE???
*
         TM    0(R2),X'80'        IS HIGH BIT ON?
         BZ    NOTCALL            NO, WE PROBABLY HAVE A CPPL
         MVI   NOCPPL,X'FF'       SAY WE DON'T HAVE A CPPL
         B     PARM0              ON WE GO
         SPACE 1
NOTCALL  DS    0H
         LA    R3,IOPLADD
         USING IOPL,R3
         L     R4,CPPLUPT
         ST    R4,IOPLUPT         SET UP IOPL
         L     R4,CPPLECT
         ST    R4,IOPLECT
         LA    R4,ECB
         ST    R4,IOPLECB
*        L     R2,CPPLCBUF
         DROP  R2,R3
         SPACE 1
***********************************************************************
*
*              SEE IF THERE IS A  PARM
*
*        VALID PARMS ARE:
*              A 5 BYTE JULIAN DATE...  YYDDD...
*              A 7 BYTE JULIAN DATE...YYYYDDD...                    Y2
*
***********************************************************************
         SPACE 1
*              GET PARM, IF ANY
         SPACE 1
PARM0    DS    0H
         L     R1,SAVE            R1 = -> TO PARM
         L     R2,0(0,R1)         R2 -> TO PARM
         LA    R10,2(0,R2)        R10 -> TO PARM
         LH    R2,0(0,R2)         R2 = PARM LTH
         LTR   R2,R2              IS THERE A PARM ?
         BZ    ENDPARM            NO
         LR    R4,R10             YES, R4 -> TO PARM
         CH    R2,=H'5'           MORE OR LESS THAN 5 CHARS?
         BE    PARM1              NO, ON WE GO...
         CH    R2,=H'7'           MORE OR LESS THAN 7 CHARS?        Y2
         BE    PARM2              NO, ON WE GO...                   Y2
*
         CLI   NOCPPL,X'FF'       DO WE HAVE A CPPL ?
         BE    ENDPARM            NO, IGNORE PARM...
         LR    R15,R2             R15 = LTH OF CPPL PARM
         SH    R15,=H'4'          - 4 FOR RDW...
         SH    R15,0(0,R10)       - OFFSET TO PARM...
         AH    R10,0(0,R10)       R10 -> TO CMD
         LA    R10,2(0,R10)       R10 -> TO REAL PARM
         LR    R4,R10             SO DOES R4
         LA    R2,5               SET LTH TO 5
         CH    R15,=H'5'          IS PARM LTH 5?
         BE    PARM1              YES                               Y2
         LA    R2,7               SET LTH TO 7                      Y2
         CH    R15,=H'7'          IS PARM LTH 7?                    Y2
         BE    PARM2              YES                               Y2
         B     ENDPARM            NO, IGNORE IT                     Y2
*        BNE   ENDPARM            NO, IGNORE IT
*
*              WE WILL ASSUME SOMEONE ON TSO CALLED JDATE AS A CMD...
*
*        AH    R10,0(0,R10)       R10 -> TO CMD
*        LA    R10,2(0,R10)       R10 -> TO REAL PARM
*        LR    R4,R10             SO DOES R4
*        LA    R2,5               SET LTH TO 5
         SPACE 1
PARM1    DS    0H                 5 BYTE PARM                       Y2
         TIME  DEC,SAVETIME,            GET THE SYS TIME            Y2 +
               LINKAGE=SYSTEM,DATETYPE=YYYYMMDD     IN THIS CASE    Y2
         UNPK  PARMDATA(3),SAVETIME+8(2) SET CENTURY                Y2
*        MVC   PARMDATA(2),=C'19' SAY CENTURY IS 19                 Y2
         MVC   PARMDATA+2(5),0(R10) SAVE PARM                       Y2
         B     PARM3              ON WE GO                          Y2
         SPACE 1                                                    Y2
SAVETIME DC    XL16'00'                                             Y2
PARMDATA DC    CL7' '                                               Y2
         SPACE 1                                                    Y2
PARM2    DS    0H                 7 BYTE PARM                       Y2
         MVC   PARMDATA(7),0(R10) SAVE PARM                         Y2
         SPACE 1                                                    Y2
PARM3    DS    0H                                                   Y2
         LA    R4,PARMDATA        R4 -> TO PARM                     Y2
         LR    R10,R4             R10 -> TO PARM                    Y2
         SPACE 1                                                    Y2
PARM5    DS    0H
         CLI   0(R4),C'0'         IS IT LESS THAN 0?
         BL    ENDPARM            YES, IGNORE IT
         CLI   0(R4),C'9'         IS IT MORE THAN 9?
         BH    ENDPARM            YES, IGNORE IT
         LA    R4,1(0,R4)         R4 -> TO NEXT KEY BYTE, IF ANY
         BCT   R2,PARM5           CHECK ALL CHARS LEFT
         PACK  LINE(4),PARMDATA   CONVERT DATE TO DECIMAL           Y2
*        PACK  LINE(4),0(5,R10)   CONVERT DATE TO DECIMAL
         SPACE 1
ENDPARM  DS    0H
         CALL  DAYDATE,(LINE),VL  GO GET TODAYS DATE
         CLI   NOCPPL,X'00'       DO WE HAVE A CPPL ?
         BE    USEPUTL            YES, USE PUTLINE..
         LA    R0,40              MESSAGE LENGTH
         LA    R1,LINE            MESSAGE ADDR
         TPUT  (1),(0)            TSO MESSAGE SENT
         B     ERREXIT            ON WE GO
         SPACE 1
USEPUTL  DS    0H
*        LA    R2,L'LINE          ENTIRE LINE LENGTH
*        LA    R2,4(0,R2)         ADD RDW LEN
*        STH   R2,RDWP
         PUTLINE PARM=PUTLIST,OUTPUT=(RDWP,TERM,SINGLE,DATA),          +
               MF=(E,IOPLADD)
ERREXIT  DS     0H
*        L      R13,4(0,R13)      R13 -> TO SYSTEM SAVE AREA
*        RETURN (14,12),RC=0      RETURN TO SYSTEM
         LA    R15,0              R15 = RC=0
         L     R13,4(R13)         R13 -> TO CALLERS SAVE AREA
         L     R14,12(R13)        SET R14 TO WHAT CALLER'S R14
         LM    R0,R12,20(R13)     SET R0-R12 TO CALLERS REGS
*              GET INTO PROPER MODE AND GO BACK TO CALLER
         BSM   0,R14              RETURN TO CALLING PROGRAM
         SPACE 2
SAVE     DC    18F'-1'
ANSWER   DS    0F
TIOT     DS    F
TSO      DS    F
PUTLIST  PUTLINE MF=L
         DS    0F                 ALIGN ON FULLWORD
RDWP     DC    H'44'              LTH OF LINE + 4
         DC    H'0'               FILLER
LINE     DC    CL40' '
ECB      DC    F'0'
IOPLADD  DS    4F
NOCPPL   DC    X'00'              FF IF NO CPPL
         SPACE 1
         LTORG
         SPACE 1
         IKJCPPL
         IKJIOPL
         SPACE 1
*    REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
          END   JDATE
/*
//*
//* NOTE...IF SYSLMOD IS A DATASET IN YOUR STEPLIB OR LINKLIST,
//* JDATE CAN BE RUN IN TSO BY JUST ENTERING JDATE
//*
//LKED.SYSLMOD DD DSN=HALL.LINKLIB,DISP=SHR
//LKED.SYSLIN2 DD DSN=IMSVS.AUTHLIB1,DISP=SHR
//LKED.SYSIN DD *
  INCLUDE SYSLIN2(DAYDATE)
  SETCODE AC(0)
  NAME JDATE(R)
/*
//
