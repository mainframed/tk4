//STEP1 EXEC   HLASMC
//ASM.SYSIN DD *
         TITLE '- SUBROUTINE TO BLANK OUT A 3270 SCREEN -'
BLNKSCN  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,R12                    DEFINE BASE REGISTER
         STM   R14,R12,12(R13)          SAVE INCOMING REGISTERS
         LR    R12,R15                  INITIALIZE BASE REGISTER
         LR    R11,R13                  COPY OLD SAVE AREA POINTER
         LA    R13,SAVE                 POINT AT NEW SAVE AREA
         ST    R13,8(,R11)              CHAIN NEW OFF OLD
         ST    R11,SAVE+4               AND OLD OFF NEW
BRANCH1  BC    0,BRANCH2                NOP/BYPASS
         OI    BRANCH1+1,X'F0'          DO THE GTSIZE ONLY ONCE
         GTSIZE
         LTR   R0,R0                    SEE IF A SCREEN
         BNZ   BRANCH2                  BYPASS INSTRUCTION IF IS
         OI    BRANCH2+1,X'F0'          NOT A SCREEN. BYPASS TPUT
BRANCH2  BC    0,RETURN                 NOP/RETURN
         STFSMODE ON,INITIAL=YES        TELL TSO/VTAM IN FULLSCRN
         TPUT  BLANKOUT,L'BLANKOUT,FULLSCR    BLANK OUT THE SCREEN
         STFSMODE OFF                   TELL TSO/VTAM OUT OF FULLSCRN
RETURN   L     R13,SAVE+4               GET OLD SAVE AREA POINTER
         LM    R14,R12,12(R13)          RESTORE ALL THE REGISTERS
         SLR   R15,R15                  ZERO CONDITION CODE
         BR    R14                      RETURN TO CALLER
SAVE     DC    18F'0'                   SAVE AREA
BLANKOUT DC    X'401140403C404000114040'    MESSAGE TO BLANK OUT SCREEN
         END
//
