//SMFEXIT  JOB
/*ROUTE  PRINT  R115
//S1        EXEC  HLASMCL,CLASS='*',
//   COND.LKED=(7,LT,ASM)
//ASM.SYSLIB DD DSN=SYS1.MACLIB,
//   DISP=SHR  ,DCB=BLKSIZE=4080
//   DD DSN=SYS1.HASPSRC,DISP=SHR
//   DD DSN=SYS1.AMODGEN,DISP=SHR
//ASM.SYSIN DD  *
         TITLE '******* USER2 EXIT FOR IFASMFDP ******'
*
*        THIS  LITTLE PROGRAM IS  USED TO PRODUCE A READABLE
*        REPORT BASED ON TYPE 14, 15, 17 AND 18 SMF RECORDS
*
         SPACE 3
SMFEXIT  CSECT
         USING *,R15              SET TEMP BASE
         B     AROUND             BRANCH AROUND DUMP TITLE
         DC    C'SMFEXIT '        PROGRAM NAME
         DC    C'&SYSDATE'        DATE WRITTEN
         DC    C'&SYSTIME'        TIME WRITTEN
AROUND   DS    0H
         STM   R14,R12,12(R13)
         LR    R12,R15
         DROP  R15
         USING SMFEXIT,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         LR    R3,R1              R3 -> TO PARMS
         L     R4,0(0,R3)         R4 -> USER PARMS
         L     R2,8(0,R4)         R2 = 3RD USER PARM
         LTR   R2,R2              HAVE WE BEEN HERE BEFORE?
         BNZ   PARMOK             YES, ON WE GO
         LA    R2,1               R2 = 1
         ST    R2,8(0,R4)         SET 3RD USER PARM
         SPACE 1
         L     R5,4(0,R4)         R5 = R1 AT ENTRY TO IFASMFDP
         L     R5,0(0,R5)         R5 -> TO ENTRY PARM
         LH    R2,0(0,R5)         R2 = PARM LTH
         STH   R2,PARMLTH         SAVE PARM LTH
         LA    R15,0              R15 = 0
         LTR   R2,R2              IS THERE A PARM?
         BZ    NOPARM             NO, GO OPEN REPORT FILE
         SPACE 1
         LA    R5,2(0,R5)         R5 -> TO PARM
         BCTR  R2,0               R2 = HEX LTH
         EX    R2,SETPARM         MOVE PARM TO PARM FIELD
         SPACE 1
NOPARM   DS    0H
         OPEN  (REPORT,(OUTPUT))  OPEN SYSPRINT FILE
         PUT   REPORT,HEADING     PRINT HEADING
         PUT   REPORT,HEADING2    PRINT HEADING 2
         LA    R15,0              R15 = 0
         SPACE 1
PARMOK   DS    0H
         L     R5,4(0,R3)         R5 -> TO SMF RECORD
         USING RECDFORM,R5        SET BASE
*        LA    R6,SMFDSN          R6 -> DSNAME
*        LA    R6,68(0,R5)        R6 -> DSNAME
         LH    R2,PARMLTH         R2 = PARM LTH
         LTR   R2,R2              IS THERE A PARM?
         BZ    PRINTIT            NO, PRINT ALL OF THEM
         LA    R6,SMF17DSN        R6 -> TYPE 17/18 DSNAME
         CLI   RECDID,15          IS IT A 17/18 RECORD?
         BH    R6SET              YES
         LA    R6,SMFDSN          R6 -> TYPE 14/15 DSNAME
         SPACE 1
R6SET    DS    0H
         BCTR  R2,0               R2 = HEX LTH
         LA    R15,4              R15 = 4...DON'T PRINT RECORD
         EX    R2,CHKNAME         CHECK DSNAME
*HKNAME  CLC   SMFDSN(0),PARMDATA CHECK DSNAME
*HKNAME  CLC   0(0,R6),PARMDATA   CHECK DSNAME
         BNE   NOMORE4            NOT OURS, EXIT
         SPACE 1
PRINTIT  DS    0H
         MVC   PACCESS,=C'READ '  DEFAULT ACCESS IS READ
         CLI   RECDID,14          IS IT A READ RECORD?
         BE    ACCESSOK           YES
         MVC   PACCESS,=C'WRITE'  ACCESS IS WRITE
         CLI   RECDID,15          IS IT A WRITE RECORD?
         BE    ACCESSOK           YES
         MVC   PACCESS,=C'DELET'  ACCESS IS DELETE
         CLI   RECDID,17          IS IT A DELETE RECORD?
         BE    ACCESSOK           YES
         MVC   PACCESS,=C'RENAM'  ACCESS IS RENAME
         CLI   RECDID,18          IS IT A RENAME RECORD?
         BNE   NOMORE4            NO, END OF EXIT
         SPACE 1
ACCESSOK DS    0H
         ICM   R9,15,RSTPTIME     PUT TIME IN WORK REG
         SR    R8,R8              CLEAR R8
         LA    R7,100             PUT DIVISOR IN R7
         DR    R8,R7              DIVIDE TIME BY 100 TO GET SECONDS
         ST    R9,SECS            SAVE SECONDS
         SR    R8,R8              CLEAR R8
         LA    R7,60              R7 = 60
         DR    R8,R7              DIVIDE SECS TO GET MINUTES
         ST    R9,MINS            SAVE MINUTES
         SR    R8,R8              CLEAR R8
         DR    R8,R7              DIVIDE MINS BY 60 TO GET HOURS
         ST    R9,HOURS           SAVE HOURS
         MR    R8,R7              R9 = HOURS IN MINS
         L     R8,MINS            R8 = MINUTES
         SR    R8,R9              R8 = MINUTES LEFT
         ST    R8,MINS            SAVE THEM
         LR    R9,R8              R9 = MINUTES
         SR    R8,R8              CLEAR R8
         MR    R8,R7              R9 = MINUTES IN SECONDS
         L     R8,SECS            R8 = SECONDS
         SR    R8,R9              R8 = SECONDS LEFT
         ST    R8,SECS            SAVE THEM
         CVD   R8,DOUBLE          CONVERT SECONDS TO DECIMAL
         MVC   DOUBLE(4),TIMEPTRN PUT IN EDIT MASK
         ED    DOUBLE(4),DOUBLE+6 EDIT SECONDS
         MVC   SS,DOUBLE+2        SAVE SECONDS
         SPACE 1
         L     R8,MINS            R8 = MINUTES
         CVD   R8,DOUBLE          CONVERT MINUTES TO DECIMAL
         MVC   DOUBLE(4),TIMEPTRN PUT IN EDIT MASK
         ED    DOUBLE(4),DOUBLE+6 EDIT MINUTES
         MVC   MM,DOUBLE+2        SAVE MINUTES
         SPACE 1
         L     R8,HOURS           R8 = HOURS
         CVD   R8,DOUBLE          CONVERT HOURS TO DECIMAL
         MVC   DOUBLE(4),TIMEPTRN PUT IN EDIT MASK
         ED    DOUBLE(4),DOUBLE+6 EDIT HOURS
         MVC   HH,DOUBLE+2        SAVE HOURS
         SPACE 1
         MVC   PTIME,HHMMSS       PUT TIME IN OUTPUT RECORD
         SPACE 1
         UNPK  PDATE(5),RSTPDATE+1(3) CONVERT DATE TO DISPLAY
         MVZ   PDATE+4(1),PDATE+3 FIX ZONE ON LOW BYTE
         SPACE 1
         MVC   PSYSTEM,RCPUID     PUT SYSTEM ID IN RECORD
         MVC   PJOBNAME,RJOBNAME  PUT JOBNAME IN RECORD
*        MVC   PUSERID,SMF14UID   PUT USERID IN RECORD
         CLI   RECDID,15          IS IT A READ/WRITE RECORD?
         BH    TYPE17             NO
         SPACE 1
TYPE1415 DS    0H
         MVC   PDDNAME,SMFDDN     PUT DDNAME IN RECORD
         MVC   PDSNAME,SMFDSN     PUT DSNAME IN RECORD
*        MVC   PRINTDAT,9(R5)     PUT PART OF SMF REC IN PRINT
         B     WRITEIT            GO PRINT IT
         SPACE 1
TYPE17   DS    0H
         CLI   RECDID,17          IS IT A SCRATCH RECORD?
         BH    TYPE18             NO, MUST BE RENAME
         MVC   PDSNAME,SMF17DSN   PUT DSNAME IN RECORD
         B     WRITEIT            GO PRINT IT
         SPACE 1
TYPE18   DS    0H
         MVC   PDSNAME,SMF18DSN   PUT NEW DSNAME IN RECORD
*        B     WRITEIT            GO PRINT IT
         SPACE 1
WRITEIT  DS    0H
         CLC   PDSNAME,SAVEDSN    SAME DSNAME ?
         BE    WRITE2             YES, CLEAR PRINT DSNAME
         MVC   SAVEDSN,PDSNAME    NO, SAVE NEW DSNAME
         B     WRITE3             PRINT RECORD
         SPACE 1
WRITE2   DS    0H
         MVI   PDSNAME,C' '       CLEAR PRINT DSNAME
         MVC   PDSNAME+1(43),PDSNAME   ''
         SPACE 1
WRITE3   DS    0H
         PUT   REPORT,PRINTREC    PRINT PART OF RECORD
         MVI   PACCESS,C' '       CLEAR PRINT REC
         MVC   PACCESS+1(131),PACCESS   ''
         SLR   R15,R15            RC = 0
         SPACE 1
NOMORE4  DS    0H
*        CLOSE (REPORT)
         L     R13,SAVE+4         R13 -> IFASMFDP REGS
         ST    R15,16(0,R13)      SAVE RC
         LM    R14,R12,12(R13)    RELOAD REGS
         BR    R14                RETURN
         SPACE 1
*HKNAME  CLC   SMFDSN(0),PARMDATA CHECK DSNAME
CHKNAME  CLC   0(0,R6),PARMDATA   CHECK DSNAME
SETPARM  MVC   PARMDATA(0),0(R5)  SET UP ATTACH PROGRAM PARM
         SPACE 1
         SPACE 3
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*        WORK AREAS
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
DUMPTITL DC    C'WORKING STORAGE SECTION'
SAVE     DC    18F'-1'
DOUBLE   DC    D'0'
HOURS    DC    F'0'
MINS     DC    F'0'
SECS     DC    F'0'
CPKECB   DC    F'0'
ATTTCB   DC    F'0'
TIMEPTRN DC    X'402120204B20204B2020'
HHMMSS   DS    0CL8
HH       DS    CL2
HHDOT    DC    C'.'
MM       DS    CL2
MMDOT    DC    C'.'
SS       DS    CL2
         DS    CL3
ATTANAME DC    CL8' '
ATTAPARM DS    0H
PARMLTH  DC    H'0'
PARMDATA DC    CL100' '
SAVEDSN  DC    CL44' '
HEADING  DS    0CL133
HEADCTL  DC    C'0'               PRINT SPACE 2
*EAD1    DC    C'ACCESS TIME     DATE  SYSTEM JOBNAME  USERID   '
HEAD1    DC    C'ACCESS TIME     DATE  SYSTEM JOBNAME  '
         DC    C'DDNAME   DSNAME'
         DC    CL(132-(*-HEAD1))'  '
HEADING2 DS    0CL133
HEADCTL2 DC    C'+'               PRINT NO SPACE
HEAD2    DC    C'______ ____     ____  ______ _______  '
         DC    C'______   ______'
         DC    CL(132-(*-HEAD2))'  '
PRINTREC DS    0CL133
PRINTCTL DC    C' '
PACCESS  DC    CL5' '
         DC    CL2' '
PTIME    DC    CL8' '
         DC    CL1' '
PDATE    DC    CL5' '
         DC    CL1' '
PSYSTEM  DC    CL4' '
         DC    CL3' '
PJOBNAME DC    CL8' '
         DC    CL1' '
*USERID  DC    CL8' '
*        DC    CL1' '
PDDNAME  DC    CL8' '
         DC    CL1' '
PDSNAME  DC    CL44' '
         DC    CL1' '
         DC    CL(132-(*-PACCESS))' '
         SPACE 1
* ------ DCB'S                    ------------------------------------
         SPACE 3
REPORT   DCB   DSORG=PS,DDNAME=REPORT,LRECL=133,RECFM=FBA,          XXXX
               MACRF=(PM),BLKSIZE=133
         SPACE 1
         LTORG
         SPACE 1
RECDFORM DSECT                          RECORD
RLNGTH   DS    F                  0     LENGTH
RCONT    DS    C                  4     CONT
RECDID   DS    C                  5     ID
RSTPTIME DS    CL4                6     STEP TIME
RSTPDATE DS    CL4                10    STEP DATE
RCPUID   DS    CL4                14    CPU ID
RJOBNAME DS    CL8                18    JOB NAME
         DS    CL8                26    SPACING
SMF14UID DS    CL8                34    USER ID
         DS    CL2                42    SPACING
SMF17DSN EQU   *                  44    DSNAME FOR TYPE 17/18
         DS    CL12               44    SPACING
SMFDDN   DS    CL8                56    DDNAME
         DS    CL4                64    SPACING
SMFDSN   DS    CL44               68    DSNAME
SMF18DSN EQU   *+24               88    NEW DSNAME FOR TYPE 18
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   SMFEXIT
//*KED.SYSLMOD DD DSN=SYS9.IMSUTIL.LOAD,DISP=SHR
//LKED.SYSLMOD DD DSN=HALL.LINKLIB,DISP=SHR
//LKED.SYSIN DD *
  SETCODE AC(0)
  NAME SMFEXIT(R)
/*
//
