//ENQ      JOB
//* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
//* $  CBT.SOURCE(ENQ)
//* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
//S1        EXEC  ASMFCLG,CLASS='*'
//ASM.SYSIN DD  *
*
*              THIS VERSION IS FOR PRE-SP3 SYSTEMS
*
ENQ      CSECT
         TITLE 'COMMAND TO LIST ENQUEUED DATASET NAMES'
         USING *,R15
         B     AROUND             BRANCH AROUND DUMP TITLE
         DC    C'ENQ     '        PROGRAM NAME
         DC    C'&SYSDATE'        DATE WRITTEN
         DC    C'&SYSTIME'        TIME WRITTEN
         SPACE 1
AROUND   DS    0H
         DROP  R15
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING ENQ,R12
         ST    R13,SAVE+4
         LR    R11,R13
         LA    R13,SAVE
         ST    R13,8(,R11)
         L     R1,0(,R1)        GET ADDR OF PARM LIST
         LA    R10,2(,R1)       SAVE PARM STRING ADDR FOR LATER
         LH    R11,0(,R1)       GET LENGTH OF PARM STRING
         SPACE 1                                                    RH
DECODE   DS    0H                                                   RH
         LTR   R11,R11          CHECK FOR NO PARMS
         BZ    HELP             GIVE HELP MSG
         LA    R7,7               R7 = MAX QNAME LTH                RH
         CLC   0(2,R10),=C'Q='    IS IT Q= PARM?                    RH
         BNE   NOQ                NO, USE DEFAULT OF SYSDSN         RH
         MVC   QNAME,OLDNAME      MAKE SURE QNAME IS BLANK          RH
         LA    R10,2(0,R10)       R10 -> TO QNAME                   RH
         LR    R7,R10             R7 -> TO QNAME                    RH
         BCTR  R11,0              R11 = LTH OF REST OF PARM         RH
         BCTR  R11,0              ''                                RH
         SPACE 1                                                    RH
QLOOP    DS    0H                                                   RH
         CLI   0(R7),C','         FOUND COMMA ?                     RH
         BE    COMMAFND           YES, SET UP QNAME                 RH
         LA    R7,1(0,R7)         R7 -> TO NEXT BYTE                RH
         BCT   R11,QLOOP          LOOK AT IT                        RH
         TPUT  MISSPARM,LMISS     SAY MISSING PARM                  RH
         B     GETANS             GO GET NEXT REPLY                 RH
         SPACE 1                                                    RH
COMMAFND DS    0H                                                   RH
         BCTR  R11,0              R11 = LTH OF REST 0F PARM         RH
         SR    R7,R10             R7 = LTH OF QNAME                 RH
         BCTR  R7,0               R7 = HEX LTH                      RH
         EX    R7,MVCQ            MOVE QNAME                        RH
         LA    R10,2(R7,R10)      R10 -> TO NEXT PARM               RH
         SPACE 1                                                    RH
NOQ      DS    0H                                                   RH
         CLC   0(2,R10),=C'*J'  LIST DSNS BY JOBNAME?
         BNE   NOJOBN           NO, DON'T GET JOBNAME LENGTH
         LR    R9,R11           GET LENGTH OF PARM STRING
         S     R9,=F'3'         SUBTRACT 1 FOR EX, 2 FOR '*J'
NOJOBN   BCTR  R11,R0           DECR FOR EXECUTE LATER
         L     R2,16            GET CVT PTR
         L     R1,556(,R2)      GET ASVT ADDR
         LA    R6,524(,R1)      ADD OFFSET TO VECTOR SEGMENT
         L     R2,640(,R2)      ADDR OF FIRST MAJOR QCB
         SPACE 2
QTEST    DS    0H                                                   RH
         CLC   QNAME,=CL8'ALL'    DO ALL QNAMES?                    RH
         BE    FOUNDIT            YES, GO TO IT !                   RH
         SPACE 1                                                    RH
MAJORLP  DS    0H                                                   RH
         EX    R7,CLCQ          LOOK FOR THIS MAJOR QCB             RH
         BE    FOUNDIT          GO PROCESS THE SYSDSN QCB
         ICM   R2,7,1(R2)       GET PTR TO NEXT MAJOR QCB
         BNZ   MAJORLP          CONTINUE SCANNING
         SPACE 1                                                    RH
QNFOUND  NOP   RETURN             B IF QNAME FOUND                  RH
         MVC   QMSGNAM,QNAME      MOVE QNAME TO MSG                 RH
         TPUT  QMSG,LQMSG         PUT MSG TO TERMINAL               RH
         SPACE 2
RETURN   L     R13,SAVE+4       COMMON RETURN POINT
         L     R14,12(,R13)
         LM    R0,R12,20(R14)
         BR    R14
         SPACE 2
MVCQ     MVC   QNAME(0),0(R10)    MOVE QNAME TO SAVE AREA           RH
CLCQ     CLC   QNAME(0),16(R2)    COMPARE QNAMES                    RH
         SPACE 2
FOUNDIT  L     R3,8(,R2)        GET ADDR OF FIRST MINOR QCB
         MVI   QNFOUND+1,X'F0'    CHANGE NOP TO B                   RH
         MVC   QMSGNAM,16(R2)     MOVE QNAME TO MSG                 RH
         TPUT  QMSG,14            SEND MSG                          RH
         SPACE 1                                                    RH
MINORLP  CLI   0(R10),C'*'      DO ALL DSNAMES?
         BE    SKIPCHEK
         EX    R11,DSNCOMP      IS IT OUR DSN?
         BNE   NEXTMIN          CHECK NEXT MINOR QCB
SKIPCHEK DS    0H
         SLR   R8,R8
         IC    R8,17(,R3)         GET FLAG BYTE
         STC   R8,TYPE+15         SET UP BYTE 2                     RH
         SRL   R8,4               DROP LOW ORDER 4 BITS             RH
         STC   R8,TYPE+14         SET UP BYTE 1                     RH
*        MVI   TYPE+13,C' '       MOVE LTH TO MSG                   RH
         MVZ   TYPE+14(1),=X'F0'  FIX ZONE                          RH
         MVZ   TYPE+15(1),TYPE+14 FIX ZONE                          RH
*
         SLR   R8,R8
         IC    R8,16(,R3)       GET LENGTH OF DSNAME
         CVD   R8,DWORD           CONVERT TO DECIMAL                RH
         UNPK  TYPE+10(3),DWORD+6(2) MOVE LTH TO MSG                RH
         MVI   TYPE+10,C' '       MOVE LTH TO MSG                   RH
         MVZ   TYPE+12(1),TYPE+11 FIX ZONE                          RH
         CH    R8,=H'52'          IS LTH OKAY?                      RH
         BNH   *+4+4              YES                               RH
         LH    R8,=H'52'          NO, SET TO MAX                    RH
         BCTR  R8,R0            MINUS ONE FOR EXECUTE
         MVI   DSNBUFF,C' '     CLEAR FIELD TO BLANKS FIRST
         MVC   DSNBUFF+1(51),DSNBUFF
         EX    R8,DSNMOVE       MOVE DSN TO OUR BUFF
         L     R4,8(,R3)        GET ADDR OF FIRST QEL
QELLP    MVC   TYPE(10),=CL10'SHARE'
         TM    12(R4),X'80'     IS IT ENQUEUED SHARE?
         BO    *+10             YES
         MVC   TYPE(10),=CL10'EXCLUSIVE'
         LH    R5,14(,R4)       GET ASID ASSOCIATED WITH QEL
         SLL   R5,2             MULT BY 4 TO INDEX TABLE
         LA    R5,0(R5,R6)      GET ASSOCIATED ASCB ADDR.
         ICM   R5,7,1(R5)
         BZ    PUTMSG
         CLC   0(4,R5),=C'ASCB'
         BNE   PUTMSG
         ICM   R1,7,173(R5)     GET JOBNAME PTR
         BZ    TRYSTC
         MVC   JOBNAME(8),0(R1) MOVE IN JOBNAME
         B     PUTMSG
TRYSTC   ICM   R1,7,177(R5)     GET STC NAME PTR
         BZ    PUTMSG
         MVC   JOBNAME(8),0(R1) MOVE IN STC NAME
PUTMSG   CLI   1(R10),C'E'      LIST ONLY EXCLUSIVE REQUESTS?
         BNE   DOALL
         CLC   TYPE(10),=CL10'EXCLUSIVE'
         BNE   NEXTQEL
DOALL    CLC   0(2,R10),=C'*J'  LIST A PARTICULAR JOBNAME?
         BNE   SKIPJOB          NO, LIST ALL JOBS
         EX    R9,JOBCOMP       DO JOBNAMES MATCH?
         BNE   NEXTQEL
SKIPJOB  CLC   DSNBUFF(52),OLDNAME
         BNE   TPUT
         MVI   DSNBUFF,C' '
         MVC   DSNBUFF+1(51),DSNBUFF
         MVI   DSNBUFF+4,C'"'
TPUT     TPUT  DSNBUFF,87
         CLI   DSNBUFF,C' '
         BE    NEXTQEL
         MVC   OLDNAME(52),DSNBUFF SAVE DSNAME
NEXTQEL  ICM   R4,7,1(R4)       GET ADDR OF NEXT QEL
         BNZ   QELLP
         SPACE 2
NEXTMIN  ICM   R3,7,1(R3)       GET ADDR OF NEXT MINOR QCB
         BNZ   MINORLP
         ICM   R2,7,1(R2)         R2 -> TO NEXT MAJOR QCB           RH
         BNZ   QTEST              GO SEE IF WE DO THIS ONE TOO      RH
         B     RETURN
HELP     TPUT  HELP1,L'HELP1
         TPUT  HELP2,L'HELP2
         TPUT  HELP3,L'HELP3
         TPUT  HELP4,L'HELP4
         TPUT  HELP5,L'HELP5
         TPUT  HELP6,L'HELP6
         TPUT  HELP7,L'HELP7
         TPUT  HELP8,L'HELP8      PUT ENTER MSG                     RH
         SPACE 1                                                    RH
GETANS   DS    0H                                                   RH
         TGET  REPLY,80,EDIT,WAIT GET INPUT                         RH
         LTR   R15,R15            WAS TGET OKAY ?                   RH
         BNZ   RETURN             NO, RETURN                        RH
         LR    R11,R1             PUT LTH IN R11                    RH
         LA    R10,REPLY          R10 -> TO REPLY                   RH
         OC    REPLY,OLDNAME      MAKE SURE ANSWER IS UPPER CASE    RH
         B     DECODE             GO DECODE ANSWER                  RH
         EJECT
DSNMOVE  MVC   DSNBUFF(0),20(R3) COPY DSN FROM MINOR QCB
DSNCOMP  CLC   0(0,R10),20(R3)  DO THE DSNS MATCH?
JOBCOMP  CLC   2(0,R10),JOBNAME DO JOBNAMES MATCH?
         SPACE 2
SAVE     DC    18F'-1'
DWORD    DC    D'0'
OLDNAME  DC    CL52' '
DSNBUFF  DC    CL52' ',CL1' '
JOBNAME  DC    CL8' ',CL2' '
TYPE     DC    CL10' ',CL60' '
HELP1    DC    C'PARM FIELD OPTIONS:'
HELP2    DC    C'Q=ENQ NAME(ABBREV) AS 1ST PARM. DEFAULT = SYSDSN'  RH
HELP3    DC    C'     *       ..... LIST ALL DSNAMES IN USE'
HELP4    DC    C'     *E      ..... LIST ALL EXCLUSIVE REQUESTS'
HELP5    DC    C'     XXXX    ..... LIST ALL DSNS STARTING WITH XXXX'
HELP6    DC    C'                   (CAN BE ANY LENGTH UP TO 44)'
HELP7    DC    C'     *JXXXX  ..... LIST ALL DSNS FOR JOBNAME XXXX'
HELP8    DC    C'ENTER PARM -EG- Q=SPF,*E'                          RH
QNAME    DC    CL8'SYSDSN'                                          RH
QMSG     DC    C'QNAME='                                            RH
QMSGNAM  DC    CL8' '                                               RH
         DC    C' NOT FOUND'                                        RH
LQMSG    EQU   *-QMSG                                               RH
REPLY    DC    CL80' '            TGET BUFFER                       RH
MISSPARM DC    C'MISSING PARAMETER, RE-ENTER'                       RH
LMISS    EQU   *-MISSPARM                                           RH
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   ENQ
//LKED.SYSLMOD DD DSN=HALL.LINKLIB(ENQ),DISP=OLD
//
