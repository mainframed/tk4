******************************************************************* RH
*                                                                   RH
*              MACRO TO DUMP CORE                                   RH
*              REMEMBER TO STEPLIB TO HALL.LINKLIB FOR HALLDUMP     RH
*              IF YOU USE AN EXISTING DCB, IT SHOULD BE             RH
*              RECFM=FBA,LRECL=133.                                 RH
*                                                                   RH
         MACRO                                                      RH
&L       HDUMP &DCB,&START,&STOP,&DISPO,&DISPC,&V=NO,&TITLE=,       RH +
               &GENDCB=YES                                          RH
         GBLA  &N                                                   RH
         GBLC  &L0,&L1,&L2,&L3,&L4,&L5,&L6                          RH
.*&N     SETA  1                                                    RH
.*                                                                  RH
.******* ***** ****************** ********************************* RH
.*                                                                  RH
.*       HDUMP IS USED TO DYNAMICALLY DUMP CORE IN A PROGRAM        RH
.*             &DCB   IS THE NAME FOR THE SYSOUT DCB                RH
.*             &START IS A REGISTER CONTAINING THE ADDRESS TO DUMP, RH
.*                    OR 0 TO DUMP JUST THE REGISTERS               RH
.*             &STOP  IS THE LENGTH TO DUMP, MAX IS 4095,           RH
.*                    OR 0 TO DUMP JUST THE REGISTERS               RH
.*             &DISPO IS THE DISPOSITION OF THE DUMP DCB            RH
.*             &DISPC IS THE DISPOSITION AT THE END OF THE MACRO    RH
.*             &V=YES WILL GENERATE A CONTROL BLOCK SET(EXCEPT DCB) RH
.*                    EVERY TIME THE MACRO IS CALLED                RH
.*             &TITLE IS A TITLE TO BE PRINTED BEFORE THE DUMP      RH
.*         &GENDCB=NO WILL USE THE EXISTING &DCB                    RH
.*                                                                  RH
.*       AN EXAMPLE USAGE WOULD BE:                                 RH
.*                                                                  RH
.*       1)    DUMP 1024 BYTES STARTING AT MESSAGES. (A DCB)        RH
.*             THE 'OUTDCB' FILE IS NOT OPEN, AND SHOULD BE LEFT    RH
.*             OPEN AT END OF THE MACRO.                            RH
.*                                                                  RH
.*       LA    R3,MESSAGES        R3 = ADDRESS TO DUMP       DEBUG  RH
.*       HDUMP OUTDCB,R3,1024,NOTOPEN,OPEN                   DEBUG  RH
.*       ...                                                        RH
.*       ...   MORE OF YOUR CODE                                    RH
.*       ...                                                        RH
.*                                                                  RH
.*       2)    DUMP 1024 BYTES STARTING AT THE ADDRESS STORED       RH
.*             AT MESSAGES+44. (A DEB)                              RH
.*             THE 'OUTDCB' FILE IS OPEN, AND SHOULD BE CLOSED      RH
.*             AT END OF THE MACRO.                                 RH
.*                                                                  RH
.*       L     R3,MESSAGES+44     R3 -> TO DEB               DEBUG  RH
.*       HDUMP OUTDCB,R3,1024,OPEN,CLOSE                     DEBUG  RH
.*                                                                  RH
.******* ***** ****************** ********************************* RH
.*                                                                  RH
         AIF   (&STOP LT 4096).STOPOK                               RH
         MNOTE 'STOP MUST BE LESS THAN 4096'                        RH
         MEXIT                                                      RH
.STOPOK  ANOP                                                       RH
.*       AIF   (&N EQ 1).FRST                                       RH
         AIF   (&N EQ 0).FRST                                       RH
         AIF   ('&V' EQ 'NO').NDC1                                  RH
.FRST    ANOP                                                       RH
&L0      SETC  'SREG&SYSNDX'                                        RH
&L1      SETC  'HREG&SYSNDX'                                        RH
&L2      SETC  'REGA&SYSNDX'                                        RH
&L3      SETC  'DCBA&SYSNDX'                                        RH
&L4      SETC  'STRT&SYSNDX'                                        RH
&L5      SETC  'STPA&SYSNDX'                                        RH
&L6      SETC  'TITL&SYSNDX'                                        RH
         AIF   (&N GT 1).LBL2                                       RH
*&L      B     BPAS&SYSNDX                                   DEBUG  RH
&L       B     LOADHDMP                                      DEBUG  RH
         AGO   .LBL9                                         DEBUG  RH
.LBL2    ANOP                                                DEBUG  RH
&L       B     HDMP&SYSNDX                                   DEBUG  RH
.LBL9    ANOP                                                DEBUG  RH
         SPACE 1                                             DEBUG  RH
&L0      DC    18F'-1'            SAVE AREA FOR HALLDUMP RTN DEBUG  RH
&L1      DC    16F'-1'            SAVE AREA FOR CALLERS REGS DEBUG  RH
&L2      DC    A(&L1)             ADDRESS OF CALLERS REGS    DEBUG  RH
&L3      DC    A(&DCB)            ADDRESS OF OUTPUT DCB      DEBUG  RH
&L4      DC    A(0)               ADDRESS OF START OF DUMP   DEBUG  RH
&L5      DC    A(0)               ADDRESS OF END OF DUMP     DEBUG  RH
&L6      DC    A(0)               ADDRESS OF TITLE           DEBUG  RH
         AIF   (&N GT 1).NDC1                                DEBUG  RH
         ENTRY DMPRADDR                                      DEBUG  RH
DMPRADDR DC    F'0'               DUMP FORMATTER ADDRESS     DEBUG  RH
         AIF   ('&GENDCB' EQ 'NO').NODCB                            RH
         ENTRY &DCB                                          DEBUG  RH
*&DCB    DCB   DDNAME=SYSOUT,RECFM=FBA,LRECL=133,BLKSIZE=3990,      RHXX
               DSORG=PS,MACRF=(PM)                           DEBUG  RH
&DCB     DCB   DDNAME=&DCB,RECFM=FBA,LRECL=133,BLKSIZE=3990,        RHXX
               DSORG=PS,MACRF=(PM)                           DEBUG  RH
.NODCB   ANOP                                                DEBUG  RH
         SPACE 1                                             DEBUG  RH
*PAS&SYSNDX DS    0H                                         DEBUG  RH
LOADHDMP DS       0H                                         DEBUG  RH
         STM   0,15,&L1           SAVE CALLERS REGS          DEBUG  RH
         L     1,=V(DMPRADDR)     R1 -> TO DUMPER CODE       DEBUG  RH
         ICM   1,15,0(1)          IS DUMP FORMATTER LOADED?  DEBUG  RH
         BNZ   LOADHEND           YES                        DEBUG  RH
         BLDL  0,BLDLLIST         FIND HALLDUMP              DEBUG  RH
         BLDL  0,BLDLLIST         FIND HALLDUMP              DEBUG  RH
         LTR   R15,R15            DID WE FIND IT?            DEBUG  RH
         BZ    BLDLOK             YES, GO LOAD IT            DEBUG  RH
         DC    H'0'               NO, ABEND                  DEBUG  RH
*        BNZ   LOADHEND           NO, DON'T TRY TO LOAD IT   DEBUG  RH
BLDLOK   DS    0H                 ALIGN ON HALFWORD...       DEBUG  RH
*        LOAD  EP=HALLDUMP        LOAD DUMP FORMATTER        DEBUG  RH
         LA    0,DUMPPGM          R0 -> TO DUMP PGM NAME     DEBUG  RH
         LOAD  EPLOC=(0)          LOAD DUMP FORMATTER        DEBUG  RH
         LTR   R15,R15            DID WE LOAD IT?            DEBUG  RH
         BZ    LOADOK             YES, GO SAVE IT            DEBUG  RH
         DC    H'0'               NO, ABEND                  DEBUG  RH
LOADOK   DS    0H                 ALIGN ON HALFWORD...       DEBUG  RH
         L     R15,=V(DMPRADDR)   R15 -> TO DUMPER CODE      DEBUG  RH
         ST    0,0(0,R15)         SAVE IT                    DEBUG  RH
         B     LOADHEND           ON WE GO                   DEBUG  RH
*                                                            DEBUG  RH
         ENTRY BLDLLIST,DUMPPGM                              DEBUG  RH
BLDLLIST DS    0H                 ALIGN ON HALFWORD...       DEBUG  RH
         DC    H'1'               ONE NAME TO FIND           DEBUG  RH
         DC    H'12'              LTH OF ENTRY               DEBUG  RH
DUMPPGM  DC    CL8'HALLDUMP'      NAME OF DUMP PGM           DEBUG  RH
         DC    AL4(0)             TTR, AND K FIELD           DEBUG  RH
*                                                            DEBUG  RH
         SPACE 1                                             DEBUG  RH
LOADHEND DS    0H                                            DEBUG  RH
         LM    0,15,&L1           RELOAD CALLERS REGS        DEBUG  RH
.NDC1    ANOP                                                DEBUG  RH
         SPACE 1                                             DEBUG  RH
*PAS&SYSNDX DS    0H                                         DEBUG  RH
HDMP&SYSNDX DS    0H                                         DEBUG  RH
         STM   0,15,&L1           SAVE CALLERS REGS          DEBUG  RH
*        LR    15,&START          R15 -> TO START ADDRESS    DEBUG  RH
         AIF   ('&START'(1,1) LT '0').STARTR1                       RH
         LA    15,0               R15 = 0 IF DUMP ONLY REGS  DEBUG  RH
         AGO   .STARTR9
.STARTR1 ANOP                                                DEBUG  RH
         LR    15,&START          R15 -> TO START ADDRESS    DEBUG  RH
.STARTR9 ANOP                                                DEBUG  RH
         ST    15,&L4             SAVE IT                    DEBUG  RH
         LA    15,&STOP.(0,15)    R15 -> TO STOP ADDRESS     DEBUG  RH
         ST    15,&L5             SAVE IT                    DEBUG  RH
         AIF   ('&TITLE' EQ '').NTTL                         DEBUG  RH
         LA    15,=CL80&TITLE     R15 -> TO TITLE            DEBUG  RH
         ST    15,&L6             SAVE IT                    DEBUG  RH
.NTTL    ANOP                                                DEBUG  RH
         LA    13,&L0             GIVE HALLDUMP ITS OWN SA   DEBUG  RH
         AIF   (&N GT 1).NDC2                                DEBUG  RH
&N       SETA  2                                             DEBUG  RH
         L     2,=V(&DCB)         R2 -> TO OUTPUT DCB        DEBUG  RH
         ST    2,&L3              SAVE IT                    DEBUG  RH
         AGO   .NDC3                                                RH
.NDC2    ANOP                                                DEBUG  RH
         SPACE 1                                             DEBUG  RH
         L     2,=V(&DCB)         R2 -> TO OUTPUT DCB        DEBUG  RH
         ST    2,&L3              SAVE IT                    DEBUG  RH
         AIF   ('&DISPO' EQ '').NDC3                                RH
         AIF   ('&DISPO' EQ 'OPEN').NDC4                            RH
.NDC3    ANOP                                                       RH
         TM    48(2),X'10'        IS DEBUG FILE OPEN ?       DEBUG  RH
         BO    OPEN&SYSNDX        YES, DON'T OPEN IT AGAIN   DEBUG  RH
*        OPEN  ((2),(OUTPUT)),MODE=31                        DEBUG  RH
         OPEN  ((2),(OUTPUT)),MODE=31                        DEBUG  RH
         SPACE 1                                             DEBUG  RH
OPEN&SYSNDX DS 0H                                            DEBUG  RH
         L     R15,=V(DMPRADDR)   R15 -> TO DUMPER CODE      DEBUG  RH
         ICM   1,15,0(R15)        IS DUMP FORMATTER LOADED?  DEBUG  RH
         BNZ   LOAD&SYSNDX        YES                        DEBUG  RH
*        AGO   .NOLOAD
*        BLDL  0,BLDLLIST         FIND HALLDUMP              DEBUG  RH
         L     0,=V(BLDLLIST)     R0 -> AT BLDL LIST         DEBUG  RH
         BLDL  0,(0)              FIND HALLDUMP              DEBUG  RH
         LTR   R15,R15            DID WE FIND IT?            DEBUG  RH
         BNZ   LOAD&SYSNDX .      NO, DON'T TRY TO LOAD IT   DEBUG  RH
*        LOAD  EP=HALLDUMP        LOAD DUMP FORMATTER        DEBUG  RH
*        LA    0,DUMPPGM          R0 -> TO DUMP PGM NAME     DEBUG  RH
         L     0,=V(DUMPPGM)      R0 -> TO DUMP PGM NAME     DEBUG  RH
         LOAD  EPLOC=(0)          LOAD DUMP FORMATTER        DEBUG  RH
         L     R15,=V(DMPRADDR)   R15 -> TO DUMPER CODE      DEBUG  RH
         ST    0,0(0,R15)         SAVE IT                    DEBUG  RH
         B     LOAD&SYSNDX .      ON WE GO                   DEBUG  RH
*LDLLIST DS    0H                 ALIGN ON HALFWORD...       DEBUG  RH
*        DC    H'1'               ONE NAME TO FIND           DEBUG  RH
*        DC    H'12'              LTH OF ENTRY               DEBUG  RH
*UMPPGM  DC    CL8'HALLDUMP'      NAME OF DUMP PGM           DEBUG  RH
*        DC    AL4(0)             TTR, AND K FIELD           DEBUG  RH
*                                                            DEBUG  RH
.NOLOAD  ANOP                                                DEBUG  RH
         SPACE 1                                             DEBUG  RH
LOAD&SYSNDX DS 0H                                            DEBUG  RH
.NDC4    ANOP                                                       RH
         TM    48(2),X'10'        IS DEBUG FILE OPEN ?       DEBUG  RH
         BZ    DBUG&SYSNDX        NO                         DEBUG  RH
         LA    1,&L2              R1 -> TO PARM LIST         DEBUG  RH
*        ICM   15,15,=V(HALLDUMP) R15 -> TO DUMP ROUTINE     DEBUG  RH
         L     R15,=V(DMPRADDR)   R15 -> TO DUMPER CODE      DEBUG  RH
         ICM   15,15,0(R15)       R15 -> TO DUMP ROUTINE     DEBUG  RH
         BZ    DBUG&SYSNDX        NO, DUMP ROUTINE, EXIT     DEBUG  RH
         BALR  14,15              GO TO IT                   DEBUG  RH
         AIF   ('&DISPC' EQ '').NDC5                                RH
         AIF   ('&DISPC' EQ 'OPEN').NDC6                            RH
.NDC5    ANOP                                                       RH
*        CLOSE ((2)),MODE=31      CLOSE THE O/P FILE         DEBUG  RH
         CLOSE ((2)),MODE=31      CLOSE THE O/P FILE         DEBUG  RH
.NDC6    ANOP                                                       RH
         SPACE 1                                             DEBUG  RH
DBUG&SYSNDX DS    0H                                         DEBUG  RH
         LM    0,15,&L1           RELOAD CALLERS REGS        DEBUG  RH
         MEND                                                       RH
*                                                                   RH
*              END OF MACRO                                  DEBUG  RH
*                                                                   RH
