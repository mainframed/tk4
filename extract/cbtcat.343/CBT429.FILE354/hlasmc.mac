//HLASMC  PROC CLASS=$,
//             MAC='SYS1.MACLIB',
//             MAC1='SYS1.MACLIB',
//             PARMC=,
//             UNIT=VIO
//*
//***   HLASMC
//*
//* THIS PROCEDURE RUNS THE HIGH LEVEL ASSEMBLER AND CAN BE USED
//* TO ASSEMBLE PROGRAMS.
//*
//ASM     EXEC PGM=ASMA90,
//             PARM='&PARMC',
//             REGION=1024K
//SYSLIB   DD  DISP=SHR,
//             DSN=&MAC
//         DD  DISP=SHR,
//             DSN=&MAC1
//SYSLIN   DD  DCB=(BLKSIZE=3040,BUFNO=1,LRECL=80,RECFM=FBS),
//             DISP=(MOD,PASS),
//             DSN=&&LOADSET,
//             SPACE=(CYL,(5,5)),
//             UNIT=&UNIT
//SYSPRINT DD  DCB=BLKSIZE=3509,
//             SYSOUT=&CLASS
//SYSPUNCH DD  SYSOUT=B
//SYSUT1   DD  DISP=(NEW,DELETE),
//             DSN=&&SYSUT1,
//             SPACE=(1700,(600,100)),
//             UNIT=&UNIT
