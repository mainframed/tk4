//FILEDUMP JOB
/*ROUTE  PRINT R7
//DUMP EXEC HLASMCL,PARM.LKED='OVLY,LIST,LET,MAP'
//ASM.SYSLIB DD DSN=SYS1.MACLIB,
//   DISP=SHR
//   DD DSN=SYS9.AMODGEN,DISP=SHR
//ASM.SYSIN DD *
         EJECT                                                      RH
***********************************************************************
*                                                                     *
*                         A T T E N T I O N                           *
*                                                                     *
***********************************************************************
*                                                                     *
*         THIS PROGRAM MATERIAL IS THE PROPRIETORY PROPERTY OF        *
*                                                                     *
*               RANDY HALL,                                           *
*               DIVERSIFIED DEVELOPMENTS,                             *
*               1151 PALMER ROAD,                                     *
*               VICTORIA, B.C., V8P-2H5                               *
*                                                                     *
*         AND MAY NOT BE USED OR REPRODUCED IN ANY MEDIUM             *
*         WITHOUT PRIOR WRITTEN CONSENT                               *
*                                                                     *
***********************************************************************
         EJECT                                                      RH
*     THE CHANGES CODED 'MVS' IN COLUMNS 69-71 WERE MADE TO CORRECT
*     SEVERAL PROBLEMS THAT OCCURRED IN THE TAPE HANDLING SECTION
*     OF DCBSETUP CSECT. CHANGES WERE ALSO MADE TO INCLUDE 6250BPI
*     TAPES AND 6250/1600 DUAL DENSITY TAPE DRIVES.
*
*     THE CHANGES CODED 'RH2' IN COLUMNS 69-71 IN THE PARMCHEK
*     ROUTINE WERE MADE TO IMPROVE THE FORMAT OF ONE OF THE ERROR
*     MESSAGES.
*
*     THE ABOVE CHANGES WERE MADE IN DECEMBER, 1977, BY RANDY HALL,
*     DIVERSIFIED DEVELOPMENTS, 1151 PALMER ROAD,
*     VICTORIA, BC. 386-1061
*
*     THE CHANGES CODED 'RH3' IN COLUMNS 69-71 IN THE MAIN ROUTINE
*     WERE MADE BECAUSE THE DCBLRECL FIELD IN THE DCB FOR ISAM FILES
*     IS SET TO THE MAXIMUN LRECL AND IS NOT CHANGED FOR VARIABLE
*     LENGTH RECORDS.
*     CHANGED JUNE, 1978.   RANDY HALL
*
*     JUNE, 1982.
*     CHANGES WERE MADE TO ALLOW THE SYSUT1 JFCB TO BE
*     ANYWHERE, PLUS OTHER CLEANUP  CHANGES
*     RANDY HALL, DIVERSIFIED DEVELOPMENTS.
*
*     NOV, 1987.
*     CHANGES WERE MADE TO ALLOW FILEDUMP TO HANDLE VSAM FILES.
*     RANDY HALL, DIVERSIFIED DEVELOPMENTS.
*
*     JAN, 1990.
*     CHANGES WERE MADE TO ALLOW FILEDUMP TO HANDLE LINEAR VSAM
*     FILES VIA CONTROL INTERVAL PROCESSING.
*     RANDY HALL, DIVERSIFIED DEVELOPMENTS.
*
*
         EJECT
*                        FILEDUMP
*
*
*     FILEDUMP WILL PROCESS RECORDS CREATED BY ANY STANDARD IBM ACCESS
*     METHOD. RECORDS ARE PRINTED 32 CHARACTERS PER LINE IN AN OS DUMP
*     FORMAT. OVERFLOW ISAM RECORDS ARE FLAGGED WITH AN ASTERISK TO THE
*     RIGHT OF THE RECORD LENGTH. READ ERRORS WILL BE ACCOMPANIED BY
*     MESSAGES, AND AN ATTEMPT WILL BE MADE TO PRINT THE BAD DATA.
*
*                        JCL STATEMENTS
*
*     JOB STATEMENT    THIS STATEMENT INITIATES THE JOB.
*
*     EXEC STATEMENT   THIS STATEMENT SPECIFIES THE PROGRAM NAME
*                      (PGM=FILEDUMP), THE REGION SIZE (REGION=20K
*                      FOR SEQUENTIAL DATASETS WITH BLOCKSIZE OF
*                      4240 OR LESS). THE REGION SHOULD BE INCREASED
*                      FOR ISAM AND LARGER BLOCKSIZES. AN OPTIONAL
*                      FIELD (PARM=), MAY BE CODED IF OTHER THAN
*                      DEFAULTS ARE REQUIRED.
*
*     STEPLIB          THIS STATEMENT MAY BE NEEDED IF FILEDUMP
*        DD STATEMENT  IS NOT IN A SYSTEM LINKLIST LIBRARY.
*
*     SYSPRINT         OUTPUT FILE, NORMALLY SYSOUT=A
*       DD STATEMENT
*
*     SYSUT1           THE INPUT DATA SET. THE FOLLOWING ARE
*       DD STATEMENT   VALID.
*
*                      DD * - FOR CARD FILES.
*
*                      DD DISP=SHR,DSN=DATASET NAME - FOR
*                      CATALOGUED DATASETS.
*
*                      DD DISP=SHR,DSN=DATASET NAME(MEMBER NAME) -
*                      FOR A MEMBER OF A PARTITIONED DATA SET. IF A
*                      MEMBER NAME IS NOT CODED FOR A PARTITIONED
*                      DATASET, THE DIRECTORY WILL BE PRINTED.
*
*                      DD DISP=SHR,DSN=,UNIT=,VOL=SER=  - FOR
*                      UNCATALGUED, STANDARD LABELLED, DATASETS.
*
*                      DD DISP=OLD,UNIT=TAPE,VOL=SER=,
*                      LABEL=(2,BLP),DCB=(RECFM=U,BLKSIZE=32196)
*                      - TO DUMP THE CONTENTS OF AN UNKNOWN TAPE.
*                      IF LABEL=(1,BLP) IS CODED, THE HEADER
*                      LABELS WILL BE PRINTED.
*
*     SYSUT2           THE OUTPUT FILE OF A COPY OPERATION.
*       DD STATEMENT   USED ONLY IF PARM=C OR B IS SPECIFIED.
*                      IF DCB INFORMATION IS NOT SPECIFIED,
*                      DCB INFORMATION WILL BE COPIED FROM
*                      SYSUT1.
*
*                        CONTROL PARAMETERS
*
*     THE DEFAULTS ARE:
*
*     1) TITLE IS DATASET NAME AND VOLUME SERIAL NUMBERS.
*     2) THE FIRST 100 RECORDS WILL BE PRINTED.
*
*     TO OVERRIDE THE DEFAULTS CODE PARM= ON THE EXEC CARD.
*     EXAMPLE 2 SHOWS HOW TO CONTINUE A PARM FIELD ON A
*     SECOND AND THIRD CARD.
*
*     C  OR  COPY
*
*     IF THE PARAMETER IS C OR COPY, ALL RECORDS WANTED WILL BE
*     COPIED FROM SYSUT1 TO SYSUT2.
*
*     B  OR  BOTH
*
*     IF THE PARAMETER IS B OR BOTH, ALL RECORDS WANTED WILL BE
*     PRINTED ON SYSPRINT AND WILL ALSO BE COPIED TO SYSUT2.
*
*     HEX              THE DEFAULT IS DECIMAL
*
*     IF HEX IS CODED, THE OFFSETS AT THE BEGINNING OF EACH
*     PRINTED LINE WILL BE PRINTED IN HEXADECIMAL RATHER
*     THAN DECIMAL.
*
*     START=N          THE DEFAULT IS 1
*
*     WHERE N IS A 1 TO 6 DIGIT VALUE REPRESENTING THE RECORD NUMBER
*     TO START PRINTING AT.
*
*     SKIP=N           THE DEFAULT IS 0
*
*     WHERE N IS A 1 TO 6 DIGIT VALUE REPRESENTING THE NUMBER OF
*     RECORDS SKIPPED BETWEEN PRINTED RECORDS.
*     THE DEFAULT OF 0 WILL PRINT EVERY RECORD.
*
*     STOP=N           THE DEFAULT IS 100
*
*     WHERE N IS A 1 TO 6 DIGIT VALUE REPRESENTING THE RECORD
*     NUMBER TO STOP PRINTING AT.
*     TO STOP PRINTING AT END OF FILE, CODE STOP=EOF.
*
*     TITLE=A          THE DEFAULT IS DATASET NAME AND VOLUME SERIAL
*                      NUMBER(S).
*                      TITLE MUST BE THE LAST PARAMETER CODED.
*
*     WHERE A IS A CHARACTER STRING THAT WILL BE USED IN PLACE OF
*
*     THE DEFAULT TITLE.
*
*                        EXAMPLE 1
*
*     //STEP1 EXEC PGM=FILEDUMP,REGION=20K
*     //STEPLIB  DD DSN=SYS9.UTIL.LOAD,DISP=SHR
*     //SYSPRINT DD SYSOUT=A
*     //SYSUT1   DD DISP=SHR,DSN=DNN.SOURCE(MYDATA)
*
*     THE FIRST 100 RECORDS OF THE MEMBER MYDATA ON THE DATASET
*     DNN.SOURCE WILL BE PRINTED AFTER A COMPLETE DESCRIPTION
*     OF THE DATASET.
*
*                        EXAMPLE 2
*
*     //STEP1  EXEC PGM=FILEDUMP,REGION=20K,PARM=('STOP=10',
*     //   'START=100,SKIP=9,STOP=900,START=1000,SKIP=1',
*     //   'TITLE=I MAKE WORK FOR MYSELF')
*     //STEPLIB  DD DSN=SYS9.UTIL.LOAD,DISP=SHR
*     //SYSPRINT DD SYSOUT=A
*     //SYSUT1   DD DSN=VOLUME.TEST,UNIT=2400-3,VOL=SER=012345,
*     //            DISP=(OLD,KEEP)
*
*     THE INFORMATION ON THE TAPE LABELS, INCLUDING CREATION DATE
*     WILL BE PRINTED, FOLLOWED BY:
*
*     1) THE FIRST 10 RECORDS,
*     2) EVERY 10TH RECORD FROM 100 TO 900,
*     3) RECORD 1000 TO END OF FILE.
*
*     NOTE THAT STOP=EOF IS THE DEFAULT AFTER THE FIRST STOP=
*     IS CODED.
*
*                        EXAMPLE 3
*
*     //STEP1  EXEC PGM=FILEDUMP,REGION=20K,PARM='C,STOP=EOF'
*     //STEPLIB  DD DSN=SYS9.UTIL.LOAD,DISP=SHR
*     //SYSPRINT DD SYSOUT=A
*     //SYSUT1   DD DSN=DNN.MYFILE,DISP=SHR
*     //SYSUT2   DD DSN=COPY.OF.MYFILE,UNIT=2400-3,DISP=(NEW,KEEP),
*     //            DCB=(RECFM=FB,LRECL=(80,BLKSIZE=4000)
*
*     ALL RECORDS ON THE INPUT FILE WILL BE COPIED TO THE OUTPUT FILE.
*
*
*
*     RETURN CODES
*
*         16 (HEX 10) - PARM FIELD ON EXEC CARD HAS INVALID PARAMETERS.
*                       CHECK ERROR MESSAGES.
*
*         88 (HEX 58) - ERROR OCCURRED WHILE PROCESSING TAPE LABEL
*                       IF SYSUT1 IS TAPE INPUT. RERUN THE JOB.
*
*       1111 (HEX 457)- UNSUCCESSFUL OPEN FOR SYSPRINT OR SYSUT1.
*
*       2222 (HEX 8AE)- UNSUCCESSFUL OPEN FOR SYSUT2.
*
*
*     THE FOLLOWING MESSAGES HAVE THE FOLLOWING MEANINGS:
*
*        SYSUT1 FAILED TO OPEN..SELF EXPLANATORY.
*
*        ERROR DOING EXCP TO SYSUT1..
*                       ERROR OCCURRED WHILE PROCESSING TAPE LABEL
*                       IF SYSUT1 IS TAPE INPUT. RERUN THE JOB.
*
*
*
*
*     ADDITIONAL COPIES OF THIS DOCUMENTATION MAY BE OBTAINED
*     BY CONTACTING RANDY HALL AT 389-3748
*
*
         TITLE 'MAINLINE OF FILE-DUMP PROGRAM'
*
*        THE FUNCTION OF THIS PROGRAM IS TO DUMP ANY IBM SUPPORTED
*   FILE STRUCTURE ACCORDING TO PARM INPUT OR DEFAULT VALUES.
*        THE PARMS ARE:
*   STOP=XXXXXX OR EOF            XXXXXX IS RECORD NUMBER,
*                                 EOF INDICATES  END OF FILE
*   START=XXXXXX                  XXXXXX IS RECORD NUMBER
*   SKIP=XXXXXX                   XXXXXX IS THE NUMBER OF RECORDS
*                                 BETWEEN PRINTED RECORDS
*   TITLE=                        ALL CHARACTERS BETWEEN THE = SIGN
*                                 AND THE END OF THE PARM ARE
*                                 CONSIDERED PART OF THE TITLE
*   THE ONLY RESTRICTION ON THE NUMBER OF START, STOP, SKIP PARAMETERS
*   IS THE LENGTH OF THE PARM.. THE DEFAULT START IS 1, THE DEFAULT
*   SKIP IS 0, AND THE DEFAULT STOP IS 100 FOR THE FIRST STOP AND
*   END OF FILE AFTER THAT.  IF MORE THAN ONE START/STOP ARE CODED,
*   THE DEFAULT SKIP IS A REPETITION OF THE LAST CODED SKIP.  THE
*   DEFAULT TITLE AFTER THE FIRST PAGE IS THE DATA SET NAME AND THE
*   VOLUME SERIAL NUMBERS OF THE FIRST FIVE VOLUMES.
*
*
*        THE PROGRAM IS DIVIDED INTO A MAINLINE AND 7 OVERLAYS AS
*   FOLLOWS:
*
*        FILEDUMP - THIS IS THE MAINLINE ROUTINE. IT READS ALL INPUT,
*   AND WRITES MOST OF THE OUTPUT. IT WILL CALL EACH OVERLAY REQUIRED
*
*        DCBSETUP - THIS OVERLAY DETERMINES THE DEVICE TYPE, ACCESSES
*   THE CREATION AND EXPIRY DATES FOR DISK FILES, CALCULATES THE
*   EXPIRY DATE FOR RNTPK DATA SETS, AND OPENS QSAM FILES.  CODES ARE
*   PASSED BACK TO THE MAINLINE IF ISAM OR BPAM ACCESSES ARE REQUIRED.
*   THE PROGRAM WILL ABEND IF THE FILES ARE NOT SUCCESSFULLY OPENED OR
*   THE CHANNEL PROGRAM THAT READS THE CREATION DATE AND EXPIRY DATE
*   FROM THE TAPE LABELS FAILS TO RESET THE TAPE TO THE FIRST DATA
*   RECORD.WHEN A DUAL DENSITY TAPE DRIVE IS ENCOUNTERED, THE UNIT
*   CONTROL BLOCK IS EXAMINED TO DETERMINE THE RECORDING DENSITY.
*
*        ERRANAL - THIS OVERLAY IS CALLED BY THE SYNAD ROUTINE AND
*   PRINTS AN EXPLANITORY MESSAGE WHENEVER AN I/O ERROR IS ENCOUNTERED.
*
*        PARMCHEK - THIS OVERLAY IS CALLED WHENEVER A PARM IS CODED.
*   IT EDITS THE PARM AND MODIFIES THE START, STOP, SKIP TABLE.  EDIT
*   MESSAGES ARE PASSED TO THE MAINLINE WHERE THEY ARE PRINTED
*   BEFORE THE PROGRAM A STOPS.
*
*        REPTINIT - THIS OVERLAY IS CALLED TO PRINT THE RESULTS OF
*   THE START STOP REQUESTS.  IT THEN PRINTS THEDATA SET NAME,
*   MEMBER NAME, EXPIRY DATE, CREATION DATE, VOLUME SERIAL MUMBERS
*   AND FORMATS THE DEFAULT TITLE.
*
*        PRINTRTN - THIS OVERLAY WILL RESIDE IN CORE UNTIL THE END
*   OF PROCESSING EXCEPT WHEN AN I/O ERROR OCCURS.IT FORMATS THE PRINT
*   LINES AND CALLSANOTHER MODULE TO CONBERT PARTS OF DATA TO
*   HEX FORMAT.
*
*        JFCBREPT - THIS OVERLAY IS CALLED DURING INITIALIZATION
*   TO PRINT THE REMAINING DATA STORED IN THE JFCB BY THE DCBSETUP
*   MODULE.
*
*        PARTISAM - THIS OVERLAY IS CALLED DURING INITIALIZATION
*   WHENEVER AN ISAM OR BPAM FILE IS TO BE READ.  FOR ISAM FILES,
*   THE DCB IS SUBSTITUTED AND THE FILE IS OPENED.  FOR PARTITIONED
*   FILES A CHECK IS MADE FOR A MEMBER NAME, AND SIMPLY OPENS THE FILE
*   IF PRESENT; IF THERE IS NO MEMBERNAME IT IS ASSUMED THAT A DUMP OF
*   THE DIRECTORY IS REQUESTED - THE DCB IS MODIFIED SO THAT THE FILE
*   IS OPENED AS UNDEFINED WITH A BLOCKSIZE OF 256, AND THEN RESTORED
*   SO THAT THE CORRECT INFO WILL SHOW ON THE REPORT.
         EJECT
FILEDUMP CSECT
         SAVE  (14,12),T,*
         BALR  R12,0               SET BASE TO R12
         USING *,R12
SYNSAVE  ST    R13,FSAVE+4         CHAIN SAVE-AREAS
         LR    R11,R13
         LA    R13,FSAVE
         ST    R13,8(R11)
         SPACE 2
*        SET AND CALL ROUTINE TO ANALYZE DCB AND OPEN FILES.  THE
*   DCBSETUP MODULE RETURNS A CODE OF 0 OR 4.
*
*        IN THE CASE OF A CODE 4, CHANGE THE ENTRY POINT IN THE CALL
*   STATEMENT TO POINT TO PARTITION, AND BRANCH BACK TO THE CALL. THE
*   EFFECT OF THIS IS TO GIVE SPECIAL HANDLING TO THE PARTITIONED
*   ORGANIZATION.
*
         L     R11,0(R1)           STORE PARM ADDR IN R11
         ST    R11,FPARMS            AND PARM LIST
*                                                                   VBS
*              DO A GETMAIN FOR THE LARGER OF BLKSIZE/LRECL         VBS
*                                                                   VBS
*        LH    R0,DCBBLKSI        R0 = DCB BLKSIZE                  VBS
*        CH    R0,DCBLRECL        BLKSIZE > THAN LRECL?             VBS
*        BH    GETIT              YES, UNLESS SPANNED FILE          VBS
*        LH    R0,DCBLRECL        IN THAT CASE, USE LRECL           VBS
*        SPACE 1                                                    VBS
GETIT    DS    0H                                                   VBS
*        GETMAIN R,LV=(0)         GET A RECORD WORK AREA            VBS
*                                                                   VBS
*              AS WE NEED THE RECORD WORK AREA BEFORE WE CALL       VBS
*              CALL DCBSETUP IN CASE INPUT IS ON TAPE, AS           VBS
*              DCDSETUP DOES A GET, GET A 32K WORK AREA             VBS
*              BEFORE WE CALL DCB SETUP                             VBS
*                                                                   VBS
         GETMAIN R,LV=32768       GET A RECORD WORK AREA            VBS
         ST    R1,INREC           SAVE ADDRESS OF RECORD AREA       VBS
         SPACE 1                                                    VBS
FCALLDCB EQU   *
* SIMULATE CODE FOR CALL DCBSETUP,(FPARMS,REPORT,INFILE,JFCB)       RH
* SIMULATE CODE FOR CALL DCBSETUP,(FPARMS,REPORT,INFILE,JFCB,INREC) VBS
         CNOP  0,4
         B     *+8                     BRANCH AROUND VCON
FMODNAME DC    V(DCBSETUP)             ENTRY POINT ADDRESS
         CNOP  0,4
         BAL   1,FMODLOAD              LOAD LIST ADDR IN REG1
         DC    A(FPARMS)               PARAMETER 1
         DC    A(REPORT)               PARAMETER 2
         DC    A(INFILE)               PARAMETER 3                  RH
         DC    A(JFCB)                 PARAMETER 4                  VBS
         DC    B'00000000'             SET VL SWITCH BIT
         DC    AL3(INREC)              PARAMETER 5                  VBS
*        DC    AL3(JFCB)               PARAMETER 4                  RH
FMODLOAD EQU   *
         L     15,FMODNAME             LOAD 15 WITH ENTRY ADDR
         BALR  14,15                   BRANCH TO ENTRY POINT R
         B     *+4(R15)           BRANCH ACCORDING TO R15
         B     FISAMCHG           FILE NOT ISAM
         B     FPART              CHECK FOR ISAM OR PO
         B     NOCLOSE            ERROR SOMEWHERE, END OF FILEDUMP
         DC    F'0'               JUST TO BE ON THE SAFE SIDE
*        LTR   R15,R15                  TEST FOR ZERO RETURN-CODE
*        BZ    FISAMCHG
         SPACE 1
FPART    DS    0H
         MVC   FMODNAME(4),=V(PARTISAM) CHANGE ENTRY POINT
         B     FCALLDCB
         SPACE 1
FISAMCHG DS    0H
         TM    INFILE+26,X'80'          ISAM FILE ?
         BZ    FEDITPRM                 IF NO, GO TO EDIT PARM
         NI    FQSAMBR+1,X'0F'          NULLIFY BRANCH AROUND ISAM CODE
         SPACE 2
*   SET AND CALL ROUTINE TO PROCESS PARM IF REQUIRED
FEDITPRM EQU   *
         LH    R2,0(R11)                LOAD LENGTH OF PARM
         LTR   R2,R2               IS IT ZERO?
         BZ    CALLREPT                 YES, THERE ARE NO PARMS     RH1
         CALL  PARMCHEK,(FPARMS,FTITLE,FSTRTTBL)
         LTR   R15,R15             PARM EDIT OK?
         BZ    FPARMOK
         PUT   REPORT,FHEADING     PRINT ERROR MESSAGE
         L     R13,FSAVE+4
         RETURN (14,12),RC=16      ABORT IF PARM INVALID
         SPACE 3
FPARMOK  EQU   *
         CLI   FFLAG2,C'B'              IS IT COPY AND PRINT ?      RH1
         BNE   COPYONLY           NO, CHECK FOR COPY ONLY
         NI    NEWSW+1,X'0F'      YES, TURN SWITCH OFF
         B     COPYPRT            SET UP REST OF FLAGS
COPYONLY DS    0H
         CLI   FFLAG2,C'C'        COPY ONLY ?                       RH1
         BNE   CALLREPT           NO, DON'T COPY THE FILE           RH1
         SPACE 1                                                    RH
COPYPRT  DS    0H
*                                                                   RH1
*        IF SYSUT2 DCB INFO IS NOT SUPPLIED, COPY IT FROM SYSUT1    RH1
*                                                                   RH1
*
         CLI   JFCB+99,X'08'      VSAM FILE?
         BE    OPENCOPY           YES
         SPACE 1
         LA    R5,JFCB2           R5 -> TO SYSUT2 JFCB
         USING INFMJFCB,R5              SET DSECT
         LA    R4,NEWFILE         R4 -> TO SYSUT2 DCB
         USING IHADCB,R4                SET DCB DSECT
         RDJFCB ((4))
         OC    JFCDSORG,JFCDSORG  JFCB DSORG SET?                   RH1
         BNZ   *+4+6              YES, DON'T CHANGE IT
         MVC   NEWFILE+26(1),INFILE+26 NO, SET TO SYSUT1 DSORG
         CLI   JFCRECFM,X'00'     JFCB RECFM SET ?                  RH1
         BNE   *+4+6              YES, DON'T CHANGE IT
         MVC   NEWFILE+36(1),INFILE+36 NO, SET TO SYSUT1 RECFM
         OC    JFCBLKSI,JFCBLKSI  JFCB BLKSIZE = 0 ?                RH1
         BNZ   *+4+6              NO, DON'T CHANGE IT
         MVC   NEWFILE+62(2),INFILE+62 NO, SET TO SYSUT1 BLKSIZE
         OC    JFCLRECL,JFCLRECL  JFCB LRECL = 0 ?                  RH1
         BNZ   *+4+6              NO, DON'T CHANGE IT
         MVC   NEWFILE+82(2),INFILE+82 NO, SET TO SYSUT1 LRECL
         DROP  R4,R5
*                                                                   RH1
OPENCOPY DS    0H
         OPEN  (NEWFILE,(OUTPUT))   OPEN NEW FILE
         TM    NEWFILE+48,X'10'   DID SYSUT2 OPEN OKAY ?
         BO    OPENOK             YES, CONTINUE
         MVC   FHDERR(21),=C'SYSUT2 FAILED TO OPEN'
         B     OPENERR            SAY IT
*        ABEND 2222,DUMP          NO, ABEND
OPENOK   DS    0H
         NI    COPYSW+1,X'0F'     TURN COPY SWITCH OFF
         NI    CLOSCOPY+1,X'0F'     TURN CLOSE SWITCH OFF
CALLREPT DS    0H                                                   RH1
*        CLI   JFCB+99,X'08'      VSAM FILE?
*        BE    BADVOL             YES
         SPACE 1
*                                       SET DATE AND PRINT TABLE
         CALL  REPTINIT,(REPORT,FHEADING,FSTRTTBL,JFCB)             RH
         LTR   R15,R15             WAS REPTINIT OKAY ?
         BNZ   BADVOL              NO, SKIP REST OF REPORT
         CALL  JFCBREPT,(REPORT,JFCB)                               RH
         SPACE 1
BADVOL   DS    0H
         SR    R2,R2               R2 - COUNT OF READS
         LM    R3,R5,FSTRTTBL      R3 - INDEX RATE
*                                  R4 - START COUNT
*                                  R5 - STOP COUNT
         CLI   JFCB+99,X'08'      VSAM FILE?
         BE    SETR6              YES
         SPACE 1
         LA    R6,INFILE           R6 - INPUT DCB
         USING IHADCB,R6              - BASE DSECT
         B     SETR8              GO ON
*                                  R7 - WORK REG FOR LOG REC LNGTH
SETR6    DS    0H
         LA    R6,INACB            R6 - INPUT ACB
         SPACE 1
SETR8    DS    0H
         SR    R8,R8               R8 - COUNT OF RECORDS PRINTED
         LA    R9,FSTRTTBL         R9 - LOAD TABLE ADDRESS IN R9
         LA    R10,FSAVE          R10 - ADDR OF SAVE AREA
         EJECT
FPROCESS EQU   *
         CLI   JFCB+99,X'08'      VSAM FILE?
         BE    READVSAM           YES
         SPACE 1
*                                                                   VBS
*              UNTIL I WRITE A SPECIAL SECTION FOR VBS,             VBS
*              WE MUST BE CONTENT WITH A GET MOVE.                  VBS
*                                                                   VBS
*        GET   INFILE              READ RECORD                      VBS
*                                                                   VBS
         L     R0,INREC           R0 -> TO INPUT RECORD AREA        VBS
         GET   INFILE,(0)          READ RECORD                      VBS
         B     SETR1              ON WE GO
         SPACE 1
READVSAM DS    0H
         GET   RPL=RPLIN          READ NEXT RECORD
         LTR   R15,R15            WAS GET OKAY ?                    RH
         BZ    READOK             YES, GO PROCESS RECORD            RH
         SPACE 1
BADREAD  DS    0H
         CLC   RPLIN+12(4),=XL4'0008009C'   SPECIAL CODE FOR SOFTWARE
         BE    FENDJOB                     EOF
         BAL   R4,SHOWCB          GO PRINT RETURN CODES, ETC        RH
*        CLI   RPLFDBK+3,X'FC'    NEED CNV PROCESSING?
*        BNE   READMSG            NO, END FOR NOW
         SPACE 1
READNOP  NOP   READMSG            B AFTER 1ST TIME THROUGH
         MVI   READNOP+1,X'F0'    CHANGE NOP TO B
         MVC   LINE1+1(L'CNVMSG),CNVMSG MOVE MSG TO LINE1           RH
         BAL   R8,PRINT1          PRINT MSG                         RH
         CLOSE INACB              MUST CLOSE ACB TO MODIFY IT
         MODCB ACB=INACB,         CHANGE ACCESS TO CNTL INTERVAL       +
               MACRF=(CNV)
         LTR   R15,R15            DID IT WORK?
         BNZ   READMSG            NO
         MODCB RPL=RPLIN,         CHANGE ACCESS TO CNTL INTERVAL       +
               OPTCD=(CNV)
         LTR   R15,R15            DID IT WORK?
         BNZ   READMSG            NO
         OPEN  INACB              OPEN VSAM DATASET AGAIN
         LTR   R15,R15            DID DATASET OPEN OKAY ?           RH
         BZ    READVSAM           YES, TRY READ AGAIN
         SPACE 1
READMSG  DS    0H
         LA    R1,5               R1 = SNAP NBR ID                  RH
*        BAL   R4,SNAPCODE        SEE IF SNAP WANTED                RH
         MVC   LINE1+1(L'LP2MSG),LP2MSG MOVE MSG TO LINE1           RH
         BAL   R8,PRINT1          PRINT MSG                         RH
         B     FENDJOB            OEF FOR NOW                       RH
         SPACE 1
READOK   DS    0H
         SPACE 1
SETR1    DS    0H
         L     R1,INREC           R1 -> TO INPUT RECORD             VBS
         LA    R2,1(0,R2)          ADD TO RECORD COUNT
FSTOPSTR EQU   *
         CR    R2,R4               COMPARE NO OF READS TO START COUNT
         BL    FPROCESS
         CR    R2,R5               COMPARE NO OF READS TO STOP COUNT
         BNH   FQSAMBR
         LA    R9,12(R9)                POINT TO NEXT SET OF ENTRIES
         LM    R3,R5,0(R9)              LOAD VALUES IN R3 - R5
         C     R4,FENDFILE              START = END OF FILE ?
         BE    FENDPARM                 YES
         SPACE
         B     FSTOPSTR                 NO, CONTINUE
*
FQSAMBR  B     FREADPRO                 BRANCH AROUND ISAM CODE, THIS
*                                       INSTRUCTION MAY BE CHANGED TO
*                                       A NOP AFTER THE DCBSETUP CALLS
*                                       ARE COMPLETED
         TM    DCBRECFM,X'40'     VARIABLE LENGTH RECORDS?          RH3
         BZ    NOTVAR             NO, DON'T CHANGE DCBLRECL         RH3
         MVC   DCBLRECL(2),0(R1)  MOVE REAL LRECL TO DCBLRECL       RH3
         SPACE 1                                                    RH3
NOTVAR   DS    0H                                                   RH3
         TM    INFILE+81,X'10'          TEST FOR OVERFLOW RECORD
         BZ    FREADPRO                 IF NO, BRANCH
         MVI   FOVFLOW,C'*'             SET OVERFLOW RECORD FLAG
FREADPRO EQU   *
         AR    R4,R3               INDEX START FOR NEXT READ
         CLI   JFCB+99,X'08'      VSAM FILE?
         BNE   FRECLEN            NO
         L     R7,RPLIN+48         LOAD LOG RCD LENGTH
         B     FRECLEN2           ON WE GO
         SPACE 1
FRECLEN  DS    0H
         LH    R7,DCBLRECL         LOAD LOG RCD LENGTH
         SPACE 1
FRECLEN2 DS    0H
         ST    R7,FPARMS+4         STORE LENGTH
         ST    R2,FPARMS           STORE COUNT
         ST    R1,FPARMS+8         STORE RECORD ADDR
COPYSW   B     NOCOPY             DON'T WRITE IF NOT TURNED OFF
*                                                                   RH
*              AS PUTX CANNOT HANDLE INPUT VBS RECORDS,             RH
*              THIS MUST BE CHANGED TO A STRAIGHT PUT.              RH
*                                                                   RH
*        PUTX  NEWFILE,INFILE     WRITE RECORD ON TO NEW FILE
*                                                                   RH
         LR    R0,R1              R0 -> TO INPUT RECORD             RH
         PUT   NEWFILE,(0)        WRITE RECORD ON TO NEW FILE
NEWSW    B     COUNTREC           READ NEXT RECORD IF COPY ONLY
NOCOPY   DS    0H
         CALL  PRINTRTN,(REPORT,FHEADING,FPARMS)
         SPACE
COUNTREC DS    0H
         LA    R8,1(0,R8)          ADD 1 TO RECORDS PRINTED
         MVI   FOVFLOW,X'40'            RESET OVERFLOW RECORD FLAG
         B     FPROCESS
*
FENDPARM BCTR  R2,0
FENDJOB  EQU   *
         MVI   FHEADING+1,C' '          CLEAR PRINT LINE            RH1
         MVC   FHEADING+2(131),FHEADING+1 *                         RH1
         MVI   FHEADING,C'0'            SET CARR CTL
         MVC   FHEADING+28(10),FREADPAT SET RECDS READ PATTERN
         MVC   FHEADING+39(12),=C'RECORDS READ'
         CVD   R2,FSTRTTBL         CONVERT RECORDS READ TO PACKED
         ED    FHEADING+28(10),FSTRTTBL+4    MOVE TO PRINT
         MVC   FHEADING+63(10),FREADPAT SET RCDS WRITTEN PATTERN
         MVC   FHEADING+74(15),=C'RECORDS WRITTEN'
         CVD   R8,FSTRTTBL         CONVERT RECORDS PRINTED TO PACKED
         ED    FHEADING+63(10),FSTRTTBL+4    MOVE TO PRINT
         SPACE 1
OPENERR  DS    0H
         PUT   REPORT,FHEADING          WRITE LAST LINE
CLOSCOPY B     NOCLOSE            DON'T CLOSE IF NOT OPEN
         CLOSE NEWFILE            CLOSE NEW FILE
         SPACE
NOCLOSE  DS    0H
*        TM    REPORT+48,X'10'    DID SYSPRINT OPEN ?
*        BZ    NOCLOSE2           NO, DON'T TRY TO CLOSE IT THEN
         CLOSE REPORT             YES, CLOSE IT
         SPACE 1
NOCLOSE2 DS    0H
         TM    INFILE+48,X'10'    DID SYSUT1 OPEN ?
         BZ    NOCLOSE3           NO, DON'T CLOSE IT THEN
         CLOSE INFILE             YES, CLOSE IT
         SPACE 1
NOCLOSE3 DS    0H
         CLI   JFCB+99,X'08'      VSAM FILE?
         BNE   NOCLOSE4           NO
         CLOSE INACB              YES, CLOSE IT
         SPACE 1
NOCLOSE4 DS    0H
         L     R13,FSAVE+4
         RETURN (14,12),RC=0
         SPACE 2
FREADER EQU    *
         STM   R14,R1,SYNSAVE           SAVE REGS 14,15,0,1
         ST    R13,SYNSAVE+16           SAVE CURRENT SAVE AREA ADDRESS
         LA    R13,SYNSAVE+20           LOAD TEMP SAVE AREA ADDRESS
*        CALL ERROR ANALYSIS ROUTINE                                RH1
         CALL  ERRANAL,(REPORT,SYNSAVE,FHEADING)                    RH1
         LM    R14,R1,SYNSAVE           LOAD 14,15,0,1
         L     R13,SYNSAVE+16           RESTORE 13 TO CURRENT SAVE AREA
         BR    R14                      RETURN TO GET ROUTINE
         DS    0D                  ALIGN WORK AREAS
FSAVE    DC    18F'0'              SAVE-AREA FOR REGS
FSTRTTBL DS    0CL120              30F TABLE FOR INDX/START/STOP VALUES
         DC    2F'1'
         DC    X'00000064'              DEFAULT STOP = 100 RECORDS
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
         DC    X'7FFFFFFF'
         DC    F'1'
         DC    X'7FFFFFFF'
FENDFILE DC    X'7FFFFFFF'
FPARMS   DC    3F'0'                    PARM ADDRESSES
FFLAG1   DC    C'N'                     THESE FLAGS MUST BE RIGHT   RH1
FFLAG2   DC    C'D'                     AFTER FPARMS. FFLAG1 IS AN  RH1
*                                       INDICATOR FOR HEX OR DEC    RH1
*                                       OFFSETS. FFLAG2 IS AN       RH1
*                                       INDICATOR FOR THE COPY      RH1
*                                       FACILITY                    RH1
FHEADING DS    0CL133
         DC    C'1'
FHDERR   DC    CL12' '
*        DC    CL13'1'
FTITLE   EQU   *
         DC    CL12' '
         DC    CL84' '
         ORG   *-84
         DC    C'DIVERSIFIED DEVELOPMENTS FILE DUMP UTILITY'  42
         DC    C', VERSION 1.3, NOVEMBER, 1987'               29 71
         ORG
         DC    CL24'PAGE   1   RECNO   LRECL'
FOVFLOW  DC    CL1' '                   ISAM OVERFLOW RECORD FLAG
FPAGECTR DC    PL2'1'                   PAGE COUNT
*
FREADPAT DC    XL10'40206B2020206B202120'
INREC    DC    F'0'               ADDRESS OF INPUT RECORD AREA      VBS
         SPACE
         LTORG
         EJECT
NEWFILE  DCB   DSORG=PS,DDNAME=SYSUT2,MACRF=PM,EXLST=JFCBADD2
REPORT   DCB   DSORG=PS,DDNAME=SYSPRINT,MACRF=PM,RECFM=FBA,            X
               LRECL=133,BLKSIZE=3990
         EJECT
*
*     IN THE FOLLOWING INFILE DCB, THE BUFNO=2 PARAMETER
*     MUST BE LEFT AS IS
*
INFILE   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,EODAD=FENDJOB,       VBSX
               SYNAD=FREADER,EROPT=ACC,EXLST=JFCBADD BUFNO=2        VBS
*NFILE   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=FENDJOB,          X
               SYNAD=FREADER,EROPT=ACC,EXLST=JFCBADD,BUFNO=2
         DS    CL156
         DS    0F
         DC    C'JFCB'            KEEP JFCB AT REPORT+352           RH1
JFCB     DC    176X'00'
         ORG   JFCB               BACK TO START OF JFCB
         IEFJFCBN                 GEN JFCB LABELS
JFCBADD  DC    X'05'              DCB OPEN EXIT                     RH1
         DC    AL3(DCBEXIT)       *                                 RH1
         DC    X'87'              JFCB ADDRESS                      RH1
         DC    AL3(JFCB)          *                                 RH1
JFCB2    DC    176X'00'
JFCBADD2 DC    X'87'              JFCB ADDRESS                      RH1
         DC    AL3(JFCB2)         *                                 RH1
         SPACE 3                                                    RH1
*        IF RECFM=U ON SYSUT1, MAKE SURE CHAINED SCHEDULING BIT     RH1
*        IN DCBOPTCD IS TURNED OFF BECAUSE STUPID OS WILL LEAVE     RH1
*        IT ON AND THEN DCBLRECL IS NOT SET CORRECTLY.              RH1
         SPACE 1                                                    RH1
DCBEXIT  DS    0H                OPEN EXIT TO CHECK RECFM AND OPTCD RH1
         DROP  R6                       RELEASE IHADCB FROM BASE 6
         USING IHADCB,R1          SET DCB DSECT BASE                RH1
         TM    DCBRECFM,DCBRECU   IS IT A RECFM U FILE?             RH1
         BNOR  R14                NO, RETURN TO OPEN                RH1
         SPACE 1                                                    RH1
         NI    DCBOPTCD,255-DCBOPTC  MAKE SURE CHAIN BIT IS OFF     RH1
         SPACE 1                                                    RH1
EXITEND  DS    0H                                                   RH1
         BR    R14                RETURN TO OPEN ROUTINE            RH1
         DROP  R1                 DROP DCB DSECT BASE               RH1
         EJECT
         SPACE 1                                                    RH
CLEAR1   DS    0H                                                   RH
         MVI   LINE1+1,C' '       CLEAR LINE 1                      RH
         MVC   LINE1+2(131),LINE1+1  *                              RH
         BR    R8                 RETURN                            RH
         SPACE 1                                                    RH
PRINT1   DS    0H                                                   RH
         PUT   REPORT,LINE1       PRINT LINE                        RH
         B     CLEAR1             CLEAR LINE1                       RH
         SPACE 1                                                    RH
SHOWCB   DS    0H                                                   RH
         ST    R4,SNAPSAVE        SAVE RETURN ADDRESS               RH
         ST    R15,DUB            SAVE GET RC                       RH
         UNPK  LINE1+4(3),DUB+3(2) UNPACK RC                        RH
         TR    LINE1+4(2),TRTAB-239 TRANSLATE TO DISPLAY            RH
         MVC   LINE1+1(3),=C'RC=' SET UP RC MSG                     RH
         XC    RPLMSGL,RPLMSGL    MAKE SURE MSG LTH FIELD IS 0      RH
CB1      SHOWCB RPL=(1),AREA=RPLDATA,LENGTH=20,OBJECT=DATA,         RH +
               FIELDS=(FTNCD,FDBK,MSGAREA,MSGLEN,RBA)               RH
         UNPK  LINE1+13(3),RPLFTNCD+3(2) UNPACK FUNCTION CODE       RH
         TR    LINE1+14(1),TRTAB-239 TRANSLATE TO DISPLAY           RH
         MVC   LINE1+6(8),=C', FTNCD=' SET UP FTN MSG               RH
         UNPK  LINE1+21(5),RPLFDBK+2(3) UNPACK FEEDBACK CODE        RH
         TR    LINE1+22(3),TRTAB-239 TRANSLATE TO DISPLAY           RH
         MVC   LINE1+15(7),=C', FDBK=' SET UP FEEDBACK MSG          RH
         UNPK  LINE1+31(9),RPLRBA+1 UNPACK RBA                      RH
         MVI   LINE1+39,X'EF'     TRANSLATES TO BLANK               RH
         TR    LINE1+31(9),TRTAB-239 TRANSLATE TO DISPLAY           RH
         MVC   LINE1+25(6),=C', RBA=' SET UP RBA MSG                RH
         MVC   LINE1+41(L'RBAMSG),RBAMSG PUT IN DEFAULT MSG         RH
         ICM   R4,15,RPLMSGL      R4 = SHOWCB MSG LTH               RH
         BZ    USDFLT             NONE, USE DEFAULT MSG             RH
         BCTR  R4,0               R4 = HEX LTH OF MSG               RH
         L     R1,RPLMSGA         R1 -> TO SHOWCB MSG               RH
         EX    R4,MVCRPLMS        PUT SHOWCB MSG IN LINE            RH
         B     USDFLT             BRANCH AROUND EXEC INST           RH
MVCRPLMS MVC   LINE1+41(0),0(R1)                                    RH
         SPACE 1                                                    RH
USDFLT   DS    0H                                                   RH
         MVC   LINE1+127(5),=C'ERROR' SAY ERROR MSG                 RH
         BAL   R8,PRINT1          PRINT ERROR MSG                   RH
         L     R4,SNAPSAVE        LOAD RETURN ADDRESS               RH
         BR    R4                 RETURN TO CALLER                  RH
         SPACE 3                                                    RH
SNAPCODE DS    0H                                                   RH
SNAPBR   BR    R4                 NOP IF SNAPDCB OPEN               RH
*        STC   R1,SNAPMAC+4       SET SNAP NBR                      RH
         STM   R0,R15,SNAPSAVE    SAVE REGS OVER SNAP         DBUG  RH
         L     R2,INREC           GET RECORD ADDRESS
*        LH    R15,0(0,R2)        R15 = LTH OF THIS RECORD    DBUG  RH
*        AR    R15,R2             SET END OF DUMP RANGE       DBUG  RH
         LA    R15,0512(0,R2)     SET END OF DUMP RANGE       DBUG  RH
*NAPMAC  SNAP  DCB=SNAPDCB,PDATA=REGS,ID=1,                            C
               STORAGE=((2),(15),RPLIN,INREC)                       RH
         LM    R0,R15,SNAPSAVE    LOAD REGS AFTER SNAP        DBUG  RH
         BR    R4                 RETURN TO CALLER                  RH
         SPACE 3
         TITLE 'CONSTANT AREA'
         SPACE 1                                                    RH
DUB      DS    D              WORK AREA FOR CVD ETC.
RPLDATA  DS    0F                                                   RH
RPLFTNCD DC    F'0'                                                 RH
RPLFDBK  DC    F'0'                                                 RH
RPLMSGA  DC    F'0'                                                 RH
RPLMSGL  DC    F'0'                                                 RH
RPLRBA   DC    F'0'                                                 RH
         DC    F'0'               TO SET UP RBA CORRECTLY           RH
         SPACE 1                  SHOW FIELDS FOR ACB               RH
ACBDATA  DS    0F                                                   RH
ACBERROR DC    F'0'                                                 RH
RBAMSG   DC    C' INVALID RBA'                                      RH
*PASSWD  DC    AL1(0),CL8' '  AREA FOR PASSWORD  + LENGTH IF USED
*RFYMSG  DC    C'VERIFY OF DATASET FAILED'                          RH
LP2MSG   DC    C'GET RBA RECORD AT LABEL LP2 FAILED'                RH
CNVMSG   DC    C'WILL TRY CONTROL INTERVAL PROCESSING'              RH
OPENERRM DC    C'VSAM DATASET DID NOT OPEN.'                        RH
         DC    C' RETURN CODE IS'                                   RH
OPENRC   DC    X'40202020'                                          RH
         DC    C', DATA ='                                          RH
ERRCODE  DC    X'40202020'                                          RH
         DC    C', INDEX ='                                         RH
ERRCODE2 DC    X'40202020'                                          RH
         DC    C', DATASET='                                        RH
ERRCATN  DC    CL44' '                                              RH
LOPENERR EQU   *-OPENERRM                                           RH
         SPACE 1                                                    RH
LINE1    DC    CL133'- '   THIS IS USED FOR DEBUGGING.
         SPACE 1                                                    RH
BLANK    DC    CL80'  '
         SPACE 1                                                    RH
TRTAB    DC    C' 0123456789ABCDEF'                                 RH
         SPACE 2
SNAPSAVE DC    16F'-1'         SNAP SAVE AREA                  DBUG RH
         SPACE 1
RPLIN    RPL   MSGAREA=GENM,MSGLEN=128,ACB=INACB,AREA=INREC,        XXXX
               AM=VSAM,RECLEN=4096,                                  XXX
               AREALEN=4,OPTCD=(SYN,LOC)
*              AREALEN=4,OPTCD=(KGE,GEN,SYN,NSP,LOC)
         SPACE 1
         SPACE 2
INACB    ACB   DDNAME=SYSUT1,STRNO=3,BUFNI=09,EXLST=EXLST,        XXXXXX
               AM=VSAM,                                              +++
               MAREA=GENM,MLEN=128,                               XXXXXX
               MACRF=(SEQ,IN)
*              MACRF=(CNV,KEY,DIR,SEQ,IN,OUT),                       +++
               PASSWD=PASSWD      FOR PASSWORD IF USED
         SPACE 1
EXLST    EXLST  EODAD=(FENDJOB,A)
         SPACE 2
GENM     DC    CL128' '
         SPACE 1
         LTORG
         EJECT
         PRINT NOGEN
         DCBD  DSORG=PS
         PRINT GEN
         TITLE 'SETUP DCB FOR QSAM FILES'
DCBSETUP CSECT
         SAVE  (14,12),T,*
         BALR  R12,R0              SET BASE TO R12
         USING *,R12
         ST    R13,DSAVE+4         CHAIN SAVE-AREAS
         LR    R11,R13
         LA    R13,DSAVE
         ST    R13,8(R11)
         SPACE 2
         LM    R2,R6,0(R1)         R2 - PARM ADDR                   VBS
*        LM    R2,R5,0(R1)         R2 - PARM ADDR                   RH
         L     R6,0(0,R6)         R6 -> TO INPUT RECORD AREA        VBS
         ST    R6,INREC2          SAVE IT                           VBS
         L     R2,0(0,R2)          R3 - REPORT DCB
*                                  R4 - INPUT DCB
         USING INFMJFCB,R5             SET JFCB DSECT
*                                 R6 -  USED FOR CHANNEL PROGR
*                                R6 - CREATION DAY             DBS ONLY
*                                 R7 -  USED FOR CHANNEL PROGR
*                                R7 - EXPIRY YEAR              DBS ONLY
*                                R8 - EXPIRY DAY               DBS ONLY
*                                R9 - WORK REG                 DBS ONLY
*                                 R11 - BAL RETURN ADDR
*                                 R12-  BASE REGISTER
         USING IHADCB,R4                SET DCB DSECT
         SPACE 3
         OPEN  ((3),(OUTPUT))           OPEN PRINT FILE
         TM    48(R3),X'10'             WAS OPEN SUCCESSFUL?
         BO    SYSOPEN            YES, ON WE GO
         ABEND 1111,DUMP          NO, ABEND
         SPACE 1
SYSOPEN  DS    0H
*        BZ    DABEND                   NO, ABEND
*
*   READ THE JFCB AND DETERMINE DEVICE TYPE FOR INPUT FILE
         RDJFCB ((4))
         DEVTYPE DCBDDNM,DEVDATA
         CLI   DEVDATA+2,X'80'          IS IT A TAPE DRIVE?
         BE    DTAPES                   YES, GO TO TAPE PROCESSING
*        CLC   DEVDATA+2(2),=X'8001'    2400 SERIES TAPE?
*        BE    DTAPES                   YES, GO TO TAPE PROCESSING
*        CLC   DEVDATA+2(2),=X'8003'    3400 SERIES TAPE?
*        BE    DTAPES                   YES, GO TO TAPE PROCESSING
*                                       NO, ASSUME DISK
*   RESOLVE PARAMETER ADDRESSES FOR THE OBTAIN MACRO
         LA    R6,JFCBDSNM        R6 -> TO DSNAME
         LA    R7,JFCBVOLS        R7 -> TO VOL SER
         STM   R6,R7,DCAMLSTA     PUT A(DSN,VOLSER) IN OBTAIN LIST
         OBTAIN DSCBCAM
         SPACE 1
         CLC   JFCDSORG,=X'0008'  VSAM FILE?
         BE    ACBOPEN            YES, GO DO VSAM OPEN
         SPACE 1
         MVC   DSTORDAT(6),DCAMWORK+9   STORE CREATION & EXPIRY DATES
         SPACE 1                                                    RH
*        MERGE DSCB FIELDS INTO JFCB FIELDS                         RH1
*        THIS  WAS AFTER DRETURN4                                   RH1
         OC    JFCDSORG,JFCDSORG  JFCB DSORG SET?                   RH1
         BNZ   *+4+6              YES, DON'T CHANGE IT              RH1
         MVC   JFCDSORG,DCAMWORK+38  NO, JFCB DSORG = DSCB DSORG    RH1
         CLI   JFCRECFM,X'00'     JFCB RECFM SET ?                  RH1
         BNE   *+4+6              YES, DON'T CHANGE IT              RH1
         MVC   JFCRECFM,DCAMWORK+40  NO, JFCB RECFM = DSCB RECFM    RH1
         OC    JFCBLKSI,JFCBLKSI  JFCB BLKSIZE = 0 ?                RH1
         BNZ   *+4+6              NO, DON'T CHANGE IT               RH1
         MVC   JFCBLKSI,DCAMWORK+42  JFCB BLKSIZE = DSCB BLKSIZE    RH1
         OC    JFCLRECL,JFCLRECL  JFCB LRECL = 0 ?                  RH1
         BNZ   *+4+6              NO, DON'T CHANGE IT               RH1
         MVC   JFCLRECL,DCAMWORK+44  JFCB LRECL = DSCB LRECL        RH1
         MVC   JFCBCRDT(6),DSTORDAT   STORE CREATION & EXPIRY DATES
*                                                                   RH1
*        THE   ABOVE WAS AFTER DRETURN4                             RH1
*                                                                   RH1
         TM    DCAMWORK+38,X'02'        PARTITIONED?
         BO    DRETURN4                 YES, GO TO RETURN
         TM    DCAMWORK+38,X'80'        ISAM?
         BZ    DOPENDSK                 NO, ASSUME QSAM PROCESSING
         SPACE
DRETURN4 EQU   *
         L     R13,DSAVE+4
         RETURN (14,12),RC=4
         SPACE
DOPENDSK EQU   *
         BAL   R11,DQSAMOPN             OPEN INPUT FILE
         MVC   JFCBCRDT(6),DSTORDAT     MOVE CREATION & EXPIRY DATES
*                                       TO JFCB
         SPACE 1                                                    RH
*              NOW THAT WE HAVE THE FILE OPEN, SET UP THE           RH1
*              JFCB THE WAY WE WANT THE REPORT TO LOOK              RH1
         SPACE 1                                                    RH
         MVC   JFCDSORG,DCAMWORK+38  JFCB DSORG = DSCB DSORG        RH1
         MVC   JFCRECFM,DCAMWORK+40  JFCB RECFM = DSCB RECFM        RH1
         MVC   JFCBLKSI,DCAMWORK+42  JFCB BLKSIZE = DSCB BLKSIZE    RH1
         MVC   JFCLRECL,DCAMWORK+44  JFCB LRECL = DSCB LRECL        RH1
         SPACE 1
         B     DRETZERO                 RETURN
         SPACE 3
DTAPES   EQU   *
*                                                                   RH
*              WE NOW APPEAR TO  HAVE ANOTHER PROBLEM...            RH
*              IF RECFM = U AND                                     RH
*              IF BLKSIZE IS OVER 32699, EXCP TO TAPE               RH
*              DOESN'T WORK, SO  CHANGE BLKSIZE TO 32000            RH
*              IF OVER 32000.                                       RH
*                                                                   RH
*        TM    JFCRECFM,DCBRECU   RECFM = U ?                       RH1
*        BNO   DTAPES2            NO, ITS OKAY                      RH
*        CLC   JFCBLKSI,=H'32000' BLKSIZE OVER 32000 ?              RH
*        BNH   DTAPES2            NO, ITS OKAY                      RH
*        MVC   JFCBLKSI,=H'32000' YES, SET BLKSIZE = 32000          RH
*        SPACE 1
DTAPES2  DS    0H
*                                                                   RH
*              FOR TAPES, DON'T   TURN OFF THE CHAINED              RH
*              SCHEDULING BIT IN  THE DCB IF RECFM=U                RH
*                                                                   RH
         L     R11,DCBEXLST       R11 -> TO EXIT LIST               RH
         SR    R6,R6              R6 = ZERO                         RH
         ST    R6,0(0,R11)        DON'T DO OPEN EXIT                RH
*                                                                   RH
         OI    JFCB+52,X'08'      DON'T WRITE JFCB BACK             RH
*
*              BECAUSE WE WILL BE  DOING EXCP ON THE TAPE,
*              IN ORDER TO PREVENT THE OPEN FROM READING TOO
*              FAR INTO THE FILE  WHEN IT PRIMES THE BUFFERS,
*              BEFORE THE OPEN,   CHANGE THE NUMBER OF BUFFERS
*              IN THE DCB TO 1.
*
*
         MVI   DCBBUFNO,X'01'     CHANGE BUFFERS TO 1 FOR TAPE      RH
*
*        BAL   R11,DQSAMOPN             OPEN INPUT FILE
         OPEN  TYPE=J,((4),INPUT)       OPEN INPUT FILE
         TM    DCBOFLGS,X'10'           WAS OPEN SUCCESSFUL?
         BZ    SYS1ERR                  NO, ABEND
*        RDJFCB ((4))                   REREAD JFCB
*
         MVI   JFCBCRDT,X'00'        INITIALIZE CREATION DATE TO 0
*        CLC   DCBBLKCT,=F'1'     ONLY 1 BLOCK IN FILE ?            RH
*        BH    DRETZERO           YES, END OF DCBSETUP              RH
*   INITIALIZE FOR CHANNEL PROGRAMMING
         ST    R4,DCBADD1               STORE ADD OF INFILE DCB
         L     R10,DCBIOBAD             SAVE CURRENT A(IOB)         MVS
         LA    R1,DIOBLK1               STORE IOBLK1 ADDRESS IN
         ST    R1,DCBIOBAD              INFILE DCB
         MVC   DCBMACRF(2),=X'D008'     INFILE MACRF = E
*
         CLI   JFCBLTYP,X'02'           STANDARD LABELS?
         BNE   DUALDEN
         SPACE 1
BACK1TM  DS    0H
         LA    R6,DJMPMARK              STORE CCW TO BACKSPACE
         ST    R6,CHANADD1              PAST TAPE MARK
         BAL   R6,DEXCPROG              EXECUTE BACKSPACE
         L     R6,TMCTR           R6 = NBR OF TM'S BACKSPACED
         LA    R6,1(0,R6)         ADD 1 TO IT
         ST    R6,TMCTR           SAVE NEW TOTAL
*        CLC   DCBBLKCT,=F'1'     ONLY 1 BLOCK IN FILE ?            RH
*        BH    MULTBLK            NO, MORE                          RH
*        BAL   R6,DEXCPROG        YES, EXECUTE BACKSPACE
*        L     R6,TMCTR           R6 = NBR OF TM'S BACKSPACED
*        LA    R6,1(0,R6)         ADD 1 TO IT
*        ST    R6,TMCTR           SAVE NEW TOTAL
         SPACE 1                                                    RH
MULTBLK  DS    0H                                                   RH
         LA    R6,DRDBACK               STORE CCW TO READ LABELS
         ST    R6,CHANADD1              BACKWARD TO CREATION DATE
         SPACE 1                                                    RH
MULTBLK2 DS    0H                                                   RH
         BAL   R6,DEXCPROG              EXECUTE READ
         CLC   HDR1(4),=C'HDR2'   FOUND HDR2 ?
         BE    HDR2DATA           YES, GO MOVE HDR2 DATA TO JCFB
         CLC   HDR1(4),=C'HDR1'   FOUND HDR1 ?
         BE    DRDBOK             YES, MOVE HDR1 DATA TO JFCB
         SPACE 1                                                    RH
         B     MULTBLK2           TRY READBACK ANOTHER RECORD
*        CH    R15,=H'4'          WAS READ BACK OKAY ?
*        BE    MULTBLK2           NO, TRY READBACK ANOTHER RECORD
*        BE    BACK1TM            NO, TRY BACKSPACE ANOTHER TM
*        BNE   DRDBOK             YES
*        DC    H'0'
         SPACE 1                                                    RH
HDR2DATA DS    0H                                                   RH
HSEQUENT EQU   *
         MVI   JFCDSORG,X'40'     TAPE IS PHYSICAL SEQUENTIAL
HRECFMA  EQU   *
HUNDEF   EQU   *
         MVI   JFCRECFM,X'C0'     DEFAULT TO UNDEFINED RECORDS
         CLI   HDR2+4,C'U'        UNDEFINED RECORDS ?
         BE    HRECFMB
HFIXED   EQU   *
         CLI   HDR2+4,C'F'        FIXED RECORDS ?
         BNE   HVARIABL
         MVI   JFCRECFM,X'80'     SAY FIXED
         B     HRECFMB
HVARIABL EQU   *
         MVI   JFCRECFM,X'40'     SAY VARIABLE
HRECFMB  EQU   *
         SPACE 1
HBLOCKED EQU   *
         CLI   HDR2+38,C'B'       BLOCKED RECORDS ?
         BNE   HSTANDRD
         OI    JFCRECFM,X'10'     SAY BLOCKED
HSTANDRD EQU   *
         CLI   HDR2+38,C'S'       SPANNED RECORDS ?
         BNE   HTRACKOF           NO
         OI    JFCRECFM,X'08'     YES, SAY SPANNED BLOCKS?
HTRACKOF EQU   *
         CLI   HDR2+38,C'R'       BLOCKED AND SPANNED RECORDS ?
         BNE   HASACTL            NO
         OI    JFCRECFM,X'18'     YES, SAY SO
HASACTL  EQU   *
         CLI   HDR2+36,C'A'       ASA CONTROL ?
         BNE   HMACHCTL           NO
         OI    JFCRECFM,X'04'     SAY ASA CONTROL
         B     HBLKSIZE
HMACHCTL EQU   *
         CLI   HDR2+36,C'M'       MACHINE CONTROL ?
         BNE   HBLKSIZE           NO
         OI    JFCRECFM,X'02'     SAY MACHINE CONTROL
HBLKSIZE EQU   *
*
*        CLC   JFCBLKSI,X'0000'   IS THERE A BLKSIZE IN JFCB ?
*        BNE   NOBLKSI            YES, DON'T CHANGE IT
*              TO SHOW SPECIFIED BLKSIZE, ENABLE ABOVE 2 INSTS.
         PACK  HDBLWORD+5(3),HDR2+5(5) MOVE BLKSIZE TO DOUBLE WORD
*        PACK  DRDBACK(8),HDR2BLKS PACK TAPE FILE BLKSIZE
         CVB   R6,HDBLWORD        CONVERT TO BINARY
         STH   R6,JFCBLKSI        PUT TAPE FILE BLKSIZE IN JFCB
         SPACE 1
NOBLKSI  DS    0H
*
*        CLC   JFCLRECL,X'0000'   IS THERE AN LRECL IN JFCB ?
*        BNE   NOLRECL            YES, DON'T CHANGE IT
*              TO SHOW SPECIFIED LRECL, ENABLE ABOVE 2 INSTS.
*        PACK  DRDBACK(8),HDR2RECL PACK TAPE FILE LRECL
         PACK  HDBLWORD+5(3),HDR2+10(5) MOVE LRECL TO DOUBLE WORD
         CVB   R6,HDBLWORD        CONVERT TO BINARY
         STH   R6,JFCLRECL        PUT TAPE FILE LRECL IN JFCB
         SPACE 1
NOLRECL  DS    0H
HLRECL   EQU   *
         B     MULTBLK2           GO READ HDR1
         SPACE 1                                                    RH
DRDBOK   DS    0H                                                   RH
*   ROUTINE TO CONVERT DATES FROM HEADER LABEL AND STORE THEM IN
*   THE JFCB
         PACK  DRDBACK(8),DSTRDATA+1(2) PACK CURRENT YEAR
         CVB   R6,DRDBACK               CONVERT TO BINARY
         STC   R6,JFCBCRDT            STORE IN JFCB
         PACK  DRDBACK(8),DSTRDATA+3(3) PACK CURRENT DAY
         CVB   R6,DRDBACK               CONVERT TO BINARY
         STH   R6,DRDBACK               MOVE TO ALIGNED STORAGE
         MVC   JFCBCRDT+1(2),DRDBACK     STORE IN JFCB
         PACK  DRDBACK(8),DSTRDATA+7(2) PACK EXPIRY YEAR
         CVB   R6,DRDBACK               CONVERT TO BINARY
         STC   R6,JFCBXPDT            STORE IN JFCB
         PACK  DRDBACK(8),DSTRDATA+9(3) PACK EXPIRY DAY
         CVB   R6,DRDBACK               CONVERT TO BINARY
         STH   R6,JFCBXPDT+1            STORE IN JFCB
*
*              ALSO, BECAUSE WE   ARE NOW DOING OPEN TYPEJ,
*              WE MUST ALSO FILL  IN THE WHOLE BLOODY JFCB
*
         CLI   JFCBDSNM,C' '      IS THERE A DSNAME ?
         BNE   *+4+6              YES, DON'T CHANGE IT
         MVC   JFCBDSNM(17),HDR1DSN NO, PUT TAPE DSN IN JFCB
*
         CLI   JFCBVOLS,C' '      IS THERE A VOLSER IN JFCB ?
         BNE   *+4+6              YES, DON'T CHANGE IT
         MVC   JFCBVOLS(6),HDR1VOL NO, PUT TAPE VOL IN JFCB
*
         CLI   JFCBVLSQ+1,X'00'   IS THERE A VOLSEQ IN JFCB ?
         BNE   NOVLSQ             YES, DON'T CHANGE IT
         PACK  DRDBACK(8),HDR1VSEQ PACK TAPE VOL SEQ NBR
         CVB   R6,DRDBACK         CONVERT TO BINARY
         STH   R6,JFCBVLSQ        PUT TAPE VOL SEQ IN JFCB
         SPACE 1
NOVLSQ   DS    0H
         CLI   JFCBFLSQ+1,X'00'   IS THERE A FILE SEQ IN JFCB ?
         BNE   NOFLSQ             YES, DON'T CHANGE IT
         PACK  DRDBACK(8),HDR1SEQN PACK TAPE FILE SEQ NBR
         CVB   R6,DRDBACK         CONVERT TO BINARY
         STH   R6,JFCBFLSQ        PUT TAPE FILE SEQ IN JFCB
         SPACE 1
NOFLSQ   DS    0H
*
*   RESTORE THE TAPE TO POINT TO THE FIRST DATA RECORD ON THE FILE
*
         L     R11,TMCTR          R11 = NBR OF TM'S BACKSPACED
         MVI   DJMPMARK,X'3F'           CHANGE CCW TO FORWARD SPACE
         LA    R6,DJMPMARK              STORE CCW TO FORWARD SPACE
         ST    R6,CHANADD1              PAST TAPE MARK
         SPACE 1
UP1TM    DS    0H
         BAL   R6,DEXCPROG              EXECUTE FORWARD SPACE
         BCT   R11,UP1TM          FORWARD SPACE AS MANY TM'S
*                                 AS WE HAD TO BACKSPACE
*        DEBUG
*        DEBUG
*        DEBUG
*        LA    R6,DEBUGCCW              STORE CCW TO READ LABELS
*        ST    R6,CHANADD1              BACKWARD TO CREATION DATE
*        BAL   R6,DEXCPROG              EXECUTE READ
*        B     NODEBUG
*        DC    H'0'
*EBUGCCW CCW   12,ENDHDR2,96,80          HDR2
*        CCW   12,ENDHDR1,32,80          HDR1
*BUGCCW  CCW   02,HDR1,96,80             HDR2
*        CCW   02,HDR2,32,80             HDR1
*ODEBUG  DS    0H
*        DEBUG
*        DEBUG
*        DEBUG
*
*
*              IT APPEARS THAT NOW THAT WE ARE DOING OPEN TYPE=J,
*              WE DON'T HAVE TO REREAD THE FIRST BLOCK AND REFRESH
*              THE DCB, SO THE FOLLOWING COMMENTED OUT CODE
*              APPEARS TO NOT BE  NEEDED.
*
*        ST    R10,28(0,R4)             RESET A(IOB) FOR GET        MVS
**                                                                  VBS
**             UNTIL I WRITE A SPECIAL SECTION FOR VBS,             VBS
**             WE MUST BE CONTENT WITH A GET MOVE.                  VBS
**                                                                  VBS
*        MVC   42(2,R4),=X'5000'        RESET INFILE MACRF TO GM    VBS
**       MVC   42(2,R4),=X'4800'        RESET INFILE MACRF TO GL    MVS
**                                                                  VBS
**       GET   (R4)                     REREAD FIRST BLOCK
**                                                                  VBS
*        L     R0,INREC2          R0 -> TO INPUT RECORD AREA        VBS
*        GET   (R4),(0)            READ RECORD                      VBS
*        L     R1,INREC2          R1 -> TO INPUT RECORD             VBS
**                                                                  VBS
*        MVC   DCBRECAD,DCBEOBAD        RELEASE FIRST BUFFER
*        MVC   DCBBLKCT,DSAVE           SET DCB BLOCK COUNT TO 0
*        MVC   42(2,R4),=X'D008'        INFILE MACRF = E            MVS
*        LA    R1,DIOBLK1               STORE IOBLK1 ADDRESS IN     MVS
*        ST    R1,28(R4)                INFILE DCB                  MVS
*
*              END OF COMMENTED   INSTRUCTIONS
*
*        CLC   DCBBLKCT,=F'1'     ONLY 1 BLOCK IN FILE ?            RH
*        BH    DUALDEN            YES, DON'T FORWARD SPACE          RH
         MVI   DJMPMARK,X'37'     CHANGE CCW TO FORWARD SPACE BLK   RH
*        SR    R11,R11            CLEAR R11                         RH
*        IC    R11,DCBBUFNO       R11 = NBR OF BUFFERS              RH
         ICM   R11,15,DCBBLKCT    R11 = BLKS ALREADY READ           RH
         BZ    DUALDEN            NONE, DON'T FORWARD SPACE
         SPACE 1                                                    RH
SKIPBLK  DS    0H                                                   RH
         BAL   R6,DEXCPROG        FORWARD SPACE 1 BLOCK             RH
         BCT   R11,SKIPBLK        FOR EACH BUFFER                   RH
*
*
*   IF THE TAPE IS ON A DUAL DENSITY DRIVE DETERMINE WHETHER IT
*   IS 800, 1600 OR 6250 BPI FROM THE UCB AND SENSE BYTES
*
DUALDEN  DS    0H
*                                 ASSUME ITS A CARTRIDGE
*
         MVI   JFCDEN,X'FF'       DEFAULT TO CART
         CLC   DEVDATA+2(2),=X'8080'    3480 SERIES TAPE? (CARTRIDGE)
         BE    DENDEXCP           YES, END FOR NOW
*
         CLC   DEVDATA+2(2),=X'8001'    2400 SERIES TAPE?
         BE    DUALDEN2                 YES, GO TO TAPE PROCESSING
         CLC   DEVDATA+2(2),=X'8003'    3400 SERIES TAPE?
         BE    DUALDEN2                 YES, GO TO TAPE PROCESSING
         SPACE 1
DUALDEN2 DS    0H
         TM    DEVDATA+1,X'30'          IF NOT A DUAL DENSITY DRIVE MVS
         BZ    DENDEXCP                 BYPASS THIS BLOCK OF CODE
         LA    R6,READCCW
         ST    R6,CHANADD1
         BAL   R6,DEXCPROG
*
         TM    SNSBYTES+3,X'04'         1600 BPI?
         BNO   DSETDEN                  NO, SEE IF 800 OR 6250      MVS
         MVI   JFCDEN,X'C3'             YES, SET JFCBDEN FOR 1600 BPI
         B     DENDEXCP
DSETDEN  DS    0H
         TM    DEVDATA+1,X'10'          DUAL DEN = 6250/1600 ?      MVS
         BZ    DSET800                  NO, IT'S 800/1600           MVS
         MVI   JFCDEN,X'D3'             YES, SET JFCBDEN FOR 6250BPIMVS
         B     DENDEXCP                 GO RESET MACRF
DSET800  MVI   JFCDEN,X'83'             SET JFCBDEN FOR 800 BPI
DENDEXCP DS    0H
*                                                                   VBS
*              UNTIL I WRITE A SPECIAL SECTION FOR VBS,             VBS
*              WE MUST BE CONTENT WITH A GET MOVE.                  VBS
*                                                                   VBS
*        MVC   DCBMACRF(2),=X'4800'     RESET INFILE MACRF TO GL
         MVC   DCBMACRF(2),=X'5000'     RESET INFILE MACRF TO GM    VBS
         ST    R10,DCBIOBAD             RESET A(IOB) FOR GET        MVS
*
         TM    DEVDATA+1,X'80'          7 TRACK DRIVE ?
         BZ    *+4+4                    NO, IT'S 9 TRACK
         OI    JFCDEN,X'08'             SAY 7 TRK IN JFCDEN
         B     DRETZERO           END OF DCBSETUP
         SPACE 1
*
*              OPEN VSAM FILE
*
         SPACE 1
ACBOPEN  DS    0H
         OPEN  INACB              OPEN VSAM DATASET
         LTR   R15,R15            DID DATASET OPEN OKAY ?           RH
         BZ    CATOPEN            YES                               RH
         SPACE 1                                                    RH
         CVD   R15,DUB            CONVERT RC TO DECIMAL             RH
         ED    OPENRC,DUB+6       CONVERT TO DISPLAY                RH
         SHOWCB ACB=INACB,AREA=ACBDATA,LENGTH=4,OBJECT=DATA,        RH +
               FIELDS=(ERROR)                                       RH
         L     R15,ACBERROR       R15 = ERROR CODE                  RH
         CVD   R15,DUB            CONVERT RC TO DECIMAL             RH
         ED    ERRCODE,DUB+6      CONVERT TO DISPLAY                RH
         CH    R15,=H'4'          DATASET ALREADY OPEN ON THIS SYS? RH
         BE    CATWAIT            YES, WAIT A BIT AND TRY AGAIN     RH
         CH    R15,=H'116'        DATASET OPEN ON ANOTHER SYSTEM ?  RH
         BE    CATWAIT            YES, WAIT HERE ALSO               RH
         SPACE 1                                                    RH
         SHOWCB ACB=INACB,AREA=ACBDATA,LENGTH=4,OBJECT=INDEX,       RH +
               FIELDS=(ERROR)                                       RH
         L     R15,ACBERROR       R15 = ERROR CODE                  RH
         CVD   R15,DUB            CONVERT RC TO DECIMAL             RH
         ED    ERRCODE2,DUB+6     CONVERT TO DISPLAY                RH
         SPACE 1
PASSOK   DS    0H                                                   RH
*        MVC   ERRCATN,GENM+25    PUT DATASET NAME IN MSG           RH
*        MVC   LINE1+1(LOPENERR),OPENERRM MOVE MSG TO PRINT LINE    RH
*        BAL   R8,PRINT1          PRINT IT                          RH
         LA    R1,3               SET ABEND CODE                    RH
         ABEND (1)                ABEND, RC=3                       RH
         SPACE 1
CATWAIT  DS    0H                                                   RH
         ICM   R15,1,WLOOP        HAVE WE WAITED 5 TIMES ALREADY ?  RH
         BZ    PASSOK             YES, ABEND                        RH
         BCTR  R15,0              - 1 FOR THIS TIME                 RH
         STCM  R15,1,WLOOP        SAVE NEW WAIT LOOPER              RH
         STIMER WAIT,BINTVL=WAITTIME   WAIT A BIT                   RH
         B     ACBOPEN            GO TRY OPEN AGAIN                 RH
WLOOP    DC    X'0A'              WAIT LOOP COUNTER                 RH
WAITTIME DC    F'100'             WAIT FOR 1 SECOND                 RH
         SPACE 1
CATOPEN  DS    0H                                                   RH
DRETZERO EQU   *
         L     R13,DSAVE+4
         RETURN (14,12),RC=0
         SPACE
*   THE FOLLOWING ROUTINE EXECUTES THE CHANNEL PROGRAMS AND RETURNS
*   TO THE ADDRESS IN R6
*
DEXCPROG DS    0H
         LA    R7,5                     SET MAX. RETRY COUNT
DEXCPTRY DS    0H
         XC    ECB,ECB            RESET ECB
         EXCP  DIOBLK1                  READ/SPACE
         WAIT  1,ECB=ECB                WAIT FOR ACTION TO FINISH
         CLI   ECB,X'44'                IF READ WAS NOT INTERCEPTED
         BNE   DEXCPOK                  GO TO TEST ITS SUCCESS
         BCT   R7,DEXCPTRY              TRY AGAIN UP TO 5 TIMES
DEXCPOK  CLI   ECB,X'7F'          WAS READ OK
         BER   R6                 READ WAS OKAY
         ABEND 88,DUMP   ERROR IN OPEN OR CHANNEL PROGRAM - RERUN JOB
         CLI   ECB,X'41'          DO CSW CONTENTS APPLY
         BNE   PRMRDERR           NO, ASSUME PERMANENT READ ERROR
         TM    DIOBLK1+12,X'01'   UNIT EXCEPTION BIT ON IN CSW
         BNO   PRMRDERR           NO, SO TM NOT READ
         SPACE 1                                                    RH
         L         R1,DCBDEBAD         A(MASTER DEB)
         LA        R1,16(,R1)          A(USER PURGE LIST)
         NI        DCBIFLGS,X'3F'      TURN OFF ERROR FLAGS
         SVC   17                      RESTORE RQE TO CHAIN
*        RESTORE   (1)                 RESTORE RQE TO CHAIN
         LA    R15,4              SAY BACKSPACE ANOTHER TAPEMARK
         BR    R6                 GO BACK WHERE WE CAME FROM
         SPACE 1
PRMRDERR DS    0H
         MVC   DCAMWORK(27),=C'-ERROR DOING EXCP TO SYSUT1'
         SPACE 1
DABEND   DS    0H
         PUT   ((3)),DCAMWORK     SAY ERROR
         L     R13,DSAVE+4        R13 -> TO PREV SAVE AREA
         RETURN (14,12),RC=8      RETURN
*        BNE   DABEND                   GO TO ABEND
*        BR    R6                       RETURN
*ABEND   ABEND 1111,DUMP ERROR IN OPEN OR CHANNEL PROGRAM - RERUN JOB
         SPACE 3
*   THE FOLLOWING ROUTINE OPENS THE QSAM INPUT FILE, AND REREADS
*   THE JFCB
*
DQSAMOPN EQU   *
         OPEN  ((4))                    OPEN INPUT FILE
         TM    DCBOFLGS,X'10'           WAS OPEN SUCCESSFUL?
         BZ    SYS1ERR                  NO, ABEND
         RDJFCB ((4))                   REREAD JFCB
         BR    R11                      GO BACK TO CALLING INSTRUCTION
         SPACE 1
SYS1ERR  DS    0H
         MVC   DCAMWORK(22),=C'-SYSUT1 FAILED TO OPEN'
         B     DABEND             GO PRINT ERROR MSG
         EJECT
*   WORKAREAS, CONSTANTS, ETC
*
DSAVE    DC    9D'0'                    REGISTER SAVE AREA          MVS
DEVDATA  DC    D'0'                     WORK AREA FOR DEVTYPE MACRO
HDBLWORD DC    D'0'               WORK AREA FOR JFCB FIELDS
INREC2   DC    F'0'               INPUT RECORD WORK AREA            VBS
DCBDDNM  DC    CL8'SYSUT1'
DSTRDATA EQU   *+41                     **
HDR1     EQU   *                        *
HDR2     EQU   *                        *
SNSBYTES DS    CL6                      *** STORAGE AREA FOR CCW COMAND
         DS    CL74                     **
         SPACE 1                                                    RH
         ORG   SNSBYTES           BACK UP TO HDR1 AREA              RH
HDR1REC  DS    0CL80                                                RH
EOF1REC  DS    0C                                                   RH
HDR1ID   DS    CL4'HDR1'                                            RH
HDR1DSN  DS    CL17' '                                              RH
HDR1VOL  DS    CL6'000001'                                          RH
HDR1VSEQ DS    CL4'0001'                                            RH
HDR1SEQN DS    CL4'0001'                                            RH
         DS    CL4' '                                               RH
         DS    CL2' '                                               RH
HDR1CRED DS    CL6' '             CREATION DATE (JFCB)              RH
HDR1EXPD DS    CL6' '                                               RH
HDR1SEC  DS    C'0'                                                 RH
EOF1BLKC DS    CL6'000000'        ZERO FOR HDR1                     RH
         DS    CL13'TAPECOPY HALL'                                  RH
         DS    CL7'DIVDEV'                                          RH
         SPACE 1                                                    RH
         ORG   SNSBYTES           BACK UP TO HDR2 AREA              RH
HDR2REC  DS    0CL80                                                RH
EOF2REC  DS    0C                                                   RH
HDR2ID   DS    CL4'HDR2'                                            RH
HDR2RCFM DS    C'F'                                                 RH
HDR2BLKS DS    CL5'04000'                                           RH
HDR2RECL DS    CL5'00080'                                           RH
HDR2DENS DS    C'4'                                                 RH
HDR2POSN DS    CL1'0'                                               RH
         DS    CL17'TAPECOPY JOB'                                   RH
HDR2TRT  DS    CL2' '                                               RH
HDR2CTLC DS    CL1' '                                               RH
         DS    C' '                                                 RH
HDR2ATTR DS    C'B'                                                 RH
         DS    CL8' '                                               RH
         DS    CL1' '                                               RH
         DS    CL15'DIVDEV'                                         RH
         ORG   ,                  BACK TO NEXT BYTE                 RH
         DS    0H                       ALIGN DATE STORAGE
ENDHDR1  EQU   *-1                      *
DENDDATA EQU   *-1                      *
HDR22    EQU   *                        *
ENDHDR2  EQU   *+79                     *
DCAMWORK DC    CL148' '                 WORK AREA FOR OBTAIN MACRO
         DS    0H                       ALIGN DATE STORAGE
DSTORDAT DC    CL6' '                   AREA TO STORE DATE DURING OPEN
*
*   THE ADDRESSES OF DCAMDSN & DCAMVOL CANNOT BE RESOLVED UNTIL
*   EXECUTION TIME, SO THE 'CAMLST' MACRO IS OVERLAID
*
DSCBCAM  CAMLST SEARCH,*,*,DCAMWORK
         ORG   DSCBCAM
CAMOVLY  DS    0F                      ALIGN ON FULL WORD
         DS    AL1(193)                THREE FLAG BYTES
         DS    AL1(0)                   INDICATING THE FUNCTION
         DS    AL1(0)                    TO BE PERFORMED
         DS    AL1(0)                  NO OPTION THREE
DCAMLSTA DS    F'0'         A(DCAMDSN) - PARAMETER TWO
         DS    F'0'         A(DCAMVOL) - PARAMETER THREE
         DS    A(DCAMWORK)               - PARAMETER FOUR
*
DJMPMARK CCW   47,0,32,1                BACKSPACE 1 TAPE MARK, FIRST
*                                       BYTE WILL BE SET TO 63 LATER
*                                       TO FORWARD SPACE (RESTORE)
*
*        READ BACKWARDS FOR TAPE LABELS UP TO CREATION DATE ON HDR1
*
DRDBACK  CCW   12,ENDHDR1,32,80   READ HDR
*RDBACK  CCW   12,ENDHDR2,96,80          HDR2
*        CCW   12,ENDHDR1,32,80          HDR1
*RDBACK  CCW   12,DENDDATA,112,80        HDR1
*        CCW   12,DENDDATA,32,80         HDR2
READCCW  CCW   4,SNSBYTES,32,6    READ 1ST 6 SENSE BYTES
DIOBLK1  DS    0FL8
         DC    X'03'              WAS 02
         DC    XL3'00'
ECBADD1  DC    A(ECB)
FLAG31   DC    X'00'
CSW1     DC    XL7'00'
CHANADD1 DC    A(READCCW)
DCBADD1  DC    A(0)                     INFILE
         DC    F'0'
BLKINC   DC    H'0'
         DC    H'0'
ECB      DC    F'0'
TMCTR    DC    F'0'               CTR FOR TMS UP OR BACK
         LTORG
         DROP  R5,R12                 DROP BASE REGS
         TITLE 'ERROR ANALYSIS ROUTINE - SYNAD'
***********************************************************************
*        S Y N A D   E R R O R   A N A L Y S I S   R O U T I N E      *
***********************************************************************
         SPACE 2
ERRANAL  CSECT
         SAVE  (14,12),T,*
         USING ERRANAL,R12
         LR    R12,R15
         L     R4,0(0,R1)               SAVE REPORT DCB ADDRESS
         L     R5,8(0,R1)               R5 -> TO HEADING LINE       RH1
         L     R1,4(0,R1)               LOAD ADDRESS SYNSAVE
         LM    R0,R1,8(R1)              RELOAD REGISTERS 0 AND 1
         ST    R13,ESAVE+4              CHAIN SAVE AREAS
         LR    R11,R13
         LA    R13,ESAVE
         ST    R13,8(R11)
         SYNADAF ACSMETH=QSAM
*                                   R2 - COUNT REG FROM MAINLINE
*                                   R4 - REPORT DCB ADDRESS
*                                   R5 - ADDR OF HDG IN MAINLINE    RH1
         LR    R7,R1                 R7 - ADDR OF SYNADAF MESSAGE
         SPACE 2
         AP    134(2,R5),=P'1'          ADD 1 TO PAGE NUMBER
         MVC   113(4,R5),=X'402020202120'  SET EDIT PATTERN
         ED    113(4,R5),134(R5)             EDIT PAGE NUMBER
         PUT   (4),(5)                       PRINT TITLE
         SPACE 2
         BAL   R11,EPUT                 PRINT ROW OF ASTERISKS
         SPACE 2
         BAL   R11,EPUT                 PRINT SPACE LINE
         SPACE 2
         MVC   EHEAD+20(72),=C'AN ERROR HAS OCCURED IN READING A BLOCK X
               OF DATA STARTING WITH RECORD NO.'
         LA    R2,1(R2)                 COUNT OF CURRENT RECORD
         CVD   R2,ESYNWORK
         MVC   EHEAD+92(10),=X'40206B2020206B202120'  MOVE PRINT PATRN
         ED    EHEAD+92(10),ESYNWORK+4  MOVE COUNT TO PRINT
         BAL   R11,EPUT                 PRINT LINE
         SPACE 2
         MVC   EHEAD+20(79),=C'THE DATA MAY STILL BE PRINTED, DEPENDINGX
                ON THE START=/STOP= REQUEST, FROM UNIT'
         MVC   EHEAD+100(3),68(R7)           MOVE IN UNIT NUMBER
         BAL   R11,EPUT                 PRINT LINE
         SPACE 2
         CLC   72(2,R7),=C'DA'          TEST FOR DIRECT ACCESS FILE
         BE    EDISKFIL                 IF YES, GO TO DIR.ACCESS MESSAG
         CLC   72(2,R7),=C'TA'          TEST FOR TAPE FILE
         BE    ETAPEFIL                 IF YES, GO TO TAPE MESSAGE
         SPACE 2
ENDSYNAD EQU   *
         BAL   R11,EPUT                 PRINT SPACER LINE
         MVC   EHEAD+2(130),EHEAD+1     PROPAGATE ASTERISKS
         BAL   R11,EPUT                 PRINT ROW OF ASTERISKS
         MVC   EHEAD+2(130),EHEAD+1     RESET HEADING
         MVI   EHEAD,C'-'                FOR NEXT CALL
         SYNADRLS
         L     R13,ESAVE+4              SET REG 13
         RETURN (14,12)                 RETURN
         SPACE 2
EDISKFIL EQU   *
         MVC   EHEAD+20(54),=C'ACTUAL TRACK ADDRESS AND BLOCK NUMBER BBX
               CCHHR IN HEX ='
         MVC   EHEAD+75(14),107(R7)     MOVE IN FIELD
         BAL   R11,EPUT                 PRINT MESSAGE
         B     ENDSYNAD
         SPACE 2
ETAPEFIL EQU   *
         MVC   EHEAD+20(33),=C'THE ERROR OCCURED IN BLOCK NUMBER'
         MVC   EHEAD+54(7),107(R7)      MOVE IN BLOCK NUMBER
         BAL   R11,EPUT                 PRINT MESSAGE
         B     ENDSYNAD
         SPACE 2
EPUT     EQU   *
         PUT   (4),EHEAD
         MVC   EHEAD(3),=C' * '         SET CARR CTL TO SINGLE SPACE
         MVC   EHEAD+3(129),EHEAD+2      AND CLEAR EXTRA CHARACTERS
         BR    R11
         SPACE 2
EHEAD    DS    0CL133
ECTLCHR  DC    C'-'
EMESS    DC    132C'*'
ESAVE    DC    18F'0'
ESYNWORK DC    D'0'
         LTORG                          LITERAL POOL
         TITLE 'PARAMETER EDIT ROUTINE'
***********************************************************************
*                                                                     *
*                                                                     *
* TITLE      PARAMETER ANALYSIS PROGRAM                               *
*                                                                     *
* LANGUAGE   ASSEMBLER F                                              *
*                                                                     *
* FUNCTION   TO ANALYZE THE PARAMETERS USED IN THE EXECUTION OF       *
*              THE HEXADECIMAL DUMP PROGRAM CALLED 'FILEDUMP'         *
*              VALID PARAMETERS ARE :                                 *
*              COPY,  - COPY FROM SYSUT1 TO SYSUT2                  RH1
*              BOTH,  - COPY FROM SYSUT1 TO SYSUT2 AND PRINT        RH1
*              HEX,   - PRINT OFFSETS IN HEX                        RH1
*               SKIP=, - NUMBER OF RECORDS TO BE SKIPPED              *
*              START=, - STARTING RECORD NUMBER                       *
*               STOP=, - STOPPING AT RECORD NUMBER                    *
*              TITLE=  - UP TO 84 CHARACTERS OF TITLE                 *
*            TO BUILD A TABLE OF THE SKIP START STOP PARAMETERS.      *
*              AS MANY PARAMETERS CAN BE CODED AS WILL FIT IN         *
*              THE MAXIMUM PARAMETER LENGTH OF 100 BYTES.             *
*            TO SEQUENCE CHECK THE START-STOP, STOP-START PARAMETERS  *
*              TO ENSURE THAT THE NEXT START OR STOP IS GREATER       *
*              THAN THE PRECEDING START OR STOP.                      *
*                                                                     *
* SYSTEM     IBM 3081/SP OPERATING SYSTEM,                            *
*            RELEASE 1.3.                                             *
*                                                                     *
* OPERATION  SAVE REGISTERS AND CHAIN REGISTER 13.                    *
*            SET UP PASSED PARAMETERS AND ADDRESSES.                  *
*            PROCESS SKIP PARAMETER.                                  *
*            PROCESS START PARAMETER.                                 *
*            PROCESS STOP PARAMETER.                                  *
*            PROCESS TITLE PARAMETER.                                 *
*            IF ANY OF THE PARAMETERS ARE INCORRECT ONE OF THE        *
*              FOLLOWING MESSAGES IS PRINTED - XX REPRESENTS          *
*              THE PARAMETER NUMBER IN ERROR:                         *
*             -PARAMETER XX IS INVALID OR IS MISSPELLED               *
*             -START PARAMETER MUST PRECEDE STOP PARAMETER XX         *
*             -PARAMETER XX CONTAINS AN INVALID CHARACTER OR          *
*              IS NOT DELIMITED WITH A COMMA.                         *
*            THE PARAMETER VALUES ARE CHECKED TO ENSURE THAT          *
*              THEY CONTAIN NUMERICS.                                 *
*            SEQUENCE CHECK THE START-STOP PARAMETERS. IF AN          *
*              ERROR OCCURS, THE FOLLOWING MESSAGE IS PRINTED:        *
*             -START PARAMETER XX MUST BE GREATER THAN PRECEDING      *
*              STOP PARAMETER.                                        *
*            RESTORE REGISTERS AND RETURN TO CALLING PROGRAM          *
*                                                                     *
* ENTRY PT   AT LABEL 'PARMCHEK' FROM 'FILEDUMP' CSECT.               *
*                                                                     *
* INPUT      THE PARAMETER PASSED TO 'FILEDUMP' BY THE SYSTEM.        *
*                                                                     *
* OUTPUT     ONE OF THE ABOVE MESSAGES IS MOVED TO THE TITLE AREA.    *
*                                                                     *
*                                                                     *
* EXIT       VIA A BRANCH TO REGISTER 14 TO RETURN TO 'FILEDUMP'.     *
*                                                                     *
* REGISTERS  0 - R0                                                   *
*            1 - R1      PARAMETER ADDRESS                            *
*            2 - R2      BASE REGISTER                                *
*            3 - R3                                                   *
*            4 - R4      SKIP ADDRESS IN PARM TABLE                   *
*            5 - R5      PARAMETER COUNTER                            *
*            6 - R6      START ADDRESS IN PARM TABLE                  *
*            7 - R7      STOP ADDRESS IN PARM TABLE                   *
*            8 - R8      BINARY EQUIVALENT OF PARAMETER               *
*            9 - R9      PARAMETER BYTES COUNTER                      *
*            10- R10     USED TO CALCULATE AND EDIT PARAMETERS        *
*            11- R11     USED TO CALCULATE AND EDIT PARAMETERS        *
*            12- R12                                                  *
*            13- R13     CONTAINS SAVE AREA ADDRESS                   *
*            14- R14     RETURN ADDRESSES                             *
*            15- R15     ENTRY POINT AND WORK REGISTER                *
*                                                                     *
*            IF ANY ERRORS OCCUR AND ONE OF THE ERROR ROUTINES        *
*            IS EXECUTED THE ABOVE REGISTER USAGE IS NOT              *
*            NECESSARILY CORRECT.                                     *
*                                                                     *
* AUTHOR     RANDY HALL                                               *
*            DIVERSIFIED DEVELOPMENTS,                                *
*            1151 PALMER ROAD,                                        *
*            VICTORIA, B.C., V8P-2H5                                  *
*                                                                     *
* DATE       APRIL, 1972                                              *
*                                                                     *
*                                                                     *
***********************************************************************
PARMCHEK CSECT                          NAME CSECT
         SAVE  (14,12),T,*              SAVE CONTENTS OF REGISTERS
         USING PARMCHEK,R2              ESTABLISH ADDRESSABILITY
         LR    R2,R15                   LOAD BASE REGISTER 1
         L     R4,8(R1,R0)              STORE PARAMETER TABLE ADDRESS
         ST    R4,TABAD                 IN REGISTER 4 (SKIP ADD)
         MVC   HEADADD,4(R1)            STORE HEADING ADDRESS
         L     R5,0(0,R1)               R5 -> TO FPARMS             RH1
         LA    R5,12(0,R5)              R5 -> TO FFLAGS             RH1
         ST    R5,FLAGADDR              SAVE A(FLAGS)               RH1
         L     R1,0(R0,R1)              LOAD PARAMETER LENGTH ADDRESS
         L     R1,0(R0,R1)              INTO REGISTER 1
         MVC   PARMLGTH,0(R1)           STORE PARAMETER LENGTH
         LA    R1,2(R0,R1)              LOAD PARAMETER ADDRESS INTO    +
                                        REGISTER 1
         SR    R5,R5                    CLEAR WORK REGISTER 5
         SR    R9,R9                    CLEAR WORK REGISTER 9
         LA    R6,4(R4,R0)              LOAD START ADDRESS
         LA    R7,4(R6,R0)              LOAD STOP ADDRESS
***********************************************************************
*        C O D E   T O   P R O C E S S   S K I P   P A R A M E T E R  *
***********************************************************************
         SPACE 1
A10SKIP  CLC   0(5,R1),SKIP             IS PARAMETER = SKIP ?
         BNE   B10START                 NO, CHECK FOR START PARAMETER
         SPACE 1
         LA    R5,1(R5,R0)              ADD 1 TO PARAMETER COUNTER
         LA    R9,5(R9,R0)              ADD 5 TO BYTES COUNTER
         LA    R1,5(R1,R0)              ADD 5 TO PARAMETER ADDRESS
         BAL   R14,F10EDIT              EDIT SKIP PARAMETER
         SPACE 1
         LTR   R8,R8              IS THERE A VALUE?
         BZ    A80                NO
* WHY ?? LA    R8,1(R8)                 ADD 1 TO SKIP PARAMETER
         ST    R8,0(R4,R0)              STORE SKIP PARM IN TABLE
         AP    SKIPCTR,ONE              ADD 1 TO SKIP COUNTER
A70      LA    R4,12(R0,R4)             LOAD ADD NEXT SKIP PARM IN TABL
A80      CH    R9,PARMLGTH              BYTES PROCESSED = PARM LENGTH ?
         BL    A10SKIP                  NO, CONTINUE EDIT
         LTR   R5,R5                    IS TITLE ONLY PARAMETER ?
         BZ    A90END                   YES, RETURN TO CALLER
         SPACE
         BAL   R14,K10CHEK              CHECK PARAMETER SEQUENCE
         SPACE
A90END   EQU   *
         LM    R14,R12,12(R13)          LOAD CALLING REGISTER CONTENTS
         LA    R15,0                    CLEAR REGISTER 15
         BR    R14                      RETURN
         SPACE 2
***********************************************************************
*   C O D E   T O   P R O C E S S   S T A R T   P A R A M E T E R     *
***********************************************************************
         SPACE
B10START CLC   0(6,R1),START            PARAMETER = START ?
         BNE   C10STOP                  NO, CHECK FOR STOP
         SPACE
         LA    R5,1(R0,R5)              ADD 1 TO PARAMETER COUNTER
         LA    R9,6(R0,R9)              ADD 6 TO BYTES COUNTER
         LA    R1,6(R0,R1)              ADD 6 TO PARAMETER ADDRESS
         BAL   R14,F10EDIT              EDIT START PARAMETER
         SPACE
         ST    R8,0(R0,R6)              STORE START PARM IN TABLE
         AP    STARTCTR,ONE             ADD 1 TO START COUNTER
         LA    R6,12(R0,R6)             POINT TO NEXT START ENTRY
         SPACE
         B     A80                      TEST FOR END OF PARAMETER LIST
         SPACE 2
***********************************************************************
*   C O D E   T O   P R O C E S S   S T O P   P A R A M E T E R       *
***********************************************************************
         SPACE
C10STOP  CLC   0(5,R1),STOP             PARAMETER = STOP ?
         BNE   D10TITLE                 NO, CHECK FOR TITLE
         SPACE
         LA    R5,1(R0,R5)              ADD 1 TO PARAMETER COUNTER
         LA    R9,5(R0,R9)              ADD 5 TO BYTES COUNTER
         LA    R1,5(R0,R1)              ADD 5 TO PARAMETER ADDRESS
         CLC   0(3,R1),EOF              STOP = EOF ?
         BNE   C10STPED
         LA    R9,3(R0,R9)               COUNTERS
         LA    R1,3(R0,R1)                FOR EOF PARAM
         MVC   0(4,R7),12(R7)           SET STOP FIELD TO EOF
         CLI   0(R1),C','               COMMA FOLLOWS?
         BNE   C10STPCT                 IF NO, CHECK COUNTS
         LA    R9,1(R0,R9)              INCREMENT POINTERS
         LA    R1,1(R0,R1)               PAST COMMA
         B     C10STPCT
C10STPED EQU   *
         BAL   R14,F10EDIT              EDIT STOP PARAMETER
         SPACE
         ST    R8,0(R0,R7)              STORE STOP PARM IN TABLE
         AP    STOPCTR,ONE              ADD 1 TO STOP COUNTER
         LA    R7,12(R0,R7)             POINT TO NEXT STOP ENTRY
         SPACE
C10STPCT EQU   *
         CP    STOPCTR,STARTCTR         MORE STOPS THAN STARTS ?
         BNH   C40                      NO, CHECK SKIPS
         SPACE
         CP    STARTCTR,ZERO            START COUNTER = ZERO ?
         BNE   H10ERR2                  NO, GO TO ERROR 2 ROUTINE
         SPACE
         AP    STARTCTR,ONE             SET START COUNTER TO 1
         LA    R6,12(R6)                POINT TO NEXT START PARAMETER
         SPACE
C40      CP    STOPCTR,SKIPCTR          MORE STOPS THAN SKIPS ?
         BNH   A80                      NO, GO TO A80
         SPACE
         CP    SKIPCTR,ZERO             SKIP COUNTER = ZERO ?
         BE    A70                      YES, GO TO A70
         SPACE
         LA    R8,12                    LOAD LENGTH OF TABLE ENTRY IN 8
         LR    R15,R4                   LOAD CURRENT SKIP ADD IN REG 15
         SR    R15,R8                   SUBTRACT ENTRY LENGTH FROM R15
         L     R15,0(R0,R15)            LOAD LAST SKIP PARM INTO REG 15
         ST    R15,0(R0,R4)             STORE LAST SKIP PARM IN        +
                                        PRESENT SKIP PARM
         ZAP   SKIPCTR,STOPCTR          UPDATE SKIP COUNTER
         B     A70                      GO TO A70
         SPACE 2
***********************************************************************
*        C H E C K   F O R   T I T L E   P A R A M E T E R            *
***********************************************************************
         SPACE
D10TITLE CLC   0(6,R1),TITLE            PARAMETER = TITLE ?
         BNE   L10HEX                   NO, CHECK FOR HEX           RH1
         SPACE 1
D20      L     R8,HEADADD               LOAD HEADING ADDRESS INTO REG 8
         MVI   0(R8),X'40'              CLEAR HEADING AREA          RH1
         MVC   1(95,R8),0(R8)           *                           RH1
         LA    R9,7(R0,R9)              ADD 7 TO BYTES COUNTER
         LA    R1,6(R0,R1)              ADD 6 TO PARAMETER ADDRESS
         LH    R6,PARMLGTH              LOAD PARM LENGTH INTO REG 6
         SR    R6,R9                    SUBTRACT BYTES COUNTER FROM    +
                                        PARAMETER LENGTH TO GET LENGTH +
                                        OF TITLE
         LA    R15,90                   LOAD  90 INTO REG 15
         SR    R15,R6                   SUBTRACT TITLE LENGTH
         SRL   R15,1                    DIVIDE BY 2
         AR    R8,R15                   CENTRE HEADING
         EX    R6,D50                   MOVE TITLE TO HEADING
         LA    R9,101                   SET BYTES CTR OVER PARM MAX
         B     C40                      GO TO C40
         SPACE
D50      MVC   0(0,R8),0(R1)            MOVE TITLE TO HEADING
***********************************************************************
*        E D I T    P A R A M E T E R    F O R    N U M E R I C S     *
***********************************************************************
         SPACE 1
F10EDIT  LR    R10,R1                   STORE START ADDRESS OF PARM
F20      CLI   0(R1),X'F0'              DIGIT LESS THAN 0 ?
         BL    F30                      YES, CHECK FOR COMMA
         SPACE 1
         CLI   0(R1),X'F9'              DIGIT GREATER THAN 9 ?
         BH    J10ERR3                  YES, GO TO ERROR 3 ROUTINE
         SPACE 1
         LA    R1,1(R1,R0)              ADD 1 TO PARM ADDRESS
         LA    R9,1(0,R9)               ADD 1 TO PARM LTH
         CH    R9,PARMLGTH              AT END OF PARM ?
         BL    F20                  NO, BRANCH TO CHECK NEXT DIGIT
*                                       YES, SET UP VALUE
         SPACE 1
         SPACE
F30      LR    R11,R1                   CALCULATE NUMBER OF DIGITS
         SR    R11,R10                  IN PARAMETER
         LA    R9,1(0,R9)               ADD 1 TO DIGIT LENGTH          +
                                        TO COMPENSATE FOR COMMA
         CLI   0(R1),C','               IS DIGIT A COMMA
         BE    F40                      YES, CONTINUE
         SPACE
         CH    R9,PARMLGTH              BYTES PROCESSED = PARM LENGTH ?
         BL    J10ERR3                  NO, GO TO ERROR 3 ROUTINE
         SPACE
F40      BCTR  R11,0                    SUBTRACT 1 FROM DIGIT LENGTH
         LA    R1,1(R1)                 ADD 1 TO PARAMETER ADDRESS
         EX    R11,F70                  PACK PARM IN DOUBLE WORD.
         CVB   R8,DWORD                 CONVERT TO BINARY
         BR    R14                      RETURN
         SPACE
F70      PACK  DWORD,0(0,R10)           PACK DIGITS TO DWORD
         SPACE 2
***********************************************************************
*        E R R O R 1   R O U T I N E                                  *
***********************************************************************
         SPACE
G10ERR1  LA    R5,1(R0,R5)              ADD 1 TO PARAMETER COUNTER
         LA    R15,G25                  LOAD ADDRESS G25 IN REG 15
G20      L     R6,HEADADD               LOAD HEADING ADDRESS
         MVI   0(R6),X'40'              CLEAR HEADING AREA          RH1
         MVC   1(089,R6),0(R6)          CLEAR HEADING AREA          RH1
         CVD   R5,DWORD                 CONVERT PARM TO DECIMAL
         BR    R15                      GO TO NEXT INSTRUCTION
G25      MVC   PARM1,EDWD1              EDIT PARAMETER NUMBER
         ED    PARM1,DWORD+6            *
         MVC   0(L'MESS1,R6),MESS1      MOVE MESSAGE TO HEADING
         SPACE
G30      EQU   *
         LM    R14,R12,12(R13)          LOAD CALLING REGISTERS
         LA    R15,12                   LOAD RETURN CODE
         BR    R14                      RETURN TO CALLER
         SPACE 2
***********************************************************************
*        E R R O R 2    R O U T I N E                                 *
***********************************************************************
         SPACE
H10ERR2  LA    R15,*+8                  LOAD ADDRESS OF NEXT INST
         B     G20                      GO TO G20
         MVC   PARM2,EDWD1              *
         ED    PARM2,DWORD+6            *
         MVC   0(L'MESS2,R6),MESS2      MOVE MESSAGE TO HEADING
         B     G30                      BRANCH TO G30
         SPACE 2
***********************************************************************
*        E R R O R 3    R O U T I N E                                 *
***********************************************************************
         SPACE
J10ERR3  LA    R15,*+8                  LOAD ADDRESS OF NEXT INST
         B     G20                      GO TO G20
         MVC   PARM3,EDWD1              *
         ED    PARM3,DWORD+6            *
         MVC   0(L'MESS3,R6),MESS3      MOVE MESSAGE TO PRINT
         B     G30                      GO TO G30
         SPACE 2
***********************************************************************
*        C H E C K    S E Q U E N C E    R O U T I N E                *
***********************************************************************
         SPACE
K10CHEK  L     R3,TABAD                 LOAD TABLE ADDRESS IN REG 3
         L     R4,4(R3)                 LOAD START PARM IN REG 4
         L     R5,8(R3)                 LOAD STOP PARM IN REG 5
         LA    R6,1                     CLEAR REGISTER 6 TO 1       RH2
         L     R7,116(R3)               LOAD LAST PARAMETER IN R7
         SPACE
         C     R5,ONEHUND               FIRST STOP PARAMETER = 100 ?
         BNE   K20                      NO, CONTINUE
         SPACE
         CR    R5,R6                    FIRST START = 1 ?           RH2
         BH    K20                      NO, DON'T CHANGE FIRST STOP RH2
         SPACE 1
         AR    R5,R4                    ADD FIRST START TO FIRST STOP
         ST    R5,8(R3)                 STORE REVISED FIRST STOP IN TBL
         SPACE
K20      CR    R5,R7                    STOP PARM = LAST PARM ?
         BCR   8,R14                    YES, RETURN
         SPACE
         LA    R3,12(R3)                POINT TO NEXT SKIP PARM
         CR    R5,R4                    STOP > START ?
         BNL   K40                      YES, CONTINUE
         MVI   M4STRT,C' '              BLANK OUT FIRST BYTE        RH2
         MVC   M4STRT+1(5),STOP         CHANGE MESSAGE TO           RH2
         MVC   M4STOP(6),START          STOP > START                RH2
         LR    R6,R5                    PUT STOP LOCATION IN R6     RH2
         SPACE
K30      CVD   R6,DWORD                 EDIT PARM VALUE
         MVC   PARM4,EDWD1              *
         ED    PARM4,DWORD+4            *                           RH2
         L     R6,HEADADD               LOAD HEADING ADDRESS
         MVI   0(R6),X'40'              CLEAR TITLE AREA            RH1
         MVC   1(089,R6),0(R6)          CLEAR TITLE AREA            RH1
         MVC   0(LMESS4,R6),MESS4       MOVE MESSAGE TO HEADING     RH2
         LM    R14,R12,12(R13)          RESTORE REGISTERS
         LA    R15,12                   SET RETURN CODE
         BR    R14                      RETURN
         SPACE
K40      DS    0H
         MVC   M4STRT(6),START          CHANGE MESSAGE TO           RH2
         MVC   M4STOP(5),STOP           START > STOP                RH2
         MVI   M4STOP+5,C' '            BLANK OUT LAST BYTE         RH2
         L     R4,4(R3)                 LOAD NEXT START PARM IN REG 4
         LR    R6,R4                    PUT START LOCATION IN R6    RH2
         CR    R4,R5                    START > STOP ?
         BNH   K30                      NO, GO TO K30
         SPACE
         L     R5,8(R3)                 LOAD NEXT STOP PARM IN REG 5
         B     K20                      GO TO K20
         SPACE 2
***********************************************************************
*        C O D E   T O    P R O C E S S   H E X   P A R A M E T E R   *
***********************************************************************
         SPACE 1                                                    RH1
L10HEX   DS    0H                                                   RH1
         CLC   0(3,R1),HEX              PARAMETER = HEX ?           RH1
         BNE   M10COPY                  NO, LOOK FOR COPY OR BOTH   RH1
         SPACE 1                                                    RH1
         LA    R5,1(0,R5)               ADD 1 TO PARAMETER COUNTER  RH1
         LA    R9,3(0,R9)               ADD 3 TO BYTES COUNTER      RH1
         LA    R1,3(0,R1)               ADD 3 TO PARAMETER ADDRESS  RH1
         L     R14,FLAGADDR             R14 -> TO FLAGS             RH1
         MVI   0(R14),C'H'              SAY WE WANT HEX OFFSETS     RH1
         SPACE 1                                                    RH1
L20LOOP  DS    0H                                                   RH1
         CLI   0(R1),C','               IS THERE A COMMA HERE       RH1
         BE    L40END                   YES, POINT PAST IT          RH1
         SPACE 1                                                    RH1
L30ADD   DS    0H                                                   RH1
         LA    R9,1(0,R9)               ADD 1 TO BYTES COUNTER      RH1
         LA    R1,1(0,R1)               ADD 1 TO PARAMETER ADDRESS  RH1
         CH    R9,PARMLGTH              ARE WE AT END OF PARMS ?    RH1
         BL    L20LOOP                  NO, LOOK FOR A COMMA        RH1
         SPACE 1                                                    RH1
L40END   DS    0H                                                   RH1
         LA    R9,1(0,R9)               ADD 1 TO BYTES COUNTER      RH1
         LA    R1,1(0,R1)               ADD 1 TO PARAMETER ADDRESS  RH1
         B     A80                      CHECK FOR ANOTHER PARM      RH1
         SPACE 2                                                    RH1
***********************************************************************
*     C O D E   T O    P R O C E S S   C O P Y   P A R A M E T E R S  *
***********************************************************************
         SPACE 1                                                    RH1
M10COPY  DS    0H                                                   RH1
         L     R14,FLAGADDR             R14 -> TO FLAGS             RH1
         CLI   0(R1),C'C'               PARAMETER = C ? (COPY)      RH1
         BNE   M20                      NO, LOOK FOR BOTH           RH1
         SPACE 1                                                    RH1
         MVI   1(R14),C'C'             YES, SAY WE WANT A COPY ONLY RH1
         LA    R5,1(0,R5)               ADD 1 TO PARAMETER COUNTER  RH1
         B     L30ADD                   LOOK FOR END OF PARM        RH1
         SPACE 1                                                    RH1
M20      DS    0H                                                   RH1
         CLI   0(R1),C'B'               PARAMETER = B ? (BOTH)      RH1
         BNE   N10BAD                   NO, SAY BAD PARM            RH1
         SPACE 1                                                    RH1
         LA    R5,1(0,R5)               ADD 1 TO PARAMETER COUNTER  RH1
         MVI   1(R14),C'B'              SAY WE WANT COPY AND PRINT  RH1
         B     L30ADD                   LOOK FOR END OF PARM        RH1
         SPACE 2                                                    RH1
***********************************************************************
*        P R O C E S S   B A D   P A R A M E T E R                    *
***********************************************************************
         SPACE 1                                                    RH1
N10BAD   DS    0H                                                   RH1
         CH    R9,PARMLGTH              ARE WE AT END OF PARMS ?    RH1
         BNL   A80                    YES, CHECK NBR OF SKIPS, ETC. RH1
         SPACE 1                                                    RH1
         B     G10ERR1                  NO, SAY BAD PARAMETER       RH1
         EJECT                          RH1
DWORD    DS    D'0'
HEADADD  DS    F
TABAD    DS    F
FLAGADDR DC    F'0'                                                 RH1
ONEHUND  DC    F'100'
PARMLGTH DC    H'0'
SKIP     DC    CL5'SKIP='
START    DC    CL6'START='
STOP     DC    CL5'STOP='
TITLE    DC    CL6'TITLE='
HEX      DC    CL3'HEX'                                             RH1
ONE      DC    P'1'
SKIPCTR  DC    P'000'
STARTCTR DC    P'000'
STOPCTR  DC    P'000'
ZERO     DC    P'000'
MESS1    DS    0CL41
         DC    C'PARAMETER'
PARM1    DS    CL4
         DC    C' IS INVALID OR IS MISSPELLED'
MESS2    DS    0CL47
         DC    C'START PARAMETER MUST PRECEDE STOP PARAMETER'
PARM2    DS    CL4
MESS3    DS    0CL76
         DC    C'PARAMETER'
PARM3    DS    CL4
         DC    C' CONTAINS AN INVALID CHARACTER OR IS NOT DELIMITED'
         DC    C' WITH A COMMA'
EDWD1    DC    X'4020202020202020'                                  RH2
MESS4    DS    0C                                                   RH2
M4STRT   DC    C'START='                                            RH2
PARM4    DS    CL8                                                  RH2
         DC    C' PARAMETER MUST BE GREATER THAN PRECEDING '        RH2
M4STOP   DC    C'STOP=  PARAMETER'                                  RH2
EOF      DC    C'EOF'
LMESS4   EQU   EOF-MESS4
         SPACE
         SPACE
         LTORG                          LITERAL POOL ORIGIN
         TITLE 'ROUTINE TO PRINT FIRST PART OF REPORT'
*        THIS MODULE WILL ACCESS THE DATE FROM THE MACHINE AND FORMAT
*   IT IN THE HEADING. IT WILL THEN PRINT THE REQUESTS FOUND ON THE
*   PARM.
*
REPTINIT CSECT
         SAVE  (14,12),T,*
         BALR  R12,0                    SET BASE TO R12
         USING *,R12
         ST    R13,RSAVE+4              CHAIN SAVE-AREAS
         LR    R11,R13
         LA    R13,RSAVE
         ST    R13,8(R11)
         SPACE 3
         LM    R2,R5,0(R1)              R2 - REPORT DCB
         USING RHEADING,R3              R3 - HEADING LINE
*                                       R4 - INDEX, START, STOP TABLE
         USING INFMJFCB,R5              R5 - ADDR OF JFCB           RH
*                                       R6 - ADDR OF JFCB DATE
*                                       R7 - WORK REG FOR PRINT-TABLE
         SPACE 2                                                    RH
*                 *****  DATE ROUTINE  *****
         TIME  DEC
         ST    R1,RPWORK                ACCESS DATE
         MVC   RDATE(4),RPWORK          MOVE TO PRINT AREA
         LA    R1,RDATE
         BAL   R15,RDATECON             CONVERT TO READABLE FORMAT
         PUT   (2),(3)                  PRINT HEADING
RTBLPRIN EQU   *
         CLC   4(4,R4),=X'7FFFFFFF'     START AT END OF FILE
         BE    RJFCB
         L     R7,4(R0,R4)              CONVERT START
         CVD   R7,RPWORK                  TO DECIMAL
         MVC   RPSTART,RPATTERN         MOVE START
         ED    RPSTART,RPWORK+4           TO PRINT
         L     R7,0(R0,R4)              CONVERT INDX
         CVD   R7,RPWORK                  TO DECIMAL
         MVC   RPINDEX,RPATTERN         MOVE INDX
         ED    RPINDEX,RPWORK+4           TO PRINT
         CLC   8(4,R4),=X'7FFFFFFF'     STOP AT END OF FILE
         BE    RENDTBL                  IF YES GO TO END
         L     R7,8(R0,R4)              CONVERT STOP
         CVD   R7,RPWORK                  TO DECIMAL
         MVC   RPSTOP,RPATTERN          MOVE STOP
         ED    RPSTOP,RPWORK+4            TO PRINT
         BAL   R11,RPUT                 PRINT DATA-LINE
         LA    R4,12(R0,R4)             MOVE TO NEXT ENTRY IN TABLE
         MVI   RPRINT+1,C' '            CLEAR PART OF
         MVC   RPRINT+2(41),RPRINT+1      PRINT-LINE
         MVC   RPRINT+34(8),=C'RESUMING'
         B     RTBLPRIN
*
RENDTBL  EQU   *
         MVC   RPSTOP(13),=C' END OF FILE.'
         PUT   (2),RPRINT
         SPACE 2
*
**                *** ANALYZE JFCB ***
*
RJFCB    EQU   *
         BAL   R11,CLEAR                CLEAR PRINTLINE             RH1
         BAL   R11,RPUT
         SPACE
         CLI   JFCBVOLS,X'C0'           VALID VOL SER NUMBER ?
         BH    R900                     YES
         L     R13,RSAVE+4              NO BYPASS REST OF HEADING
         RETURN (14,12),RC=4       RETURN WITH RC=4
         SPACE
R900     DS    0H
         MVC   RPRINT+11(8),=C'DSNAME ='     MOVE DSN
         MVC   RPRINT+20(44),JFCBDSNM          TO PRINT
         CLI   JFCBELNM,C' '                 OTHER DSN INFO ?
         BE    R1000
         MVC   RPRINT+70(8),=C'MEMBER ='          MOVE OTHER DSN INFO
         MVC   RPRINT+79(8),JFCBELNM                      TO PRINT.
R1000    CLI   13(R3),X'00'             TITLE CODED IN PARM ?
         BE    R1010
         MVI   15(R3),C' '              CLEAR DEFAULT TITLE         RH1
         MVC   16(69,R3),15(R3)          *                          RH1
         MVC   15(53,R3),RPRINT+11           MOVE DSN TO TITLE
R1010    BAL   R11,RPUT                 PRINT LINE
         BAL   R11,CLEAR                CLEAR PRINT LINE            RH1
         SPACE 3
         MVC   RPRINT+11(9),=C'VOL SER ='    MOVE VOL SERS.
         MVC   RPRINT+21(6),JFCBVOLS
         MVC   RPRINT+28(6),JFCBVOLS+6
         MVC   RPRINT+35(6),JFCBVOLS+12
         MVC   RPRINT+42(6),JFCBVOLS+18
         MVC   RPRINT+49(6),JFCBVOLS+24
         CLI   13(R3),X'00'             TITLE CODED IN PARM ?
         BE    R1020
         MVC   71(37,R3),RPRINT+11           MOVE VOL SER TO TITLE
R1020    EQU   *
         CLI   JFCDSORG+1,X'08'   IS THIS A VSAM FILE?
         BE    RPRLABL            YES, FORGET LABEL TYPE
         SPACE 1
         MVC   RPRINT+70(12),=C'LABEL TYPE ='
         MVC   RPRINT+83(3),=C'SL '  ASSUME SL
         CLI   JFCBLTYP,X'02'     IS IT STANDARD LABEL
         BE    RPRLABL            YES
         MVC   RPRINT+83(3),=C'BLP' ASSUME BLP
         CLI   JFCBLTYP,X'10'     IS IT BYPASS LABEL PROCESSING
         BE    RPRLABL            YES
         MVC   RPRINT+83(3),=C'NSL' NO, NON STANDARD LABEL
RPRLABL  EQU   *
         BAL   R11,RPUT
         BAL   R11,CLEAR                CLEAR PRINT LINE            RH1
         SPACE 2
         CLI   JFCDSORG+1,X'08'   IS THIS A VSAM FILE?
         BE    RPDATE9            YES, FORGET DATES
         SPACE 1
         CLI   JFCBCRDT,X'00'
         BE    RPDATES+4
         MVC   RPRINT+11(15),=C'CREATION DATE ='
         LA    R6,JFCBCRDT              SET POINTER TO CREATION DATE
         LA    R1,RPRINT+26             SET POINTER TO TARGET AREA
         BAL   R15,RDATEFIX             CONVERT DATE TO PRINT FORMAT
         CLI   JFCBXPDT,X'00'           IS EXPIRY DATE AVAILABLE
         BNE   REXPDT
RPDATES  EQU   *
         BAL   R11,RPUT                 PRINT DATE LINE
RPDATE9  EQU   *
         L     R13,RSAVE+4
         RETURN (14,12),RC=0       RETURN, RC=0
RPUT     EQU   *
         PUT   (2),RPRINT               PRINT LINE
         BR    R11                      RETURN
         SPACE 1                                                    RH1
CLEAR    DS    0H                                                   RH1
         MVI   RPRINT+1,C' '            CLEAR PRINT LINE            RH1
         MVC   RPRINT+2(131),RPRINT+1    *                          RH1
         BR    R11                     RETURN TO CALLER             RH1
         SPACE 1                                                    RH1
REXPDT   EQU   *
         MVC   RPRINT+70(13),=C'EXPIRY DATE ='
         LA    R6,JFCBXPDT              SET POINTER TO EXPIRY DATE
         LA    R1,RPRINT+83             SET POINTER TO TARGET
         BAL   R15,RDATEFIX             CONVERT DATE TO PRINT FORMAT
         B     RPDATES
         SPACE 3
*        THIS ROUTINE WILL CONVERT THE 3 BYTE AREA POINTED TO IN R6
*   FROM HEX YDD TO PACKED 00YYDDDS IN THE AREA POINTED TO BY R1.
******** THIS CODE MUST IMMEDIATELY PRECEDE THE LABEL RDATECON. ******
*
RDATEFIX EQU   *
         SR    R7,R7                    CLEAR WORK REG
         IC    R7,0(R6)                 INSERT YEAR
         CVD   R7,RPWORK                CONVERT YEAR TO DEC OYYS
         MVO   0(3,R1),RPWORK+5(3)      MOVE 00YY TO TARGET
         MVC   RPWORK(2),1(R6)          ALIGN DAY
         LH    R7,RPWORK                MOVE TO REG
         CVD   R7,RPWORK                CONVERT TO DEC DDDS
         MVC   2(2,R1),RPWORK+6         MOVE DDDS TO TARGET
         SPACE
*                          *** DATE CONVERSION ROUTINE ***
*
*        ON ENTRY TO THIS ROUTINE REG 1 POINTS TO A 12 BYTE TARGET
*   AREA CONTAINING, IN THE FIRST 4 BYTES PACKED 00YYDDDS, R6 & R7 ARE
*   USED AS WORK REGISTERS.
*        ON EXIT, THE 12 BYTE AREA CONTAINS ' DD MMM 19YY'.
*
*        EXIT WILL BE BY BRANCH TO THE ADDRESS IN REG 15.
*
RDATECON EQU   *
         MVO   RFELD2,0(2,R1)           ACCESS YEAR
         UNPK  10(2,R1),RFELD2+1(2)
         MVZ   11(1,R1),10(R1)
         MVC   7(3,R1),=C' 19'          SET CONSTANT
         DP    RFELD2,=P'4'             TEST FOR
         CP    RFELD2+2(1),=P'0'         LEAP YEAR
         BNE   RNOLEAP
         LA    R7,RYTAB3+3              ADD 1 TO JULIAN DAY IN TABLE
         LA    R6,10                    TO COMPENSATE FOR LEAP YEAR
RLEAP    AP    0(2,R7),=P'1'            TO MONTHS MARCH TO DECEMBER
         LA    R7,5(R7)                 *
         BCT   R6,RLEAP                 *
         NI    RESETLEP+1,X'0F'
*
RNOLEAP  ZAP   RFELD3,2(2,R1)           DETERMINE MONTH AND DAY
         MVC   0(4,R1),=X'40202140'     MOVE EDIT PATTERN
         LA    R7,TABEND
         LA    R6,5
R45      CP    3(2,R7),RFELD3
         BL    R50
         SR    R7,R6
         B     R45
R50      MVC   4(3,R1),0(R7)            MOVE IN MONTH
         SP    RFELD3,3(2,R7)
         MVO   RFELD3,RFELD3
         ED    0(3,R1),RFELD3
RESETLEP BR    R15                 RETURN IF NOT LEAP YEAR (NOP INST)
         OI    RESETLEP+1,X'F0'    RESET BRANCH
         LA    R7,RYTAB+3          ADDRESS
         LA    R6,10                TABLE AREAS
RESLOOP  SP    0(2,R7),=P'1'       RESTORE
         LA    R7,5(R7)             MONTH
         BCT   R6,RESLOOP            TABLE
         BR    R15                 RETURN
RFELD2   DC    PL3'0'
RFELD3   DC    PL2'0'
RYTAB    DC    C'JAN'
         DC    P'000'
         DC    C'FEB'
         DC    P'031'
RYTAB3   DC    C'MAR'
         DC    P'059'
         DC    C'APR'
         DC    P'090'
         DC    C'MAY'
         DC    P'120'
         DC    C'JUN'
         DC    P'151'
         DC    C'JUL'
         DC    P'181'
         DC    C'AUG'
         DC    P'212'
         DC    C'SEP'
         DC    P'243'
         DC    C'OCT'
         DC    P'273'
         DC    C'NOV'
         DC    P'304'
TABEND   DC    C'DEC'
         DC    P'334'
         EJECT
RPWORK   DS    D
RSAVE    DS    9D
RFELD1   DC    PL4'0'
RPRINT   DS    0CL133
         DC    CL11'0'
         DC    CL41'REQUEST FOR FILE PRINT STARTING AT RECORD'
RPSTART  DC    CL8' '
         DC    CL17', INCREMENTING BY'
RPINDEX  DC    CL8' '
         DC    CL13', STOPPING AT'
RPSTOP   DC    CL8' '
         DC    CL27' '
RPATTERN DC    XL8'4020202020202120'
         LTORG
         DROP  R5,R12                  DROP BASE REGS
RHEADING DSECT
         DS    CL1
RDATE    DS    CL12
         DS    CL120
         TITLE 'MODULE TO GIVE HEX/CHARACTER PRINT FORMAT'
*        PARAMETERS ON INPUT ARE
*
*             1) DCB OF OPEN PRINT FILE (ADDR, MOVE MODE)
*             2) HEADING LINE (133 CHAR)
*             3) 3-WORD AREA CONTAINING 1- RECORD COUNT
*                                       2- LENGTH OF RECORD
*                                       3- START ADDR OF RECORD
*
PRINTRTN CSECT
         SAVE  (14,12),T,*
         BALR  R12,R0
         USING *,R12                    SET BASE REG TO 12
         ST    R13,HSAVE+4              CHAIN SAVE AREAS
         LR    R11,R13
         LA    R13,HSAVE
         ST    R13,8(R11)
         SPACE 3
         LM    R6,R8,0(R1)              STORE PARAMETERS
PSET     NOP   PNOSET                                               RH1
*        WHILE R8 IS POINTING TO FPARMS, ZAP HSETMOVE ONCE          RH1
*        FROM A BRANCH TO A NO OP IF USER WANTS HEX OFFSETS         RH1
         MVI   PSET+1,X'F0'             CHANGE NOP TO B             RH1
         CLI   12(R8),C'H'              DOES HE WANT HEX OFFSETS?   RH1
         BNE   PNOSET                   NO, DON'T ZAP HSETMOVE      RH1
         MVI   HSETMOVE+1,X'00'         YES, CHANGE B TO NOP        RH1
         MVI   PK1,X'0C'          CHANGE OFFSET STARTER TO 0        RH1
         SPACE 1                                                    RH1
PNOSET   DS    0H                                                   RH1
*                                       R2 - NOT USED
*                                       R3 - LENGTH FOR HEXIT RTN
*                                       R4 - CHAR ADDR FOR HEXIT RTN
         LA    R5,HEXAREA               R5 - HEX AREA FOR HEXIT RTN
*                                       R6 - PRINT DCB ADDR
*                                       R7 - HEADING LINE
         LM    R8,R10,0(R8)             R8 - RECORD COUNT
*                                       R9 - LENGTH OF RECORD
*                                       R10- START OF RECORD
         LA    R11,32(0,0)              R11- CONSTANT 32
*
         MVI   HCARCTL,C'0'        MOVE DOUBLE SPACE TO PRINT
         NI    HCLEAR+1,X'0F'      BYPASS BRANCH TO CLEAR AREA
         AP    HLINCTR,=P'1'       INCR LINE-CTR FOR BLANK LINE
         ZAP   HCOUNTER,PK1             RESET OFFSET COUNTER        RH1
         CVD   R8,HDBLWORK
         MVC   HRECNO,HCTRPAT
         ED    HRECNO,HDBLWORK+4   MOVE RECORD NUMBER TO PRINT
         MVC   HOVFLOW,133(R7)          MOVE ISAM OVERFLOW FLAG TO PRNT
         CVD   R9,HDBLWORK
         MVC   HRECLN,HCTRPAT
         ED    HRECLN,HDBLWORK+4   MOVE RECORD LENGTH TO PRINT
         EJECT
HPROCESS EQU   *
         LTR   R9,R9               ANY DATA TO MOVE
         BZ    HENDJOB             IF NO GO TO END OF MODULE
         CR    R9,R11              IS THERE MORE THAN 1 LINE TO MOVE
         BNH   HSETLEN             IF NO, BRANCH
         LR    R3,R11              SET LENGTH TO 32
         SR    R9,R11              SET R9 TO REDUCED LENGTH
         B     HSETMOVE
HSETLEN  EQU   *
         LR    R3,R9               SET R3 TO LENGTH
         SR    R9,R3               SET R9 TO ZERO
HSETMOVE B     HNOHEX                   NOP IF HEX OFFSETS          RH1
         ZAP   HDBLWORK,HCOUNTER        MOVE OFFSET TO DBLE WORD    RH1
         CVB   R4,HDBLWORK              CONVERT TO BINARY           RH1
         STM   R3,R5,HEX3SAVE           SAVE REGS 3, 4, 5           RH1
         SR    R3,R3                    TELL HEXIT IT'S AN OFFSET   RH1
         LA    R4,HEX3SAVE+5            R4 -> TO HEX NBR            RH1
         LA    R5,HLINMARK              R5 -> TO OFFSET PRINT LOC   RH1
         CALL  HEXIT                    PUT IT IN PRINT LINE        RH1
         LM    R3,R5,HEX3SAVE           RELOAD REGS 3, 4, 5         RH1
         B     HHEX                     DECODE REST OF LINE         RH1
         SPACE 1                                                    RH1
HNOHEX   DS    0H                                                   RH1
         MVC   HLINMARK(6),HLINPTRN     PRINT BYTE COUNTER
         ED    HLINMARK(6),HCOUNTER       IN LEFT MARGIN
         SPACE 1                                                    RH1
HHEX     DS    0H                                                   RH1
         LR    R4,R10                   SET CHAR FLD ADDR.
         MVI   HCHARS+1,C' '            CLEAR
         MVC   HCHARS+2(31),HCHARS+1      PRINTAREA
         LR    R2,R3                    MOVE LENGTH TO WORK REG
         BCTR  R2,0                     REDUCE TO MACHINE LEN
         EX    R2,HMOVCHAR              MOVE CHARS TO PRINT
         TR    HCHARS+1(32),HPRINTAB    CONVERT NON-PRINT TO PERIOD
         CALL  HEXIT                    FORMAT HEX AREA.
         AP    HLINCTR,=P'1'            INC LINE COUNT
         AP    HCOUNTER,=P'32'          INC BYTE COUNT
         CP    HLINCTR,=P'58'           END OF PAGE TEST
         BL    HPRINT                     IF NO BYPASS HEADINGS
         AP    134(2,R7),=P'1'          INC PAGE COUNT
         MVC   113(4,R7),HLINPTRN
         ED    113(4,R7),134(R7)        EDIT PAGE NO TO PRINT
         MVI   0(R7),C'1'               SKIP TO NEW PAGE
         MVI   1(R7),C' '
         PUT   (6),(7)                  PRINT HEADING
         MVI   HCARCTL,C'0'             DOUBLE SPACE AFTER HEADING
         SP    HLINCTR,HLINCTR          ZERO LINECOUNT
HPRINT   EQU   *
         PUT   (6),HPRNTLIN             PRINT DATA-LINE
         MVI   HCARCTL,C' '             SET CARR-CTL TO SINGLE SPACE
         LA    R10,32(0,R10)       SET PTR TO NEXT AREA
HCLEAR   NOP   HPROCESS            BYPASS CLEAR FUNCTION
         OI    HCLEAR+1,X'F0'      SET BRANCH TO BYPASS NEXT FILE
         MVI   HRECNO,C' '              CLEAR
         MVC   HRECNO+1(16),HRECNO        LAST PART OF PRINT-LINE
         B     HPROCESS
         SPACE 2
HMOVCHAR MVC   HCHARS+1(0),0(R10)     EXECUTE MOVE CHARS TO PRINT
         SPACE 2
HENDJOB  DS    0H
         L     R13,HSAVE+4
         RETURN (14,12)
         DS    0D
HSAVE    DC    18F'0'
HEX3SAVE DC    3F'0'
PK1      DC    P'1'               START OF OFFSET. 0 FOR HEX        RH1
HDBLWORK DC    D'0'
*
HPRNTLIN DS    0CL133
HCARCTL  DC    CL1' '
HLINMARK DC    CL7' '
HEXAREA  DC    CL74' '
HCHARS   DC    34CL1'*'
HRECNO   DC    CL8' '
HOVFLOW  DC    CL1' '                   ISAM OVERFLOW RECORD FLAG
HRECLN   DC    CL8' '
*
HLINPTRN DC    XL6'402020202120'        PRINT
HCTRPAT  DC    XL8'4020202020202021'      PATTERNS
HLINCTR  DC    PL2'24'                  LINECOUNT
HCOUNTER DC    PL3'1'                   BYTE COUNT PER RECORD/LINE
HPRINTAB DS    0CL256              TABLE TO CONVERT NO-PRINT CHARS
         DC    64CL1'.'
         DC    C' '
*        DC    128CL1'.'
         DC    9CL1'.'
         DC    C'.<(+|&&'        MUST USE && FOR &
         DC    9CL1'.'
         DC    C'!$*);^-/'
         DC    8CL1'.'
         DC    C',%_>?'
         DC    10CL1'.'
         DC    C':#@''="'         MUST USE '' FOR '
         DC    64CL1'.'
         DC    C'{ABCDEFGHI'
*        DC    7CL1'.'
         DC    6CL1'.'
         DC    C'}JKLMNOPQR'
         DC    8CL1'.'
         DC    C'STUVWXYZ'
         DC    6CL1'.'
         DC    C'0123456789'
         DC    6CL1'.'
         LTORG
         TITLE 'CHARACTER TO HEX MAX 32 CHARS'
*        ON ENTRY TO THIS ROUTINE, REC 3 TO 5 CONTAIN
*             R3 - LENGTH OF CHARACTER FIELD
*             R4 - ADDRESS OF CHARACTER FIELD
*             R5 - ADDRESS OF HEX FIELD
*
HEXIT    CSECT
BEGIN    SAVE  (14,12),T,*
         BALR  12,0
         USING *,12
         MVI   TRANSLAT+1,X'47'         DEFAULT TRANSLATE LTH = 72  RH1
         LTR   R3,R3                    ARE WE PRINTING HEX OFFSET  RH1
         BP    NOTHOFF                  NO, WE'RE HEXING DATA       RH1
         LA    R3,3           YES, SET R3 TO LTH OF HEX OFFSET      RH1
         MVI   TRANSLAT+1,X'05'         CHANGE TRANSLATE LTH TO 6   RH1
         SPACE 1                                                    RH1
         SPACE 1                                                    RH1
NOTHOFF  DS    0H                                                   RH1
         SR    R1,R1               R1 - INDEX REG
         LA    R2,1(0,0)           R2 - INDEX VALUE OF ONE
         BCTR  R3,R0               R3 - MACHINE CODE LEN OF CHAR FLD
*                                  R4 - ADDR OF CHAR FLD
*                                  R5 - ADDR OF HEX FLD
         LR    R6,R4               R6 - CURR BYTE IN CHAR FLD
         LR    R7,R5               R7 - CURR BYTES IN HEX FLD
         LA    R8,2(0,0)           R8 - INDEX VALUE OF TWO
         SR    R9,R9               R9 - WORK REG
         LA    R10,4(0,0)          SET LOOP OF FOUR INDEX REG
LOOP     EQU   *
         IC    R9,0(0,R6)          PLACE CHAR IN WORK REG
         STC   R9,1(0,R7)          SET UP 2ND HALF OF BYTE          RH1
         NI    1(R7),X'0F'         STRIP ZONE FROM CHAR
         SRL   R9,4(R0)            MOVE ZONE TO LOW-ORDER
         STC   R9,0(0,R7)          STORE 1ST HALF OF CHAR INTO HEX FLD
         AR    R6,R2               INCR TO NEXT CHAR FLD BYTE
         AR    R7,R8               INCR TO NEXT HEX FLD BYTES
         BCT   R10,NOINSRT        IF NO INSERT CONTINUE
         MVI   0(R7),X'10'         INSERT SPACE AFTER 8 HEX POSITIONS
         LA    R7,1(R0,R7)         INCREMENT HEX BYTE POINTER       S
         LA    R10,4(0,0)          RESET INDEX REG
NOINSRT  EQU   *
         BXLE  R1,R2,LOOP          REPEAT NO OF BYTES TIMES
         SPACE 2
         CLI   TRANSLAT+1,X'05'    ARE WE TRANSLATING HEX OFFSET    RH1
         BE    TRANSLAT                 YES, GO TRANSLATE IT        RH1
         SPACE 1                                                    RH1
         C     R3,=F'31'           ANY BYTES TO PAD?
         BE    TRANSLAT            IF NO GO TO TRANSLATE
         MVI   0(R7),X'10'         PAD 1ST BYTE
         LA    R9,70(R0,R5)        ADDRESS END OF HEX FLD -2, 1 FOR
*                                 THE FIRST BYTE WHICH IS ALREADY
*                                 SET AND 2, TO MAKE THE LTH HEX
         SR    R9,R7              COUNT BYTES TO PAD
         EX    R9,PADMOVE          MOVE IN PAD CHARACTERS
         SPACE 2
TRANSLAT EQU   *
         TR    0(72,R5),HEXTABLE   CONVERT HEX FIELD TO PRINT CHARS
         RETURN (14,12)
         SPACE 2
PADMOVE  MVC   1(0,R7),0(R7)       PROPAGATE PADDING CHAR
         SPACE 2
HEXTABLE DC    CL17'0123456789ABCDEF '
         SPACE 2
         LTORG
         TITLE 'ANALYZE JFCB ATTRIBUTES'
*        THIS MODULE PRINTS THE LAST TWO LINES OF JFCB INFORMATION
*
JFCBREPT CSECT
         SAVE  (14,12),T,*
         BALR  R12,0                    SET BASE TO R12
         USING *,R12
         ST    R13,JSAVE+4              CHAIN SAVE-AREAS
         LR    R11,R13
         LA    R13,JSAVE
         ST    R13,8(R11)
         SPACE 3
         LM    R2,R3,0(R1)              R2 - REPORT DCB
         USING INFMJFCB,R3              R3 - JFCB DSECT
*                                       R5 - WORK REGISTER
         SPACE 3
*        FORMAT PRINT-LINE
         MVC   JPRINT+11(07),=C'DSORG = '
JISAM    TM    JFCDSORG,X'80'           ISAM?
         BNO   JPART
         MVC   JPRINT+19(2),=C'IS'
JPART    EQU   *
         TM    JFCDSORG,X'02'           PARTITIONED?
         BNO   JSEQUENT
         MVC   JPRINT+19(2),=C'PO'
JSEQUENT EQU   *
         TM    JFCDSORG,X'40'           PHYSICAL SEQUENTIAL?
         BNO   JDIRECT
         MVC   JPRINT+19(2),=C'PS'
JDIRECT  EQU   *
         TM    JFCDSORG,X'20'          DIRECT?
         BNO   JVSAM
         MVC   JPRINT+19(2),=C'DA'
JVSAM    EQU   *
         TM    JFCDSORG+1,X'08'   VSAM?
         BNO   JRECFMA            NO
         MVC   JPRINT+19(4),=C'VSAM'
         B     JPRINT1            END FOR NOW
         SPACE 1
JRECFMA  EQU   *
         MVC   JPRINT+31(7),=C'RECFM ='
         LA    R5,JPRINT+39             POINTER TO NEXT PRINT BYTE
JUNDEF   EQU   *
         TM    JFCRECFM,X'C0'           UNDEFINED?
         BNO   JFIXED
         MVI   0(R5),C'U'
         B     JRECFMB
JFIXED   EQU   *
         TM    JFCRECFM,X'80'           FIXED?
         BNO   JVARIABL
         MVI   0(R5),C'F'
         B     JRECFMB
JVARIABL EQU   *
         MVI   0(R5),C'V'               DEFAULT TO VARIABLE
JRECFMB  EQU   *
         LA    R5,1(R5)                 ADDRESS NEXT BYTE FOR PRINT
JBLOCKED EQU   *
         TM    JFCRECFM,X'10'           BLOCKED?
         BNO   JSTANDRD
         MVI   0(R5),C'B'
         LA    R5,1(R5)
JSTANDRD EQU   *
         TM    JFCRECFM,X'08'           STANDARD BLOCKS?
         BNO   JTRACKOF
         MVI   0(R5),C'S'
         LA    R5,1(R5)
JTRACKOF EQU   *
         TM    JFCRECFM,X'20'           TRACK OVERFLOW?
         BNO   JASACTL
         MVI   0(R5),C'T'
         LA    R5,1(R5)
JASACTL  EQU   *
         TM    JFCRECFM,X'04'           ASA CONTROL?
         BNO   JMACHCTL
         MVI   0(R5),C'A'
         B     JBLKSIZE
JMACHCTL EQU   *
         TM    JFCRECFM,X'02'           MACHINE CONTROL?
         BNO   JBLKSIZE
         MVI   0(R5),C'M'
JBLKSIZE EQU   *
         MVC   JPRINT+51(09),=C'BLKSIZE ='
         MVC   JPRINT+60(07),=X'4020206B202120'
         LH    R5,JFCBLKSI
         CVD   R5,JDBLWORD
         ED    JPRINT+60(07),JDBLWORD+5
JLRECL   EQU   *
         SR    R5,R5
         CH    R5,JFCLRECL              IS LRECL SET TO ZERO
         BE    JPRINT1
         MVC   JPRINT+70(7),=C'LRECL ='
         MVC   JPRINT+77(7),=X'4020206B202120'
         LH    R5,JFCLRECL
         CVD   R5,JDBLWORD
         ED    JPRINT+77(7),JDBLWORD+5
JPRINT1  EQU   *
         BAL   R11,JPRINTLN
*
         EJECT
*        FORMAT NEXT PRINT-LINE
         MVC   JPRINT+11(6),=C'UNIT ='
JDIRACC  EQU   *
         CLI   JFCDEN,X'00'             TAPE DENSITY PRESENT?
         BNE   JTAPES             YES
         MVC   JPRINT+18(13),=C'DIRECT ACCESS'
         B     JPRINT2
JTAPES   EQU   *
         MVC   JPRINT+18(12),=C'CARTRIDGE   '
         CLI   JFCDEN,X'FF'       CARTRIDGE DRIVE?
         BE    JPRINT2            YES
JTAPES1  EQU   *
         MVC   JPRINT+18(12),=C'9 TRACK TAPE'
         TM    JFCDEN,X'08'             7 TRACK DRIVE ?
         BZ    JTAPEDEN                 NO, IT'S 9 TRACK
         NI    JFCDEN,X'F7'             TURN 7 TRK INDICATOR OFF
         MVI   JPRINT+18,C'7'
JEVENPAR EQU   *
         MVC   JPRINT+71(7),=C'TRTCH ='
         TM    JFCTRTCH,X'2B'           EVEN PARITY/TRANSLATE ?
         BNO   JEBCDIC
         MVC   JPRINT+79(2),=C'ET'      YES, MOVE ET TO PRINT
         B     JTAPEDEN
JEBCDIC  EQU   *
         TM    JFCTRTCH,X'3B'           BDC/EBCDIC TRANSLATION
         BNO   JDATACON
         MVI   JPRINT+79,C'T'
         B     JTAPEDEN
JDATACON EQU   *
         TM    JFCTRTCH,X'13'           DATA CONVERSION
         BNO   JEVENTRN
         MVI   JPRINT+79,C'C'
         B     JTAPEDEN
JEVENTRN EQU   *
         MVI   JPRINT+79,C'E'           DEFAULT TO EVEN PARITY
JTAPEDEN EQU   *
         MVC   JPRINT+41(9),=C'DENSITY ='
         MVC   JPRINT+56(3),=C'BPI'
J200BPI  EQU   *
         CLI   JFCDEN,X'03'             200 BPI?
         BNE   J556BPI
         MVC   JPRINT+52(3),=C'200'
J556BPI  EQU   *
         CLI   JFCDEN,X'43'             556 BPI?
         BNE   J800BPI
         MVC   JPRINT+52(3),=C'556'
J800BPI  EQU   *
         CLI   JFCDEN,X'83'             800 BPI?
         BNE   J1600BPI
         MVC   JPRINT+52(3),=C'800'
J1600BPI EQU   *
         CLI   JFCDEN,X'C3'             1600 BPI?
         BNE   J6250BPI                                             MVS
         MVC   JPRINT+51(4),=C'1600'
J6250BPI DS    0H
         CLI   JFCDEN,X'D3'             6250 BPI ?                  MVS
         BNE   JPRINT2                   NO                         MVS
         MVC   JPRINT+51(4),=C'6250'     SAY 6250 BPI               MVS
JPRINT2  EQU   *
         BAL   R11,JPRINTLN
         SPACE 3
         MVI   JPRINT,C'-'
         BAL   R11,JPRINTLN
         L     R13,JSAVE+4
         RETURN (14,12)
JPRINTLN EQU   *
         PUT   (2),JPRINT
         MVI   JPRINT+1,C' '                                        RH1
         MVC   JPRINT+2(131),JPRINT+1     *                         RH1
         BR    R11
         SPACE 3
JSAVE    DC    18F'0'                   REGISTER SAVE AREA
JDBLWORD DC    D'0'
JPRINT   DS    0CL133                   PRINT AREA
         DC    C'0'
         DC    CL132' '
         LTORG
         DROP  R3,R12             DROP BASE REGS
         TITLE 'SPECIAL DCB SETUP FOR PARTITIONED AND ISAM'
PARTISAM CSECT
         SAVE  (14,12),T,*
         BALR  R12,R0                   SET BASE TO R12
         USING *,R12
         ST    R13,PSAVE+4              CHAIN SAVE-AREAS
         LR    R11,R13
         LA    R13,PSAVE
         ST    R13,8(R11)
         SPACE 3
         LM    R2,R5,0(R1)              R2 - PARM ADDR              RH
         L     R2,0(0,R2)               R3 - REPORT DCB
*                                       R4 - INPUT  DCB
         USING INFMJFCB,R5              R5 - JFCB ADDR BASE
         MVC   PSTORDAT(6),JFCBCRDT     STORE CREATION & EXPIRY DATE
*                                       UNTIL AFTER OPEN
         TM    JFCDSORG,X'80'           ISAM FILE?
         BO    PISAMFIL                 YES, GO TO ISAM PROCESS
         SPACE 3
*   DEFAULT TO PARTITIONED ORGANIZATION
*
         MVC   PDSORECF,JFCDSORG        SAVE RECFM & DSORG
         MVC   PBLKLREC,JFCBLKSI        SAVE BLKSIZE & LRECL
         MVI   JFCDSORG,X'40'           SET DSORG TO PS
*        MVI   JFCBLKSI,X'40'           SET DSORG TO PS
         CLI   JFCBELNM,X'40'           TEST FOR MEMBER NAME
         BNE   PARTOPEN                 PREPARE TO PRINT MEMBER
         SPACE 2
*   WHEN A MEMBER-NAME IS ABSENT, IT CAN BE ASSUMED THAT A DIRECTORY
*   LIST IS BEING REQUESTED.  THE FILE ATTRIBUTES MAY NOT SUPPORT THE
*   256 BYTE BLOCK LENGHTS AND THUS ARE ADJUSTED UNTIL AFTER THE OPEN.
*   TO PRINT THE DIRECTORY, SET THE JFCB BLOCK/RECORD LENGTHS, & RECFM
         MVI   JFCRECFM,X'C0'           SET RECFM TO UNDEFINED
         MVC   JFCBLKSI(4),=X'04000000' SET MAX BLK TO 1024 & LRECL=0
         SPACE 2
PARTOPEN EQU   *
         BAL   R11,POPENRTN             OPEN FILE AND GET JFCB
         MVC   JFCDSORG(3),PDSORECF     RESTORE RECFM & DSORG
         MVC   JFCBLKSI(4),PBLKLREC     RESTORE BLKSIZE & LRECL
         B     PRETZERO                 GO TO SUCCESSFUL RETURN
         SPACE 3
*   PROCESS ISAM OPEN
*
PISAMFIL EQU   *
         MVC   PISAMDCB+33(7),33(R4)    TRANSFER EODAD & EXLIST TO ISAM
         MVC   PISAMDCB+56(4),56(R4)    TRANSFER SYNAD TO ISAM DCB
         MVC   0(252,R4),PISAMDCB       SUBSTITUE ISAM FOR QSAM DCB
         BAL   R11,POPENRTN             OPEN FILE AND GET JFCB
         B     PRETZERO                 GO TO SUCCESSFUL RETURN
         SPACE 3
*   ROUTINE TO OPEN FILE, GET JFCB AND RESTORE DATES AND RETURN
*
POPENRTN EQU   *
         OI    JFCBTSDM,X'08'     DON'T WRITE JFCB BACK             RH
         OPEN  ((4),INPUT),TYPE=J       OPEN FILE WITH MODIFIED JFCB
         TM    DCBOFLGS,X'10'           WAS OPEN SUCCESSFUL?
         BZ    PABEND                   NO, ABEND
         RDJFCB ((4))                   REREAD JFCB
         MVC   JFCBCRDT(6),PSTORDAT     RESTORE DATES
         BR    R11                      RETURN TO CALLING INSTRUCTION
         SPACE 3
PRETZERO EQU   *                        RETURN AFTER SUCCESSFUL END
         L     R13,PSAVE+4
         RETURN (14,12),RC=0
         SPACE 2
PABEND   ABEND 1111,DUMP                ERROR IN OPEN - RERUN JOB
         SPACE 3
PSAVE    DS    18F
PSTORDAT DC    CL6' '
PISAMDCB DCB   DDNAME=SYSUT1,DSORG=IS,MACRF=GM                      VBS
*ISAMDCB DCB   DDNAME=SYSUT1,DSORG=IS,MACRF=GL
         ORG   PISAMDCB
PDSORECF DS    CL3                      JFCB + 98 (3) RECFM/DSORG
PBLKLREC DS    CL4                      JFCB +102(4) BLKSI/LRECL
         ORG   PISAMDCB+252
         LTORG
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         DROP  R5,R12             DROP BASE REGS
         END
/*
//*
//LKED.SYSLMOD DD DSN=HALL.LINKLIB(FILEDUMP),DISP=SHR
//LKED.SYSINO DD *
         ENTRY FILEDUMP
       OVERLAY ONE
        INSERT DCBSETUP
       OVERLAY ONE
        INSERT PARTISAM
       OVERLAY ONE
        INSERT ERRANAL
       OVERLAY ONE
        INSERT PARMCHEK
       OVERLAY ONE
        INSERT REPTINIT
       OVERLAY ONE
        INSERT JFCBREPT
       OVERLAY ONE
        INSERT PRINTRTN
        INSERT HEXIT
/*
//
//*
//*    RUN FILEDUMP....
//*
//STEP1 EXEC PGM=FILEDUMP,PARM='C,STOP=10'
//STEPLIB DD DSN=HALL.LINKLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSUT1 DD DSN=HALL.INPUT.CNTL,DISP=SHR
//SYSUT2 DD SYSOUT=(A)
//*YSUT2 DD DSN=HALL.COPY.CNTL,DISP=(,DELETE),UNIT=DISK,
//*   SPACE=(TRK,1),DCB=*.SYSUT1
//SYSABEND DD SYSOUT=*
//
