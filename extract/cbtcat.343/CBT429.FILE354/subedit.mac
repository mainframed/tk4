//SUBEDIT  JOB
/*ROUTE  PRINT  R7
//*
//JES    OUTPUT JESDS=ALL,PAGEDEF=P1E100,FORMDEF=P1E000
//*UT1   OUTPUT DEFAULT=YES,COPIES=1,FORMS=X3NA,CLASS=1,
//OUT1   OUTPUT DEFAULT=NO,COPIES=1,CLASS='*',       LANDSCAPE 67X138
//             PAGEDEF=L1C100,FORMDEF=L1C000
//OUT2   OUTPUT DEFAULT=NO,COPIES=1,CLASS='*',       PORTRAIT 88X96
//             PAGEDEF=P1E100,FORMDEF=P1E000
//*
//*
//*       DSN=CBT.SOURCE(SUBEDIT)
//*       WILL  SUBMIT ANY 1-126 BYTE COMMAND FROM PARM= OR  //SYSIN
//*
//*      IF INPUT IS VIA //SYSIN, A WAIT=NNN COMMAND CAN BE
//*      USED TO WAIT UP TO 999 (10 SECONDS) TIMER UNITS BETWEEN
//*      EACH SYSTEM COMMAND. A WAIT COMMAND WILL BE IN EFFECT UNTIL
//*      ANOTHER WAIT= COMMAND IS FOUND.
//*
//S1        EXEC  HLASMCL,CLASS='*',
//   COND.LKED=(7,LT,ASM)
//ASM.SYSPRINT  DD SYSOUT=(,),OUTPUT=*.OUT2
//ASM.SYSIN DD  *
         TITLE '*** PROGRAM TO SUBMIT SYSTEM COMMAND WITH SVC 34 ***'
*
*              THIS LITTLE PROGRAM IS USED TO RUN THE CMD PASSED
*              TO IT AS A SYSTEM  COMMAND, USING SVC 34
*
         SPACE 3
SUBEDIT  CSECT
         USING *,R15              SET TEMP BASE
         B     AROUND             BRANCH AROUND DUMP TITLE
         DC    C'SUBEDIT '        PROGRAM NAME
         DC    C'&SYSDATE'        DATE WRITTEN
         DC    C'&SYSTIME'        TIME WRITTEN
AROUND   DS    0H
         STM   R14,R12,12(R13)
         LR    R12,R15
         DROP  R15
         USING SUBEDIT,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         L     R1,0(0,R1)         R1 -> TO PARMS
         LH    R2,0(0,R1)         R2 = PARM LTH
         LTR   R2,R2              IS THERE A PARM ?
         BZ    NOPARM             NO, LOOK FOR SYSIN
         CH    R2,MAXCMDL         IS PARM TOO LONG ?
         BNH   PARMOK             NO, ON WE GO
         ST    R2,RETCODE         SAVE LTH AS RC
         B     NOMORE4            RETURN WITH RC>0
         SPACE 1
PARMOK   DS    0H
         B     COMEND             GO SEND COMMAND
         SPACE 1
NOPARM   DS    0H
         OPEN  (SYSIN,(INPUT))    OPEN INPUT DCB
*
         LA    R6,SYSIN           R6 -> TO DCB
         USING IHADCB,R6
         TM    DCBOFLGS,DCBOFOPN  TEST IF OPEN
         BZ    NOSYSIN            NO
         MVC   CMDLRECL,DCBLRECL  SAVE SYSIN LRECL
         CLC   CMDLRECL,MAXCMDL   OVER MAX CMD LTH?
         BNH   READSYS            NO, IT'S OKAY
         MVC   CMDLRECL,MAXCMDL   YES, USE MAX LTH ONLY
         DROP  R6
*
READSYS  DS    0H
         GET   SYSIN,MBRNAME           GET INPUT RECORD
         LA    R4,MBRNAME         R4 -> AT INPUT
         CLC   =C'WAIT=',0(R4)    IS IT THE 'WAIT=' PARM?
         BNE   READSYS0           NO, GO SET UP COMMAND
         SPACE 1
         LA    R4,5(0,R4)         R4 -> TO WAIT= PARM, IF ANY
         SPACE 1
         LR    R15,R4             R15 -> WAIT PARM TOO
         LA    R14,0              R14 = 0
         SPACE 1
PARM5B   DS    0H
         CLI   0(R15),C'0'        IS BYTE NUMERIC?
         BL    READSYS            NO, IGNORE PARM
         LA    R15,1(0,R15)       R15 -> TO NEXT PARM BYTE
         LA    R14,1(0,R14)       R14 = NBR OF CHARS
         CLI   0(R15),C' '        AT END OF WAIT TIME?
         BNE   PARM5B             NO, LOOK AGAIN
         SPACE 1
PARM5C   DS    0H
         CH    R14,=H'4'          TOO MANY CHARS?
         BL    PARM5D             NO, IT'S OKAY
         LA    R14,3              YES, SET R14 TO 3
         SPACE 1
PARM5D   DS    0H
         BCTR  R14,0              R14 = HEX LTH
         EX    R14,PARM5EX        PACK WAIT TIME
         CVB   R15,DWORD          R15 = SECONDS TO WAIT
*        MH    R15,MULTSEC        CONVERT TO TIMER UNITS
         ST    R15,WAITTIME       SAVE IT FOR STIMER
         B     READSYS            GO GET NEXT COMMAND
         SPACE 1
PARM5EX  PACK  DWORD+6(2),0(0,R4) USE 1ST 2 BYTES ONLY
         SPACE 1
READSYS0 DS    0H
         CLI   MBRNAME,C'*'       A COMMENT?
         BE    READSYS            YES, GET NEXT INPUT RECORD
         LA    R1,MBRNAME-1       R1 -> TO INPUT DATA-1
         SPACE 1
READSYS1 DS    0H
         LR    R2,R1              R2 -> TO INPUT DATA-1
         AH    R2,CMDLRECL        R2 -> TO END OF INPUT DATA
         CLI   0(R2),C' '         LAST CHAR BLANK?
         BE    READSYS2           YES, NO LINE NUMBER
         SH    R2,=H'8'           NO, DROP LINE NBR FROM END...
         SPACE 1
READSYS2 DS    0H
         CR    R2,R1              R2 -> TO INPUT DATA-1 ?
         BE    READSYS            YES, RECORD IS BLANK
         CLI   0(R2),C' '         END OF MODULE NAME?
         BNE   READSYS3           YES
         BCT   R2,READSYS2        NO, R2 -> AT PREV CHAR
         SPACE 1
READSYS3 DS    0H
         SR    R2,R1              R2 = MODULE NAME LTH
         CH    R2,MAXCMDL         IS PARM TOO LONG ?
         BNH   READSYS4           NO, ON WE GO
         ST    R2,RETCODE         SAVE LTH AS RC
*        B     NOMORE4            RETURN WITH RC>0
         B     BADINPUT           THEN ERROR
*
READSYS4 DS    0H
         BCTR  R1,0               SET UP R1 FOR EX SETMOVE LATER...
         B     COMEND             GO DO COMMAND
         SPACE 1
BADINPUT DS    0H
*        MVC   PSTYPE+9(L'INPMSG),INPMSG PUT ERROR MSG IN PRINT
*        B     BADPARM2           THEN ERROR
*INPMSG  DC    C'SYSIN DATA OVER 126 BYTES'
         SPACE 1
EOFREAD  DS    0H
         CLOSE (SYSIN)            CLOSE INPUT DCB
         B     NOMORE4            RETURN WITH RC>0
         SPACE 1
*
*        COMMON EXIT  -  SHIP COMMAND AND RETURN
*
COMEND   DS    0H
         BCTR  R2,0               R2 = HEX LTH
         EX    R2,SETPARM         SET UP COMMAND FOR SVC 34
         LA    R2,5(0,R2)         R2 = LTH OF CMD + 4
         STH   R2,CSWACMDL        INSERT COMMAND LENGTH
         SPACE 2
         MODESET KEY=ZERO          GET INTO PRIVILEGED STATE
         SLR   R0,R0               INDICATE SYSTEM ROUTINE
         SLR   R15,R15             INDICATE SYSTEM COMMAND
         LA    R1,CSWACMD          LOAD COMMAND ADDRESS
         SVC   34                  ISSUE COMMAND
         MODESET KEY=NZERO         BACK INTO NORMAL STATE
         SPACE 1
         MVI   CSWACMDT,C' '      CLEAR COMMAND ARE
         MVC   CSWACMDT+1(125),CSWACMDT
         TM    SYSIN+48,X'10'     IS SYSIN OPEN ?
         BZ    NOMORE4            NO, END OF SUBEDIT
         ICM   R15,15,WAITTIME    R15 = WAIT TIME
         BZ    NOWAIT             NONE, DON'T WAIT
         STIMER WAIT,BINTVL=WAITTIME   WAIT A BIT
         SPACE 1
NOWAIT   DS    0H
         B     READSYS            GO GET NEXT COMMAND
*
NOSYSIN  DS    0H
         SPACE 1
NOMORE4  DS    0H
         L     R13,SAVE+4
         L     R15,RETCODE        R15 = RETURN CODE
         ST    R15,16(0,R13)      PASS IT TO CALLER
         LM    R14,R12,12(R13)
         BR    R14
         SPACE 1
SETPARM  MVC   CSWACMDT(0),2(R1)  SET UP COMMAND
         SPACE 3
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
*        WORK AREAS
* $$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
DUMPTITL DC    C'WORKING STORAGE SECTION'
DWORD    DC    D'0'
SAVE     DC    18F'-1'
RETCODE  DC    F'0'               RETURN CODE
CMDLRECL DC    H'80'              ASSUME SYSIN IS LRECL 80
MAXCMDL  DC    H'126'             MAX COMMAND LENGTH
MBRNAME  DC    CL126' '           READ AREA FOR GET AT READSYS
*
*        COMMAND CONSTRUCTION AREA
*
CSWACMD  DS    0F                  BEGIN COMMAND
CSWACMDL DC    H'126'              COMMAND LENGTH
         DC    H'0'                NOT USED
CSWACMDT DC    CL126' '            COMMAND TEXT AREA
         SPACE 1
WAITTIME DC    F'0000'            WAIT FOR 0 SECONDS
MULTSEC  DC    H'100'             MULTIPLIER FACTOR TO GET TIMER UNITS
         SPACE 1
         LTORG
*
SYSIN    DCB   DSORG=PS,MACRF=(GM),EODAD=EOFREAD,   LRECL=126,         +
               DDNAME=SYSIN
SYSINL   EQU   *-SYSIN
*
         PRINT NOGEN
         DCBD  DEVD=DA,DSORG=PS
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   SUBEDIT
//LKED.SYSLMOD DD DSN=IMSVS.AUTHLIB1,DISP=SHR
//LKED.SYSIN DD *
  SETCODE AC(1)
  NAME SUBEDIT(R)
/*
//
