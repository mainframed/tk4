         TITLE 'CLIB --- CONCATENATE DATASET FIRST TO DDNAME'
*-------
*------- PURPOSE: PROVIDE TSO USER QUICK ACCESS TO PRIVATE CLIST
*-------
*------- AUTHOR:  DICK SZIEDE  PRC. 1987.
*-------
*------- METHOD:  USE SVC 99 TO DETERMINE THE DSNAMES OF ALL DATASETS
*-------          CONCATENATED TO  A PARTICULAR DDNAME.
*-------          REALLOCATE THE  ARGUMENT DSNAME IN FRONT OF ALL THE
*-------          OTHERS.
*-------
*-------
*-------
*------- OPTIONS: CLIB MYLIB.CLIST DD(SYSPROC)
*-------
*-------
*------- DETAIL:  CLIB TAKES AN ARGUMENT OF DSNAME, AND OPTIONAL
*-------          DDNAME, (DEFAULTS TO SYSPROC), AND MAKES A
*-------          CONCATENATION.
*-------
*-------          POSSIBLE RETURN CODES ARE:
*-------
*-------             0 ===> DATASET ALLOCATED
*-------             4 ===> DATASET NOT IN CATALOG
*-------             8 ===> PARSE ERROR - UNABLE TO MAKE SENSE OF
*-------                    COMMAND PARMS.
*-------          IF CLIB DETECTS AN ERROR IN DYNAMIC ALLOCATION
*-------          IT WILL ABEND WITH A COMPLETION CODE OF S0C3.
*-------
*-------
*------- ATTRIBUTES:  SERIALLY REUSABLE, NOT REENTRANT, NOT AUTHORIZED
*-------
*-------          NOTE THAT REENTRANCY CAN BE ACHIEVED BY MOVING THE
*-------          NON-REENTRANT DATA AREAS, FROM "RBPTR" ON
*-------          INTO THE GETMAIN'ED WKA.  THESE AREAS CONTAIN THE
*-------          DYNAMAC ALLOCATION REQUEST BLOCK, AND TEXT UNITS.
*-------          WHILE THE UPL'S ARE NOT VOLITILE, ONE CANNOT HAVE
*-------          AN ADCON FOR A GETMAIN'ED AREA, SO THEY TOO MUST
*-------          GO TO WKA.
*-------
*-------          I CAN'T IMAGINE ANYONE ACTUALLY DOING THIS, BUT
*-------          JUST IN CASE, I'VE USED THE MAPPING MACROS INSTEAD
*-------          OF MY OWN LABELS.
*-------
*------- BACKGROUND:  THE NOTION  FOR THIS COMMAND COMES FROM THE
*-------       CLIST "CLIB" BY KERMIT KISER & JEFF SPREHN OF WASH.
*-------       STATE (FILE 270 ON CBT).   THEIR CODE WAS TOO
*-------       INSTALLATION-DEPENDENT FOR MY TASTE, SO I WROTE MY OWN.
*-------
*-------)F FUNCTION -
*-------   THE CLIB COMMAND IS USED TO CONCATENATE A PRIVATE
*-------   LIBRARY AHEAD OF THE LIBRARIES ALREADY ALLOCATED
*-------   TO A SPECIFIC DDNAME.
*-------
*-------)X SYNTAX -
*-------     CLIB     ('DSNAME') DDNAME('DDNAME')
*-------
*-------     REQUIRED -- 'DSNAME'
*-------     DEFAULTS -- DDNAME(SYSPROC)
*-------)O OPERANDS -
*-------))'DSNAME'  - 'DATA SET NAME' IS THE NAMES OF DATA SET TO BE
*-------            CONCATENATED TO THE DDNAME.
*-------))DDNAME('DDNAME') - DDNAME TO WHICH 'DSNAME' IS TO BE
*-------            CONCATENATED.  DEFAULT IS 'SYSPROC'.
*-------
*------- CHANGE:  NO ACTIVITY.
*-------
*-------
*-------
         EJECT
CLIB     PRIME ID=DICK_SZIEDE_PRC_1987,LV=4096
         REGS
         USING WKA,R13
*-------
*------- PARSE THE COMMAND BUFFER AND SAVE DSNAME AND (OPTIONAL)
*------- DDNAME IN TEXT UNITS FOR LATER ALLOCATION.  DDNAME
*------- DEFAULTS TO "SYSPROC".
*-------
         LR    R10,R1
         ST    R10,DFCPPLP        SAVE CPPL FOR DAIRFAIL
         USING CPPL,R10               CPPL ADDR WAS SAVED IN R10
         MVC   IOPLUPT(4),CPPLUPT      PREPARE IOPL
         MVC   IOPLECT(4),CPPLECT
         LA    R2,ECB
         ST    R2,IOPLECB
         USING PPL,R8
PARSINPT LA    R8,PPLAREA                MAKE PPL ADDRESSABLE
         ST    R2,PPLECB
         MVC   PPLUPT,CPPLUPT          SET UP IKJPARSE PARMS
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         MVC   PPLPCL(4),=A(PARMDSN)
         LA    R9,ANS
         ST    R9,PPLANS
*-------
         CALLTSSR EP=IKJPARS,MF=(E,PPL)  PARSE  THE INPUT BUFFER
         LTR   R15,R15            CHECK RETURN CODE IMMEDIATELY
         BNZ   PARSERR
         ICM   R10,B'1111',PPLANS      TOP OF PDL CHAIN
         BNP   PARSERR                 PARSE OF BUFFER UNSUCESSFUL
         L     R10,0(R10)              PDL HEADER
         DROP  R8
         DROP  R10                WE NO LONGER NEED THE CPPL
*-------
         USING PARANSWR,R10
         MVC   WKADDNL(10),REFRDDN  PRIME DDNAME WITH BLANKS
         TM    PARDSN+6,X'80'     IS DSNAME PRESENT
         BNO   PARSERR            QUIT IF NOT
         LM    R2,R3,PARDSN       GET DSNAME POINTER & LENGTH
         SRL   R3,16              STICK DSNAME IN TEXT UNIT
         STH   R3,WKADSNL
         BCTR  R3,0
         EX    R3,DSNMOVE
         TM    PARPND+6,X'80'     IS DDNAME PRESENT
         BNO   SYSPORK            DEFAULT IF NOT
         LM    R2,R3,PARPND       GET DDNAME POINTER & LENGTH
         SRL   R3,16              STICK DDNAME IN TEXT UNIT
*------- STH   R3,WKADDNL         *****NOTE**** ALWAYS USE 8 FOR
         BCTR  R3,0               DDNAME LENGTH, EVEN IF IT'S A LIE
         EX    R3,DDNMOVE
         B     SEARCH
SYSPORK  MVC   WKADDNAM,=C'SYSPROC ' SET DEFAULT OF SYSPROC
         B     SEARCH
DSNMOVE  MVC   WKADSNAM(0),0(R2)
DDNMOVE  MVC   WKADDNAM(0),0(R2)
         DROP  R10
*-------
*------- SAVE THE DSNAMES ATTACHED TO DDNAME, SO'S WE CAN REALLOCATE
*-------       THEM LATER.
*-------
SEARCH   LA    R10,1               FIRST RELATIVE ENTRY
         SLR   R9,R9               INITIAL HIT COUNT
         MVI   RB1+(S99VERB-S99RB),S99VRBIN -- INFO REQUEST VERB
         MVC   RB1+(S99TXTPP-S99RB)(4),=A(SRCHUPL)
*-------
SRCHLOOP STH   R10,TXT15+(S99TUPAR-S99TUNIT) RELATIVE ENTRY IN TEXT
         LA    R10,1(R10)          INCREMENT FOR NEXT REQUEST
         MVC   TXT5+(S99TULNG-S99TUNIT)(46),REFRDSN
         MVC   TXT4+(S99TULNG-S99TUNIT)(10),REFRDDN
         LA    R1,RBPTR
         DYNALLOC
SRCHED   LTR   R15,R15            AN INFO REQUEST SHOULDN'T FAIL
         BZ    SRCHCK
         BAL   R3,DARFL
         BAL   R3,DAMNIT
SRCHCK   LTR   R9,R9               HAVE WE ALREADY FOUND HITS?
         BZ    SRCHDDNM            NO  ===> LOOK FOR DDNAME
         CLC   =CL8' ',TXT4+(S99TUPAR-S99TUNIT) IZZIT A CONCATENATED
         BNE   SRCHDONE                             DATASET?
         B     SRCHHIT
SRCHDDNM CLC   WKADDNAM,TXT4+(S99TUPAR-S99TUNIT) DOES IT MATCH DDNAME
         BNE   SRCHLAST
SRCHHIT  LA    R8,WKADSNS(R9)      ADDR OF NEXT DSN SAVE AREA
         MVC   0(46,R8),TXT5+(S99TULNG-S99TUNIT) MOVE IN DSN
         LA    R9,46(R9)           AND INCREMENT HIT COUNT
*
SRCHLAST CLI   TXT13+(S99TUPAR-S99TUNIT),X'80' CHECK FOR LAST NTRY
         BE    SRCHDONE
         B     SRCHLOOP
*-------
*------- THE FIRST NONBLANK DDNAME AFTER A DDNAME MATCH, OR THE
*-------       LAST ALLOCATED DDNAME BRINGS US HERE FOR EVALUATION.
*-------
SRCHDONE ST    R9,WKAHITS
         LTR   R9,R9               ANY HITS?
         BZ    ALLOCIT             N0  ===> SKIP THE FREE
*-------
*------- WE CAN ADD THE 16TH LIBRARY TO A CONCATENATION, BUT THAT'S
*-------       ALL.  IF HITS > 15 ==> QUIT WITH NO ACTION.
*-------
         C     R9,=A(16*46)       MORE THAN 15 HITS?
         BL    PREALLOC
         BAL   R3,TOOMANY
         B     RET8
*-------
*------- PRE-ALLOCATE THE NEW LIBRARY, SO WE DON'T FREE THE
*-------       CONCATENATION, THEN FIND THE NEW KID DOESN'T EXIST.
*-------
PREALLOC MVC   V1TXT2+(S99TULNG-S99TUNIT)(46),WKADSNL
         MVC   V1TXT85+(S99TULNG-S99TUNIT)(10),REFRDDN
         MVI   RB1+(S99VERB-S99RB),S99VRBAL ALLOCATE
         MVC   RB1+(S99TXTPP-S99RB)(4),=A(ALOC2UPL)
         LA    R1,RBPTR
         DYNALLOC
         LTR   R15,R15
         BZ    FREEIT
         BAL   R3,DARFL
         B     RET4
*-------
*------- FREE  THE CONCATENATED DATASETS.
*-------
FREEIT   MVI   RB1+(S99VERB-S99RB),S99VRBUN -- FREE REQUEST
         MVC   RB1+(S99TXTPP-S99RB)(4),=A(FREEUPL)
         MVC   TXT1+(S99TULNG-S99TUNIT)(10),WKADDNL DDNAME & LENGTH
         LA    R1,RBPTR
         DYNALLOC
FREESTOP LTR   R4,R15             HOW BAD WAS THE ERROR?
         BZ    ALLOCIT
         BAL   R3,DARFL           PRINT DAIR ERROR MESSAGE
         CH    R4,=H'4'
         BE    RET4
         B     DAMNIT
*-------
*------- NOW   WE CAN FINNALLY ALLOCATE DATSET TO DDNAME.  WE WAIT
*-------       TILL NOW SO THAT IF THE CONCATENATION DIDN'T EXIST,
*-------       THIS COMMAND IS EQUIVALENT TO:
*-------       "ALLOC DSNAME DD(SYSPROC) SHR REUSE"
*-------
ALLOCIT  MVI   RB1+(S99VERB-S99RB),S99VRBAL -- ALLOCATE REQUEST
         MVC   RB1+(S99TXTPP-S99RB)(4),=A(ALOCUPL)
         MVC   V1TXT1+(S99TULNG-S99TUNIT)(10),WKADDNL DDNAME & LENGTH
         MVC   V1TXT2+(S99TULNG-S99TUNIT)(46),WKADSNL DSNAME & LENGTH
         LA    R1,RBPTR
         DYNALLOC
         B     *+4(R15)
         B     COPYCON            GOOD RETURN
         BAL   R3,DARFL           DATASET NOT FOUND
         BAL   R3,DARFL           DYNAMIC ALLOCATION ERROR
         BAL   R3,DAMNIT          SHOULD NOT HAPPEN
         BAL   R3,DAMNIT          INSURANCE
*-------
*------- HERE  WE COPY THE CONCATENATION.
*------- NOW   ALLOCATE EACH DSN SEPARATELY, THEN CONCATENATE BY DDN
*-------
COPYCON  SLR   R8,R8               GET COUNT OF DATASETS IN REG 9
         L     R9,WKAHITS
         LTR   R9,R9              IF NO HITS, WE DON'T NEED TO DO THIS
         BZ    RETZERO
         D     R8,=F'46'
         LA    R9,1(R9)           CONCATENATE COUNT IS HITS+1
         STH   R9,V3TXT1+(S99TUNUM-S99TUNIT) COUNT FOR CONCATENATE
         LA    R9,WKADSNS          SET UP TO LOOP THROUGH CONCATENATED
         L     R11,WKAHITS         DATSETS.
         LA    R10,46
         SR    R11,R10
         LA    R11,0(R9,R11)
         LA    R8,V3TXT1+(S99TULNG-S99TUNIT) DDNAME FEEDBACK GOES
*-------                                     INTO CONCAT TEXT UNIT
CAT1LOOP MVC   V1TXT2+(S99TULNG-S99TUNIT)(46),0(R9) GET DAT DSN
         MVC   V1TXT85+(S99TULNG-S99TUNIT)(10),REFRDDN
         MVI   RB1+(S99VERB-S99RB),S99VRBAL ALLOCATE
         MVC   RB1+(S99TXTPP-S99RB)(4),=A(ALOC2UPL)
         LA    R1,RBPTR
         DYNALLOC
CATLPED  B     *+4(R15)
         B     CATLPBMP            TO NEXT DATASET
         B     FREEIT              RETRY FOR @0000001 --- NOTE THAT
         BAL   R3,DARFL                THIS WILL CAUSE A  LOOP IF
         B     DAMNIT                  @0000001 IS NOT THE DDNAME
*-------                               THAT IS UNAVAILABLE.
CATLPBMP LA    R8,10(R8)           AND BUMP DDNAME POINTER
         MVC   0(10,R8),V1TXT85+(S99TULNG-S99TUNIT) DDN FEEBACK
         BXLE  R9,R10,CAT1LOOP
*-------
*------- NOW   CONCATENATE ALL THE  DDNAMES TOGETHER
*-------
*-------
         MVC   V3TXT1+(S99TULNG-S99TUNIT)(10),WKADDNL DDNAME & LENGTH
         MVI   RB1+(S99VERB-S99RB),S99VRBCC   CONCATENATION VERB
         MVC   RB1+(S99TXTPP-S99RB)(4),=A(CONCUPL)
         LA    R1,RBPTR
         DYNALLOC
         LTR   R15,R15
         BZ    RETZERO
         BAL   R3,DARFL
         B     DAMNIT
*-------
*------- EXITS
*-------
RET8     LA    R15,8               PARSE ERROR
         B     QUIT
RET4     LA    R15,4               DATASET NOT FOUND
         B     QUIT
RETZERO  SLR   R15,R15
QUIT     TERME RC=(15)
         EJECT
*-------
*-------     * ERROR ROUTINES *
*-------
DAMNIT   EX    0,*                 UNEXPECTED ERROR.
*-------
PARSERR  PUTLINE PARM=PUTBLOK,OUTPUT=(PAROLD1,TERM,MULTLVL,INFOR),     X
               MF=(E,IOPL)
         B     RET8
*-------
TOOMANY  PUTLINE PARM=PUTBLOK,OUTPUT=(PAROLD3,TERM,MULTLVL,INFOR),     X
               MF=(E,IOPL)
         BR    R3
*-------
*------- DAIR  FAIL
*-------
DARFL    ST    R15,MYRCP          SVC 99 RETURN CODE
         LA    R15,MYRCP
         ST    R15,DFRCP
         SLR   R15,R15            ADDR OF IKJEFF02
         ST    R15,MYJEFF02
         LA    R15,MYJEFF02
         ST    R15,DFJEFF02
*-------
         MVC   MYRFLSWT,=X'0032'  JUST WRITE MESSAGE - NO RETURN
         LA    R15,MYRFLSWT       SWITCHES
         ST    R15,DFIDP
         LA    R15,DFBUFS
         ST    R15,DFBUFP         DBUFS BUFFER
         LA    R15,RB1
         ST    R15,DFDAPLP
         LA    R1,DFDAPLP
         LINK  EP=IKJEFF18
         LTR   R15,R15
         BNZ   DAMNIT
         BR    R3
*-------
*-------       CONSTANT DATA AREAS
*-------
*-------       STRAY CONSTANTS
*-------
REFRDSN  DC    H'44',CL44' '       REFRESH   DSNAME
REFRDDN  DC    H'8',CL8' '         REFRESH   DDNAME
SRCLEVEL DC    C' '
         LTORG
*-------
*-------       PUTLINE STUFF
*-------
PUTBLOK  PUTLINE MF=L
*-------
PAROLD1  DC    A(PAROLD2),A(1),A(PARMSG1)
PAROLD2  DC    A(0),A(1),A(PARMSG2)
PARMSG1  DC    AL2(PARM1END-PARMSG1),AL2(0)
         DC    C'CLIB0001 THE PROGRAM REGRETS TO INFORM YOU '
         DC    C'IT IS UNABLE TO MAKE SENSE OF YOUR INPUT.'
PARM1END EQU   *
PARMSG2  DC    AL2(PARM2END-PARMSG2),AL2(0)
         DC    C' LORDY!  DO YOU  TYPE WITH YOUR ELBOWS?'
PARM2END EQU   *
*-------
PAROLD3  DC    A(PAROLD4),A(1),A(PARMSG3)
PAROLD4  DC    A(0),A(1),A(PARMSG4)
PARMSG3  DC    AL2(PARM3END-PARMSG3),AL2(0)
         DC    C'CLIB0002 THE PROGRAM REGRETS TO INFORM YOU '
         DC    C'THERE IS A LIMIT OF 16 FOR A PDS CONCATENATION'
PARM3END EQU   *
PARMSG4  DC    AL2(PARM4END-PARMSG4),AL2(0)
         DC    C' RETURN CODE = 8 '
PARM4END EQU   *
*-------
*-------       TEXT UNIT POINTERS
*-------
ALOCUPL  DC    A(V1TXT1)           DDNAME
         DC    A(V1TXT2)           DSNAME
         DC    A(V1TXT4)           SHR
         DC    A(V1TXT5)           KEEP
         DC    A(V1TXT6)           KEEP
         DC    AL1(128),AL3(V1TXT33) ,,,IN
ALOC2UPL DC    A(V1TXT2)           DSNAME
         DC    A(V1TXT4)           SHR
         DC    A(V1TXT5)           KEEP
         DC    A(V1TXT6)           KEEP
         DC    AL1(128),AL3(V1TXT85) RETURN DDNAME
CONCUPL  DC    A(V3TXT1)             LIST OF DDNAMES
         DC    AL1(128),AL3(V3TXT4)  PERMANANTLY ALLOCATED
SRCHUPL  DC    A(TXT4)
         DC    A(TXT5)
         DC    A(TXT13)
         DC    AL1(128),AL3(TXT15)
FREEUPL  DC    A(TXT1)                      DDNAME
         DC    AL1(128),AL3(TXT7)
*-------
*------- NON   REENTRANT DATA AREAS
*-------
         DS    0F
RBPTR    DC    AL1(128),AL3(RB1)
*-------       DYNAMIC ALLOCATION  REQUEST BLOCK
RB1      DC    AL1(20)
RBVERB   DC    AL1(7)              INFO REQUEST
RBFLAG1  DC    B'00100000',AL1(0)
         DC    A(0)
RBTEXTP  DC    A(SRCHUPL)
         DC    A(0)
         DC    A(0)
*-------       ALLOCATE TEXT UNITS
V1TXT1   DC    H'1',H'1',H'8',CL8' '        DDNAME
V1TXT2   DC    H'2',H'1',H'44',CL44' '      DATA SET NAME
V1TXT3   DC    H'3',H'1',H'8',CL8' '        MEMBER NAME
V1TXT4   DC    H'4',H'1',H'1',X'08'         SHR
V1TXT5   DC    H'5',H'1',H'1',X'08'         KEEP
V1TXT6   DC    H'6',H'1',H'1',X'08'         KEEP
V1TXT33  DC    H'33',H'1',H'1',X'80'        ,,,IN
V1TXT85  DC    H'85',H'1',H'8',CL8' '       RETURN DDNAME
*-------       CONCATENATE TEXT UNITS
V3TXT1   DC    H'1',H'2'                    DDNAMES
         DC    H'8',CL8'@0000001',H'8',CL8'@0000002'
         DC    H'8',CL8'@0000003',H'8',CL8'@0000004'
         DC    H'8',CL8'@0000005',H'8',CL8'@0000006'
         DC    H'8',CL8'@0000007',H'8',CL8'@0000008'
         DC    H'8',CL8'@0000009',H'8',CL8'@0000010'
         DC    H'8',CL8'@0000011',H'8',CL8'@0000012'
         DC    H'8',CL8'@0000013',H'8',CL8'@0000014'
         DC    H'8',CL8'@0000015',H'8',CL8'@0000016'
V3TXT4   DC    H'4',H'0'                    PERMANANTLY CONCATENATED
*-------       GENERAL     TEXT UNITS
TXT1     DC    H'1',H'1',H'8',CL8' '        DDNAME
TXT4     DC    H'4',H'1',H'8',CL8' '        RETURN DDNAME
TXT5     DC    H'5',H'1',H'44',CL44' '      RETURN DSNAME
TXT7     DC    H'7',H'0'                    UNALLOCATE DAMNIT
TXT13    DC    H'13',H'1',H'1',H'0'         RETURN LAST ENTRY
TXT15    DC    H'15',H'1',H'2',H'0'         SPECIFY RELATIVE REQUEST
         EJECT
*
         PUSH  PRINT
         PRINT ON,NOGEN
PARMDSN  IKJPARM  DSECT=PARANSWR
PARDSN   IKJPOSIT DSNAME,USID,PROMPT='DSNAME TO BE CONCATENATED'
PARPNT   IKJKEYWD
         IKJNAME  'FILE',SUBFLD=PARPN
         IKJNAME  'DD',SUBFLD=PARPN
         IKJNAME  'F',SUBFLD=PARPN
PARPN    IKJSUBF
PARPND   IKJPOSIT DSNAME,DDNAM,PROMPT='DDNAME OTHER THAN SYSPROC'
         IKJENDP
         POP   PRINT
*-------
*-------       REENTRANT DATA AREAS.  MY SETUP AND END MACROS PROVIDE
*-------       A REENTRANT WORK AREA,  BASED ON REG 13.
*-------
WKA      DSECT
WKASAVE  DS    18F
WKADDNL  DS    H                  THIS IS THE LENGTH AND DDNAME
WKADDNAM DS    CL8                TO WHICH WE'RE GONNA CONCATENATE
WKADSNL  DS    H                  THIS IS THE LENGTH AND DSNAME
WKADSNAM DS    CL44               WHICH WE'RE GONNA ADD
WKADBL   DS    D
WKAHITS  DS    F                   NUMBER OF CONCATENATED DSN'S  * 46
WKADSNS  DS    16CL46              SAVE CONCATENATED DSNAMES
         SPACE
*-------
ECB      DS    F
ANS      DS    F
PPLAREA  DS    7F
*-------
IOPL     DS    0F
IOPLUPT  DS    F
IOPLECT  DS    F
IOPLECB  DS    F
IOPLIOPB DS    F
*-------
MYRCP    DS    F
MYJEFF02 DS    F
MYRFLSWT DS    H
         PUSH  PRINT
         PRINT ON,NOGEN
         IKJEFFDF ,               DAIRFAIL PARAMETERS
WKLENGTH EQU   *-WKA
         DROP  R13
         EJECT
*-------
*-------                     * * *  DSECTS  * * *
*-------
         IKJPPL
         IEFZB4D0
         IEFZB4D2
         CVT   DSECT=YES
         POP   PRINT
         SPACE
         IKJCPPL
         END
