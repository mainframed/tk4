PRC      TITLE 'ZAP TO IGG01946 FOR TCAM OPENQ SMF RECORD.'
********************************************************************
*                                                                  *
* NAME - TCAM OPENQ SMF RECORD WRITER.                             *
*                                                                  *
* DESCRIPTION - THIS CODE WRITES AN SMF240 RECORD FOR EACH OPEN    *
*   OF AN TCAM APPLICATION Q.                                      *
*                                                                  *
* LEVEL - WE TESTED THIS EXIT WITH TCAM V2R4.  PLEASE NOTE THE USE *
*   OF MODLEV BELOW THAT INDICATES MAINTENANCE LEVEL OF THIS MODULE*
*   IN THE SMF RECORD.                                             *
*                                                                  *
* UPON ENTRY -                                                     *
*                                                                  *
* LOG -                                                            *
*   07/XX/86 PTW L01 IMPLEMENTATION                                *L01
*   05/21/87 PTW L02 REFIT FOR UY00456                             *L02
*                                                                  *
********************************************************************
*ZAP     CARD ++USERMOD(LM02702).         /* TCAM OPENQ WRITE SMF */
*ZAP     CARD ++VER(Z038) FMID(JTC2412) PRE(UY00456).
*ZAP     CARD ++ZAP (IGG01946).
*ZAP     CARD    NAME IGG01946
*ZAP     CARD  EXPAND IGG01946(450)
         LCLC  &CSECT
&CSECT   SETC  'IGG01946'          LIST OUTPUT CSECT NAME.
&CSECT   CSECT                     START THE CSECT.
*
RECTYPE  EQU   240                SMF RECORD NUMBER.
RECSTYPE EQU   2                  SMF RECORD SUBTYPE.
MODLEV   EQU   2                  THIS MODULE'S LEVEL.              L02
TCAMLEV  EQU   24                 VTAM VERSION/RELEASE.
*
A        EQU   &CSECT              FOR CONVENIENCE.
EXITTST  EQU   A+X'2E6'            BRANCH BACK LABEL.               L02
HOOK     EQU   A+X'7DE'            OFFSET TO 'STEAL' POINT.         L02
PATCH    EQU   A+X'8D4'            OFFSET TO 'PATCH' POINT.         L02
OFFSET   EQU   X'004'              OFFSET OF CSECT ADDRESSIBILITY.
* NOTE THAT WE ONLY DESTROY R0-R2, AND R14-R15 IN THIS ROUTINE.
RCORE    EQU   3                   A(OPEN WORKAREA).
RDCB     EQU   4                   A(DCB PROTECTED COPY).
RTCX     EQU   9                   A(TCX).
RBASE    EQU   12                  BASE REGISTER.
RTCB     EQU   13                  A(TCB).
TCXASCB  EQU   X'01C',4,C'A'       A(TCAM'S ASCB).
FORCORE  DSECT                     A FAKE DSECT FOR THE WORKAREA.
         ORG   *+X'20E'
DXUKEY   DS    X
&CSECT   CSECT                     START THE CSECT.
         USING A+OFFSET,RBASE      ADDRESSIBILITY.
         USING FORCORE,RCORE       WORKAREA.
         PRINT DATA
*ZAP START
*ZAP START VER
         ORG   HOOK
         B     EXITTST            OUR STOLEN INSTRUCTION.
         ORG   PATCH
         DC    XL4'00'
*ZAP START REP
         ORG   HOOK               HOOK POINT.
         B     DOSMF              BRANCH TO PATCH.
         ORG   PATCH              PATCH POINT.
DOSMF    DS    0H                 PATCH LABEL.
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         GETMAIN RU,LV=DYNLEN,SP=230 GETMAIN (SP230 TASK PRIV HI).
         LR    R2,R1              SAVE AREA
         USING DYNAMIC,R2
         LA    R0,DYNAMIC         ZERO
         LA    R1,DYNLEN            DYNAMIC
         LR    R14,R0                 STORAGE.
         SLR   R15,R15
         MVCL  R0,R14             DO IT.
********************************************************************
*                                                                  *
*        C R E A T E   S M F   R E C O R D                         *
*                                                                  *
********************************************************************
         MVI   SM240RTY,RECTYPE   RECORD TYPE 240.
         MVC   SM240TYP,=AL2(RECSTYPE) WE ARE SUBTYPE 2.
         LA    R14,S2402DAT       R14 = (SMF POINTER).
         MVC   S2402TRN,=AL2(S2402TR#) # TRIPLETS.
* CREATE SOFTWARE LEVEL SECTION OF SMF RECORD.
         LA    R0,S2402REC        CALCULATE AND STORE OFFSET.
         LR    R1,R14
         SLR   R1,R0
         ST    R1,S2402LVO
         MVC   S2402LVL,=AL2(S2402LLN) MOVE LENGTH.
         MVC   S2402LV#,=H'1'     ONLY 1 SECTION.
         USING S2402LEV,R14       USE LEV MAPPING.
         MVI   S2402LAM,S2402LAT  MOVE ACCESS METHOD.
         MVI   S2402LVR,TCAMLEV   MOVE VERSION/RELEASE.
         MVI   S2402LMD,MODLEV    OUR RECORD VERSION.
         DROP  R14
         LA    R14,S2402LLN(,R14) BUMP SMF POINTER.
* CREATE ACCOUNTING SECTION OF SMF RECORD.
         LA    R0,S2402REC        CALCULATE AND STORE OFFSET.
         LR    R1,R14
         SLR   R1,R0
         ST    R1,S2402ACO
         MVC   S2402ACL,=AL2(S2402ALN) MOVE LENGTH.
         MVC   S2402AC#,=H'1'     ONLY 1 SECTION.
         USING S2402ACT,R14       USE ACCT MAPPING.
         TIME  BIN                STORE TIME, DATE.                 L02
         STM   R0,R1,S2402ATM
         MVI   S2402ATY,S2402ATS  MOVE FUNCTION CODE.
         MVC   S2402APL,=CL8' '   INIT PLUNAME.
         L     R1,TCBTIO-T(,RTCB) R1 = A(TCBTIOT).
         AH    R1,DCBTIOT-D(,RDCB) R1 = A(TIOT ENTRY).
         ICM   R1,7,TIOEJFCB-TIOENTRY(R1) R1 = A(JFCB).
* MUST ADJUST A(JFCB) BY LENGTH OF HEADER IN NEXT INSTRUCTION.
         LA    R1,JFCBQNAM-J+X'10'(,R1)
         LA    R0,L'JFCBQNAM      THIS LOOP STRIPS OFF THE QUALIFIER
         LR    R15,R1               FROM THE QNAME.JOBNAME FORM OF
TESTQUAL DS    0H                   ALLOCATION.
         CLI   1(R15),C'.'        LOOK FOR '.'.
         BE    GOTQUAL            EXIT IF FOUND.
         LA    R15,1(,R15)        BUMP POINTER.
         BCT   R0,TESTQUAL        LIMIT OF 8 TIMES.
         BCTR  R15,0              ADJUST POINTER BACK.
GOTQUAL  DS    0H
         SLR   R15,R1             FIND LENGTH.
         EX    R15,MOVEQNAM       MOVE QNAME.
         MVC   S2402ASL,=CL8' '   MOVE SLUNAME.
         MVC   S2402AMJ,=CL8' '   INIT ACC METHOD JOBNAME.
         MODESET EXTKEY=ZERO      TCX IS FETCH PROTECTED.
         L     R1,TCXASCB(,RTCX)  R1 = A(TCAM ASCB).
         MODESET KEYADDR=DXUKEY,WORKREG=15 RETURN TO USER KEY.
         L     R15,ASCBJBNI-ASCB(,R1) IF A(JOB JOBNAME) NOT
         LTR   R15,R15                  BLANK, USE IT.
         BNZ   MOVEJOBM
         L     R15,ASCBJBNS-ASCB(,R1) ELSE TRY FOR START/MOUNT/
         LTR   R15,R15                  LOGON JOBNAME.
         BZ    *+10
MOVEJOBM MVC   S2402AMJ,0(R15)    MOVE IN ACCESS METHOD JOBNAME.
         MVC   S2402AAJ,=CL8' '   BLANK APPL JOBNAME.
         L     R1,PSAAOLD-P       R1 = A(CURRENT ASCB).
         L     R15,ASCBJBNI-ASCB(,R1) SAME AS ABOVE.
         LTR   R15,R15
         BNZ   MOVEJOBA
         L     R15,ASCBJBNS-ASCB(,R1)
         LTR   R15,R15
         BZ    *+10
MOVEJOBA MVC   S2402AAJ,0(R15)    MOVE IN ACCESS METHOD JOBNAME.
         DROP  R14
         LA    R14,S2402ALN(,R14) BUMP SMF POINTER.
         LA    R0,SM240ULN
         LR    R1,R14             CALCULATE L'USER SECTION.
         SLR   R1,R0
         STH   R1,SM240ULN        STORE L'USER SECTION IN SMF REC.
         LA    R1,S2402REC        R1 = A(SMF RECORD).
         SVC   226                CALL USER SVC ROUTINE.
         LTR   R15,R15            WTO IF BAD RC.
         BNZ   BADSVC
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
RETURN   DS    0H
         FREEMAIN RU,LV=DYNLEN,A=(R2),SP=230
         B     EXITTST
********************************************************************
*                                                                  *
*        E R R O R S                                               *
*                                                                  *
********************************************************************
BADSVC   DS    0H
*WTO 'IEDPRC5I NON-ZERO RC FROM PRC SMF SVC - NO ACCT RECORD WRITTEN.'2
         B     RETURN
********************************************************************
*                                                                  *
*        S T A T I C   A R E A                                     *
*                                                                  *
********************************************************************
MOVEQNAM MVC   S2402APL(*-*),0(R1)     MOVE PLUNAME.
         LTORG *
*ZAP END
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A S                                 *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
         PRINT GEN
S2402REC SMF240 DSECT=NO,SUBTYPE=2     TYPE 240 RECORD.
DYNLEN   EQU   *-DYNAMIC
         PRINT NOGEN
         IHAPSA
P        EQU   PSA
         IHAASCB
         IKJTCB
T        EQU   TCB
TI       DSECT
         IEFTIOT1
J        DSECT
         IEFJFCBN
         DCBD  DSORG=PS,DEVD=DA
D        EQU   IHADCB
         PRINT OFF
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11                 A(PLIST).
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
         PRINT ON
         END   &CSECT
