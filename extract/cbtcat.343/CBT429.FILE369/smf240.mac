         MACRO
&SMF240N SMF240 &DSECT=YES,&SUBTYPE=
.**********************************************************************
.*                                                                    *
.* NAME         SMF240                                                *
.*                                                                    *
.* COMPONENT    UTILITY                                               *
.*                                                                    *
.* FUNCTION     MAPS USER SMF RECORD AND SUBTYPES                     *
.*                                                                    *
.* PARAMETERS   DSECT=YES       CREATE DSECT CARD                     *
.*              SUBTYPE=        EXPAND SELECTED SUBTYPE DEFINITION    *
.*                                                                    *
.* RESTRICTIONS NONE                                                  *
.*                                                                    *
.* NOTES        USED WITH LOCAL SVC 226                               *
.*                                                                    *
.*              SUBTYPE DESCRIPTION                                   *
.*                 1    USPTO SYSTEM MANAGEMENT SUBSYSTEM             *
.*                      (GENERALLY CREATED WITH A PL/I PROGRAM        *
.*                       WHICH CONTAINS THE DETAILED DESCRIPTION)     *
.*                 2    VTAM SESSION ACCOUNTING                       *
.*                 3    USPTO MESSENGER ACCOUNTING                    *
.*                 4    USPTO ACF2 SHARED LOGONID USE                 *
.*                                                                    *
.* SOURCE LEVEL                                                       *
.*                                                                    *
.* AUTHOR       DICK SZIEDE CSC  1972                                 *
.*                                                                    *
.* MAINTENANCE  WHO   DATE     DESCRIPTION                            *
.*              RRS   1985     CHANGE RECORD TYPE FORM 230 TO 240     *
.*              PTW   1986     ADDED SUBTYPES                         *
.*              CJP    4/ 8/87 ADDED SUBTYPE 4, DOCUMENTED SUBTYPE 1  *
.*              CJP    1/20/88 ADDED SYMBOL AT START OF AREA FOR      *
.*                             DSECT=NO                               *
.*                                                                    *
.**********************************************************************
.*
         AIF   ('&DSECT' EQ 'NO').SM240A
SM240BLK DSECT ,                   DESCRIBE SMF 240 HEADER AND SUBTYPE
         AGO   .SM240C
.SM240A  ANOP
SM240BLK DS    0F
.SM240C  ANOP
.*
SM240MLN EQU   4095                MAXIMUM LENGTH OF WHOLE RECORD
&SMF240N DS    0H
SM240LEN DS    H                   RDW
SM240SEG DS    H                   RDW
SM240FLG DS    XL1                 SYSTEM INDICATOR
SM240RTY DS    XL1                 RECORD TYPE MUST BE 240
SM240TME DS    XL4                 TIME RECORD MOVED TO BUFFER
SM240DTE DS    XL4                 DATE IN PACKED DECIMAL
SM240SID DS    CL4                 SYSTEM ID
SM240RST DS    XL4                 TIME AND DATE READER RECOGNIZED
SM240RSD DS    XL4                     JOB CARD FOR THIS JOB
SM240ULN DS    H                   PRC USER RECORD LENGTH
SM240TYP DS    H                   PRC USER RECORD SUBTYPE
SM240USE DS    0X                  PRC USER RECORD AREA START
SM240HLN EQU   *-SM240BLK          COMMON HEADER LENGTH
.*
         AIF   ('&SUBTYPE' EQ '1').DOSTYP1
         AIF   ('&SUBTYPE' EQ '2').DOSTYP2
         AIF   ('&SUBTYPE' EQ '3').DOSTYP3
         AIF   ('&SUBTYPE' EQ '4').DOSTYP4
         AGO   .MEND
.*
.*             TYPE 1 RECORDS (USPTO SMS)
.*
.DOSTYP1 ANOP
         ORG   SM240USE
S2401    DS    0F                 START OF SUBTYPE 1 RECORD
S2401DLN EQU   SM240MLN-(*-SM240BLK)  MAXIMUM LENGTH OF VARIABLE DATA
S2401DAT DS    XL(S2401DLN)       VARIABLE DATA
S2401LEN EQU   *-SM240BLK         MAXIMUM LENGTH OF SUBTYPE 1
         AGO   .MEND
.*
.*             TYPE 2 RECORDS (VTAM/TCAM SESSION)
.*
.DOSTYP2 ANOP
         ORG   SM240USE
S2402    DS    0F                  START OF SUBTYPE 2 RECORD
S2402TRN DS    XL2                 NUMBER OF TRIPLETS THAT FOLLOW
S2402TR# EQU   8
         DS    XL2                 RESERVED
S2402LVO DS    XL4                 OFFSET SOFTWARE LVL SECT (WITH RDW)
S2402LVL DS    XL2                 LENGTH SOFTWARE
S2402LV# DS    XL2                 NUMBER SOFTWARE
S2402ACO DS    XL4                 OFFSET ACCOUNTING SECTION (WITH RDW)
S2402ACL DS    XL2                 LENGTH ACCOUNTING
S2402AC# DS    XL2                 NUMBER ACCOUNTING
*              FOLLOWING TRIPLETS RESERVED FOR FUTURE USE
S2402SEO DS    XL4                 OFFSET SESSION SECTION (WITH RDW)
S2402SEL DS    XL2                 LENGTH SESSION
S2402SE# DS    XL2                 NUMBER SESSION
S2402EVO DS    XL4                 OFFSET EVL
S2402EVL DS    XL2                 LENGTH EVL
S2402EV# DS    XL2                 NUMBER EVL
S2402PRO DS    XL4                 OFFSET PRV
S2402PRL DS    XL2                 LENGTH PRV
S2402PR# DS    XL2                 NUMBER PRV
S2402SRO DS    XL4                 OFFSET SRV
S2402SRL DS    XL2                 LENGTH SRV
S2402SR# DS    XL2                 NUMBER SRV
S2402OGO DS    XL4                 OFFSET OGV
S2402OGL DS    XL2                 LENGTH OGV
S2402OG# DS    XL2                 NUMBER OGV
S2402DGO DS    XL4                 OFFSET DGV
S2402DGL DS    XL2                 LENGTH DGV
S2402DL# DS    XL2                 NUMBER DGV
S2402TLN EQU   *-S2402TRN          LENGTH OF TRIPLET SECTION
S2402DAT EQU   *                   VARIABLE DATA
         SPACE
S2402LEV EQU   *                   SOFTWARE LEVEL SECTION MAP
S2402LAM DS    X                   ACCESS METHOD
S2402LAV EQU   X'01'               VTAM
S2402LAT EQU   X'02'               TCAM
S2402LVR DS    X                   VERSION/RELEASE
S2402LMD DS    X                   OUR RECORD LEVEL
         DS    X                   RESERVED
S2402LLN EQU   *-S2402LEV
         SPACE
S2402ACT EQU   *                   SOFTWARE LEVEL SECTION MAP
S2402ATM DS    XL8                 TIME, DATE EXIT ENTERED
S2402ATY DS    X                   TYPE OF RECORD (SESSION START/END)
S2402ATS EQU   X'01'               START
S2402ATE EQU   X'02'               END
         DS    X                   RESERVED
S2402APL DS    CL8                 PRIMARY LUNAME
S2402ASL DS    CL8                 SECONDARY LUNAME (VTAM ONLY)
S2402AMJ DS    CL8                 ACCESS METHOD JOBNAME
S2402AAJ DS    CL8                 APPLICATION JOBNAME (TCAM ONLY)
S2402ALN EQU   *-S2402ACT          LENGTH
S2402LEN EQU   *-SM240BLK          LENGTH OF SUBTYPE 2 RECORD
         AGO   .MEND
.*
.*             TYPE 3 RECORDS (MESSENGER STATS)
.*
.DOSTYP3 ANOP
         ORG   SM240USE
S2403    DS    0F                  START OF SUBTYPE 3 RECORD
S2403TYP DS    FL4                 CAS SUBTYPE CODE
S2403RS1 DS    FL4                 RESERVED - SET TO 0
S2403DLN EQU   SM240MLN-(*-SM240BLK)  MAXIMUM LENGTH OF VARIABLE DATA
S2403DAT DS    XL(S2403DLN)        VARIABLE ARBITRARY LENGTH DATA
S2403LEN EQU   *-SM240BLK          LENGTH OF SUBTYPE 3 RECORD
         AGO   .MEND
.*
.*             TYPE 4 RECORDS (ACF2 SHARED LOGON ID USE)
.*
.DOSTYP4 ANOP
         ORG   SM240USE
S2404    DS    0F                  START OF SUBTYPE 4 RECORD
S2404SLI DS    CL8                 SHARED LOGON ID BEING USED
S2404RLI DS    CL8                 REAL LOGON ID OF USER
S2404TRM DS    CL8                 TERMINAL USED
S2404DLN EQU   *-SM240USE          LENGTH OF SUBTYPE 4 FIELDS
S2404LEN EQU   *-SM240BLK          LENGTH OF SUBTYPE 4 RECORD
         AGO   .MEND
.*
.*
.MEND    MEND
