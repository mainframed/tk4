         TITLE 'AC#VIOX -- ACF2 VIOLATION EXIT'
***********************************************************************
*                                                                     *
*                                                                     *
*        MODULE NAME = AC#VIOX                                        *
*                                                                     *
*        DESCRIPTIVE NAME = ACF2 DATASET VIOLATION EXIT               *
*                                                                     *
*        AUTHOR = RUTH STUBBLEFIELD   DATE = MAY 19, 1981             *
*                                                                     *
*        FUNCTION =                                                   *
*                                                                     *
*           ALLOW USER SERVICES ANALYSTS TO DEFINE AND DELETE         *
*           ALIASES IN A VSAM MASTER CATALOG.                         *
*                                                                     *
*        OPERATION =                                                  *
*                                                                     *
*           THE ROUTINE CHECKS THE ACF2 VIOLATION TO DETERMINE        *
*           WHETHER IT IS FROM VSAM CATALOG MANAGEMENT. IF SO,        *
*           IT THEN CHECKS TO SEE WHETHER THE REQUEST IS TO DEFINE    *
*           OR DELETE AN ALIAS. IF YES, IT CHECKS TO SEE              *
*           IF THE USER HAS ACCOUNT AUTHORITY. IF ALL                 *
*           CONDITIONS MATCH, THE ACCESS IS ALLOWED.                  *
*                                                                     *
*        REGISTER CONVENTIONS = STANDARD CONVENTIONS.                 *
*           REGISTER  0 = UNUSED                                      *
*           REGISTER  1 = ADDRESS OF PARM LIST                        *
*           REGISTER  2 = UNUSED                                      *
*           REGISTER  3 = ADDRESS OF ACSXP                            *
*           REGISTER  4 = ADDRESS OF CATALOG PARM LIST                *
*           REGISTER  5 = ADDRESS OF ACUCB                            *
*           REGISTER  6 = ADDRESS OF UID STRING                       *
*           REGISTER  7 = WORK REGISTER                               *
*           REGISTER  8 = ADDRESS OF FIELD VECTOR TABLE               *
*           REGISTER  9 = ADDRESS OF LIDREC  ***MGK*** 3.20.84        *
*           REGISTER 10 = UNUSED                                      *
*           REGISTER 11 = UNUSED                                      *
*           REGISTER 12 = BASE REGISTER                               *
*           REGISTER 13 = SAVE AREA POINTER                           *
*           REGISTER 14 = RETURN ADDRESS                              *
*           REGISTER 15 = ENTRY POINT ADDDRESS                        *
*                                                                     *
*        MODULE TYPE = EXIT                                           *
*                                                                     *
*               PROCESSOR = ASM                                       *
*                                                                     *
*               ATTRIBUTES = REENTRANT                                *
*                                                                     *
*        ENTRY POINTS = AC#VIOX                                       *
*                                                                     *
*               LINKAGE FROM = ACF2 SVC                               *
*                                                                     *
*        INPUT =                                                      *
*                                                                     *
*               REGISTER 1 - POINTER TO ACSXP PARM LIST               *
*                                                                     *
*        OUTPUT =                                                     *
*                                                                     *
*               REGISTER 15 =  0--> CONTINUE NORMAL ACF2 PROCESSING   *
*                           = 12--> ALLOW ACCESS                      *
*                                                                     *
*        EXIT - NORMAL = RETURN TO ACF2                               *
*                                                                     *
*               OUTPUT = RETURN CODE                                  *
*                                                                     *
*               RETURN CODE = 0 OR 12                                 *
*                                                                     *
*        EXIT - ERROR = NONE                                          *
*                                                                     *
*        EXTERNAL REFERENCES =                                        *
*                                                                     *
*               ROUTINES = NONE                                       *
*                                                                     *
*               DATA AREAS = IEZCTGPL                                 *
*                            IEZCTGFV                                 *
*                            ACSXP                                    *
*                                                                     *
*               CONTROL BLOCKS = ACUCB                                *
*                                                                     *
*               TABLES = NONE                                         *
*                                                                     *
*        MACROS USED =                                                *
*                  MACRO           DSNAME                             *
*                  --------        --------------------               *
*                  ACUCB           SYSA.ACF2.R40.ACFMAC               *
*                  ACSXP           SYSA.ACF2.R40.ACFMAC               *
*                  ACFREGS         SYSA.ACF2.R40.ACFMAC               *
*                  LIDREC          SYSA.ACF2.R40.ACFMAC               *
*                                                                     *
*        CHANGE ACTIVITY =                                            *
*                  NAME            DATE                               *
*                  MGK             03/21/84                           *
*                                   CHANGES ALLOWS ACCOUNT USER TO    *
*                                   DEFINE AND DELETE ALIAS USING AMS *
*                                   WITHOUT ABORTING.  RETURN CODE    *
*                                   OF 12 GENERATED TO ALLOW PROCESS- *
*                                   ING TO CONTINUE IN ADDITIONAL TO  *
*                                   LOGGING THE ACTION.               *
*                  CJP             09/23/85                           *
*                                   CLEANUP UNUSED PORTIONS, UPDATE   *
*                                   COMMENTS FOR PRC, DULLES.         *
*                                                                     *
*        MESSAGES = NONE                                              *
*                                                                     *
*        ABEND CODES = NONE                                           *
*                                                                     *
***********************************************************************
         SPACE 2
AC#VIOX  CSECT
         SAVE  (14,12),,AC#VIOX.&SYSDATE..&SYSTIME
         USING AC#VIOX,R12             CSECT ADDRESSABILITY
         USING ACSXP,R3                PARM LIST ADDRESSABILITY
         LR    R12,R15                 LOAD BASE REGISTER
         LR    R3,R1                   LOAD PARM LIST ADDRESS
         SPACE 2
         TM    ACUUFLG1,ACUU1ABE       ABNORMAL CONDITION FOUND?
         BO    RETURN                  YES, GO RETURN
         SPACE 2
         CLI   ACUUFLG3,ACUU3CMS       ENTERED FROM AMS CATLG MGMT?
         BNE   RETURN                  NO, GO RETURN
         SPACE 2
*        CHECK PARAMETERS IN CATALOG PARAMETER LIST
         SPACE 1
         L     R4,ACUUCMS              LOAD ADDRESS OF CTG PL
         USING IEZCTGPL,R4             ESTABLISH ADDRESSABILITY
         IC    R7,CTGOPTNS             LOAD OPTION BYTE
         N     R7,MASK                 ZERO OUT RESERVED BITS
         CLM   R7,1,=AL1(CTGDEFIN)     IS REQUEST FOR DEFINE?
         BNE   CHKDELET                NO, GO CHECK DELETE
         TM    CTGOPTN1,CTGNAME        IS THERE A FIELD VECTOR TBL?
         BNO   RETURN                  NO, GO RETURN
         SPACE 1
         L     R8,CTGFVT               LOAD ADDRESS OF FVT
         USING IEZCTGFV,R8             ESTABLISH ADDRESSABILITY
         CLI   CTGFVTYP,CTGFVANM       IS REQUEST FOR AN ALIAS RECORD?
         BNE   RETURN                  NO, GO RETURN
         B     CHKUID                  YES, CHECK UID STRING
         DROP  R8
         SPACE 2
*        CHECK FOR DELETE REQUEST
         SPACE 1
CHKDELET EQU   *
         CLM   R7,1,=AL1(CTGDELET)     IS REQUEST FOR DELETE?
         BNE   RETURN                  NO, GO RETURN
         SPACE 1
         CLI   CTGTYPE,CTGTANM         IS REQUEST FOR AN ALIAS RECORD?
         BNE   RETURN                  NO, GO RETURN
         DROP  R4
         SPACE 2
*        DETERMINE WHETHER USER IS IN USER SERVICES
         SPACE 1
CHKUID   EQU   *
         L     R5,ACUUAUCB             LOAD ADDRESS OF ACUCB
         USING ACUCB,R5                ESTABLISH ADDRESSABILITY
         SPACE 1
         L     R9,ACULRECP             LOAD ADDRESS OF LOGONREC
         USING LIDREC,R9               ESTABLISH ADDRESSIBLITY
         TM    LIDTFLAG,LIDTACT        IS REQUEST BY ACCNT USER?
         BNO   RETURN                  NO, GO RETURN
         DROP  R5,R9
         SPACE 2
*        ALLOW ACCESS
         SPACE 1
         RETURN (14,12),RC=12
         SPACE 2
*        CONTINUE NORMAL ACF2 PROCESSING AND ABORT THE REQUEST
         SPACE 1
RETURN   EQU   *
         RETURN (14,12),RC=0
         EJECT
*        CONSTANTS AND EQUATES
         DS    0F                      ALIGN ON FULLWORD
MASK     DC    X'000000F8'             AND MASK FOR CTGOPTNS FIELD
         SPACE 2
         LTORG
         SPACE 2
*        DSECTS
         SPACE 2
         ACUCB DSECT=YES               USER CONTROL BLOCK
         SPACE 2
         ACSXP DSECT=YES               DS EXIT PARAMETER LIST
         SPACE 2                                                MGK
         LIDREC DSECT=YES,SIZE=1024                             MGK
         SPACE 2
*        AMS CATALOG PARAMETER LIST
         SPACE 2
IEZCTGPL DSECT
CTGOPTN1 DS    X                       OPTION BYTE 1
CTGNAME  EQU   X'04'                   CTGFVT POINTS TO FVT
         DS    XL3                     FILLER
CTGFVT   DS    F                       ADDRESS OF FIELD VECTOR TABLE
CTGFILL  DS    XL8                     FILLER
CTGOPTNS DS    X                       CMS REQUEST OPTIONS
CTGDEFIN EQU   X'08'                   DEFINE
CTGDELET EQU   X'18'                   DELETE
         DS    X                       RESERVED
CTGTYPE  DS    X                       TYPE OF CATALOG RECORD
CTGTANM  EQU   C'X'                    ALIAS NAME
         DS    XL13                    FILLER
         SPACE
*        AMS FIELD VECTOR TABLE
         SPACE
IEZCTGFV DSECT
CTGFVTYP DS    X                       RECORD TYPE
CTGFVANM EQU   C'X'                    ALIAS NAME
CTGFVFIL DS    XL91                    FILLER
         SPACE 2
         ACFREGS
         SPACE 2
         END   AC#VIOX
