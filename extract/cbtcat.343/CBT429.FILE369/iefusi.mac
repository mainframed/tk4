         TITLE 'MEMLIMIT - IEFUSI EXIT TO SET REGION AND GETMAIN PARAME+
               TERS'
***********************************************************************
*                                                                     *
* NAME         MEMLIMIT                                               *
*                                                                     *
* COMPONENT    SYSTEM IEFUSI EXIT                                     *
*                                                                     *
* FUNCTION     SET LIMITS FOR REGION AND GETMAINS.                    *
*                                                                     *
* ENTRY        FROM IEFSMFIE                                          *
*                                                                     *
* PARAMETERS   SEE PARMLIST DSECT                                     *
*                                                                     *
* INPUT        PARMLIST                                               *
*                                                                     *
* OUTPUT       UPDATED VSM PARAMETER LIST                             *
*                                                                     *
* RETURN CODES ZERO                                                   *
*                                                                     *
* EXT REF      NONE                                                   *
*                                                                     *
* MESSAGES     NONE                                                   *
*                                                                     *
* MACROS       SAVE                                                   *
*              REGS                                                   *
*              RETURN                                                 *
*                                                                     *
* TABLES       NONE                                                   *
*                                                                     *
* OPERATION    THE IEFUSI LIMIT FLAG IS SET ON IN THE VSM             *
*              PARAMETER LIST. THIS ENABLES VSM LOGIC TO SET          *
*              REGION AND GETMAIN LIMITS. NO SPECIFIC VALUES ARE      *
*              SET.                                                   *
*                                                                     *
* ATTRIBUTES   REENTRANT                                              *
*              REUSABLE                                               *
*              SUPERVISOR STATE                                       *
*              KEY ZERO                                               *
*              AUTHORIZED                                             *
*              LPALIB                                                 *
*              AMODE ANY                                              *
*              RMODE ANY                                              *
*                                                                     *
* RESTRICTIONS NONE                                                   *
*                                                                     *
* NOTES        THIS EXIT ADDRESSES A PROBLEM WHICH CAUSES ADDRESS     *
*              SPACES TO FAIL WHEN A V-FORM GETMAIN IS ISSUED         *
*              WHICH GETS THE ENTIRE PRIVATE AREA BELOW THE 16M       *
*              LINE. ITS PURPOSE IS TO RESERVE FOR SYSTEM USE         *
*              (EG, ABTERM) A REASONABLE AMOUNT OF STORAGE.           *
*                                                                     *
* PROGRAMMER   C. J. PITTS                                            *
*                                                                     *
* MAINTENANCE  WHO    DATE     DESCRIPTION                            *
*                                                                     *
**********************************************************************/
*                                                                     *
* REGISTERS AT ENTRY                                                  *
*              R1    PARM LIST POINTER                                *
*              R13   SAVE AREA                                        *
*              R14   RETURN ADDRESS                                   *
*              R15   ENTRY POINT                                      *
*                                                                     *
**********************************************************************/
         EJECT
MEMLIMIT CSECT
         SPACE
MEMLIMIT AMODE ANY
MEMLIMIT RMODE ANY
         SPACE
IEFUSI   DS    0H
         ENTRY IEFUSI
         SAVE  (14,12),,MEMLIMIT.&SYSDATE..&SYSTIME
         SPACE
         LR    R11,R15             SET UP
         USING MEMLIMIT,R11        REG 11 AS BASE
         SPACE
         USING PARMLIST,R1
         L     R2,VSMLISTA         VSM LIST PARAMETER
         USING VSMLIST,R2
         LA    R2,0(,R2)           PURIFY R2 AGAINST ADDRESS MODE ERROR
         SPACE
         OI    VSMFLAG1,VSMFUSIL   TELL VSM IEFUSI IS SETTING LIMIT
         SPACE
         RETURN (14,12),RC=0
         SPACE 2
         LTORG
         EJECT
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
         SPACE
PARMLIST DSECT
SMFPA    DS    A                   COMMON SMF EXIT PARM AREA ADDRESS
STEPNMA  DS    A                   STEP NAME ADDRESS
PROGNMA  DS    A                   PROGRAM NAME ADDRESS
ACCTA    DS    A                   ACCOUNTING INFO ADDRESS
VSMLISTA DS    A                   VSM PARM LIST ADDRESS
SRMINFOA DS    A                   SRM INFO WORD ADDRESS
         SPACE
VSMLIST  DSECT
VSMFLAGS DS    0XL4                FLAGWORD
VSMFLAG1 DS    X                   FLAG BYTE 1
VSMFUSIL EQU   X'80'               IEFUSI SUPPLYING REGION LIMIT VALUES
VSMFNOLO EQU   X'40'               NO VSM CHECK FOR FIT BELOW 16M
VSMFGOHI EQU   X'20'               VSM CHECK FOR FIT ABOVE 16M
VSMFLAG2 DS    X                   RESERVED
VSMFLAG3 DS    X                   RESERVED
VSMFLAG4 DS    X                   RESERVED
VSMREGRQ DS    F                   REGION SIZE REQUESTED
VSMGMLO  DS    F                   GETMAIN LIMIT BELOW 16M
VSMREGLO DS    F                   REGION SIZE BELOW 16M
VSMGMHI  DS    F                   GETMAIN LIMIT ABOVE 16M
VSMREGHI DS    F                   REGION SIZE ABOVE 16M
         SPACE
         REGS    ,                 REGISTER EQUATE
         SPACE 3
         END
