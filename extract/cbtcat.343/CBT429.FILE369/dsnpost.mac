PRC      TITLE 'DSNPOST - ACF2 DSN POST-PROCESSING EXIT.'
********************************************************************
*                                                                  *
* NAME - DSNPOST.                                                  *
*                                                                  *
* DESCRIPTION -                                                    *
*   THIS IS AN ACF2 DSN POST-PROCESSING EXIT. WE USE THIS EXIT     *
*   TO OVERRIDE A PARTICULAR VIOLATION. WE ATTEMPTED TO LIMIT      *
*   ACCESS TO THE JES2 SPOOL AND CHECKPOINT DATASETS FROM          *
*   SDSF VIA ACF2'S PROGRAM PATHING FACILITY, BUT THE LIMITATIONS  *
*   OF THIS FACILITY ESPECIALLY WITHIN  ISPF DIALOGS BECAME        *
*   EVIDENT (YOU HAVE TO ACCURATELY DESCRIBE THE TASK AND RB       *
*   CHAINS FOR EVERY DIALOG THAT WILL BE USED). THUS WE ATTEMPT    *
*   TO OVERRIDE VIOLATIONS AGAINST THE JES2 DATASETS THAT ARE      *
*   REALLY NORMAL SDSF ACCESSES.  WE CHECK FOR OPEN FOR INPUT      *
*   ACCESSES TO FILES HASPCKPT OR HASPAC00, AND THEN CHECK FOR     *
*   THE PRESENCE OF AN AUTHORIZED LIBRARY VERSION OF ISFJINIT.     *
*                                                                  *
* ON ENTRY -                                                       *
*   R1 = A(ACSXP).                                                 *
*                                                                  *
* ON EXIT -                                                        *
*   R15 = 0  - LEAVE ACF2 DECISION ALONE.                          *
*   R15 = 12 - ALLOW BUT LOG ACCESS.                               *
*                                                                  *
* TO TEST ON THE FLY -                                             *
*   FROM TSO:                                                      *
*     ACF                                                          *
*     SET CONTROL(GSO)                                             *
*     LIST EXITS SYSID(****)                                       *
*     CH SYSID(****) EXITS DEL DSNPOST()                           *
*     END                                                          *
*   FROM THE CONSOLE:                                              *
*     F ACF2,REFRESH(EXITS),SYSID(****)                            *
*     R XX,USERID                                                  *
*     R XX,PASSWORD (SUPPRESSED)                                   *
*   OMEGAMON OUT THE LOAD MODULE                                   *
*   OMEGAMON IN THE NEW LOAD MODULE...                             *
*   FROM TSO:                                                      *
*     ACF                                                          *
*     SET CONTROL(GSO)                                             *
*     LIST EXITS SYSID(****)                                       *
*     CH SYSID(****) EXITS REP DSNPOST(DSNPOST)                    *
*     END                                                          *
*   FROM THE CONSOLE:                                              *
*     F ACF2,REFRESH(EXITS),SYSID(****)                            *
*     R XX,USERID                                                  *
*     R XX,PASSWORD (SUPPRESSED)                                   *
*                                                                  *
* NOTES -                                                          *
*   WE ARE REENTRANT AND SHOULD BE INSTALLED IN LPALIB.            *
*   WE NEED ACFMAC, AMODGEN, AND MACLIB TO ASSEMBLE.               *
*                                                                  *
* LOG -                                                            *
*   09/25/88 L01 PTW IMPLEMENTED                                   *L01
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'DSNPOST'
&CSECT   CSECT ,
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15
         USING &CSECT,R12
********************************************************************
*                                                                  *
*        M A I N L I N E                                           *
*                                                                  *
********************************************************************
         LR    R11,R1             R1 = A(ACSXP).
         USING ACSXP,R11
         TM    ACUUFLG1,ACUU1ABE  IF ACF2 IS ABENDING,
         BO    RETURN               GET LOST.
*        L     R2,ACUUAUCB        R2 = A(AUCB).
*        USING ACUCB,R2
*        CLC   =CL8'PTR001',ACULID TESTING ONLY
*        BNE   RETURN
*        DROP  R2
DOSDSF   DS    0H
         TM    ACUUFLG1,ACUU1VIO  IF NOT A VIOLATION,
         BZ    RETURN               RETURN.
         CLI   ACUURRET,ACUURNAR  NO RULE APPLIES?
         BNE   RETURN               IF A RULE APPLIES, LEAVE.
         CLI   ACUUFLG3,ACUU3OPN  IF NOT AN OPEN REQUEST,
         BNE   RETURN               RETURN.
         CLI   ACUUFLG4,ACUU4INP  IF NOT AN INPUT REQUEST,
         BNE   RETURN               RETURN.
         L     R2,ACUUDDNP
         CLC   =CL8'HASPCKPT',0(R2) DDNAMES USED BY SDSF.
         BE    TESTRB
         CLC   =CL8'HASPAC00',0(R2)
         BE    TESTRB
RETURN   DS    0H
         RETURN (14,12),RC=0
TESTRB   DS    0H
         L     R2,PSATOLD-PSA     CHECK PR RB OFF TASK.
         L     R2,TCBRBP-TCB(,R2) R2 = A(RB).
         USING RBSECT,R2
RBLOOP   DS    0H
         TM    RBSTAB1,RBFTP      WE ONLY CARE ABOUT PRB'S.
         BZ    GOTRB
         TM    RBSTAB2,RBTCBNXT   IF LAST RB,
         BO    RETURN               GET LOST.
         L     R2,RBLINK          LOOP.
         B     RBLOOP
GOTRB    DS    0H                 LOOK FOR ISFJINIT.
         SLR   R3,R3
         ICM   R3,7,RBCDE1        R3 = A(CDE).
         BZ    RETURN
         USING CDENTRY,R3
         CLC   =CL8'ISFJINIT',CDNAME
         BNE   RETURN
GOTCDE   DS    0H
         TM    CDATTR,CDMIN       NO IDENTIFY'S ALLOWED.
         BO    RETURN
         TM    CDATTR2,CDSYSLIB   MAKE SURE AN AUTHORIZED LIB.
         BZ    RETURN
         TM    CDATTR,CDREN       AND MODULE IS REENTRANT.
         BZ    RETURN
         RETURN (14,12),RC=12
         DROP  R2,R3
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
SRCLEVEL DC    C'                                        '
         LTORG *
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A S.                                *
*                                                                  *
********************************************************************
         ACSXP  ,
         ACUCB  ,
         IHAPSA ,
         IKJTCB ,
         IKJRB  ,
         IHACDE ,
         PRINT OFF
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT ON,NOGEN
         END   &CSECT
