         TITLE 'WTO    - OPERATIONS COMMUNICATION'
*/* LIB: IPO1.SAMPLIB(WTO)
*/* GDE: CBIPO MVS CUSTOMIZATION
*/* DOC: THIS PROGRAM PROVIDES PROGRAMMER TO OPERATOR COMMUNICATION.
***********************************************************************
*                                                                     *
*             MODULE NAME = WTO                                       *
*                                                                     *
*             DESCRIPTIVE NAME =  CBIPO-SUPPLIED PROGRAM FOR          *
*                            PROGRAMMER TO COMMUNICATE WITH OPERATOR  *
*                                                                     *
*             FUNCTION =                                              *
*                SERVICE THE COMMUNICATIONS NEEDS OF A PROGRAM TO THE *
*                CONSOLE OPERATOR BY READING SYSIN AND ISSUING WTO'S  *
*                TO DISPLAY THESE MESSAGES TO THE OPERATOR.  THE      *
*                PROGRAM THEN ISSUES A WTOR:                          *
*               'ENTER C TO CONINUE PROCESSING OR R TO REPEAT DISPLAY'*
*
*             OPERATION =                                             *
*        WTO     READS CARD IMAGES FROM SYSIN AND THEN                *
*        (1) DISPLAYS THEM TO THE OPERATOR USING WTO MACRO            *
*        (2) WRITES THEM TO SYSPRINT USING PUT MACRO                  *
*        (3) ISSUES A WTOR MACRO TO LET THE OPERATOR CONTINUE         *
*            PROCESSING (REPLY C) OR TO REPEAT DISPLAY (REPLY R)      *
*        NOTE THAT IF A PARM IS PASSED, IT IS USED AS FIRST MESSAGE   *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER IS EBCDIC.  REASSEMBLE     *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = MAY SUPPLY BLKSIZE ON    CJP - 7AUG86*
*                    SYSPRINT DD CARD, E.G.               CJP - 7AUG86*
*                    DCB=(BLKSIZE=12800)                  CJP - 7AUG86*
*                    SYSIN RECFM MUST BE F OR FB          CJP - 7AUG86*
*                                                                     *
*                PATCH LABEL = N/A                                    *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 1K                                     *
*                                                                     *
*                ATTRIBUTES = PROTECT KEY 8, NONREUSABLE              *
*                                                                     *
*             ENTRY POINTS = WTO      (ONLY ENTRY POINT)              *
*                                                                     *
*                LINKAGE =  // EXEC PGM=WTO                           *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO PARM FIELD ON EXEC CARD    *
*                                                                     *
*             OUTPUT = WTO OF PARM FIELD IF AVAILABLE                 *
*                      WTO OF SYSIN CARD IMAGES                       *
*                      WTOR TO OPERATOR ASKING FOR PERMISSION TO      *
*                           CONTINUE PROCESSING OR REDISPLAY MSG.     *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*             EXTERNAL REFERENCES =  NONE                             *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = WTO,WTOR                                       *
*                                                                     *
*             CHANGE ACTIVITY                                         *
*                7AUG86  CJ PITTS   CHANGED ML EQUATE TO 128          *
*                                   INCREASED SIZE OF IPOWTOL         *
*                                   INCREASED SIZE OF IPOPAD          *
*                                   ADDED SOME PRINT NOGEN/GEN        *
*                                   ADDED SYSPRINT LRECL & RECFM      *
*                                   ADDED SYSIN RECFM                 *
*                                                                     *
*             MESSAGES = NO ERROR MESSAGES ISSUED                     *
*                                                                     *
*             ABEND CODES = 013 IF DCB ATTRIBUTES NOT SUPPLIED ON     *
*                           SYSPRINT DD CARD  >>RELAXED - CJP - 7AUG86*
*                                                                     *
***********************************************************************
*        REGISTER USAGE FOLLOWS...                                    *
R0       EQU   0                   WORK REG                           *
R1       EQU   1                   PTR TO INPUT PARM                  *
R2       EQU   2                   WORK REG                           *
R11      EQU   11                  PTR TO FIRST MESSAGE IF NOZERO PARM*
R12      EQU   12                  BASE REG                           *
R13      EQU   13                  PTR TO IPOSAVE                     *
R14      EQU   14                  LINKAGE REG                        *
R15      EQU   15                  LINKAGE REG                        *
ML       EQU   128                 MSG LENGTH             CJP - 7AUG86*
***********************************************************************
WTO      CSECT                     BEGINING OF PROGRAM
         USING *,R12               SET UP BASE REG
         SAVE  (14,12),,IPO.WTO..&SYSDATE..&SYSTIME        CJP - 7AUG86
         LR    R12,R15             SET UP ADDRESSABILITY
         LR    R2,R13              SAVE ADDR OF PREVIOUS SAVEAREA
         LA    R13,IPOSAVE         POINT TO CURRENT SAVEAREA
         ST    R13,8(R2)           LINK PREVIOUS SAVEAREA TO IPOSAVE
         ST    R2,4(R13)           LINK IPOSAVE TO PREVIOUS SAVEAREA
         LR    R11,R1              PTR PARM ADDR - FIRST COMMAND
         L     R11,0(R11)          SAVE ADDR OF PARM IN R11
IPOPARM  LH    R2,0(R11)           LENGTH TO R2
         C     R2,=F'0'            PARM LENGTH = 0?
         BE    IPOGETIN            YES - GET RECORD FROM SYSIN
         C     R2,=A(ML)           COMPARE LENGTH OF TO MAX LENGTH
         BNH   IPOMOVE             LENGTH OF PARM OKAY
         LA    R2,ML               RESET PARM LENGTH TO MAX
IPOMOVE  MVC   IPOMSG,=CL(ML)' '   CLEAR OUT MSG AREA
         BCTR  R2,0                LENGTH-1 FOR MVC
         EX    R2,IPOMVC           MOVE PARM INTO MSGAREA
         B     IPOGOTIN            PROCESS FIRST MSG
IPOGETIN TM    INPUT+48,X'10'      INPUT FILE OPEN?
         BO    IPOGET              BR IF SO
         OPEN  (INPUT)             OPEN IT
         TM    INPUT+48,X'10'      DID IT OPEN
         BZ    IPOEND              NO - NO VERICATION (NO WTOR)
IPOGET   GET   INPUT               READ RECORD FROM SYSIN
         LH    R14,INPUT+82        DCBLRECL                CJP - 7AUG86
         C     R14,=A(ML)          COMPARE TO MAX LENGTH   CJP - 7AUG86
         BNH   IPOGLEN             GO IF NO LARGER         CJP - 7AUG86
         L     R14,=A(ML)          SET TO MAX              CJP - 7AUG86
IPOGLEN  DS    0H                                          CJP - 7AUG86
         BCTR  R14,0               DECREMENT FOR EXECUTE   CJP - 7AUG86
         MVI   IPOMSG,C' '         BLANK OUT MSG BUFFER    CJP - 7AUG86
         MVC   IPOMSG(L'IPOMSG-1),IPOMSG                   CJP - 7AUG86
         EX    R14,IPOMVC2         MOVE MESSAGE            CJP - 7AUG86
IPOGOTIN CLC   IPOMSG,=CL(ML)' '   TEST FOR BLANK MESSAGE
         BE    IPOGETIN            SKIP BLANK RECORD
         MVC   IPOWTOL+4(ML),IPOMSG PLUG WTO MSG
         WTO   MF=(E,IPOWTOL)      DISPLAY MSG
         TM    OUTPUT+48,X'10'     OUTPUT FILE OPEN?
         BO    IPOOUT              BR IF SO
         OPEN  (OUTPUT,(OUTPUT))   OPEN IT
         TM    OUTPUT+48,X'10'     DID OUTPUT OPEN
         BZ    IPOOUTNO            NO - SKIP OUTPUT
IPOOUT   PUT   OUTPUT,IPOMSG       SAME MSG TO OUTPUT FILE
IPOOUTNO B     IPOGETIN            READ NEXT RECORD
IPOEOD   MVI   IPORPECB,0          CLEAR OUT ECB FOR REPLY
         WTOR  'REPLY C TO CONTINUE PROCESSING OR R TO REPEAT DISPLAY',X
               IPOREPLY,1,IPORPECB,ROUTCDE=(1,5,11)
         WAIT  1,ECB=IPORPECB      WAIT FOR OPERATOR TO REPLY
         CLI   IPOREPLY,C'C'       IS IT CONTINUE?
         BE    IPOEND              YES - CONTINUE PROCESSING
         CLI   IPOREPLY,C'R'       IS IT REPEAT MESSAGE?
         BNE   IPOEOD              NO - ASK OPERATOR AGAIN
         CLOSE (INPUT)             YES - REPEAT MSG - CLOSE INPUT
         B     IPOPARM             GO TO FIRST MSG
IPOEND   L     R13,IPOSAVE+4       RESTORE R13
         RETURN (14,12),T,RC=0     RETURN TO MVS
         PRINT NOGEN                                       CJP - 7AUG86
OUTPUT   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),        CJP - 7AUG86X
               LRECL=ML,RECFM=FB                           CJP - 7AUG86
INPUT    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),           CJP - 7AUG86X
               EODAD=IPOEOD,RECFM=FB                       CJP - 7AUG86
IPOMVC   MVC   IPOMSG(0),2(R11)    EXEC INSTRUCTION TO MOVE PARM
IPOMVC2  MVC   IPOMSG(*-*),0(R1)   EXECUTED - MOVE FROM SYSIN  - 7AUG86
IPOSAVE  DS    18F                 IPOSAVE AREA
IPORPECB DC    F'0'                ECB FOR WTOR
IPOAREA  DC    0F'0',AL2(ML+4,0)   LENGTH FOR OUTPUT IF RECFM=VB
IPOMSG   DC    CL(ML)' '           STORAGE FOR ONE MSG
IPOREPLY DS    CL1                 REPLY FOR WTO
         PRINT GEN                                         CJP - 7AUG86
*              MESSAGE OF IPOWTOL MUST BE SAME LEN AS ML   CJP - 7AUG86
IPOWTOL  WTO   '1234567891123456789212345678931234567894123456789512345X
               67896123456789712345678981234567899123456789012345678911X
               23456789212345678',                         CJP - 7AUG86X
               ROUTCDE=(1,5,11),DESC=(7),MF=L              CJP - 7AUG86
         LTORG                   , LTORG NEEDED FOR ADDRESSABILITY
         END                     , END OF PROGRAM
