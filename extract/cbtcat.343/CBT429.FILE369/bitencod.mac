         PUNCH ' ALIAS BITDECOD'
BITENCOD PRIME ID=DICK_SZIEDE_PRC_1986
*-------
*-------  TITLE  : BITENCODE
*-------  AUTHOR : DICK SZIEDE, PRC, 1986
*-------  PURPOSE: THIS SUBROUTINE TAKES A SINGLE CHARACTER ARGUMENT
*-------           OF ONE BYTE, AND RETURNS EIGHT CHARACTERS OF
*-------           ONES OR ZEROS DEPENDING ON THE BITS SET IN THE
*-------           ARGUMENT.
*-------  ARGUMENTS:
*-------           1.  THE ADDRESS OF THE BYTE TO BE DECODED.
*-------           2.  THE ADDRESS OF AN EIGHT BYTE CHARACTER VARIABLE
*-------               THAT WILL CONTAIN THE RESULT.
*-------  OUTPUT:  RETURN CODE IS ALWAYS ZERO.
*-------
*-------  ALIAS  : BITDECODE
*-------  PURPOSE: THIS SUBROUTINE TAKESS EIGHT CHARACTERS OF
*-------           DATA AND RETURNS A SINGLE BYTE, THE BITS OF
*-------           WHICH ARE THE RIGHTMOST BITS OF EACH ARGUMENT
*-------           BYTE.  THUS IS  IS THE OPPOSITE OF BITENCODE.
*-------  ARGUMENTS:
*-------           1.  THE ADDRESS OF THE BYTE TO BE ENCODED.
*-------           2.  THE ADDRESS OF AN EIGHT BYTE CHARACTER VARIABLE
*-------               THAT WILL BE CONVERTED TO 8 BITS.
*-------  OUTPUT:  RETURN CODE IS ALWAYS ZERO.
*-------
*-------
         REGS
BITENCOD  AMODE 24
BITENCOD  RMODE 24
*
         LM    R10,R11,0(R1)       POINTER TO PARMLIST.
         ICM   R9,B'1000',0(R10)   R9 NOW HAS BYTE TO BE DECODED.
         LA    R5,0(,R11)         R5 IS TARGET ADDRESS
         LA    R7,8(,R11)         R7 IS LIMIT ADDRESS
         LA    R6,1               LOOP INCREMENT OF 1
         MVC   0(8,R5),=C'00000000'   SET DEFAULT
         LA    R4,C'1'            SET FOR CUTE TRICK
*
LOOP     SR    R8,R8              BE SURE MASK IS SERO
         SLDL  R8,1               GET MASK BIT FROM ARGUMENT
         EX    R8,STOMPIT         STORE A 1 IF A BIT SHIFTED
         BXLE  R5,R6,LOOP         INTO R8
*
         TERME RC=0
STOMPIT  STCM  R4,0,0(R5)
         LTORG
BITDECOD PRIME ID=DICK_SZIEDE_PRC_1986
BITDECOD  AMODE 24
BITDECOD  RMODE 24
*
         LM    R10,R11,0(R1)       POINTER TO PARMLIST.
         LA    R2,2               OUTERLOOP COUNTER
         L     R8,4(R11)          RIGHT 4 BYTES TO ENCODE
OUTLOOP  LA    R3,4               INNER LOOP COUNTER
INLOOP   SRDL  R8,1               GET RIGHTMOST BIT OF BYTE
         SRL   R8,7               DUMP GARBAGE BITS
         BCT   R3,INLOOP
         L     R8,0(R11)          LEFT 4 BYTES TO ENCODE
         BCT   R2,OUTLOOP
*
DEEND    STCM  R9,B'1000',0(R10)
         TERME RC=0
SRCLEVEL DC    C'BITENCOD V01.M06 86/05/13 13:15 PSYRRS  '
         CSECT
         END
