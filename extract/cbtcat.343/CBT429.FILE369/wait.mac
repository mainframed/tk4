PRC      TITLE 'WAIT - WAIT A LITTLE BIT.'
********************************************************************
*                                                                  *
* NAME - WAIT.                                                     *
*                                                                  *
* DESCRIPTION -                                                    *
*                                                                  *
*   THIS MODULE WILL WAIT THE REQUESTED NUMBER OF SECONDS.         *
*                                                                  *
* TO USE -                                                         *
*                                                                  *
*   PARM='NNNN', WHERE 0 < NNNN <= 9999, THE NUMBER OF SECONDS TO  *
*     WAIT. DEFAULT IS 10 SECONDS.                                 *
*                                                                  *
* RETURN CODES -                                                   *
*                                                                  *
*   R15 = 20, BAD PARM FIELD.                                      *
*                                                                  *
* LOG -                                                            *
*   05/30/86 L01 PTW IMPLEMENTED                                   *L01
*   09/27/88 L02 PTW IMPLEMENTED                                   *L02
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'WAIT'
&CSECT   CSECT
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12
         GETMAIN RU,LV=DYNLEN
         LR    R2,R13
         LR    R13,R1
         USING DYNAMIC,R13
         LR    R0,R1
         LA    R14,DYNLEN
         LA    R1,R1
         SLR   R15,R15
         MVCL  R0,R14             ZERO STORAGE.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         XC    RC,RC
********************************************************************
*                                                                  *
*        P A R M   P R O C E S S I N G                             *
*                                                                  *
********************************************************************
         MVC   SECONDS,=F'10'     DEFAULT.
         L     R2,4(,R13)         A(PREVIOUS SAVEAREA).
         L     R2,24(,R2)         A(A(PARM)).
         LTR   R2,R2              CHECK FOR NULL PARM.
         BZ    DOTIME
         L     R2,0(,R2)          A(PARM).
         LH    R3,0(,R2)          L'PARM.
         LTR   R3,R3              IF ZEROS +
         BZ    DOTIME               THEN EXIT.
         CH    R3,=H'4'           IF LENGTH NGT '4' +
         BH    BADTIME            FORGET NEXT.
         LA    R4,2(,R2)
VERNUM   DS    0H
         CLI   0(R4),C'0'         SCAN
         BL    BADTIME             FORWARD
         CLI   0(R4),C'9'         SCAN
         BH    BADTIME             FORWARD
         LA    R4,1(,R4)            TO FIRST
         BCT   R3,VERNUM
         LH    R3,0(,R2)
         BCTR  R3,0               MACHINE LENGTH.
         EX    R3,PACKSEC         PACK SECONDS.
         CVB   R3,DOUBLE          CONVERT TO BINARY.
         CH    R3,=H'1'           TEST SECONDS
         BL    BADTIME             FOR LIMITS.
         CH    R3,=H'9999'
         BH    BADTIME
         ST    R3,SECONDS          AND STORE.
PACKSEC  PACK  DOUBLE,2(*-*,R2)
********************************************************************
*                                                                  *
*        W A I T                                                   *
*                                                                  *
********************************************************************
DOTIME   DS    0H
         L     R2,SECONDS
         MH    R2,=H'100'
         ST    R2,SECONDS
         STIMER WAIT,BINTVL=SECONDS
         XC    RC,RC
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
RETURN   DS    0H
         L     R2,RC                  R2 = RC.
         LR    R1,R13              PREPARE
         L     R13,4(,R13)         FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1)
         LR    R15,R2              AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
********************************************************************
*                                                                  *
*        E R R O R S                                               *
*                                                                  *
********************************************************************
BADTIME  DS    0H
         WTO   'WAIT: BAD PARM - SPECIFY NNNN (1-9999 SECONDS).',      X
               ROUTCDE=11,MCSFLAG=HRDCPY
         MVC   RC,=F'20'
         B     RETURN
********************************************************************
*                                                                  *
*        S T A T I C   A R E A                                     *
*                                                                  *
********************************************************************
SRCLEVEL DC    C'                                        '
         LTORG *
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A                                   *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
SAVE     DS    18F .
DOUBLE   DS    D
RC       DS    F
SECONDS  DS    F
DYNLEN   EQU   *-DYNAMIC
         PRINT OFF
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
         PRINT ON
         END   &CSECT
