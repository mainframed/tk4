PRC      TITLE 'NONDESC - WTO EXIT TO MAKE ACTION MESSAGES ROLLABLE.'
********************************************************************
*                                                                  *
* NAME - NONDESC - WTO EXIT TO MAKE ACTION MESSAGES ROLLABLE.      *
*                                                                  *
* DESCRIPTION - HERE WE TURN OFF DESC=1,2,3,5,11 UNCONDITIONALLY,  *
*   MAKING THE MESSAGE ROLL OFF THE DISPLAY.  THIS EXIT WAS        *
*   ORIGINALLY DESIGNED TO MAKE SELECTED TCAM MSGS ROLLABLE.       *
*   THIS EXIT IS WELL DOCUMENTED IN THE USER EXITS SPL, GC28-1147. *
*                                                                  *
* NOTE - WE MUST BE IN AN APF-AUTHORIZED LINKLIST LIBRARY.         *
*                                                                  *
* TO USE -                                                         *
*   SPECIFY IN AN MPFLSTXX MEMBER OF PARMLIB:                      *
*      MSGID,SUP(NO),USEREXIT(NONDESC)                             *
*                                                                  *
* ON ENTRY -                                                       *
*   R1 = A(CTXT - COMMTASK EXIT PARMLIST).                         *
*   STD LINKAGE.                                                   *
*                                                                  *
* ON EXIT -                                                        *
*   R15 = NOT USED.                                                *
*                                                                  *
* LOG -                                                            *
*     DATE   WHO LV    CHANGE                                      *
*   07/05/86 PTW L01 IMPLEMENTATION                                *L01
*   11/06/86 PTW L02 UNCONDITIONALLY TURN OFF DESC=1,2,3,5,11.     *L02
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'NONDESC'
&CSECT   CSECT ,
&CSECT   AMODE 31
&CSECT   RMODE ANY
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12         TELL ASSEMBLER.
         L     R11,0(,R1)         R11 = A(CTXT - PARMLIST).
         USING CTXT,R11           TELL ASSEMBLER.
         L     R10,CTXTTXPJ       R10 = A(CTXTATTR - TEXT).
         USING CTXTATTR,R10       TELL ASSEMBLER.
         L     R9,CTXTDCP         R10 = A(CTXTDESC - DESC CODES).
         USING CTXTDESC,R9        TELL ASSEMBLER.
         GETMAIN RU,LV=DYNLEN,SP=230 GET DYNAMIC AREA.
         LR    R2,R13             SAVE AREA
         LR    R13,R1             SAVE AREA
         USING DYNAMIC,R13        TELL ASSEMBLER.
         LA    R0,DYNAMIC         ZERO
         LA    R1,DYNLEN            DYNAMIC
         LR    R14,R0                 STORAGE.
         SLR   R15,R15
         MVCL  R0,R14             DO IT.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         XC    RC,RC              ASSUME GOOD RC.
********************************************************************
*                                                                  *
*        M A K E   M E S S A G E S   R O L L A B L E               *
*                                                                  *
********************************************************************
         NI    CTXTDC1,255-(CTXTDC01+CTXTDC02+CTXTDC03+CTXTDC05)    L02
         NI    CTXTDC2,255-(CTXTDC11) TURN OFF 1,2,3,5,11 DESCS.    L02
         OI    CTXTRFB1,CTXTRCDC   REQUEST CHANGE OF DESC CODES.
********************************************************************
*                                                                  *
*        R E T U R N (S)                                           *
*                                                                  *
********************************************************************
RETURN   DS    0H
         L     R2,RC                  R2 = RC.
         LR    R1,R13              PREPARE
         L     R13,4(,R13)         FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1),SP=230
         LR    R15,R2              AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
********************************************************************
*                                                                  *
*        S T A T I C   A R E A                                     *
*                                                                  *
********************************************************************
         LTORG  *
         DROP  R11,R10,R9
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A S                                 *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
SAVE     DS    18F .
RC       DS    F
DYNLEN   EQU   *-DYNAMIC          LENGTH OF WORKAREA.
         IEZVX100
         PRINT OFF
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11                 A(PLIST).
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
         PRINT ON
         END   &CSECT
