         TITLE '<< SDSF USER AUTHORIZATION EXIT (ISFUSER) >>'
***********************************************************************
*                                                                     *
* NAME         ISFUSER                                                *
*                                                                     *
* COMPONENT    SPOOL DISPLAY AND SEARCH FACILITY USER AUTHORIZATION   *
*              EXIT                                                   *
*                                                                     *
* FUNCTION     REFER ALL SDSF USER AUTHORIZATION CHECKS TO ACF2       *
*                                                                     *
* ENTRY        CALLED FROM ISFAUTH                                    *
*                                                                     *
* PARAMETERS   N/A                                                    *
*                                                                     *
* OPERATION:                                                          *
*          THE AUTHORIZATION CHECK EXIT IS ENTERED KEY ONE AND        *
*        SUPERVISOR THROUGH THE SDSF SVC.                             *
*                                                                     *
*          THIS IS THE AUTHORIZATION CHECK USER EXIT FOR SDSF.        *
*        ON ENTRY REGISTER 0 CONTAINS THE FOLLOWING:                  *
*                                                                     *
*              0 - FOR THE INITIALIZATION CALL                        *
*              4 - FOR A COMMAND CHECK CALL                           *
*              8 - FOR A DISPLAY SYSOUT CALL                          *
*             12 - FOR THE TERMINATION CALL                           *
*                                                                     *
*              ON ENTRY REGISTER ONE CONTAINS A PARAMETER LIST        *
*        WHICH IS MAPPED BY THE USERPARM DSECT CONTAINED IN THIS      *
*        MEMBER.  A RETURN CODE OF OTHER THAN ZERO WILL CANCEL THE    *
*        FUNCTION. NOTE THAT IF THE SVC IS AVAILABLE THE PARAMETER    *
*        LIST IS IN KEY ONE STORAGE.                                  *
*              THE TERMINATION CALL IS PROVIDED FOR THE USER EXIT     *
*        TO CLEAN UP ANY WORK AREAS GOTTEN DURING PRIOR CALLS.        *
*                                                                     *
* INPUT        USERPARM DSECT PASSED IN REG1                          *
*                                                                     *
* OUTPUT       RETURN CODE IN REG15                                   *
*                                                                     *
* RETURN CODES 0 - COMMAND ISSUED                                     *
*              4 - AUTHORIZATION CHECK FAILED                         *
*                                                                     *
* EXITS        SVC CALL TO ACF2                                       *
*                                                                     *
* MESSAGES     N/A                                                    *
*                                                                     *
* MACROS       FREEMAIN                                               *
*              GETMAIN                                               *
*              REGS                                                   *
*              RETURN                                                 *
*              SAVE                                                   *
*              WTO                                                    *
*                                                                     *
* TABLES       ACCVT                                                  *
*              ACFASVT                                                *
*              ACGRSRC                                                *
*              ACUCB                                                  *
*              IHAASCB                                                *
*              IHAPSA                                                 *
*              ISFUPRM                                                *
*              LIDREC                                                 *
*                                                                     *
* ATTRIBUTES   REENTRANT                                              *
*              REFRESHABLE                                            *
*              SUPERVISOR STATE                                       *
*              STORAGE KEY 0                                          *
*              NOT AUTHORIZED                                         *
*              AMODE 24                                               *
*              RMODE 24                                               *
*                                                                     *
* OTHER RESOURCES:                                                    *
*              TWO RESOURCE DIRECTORIES ARE NEEDED WITHIN THE ACF2    *
*              ENVIRONMENT TO ALLOW THE USE OF THE OPERATOR COMMAND   *
*              AND JOB OUTLIST CHECKING FACILITIES PROVIDED WITHIN    *
*              THIS EXIT. THEY ARE CALLED RCMD AND RJOL RESPECTIVELY  *
*              AT OUR INSTALLATION, AND CONTAIN A CUSTOMIZED RESOURCE *
*              RULE SET BASED ON OUR SECURITY STANDARDS.              *
*                                                                     *
* AUTHOR       KEN MATTHEWS                                           *
*              (THE COMMAND PARSER IN CMDCHK WAS BASTARDIZED          *
*              FROM MVSCMD WRITTEN BY C.J. PITTS)                     *
*                                                                     *
* MAINTENANCE  WHO    DATE     DESCRIPTION                            *
*              KEN    05/87    ADDED CMD PARSER TO LIMIT USER         *
*                              AUTHORITY TO ISSUE OPERATOR COMMANDS   *
*                              THROUGH ACF2.                          *
*              CJP    4/14/88  UPDATED FOR NEW SDSF HQX1102. DSECT    *
*                              CHANGES. SLIGHT COSMETIC CLEANUP.      *
*                              ADMININTERED VIA LM039.                *
*                                                                     *
**********************************************************************/
*                                                                     *
* REGISTERS AT ENTRY                                                  *
*              R0    CALL TYPE                                        *
*              R1    PARM LIST POINTER                                *
*              R12   BASE                                             *
*              R13   SAVE AREA                                        *
*                                                                     *
* REGISTER USE IN PROGRAM                                             *
*              R2    GETMAINED STORAGE ADDRESS                        *
*              R4    COMPARE REGISTER                                 *
*              R8    ACF2 CVT POINTER                                 *
*              R13   BASE REGISTER                                    *
*              R15   RETURN CODE FROM ACF2 SVC                        *
*                                                                     *
* REGISTERS AT EXIT                                                   *
*              R15   RETURN CODE                                      *
*                                                                     *
**********************************************************************/
         EJECT
ISFUSER  CSECT
         USING ISFUSER,R15
         SAVE  (14,12),,ISFUSER-&SYSDATE-&SYSTIME
         DROP  R15
         BALR  R12,0                        BASE REGISTER
         USING *,R12
         LR    R11,R1                       PICK UP THE PARMLIST
         USING USERPARM,R11                 ESTABLISH ADDRESSABILITY
         LR    R10,R0                       LOAD CALL TYPE IN REG10
         B     CALLTYPE(R10)                GO TO PROPER CODE
CALLTYPE B     INITCK                       INITIALIZATION
         B     CMDCK                        COMMAND CHECK
         B     DSPCK                        DISPLAY SYSOUT CHECK
         B     EXIT                         EXIT PROCESSING
         SPACE 2
***********************************************************************
*                                                                     *
*                      INITIALIZATION USER CHECK SECTION              *
*                                                                     *
***********************************************************************
* THE INITIALIZATION CHECK SECTION ASSUMES THAT ANYONE MAKING IT      *
* THIS FAR IS A VALID USER AND THEREFORE DOES NO USER AUTHORIZATION   *
* CHECKING. THIS SECTION OBTAINS STORAGE FOR REENTERABILITY, AND      *
* PERFORMS THE OPENING HOUSKEEPING ROUTINES.                          *
***********************************************************************
         SPACE 2
INITCK   DS    0H                           INITIALIZATION USER CHECK
GETMAIN  GETMAIN RU,LV=SAVLEN,SP=230        STORAGE FOR WTO REENTRANT
         LR    R2,R1                        GETMAINED ADDR IN REG2
         LR    R0,R2                        DITTO REG0
         LA    R1,SAVLEN                    END OF GETMAIN AREA
         LR    R14,R2                       GETMAINED ADDRESS AGAIN
         XR    R15,R15                      ZERO OUT REG15
         MVCL  R0,R14                       CLEAR OUT GETMAINED STORAGE
         ST    R13,4(R2)                    BACKWARD...
         ST    R2,8(R13)                    AND FORWARD CHAINING
         LR    R13,R2                       LOAD REG 13 AS BASE
         ST    R13,UPRUWORD                 STORE IN UPRUWORD
         USING SAVEAREA,R13                 ESTABLISH BASE
         B     RETURN                       AND RETURN (RC=0) TO CALLER
         SPACE 5
         EJECT
***********************************************************************
*                                                                     *
*                      OPERATOR COMMAND CHECK SECTION                 *
*                                                                     *
***********************************************************************
* THE OPERATOR COMMAND SECTION RECEIVES CONTROL AFTER AN MVS COMMAND  *
* IS ENTERED THROUGH SDSF. THIS SECTION FIRST CHECKS FOR SECURITY, OR *
* NON-CANCEL AUTHORITY AND LOGS THE COMMAND AND USER IF THIS BIT IS   *
* ON. ALL OTHER USERS ARE SUMMARILY DENIED ACCESS TO ANY COMMANDS THAT*
* ARE NORMALLY RESERVED FOR OPERATOR USE EXCEPT FOR SPECIAL EXCEPTIONS*
* ON THE START, STOP, AND MODIFY COMMANDS AS DICTATED THROUGH ACF2.   *
* THIS SECTION PARSES THE COMMAND ENTERED INTO THE VERB AND JOBNAME,  *
* AND FEEDS THE DIGESTED INFORMATION TO ACF2. PLEASE NOTE THAT AN     *
* ACF2 RESOURCE DIRECTORY FOR COMMANDS IS NEEDED ONLINE BEFORE USING  *
* THIS VERSION OF ISFUSER. THE RESOURCE DIRECTORY USED AT OUR SITE    *
* IS CALLED RCMD.                                                     *
***********************************************************************
         SPACE 2
CMDCK    DS    0H                           OPERATOR COMMAND CHECK
         L     R2,UPRUWORD                  PICK UP GETMAINED
         ST    R13,4(R2)                    STORAGE,FORWARD CHAIN
         ST    R2,8(R13)                    BACK CHAIN AND...
         LR    R13,R2                       SET UP THE BASE
         USING SAVEAREA,R13
         SPACE
         ACFINCVT R6,NONE=RETURN            GET CVT FOR ACF2
         USING ACCVT,R6                     SET UP DSECT BASE
         ACFGUCB R8,INLINE=YES,SYS=AOS2,NONE=RETURN
         USING ACUCB,R8
         L     R14,ACULRECP                 LIDREC ADDRESS
         USING LIDREC,R14
         TM    LIDTFLAG,LIDTSEC             SECURITY AUTHORITY?
         BO    WTO2LOG                      YEP...GO ISSUE COMMAND
         TM    LIDMFLG,LIDMNCNL             NON-CANCEL AUTHORITY?
         BO    WTO2LOG                      YEP...GO ISSUE COMMAND
         DROP  R14
         MVI   UPRARETC,X'00'               ZERO PREVIOUS AUTH
         SPACE 3
CMD2000  DS    0H
         MVC   VERB,=CL8' '                ---
         XC    VERBL,VERBL                 -INITIALIZE
         MVC   JOBNAME,=CL8' '             - ANSWER AREAS
         XC    JOBNAMEL,JOBNAMEL           ---
         XR    R1,R1                       CLEAR OUT TRT REGISTER
         SPACE
         LA    R7,UPROPCMD
         LH    R14,0(,R7)                  LENGTH OF PARM STRING
         BCTR  R14,0                       DECREMENT FOR EXECUTES
         LA    R15,4(,R7)                  ADDRESS  FIRST BYTE OF PARM
         EX    R14,NONBLANK                TRT TO GET NON-BLANK
         CLI   UPRACTCR,X'00'              IF OUTLIST COMMAND
         BH    JESCHK                      GO TO JES CHECK SECTION
         SPACE
         LTR   R14,R14                     REMAINING LENGTH OKAY?
         BNP   CMD2900                     NONE LEFT --- GO EXIT
         EX    R14,NONBLANK                TRT TO GET NON-BLANK
         BZ    CMD2900                     NO NON-BLANKS - GO EXIT
         SR    R1,R15                      ---
         SR    R14,R1                    - ADJUST POINTERS AND COUNTERS
         AR    R15,R1                      ---
         SPACE
         LTR   R14,R14                     REMAINING LENGTH OKAY?
         BNP   CMD2900                     NONE LEFT --- GO EXIT
         EX    R14,DELIMIT                 TRT TO GET DELIMITER
         BZ    CMD2900                     GO EXIT IF NO DELIMITER
         CLM   R2,B'0001',=C' '            BLANK DELIMITER?
         BNE   CMD2900                     NOPE...INVALID - GO EXIT
         LR    R3,R15                      SAVE STARTING ADDRESS
         SR    R1,R15                      ---
         SR    R14,R1                    - ADJUST POINTERS AND COUNTERS
         AR    R15,R1                      ---
         BCTR  R1,0                        DECREMENT FOR EXECUTE
         CH    R1,=Y(L'VERB-1)             COMPARE TO MAX
         BH    CMD2900                     TOO LONG...INVALID - GO EXIT
         EX    R1,MOVEVERB                 EXTRACT VERB
         STH   R1,VERBL                    SAVE LENGTH
         SPACE
         LTR   R14,R14                     REMAINING LENGTH OKAY?
         BNP   CMD2900                     NONE LEFT --- GO EXIT
         EX    R14,NONBLANK                TRT TO GET NON-BLANK
         BZ    CMD2900                     NO NON-BLANKS - GO EXIT
         SR    R1,R15                      ---
         SR    R14,R1                    - ADJUST POINTERS AND COUNTERS
         AR    R15,R1                      ---
         SPACE
         LTR   R14,R14                     REMAINING LENGTH OKAY?
         BNP   CMD2900                     NONE LEFT --- GO EXIT
         EX    R14,DELIMIT                 TRT TO GET DELIMITER
         BNZ   CMD2100                     GO IF DELIMITER FOUND
         LA    R1,2(R14,R15)               FAKE,NO TRAILING DELIMITER
CMD2100  DS    0H
         SR    R1,R15                      COMPUTE LENGTH
         BCTR  R1,0                        DECREMENT FOR EXECUTE
         CH    R1,=Y(L'JOBNAME-1)          COMPARE TO MAX
         BH    CMD2900                     TOO LONG...INVALID - GO EXIT
         EX    R1,MOVEJOBN                 EXTRACT JOBNAME
         STH   R1,JOBNAMEL                 SAVE LENGTH
CMD2900  DS    0H
         EJECT
***********************************************************************
* CMD3000:                                                            *
*        IF VERB IS START/STOP/MODIFY                                 *
*          THEN SET ACCESS REQUIREMENT BASED ON VERB TYPE;            *
*          ELSE DO;                                                   *
*                 SET RETURN CODE;                                    *
*                 GO RETURN;                         /*  CMD9000  */  *
*               END;                                                  *
*                                                                     *
***********************************************************************
         SPACE
CMD3000  DS    0H
         CLC   =C'START ',VERB              ---
         BE    CMD3100                      -
         CLC   =C'S ',VERB                  -
         BE    CMD3100                      -
         CLC   =C'STOP ',VERB               - IDENTIFY
         BE    CMD3200                      -
         CLC   =C'P ',VERB                  -
         BE    CMD3200                      - VERB
         CLC   =C'MODIFY ',VERB             -
         BE    CMD3300                      -
         CLC   =C'F ',VERB                  -
         BE    CMD3300                      ---
         SPACE
         MVC   RETCODE,=H'12'               SET BAD RETURN CODE
         B     FAIL                         GO EXIT
         SPACE
CMD3100  DS    0H
         MVI   VERBTYPE,ACGFAADD            ADD AUTHORITY FOR START
         B     CMD3500                      ONWARD
         SPACE
CMD3200  DS    0H
         MVI   VERBTYPE,ACGFADEL            DELETE AUTHORITY FOR STOP
         B     CMD3500                      ONWARD
         SPACE
CMD3300  DS    0H
         MVI   VERBTYPE,ACGFAUPT            UPDATE AUTHORITY FOR MODIFY
         SPACE
CMD3500  DS    0H
         EJECT
***********************************************************************
* CMD4000:                                                            *
*        IF ACF2 USER ID MATCHES BEGINNING OF JOBNAME                 *
*          THEN ALLOW ACCESS;                                         *
*          ELSE DO;                                                   *
*                 FORMAT PARAMETER LIST FOR ACF2 SVC;                 *
*                 ISSUE ACF2 SVC TO VALIDATE REQUEST;                 *
*                 IF ACCESS IS DENIED                                 *
*                   THEN DO;                                          *
*                          SET RETURN CODE;                           *
*                          GO RETURN;                /*  CMD9000  */  *
*                        END;                                         *
*               END;                                                  *
*                                                                     *
***********************************************************************
         SPACE
CMD4000  DS    0H
         LA    R14,L'ACULID-1              LENGTH FOR EXECUTE
         LA    R15,ACULID                  ADDRESS OF LID
         EX    R14,DELIMIT                 FIND TRAILING BLANK
         BNZ   CMD4100                     GO IF NO DELIMITER
         LA    R1,2(R14,R15)              FAKE OUT ADDRESS OF DELIMITER
CMD4100  DS    0H
         SR    R1,R15                      COMPUTE LENGTH
         BCTR  R1,0                        DECREMENT FOR EXECUTE
         CH    R1,JOBNAMEL                 CHECK JOBNAME LENGTH
         BH    CMD4500                    GO IF LID LONGER THAN JOBNAME
         EX    R1,OWNERCHK                 CHECK FOR 'OWNERSHIP'
         BE    WTO2LOG                     GO IF 'OWNED'
         SPACE
CMD4500  DS    0H
         XC    ACFPLIST,ACFPLIST           CLEAR PARAMETER LIST
         LA    R10,ACFPLIST                BASE REGISTER
         USING ACGRSRC,R10
         MVI   ACGFCN,4                    FUNCTION CODE
         MVI   ACGSFCN,ACGSINP             SUBFUNCTION - INTERPRET RULE
         MVI   ACGFLGS,ACGFADRQ            USE ADDRESS SPACE ACUCB
         OC    ACGFLGS,VERBTYPE            SET IN REQUEST CODE
         LA    R14,ACFMSG                  MESSAGE BUFFER ADDRESS
         ST    R14,ACGMSG                  SET IT
         MVC   ACGRTYPE,RULETYPE           RESOURCE RULE TYPE CODE
         MVC   ACGRNAME(L'JOBNAME),JOBNAME SET RESOURCE NAME
         MVI   ACGRNAME+L'JOBNAME,C' '     BLANK OUT REST OF NAME
         MVC   ACGRNAME+L'JOBNAME+1(L'ACGRNAME-L'JOBNAME-1),ACGRNAME+L'+
               JOBNAME                     FILL REST OF NAME WITH BLANK
         MVC   ACGMODID,=CL8'ISFUSER' SMF RECORD MODID
         SPACE
         ACFSVC ACGRSRC,TYPE=A,CVT=HAVE,NONE=RETURN
         SPACE
         DROP  R10
         LTR   R15,R15                     CHECK RETURN CODE
         BZ    WTO2LOG                     ONWARD IF OKAY
         SPACE
         MVC   RETCODE,=H'8'               SET RETURN CODE
         B     FAIL                        GO EXIT
         EJECT
***********************************************************************
* THE DISPLAY SYSOUT CHECK PORTION IS ENTERED AT DSPCK WHEN A DISPLAY *
* IS REQUESTED FROM SDSF AND AT JESCHK FROM ANY OTHER LINE COMMAND    *
* THAT MAY HAVE BEEN ENTERED. ALL OPERATOR TYPE COMMANDS ARE DENIED   *
* (REMEMBER ONLY SECURITY AND NON-CNCL TYPE FOLKS ARE ALLOWED THESE   *
* PRIVELEGES). IF THE JOBNAME AND THE USERID MATCH FOR THE LENGTH OF  *
* THE USERID, THE REQUEST IS ALLOWED WITHOUT CHECKING ACF2. IF NOT    *
* THE COMMAND INFO, JOBNAME, AND USER ARE PARSED AND LOADED INTO THE  *
* PROPER ACF2 FIELDS AND THE BALL IS GIVEN TO ACF2. NOTE THAT A       *
* RESOURCE DIRECTORY FOR JOB OUTLIST MUST BE ONLINE TO ACF2, IN THIS  *
* CASE IT IS CALLED JOL.                                              *
***********************************************************************
DSPCK    DS    0H                           DISPLAY SYSOUT CHECK
         L     R2,UPRUWORD                  PICK UP GETMAINED
         ST    R13,4(R2)                    STORAGE,FORWARD CHAIN
         ST    R2,8(R13)                    BACK CHAIN AND...
         LR    R13,R2                       SET UP THE BASE
         USING SAVEAREA,R13
         SPACE
JESCHK   DS    0H
         LA    R4,UPRACTCR                  ACTION VERB FROM SDSF SESS
         CLI   0(R4),C'A'                   CHECK FOR
         BE    FAIL                         RESERVED OPERATOR COMMANDS
         CLI   0(R4),C'E'                   AND DENY ACCESS TO ALL
         BE    FAIL                         NON AUTHORIZED
         CLI   0(R4),C'H'                   USERS...
         BE    FAIL
         CLI   0(R4),C'T'
         BE    FAIL                         ELSE...
         CLC   UPRTSOID(6),UPRJNAME         COMPARE USERID & JOBNAME
         BE    RETURN                       AND ALLOW ANYTHING ELSE!
         EJECT
         ACFINCVT R8,NONE=RETURN            GET CVT FOR ACF2
         USING ACCVT,R8                     SET UP DSECT BASE
         XC    ACFPARMS,ACFPARMS            CLEAR PARAMETER LIST
         LA    R9,ACFPARMS                  SET PARMS BASE
         USING ACGRSRC,R9
         MVI   ACGRSRC,X'00'                CLEAR OUT AREA
         MVC   ACGRSRC+1(L'ACGRSRC-1),ACGRSRC
         MVI   ACGFCN,4                     LOAD FUNCTION
         MVI   ACGSFCN,ACGSINP              INTERPRET RULE SUBFUNCTION
         MVI   ACGFLGS,ACGFADRQ
         MVI   ACGRFLGS,0
         LA    R14,ACFMSG                   MESSAGE BUFFER ADDRESS
         ST    R14,ACGMSG                   SET IT
         MVC   ACGRTYPE,=C'RJOL'            LOAD RESOURCE TYPE
         MVI   ACGRNAME,C' '                CLEAR OUT AREA
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         MVC   ACGRNAME(L'UPRJNAME),UPRJNAME
         MVC   ACGMODID,=CL8'ISFUSER'       WRITE PROGNAME
         CLI   UPRACTCR,X'00'               LET EM SELECT
         BE    READACC
         CLI   UPRACTCR,C'D'
         BE    READACC                      LOOK FOR
         CLI   UPRACTCR,C'L'
         BE    READACC                      READ ACCESS
         CLI   UPRACTCR,C'O'
         BE    UPDTACC                      UPDATE ACCESS
         CLI   UPRACTCR,C'P'
         BE    DELACC                       OR DELETE ACCESS
         CLI   UPRACTCR,C'C'
         BE    DELACC                       AND DEAL ACCORDINGLY
         B     FAIL                         OTHERWISE...
READACC  DS    0H
         OI    ACGFLGS,ACGFARD              SET READ FLAG
         B     SVC                          AND CALL ACF2
DELACC   DS    0H
         OI    ACGFLGS,ACGFADEL             SET DELETE ACCESS FLAG
         B     SVC                          AND CALL ACF2
UPDTACC  DS    0H
         OI    ACGFLGS,ACGFAUPT             SET UPDATE FLAG
SVC      ACFSVC ACGRSRC,TYPE=A,CVT=HAVE,NONE=RETURN
         LTR   R15,R15                      CHECK ACF2 RETCDE
         BNZ   FAIL                         AND DENY ACCESS IF >0
         B     RETURN                       OTHERWISE ALLOW ACCESS
         EJECT
***********************************************************************
*                                                                     *
*                      EXIT CLEAN UP SECTION                          *
*                                                                     *
***********************************************************************
         SPACE 5
EXIT     DS    0H                           EXIT PROCESSING
         L     R1,UPRUWORD
         FREEMAIN RU,LV=SAVLEN,A=(1),SP=230 VIRT STORAGE
         RETURN (14,12),T,RC=0              RETURN RETURN CODE 0
         SPACE 5
WTO2LOG  MVC   LMESS(IMESSLEN),IMESSL       LOAD WTO MESSAGE AREA
         MVC   LUSER,UPRTSOID               WHODUNIT,
         MVC   LCMD,UPROPCMD+2              MOVE IN COMMAND ATTEMPTED,
         WTO   MF=(E,LMESS)                 AND SEND IT TO MSTCONS
RETURN   DS    0H
         L     13,SAVE+4                    LOAD REG13 STUFF BACK
         RETURN (14,12),T,RC=0              RETURN RETURN CODE 0
FAIL     DS    0H
         L     13,SAVE+4                    LOAD REG13 STUFF BACK
         RETURN (14,12),T,RC=4              RETURN
         EJECT
***********************************************************************
*                                                                     *
*               LITERALS                                              *
*                                                                     *
***********************************************************************
IMESSL   WTO   'SDSF069                              ',MF=L
IMESSLEN EQU   *-IMESSL
LUSER    EQU   LMESS+13,7
LCMD     EQU   LMESS+20,20
OPERCNTL EQU   X'80'
NONBLANK TRT   0(*-*,R15),TRNBLANK       EXECUTED - FIND NEXT NON-BLANK
DELIMIT  TRT   0(*-*,R15),TRTDELIM       EXECUTED - FIND NEXT DELIMITER
MOVEVERB MVC   VERB(*-*),0(R3)           EXECUTED - MOVE VERB
MOVEJOBN MVC   JOBNAME(*-*),0(R15)       EXECUTED - MOVE JOBNAME
OWNERCHK CLC   0(*-*,R15),JOBNAME        EXECUTED - COMPARE OWNERSHIP
MOVECMD  MVC   0(*-*,R15),2(R7)          EXECUTED - MOVE COMMAND >PLIST
         SPACE
         PRINT NOGEN
RULETYPE DC    C'RCMD'                     RESOURCE RULE TYPE 'CMD'
         SPACE
EXTRACTL EXTRACT FIELDS=TJID,MF=L
         SPACE
         DS    0D
TRTDELIM DC    256X'00'                TABLE TO LOCATE C' ', C'.', C','
         ORG   TRTDELIM+C' '
         DC    C' '
         ORG   TRTDELIM+C'.'
         DC    C'.'
         ORG   TRTDELIM+C','
         DC    C','
         ORG
         SPACE
TRNBLANK DC    256X'FF'                  TABLE TO LOCATE NONBLANK
         ORG   TRNBLANK+C' '
         DC    X'00'
         ORG
         SPACE
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*               USER EXIT COMMUNICATION AREA                          *
*                                                                     *
***********************************************************************
         SPACE 5
SAVEAREA DSECT
SAVE     DS    18F
LMESS    DS    0F,CL(IMESSLEN)
ACFPARMS DS    0F,XL(ACGRSLEN)
VERB     DS    CL8                         COMMAND VERB
VERBL    DS    H                           VERB LENGTH LESS 1
JOBNAME  DS    CL8                         COMMAND JOBNAME
JOBNAMEL DS    H                           JOBNAME LENGTH LESS 1
VERBTYPE DS    X                           ACCESS CD BASED ON VERB TYPE
TJID     DS    A                           TSO TJID OR ZERO
         DS    0F
ACFPLIST DS    0F,XL(ACGRSLEN)             ACF2 RULE CHECK PARM LIST
ACFMSG   DS    CL128                       ACF2 ERROR MESSAGE BUFFER
RETCODE  DS    H                           RETURN CODE
SAVLEN   EQU   *-SAVEAREA
         SPACE 3
         PRINT GEN
USERPARM DSECT                             USER EXIT PARM
         ISFUPRM                           SDSF PARAMETER LIST
         PRINT NOGEN
         ACFASVT
         ACCVT   ,                         ACF2 CVT
         ACGRSRC ,                         ACF SVC PARMS
         ACUCB   ,                         ACF USER CONTROL BLOCK
         IEZBITS
         IHAASCB ,                         ASCB
         IHAPSA  ,                         PSA
         LIDREC  ,                         ACF2 LOGICAL ID RECORDS
         REGS
         SPACE 3
         END ISFUSER
