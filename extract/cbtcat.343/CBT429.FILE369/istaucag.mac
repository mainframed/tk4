PRC      TITLE 'VTAM SESSION ACCOUNTING EXIT ROUTINE.'
********************************************************************
*                                                                  *
* NAME - VTAM SESSION ACCOUNTING EXIT ROUTINE.                     *
*                                                                  *
* DESCRIPTION - THIS EXIT IS DOCUMENTED IN THE ACF/VTAM CUSTOM.    *
*   MANUAL (SC27-0613).  WHAT WE DO IN THIS EXIT IS BANG OUT AN    *
*   SMF RECORD (TYPE 240) THAT CONTAINS ALL RELEVANT INFORMATION   *
*   PASSED TO US.  THIS INCLUDES PRIMARY AND SECONDARY LU NAMES.   *
*   RECORD IS IN RELOCATE FORMAT (SECTIONS WITH A HEADER CONTAIN   *
*   OFFSET, LENGTH, AND NUMBER).  PLEASE NOTE THAT THE RECORD IS   *
*   IN A FORMAT COMPATIBLE WITH THE VECTORS THAT WILL BE OUTPUT    *
*   BY THE VTAM SESSION MANAGEMENT EXIT ROUTINE WE WILL USE WHEN   *
*   VTAM 2.2 IS IMPLEMENTED.                                       *
*                                                                  *
*                                                                  *
* LEVEL - WE TESTED THIS EXIT WITH VTAM V2R1.  PLEASE NOTE THE USE *
*   OF MODLEV BELOW THAT INDICATES MAINTENANCE LEVEL OF THIS MODULE*
*   IN THE SMF RECORD.                                             *
*                                                                  *
* UPON ENTRY -                                                     *
*  R0 = X'00000000' IF SESSION ESTABLISHMENT                       *
*       X'FFFFFFFF' IF SESSION TERMINATION                         *
*  R7 = A(PLU NAME).                                               *
*  R11 = A(SLU NAME).                                              *
*  NOTE THAT R15 IS IGNORED UPON RETURN.                           *
*                                                                  *
* LOG -                                                            *
*     DATE   WHO LV    CHANGE                                      *
*   11/21/85 PTW 01 IMPLEMENTATION                                 *
*   06/XX/86 PTW 02 CHANGE STCK TO TIME MACRO FOR GMT, MISC.CHGS.  *L02
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'ISTAUCAG'
&CSECT   CSECT ,
RECTYPE  EQU   240                SMF RECORD NUMBER.
RECSTYPE EQU   2                  SMF RECORD SUBTYPE.
MODLEV   EQU   2                  THIS MODULE'S LEVEL.              L02
VTAMLEV  EQU   21                 VTAM VERSION/RELEASE.             L02
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12         TELL ASSEMBLER.
         LR    R10,R0             R10 = A(PLIST).
         GETMAIN RU,LV=DYNLEN     GET DYNAMIC AREA.
         LR    R2,R13             SAVE AREA
         LR    R13,R1             SAVE AREA
         USING DYNAMIC,R13        TELL ASSEMBLER.
         LA    R0,DYNAMIC         ZERO
         LA    R1,DYNLEN            DYNAMIC
         LR    R14,R0                 STORAGE.
         SLR   R15,R15
         MVCL  R0,R14             DO IT.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         XC    RC,RC              ASSUME GOOD RC.
********************************************************************
*                                                                  *
*        C R E A T E   S M F   R E C O R D                         *
*                                                                  *
********************************************************************
         MVI   SM240RTY,RECTYPE   RECORD TYPE 240.
         MVC   SM240TYP,=AL2(RECSTYPE) WE ARE SUBTYPE 2.
         LA    R2,S2402DAT        R2 = (SMF POINTER).
         MVC   S2402TRN,=AL2(S2402TR#) # TRPILETS.
* CREATE SOFTWARE LEVEL SECTION OF SMF RECORD.
         LA    R3,S2402REC        CALCULATE AND STORE OFFSET.
         LR    R4,R2
         SLR   R4,R3
         ST    R4,S2402LVO
         MVC   S2402LVL,=AL2(S2402LLN) MOVE LENGTH.
         MVC   S2402LV#,=H'1'     ONLY 1 SECTION.
         USING S2402LEV,R2        USE LEV MAPPING.
         MVI   S2402LAM,S2402LAV  MOVE ACCESS METHOD.
         MVI   S2402LVR,VTAMLEV   MOVE VERSION/RELEASE.
         MVI   S2402LMD,MODLEV    OUR RECORD VERSION.
         DROP  R2
         LA    R2,S2402LLN(,R2)   BUMP SMF POINTER.
* CREATE ACCOUNTING SECTION OF SMF RECORD.
         LA    R3,S2402REC        CALCULATE AND STORE OFFSET.
         LR    R4,R2
         SLR   R4,R3
         ST    R4,S2402ACO
         MVC   S2402ACL,=AL2(S2402ALN) MOVE LENGTH.
         MVC   S2402AC#,=H'1'     ONLY 1 SECTION.
         USING S2402ACT,R2        USE ACCT MAPPING.
         TIME  BIN                STORE TIME, DATE.                 L02
         STM   R0,R1,S2402ATM
         MVI   S2402ATY,S2402ATS  MOVE FUNCTION CODE.
         LTR   R10,R10
         BZ    *+8
         MVI   S2402ATY,S2402ATE  MOVE FUNCTION CODE.
         MVC   S2402APL,0(R7)     MOVE PRIMARY LUNAME.
         MVC   S2402ASL,0(R11)    MOVE LUNAME.
         MVC   S2402AMJ,=CL8' '
         L     R3,PSAAOLD-P
         L     R4,ASCBJBNS-A(,R3)
         LTR   R4,R4
         BZ    *+10
         MVC   S2402AMJ,0(R4)     MOVE IN ACCESS METHOD JOBNAME.
         MVC   S2402AAJ,=CL8' '   BLANK APPL JOBNAME.
         DROP  R2
         LA    R2,S2402ALN(,R2)   BUMP SMF POINTER.
         LA    R3,SM240ULN
         LR    R4,R2              CALCULATE L'USER SECTION.
         SLR   R4,R3
         STH   R4,SM240ULN        STORE L'USER SECTION IN SMF REC.
         LA    R1,S2402REC        R1 = A(SMF RECORD).
         SVC   226                CALL USER SVC ROUTINE.
         LTR   R15,R15            WTO IF BAD RC.
         BNZ   BADSVC
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
RETURN   DS    0H
         L     R2,RC               R2 = RC.
         LR    R1,R13              PREPARE
         L     R13,4(,R13)         FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1)
         LR    R15,R2              AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
********************************************************************
*                                                                  *
*        E R R O R S                                               *
*                                                                  *
********************************************************************
BADSVC   DS    0H
 WTO 'ISTCAGI NON-ZERO RC FROM PRC SMF SVC - NO ACCT RECORD WRITTEN.'
         MVC   RC,=F'12'
         B     RETURN
********************************************************************
*                                                                  *
*        S T A T I C   A R E A                                     *
*                                                                  *
********************************************************************
         LTORG *
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A S                                 *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
SAVE     DS    18F .
RC       DS    F
         PRINT GEN
S2402REC SMF240 DSECT=NO,SUBTYPE=2     TYPE 240 RECORD.
DYNLEN   EQU   *-DYNAMIC
         PRINT NOGEN
         IHAPSA
P        EQU   PSA
         IHAASCB
A        EQU   ASCB
         PRINT OFF
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11                 A(PLIST).
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
         PRINT ON
         END   &CSECT
