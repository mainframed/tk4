         MACRO
&LABLE   PRIME &SA=,&BR=12,&START=0,&ID=*,&LV=72,&BR2=,&BR3=
* SOURCE LEVEL PRIME    V01.M02 85/09/26 18:07 PSYRRS
         GBLA  &SIZE
         GBLB  &SASW
         GBLC  &OPSYS        GLOABAL SWITCH TO DEFINE OPERATING SYSTEM
         LCLA  &NDW
         LCLC  &X
&OPSYS   SETC  'DOS'         OPERATING SYSTEM  'OS' OR 'DOS'
&OPSYS   SETC   'OS'         'DOS' OR  'OS'
&SIZE    SETA  &LV
&SASW    SETB  0
&X       SETC  '&SYSNDX'
.*      GORDON M. JOHNSON
         SPACE 1
         AIF   (&SYSNDX EQ 1).START
&LABLE   CSECT
         AGO   .CONT1
.START   ANOP
&LABLE   START &START
.CONT1   ANOP
         AIF   ('&ID' EQ 'NO' OR '&OPSYS' EQ 'DOS').SETNOID
         SAVE  (14,12),,&ID   SAVE REGISTERS AND FORM IDENTIFIER
         AGO   .CONT2
.SETNOID ANOP
         SAVE  (14,12)        SAVE REGISTERS
.CONT2   ANOP
         BALR  &BR,0          CREATE BASE REGISTER
         USING *,&BR
         AIF   ('&BR' EQ '0').BADBR
         AIF   ('&BR' EQ '1').BADBR
         AIF   ('&BR' EQ '2').BADBR
         AIF   ('&BR' EQ '13').BADBR
         AIF   ('&BR' EQ '14').BADBR
         AIF   ('&BR' EQ '15').BADBR
         AGO   .GOODBR1
.BADBR   MNOTE 1,'INVALID BASE REGISTER, 0,1,2,13,14,15 NOT ALLOWED BR'
         SPACE 3
.GOODBR1 ANOP
         AIF   (T'&BR2 EQ 'O').NOXTRA
         AIF   ('&BR' NE '&BR2').OKBR2
         SPACE 1
         MNOTE 1,'ERROR  BR AND BR2 EQUAL,  IGNORE BR2'
* DEFAULT VALUE FOR BR=12
         SPACE 2
         AGO   .NOXTRA
.OKBR2   ANOP
         AIF   ('&BR2' EQ '0').BADBR2
         AIF   ('&BR2' EQ '1').BADBR2
         AIF   ('&BR2' EQ '2').BADBR2
         AIF   ('&BR2' EQ '13').BADBR2
         AIF   ('&BR2' EQ '14').BADBR2
         AIF   ('&BR2' EQ '15').BADBR2
HERE&X   L     &BR2,=A(HERE&X.+4096)   LOAD SECOND BSE
         USING HERE&X.+4096,&BR2   SECOND BASE
         AGO   .TRYBR3
.BADBR2  MNOTE 1,'INVALID BASE REGISTER, 0,1,2,13,14,15 NOT ALLOWED BR2C
                '
         SPACE 1
.TRYBR3  ANOP
         AIF   (T'&BR3 EQ 'O').NOXTRA
         AIF   ('&BR3' NE '&BR2' AND '&BR3' NE '&BR').GOOD3
         MNOTE 1,'ERROR  BR3 EQUAL TO BR2 OR BR, BR3 IGNORED '
* DEFAULT VALUE FOR BR=12
         SPACE 1
         AGO   .NOXTRA
.GOOD3   ANOP
         AIF   ('&BR3' EQ '0').BADBR3
         AIF   ('&BR3' EQ '1').BADBR3
         AIF   ('&BR3' EQ '2').BADBR3
         AIF   ('&BR3' EQ '13').BADBR3
         AIF   ('&BR3' EQ '14').BADBR3
         AIF   ('&BR3' EQ '15').BADBR3
         L     &BR3,=A(HERE&X+2*4096)  LOAD 3RD BASE REG
         USING HERE&X.+2*4096,&BR3   THIRD BASE
         AGO   .NOXTRA
.BADBR3  MNOTE 1,'INVALID BASE REGISTER, 0,1,2,13,14,15 NOT ALLOWED BR3C
                '
         SPACE 2
.NOXTRA  ANOP
         LR    2,1            SAVE PARAMETER REGISTER
         AIF   ('&SA' EQ '').DYNMC
         L     1,=A(&SA)      GET ADDRESS OF SAVE AREA
         AGO   .END
.DYNMC   ANOP
&SASW    SETB  1
         AIF   ('&OPSYS' NE 'DOS').GETCORE
&NDW     SETA  (&SIZE+7)/8   COMPUTE NUMBER OF DOUBLE WORDS NEEDED
         LA    1,NDW&X        SAVEAREA IN DOUBLE WORDS
         B     SKIP&X        BRANCH OVER SAVE AREA
NDW&X    DS    &NDW.D        SAVEAREA
SKIP&X   DS    0H
         AGO   .END
.GETCORE ANOP
         GETMAIN R,LV=&SIZE
         L     0,20(13)       RESTORE R0 AFTER GETMAIN
.END     ANOP
         ST    1,8(13)        STORE FORWARD POINTER
         ST    13,4(1)        STORE BACKWARD POINTER
         LR    13,1           SET UP REGISTER FOR SAVE AREA
         LR    1,2            RESTORE PARAMETER REGISTER
         SPACE 1
         MEND
