PRC      TITLE 'WTONR - WRITE NON-ROLLABLE OPERATOR MESSAGES.'
********************************************************************
*                                                                  *
* NAME - WTONR - WRITE NON-ROLLABLE MSGS TO OPERATOR CONSOLE.      *
*                                                                  *
* DESCRIPTION - WE READ SYSIN INPUT AND WTO CARD IMAGES TO THE     *
*   OPERATOR CONSOLE THAT ARE NON-ROLLABLE.  ORGINALLY USED BY     *
*   THE MESSENGER JOBS FOR AN OPERATOR RESTART.                    *
*                                                                  *
* NOTE - WE MUST BE APF-AUTHORIZED TO WRITE NON-ROLLABLE MESSAGES  *
*                                                                  *
* TO USE -                                                         *
*  //S1 EXEC PGM=WTONR                                             *
*  //STEPLIB DD DSN=AN.APF.LIBRARY,DISP=SHR                        *
*  //SYSIN   DD *                                                  *
*  * THIS IS A COMMENT                                             *
*  ---> UP TO THREE LINES OF TEXT <---                             *
*  ---> AFTER THREE LINES IGNORED <---                             *
*  ---> COLS 1-72 ARE PROCESSED   <---                             *
*  /*                                                              *
*                                                                  *
* LOG -                                                            *
*     DATE   WHO LV    CHANGE                                      *
*   06/16/86 PTW L01 IMPLEMENTATION                                *
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'WTONR'
&CSECT   CSECT ,
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12         TELL ASSEMBLER.
         GETMAIN RU,LV=DYNLEN     GET DYNAMIC AREA.
         LR    R2,R13             SAVE AREA
         LR    R13,R1             SAVE AREA
         USING DYNAMIC,R13        TELL ASSEMBLER.
         LA    R0,DYNAMIC         ZERO
         LA    R1,DYNLEN            DYNAMIC
         LR    R14,R0                 STORAGE.
         SLR   R15,R15
         MVCL  R0,R14             DO IT.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         XC    RC,RC              ASSUME GOOD RC.
         MVC   INDCB(INDCBLEN),XINDCB INITIALIZE LISTS.
         MVC   WTO(WTOLEN),XWTO
         MVC   OPEN(OPENLEN),XOPEN
********************************************************************
*                                                                  *
*        W T O   M E S S A G E S   F R O M   S Y S I N             *
*                                                                  *
********************************************************************
         OPEN  (INDCB),MF=(E,OPEN)
         TM    DCBOFLGS-IHADCB+INDCB,DCBOFOPN
         BNO   BADOPEN            CHECK RESULT OF OPEN.
         LA    R2,3               LIMIT OF 3 WTOS/EXECUTION.
GET      GET   INDCB,INREC
         CLI   INREC,C'*'         DON'T PROCESS COMMENTS.
         BE    GET
         MVC   WTOMSG,INREC       MOVE TEXT FOR WTO.
         WTO   MF=(E,WTO)         WTO.
         BCT   R2,GET
         B     RETURN
********************************************************************
*                                                                  *
*        E R R O R S                                               *
*                                                                  *
********************************************************************
BADOPEN  DS    0H
         MVC   RC,=F'12'          SET BAD RETURN CODE.
         B     RETURN
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
RETURN   DS    0H
         L     R2,RC                  R2 = RC.
         LR    R1,R13              PREPARE
         L     R13,4(,R13)         FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1)
         LR    R15,R2              AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
********************************************************************
*                                                                  *
*        S T A T I C   A R E A                                     *
*                                                                  *
********************************************************************
XINDCB   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=RETURN
INDCBLEN EQU   *-XINDCB
XOPEN    OPEN  0,MF=L
OPENLEN  EQU   *-XOPEN
XWTO     WTO   '                                                       X
                                ',ROUTCDE=1,DESC=2,MF=L
WTOLEN   EQU   *-XWTO
         LTORG *
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A S                                 *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
SAVE     DS    18F .
RC       DS    F
OPEN     DS    F
INDCB    DS    0F,CL(INDCBLEN)
WTO      DS    0F,CL(WTOLEN)
WTOMSG   EQU   WTO+4,72,C'C'
INREC    DS    CL80
DYNLEN   EQU   *-DYNAMIC          LENGTH OF WORKAREA.
         PRINT OFF
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11                 A(PLIST).
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
         PRINT ON,NOGEN
         DCBD  DSORG=PS,DEVD=DA
         END   &CSECT
