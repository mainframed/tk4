PRC      TITLE 'ISRPID - FETCH THE CURRENT ISPF PANELID.'
*********************************************************************
*                                                                   *
*   THIS ROUTINE RUNS UNDER THE ISPF DIALOG MANAGER AND RETURNS     *
*   THE NAME OF THE CURRENT PANEL IN DIALOG VARIABLE PANELID.       *
*   IT WAS DESIGNED FOR USE BY THE PRINTO COMMAND UNDER EDIT AND    *
*   BROWSE.                                                         *
*                                                                   *
*   TO USE -                                                        *
*     ISPEXEC SELECT PGM(ISRPID)                                    *
*     ISPEXEC VGET (PANELID)                                        *
*                                                                   *
*   05/01/86 - L01 PTW IMPLEMENTED                                  *
*                                                                   *
*********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'ISRPID'
&CSECT   CSECT ,
*********************************************************************
*                                                                   *
*        I N I T I A L I Z E   A N D   D E T E R M I N E            *
*                                                                   *
*********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12         TELL ASSEMBLER.
         GETMAIN RU,LV=DYNLEN     GET DYNAMIC AREA.
         LR    R2,R13             SAVE AREA
         LR    R13,R1             SAVE AREA
         USING DYNAMIC,R13        TELL ASSEMBLER.
         LA    R0,DYNAMIC         ZERO
         LA    R1,DYNLEN            DYNAMIC
         LR    R14,R0                 STORAGE.
         SLR   R15,R15
         MVCL  R0,R14             DO IT.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         XC    RC,RC              ASSUME GOOD RC.
         MVC   CALL(CALLL),XCALL               MOVE DCB INTO WORK AREA
         MVC   PID,=CL8' '
*********************************************************************
*                                                                   *
*        M A I N L I N E                                            *
*                                                                   *
*********************************************************************
         MVC   PID,PANELID(R9)         R9 = A(TLD).
         LOAD  EP=ISPLINK
         ST    R0,ISPLINK
         L     R15,ISPLINK
         CALL  (15),(=CL8'VREPLACE',=CL8'PANELID',=A(L'PID),PID),      X
               VL,MF=(E,CALL)
         LTR   R15,R15
         BNZ   BADCALL
         L     R15,ISPLINK
         CALL  (15),(=CL8'VPUT',=C'(PANELID)',=CL8'SHARED'),           X
               VL,MF=(E,CALL)
         LTR   R15,R15
         BNZ   BADCALL
*        TPUT  PID,L'PID
         DELETE EP=ISPLINK
RETURN   DS    0H
         L     R2,RC                  R2 = RC.
         LR    R1,R13              PREPARE
         L     R13,4(,R13)         FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1)
         LR    R15,R2              AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
BADCALL  DS    0H
         MVC   RC,=F'12'              R2 = RC.
*        TPUT  =C'BADCALL 12',10
         B     RETURN
BADCHAIN DS    0H
         MVC   RC,=F'16'              R2 = RC.
*        TPUT  =C'BADCALL 16',10
         B     RETURN
XCALL    CALL  ,(,,,,,,,,),VL,MF=L
CALLL    EQU   *-XCALL
         LTORG ,
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A S.                                *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
SAVE     DS    18F                     SAVE AREA
RC       DS    F
ISPLINK  DS    A
CALL     DS    XL(CALLL)
PID      DS    CL8
DYNLEN   EQU   *-DYNAMIC
*
BACK     EQU   X'04',4
SAVR1    EQU   X'18',4
HIORDER  EQU   X'80'
PANELID  EQU   X'158',8,C'C'     CURRENT PANELID.
*
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11                 A(PLIST).
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
         END   &CSECT
