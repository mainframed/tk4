         TITLE 'SUBAUTHX - CHECK FOR AUTHORIZED JOB SUBMISSION PROGRAM'
***********************************************************************
*                                                                     *
* NAME         SUBAUTHX                                               *
*                                                                     *
* COMPONENT    JES2 EXIT FOR ACF2                                     *
*                                                                     *
* FUNCTION     CHECK FOR AUTHORIZED JOB SUBMISSION PROGRAM            *
*                                                                     *
* ENTRY        AS EXIT 221 FROM HASPSSSM                              *
*                                                                     *
* PARAMETERS   DCT                                                    *
*                                                                     *
* INPUT        MVS AND JES CONTROL BLOCKS                             *
*                                                                     *
* OUTPUT       FLAG SET IN ACF2 DCT EXTENSION                         *
*                                                                     *
* RETURN CODES ZERO                                                   *
*                                                                     *
* FILES        NONE                                                   *
*                                                                     *
* EXT REF      NONE                                                   *
*                                                                     *
* MESSAGES     NONE                                                   *
*                                                                     *
* MACROS       $MODULE                                                *
*              $ENTRY                                                 *
*              $MODEND                                                *
*              #ACFJES2                                               *
*              MODESET                                                *
*              RETURN                                                 *
*              SAVE                                                   *
*                                                                     *
* ACF2 TABLES  ACFDCTX             DEVICE CONTROL TABLE EXTENSION     *
*                                                                     *
* JES2 TABLES  DCT                 DEVICE CONTROL TABLE               *
*              SDB                 SUBSYSTEM DATASET BLOCK            *
*                                                                     *
* MVS  TABLES  CDE                 CONTENTS DATA ENTRY                *
*              PSA                 PREFIX SAVE AREA                   *
*              RB                  REQUEST BLOCK                      *
*              TCB                 TASK CONTROL BLOCK                 *
*                                                                     *
* OPERATION    IF THE SUBMITTING PROGRAM IS REENTRANT AND COMES       *
*              FROM AN APF-AUTHORIZED LIBRARY, THE BIT IN THE ACF2    *
*              DCT EXTENSION IS SET TO ALLOW THE 'SUBAUTH'            *
*              RESTRICTION TO BE MET.                                 *
*                                                                     *
* ATTRIBUTES   REENTRANT                                              *
*              REUSABLE                                               *
*              SUPERVISOR STATE                                       *
*              PROBLEM PROGRAM KEY                                    *
*              AUTHORIZED                                             *
*              LPALIB                                                 *
*              AMODE 24                                               *
*              RMODE 24                                               *
*                                                                     *
* RESTRICTIONS MUST BE IN LPA SINCE IT IS INVOKED FROM HASPSSSM       *
*              IN THE USER'S ADDRESS SPACE DURING INTRDR OPEN.        *
*                                                                     *
*              MUST BE INVOKED AT ACF2 EXIT POINT 221 AFTER ACF2XIRD. *
*                                                                     *
*              THE RBCDE POINTER MAY POINT TO EITHER A CDE OR A LPDE. *
*              THE FIELDS CHECKED HERE ARE THE SAME IN BOTH,          *
*              SO ONLY THE CDE NAMES ARE USED.                        *
*                                                                     *
* NOTES        THIS EXIT EXTENDS THE FUNCTIONS PROVIDED BY ACF2       *
*              EXIT ACF2XIRD (INTERNAL READER OPEN) PACKAGED IN       *
*              LOAD MODULE ACF2X1J2. THE EXIT POINT IS INSERTED IN    *
*              HASPSSSM AS PART OF ACF2 INSTALLATION (RELEASE 4.0).   *
*              THE ORIGINAL EXIT IS DESIGNED TO CAPTURE THE           *
*              SUBMITTING ENVIRONMENT WHEN INTRDR IS OPENED. THE      *
*              PROGRAM NAME AND IT'S APF AUTHORIZATION ARE AMONG      *
*              THE DATA PRESERVED. THESE DATA ARE THEN USED DURING    *
*              ACF2 ENTRY VALIDATION, USUALLY FOR A LID WITH THE      *
*              RESTRICT ATTRIBUTE. THE PROGRAM NAME IS MATCHED WITH   *
*              THE PROGRAM SPECIFIED IN THE LID, AND APF              *
*              AUTHORIZATION IS REQUIRED IF THE LID ALSO HAS          *
*              SUBAUTH SPECIFIED.                                     *
*                                                                     *
*              A PROBLEM COMES ABOUT WHEN USING ISPF V2R2M0. AN       *
*              AUTHORIZED PROGRAM CANNOT ISSUE ISPF SERVICE CALLS.    *
*              TO SUPPORT THE ACFFTSUB LOCAL UTILITY, WE RELAX THE    *
*              SUBAUTH DEFINITION SO THAT THE PROGRAM NEED ONLY       *
*              COME FROM AN APF AUTHORIZED LIBRARY. IT NEED NOT       *
*              ACTUALLY BE EXECUTING AUTHORIZED, BUT THAT IS OKAY     *
*              TOO.                                                   *
*                                                                     *
*              TO GUARD AGAINST SOMEONE MUCKING WITH THE EXECUTION    *
*              ENVIRONMENT, WE ALSO REQUIRE THE PROGRAM BE            *
*              REENTRANT. THIS SHOULD PRECLUDE SOMEONE LOADING        *
*              A PROGRAM FROM AN AUTHORIZED LIBRARY AND MODIFYING     *
*              IT WHILE IT IS IN MEMORY SINCE REENTRANT PROGRAMS ARE  *
*              LOADED INTO KEY ZERO STORAGE. THERE IS A SIMILAR CHECK *
*              OF A HANDY FLAG IN THE CDE TO MAKE SURE THE ENTRY      *
*              POINT WAS NOT CREATED BY AN UNAUTHORIZED IDENTIFY.     *
*                                                                     *
*              THERE IS AN IMPLICIT PRESUMPTION HERE THAT ALL APF     *
*              AUTHORIZED LIBRARIES HAVE GOOD UPDATE/ALLOCATE         *
*              CONTROLS.                                              *
*                                                                     *
* PROGRAMMER   C. J. PITTS                                            *
*                                                                     *
* MAINTENANCE  WHO    DATE     DESCRIPTION                            *
*                                                                     *
**********************************************************************/
*                                                                     *
* REGISTERS AT ENTRY                                                  *
*              R10   DCT OR SDB                                       *
*              R11   SSVT                                             *
*              R13   OS SAVE AREA                                     *
*              R14   RETURN ADDRESS                                   *
*              R15   ENTRY POINT                                      *
*                                                                     *
* REGISTER USE IN PROGRAM                                             *
*              R10   ACF2 DCT EXTENSION                               *
*              R11   SSVT                                             *
*              R12   BASE REGISTER                                    *
*              R13   OS SAVE AREA                                     *
*                                                                     *
* REGISTERS AT EXIT                                                   *
*              R15   RETURN CODE                                      *
*                                                                     *
**********************************************************************/
         SPACE 3
*        COPY  $HASPGBL
*        COPY  #ACFVAR
         PRINT OFF
         COPY  $HASPGBL
         COPY  #ACFVAR
         PRINT ON,NOGEN
         #ACFJES2
         SPACE
         #ACFDCTX
         PRINT GEN
         SPACE
SUBAUTHX $MODULE SYSP=(NOGEN,GEN,NODATA,NOGEN,NOGEN),                  X
               TITLE='SUBAUTHX - CHECK FOR AUTHORIZED JOB SUBMISSION PRX
               OGRAM',             LISTING TITLE                       X
               ENVIRON=USER,       HASPSSSM EXIT ENVIRONMENT           X
               $BUFFER,            IO BUFFER DSECT                     X
               $CAT,               CLASS ATTRIBUTE TABLE               X
               $DCT,               DEVICE CONTROL TABLE                X
               $HASPEQU,           REGISTER EQUATES, ETC               X
               $HCT,               HASP COMMUNICATIONS TABLE           X
               $JCT,               JOB CONTROL TABLE                   X
               $JQE,               JOB QUEUE ELEMENT                   X
               $MIT,               MODULE INFORMATION TABLE            X
               $PCE,               PROCESSOR CONTROL ELEMENT           X
               $SDB,               SUBSYSTEM DATASET BLOCK             X
               $TAB,               TRACK ALLOCATION BLOCK              X
               $XECB,              EXTENDED ECB                        X
               CDE,                CONTENTS DATA ENTRY                 X
               PSA,                PREFIX SAVE AREA                    X
               RB,                 REQUEST BLOCK                       X
               TCB                 TASK CONTROL BLOCK
         EJECT
***********************************************************************
* AUTHX221:                                  /*  JES ENTRY POINT  */  *
*        SAVE REGISTERS;                                              *
*        SET BASE REGISTER;                                           *
*        IF DCT WAS NOT PASSED                                        *
*          THEN GO RETURN TO CALLER;                /*  AUTH9000  */  *
*        LOCATE ACF2 DCT EXTENSION;                                   *
*        IF NOT AVAILABLE                                             *
*          THEN GO RETURN TO CALLER;                /*  AUTH9000  */  *
*        IF AUTHORIZATION FLAG IS ALREADY SET IN DCT EXTENSION        *
*          THEN GO RETURN TO CALLER;                /*  AUTH9000  */  *
***********************************************************************
         SPACE
AUTHX221 $ENTRY BASE=R12,CSECT=NO,ENTRY=YES
         SPACE
         SAVE (14,12)
         SPACE
         LR    R12,R15             SET BASE REGISTER
         SPACE
         USING SDBDSECT,R10
         CLC   SDBID,=CL4'SDB'     WAS AN SDB PASSED?
         BE    AUTH9000            YEP...GO EXIT
         SPACE
         USING DCTDSECT,R10
         ICM   R10,B'1111',&ACF2DCT GET ACF2 DCT EXTENSION
         BZ    AUTH9000            NOT THERE...GO EXIT
         USING #ACFDCTX,R10
         CLC   #ACFD,=CL4'ACFD'    CHECK EYECATCHER
         BNE   AUTH9000            NO GOT...GO EXIT
         SPACE
         TM    #ACDFLAG,#ACDAUTH   ALREADY AUTHORIZED?
         BO    AUTH9000            YEP...GO EXIT
         EJECT
***********************************************************************
*        LOCATE TCB VIA PSATOLD;                                      *
*        LOCATE TOP PRB ON TCB;                                       *
*        IF TOP RB IS NOT A PRB                                       *
*          THEN GO RETURN TO CALLER;                /*  AUTH9000  */  *
*        LOCATE CDE/LPDE FROM PRB;                                    *
*        IF CDE/LPDE DOES NOT EXIST                                   *
*          THEN GO RETURN TO CALLER;                /*  AUTH9000  */  *
*        IF PROGRAM IS REENTRANT                           -- AND --  *
*           ENTRY POINT IS NOT FROM UNAUTHORIZED IDENTIFY  -- AND --  *
*           PROGRAM NAME MATCHES THE DCT EXTENSION         -- AND --  *
*           PROGRAM COMES FROM APF-AUTHORIZED LIBRARY                 *
*          THEN SET AUTHORIZED SOURCE FLAG IN DCT EXTENSION;          *
***********************************************************************
         SPACE
         USING PSA,R0
         L     R9,PSATOLD          GET TCB ADDRESS
         USING TCB,R9
         L     R9,TCBRBP           CURRENT RB
         USING RBBASIC,R9
         SPACE
AUTH2200 DS    0H
         TM    RBSTAB2,RBTCBNXT    TOP RB?
         BO    AUTH2400            YEP...ONWARD
         L     R9,RBLINK           NEXT RB
         B     AUTH2200            TRY AGAIN
         SPACE
AUTH2400 DS    0H
         TM    RBSTAB1,RBFTP       IS THE TOP RB A PRB?
         BNZ   AUTH9000            NOPE...GO EXIT
         ICM   R8,B'0111',RBCDE1   CDE/LPDE
         BZ    AUTH9000            NOT THERE...GO EXIT
         USING CDENTRY,R8
         SPACE
         CLC   #ACDPROG,CDNAME     PROGRAM NAME MATCH?
         BNE   AUTH3800            NOPE...NOT AUTHORIZED
         TM    CDATTRB,CDIDENTY    UNAUTHORIZED IDENTIFY?
         BO    AUTH3800            YEP...NOT AUTHORIZED
         TM    CDATTR,CDREN        REENTRANT?
         BNO   AUTH3800            NOPE...NOT AUTHORIZED
         TM    CDATTR2,CDSYSLIB    APF-AUTHORIZED LIBRARY?
         BNO   AUTH3800            NOPE...NOT AUTHORIZED
         SPACE
         MODESET EXTKEY=ZERO,SAVEKEY=(2)
         SPACE
         OI    #ACDFLAG,#ACDAUTH   SET FLAG TO ALLOW SUBAUTH
         SPACE
         MODESET KEYREG=2
         SPACE
AUTH3800 DS    0H
         EJECT
***********************************************************************
* AUTH9000:                                      /*  MODULE EXIT  */  *
*        SET RETURN CODE ZERO;                                        *
*        RESTORE REGISTERS;                                           *
*        RETURN TO CALLER;                                            *
***********************************************************************
         SPACE
AUTH9000 DS    0H
         RETURN (14,12),RC=0
         SPACE 2
         LTORG
         SPACE 3
***********************************************************************
*        JES EPILOGUE                                                 *
***********************************************************************
         SPACE
         PRINT NOGEN
         $MODEND
         SPACE 2
         END
