         TITLE 'COMMAND - ISSUE SVC34 FOR PROBLEM PROGRAM'
         MACRO
&NAME    SETUP &BASE2=
&NAME    CSECT ,                            BEGIN CSECT
         USING &NAME,R15                    TEMP. ADDRESS.
         B     S&SYSNDX
         DC    17XL4'FFFFFFFF'              SAVEAREA
         DC    CL8'&NAME'                  'EYE CATCHER'
         DC    CL8'&SYSDATE',CL6'&SYSTIME'  AUDIT TRAIL
         AIF   ('&BASE2' EQ '').HNDBAS0
B&SYSNDX DC    A(&NAME+X'1000')
.HNDBAS0 ANOP
S&SYSNDX DS    0H                           FOR COMPUTATION
         STM   R14,R12,12(R13)              STORE HIS REGS.
         LR    R2,R13                       SAVE ADDRESS OF SA.
         LR    R13,R15                      SET UP NORMAL BASE
         AIF   ('&BASE2' NE '').HNDBAS1
         USING &NAME,R13                    NORMAL ADDRESS.
         AGO   .HNDBAS2
.HNDBAS1 ANOP
         USING &NAME,R13,&BASE2             OBTAIN ADDRESSIBILITY
         L     &BASE2,B&SYSNDX              LOAD SECOND BASE
.HNDBAS2 ANOP
         XR    R15,R15
         DROP  R15                          DON'T NEED THIS
         ST    R2,4(R13)                    BACKWARD POINTER
         ST    R13,8(R2)                    FORWARD POINTER
         MEND
***********************************************************************
*                                                                     *
*        PROG  - COMMAND                                              *
*        USE   - ALLOW COMMANDS TO BE ISSUED BY PROGRAM CONTROL       *
*                EITHER THROUGH THE PARM FIELD OR BY A CALL FROM      *
*                ANOTHER PROGRAM.                                     *
*        DATE  - 6/16/82                                              *
*                                                                     *
*                                                                     *
*        REGS                                                         *
*        R1  - PARM REG                                               *
*        R2  - PTR TO FULLWORD AUTHORIZATION CODE OR ZERO             *
*        R3  - PTR TO COMMAND LIST (HALFWORD LENGTH, THEN COMMAND)    *
*        R4  - WORK REG                                               *
*        R5  - WORK REG                                               *
*        R6  -                                                        *
*        R7  -                                                        *
*        R8  -                                                        *
*        R9  -                                                        *
*        R10 -                                                        *
*        R11 - BAL REGISTER                                           *
*        R12 -                                                        *
*                                                                     *
*        ATTRIBUTES                                                   *
*        - AUTHORIZED, SUPERVISOR STATE (FOR SVC 34)                  *
*                                                                     *
*        METHOD-OF-OPERATION                                          *
*        1. PARM FIELD IS PROCESSED :                                 *
*           IF ONE ADDRESS IN LIST, PROGRAM IS MAIN PROGRAM AND       *
*           COMMAND EXISTS IN PARM FIELD PASSED BY MVS.               *
*                                                                     *
*           IF TWO ADDRESSES IN LIST, PROGRAM IS SUBPROGRAM AND :     *
*           PARM 1 -> FULLWORD AUTHORIZATION FIELD                    *
*           PARM 2 -> COMMAND LIST FORMATTED AS IN MVS PARM FIELD     *
*                                                                     *
*        5. IF ISSUE -> SVC 34                                        *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
COMMAND  SETUP
         EJECT
***********************************************************************
*                                                                     *
*        MAIN CALL ROUTINES                                           *
*                                                                     *
*                                                                     *
***********************************************************************
MAIN0    DS    0H
         BAL   R11,IPARM0              PROCESS THE PARM
MAIN1    DS    0H
         BAL   R11,CMD0                ISSUE COMMAND
         B     RETURN                  AND GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        PROCESS THE PARM -                                           *
*                                                                     *
*         IF ONE FIELD, PARM FIELD FROM EXECUTE CARD                  *
*         IF TWO FIELDS, CALLED FROM HIGHER LEVEL ROUTINE             *
*                                                                     *
*                                                                     *
***********************************************************************
IPARM0   DS    0H
         XR    R2,R2                           AND CLEAR R2
         L     R3,0(,R1)               LOAD FIRST ADDRESS
         LTR   R3,R3                   IS IT THE ONLY ONE?
         BM    IPARM1                     NO, CONTINUE
         L     R2,4(,R1)              LOAD THE SECOND ADDRESS
         L     R2,0(,R2)              LOAD THE SECOND PARAMETER
IPARM1   DS    0H
         LH    R4,0(,R3)              LOAD LENGTH OF COMMAND
         CH    R4,=H'79'             CHECK LENGTH LIMIT             ptw
         BNH   IPARM2                    GOOD, CONTINUE
         LH    R4,=H'79'                BAD, FORCED SET TO LIMIT    ptw
IPARM2   DS    0H
         MVI   COMAND,C' '                                          ptw
         MVC   COMAND(L'COMAND-1),COMAND+1                          ptw
         LA    R5,4(,R4)              GET LENGTH OF COMMAND + 4
         STH   R5,COMLEN              SAVE ACTUAL LENGTH
         BCTR  R4,0                   BUMP DOWN BY ONE FOR HARDWARE
EXECUTED MVC   COMAND(0),2(R3)        EXECUTED INSTRUCTION
         EX    R4,EXECUTED            MOVE COMMAND INTO AREA
         MVC   WTO,XWTO                                             ptw
         EXTRACT PSCBADDR,'S',FIELDS=PSB                            ptw
         L     R1,PSCBADDR                                          ptw
         LTR   R1,R1                                                ptw
         BZ    *+10                                                 ptw
         MVC   WTOUSER,PSCBUSER-PSCB(R1)                            ptw
         EX    R4,WTOMOVE             MOVE COMMAND INTO WTO AREA    ptw
         BR    R11                    AND RETURN
         EJECT
***********************************************************************
*                                                                     *
*        ISSUE COMMAND                                                *
*                                                                     *
*                                                                     *
***********************************************************************
CMD0     DS    0H
*        XAUTHON ,
         WTO  MF=(E,WTO)                                            ptw
         MODESET KEY=ZERO              GET AUTHORIZED FOR COMMANDS
         LA    R0,0                    A SYSTEM REQUEST
*        LR    R0,R2                   LOAD COMMAND AUTHORIZATION IN R0
         LA    R1,COMAREA              POINT TO COMMAND
         MGCR  (1)                     ISSUE COMMAND
         STC   R15,RCODE               SAVE RETURN CODE
         MODESET KEY=NZERO             NO MORE AUTH.
*        XAUTHOFF ,
CMD99    DS    0H
         BR    R11
         EJECT
***********************************************************************
*                                                                     *
*         RETURN TO SYSTEM                                            *
*                                                                     *
*                                                                     *
***********************************************************************
RETURN   DS    0H
         XR    R15,R15
         IC    R15,RCODE
         L     R13,4(R13)
         RETURN (14,12),T,RC=(15)
         EJECT
***********************************************************************
*                                                                     *
*        DATA AREAS                                                   *
*                                                                     *
*                                                                     *
***********************************************************************
RCODE    DC    XL1'00'                 RETURN CODE
PSCBADDR DS    F                                                    ptw
COMAREA  DS    0H                      RESULTANT COMMAND
COMLEN   DC    H'0',H'0'               1ST IS LENGTH, SECOND IS IGNORED
COMAND   DC    CL80' '                 COMMAND AREA                 ptw
XWTO WTO 'SPY010I  USERID :                                            X
                                                   ',ROUTCDE=2,MF=L
WTOLEN   EQU   *-XWTO                                               ptw
WTO      DS    XL(WTOLEN)         WTO AREA.                         ptw
WTOUSER  EQU   WTO+13,7           USERID.                           ptw
WTOMOVE  MVC   WTO+22(*-*),2(R3)                                    ptw
         EJECT
***********************************************************************
*                                                                     *
*         REGISTER EQUATES, LITERAL POOL                              *
*                                                                     *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG  *
         IKJPSCB                                                    ptw
         END   COMMAND
