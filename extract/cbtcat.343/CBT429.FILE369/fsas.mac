PROC 0 WORK(900) BLOCK(4114) QUAL(SAS) SORT(YES) SORTWORK(5) FMT(YES) +
       GRAPH(YES) +
       SASUTL  ('APS1.SAS.UTILLIB') +
       SASHELP ('APS1.SAS.SASHELP') +
       SASAUTOS('APS1.SAS.MACAUTOS') +
       SASMSGS ('APS1.SAS.SASMSGS')
/* LAST CHANGE:
/* 04/20/87 PTW ALL PROFILE VARS BEGIN WITH SAS, FIX SASOPT BUG.
CONTROL NOLIST NOCONLIST NOFLUSH NOMSG MAIN END(END)
ISPEXEC CONTROL ERRORS RETURN
FREE F(SASPGM,FT11F001,FT12F001,FT14F001,SASLIB,LIBRARY,+
  WORK,PRINTTO,SASUTL,SASHELP,SASAUTOS,SASMSGS) ATTRLIST(SASP,PO)
ATTRIB SASP RECFM(V B A) LRECL(137) BLKSIZE(&BLOCK.)
ATTRIB PO   DSORG(PO)
ALLOC F(WORK) BLOCK(6394) SPACE(&WORK. 20)
/* ALLOCATE FORMAT LIBRARY.
IF &FMT = &STR(YES) THEN +
  DO
    DEL &QUAL..LIB
    ALLOC F(LIBRARY) TRACK SPACE(15 5) USING(PO) DI(10) +
      DA(&QUAL..LIB)
    ALLOC F(SASLIB) DA(&QUAL..LIB) SHR
  END
/* ALLOCATE SAS LOG.
ALLOC DA(&QUAL..LOG) F(FT11F001) SHR
IF &LASTCC NE 0 THEN +
  ALLOC F(FT11F001) DA(&QUAL..LOG) NEW TRA +
    USING(SASP) SPACE(10 10)
/* ALLOCATE SAS LIST.
ALLOC DA(&QUAL..LIST) F(FT12F001) USING(SASP) SHR
IF &LASTCC NE 0 THEN +
  ALLOC F(FT12F001) DA(&QUAL..LIST) NEW TRA +
    USING(SASP) SPACE(10 10)
/* ALLOCATE GRAPHICS FILES.
IF &GRAPH = &STR(YES) THEN +
  ALLOC F(FT14F001) DA(*)
/* ALLOCATE SPECIAL SAS STATEMENT FOR PROC PRINTTO.
ALLOC DA(&QUAL..PRINTTO) F(PRINTTO) SHR
IF &LASTCC NE 0 THEN +
  DO
    ALLOC F(PRINTTO) DA(&QUAL..PRINTTO) NEW BLOCK(80) SP(1 1)
    OPENFILE PRINTTO OUTPUT
    SET PRINTTO=&STR(PROC PRINTTO UNIT=12 NEW;)
    PUTFILE  PRINTTO
    CLOSFILE PRINTTO
    FREE F(PRINTTO)
  END
/* ALLOCATE SORT FILES.
IF &SORT = &STR(YES) THEN +
  DO
    FREE  F(SORTLIB,SORTWK01,SORTWK02,SORTWK03,SYSOUT,SYSORT)
 /* ALLOC F(SORTLIB) DA('SYS1.SYNCLIB') SHR
    ALLOC F(SORTWK01) BLOCK(13030) SPACE(&SORTWORK.)
    ALLOC F(SORTWK02) BLOCK(13030) SPACE(&SORTWORK.)
    ALLOC F(SORTWK03) BLOCK(13030) SPACE(&SORTWORK.)
    ALLOC F(SYSOUT) DUMMY BLOCK(80)
    ALLOC F(SYSORT) DUMMY BLOCK(80)
  END
/* ALLOCATE V5 LIBRARIES.
ALLOC F(SASUTL  ) DA('&SASUTL'  ) SHR
ALLOC F(SASHELP ) DA('&SASHELP' ) SHR
ALLOC F(SASAUTOS) DA('&SASAUTOS') SHR
ALLOC F(SASMSGS)  DA('&SASMSGS') SHR
SET &ERRMSG = &STR()
SET &ERRSM  = &STR()
SET &ERRLM  = &STR()
ISPEXEC VPUT (QUAL ERRMSG ERRSM ERRLM) SHARED
ISPEXEC VGET (SASPGM,SASOPT,SASPDEST,+
              SASDD1,SASDD2,SASDD3,+
              SASDSN1,SASDSN2,SASDSN3,+
              SASDISP1,SASDISP2,SASDISP3,+
              SASP1,SASP2,SASP3) PROFILE
SET &RC = &LASTCC
IF  &RC = 8 THEN +
  ISPEXEC VPUT (SASPGM,SASOPT,SASPDEST,+
                SASDD1,SASDD2,SASDD3,+
                SASDSN1,SASDSN2,SASDSN3,+
                SASDISP1,SASDISP2,SASDISP3,+
                SASP1,SASP2,SASP3) PROFILE
ISPEXEC SELECT PANEL(FSASP)
FREE F(SASPGM,FT11F001,FT12F001,FT14F001,SASLIB,LIBRARY,+
  WORK,PRINTTO,SORTWK01,SORTWK02,SORTWK03,SYSOUT,SYSORT,+
  SASUTL,SASHELP,SASAUTOS,SASMSGS) +
  ATTRLIST(SASP,PO)
EXIT CODE(0)
END
