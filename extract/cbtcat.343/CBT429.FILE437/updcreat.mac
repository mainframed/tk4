         OPSYNBRI ,
UPDCREAT CSECT ,
UPDCREAT RMODE 24
UPDCREAT AMODE 31
         BAKR  R14,0
         BASR  R12,0
         LA    R0,*-UPDCREAT
         SLR   R12,R0
         USING UPDCREAT,R12
         LA    R13,SAVEAREA
         MVC   4(4,R13),=C'F1SA'
*
         OPEN  (SYSUT1D,(INPUT),SYSUT2,(OUTPUT)),MODE=31
*
         RDJFCB SYSUT1
         OI    JFCBIND1,JFCPDS
*
GETDREC  DS    0H
         GET   SYSUT1D
         LH    R2,0(,R1)               LENGTH OF DIRECTORY BLOCK
         BCTR  R2,0
         LA    R3,2(,R1)
         LA    R5,0(R2,R1)
MEMLOOP  DS    0H
         CLC   =X'FFFFFFFFFFFFFFFF',0(R3)      END-OF-DIRECTORY
         BE    EODIR
*
         PUT   SYSUT2                  INSERT IEBUPDTE CONTROL CARD
         MVC   0(L'ADDCARD,R1),ADDCARD
         MVC   12(8,R1),0(R3)            INSERT MEMBER NAME
*
         TM    11(R3),B'10000'         ADD ISPF STATS TO CONTROL CARD
         BO    NOSTATS
         TM    11(R3),B'01111'
         BNO   NOSTATS
         SLR   R2,R2
         IC    R2,12(,R3)              VV
         CVD   R2,DWORD
         UNPK  24(2,R1),DWORD+6(2)
         OI    25(R1),C'0'
         IC    R2,13(,R3)              MM
         CVD   R2,DWORD
         UNPK  27(2,R1),DWORD+6(2)
         OI    28(R1),C'0'
         UNPK  30(5,R1),17(3,R3)       CREATED (DATE)
         OI    34(R1),C'0'
         UNPK  36(5,R1),21(3,R3)       CHANGED (DATE)
         OI    40(R1),C'0'
         MVO   DWORD+2(2),15(1,R3)     CHANGED (TIME)
         MVO   DWORD(3),24(2,R3)
         UNPK  42(6,R1),DWORD(4)
         OI    47(R1),C'0'
         LH    R2,26(,R3)              SIZE
         CVD   R2,DWORD
         UNPK  49(4,R1),DWORD+5(3)
         OI    52(R1),C'0'
         LH    R2,28(,R3)              INIT
         CVD   R2,DWORD
         UNPK  54(4,R1),DWORD+5(3)
         OI    57(R1),C'0'
         LH    R2,30(,R3)              MOD
         CVD   R2,DWORD
         UNPK  59(4,R1),DWORD+5(3)
         OI    62(R1),C'0'
         MVC   64(7,R1),32(R3)         ID
NOSTATS  DS    0H
*
         MVC   JFCBELNM,0(R3)          COPY MEMBER
         OPEN  (SYSUT1,(INPUT)),TYPE=J
GSYSUT1  DS    0H
         GET   SYSUT1
         LR    R2,R1
         PUT   SYSUT2
         MVC   0(80,R1),0(R2)
         CLC   0(2,R1),ADDCARD
         BNE   GSYSUT1
         MVI   0(R1),C'@'              CHANGE ./ TO ./ ON INPUT
         B     GSYSUT1
EODAD    DS    0H
         CLOSE (SYSUT1),MODE=31
*
         IC    R4,11(,R3)              USERDATA LENGTH IN HW
         N     R4,=XL4'1F'
         SLL   R4,1
         LA    R4,12(,R4)              ENTRY LENGTH
         BXLE  R3,R4,MEMLOOP
         B     GETDREC
EODIR    DS    0H
*
         CLOSE (SYSUT1D,,SYSUT2),MODE=31
*
         PR    ,
*
         LTORG ,
*
ADDCARD  DC    CL80'./ ADD NAME='
*
SYSUT1D  DCB   DDNAME=SYSUT1,                                          X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               MACRF=GL,                                               X
               LRECL=256,                                              X
               BLKSIZE=256,                                            X
               DCBE=SYSUT1DE
SYSUT1DE DCBE  RMODE31=BUFF
*
SYSUT1   DCB   DDNAME=SYSUT1,                                          X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               MACRF=GL,                                               X
               EXLST=EXITLIST,                                         X
               DCBE=SYSUT1E
EXITLIST DC    AL1(X'87'),AL3(INFMJFCB)
         IEFJFCBN ,
SYSUT1E  DCBE  RMODE31=BUFF,                                           X
               EODAD=EODAD
*
SYSUT2   DCB   DDNAME=SYSUT2,                                          X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               MACRF=PL,                                               X
               RECFM=FB,                                               X
               LRECL=80,                                               X
               DCBE=SYSUT2E
SYSUT2E  DCBE  RMODE31=BUFF
*
SAVEAREA DS    18F
DWORD    DS    1D
*
         YREGS ,
*
         END   ,
