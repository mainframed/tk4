./ ADD NAME=JJ$CDE      01 00 98033 98033 145139 0019 0019 0000 IBMUSER
//IBMUSERC JOB ACCT,'JAN JAEGER',NOTIFY=IBMUSER,CLASS=A,MSGCLASS=A
//ASMA90   EXEC PGM=ASMA90,PARM='DECK,NOOBJ,XREF(SHORT)'
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=IBMUSER.ISTEXCAA.SOURCE,DISP=SHR
//SYSUT1   DD  UNIT=SYSALLDA,SPACE=(CYL,10)
//SYSIN    DD  DSN=IBMUSER.ISTEXCAA.SOURCE(JJ$RRCDE),DISP=SHR
//SYSPUNCH DD  DSN=&&PUNCH,DISP=(MOD,PASS),
//          UNIT=SYSALLDA,SPACE=(CYL,10)
//IEWL     EXEC PGM=IEWL,PARM='RENT,REFR,XREF'
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=SYSALLDA,SPACE=(CYL,10)
//SYSLMOD  DD  DSN=SYS1.LINKLIB,DISP=SHR
//SYSLIN   DD  DSN=&&PUNCH,DISP=(OLD,DELETE)
//         DD  *
 ORDER F1REWALL,ICHRRCDE
 ENTRY F1REWALL
 NAME  ICHRRCDE(R)
./ ADD NAME=JJ$EXC      01 00 98033 98033 144901 0030 0030 0000 IBMUSER
//IBMUSERV JOB ACCT,'JAN JAEGER',NOTIFY=IBMUSER,CLASS=A,MSGCLASS=A
//ASMMEM   PROC MEM=
//ASMA90   EXEC PGM=ASMA90,PARM='DECK,NOOBJ,XREF(SHORT)'
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=IBMUSER.ISTEXCAA.SOURCE,DISP=SHR
//SYSUT1   DD  UNIT=SYSALLDA,SPACE=(CYL,10)
//SYSIN    DD  DSN=IBMUSER.ISTEXCAA.SOURCE(&MEM),DISP=SHR
//SYSPUNCH DD  DSN=&&PUNCH,DISP=(MOD,PASS),
//          UNIT=SYSALLDA,SPACE=(CYL,10)
// PEND
// EXEC ASMMEM,MEM=JJ$EXCAA
// EXEC ASMMEM,MEM=JJ$EXCRR
// EXEC ASMMEM,MEM=JJ$EXCCM
// EXEC ASMMEM,MEM=JJ$EXC00
// EXEC ASMMEM,MEM=JJ$EXC01
// EXEC ASMMEM,MEM=JJ$EXC09
// EXEC ASMMEM,MEM=JJ$EXC0A
// EXEC ASMMEM,MEM=JJ$EXCFE
// EXEC ASMMEM,MEM=JJ$EXCFF
//IEWL     EXEC PGM=IEWL,PARM='RENT,REFR,AMODE=31,RMODE=ANY,XREF'
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=SYSALLDA,SPACE=(CYL,10)
//SYSLMOD  DD  DSN=SYS1.VTAMLIB,DISP=SHR
//SYSLIN   DD  DSN=&&PUNCH,DISP=(OLD,DELETE)
//         DD  *
 ORDER JJ$EXCAA
 ENTRY JJ$EXCAA
 NAME  ISTEXCAA(R)
./ ADD NAME=JJ$EXCAA    01 00 98033 98033 144012 0141 0141 0000 IBMUSER
 TITLE 'ISTEXCAA - VTAM SESSION SETUP VERIFICATION BY RACF'
***********************************************************************
*                                                                     *
* JUSTIFICATION:                                                      *
*                                                                     *
*   THIS EXIT PROVIDES A VTAM FIREWALL FUNCTION                       *
*                                                                     *
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   TO ALLOW OR PROHIBIT THE SETUP OF VTAM SESSIONS BY MEANS OF       *
*   RACF PROFILES                                                     *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   CHECK IF LU IS ALLOWED ACCESS TO DESTINATION                      *
*     (PROFILE PRIMNET.PRIMLU.SECNET.SECLU)                           *
*                                                                     *
*   IF NO ACCESS THEN CHECK FOR CLSDSP PASS                           *
*     (PROFILE PRIMNET.PRIMLU.SECNET.SECLU.INITNET.INITLU)            *
*     IF ACCESS GRANTED (PROFILE MUST EXIST) THEN SESSION SETUP       *
*     WILL CONTINUE.                                                  *
*                                                                     *
*   IF DURING INITIAL AUTHORIZATION NOT ALL QUALIFIERS ARE KNOWN      *
*     THEN ACCESS CHECKING WILL TAKE PLACE IN THE SECONDARY           *
*     AUTHORIZATION EXIT.  THIS MAINLY OCCURS WHEN A SESSION SETUP    *
*     IS ATTEMPTED WITH A PARTNER LU FOR WHICH NO CROSS DOMAIN        *
*     RESOURCE EXISTS, DURING SECONDARY AUTHORIZATION THIS WILL       *
*     EXISTS AND HENCE THE TRUE NAME WILL BE KNOWN. IF THE TRUENAME   *
*     CAN STILL NOT BE RESOLVED (TARGET OUTSIDE OUR NETWORK) THEN     *
*     A TARGET OF ?.NETID WILL BE CONSTRUCTED                         *
*                                                                     *
*                                                                     *
* CONSTANTS:                                                          *
*                                                                     *
*   MEMBER JJ$EXCDS CONTAINS THE RACF RESOURCE CLASS (LABEL RACCLASS) *
*                                                                     *
*                                                                     *
* EXAMPLES:                                                           *
*                                                                     *
*   ALLOW CLSDSTPASS FROM TCAS:                                       *
*     RDEF VTAMWALL *.*.*.*.OURNETID.XTSO UACC(R)                     *
*                                                                     *
*   ALLOW ONLY LUS FROM OUR NETWORK TO TSO                            *
*     RDEF VTAMWALL OUTNETID.XTSO.OURNETID.* UACC(R)                  *
*   AND PREVENT OTHERS:                                               *
*     RDEF VTAMWALL OURNETID.XTSO.*.* UACC(N)                         *
*                                                                     *
*                                                                     *
* VTAM:                                                               *
*                                                                     *
*   VERSION 4.3.0 REQUIRED FOR VTAM EXIT SERVICES                     *
*                                                                     *
*                                                                     *
* RACF:                                                               *
*                                                                     *
*   VERSION 2.1.0 REQUIRED FOR RACLIST GLOBAL=YES                     *
*                                                                     *
*   IF PRIMNET.PRIMLU.SECNET.SECLU IS REJECTED A SECOND CHECK ON      *
*     PRIMNET.PRIMLU.SECNET.SECLU.INITNET.INITLU IS WHICH MAY         *
*     OVERRIDE THE FIRST CHECK IF THE 2ND PROFILE IS DEFINED          *
*                                                                     *
*   SETR GENERIC(VTAMWALL) GENCMD(VTAMWALL) RACLIST(VTAMWALL)         *
*                                                                     *
*   NOTE:                                                             *
*     ALWAYS CODE AT LEAST 2 NON-WILDCARDS IN THE PROFILES AS RACF    *
*     DOES NOT HANDLE MODE THAN 5 GENERIC QUALIFIERS CORRECTLY        *
*                                                                     *
*     THE COMMAND                                                     *
*       MODIFY NET,EXIT,ID=ISTEXCAA,OPTION=REPL                       *
*     WILL REINITIALIZE THE EXIT AND PICKUP ANY CHANGES IN THE        *
*     RACFCLASS (SUCH AS ACTIVATION OF THE F1REWALL CLASS)            *
*                                                                     *
*                                                                     *
*   RACF ROUTER TABLE (ICHRFR01):                                     *
*                                                                     *
*     ICHRFRTB CLASS=F1REWALL,ACTION=RACF                             *
*                                                                     *
*                                                                     *
*   RACF CLASS DESCRIPTOR TABLE:                                      *
*                                                                     *
*     ICHERCDE CLASS=F1REWALL,              CLASS NAME                *
*           FIRST=ANY,                      FIRST CHAR                *
*           OTHER=ANY,                      OTHER CHARS               *
*           MAXLNTH=54,                     MAXIMUM LENGTH            *
*           ID=255,                         ID NUMBER                 *
*           POSIT=255,                      POSIT VALUE               *
*           GENLIST=ALLOWED,                ALLOW GENLIST             *
*           RACLIST=ALLOWED,                CLASS MAY BE RACLISTED    *
*           RACLREQ=YES                     MUST ISSUE RACLIST        *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXCAA RSECT ,
JJ$EXCAA AMODE 31
JJ$EXCAA RMODE ANY
         SAVE  (14,12),,JJ$EXCAA-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXCAA,R12
*
         L     R11,4(,R1)              ADDRESS OF FUNCTION CODE
*
         LM    R3,R5,=A(EXCAAFT,L'EXCAAFT,EXCAAFE)
EXCAALP  DS    0H
         CLC   0(1,R3),0(R11)          FIND EXIT ROUTINE IN TABLE
         BE    EXCAART
         BXLE  R3,R4,EXCAALP           LOOP THROUGH TABLE
*
         RETURN (14,12),RC=0           RETURN TO CALLER IF NO EXIT
*
EXCAART  DS    0H
         L     R15,4(,R3)              ADDRESS OF EXIT ROUTINE
         L     R14,12(,R13)            RELOAD ALL
         LM    R0,R12,20(R13)            REGISTERS
         BR    R15                     BRANCH TO EXIT ROUTINE
*
         DROP  R12
*
EXCAAFT  DS    0D
         DC    AL1(X'00'),AL3(0),V(JJ$EXC00)
         DC    AL1(X'01'),AL3(0),V(JJ$EXC01)
         DC    AL1(X'09'),AL3(0),V(JJ$EXC09)
         DC    AL1(X'0A'),AL3(0),V(JJ$EXC0A)
         DC    AL1(X'FE'),AL3(0),V(JJ$EXCFE)
EXCAAFE  DC    AL1(X'FF'),AL3(0),V(JJ$EXCFF)
*
         LTORG ,
*
         YREGS ,
*
         END   ,
./ ADD NAME=JJ$EXCCM    01 00 98033 98033 144100 0017 0017 0000 IBMUSER
 TITLE 'ISTEXCCM - COMMOM DATA AREA'
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   PROVIDE COMMON DATA AREAS FOR SESSION AUTHORIZATION EXIT          *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
         JJ$EXCDS RSECT=YES
*
         END   ,
./ ADD NAME=JJ$EXCDS    01 00 98033 98033 135745 0101 0101 0000 IBMUSER
.**********************************************************************
.*                                                                    *
.* FUNCTION:                                                          *
.*                                                                    *
.*   GENERATE COMMON DATA AREAS FOR SESSION AUTHORIZATION MODULES     *
.*                                                                    *
.*                                                                    *
.* CONSTANTS:                                                         *
.*                                                                    *
.*   RACF RESOURCE CLASS DEFINED AT LABEL RACCLASS                    *
.*                                                                    *
.*                                                                    *
.* ATTRIBUTES:                                                        *
.*   AMODE(31),RMODE(ANY),REFR                                        *
.*                                                                    *
.*                                                                    *
.* HISTORY:                                                           *
.*                                                                    *
.*   DATE       BY              COMMENTS                              *
.*   02/02/98   JAN JAEGER      INITIAL VERSION                       *
.*                                                                    *
.**********************************************************************
         MACRO ,
         JJ$EXCDS &RSECT=NO
         AIF   ('&RSECT.' NE 'NO').RSECT
JJ$EXCDS DSECT ,
         EXTRN JJ$EXCCM
         AGO   .DSECT
.RSECT   ANOP  ,
JJ$EXCCM RSECT ,
JJ$EXCCM AMODE 31
JJ$EXCCM RMODE ANY
.DSECT   ANOP  ,
*
RACCLASS DC    CL8'F1REWALL'           RESOURCE CLASS
*
MFLRSTAT RACROUTE REQUEST=STAT,                                        X
               RELEASE=2.1,                                            X
               CLASS=RACCLASS,                                         X
               MF=L
LENRSTAT EQU   *-MFLRSTAT
*
MFLRVRFY RACROUTE REQUEST=VERIFY,                                      X
               ACEE=,                                                  X
               ENVIR=,                                                 X
               LOC=ANY,                                                X
               RELEASE=2.1,                                            X
               SYSTEM=YES,                                             X
               MF=L
LENRVRFY EQU   *-MFLRVRFY
*
MFLRLIST RACROUTE REQUEST=LIST,                                        X
               CLASS=RACCLASS,                                         X
               ENVIR=,                                                 X
               GLOBAL=YES,                                             X
               RELEASE=2.1,                                            X
               MF=L
LENRLIST EQU   *-MFLRLIST
*
MFLFAUTH RACROUTE REQUEST=FASTAUTH,                                    X
               RELEASE=2.1,                                            X
               CLASS=RACCLASS,                                         X
               ENTITY=,                                                X
               ATTR=READ,                                              X
               ACEE=,                                                  X
               MF=L
LENFAUTH EQU   *-MFLFAUTH
*
         LTORG ,
*
USERAREA DSECT ,
ANONACEE DS    1F                      ANONYMOUS ACEE (USERID='*')
USERLEN  EQU   *-USERAREA
*
WORKAREA DSECT ,
SAVEAREA DS    18F
WORKARAC DS    0D
RACSAVE  DS    18F                     SAVEAREA FOR EXCRR
PROFILEP DS    0D                      EXIT SERVICES PARAMETER
PROFRSVD DS    H
PROFILEL DS    H                       MESSAGE LENGTH
PROFILEA DS    A
PROFMSG  DS    C'JJ$EXCXX REJECTING '
PROFMSGR EQU   PROFMSG+9,9,C'C'
PROFMSGI EQU   PROFMSG+6,2,C'C'
*        DC    C'         ALLOWING '
PROFILE  DS    CL54                    RACF PROFILE TO BE CHECKED
         DS    0F
RACRSTAT DS    XL(LENRSTAT)
         DS    0F
RACRVRFY DS    XL(LENRVRFY)
         DS    0F
RACRLIST DS    XL(LENRLIST)
         DS    0F
RACFAUTH DS    XL(LENFAUTH)
SAFWK    DS    128F                    SAF WORK AREA
SAFFWK   DS    16F                     FASTAUTH WORKAREA
WORKLEN  EQU   *-WORKAREA
*
         MEXIT ,
         MEND  ,
./ ADD NAME=JJ$EXCFE    01 00 98033 98033 144110 0148 0148 0000 IBMUSER
 TITLE 'JJ$EXCFE - BEGIN FUNCTION X''FE'''
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   INITIALIZE SESSION AUTHORIZATION EXIT                             *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   CHECK IF F1REWALL CLASS IS ACTIVE                                 *
*   IF NOT ISSUE MESSAGE AND EXIT                                     *
*                                                                     *
*   OBTAIN STORAGE FOR USER DATA AREA                                 *
*   INITIALIZE ANONYMOUS ACEE (USERID='*')                            *
*   RACLIST F1REWALL CLASS                                            *
*                                                                     *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*   R1    ENTRY PARAMETER                                             *
*   R13   SAVEAREA POINTER                                            *
*   R14   RETURN ADDRESS                                              *
*   R15   ENTRY POINT ADDRESS / RETURN CODE                           *
*                                                                     *
*   R6    COMMON DATA AREA                                            *
*   R11   SAVED ENTRY PARAMETER                                       *
*   R13   SAVEAREA/WORKAREA POINTER                                   *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXCFE RSECT ,
JJ$EXCFE AMODE 31
JJ$EXCFE RMODE ANY
         SAVE  (14,12),,JJ$EXCFE-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXCFE,R12
*
         LR    R11,R1                  SAVE ENTRY PARAMETER
*
         STORAGE OBTAIN,                                               X
               LENGTH=WORKLEN
         ST    R13,4(,R1)              CHAIN SAVEAREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORKAREA,R13
*
         L     R6,=A(JJ$EXCCM)         MAKE COMMON SECTION ADDRESSABLE
         USING JJ$EXCDS,R6
*
         STORAGE OBTAIN,               OBTAIN USER DATA AREA           X
               LENGTH=USERLEN
         L     R15,8(,R11)             ADDRESS OF USER DATA FIELD
         LR    R10,R1
         USING USERAREA,R10            USER DATA AREA
         XC    USERAREA(USERLEN),USERAREA
         ST    R10,0(,R15)             STORE ADDRESS
*
         L     R9,12(,R11)             ADDRESS OF EXIT OPTIONS
         OC    0(2,R9),=BL2'1100100000000010' PRIM/SEC/END/MODIFY
*
         LA    R8,EXCFEM2G             NOT ACTIVE MESSAGE
*
         MVC   RACRSTAT,MFLRSTAT
         RACROUTE REQUEST=STAT,        CHECK IF RACF CLASS IS ACTIVE   X
               RELEASE=2.1,                                            X
               WORKA=SAFWK,                                            X
               MF=(E,RACRSTAT)
         LTR   R15,R15
         BNZ   EXCFENA
*
         MVC   RACRLIST,MFLRLIST       RACLIST THE FIREWALL CLASS
         RACROUTE REQUEST=LIST,          TO ALLOW FOR FASTAUTH         X
               ENVIR=CREATE,                                           X
               GLOBAL=YES,                                             X
               RELEASE=2.1,                                            X
               WORKA=SAFWK,                                            X
               MF=(E,RACRLIST)
         LTR   R15,R15
         BNZ   EXCFENA
*
         MVC   RACRVRFY,MFLRVRFY       CREATE ANONYMOUS ACEE FOR
         RACROUTE REQUEST=VERIFY,        FOR WHICH ACCESS CHECKING     X
               ACEE=ANONACEE,            WILL BE DONE                  X
               ENVIR=CREATE,                                           X
               LOC=ANY,                                                X
               RELEASE=2.1,                                            X
               SYSTEM=YES,                                             X
               WORKA=SAFWK,                                            X
               MF=(E,RACRVRFY)
         LTR   R15,R15
         BNZ   EXCFENA
*
         LA    R8,EXCFEM1G             ACTIVE MESSAGE
EXCFENA  DS    0H
         LTR   R15,R15                 WAS ALL OK
         BZ    EXCFEAOK
         L     R2,8(,R11)              POINTER TO USER DATA AREA
         XC    0(4,R2),0(R2)           CLEAR POINTER
         LR    R1,R10
         DROP  R10
         STORAGE RELEASE,              RELEASE USER DATA AREA STORAGE  X
               LENGTH=USERLEN,                                         X
               ADDR=(1)
*
EXCFEAOK DS    0H
         L     R1,72(,R11)             EXIT SERVICES PARAMETER LIST
         L     R3,0(,R1)               EXIT SERVICES SUPPORTED FUNCTIONX
                                       S BITMAP
         TM    0(R3),B'01000000'       MESSAGE FUNCTION AVAILABLE
         BZ    EXCFENOM
         ST    R8,12(,R1)              MESSAGE PARAMETER LIST
         MVI   4(R1),X'01'
         L     R15,8(,R1)              ADDRESS OF ISTIECXS
         BASR  R14,R15                 CALL ISTIECXS
EXCFENOM DS    0H
*
         DROP  R13
         LR    R1,R13
         L     R13,4(,R13)             RELOAD PREVIOUS SAVEAREA ADDR
         STORAGE RELEASE,                                              X
               LENGTH=WORKLEN,                                         X
               ADDR=(1)
         RETURN (14,12),RC=0
*
EXCFEM1G DC    AL2(0,L'EXCFEM1T),AL4(EXCFEM1T)   MSG PARM LIST
EXCFEM1T DC    C'JJ$EXCFE SESSION SETUP AUTHORIZATION VERIFICATION ACTIX
               VE'
*
EXCFEM2G DC    AL2(0,L'EXCFEM2T),AL4(EXCFEM2T)   MSG PARM LIST
EXCFEM2T DC    C'JJ$EXCFE SESSION SETUP AUTHORIZATION VERIFICATION INACX
               TIVE'
*
         LTORG ,
*
         YREGS ,
*
         JJ$EXCDS ,
*
         END   ,
./ ADD NAME=JJ$EXCFF    01 00 98033 98033 144123 0116 0116 0000 IBMUSER
 TITLE 'JJ$EXCFF - END FUNCTION X''FF'''
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   CLEANUP RESOURCES USED BY SESSION AUTHORIZATION EXIT              *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   DELETE ACEE                                                       *
*   DELETE RACLISTED PROFILES                                         *
*   RELEASE STORAGE                                                   *
*                                                                     *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*   R1    ENTRY PARAMETER                                             *
*   R13   SAVEAREA POINTER                                            *
*   R14   RETURN ADDRESS                                              *
*   R15   ENTRY POINT ADDRESS / RETURN CODE                           *
*                                                                     *
*   R6    COMMON DATA AREA                                            *
*   R11   SAVED ENTRY PARAMETER                                       *
*   R13   SAVEAREA/WORKAREA POINTER                                   *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXCFF RSECT ,
JJ$EXCFF AMODE 31
JJ$EXCFF RMODE ANY
         SAVE  (14,12),,JJ$EXCFF-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXCFF,R12
*
         LR    R11,R1                  SAVE ENTRY PARAMETER
*
         L     R6,=A(JJ$EXCCM)
         USING JJ$EXCDS,R6
*
         L     R10,8(,R11)             ADDRESS USER DATA WORD
         L     R10,0(,R10)             USER DATA WORD
         LTR   R10,R10
         BZ    EXCFFINA
         USING USERAREA,R10            USER DATA AREA
*
         STORAGE OBTAIN,                                               X
               LENGTH=WORKLEN
         ST    R13,4(,R1)              CHAIN SAVEAREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORKAREA,R13
*
         MVC   RACRVRFY,MFLRVRFY       DELETE ACEE
         RACROUTE REQUEST=VERIFY,                                      X
               ACEE=ANONACEE,                                          X
               ENVIR=DELETE,                                           X
               RELEASE=2.1,                                            X
               SYSTEM=YES,                                             X
               WORKA=SAFWK,                                            X
               MF=(E,RACRVRFY)
*
         MVC   RACRLIST,MFLRLIST       DELETE RACLISTED PROFILES
         RACROUTE REQUEST=LIST,                                        X
               ENVIR=DELETE,                                           X
               RELEASE=2.1,                                            X
               WORKA=SAFWK,                                            X
               MF=(E,RACRLIST)
*
         L     R1,72(,R11)             EXIT SERVICES PARAMETER LIST
         L     R3,0(,R1)               EXIT SERVICES SUPPORTED FUNCTIONX
                                       S BITMAP
         TM    0(R3),B'01000000'       MESSAGE FUNCTION AVAILABLE
         BZ    EXCFFNOM
         MVC   12(4,R1),=A(EXCFFMSG)   MESSAGE PARAMETER LIST
         MVI   4(R1),X'01'
         L     R15,8(,R1)              ADDRESS OF ISTIECXS
         BASR  R14,R15                 CALL ISTIECXS
EXCFFNOM DS    0H
*
         L     R15,8(,R11)             POINTER TO USER DATA AREA
         XC    0(4,R15),0(R15)         CLEAR POINTER
         LR    R1,R10
         DROP  R10
         STORAGE RELEASE,              RELEASE USER DATA AREA STORAGE  X
               LENGTH=USERLEN,                                         X
               ADDR=(1)
*
         DROP  R13
         LR    R1,R13
         L     R13,4(,R13)             RELOAD PREVIOUS SAVEAREA ADDR
         STORAGE RELEASE,                                              X
               LENGTH=WORKLEN,                                         X
               ADDR=(1)
EXCFFINA DS    0H
         RETURN (14,12),RC=0
*
EXCFFMSG DC    AL2(0,L'EXCFFMST),AL4(EXCFFMST)   MSG PARM LIST
EXCFFMST DC    C'JJ$EXCFF SESSION SETUP AUTHORIZATION VERIFICATION DEACX
               TIVATED'
*
         LTORG ,
*
         JJ$EXCDS ,
*
         YREGS ,
*
         END   ,
./ ADD NAME=JJ$EXCRR    01 00 98033 98033 141213 0219 0219 0000 IBMUSER
 TITLE 'JJ$EXCRR - BUILD PROFILE AND ISSUE FASTAUTH'
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   BUILD PROFILE AND ISSUE FASTAUTH TO VERIFY AUTHORIZATION          *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   BUILD PROFILE PRIMNET.PLU.SECNET.SLU                              *
*   CALL FASTAUTH FOR AUTHORIZATION CHECKING                          *
*   IF ACCESS REFUSED AND THIS IS A CLSDST PASS THEN                  *
*     BUILD PROFILE PRIMNET.PLU.SECNET.SLU.ORIGNET.OLU                *
*     IF ACCESS GRANTED THEN OVERRIDE RETURNCODE TO 0                 *
*                                                                     *
*   IF A NAME CANNOT BE RESOLVED THEN A C'?' IS USED                  *
*                                                                     *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*   R1    ENTRY PARAMETER                                             *
*   R13   SAVEAREA POINTER                                            *
*   R14   RETURN ADDRESS                                              *
*   R15   ENTRY POINT ADDRESS / RETURN CODE                           *
*                                                                     *
*   R6    COMMON DATA AREA                                            *
*   R11   SAVED ENTRY PARAMETER                                       *
*   R13   SAVEAREA/WORKAREA POINTER                                   *
*                                                                     *
*                                                                     *
* RETURNCODES:                                                        *
*   R15   0  ACCESS GRANTED                                           *
*         4  UNDETERMINED                                             *
*         8  ACCESS REFUSED                                           *
*                                                                     *
*   R0    NUMBER OF QUALIFIERS THAT COULD NOT BE RESOLVED             *
*         (IE UNKNOWN NETID)                                          *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXCRR RSECT ,
JJ$EXCRR AMODE 31
JJ$EXCRR RMODE ANY
         SAVE  (14,12),,JJ$EXCRR-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXCRR,R12
*
         LR    R11,R1                  SAVE ENTRY PARAMETER
*
         USING WORKAREA,R13
*
         LA    R15,RACSAVE
         ST    R13,4(,R15)             CHAIN SAVEAREAS
         ST    R15,8(,R13)
         LR    R13,R15
         DROP  R13
         USING WORKARAC,R13
*
         L     R6,=A(JJ$EXCCM)         COMMON SECTION
         USING JJ$EXCDS,R6
*
         L     R10,8(,R11)             ADDRESS USER DATA WORD
         L     R10,0(,R10)             USER DATA WORD
         USING USERAREA,R10            USER DATA AREA
*
*
         XC    PROFRSVD,PROFRSVD
         MVI   PROFILE,C' '
         MVC   PROFILE+1(L'PROFILE-1),PROFILE
         LA    R3,PROFILE
         SLR   R5,R5                   UNKNOWN RESOURCE COUNT
*
         L     R2,12(,R11)             ADDR OF PLU RIC
         BAS   R14,BLDPROF
         MVI   0(R3),C'.'
         LA    R3,1(,R3)
         L     R2,16(,R11)             ADDR OF SLU RIC
         BAS   R14,BLDPROF
*
         L     R2,ANONACEE
         MVC   RACFAUTH,MFLFAUTH
         RACROUTE REQUEST=FASTAUTH,    REQUEST ACCESS                  X
               RELEASE=2.1,                                            X
               ENTITY=PROFILE,         PROFILE NAME                    X
               ATTR=READ,              ACCESS(READ)                    X
               ACEE=(2),               ACEE                            X
               WKAREA=SAFFWK,          FASTAUTH WORKAREA               X
               WORKA=SAFWK,            SAF WORKAREA ADDRESS            X
               MF=(E,RACFAUTH)
         LR    R8,R15                  SAVE RC FROM BASIC RACROUTE
*
         L     R2,56(,R11)             ADDRESS IF ILU RIC
         LTR   R2,R2
         BZ    EXCRRNOI
         MVI   0(R3),C'.'
         LA    R3,1(,R3)
         BAS   R14,BLDPROF
*
         LTR   R9,R15                  IF ACCESS OK
         BZ    EXCRRMSG                  THEN EXIT
*
         L     R2,ANONACEE
         MVC   RACFAUTH,MFLFAUTH
         RACROUTE REQUEST=FASTAUTH,    REQUEST ACCESS                  X
               RELEASE=2.1,                                            X
               ENTITY=PROFILE,         PROFILE NAME                    X
               ATTR=READ,              ACCESS(READ)                    X
               ACEE=(2),               ACEE                            X
               WKAREA=SAFFWK,          FASTAUTH WORKAREA               X
               WORKA=SAFWK,            SAF WORKAREA ADDRESS            X
               MF=(E,RACFAUTH)
EXCRRNOI DS    0H
         LTR   R9,R15                  SETUP AUTHORIZED
         BZ    EXCRRMSG                EXIT NOMSG
         MVC   PROFMSG,=CL19'JJ$EXCRR ALLOWING'
         CH    R9,=H'4'
         BNE   EXCRRREJ
         CH    R8,=H'8'                WAS BASIC RACROUTE REJECTED?
         BNE   EXCRRMSG                YES: THEN REJECT
EXCRRREJ DS    0H
         LA    R9,8                    FAIL SETUP
         MVC   PROFMSGR,=CL9'REJECTING'
*
EXCRRMSG DS    0H
         LA    R4,PROFILE              UPDATE MESSAGE LENGTH
         SLR   R3,R4
         AL    R3,=A(L'PROFMSG)
         STH   R3,PROFILEL
         LA    R4,PROFMSG
         ST    R4,PROFILEA
*
         DROP  R13
         LR    R15,R9                  RETURN CODE
         LR    R0,R5                   UNKNOWN RESOURCE COUNT
         L     R13,4(,R13)             RELOAD PREVIOUS SAVEAREA ADDR
         L     R14,12(,R13)
         LM    R1,R12,24(R13)
         BR    R14
*
* R2 POINTER TO RIC
* R3 POINTER TO PROFILE
* R4 WORK
* R5 UNKOWN RESOURCE COUNTER
* R14 RETURN ADDRESS
*
BLDPROF  DS    0H
         CLI   0(R2),X'19'             VECTOR KEY
         BE    BLDPROFG
         MVC   0(3,R3),=C'?.?'         UNKNOWN PROFILE
         LA    R5,2(,R5)
         LA    R3,3(,R3)
         BR    R14
BLDPROFG DS    0H
         SLR   R4,R4                   WORK REG, CLEAR FOR IC
         IC    R4,4(,R2)               LENGTH OF SSCPNAME
         LA    R2,5(R4,R2)             POINT TO NETWORK ID
*
         ICM   R4,B'0001',0(R2)        LENGTH OF NETWORK ID
         BNZ   BLDPNID                 NETWORK ID UNKNOWN
BLDPNIG  DS    0H
         MVI   0(R3),C'?'              INSERT UNKNOWN NAME
         LA    R5,1(,R5)
         B     BLDPUNID
BLDPNID  DS    0H
         CLI   1(R2),C' '              FIRST BYTE BLANK
         BE    BLDPNIG                 NO VALID NAME
         BCTR  R4,0                    SET MACHINE LENGTH
         EX    R4,BLDPROFC             COPY NETWORK ID
         LA    R3,0(R4,R3)             LAST BYTE OF NETWORK ID
BLDPNIDT DS    0H
         CLI   0(R3),C' '              LAST BYTE BLANK
         BNZ   BLDPUNID                NO THEN CONTINUE
         BCT   R3,BLDPNIDT             STRIP ONE BYTE
BLDPUNID DS    0H
         LA    R3,1(,R3)
         MVI   0(R3),C'.'              INSERT PERIOD
         LA    R3,1(,R3)               POINT PAST PERIOD
*
         LA    R2,2(R4,R2)             POINT TO LUNAME
*
         ICM   R4,B'0001',0(R2)        LENGTH OF LUNAME
         BNZ   BLDPLUN                 LUNAME UNKNOWN
BLDPLUG  DS    H
         MVI   0(R3),C'?'              INSERT UNKNOWN NAME
         LA    R5,1(,R5)
         B     BLDPULUN
BLDPLUN  DS    0H
         CLI   1(R2),C' '              FIRST BYTE BLANK
         BE    BLDPLUG                 NO VALID NAME
         BCTR  R4,0                    SET MACHINE LENGTH
         EX    R4,BLDPROFC             COPY LUNAME
         LA    R3,0(R4,R3)             LAST BYTE OF LUNAME
BLDPLUNT DS    0H
         CLI   0(R3),C' '              LAST BYTE BLANK
         BNZ   BLDPULUN                NO THEN CONTINUE
         BCT   R3,BLDPLUNT             STRIP ONE BYTE
BLDPULUN DS    0H
         LA    R3,1(,R3)
         BR    R14
*
BLDPROFC MVC   0(0,R3),1(R2)
*
         LTORG ,
*
         JJ$EXCDS ,
*
         YREGS ,
*
         END   ,
./ ADD NAME=JJ$EXC0A    01 00 98033 98033 144136 0137 0137 0000 IBMUSER
 TITLE 'JJ$EXC0A - EXIT REPLACED FUNCTION X''0A'''
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   REINITIALIZE SESSION AUTHORIZATION EXIT                           *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   CHECK IF F1REWALL CLASS IS ACTIVE                                 *
*   IF NOT ISSUE MESSAGE AND EXIT                                     *
*                                                                     *
*   OBTAIN STORAGE FOR USER DATA AREA                                 *
*   INITIALIZE ANONYMOUS ACEE (USERID='*')                            *
*   RACLIST F1REWALL CLASS                                            *
*                                                                     *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*   R1    ENTRY PARAMETER                                             *
*   R13   SAVEAREA POINTER                                            *
*   R14   RETURN ADDRESS                                              *
*   R15   ENTRY POINT ADDRESS / RETURN CODE                           *
*                                                                     *
*   R6    COMMON DATA AREA                                            *
*   R11   SAVED ENTRY PARAMETER                                       *
*   R13   SAVEAREA/WORKAREA POINTER                                   *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXC0A RSECT ,
JJ$EXC0A AMODE 31
JJ$EXC0A RMODE ANY
         SAVE  (14,12),,JJ$EXC0A-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXC0A,R12
*
         LR    R11,R1                  SAVE ENTRY PARAMETER
*
         STORAGE OBTAIN,                                               X
               LENGTH=WORKLEN
         ST    R13,4(,R1)              CHAIN SAVEAREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORKAREA,R13
*
         L     R6,=A(JJ$EXCCM)         MAKE COMMON SECTION ADDESSABLE
         USING JJ$EXCDS,R6
*
         STORAGE OBTAIN,               OBTAIN USER DATA AREA           X
               LENGTH=USERLEN
         L     R15,8(,R11)             ADDRESS OF USER DATA FIELD
         LR    R10,R1
         USING USERAREA,R10            USER DATA AREA
         XC    USERAREA(USERLEN),USERAREA
         ST    R10,0(,R15)             STORE ADDRESS
*
         MVC   RACRSTAT,MFLRSTAT       CHECK IF CLASS IF ACTIVE
         RACROUTE REQUEST=STAT,                                        X
               RELEASE=2.1,                                            X
               WORKA=SAFWK,                                            X
               MF=(E,RACRSTAT)
         LTR   R9,R15
         BNZ   EXC0ANA
*
         MVC   RACRLIST,MFLRLIST       RACLIST PROFILES
         RACROUTE REQUEST=LIST,                                        X
               ENVIR=CREATE,                                           X
               GLOBAL=YES,                                             X
               RELEASE=2.1,                                            X
               WORKA=SAFWK,                                            X
               MF=(E,RACRLIST)
         LTR   R9,R15
         BNZ   EXC0ANA
*
         MVC   RACRVRFY,MFLRVRFY       BUILD ACEE FOR CHECKING
         RACROUTE REQUEST=VERIFY,                                      X
               ACEE=ANONACEE,                                          X
               ENVIR=CREATE,                                           X
               LOC=ANY,                                                X
               RELEASE=2.1,                                            X
               SYSTEM=YES,                                             X
               WORKA=SAFWK,                                            X
               MF=(E,RACRVRFY)
         LTR   R9,R15
         BNZ   EXC0ANA
*
         L     R1,72(,R11)             EXIT SERVICES PARAMETER LIST
         L     R3,0(,R1)               EXIT SERVICES SUPPORTED FUNCTIONX
                                       S BITMAP
         TM    0(R3),B'01000000'       MESSAGE FUNCTION AVAILABLE
         BZ    EXC0ANA
         ST    R8,12(,R1)              MESSAGE PARAMETER LIST
         MVC   12(4,R1),=A(EXC0AMSG)
         MVI   4(R1),X'01'
         L     R15,8(,R1)              ADDRESS OF ISTIECXS
         BASR  R14,R15                 CALL ISTIECXS
EXC0ANA  DS    0H
*
         LTR   R9,R9                   WAS ALL OK
         BZ    EXC0AAOK
         L     R2,8(,R11)              POINTER TO USER DATA AREA
         XC    0(4,R2),0(R2)           CLEAR POINTER
         LR    R1,R10
         DROP  R10
         STORAGE RELEASE,              RELEASE USER DATA AREA STORAGE  X
               LENGTH=USERLEN,                                         X
               ADDR=(1)
EXC0AAOK DS    0H
         DROP  R13
         LR    R1,R13
         L     R13,4(,R13)             RELOAD PREVIOUS SAVEAREA ADDR
         STORAGE RELEASE,                                              X
               LENGTH=WORKLEN,                                         X
               ADDR=(1)
         RETURN (14,12),RC=0
*
EXC0AMSG DC    AL2(0,L'EXC0AMST),AL4(EXC0AMST)   MSG PARM LIST
EXC0AMST DC    C'JJ$EXC0A SESSION SETUP AUTHORIZATION VERIFICATION NOW X
               ACTIVE'
*
         LTORG ,
*
         YREGS ,
*
         JJ$EXCDS ,
*
         END   ,
./ ADD NAME=JJ$EXC00    01 00 98033 98033 143451 0112 0112 0000 IBMUSER
 TITLE 'JJ$EXC00 - INITIAL AUTHORIZATION FUNCTION X''00'''
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   INITIAL AUTHORIZATION CHECKING                                    *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   OBTAIN WORKING STORAGE                                            *
*   CALL JJ$EXCRR TO VERIFY ACCESS                                    *
*   IF NOT ALL QUALIFIERS KNOWN THEN PASS TO SECONDARY AUTHORISATION  *
*   IF NO PROFILE THEN ISSUE WARNING MESSAGE                          *
*   IF ACCESS REFUSED ISSUE ERROR MESSAGE                             *
*                                                                     *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*   R1    ENTRY PARAMETER                                             *
*   R13   SAVEAREA POINTER                                            *
*   R14   RETURN ADDRESS                                              *
*   R15   ENTRY POINT ADDRESS / RETURN CODE                           *
*                                                                     *
*   R6    COMMON DATA AREA                                            *
*   R9    RETURN CODE TO BE PASSED TO VTAM                            *
*   R10   USER DATA AREA                                              *
*   R11   SAVED ENTRY PARAMETER                                       *
*   R13   SAVEAREA/WORKAREA POINTER                                   *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXC00 RSECT ,
JJ$EXC00 AMODE 31
JJ$EXC00 RMODE ANY
         SAVE  (14,12),,JJ$EXC00-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXC00,R12
*
         LR    R11,R1                  SAVE ENTRY PARAMETER
*
         L     R6,=A(JJ$EXCCM)
         USING JJ$EXCDS,R6
*
         SLR   R9,R9                   INITIAL RETURN CODE
         L     R10,8(,R11)             ADDRESS USER DATA WORD
         L     R10,0(,R10)             USER DATA WORD
         LTR   R10,R10
         BZ    EXC00INA                EXIT INACTIVE
         USING USERAREA,R10            USER DATA AREA
*
         STORAGE OBTAIN,                                               X
               LENGTH=WORKLEN
         ST    R13,4(,R1)              CHAIN SAVEAREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORKAREA,R13
*
         LR    R1,R11
         CALL  JJ$EXCRR                CHECK AUTHORIZATION
         LR    R9,R15                  SAVE RC IN R9
*
         LTR   R0,R0                   FULL PROFILE KNOWN
         BE    EXC00NSC
         LA    R9,4                    IF NOT THEN PASS TO SECONDARY
         B     EXC00NOM                  AUTH AND DO NOT ISSUE MESSAGE
EXC00NSC DS    0H
         LTR   R15,R15                 WAS ACCESS OK?
         BZ    EXC00NOM                EXIT IF OK
         CH    R15,=H'4'               IF RACROUTE RC>4 THEN FAIL SETUP
         BH    EXC00MSG                  ELSE ALLOW SETUP
         SLR   R9,R9
*
EXC00MSG DS    0H
         MVC   PROFMSGI,=C'00'         EXC00 ISSUED MESSAGE
         LA    R2,PROFILEP
         L     R1,72(,R11)             EXIT SERVICES PARAMETER LIST
         L     R3,0(,R1)               EXIT SERVICES SUPPORTED FUNCTIONX
                                       S BITMAP
         TM    0(R3),B'01000000'       MESSAGE FUNCTION AVAILABLE
         BZ    EXC00NOM
         MVI   4(R1),X'01'
         ST    R2,12(,R1)              MESSAGE PARAMETER LIST
         L     R15,8(,R1)              ADDRESS OF ISTIECXS
         BASR  R14,R15                 CALL ISTIECXS
EXC00NOM DS    0H
*
         DROP  R13
         LR    R1,R13
         L     R13,4(,R13)             RELOAD PREVIOUS SAVEAREA ADDR
         STORAGE RELEASE,                                              X
               LENGTH=WORKLEN,                                         X
               ADDR=(1)
EXC00INA DS    0H
         LR    R15,R9
         RETURN (14,12),RC=(15)
*
         LTORG ,
*
         JJ$EXCDS ,
*
         YREGS ,
*
         END   ,
./ ADD NAME=JJ$EXC01    01 00 98033 98033 143538 0105 0105 0000 IBMUSER
 TITLE 'SECONDARY AUTHORIZATION FUNCTION X''01'''
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   SECONDARY AUTHORIZATION CHECKING                                  *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   OBTAIN WORKING STORAGE                                            *
*   CALL JJ$EXCRR TO VERIFY ACCESS                                    *
*   IF NO PROFILE THEN ISSUE WARNING MESSAGE                          *
*   IF ACCESS REFUSED ISSUE ERROR MESSAGE                             *
*                                                                     *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*   R1    ENTRY PARAMETER                                             *
*   R13   SAVEAREA POINTER                                            *
*   R14   RETURN ADDRESS                                              *
*   R15   ENTRY POINT ADDRESS / RETURN CODE                           *
*                                                                     *
*   R6    COMMON DATA AREA                                            *
*   R9    RETURN CODE TO BE PASSED TO VTAM                            *
*   R10   USER DATA AREA                                              *
*   R11   SAVED ENTRY PARAMETER                                       *
*   R13   SAVEAREA/WORKAREA POINTER                                   *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXC01 RSECT ,
JJ$EXC01 AMODE 31
JJ$EXC01 RMODE ANY
         SAVE  (14,12),,JJ$EXC01-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXC01,R12
*
         LR    R11,R1                  SAVE ENTRY PARAMETER
*
         L     R6,=A(JJ$EXCCM)
         USING JJ$EXCDS,R6
*
         SLR   R9,R9                   INITIAL RETURN CODE
         L     R10,8(,R11)             ADDRESS USER DATA WORD
         L     R10,0(,R10)             USER DATA WORD
         LTR   R10,R10
         BZ    EXC01INA                EXIT INACTIVE
         USING USERAREA,R10            USER DATA AREA
*
         STORAGE OBTAIN,                                               X
               LENGTH=WORKLEN
         ST    R13,4(,R1)              CHAIN SAVEAREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORKAREA,R13
*
         LR    R1,R11
         CALL  JJ$EXCRR                AUTHORIZATION CHECK
         LTR   R9,R15
         BZ    EXC01NOM
*
         MVC   PROFMSGI,=C'01'         EXC01 ISSUED MESSAGE
         LA    R2,PROFILEP
         L     R1,72(,R11)             EXIT SERVICES PARAMETER LIST
         L     R3,0(,R1)               EXIT SERVICES SUPPORTED FUNCTIONX
                                       S BITMAP
         TM    0(R3),B'01000000'       MESSAGE FUNCTION AVAILABLE
         BZ    EXC01NOM
         MVI   4(R1),X'01'
         ST    R2,12(,R1)              MESSAGE PARAMETER LIST
         L     R15,8(,R1)              ADDRESS OF ISTIECXS
         BASR  R14,R15                 CALL ISTIECXS
EXC01NOM DS    0H
*
         CH    R9,=H'4'                RC4 IS OK
         BH    EXC01FAS
         SLR   R9,R9
EXC01FAS DS    0H
*
         DROP  R13
         LR    R1,R13
         L     R13,4(,R13)             RELOAD PREVIOUS SAVEAREA ADDR
         STORAGE RELEASE,                                              X
               LENGTH=WORKLEN,                                         X
               ADDR=(1)
EXC01INA DS    0H
         LR    R15,R9
         RETURN (14,12),RC=(15)
*
         LTORG ,
*
         JJ$EXCDS ,
*
         YREGS ,
*
         END   ,
./ ADD NAME=JJ$EXC09    01 00 98033 98033 143708 0116 0116 0000 IBMUSER
 TITLE 'JJ$EXC09 - EXIT REPLACEMENT FUNCTION X''09'''
***********************************************************************
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*   CLEANUP RESOURES FOR EXIT REPLACEMENT                             *
*                                                                     *
*                                                                     *
* LOGIC:                                                              *
*                                                                     *
*   DELETE ACEE                                                       *
*   DELETE RACLISTED PROFILES                                         *
*   RELEASE STORAGE                                                   *
*                                                                     *
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
*   R1    ENTRY PARAMETER                                             *
*   R13   SAVEAREA POINTER                                            *
*   R14   RETURN ADDRESS                                              *
*   R15   ENTRY POINT ADDRESS / RETURN CODE                           *
*                                                                     *
*   R6    COMMON DATA AREA                                            *
*   R11   SAVED ENTRY PARAMETER                                       *
*   R13   SAVEAREA/WORKAREA POINTER                                   *
*                                                                     *
* ATTRIBUTES:                                                         *
*   AMODE(31),RMODE(ANY),REFR                                         *
*                                                                     *
*                                                                     *
* HISTORY:                                                            *
*                                                                     *
*   DATE       BY              COMMENTS                               *
*   02/02/98   JAN JAEGER      INITIAL VERSION                        *
*                                                                     *
***********************************************************************
JJ$EXC09 RSECT ,
JJ$EXC09 AMODE 31
JJ$EXC09 RMODE ANY
         SAVE  (14,12),,JJ$EXC09-J.JAEGER-&SYSDATE.-&SYSTIME.
         LR    R12,R15                 SET BASE REGISTER
         USING JJ$EXC09,R12
*
         LR    R11,R1                  SAVE ENTRY PARAMETER
*
         L     R6,=A(JJ$EXCCM)
         USING JJ$EXCDS,R6
*
         L     R10,8(,R11)             ADDRESS USER DATA WORD
         L     R10,0(,R10)             USER DATA WORD
         LTR   R10,R10
         BZ    EXC09INA
         USING USERAREA,R10            USER DATA AREA
*
         STORAGE OBTAIN,                                               X
               LENGTH=WORKLEN
         ST    R13,4(,R1)              CHAIN SAVEAREAS
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORKAREA,R13
*
         MVC   RACRVRFY,MFLRVRFY       DELETE ACEE
         RACROUTE REQUEST=VERIFY,                                      X
               ACEE=ANONACEE,                                          X
               ENVIR=DELETE,                                           X
               RELEASE=2.1,                                            X
               SYSTEM=YES,                                             X
               WORKA=SAFWK,                                            X
               MF=(E,RACRVRFY)
*
         MVC   RACRLIST,MFLRLIST       DELETE RACLISTED PROFILES
         RACROUTE REQUEST=LIST,                                        X
               ENVIR=DELETE,                                           X
               RELEASE=2.1,                                            X
               WORKA=SAFWK,                                            X
               MF=(E,RACRLIST)
*
         L     R1,72(,R11)             EXIT SERVICES PARAMETER LIST
         L     R3,0(,R1)               EXIT SERVICES SUPPORTED FUNCTIONX
                                       S BITMAP
         TM    0(R3),B'01000000'       MESSAGE FUNCTION AVAILABLE
         BZ    EXC09NOM
         MVC   12(4,R1),=A(EXC09MSG)   MESSAGE PARAMETER LIST
         MVI   4(R1),X'01'
         L     R15,8(,R1)              ADDRESS OF ISTIECXS
         BASR  R14,R15                 CALL ISTIECXS
EXC09NOM DS    0H
*
         L     R15,8(,R11)             POINTER TO USER DATA AREA
         XC    0(4,R15),0(R15)         CLEAR POINTER
         LR    R1,R10
         DROP  R10
         STORAGE RELEASE,                                              X
               LENGTH=USERLEN,                                         X
               ADDR=(1)
*
         DROP  R13
         LR    R1,R13
         L     R13,4(,R13)             RELOAD PREVIOUS SAVEAREA ADDR
         STORAGE RELEASE,                                              X
               LENGTH=WORKLEN,                                         X
               ADDR=(1)
EXC09INA DS    0H
         RETURN (14,12),RC=0
*
EXC09MSG DC    AL2(0,L'EXC09MST),AL4(EXC09MST)   MSG PARM LIST
EXC09MST DC    C'JJ$EXC09 SESSION SETUP AUTHORIZATION VERIFICATION NOW X
               INACTIVE'
*
         LTORG ,
*
         JJ$EXCDS ,
*
         YREGS ,
*
         END   ,
./ ADD NAME=JJ$RFR      01 00 98033 98033 145025 0017 0017 0000 IBMUSER
//IBMUSERR JOB ACCT,'JAN JAEGER',NOTIFY=IBMUSER,CLASS=A,MSGCLASS=A
//ASMA90   EXEC PGM=ASMA90,PARM='DECK,NOOBJ,XREF(SHORT)'
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=IBMUSER.ISTEXCAA.SOURCE,DISP=SHR
//SYSUT1   DD  UNIT=SYSALLDA,SPACE=(CYL,10)
//SYSIN    DD  DSN=IBMUSER.ISTEXCAA.SOURCE(JJ$RFR01),DISP=SHR
//SYSPUNCH DD  DSN=&&PUNCH,DISP=(MOD,PASS),
//          UNIT=SYSALLDA,SPACE=(CYL,10)
//IEWL     EXEC PGM=IEWL,PARM='RENT,REFR,XREF'
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=SYSALLDA,SPACE=(CYL,10)
//SYSLMOD  DD  DSN=SYS1.LINKLIB,DISP=SHR
//SYSLIN   DD  DSN=&&PUNCH,DISP=(OLD,DELETE)
//         DD  *
 NAME  ICHRFR01(R)
./ ADD NAME=JJ$RFR01    01 00 98033 98033 145032 0007 0007 0000 IBMUSER
ICHRFR01 RSECT ,
ICHRFR01 RMODE 24
*
         ICHRFRTB CLASS=F1REWALL,ACTION=RACF
         ICHRFRTB TYPE=END
*
         END   ,
./ ADD NAME=JJ$RRCDE    01 00 98033 98033 145039 0010 0010 0000 IBMUSER
         ICHERCDE CLASS=F1REWALL,              CLASS NAME              X
               FIRST=ANY,                      FIRST CHAR              X
               OTHER=ANY,                      OTHER CHARS             X
               MAXLNTH=54,                     MAXIMUM LENGTH          X
               ID=255,                         ID NUMBER               X
               POSIT=255,                      POSIT VALUE             X
               GENLIST=ALLOWED,                ALLOW GENLIST           X
               RACLIST=ALLOWED,                CLASS MAY BE RACLISTED  X
               RACLREQ=YES                     MUST ISSUE RACLIST
         ICHERCDE ,
