INVOKE0  CSECT ,
INVOKE0  AMODE 31
INVOKE0  RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING INVOKE0,R12
*
         LR    R11,R1                  ESTABLISH ADDRESSABILITY
         USING CPPL,R11                  TO THE CPPL
*
         STORAGE OBTAIN,               OBTAIN WORKAREA                 X
               LENGTH=WORKALEN,                                        X
               LOC=BELOW
         LR    R10,R1
         USING WORKAREA,R10
         USING PPL,WORKPPL
         USING IOPL,WORKIOPL
         LA    R13,SAVEAREA
         MVC   4(4,R13),=C'F1SA'
         STM   R10,R13,PGMREGS
*
         MVC   PPLUPT,CPPLUPT          CALL PARSE ROUTINE
         MVC   PPLECT,CPPLECT
         XC    CPECB,CPECB
         LA    R1,PGMREGS
         ST    R1,PPLUWA
         LA    R1,CPECB
         ST    R1,PPLECB
         MVC   PPLPCL,=A(INVOKE1)
         LA    R1,PDL
         ST    R1,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
*
         MVI   SWITCHES,0
*
         TM    PDS2FTB1,PDSAPFLG       APF SECTION?
         BZ    NOAPF
         LA    R1,PDSBCEND             END OF BASIC SECTION
         TM    PDS2ATR1,PDS2SCTR
         BZ    NOSCTR
         LA    R1,PDSS01LN(,R1)
NOSCTR   DS    0H
         TM    PDS2INDC,PDS2ALIS
         BZ    NOALIAS
         LA    R1,PDSS02LN(,R1)
NOALIAS  DS    0H
         TM    PDS2FTB1,PDS2SSI
         BZ    NOSSI
         AL    R1,=F'1'
         N     R1,=F'-2'
         LA    R1,PDSS03LN(,R1)
NOSSI    DS    0H
         CLC   =X'0101',0(R1)          IS THIS AN AC=1 MODULE
         BNE   NOAPF
         OI    SWITCHES,PGMTSF         DEFAULT TO TSF FOR APF MODULE
NOAPF    DS    0H
*
         L     R9,PDL                  PARAMETER DESCRIPTOR LIST
         USING IKJPARMD,R9
*
         LA    R1,PGMPARML             BUILD PROGRAM PARAMETER
         ST    R1,PGMPARMA
         OI    PGMPARMA,X'80'
         XC    PGMPARML,PGMPARML
         TM    PRMPCE+6,X'80'
         BZ    NOPARM
         L     R1,PRMPCE
         LH    R2,PRMPCE+4
         STH   R2,PGMPARML
         BCTR  R2,0
         EX    R2,MVCPARM              COPY PARM
NOPARM   DS    0H
*
         LH    R2,TSFPCE               CHECK FOR TSF/NOTSF
         LTR   R2,R2
         BZ    NOTSFPRM
         NI    SWITCHES,255-PGMTSF
         CH    R2,=H'1'
         BNE   NOTSFPRM
         OI    SWITCHES,PGMTSF
NOTSFPRM DS    0H
*
         CLC   DMPPCE,=H'1'            CHECK FOR DUMP/NODUMP
         BNE   NODUMP
         OI    SWITCHES,PGMDUMP
NODUMP   DS    0H
*
         CLC   CAPPCE,=H'1'            CHECK FOR CAPS/ASIS
         BNE   NODUMP
         OI    SWITCHES,PGMCAPS
         OC    PGMPARMC,=CL256' '      FORCE PARM UPPERCASE
NOCAPS   DS    0H
*
         IKJRLSA PDL                   RELEASE PDL
         DROP  R9
*
         TM    SWITCHES,PGMTSF
         BO    DOTSF
*
* LINK TO PROGRAM
*
         MVC   LLINK,MFLLINK
         LINK  DE=PDS2,                                                X
               MF=(E,PGMPARMA),                                        X
               SF=(E,LLINK)
         LR    R2,R15
         B     EXIT
*
* CALL PROGRAM THROUGH TSO SERVICE FACILITY
*
DOTSF    DS    0H
         MVC   SFFLAGS,=AL1(0,0,0,2)
         TM    SWITCHES,PGMDUMP
         BZ    NOSFDMP
         MVI   SFFLAGS+2,1
NOSFDMP  DS    0H
         L     R15,CVTPTR
         L     R15,CVTTVT-CVT(,R15)
         L     R15,TSVTASF-TSVT(,R15)  TSO SERVICE FACILITY
         CALL  (15),                                                   X
               (SFFLAGS,                                               X
               PDS2NAME,                                               X
               PGMLEN,                                                 X
               SFRETC,                                                 X
               SFREAS,                                                 X
               SFABND,                                                 X
               PGMPARMA),                                              X
               VL,                                                     X
               MF=(E,LCALL7)
         LTR   R2,R15
         BZ    EXIT
         CH    R2,=H'12'
         BH    PARMERR
         B     *(R2)
         B     TSFRC4
         B     TSFRC8
TSFRC12  DS    0H                      SIMULATE ABEND
         L     R1,SFABND
         L     R2,SFREAS
         ABEND (1),REASON=(2)
TSFRC4   DS    0H                      SIMULATE RETURN CODE
         L     R2,SFRETC
         B     EXIT
TSFRC8   DS    0H                      ATTENTION INTERRUPT
         LA    R2,12
         LA    R5,MSGATTN
         B     DOMSG
PARMERR  DS    0H                      TSF PARAMETER ERROR
         LA    R2,16
         LA    R5,MSGPRME
DOMSG    DS    0H
         XC    CPECB,CPECB
         MVC   WORKPUTL,MFLPTL
         L     R3,CPPLUPT
         L     R4,CPPLECT
         PUTLINE PARM=WORKPUTL,                                        X
               UPT=(3),                                                X
               ECT=(4),                                                X
               ECB=CPECB,                                              X
               OUTPUT=((5),TERM,SINGLE,DATA),                          X
               MF=(E,IOPL)
*
EXIT     DS    0H                      RETURN TO CALLER
         LR    R1,R13
         STORAGE RELEASE,                                              X
               LENGTH=WORKALEN,                                        X
               ADDR=(1)
         LR    R15,R2
         PR    ,
*
MVCPARM  MVC   PGMPARMC(0),0(R1)
*
DOBLDL   DS    0H                      ISSUE BLDL FOR PROGRAM NAME
         BAKR  R14,0
         LM    R7,R9,0(R1)
         LM    R10,R13,0(R8)
         LR    R6,R1
         LA    R13,72(,R13)
         MVC   4(4,R13),=C'F1SA'
*
         MVC   BLDLPARM,=AL2(1,BLDLLL)
         MVC   PDS2NAME,=CL8' '
         TM    6(R7),X'80'
         BZ    RC4
         L     R1,0(,R7)
         LH    R2,4(,R7)
         ST    R2,PGMLEN
         BCTR  R2,0
         EX    R2,MVCPGMNM
         BLDL  0,BLDLPARM
         LTR   R15,R15
         BNZ   RC4
*
         PR    ,
*
RC4      DS    0H
         LA    R15,4
         MVC   8(4,R6),=A(MSGBLDL)
         PR    ,
*
MVCPGMNM MVC   PDS2NAME(0),0(R1)
*
MSGBLDL  DC    AL2(L'MSGBLDLT+MSGBLDLT-*,0)
MSGBLDLT DC    C'BLDL FAILED FOR SPECIFIED PROGRAM NAME'
*
MSGATTN  DC    AL2(L'MSGATTNT+MSGATTNT-*,0)
MSGATTNT DC    C'PROGRAM TERMINATED BY ATTENTION INTERRUPT'
*
MSGPRME  DC    AL2(L'MSGPRMET+MSGPRMET-*,0)
MSGPRMET DC    C'TSO SERVICE FACILITY PARAMETER ERROR'
*
MFLPTL   PUTLINE MF=L
L_MFLPTL EQU   *-MFLPTL
*
MFLLINK  LINK  SF=L
L_LINK   EQU   *-MFLLINK
*
INVOKE1  IKJPARM ,
PGMPCE   IKJIDENT 'PGMNAME',                                           X
               UPPERCASE,                                              X
               MAXLNTH=8,                                              X
               FIRST=ALPHA,                                            X
               OTHER=ALPHANUM,                                         X
               VALIDCK=DOBLDL,                                         X
               PROMPT='ENTER PROGRAM NAME',                            X
               HELP=('ENTER NAME OF PROGRAM TO BE INVOKED')
PRMPCE   IKJPOSIT QSTRING,                                             X
               ASIS,                                                   X
               HELP=('ENTER PROGAM PARAMETER IN QUOTES')
TSFPCE   IKJKEYWD
         IKJNAME 'TSF',ALIAS=('NOTASK')
         IKJNAME 'NOTSF',ALIAS=('TASK','LINK')
DMPPCE   IKJKEYWD DEFAULT='NODUMP'
         IKJNAME 'DUMP'
         IKJNAME 'NODUMP'
CAPPCE   IKJKEYWD DEFAULT='ASIS'
         IKJNAME 'ASIS',ALIAS=('NOCAPS')
         IKJNAME 'CAPS'
         IKJENDP ,
*
         LTORG ,
*
WORKAREA DSECT ,
SAVEAREA DS    (2*18)F
PGMREGS  DS    4F
CPECB    DS    1F
WORKPPL  DS    XL(L_PPL)
WORKIOPL DS    XL(L_IOPL)
WORKPUTL DS    XL(L_MFLPTL)
PDL      DS    1F
BLDLPARM DS    1F
         IHAPDS DSECT=NO
BLDLLL   EQU   *-PDS2
PGMLEN   DS    F
SWITCHES DS    1X
SFFLAGS  DS    F
SFRETC   DS    F
SFREAS   DS    F
SFABND   DS    F
LCALL7   DS    7F
PGMTSF   EQU   B'10000000'             TSF/NOTSF
PGMDUMP  EQU   B'01000000'             DUMP/NODUMP
PGMCAPS  EQU   B'00100000'             CAPS/NOCAPS
LLINK    DS    XL(L_LINK)
PGMPARMA DS    F
PGMPARML DS    H
PGMPARMC DS    CL256
WORKALEN EQU   *-WORKAREA
*
         YREGS ,
*
         IKJTSVT ,
         IKJPPL ,
L_PPL    EQU   *-PPL
         IKJCPPL ,
L_CPPL   EQU   *-CPPL
         IKJIOPL ,
L_IOPL   EQU   *-IOPL
*
         CVT   DSECT=YES
*
         END   ,
