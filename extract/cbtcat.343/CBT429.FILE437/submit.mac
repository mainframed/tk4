         OPSYNBRI ,
SUBMIT0  CSECT ,
SUBMIT0  AMODE 24
SUBMIT0  RMODE 24
         BAKR  R14,0
         LR    R12,R15
         USING SUBMIT0,R12
*
         LR    R11,R1                  ESTABLISH ADDRESSABILITY
         USING CPPL,R11                  TO THE CPPL
*
         STORAGE OBTAIN,               OBTAIN WORKAREA                 X
               LENGTH=WORKALEN
         LR    R10,R1
         USING WORKAREA,R10
         USING PPL,WORKPPL
         USING IOPL,WORKIOPL
         USING IFGACB,WORKACB
         USING IFGRPL,WORKRPL
         USING S99RBX,RBX
         USING IHADCB,WORKDCB
*
         LA    R13,SAVEAREA
         MVC   4(4,R1),=C'F1SA'
         STM   R10,R13,PARMADDR
*
         LA    R1,RB                   ALLOCATE INTERNAL READER
         ST    R1,RBP
         OI    RBP,X'80'
         MVC   RB,DYNRB
         MVC   RBX,DYNRBX
         MVC   TU,DYNTU
         ST    R11,S99ECPPL
         LA    R1,TUPL
         ST    R1,A_TUPL
         LA    R1,RBX
         ST    R1,A_RBX
         LA    R1,TUSYSOUT
         ST    R1,A_SYSOUT
         LA    R1,TUINTRDR
         ST    R1,A_INTRDR
         LA    R1,TULRECL
         ST    R1,A_LRECL
         LA    R1,TURECFM
         ST    R1,A_RECFM
         LA    R1,TUCLOSE
         ST    R1,A_CLOSE
         LA    R1,TURETDDN
         ST    R1,A_RETDDN
         OI    A_LAST,X'80'
         LA    R1,RBP
         DYNALLOC
         LTR   R2,R15
         BNZ   EXIT
*
         MVC   WORKACB,RDRACB          OPEN INTERNAL READER
         MVC   WORKRPL,RDRRPL
         MVC   ACBDDNM,TUDDNAME
         LA    R1,WORKACB
         ST    R1,RPLDACB
         LA    R1,CARD
         ST    R1,RPLAREA
         MVC   LOPEN,MFLOPEN
         OPEN  (WORKACB,(OUTPUT)),MF=(E,LOPEN)
         LTR   R2,R15
         BNZ   EXIT
*
         MVC   PPLUPT,CPPLUPT          CALL PARSE ROUTINE
         MVC   PPLECT,CPPLECT
         LA    R1,PARMADDR
         ST    R1,PPLUWA
         XC    CPECB,CPECB
         LA    R1,CPECB
         ST    R1,PPLECB
         MVC   PPLPCL,=A(SUBMIT1)
         LA    R1,PDL
         ST    R1,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
*
         L     R2,RC
*
         IKJRLSA PDL                   RELEASE PDL
*
         MVC   LCLOSE,MFLCLOSE         CLOSE INTERNAL READER
         CLOSE (WORKACB),MF=(E,LCLOSE)
*
EXIT     DS    0H                      RETURN TO CALLER
         LR    R1,R13
         STORAGE RELEASE,                                              X
               LENGTH=WORKALEN,                                        X
               ADDR=(1)
         LR    R15,R2
         PR    ,
*
PROC_DSN DS    0H
         BAKR  R14,0
*
         LM    R7,R9,0(R1)             VALIDCK PARAMETERS
         LM    R10,R13,0(R8)
         LA    R13,72(,R13)
         MVC   4(4,R13),=C'F1SA'
*
         LA    R1,RB                   ALLOCATE JCL DATA SET
         ST    R1,RBP
         OI    RBP,X'80'
         MVC   RB,DYNRB
         MVC   RBX,DYNRBX
         MVC   TU2,DYNTU2
         ST    R11,S99ECPPL
         LA    R1,TUPL2
         ST    R1,A_TUPL
         LA    R1,RBX
         ST    R1,A_RBX
         LA    R1,TU2DSN
         ST    R1,A_2DSN
*
         TM    6(R7),X'80'             IS DSNAME PRESENT
         BZ    PROC_RC4
         L     R1,0(,R7)               POINTER TO DSNAME
         LH    R2,4(,R7)               LENGTH OF DSNAME
         STH   R2,TU2DSLEN
         BCTR  R2,0
         EX    R2,MVCDSNAM
*
         LA    R1,TU2DISP
         ST    R1,A_2DISP
         LA    R1,TU2CLOSE
         ST    R1,A_2CLOSE
         LA    R1,TU2RETDD
         ST    R1,A_2RETDD
         LA    R1,TU2RTORG
         ST    R1,A_2RTORG
         OI    A_2LAST,X'80'
         LA    R1,RBP
         DYNALLOC
         LTR   R15,R15
         BNZ   PROC_RC8
*
         MVC   WORKDCB,JCLDCB          OPEN JCL DATA SET
         MVC   DCBDDNAM,TU2DDNAM
         LA    R1,EXLST
         STCM  R1,B'0111',DCBEXLSA
         LA    R1,INFMJFCB
         ST    R1,EXLST
         MVI   EXLST,X'87'
         MVC   LRDJFCB,MFLRDJFC
         RDJFCB (WORKDCB),MF=(E,LRDJFCB)
         LTR   R15,R15
         BNZ   PROC_RC8
*
         CLC   TU2RTORG,=H'0'
         BNZ   HASDSORG
         TM    14(R7),X'80'
         BO    NODSORG
         BZ    PROC_SEQ
HASDSORG DS    0H
         TM    TU2RTORG,X'40'          PS(U)
         BO    PROC_SEQ
         TM    TU2RTORG,X'02'          PO(U)
         BZ    PROC_RC4
         LA    R1,=C'TEMPNAME'
         LA    R2,8
NODSORG  DS    0H
         TM    14(R7),X'80'            IS MEMBER NAME PRESENT
         BZ    TEMPNAME
         L     R1,8(,R7)               POINTER TO MEMBERNAME
         LH    R2,12(,R7)              LENGTH OF MEMBERNAME
TEMPNAME DS    0H
         BCTR  R2,0
         EX    R2,MVCMEMNM
         OI    JFCBIND1,JFCPDS
PROC_SEQ DS    0H
*
         MVC   LOPEN,MFLOPEN
         OPEN  (WORKDCB,(INPUT)),                                      X
               TYPE=J,                                                 X
               MF=(E,LOPEN)
         LTR   R15,R15
         BNZ   PROC_RC8
*
GETCARD  DS    0H                      READ JCL CARD LOOP
         GET   WORKDCB
*
         MVC   CARD,0(R1)
*
         PUT   RPL=WORKRPL             PUNCH JCL TO INTERNAL READER
         B     GETCARD
*
JCLEOD   DS    0H
         ENDREQ RPL=WORKRPL            INFORM READER ABOUT EOF
         MVC   LCLOSE,MFLCLOSE         CLOSE JCL DATA SET
         CLOSE (WORKDCB),MF=(E,LCLOSE)
         FREEPOOL WORKDCB
*
         TM    RPLRBAR,X'BF'
         BZ    NOMSG
         MVC   WORKMSG,MSGSUBM         ISSUE SUBMITTED MESSAGE
         MVC   MSGJOBID,RPLRBAR
         XC    CPECB,CPECB
         MVC   WORKPUTL,MFLPTL
         L     R2,CPPLUPT
         L     R3,CPPLECT
         LA    R4,WORKMSG
         PUTLINE PARM=WORKPUTL,                                        X
               UPT=(2),                                                X
               ECT=(3),                                                X
               ECB=CPECB,                                              X
               OUTPUT=((4),TERM,SINGLE,DATA),                          X
               MF=(E,IOPL)
NOMSG    DS    0H
*
         SLR   R15,R15
         ST    R15,RC
         PR    ,
*
PROC_RC4 DS    0H
         LA    R15,4
         ST    R15,RC
         PR    ,
*
PROC_RC8 DS    0H
         LA    R15,8
         ST    R15,RC
         PR    ,
*
MVCDSNAM MVC   TU2DSNAM(0),0(R1)
MVCMEMNM MVC   JFCBELNM(0),0(R1)
*
MSGSUBM  DC    AL2(L'MSGSUBMT+MSGSUBMT-*,0)
MSGSUBMT DC    C'JOBXXXXX SUBMITTED'
MSGJOBID EQU   WORKMSG+2*L'MSGSUBM,8,C'C'
*
SUBMIT1  IKJPARM ,
PCE      IKJPOSIT DSNAME,                                              X
               LIST,                                                   X
               USID,                                                   X
               VALIDCK=PROC_DSN,                                       X
               PROMPT='ENTER DATA SET NAME',                           X
               HELP=('ENTER NAME OF DATA SET CONTAINING JCL')
         IKJENDP ,
*
MFLPTL   PUTLINE MF=L
L_MFLPTL EQU   *-MFLPTL
*
RDRACB   ACB   MACRF=(ADR,OUT)
L_ACB    EQU   *-RDRACB
*
RDRRPL   RPL   OPTCD=ADR,                                              X
               RECLEN=80,                                              X
               AREALEN=L'CARD
L_RPL    EQU   *-RDRRPL
*
         DS    0H
DYNRB    DC    AL1(L_DYNRB,S99VRBAL,0,0)
         DS    H
         DS    H
A_TUPL   EQU   RB+*-DYNRB
         DS    A
A_RBX    EQU   RB+*-DYNRB
         DS    A
         DC    AL1(0,0,0,0)
L_DYNRB  EQU   *-DYNRB
*
         DS    0H
DYNRBX   DC    CL6'S99RBX'
         DC    AL1(S99RBXVR,S99EIMSG,0,0,S99XWARN,0)
         DC    A(0)
         DS    F
         DS    F
         DS    F
         DS    F
         DS    F
L_DYNRBX EQU   *-DYNRBX
*
DYNTU    DS    0H
SSYSOUT  DC    AL2(DALSYSOU,1,L'MSGCLASS)
MSGCLASS DC    C'H'
SINTRDR  DC    AL2(DALSPGNM,1,L'SINTRDRC)
SINTRDRC DC    C'INTRDR'
SLRECL   DC    AL2(DALLRECL,1,L'SLRECLA)
SLRECLA  DC    AL2(80)
SRECFM   DC    AL2(DALRECFM,1,L'SRECFMX)
SRECFMX  DC    X'80'
SCLOSE   DC    AL2(DALCLOSE,0)
SRETDDN  DC    AL2(DALRTDDN,1,L'SRETDDNC)
SRETDDNC DS    CL8
L_DYNTU  EQU   *-DYNTU
TUSYSOUT EQU   TU+SSYSOUT-DYNTU
TUINTRDR EQU   TU+SINTRDR-DYNTU
TULRECL  EQU   TU+SLRECL-DYNTU
TURECFM  EQU   TU+SRECFM-DYNTU
TUCLOSE  EQU   TU+SCLOSE-DYNTU
TURETDDN EQU   TU+SRETDDN-DYNTU
TUDDNAME EQU   TU+SRETDDNC-DYNTU,8,C'C'
*
DYNTU2   DS    0H
SDSN     DC    AL2(DALDSNAM,1,*-*)
SDSNC    DS    CL44
SDISP    DC    AL2(DALSTATS,1,L'SDISPC)
SDISPC   DC    X'08'   SHR
SCLOSE2  DC    AL2(DALCLOSE,0)
SRETDD2  DC    AL2(DALRTDDN,1,L'SRETDD2C)
SRETDD2C DS    CL8
SRTORG2  DC    AL2(DALRTORG,1,L'SRTORG2C)
SRTORG2C DS    XL2
L_DYNTU2 EQU   *-DYNTU2
TU2DSN   EQU   TU2+SDSN-DYNTU2
TU2DISP  EQU   TU2+SDISP-DYNTU2
TU2CLOSE EQU   TU2+SCLOSE2-DYNTU2
TU2RETDD EQU   TU2+SRETDD2-DYNTU2
TU2DSNAM EQU   TU2+SDSNC-DYNTU2,L'SDSNC,C'C'
TU2DSLEN EQU   TU2+SDSNC-DYNTU2-2,2,C'H'
TU2DDNAM EQU   TU2+SRETDD2C-DYNTU2,L'SRETDD2C,C'C'
TU2RTORG EQU   TU2+SRTORG2C-DYNTU2,L'SRTORG2C,C'C'
*
MFLRDJFC RDJFCB (,),MF=L
L_RDJFCB EQU   *-MFLRDJFC
MFLOPEN  OPEN  (,),MF=L
L_OPEN   EQU   *-MFLOPEN
MFLCLOSE CLOSE (,),MF=L
L_CLOSE  EQU   *-MFLCLOSE
*
JCLDCB   DCB   DDNAME=JCLDCB,                                          X
               MACRF=(GL),                                             X
               DSORG=PS,                                               X
               EODAD=JCLEOD
L_JCLDCB EQU   *-JCLDCB
*
         LTORG ,
*
WORKAREA DSECT ,
SAVEAREA DS    (2*18)F
PARMADDR DS    4F
WORKPPL  DS    XL(L_PPL)
WORKIOPL DS    XL(L_IOPL)
WORKPUTL DS    XL(L_MFLPTL)
WORKMSG  DS    XL(L'MSGSUBM*2+L'MSGSUBMT)
WORKACB  DS    XL(L_ACB)
WORKRPL  DS    XL(L_RPL)
WORKDCB  DS    XL(L_JCLDCB)
EXLST    DS    F
WORKJFCB IEFJFCBN ,
CARD     DS    CL80
PDL      DS    1F
CPECB    DS    1F
RC       DS    1F
*
RBP      DS    A
RB       DS    XL(L_DYNRB)
         DS    0H
RBX      DS    XL(L_DYNRBX)
TUPL     DS    0F
A_SYSOUT DS    A
A_INTRDR DS    A
A_CLOSE  DS    A
A_LRECL  DS    A
A_RECFM  DS    A
A_RETDDN DS    A
A_LAST   EQU   A_RETDDN
TUPL2    DS    0F
A_2DSN   DS    A
A_2DISP  DS    A
A_2CLOSE DS    A
A_2RETDD DS    A
A_2RTORG DS    A
A_2LAST  EQU   A_2RTORG
TU       DS    XL(L_DYNTU)
         DS    0H
TU2      DS    XL(L_DYNTU2)
         DS    0F
LRDJFCB  DS    XL(L_RDJFCB)
         DS    0F
LOPEN    DS    XL(L_OPEN)
         DS    0F
LCLOSE   DS    XL(L_CLOSE)
WORKALEN EQU   *-WORKAREA
*
         YREGS ,
*
         IKJTSVT ,
         IKJPPL ,
L_PPL    EQU   *-PPL
         IKJCPPL ,
L_CPPL   EQU   *-CPPL
         IKJIOPL ,
L_IOPL   EQU   *-IOPL
*
         IFGACB ,
         IFGRPL ,
*
         IEFZB4D0 ,
         IEFZB4D2 ,
*
         DCBD  DSORG=DA
*
         CVT   DSECT=YES
*
         END   ,
