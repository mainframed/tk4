UPDSTATS CSECT ,
UPDSTATS RMODE 24
UPDSTATS AMODE 24
         BAKR  R14,0
         BASR  R12,0
         LA    R0,*-UPDSTATS
         SLR   R12,R0
         USING UPDSTATS,R12
         LA    R13,SAVEAREA
         MVC   4(4,R13),=C'F1SA'
*
         OPEN  (SYSIN,(INPUT),SYSUT2,(UPDAT))
*
GETSYSIN DS    0H
         GET   SYSIN
         CLC   ADDCARD,0(R1)
         BNE   GETSYSIN
         TM    64(R1),X'BF'
         BZ    GETSYSIN
         MVC   MEMBER(8),12(R1)
         LR    R3,R1
         BLDL  SYSUT2,ENTRY
         LTR   R15,R15
         BNZ   GETSYSIN
         TM    MEMBER+13,B'11111'
         BNZ   GETSYSIN
         MVI   MEMBER+11,15
         XC    DWORD,DWORD
         PACK  DWORD+6(2),24(2,R3)     VV
         CVB   R2,DWORD
         STC   R2,MEMBER+12
         PACK  DWORD+6(2),27(2,R3)     MM
         CVB   R2,DWORD
         STC   R2,MEMBER+13
         MVI   MEMBER+14,0
         PACK  MEMBER+16(4),30(5,R3)   CREATED (DATE)
         PACK  MEMBER+20(4),36(5,R3)   CHANGED (DATE)
         PACK  DWORD(4),42(6,R3)       CHANGED (TIME)
         MVO   DWORD+4(4),DWORD(3)
         MVC   MEMBER+24(2),DWORD+5
         MVO   DWORD+4(3),DWORD+2(2)
         MVC   MEMBER+15(1),DWORD+5
         XC    DWORD,DWORD
         PACK  DWORD+5(3),49(4,R3)     SIZE
         CVB   R2,DWORD
         STH   R2,MEMBER+26
         PACK  DWORD+5(3),54(4,R3)     INIT
         CVB   R2,DWORD
         STH   R2,MEMBER+28
         PACK  DWORD+5(3),59(4,R3)     MOD
         CVB   R2,DWORD
         STH   R2,MEMBER+30
         MVC   MEMBER+32(7),64(R3)     ID
         MVC   MEMBER+39(3),=CL3' '
         STOW  SYSUT2,MEMBER,R
         B     GETSYSIN
*
EODAD    DS    0H
         CLOSE (SYSIN,,SYSUT2)
*
         PR    ,
*
         LTORG ,
*
ADDCARD  DC    C'./ ADD NAME='
*
SYSUT2   DCB   DDNAME=SYSUT2,                                          X
               DEVD=DA,                                                X
               DSORG=PO,                                               X
               MACRF=(R,W)
*
SYSIN    DCB   DDNAME=SYSIN,                                           X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               MACRF=GL,                                               X
               EODAD=EODAD
*
ENTRY    DS    0F
FF       DC    AL2(1)
LL       DC    AL2(L'MEMBER)
MEMBER   DS    XL(76)
*
SAVEAREA DS    18F
DWORD    DS    1D
*
         YREGS ,
*
         END   ,
