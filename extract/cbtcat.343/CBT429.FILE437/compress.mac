COMPRES0 CSECT ,
COMPRES0 AMODE ANY
COMPRES0 RMODE ANY
         BAKR  R14,0
         LR    R12,R15
         USING COMPRES0,R12
*
         LR    R11,R1                  ESTABLISH ADDRESSABILITY
         USING CPPL,R11                  TO THE CPPL
*
         STORAGE OBTAIN,               OBTAIN WORKAREA                 X
               LENGTH=WORKALEN
         LR    R10,R1
         USING WORKAREA,R10
         USING PPL,WORKPPL
         USING IOPL,WORKIOPL
         USING S99RBX,RBX
*
         LA    R13,SAVEAREA
         MVC   4(4,R1),=C'F1SA'
         STM   R10,R13,PARMADDR
*
         MVC   PPLUPT,CPPLUPT          CALL PARSE ROUTINE
         MVC   PPLECT,CPPLECT
         LA    R1,PARMADDR
         ST    R1,PPLUWA
         XC    CPECB,CPECB
         LA    R1,CPECB
         ST    R1,PPLECB
         MVC   PPLPCL,=A(COMPRES1)
         LA    R1,PDL
         ST    R1,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
*
         L     R2,RC
         LTR   R2,R2
         BNZ   EXIT
*
         LA    R1,RB                   ALLOCATE SYSPRINT
         ST    R1,RBP
         OI    RBP,X'80'
         MVC   RB,DYNRB
         MVC   RBX,DYNRBX
         MVC   TU,DYNTU
         ST    R11,S99ECPPL
         LA    R1,TUPL
         ST    R1,A_TUPL
         LA    R1,RBX
         ST    R1,A_RBX
         LA    R1,TUTERM
         ST    R1,A_TERM
*
         L     R1,PDL                  PARAMETER DESCRIPTOR LIST
         CLC   =H'1',SPRPCE-IKJPARMD(R1)  SYSPRINT SPECIFIED?
         BNE   NOSYSPR
         MVC   TUTERM(2),=AL2(DALDUMMY)
NOSYSPR  DS    0H
*
         LA    R1,TUCLOSE
         ST    R1,A_CLOSE
         LA    R1,TURETDDN
         ST    R1,A_RETDDN
         OI    A_LAST,X'80'
         LA    R1,RBP
         DYNALLOC ,
         LTR   R2,R15
         BNZ   EXIT
*
         MVC   SRPGMPRM,PGMPRM
         LA    R1,A_PGMP2
         ST    R1,APGMP2
         OI    APGMP2,X'80'
         MVC   SYSPRINT,TUDDNAME
         MVC   SYSUT2,TU2DDNAM
         L     R15,CVTPTR
         L     R15,CVTTVT-CVT(,R15)
         L     R15,TSVTASF-TSVT(,R15)  TSO SERVICE FACILITY
         CALL  (15),                                                   X
               (SRFLAGS,                                               X
               SRPGM,                                                  X
               SRPGMLEN,                                               X
               SRRETC,                                                 X
               SRREAS,                                                 X
               SRABND,                                                 X
               SRPGMPRM),                                              X
               VL,                                                     X
               MF=(E,LCALL7)
*
         LA    R1,UNRB                 UNALLOCATE SYSUT2
         ST    R1,UNRBP                (FREE=CLOSE DOES NOT WORK FOR
         OI    UNRBP,X'80'              IEBCOPY SYSUTX D/SETS)
         MVC   UNRB,DUNRB
         MVC   RBX,DYNRBX
         MVC   TU3,DYNTU3
         ST    R11,S99ECPPL
         LA    R1,TUPL3
         ST    R1,A_UTUPL
         LA    R1,RBX
         ST    R1,A_URBX
         LA    R1,TU3UNDDN
         ST    R1,A_3DUN
         OI    A_3LAST,X'80'
         MVC   TU3DDNAM,TU2DDNAM
         LA    R1,UNRBP
         DYNALLOC ,
*
         MVC   WORKMSG,MSGCOMP         ISSUE COMPRESSED MESSAGE
         L     R2,SRRETC
         LTR   R2,R2
         BZ    NOMSG
         CVD   R2,DWORD
         UNPK  MSGCOMPR,DWORD+5(3)
         OI    MSGCOMPR+3,C'0'
         XC    CPECB,CPECB
         MVC   WORKPUTL,MFLPTL
         L     R2,CPPLUPT
         L     R3,CPPLECT
         LA    R4,WORKMSG
         PUTLINE PARM=WORKPUTL,                                        X
               UPT=(2),                                                X
               ECT=(3),                                                X
               ECB=CPECB,                                              X
               OUTPUT=((4),TERM,SINGLE,DATA),                          X
               MF=(E,IOPL)
         L     R2,SRRETC               RETURN CODE
NOMSG    DS    0H
*
         IKJRLSA PDL                   RELEASE PDL
*
EXIT     DS    0H                      RETURN TO CALLER
         LR    R1,R13
         STORAGE RELEASE,                                              X
               LENGTH=WORKALEN,                                        X
               ADDR=(1)
         LR    R15,R2
         PR    ,
*
PROC_DSN DS    0H
         BAKR  R14,0
*
         LM    R7,R9,0(R1)             VALIDCK PARAMETERS
         LM    R10,R13,0(R8)
         LA    R13,72(,R13)
         MVC   4(4,R13),=C'F1SA'
*
         LA    R1,RB                   ALLOCATE PDS
         ST    R1,RBP
         OI    RBP,X'80'
         MVC   RB,DYNRB
         MVC   RBX,DYNRBX
         MVC   TU2,DYNTU2
         ST    R11,S99ECPPL
         LA    R1,TUPL2
         ST    R1,A_TUPL
         LA    R1,RBX
         ST    R1,A_RBX
         LA    R1,TU2DSN
         ST    R1,A_2DSN
*
         TM    6(R7),X'80'             IS DSNAME PRESENT
         BZ    PROC_RC4
         L     R1,0(,R7)               POINTER TO DSNAME
         LH    R2,4(,R7)               LENGTH OF DSNAME
         STH   R2,TU2DSLEN
         BCTR  R2,0
         EX    R2,MVCDSNAM
*
         LA    R1,TU2DISP
         ST    R1,A_2DISP
         LA    R1,TU2RETDD
         ST    R1,A_2RETDD
         LA    R1,TU2RTORG
         ST    R1,A_2RTORG
         OI    A_2LAST,X'80'
         LA    R1,RBP
         DYNALLOC ,
         LTR   R15,R15
         BNZ   PROC_RC8
*
         CLC   TU2RTORG,=H'0'
         BNZ   HASDSORG
         TM    14(R7),X'80'
         BO    NODSORG
         BZ    PROC_PDS
HASDSORG DS    0H
         TM    TU2RTORG,X'40'          PS(U)
         BO    PROC_RC4
         TM    TU2RTORG,X'02'          PO(U)
         BZ    PROC_RC4
NODSORG  DS    0H
         TM    14(R7),X'80'            IS MEMBER NAME PRESENT
         BO    PROC_RC4
PROC_PDS DS    0H
*
         SLR   R15,R15
         ST    R15,RC
         PR    ,
*
PROC_RC4 DS    0H
         LA    R15,4
         ST    R15,RC
         PR    ,
*
PROC_RC8 DS    0H
         LA    R15,8
         ST    R15,RC
         PR    ,
*
MVCDSNAM MVC   TU2DSNAM(0),0(R1)
*
MSGCOMP  DC    AL2(L'MSGCOMPT+MSGCOMPT-*,0)
MSGCOMPT DC    C'IEBCOPY RETURNCODE XXXX'
MSGCOMPR EQU   WORKMSG+2*L'MSGCOMP+19,4,C'C'
*
COMPRES1 IKJPARM ,
PCE      IKJPOSIT DSNAME,                                              X
               LIST,                                                   X
               USID,                                                   X
               VALIDCK=PROC_DSN,                                       X
               PROMPT='ENTER DATA SET NAME',                           X
               HELP=('ENTER NAME OF DATA SET TO BE COMPRESSED')
SPRPCE   IKJKEYWD DEFAULT='NOSYSPRINT'
         IKJNAME 'NOSYSPRINT',ALIAS=('NOSYSPR')
         IKJNAME 'SYSPRINT',ALIAS=('SYSPR')
         IKJENDP ,
*
MFLPTL   PUTLINE MF=L
L_MFLPTL EQU   *-MFLPTL
*
         DS    0H
DYNRB    DC    AL1(L_DYNRB,S99VRBAL,0,0)
         DS    H
         DS    H
A_TUPL   EQU   RB+*-DYNRB
         DS    A
A_RBX    EQU   RB+*-DYNRB
         DS    A
         DC    AL1(0,0,0,0)
L_DYNRB  EQU   *-DYNRB
*
         DS    0H
DUNRB    DC    AL1(L_DUNRB,S99VRBUN,0,0)
         DS    H
         DS    H
A_UTUPL  EQU   UNRB+*-DUNRB
         DS    A
A_URBX   EQU   UNRB+*-DUNRB
         DS    A
         DC    AL1(0,0,0,0)
L_DUNRB  EQU   *-DUNRB
*
         DS    0H
DYNRBX   DC    CL6'S99RBX'
         DC    AL1(S99RBXVR,S99EIMSG,0,0,S99XWARN,0)
         DC    A(0)
         DS    F
         DS    F
         DS    F
         DS    F
         DS    F
L_DYNRBX EQU   *-DYNRBX
*
DYNTU    DS    0H
STERM    DC    AL2(DALTERM,0)
SCLOSE   DC    AL2(DALCLOSE,0)
SRETDDN  DC    AL2(DALRTDDN,1,L'SRETDDNC)
SRETDDNC DS    CL8
L_DYNTU  EQU   *-DYNTU
TUTERM   EQU   TU+STERM-DYNTU
TUCLOSE  EQU   TU+SCLOSE-DYNTU
TURETDDN EQU   TU+SRETDDN-DYNTU
TUDDNAME EQU   TU+SRETDDNC-DYNTU,L'SRETDDNC,C'C'
*
DYNTU2   DS    0H
SDSN     DC    AL2(DALDSNAM,1,*-*)
SDSNC    DS    CL44
SDISP    DC    AL2(DALSTATS,1,L'SDISPC)
SDISPC   DC    X'08'   SHR
SRETDD2  DC    AL2(DALRTDDN,1,L'SRETDD2C)
SRETDD2C DS    CL8
SRTORG2  DC    AL2(DALRTORG,1,L'SRTORG2C)
SRTORG2C DS    XL2
L_DYNTU2 EQU   *-DYNTU2
TU2DSN   EQU   TU2+SDSN-DYNTU2
TU2DISP  EQU   TU2+SDISP-DYNTU2
TU2RETDD EQU   TU2+SRETDD2-DYNTU2
TU2DSNAM EQU   TU2+SDSNC-DYNTU2,L'SDSNC,C'C'
TU2DSLEN EQU   TU2+SDSNC-DYNTU2-2,2,C'H'
TU2DDNAM EQU   TU2+SRETDD2C-DYNTU2,L'SRETDD2C,C'C'
TU2RTORG EQU   TU2+SRTORG2C-DYNTU2,L'SRTORG2C,C'C'
*
DYNTU3   DS    0H
SUNDDN   DC    AL2(DUNDDNAM,1,L'SUNDDNC)
SUNDDNC  DS    CL8
L_DYNTU3 EQU   *-DYNTU3
TU3UNDDN EQU   TU3+SUNDDN-DYNTU3
TU3DDNAM EQU   TU3+SUNDDNC-DYNTU3,L'SUNDDNC,C'C'
*
PGMPRM   DS    0F
         DC    A(PGMP1)
APGMP2   EQU   *-PGMPRM+SRPGMPRM,4,C'A'
         DS    A(PGMP2+X'80000000')
PGMP2    DC    AL2(L_P2)
P2       DC    XL(11*8)'00'
SYSPRINT EQU   SRPGMPRM+(P2-PGMPRM)+(5*8),8,C'C'
SYSUT2   EQU   SRPGMPRM+(P2-PGMPRM)+(8*8),8,C'C'
L_P2     EQU   *-P2
L_PGMPRM EQU   *-PGMPRM
A_PGMP2  EQU   SRPGMPRM+(PGMP2-PGMPRM)
*
         DS    0H
PGMP1    DC    AL2(L'P1)
P1       DC    C'COMPRESS'
*
         DS    0F
SRFLAGS  DC    AL1(0,0,0,2)            INVOKE A PROGRAM
SRPGM    DC    C'IEBCOPY'
SRPGMLEN DC    A(L'SRPGM)
*
         LTORG ,
*
WORKAREA DSECT ,
SAVEAREA DS    (2*18)F
DWORD    DS    1D
PARMADDR DS    4F
WORKPPL  DS    XL(L_PPL)
WORKIOPL DS    XL(L_IOPL)
WORKPUTL DS    XL(L_MFLPTL)
WORKMSG  DS    XL(L'MSGCOMP*2+L'MSGCOMPT)
PDL      DS    1F
CPECB    DS    1F
RC       DS    1F
SRRETC   DS    1F
SRREAS   DS    1F
SRABND   DS    1F
LCALL7   DS    7F
*
RBP      DS    A
RB       DS    XL(L_DYNRB)
UNRBP    DS    A
UNRB     DS    XL(L_DUNRB)
         DS    0H
RBX      DS    XL(L_DYNRBX)
TUPL     DS    0F
A_CLOSE  DS    A
A_TERM   DS    A
A_RETDDN DS    A
A_LAST   EQU   A_RETDDN
TUPL2    DS    0F
A_2DSN   DS    A
A_2DISP  DS    A
A_2RETDD DS    A
A_2RTORG DS    A
A_2LAST  EQU   A_2RTORG
TUPL3    DS    0F
A_3DUN   DS    A
A_3LAST  EQU   A_3DUN
TU       DS    XL(L_DYNTU)
         DS    0F
TU2      DS    XL(L_DYNTU2)
         DS    0F
TU3      DS    XL(L_DYNTU3)
         DS    0F
SRPGMPRM DS    XL(L_PGMPRM)
WORKALEN EQU   *-WORKAREA
*
         YREGS ,
*
         IKJTSVT ,
         IKJPPL ,
L_PPL    EQU   *-PPL
         IKJCPPL ,
L_CPPL   EQU   *-CPPL
         IKJIOPL ,
L_IOPL   EQU   *-IOPL
*
         IEFZB4D0 ,
         IEFZB4D2 ,
*
         CVT   DSECT=YES
*
         END   ,
