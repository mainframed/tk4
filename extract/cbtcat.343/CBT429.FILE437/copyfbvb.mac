COPYFBVB CSECT ,
COPYFBVB RMODE 24
COPYFBVB AMODE 31
         BAKR  R14,0
         BASR  R12,0
         LA    R0,*-COPYFBVB
         SLR   R12,R0
         USING COPYFBVB,R12
         LA    R13,SAVEAREA
         MVC   4(4,R13),=C'F1SA'
*
JFCB1    USING JFCB,SYSUT1J
JFCB2    USING JFCB,SYSUT2J
DCB1     USING IHADCB,SYSUT1
DCB2     USING IHADCB,SYSUT2
*
         OPEN  (SYSUT1D,(INPUT),SYSUT2D,(UPDAT),SYSPRINT,(OUTPUT)),    X
               MODE=31
         LTR   R10,R15
         BNZ   RETURN
*
         SLR   R10,R10                 RETURN CODE
*
         RDJFCB (SYSUT1,,SYSUT2)
         OI    JFCB1.JFCBIND1,JFCPDS
         OI    JFCB2.JFCBIND1,JFCPDS
*
         MVI   MSG1UT,C'1'
         TM    JFCB1.JFCRECFM,JFCVAR
         BO    UT1VAR
         MVC   MSG1REC,=CL8'FIXED'
UT1VAR   DS    0H
         MVC   MSG1VOL,JFCB1.JFCBVOLS
         MVC   MSG1DSN,JFCB1.JFCBDSNM
         PUT   SYSPRINT,MSG1
         MVI   MSG1ASA,C' '
         MVC   MSG1REC,=CL8'VARIABLE'
         TM    JFCB2.JFCRECFM,JFCVAR
         BO    UT2VAR
         MVC   MSG1REC,=CL8'FIXED'
UT2VAR   DS    0H
         MVI   MSG1UT,C'2'
         MVC   MSG1VOL,JFCB2.JFCBVOLS
         MVC   MSG1DSN,JFCB2.JFCBDSNM
         PUT   SYSPRINT,MSG1
*
GETDREC  DS    0H
         GET   SYSUT1D
         LH    R2,0(,R1)               LENGTH OF DIRECTORY BLOCK
         BCTR  R2,0
         LA    R3,2(,R1)
         LA    R5,0(R2,R1)
MEMLOOP  DS    0H
         CLC   =X'FFFFFFFFFFFFFFFF',0(R3)      END-OF-DIRECTORY
         BE    EODIR
*
         MVC   JFCB1.JFCBELNM,0(R3)    COPY MEMBER NAME
         MVC   JFCB2.JFCBELNM,0(R3)
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT)),TYPE=J
         LTR   R10,R15
         BNZ   ADVDIR
GSYSUT1  DS    0H
         GET   SYSUT1
         LR    R2,R1                   INPUT RECORD ADDRESS IN R2
         TM    DCB1.DCBRECFM,DCBRECF   INPUT RECFM=FB
         BO    UT1RECF
*
* VB INPUT DATASET
*
         LH    R6,0(,R2)               RECORD LENGTH
         SH    R6,=H'4'                SUBTRACT RDW LEN
         BNP   UT1VEOL                 RECORD LENGHT IS 0
UT1VFEOL DS    0H
         LA    R7,3(R6,R2)             POINT TO LAST BYTE IN RECORD
         CLI   0(R7),X'40'             IS BYTE A BLANK?
         BNE   UT1VEOL
         BCT   R6,UT1VFEOL
UT1VEOL  DS    0H                      R6 CONTAINS DATA LENGTH
         LA    R7,4(,R2)               POINTER TO NUMBER FIELD
         SH    R6,=H'8'
         BP    UT1NUM
         SLR   R7,R7                   NO NUMBER FIELD
         SLR   R6,R6                   NO DATA FIELD
UT1NUM   DS    0H
         LA    R8,12(,R2)              POINTER TO DATA RECORD
         B     R6LEN
*
* FB INPUT DATASET
*
UT1RECF  DS    0H
         LR    R1,R2
         BCTR  R1,0
         LH    R6,DCB1.DCBLRECL
         SH    R6,=H'8'                LENGTH OF NUMBER FIELD
UT1FFEOL DS    0H
         LA    R7,0(R6,R1)             ADDRESS LAST BYTE IN RECORD
         CLI   0(R7),X'40'
         BNE   UT1FEOL
         BCT   R6,UT1FFEOL
UT1FEOL  DS    0H
         SLR   R7,R7                   ASSUME NO NUMBERS
         LH    R9,DCB1.DCBLRECL
         SH    R9,=H'8'
         LA    R9,0(R9,R2)
         CLC   =CL8' ',0(R9)           ARE THERE SEQUENCE NUMBERS
         BE    UT1NONUM
         LR    R7,R9                   POINTER TO NUMBER FIELD
UT1NONUM DS    0H
         LR    R8,R2                   POINTER TO DATA FIELD
*
* R6 CONTAINS LENGTH OF DATA PORTION RECORD
* R7 CONTAINS POINTER TO NUMBER FIELD OR ZERO
* R8 CONTAINS POINTER TO DATA FIELD
*
R6LEN    DS    0H
         LH    R9,DCB2.DCBLRECL  ***
         TM    DCB2.DCBRECFM,DCBRECV
         BZ    UT2NOTV
         LA    R0,4+8(,R6)
         STH   R0,DCB2.DCBLRECL
UT2NOTV  DS    0H
         PUT   SYSUT2
         STH   R9,DCB2.DCBLRECL  ***
         TM    DCB2.DCBRECFM,DCBRECF
         BO    UT2F
*
* VB OUTPUT DATASET
*
         MVC   4(8,R1),=CL8' '         INIT NUMBER FIELD
         LTR   R7,R7
         BZ    UT2NONUM
         MVC   4(8,R1),0(R7)           COPY NUMBER FIELD
UT2NONUM DS    0H
         LA    R0,4+8(,R6)             RDW LEN
         STH   R0,0(,R1)               RECORD LENGTH
         STCM  R0,B'1100',2(R1)        ZEROS
         LTR   R6,R6
         BZ    GSYSUT1
         BCTR  R6,0
         EX    R6,COPYVREC
         B     GSYSUT1
COPYVREC MVC   12(0,R1),0(R8)
*
* FB OUTPUT DATASET
*
UT2F     DS    0H
         LH    R9,DCB2.DCBLRECL
         SH    R9,=H'8'
         LA    R9,0(R9,R1)
         MVC   0(8,R9),=CL8' '
         LTR   R7,R7
         BZ    UT2NOSEQ
         MVC   0(8,R9),0(R7)
UT2NOSEQ DS    0H
         MVI   0(R1),C' '
         LH    R9,DCB2.DCBLRECL
         SH    R9,=AL2(8+1+1)
         EX    R9,CLRFREC
         LTR   R6,R6
         BZ    GSYSUT1
         BCTR  R6,0
         EX    R6,COPYFREC
         B     GSYSUT1
COPYFREC MVC   0(0,R1),0(R8)
CLRFREC  MVC   1(0,R1),0(R1)
*
SYNAD    DS    0H
         SYNADAF ACSMETH=QSAM
         MVC   MSG3SYNA,36+8(R1)
         CLOSE (SYSUT1,,SYSUT2),MODE=31
         STOW  SYSUT2D,JFCB2.JFCBELNM,D
         MVC   MSG3MN,0(3)
         PUT   SYSPRINT,MSG3
         LA    R10,8
         B     ADVDIR
*
EODAD    DS    0H
         CLOSE (SYSUT1,,SYSUT2),MODE=31
         MVC   MSG2MN,0(3)
         PUT   SYSPRINT,MSG2
*
ADVDIR   DS    0H
         IC    R4,11(,R3)              USERDATA LENGTH IN HW
         N     R4,=XL4'1F'
         SLL   R4,1
         LA    R4,12(,R4)              ENTRY LENGTH
         BXLE  R3,R4,MEMLOOP
         B     GETDREC
EODIR    DS    0H
*
EXIT     DS    0H
         PUT   SYSPRINT,MSG4
         CLOSE (SYSUT1D,,SYSUT2D,,SYSPRINT),MODE=31
*
RETURN   DS    0H
         LR    R15,R10
         PR    ,
*
         LTORG ,
*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               RECFM=VBA,                                              X
               LRECL=133,                                              X
               MACRF=PM
*
SYSUT1D  DCB   DDNAME=SYSUT1,                                          X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               MACRF=GL,                                               X
               LRECL=256,                                              X
               RECFM=F,                                                X
               BLKSIZE=256,                                            X
               DCBE=SYSUT1DE
SYSUT1DE DCBE  RMODE31=BUFF
*
SYSUT1   DCB   DDNAME=SYSUT1,                                          X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               MACRF=GL,                                               X
               EXLST=SYSUT1X,                                          X
               DCBE=SYSUT1E
SYSUT1X  DC    AL1(X'87'),AL3(SYSUT1J)
SYSUT1J  DS    0F,XL(JFCBLGTH)
SYSUT1E  DCBE  RMODE31=BUFF,                                           X
               EODAD=EODAD,                                            X
               SYNAD=SYNAD
*
SYSUT2   DCB   DDNAME=SYSUT2,                                          X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               MACRF=PL,                                               X
               EXLST=SYSUT2X,                                          X
               DCBE=SYSUT2E
SYSUT2X  DC    AL1(X'87'),AL3(SYSUT2J)
SYSUT2J  DS    0F,XL(JFCBLGTH)
SYSUT2E  DCBE  RMODE31=BUFF
*
SYSUT2D  DCB   DDNAME=SYSUT2,                                          X
               DEVD=DA,                                                X
               DSORG=PO,                                               X
               MACRF=(R,W)
*
MSG1     DC    AL2(4+L'MSG1TEXT,0)
MSG1TEXT DC    C'1SYSUTX RECFM=VARIABLE VOL=123456 DSN=12345678.1234567X
               8.12345678.12345678.12345678'
MSG1ASA  EQU   MSG1TEXT,1,C'C'
MSG1UT   EQU   MSG1TEXT+6,1,C'C'
MSG1REC  EQU   MSG1TEXT+14,8,C'C'
MSG1VOL  EQU   MSG1TEXT+27,6,C'C'
MSG1DSN  EQU   MSG1TEXT+38,44,C'C'
*
MSG2     DC    AL2(4+L'MSG2TEXT,0)
MSG2TEXT DC    C' MEMBER 12345678 COPIED'
MSG2MN   EQU   MSG2TEXT+8,8,C'C'
*
MSG3     DC    AL2(4+L'MSG3TEXT,0)
MSG3TEXT DC    CL120' I/O ERROR ON MEMBER 12345678'
MSG3MN   EQU   MSG3TEXT+21,8,C'C'
MSG3SYNA EQU   MSG3TEXT+36,120-36,C'C'
*
MSG4     DC    AL2(4+L'MSG4TEXT,0)
MSG4TEXT DC    C' COPY COMPLETE'
*
SAVEAREA DS    18F
*
         YREGS ,
         DCBD  DSORG=PO,DEVD=DA
JFCB     DSECT ,
         IEFJFCBN ,
*
         END   COPYFBVB,(JAN JAEGER,0101,93120)
