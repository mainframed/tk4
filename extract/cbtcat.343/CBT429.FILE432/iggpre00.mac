***********************************************************************
*                                                                     *
*             DADSM PRE-PROCESSING EXIT (IGGPRE00) :                  *
*                                                                     *
* - CET EXIT EST APPELE EN MODE SUPERVISEUR, CLE DE PROTECTION 0      *
* - CET EXIT EST APPELE APRES CONTROLE DE VALIDITE DES PARAMETRES ET  *
* AVANT MISE A JOUR DE LA VTOC POUR LES FONCTIONS : ALLOCATE, EXTEND, *
* SCRATCH, PARTIAL RELEASE, RENAME. UN CODE RETOUR 0 INDIQUE A L'APPE-*
* LANT QUE LA FONCTION DADSM EST AUTORISEE ET PEUT COMMENCER.         *
* - SEULES LES FONCTIONS ALLOCATE ET EXTEND SONT TRAITEES PAR L'EXIT. *
* - L'ACCES EST PERMIS SI USER A L'ACCES UPDATE SUR LA RESSOURCE RACF *
* VOLUME, CLASSE ALLOC.                                               *
*                                                                     *
*             EMPLOI DES REGISTRES :                                  *
*                                                                     *
*  R2   = TRAVAIL (ADRESSAGE DE CERTAINS BLOCS DE CONTROLE)           *
*  R3   DISPONIBLE                                                    *
*  R4   = TRAVAIL (ADRESSE DE RETOUR)                                 *
*  R5   = REASON CODE DU REJET :                                      *
*              1 : RESSOURCE INCONNUE DE RACF ET USER NON OPERATIONS  *
*              2 : ACCES INTERDIT PAR RACF POUR CETTE RESSOURCE       *
*              3 : CODE RETOUR APRES RACHECK DIFFERENT DE 0, 4, 8     *
*              4 : GETMAIN POUR ZONE DE TRAVAIL A ECHOUE              *
*                                                                     *
*  R6   DISPONIBLE                                                    *
*  R7   DISPONIBLE                                                    *
*  R8   DISPONIBLE                                                    *
*                                                                     *
*  R9   = CODE RETOUR POUR LE PROGRAMME APPELANT :                    *
*   1)ALLOC    0 : ALLOCATION AUTORISEE                               *
*     -----    4 : ALLOCATION REFUSEE POUR CE VOLUME (X'B4')          *
*              8 : ALLOCATION REFUSEE (INCONDITIONNELLEMENT) (X'B0')  *
*   2)EXTEND   SI R9 <> 0, REFUSEE                                    *
*     ------                                                          *
*                                                                     *
*  R10  DISPONIBLE                                                    *
*  R11  = ADRESSE ZONE PARAMETRE PASSEE A L'EXIT, MACRO IECIEXPL      *
*  R12  = REGISTRE DE BASE POUR LA CSECT                              *
*  R13  = ADRESSE ZONE TRAVAIL OBTENUE PAR GETMAIN POUR RE-ENTRANCE   *
*                                                                     *
*                                                                     *
*  NOTES DE PROGRAMMATION :                                           *
*  ----------------------                                             *
*                                                                     *
*  CE MODULE SERA EN PLPA -> RE-ENTRANCE ABSOLUMENT NECESSAIRE        *
*     CE QUI IMPLIQUE :                                               *
*  1) UTILISATION DES MACROS SOUS FORME MF=L ET MF=(E,XX)             *
*     (INUTILE SI MACRO INVARIANTES, COMME WTO D'UN MESSAGE FIGE)     *
*  2) LA PLPA EST CONSTITUEE DE PAGES QUI SONT **READ-ONLY **         *
*     PAS QUESTION DE STOCKER DES DONNEES DANS CE MODULE, ON DOIT     *
*     STOCKER LES DONNEES DANS L'ESPACE WORK OBTENU PAR GETMAIN (ET   *
*     QUI EST EN FAIT DANS LA PRIVATE DE L'ESPACE-ADRESSE APPELANT).  *
*                                                                     *
*                                                                     *
*  CET EXIT ET  R A C F :                                             *
*  --------------------                                               *
*                                                                     *
*  L'EXIT VERIFIE, POUR LES FONCTIONS : ALLOCATION , EXTEND ,         *
*     QUE LE USER EST AUTORISE EN UPDATE SUR LA RESSOURCE VOLSER      *
*     CLASSE ALLOC.                                                   *
*  CETTE VERIFICATION EST EFFECTUEE PAR LA MACRO : RACROUTE,          *
*     QUI UTILISE L'INTERFACE SAF (SYSTEM AUTHORIZATION FACILITY)     *
*     LE ROUTER MVS PASSE LE CONTROLE AU ROUTER RACF (ICHRFR00)       *
*     QUI SE BASE SUR LA RACF ROUTER TABLE (ICHRFR01) POUR TRAITER    *
*     LA DEMANDE -> ICHRFR01 ET ICHRRCDE DOIVENT ETRE A NIVEAU...     *
*                                                                     *
*  CAS PARTICULIERS ET DECISIONS DE L'EXIT :                          *
*  ---------------------------------------                            *
*                                                                     *
*     SAF  INACTIF -> TOUT EST PERMIS                                 *
*     RACF INACTIF -> TOUT EST PERMIS                                 *
*     CLASSE ALLOC INACTIVE -> TOUT EST PERMIS                        *
*                                                                     *
***********************************************************************
*
         EJECT
         LCLC  &WTO
&WTO     SETC  'NO'       WTO A ENVOYER A CHAQUE ANOMALIE
* CODER AUTRE CHOSE QUE 'YES' PERMET DE SUPPRIMER TOUS LES MESSAGES
         TITLE '**    IGGPRE00 - DADSM PRE-PROCESSING EXIT    **'
IGGPRE00 CSECT
         PRINT  ON,NOGEN
         SAVE  (14,12),,IGGPRE00-&SYSDATE-&SYSTIME
         SPACE
         LR    R12,R15             REGISTRE DE BASE = R12
         USING IGGPRE00,R12        ADRESSABILITE DE LA CSECT
         SPACE
         LR    R11,R1              LISTE DE PARAMETRES IECIEXPL
         USING PREPL,R11           ADRESSEE PAR R11
         USING WORK,R13            ADRESSABILITE ZONE DE TRAV. RACHECK
         SPACE
         SR    R5,R5               REASON CODE 0 A PRIORI
         SR    R9,R9               CODE RETOUR 0 A PRIORI
         TM    IEXFLAG,IEXVIO      VIO   DATA SET ?
         BO    EXIT                OUI - FIN , TOUT EST PERMIS
         SPACE
         CLI   IEXFUNC,IEXALL      FONCTION ALLOCATE ?
         BNE   EXIT                NON ==>
         SPACE
*
*    A L L O C A T E
*
ALLOCFCT BAL   R4,SETUP            PREPARER LA ZONE WORK
         TM    INDICE,JFCTEMP      FICHIER ALLOUE EN TEMPORAIRE ?
         BO    RTNTOCLR            OUI, AUTORISE
         TM    FLAGSMS,UCBSMS      VOLUME SMS ?    RAJOUT TF 03-10-1990
         BO    RTNTOCLR            OUI, AUTORISE             03-10-1990
         BAL   R4,CHECKS           DIVERSES VERIFS SUR L'ETAT DE RACF
         LTR   R15,R15
         BZ    RTNTOCLR            AUTORISE INCONDITIONNELLEMENT
         SPACE
         TM    FLAG1,OPER          USER OPERATIONS ?
         BO    RTNTOCLR            OUI, ON L'AUTORISE
         SPACE
         BAL   R14,ISSUERAC        RACHECK VIA RACROUTE ==> R15
         B     RTNTOCLR            ET RETOUR
         SPACE
*
*    F  I  N
*
RTNTOCLR DS    0H
         SPACE
         STH   R5,IEXREASN         POSITIONNER LE REJECT REASON CODE
         SPACE
         LR    R1,R13              ADRESSER LA ZONE DE WORK
         SPACE
         L     R13,4(,R1)          RESTAURE ADRESSE SAVE AREA APPELANT
         SPACE
         LA    R0,WKEND-WKSTART    LONGUEUR ZONE DE TRAVAIL
         FREEMAIN RU,LV=(0),A=(1)  LIBERER ZONE MEMOIRE ALLOUEE A WORK
EXIT     DS    0H
         LR    R15,R9              RESTAURE CODE RETOUR
         SPACE
         RETURN (14,12),RC=(15)    RETOUR APPELANT
         EJECT
*
***********************************************************************
* 1) VERIFIER QUE LE ROUTER MVS SAF EST PRESENT POUR RACROUTE         *
* 2) VERIFIER QUE LA CLASSE ALLOC EST ACTIVE ET DEFINIE               *
*    VERIFIER QUE RACF EST ACTIF                                      *
* A RENVOYER : R15=0 SI AUTORISATION INCONDITIONNELLE (CAR ANOMALIE)  *
*              R15=4 SI RACROUTE NORMAL A EFFECTUER                   *
***********************************************************************
*
CHECKS   DS    0H
         L     R2,CVTPTR           CHARGER ADRESSE DE LA CVT
         USING CVT,R2
         ICM   R2,B'1111',CVTSAF   ADRESSE ROUTER VECTOR TABLE
         DROP  R2
         BNZ   SAFACTIF
* SAF INACTIF : LE RACROUTE RENVERRA SYSTEMATIQUEMENT R15=4
         AIF   ('&WTO' NE 'YES').L1
         WTO   'IGGPRE00-001  SAF ROUTER INDISPONIBLE, ACCES AUTORISE',*
               ROUTCDE=11          ENVOI MESSAGE AU PROGRAMMEUR
.L1      ANOP
         XR    R15,R15             ACCES PERMIS INCONDITIONNELLEMENT
         B     FINCHEK
*
SAFACTIF DS    0H                  SAF ROUTER PRESENT=RACROUTE POSSIBLE
* VERIFIER LE STATUS DE RACF
         MVC   WKRACSTA(RACSTL),RACSTAT
         RACSTAT  CLASS=ALLOC,MF=(E,WKRACSTA)
         LTR   R15,R15
         BNZ   CHENOT0
         LA    R15,4               POURSUIVRE LES CONTROLES NORMALEMENT
         B     FINCHEK
*      R15=0 : RACF ACTIF CLASSE ACTIVE
CHENOT0  DS    0H
         C     R15,CON4
         BNE   CHENOT4
*      R15=4 : RACF ACTIF, CLASSE INACTIVE
         AIF   ('&WTO' NE 'YES').L2
         WTO   'IGGPRE00-002  CLASSE ALLOC INACTIVE, ACCES AUTORISE',  *
               ROUTCDE=11          ENVOI MESSAGE AU PROGRAMMEUR
.L2      ANOP
         XR    R15,R15             ACCES PERMIS INCONDITIONNELLEMENT
         B     FINCHEK
CHENOT4  DS    0H
         C     R15,CON8
         BNE   CHENOT8
*      R15=8 : RACF ACTIF, CLASSE NON DEFINIE
         AIF   ('&WTO' NE 'YES').L3
         WTO   'IGGPRE00-003  CLASSE ALLOC INCONNUE, ACCES AUTORISE',  *
               ROUTCDE=11          ENVOI MESSAGE AU PROGRAMMEUR
.L3      ANOP
         XR    R15,R15             ACCES PERMIS INCONDITIONNELLEMENT
         B     FINCHEK
CHENOT8  DS    0H
*      R15>=12 : RACF INACTIF
         AIF   ('&WTO' NE 'YES').L4
         WTO   'IGGPRE00-004  RACF INACTIF, ACCES AUTORISE',           *
               ROUTCDE=11          ENVOI MESSAGE AU PROGRAMMEUR
.L4      ANOP
         XR    R15,R15             ACCES PERMIS INCONDITIONNELLEMENT
*
FINCHEK  BR    R4
         EJECT
*
***********************************************************************
*    FONCTION   ALLOCATE                                              *
*        FAIRE RACHECK VIA RACROUTE -                                 *
*        SI CODE RETOUR 0 - C'EST PERMIS                              *
*        SI CODE RETOUR 4 - RESSOURCE INCONNUE, REJET SAUF SI         *
*                           USER A L'ATTRIBUT OPERATIONS              *
*        SI CODE RETOUR 8 - ACCES INTERDIT PAR RACF -> REJET          *
*        AUTRE CODE RETOUR -> REJET                                   *
*                                                                     *
* CE PARAGRAPHE DOIT RETOURNER :                                      *
*        R5 -> REASON CODE  (=0 PAR DEFAUT)                           *
*        R9 -> CODE RETOUR  (=0 PAR DEFAUT, =8 SI REJET)              *
*                                                                     *
***********************************************************************
*
ISSUERAC DS    0H
         ST    R14,ISSUER14        SAUVER L'ADRESSE DE RETOUR
         SPACE
         MVC   WKRACK(RACKLEN),RACK1   PREPARE RACHECK LOG=ASIS
         SPACE
ISSU0010 DS    0H
*        RACHECK
         RACROUTE REQUEST=AUTH,                                        *
               ENTITY=(VOLSER),                                        *
               MF=(E,WKRACK),                                          *
               WORKA=RACWORK
* RAJOUTER INSTLN=INST POUR ADRESSE INSTALLATION DATA POUR EXIT RACHECK
         SPACE 3
*---------------------------------------------------------------------*
* CODE RETOUR = 0      --->   LE USER EST AUTORISE                    *
*---------------------------------------------------------------------*
         LTR   R15,R15             ACCES AUTORISE  ?
         BZ    AUTHOK              OUI - RETURN ==>
         SPACE
NOTAUTH  DS    0H
         C     R15,CON4            CODE 4 , RESSOURCE INCONNUE
         BNE   NOTAUTH1
         SPACE 3
*---------------------------------------------------------------------*
* CODE RETOUR = 4      --->   LA RESSOURCE N'EXISTE PAS               *
*---------------------------------------------------------------------*
         B     AUTHEND       ** VOLUME INCONNU DE RACF -> PERMIS
         SPACE
NOTAUTH1 DS    0H
         C     R15,CON8            ACCES INTERDIT ?
         BNE   NOTAUTH2            NO ==>
         SPACE 3
*---------------------------------------------------------------------*
* CODE RETOUR = 8      --->   LE USER NON AUTORISE A LA RESSOURCE     *
*---------------------------------------------------------------------*
**TF     AIF   ('&WTO' NE 'YES').L7
         WTO   'IGGPRE00-006 ALLOCATION INTERDITE SUR CE VOLUME',      *
               ROUTCDE=11          ENVOI MESSAGE AU PROGRAMMEUR
         BAL   R4,MESSVOL          ENVOI MESSAGE INDIQUANT VOLUME
.L7      ANOP
         LA    R9,REJREQ           REJECT THE REQUEST
         LA    R5,ACU              ACCES INTERDIT
         B     AUTHEND             END ==>
         SPACE 3
*---------------------------------------------------------------------*
* AUTRE CODE RETOUR    --->   ON REFUSE L'ACCES                       *
*---------------------------------------------------------------------*
NOTAUTH2 DS    0H
         LA    R9,REJREQ           REJECT THE REQUEST
         LA    R5,BADRC            CODE RETOUR RACHECK SURPRENANT
         B     AUTHEND             END ==>
         SPACE
AUTHOK   DS    0H
         XR    R9,R9              CODE RETOUR = 0
         SPACE
AUTHEND  DS    0H
         L     R14,ISSUER14        RESTORE RETURN REG
         BR    R14                 RETURN ==>
         EJECT
*
*
*
***********************************************************************
*                                                                     *
*        ENVOI DU MESSAGE SIGNALANT LE VOLUME QUI POSE DES PBS        *
*                                                                     *
***********************************************************************
*
MESSVOL  DS    0H
         MVC   WTO1(WTOLONG),WTOLIST
         MVC   WTOVOL(6),VOLSER
         MVC   WTODSNAM(44),DSNAME
         WTO   MF=(E,WTO1)         ** ENVOI MESSAGE A L'ECRAN **
         BR    R4                  RETOUR
*
***********************************************************************
*                                                                     *
*        SUBROUTINE TO GET AND INITIALISE THE WORK AREA               *
*                                                                     *
* CE PARAGRAPHE DOIT RENVOYER :                                       *
*        FLAG1  : FLAG USER OPERATIONS                                *
*        VOLSER : VOLUME CONCERNE PAR ALLOCATE/EXTEND                 *
*        R13 ->  POINTE SUR ZONE TEMPORAIRE                           *
***********************************************************************
*
SETUP    DS    0H
         SPACE
         LA    R0,WKEND-WKSTART    LONGUEUR ZONE WORK  (SUBPOOL 0)
         GETMAIN RU,LV=(0)         ACQUERIR ZONE DE TRAVAIL
         LTR   R15,R15
         BZ    CONTIN
* PB LORS DU GETMAIN -> ACCES REFUSE
         AIF   ('&WTO' NE 'YES').L5
         WTO   'IGGPRE00-008 UNABLE TO GET STORAGE FOR RXF-DADSM EXIT',*
               ROUTCDE=11          ENVOI MESSAGE AU PROGRAMMEUR
.L5      ANOP
         LA    R5,4
         STH   R5,IEXREASN         REASON CODE 4
         LA    R9,4
         B     EXIT                RETOUR DIRECT PGM APPELANT
         SPACE
* CHAINAGE DES SAVE AREAS :
CONTIN   DS    0H
         ST    R1,8(0,R13)         ADRESSE DE MA S.A. -> S.A. APPELANT
         ST    R13,4(0,R1)         ADRESSE S.A. APPELANT -> MA S.A.
         SPACE
         LR    R13,R1              ADRESSER LA ZONE WORK PAR R13
         SPACE
         MVI   FLAG1,X'00'         FLAG PAR DEFAUT : USER NON OPERATION
         SPACE
         USING PSA,R0              PSA ADRESSABILITE BIDON (ADRESSE 0)
         L     R2,PSAANEW          ASCB POINTEUR
         DROP  R0
         USING ASCB,R2             ASCB ADRESSABILITE
         L     R2,ASCBASXB         ASXB POINTEUR
         DROP  R2
         USING ASXB,R2             ASXB ADRESSABILITE
         L     R2,ASXBSENV         ACEE POINTEUR
         DROP  R2
         USING ACEE,R2             ACEE ADRESSABILITE
         TM    ACEEFLG1,ACEEOPER   USER A L'ATTRIBUT OPERATIONS ?
         BNO   SETU0010            NON ==>
         OI    FLAG1,OPER          METTONS FLAG OPERATIONS
         DROP  R2
         SPACE
SETU0010 DS    0H
         SPACE
* RECUPERER LE VOLSER OU EST TENTEE L'ALLOCATION
         L     R2,IEXUCB           UCB POINTEUR
         USING UCBCMSEG,R2         ADRESSABILITE DE L'UCB
         MVC   VOLSER,UCBVOLI      SAUVER LE VOLSER
* RAJOUT TF 03-10-1990 : PAS DE CONTROLE SI VOLUME SMS
         MVC   FLAGSMS,UCBFL5      VOLUME SMS ?    RAJOUT TF 03-10-1990
         DROP  R2
* RECUPERER LE DSNAME
         L     R2,IEXDSN           DSN POINTEUR
         MVC   DSNAME,0(R2)        LE STOCKER CHEZ NOUS
* RECUPERER LE JFCB POUR SAVOIR SI ALLOC DE FICHIER TEMPORAIRE
         L     R6,IEXPTR1          JFCB
         USING INFMJFCB,R6
AMODSUIT DS    0H                                            13-2-89
*     ACCEDER AU JFCB                                        13-2-89
         MVC   INDICE,JFCBIND2     MOVE INDICATEUR
         DROP  R6
         SPACE
         BR    R4                  RETURN ==>
         EJECT
*
*  - RACHECK SOUS FORME LISTE AVEC LOG=ASIS (VALEUR PAR DEFAUT) :
*       LES ACCES SONT ENREGISTRES DANS LES FICHIERS SMF
*       SELON CE QUI A ETE SPECIFIE LORS DE ADDSD/ALTDSD/RDEFINE/RALTER
*
RACK1    RACROUTE REQUEST=AUTH,ENTITY=(0),CLASS='ALLOC',ATTR=UPDATE,   *
               LOG=ASIS,MF=L
         SPACE 3
*
*  - RACSTAT SOUS FORME LISTE
*
RACSTAT  RACSTAT  CLASS='ALLOC',MF=L
*
*  - WTO     SOUS FORME LISTE
*
WTOLIST  WTO   'IGGPRE00-009 VVVVVV DDDDDDDD.DDDDDDDD.DDDDDDDD.DDDDDDDD*
               .DDDDDDDD           ',ROUTCDE=11,MF=L  SIGNALER VOL-DSN
         CNOP  2,4
CON4     DC    F'4'
CON8     DC    F'8'
ALLOC    DC    CL8'ALLOC'
         LTORG
         TITLE '***** IGGPRE00 - EQUATES *****'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 4
RNK      EQU   1                   RESSOURCE INCONNUE
ACU      EQU   2                   ACCES INTERDIT
BADRC    EQU   3                   BAD RC FROM RACHECK
REJREQ   EQU   4       20-6-89     REJECT THE REQUEST POUR CE VOLUME
         TITLE '***** IGGPRE00 - DATA AREAS *****'
PATCHES  DC    0A(PATCHES),40S(*)
 TITLE '* DSECT DE TRAVAIL POUR RE-ENT. : Y METTRE ZONES TEMPORAIRES '
WORK     DSECT ,
WKSTART  EQU   *
WKSAVE   DS    18F                 OS SAVE AREA , DOIT RESTER EN 1ER
         SPACE 2
*
ISSUER14 DS    F                   ADRESSE DE RETOUR
EXTENR14 DS    F                   ADRESSE DE RETOUR
         SPACE 2
*
VOLSER   DS    CL6                 VOLUME SERIAL NUMBER
DSNAME   DS    CL44                DATA SET NAME
INDICE   DS    CL1                 INDICATEUR JFCB
FLAGSMS  DS    CL1            FLAG VOLUME SMS      RAJOUT TF 03-10-1990
*
* ZONES DE TRAVAIL POUR RACHECK
*
WKRACK   DS    0H       ZONE BIDON INITIALISEE AVANT RACHECK
*
         RACROUTE REQUEST=AUTH,ENTITY=(R3),CLASS='ALLOC',ATTR=UPDATE,  *
               LOG=ASIS,WORKA=RACWORK,MF=L
*              INSTLN=INST  ADRESSE INSTALLATION DATA POUR EXIT RACHECK
RACKLEN  EQU   *-WKRACK
         SPACE 2
*
*  - RACSTAT SOUS FORME LISTE
*
WKRACST1 DS    0H
WKRACSTA RACSTAT  CLASS='ALLOC',MF=L
RACSTL   EQU   *-WKRACST1
*
*
*  - WTO     SOUS FORME LISTE
*
WTODEBUT DS    0H
WTO1     WTO   'IGGPRE00-009 VVVVVV DDDDDDDD.DDDDDDDD.DDDDDDDD.DDDDDDDD*
               .DDDDDDDD           ',ROUTCDE=11,MF=L  SIGNALER VOL-DSN
WTOVOL   EQU   WTO1+17     ADRESSE OU ECRIRE LE NOM DU VOLUME
WTODSNAM EQU   WTO1+24     ADRESSE OU ECRIRE LE NOM DU FICHIER
WTOLONG  EQU   *-WTODEBUT
*
         SPACE 3
RACWORK  DS    XL512               WORK AREA FOR RACROUTE
FLAG1    DS    X
OPER     EQU   X'40'               USER A L'ATTRIBUT OPERATIONS
WKEND    DS    0D
         IECIEXPL ,                EXIT PARM LIST
         IEFUCBOB   LIST=YES       UCB
         IHAPSA   ,                PSA
         IHAASCB  ,                ASCB
         IHAASXB  ,                ASXB
         IHAACEE  ,                ACEE
IEFJFCBN DSECT
         IEFJFCBN ,                JFCB
         CVT   DSECT=YES           CVT
         END
