         MACRO
&NLA     PRINTHX  &BIN,&DISPL
&NLA     DS    0H
.*
.* CONVERSION D'UNE CHAINE BINAIRE EN CARACTERES HEXA
.* EX.      1 MOT CONTENANT X'80103444'  -> C'80103444'
.*       REGISTRES UTILISES : AUCUN
.*
.* RESTRICTIONS :
.*
.* - LA CHAINE BINAIRE &BIN NE PEUT DEPASSER 7C
.* - L'OCTET SUIVANT LA CHAINE BINAIRE &BIN EST LU...
.* - ON SUPPOSE (SANS LE VERIFIER) QUE &DISPL EST AU MOINS
.*     2 FOIS PLUS LONGUE QUE &BIN (+ 1 OCTET QUI SERA ECRASE)
.*
         LCLA  &LB,&LD,&LB1,&LD1     LONGUEURS ZONES
&LB      SETA  L'&BIN
&LB1     SETA  &LB+1
&LD      SETA  L'&DISPL
&LD1     SETA  &LD+1
&LD      SETA  2*&LB1-1
&LB      SETA  &LB*2
.*
         UNPK    &DISPL.(&LD),&BIN.(&LB1)   ETENDRE : X'1D'->X'F1FD'
         NC      &DISPL.(&LB),=XL&LB'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F' X
               SUPPRIMER DEMI-OCTET A F DANS LE X'F?': X'F1FD'->X'010D'
         TR      &DISPL.(&LB),=C'0123456789ABCDEF' VALEURS HEXA FINALES
         MEXIT
         MEND
