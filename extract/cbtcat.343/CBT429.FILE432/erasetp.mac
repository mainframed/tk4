* PROGRAM TO ERASE TAPE DATA.
*
* //ERASE    EXEC PGM=ERASETP
* //STEPLIB  DD DISP=SHR,DSN=MY.LOAD
* //SYSPRINT DD SYSOUT=*
* //TAPE     DD DISP=(NEW,KEEP),DSN=TEST.ERASE.WRITTAPE,VOL=SER=TTTTTT,
* //         UNIT=3480,LABEL=(1,SL,EXPDT=98000),DCB=TRTCH=NOCOMP
*
*
ERASETP CSECT
      PROLOG BASEREG=12,REQUATE=YES
      XR   R0,R0
      L    R1,=F'65526'
      LA   R2,ZONE999
      LR   R3,R1
      MVCL R2,R0                    CLEAR IO ZONE
* AT CCW1+6(2) : WISHED DATA LENGTH
* MOVE THE BLKSIZE VALUE
      MVC  LON,=CL5'65535'          64K BLOCKS
      PACK  LONP,LON
      CVB  R3,LONP
      STCM  R3,3,CCW1+6             SET BLOCKSIZE IN CCW
      XR   R7,R7
*
      LA   R1,ZONEIO
      LR   R5,R1
      ST   R5,CCW1    DATA ADDRESS
      MVI  CCW1,X'01'  COMMAND CODE :  ** WRITE **
*
      XC   ECB,ECB
      XC   IOB1,IOB1
      LA   R1,ECB
      ST   R1,IOBECB
      LA   R1,CCW1
      ST   R1,IOBSTART
      LA   R1,TAPEOUT
      ST   R1,IOBDCB
      OPEN  (TAPEOUT,(OUTPUT))
      OPEN  (SYSPRINT,(OUTPUT))
LOOP  DS    0H
      EXCP  IOB1
      WAIT ECB=ECB
      TM   ECB,X'7F'                TEST IO-ERROR
      BO   NOERROR
* PROCESS IO-ERROR
      PRINTHX IOBCSW,CSWHEX
      PRINTHX IOBECB,ECBHEX
      PRINTHX IOBSENS0,IOBS0
      PRINTHX IOBSENS1,IOBS1
      PUT  SYSPRINT,M1
      B    FINLECT
NOERROR DS  0H
      LA   R7,1(0,R7)  COMPTEUR
      B  LOOP
FINLECT  CLOSE (TAPEOUT)
         CLOSE (SYSPRINT)
       ST  R7,MOT
       CONVERT FROMB=MOT,TOZ=COMPT
       WTOPUT '** TAPE HAS BEEN ERASED, 64K-BLOCKS WRITTEN : ',COMPT
*
       L   R7,MOT
       XR  R6,R6
       M   R6,=F'65535'         CALCUL EN OCTETS
       D   R6,=F'1000000'       CALCUL EN MILLIONS D'OCTETS
       ST  R7,MILLION
       CONVERT FROMB=MILLION,TOZ=COMPT
       WTOPUT '**         CAPACITY = ',COMPT,' MILLIONS OF BYTES'
*
       L   R7,MOT
       XR  R6,R6
       M   R6,=F'65535'         CALCUL EN OCTETS
       D   R6,=F'1048576'       CALCUL EN MEGABYTES  (1024*1024)
       ST  R7,MEGABYTE
       CONVERT FROMB=MEGABYTE,TOZ=COMPT
       WTOPUT '**               OR = ',COMPT,' MEGABYTES'
*
RET       L       R13,4(R13)
          RETURN  (14,12),T,RC=0
TAPEOUT DCB MACRF=(E),OPTCD=Z,DDNAME=TAPE,DSORG=PS,                    *
               DEVD=TA   IOB=IOB1
SYSPRINT DCB  MACRF=PM,DSORG=PS,DDNAME=SYSPRINT,LRECL=133,BLKSIZE=13300
MEGABYTE DS   F
MILLION  DS   F
METRES   DS   F
MOT      DS   F
COMPT    DS   CL8
ECB      DS   F
LON      DS   CL5
    CNOP 0,8
LONP     DC   PL8'0'
ZONE133  DS   0CL133
         DC   C' '
         DC   CL80' '
         DC   CL(133-81)' '
M1       DS   0CL133
         DC   C' ** I/O ERROR, CSW = X'''
CSWHEX   DS   CL14
         DC   C''', ECB=X'''
ECBHEX   DS   CL8
         DC   C''', IOBSENS0=X'''
IOBS0    DS   CL2
         DC   C''', IOBSENS1=X'''
IOBS1    DS   CL2
         DC   CL(M1+L'M1-*)'''   '
BIDON  DS  0H
CCW1     CCW  X'01',BIDON,X'20',80   WRITE ZONE
         CNOP 0,4
IOB1     DS   0CL32
IOBFLAG1 DS   CL1
IOBFLAG2 DS   CL1
IOBSENS0 DS   CL1
IOBSENS1 DS   CL1
IOBECB   DS   F
IOBFLAG3 DS   CL1
IOBCSW   DS   CL7
IOBSTART DS   F
IOBDCB   DS   F
         DS   CL12  THE REMAINING BYTES
  LTORG
ZONEIO   DS  0CL1
         DC   CL10'* ERASED *'
ZONE999  DS   CL65526
         END
