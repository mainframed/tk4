         MACRO
&NLA     EXTEND  &BIN,&ETEND,&REG
*EXTEND A BINARY ZONE (1ST PARAM) INTO AN EXTENDED (2ND) USING WORK REG
.* LA ZONE BINAIRE DOIT ETRE DE 2, 3 OU 4 OCTETS
.* LE RESULTAT EST DONNE EN VALEUR ABSOLUE (IL REPRESENTE UN NOMBRE
.* POSITIF OU NEGATIF SELON QUE LE DERNIER BIT A GAUCHE DU NOMBRE BIN
.* EST 0 OU 1. SI ON CONSIDERE QUE LE NBRE BIN EST NON SIGNE, IL FAUT
.* FAIRE  :  NI  ZONEBIN,B'01111111' AVANT EXTEND
.* POUR FORCER LE SIGNE A +
         LCLA  &REGIS
         LCLA  &NBR
         LCLA  &LEN
         LCLA  &ZZ
         LCLA  &MASK
&NLA     DS    0H
&LEN     SETA  L'&BIN
         AIF (&LEN GT 4).ERR1
         AGO   .FOL0
.ERR1    ANOP
         MNOTE 8,'** LENGTH OF &BIN EXCEEDS 4 BYTES, CANNOT EXTEND'
         MEXIT
.FOL0    ANOP
         AIF (T'&ETEND NE 'U').FOLLOW
&LEN     SETA  (L'&BIN*2)+2    LONGUEUR DE LA ZONE ETENDUE
         B    EXPR&SYSNDX
&ETEND   DC   CL&LEN'0'
.FOLLOW  ANOP
&NBR     SETA  N'&SYSLIST
        AIF  (&NBR EQ 2).SUIT1
        AIF  (&NBR EQ 3).SUIT2
        MNOTE 8,' ** NUMBER OF PARAMETERS MUST BE 2 OR 3'
        MEXIT
.SUIT1  ANOP
&REGIS  SETA   1        USE REGISTER 1 FOR WORKING ZONE
        AGO  .SUIT3
.SUIT2  ANOP
&REGIS  SETA     &REG
.SUIT3  ANOP
EXPR&SYSNDX   DS  0H
        XR    &REGIS,&REGIS            SET REGISTER TO ZERO
.*      CNOP 0,4                       ALIGN ON WORD
&LEN    SETA  L'&BIN
.*
.* CALCUL DU MASQUE DE CHARGEMENT DE LA ZONE
&ZZ     SETA  1
&MASK   SETA  1
.LOPM   ANOP
        AIF   (&ZZ GT &LEN).FINM
&MASK   SETA  2*&MASK
&ZZ     SETA  &ZZ+1
        AGO   .LOPM
.FINM   ANOP
&MASK   SETA  &MASK-1     MASQUE POUR CHARGER ZONE BIN 1 2 3 OU 4 BYTES
        ICM   &REGIS,&MASK,&BIN            LOAD BINARY ZONE
.*
.*
        CNOP 0,8                       ALIGN ON DOUBLE WORD
        CVD   &REGIS,EXTD&SYSNDX       CONVERT TO DECIMAL
        UNPK  &ETEND,EXTD&SYSNDX.(8)   UNPACK WORK ZONE
&LEN    SETA  L'&ETEND
        OI    &ETEND+(&LEN-1),X'F0'    LAST BYTE ALSO TO EXTEND
        B     EXTF&SYSNDX              CONTINUE
        CNOP 0,8                       ALIGN ON DOUBLE WORD
EXTD&SYSNDX DC     FL8'0'                WORKING ZONE ON TWO WORDS
EXTF&SYSNDX DS 0H
         MEND
