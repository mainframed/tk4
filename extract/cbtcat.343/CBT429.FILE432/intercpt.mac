*
*  CET EXIT PERMET D'INTERCEPTER UN MESSAGE (PREFIXE 8C) ET DE PASSER
*  AUTOMATIQUEMENT UNE COMMANDE MVS CENSEE SE RAPPORTER A CE MESSAGE
*
*  POUR QU'IL FONCTIONNE :
*    MSGNO??,SUP(NO),USEREXIT(INTERCPT)  A METTRE EN PARMLIB(MPFLSTXX)
*
INTERCPT CSECT
INTERCPT AMODE 31
INTERCPT RMODE ANY
R0   EQU 0
R1   EQU 1
R2   EQU 2
R3   EQU 3
R4   EQU 4
R5   EQU 5
R6   EQU 6
R7   EQU 7
R8   EQU 8
R9   EQU 9
R10  EQU 10     2EME REGISTRE DE BASE
R11  EQU 11
R12  EQU 12      1ER REGISTRE DE BASE
R13  EQU 13
R14  EQU 14
R15  EQU 15
         STM  R14,R12,12(R13)    SAUVER REGISTRES DE L'APPELANT
         BALR R12,0              R12 REGISTRE DE BASE
         USING *,R12,R10,R9
         LR   R10,R12            R10 2EME REGISTRE DE BASE TF 1-10-90
         LA   R10,4095(0,R10)    R10 2EME REGISTRE DE BASE TF 1-10-90
         LA   R10,1(0,R10)       R10 2EME REGISTRE DE BASE TF 1-10-90
         LR   R9,R10             R9 2EME REGISTRE DE BASE TF 1-10-90
         LA   R9,4095(0,R9)      R9 2EME REGISTRE DE BASE TF 1-10-90
         LA   R9,1(0,R9)         R9 2EME REGISTRE DE BASE TF 1-10-90
         L    R5,0(R1)           ADRESSER LA CTXT
         USING CTXT,R5
*
         GETMAIN  RU,LV=DATALONG,SP=230,LOC=BELOW
         LR   R11,R1
         USING DATAAREA,R11      ADRESSER LA ZONE MEMOIRE
         ST   R13,SAVE+4
         LA   R15,SAVE
         ST   R15,8(R13)
         LR   R13,R15            POINTER SUR MA SAVE AREA
*
         L    R2,CTXTTXPJ
         USING CTXTATTR,R2       ADRESSER LES MSG ATTRIBUTES
         LA   R4,CTXTTMSG        TEXT AREA
         USING MSGTEXT,R4
*
* RECHERCHER LE PREFIXE DU MESSAGE
*
         LA   R7,TABMSG          ADRESSER DEBUT TABLE DES MESSAGES
SCANTAB  CLC  FINTAB,0(R7)       FIN DE LA TABLE DES MESSAGES ?
         BE   FINI               OUI, ON A ETE APPELE INUTILEMENT
         CLC  MSGID,0(R7)        EST-CE CE MESSAGE ?
         BE   TRAITMSG           OUI, TRAITER CE MESSAGE
*                                NON, PASSER POSTE SUIVANT
LOOPTB   LA   R7,89(0,R7)        SAUTER PREFIXE + FLAG + COMMANDE
         B    SCANTAB
*
TRAITMSG DS   0H
***      OI   CTXTRFB2,CTXTRHCO  SUPPRIMER LE MESSAGE   6-4-89 TF
         XC   MGCRPL(MGCRLTH),MGCRPL   CLEAR PARAMETER LIST
         MVC  MGCRTEXT(80),9(R7) PREPARER COMMANDE MVS
         CLI  8(R7),C'R'         EST-CE UN REPLY ?
         BNE  NOREPLY            NON, TRAITEMENT HABITUEL
* TRAITEMENT D'UN REPLY AUTOMATIQUE
         MVI  MGCRTEXT,C' '      REMISE A BLANC
         MVC  MGCRTEXT+1(79),MGCRTEXT  REMISE A BLANC COMPLETE
         MVC  MGCRTEXT(L'REPONSE),REPONSE   DEBUT DE LA COMMANDE
         MVC  MGCRTEXT+6(2),CTXTRPID        NUMERO DE REPLY
         MVC  MGCRTEXT+L'REPONSE(80-L'REPONSE),9(R7) REPLY LUI-MEME
NOREPLY  DS   0H
         BAL  R14,ISSUMGCR       ENVOYER COMMANDE
         BAL  R14,ISSUWTO        ET MESSAGE SIGNALANT INTERCEPTION
*** RETOUR EN BOUCLE POUR PLUSIEURS REPONSES 1-10-90
         B    LOOPTB   ** RETOUR BOUCLE POUR PLUSIEURS REPONSES 1-10-90
FINI     DS   0H
         L    R13,4(R13)
         FREEMAIN  RU,LV=DATALONG,A=(R11),SP=230
         LM   R14,R12,12(R13)    RESTORER REGISTRES DE L'APPELANT
         BR   R14                RETOUR APPELANT
*
ISSUMGCR DS   0H
         LA   R1,(MGCRTEXT-MGCRPL)+80          LONGUEUR DE MGCRPL
         STC  R1,MGCRLGTH        STOCKER DANS LA ZONE PARAMETRE MGCR
         CLI  8(R7),C'J'  21-6-89 JOBNAME DOIT-IL FIGURER DANS LA CMD ?
         BNE  NOJOBN      21-6-89 NON, TRAITEMENT HABITUEL
* METTRE LE JOBNAME DANS LA COMMANDE
         MVC  MGCRTEXT+4(8),CTXTJBNM    ** MOVE DU JOBNAME ** 21-6-89
         B    SENDCOMM
NOJOBN   DS   0H
         CLI  8(R7),C'T'  15-10-90 TIME DOIT-IL FIGURER DANS LA CMD ?
         BNE  NOTIMEF     15-10-90 NON, TRAITEMENT HABITUEL
* METTRE LE TIME DANS LA COMMANDE
         LA   R1,2               TIME DECIMAL
         SVC  11                 TIME SVC
         ST   R0,DOUBLEW         LE STOCKER
         UNPK SYSTIME(9),DOUBLEW(5)      TRADUCTION EN EDITABLE
         MVC  TIME(2),SYSTIME       MOVE HH
         MVI  TIME+2,C':'           MOVE   :
         MVC  TIME+3(2),SYSTIME+2   MOVE    MM
         MVI  TIME+5,C':'           MOVE      :
         MVC  TIME+6(2),SYSTIME+4   MOVE       SS
         MVC  MGCRTEXT+4(8),TIME        ** MOVE DU TIME ** 15-10-90
         B    SENDCOMM
NOTIMEF  DS   0H
         CLI  8(R7),C'M'  11-03-96
         BNE  NOMSGF
* TF 11-03-96
* METTRE LE MSG DANS LA COMMANDE
         MVC  MGCRTEXT+14(65),0(R4)       ** MOVE DU TEXTE DU MSG **
         B    SENDCOMM
NOMSGF   DS   0H
SENDCOMM XR   R0,R0
         MGCR MGCRPL             ENVOYER LA COMMANDE MVS
         BR   R14
*
ISSUWTO  DS   0H
         L    R2,CTXTTXPJ
         USING CTXTATTR,R2
         LA   R4,CTXTTMSG        ADRESSE TEXT AREA
         USING MSGTEXT,R4
         MVC  USERAUTO,USERSTAT  FORME WTO LIST
         MVC  USERAUTO+24(8),0(R7)      INDIQUER PREFIXE DU MESSAGE
         DROP R2,R4
         WTO  MF=(E,USERAUTO)
         BR   R14
* DONNEES
REPONSE  DC   C'REPLY XX,'     DEBUT COMMANDE REPLY
USERSTAT WTO  'INTERCPT   MESSAGE  ????????  INTERCEPTE PAR EXIT MPF', *
               ROUTCDE=(11),DESC=(6),MF=L
CNCLMSG  EQU  *-USERSTAT
*-----------------------------------------------------------*
*-                                                         -*
*-  T A B L E      D E S      M E S S A G E S   A TRAITER  -*
*-                                                         -*
*-  CHAQUE POSTE EST CONSTITUE DE :                        -*
*-       1)  LE PREFIXE DU MESSAGE A INTERCEPTER (8C)      -*
*-       2)  'R' S IL FAUT ENVOYER UN REPLY      (1C)      -*
*-           'J' SI LE JOBNAME DOIT FIGURER EN POSITION 4  -*
*-           'T' SI L'HEURE    DOIT FIGURER EN POSITION 4  -*
*-           ' ' SI COMMANDE A PASSER SANS RIEN DE PLUS    -*
*-       3)  LA COMMANDE OU LE REPLY A PASSER    (80C)     -*
*-                                                         -*
*-                                                         -*
*-                                                         -*
*-----------------------------------------------------------*
TABMSG   DS   0F
* AVERTIR INIT DISQUE EN COURS
         DC   CL8'ICK003D '    PREFIXE DU MESSAGE REPLY U TO ALTER VOL
         DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''JJJJJJJJ  ICKDSF INIT DISQUE   '',USER=(SY02)'
* TRAITEMENT DE L'EREP AUTOMATIQUE
         DC   CL8'IFB060E '    PREFIXE DU MESSAGE SYS1.LOGREC NEAR FULL
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''SYS1.LOGREC IS NEAR FULL'',USER=(SY02,SY13)'
* LOGREC FORMAT ERROR
         DC   CL8'IFB050I '    PREFIXE DU MESSAGE LOGREC FORMAT ERROR
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''LOGREC FORMAT ERROR'',USER=(SY02,SY13)'
* VIDAGE DES FICHIERS DUMP AUTOMATIQUE
         DC   CL8'IEA994E '    PREFIXE DU MESSAGE ALL SYS1.DUMP FULL
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'S VIDEDUMP' COMMANDE A PASSER
* AVERTISSEMENT PRISE DE SYSMDUMP
         DC   CL8'IEA993I '    PREFIXE DU MESSAGE SYSMDUMP TAKEN
         DC   CL1'J'           FLAG = 'J'
         DC   CL80'SE ''JJJJJJJJ DUMP SUR SYSMDUMP PRIS'',USER=(SY02)'
* AVERTISSEMENT PRISE DE SYSMDUMP PARTIEL
         DC   CL8'IEA998I '    PREFIXE DU MESSAGE SYSMDUMP TAKEN
         DC   CL1'J'           FLAG = 'J'
         DC   CL80'SE ''JJJJJJJJ SYSMDUMP PARTIEL PRIS '',USER=(SY02)'
* AVERTISSEMENT DE PRISE DE DUMP SYSTEME
         DC   CL8'IEA911E '    COMPLETE/PARTIAL DUMP ON SYS1.DUMPXX
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'S TRAPDUMP '  ENVOI MSG + NOTER RAISON DUMP
* AVERTISSEMENT DE PRISE DE DUMP SYSTEME
**    DC   CL8'IEA911E '    COMPLETE/PARTIAL DUMP ON SYS1.DUMPXX
**    DC   CL80'SE ''** UN DUMP SYSTEME A ETE PRIS'',USER=(SY14),L*
**          OGON '
* AVERTISSEMENT DE PRISE DE DUMP SYSTEME IMPOSSIBLE
      DC   CL8'IEA994A '    COMPLETE/PARTIAL DUMP ON SYS1.DUMPXX
      DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''** SVCDUMP ET FICHIERS PLEINS'',USER=(SY02)'
* AVERTISSEMENT DE FIN DE REFRESH DE LA LLA
         DC   CL8'CSV210I'     LLA REFRESHED
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''** LA LLA A ETE RAFRAICHIE **'',USER=(SY02,SY13X
               ,SY14,SY15) '
* AVERTISSEMENT DE DEMANDE DE ZAP AVEC CONFIRMATION PUPITREUR
         DC   CL8'AMA117D'     ZAP ON ...  REPLY Y OR N
         DC   CL1'J'           FLAG = 'J'
         DC   CL80'SE ''JJJJJJJJ ZAP EN COURS AVEC REPLY'',USER=(SY02)'
* AVERTISSEMENT DE ZAP REUSSI
         DC   CL8'AMA121I'     ZAP
         DC   CL1'J'           FLAG = 'J'
  DC CL80'SE ''JJJJJJJJ ZAP EFFECTUE CORRECTEMENT'',USER=(SY02),LOGON'
* AVERTISSEMENT DE SPOOL PLEIN (LIMITE DEPASSEE)
         DC   CL8'$HASP050'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''** MESSAGE $HASP050, SPOOL PLEIN ?'',USER=(SY02*
               ,SY13,SY14,SY15) '
* AVERTISSEMENT DE CATASTROPHIC ERROR
         DC   CL8'$HASP095'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT JES2 CATASTROPHIC ERROR *'',USER=(SY02*
               ,SY13,SY14),LOGON'
* AVERTISSEMENT DE 'DATASET UNPRINTABLE'
         DC   CL8'$HASP704'
         DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''JJJJJJJJ DATASET UNPRINTABLE (FSS?) '',USER=(SY02*
               ,SY14,SY15) '
* AVERTISSEMENT DE 'WAITING FOR ACCESS TO JES2 CHECKPOINT'
         DC   CL8'$HASP263'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''** $HASP263 WAITING FOR CHECKPOINT'',USER=(SY02*
               ,SY13,SY14,SY15) '
         DC   CL8'$HASP263'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'F R,RES'
         DC   CL8'$HASP263'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'F CATALOG,LIST'
* AVERTISSEMENT DE SPOOL PLEIN (A 100%)
         DC   CL8'$HASP355'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''** LE SPOOL EST PLEIN, VEUILLEZ FAIRE DU MENAGE*
                DANS VOS SYSOUTS **'' '
*                           TF 21-9-93 POUR ESSAYER DE FAIRE LE MENAGE
         DC   CL8'$HASP355'
         DC   CL1' '
         DC   CL80'$PQ,Q=ALL,A=7'
*                           TF 21-9-93 POUR ESSAYER DE FAIRE LE MENAGE
         DC   CL8'$HASP355'
         DC   CL1' '
         DC   CL80'$OQ,Q=X,A=7,CANCEL'
* AVERTISSEMENT DE FICHIERS SMF PLEINS
         DC   CL8'IEE361I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT  IEE361I FICH. SMF PLEINS'',USER=(SY02*
               ,SY13,SY14,SY15) '
* AVERTISSEMENT DE FICHIERS SMF PLEINS
         DC   CL8'IEE366I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT  IEE366I FICH. SMF PLEINS'',USER=(SY02*
               ,SY13,SY14,SY15) '
*
* AVERTISSEMENT AUXILIARY STORAGE MANAGER
*
ILR009E  DC   CL8'ILR009E'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT ILR009E FICHIER PAGE MORT'',USER=(SY02*
               ,SY13,SY14,SY15) '
*
* AVERTISSEMENT DE DIVERS SHORTAGES GRAVES (MESSAGES IRA*)
*
IRA100E  DC   CL8'IRA100E'
         DC   CL1' '
         DC   CL80'C IDMS80'
IRA100E1 DC   CL8'IRA100E'
         DC   CL1' '
         DC   CL80'C IDMS83'
IRA100E3 DC   CL8'IRA100E'
         DC   CL1' '
         DC   CL80'C EM02DB22'
IRA100E2 DC   CL8'IRA100E'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA100E   SQA SHORTAGE   '',USER=(SY02*
               ,SY13,SY14,SY15) '
IRA101E  DC   CL8'IRA101E'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA101E CRITIC SQA SHORTAGE'',USER=(SY*
               02,SY13,SY14,SY15) '
IRA103I  DC   CL8'IRA103I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA103I SQA EXPANDED IN CSA'',USER=(SY*
               02,SY13,SY14,SY15) '
IRA200E  DC   CL8'IRA200E'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA200E  AUXIL. STORAGE SHORTAGE'',USE*
               R=(SY02,SY13,SY14,SY15) '
IRA200E1 DC   CL8'IRA200E'
         DC   CL1' '
         DC   CL80'PAGEADD PAGE.VSYSHM2.LOCAL'  RAJOUT D'UN FICH. PAGE
IRA201I  DC   CL8'IRA201I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA201I CRITIC AUX STOR SHORTAGE'',USE*
               R=(SY02,SY13,SY14,SY15) '
IRA202I  DC   CL8'IRA202I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA202I   FIN AUXILIARY SHORTAGE'',USE*
               R=(SY02,SY13,SY14,SY15) '
IRA400I  DC   CL8'IRA400I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA400I  PAGEABLE STOR. SHORTAGE'',USE*
               R=(SY02,SY13,SY14,SY15) '
IRA401I  DC   CL8'IRA401I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IRA401I CRITIC PAG STOR SHORTAGE'',USE*
               R=(SY02,SY13,SY14,SY15) '
* AVERTISSEMENT 'PLT PROGRAM NOT FOUND  REPLY GO OR CANCEL'
       DC   CL8'DFH1579'
         DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''JJJJJJJJ  PLT PGM NOT FOUND, REPLY ?'',USER=(SY02*
               ,SY14,SY15) '
* AVERTISSEMENT 'PLTPI NOT FOUND        REPLY GO OR CANCEL'
       DC   CL8'DFH1578'
         DC   CL1'J'           FLAG = 'J'
         DC   CL80'SE ''JJJJJJJJ PLTPI NOT FOUND , REPLY ?'',USER=(SY02*
               ,SY14,SY15) '
* AVERTISSEMENT 'UNABLE TO OPEN ACB                       '
       DC   CL8'DFH1572'
         DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ UNABLE TO OPEN ACB, WARNING'',USER=(SY02*
               ,SY14,SY15) '
* CICS UNDER STRESS
       DC   CL8'DFH0506'
         DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ CICS UNDER STRESS (S.O.S.) '',USER=(SY02*
               ,SY13,SY14,SY15) '
* CICS NO LONGER SOS
       DC   CL8'DFH0507'
         DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ CICS NO LONGER IN S.O.S.   '',USER=(SY02*
               ,SY13,SY15) '
* PROBLEME GDON
         DC   CL8'WMN950I'    CAS D'UN DUMP DANS CICARCHI POUR GDON.
         DC   CL1' '
         DC   CL80'SE ''PROBLEME DISQUE OPTIQUE'',USER=(SY14)'
* PROBLEME CPU
         DC   CL8'PWSCPU'     CAS D'UN WARNING RESOLVE SUR LA CPU.
         DC   CL1' '
         DC   CL80'SE ''PROBLEME CPU. PWSCPU   '',USER=(SY14)'
* PROBLEME VTAMPRINT
         DC   CL8'VAOTMP03'   CAS D'UN PB IMPRESSION VTP.
         DC   CL1' '
         DC   CL80'SE ''PROBLEME IMPRESSION VTP'',USER=(SY14)'
* AVERTISSEMENT 'PEP PLANTE'
       DC   CL8'IST095A'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''** IST095A  REPLY POUR DUMP DU NCP...'',USER=(SY0*
               2) '
* AVERTISSEMENT 'HIPERBATCH CONNECT/RETAIN'
       DC   CL8'DLX801I'
         DC   CL1'J'           FLAG = 'J'
   DC   CL80'SE ''JJJJJJJJ HIPERBATCH CONNECT/RETAIN'',USER=(SY02),LOGO*
               N  '
* AVERTISSEMENT 'HIPERBATCH DISCONNECT'
       DC   CL8'DLX804I'
         DC   CL1'J'           FLAG = 'J'
   DC   CL80'SE ''JJJJJJJJ HIPERBATCH DISCONNECT'',USER=(SY02),LOGON '
* AVERTISSEMENT 'I/O ERREUR'
       DC   CL8'IOS000I'
         DC   CL1'J'           FLAG = 'J'
   DC   CL80'SE ''JJJJJJJJ  I/O ERROR - MESSAGE IOS000I ...'',USER=(SY0*
               2) '
* AVERTISSEMENT 'CHANNEL DETECTED ERROR'
       DC   CL8'IOS050I'
         DC   CL1'J'           FLAG = 'J'
   DC   CL80'SE ''JJJJJJJJ CHANNEL DETECTED ERROR (IOS050I)'',USER=(SY0*
               2,SY13) '
* AVERTISSEMENT ALERTE SERVICE CACHE 3990-3
       DC   CL8'IEA480E'
         DC   CL1'J'           FLAG = 'J'
   DC   CL80'SE ''JJJJJJJJ  IEA480E ALERTE DISQUE          '',USER=(SY0*
               2,SY13) '
* AVERTISSEMENT PINNED DATA
       DC   CL8'IEA478E'
         DC   CL1'J'           FLAG = 'J'
   DC   CL80'SE ''JJJJJJJJ  IEA478E PINNED DATA            '',USER=(SY0*
               2,SY13) '
* AVERTISSEMENT 'START PENDING'
       DC   CL8'IOS071I'
         DC   CL1'J'           FLAG = 'J'
   DC   CL80'SE ''JJJJJJJJ START PENDING (IOS071I)'',USER=(SY02) '
* AVERTISSEMENT CHPID ALERT
       DC   CL8'IOS162A'
         DC   CL1' '           FLAG = 'J'
   DC   CL80'SE ''CHPID ALERT, MALFUNCTION '',USER=(SY02,SY13,SY14) '
* AVERTISSEMENT 'START PENDING'   ARRETER TRACE HDS   PB SAS
*        DC   CL8'IOS071I'
*        DC   CL1' '
*        DC   CL80'P 66D'
* AVERTISSEMENT 'START PENDING'   ARRETER TRACE HDS   PB SAS
*        DC   CL8'IOS071I'
*        DC   CL1' '
*        DC   CL80'P GTF'
* AVERTISSEMENT ECRITURE SUR FICHIER NON EXPIRE
*      DC   CL8'IEC507D'
*        DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
*      DC   CL80'SE ''** FICHIER NON EXPIRE, REPONDRE U POUR L ECRASER,
*               M SINON '',BRDCST '
* AVERTISSEMENT ECRITURE SUR FICHIER NON EXPIRE
       DC   CL8'IEC507D'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''** FICHIER AVEC DATE D EXPIRATION, ECRITURE IMPOS*
               SIBLE **'',BRDCST '
* AVERTISSEMENT VTOCIX DISABLED                      TF 17-2-95
       DC   CL8'IEC608I'
         DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''JJJJJJJJ IEC608I VTOCIX DISABLED '',USER=(SY02,SY*
               14,SY13),LOGON          '
* INTERDICTION ECRITURE SUR FICHIER NON EXPIRE       TF 3 JUIN 92
       DC   CL8'IEC507D'
         DC   CL1'R'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'M'
* MESSAGE RACF POUR DEMANDE OU NON DE REVOKE D UN USER RACF 'SPECIAL'
       DC   CL8'ICH302D'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
    DC CL80'S MSG,M=''* ICH302D : CONTACTER LE RESPONSABLE RACF  AVANT X
               DE REPONDRE'' '
* MESSAGE RACF POUR SIGNALER DB PLEINE
       DC   CL8'ICH405I'
       DC   CL1'T'
       DC   CL80'SE ''TTTTTTTT ICH405I DB RACF PLEINE  '',USER=(SY02,SY*
               14,SY13,SY15)           '
* MESSAGE SIGNALANT QU'UN USER EST EN TRAIN D'ETRE REVOQUE
       DC   CL8'ICH70003I'
       DC   CL1'J'           FLAG = 'J'
    DC CL80'SE ''JJJJJJJJ  USER REVOQUE'',USER=(SY02) '
* MESSAGE DE RESOLVE SIGNALANT ABSENCE D'UN JOB
       DC   CL8'AMTCF8I'
       DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ AMTCF8I RESOLVE->ABSENCE...'',USER=(SY02*
               ,SY14,SY15) '
* MESSAGE IEE328I : RISQUE DE MAXUSERS ATTEINT...
       DC   CL8'IEE328I'
       DC   CL1' '           FLAG =
       DC   CL80'SE ''IEE328I CMD ABORTED / MAXUSERS ?'',USER=(SY02,SY1*
               3,SY15,SY14) '
* MESSAGE SIGNALANT PB PSF SUR UN PRINTER
       DC   CL8'APS532I'
       DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ RESSOURCE PSF MANQUANTE '',USER=(SY02,SY*
               14)           '
* MESSAGE SIGNALANT PB PSF SUR UN PRINTER
       DC   CL8'APS532I'
       DC   CL1'T'           FLAG = 'J'
       DC   CL80'SE ''TTTTTTTT FSS ARRETE PAR APSUX06  '',USER=(SY02,SY*
               14)           '
* MESSAGE SIGNALANT BUFFERS VTAM PLEINS
       DC   CL8'IST562I'
       DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ CSALIMIT REACHED **'',USER=(SY02,SY13,SY*
               15,SY14)      '
* MESSAGE SIGNALANT BUFFERS VTAM PLEINS
       DC   CL8'IST562I'
       DC   CL1' '           FLAG = 'J'
       DC   CL80'SET MPF=NO'
* MESSAGE VTAM INITIALIZATION COMPLETE
       DC   CL8'IST020I'
       DC   CL1' '            **** LANCEMENT PRODUITS RESEAU ****
       DC   CL80'S VNET'
* MESSAGE NETMASTER    TF  12-1-95
*      DC   CL8'N00503'
*      DC   CL1' '            **** LANCEMENT PRODUITS RESEAU ****
*      DC   CL80'S VNET'
* MESSAGE SIGNALANT L'ARRET EN COURS D'UN CICS
       DC   CL8'DFH1701'
       DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ    -> CICS BEING TERMINATED'',USER=(SY02*
               ,SY14,SY15) '
* MESSAGE SIGNALANT
       DC   CL8'ARC0909I'
       DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ    -> JOURNAL DFHSM PLEIN..'',USER=(SY02*
               ,SY14,SY15) '
* DFHSM SPACE MGMT
       DC   CL8'ARC0520I'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''TTTTTTTT  DEBUT HSM PRIMARY SPACE MANAGEMENT'',USER*
               =(SY02),LOGON '
* DFHSM SPACE MGMT
       DC   CL8'ARC0521I'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''TTTTTTTT  FIN   HSM PRIMARY SPACE MANAGEMENT'',USER*
               =(SY02),LOGON '
* DFHSM SPACE MGMT
       DC   CL8'ARC0517I'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''TTTTTTTT  DEBUT HSM SECOND. SPACE MANAGEMENT'',USER*
               =(SY02),LOGON '
* DFHSM SPACE MGMT
       DC   CL8'ARC0518I'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''TTTTTTTT  FIN   HSM SECOND. SPACE MANAGEMENT'',USER*
               =(SY02),LOGON '
* DFHSM AUTOBACKUP
       DC   CL8'ARC0720I'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''TTTTTTTT  DEBUT HSM AUTOMATIC BACKUP        '',USER*
               =(SY02),LOGON '
* DFHSM AUTOBACKUP
       DC   CL8'ARC0721I'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''TTTTTTTT  FIN   HSM AUTOMATIC BACKUP        '',USER*
               =(SY02),LOGON '
* MESSAGE SIGNALANT PB SERVEUR AS               TF  03-07-90
       DC   CL8'DAS1353E'
       DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ A EMIS LE MESSAGE DAS1353E'',USER=(SY02,*
               EC05,AS0005)'
* MESSAGE SIGNALANT PB SERVEUR AS               TF  03-07-90
       DC   CL8'DAS1352E'
       DC   CL1'J'           FLAG = 'J'
       DC   CL80'SE ''JJJJJJJJ A EMIS LE MESSAGE DAS1352E'',USER=(SY02,*
               EC05,AS0005)'
* LOG NOT ACTIVE
       DC   CL8'IEE037I'
         DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT  IEE037I LOG NOT ACTIVE  '',USER=(SY02*
               ,SY13,SY14,SY15),LOGON '
* CASSETTE PROTEGEE PAR RACF
       DC   CL8'IEC089I-'
         DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
         DC   CL80'SE ''TTTTTTTT IEC089I K7 PROTEGEE RACF '',USER=(SY02*
               ,SY14),LOGON '
* REPLY : WAIT REQUESTED, REPLY HOLD OR NO HOLD
       DC   CL8'IEF433D'
       DC   CL1'R'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'HOLD'      28-3-89
*
* REPLY : REPLY Y OR N TO REQUEST (RACF NON INITIALISE)
       DC   CL8'ICH802D'
       DC   CL1'R'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'Y'      16-8-90
*
* MSG SIGNALANT BUFFERS CONSOLES PLEINS A 80 %      01-10-1990
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'D C'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K M,MLIM=9999'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K S,DEL=R,L=1'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K S,DEL=R,L=2'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K S,DEL=R,L=3'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K S,DEL=R,L=4'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE '' IEA405E BUFFERS CONSOLE A 80%'',USER=(SY02,SY14,*
               SY15,SY13)'
*
* MSG SIGNALANT BUFFERS CONSOLES PLEINS A 100 %      01-10-1990
* NON EN PLACE, PREFERENCE DONNE A UN TRAITEMENT PAR NETVIEW
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K M,AMRF=N'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K Q,R=HC,L=1'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K Q,R=HC,L=2'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K Q,R=HC,L=3'
*
       DC   CL8'IEA405E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'K Q,R=HC,L=4'
*
       DC   CL8'IEA404A'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'V F02,CONSOLE,ROUT=NONE'
*
       DC   CL8'IEA404A'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'V F07,CONSOLE,ROUT=NONE'
*
       DC   CL8'IEA404A'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'V 005,CONSOLE,ROUT=NONE'
*
       DC   CL8'IEA404A'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'V 007,CONSOLE,ROUT=NONE'
*
       DC   CL8'IEA404A'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'V 9A1,CONSOLE,ROUT=NONE'
*
       DC   CL8'IEA404A'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'V 92C,CONSOLE,ROUT=NONE'
* PB IEBCOPY INCORRECT SUITE A IOS000I CMD    TF 21-9-93
       DC   CL8'IEB1022I'                    WSA SHORTAGE
       DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''JJJJJJJJ IEB1022I I/O ERROR IEBCOPY'',USER=(SY02,*
               SY14),LOGON'
*
* PB CMF (QUI PRODUIT ABSENCE DE STATS)       TF 21-9-93
       DC   CL8'CMFDIE05'                    WSA SHORTAGE
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'P MVSPAS'
*
       DC   CL8'CMFDIE05'                    WSA SHORTAGE
       DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''JJJJJJJJ WSA SHORTAGE,ARRET/RELANCE'',USER=(SY02,*
               SY14),LOGON'
*
       DC   CL8'CMFDIE05'                    WSA SHORTAGE
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'S MVSPAS'
*
* WARNING : INCORRECT RANGE OU BANDE NON SCRATCH TF  04-10-90
*
       DC   CL8'CAT9030I'
       DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''JJJJJJJJ  MESSAGE RANGE/NOT SCRATCH'',USER=(SY02,*
               SY13,KA01,SY14)'
*
* WARNING : RESOURCE MAY BE DAMAGED              TF  16-02-95
*
       DC   CL8'ISG032E'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'SE ''TTTTTTTT  ISG032E RESOURCE DAMAGED?'',USER=(SY02,*
               SY13,SY14),LOGON'
*
* REPLY : VERIFY DATE/TIME DE TLMS, QUAND TROP DE TEMPS DEPUIS DERNIER
* START (OU VMF REINITIALISE)  ; INTERCEPT A METTRE SEULEMENT SUR TEST
*                                                TF  08-08-94
*             MIS EGALEMENT EN PROD              TF  15-06-95
       DC   CL8'CAT9013P'
       DC   CL1'R'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'U'
*
* WARNING : TLMS NOT ACTIVE
*
       DC   CL8'CADO9097'
       DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''JJJJJJJJ  TLMS NOT ACTIVE'',USER=(SY02,SY14,SY13),L*
               OGON '
*
* WARNING : ESTAE ROUTINE ENTERED ON BEHALF OF
*
       DC   CL8'CADO098E'
       DC   CL1'T'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''TTTTTTTT  TLMS FAIT ABEND'',USER=(SY02,SY14,SY13),L*
               OGON '
*
* WARNING : QUEUE FULL
*
       DC   CL8'CADO9096'
       DC   CL1'J'           FLAG = 'R' SI CECI EST UN REPLY
     DC   CL80'SE ''JJJJJJJJ  TLMS QUEUE FULL'',USER=(SY02,SY14,SY13),L*
               OGON '
*
* WARNING : VMF DE TLMS BIENTOT PLEIN    TF  27-08-90
*
       DC   CL8'CAT2402E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
    DC CL80'S MSG,M=''LE VMF DE TLMS EST PRESQUE PLEIN, EFFECTUER REORGX
               ANISATION  '' '
*
* WARNING : VMF DE TLMS PLEIN    TF  27-08-90
*
       DC   CL8'CAT2401E'
       DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
    DC CL80'S MSG,M=''** VMF DE TLMS PLEIN ** APPELER SYSTEME OU FAIRE X
               UNE REORG  '' '
*
* SWITCH DE FICHIER SMF, VIDAGE A DECLENCHER
* UNIQUEMENT POUR MVSTEST          <==========  ATTENTION
*
       DC   CL8'IEE362A'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'S JOB,M=SMFDUMP'
*
* NO SPACE IN BRODCAST DATASET FOR MAIL
*
       DC   CL8'IKJ574I'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'S JOB,M=SYNCBROD'
*
*  FIN D'EXPIRATION DES VOLUMES DUMP DE DFHSM
*
       DC   CL8'ARC0629I'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'F DFHSM,HOLD DUMP(AUTO)'
       DC   CL8'ARC0629I'
         DC   CL1' '           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'F DFHSM,SETSYS ADSTART(0000 0000 0000)'
*
* NO DUMP DATASET AVAILABLE FOR DUMPID REPLY D TO DELETE THE DUMP
*
       DC   CL8'IEA793A'
         DC   CL1'R'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'D'
*
* DEMANDER DIRECTEMENT LE MOUNT, SANS CONFIRMER LA PRSENCE DES K7
*
       DC   CL8'ARC0314A'
         DC   CL1'R'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'Y'
       DC   CL8'ARC0366A'
         DC   CL1'R'           FLAG = 'R' SI CECI EST UN REPLY
       DC   CL80'Y'
*
* INTERCEPTER TEXTE DU MESSAGE
*
       DC   CL8'$HASP150'
         DC   CL1'M'           FLAG = 'M' POUR EN TIRER LE MESSAGE
       DC   CL14'S EXTRACT0,P='''
       DC   CL65'TEXTE DU MESSAGE'
       DC   CL1'''     '
*
       DC   CL8'$HASP170'
         DC   CL1'M'           FLAG = 'M' POUR EN TIRER LE MESSAGE
       DC   CL14'S EXTRACT0,P='''
       DC   CL65'TEXTE DU MESSAGE'
       DC   CL1'''     '
*====================================================================
FINTAB   DC   8X'FF'           ** INDICATEUR DE LA FIN DE LA TABLE **
*====================================================================
         LTORG
*
*  ZONE DE TRAVAIL TEMPORAIRE
*
DATAAREA DSECT
         DS   0F
SAVE     DS   18F
DOUBLEW  DS   D          DOUBLE MOT DE TRAVAIL
SYSTIME  DS   CL6        HHMMSS
         DS   CL3        COMPLEMENT DE SYSTIME
TIME     DS   CL8        09:30:42
USERAUTO DS   CL(CNCLMSG)
*DATALONG EQU  *-DATAAREA+MGCRSIZ     MGCRLTH MODIF TF 10-8-90 05/09/90
DATALONG EQU  *-DATAAREA+MGCRSIZB             MODIF TF 26-3-91
MGCR     IEZMGCR  DSECT=NO
*
MSGTEXT  DSECT
MSGID    DS   CL8                MESSAGE ID
JOBDATA  DS   CL8
         ORG  MSGTEXT
         DS   CL4
WTORID   DS   CL8
         IEZVX100
         END
