*
* LIST ALL JOBS (BASED ON SMF TYPE 30 RECORDS)
*
* //S1       EXEC PGM=SMFJOBS
* //STEPLIB  DD DISP=SHR,DSN=ETIC.LOAD
* //SYSUDUMP DD SYSOUT=*
* //SYSPRINT DD SYSOUT=*
* //SYSSMF   DD DISP=SHR,DSN=ETIC.DUMPSMF
*
        PRINT ON,GEN
        IFASMFR    30   RECORD TYPE  30
        PRINT ON,NOGEN
R0       EQU 0
R1       EQU 1
R2       EQU 2
R3       EQU 3
R4       EQU 4
R5       EQU 5
R6       EQU 6
R7       EQU 7
R8       EQU 8
R9       EQU 9
R10      EQU 10
R11      EQU 11
R12      EQU 12
R13      EQU 13
R14      EQU 14
R15      EQU 15
SMFJOBS  CSECT
         PRINT ON,NOGEN
         STM   R14,R12,12(R13)        SAVE CALLER'S REGISTERS
         LR    R12,R15                LOAD BASE REGISTER
         USING SMFJOBS,R12            ESTABLISH ADDRESSABILITY
         ST    R13,SAVE+4             BACK CHAIN
         LA    R15,SAVE               NEW SAVE AREA
         ST    R15,8(,R13)            FORWARD CHAIN
         LR    R13,R15                LOAD PROGRAM'S SAVE AREA ADDRESS
*
SUITEIN DS    0H
* OUVERTURE FICHIER SYSPRINT
        OPEN  (SYSPRINT,(OUTPUT))
        PUT   SYSPRINT,TITLE
* OUVERTURE FICHIER VBS  DES RECORDS SMF     *VBS
* MAIS D'ABORD ACQUISITION ZONE IO DE 40000 OCTETS
        L     R3,=F'200000'
        GETMAIN  R,LV=(3)
        LR    R5,R1        R5 POINTE SUR LA ZONE
        OPEN  (SYSSMF,(INPUT))               *VBS
* LECTURE DU FICHIER
SUITE1  DS    0H
        GET   SYSSMF,(5)                     *VBS
* ANALYSE DU RECORD
ANALREC AP    CTR,=PL4'1'
        USING SMFRCD30+4,R5                  *VBS
        CLI   SMF30RTY,X'1E'  RECORD DU TYPE VOULU ??
        BNE   SUITE1
        CLC   SMF30STP,=X'0005'   NE GARDER QUE LE SUBTYPE 5
        BNE   SUITE1
* END-TIME PRIS EGAL AU TIME RECORD WAS MOVED IN SMF BUFFER
*
        XTOD  SMF30TME,ZTME    TIME
* EXPLOITER IDENTIFICATION SECTION
        LR    R3,R5
        A     R3,SMF30IOF      AJOUTER OFFSET VERS LA SECTION
        S     R3,QUATRE
*       DROP  R3
        USING SMF30ID,R3
        MVC   ZJBN,SMF30JBN    MOVE JOBNAME
        MVC   ZJNM,SMF30JNM    MOVE JOB ID
        MVC   ZCLS,SMF30CLS    MOVE JOB CLASS
        MVC   ZUSR,SMF30USR    MOVE PROGRAMMER'S NAME
        XTOD  SMF30SIT,ZSIT    TIME SELECTED
        PRINTHX  SMF30STD,WORK8
        MVC   ZSTD,WORK8+2     DATE SELECTED
        MVC   ZRUD,SMF30RUD    MOVE RACF USER
* EXPLOITER I/O ACTIVITY   SECTION
*
        LR    R3,R5
        A     R3,SMF30UOF      AJOUTER OFFSET VERS LA SECTION
        S     R3,QUATRE
        DROP  R3
        USING SMF30URA,R3
        CONVERT FROMB=SMF30TEP,TOZ=WORK10
        MVC   ZTEP,WORK10+2    MOVE EXCP COUNT
* EXPLOITER COMPLETION     SECTION
        LR    R3,R5
        A     R3,SMF30TOF      AJOUTER OFFSET VERS LA SECTION
        S     R3,QUATRE
        DROP  R3
        USING SMF30CMP,R3
        PRINTHX   SMF30SCC,ZSCC
* EXPLOITER PROCESSOR ACCT SECTION
        LR    R3,R5
        A     R3,SMF30COF      AJOUTER OFFSET VERS LA SECTION
        S     R3,QUATRE
        DROP  R3
        USING SMF30CAS,R3
        CONVERT FROMB=SMF30CPT,TOZ=WORK10
        MVC   ZCPT,WORK10+2
        CONVERT FROMB=SMF30CPS,TOZ=WORK10
        MVC   ZCPS,WORK10+2
* ECRITURE DU RECORD
WRITEEN       DS  0H
        PUT   SYSPRINT,ZONEOUT
        MVI   ZONEOUT,C' '
        MVC   ZONEOUT+1(L'ZONEOUT-1),ZONEOUT
        B     SUITE1
*
*
RETOUR    DS   0H
        CLOSE (SYSSMF)
        CLOSE (SYSPRINT)
        L       R13,4(R13)
        RETURN  (14,12),T,RC=0
*
*
SAVE      DS   18F
SYSSMF    DCB DDNAME=SYSSMF,MACRF=GD,RECFM=VBS,DSORG=PS,EODAD=RETOUR
SYSPRINT  DCB  DDNAME=SYSPRINT,RECFM=FBA,MACRF=PM,LRECL=133,           *
               BLKSIZE=1330,DSORG=PS
ZONEOUT  DS   0CL133
*        DC   CL133' '
         DC   C' '     CODE SAUT
ZJBN     DC   CL8' '   JOBNAME
         DC   C' '
ZJNM     DC   CL8' '   JES JOB ID
         DC   C' '
ZCLS     DC   CL1' '   JES JOB CLASS
         DC   C' '
ZSTD     DC   CL5' '   DATE INITIATOR SELECTED THIS JOB
         DC   C' '
ZSIT     DC   CL5' '   TIME INITIATOR SELECTED THIS JOB
         DC   C' '
ZTME     DC   CL5' '   TIME RECORD MOVED IN BUFFER
         DC   C' '
ZUSR     DC   CL20' '  PROGRAMMER'S NAME
         DC   C' '
ZRUD     DC   CL8' '   RACF USER ID
         DC   C' '
ZTEP     DC   CL10' '  EXCP COUNT
         DC   C' '
ZCPT     DC   CL10' '  STEP CPU TIME TCB
         DC   C' '
ZCPS     DC   CL10' '  STEP CPU TIME SRB
         DC   C' '
ZSCC     DC   CL4' '   STEP COMPLETION CODE
         DC   CL(ZONEOUT+L'ZONEOUT-*)' '
*
*
TITLE    DS   0CL133
*        DC   CL133' '
         DC   C' '     CODE SAUT
         DC   CL8'JOBNAME'
         DC   C' '
         DC   CL8'JOBID'
         DC   C' '
         DC   CL1'C'   JES JOB CLASS
         DC   C' '
         DC   CL5'DATE' DATE INITIATOR SELECTED THIS JOB
         DC   C' '
         DC   CL5'STIME'   TIME INITIATOR SELECTED THIS JOB
         DC   C' '
         DC   CL5'ETIME'   TIME SMF RECORD MOVED INTO BUFFER
         DC   C' '
         DC   CL20' PROGRAMMER''S NAME'
         DC   C' '
         DC   CL8'USER' RACF USER ID
         DC   C' '
         DC   CL10'EXCP '  EXCP COUNT
         DC   C' '
         DC   CL10'CPU TCB'  STEP CPU TIME TCB CENTIEMES DE SECONDES
         DC   C' '
         DC   CL10'CPU SRB'  STEP CPU TIME SRB
         DC   C' '
         DC   CL4'CC'   STEP COMPLETION CODE
         DC   CL(TITLE+L'TITLE-*)' '
*
WORK6    DS  0CL6
WORK4    DS  0CL4
WORK5    DS  0CL5
WORK8    DS  0CL8
WORK10   DS  CL10
DEMIMOT  DC   H'0'
CTR      DC   PL4'0'
QUATRE   DC   F'4'
         LTORG
        END
