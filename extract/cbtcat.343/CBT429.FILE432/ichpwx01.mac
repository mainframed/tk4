*
* EXIT RACF DE CONTROLE DU NEW PASSWORD             - REENTRANT, LPA
* (INTERVIENT LORS DE TOUT CHANGEMENT DE PASSWORD)
*
* POUR SA PRISE EN COMPTE PAR LE SYSTEME, 2 POSSIBILITES :
*   * COPIE EN SYS1.LPALIB ET IPL CLPA
*   * CHARGEMENT DU MODULE EN CSA (PAR RESOLVE, COMMANDE "LOADLPA,ADD")
*     ET ZAPPER EN MEMOIRE EN RCVT + X'EC' POUR METTRE SON ADRESSE
*      NB : L'ADRESSE DE LA RCVT EST EN CVT+X'3E0', LE CHAINAGE EST :
*         - CVT+3E0 DONNE : CVTRAC (ADRESSE RCVT)
*         - RCVT+EC DONNE : ADRESSE DE CHARGEMENT DE ICHPWX01
*     METTRE LE MOT EN RCVT+EC A ZERO REVIENT A DESACTIVER L'EXIT
*
* LES WTOS PERMETTENT D'EXAMINER LA LOG MVS POUR VOIR LA CAUSE DE REJET
*
         ICHPWXP    ,      ZONE PARAMETRE DE L'EXIT
*        IHAACEE    ,      ACEE
R0       EQU 0
R1       EQU 1
R2       EQU 2          TRAVAIL
R3       EQU 3          ZONE USERID
R4       EQU 4          ZONE NOUVEAU MOT DE PASSE
R5       EQU 5          LONGUEUR USERID
R6       EQU 6          LONGUEUR NOUVEAU MOT DE PASSE
R7       EQU 7
R8       EQU 8          TRAVAIL
R9       EQU 9          PARAMETRES EN ENTREE DE L'EXIT
R10      EQU 10         ZONE DE TRAVAIL
R11      EQU 11
R12      EQU 12         REGISTRE DE BASE
R13      EQU 13         ** NE PAS LE MODIFIER **
R14      EQU 14
R15      EQU 15
ICHPWX01 CSECT
ICHPWX01 AMODE  31
ICHPWX01 RMODE  ANY
         SAVE   (14,12),,ICHPWX01.ETIC.&SYSDATE..&SYSTIME
         LR     R12,R15          R12 EST LE REGISTRE DE BASE
         USING  ICHPWX01,R12
* POUR DESACTIVER L'EXIT : CHANGER LE "NOP" QUI SUIT EN "B"
         NOP    QUICKEND
         LR     R9,R1            POINTONS SUR LE PARAMETRE
         USING  PWXPL,R9
*
* LE TYPE D'APPEL 'ALTUSER' N'EST PAS CONTROLE
* (LE GESTIONNAIRE RACF PEUT IMPOSER UN NEW PWD TEMPORAIRE QUELCONQUE)
*
         L    R1,PWXCALLR      CALLER ADDRESS
         LTR  R1,R1            RENSEIGNE ?
         BZ   NOCALLR
         CLI  0(R1),PWXALTUS   ALTUSER COMMAND ?
         BE   RET0             OUI, NO CONTROL
*
NOCALLR  DS   0H
         L    R3,PWXUSRID      ADRESSE ZONE USERID
         L    R4,PWXNEWPW      ADRESSE NEW PASSWORD
         LTR  R4,R4
         BZ   RET0             PAS DE NEW PASSWORD, PAS DE CONTROLE
         LTR  R3,R3
         BZ   RET0             PAS DE USER, ANORMAL, MAIS TANT PIS
         XR   R5,R5
         IC   R5,0(R3)         LONGUEUR DU USERID
         XR   R6,R6
         IC   R6,0(R4)         LONGUEUR DU NOUVEAU PASSWORD
         LTR  R6,R6
         BZ   RET0             PAS DE NEW PASSWORD, PAS DE CONTROLE
* OBTENTION ZONE DE TRAVAIL
         LA   R2,WORKLONG
         GETMAIN  R,LV=(2)
         LR   R10,R1           SAUVER ADRESSE DE LA ZONE OBTENUE
         LTR  R15,R15
         BNZ  RET0
         USING WORKZONE,R10
         ST   R5,LONGUSER      LONGUEUR ZONE USER
         ST   R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         LA   R3,1(0,R3)       ACCES A ZONE USER (ON SAUTE LA LONGUEUR)
         ST   R3,ADDUSER       ADRESSE ZONE USER
         LA   R4,1(0,R4)       ACCES A ZONE PSWD (ON SAUTE LA LONGUEUR)
         ST   R4,ADDPASS       ADRESSE ZONE PSWD
*
*---------------------------------------------------------------------*
* CONTROLE 0 : LE NEW PASSWORD NE DOIT PAS CONTENIR 'ETIC'
*---------------------------------------------------------------------*
*
CONTROL0 DS   0H
         L    R4,ADDPASS       ADRESSE ZONE PSWD
         L    R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         C    R6,=F'4'         SI PAS >= 4
         BL   ENDCTRL0         PAS DE CONTROLE
         S    R6,=F'3'         - 4
LOOPCTR0 CLC  0(4,R4),=CL4'ETIC'        CONTIENT "ETIC"
         BE   CHECK0
         LA   R4,1(0,R4)
         LTR  R6,R6            = 0 ?
         BZ   ENDCTRL0         CONTROLE FINI
         BCT  R6,LOOPCTR0
         B    ENDCTRL0         CONTROLE FINI
CHECK0   DS   0H
         WTO  'ICHPWX01Y- PASSWORD INCORRECT, NE DOIT PAS CONTENIR LA CX
               HAINE DE CARACTERES ETIC',ROUTCDE=11
         B    REJECT           NEW PASSWORD INVALIDE
ENDCTRL0 DS   0H
*
*---------------------------------------------------------------------*
* CONTROLE 1 : LE NEW PASSWORD NE DOIT PAS CONTENIR PLUS DE 3C
*            CONSECUTIFS DU USERID
*---------------------------------------------------------------------*
*
CONTROL1 DS   0H
* POUR DESACTIVER LE CONTROLE : CHANGER LE "NOP" QUI SUIT EN "B"
         NOP  CONTROL2
*
*             COPIER USERID ET PASSWORD EN ZONE DE TRAVAIL
*
         L    R3,ADDUSER       ADRESSE ZONE USER
         L    R4,ADDPASS       ADRESSE ZONE PSWD
         L    R5,LONGUSER      LONGUEUR ZONE USER
         L    R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         MVI  USERID,X'00'     INIT DE LA ZONE USERID DE TRAVAIL
         MVC  USERID+1(7),USERID
         MVI  PASSWORD,X'FF'   INIT DE LA ZONE NEW PASSWORD DE TRAVAIL
         MVC  PASSWORD+1(7),PASSWORD
         BCTR R5,0             LONGUEUR USERID - 1 POUR EXECUTE
         BCTR R6,0             LONGUEUR PASSWORD - 1 POUR EXECUTE
         EX   R5,MOVUSER       COPIER ZONE USERID
         EX   R6,MOVPASS       COPIER ZONE PASSWORD
*
*             BOUCLE DE COMPARAISON DES CARACTERES 3 @ 3
*
         CLC  LONGPASS,=F'2'   CONTROLE IMPOSSIBLE SI LE PASSWORD ...
         BNH  CONTROL2         ... A MOINS DE 3 CARACTERES
         LA   R3,USERID        ADRESSE DU USERID EN WORK
         LA   R2,6             6 COMPARAISONS SUR LE USERID
LOOP1A   LA   R4,PASSWORD      ADRESSE DU PASSWORD EN WORK
         LA   R1,6             6 COMPARAISONS SUR LE PASSWORD
LOOP1B   CLC  0(3,R3),0(R4)    COMPARE 3 C. USER AVEC 3 C. PASSWORD
         BE   CHECK1           EGALITE, REFUS
         LA   R4,1(0,R4)       CARACTERES SUIVANTS DU PASSWORD
         BCT  R1,LOOP1B
         LA   R3,1(0,R3)       CARACTERES SUIVANTS DU USERID
         BCT  R2,LOOP1A
         B    CONTROL2         CONTROLE PASSE AVEC SUCCES
CHECK1   DS   0H
         WTO  'ICHPWX01A- PASSWORD INCORRECT, NE DOIT PAS COMPORTER PLUX
               S DE 3 LETTRES CONSECUTIVES DU USERID',ROUTCDE=11
         B    REJECT           NEW PASSWORD INVALIDE
MOVUSER  MVC  USERID(1),0(R3)    COPIER ZONE USERID
MOVPASS  MVC  PASSWORD(1),0(R4)  COPIER ZONE PASSWORD
*
*---------------------------------------------------------------------*
* CONTROLE 2 : LE NEW PASSWORD NE DOIT PAS CONTENIR LES 3 PREMIERES
*            LETTRES D'UN MOIS DE L'ANNEE (JAN, FEV, ETC)
*---------------------------------------------------------------------*
*
CONTROL2 DS   0H
* POUR DESACTIVER LE CONTROLE : CHANGER LE "NOP" QUI SUIT EN "B"
         NOP  CONTROL3
         CLC  LONGPASS,=F'2'   CONTROLE IMPOSSIBLE SI LE PASSWORD ...
         BNH  CONTROL3         ... A MOINS DE 3 CARACTERES
         LA   R1,MOISFRAN      LIBELLES MOIS EN FRANCAIS
         LA   R2,12            12 COMPARAISONS
LOOP2A   L    R4,ADDPASS       ADRESSE ZONE PSWD
         L    R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         S    R6,=F'2'         ENLEVER 2
LOOP2B   CLC  0(3,R1),0(R4)    COMPARER LIBELLE MOIS AVEC PASSWORD
         BE   CHECK2
         LA   R4,1(0,R4)       CARACTERE SUIVANT PASSWORD
         BCT  R6,LOOP2B        AVANCER DANS LE PASSWORD
         LA   R1,3(0,R1)       MOIS SUIVANT
         BCT  R2,LOOP2A        AVANCER DANS LES MOIS
* MEME CONTROLE AVEC LES MOIS EN ANGLAIS
         LA   R1,MOISANGL      LIBELLES MOIS EN ANGLAIS
         LA   R2,12            12 COMPARAISONS
LOOP2C   L    R4,ADDPASS       ADRESSE ZONE PSWD
         L    R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         S    R6,=F'2'         ENLEVER 2
LOOP2D   CLC  0(3,R1),0(R4)    COMPARER LIBELLE MOIS AVEC PASSWORD
         BE   CHECK2
         LA   R4,1(0,R4)       CARACTERE SUIVANT PASSWORD
         BCT  R6,LOOP2D        AVANCER DANS LE PASSWORD
         LA   R1,3(0,R1)       MOIS SUIVANT
         BCT  R2,LOOP2C        AVANCER DANS LES MOIS
         B    CONTROL3         CONTROLE PASSE AVEC SUCCES
CHECK2   DS   0H
         WTO  'ICHPWX01B- PASSWORD INCORRECT, NE DOIT PAS COMPORTER LE X
               DEBUT D''UN NOM DE MOIS',ROUTCDE=11
         B    REJECT           NEW PASSWORD INVALIDE
*
*---------------------------------------------------------------------*
* CONTROLE 3 : LE NEW PASSWORD NE DOIT PAS CONTENIR LE NUMERO DU MOIS
*            EN COURS (01, 02, ETC)
*---------------------------------------------------------------------*
*
* OBTENIR LE NUMERO DU MOIS EN COURS
*
CONTROL3 DS   0H
* POUR DESACTIVER LE CONTROLE : CHANGER LE "NOP" QUI SUIT EN "B"
         NOP  CONTROL4
         CLC  LONGPASS,=F'1'   CONTROLE IMPOSSIBLE SI LE PASSWORD ...
         BNH  CONTROL4         ... A MOINS DE 2 CARACTERES
         MVC  MOISTB,MOISREF    TABLE DES NOMBRES DE JOUR/MOIS (WORK)
         LA   R1,2              TIME SOUS FORME DECIMALE
         SVC  11                TIME SVC
         XC   DATE1,DATE1       RAZ AVANT CVB
         ST   R1,DATE           DATE DU JOUR SOUS FORME AAQQQ
         CVB  R1,DATE1
         XR   R0,R0             R0 = 0 POUR LA DIVISION
         D    R0,=F'1000'       CALCULER L'ANNEE AA
         ST   R1,ANNEE          ANNEE AA
         ST   R0,QUANTIEM       QUANTIEME QQQ
         SLL  R1,2              DIVISER PAR 4 POUR VOIR SI BISSEXTILE
         SRL  R1,2              MULTIPLIER PAR 4
         C    R1,ANNEE          COMPARER, SI EGALITE, BISSEXTILE
         BNE  NONBISSX          ANNEE NON BISSEXTILE
         MVC  MOISTB+2(2),=H'29'    BISSEXTILE, FEVRIER A 29 JOURS
NONBISSX DS   0H
         LA   R1,1              1ER MOIS = JOANVIER (01)
         LA   R2,MOISTB         TABLE DES MOIS
         LA   R3,12             12 MOIS MAXIMUM
         L    R8,QUANTIEM       QQQ (DE 1 A 365/366)
LOOP3A   SH   R8,0(R2)          QQQ - NOMBRE DE JOURS DU MOIS
         BNP  FINMOIS           SI <=0 C'EST FINI
         LA   R1,1(0,R1)        + 1 SUR NOTRE NUMERO DE MOIS
         LA   R2,2(0,R2)        NOMBRE DE JOURS DU MOIS SUIVANT
         BCT  R3,LOOP3A
FINMOIS  ST   R1,MOIS           ** NUMERO DU MOIS COURANT (1 @ 12) **
         CVD  R1,MOISPACK       CONVERSION EN PACKE
         UNPK NUMMOIS,MOISPACK+6(2)    CONVERSION SOUS FORME MM
         OI   NUMMOIS+1,X'F0'   NORMALISER LE SIGNE
*
* FAIRE LE CONTROLE
*
         L    R4,ADDPASS       ADRESSE ZONE PSWD
         L    R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         S    R6,=F'1'         ENLEVER 1
LOOP3B   CLC  NUMMOIS,0(R4)    COMPARER NUMERO MOIS AVEC PASSWORD
         BE   CHECK3
         LA   R4,1(0,R4)       CARACTERE SUIVANT PASSWORD
         BCT  R6,LOOP3B        AVANCER DANS LE PASSWORD
         B    CONTROL4         CONTROLE PASSE AVEC SUCCES
CHECK3   DS   0H
         WTO  'ICHPWX01C- PASSWORD INCORRECT, NE DOIT PAS COMPORTER LE X
               NUMERO DU MOIS COURANT',ROUTCDE=11
         B    REJECT           NEW PASSWORD INVALIDE
*
*---------------------------------------------------------------------*
* CONTROLE 4 : LE NEW PASSWORD NE DOIT PAS CONTENIR PLUS DE 3 FOIS
*            DE SUITE LE MEME CARACTERE
*---------------------------------------------------------------------*
*
CONTROL4 DS   0H
* POUR DESACTIVER LE CONTROLE : CHANGER LE "NOP" QUI SUIT EN "B"
         NOP  CONTROL5
         CLC  LONGPASS,=F'2'   CONTROLE IMPOSSIBLE SI LE PASSWORD ...
         BNH  CONTROL5         ... A MOINS DE 3 CARACTERES
         L    R4,ADDPASS       ADRESSE ZONE PSWD
         L    R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         S    R6,=F'2'         ENLEVER 2
LOOP4    CLC  0(1,R4),1(R4)    COMPARER CARACTERE AVEC LE SUIVANT
         BNE  NEXT4
         CLC  1(1,R4),2(R4)    COMPARER LE SUIVANT AVEC SON SUIVANT
         BE   CHECK4
NEXT4    LA   R4,1(0,R4)       CARACTERE SUIVANT PASSWORD
         BCT  R6,LOOP4         AVANCER DANS LE PASSWORD
         B    CONTROL5         CONTROLE PASSE AVEC SUCCES
CHECK4   DS   0H
         WTO  'ICHPWX01D- PASSWORD INCORRECT, NE DOIT PAS COMPORTER PLUX
               S DE 3 CARACTERES IDENTIQUES CONSECUTIFS',ROUTCDE=11
         B    REJECT           NEW PASSWORD INVALIDE
*
*---------------------------------------------------------------------*
* CONTROLE 5 : LE NEW PASSWORD NE DOIT PAS CONTENIR PLUS DE 2 CARACT.
*            CONSECUTIFS D'UNE TABLE DE CARACTERES ETIC (SITUES A
*            PROXIMITE LES UNS DES AUTRES SUR LE CLAVIER)
*---------------------------------------------------------------------*
*
CONTROL5 DS   0H
* POUR DESACTIVER LE CONTROLE : CHANGER LE "NOP" QUI SUIT EN "B"
         NOP  CONTROL6
         CLC  LONGPASS,=F'2'   CONTROLE IMPOSSIBLE SI LE PASSWORD ...
         BNH  CONTROL6         ... A MOINS DE 3 CARACTERES
         L    R4,ADDPASS       ADRESSE ZONE PSWD
         L    R6,LONGPASS      LONGUEUR ZONE NEW PASSWORD
         S    R6,=F'2'         ENLEVER 2
LOOP5A   LA   R8,TBETIC        TABLE SPECIALE
         LA   R2,LTBETIC       LONGUEUR DE LA TABLE SPECIALE
         S    R2,=F'2'         ENLEVER 2
LOOP5B   CLC  0(1,R4),0(R8)    COMPARER CARACTERE AVEC CARACT TABLE
         BNE  NEXT5B
         CLC  1(1,R4),1(R8)    COMPARER CARACT. SUIVANT AVEC SUIVANT TB
         BNE  NEXT5B
         CLC  2(1,R4),2(R8)    COMPARER CARACT. SUIVANT AVEC SUIVANT TB
         BE   CHECK5
NEXT5B   LA   R8,1(0,R8)       CARACTERE SUIVANT EN TABLE SPECIALE
         BCT  R2,LOOP5B
         LA   R4,1(0,R4)       CARACTERE SUIVANT PASSWORD
         BCT  R6,LOOP5A        AVANCER DANS LE PASSWORD
         B    CONTROL6         CONTROLE PASSE AVEC SUCCES
CHECK5   DS   0H
         WTO  'ICHPWX01E- PASSWORD INCORRECT, NE DOIT PAS COMPORTER 3 CX
               ARACTERES PROCHES LES UNS DES AUTRES SUR LE CLAVIER',   X
               ROUTCDE=11
         B    REJECT           NEW PASSWORD INVALIDE
*
*---------------------------------------------------------------------*
* CONTROLE 6 : LE NEW PASSWORD NE DOIT PAS CONTENIR PLUS DE 3 CARACT.
*            CONSECUTIFS DU MOT DE PASSE PRECEDENT
*---------------------------------------------------------------------*
*
CONTROL6 DS   0H
* POUR DESACTIVER LE CONTROLE : CHANGER LE "NOP" QUI SUIT EN "B"
         NOP  CONTROL7
         L    R4,PWXCURPW      ADRESSE CURRENT PASSWORD
         LTR  R4,R4
         BZ   CONTROL7         PAS DE PASSWORD, PAS DE CONTROLE
         XR   R6,R6
         IC   R6,0(R4)         LONGUEUR DU CURRENT PASSWORD
         LTR  R6,R6
         BZ   CONTROL7         PAS DE PASSWORD, PAS DE CONTROLE
         LA   R4,1(0,R4)       SAUTER LA LONGUEUR
         ST   R4,ADDPASSC      ADRESSE ZONE PSWD CURRENT
         ST   R6,LONGPASC      LONGUEUR ZONE PASSWORD
*
         CLC  LONGPASC,=F'3'   CONTROLE IMPOSSIBLE SI LE PASSWORD ...
         BNH  CONTROL7         ... A MOINS DE 4 CARACTERES
         CLC  LONGPASS,=F'3'   CONTROLE IMPOSSIBLE SI LE PASSWORD ...
         BNH  CONTROL7         ... A MOINS DE 4 CARACTERES
         MVI  PASSCURR,X'00'   INIT DE LA ZONE PASSCURR DE TRAVAIL
         MVC  PASSCURR+1(7),PASSCURR
         BCTR R6,0             LONGUEUR PASSWORD - 1 POUR EXECUTE
         EX   R6,MOVPASSC      COPIER ZONE PASSWORD
         LA   R3,PASSCURR      ADRESSE DU CURRENT PASSWORD EN WORK
         LA   R2,5             5 COMPARAISONS SUR LE PASSCURR
LOOP6A   LA   R4,PASSWORD      ADRESSE DU NEW PASSWORD EN WORK
         LA   R1,5             5 COMPARAISONS SUR LE PASSWORD
LOOP6B   CLC  0(4,R3),0(R4)    COMPARE 4 C. PASSWORD AVEC 4 C. PASSWORD
         BE   CHECK6           EGALITE, REFUS
         LA   R4,1(0,R4)       CARACTERES SUIVANTS DU PASSWORD NOUVEAU
         BCT  R1,LOOP6B
         LA   R3,1(0,R3)       CARACTERES SUIVANTS DU PASSWORD COURANT
         BCT  R2,LOOP6A
         B    CONTROL7         CONTROLE PASSE AVEC SUCCES
CHECK6   DS   0H
         WTO  'ICHPWX01F- PASSWORD INCORRECT, NE DOIT PAS COMPORTER PLUX
               S DE 3 CARACTERES CONSECUTIFS DU PASSWORD PRECEDENT',   X
               ROUTCDE=11
         B    REJECT           NEW PASSWORD INVALIDE
MOVPASSC MVC  PASSCURR(1),0(R4)  COPIER ZONE PASSWORD
*
CONTROL7 DS   0H
         B    ACCEPT
*
* REFUS
*
REJECT   DS   0H
         LA   R2,WORKLONG
         FREEMAIN  R,LV=(2),A=(10)
         LA   R15,4             ** REFUS DU NEW PASSWORD **
         B    FIN
*
* ACCEPTATION
*
ACCEPT   DS   0H
         LA   R2,WORKLONG
         FREEMAIN  R,LV=(2),A=(10)
RET0     DS   0H
         WTO  'ICHPWX01Z- MODIFICATION DU MOT DE PASSE',ROUTCDE=11
*
* FIN
*
QUICKEND XR   R15,R15
FIN      ST   R15,16(R13)      METTRE CODE RETOUR DANS SAVE AREA
         LM   R14,R12,12(R13)
         BR   R14              RETOUR A L'APPELANT
*
* DONNEES FIXES
*
MOISFRAN DC   C'JANFEVMARAVRMAIJUIJUIAOUSEPOCTNOVDEC'
MOISANGL DC   C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'
MOISREF  DC   H'31',H'28',H'31',H'30',H'31',H'30'
         DC   H'31',H'31',H'30',H'31',H'30',H'31'
TBETIC   DC   C'1234567890AZERTYUIOPQSDFGHJKLMWXCVBN'
         DC   C'1AQW2ZSX3EDC4RFV5TGB6YHN7UJ8IK9OL0PM'
LTBETIC  EQU  *-TBETIC
         LTORG
*
* ZONE DE TRAVAIL
*
WORKZONE DSECT
MOISPACK DS   D                MOIS EN COURS (PACKE)
DATE1    DS   0D
         DS   F
DATE     DS   F                DATE DU JOUR AAQQQ
MOISTB   DS   0CL24
         DS   12H              TABLE DES NOMBRES DE JOURS PAR MOIS
LONGUSER DS   F                LONGUEUR EN C. DE LA ZONE USERID
LONGPASS DS   F                LONGUEUR EN C. DE LA ZONE NEW PASSWORD
LONGPASC DS   F                LONGUEUR EN C. DE LA ZONE CUR PASSWORD
ADDUSER  DS   F                ADRESSE DE LA ZONE USERID
ADDPASS  DS   F                ADRESSE DE LA ZONE NEW PASSWORD
ADDPASSC DS   F                ADRESSE DE LA ZONE CURRENT PASSWORD
USERID   DS   CL8              USERID
PASSWORD DS   CL8              ZONE NEW PASSWORD
PASSCURR DS   CL8              ZONE CURRENT PASSWORD
ANNEE    DS   F                ANNEE AA
QUANTIEM DS   F                QUANTIEME QQQ
MOIS     DS   F                MOIS EN COURS (BINAIRE)
NUMMOIS  DS   CL2              MOIS EN COURS (NUM DISPLAY)
WORKLONG EQU  *-WORKZONE
         END
