         MACRO
.*
&NLA     WTOPUT &DESC=
&NLA     DS    0H
.*   DISPLAY ON CONSOLE
         LCLA  &NBR             NOMBRE D'OPERANDES
         LCLA  &N               INDEX OPERANDES
         LCLA  &LEN             LONGUEUR ZONE
         LCLA  &A               ADRESS FOR MVC
         LCLA  &TEMP
         LCLC  &TEMPA
         LCLC  &NAMZN
&NAMZN   SETC  'WPPLCZNE'       ZONE ECRITURE MESSAGE
         AIF   (T'&NAMZN NE 'U').DEFZN
         B     TPTZ&SYSNDX
&NAMZN   DC    CL120' '
TPTZ&SYSNDX  DS  0H
.DEFZN   ANOP
&NBR     SETA  N'&SYSLIST
.PROC    ANOP
         MVI   &NAMZN,C' '          RESET ZONE TO SPACES
         MVC   &NAMZN+1(L'&NAMZN-1),&NAMZN    RESET ZONE TO SPACES
&N       SETA  0
         AIF   (&NBR EQ 0).FIN   MOVE A BLANC SI PAS DE PARAMETRES
.LOOP    ANOP                   BOUCLE DE TRAITEMENT 1 PARAMETRE
&N       SETA  &N+1
         AIF   (&N GT &NBR).FIN
&TEMPA   SETC  '&SYSLIST(&N))'(1,1)
         AIF   ('&TEMPA' EQ '''').QUOTE
&TEMPA   SETC  '&SYSLIST(&N)'
.* 31-10-95 : SORTIR SI PARAMETRE VIDE                      31-10-95
         AIF   ('&TEMPA' EQ '').FIN
.*
.*   MOVE AVEC ZONE FROM SYMBOLIQUE
.*
&LEN     SETA   L'&SYSLIST(&N)
         AIF   (&LEN GT 255).ERR3
&TEMP    SETA  &A+&LEN
         AIF   (&TEMP GT 120).ERR4
         MVC   &NAMZN+&A.(&LEN),&SYSLIST(&N)
&A       SETA  &TEMP
         AGO   .LOOP
.*
.*   MOVE AVEC ZONE FROM LITTERALE ENTRE APOSTROPHES
.*
.QUOTE   ANOP                      EXPRESSION ENTRE APOSTROPHES
&TEMPA   SETC  '&SYSLIST(&N))'(K'&SYSLIST(&N),1)
         AIF   ('&TEMPA' NE '''').ERR2
&TEMPA   SETC '=C'.'&SYSLIST(&N)'
&LEN     SETA  K'&SYSLIST(&N)-2
&TEMP    SETA  &A+&LEN
         AIF   (&TEMP GT 120).ERR4
         MVC   &NAMZN+&A.(&LEN),&TEMPA
&A       SETA  &TEMP
         AGO   .LOOP
.*
.ERR2    ANOP
         MNOTE 8,' ** END QUOTE MISSING'
         MEXIT
.ERR3    ANOP
         MNOTE 8,' ** &SYSLIST(&N) IS TOO LONG, CANNOT BE USED'
         MEXIT
.ERR4    ANOP
         MNOTE 4,' ** EXPRESSION EXCEEDS 120 BYTES, WILL BE TRONCATED'
         MEXIT
.FIN     ANOP
         MVC  WTOY&SYSNDX+8(L'&NAMZN),&NAMZN
WTOY&SYSNDX WTO '                                                      *
                                                                       *
                         ',DESC=&DESC       ,ROUTCDE=11
         MEXIT
.EXIT    ANOP
         MEND
