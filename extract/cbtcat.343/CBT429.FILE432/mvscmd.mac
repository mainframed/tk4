* AN AGELESS BOX-OFFICE SUCCESS, USED TO ISSUE ANY MVS OR JES2 COMMAND
*(JUST TRY TO ISSUE THE $TA COMMAND BELOW IN BATCH BY ANOTHER MEANS...)
*
* //S0 EXEC PGM=MVSCMD,PARM='D T'
* //S1 EXEC PGM=MVSCMD
* //SYSIN DD *
* $TA,I=200,T=12.05,'$VS,''S MAINT'''
*
*
MVSCMD   CSECT
         PROLOG BASEREG=11,REQUATE=YES,AUTHOR=TF
         L    R10,0(R1)
         USING PARAM,R10
         CLC  LL,=H'0'
         BNE  PARMYES
* PAS DE PARAMETRES, OUVERTURE SYSIN
         OPEN (SYSIN,(INPUT))
LOOPGET  GET  SYSIN,CMD
EMETTRE  MODESET MODE=SUP,KEY=ZERO
         LA   R1,CMDLEN
         XR   R0,R0
         SVC  34
         MODESET MODE=PROB,KEY=NZERO
         B    LOOPGET
ENDSYS   CLOSE (SYSIN)
         B    FIN
PARMYES  DS   0H       PARM=  SPECIFIE
         MVI  CMD,C' '
         MVC  CMD+1(L'CMD-1),CMD      REMISE A BLANC ZONE CMD
         XR   R3,R3
         LH   R3,LL
         BCTR R3,0   MOINS 1 POUR MVC
         EX   R3,MOVE
* CHANGER LES X'00' EN BLANCS
*
         LA   R5,CMD
         LA   R6,CMD
         LA   R6,L'CMD(0,R6)    ADRESSE DE FIN
LOOP     CLI  0(R5),X'00'
         BNE  SUIT
         MVI  0(R5),C' '
SUIT     LA   R5,1(0,R5)
         CR   R5,R6
         BNL  FINI
         B    LOOP
FINI     DS   0H
         MODESET MODE=SUP,KEY=ZERO
         LA   R1,CMDLEN
         XR   R0,R0
         SVC  34
         MODESET MODE=PROB,KEY=NZERO
FIN      L       R13,4(R13)
         RETURN  (14,12),T,RC=0
MOVE     MVC  CMD(1),COMMANDE
         CNOP 0,8
CMDLEN   DC   H'80'
         DC   H'0'
CMD      DS   CL256
REG3     DC   CL8' '
MOT      DS   F
SYSIN    DCB  DDNAME=SYSIN,MACRF=GM,EODAD=ENDSYS,DSORG=PS,LRECL=80
         LTORG
PARAM    DSECT
LL       DS   H
COMMANDE DS   CL256
         END
