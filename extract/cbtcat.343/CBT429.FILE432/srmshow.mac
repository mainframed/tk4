*
* SRMSHOW
*
* THIS PROGRAM DISPLAYS THE CURRENT AND THRESHOLD VALUES OF MAJOR
* SRM PARAMETERS.
* SIMPLY USE IT UNDER TSO BY CALLING IT. APF-AUTHORIZATION NOT REQUIRED
* MVS-XA220 - ASSEMBLER H
* XEPHON - MVS UPDATE - FEBRUARY 1990
*
* DISPLAYING MAJOR SRM PARAMETERS UNDER TSO
* THE FOLLOWING PROGRAM EXTRACTS CURRENT AND THRESHOLD VALUES OF
* SRM PARAMETERS FROM THE FOLLOWING SRM CONTROL BLOCKS:
* RMCT (SRM CONTROL TABLE), LSCT (LOGICAL SWAP CONTROL
* TABLE), RCT (SRM RESOURCE CONTROL TABLE), AND ICT (I/O
* MANAGEMENT CONTROL TABLE).  THIS INFORMATION IS MORE ACCURATE
* THAN THAT OBTAINED FROM RMF II.  YOU GET THE SO-CALLED 'HAPPY
* RANGES' (LOW AND HIGH THRESHOLDS), THE CURRENT VALUE OF THE
* PARAMETER (PAGING RATE, CPU UTILISATION, ETC), AND A COMMENT
* OF 'GOOD' OR 'BAD' (NO COMMENT SIGNIFIES THAT THE VALUE IS IN
* THE HAPPY RANGE).
* FOR EXAMPLE, YOU CAN EXAMINE HOW SRM ADAPTS THE USER THINK
* TIME (GRACE PERIOD FOR LOGICAL SWAP BEFORE PHYSICAL SWAP)
* DEPENDING ON THE AFQ AND THE UIC (SRM ADDS 0.5 SECONDS
* WHEN ALL IS RIGHT, SUBTRACTS 1 SECOND IF NOT).  FIGURE 1 SHOWS AN
* EXAMPLE OF THE DISPLAY YOU GET AFTER CALLING SRMSHOW.  SUCH
* A DISPLAY CAN PINPOINT SOME CRITICAL BOTTLENECKS - FOR EXAMPLE,
* WHY IS THE PAGE DELAY SO BAD (280 MILLISECONDS)?  YOU MAY
* DISCOVER THAT SOME PAGE DATASETS ARE NOT ISOLATED FROM
* PRODUCTION FILES.
* THE AFQ AND CPU UTILISATIONS HELP YOU DETERMINE WHETHER
* YOUR MACHINE IS STORAGE- OR CPU-CONSTRAINED AT THE INSTANT YOU
* LOOK AT IT.  THE CURRENT VALUES OF PAGING RATES MAY SUGGEST THAT
* YOU CHANGE THE PAGE FAULT RATE THRESHOLDS (RCCPTRT) IN
* IEAOPTXX TO TAKE INTO ACCOUNT THE INFLUENCE OF PAGE-FAULTING
* FOR IMPACTING THE TARGET MPL.
*
*---------------------------------------------------*
* THIS MACRO CONVERTS A BINARY HALFWORD TO DECIMAL  *
*---------------------------------------------------*
         MACRO
         EXTEND  &BIN,&ETEND
         LCLA  &LEN
         AIF   (T'&ETEND NE 'U').FOLLOW
         B     EXPR&SYSNDX
&ETEND   DC    CL6'0'               CREATE THE ZONE IF IT DOESN'T EXIST
.FOLLOW  ANOP
EXPR&SYSNDX    DS  0H
         XR    R1,R1                SET REGISTER TO ZERO
         ICM   R1,3,&BIN            LOAD BINARY ZONE
         CVD   R1,DBLEWORD          CONVERT TO DECIMAL
         MVC   &ETEND,MASK1         LOAD MASK FOR ED INSTRUCTION
         ED    &ETEND.(6),PACKED3   CONVERSION FROM PACKED TO DECIMAL
         MEND
*---------------------------------------------------*
* THIS MACRO CONVERTS A BINARY FULLWORD TO DECIMAL  *
*---------------------------------------------------*
         MACRO
         EXTENDW &BIN,&ETEND
         LCLA  &LEN
         AIF   (T'&ETEND NE 'U').FOLLOW
         B     EXPR&SYSNDX
&ETEND   DC    CL10'0'              CREATE THE ZONE IF IT DOESN'T EXIST
.FOLLOW  ANOP
EXPR&SYSNDX    DS  0H
         XR    R1,R1                SET REGISTER TO ZERO
         ICM   R1,15,&BIN           LOAD BINARY ZONE
         CVD   R1,DBLEWORD          CONVERT TO DECIMAL
         MVC   &ETEND,MASK2         LOAD MASK FOR ED INSTRUCTION
         ED    &ETEND.(10),PACKED5  CONVERSION FROM PACKED TO DECIMAL
         MEND
*---------------------------------------------------*
* THIS MACRO SETS A ZONE TO ALL SPACES              *
*---------------------------------------------------*
         MACRO
&NLA     SPACES  &ZONE
         MVI   &ZONE,C' '                  FIRST BYTE SET TO SPACE
         MVC   &ZONE+1(L'&ZONE-1),&ZONE    PROPAGATE TO ALL BYTES
         MEND
*---------------------------------------------------*
         CVT   DSECT=YES
SRMSHOW  CSECT
R0  EQU 0
R1  EQU 1
R2  EQU 2
R3  EQU 3
R4  EQU 4
R5  EQU 5
R6  EQU 6
R7  EQU 7
R8  EQU 8
R9  EQU 9
R10 EQU 10  RETURN ADDRESS
R11 EQU 11
R12 EQU 12  BASE REGISTER
R13 EQU 13
R14 EQU 14
R15 EQU 15
         STM R14,R12,12(R13)     SAVE REGISTERS
         LR  R12,R15
         USING SRMSHOW,R12       R12 BASE REGISTER
         ST  R13,SAVE+4
         LA  R13,SAVE
* SET ADDRESSING MODE TO 31 BITS
         L   R1,AMODE31A
         BSM 0,R1                GET INTO 31-BITS ADDRESSING MODE
AMODE31A DC  A(AMODE31B+X'80000000')
AMODE31B DS  0H
*
         L   R2,CVTPTR           ADDRESS OF CVT
         USING CVT,R2
         L   R2,CVTOPCTP         ADDRESS OF RMCT  (SRM MAJOR TABLE)
         MVC TRAP,RMCT
         CLC RMCT,0(R2)
         BNE NOBLOC
         DROP R2
         L   R3,60(R2)           ADDRESS OF LSCT (LOGICAL SWAP CTRL TB)
         MVC TRAP,LSCT
         CLC LSCT,0(R3)
         BNE NOBLOC
         L   R4,228(R2)          ADDRESS OF RCT (SRM RESOURCE CTRL TB)
         MVC TRAP,RCT
         CLC RCT,0(R4)
         BNE NOBLOC
         L   R5,8(R2)            ADDRESS OF ICT  (SRM I/O MGT CTRL TB)
         MVC TRAP,ICT
         CLC ICT,0(R5)
         BNE NOBLOC
* INFORMATIONS FROM LSCT (LOGICAL SWAP)
         MVC LSUICL(2),4(R3)     UIC LOW FOR LOGICAL SWAP
         MVC LSUICH(2),6(R3)     UIC HIGH FOR LOGICAL SWAP
         MVC UIC(2),54(R4)       UIC CURRENT (INFORMATION FROM RCT)
*
         MVC LSTH(4),36(R3)      LOGICAL SWAP CURRENT USER THINK TIME
         MVC LSLTH(4),16(R3)     LOGICAL SWAP LOW USER THINK TIME
         MVC LSHTH(4),20(R3)     LOGICAL SWAP HIGH USER THINK TIME
         MVC LSUSERS(4),40(R3)   NUMBER OF USERS IN LOGICAL SWAP
         MVC AFQL(2),12(R3)      AFQ LOW FOR LOGICAL SWAP
         MVC AFQH(2),14(R3)      AFQ HIGH FOR LOGICAL SWAP
* INFORMATIONS FROM RCT
         MVC UICTL(2),4(R4)      UIC LOW
         MVC UICTH(2),6(R4)      UIC HIGH
         LH  R1,8(R4)
         SRL R1,4                DIVIDE BY 16
         STH R1,CPUTL            CPU LOW
         LH  R1,10(R4)
         SRL R1,4                DIVIDE BY 16
         STH R1,CPUTH            CPU HIGH
         LH  R1,56(R4)
         SRL R1,4                DIVIDE BY 16
         STH R1,CPU              CPU UTILIZATION CURRENT
         MVC PAGTL(2),12(R4)     PAGE FAULT RATE LOW
         MVC PAGTH(2),14(R4)     PAGE FAULT RATE HIGH
         MVC PAG(2),64(R4)       PAGE FAULT RATE CURRENT
         MVC DPGTL(2),20(R4)     PAGE DEMAND RATE LOW
         MVC DPGTH(2),22(R4)     PAGE DEMAND RATE HIGH
         MVC DPG(2),66(R4)       PAGE DEMAND RATE CURRENT
         MVC ASMTL(2),16(R4)     ASM QUEUE LOW
         MVC ASMTH(2),18(R4)     ASM QUEUE HIGH
         MVC ASM(2),62(R4)       ASM QUEUE CURRENT
         MVC PDLTL(2),28(R4)     PAGE DELAY TIME LOW
         MVC PDLTH(2),30(R4)     PAGE DELAY TIME HIGH
         MVC PDL(2),70(R4)       PAGE DELAY TIME CURRENT
         MVC AFQ(2),60(R4)       AFQ CURRENT
         MVC PGTOT(2),68(R4)     TOTAL PAGING RATE
         MVC FIXED(2),128(R4)    % OF FIXED FRAMES
         MVC FIXEDL(2),120(R4)   % OF FIXED FRAMES LOW
         MVC FIXEDH(2),122(R4)   % OF FIXED FRAMES HIGH
         MVC FIXEDB(2),130(R4)   % OF FIXED FRAMES BELOW 16 MEGS
         MVC FIXEDBL(2),124(R4)  % OF FIXED FRAMES BELOW 16 MEGS LOW
         MVC FIXEDBH(2),126(R4)  % OF FIXED FRAMES BELOW 16 MEGS HIGH
* INFORMATIONS ICT   (IO SRM)
         MVC TPILOW(2),30(R5)    % OF I/O VIA TPI INSTRUCTION LOW
         MVC TPIHIGH(2),32(R5)   % OF I/O VIA TPI INSTRUCTION HIGH
         MVC TPI(2),68(R5)       % OF I/O VIA TPI INSTRUCTION CURRENT
* PROCESS ALL DATA COLLECTED
* SET ADDRESSING MODE BACK TO 24 BITS
         LA  R1,AMODE24
         BSM 0,R1
AMODE24  DS  0H
*   UIC FOR LOGICAL SWAP
         EXTEND LSUICL,ZL
         EXTEND LSUICH,ZH
         EXTEND UIC,Z
         TPUT   L0,65            FIRST LINE
         SPACES L1
         MVC L1P,LIBUICL
         BAL R10,COMPARE2
         BAL R10,SEND
*  USER THINK TIME
         EXTENDW LSTH,M
         EXTENDW LSLTH,ML
         EXTENDW LSHTH,MH
         MVC L1P,LIBTHINK
         MVC L1V,M
         MVC L1L,ML
         MVC L1H,MH
         MVC L1GOOD,C
         TPUT   L1,65   **
         SPACES L1
*  AVAILABLE FRAME QUEUE
         EXTEND AFQL,ZL           LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND AFQH,ZH           HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND AFQ,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         MVC L1P,LIBAFQ           INDICATE WHAT KIND OF PARAMETER
         BAL R10,COMPARE2         COMPARE WITH THRESHOLDS, GOOD OR BAD?
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  NUMBER OF USERS LOGICALLY SWAPPED
         EXTENDW LSUSERS,M        FROM BINARY FULLWORD TO DECIMAL
         MVC L1V,M
         MVC L1P,LIBUSERS
         TPUT   L1,65
         SPACES L1
*  UIC
         EXTEND UICTL,ZL          LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND UICTH,ZH          HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND UIC,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE2         COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBUIC           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  CPU
         EXTEND CPUTL,ZL          LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND CPUTH,ZH          HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND CPU,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBCPU           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  PAGINATION (PAGE FAULTS)
         EXTEND PAGTL,ZL          LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND PAGTH,ZH          HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND PAG,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBPAG           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  PAGINATION (DEMAND PAGING)
         EXTEND DPGTL,ZL          LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND DPGTH,ZH          HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND DPG,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBDPG           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  ASM QUEUE
         EXTEND ASMTL,ZL          LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND ASMTH,ZH          HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND ASM,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBASM           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  PAGE DELAY
         EXTEND PDLTL,ZL          LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND PDLTH,ZH          HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND PDL,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBPGD           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  TPI
         EXTEND TPILOW,ZL         LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND TPIHIGH,ZH        HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND TPI,Z             CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBTPI           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  FIXED FRAMES %
         EXTEND FIXEDL,ZL         LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND FIXEDH,ZH         HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND FIXED,Z           CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBFIX           INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  FIXED FRAMES % BELOW 16 MEGS
         EXTEND FIXEDBL,ZL        LOW THRESHOLD FROM BINARY TO DECIMAL
         EXTEND FIXEDBH,ZH        HIGH THRESHOLD FROM BINARY TO DECIMAL
         EXTEND FIXEDB,Z          CURRENT VALUE FROM BINARY TO DECIMAL
         BAL R10,COMPARE          COMPARE WITH THRESHOLDS, GOOD OR BAD?
         MVC L1P,LIBFIXB          INDICATE WHAT KIND OF PARAMETER
         BAL R10,SEND             DISPLAY ONTO SCREEN
*  TOTAL PAGING
         EXTEND PGTOT,Z
         MVC L1P,LIBPGTOT
         BAL R10,SEND
*
ENDOFPGM L       R13,4(R13)
         RETURN  (14,12),T,RC=0
*
* SUBROUTINES
*
COMPARE  DS 0H   ** COMPARE CURRENT VALUE WITH LOW AND HIGH THRESHOLD
         SPACES C
         CLC Z,ZL
         BNL LABEL1
         MVC C,GOOD               GOOD, LOWER THAN LOW THRESHOLD
LABEL1   DS  0H
         CLC Z,ZH
         BNH LABEL2
         MVC C,BAD                BAD, HIGHER THAN HIGH THRESHOLD
LABEL2   DS  0H
         BR  R10
*
COMPARE2 DS 0H   ** COMPARE CURRENT VALUE WITH LOW AND HIGH THRESHOLD
         SPACES C
         CLC Z,ZL
         BNL LABEL3
         MVC C,BAD                BAD, LOWER THAN LOW THRESHOLD
LABEL3   DS  0H
         CLC Z,ZH
         BNH LABEL4
         MVC C,GOOD               GOOD, HIGHER THAN HIGH THRESHOLD
LABEL4   DS  0H
         BR  R10
         BR  R10
*
SEND     DS  0H           ** SEND LINE OF INFORMATION TO SCREEN **
         MVC L1V+4(6),Z
         MVC L1L+4(6),ZL
         MVC L1H+4(6),ZH
         MVC L1GOOD,C
         TPUT  L1,65
         SPACES L1
         SPACES ZL
         SPACES ZH
         SPACES Z
         SPACES C
         BR  R10
*
C        DS  CL15
NOBLOC   DS 0H            ** EYE-CATCHER MISMATCH **
         MVC  ERRMSG+24(4),TRAP
         TPUT ERRMSG,80
         B   ENDOFPGM
ERRMSG   DC  CL80'** EYE-CATCHER OF BLOCK XXXX NOT FOUND **'
GOOD     DC  CL15'         GOOD  '
BAD      DC  CL15'      ** BAD **'
TRAP     DC  C'NONE'
RMCT     DC  C'RMCT'
ICT      DC  C'ICT '
RCT      DC  C'RCT '
LSCT     DC  C'LSCT'
*
L0       DS  0CL65                  ** HEAD LINE **
         DC  CL20'   P A R A M E T E R'
         DC  CL10'     VALUE'
         DC  CL10'       LOW'
         DC  CL10'      HIGH'
         DC  CL15'          '
L1       DS  0CL65                  ** CURRENT LINE **
L1P      DC  CL20' '
L1V      DC  CL10' '
L1L      DC  CL10' '
L1H      DC  CL10' '
L1GOOD   DC  CL15' '
*
LIBUICL  DC  CL20'UIC FOR LOGICAL SWAP'
LIBTHINK DC  CL20'THINK TIME (MS)     '
LIBUSERS DC  CL20'USERS IN LOGIC. SWAP'
LIBUIC   DC  CL20'UIC                 '
LIBCPU   DC  CL20'% CPU UTILIZATION   '
LIBPAG   DC  CL20'PAGE FAULTS         '
LIBDPG   DC  CL20'PAGING (DEMAND)     '
LIBPGTOT DC  CL20'TOTAL PAGING        '
LIBASM   DC  CL20'ASM QUEUE           '
LIBPGD   DC  CL20'PAGE DELAY (MS)     '
LIBTPI   DC  CL20'% I/O VIA TPI       '
LIBAFQ   DC  CL20'AFQ FOR LOGICAL SWAP'
LIBFIX   DC  CL20'% FIXED FRAMES      '
LIBFIXB  DC  CL20'% FIXED FRAMES < 16M'
*
UICTL    DS  H     RCT ->   UIC LOW
UICTH    DS  H     RCT ->   UIC HIGH
UIC      DS  H     RCT ->   UIC
CPUTL    DS  H     RCT ->   CPU LOW
CPUTH    DS  H     RCT ->   CPU HIGH
CPU      DS  H     RCT ->   CPU
PAGTL    DS  H     RCT ->   PAGING LOW
PAGTH    DS  H     RCT ->   PAGING HIGH
PAG      DS  H     RCT ->   PAGING
DPGTL    DS  H     RCT ->   DEMAND PAGING LOW
DPGTH    DS  H     RCT ->   DEMAND PAGING HIGH
DPG      DS  H     RCT ->   DEMAND PAGING
ASMTL    DS  H     RCT ->   ASM QUEUE LOW
ASMTH    DS  H     RCT ->   ASM QUEUE HIGH
ASM      DS  H     RCT ->   ASM QUEUE
PDLTL    DS  H     RCT ->   PAGE DELAY LOW
PDLTH    DS  H     RCT ->   PAGE DELAY HIGH
PDL      DS  H     RCT ->   PAGE DELAY
AFQ      DS  H     RCT ->   AVAILABLE FRAME QUEUE
AFQL     DS  H     RCT ->   AVAILABLE FRAME QUEUE LOW
AFQH     DS  H     RCT ->   AVAILABLE FRAME QUEUE HIGH
PGTOT    DS  H     RCT ->   TOTAL PAGING
FIXED    DS  H     RCT -> % FRAMES FIXED
FIXEDL   DS  H     RCT -> % FRAMES FIXED LOW
FIXEDH   DS  H     RCT -> % FRAMES FIXED HIGH
FIXEDB   DS  H     RCT -> % FRAMES FIXED BELOW 16 M
FIXEDBL  DS  H     RCT -> % FRAMES FIXED BELOW 16 M LOW
FIXEDBH  DS  H     RCT -> % FRAMES FIXED BELOW 16 M HIGH
LSUICL   DS  H     LOGICAL SWAP UIC LOW
LSUICH   DS  H     LOGICAL SWAP UIC HIGH
LSTH     DS  F     LOGICAL SWAP THINK TIME
LSLTH    DS  F     LOGICAL SWAP THINK TIME LOW
LSHTH    DS  F     LOGICAL SWAP THINK TIME HIGH
LSUSERS  DS  F     LOGICAL SWAP NBRE DE USERS
TPILOW   DS  H     %IO TPI LOW
TPIHIGH  DS  H     %IO TPI HIGH
TPI      DS  H     %IO TPI
* SAVE AREA
SAVE     DS  18F   MY SAVE AREA
MASK1    DC  X'402020202020'              MASK FOR PACKED LENGTH 3
MASK2    DC  X'40202020202020202020'      MASK FOR PACKED LENGTH 5
* WORK ZONES
DBLEWORD DS  D     DOUBLE WORD FOR INSTRUCTION CVD
         ORG DBLEWORD+5
PACKED3  DS  PL3
         ORG DBLEWORD+3
PACKED5  DS  PL5
         LTORG
         END
