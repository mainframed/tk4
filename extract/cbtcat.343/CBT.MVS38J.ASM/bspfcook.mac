         PRINT OFF
         DCBD  DSORG=(PS),DEVD=DA     , DCB layout
         IEFZB4D0                     , dynalloc dsects
         IEFZB4D2                     , and equates
         PRINT ON,NOGEN               , Macro expansion not needed
BSPFCOOK TITLE 'Fortune Cookie Jar'
**********************************************************************
* This program display my snoozing little cat (well, actually, he    *
* is no longer little at all, but a proud, big tomcat.  But snoozing *
* is still his favourite pastime)                                    *
*                                                                    *
* As usual, my little cat comes with some wisdom from the fortune    *
* cookie jar.  I merged my database with about 7000 entries with     *
* the one found on CBTTAPE 429, file 300, member MURPHY              *
*                                                                    *
**********************************************************************
*
*
*                !l      _,,,---,,_
*          ZZZzz /,!.-'`'    -.  ;-;l,
*               !,4-  ) )-,_. ,( (  :'-'
*              '---''(_/--'  `-'(_)
*
* If not controlled, work will flow to the competent man ....
* .... until he submerges
*
*    (Another wisdom from the fortune cookie jar)
*
**********************************************************************
BSPFCOOK BSPENTER BASE=(R12),RENT=YES
         TITLE 'Symbols'
         GBLA  &COUNT
&COUNT   SETA  0
WCC      EQU   X'C3'                  , restore Kbd, reset MDT
SF       EQU   X'1D'                  , Start Field
SBA      EQU   X'11'                  , Set Buffer Address
IC       EQU   X'13'                  , Insert Cursor
RA       EQU   X'3C'                  , Repeat to Address
         TITLE 'Check the Environment'
         TESTENV                      , Are we TSO, Batch, or STC
         B     BRTABLE(R1)            , Branch to service routine
BRTABLE  DS    0F
         B     JOB                    , R1 = 0: This is a JOB
         B     STC                    , R1 = 4: This is an STC
         B     TSO                    , R1 = 8: This is a TSO User
         B     EXIT                   , R1 =12:  ????
         B     EXIT                   , R1 =16:  CICS as a JOB
         B     EXIT                   , R1 =20:  CICS as a STC
         B     EXIT                   , R1 =24:  CICS as a TSU
         B     EXIT                   , R1 =30:  CICS, as a ????
         TITLE 'STC : Use WTOs to show the little cat'
STC      DS    0H
         MVC   MYWTO,WTO              , move skeleton to WS
         LA    R8,HAEGAR              , point to little cat
         LA    R7,HAEGARL             , length of one line
         LA    R6,(HAEGARE-HAEGAR)/HAEGARL num of lines
         DO    FROM=(R6)
          BLANK WTOTEXT
          MVC   WTOTEXT+5(HAEGARL),0(R8)
          WTO   MF=(E,MYWTO)
          AR    R8,R7
         ENDDO R6
         WTO   ' '                    , output empty line
         BAL   R6,GETFCOOK            , get cookie from Jar
         TITLE 'STC: Show fortune cookie and trailer line'
         DO    WHILE=(CLI,0(R8),NE,X'FE')
          BLANK WTOTEXT               , clear the print line
          SR   R7,R7                  , clear workreg
          IC   R7,0(R8)               , R7 now has length of message
          BCTR R7,0                   , decrement for execute
          LA   R8,1(,R8)              , R8 now has addr. of msg proper
          MVC  WTOTEXT(*-*),0(R8)     , insert the message text
          EX   R7,*-6                 , via EX
          WTO  MF=(E,MYWTO)           , write the text
          AR   R8,R7                  , Get to next line
          LA   R8,1(,R8)              , Add 1 for the decrement above
         ENDDO (WHILE,CLI,0(R8),NE,X'FE')
         WTO   ' '
         BLANK WTOTEXT                , first initialize
         MVC   WTOTEXT(L'HAEGARE),HAEGARE
         WTO   MF=(E,MYWTO)           , Write the trailer
         WTO   ' '
         B     EXIT
         TITLE 'JOB : Allocate and open files as needed'
***********************************************************************
* Allocate SYSUDUMP DD if not already present                         *
***********************************************************************
         SPACE
JOB      DS    0H                     , show fortune cookie in a job
         DEVTYPE SYSUDUMP,DEVTYPE     , is there a SYSUDUMP DD
         IF    (LTR,R15,R15,NZ)       , if no DD statement
          MVC  TUSDPTR,DPARM01        , MOVE SVC99 parms to WS
          LA   R1,TUSDPTR+TUSDDDN-DPARM01   DDNAME TExt unit address
          ST   R1,TUSDPTR             , place into parmlist
          LA   R1,TUSDPTR+TUSDCLS-DPARM01   CLASS Text unit address
          ICM  R1,B'1000',=X'80'      , flag as "last parm"
          ST   R1,TUSDPTR+4           , put into parmlist
          LA   R1,TUSDPTR             , point to Text Unit Pointer List
          BAL  R14,DOSVC99            , now do DYNALLOC
         ENDIF (LTR,R15,R15,NZ)
***********************************************************************
* Allocate FCOOKPRT DD if not already present                         *
***********************************************************************
         SPACE
         MVC   MYDCB,FCOOKPRT
         DEVTYPE DDNAME,DEVTYPE       , is there a SYSPRINT DD
         IF    (LTR,R15,R15,NZ)       , if no DD statement
          MVC  TUSPPTR,DPARM02        , move to reentrant storage
          LA   R1,TUSPPTR+TUSPDDN-DPARM02 put address of DDNAME
          ST   R1,TUSPPTR             , text unit into parmlist
          LA   R1,TUSPPTR+TUSPCLS-DPARM02 put address of CLASS text
          ICM  R1,B'1000',=X'80'      , unit into parmlist and flag
          ST   R1,TUSPPTR+4           , as last parameter
          LA   R1,TUSPPTR             , point to text unit pointers
          BAL  R14,DOSVC99            , Try DYNALLOC
         ENDIF (LTR,R15,R15,NZ)
         SPACE 2
***********************************************************************
* Open FCOOKPRT                                                       *
***********************************************************************
         SPACE
         MVC   MYOPEN,OPEN
         LA    R3,MYDCB               , point to the DCB
         USING IHADCB,R3              , tell assembler
         OPEN  ((R3),OUTPUT),MF=(E,MYOPEN)
         IF    (TM,DCBOFLGS,DCBOFOPN,Z) if file did not open
          WTO  'BSPFCOOK - DD statement for FCOOKPRT missing'
          B    EXIT                   , and just exit
         ENDIF (TM,DCBOFLGS,DCBOFOPN,Z) if file did not open
         TITLE 'JOB : Init print line and show the little cat'
         BLANK MYLINE,CC=C'1'         , clear line, set form feed
         LA    R8,HAEGAR              , point to little cat
         LA    R7,HAEGARL             , length of one line
         LA    R6,(HAEGARE-HAEGAR)/HAEGARL num of lines
         DO    FROM=(R6)
          MVC  MYLINE+10(HAEGARL),0(R8) insert into printline
          PUT  MYDCB,MYLINE           , write the record
          BLANK MYLINE                , clear the printline
          AR   R8,R7                  , add length to get to next line
         ENDDO
         BAL   R6,GETFCOOK            , get cookie from Jar
         TITLE 'JOB: Print fortune cookie and trailer line'
         DO    WHILE=(CLI,0(R8),NE,X'FE')
          BLANK MYLINE                , clear the print line
          SR   R7,R7                  , clear workreg
          IC   R7,0(R8)               , R7 now has length of message
          BCTR R7,0                   , decrement for execute
          LA   R8,1(,R8)              , R8 now has addr. of msg proper
          MVC  MYLINE+1(*-*),0(R8)    , insert the message text
          EX   R7,*-6                 , via EX
          PUT  MYDCB,MYLINE           , write the text
          AR   R8,R7                  , Get to next line
          LA   R8,1(,R8)              , Add 1 for the decrement above
         ENDDO (WHILE,CLI,0(R8),NE,X'FE')
         BLANK MYLINE,CC=C'0'         , first initialize
         MVC   MYLINE+1(L'HAEGARE),HAEGARE
         PUT   MYDCB,MYLINE           , Write the trailer
         B     CLOSE                  , and exit
         TITLE 'TSO : Erase the screen, then show the little cat'
TSO      DS    0H
         GTSIZE                       , get screen size info
         LTR   R0,R0                  , Is it 3270?
         BZ    NOCLS                  , BIF not
         MR    R0,R0                  , Screen end = Lines times Len
         BCTR  R1,0                   , Convert position to offset
         D     R0,=F'64'              , Module 64
         N     R1,=X'0000003F'        , Make absoultely certain
         MVC   SCRCNTL,SKLCNTL        , Moce skeleton to cmd buffer
         STC   R0,SCREND+1            , column offset mod 64
         STC   R1,SCREND              , row offset mod 64
         TR    SCREND,SCRPOS          , make 3270 buffer address
         TPUT  SCRCNTL,LSCRCNTL,FULLSCR,,HOLD   and send
NOCLS    DS    0H
         LA    R8,HAEGAR              , point to little cat
         LA    R7,HAEGARL             , length of one line
         LA    R6,(HAEGARE-HAEGAR)/HAEGARL num of lines
         DO    FROM=(R6)
          TPUT (R8),(R7),R            , show one line
          AR   R8,R7                  , add length to get to next line
         ENDDO                        , loop around
         BAL   R6,GETFCOOK            , get cookie from Jar
         TITLE 'Display fortune cookie and trailer line'
         DO    WHILE=(CLI,0(R8),NE,X'FE')
          SR   R7,R7                  , clear workreg
          IC   R7,0(R8)               , R7 now has length of message
          LA   R8,1(,R8)              , R8 now has addr. of msg proper
          TPUT (R8),(R7),R            , Show the message
          AR   R8,R7                  , Get to next line
         ENDDO                        , and go around again
         LA    R7,1                   , first
         LA    R8,=C' '               , one space
         TPUT  (R8),(R7),R            , for s blank line
         LA    R7,50                  , length of trsiler text
         LA    R8,HAEGARE             , address of trsiler text
         TPUT  (R8),(R7),R            , show trailer
         LA    R7,1                   , now another
         LA    R8,=C' '               , space for
         TPUT  (R8),(R7),R            , a blank line
         B     EXIT
         TITLE 'Subroutines: DOSVC99 - Perform DYNALLOC functions'
DOSVC99  DS    0H                     , perform DYNALLOC functions
         ST    R14,DOSVSAVE           , save retrun address
         LA    R10,REQBLK             , point to request block
         USING S99RB,R10              , tell assembler
         XC    REQBLK,REQBLK          , clear RB
         MVI   S99RBLN,REQBLKLN       , set up length
         MVI   S99VERB,S99VRBAL       , indicate ALLOC function
         LR    R9,R1                  , point to Text Unit Pointerlist
         ST    R9,S99TXTPP            , put into Request block
         LA    R9,RBPTR               , Point to RB pointer
         USING S99RBP,R9              , tell assembler
         ST    R10,S99RBPTR           , st RB address into RB PTR
         OI    S99RBPTR,S99RBPND      , turn on high order bit
         LA    R1,RBPTR               , get addres of RB pointer
         DYNALLOC                     , issue SVC 99
         IF    (LTR,R15,R15,NZ)
           XR  R14,R14
           ICM R14,B'0011',S99ERROR
           EX  R15,*
         ENDIF
         DROP  R9,R10                 , not needed any more
         L     R14,DOSVSAVE           , restore return address
         BR    R14                    , and retrun to caller
CLOSE    DS    0H
         LA    R3,MYDCB
         CLOSE ((R3)),MF=(E,MYOPEN)   , done with it
*        B     EXIT
EXIT     DS    0H                     , Leave
         BSPRET RC=0                  , and retrun with RC = 0
         TITLE 'Get pseudo random number from STCK and select cookie'
GETFCOOK DS    0H                     , Get random fortune cookie
         XC    DWORD,DWORD            , initialize
         MVC   MSG#(7),=CL7'ML'       , set msg prefix
         STCK  DWORD                  , get current time
         SR    R10,R10                , clear a work reg
         ICM   R11,7,DWORD+2          , get a part of the clock
         LA    R11,0(R11)             , clear high order byte
         D     R10,=A((ML00000-MLTBL)/4)  Divide by number of entries
         SLL   R10,2                  , remainder times 4
*                                     , R10 has offset into table
         LA    R9,MLTBL(R10)          , R9 now has address of text ptr
         L     R8,0(R9)               , R8 now has address of text
         BR    R6                     , return to caller
         TITLE 'LOGO and Constants'
         DS    0F
HAEGAR   DC    CL50'                                        '
HAEGARL  EQU   *-HAEGAR
         DC    CL(HAEGARL)'                                        '
         DC    CL(HAEGARL)'               ×\      _,,,---,,_       '
         DC    CL(HAEGARL)'         ZZZzz /,`.-''''''    -.  ;-;;,    '
         DC    CL(HAEGARL)'              ×,4-  ) )-,_. ,- ( (''-''  '
         DC    CL(HAEGARL)'             ''---''''(_/--''  `-'')_)    '
         DC    CL(HAEGARL)' '
HAEGARE  DC    CL(HAEGARL)'Another bit of wisdom from the fortunes cooki
               ie jar'
SYSUDUMP DC    CL8'SYSUDUMP'          , for SVC 99
SKLCNTL  DS    0CL09                  , 3270 command string
         DC    AL1(WCC)               , Write control Character
         DC    AL1(SBA)               , Set Buffer Address Order
         DC    X'4040'                , RC = 01,01
         DC    AL1(IC)                , Insert Cursor Order
         DC    AL1(RA)                , Repeat Until Address order
SKLEND   DC    X'5D7F'                , Last position on screen
         DC    X'00'                  , Character to be repeated
LSKLCNTL EQU   *-SKLCNTL              , length of command string
SCRPOS   DS    0H                     , modulo 64 translate table
         DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
         TITLE 'Constants - Skeleton macros for reentrant codeing'
         DS    0D
WTO      WTO   '1234567890123456789012345678901234567890123456789012345-
               678961234567890123456789',MF=L
WTOL     EQU    *-WTO
FCOOKPRT DCB   DDNAME=FCOOKPRT,                                        +
               MACRF=(PM),                                             +
               DSORG=PS,                                               +
               LRECL=133,                                              +
               RECFM=FBA,                                              +
               BLKSIZE=1330
DCBL     EQU   *-FCOOKPRT
OPEN     OPEN  (,),MF=L
OPENL    EQU   *-OPEN
         TITLE 'Constants - Dynalloc text units'
***********************************************************************
* SYSUDUMP DYNALLOC parameters                                        *
***********************************************************************
         SPACE
DPARM01  DS    0F
         DC    A(TUSDDDN)             , address of DDNAME
         DC    X'80'                  , end of list indicator
         DC    AL3(TUSDCLS)           , address of SYSOUT CLASS info
TUSDDDN  DC    AL2(DALDDNAM)          , key for DDNAME
         DC    AL2(1)                 , number of entries
         DC    AL2(8)                 , length od 1 entry
         DC    CL8'SYSUDUMP'          , contens of entry
TUSDCLS  DC    AL2(DALSYSOU)          , key for SYSOUT
         DC    AL2(1)                 , number of entries
         DC    AL2(1)                 , length of 1 entry
         DC    C'X'                   , sysout class
DPARM01L EQU   *-DPARM01
         SPACE 2
***********************************************************************
* FCOOKPRT DYNALLOC text units                                        *
***********************************************************************
         SPACE
DPARM02  DS    0F
         DC    A(TUSPDDN)             , address of DDNAME
         DC    X'80'                  , end of list indicator
         DC    AL3(TUSPCLS)           , address of SYSOUT CLASS info
TUSPDDN  DC    AL2(DALDDNAM)          , key for DDNAME
         DC    AL2(1)                 , number of entries
         DC    AL2(8)                 , length od 1 entry
         DC    CL8'FCOOKPRT'          , contens of entry
TUSPCLS  DC    AL2(DALSYSOU)          , key for SYSOUT
         DC    AL2(1)                 , number of entries
         DC    AL2(1)                 , length of 1 entry
         DC    C'X'                   , sysout class
DPARM02L EQU   *-DPARM02
         LTORG
         COPY  BSPFCOO1               , ADCONS, part 1
         COPY  BSPFCOO2               , ADCONS, part 2
         COPY  BSPFCOO3               , cookies, part 1
         COPY  BSPFCOO4               , cookies, part 2
.TEST02  ANOP
         PRINT ON
         TITLE 'Workarea for Reentrant Code'
WORKAREA DSECT
DWORD    DS    D                      , resume DSECT
DOSVSAVE DS    F                      , save area for DOSVC
DEVTYPE  DS    6F                     , for DEVTYPE Macro
MSG#     DS    0CL7                   , number of fortune cookie
         DS    CL2                    , fixed header (ML)
WORK     DS    CL5                    , varying number
MYENV    DS    C                      , Environment indicator
MYWTO    DS    CL(WTOL)
WTOTEXT  EQU   MYWTO+4,79
MYDCB    DS    CL(DCBL)
MYOPEN   DS    CL(OPENL)
DDNAME   EQU   MYDCB+DCBDDNAM-IHADCB,8
MYLINE   DS    0CL133
MYCTLCHR DS    C
MYTEXT   DS    CL132
***********************************************************************
* 3270 datastream for clearing the screen                             *
***********************************************************************
SCRCNTL  DS    0CL09                  , 3270 command string
         DS    AL1                    , Write control Character
         DS    AL1                    , Set Buffer Address Order
         DS    XL2                    , RC = 01,01
         DS    AL1                    , Insert Cursor Order
         DS    AL1                    , Repeat Until Address order
SCREND   DS    XL2                    , Last position on screen
         DS    X                      , Character to be repeated
LSCRCNTL EQU   *-SCRCNTL              , length of command string
         DS    0F
TUSDPTR  DS    CL(DPARM01L)           , text unit pointers
         DS    0F
TUSPPTR  DS    CL(DPARM02L)           , text unit pointers
***********************************************************************
* SVC 99 Request Block                                                *
***********************************************************************
         SPACE
RBPTR    DS    F                      , request block pointer
REQBLK   DS    CL(S99RBEND-S99RB)     , Request block
REQBLKLN EQU   L'REQBLK               , length of request block
         SPACE
         BSPEND                       , of module
