SETDATE  CSECT
         REGISTER
         ENTER BASE=12,SAVE=SAVEAREA
         EJECT
*********************************************************************
*                                                                   *
*  Create SET JCL statements that define month, day, year.          *
*  These statements would normally be written into a proclib member *
*  each day just after midnight.                                    *
*                                                                   *
*  OUTPUT DD statement should point to a proclib or jcllib member.  *
*                                                                   *
*  Mark C. Lawrence 1999/07/22                                      *
*                                                                   *
*********************************************************************

         OPEN (OUTDCB,(OUTPUT))
         TIME  DEC,DATE1,ZONE=LT,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
*        TIME  DEC,DATE2,ZONE=LT,DATETYPE=YYYYDDD,LINKAGE=SYSTEM

*  Convert to zoned

         MVI   SN1,X'0F'           valid digit and sign
         UNPK  DATEWORK,YMDP       get YYYYMMDD0
         MVC   SDT,DATEWORK        YYYYMMDD
         MVC   SYR,DATEWORK        YYYY
         MVC   SMO,DATEWORK+4      MM
         MVC   SDA,DATEWORK+6      DD
         PUT   OUTDCB,REC1
         PUT   OUTDCB,REC2
         PUT   OUTDCB,REC3
         PUT   OUTDCB,REC4
         PUT   OUTDCB,REC5
         CLOSE OUTDCB
         EXIT
         TITLE 'DATA AREA'
         DC    0D'0'
SAVEAREA DC    18F'0'
DATE1    DC    2D'0'
YMDP     EQU   DATE1+8,5           YYYYMMDD unsigned plus SN1
SN1      EQU   DATE1+12,1          to append sign

DATEWORK DC    CL9' '

REC1     DC    CL80'//*  System date parameters, set by SETDATE'

*********************************************************************
*                                                                   *
*  The following four records define "year", "month", "day", and    *
*  "date" variables.  Assign whatever names to these fit your       *
*  installation requirements -- you may wish to avoid "SYS" names   *
*  so you don't conflict with MVS symbols.                          *
*                                                                   *
*********************************************************************

REC2     DC    C'//  SET  SYSYEAR='
SYR      DC    C'0000'
         FILL  REC2,80

REC3     DC    C'//  SET  SYSMONTH='
SMO      DC    C'00'
         FILL  REC3,80

REC4     DC    C'//  SET  SYSDAY='
SDA      DC    C'00'
         FILL  REC4,80

REC5     DC    C'//  SET  SYSDATE='
SDT      DC    C'00000000'
         FILL  REC5,80

         PRINT NOGEN
OUTDCB   DCB   DDNAME=OUTPUT,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80
         PRINT GEN
         END
