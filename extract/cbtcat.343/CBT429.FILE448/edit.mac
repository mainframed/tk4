         MACRO
.*  Last revision 03/06/89
&N       EDIT  &TO,&FROM,&P,&DECIMAL=0,&ZEROS=1,&COMMAS=NO,&STYLE=USA,&*
               FILL=40
         GBLC  &EDWORK             Global work area
         LCLA  &LEN,&K,&D,&NDS,&KMIN,&NDD,&NDL,&NDT,&NC,&XS
         LCLA  &NDP,&NPAD
         LCLB  &W,&CMA,&LC
         LCLC  &R,&PACK,&P0,&CC,&DC
.* Initialize codes...North American standards
&CC      SETC  '6B'                Comma (,)...to group thousands
&DC      SETC  '4B'                Period (.) ... decimal point
         AIF   ('&STYLE' EQ 'USA').USA
&CC      SETC  '4B'                European Style:  nnn.nnn.nnn,n
&DC      SETC  '6B'                So "decimal point" is a comma
.USA     ANOP
.* -------------  Load Value  ------------------------------------
         AIF   ('&FROM'(1,1) EQ '(').REG
.*  Load value from storage.  Is binary value in 1, 2, 3, or 4-byte
&R       SETC  '0'                 Use R0 for work reg
         AIF   (T'&FROM EQ 'U').LF If undefined, assume fullword
&LEN     SETA  L'&FROM             Length
         AIF   (&LEN GT  4).BADLEN
         AIF   (&LEN EQ 4).LF      Fullword
         AIF   (&LEN EQ 2).LH      Halfword
&N       SR    &R,&R
         AIF   (&LEN EQ 3).L3      3 bytes
.*  One byte...
         IC    &R,&FROM            Load value
         AGO   .LOADED
.L3      ANOP
         ICM   &R,7,&FROM          Load value
         AGO   .LOADED
.LF      ANOP
&N       L     &R,&FROM            Load value
         AGO   .LOADED
.LH      ANOP
&N       LH    &R,&FROM            Load value
         AGO   .LOADED
.REG     ANOP
&R       SETC  '&FROM(1)'          Value in reg
         AIF   ('&N' EQ '').LOADED
&N       EQU   *
.LOADED  ANOP
.* -------------------- Work Area ---------------------------------
&PACK    SETC  '&EDWORK'           Global value if any
         AIF   ('&PACK' NE '').GSET
&PACK    SETC  'IHB&SYSNDX'
         AGO   .WT
.GSET    ANOP
&W       SETB  1                   Show we have work area
.WT      AIF   (T'&P EQ 'O').NOW   If no work area supplied
&W       SETB  1
&PACK    SETC  '&P'
.NOW     ANOP
.* ------------ Calculate values for pattern -----------------------
         AIF   ('&COMMAS'(1,1) NE 'Y').NCA
&CMA     SETB  1                   Flag for commas
.NCA     ANOP
&NPAD    SETA  1                   Minimum number of pad chars.
&K       SETA  L'&TO               Field length
&NDD     SETA  &DECIMAL
.*  NDP = 0 if no decimal point, 1 otherwise
.*  NDD = no. decimal digits
.*  NDT = total number of digits
.*  NDL = no. digits left of decimal point
&NDT     SETA  &K-1                1 for fill (see even/odd below)
&KMIN    SETA  4                   If no decimal
         AIF   (&NDD EQ 0).ND1
&KMIN    SETA  4+&NDD              Min. length
&NDT     SETA  &NDT-1              1 less digit select
&NDP     SETA  1                   1 byte for the decimal point
.ND1     ANOP
         AIF   (&K LT &KMIN).BAD   Too small
&NDL     SETA  &NDT-&NDD           No. digits left of decimal
.*                                 (will be reduced if it ends up odd)
         AIF   (NOT &CMA).NC1
&NC      SETA  (&NDL-1)/4          No. commas
         AIF   (&NDL/4*4 NE &NDL).NOTMOD4
&NDL     SETA  &NDL-1              length is 0 mod 4, would
&NPAD    SETA  &NPAD+1                result in leading comma
.NOTMOD4 ANOP
&NDL     SETA  &NDL-&NC            Digits remaining
         AIF   (&NDL-&NDL/3*3 NE 1).N31
&LC      SETB  1                   digits=1 mod 3
.N31     ANOP
.NC1     ANOP
&NDT     SETA  &NDL+&NDD           recompute total digits
         AIF   (&NDT LE 15).OK     All right
.* Field too large, calculate padding
&NDL     SETA  15-&NDD             left digits
&NC      SETA  0
         AIF   (NOT &CMA).CPD
&NC      SETA  (&NDL+1)/3          No. commas
.CPD     ANOP
&NDT     SETA  15
&NPAD    SETA  &K-(&NDL+&NC+&NDD+&NDP)   pad on left
&XS      SETA  &NPAD-1
         AIF   (&XS LE 0).PBLD
         MNOTE 4,'Field too large, &XS bytes ignored'
         AGO   .PBLD
.OK      ANOP
         AIF   (&NDT/2*2 NE &NDT).ODD  See if even or odd
&NPAD    SETA  &NPAD+1             Pad with extra blank
&NDL     SETA  &NDL-1              ...means 1 less digit
         AIF   (NOT &LC).ODD
&NPAD    SETA  &NPAD+1             replace leading comma with blank
&NC      SETA  &NC-1
.ODD     ANOP
&NDT     SETA  &NDL+&NDD           recalc. total
.* ------------- build the pattern -----------------------------
.PBLD    ANOP
&P0      SETC  '=X''&FILL'         Pattern start
         AGO   .P0
.PLOOP   ANOP
&P0      SETC  '&P0.&FILL'         Another blank (or whatever)
.P0      ANOP
&NPAD    SETA  &NPAD-1
         AIF   (&NPAD GT 0).PLOOP
&D       SETA  8-(&NDT+1)/2        Displacement in packed area
         AIF   (&NDL LE 2).NODS    No digit selects (we'll add 2)
         AGO   .NOT3               Don't begin with a comma
.DSLOOP  ANOP
         AIF   (NOT &CMA).NOT3     No commas...
         AIF   (&NDL/3*3 NE &NDL).NOT3
&P0      SETC  '&P0.&CC'            Put in a comma
.NOT3    ANOP
&P0      SETC  '&P0.20'            Add a digit select
&NDL     SETA  &NDL-1              Decrement count
         AIF   (&NDL GE 3).DSLOOP  Do another
.NODS    ANOP
         AIF   ('&ZEROS' EQ 'N').NZ    Don't print zero if N=0
&P0      SETC  '&P0.2120'          Sig. starter and last dig. sel.
         AGO   .SSD
.NZ      ANOP
&P0      SETC  '&P0.2021'          Last digit sel, sig. start
.*  In this mode, field (before decimal, if any) will be blank if N=0
.SSD     ANOP
         AIF   (&NDD LE 0).NODD    No decimal digits
&P0      SETC  '&P0.&DC'            Decimal point
.DDLOOP  ANOP
&P0      SETC  '&P0.20'            Add a digit select
&NDD     SETA  &NDD-1              Counter
         AIF   (&NDD GE 1).DDLOOP  Do another
.NODD    ANOP
&P0      SETC  '&P0.'''            Trailing quote
.* ------------------- machine instructions -----------------------
         CVD   &R,&PACK
         MVC   &TO,&P0
         ED    &TO,&PACK+&D
         AIF   (&W).OUT
         B     &PACK+8             Branch around work area
&PACK    DS    D                   For CVD
.OUT     MEXIT
.BAD     MNOTE 8,'   Invalid field Length'
         MEXIT
.BADLEN  MNOTE 8,'   &LEN:  Invalid source length'
         MEND
