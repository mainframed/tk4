         MACRO
&NAME   @GETMSG &MSG,                   MESSAGE NAME                   *
               &SMSG,                   VAR FOR SHORT MESSAGE          *
               &LMSG,                   VAR FOR LONG MESSAGE           *
               &ALARM,                  VAR FOR ALARM VALUE            *
               &HELP,                   VAR FOR HELP PANEL             *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFGETMSG,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&MSG' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFGETMSG).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&MSG' EQ '').CNT1
         AIF   ('&MSG'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&SMSG' EQ '').CNT2
         AIF   ('&SMSG'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&LMSG' EQ '').CNT3
         AIF   ('&LMSG'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&ALARM' EQ '').CNT4
         AIF   ('&ALARM'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&HELP' EQ '').CNT5
         AIF   ('&HELP'(1,1) EQ '''').GENLIT
.CNT5    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFGETMSG).BYP20
GETMSG   DC    CL8'GETMSG'              ISPF KEYWORD
&SPFGETMSG SETB 1
.BYP20   ANOP
         AIF   ('&MSG' EQ '').BYP30
         AIF   ('&MSG'(1,1) NE '''').BYP30
SPFM&NDX DC    CL8&MSG                  GENERATED MESSAGE NAME
.BYP30   ANOP
         AIF   ('&SMSG' EQ '').BYP40
         AIF   ('&SMSG'(1,1) NE '''').BYP40
SPFS&NDX DC    CL8&SMSG                 GENERATED SHORT MESSAGE
.BYP40   ANOP
         AIF   ('&LMSG' EQ '').BYP50
         AIF   ('&LMSG'(1,1) NE '''').BYP50
SPFL&NDX DC    CL8&LMSG                 GENERATED LONG MESSAGE
.BYP50   ANOP
         AIF   ('&ALARM' EQ '').BYP60
         AIF   ('&ALARM'(1,1) NE '''').BYP60
SPFA&NDX DC    CL8&ALARM                GENERATED ALARM VAR FIELD
.BYP60   ANOP
         AIF   ('&HELP' EQ '').BYP70
         AIF   ('&HELP'(1,1) NE '''').BYP70
SPFH&NDX DC    CL8&HELP                 GENERATED HELP VAR FIELD
.BYP70   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,GETMSG                GET ADDRESS OF GETMSG KEYWORD
.*
         AIF   ('&MSG' EQ '').BYP1000
         AIF   ('&MSG'(1,1) EQ '''').BYP1010
         LA    15,&MSG                  GET ADDRESS OF MSG NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR MSG
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFM&NDX              GET ADDRESS OF GENERATED MSG
.BYP1050 ANOP
.*
         AIF   ('&SMSG' EQ '').BYP1100
         AIF   ('&SMSG'(1,1) EQ '''').BYP1110
         LA    0,&SMSG                  GET ADDRESS OF SHORT MSG
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR SHORT MSG
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFS&NDX               GET ADDRESS OF GENERATED SHORT
.BYP1150 ANOP
.*
         AIF   ('&LMSG' EQ '').BYP1200
         AIF   ('&LMSG'(1,1) EQ '''').BYP1210
         LA    1,&LMSG                  GET ADDRESS OF LONG MSG NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR LONG MSG
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFL&NDX               GET ADDRESS OF GENERATED LONG
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&ALARM' EQ '').BYP1300
         AIF   ('&ALARM'(1,1) EQ '''').BYP1310
         LA    14,&ALARM                GET ADDRESS OF ALARM NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR ALARM
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFA&NDX              GET ADDRESS OF GENERATED ALARM
.BYP1350 ANOP
.*
         AIF   ('&HELP' EQ '').BYP1400
         AIF   ('&HELP'(1,1) EQ '''').BYP1410
         LA    15,&HELP                 GET ADDRESS OF HELP NAME
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR HELP
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFH&NDX              GET ADDRESS OF GENERATED HELP
.BYP1450 ANOP
.*
         STM   14,15,&TEMP+16           STORE PARAMETERS INTO LIST
         OI    &TEMP+20,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 6).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 6).BYPVARS
&SPFVARS SETA  6
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(GETMSG)                GETMSG LITERAL
.*
         AIF   ('&MSG' EQ '').BYP2000
         AIF   ('&MSG'(1,1) EQ '''').BYP2010
         DC    A(&MSG)                  ADDRESS OF MSG NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MSG NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFM&NDX)              ADDRESS OF GENERATED MSG
.BYP2050 ANOP
.*
         AIF   ('&SMSG' EQ '').BYP2100
         AIF   ('&SMSG'(1,1) EQ '''').BYP2110
         DC    A(&SMSG)                 ADDRESS OF SHORT MSG
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR SMSG SHORT MSG
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFS&NDX)              ADDRESS OF GENERATED SHORT MSG
.BYP2150 ANOP
.*
         AIF   ('&LMSG' EQ '').BYP2200
         AIF   ('&LMSG'(1,1) EQ '''').BYP2210
         DC    A(&LMSG)                 ADDRESS OF LONG MESSAGE
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR LONG MESSAGE
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LONG MES
.BYP2250 ANOP
.*
         AIF   ('&ALARM' EQ '').BYP2300
         AIF   ('&ALARM'(1,1) EQ '''').BYP2310
         DC    A(&ALARM)                ADDRESS OF ALARM NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ALARM
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFA&NDX)              ADDRESS OF GENERATED ALARM
.BYP2350 ANOP
.*
         AIF   ('&HELP' EQ '').BYP2800
         AIF   ('&HELP'(1,1) EQ '''').BYP2810
         DC    A(&HELP+X'80000000')     ADDRESS OF HELP NAME
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR HELP
         AGO   .BYP2850
.BYP2810 ANOP
         DC    A(SPFH&NDX+X'80000000')  ADDRESS OF GENERATED HELP
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'THE MESSAGE NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
