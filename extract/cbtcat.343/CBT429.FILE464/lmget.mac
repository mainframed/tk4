         MACRO
&LABEL   LMGET &DATAID=,                DATAID NAME                    *
               &MODE=,                  MODE                           *
               &DATALOC=,                                              *
               &DATALEN=,                                              *
               &MAXLEN=,                                               *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFLMGET                GLOBAL FOR LMGET LITERAL
         GBLB  &SPFNULLC                DUMMY PARAMTER FOR CALL
         GBLB  &SPFDEBUG                DEBUG MODE
         GBLB  &SPFVAR                  NUMBER OF REQUIRED FULLWORDS
         GBLA  &SPFVARS                 NUMBER OF FULLWORDS
         GBLA  &SPFVARC                 PREVIOUS FULLWORDS USED
         LCLC  &NDX                     USED IN PLACE OF SYSNDX
         LCLC  &TEMP                    USED AS TEMPORARY VARIABLE
.*
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LABEL' EQ '').BYP10
&LABEL   DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&DATAID' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFLMGET).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&DATAID'(1,1) EQ '''').GENLIT
         AIF   ('&MODE' EQ '').CNT2
         AIF   ('&MODE'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&DATALOC' EQ '').CNT3
         AIF   ('&DATALOC'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&DATALEN' EQ '').CNT4
         AIF   ('&DATALEN'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   (T'&MAXLEN EQ 'N').GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFNULLC).BYP001
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP001  ANOP
.*
.*
         AIF   (&SPFLMGET).BYP20
LMGET    DC    CL8'LMGET'               ISPF KEYWORD
&SPFLMGET SETB 1
.BYP20   ANOP
.*
.*
         AIF   ('&DATAID' EQ '').BYP30
         AIF   ('&DATAID'(1,1) NE '''').BYP30
SPFZ&NDX DC    CL8&DATAID               GENERATED DATAID NAME
.BYP30   ANOP
.*
.*
         AIF   ('&MODE' EQ '').BYP40
         AIF   ('&MODE'(1,1) NE '''').BYP40
SPFA&NDX DC    CL8&MODE                 MODE TYPE
.BYP40   ANOP
.*
.*
         AIF   ('&DATALOC' EQ '').BYP50
         AIF   ('&DATALOC'(1,1) NE '''').BYP50
SPFB&NDX DC    CL8&DATALOC              DATALOC VARIABLE NAME
.BYP50   ANOP
.*
.*
         AIF   ('&DATALEN' EQ '').BYP60
         AIF   ('&DATALEN'(1,1) NE '''').BYP60
SPFC&NDX DC    CL8&DATALEN              DATALEN VARIABLE NAME
.BYP60   ANOP
.*
.*
         AIF   (T'&MAXLEN NE 'N').BYP70
SPFD&NDX DC    F'&MAXLEN'               MAXLEN
.BYP70   ANOP
.*
.*
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
.*
.*
         LA    14,LMGET                 GET ADDRESS OF LMGET KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BYP1010
         LA    15,&DATAID               GET ADDRESS OF DATAID NAME
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFZ&NDX              GET ADDRESS OF GENERATED DATAID
.BYP1050 ANOP
.*
.*
         AIF   ('&MODE' EQ '').BYP1100
         AIF   ('&MODE'(1,1) EQ '''').BYP1110
         LA    0,&MODE                  GET ADDRESS OF &MODE
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR MODE
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFA&NDX               GET GENNED MODE
.BYP1150 ANOP
.*
.*
         AIF   ('&DATALOC' EQ '').BYP1200
         AIF   ('&DATALOC'(1,1) EQ '''').BYP1210
         LA    1,&DATALOC               GET ADDRESS OF DATALOC VARIABLE
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR DATALOC
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFB&NDX               GET ADDRESS OF GENNED DATALOC
.BYP1250 ANOP
.*
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
.*
.*
.*
         AIF   ('&DATALEN' EQ '').BYP1300
         AIF   ('&DATALEN'(1,1) EQ '''').BYP1310
         LA    14,&DATALEN              GET ADDRESS OF DATALEN VARIABLE
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR DATALEN
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENNED DATALEN
.BYP1350 ANOP
.*
.*
         AIF   (T'&MAXLEN EQ 'N').BYP1410
         AIF   ('&MAXLEN' EQ '').BYP1400
         LA    15,&MAXLEN               GET ADDRESS OF MAXLEN
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR MAXLEN
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFD&NDX              GET ADDRESS OF GENNED MAXLEN
.BYP1450 ANOP
.*
.*
         STM   14,15,&TEMP+16           STORE PARAMETERS INTO LIST
.*
.*
.*
         OI    &TEMP+20,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 6).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 6).BYPVARS
&SPFVARS SETA  6
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
.*
.*
         DC    A(LMGET)                 GET ADDRESS OF LMGET KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BY1010
         DC    A(&DATAID)               GET ADDRESS OF DATAID NAME
         AGO   .BY1050
.BY1010 ANOP
         DC    A(SPFZ&NDX)              GET ADDRESS OF GENERATED DATAID
.BY1050 ANOP
.*
.*
         AIF   ('&MODE' EQ '').BY1100
         AIF   ('&MODE'(1,1) EQ '''').BY1110
         DC    A(&MODE)                 GET ADDRESS OF &MODE
         AGO   .BY1150
.BY1100 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR MODE
         AGO   .BY1150
.BY1110 ANOP
         DC    A(SPFA&NDX)              GET GENNED MODE
.BY1150 ANOP
.*
.*
         AIF   ('&DATALOC' EQ '').BY1200
         AIF   ('&DATALOC'(1,1) EQ '''').BY1210
         DC    A(&DATALOC)              GET ADDRESS OF DATALOC VARIABLE
         AGO   .BY1250
.BY1200 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR DATALOC
         AGO   .BY1250
.BY1210 ANOP
         DC    A(SPFB&NDX)              GET ADDRESS OF GENNED DATALOC
.BY1250 ANOP
.*
.*
         AIF   ('&DATALEN' EQ '').BY1300
         AIF   ('&DATALEN'(1,1) EQ '''').BY1310
         DC    A(&DATALEN)              GET ADDRESS OF DATALEN VARIABLE
         AGO   .BY1350
.BY1300 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR DATALEN
         AGO   .BY1350
.BY1310 ANOP
         DC    A(SPFC&NDX)              GET ADDRESS OF GENNED DATALEN
.BY1350 ANOP
.*
.*
         AIF   ('&MAXLEN' EQ '').BY1400
         AIF   (T'&MAXLEN EQ 'N').BY1410
         DC    A(&MAXLEN+X'80000000')   GET ADDRESS OF MAXLEN VARIABLE
         AGO   .BY1450
.BY1400 ANOP
         DC    A(SPFNULLC+X'80000000')  GET DUMMY PARM FOR MAXLEN
         AGO   .BY1450
.BY1410 ANOP
         DC    A(SPFD&NDX+X'80000000')  GET ADDRESS OF GENNED MAXLEN
.BY1450 ANOP
.*
.*
SPFE&NDX DS    0H
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'DATAID NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
