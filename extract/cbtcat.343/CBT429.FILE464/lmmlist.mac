         MACRO
&LABEL   LMMLIST &DATAID=,              DATAID NAME                    *
               &OPTION=,                OPTION OF INPUT OR OUTPUT      *
               &MEMBER=,                VARIABLE FOR MEMBER            *
               &STATS=,                 VARIABLE FOR STATS             *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFLMMLIST              GLOBAL FOR LMMLIST LITERAL
         GBLB  &SPFNULLC                DUMMY PARAMTER FOR CALL
         GBLB  &SPFDEBUG                DEBUG MODE
         GBLB  &SPFVAR                  NUMBER OF REQUIRED FULLWORDS
         GBLA  &SPFVARS                 NUMBER OF FULLWORDS
         GBLA  &SPFVARC                 PREVIOUS FULLWORDS USED
         LCLC  &NDX                     USED IN PLACE OF SYSNDX
         LCLC  &TEMP                    USED AS TEMPORARY VARIABLE
.*
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LABEL' EQ '').BYP10
&LABEL   DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&DATAID' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFLMMLIST).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&DATAID'(1,1) EQ '''').GENLIT
.*
         AIF   ('&OPTION' EQ '').CNT1
         AIF   ('&OPTION'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&MEMBER' EQ '').CNT2
         AIF   ('&MEMBER'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&STATS' EQ '').CNT3
         AIF   ('&STATS'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFNULLC).BYP001
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP001  ANOP
.*
.*
         AIF   (&SPFLMMLIST).BYP20
LMMLIST  DC    CL8'LMMLIST'             ISPF KEYWORD
&SPFLMMLIST SETB 1
.BYP20   ANOP
.*
.*
         AIF   ('&DATAID' EQ '').BYP30
         AIF   ('&DATAID'(1,1) NE '''').BYP30
SPFZ&NDX DC    CL8&DATAID               GENERATED DATAID NAME
.BYP30   ANOP
.*
.*
         AIF   ('&OPTION' EQ '').BYP40
         AIF   ('&OPTION'(1,1) NE '''').BYP40
SPFA&NDX DC    CL8&OPTION               OPTION TYPE
.BYP40   ANOP
.*
.*
         AIF   ('&MEMBER' EQ '').BYP50
         AIF   ('&MEMBER'(1,1) NE '''').BYP50
SPFB&NDX DC    CL8&MEMBER               MEMBER VARIABLE NAME
.BYP50   ANOP
.*
.*
         AIF   ('&STATS' EQ '').BYP60
         AIF   ('&STATS'(1,1) NE '''').BYP60
SPFC&NDX DC    CL8&STATS                STATS VARIABLE NAME
.BYP60   ANOP
.*
.*
.*
.*
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
.*
.*
         LA    14,LMMLIST               GET ADDRESS OF LMMLIST KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BYP1010
         LA    15,&DATAID               GET ADDRESS OF DATAID NAME
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFZ&NDX              GET ADDRESS OF GENERATED DATAID
.BYP1050 ANOP
.*
.*
         AIF   ('&OPTION' EQ '').BYP1100
         AIF   ('&OPTION'(1,1) EQ '''').BYP1110
         LA    0,&OPTION                GET ADDRESS OF &OPTION
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR OPTION
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFA&NDX               GENNED OPTION
.BYP1150 ANOP
.*
.*
         AIF   ('&MEMBER' EQ '').BYP1200
         AIF   ('&MEMBER'(1,1) EQ '''').BYP1210
         LA    1,&MEMBER                GET ADDRESS OF MEMBER VARIABLE
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR MEMBER
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFB&NDX               GET ADDRESS OF GENNED MEMBER
.BYP1250 ANOP
.*
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
.*
.*
.*
         AIF   ('&STATS' EQ '').BYP1300
         AIF   ('&STATS'(1,1) EQ '''').BYP1310
         LA    14,&STATS                GET ADDRESS OF STATS VARIABLE
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR STATS
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENNED STATS
.BYP1350 ANOP
.*
.*
         ST    14,&TEMP+16              STORE PARAMETERS INTO LIST
.*
.*
.*
         OI    &TEMP+16,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 5).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 5).BYPVARS
&SPFVARS SETA  5
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
.*
.*
         DC    A(LMMLIST)               GET ADDRESS OF LMMLIST KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BY1010
         DC    A(&DATAID)               GET ADDRESS OF DATAID NAME
         AGO   .BY1050
.BY1010 ANOP
         DC    A(SPFZ&NDX)              GET ADDRESS OF GENERATED DATAID
.BY1050 ANOP
.*
.*
         AIF   ('&OPTION' EQ '').BY1100
         AIF   ('&OPTION'(1,1) EQ '''').BY1110
         DC    A(&OPTION)               GET ADDRESS OF &OPTION
         AGO   .BY1150
.BY1100 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR OPTION
         AGO   .BY1150
.BY1110 ANOP
         DC    A(SPFA&NDX)              GENNED OPTION
.BY1150 ANOP
.*
.*
         AIF   ('&MEMBER' EQ '').BY1200
         AIF   ('&MEMBER'(1,1) EQ '''').BY1210
         DC    A(&MEMBER)               GET ADDRESS OF MEMBER VARIABLE
         AGO   .BY1250
.BY1200 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR MEMBER
         AGO   .BY1250
.BY1210 ANOP
         DC    A(SPFB&NDX)              GET ADDRESS OF GENNED MEMBER
.BY1250 ANOP
.*
.*
         AIF   ('&STATS' EQ '').BY1300
         AIF   ('&STATS'(1,1) EQ '''').BY1310
         DC    A(&STATS+X'80000000')    GET ADDRESS OF STATS VARIABLE
         AGO   .BY1350
.BY1300 ANOP
         DC    A(SPFNULLC+X'80000000')  GET DUMMY PARM FOR STATS
         AGO   .BY1350
.BY1310 ANOP
         DC    A(SPFC&NDX+X'80000000')  GET ADDRESS OF GENNED STATS
.BY1350 ANOP
.*
.*
SPFE&NDX DS    0H
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'DATAID NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID OPTION - EXPECTED INPUT OR OUTPUT - FOUND ''*
               &OPTION'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
