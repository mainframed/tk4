         MACRO
&LBL     TBCREATE &TABLE,               TABLE NAME                     *
               &P2,                                                    *
               &P3,                                                    *
               &P4,                                                    *
               &P5,                                                    *
               &P6,                                                    *
               &P7,                                                    *
               &KEYS=,                  KEYS NAME LIST                 *
               &NAMES=,                 NAMES NAME LIST                *
               &WRITE=,                 WRITE NOWRITE KEYWORD          *
               &REPLACE=,               REPLACE KEYWORD                *
               &LIBRARY=,               LIBRARY                        *
               &SHARE=,                 SHARE STATUS                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBCREATE,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @TBCREAT &TABLE,&P2,&P3,&P4,&P5,&P6,&P7,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P2' NE '').LVLERR
         AIF   ('&P3' NE '').LVLERR
         AIF   ('&P4' NE '').LVLERR
         AIF   ('&P5' NE '').LVLERR
         AIF   ('&P6' NE '').LVLERR
         AIF   ('&P7' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LBL' EQ '').BYP10
&LBL     DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&REPLACE' EQ '').K1
         AIF   ('&REPLACE'(1,1) NE '''').K1
         AIF   ('&REPLACE' EQ '''REPLACE''').K1
         MNOTE 4,'WARNING - REPLACE KEYWORD EXPECTED IN QUOTES'
.K1      ANOP
         AIF   ('&SHARE' EQ '').K2
         AIF   ('&SHARE'(1,1) NE '''' OR '&SHARE' EQ '''SHARE''').K2
         MNOTE 4,'WARNING - SHARE KEYWORD EXPECTED IN QUOTES'
.K2      ANOP
         AIF   ('&WRITE' EQ '').K3
         AIF   ('&WRITE'(1,1) NE '''').K3
         AIF   ('&WRITE' EQ '''WRITE''').K3
         AIF   ('&WRITE' EQ '''NOWRITE''').K3
         MNOTE 4,'WARNING - WRITE KEYWORD EXPECTED WRITE OR NOWRITE IN *
               QUOTES'
.K3      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBCREATE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&KEYS' EQ '').CNT2
         AIF   ('&KEYS'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&NAMES' EQ '').CNT3
         AIF   ('&NAMES'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&WRITE' EQ '').CNT4
         AIF   ('&WRITE'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&REPLACE' EQ '').CNT5
         AIF   ('&REPLACE'(1,1) EQ '''').GENLIT
.CNT5    ANOP
         AIF   ('&LIBRARY' EQ '').CNT6
         AIF   ('&LIBRARY'(1,1) EQ '''').GENLIT
.CNT6    ANOP
         AIF   ('&SHARE' EQ '').CNT7
         AIF   ('&SHARE'(1,1) EQ '''').GENLIT
.CNT7    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBCREATE).BYP20
TBCREATE DC    CL8'TBCREATE'            ISPF KEYWORD
&SPFTBCREATE SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&KEYS' EQ '').BYP40
         AIF   ('&KEYS'(1,1) NE '''').BYP40
SPFK&NDX DC    C&KEYS                   GENERATED KEYS NAME LIST
.BYP40   ANOP
         AIF   ('&NAMES' EQ '').BYP50
         AIF   ('&NAMES'(1,1) NE '''').BYP50
SPFN&NDX DC    C&NAMES                  GENERATED NAMELIST
.BYP50   ANOP
         AIF   ('&WRITE' EQ '').BYP60
         AIF   ('&WRITE'(1,1) NE '''').BYP60
SPFW&NDX DC    CL8&WRITE                GENERATED WRITE KEYWORD
.BYP60   ANOP
         AIF   ('&REPLACE' EQ '').BYP70
         AIF   ('&REPLACE'(1,1) NE '''').BYP70
SPFR&NDX DC    CL8&REPLACE              GENERATED REPLACE KEYWORD
.BYP70   ANOP
         AIF   ('&LIBRARY' EQ '').BYP80
         AIF   ('&LIBRARY'(1,1) NE '''').BYP80
SPFL&NDX DC    CL8&LIBRARY              GENERATED LIBRARY
.BYP80   ANOP
         AIF   ('&SHARE' EQ '').BYP90
         AIF   ('&SHARE'(1,1) NE '''').BYP90
SPFS&NDX DC    CL8&SHARE                GENERATED SHARE KEYWORD
.BYP90   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBCREATE              GET ADDRESS OF TBCREATE KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&KEYS' EQ '').BYP1100
         AIF   ('&KEYS'(1,1) EQ '''').BYP1110
         LA    0,&KEYS                  GET ADDRESS OF KEYS NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR KEYS
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFK&NDX               GET ADDRESS OF GENERATED KEYS
.BYP1150 ANOP
.*
         AIF   ('&NAMES' EQ '').BYP1200
         AIF   ('&NAMES'(1,1) EQ '''').BYP1210
         LA    1,&NAMES                 GET ADDRESS OF NAME NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR NAME
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFN&NDX               GET ADDRESS OF GENERATED NAME
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&WRITE' EQ '').BYP1300
         AIF   ('&WRITE'(1,1) EQ '''').BYP1305
         LA    14,&WRITE                GET ADDRESS OF WRITE NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR WRITE
         AGO   .BYP1350
.BYP1305 ANOP
         LA    14,SPFW&NDX              GET GENNED WRITE KEYWORD
.BYP1350 ANOP
.*
         AIF   ('&REPLACE' EQ '').BYP1400
         AIF   ('&REPLACE'(1,1) EQ '''').BYP1405
         LA    15,&REPLACE              GET ADDRESS OF REPLACE KEYWORD
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR REPLACE
         AGO   .BYP1450
.BYP1405 ANOP
         LA    15,SPFR&NDX              GET GENNED REPLACE KEYWORD
.BYP1450 ANOP
.*
         AIF   ('&LIBRARY' EQ '').BYP1500
         AIF   ('&LIBRARY'(1,1) EQ '''').BYP1510
         LA    0,&LIBRARY               GET ADDRESS OF LIB NAME
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR LIB
         AGO   .BYP1550
.BYP1510 ANOP
         LA    0,SPFL&NDX               GET ADDRESS OF GENERATED LIB
.BYP1550 ANOP
.*
         AIF   ('&SHARE' EQ '').BYP1600
         AIF   ('&SHARE'(1,1) EQ '''').BYP1605
         LA    1,&SHARE                 GET ADDRESS OF SHARE NAME
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR SHARE
         AGO   .BYP1650
.BYP1605 ANOP
         LA    1,SPFS&NDX               GET GENNED SHARE KEYWORD
.BYP1650 ANOP
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
         OI    &TEMP+28,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 8).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 8).BYPVARS
&SPFVARS SETA  8
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBCREATE)              TBCREATE LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&KEYS' EQ '').BYP2100
         AIF   ('&KEYS'(1,1) EQ '''').BYP2110
         DC    A(&KEYS)                 ADDRESS OF KEYS NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR KEYS NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFK&NDX)              ADDRESS OF GENERATED KEYS
.BYP2150 ANOP
.*
         AIF   ('&NAMES' EQ '').BYP2200
         AIF   ('&NAMES'(1,1) EQ '''').BYP2210
         DC    A(&NAMES)                ADDRESS OF NAME LIST
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR NAME
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFN&NDX)              ADDRESS OF GENERATED NAME
.BYP2250 ANOP
.*
         AIF   ('&WRITE' EQ '').BYP2300
         AIF   ('&WRITE'(1,1) EQ '''').BYP2305
         DC    A(&WRITE)                ADDRESS OF WRITE KEYWORD
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR WRITE
         AGO   .BYP2350
.BYP2305 ANOP
         DC    A(SPFW&NDX)              GENNED WRITE KEYWORD
.BYP2350 ANOP
.*
         AIF   ('&REPLACE' EQ '').BYP2400
         AIF   ('&REPLACE'(1,1) EQ '''').BYP2405
         DC    A(&REPLACE)              ADDRESS OF REPLACE KEYWORD
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR REPLACE
         AGO   .BYP2450
.BYP2405 ANOP
         DC    A(SPFR&NDX)              GENNED REPLACE KEYWORD
.BYP2450 ANOP
.*
         AIF   ('&LIBRARY' EQ '').BYP2500
         AIF   ('&LIBRARY'(1,1) EQ '''').BYP2510
         DC    A(&LIBRARY)              ADDRESS OF LIB NAME
         AGO   .BYP2550
.BYP2500 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR LIB
         AGO   .BYP2550
.BYP2510 ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LIB
.BYP2550 ANOP
.*
         AIF   ('&SHARE' EQ '').BYP2800
         AIF   ('&SHARE'(1,1) EQ '''').BYP2805
         DC    A(&SHARE+X'80000000')    ADDRESS OF SHARE KEYWORD
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR SHARE
         AGO   .BYP2850
.BYP2805 ANOP
         DC    A(SPFS&NDX+X'80000000')  GENNED SHARE KEYWORD
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'REPLACE KEYWORD EXPECTED - FOUND ''&REP'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'SHARE KEYWORD EXPECTED - FOUND ''&SHARE'''
         MEXIT
.ERR4    ANOP
         MNOTE 16,'WRITE/NOWRITE KEYWORD EXPECTED - FOUND ''&WRITE'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
