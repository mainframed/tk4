         MACRO
&NAME    TBTOP &TABLE,                  TABLE NAME                     *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBTOP,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&TABLE' EQ '').ERR3
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBTOP).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBTOP).BYP20
TBTOP DC       CL8'TBTOP'               ISPF KEYWORD
&SPFTBTOP SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP40
         AIF   ('&TABLE'(1,1) NE '''').BYP40
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP40   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBTOP                 GET ADDRESS OF TBTOP KEYWORD
         AIF   ('&TABLE' EQ '').BYP100
         AIF   ('&TABLE'(1,1) EQ '''').BYP110
         LA    15,&TABLE                GET ADDRESS OF TABLE
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
.BYP120  ANOP
         STM   14,15,&TEMP              STORE PARAMETERS
         OI    &TEMP+4,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 2).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 2).BYPVARS
&SPFVARS SETA  2
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBTOP)                 BROWSE LITERAL
         AIF   ('&TABLE' EQ '').BYP200
         AIF   ('&TABLE'(1,1) EQ '''').BYP210
         DC    A(&TABLE+X'80000000')    ADDRESS OF TABLE
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFT&NDX+X'80000000')  ADDRESS OF GENERATED TABLE
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR TABLE
.BYP220  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
