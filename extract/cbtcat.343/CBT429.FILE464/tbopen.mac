         MACRO
&NAME    TBOPEN &TABLE,                 TABLE NAME                     *
               &P2,                                                    *
               &P3,                                                    *
               &P4,                                                    *
               &WRITE=,                 WRITE/NOWRITE PARM             *
               &LIBRARY=,               LIBRARY                        *
               &SHARE=,                 SHARE OPERAND                  *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBOPEN,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLB  &SPFSHARE
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @TBOPEN &TABLE,&P2,&P3,&P4,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P2' NE '').LVLERR
         AIF   ('&P3' NE '').LVLERR
         AIF   ('&P4' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&WRITE' EQ '').K3
         AIF   ('&WRITE'(1,1) NE '''').K3
         AIF   ('&WRITE' EQ '''WRITE''').K3
         AIF   ('&WRITE' EQ '''NOWRITE''').K3
         MNOTE 4,'WARNING - WRITE KEYWORD EXPECTED WRITE OR NOWRITE IN *
               QUOTES'
.K3      ANOP
         AIF   ('&SHARE' EQ '').K2
         AIF   ('&SHARE'(1,1) NE '''').K2
         AIF   ('&SHARE' EQ '''SHARE''').K2
         MNOTE 4,'WARNING - SHARE KEYWORD EXPECTED ENCLOSED IN QUOTES'
.K2      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBOPEN).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&WRITE' EQ '').CNT2
         AIF   ('&WRITE'(1,1) EQ '''').GENLIT
.CNT2    AIF   ('&LIBRARY' EQ '').CNT3
         AIF   ('&LIBRARY'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&SHARE' EQ '').CNT4
         AIF   ('&SHARE'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBOPEN).BYP20
TBOPEN   DC    CL8'TBOPEN'              ISPF KEYWORD
&SPFTBOPEN SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&WRITE' EQ '').BYP40
         AIF   ('&WRITE'(1,1) NE '''').BYP40
SPFW&NDX DC    CL8&WRITE                ISPF KEYWORD
.BYP40   ANOP
         AIF   ('&LIBRARY' EQ '').BYP50
         AIF   ('&LIBRARY'(1,1) NE '''').BYP50
SPFL&NDX DC    CL8&LIBRARY              GENERATED LIBRARY NAME
.BYP50   ANOP
         AIF   ('&SHARE' EQ '').BYP60
         AIF   ('&SHARE'(1,1) NE '''').BYP60
SPFX&NDX DC    CL8&SHARE                ISPF KEYWORD
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBOPEN                GET ADDRESS OF TBOPEN KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&WRITE' EQ '').BYP1100
         AIF   ('&WRITE'(1,1) EQ '''').BYP1105
         LA    0,&WRITE                 GET ADDRESS OF WRITE NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR WRITE
         AGO   .BYP1150
.BYP1105 ANOP
         LA    0,SPFW&NDX               GENNED WRITE KEYWORD
.BYP1150 ANOP
.*
         AIF   ('&LIBRARY' EQ '').BYP1200
         AIF   ('&LIBRARY'(1,1) EQ '''').BYP1210
         LA    1,&LIBRARY               GET ADDRESS OF LIBRARY NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR LIBRARY
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFL&NDX               GET ADDRESS OF GENERATED LIB
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&SHARE' EQ '').BYP1300
         AIF   ('&SHARE'(1,1) EQ '''').BYP1305
         LA    14,&SHARE                GET ADDRESS OF FIELD NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR FIELD
         AGO   .BYP1350
.BYP1305 ANOP
         LA    14,SPFX&NDX              GENNED SHARE KEYWORD
.BYP1350 ANOP
.*
         ST    14,&TEMP+16              STORE PARAMETERS INTO LIST
         OI    &TEMP+16,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 5).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 5).BYPVARS
&SPFVARS SETA  5
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBOPEN)                TBOPEN LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&WRITE' EQ '').BYP2100
         AIF   ('&WRITE'(1,1) EQ '''').BYP2105
         DC    A(&WRITE)                ADDRESS OF WRITE NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR WRITE NAME
         AGO   .BYP2150
.BYP2105 ANOP
         DC    A(SPFW&NDX)              WRITE KEYWORD
.BYP2150 ANOP
.*
         AIF   ('&LIBRARY' EQ '').BYP2200
         AIF   ('&LIBRARY'(1,1) EQ '''').BYP2210
         DC    A(&LIBRARY)              ADDRESS OF LIBRARY NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR LIBRARY
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LIBRARY
.BYP2250 ANOP
.*
         AIF   ('&SHARE' EQ '').BYP2300
         AIF   ('&SHARE'(1,1) EQ '''').BYP2305
         DC    A(&SHARE+X'80000000')    ADDRESS OF SHARE NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR SHARE
         AGO   .BYP2350
.BYP2305 ANOP
         DC    A(SPFX&NDX+X'80000000')  GENNED SHARE KEYWORD
.BYP2350 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'SHARE KEYWORD EXPECTED - FOUND ''&SHARE'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'WRITE NOWRITE KEYWORD EXPECTED - FOUND ''&WRITE'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
