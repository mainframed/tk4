         MACRO
&LABEL   LMFREE &DATAID=,               DATAID NAME                    *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFLMFREE               GLOBAL FOR LMFREE LITERAL
         GBLB  &SPFNULLC                DUMMY PARAMTER FOR CALL
         GBLB  &SPFDEBUG                DEBUG MODE
         GBLB  &SPFVAR                  NUMBER OF REQUIRED FULLWORDS
         GBLA  &SPFVARS                 NUMBER OF FULLWORDS
         GBLA  &SPFVARC                 PREVIOUS FULLWORDS USED
         LCLC  &NDX                     USED IN PLACE OF SYSNDX
         LCLC  &TEMP                    USED AS TEMPORARY VARIABLE
.*
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LABEL' EQ '').BYP10
&LABEL   DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&DATAID' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFLMFREE).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&DATAID'(1,1) EQ '''').GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFNULLC).BYP001
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP001  ANOP
.*
.*
         AIF   (&SPFLMFREE).BYP20
LMFREE   DC    CL8'LMFREE'              ISPF KEYWORD
&SPFLMFREE SETB 1
.BYP20   ANOP
.*
.*
         AIF   ('&DATAID' EQ '').BYP30
         AIF   ('&DATAID'(1,1) NE '''').BYP30
SPFZ&NDX DC    CL8&DATAID               GENERATED DATAID NAME
.BYP30   ANOP
.*
.*
.*
.*
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
.*
.*
         LA    14,LMFREE                GET ADDRESS OF LMFREE KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BYP1010
         LA    15,&DATAID               GET ADDRESS OF DATAID NAME
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFZ&NDX              GET ADDRESS OF GENERATED DATAID
.BYP1050 ANOP
.*
.*
         STM   14,15,&TEMP              STORE PARAMETERS INTO LIST
.*
.*
.*
         OI    &TEMP+4,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 2).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 2).BYPVARS
&SPFVARS SETA  2
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
.*
.*
         DC    A(LMFREE)                GET ADDRESS OF LMFREE KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BY1010
         DC    A(&DATAID+X'80000000')   GET ADDRESS OF DATAID NAME
         AGO   .BY1050
.BY1010 ANOP
         DC    A(SPFZ&NDX+X'80000000')  GET ADDRESS OF GENERATED DATAID
.BY1050 ANOP
.*
.*
.*
SPFE&NDX DS    0H
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'DATAID NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
