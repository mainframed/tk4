         MACRO
&NAME    @FTINCL &SKEL,                 MEMBER NAME                    *
               &NOFTL,                  NOFT KEYWORD                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFFTINCL,&SPFDEBUG,&SPFVAR,&SPFNOFT
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&NOFTL' NE '' AND                                     *
               '&NOFTL' NE 'NOFT').ERR1
         AIF   ('&SKEL' EQ '').ERR3
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFFTINCL).GENLIT
         AIF   ('&SKEL' EQ '').CNT1
         AIF   ('&SKEL'(1,1) EQ '''').GENLIT
.CNT1    AIF   ('&NOFTL' NE '' AND NOT &SPFNOFT).GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFFTINCL).BYP20
FTINCL   DC    CL8'FTINCL'              ISPF KEYWORD
&SPFFTINCL SETB 1
.BYP20   ANOP
         AIF   ('&SKEL' EQ '').BYP40
         AIF   ('&SKEL'(1,1) NE '''').BYP40
SPFS&NDX DC    CL8&SKEL                 GENERATED MEMBER NAME
.BYP40   ANOP
         AIF   ('&NOFTL' EQ '').BYP60
         AIF   (&SPFNOFT).BYP60
NOFT     DC    CL8'NOFT'                ISPF KEYWORD
&SPFNOFT SETB  1
.BYP60   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,FTINCL                GET ADDRESS OF FTINCL KEYWORD
         AIF   ('&SKEL' EQ '').BYP100
         AIF   ('&SKEL'(1,1) EQ '''').BYP110
         LA    15,&SKEL                 GET ADDRESS OF MEMBER
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFS&NDX              GET ADDRESS OF GENERATED MEMBER
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR MEMBER
.BYP120  ANOP
         AIF   ('&NOFTL' EQ '').BYP170
         LA    0,NOFT                   GET ADDRESS OF NOFT
         AGO   .BYP180
.BYP170  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR NOFT
.BYP180  ANOP
         STM   14,0,&TEMP               STORE PARAMETERS
         OI    &TEMP+8,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 3).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 3).BYPVARS
&SPFVARS SETA  3
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(FTINCL)                BROWSE LITERAL
         AIF   ('&SKEL' EQ '').BYP200
         AIF   ('&SKEL'(1,1) EQ '''').BYP210
         DC    A(&SKEL)                 ADDRESS OF MEMBER
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFS&NDX)              ADDRESS OF GENERATED MEMBER
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MEMBER
.BYP220  ANOP
         AIF   ('&NOFTL' EQ '').BYP270
         DC    A(NOFT+X'80000000')      ADDRESS OF NOFT KEYWORD
         AGO   .BYP300
.BYP270  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR NOFT
.BYP300  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'INVALID VALUE FOR NOFT PARAMETER - FOUND ''&NOFTL'''
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'SKELETON NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
