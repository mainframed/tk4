         MACRO
&NAME    CONTROL &TYPE,                 CONTROL TYPE                   *
               &MODE,                   MODE WITHIN TYPE               *
               &LINENUM,                LINENUM OF LINE START FUNC     *
               &MF=I,                   MACRO FORMAT                   *
               &LOAD=ISPLINK            ADDRESS OF ISPF MODULE
.*
.*********************************************************************
.*       GLOBALS
.*********************************************************************
.*
         GBLB  &SPFCONTROL,&SPFDISPLAY,&SPFNONDISPL
         GBLB  &SPFLOCK,&SPFLINE,&SPFSM,&SPFREFRESH
         GBLB  &SPFENTER,&SPFEND,&SPFCANCEL,&SPFRETURN
         GBLB  &SPFNULLF,&SPFVAR
         GBLB  &SPFERRORS,&SPFSPLIT,&SPFNOCMD
         GBLB  &SPFSAVE,&SPFRESTORE
         GBLB  &SPFENABLE,&SPFDISABLE,&SPFDEBUG
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYPTEMA
&TEMP    SETC  'SPFPARMS'
         AGO   .BYPTEMB
.BYPTEMA ANOP
&TEMP    SETC  '&MF(2)'
.BYPTEMB ANOP
         AIF   ('&NAME' EQ '').BYP0
&NAME    DS    0H
.BYP0    ANOP
.*
.*********************************************************************
.*       GENERAL EDITS
.*********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
.*
.*********************************************************************
.*       EDIT THE &TYPE FIELD
.*********************************************************************
.*
         AIF   ('&TYPE' EQ 'DISPLAY').DISP
         AIF   ('&TYPE' EQ 'NONDISPL').NOND
         AIF   ('&TYPE' EQ 'ERRORS').ERRO
         AIF   ('&TYPE' EQ 'SPLIT').SPLI
         AIF   ('&TYPE' EQ 'NOCMD').NOCM
         AGO   .ERR1
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY
.*********************************************************************
.*
.DISP    ANOP
         AIF   ('&MODE' EQ 'LOCK').DISPLOCK
         AIF   ('&MODE' EQ 'LINE').DISPLINE
         AIF   ('&MODE' EQ 'SM').DISPSM
         AIF   ('&MODE' EQ 'REFRESH').DISPREFR
         AIF   ('&MODE' EQ 'SAVE').DISPSAVE
         AIF   ('&MODE' EQ 'RESTORE').DISPREST
         AGO   .ERR2
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY LOCK
.*********************************************************************
.*
.DISPLOCK ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN1
         AIF   (NOT &SPFDISPLAY).GEN1
         AIF   (NOT &SPFLOCK).GEN1
         AGO   .BYPLIT1
.GEN1    ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYP10
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYP10   ANOP
         AIF   (&SPFDISPLAY).BYP20
&SPFDISPLAY SETB 1
DISPLAY  DC    CL8'DISPLAY'             CONTROL KEYWORD FOR ISPF
.BYP20   ANOP
         AIF   (&SPFLOCK).BYP30
&SPFLOCK SETB  1
LOCK     DC    CL8'LOCK'                CONTROL KEYWORD FOR ISPF
.BYP30   ANOP
SPF&NDX  DS    0H
.BYPLIT1 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY LOCK   CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').DISMF1
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,DISPLAY               GET ADDRESS OF DISPLAY OPERAND
         LA    0,LOCK                   GET ADDRESS OF LOCK OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR1
         AIF   (&SPFVARC LT 3).ERRVAR
.BYVAR1  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
.BYPVAR1 ANOP
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY LOCK   CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.DISMF1  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(DISPLAY)               ADDRESS OF DISPLAY OPERAND
         DC    A(LOCK+X'80000000')      ADDRESS OF LOCK OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY LINE
.*********************************************************************
.*
.DISPLINE ANOP
         AIF   (NOT &SPFCONTROL).GEN2
         AIF   (NOT &SPFDISPLAY).GEN2
         AIF   (NOT &SPFLINE).GEN2
         AIF   (T'&LINENUM EQ 'N').GEN2
         AIF   ('&LINENUM' EQ '' AND NOT &SPFNULLF).GEN2
         AGO   .BYPLIT2
.GEN2    ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYP110
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYP110  ANOP
         AIF   (&SPFDISPLAY).BYP120
&SPFDISPLAY SETB 1
DISPLAY  DC    CL8'DISPLAY'             CONTROL KEYWORD FOR ISPF
.BYP120  ANOP
         AIF   (&SPFLINE).BYP130
&SPFLINE SETB  1
LINE     DC    CL8'LINE'                CONTROL KEYWORD FOR ISPF
.BYP130  ANOP
         AIF   (T'&LINENUM NE 'N').BYP140
SPFL&NDX DC    F'&LINENUM'              GENERATED SCREEN LINE NUMBER
         AGO   .BYP150
.BYP140  ANOP
         AIF   ('&LINENUM' NE '').BYP150
         AIF   (&SPFNULLF).BYP150
&SPFNULLF SETB 1
SPFNULLF DC    F'0'                     ISPF DUMMY FULLWORD GENERATED
.BYP150  ANOP
SPF&NDX  DS    0H
.BYPLIT2 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY LINE   CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').DISMF2
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,DISPLAY               GET ADDRESS OF DISPLAY OPERAND
         LA    0,LINE                   GET ADDRESS OF LINE OPERAND
         AIF   (T'&LINENUM EQ 'N').BYP160
         AIF   ('&LINENUM' EQ '').BYP170
         LA    1,&LINENUM               GET ADDRESS OF LINE NUMBER
         AGO   .BYP180
.BYP160  ANOP
         LA    1,SPFL&NDX               GET ADDRESS OF GENERATED LINE #
         AGO   .BYP180
.BYP170  ANOP
         LA    1,SPFNULLF               GET DUMMY FULLWORD FOR LINE #
.BYP180  ANOP
         STM   14,1,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+12,X'80'           SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR2
         AIF   (&SPFVARC LT 4).ERRVAR
.BYVAR2  AIF   (&SPFVARS GE 4).BYPVAR1
&SPFVARS SETA  4
         AGO   .BYPVAR1
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY LINE   CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.DISMF2  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(DISPLAY)               ADDRESS OF DISPLAY OPERAND
         DC    A(LINE)                  ADDRESS OF LINE OPERAND
         AIF   (T'&LINENUM EQ 'N').BYP185
         AIF   ('&LINENUM' EQ '').BYP190
         DC    A(&LINENUM+X'80000000')  ADDRESS OF LINE NUMBER
         AGO   .BYP195
.BYP185  ANOP
         DC    A(SPFL&NDX+X'80000000')  ADDRESS OF GENERATED LINE #
         AGO   .BYP195
.BYP190  ANOP
         DC    A(SPFNULLF+X'80000000')  DUMMY FULLWORD FOR LINE #
.BYP195  ANOP
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY SM
.*********************************************************************
.*
.DISPSM  ANOP
         AIF   (NOT &SPFCONTROL).GEN3
         AIF   (NOT &SPFDISPLAY).GEN3
         AIF   (NOT &SPFSM).GEN3
         AIF   (T'&LINENUM EQ 'N').GEN3
         AIF   ('&LINENUM' EQ '' AND NOT &SPFNULLF).GEN3
         AGO   .BYPLIT3
.GEN3    ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYP210
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYP210  ANOP
         AIF   (&SPFDISPLAY).BYP220
&SPFDISPLAY SETB 1
DISPLAY  DC    CL8'DISPLAY'             CONTROL KEYWORD FOR ISPF
.BYP220  ANOP
         AIF   (&SPFSM).BYP230
&SPFSM   SETB  1
SM       DC    CL8'SM'                  CONTROL KEYWORD FOR ISPF
.BYP230  ANOP
         AIF   (T'&LINENUM NE 'N').BYP240
SPFL&NDX DC    F'&LINENUM'              GENERATED SCREEN LINE NUMBER
         AGO   .BYP250
.BYP240  ANOP
         AIF   ('&LINENUM' NE '').BYP250
         AIF   (&SPFNULLF).BYP250
&SPFNULLF SETB 1
SPFNULLF DC    F'0'                     ISPF DUMMY FULLWORD GENERATED
.BYP250  ANOP
SPF&NDX  DS    0H
.BYPLIT3 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY SM     CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').DISMF3
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,DISPLAY               GET ADDRESS OF DISPLAY OPERAND
         LA    0,SM                     GET ADDRESS OF SM OPERAND
         AIF   (T'&LINENUM EQ 'N').BYP260
         AIF   ('&LINENUM' EQ '').BYP270
         LA    1,&LINENUM               GET ADDRESS OF LINE NUMBER
         AGO   .BYP280
.BYP260  ANOP
         LA    1,SPFL&NDX               GET ADDRESS OF GENERATED LINE #
         AGO   .BYP280
.BYP270  ANOP
         LA    1,SPFNULLF               GET DUMMY FULLWORD FOR LINE #
.BYP280  ANOP
         STM   14,1,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+12,X'80'           SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR3
         AIF   (&SPFVARC LT 4).ERRVAR
.BYVAR3  AIF   (&SPFVARS GE 4).BYPVAR1
&SPFVARS SETA  4
         AGO   .BYPVAR1
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY SM     CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.DISMF3  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(DISPLAY)               ADDRESS OF DISPLAY OPERAND
         DC    A(SM)                    ADDRESS OF SM OPERAND
         AIF   (T'&LINENUM EQ 'N').BYP285
         AIF   ('&LINENUM' EQ '').BYP290
         DC    A(&LINENUM+X'80000000')  ADDRESS OF LINE NUMBER
         AGO   .BYP295
.BYP285  ANOP
         DC    A(SPFL&NDX+X'80000000')  ADDRESS OF GENERATED LINE #
         AGO   .BYP295
.BYP290  ANOP
         DC    A(SPFNULLF+X'80000000')  DUMMY FULLWORD FOR LINE #
.BYP295  ANOP
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY REFRESH
.*********************************************************************
.*
.DISPREFR ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN4
         AIF   (NOT &SPFDISPLAY).GEN4
         AIF   (NOT &SPFREFRESH).GEN4
         AGO   .BYPLIT4
.GEN4    ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYP310
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYP310  ANOP
         AIF   (&SPFDISPLAY).BYP320
&SPFDISPLAY SETB 1
DISPLAY  DC    CL8'DISPLAY'             CONTROL KEYWORD FOR ISPF
.BYP320  ANOP
         AIF   (&SPFREFRESH).BYP330
&SPFREFRESH SETB 1
REFRESH  DC    CL8'REFRESH'             CONTROL KEYWORD FOR ISPF
.BYP330  ANOP
SPF&NDX  DS    0H
.BYPLIT4 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY REFRESH CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').DISMF4
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,DISPLAY               GET ADDRESS OF DISPLAY OPERAND
         LA    0,REFRESH                GET ADDRESS OF REFRESH OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR4
         AIF   (&SPFVARC LT 3).ERRVAR
.BYVAR4  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY REFRESH CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.DISMF4  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(DISPLAY)               ADDRESS OF DISPLAY OPERAND
         DC    A(REFRESH+X'80000000')   ADDRESS OF REFRESH OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY SAVE
.*********************************************************************
.*
.DISPSAVE ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN5
         AIF   (NOT &SPFDISPLAY).GEN5
         AIF   (NOT &SPFSAVE).GEN5
         AGO   .BYPLIT5
.GEN5    ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYP410
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYP410  ANOP
         AIF   (&SPFDISPLAY).BYP420
&SPFDISPLAY SETB 1
DISPLAY  DC    CL8'DISPLAY'             CONTROL KEYWORD FOR ISPF
.BYP420  ANOP
         AIF   (&SPFSAVE).BYP430
&SPFSAVE SETB  1
SAVE     DC    CL8'SAVE'                CONTROL KEYWORD FOR ISPF
.BYP430  ANOP
SPF&NDX  DS    0H
.BYPLIT5 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY SAVE   CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').DISMF5
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,DISPLAY               GET ADDRESS OF DISPLAY OPERAND
         LA    0,SAVE                   GET ADDRESS OF SAVE OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR5
         AIF   (&SPFVARC LT 3).ERRVAR
.BYVAR5  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY SAVE   CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.DISMF5  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(DISPLAY)               ADDRESS OF DISPLAY OPERAND
         DC    A(SAVE+X'80000000')      ADDRESS OF SAVE OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY RESTORE
.*********************************************************************
.*
.DISPREST ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN6
         AIF   (NOT &SPFDISPLAY).GEN6
         AIF   (NOT &SPFRESTORE).GEN6
         AGO   .BYPLIT6
.GEN6    ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYP510
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYP510  ANOP
         AIF   (&SPFDISPLAY).BYP520
&SPFDISPLAY SETB 1
DISPLAY  DC    CL8'DISPLAY'             CONTROL KEYWORD FOR ISPF
.BYP520  ANOP
         AIF   (&SPFRESTORE).BYP530
&SPFRESTORE SETB 1
RESTORE  DC    CL8'RESTORE'             CONTROL KEYWORD FOR ISPF
.BYP530  ANOP
SPF&NDX  DS    0H
.BYPLIT6 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY RESTORE CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').DISMF6
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,DISPLAY               GET ADDRESS OF DISPLAY OPERAND
         LA    0,RESTORE                GET ADDRESS OF RESTORE OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR6
         AIF   (&SPFVARC LT 3).ERRVAR
.BYVAR6  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
.*
.*********************************************************************
.*       ISPEXEC CONTROL DISPLAY RESTORE CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.DISMF6  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(DISPLAY)               ADDRESS OF DISPLAY OPERAND
         DC    A(RESTORE+X'80000000')   ADDRESS OF RESTORE OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL NONDISPL
.*********************************************************************
.*
.NOND    ANOP
         AIF   ('&MODE' EQ '').NONDENTE
         AIF   ('&MODE' EQ 'ENTER').NONDENTE
         AIF   ('&MODE' EQ 'END').NONDEND
         AGO   .ERR2
.*
.*********************************************************************
.*       ISPEXEC CONTROL NONDISPL ENTER
.*********************************************************************
.*
.NONDENTE ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN10
         AIF   (NOT &SPFNONDISPL).GEN10
         AIF   (NOT &SPFENTER).GEN10
         AGO   .BYPLIT10
.GEN10   ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYA10
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYA10   ANOP
         AIF   (&SPFNONDISPL).BYA20
&SPFNONDISPL SETB 1
NONDISPL DC    CL8'NONDISPL'            CONTROL KEYWORD FOR ISPF
.BYA20   ANOP
         AIF   (&SPFENTER).BYA30
&SPFENTER SETB 1
ENTER    DC    CL8'ENTER'               CONTROL KEYWORD FOR ISPF
.BYA30   ANOP
SPF&NDX  DS    0H
.BYPLIT10 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL NONDISPL ENTER CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').NONMF1
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,NONDISPL              GET ADDRESS OF NONDISPL
         LA    0,ENTER                  GET ADDRESS OF ENTER OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BAVAR1
         AIF   (&SPFVARC LT 3).ERRVAR
.BAVAR1  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL NONDISP ENTER  CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.NONMF1  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(NONDISPL)              ADDRESS OF NONDISPL OPERAND
         DC    A(ENTER+X'80000000')     ADDRESS OF ENTER OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL NONDISPL END
.*********************************************************************
.*
.NONDEND ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN20
         AIF   (NOT &SPFNONDISPL).GEN20
         AIF   (NOT &SPFEND).GEN20
         AGO   .BYPLIT20
.GEN20   ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYB10
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYB10   ANOP
         AIF   (&SPFNONDISPL).BYB20
&SPFNONDISPL SETB 1
NONDISPL DC    CL8'NONDISPL'            CONTROL KEYWORD FOR ISPF
.BYB20   ANOP
         AIF   (&SPFEND).BYB30
&SPFEND  SETB  1
END      DC    CL8'END'                 CONTROL KEYWORD FOR ISPF
.BYB30   ANOP
SPF&NDX  DS    0H
.BYPLIT20 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL NONDISPL END   CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').NONMF2
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,NONDISPL              GET ADDRESS OF NONDISPL
         LA    0,END                    GET ADDRESS OF END OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BAVAR2
         AIF   (&SPFVARC LT 3).ERRVAR
.BAVAR2  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL NONDISP END    CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.NONMF2  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(NONDISPL)              ADDRESS OF NONDISPL OPERAND
         DC    A(END+X'80000000')       ADDRESS OF END OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL ERRORS
.*********************************************************************
.*
.ERRO    ANOP
         AIF   ('&MODE' EQ '').ERROCANC
         AIF   ('&MODE' EQ 'CANCEL').ERROCANC
         AIF   ('&MODE' EQ 'RETURN').ERRORETU
         AGO   .ERR2
.*
.*********************************************************************
.*       ISPEXEC CONTROL ERRORS CANCEL
.*********************************************************************
.*
.ERROCANC ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN30
         AIF   (NOT &SPFERRORS).GEN30
         AIF   (NOT &SPFCANCEL).GEN30
         AGO   .BYPLIT30
.GEN30   ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYC10
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYC10   ANOP
         AIF   (&SPFERRORS).BYC20
&SPFERRORS SETB 1
ERRORS   DC    CL8'ERRORS'              CONTROL KEYWORD FOR ISPF
.BYC20   ANOP
         AIF   (&SPFCANCEL).BYC30
&SPFCANCEL SETB 1
CANCEL   DC    CL8'CANCEL'              CONTROL KEYWORD FOR ISPF
.BYC30   ANOP
SPF&NDX  DS    0H
.BYPLIT30 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL ERRORS CANCEL  CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').ERRMF1
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,ERRORS                GET ADDRESS OF ERRORS OPERAND
         LA    0,CANCEL                 GET ADDRESS OF CANCEL OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BBVAR1
         AIF   (&SPFVARC LT 3).ERRVAR
.BBVAR1  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL ERRORS CANCEL  CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.ERRMF1  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(ERRORS)                ADDRESS OF ERRORS OPERAND
         DC    A(CANCEL+X'80000000')    ADDRESS OF CANCEL OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL ERRORS RETURN
.*********************************************************************
.*
.ERRORETU ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEN40
         AIF   (NOT &SPFERRORS).GEN40
         AIF   (NOT &SPFRETURN).GEN40
         AGO   .BYPLIT40
.GEN40   ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BYB40
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BYB40   ANOP
         AIF   (&SPFERRORS).BYB50
&SPFERRORS SETB 1
ERRORS   DC    CL8'ERRORS'              CONTROL KEYWORD FOR ISPF
.BYB50   ANOP
         AIF   (&SPFRETURN).BYB60
&SPFRETURN SETB 1
RETURN   DC    CL8'RETURN'              CONTROL KEYWORD FOR ISPF
.BYB60   ANOP
SPF&NDX  DS    0H
.BYPLIT40 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL ERRORS CANCEL  CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').ERRMF2
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,ERRORS                GET ADDRESS OF ERRORS OPERAND
         LA    0,RETURN                 GET ADDRESS OF RETURN OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BBVAR2
         AIF   (&SPFVARC LT 3).ERRVAR
.BBVAR2  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL ERRORS CANCEL  CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.ERRMF2  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(ERRORS)                ADDRESS OF ERRORS OPERAND
         DC    A(RETURN+X'80000000')    ADDRESS OF RETURN OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT
.*********************************************************************
.*
.SPLI    ANOP
         AIF   ('&MODE' EQ '').SPLIENAB
         AIF   ('&MODE' EQ 'ENABLE').SPLIENAB
         AIF   ('&MODE' EQ 'DISABLE').SPLIDISA
         AGO   .ERR2
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT ENABLE
.*********************************************************************
.*
.SPLIENAB ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEC30
         AIF   (NOT &SPFSPLIT).GEC30
         AIF   (NOT &SPFENABLE).GEC30
         AGO   .BYPLIC30
.GEC30   ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BCC10
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BCC10   ANOP
         AIF   (&SPFSPLIT).BCC20
&SPFSPLIT SETB  1
SPLIT    DC    CL8'SPLIT'               CONTROL KEYWORD FOR ISPF
.BCC20   ANOP
         AIF   (&SPFENABLE).BCC30
&SPFENABLE SETB 1
ENABLE   DC    CL8'ENABLE'              CONTROL KEYWORD FOR ISPF
.BCC30   ANOP
SPF&NDX  DS    0H
.BYPLIC30 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT ENABLE   CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').ERRMC1
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,SPLIT                 GET ADDRESS OF SPLIT OPERAND
         LA    0,ENABLE                 GET ADDRESS OF ENABLE OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BBVAC1
         AIF   (&SPFVARC LT 3).ERRVAR
.BBVAC1  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT ENABLE   CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.ERRMC1  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(SPLIT)                 ADDRESS OF SPLIT OPERAND
         DC    A(ENABLE+X'80000000')    ADDRESS OF ENABLE OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT DISABLE
.*********************************************************************
.*
.SPLIDISA ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GED30
         AIF   (NOT &SPFSPLIT).GED30
         AIF   (NOT &SPFDISABLE).GED30
         AGO   .BYPLID30
.GED30   ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BCD10
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BCD10   ANOP
         AIF   (&SPFSPLIT).BCD20
&SPFSPLIT SETB  1
SPLIT    DC    CL8'SPLIT'               CONTROL KEYWORD FOR ISPF
.BCD20   ANOP
         AIF   (&SPFDISABLE).BCD30
&SPFDISABLE SETB 1
DISABLE  DC    CL8'DISABLE'             CONTROL KEYWORD FOR ISPF
.BCD30   ANOP
SPF&NDX  DS    0H
.BYPLID30 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT DISABLE  CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').ERRMD1
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,SPLIT                 GET ADDRESS OF SPLIT OPERAND
         LA    0,DISABLE                GET ADDRESS OF DISABLE OPERAND
         STM   14,0,&TEMP               STORE PARAMETER LIST
         OI    &TEMP+8,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BBVAD1
         AIF   (&SPFVARC LT 3).ERRVAR
.BBVAD1  AIF   (&SPFVARS GE 3).BYPVAR1
&SPFVARS SETA  3
         AGO   .BYPVAR1
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT DISABLE  CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.ERRMD1  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(SPLIT)                 ADDRESS OF SPLIT OPERAND
         DC    A(DISABLE+X'80000000')   ADDRESS OF DISABLE OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL NOCMD
.*********************************************************************
.*
.NOCM    ANOP
         AIF   ('&MODE' EQ '').NOCMD
         AGO   .ERR2
.*
.*********************************************************************
.*       ISPEXEC CONTROL NOCMD
.*********************************************************************
.*
.NOCMD   ANOP
         AIF   ('&LINENUM' NE '').ERR3
         AIF   (NOT &SPFCONTROL).GEE30
         AIF   (NOT &SPFNOCMD).GEE30
         AGO   .BYPE30
.GEE30   ANOP
         B     SPF&NDX
         AIF   (&SPFCONTROL).BCE10
&SPFCONTROL SETB 1
CONTROL  DC    CL8'CONTROL'             CONTROL KEYWORD FOR ISPF
.BCE10   ANOP
         AIF   (&SPFNOCMD).BCE20
&SPFNOCMD SETB  1
NOCMD    DC    CL8'NOCMD'               CONTROL KEYWORD FOR ISPF
.BCE20   ANOP
SPF&NDX  DS    0H
.BYPE30 ANOP
.*
.*********************************************************************
.*       ISPEXEC CONTROL NOCMD          CALL ISPLINK AREA
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').ERRME1
         LA    14,CONTROL               GET ADDRESS OF CONTROL KEYWORD
         LA    15,NOCMD                 GET ADDRESS OF NOCMD OPERAND
         STM   14,15,&TEMP              STORE PARAMETER LIST
         OI    &TEMP+4,X'80'            SET VL BIT ON
         LA    1,&TEMP                  GET ADDRESS OF PARMS
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BBVAE1
         AIF   (&SPFVARC LT 2).ERRVAR
.BBVAE1  AIF   (&SPFVARS GE 2).BYPVAR1
&SPFVARS SETA  2
         AGO   .BYPVAR1
         MEXIT
.*
.*********************************************************************
.*       ISPEXEC CONTROL SPLIT ENABLE   CALL ISPLINK AREA WITH MF=I
.*********************************************************************
.*
.ERRME1  ANOP
         CNOP  0,4                      FULLWORD ALLIGNMENT
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(CONTROL)               ADDRESS OF CONTROL KEYWORD
         DC    A(NOCMD+X'80000000')     ADDRESS OF NOCMD OPERAND
SPFE&NDX DS    0H
         L     15,&LOAD                 GET ISPLINK ADDRESS
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.*********************************************************************
.*       MACRO ERROR MESSAGES
.*********************************************************************
.*
.ERR1    ANOP
         MNOTE 16,'INVALID SERVICE FOR CONTROL - FOUND ''&TYPE'''
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID MODE FOR &TYPE SERVICE - FOUND ''&MODE'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'LINENUM NOT ALLOWED FOR &TYPE - FOUND ''&LINENUM'''
         MEXIT
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
