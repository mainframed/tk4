         MACRO
&NAME    TBQUERY &TABLE,                TABLE NAME                     *
               &P2,                                                    *
               &P3,                                                    *
               &P4,                                                    *
               &P5,                                                    *
               &P6,                                                    *
               &P7,                                                    *
               &KEYS=,                  KEY NAME                       *
               &NAMES=,                 NAMES NAME                     *
               &ROWNUM=,                NUMBER OF ROWS                 *
               &KEYNUM=,                NUMBER OF KEYS                 *
               &NAMENUM=,               NUMBER OF NAMES                *
               &POSITION=,              CURRENT CRP                    *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBQUERY,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBQUERY).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&KEYS' EQ '').CNT2
         AIF   ('&KEYS'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&NAMES' EQ '').CNT3
         AIF   ('&NAMES'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&ROWNUM' EQ '').CNT4
         AIF   ('&ROWNUM'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&KEYNUM' EQ '').CNT5
         AIF   ('&KEYNUM'(1,1) EQ '''').GENLIT
.CNT5    ANOP
         AIF   ('&NAMENUM' EQ '').CNT6
         AIF   ('&NAMENUM'(1,1) EQ '''').GENLIT
.CNT6    ANOP
         AIF   ('&POSITION' EQ '').CNT7
         AIF   ('&POSITION'(1,1) EQ '''').GENLIT
.CNT7    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBQUERY).BYP20
TBQUERY  DC    CL8'TBQUERY'             ISPF KEYWORD
&SPFTBQUERY SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&KEYS' EQ '').BYP40
         AIF   ('&KEYS'(1,1) NE '''').BYP40
SPFK&NDX DC    CL8&KEYS                 GENERATED KEY NAMES NAME
.BYP40   ANOP
         AIF   ('&NAMES' EQ '').BYP50
         AIF   ('&NAMES'(1,1) NE '''').BYP50
SPFV&NDX DC    CL8&NAMES                GENERATED NAMES NAME
.BYP50   ANOP
         AIF   ('&ROWNUM' EQ '').BYP60
         AIF   ('&ROWNUM'(1,1) NE '''').BYP60
SPFR&NDX DC    CL8&ROWNUM               GENERATED ROWNUM NAMES
.BYP60   ANOP
         AIF   ('&KEYNUM' EQ '').BYP70
         AIF   ('&KEYNUM'(1,1) NE '''').BYP70
SPFE&NDX DC    CL8&KEYNUM               GENERATED KEYNUM NAMES
.BYP70   ANOP
         AIF   ('&NAMENUM' EQ '').BYP80
         AIF   ('&NAMENUM'(1,1) NE '''').BYP80
SPFA&NDX DC    CL8&NAMENUM              GENERATED VARNUM NAMES
.BYP80   ANOP
         AIF   ('&POSITION' EQ '').BYP90
         AIF   ('&POSITION'(1,1) NE '''').BYP90
SPFC&NDX DC    CL8&POSITION             GENERATED CRP NAMES
.BYP90   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBQUERY               GET ADDRESS OF TBQUERY KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&KEYS' EQ '').BYP1100
         AIF   ('&KEYS'(1,1) EQ '''').BYP1110
         LA    0,&KEYS                  GET ADDRESS OF KEY NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR KEY
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFK&NDX               GET ADDRESS OF GENERATED KEY
.BYP1150 ANOP
.*
         AIF   ('&NAMES' EQ '').BYP1200
         AIF   ('&NAMES'(1,1) EQ '''').BYP1210
         LA    1,&NAMES                 GET ADDRESS OF NAMES NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR NAMES
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFV&NDX               GET ADDRESS OF GENERATED NAMES
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&ROWNUM' EQ '').BYP1300
         AIF   ('&ROWNUM'(1,1) EQ '''').BYP1310
         LA    14,&ROWNUM               GET ADDRESS OF ROW NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR ROW
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFR&NDX              GET ADDRESS OF GENERATED ROW
.BYP1350 ANOP
.*
         AIF   ('&KEYNUM' EQ '').BYP1400
         AIF   ('&KEYNUM'(1,1) EQ '''').BYP1410
         LA    15,&KEYNUM               GET ADDRESS OF KEYNUM NAME
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR KEYNUM
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFE&NDX              GET ADDRESS OF GENERATED KEY
.BYP1450 ANOP
.*
         AIF   ('&NAMENUM' EQ '').BYP1500
         AIF   ('&NAMENUM'(1,1) EQ '''').BYP1510
         LA    0,&NAMENUM               GET ADDRESS OF VARN NAME
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR VARN
         AGO   .BYP1550
.BYP1510 ANOP
         LA    0,SPFA&NDX               GET ADDRESS OF GENERATED VARN
.BYP1550 ANOP
.*
         AIF   ('&POSITION' EQ '').BYP1600
         AIF   ('&POSITION'(1,1) EQ '''').BYP1610
         LA    1,&POSITION              GET ADDRESS OF CRP NAME
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR CRP
         AGO   .BYP1650
.BYP1610 ANOP
         LA    1,SPFC&NDX               GET ADDRESS OF GENERATED CRP
.BYP1650 ANOP
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
         OI    &TEMP+28,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 8).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 8).BYPVARS
&SPFVARS SETA  8
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFQ&NDX               BRANCH AROUND LITERALS
         DC    A(TBQUERY)               TBQUERY LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&KEYS' EQ '').BYP2100
         AIF   ('&KEYS'(1,1) EQ '''').BYP2110
         DC    A(&KEYS)                 ADDRESS OF KEY NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR KEY NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFK&NDX)              ADDRESS OF GENERATED KEY
.BYP2150 ANOP
.*
         AIF   ('&NAMES' EQ '').BYP2200
         AIF   ('&NAMES'(1,1) EQ '''').BYP2210
         DC    A(&NAMES)                ADDRESS OF NAMES NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR NAMES
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFV&NDX)              ADDRESS OF GENERATED NAMES
.BYP2250 ANOP
.*
         AIF   ('&ROWNUM' EQ '').BYP2300
         AIF   ('&ROWNUM'(1,1) EQ '''').BYP2310
         DC    A(&ROWNUM)               ADDRESS OF ROW NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ROW
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFR&NDX)              ADDRESS OF GENERATED ROW
.BYP2350 ANOP
.*
         AIF   ('&KEYNUM' EQ '').BYP2400
         AIF   ('&KEYNUM'(1,1) EQ '''').BYP2410
         DC    A(&KEYNUM)               ADDRESS OF KEYN NAME
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR KEYN
         AGO   .BYP2450
.BYP2410 ANOP
         DC    A(SPFE&NDX)              ADDRESS OF GENERATED KEYN
.BYP2450 ANOP
.*
         AIF   ('&NAMENUM' EQ '').BYP2500
         AIF   ('&NAMENUM'(1,1) EQ '''').BYP2510
         DC    A(&NAMENUM)              ADDRESS OF VARN NAME
         AGO   .BYP2550
.BYP2500 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR VARN
         AGO   .BYP2550
.BYP2510 ANOP
         DC    A(SPFA&NDX)              ADDRESS OF GENERATED VARN
.BYP2550 ANOP
.*
         AIF   ('&POSITION' EQ '').BYP2600
         AIF   ('&POSITION'(1,1) EQ '''').BYP2610
         DC    A(&POSITION+X'80000000') ADDRESS OF CRP NAME
         AGO   .BYP2650
.BYP2600 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR CRP
         AGO   .BYP2650
.BYP2610 ANOP
         DC    A(SPFC&NDX+X'80000000')  ADDRESS OF GENERATED AUTOSE
.BYP2650 ANOP
SPFQ&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
