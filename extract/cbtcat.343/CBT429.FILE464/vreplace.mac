         MACRO
&NAME    VREPLACE &VAR,                 NAME LIST                      *
               &LEN,                    LENGTH ARRAY                   *
               &VAL,                    VALUE ARRAYS                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFVREPLACE,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLA  &COUNT,&LIMIT
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&VAR' EQ '').ERR1
         AIF   ('&LEN' EQ '').ERR2
         AIF   ('&VAL' EQ '').ERR3
         AIF   ('&VAL'(1,1) EQ '''').ERR3
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFVREPLACE).GENLIT
         AIF   ('&VAR' EQ '').CNT1
         AIF   ('&VAR'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&LEN' EQ '').CNT2
         AIF   (T'&LEN EQ 'N').GENLIT
         AIF   (N'&LEN GT 1).GENLIT
.CNT2    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFVREPLACE).BYP20
VREPLACE DC    CL8'VREPLACE'            ISPF KEYWORD
&SPFVREPLACE SETB 1
.BYP20   ANOP
         AIF   ('&VAR' EQ '').BYP30
         AIF   ('&VAR'(1,1) NE '''').BYP30
SPFV&NDX DC    C&VAR                    GENERATED NAME LIST{
.BYP30   ANOP
         AIF   ('&LEN' EQ '').BYP40
         AIF   (T'&LEN NE 'N' AND N'&LEN EQ 1).BYP40
&COUNT   SETA  1
SPFL&NDX DC    F'&LEN(1)'
.BYP32   ANOP
&COUNT   SETA  &COUNT+1
         AIF   (&COUNT GT N'&LEN).BYP40
         DC    F'&LEN(&COUNT)'
         AGO   .BYP32
.BYP40   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,VREPLACE              GET ADDRESS OF VREPLACE KEYWORD
.*
         AIF   ('&VAR' EQ '').BYP1000
         AIF   ('&VAR'(1,1) EQ '''').BYP1010
         LA    15,&VAR                  GET ADDRESS OF NAMELIST
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR NAMELIST
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFV&NDX              GET ADDRESS OF GENERATED NAME
.BYP1050 ANOP
.*
         AIF   (T'&LEN EQ 'N' OR N'&LEN GT 1).BYP1110
         LA    0,&LEN                   GET ADDRESS OF LENGTH ARRAY
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFL&NDX               GET ADDRESS OF GENERATED LENGTH
.BYP1150 ANOP
.*
         AIF   ('&VAL' EQ '').BYP1200
         AIF   ('&VAL'(1,1) EQ '''').BYP1210
         LA    1,&VAL                   GET ADDRESS OF VALUE ADDRESS
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR VALUE ADDRE
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFA&NDX               GET ADDRESS OF GENERATED VALUE
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
.*
         OI    &TEMP+12,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 4).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 4).BYPVARS
&SPFVARS SETA  4
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(VREPLACE)              VREPLACE LITERAL
.*
         AIF   ('&VAR' EQ '').BYP2000
         AIF   ('&VAR'(1,1) EQ '''').BYP2010
         DC    A(&VAR)                  ADDRESS OF NAMELIST
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR NAMELIST
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFV&NDX)              ADDRESS OF GENERATED NAMELIST
.BYP2050 ANOP
.*
         AIF   (T'&LEN EQ 'N' OR N'&LEN GT 1).BYP2110
         DC    A(&LEN)                  ADDRESS OF LENGTH ARRAY
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFL&NDX)              ADDRESS OF GENERATED LENGTHS
.BYP2150 ANOP
.*
         AIF   ('&VAL' EQ '').BYP2200
         AIF   ('&VAL'(1,1) EQ '''').BYP2210
         DC    A(&VAL+X'80000000')      ADDRESS OF STORAGE ADDRESS
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR STORAGE ADDRESS
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFA&NDX+X'80000000')
.BYP2250 ANOP
.*
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'NAME LIST IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'LENGTH VALUES OR ADDRESS REQUIRED'
         MEXIT
.ERR3    ANOP
         MNOTE 16,'VALUE ADDRESS REQUIRED'
         MEXIT
.ERR4    ANOP
         MNOTE 16,'EXPECTED MOVE/LOCATE KEYWORD - FOUND ''&MOVE'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
