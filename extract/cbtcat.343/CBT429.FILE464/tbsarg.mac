         MACRO
&NAME    TBSARG &TABLE,                 TABLE NAME                     *
               &P2,                                                    *
               &P3,                                                    *
               &P4,                                                    *
               &ARGLIST=,               NAME LIST                      *
               &DIRECTION=,             NEXT OR PREVIOUS               *
               &NAMECOND=,              NAME COND PAIRS                *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBSARG,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @TBSARG &TABLE,&P2,&P3,&P4,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P2' NE '').LVLERR
         AIF   ('&P3' NE '').LVLERR
         AIF   ('&P4' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&DIRECTION' EQ '').K3
         AIF   ('&DIRECTION'(1,1) NE '''').K3
         AIF   ('&DIRECTION' EQ '''NEXT''').K3
         AIF   ('&DIRECTION' EQ '''PREVIOUS''').K3
         MNOTE 4,'WARNING - DIRECTION KEYWORD EXPECTED NEXT OR PREVIOUS*
                ENCLOSED IN QUOTES'
.K3      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBSARG).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&ARGLIST' EQ '').CNT2
         AIF   ('&ARGLIST'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&DIRECTION' EQ '').CNT3
         AIF   ('&DIRECTION'(1,1) EQ '''').GENLIT
.CNT3    AIF   ('&NAMECOND' EQ '').CNT4
         AIF   ('&NAMECOND'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBSARG).BYP20
TBSARG   DC    CL8'TBSARG'              ISPF KEYWORD
&SPFTBSARG SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&ARGLIST' EQ '').BYP40
         AIF   ('&ARGLIST'(1,1) NE '''').BYP40
SPFV&NDX DC    C&ARGLIST                GENERATED NAMELIST
.BYP40   ANOP
         AIF   ('&DIRECTION' EQ '').BYP50
         AIF   ('&DIRECTION'(1,1) NE '''').BYP50
SPFD&NDX DC    CL8&DIRECTION            ISPF KEYWORD
.BYP50   ANOP
         AIF   ('&NAMECOND' EQ '').BYP60
         AIF   ('&NAMECOND'(1,1) NE '''').BYP60
SPFC&NDX DC    C&NAMECOND               GENERATED CONDITIONS
.BYP60   ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBSARG                GET ADDRESS OF TBSARG KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&ARGLIST' EQ '').BYP1100
         AIF   ('&ARGLIST'(1,1) EQ '''').BYP1110
         LA    0,&ARGLIST               GET ADDRESS OF VAR NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR VAR
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFV&NDX               GET ADDRESS OF GENERATED VAR
.BYP1150 ANOP
.*
         AIF   ('&DIRECTION' EQ '').BYP1200
         AIF   ('&DIRECTION'(1,1) EQ '''').BYP1205
         LA    1,&DIRECTION             GET ADDRESS OF KEYWORD
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM
         AGO   .BYP1250
.BYP1205 ANOP
         LA    1,SPFD&NDX               GENNED NEXT/PREVIOUS KEYWORD
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&NAMECOND' EQ '').BYP1300
         AIF   ('&NAMECOND'(1,1) EQ '''').BYP1310
         LA    14,&NAMECOND             GET ADDRESS OF COND NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR COND
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENERATED COND
.BYP1350 ANOP
         ST    14,&TEMP+16              STORE PARAMETERS INTO LIST
         OI    &TEMP+16,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 5).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 5).BYPVARS
&SPFVARS SETA  5
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBSARG)                TBSARG LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&ARGLIST' EQ '').BYP2100
         AIF   ('&ARGLIST'(1,1) EQ '''').BYP2110
         DC    A(&ARGLIST)              ADDRESS OF VAR NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR VAR NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFV&NDX)              ADDRESS OF GENERATED VAR
.BYP2150 ANOP
.*
         AIF   ('&DIRECTION' EQ '').BYP2200
         AIF   ('&DIRECTION'(1,1) EQ '''').BYP2205
         DC    A(&DIRECTION)            ISPF KEYWORD
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MSG
         AGO   .BYP2250
.BYP2205 ANOP
         DC    A(SPFD&NDX)              GENNED NEXT/PREVIOUS KEYWORD
.BYP2250 ANOP
.*
         AIF   ('&NAMECOND' EQ '').BYP2300
         AIF   ('&NAMECOND'(1,1) EQ '''').BYP2310
         DC    A(&NAMECOND+X'80000000') ADDRESS OF COND NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR COND
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFC&NDX+X'80000000')  ADDRESS OF GENERATED COND
.BYP2350 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
