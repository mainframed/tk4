         MACRO
&NAME    TBDISPL &TABLE,                TABLE NAME                     *
               &P2,                                                    *
               &P3,                                                    *
               &P4,                                                    *
               &P5,                                                    *
               &P6,                                                    *
               &P7,                                                    *
               &P8,                                                    *
               &P9,                                                    *
               &PANEL=,                 PANEL NAME                     *
               &MSG=,                   MESSAGE ID                     *
               &CURSOR=,                CURSOR POSITION                *
               &CSRROW=,                TABLE ROW NUMBER               *
               &CSRPOS=,                CSR POSITION                   *
               &AUTOSEL=,               AUTOSEL INDICATOR              *
               &POSITION=,              ROW POINTED TO BY CRP          *
               &ROWID=,                 SPECIAL ROWN NUMBER            *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFTBDISPL,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
         GBLC  &SPFLVL
         AIF   ('&SPFLVL' NE '1').NL1
&NAME    @TBDISPL &TABLE,&P2,&P3,&P4,&P5,&P6,&P7,                      *
               &P8,&P9,LOAD=&LOAD,MF=&MF
         MEXIT
.NL1     ANOP
         AIF   ('&P2' NE '').LVLERR
         AIF   ('&P3' NE '').LVLERR
         AIF   ('&P4' NE '').LVLERR
         AIF   ('&P5' NE '').LVLERR
         AIF   ('&P6' NE '').LVLERR
         AIF   ('&P7' NE '').LVLERR
         AIF   ('&P8' NE '').LVLERR
         AIF   ('&P9' NE '').LVLERR
         AGO   .NL2
.LVLERR  ANOP
         MNOTE 12,'POSITIONAL PARAMETER FOUND - NOT ALLOWED'
         MEXIT
.NL2     ANOP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&TABLE' EQ '').ERR1
         AIF   ('&AUTOSEL' EQ '').K3
         AIF   ('&AUTOSEL'(1,1) NE '''').K3
         AIF   ('&AUTOSEL' EQ '''YES''').K3
         AIF   ('&AUTOSEL' EQ '''NO''').K3
         MNOTE 4,'WARNING - AUTOSEL KEYWORD EXPECTED TO BE YES OR NO'
.K3      ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFTBDISPL).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&TABLE' EQ '').CNT1
         AIF   ('&TABLE'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&PANEL' EQ '').CNT2
         AIF   ('&PANEL'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&MSG' EQ '').CNT3
         AIF   ('&MSG'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&CURSOR' EQ '').CNT4
         AIF   ('&CURSOR'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&CSRROW' EQ '').CNT5
         AIF   (T'&CSRROW EQ 'N').GENLIT
.CNT5    ANOP
         AIF   ('&CSRPOS' EQ '').CNT6
         AIF   (T'&CSRPOS EQ 'N').GENLIT
.CNT6    ANOP
         AIF   ('&AUTOSEL' EQ '').CNT7
         AIF   ('&AUTOSEL'(1,1) EQ '''').GENLIT
.CNT7    ANOP
         AIF   ('&POSITION' EQ '').CNT8
         AIF   ('&POSITION'(1,1) EQ '''').GENLIT
.CNT8    ANOP
         AIF   ('&ROWID' EQ '').CNT9
         AIF   ('&ROWID'(1,1) EQ '''').GENLIT
.CNT9    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFTBDISPL).BYP20
TBDISPL  DC    CL8'TBDISPL'             ISPF KEYWORD
&SPFTBDISPL SETB 1
.BYP20   ANOP
         AIF   ('&TABLE' EQ '').BYP30
         AIF   ('&TABLE'(1,1) NE '''').BYP30
SPFT&NDX DC    CL8&TABLE                GENERATED TABLE NAME
.BYP30   ANOP
         AIF   ('&PANEL' EQ '').BYP40
         AIF   ('&PANEL'(1,1) NE '''').BYP40
SPFP&NDX DC    CL8&PANEL                GENERATED PANEL NAME
.BYP40   ANOP
         AIF   ('&MSG' EQ '').BYP50
         AIF   ('&MSG'(1,1) NE '''').BYP50
SPFM&NDX DC    CL8&MSG                  GENERATED MSG NAME
.BYP50   ANOP
         AIF   ('&CURSOR' EQ '').BYP60
         AIF   ('&CURSOR'(1,1) NE '''').BYP60
SPFF&NDX DC    CL8&CURSOR               GENERATED CURSOR NAME
.BYP60   ANOP
         AIF   ('&CSRROW' EQ '').BYP70
         AIF   (T'&CSRROW NE 'N').BYP70
SPFR&NDX DC    F'&CSRROW'               GENERATED ROW NUMBER
.BYP70   ANOP
         AIF   ('&CSRPOS' EQ '').BYP80
         AIF   (T'&CSRPOS NE 'N').BYP80
SPFC&NDX DC    F'&CSRPOS'               GENERATED CSRPOS
.BYP80   ANOP
         AIF   ('&AUTOSEL' EQ '').BYP90
         AIF   ('&AUTOSEL'(1,1) NE '''').BYP90
SPFX&NDX DC    CL8&AUTOSEL              ISPF KEYWORD
.BYP90   ANOP
         AIF   ('&POSITION' EQ '').BYP100
         AIF   ('&POSITION'(1,1) NE '''').BYP100
SPFW&NDX DC    CL8&POSITION             GENERATED POSITION VALUE
.BYP100  ANOP
         AIF   ('&ROWID' EQ '').BYP110
         AIF   ('&ROWID'(1,1) NE '''').BYP110
SPFI&NDX DC    CL8&ROWID                GENERATED ROWID VALUE
.BYP110  ANOP
         AIF   (&SPFNULLC).BYP120
SPFNULLC DC    CL8'        '            DUMMY ISPF CURSOR
&SPFNULLC SETB  1
.BYP120  ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,TBDISPL               GET ADDRESS OF TBDISPL KEYWORD
.*
         AIF   ('&TABLE' EQ '').BYP1000
         AIF   ('&TABLE'(1,1) EQ '''').BYP1010
         LA    15,&TABLE                GET ADDRESS OF TABLE NAME
         AGO   .BYP1050
.BYP1000 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR TABLE
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFT&NDX              GET ADDRESS OF GENERATED TABLE
.BYP1050 ANOP
.*
         AIF   ('&PANEL' EQ '').BYP1100
         AIF   ('&PANEL'(1,1) EQ '''').BYP1110
         LA    0,&PANEL                 GET ADDRESS OF PANEL NAME
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR PANEL
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFP&NDX               GET ADDRESS OF GENERATED PANEL
.BYP1150 ANOP
.*
         AIF   ('&MSG' EQ '').BYP1200
         AIF   ('&MSG'(1,1) EQ '''').BYP1210
         LA    1,&MSG                   GET ADDRESS OF MSG NAME
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR MSG
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFM&NDX               GET ADDRESS OF GENERATED MSG
.BYP1250 ANOP
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&CURSOR' EQ '').BYP1300
         AIF   ('&CURSOR'(1,1) EQ '''').BYP1310
         LA    14,&CURSOR               GET ADDRESS OF CURSOR NAME
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR CURSOR
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFF&NDX              GET ADDRESS OF GENERATED CURSOR
.BYP1350 ANOP
.*
         AIF   ('&CSRROW' EQ '').BYP1400
         AIF   (T'&CSRROW EQ 'N').BYP1410
         LA    15,&CSRROW               GET ADDRESS OF ROW NAME
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR ROW
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFR&NDX              GET ADDRESS OF GENERATED ROW
.BYP1450 ANOP
.*
         AIF   ('&CSRPOS' EQ '').BYP1500
         AIF   (T'&CSRPOS EQ 'N').BYP1510
         LA    0,&CSRPOS                GET ADDRESS OF CSRPOS NAME
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR CSRPOS
         AGO   .BYP1550
.BYP1510 ANOP
         LA    0,SPFC&NDX               GET ADDRESS OF GENERATED CSRPOS
.BYP1550 ANOP
.*
         AIF   ('&AUTOSEL' EQ '').BYP1600
         AIF   ('&AUTOSEL'(1,1) EQ '''').BYP1605
         LA    1,&AUTOSEL               GET ADDRESS OF AUTOSEL NAME
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR AUTOSEL
         AGO   .BYP1650
.BYP1605 ANOP
         LA    1,SPFX&NDX               GET GENNED AUTOSEL VALUE
.BYP1650 ANOP
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
         AIF   ('&POSITION' EQ '').BYP1700
         AIF   ('&POSITION'(1,1) EQ '''').BYP1710
         LA    14,&POSITION             GET ADDRESS OF POSITION NAME
         AGO   .BYP1750
.BYP1700 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR POSITION
         AGO   .BYP1750
.BYP1710 ANOP
         LA    14,SPFW&NDX              GET ADDRESS OF GENERATED CRPROW
.BYP1750 ANOP
.*
         AIF   ('&ROWID' EQ '').BYP1800
         AIF   ('&ROWID'(1,1) EQ '''').BYP1810
         LA    15,&ROWID                GET ADDRESS OF ROWID NAME
         AGO   .BYP1850
.BYP1800 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR ROWID
         AGO   .BYP1850
.BYP1810 ANOP
         LA    15,SPFI&NDX              GET ADDRESS OF GENERATED ROWID
.BYP1850 ANOP
         STM   14,15,&TEMP+32           STORE PARAMETERS INTO LIST
         OI    &TEMP+36,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 10).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 10).BYPVARS
&SPFVARS SETA  10
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(TBDISPL)               TBDISPL LITERAL
.*
         AIF   ('&TABLE' EQ '').BYP2000
         AIF   ('&TABLE'(1,1) EQ '''').BYP2010
         DC    A(&TABLE)                ADDRESS OF TABLE NAME
         AGO   .BYP2050
.BYP2000 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR TABLE NAME
         AGO   .BYP2050
.BYP2010 ANOP
         DC    A(SPFT&NDX)              ADDRESS OF GENERATED TABLE
.BYP2050 ANOP
.*
         AIF   ('&PANEL' EQ '').BYP2100
         AIF   ('&PANEL'(1,1) EQ '''').BYP2110
         DC    A(&PANEL)                ADDRESS OF PANEL NAME
         AGO   .BYP2150
.BYP2100 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR PANEL NAME
         AGO   .BYP2150
.BYP2110 ANOP
         DC    A(SPFP&NDX)              ADDRESS OF GENERATED PANEL
.BYP2150 ANOP
.*
         AIF   ('&MSG' EQ '').BYP2200
         AIF   ('&MSG'(1,1) EQ '''').BYP2210
         DC    A(&MSG)                  ADDRESS OF MSG NAME
         AGO   .BYP2250
.BYP2200 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MSG
         AGO   .BYP2250
.BYP2210 ANOP
         DC    A(SPFM&NDX)              ADDRESS OF GENERATED MSG
.BYP2250 ANOP
.*
         AIF   ('&CURSOR' EQ '').BYP2300
         AIF   ('&CURSOR'(1,1) EQ '''').BYP2310
         DC    A(&CURSOR)               ADDRESS OF CURSOR NAME
         AGO   .BYP2350
.BYP2300 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR CURSOR
         AGO   .BYP2350
.BYP2310 ANOP
         DC    A(SPFF&NDX)              ADDRESS OF GENERATED CURSOR
.BYP2350 ANOP
.*
         AIF   ('&CSRROW' EQ '').BYP2400
         AIF   (T'&CSRROW EQ 'N').BYP2410
         DC    A(&CSRROW)               ADDRESS OF ROW NAME
         AGO   .BYP2450
.BYP2400 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR ROW
         AGO   .BYP2450
.BYP2410 ANOP
         DC    A(SPFR&NDX)              ADDRESS OF GENERATED ROW
.BYP2450 ANOP
.*
         AIF   ('&CSRPOS' EQ '').BYP2500
         AIF   (T'&CSRPOS EQ 'N').BYP2510
         DC    A(&CSRPOS)               ADDRESS OF CSRPOS NAME
         AGO   .BYP2550
.BYP2500 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR CSRPOS
         AGO   .BYP2550
.BYP2510 ANOP
         DC    A(SPFC&NDX)              ADDRESS OF GENERATED CSRPOS
.BYP2550 ANOP
.*
         AIF   ('&AUTOSEL' EQ '').BYP2600
         AIF   ('&AUTOSEL'(1,1) EQ '''').BYP2605
         DC    A(&AUTOSEL)              ADDRESS OF AUTOSEL NAME
         AGO   .BYP2650
.BYP2600 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR AUTOSEL
         AGO   .BYP2650
.BYP2605 ANOP
         DC    A(SPFX&NDX)              GENNED AUTOSEL KEYWORD
.BYP2650 ANOP
.*
         AIF   ('&POSITION' EQ '').BYP2700
         AIF   ('&POSITION'(1,1) EQ '''').BYP2710
         DC    A(&POSITION)             ADDRESS OF POSITION NAME
         AGO   .BYP2750
.BYP2700 ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR POSITION
         AGO   .BYP2750
.BYP2710 ANOP
         DC    A(SPFW&NDX)              ADDRESS OF GENERATED POSITION
.BYP2750 ANOP
.*
         AIF   ('&ROWID' EQ '').BYP2800
         AIF   ('&ROWID'(1,1) EQ '''').BYP2810
         DC    A(&ROWID+X'80000000')    ADDRESS OF ROWID NAME
         AGO   .BYP2850
.BYP2800 ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR ROWID
         AGO   .BYP2850
.BYP2810 ANOP
         DC    A(SPFI&NDX+X'80000000')  ADDRESS OF GENERATED ROWID
.BYP2850 ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'TABLE NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
