         MACRO
&LABEL   LMOPEN &DATAID=,               DATAID NAME                    *
               &OPTION=,                OPTION OF INPUT OR OUTPUT      *
               &LRECL=,                 VARIABLE FOR LRECL             *
               &RECFM=,                 VARIABLE FOR RECFM             *
               &ORG=,                   VARIABLE FOR DSORG             *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFLMOPEN               GLOBAL FOR LMOPEN LITERAL
         GBLB  &SPFNULLC                DUMMY PARAMTER FOR CALL
         GBLB  &SPFDEBUG                DEBUG MODE
         GBLB  &SPFVAR                  NUMBER OF REQUIRED FULLWORDS
         GBLA  &SPFVARS                 NUMBER OF FULLWORDS
         GBLA  &SPFVARC                 PREVIOUS FULLWORDS USED
         LCLC  &NDX                     USED IN PLACE OF SYSNDX
         LCLC  &TEMP                    USED AS TEMPORARY VARIABLE
.*
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LABEL' EQ '').BYP10
&LABEL   DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&DATAID' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFLMOPEN).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&DATAID'(1,1) EQ '''').GENLIT
         AIF   ('&OPTION' EQ '').CNT1
         AIF   ('&OPTION'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&LRECL' EQ '').CNT2
         AIF   ('&LRECL'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&RECFM' EQ '').CNT3
         AIF   ('&RECFM'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&ORG' EQ '').CNT4
         AIF   ('&ORG'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFNULLC).BYP001
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP001  ANOP
.*
.*
         AIF   (&SPFLMOPEN).BYP20
LMOPEN   DC    CL8'LMOPEN'              ISPF KEYWORD
&SPFLMOPEN SETB 1
.BYP20   ANOP
.*
.*
         AIF   ('&DATAID' EQ '').BYP30
         AIF   ('&DATAID'(1,1) NE '''').BYP30
SPFZ&NDX DC    CL8&DATAID               GENERATED DATAID NAME
.BYP30   ANOP
.*
.*
         AIF   ('&OPTION' EQ '').BYP40
         AIF   ('&OPTION'(1,1) NE '''').BYP40
SPFX&NDX DC    CL8&OPTION               OPTION KEYWORD
.BYP40   ANOP
.*
.*
         AIF   ('&LRECL' EQ '').BYP50
         AIF   ('&LRECL'(1,1) NE '''').BYP50
SPFB&NDX DC    CL8&LRECL                LRECL VARIABLE NAME
.BYP50   ANOP
.*
.*
         AIF   ('&RECFM' EQ '').BYP60
         AIF   ('&RECFM'(1,1) NE '''').BYP60
SPFC&NDX DC    CL8&RECFM                RECFM VARIABLE NAME
.BYP60   ANOP
.*
.*
         AIF   ('&ORG' EQ '').BYP70
         AIF   ('&ORG'(1,1) NE '''').BYP70
SPFD&NDX DC    CL8&ORG                  ORG VARIABLE NAME
.BYP70   ANOP
.*
.*
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
.*
.*
         LA    14,LMOPEN                GET ADDRESS OF LMOPEN KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BYP1010
         LA    15,&DATAID               GET ADDRESS OF DATAID NAME
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFZ&NDX              GET ADDRESS OF GENERATED DATAID
.BYP1050 ANOP
.*
.*
         AIF   ('&OPTION' EQ '').BYP1100
         AIF   ('&OPTION'(1,1) EQ '''').BYP1110
         LA    0,&OPTION                GET ADDRESS OF OPTION
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR OPTION
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFX&NDX               GET ADDRESS OF GENNED OPTION
.BYP1150 ANOP
.*
.*
         AIF   ('&LRECL' EQ '').BYP1200
         AIF   ('&LRECL'(1,1) EQ '''').BYP1210
         LA    1,&LRECL                 GET ADDRESS OF LRECL VARIABLE
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR LRECL
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFB&NDX               GET ADDRESS OF GENNED LRECL
.BYP1250 ANOP
.*
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
.*
.*
.*
         AIF   ('&RECFM' EQ '').BYP1300
         AIF   ('&RECFM'(1,1) EQ '''').BYP1310
         LA    14,&RECFM                GET ADDRESS OF RECFM VARIABLE
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR RECFM
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENNED RECFM
.BYP1350 ANOP
.*
.*
         AIF   ('&ORG' EQ '').BYP1400
         AIF   ('&ORG'(1,1) EQ '''').BYP1410
         LA    15,&ORG                  GET ADDRESS OF ORG VARIABLE
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR ORG
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFD&NDX              GET ADDRESS OF GENNED ORG
.BYP1450 ANOP
.*
.*
         STM   14,15,&TEMP+16           STORE PARAMETERS INTO LIST
.*
.*
.*
         OI    &TEMP+20,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 6).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 6).BYPVARS
&SPFVARS SETA  6
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
.*
.*
         DC    A(LMOPEN)                GET ADDRESS OF LMOPEN KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BY1010
         DC    A(&DATAID)               GET ADDRESS OF DATAID NAME
         AGO   .BY1050
.BY1010 ANOP
         DC    A(SPFZ&NDX)              GET ADDRESS OF GENERATED DATAID
.BY1050 ANOP
.*
.*
         AIF   ('&OPTION' EQ '').BY1100
         AIF   ('&OPTION'(1,1) EQ '''').BY1110
         DC    A(&OPTION)               GET ADDRESS OF OPTION
         AGO   .BY1150
.BY1100 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR OPTION
         AGO   .BY1150
.BY1110 ANOP
         DC    A(SPFX&NDX)              GET ADDRESS OF GENNED OPTION
.BY1150 ANOP
.*
.*
         AIF   ('&LRECL' EQ '').BY1200
         AIF   ('&LRECL'(1,1) EQ '''').BY1210
         DC    A(&LRECL)                GET ADDRESS OF LRECL VARIABLE
         AGO   .BY1250
.BY1200 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR LRECL
         AGO   .BY1250
.BY1210 ANOP
         DC    A(SPFB&NDX)              GET ADDRESS OF GENNED LRECL
.BY1250 ANOP
.*
.*
         AIF   ('&RECFM' EQ '').BY1300
         AIF   ('&RECFM'(1,1) EQ '''').BY1310
         DC    A(&RECFM)                GET ADDRESS OF RECFM VARIABLE
         AGO   .BY1350
.BY1300 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR RECFM
         AGO   .BY1350
.BY1310 ANOP
         DC    A(SPFC&NDX)              GET ADDRESS OF GENNED RECFM
.BY1350 ANOP
.*
.*
         AIF   ('&ORG' EQ '').BY1400
         AIF   ('&ORG'(1,1) EQ '''').BY1410
         DC    A(&ORG+X'80000000')      GET ADDRESS OF ORG VARIABLE
         AGO   .BY1450
.BY1400 ANOP
         DC    A(SPFNULLC+X'80000000')  GET DUMMY PARM FOR ORG
         AGO   .BY1450
.BY1410 ANOP
         DC    A(SPFD&NDX+X'80000000')  GET ADDRESS OF GENNED ORG
.BY1450 ANOP
.*
.*
SPFE&NDX DS    0H
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'DATAID NAME IS REQUIRED'
         MEXIT
.ERR2    ANOP
         MNOTE 16,'INVALID OPTION - EXPECTED INPUT OR OUTPUT - FOUND ''*
               &OPTION'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
