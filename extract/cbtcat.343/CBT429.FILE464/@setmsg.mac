         MACRO
&NAME    @SETMSG &MSG,                  MEMBER NAME                    *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFSETMSG,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
.*
.**********************************************************************
.*       GENERAL EDITS
.**********************************************************************
.*
         AIF   ('&MSG' EQ '').ERR3
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERR2
.*
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(2)' NE '').BYP15
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP17
.BYP15   ANOP
&TEMP    SETC  '&MF(2)'
.BYP17   ANOP
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFSETMSG).GENLIT
         AIF   ('&MSG' EQ '').CNT1
         AIF   ('&MSG'(1,1) EQ '''').GENLIT
.CNT1    AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFSETMSG).BYP20
SETMSG   DC    CL8'SETMSG'              ISPF KEYWORD
&SPFSETMSG SETB 1
.BYP20   ANOP
         AIF   ('&MSG' EQ '').BYP40
         AIF   ('&MSG'(1,1) NE '''').BYP40
SPFS&NDX DC    CL8&MSG                  GENERATED MEMBER NAME
.BYP40   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,SETMSG                GET ADDRESS OF SETMSG KEYWORD
         AIF   ('&MSG' EQ '').BYP100
         AIF   ('&MSG'(1,1) EQ '''').BYP110
         LA    15,&MSG                  GET ADDRESS OF MEMBER
         AGO   .BYP120
.BYP110  ANOP
         LA    15,SPFS&NDX              GET ADDRESS OF GENERATED MEMBER
         AGO   .BYP120
.BYP100  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR MEMBER
.BYP120  ANOP
         STM   14,15,&TEMP              STORE PARAMETERS
         OI    &TEMP+4,X'80'            MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 2).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 2).BYPVARS
&SPFVARS SETA  2
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(SETMSG)                BROWSE LITERAL
         AIF   ('&MSG' EQ '').BYP200
         AIF   ('&MSG'(1,1) EQ '''').BYP210
         DC    A(&MSG+X'80000000')      ADDRESS OF MEMBER
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFS&NDX+X'80000000')  ADDRESS OF GENERATED MEMBER
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR MEMBER
.BYP220  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERR2    ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR3    ANOP
         MNOTE 16,'MSG NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
