         MACRO
&NAME    @DISPLAY &PANEL,               PANEL NAME                     *
               &MSG,                    MESSAGE ID                     *
               &FIELD,                  FIELD FOR CURSOR               *
               &CURPOS,                 POSITION WITHIN FIELD          *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFDISPLAY,&SPFNULLC,&SPFDEBUG,&SPFVAR
         GBLA  &SPFVARS,&SPFVARC
         LCLC  &NDX,&TEMP
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&NAME' EQ '').BYP10
&NAME    DS    0H
.BYP10   ANOP
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFDISPLAY).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&MSG' EQ '').CNT1
         AIF   ('&MSG'(1,1) EQ '''').GENLIT
.CNT1    AIF   ('&FIELD' EQ '').CNT2
         AIF   ('&FIELD'(1,1) EQ '''').GENLIT
.CNT2    AIF   ('&PANEL' EQ '').CNT3
         AIF   ('&PANEL'(1,1) EQ '''').GENLIT
.CNT3    AIF   (T'&CURPOS EQ 'N').GENLIT
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFDISPLAY).BYP20
DISPLAY  DC    CL8'DISPLAY'             DISPLAY LITERAL FOR ISPF
&SPFDISPLAY SETB 1
.BYP20   ANOP
         AIF   ('&PANEL' EQ '').BYP30
         AIF   ('&PANEL'(1,1) NE '''').BYP30
SPFP&NDX DC    CL8&PANEL                DISPLAY THIS PANEL
.BYP30   ANOP
         AIF   ('&MSG' EQ '').BYP40
         AIF   ('&MSG'(1,1) NE '''').BYP40
SPFM&NDX DC    CL8&MSG                  GENERATED MSG
.BYP40   ANOP
         AIF   ('&FIELD' EQ '').BYP50
         AIF   ('&FIELD'(1,1) NE '''').BYP50
SPFF&NDX DC    CL8&FIELD                GENERATED FIELD POSITION
.BYP50   ANOP
         AIF   (T'&CURPOS NE 'N').BYP62
SPFC&NDX DC    F'&CURPOS'               GENERATED CSRPOS
.BYP62   ANOP
         AIF   (&SPFNULLC).BYP70
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP70   ANOP
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
         LA    14,DISPLAY               GET ADDRESS OF DISPLAY KEYWORD
         AIF   ('&PANEL' EQ '').BYP80A
         AIF   ('&PANEL'(1,1) NE '''').BYP80
         LA    15,SPFP&NDX              GET ADDRESS OF GENERATED PANEL
         AGO   .BYP90
.BYP80   ANOP
         LA    15,&PANEL                GET ADDRESS OF PANEL NAME
         AGO   .BYP90
.BYP80A  ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR PANEL
.BYP90   ANOP
         AIF   ('&MSG' EQ '').BYP100
         AIF   ('&MSG'(1,1) EQ '''').BYP110
         LA    0,&MSG                   GET ADDRESS OF MSG
         AGO   .BYP120
.BYP110  ANOP
         LA    0,SPFM&NDX               GET ADDRESS OF GENERATED MSG
         AGO   .BYP120
.BYP100  ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR MSG
.BYP120  ANOP
         AIF   ('&FIELD' EQ '').BYP140
         AIF   ('&FIELD'(1,1) NE '''').BYP130
         LA    1,SPFF&NDX               GET ADDRESS OF GENERATED FIELD
         AGO   .BYP150
.BYP130  ANOP
         LA    1,&FIELD                 GET ADDRESS OF FIELD
         AGO   .BYP150
.BYP140  ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR FIELD
.BYP150  ANOP
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
         AIF   ('&CURPOS' EQ '').BYP172
         AIF   (T'&CURPOS EQ 'N').BYP162
         LA    14,&CURPOS               GET ADDRESS OF CSRPOS
         AGO   .BYP182
.BYP162  ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENNED CSRPOS
         AGO   .BYP182
.BYP172  ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR CSRPOS
.BYP182  ANOP
         ST    14,&TEMP+16              STORE AS FIFTH PARM
         OI    &TEMP+16,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 5).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 5).BYPVARS
&SPFVARS SETA  5
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
         DC    A(DISPLAY)               DISPLAY LITERAL
         AIF   ('&PANEL' EQ '').BYP282
         AIF   ('&PANEL'(1,1) NE '''').BYP280
         DC    A(SPFP&NDX)              ADDRESS OF GENERATED PANEL
         AGO   .BYP290
.BYP280  ANOP
         DC    A(&PANEL)                ADDRESS OF PANEL
         AGO   .BYP290
.BYP282  ANOP
         DC    A(SPFNULLC)              ADDRESS OF DUMMY PANEL
.BYP290  ANOP
         AIF   ('&MSG' EQ '').BYP200
         AIF   ('&MSG'(1,1) EQ '''').BYP210
         DC    A(&MSG)                  ADDRESS OF MSG
         AGO   .BYP220
.BYP210  ANOP
         DC    A(SPFM&NDX)              ADDRESS OF GENERATED MSG
         AGO   .BYP220
.BYP200  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR MSG
.BYP220  ANOP
         AIF   ('&FIELD' EQ '').BYP240
         AIF   ('&FIELD'(1,1) NE '''').BYP230
         DC    A(SPFF&NDX)              ADDRESS OF GENERATED FIELD
         AGO   .BYP250
.BYP230  ANOP
         DC    A(&FIELD)                ADDRESS OF FIELD
         AGO   .BYP250
.BYP240  ANOP
         DC    A(SPFNULLC)              DUMMY PARM FOR FIELD
.BYP250  ANOP
         AIF   ('&CURPOS' EQ '').BYP320
         AIF   (T'&CURPOS EQ 'N').BYP310
         DC    A(&CURPOS+X'80000000')   ADDRESS OF CSRPOS
         AGO   .BYP330
.BYP310  ANOP
         DC    A(SPFC&NDX+X'80000000')  ADDRESS OF GENNED CSRPOS
         AGO   .BYP330
.BYP320  ANOP
         DC    A(SPFNULLC+X'80000000')  DUMMY PARM FOR CSRPOS
.BYP330  ANOP
SPFE&NDX DS    0H                       MARK LAST PARM
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
