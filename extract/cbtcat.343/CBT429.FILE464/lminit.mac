         MACRO
&LABEL   LMINIT &DATAID=,               DATAID NAME                    *
               &PROJECT=,               PROJECT NAME                   *
               &GROUP1=,                GROUP 1 NAME                   *
               &GROUP2=,                GROUP 2 NAME                   *
               &GROUP3=,                GROUP 3 NAME                   *
               &GROUP4=,                GROUP 4 NAME                   *
               &TYPE=,                  TYPE                           *
               &DATASET=,               DATASET NAME                   *
               &DDNAME=,                DDNAME                         *
               &VOLUME=,                VOL SER                        *
               &PASSWORD=,              PASSWORD                       *
               &ENQ=,                   ENQ TYPE                       *
               &ORG=,                   ORGANIZATION                   *
               &LOAD=ISPLINK,           ISPLINK ADDRESS                *
               &MF=I                    MACRO FORMAT
.*
.**********************************************************************
.*       GLOBALS
.**********************************************************************
.*
         GBLB  &SPFLMINIT               GLOBAL FOR LMINIT LITERAL
         GBLB  &SPFNULLC                DUMMY PARAMTER FOR CALL
         GBLB  &SPFDEBUG                DEBUG MODE
         GBLB  &SPFVAR                  NUMBER OF REQUIRED FULLWORDS
         GBLA  &SPFVARS                 NUMBER OF FULLWORDS
         GBLA  &SPFVARC                 PREVIOUS FULLWORDS USED
         LCLC  &NDX                     USED IN PLACE OF SYSNDX
         LCLC  &TEMP                    USED AS TEMPORARY VARIABLE
.*
&NDX     SETC  '&SYSNDX'
         AIF   ('&MF(2)' NE '').BYP0
&TEMP    SETC  'SPFPARMS'
         AGO   .BYP00
.BYP0    ANOP
&TEMP    SETC  '&MF(2)'
.BYP00   ANOP
         AIF   ('&LABEL' EQ '').BYP10
&LABEL   DS    0H
.BYP10   ANOP
.*
.**********************************************************************
.*       EDITS
.**********************************************************************
.*
         AIF   ('&MF(1)' NE 'I' AND                                    *
               '&MF(1)' NE 'E').ERRMF
         AIF   ('&DATAID' EQ '').ERR1
.*
.**********************************************************************
.*       CHECK TO SEE IF A BRANCH AROUND LITERALS IS REQUIRED
.**********************************************************************
.*
         AIF   (NOT &SPFLMINIT).GENLIT
         AIF   (NOT &SPFNULLC).GENLIT
         AIF   ('&DATAID'(1,1) EQ '''').GENLIT
         AIF   ('&PROJECT' EQ '').CNT1
         AIF   ('&PROJECT'(1,1) EQ '''').GENLIT
.CNT1    ANOP
         AIF   ('&GROUP1' EQ '').CNT2
         AIF   ('&GROUP1'(1,1) EQ '''').GENLIT
.CNT2    ANOP
         AIF   ('&GROUP2' EQ '').CNT3
         AIF   ('&GROUP2'(1,1) EQ '''').GENLIT
.CNT3    ANOP
         AIF   ('&GROUP3' EQ '').CNT4
         AIF   ('&GROUP3'(1,1) EQ '''').GENLIT
.CNT4    ANOP
         AIF   ('&GROUP4' EQ '').CNT5
         AIF   ('&GROUP4'(1,1) EQ '''').GENLIT
.CNT5    ANOP
         AIF   ('&TYPE' EQ '').CNT6
         AIF   ('&TYPE'(1,1) EQ '''').GENLIT
.CNT6    ANOP
         AIF   ('&DATASET' EQ '').CNT7
         AIF   ('&DATASET'(1,1) EQ '''').GENLIT
.CNT7    ANOP
         AIF   ('&DDNAME' EQ '').CNT8
         AIF   ('&DDNAME'(1,1) EQ '''').GENLIT
.CNT8    ANOP
         AIF   ('&VOLUME' EQ '').CNT9
         AIF   ('&VOLUME'(1,1) EQ '''').GENLIT
.CNT9    ANOP
         AIF   ('&PASSWORD' EQ '').CNT10
         AIF   ('&PASSWORD'(1,1) EQ '''').GENLIT
.CNT10   ANOP
         AIF   ('&ENQ' EQ '').CNT11
         AIF   ('&ENQ'(1,1) EQ '''').GENLIT
.CNT11   ANOP
         AIF   ('&ORG' EQ '').CNT12
         AIF   ('&ORG'(1,1) EQ '''').GENLIT
.CNT12   ANOP
         AGO   .BRAREA
.GENLIT  ANOP
.*
.*********************************************************************
.*       GENERATE THE BRANCH AROUND LITERALS AND THE LITERALS
.*********************************************************************
.*
         B     SPF&NDX
         AIF   (&SPFNULLC).BYP001
SPFNULLC DC    CL8'        '            DUMMY ISPF FIELD
&SPFNULLC SETB  1
.BYP001  ANOP
.*
.*
         AIF   (&SPFLMINIT).BYP20
LMINIT   DC    CL8'LMINIT'              ISPF KEYWORD
&SPFLMINIT SETB 1
.BYP20   ANOP
.*
.*
         AIF   ('&DATAID' EQ '').BYP30
         AIF   ('&DATAID'(1,1) NE '''').BYP30
SPFZ&NDX DC    CL8&DATAID               GENERATED DATAID NAME
.BYP30   ANOP
.*
.*
         AIF   ('&PROJECT' EQ '').BYP40
         AIF   ('&PROJECT'(1,1) NE '''').BYP40
SPFA&NDX DC    CL8&PROJECT              PROJECT VARIABLE NAME
.BYP40   ANOP
.*
.*
         AIF   ('&GROUP1' EQ '').BYP50
         AIF   ('&GROUP1'(1,1) NE '''').BYP50
SPFB&NDX DC    CL8&GROUP1               GROUP1 VARIABLE NAME
.BYP50   ANOP
.*
.*
         AIF   ('&GROUP2' EQ '').BYP60
         AIF   ('&GROUP2'(1,1) NE '''').BYP60
SPFC&NDX DC    CL8&GROUP2               GROUP2 VARIABLE NAME
.BYP60   ANOP
.*
.*
         AIF   ('&GROUP3' EQ '').BYP70
         AIF   ('&GROUP3'(1,1) NE '''').BYP70
SPFD&NDX DC    CL8&GROUP3               GROUP3 VARIABLE NAME
.BYP70   ANOP
.*
.*
         AIF   ('&GROUP4' EQ '').BYP80
         AIF   ('&GROUP4'(1,1) NE '''').BYP80
SPFE&NDX DC    CL8&GROUP4               USERE VARIABLE NAME
.BYP80   ANOP
.*
.*
         AIF   ('&TYPE' EQ '').BYP90
         AIF   ('&TYPE'(1,1) NE '''').BYP90
SPFF&NDX DC    CL8&TYPE                 TYPE VARIABLE NAME
.BYP90   ANOP
.*
.*
         AIF   ('&DATASET' EQ '').BYP100
         AIF   ('&DATASET'(1,1) NE '''').BYP100
SPFG&NDX DC    CL56&DATASET             DATASET NAME
.BYP100  ANOP
.*
.*
         AIF   ('&DDNAME' EQ '').BYP110
         AIF   ('&DDNAME'(1,1) NE '''').BYP110
SPFH&NDX DC    CL8&DDNAME               DDNAME VARIABLE NAME
.BYP110  ANOP
.*
.*
         AIF   ('&VOLUME' EQ '').BYP120
         AIF   ('&VOLUME'(1,1) NE '''').BYP120
SPFI&NDX DC    CL6&VOLUME               VOLUME VARIABLE NAME
.BYP120  ANOP
.*
.*
         AIF   ('&PASSWORD' EQ '').BYP130
         AIF   ('&PASSWORD'(1,1) NE '''').BYP130
SPFJ&NDX DC    CL8&PASSWORD             PASSWORD VARIABLE NAME
.BYP130  ANOP
.*
.*
         AIF   ('&ENQ' EQ '').BYP140
         AIF   ('&ENQ'(1,1) NE '''').BYP140
SPFK&NDX DC    CL8&ENQ                  ENQ VARIABLE NAME
.BYP140  ANOP
.*
.*
         AIF   ('&ORG' EQ '').BYP150
         AIF   ('&ORG'(1,1) NE '''').BYP150
SPFL&NDX DC    CL8&ORG                  ORG VARIABLE NAME
.BYP150  ANOP
.*
.*
.*
SPF&NDX  DS    0H
.BRAREA  ANOP
.*
.*********************************************************************
.*       GENERATE THE CALL
.*********************************************************************
.*
         AIF   ('&MF(1)' EQ 'I').MFI
.*
.*
         LA    14,LMINIT                GET ADDRESS OF LMINIT KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BYP1010
         LA    15,&DATAID               GET ADDRESS OF DATAID NAME
         AGO   .BYP1050
.BYP1010 ANOP
         LA    15,SPFZ&NDX              GET ADDRESS OF GENERATED DATAID
.BYP1050 ANOP
.*
.*
         AIF   ('&PROJECT' EQ '').BYP1100
         AIF   ('&PROJECT'(1,1) EQ '''').BYP1110
         LA    0,&PROJECT               GET ADDRESS OF PROJECT VARIABLE
         AGO   .BYP1150
.BYP1100 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR PROJECT
         AGO   .BYP1150
.BYP1110 ANOP
         LA    0,SPFA&NDX               GET ADDRESS OF GENNED PROJECT
.BYP1150 ANOP
.*
.*
         AIF   ('&GROUP1' EQ '').BYP1200
         AIF   ('&GROUP1'(1,1) EQ '''').BYP1210
         LA    1,&GROUP1                GET ADDRESS OF GROUP1 VARIABLE
         AGO   .BYP1250
.BYP1200 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR GROUP1
         AGO   .BYP1250
.BYP1210 ANOP
         LA    1,SPFB&NDX               GET ADDRESS OF GENNED GROUP1
.BYP1250 ANOP
.*
.*
         STM   14,1,&TEMP               STORE PARAMETERS INTO LIST
.*
.*
.*
         AIF   ('&GROUP2' EQ '').BYP1300
         AIF   ('&GROUP2'(1,1) EQ '''').BYP1310
         LA    14,&GROUP2               GET ADDRESS OF GROUP2 VARIABLE
         AGO   .BYP1350
.BYP1300 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR GROUP2
         AGO   .BYP1350
.BYP1310 ANOP
         LA    14,SPFC&NDX              GET ADDRESS OF GENNED GROUP2
.BYP1350 ANOP
.*
.*
         AIF   ('&GROUP3' EQ '').BYP1400
         AIF   ('&GROUP3'(1,1) EQ '''').BYP1410
         LA    15,&GROUP3               GET ADDRESS OF GROUP3 VARIABLE
         AGO   .BYP1450
.BYP1400 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR GROUP3
         AGO   .BYP1450
.BYP1410 ANOP
         LA    15,SPFD&NDX              GET ADDRESS OF GENNED GROUP3
.BYP1450 ANOP
.*
.*
         AIF   ('&GROUP4' EQ '').BYP1500
         AIF   ('&GROUP4'(1,1) EQ '''').BYP1510
         LA    0,&GROUP4                GET ADDRESS OF GROUP4 VARIABLE
         AGO   .BYP1550
.BYP1500 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR GROUP4
         AGO   .BYP1550
.BYP1510 ANOP
         LA    0,SPFE&NDX               GET ADDRESS OF GENNED GROUP4
.BYP1550 ANOP
.*
.*
         AIF   ('&TYPE' EQ '').BYP1600
         AIF   ('&TYPE'(1,1) EQ '''').BYP1610
         LA    1,&TYPE                  ADDRESS OF TYPE VARIABLE
         AGO   .BYP1650
.BYP1600 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR TYPE
         AGO   .BYP1650
.BYP1610 ANOP
         LA    1,SPFF&NDX               GET ADDRESS OF GENNED TYPE
.BYP1650 ANOP
.*
.*
         STM   14,1,&TEMP+16            STORE PARAMETERS INTO LIST
.*
.*
.*
         AIF   ('&DATASET' EQ '').BYP1700
         AIF   ('&DATASET'(1,1) EQ '''').BYP1710
         LA    14,&DATASET              ADDRESS OF DATASET NAME
         AGO   .BYP1750
.BYP1700 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR DATASET
         AGO   .BYP1750
.BYP1710 ANOP
         LA    14,SPFG&NDX              GET ADDRESS OF GENNED DATASET
.BYP1750 ANOP
.*
.*
         AIF   ('&DDNAME' EQ '').BYP1800
         AIF   ('&DDNAME'(1,1) EQ '''').BYP1810
         LA    15,&DDNAME               ADDRESS OF DDNAME VARIABLE
         AGO   .BYP1850
.BYP1800 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR DDNAME
         AGO   .BYP1850
.BYP1810 ANOP
         LA    15,SPFH&NDX              GET ADDRESS OF GENNED DDNAME
.BYP1850 ANOP
.*
.*
         AIF   ('&VOLUME' EQ '').BYP1900
         AIF   ('&VOLUME'(1,1) EQ '''').BYP1910
         LA    0,&VOLUME                ADDRESS OF VOLUME VARIABLE
         AGO   .BYP1950
.BYP1900 ANOP
         LA    0,SPFNULLC               GET DUMMY PARM FOR VOLUME
         AGO   .BYP1950
.BYP1910 ANOP
         LA    0,SPFI&NDX               GET ADDRESS OF GENNED VOLUME
.BYP1950 ANOP
.*
.*
         AIF   ('&PASSWORD' EQ '').BYP2000
         AIF   ('&PASSWORD'(1,1) EQ '''').BYP2010
         LA    1,&PASSWORD              ADDRESS OF PASSWORD VARIABLE
         AGO   .BYP2050
.BYP2000 ANOP
         LA    1,SPFNULLC               GET DUMMY PARM FOR PASSWORD
         AGO   .BYP2050
.BYP2010 ANOP
         LA    1,SPFJ&NDX               GET ADDRESS OF GENNED PASSWORD
.BYP2050 ANOP
.*
.*
         STM   14,1,&TEMP+32            STORE PARAMETERS INTO LIST
.*
.*
.*
         AIF   ('&ENQ' EQ '').BYP2100
         AIF   ('&ENQ'(1,1) EQ '''').BYP2110
         LA    14,&ENQ                  ADDRESS OF ENQ VARIABLE
         AGO   .BYP2150
.BYP2100 ANOP
         LA    14,SPFNULLC              GET DUMMY PARM FOR ENQ
         AGO   .BYP2150
.BYP2110 ANOP
         LA    14,SPFK&NDX              GET ADDRESS OF GENNED ENQ
.BYP2150 ANOP
.*
.*
         AIF   ('&ORG' EQ '').BYP2200
         AIF   ('&ORG'(1,1) EQ '''').BYP2210
         LA    15,&ORG                  ADDRESS OF ORG VARIABLE
         AGO   .BYP2250
.BYP2200 ANOP
         LA    15,SPFNULLC              GET DUMMY PARM FOR ORG
         AGO   .BYP2250
.BYP2210 ANOP
         LA    15,SPFL&NDX              GET ADDRESS OF GENNED ORG
.BYP2250 ANOP
.*
.*
         STM   14,15,&TEMP+48           STORE PARAMETERS INTO LIST
.*
.*
.*
         OI    &TEMP+52,X'80'           MARK LAST PARM
         LA    1,&TEMP                  GET PARAMETER LIST ADDRESS
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         AIF   (NOT &SPFVAR).BYVAR
         AIF   (&SPFVARC LT 14).ERRVAR
         AGO   .BYPVARS
.BYVAR   AIF   (&SPFVARS GE 14).BYPVARS
&SPFVARS SETA  14
.BYPVARS ANOP
         MEXIT
.*
.**********************************************************************
.*       MACRO FORMAT I
.**********************************************************************
.*
.MFI     ANOP
         CNOP  0,4
         BAL   1,SPFE&NDX               BRANCH AROUND LITERALS
.*
.*
         DC    A(LMINIT)                GET ADDRESS OF LMINIT KEYWORD
.*
.*
         AIF   ('&DATAID'(1,1) EQ '''').BY1010
         DC    A(&DATAID)               GET ADDRESS OF DATAID NAME
         AGO   .BY1050
.BY1010 ANOP
         DC    A(SPFZ&NDX)              GET ADDRESS OF GENERATED DATAID
.BY1050 ANOP
.*
.*
         AIF   ('&PROJECT' EQ '').BY1100
         AIF   ('&PROJECT'(1,1) EQ '''').BY1110
         DC    A(&PROJECT)              GET ADDRESS OF PROJECT VARIABLE
         AGO   .BY1150
.BY1100 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR PROJECT
         AGO   .BY1150
.BY1110 ANOP
         DC    A(SPFA&NDX)              GET ADDRESS OF GENNED PROJECT
.BY1150 ANOP
.*
.*
         AIF   ('&GROUP1' EQ '').BY1200
         AIF   ('&GROUP1'(1,1) EQ '''').BY1210
         DC    A(&GROUP1)               GET ADDRESS OF GROUP1 VARIABLE
         AGO   .BY1250
.BY1200 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR GROUP1
         AGO   .BY1250
.BY1210 ANOP
         DC    A(SPFB&NDX)              GET ADDRESS OF GENNED GROUP1
.BY1250 ANOP
.*
.*
         AIF   ('&GROUP2' EQ '').BY1300
         AIF   ('&GROUP2'(1,1) EQ '''').BY1310
         DC    A(&GROUP2)               GET ADDRESS OF GROUP2 VARIABLE
         AGO   .BY1350
.BY1300 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR GROUP2
         AGO   .BY1350
.BY1310 ANOP
         DC    A(SPFC&NDX)              GET ADDRESS OF GENNED GROUP2
.BY1350 ANOP
.*
.*
         AIF   ('&GROUP3' EQ '').BY1400
         AIF   ('&GROUP3'(1,1) EQ '''').BY1410
         DC    A(&GROUP3)               GET ADDRESS OF GROUP3 VARIABLE
         AGO   .BY1450
.BY1400 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR GROUP3
         AGO   .BY1450
.BY1410 ANOP
         DC    A(SPFD&NDX)              GET ADDRESS OF GENNED GROUP3
.BY1450 ANOP
.*
.*
         AIF   ('&GROUP4' EQ '').BY1500
         AIF   ('&GROUP4'(1,1) EQ '''').BY1510
         DC    A(&GROUP4)               GET ADDRESS OF GROUP4 VARIABLE
         AGO   .BY1550
.BY1500 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR GROUP4
         AGO   .BY1550
.BY1510 ANOP
         DC    A(SPFE&NDX)              GET ADDRESS OF GENNED GROUP4
.BY1550 ANOP
.*
.*
         AIF   ('&TYPE' EQ '').BY1600
         AIF   ('&TYPE'(1,1) EQ '''').BY1610
         DC    A(&TYPE)                 ADDRESS OF TYPE VARIABLE
         AGO   .BY1650
.BY1600 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR TYPE
         AGO   .BY1650
.BY1610 ANOP
         DC    A(SPFF&NDX)              GET ADDRESS OF GENNED TYPE
.BY1650 ANOP
.*
.*
         AIF   ('&DATASET' EQ '').BY1700
         AIF   ('&DATASET'(1,1) EQ '''').BY1710
         DC    A(&DATASET)              ADDRESS OF DATASET NAME
         AGO   .BY1750
.BY1700 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR DATASET
         AGO   .BY1750
.BY1710 ANOP
         DC    A(SPFG&NDX)              GET ADDRESS OF GENNED DATASET
.BY1750 ANOP
.*
.*
         AIF   ('&DDNAME' EQ '').BY1800
         AIF   ('&DDNAME'(1,1) EQ '''').BY1810
         DC    A(&DDNAME)               ADDRESS OF DDNAME VARIABLE
         AGO   .BY1850
.BY1800 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR DDNAME
         AGO   .BY1850
.BY1810 ANOP
         DC    A(SPFH&NDX)              GET ADDRESS OF GENNED DDNAME
.BY1850 ANOP
.*
.*
         AIF   ('&VOLUME' EQ '').BY1900
         AIF   ('&VOLUME'(1,1) EQ '''').BY1910
         DC    A(&VOLUME)               ADDRESS OF VOLUME VARIABLE
         AGO   .BY1950
.BY1900 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR VOLUME
         AGO   .BY1950
.BY1910 ANOP
         DC    A(SPFI&NDX)              GET ADDRESS OF GENNED VOLUME
.BY1950 ANOP
.*
.*
         AIF   ('&PASSWORD' EQ '').BY2000
         AIF   ('&PASSWORD'(1,1) EQ '''').BY2010
         DC    A(&PASSWORD)             ADDRESS OF PASSWORD VARIABLE
         AGO   .BY2050
.BY2000 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR PASSWORD
         AGO   .BY2050
.BY2010 ANOP
         DC    A(SPFJ&NDX)              GET ADDRESS OF GENNED PASSWORD
.BY2050 ANOP
.*
.*
         AIF   ('&ENQ' EQ '').BY2100
         AIF   ('&ENQ'(1,1) EQ '''').BY2110
         DC    A(&ENQ)                  ADDRESS OF ENQ VARIABLE
         AGO   .BY2150
.BY2100 ANOP
         DC    A(SPFNULLC)              GET DUMMY PARM FOR ENQ
         AGO   .BY2150
.BY2110 ANOP
         DC    A(SPFK&NDX)              GET ADDRESS OF GENNED ENQ
.BY2150 ANOP
.*
.*
         AIF   ('&ORG' EQ '').BY2200
         AIF   ('&ORG'(1,1) EQ '''').BY2210
         DC    A(&ORG+X'80000000')      ADDRESS OF ORG VARIABLE
         AGO   .BY2250
.BY2200 ANOP
         DC    A(SPFNULLC+X'80000000')  GET DUMMY PARM FOR ORG
         AGO   .BY2250
.BY2210 ANOP
         DC    A(SPFL&NDX+X'80000000')  GET ADDRESS OF GENNED ORG
.BY2250 ANOP
.*
.*
SPFE&NDX DS    0H
         L     15,&LOAD                 LOAD ADDRESS OF ISPF
         BALR  14,15                    CALL ISPF
         MEXIT
.*
.**********************************************************************
.*       MACRO ERRORS
.**********************************************************************
.*
.ERRMF   ANOP
         MNOTE 16,'INVALID MACRO FORMAT SPECIFIED - FOUND ''&MF(1)'''
         MEXIT
.ERR1    ANOP
         MNOTE 16,'DATAID NAME IS REQUIRED'
         MEXIT
.ERRVAR  ANOP
         MNOTE 16,'ISPF PARMS MACRO WILL NOT HOLD FULL PARMLIST'
         MNOTE *,' ISPF PARMS MACRO GENERATE PARMLIST TOO SMALL'
         MEXIT
         MEND
