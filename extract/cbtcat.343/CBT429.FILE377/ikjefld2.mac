         TITLE 'IKJEFLD2 -- LOGOFF EXIT '
IKJEFLD2 CSECT
IKJEFLD2 AMODE 24
IKJEFLD2 RMODE 24
***********************************************************************
*        KILL 'IKJ56457I PROGRAM ERROR' MESSAGE IF LOGON UNDER 'MSTR'
***********************************************************************
*  REGISTER ASSIGNMENTS
***********************************************************************
*
R0       EQU   0
R1       EQU   1                   Input ptr.
R2       EQU   2                   Work register.
R3       EQU   3                   Work register.
R4       EQU   4                   Work register.
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
RBASE    EQU   12                  Base register.
R13      EQU   13                  Hi-save ptr.
R14      EQU   14                  Return register.
R15      EQU   15                  Link register.
*
         SPACE
         SAVE  (14,12),,===>IKJEFLD2-&SYSDATE.-&SYSTIME<===
         LR    RBASE,R15           Load base register
         USING IKJEFLD2,RBASE  *** Synchronize base register
         USING PSA,R0          *** Synchronize PSA DSECT
         L     R2,PSATOLD          Load TCB address
         USING TCB,R2          *** Synchronize TCB DSECT
         L     R2,TCBJSCB          Load JSCB address
         DROP  R2              *** Drop TCB DSECT
         USING IEZJSCB,R2      *** Synchronize JSCB DSECT
         L     R2,JSCBSSIB         Load life-of-job SSIB address
         DROP  R2              *** Drop JSCB DSECT
         USING SSIB,R2         *** Synchronize SSIB DSECT
         CLC   SSIBSSNM,=C'MSTR'   Is this MSTR subsystem ?
         BNE   RET                 Branch if not
         DROP  R2              *** Drop SSIB DSECT
         L     R2,PSAAOLD          Load ASCB address
         USING ASCB,R2         *** Synchronize ASCB DSECT
         L     R2,ASCBASXB         Load ASXB address
         DROP  R2              *** Drop ASCB DSECT
         USING ASXB,R2         *** Synchronize ASXB DSECT
         L     R2,ASXBLWA          Load LWA address
         DROP  R2              *** Drop ASXB DSECT
         USING LWA,R2          *** Synchronize LWA DSECT
         L     R2,LWAJSEL          Load JSEL address
         DROP  R2              *** Drop LWA DSECT
         USING JSEL,R2         *** Synchronize JSEL DSECT
         L     R3,JSELJSXL         Load JSXL address
         DROP  R2              *** Drop JSEL DSECT
         CLI   2(R3),X'24'         JSS RC = 32 (ERR DUE TO ABEND33E) ?
         BNE   RET                 Branch if not
         IPK                       Save PSW key
         SPKA  X'00'               Switch to PSW key zero
         MVI   2(R3),X'00'         Set return code=00 (KILL IKJ56457I)
         SPKA  0(R2)               Restore PSW key
RET      DS    0H
         RETURN (14,12),RC=0       Return
*-------
         LTORG
         TITLE 'IKJEFLD2--SSIB Map'
         IEFJSSIB ,
JSELSECT DSECT ,
         TITLE 'IKJEFLD2--JSEL Map'
         IEFVJSEL ,
         TITLE 'IKJEFLD2--JSCB Map'
         IEZJSCB ,
         TITLE 'IKJEFLD2--ASCB Map'
         IHAASCB ,
         TITLE 'IKJEFLD2--ASXB Map'
         IHAASXB ,
         TITLE 'IKJEFLD2--PSA Map'
         IHAPSA ,
         TITLE 'IKJEFLD2--LWA Map'
         IKJEFLWA ,
         TITLE 'IKJEFLD2--TCB Map'
         IKJTCB ,
         END   IKJEFLD2
