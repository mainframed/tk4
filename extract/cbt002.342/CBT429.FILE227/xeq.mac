         TITLE 'XEQ TSO COMMAND PROCESSOR'
XEQ      CSECT
************************************************************
* XEQ TSO COMMAND PROCESSOR                                *
*                                                          *
* THIS COMMAND PROCESSOR WILL INVOKE A PROGRAM RESIDING    *
* IN A SYSTEM LIBRARY                                      *
*                                                          *
* FORMAT:   XEQ   PROGRAM-NAME   'PARM-DATA'               *
*                                                          *
************************************************************
 SPACE 1
*                   MAIN                     VALIDITY CHECK EXIT
*                   ----                     -------------------
R0       EQU   0    WORK REG                 WORK REG
R1       EQU   1    WORK REG                 WORK REG
R2       EQU   2    NOT USED                 NOT USED
R3       EQU   3    MODULE BASE REG          MODULE BASE REG
R4       EQU   4    A(CPPL)                  A(CPPL)
R5       EQU   5    A(PPL)                   NOT USED
R6       EQU   6    WORK REG                 A(WORKAREA)
R7       EQU   7    NOT USED                 A(PDE)
R8       EQU   8    WORK REG                 WORK REG
R9       EQU   9    WORK REG                 WORK REG
R10      EQU   10   WORK REG                 WORK REG
R11      EQU   11   A(PDL)                   A(PSCB)
R12      EQU   12   WORK REG                 NOT USED
R13      EQU   13   A(WORKAREA)              A(EXITSAVE)
R14      EQU   14   LINK REG                 LINK REG
R15      EQU   15   RETURN CODES             RETURN CODES
 SPACE 1
****************************
* ENTRY AND INITIALIZATION *
****************************
XEQ      CSECT
         USING *,R15          USING R15 AT *
         B     XEQENT         GO AROUND MODULE ID
         DROP  R15            DROP R15
         DC    CL4'XEQ'       MODULE ID
         DC    CL4'0101'      VERSION
 SPACE 1
XEQENT   STM   R14,R12,12(R13) SAVE R14-R12
         BALR  R3,0           R3 IS MODULE BASE REG
         USING *,R3           USING R3 AT *
         LR    R4,R1          A(CPPL) IN R4
         USING CPPL,R4        USING R4 AT CPPL
 SPACE 1
         LA    R0,WORKLEN     L'WORKAREA IN R0
         GETMAIN R,LV=(R0)    ACQUIRE WORKAREA
         LR    R8,R1          A(WORKAREA) IN R8
         ST    R13,4(R8)      A(HISAVE) IN MYSAVE+4
         ST    R8,8(R13)      A(MYSAVE) IN HISAVE+8
         LR    R13,R8         A(MYSAVE) IN R13
         USING WORKAREA,R13   USING R13 AT WORKAREA
 SPACE 1
************************
* PARSE COMMAND BUFFER *
************************
XEQPARS  LA    R5,XEQPPL      A(PPL) IN R5
         USING PPL,R5         USING R5 AT PPL
         MVC   PPLUPT,CPPLUPT A(UPT) IN PPL
         MVC   PPLECT,CPPLECT A(ECT) IN PPL
         MVC   PPLCBUF,CPPLCBUF A(CBUF) IN PPL
         MVC   PPLPCL,XEQPCLA A(PCL) IN PPL
         XC    ECB,ECB        ZERO CP ECB
         LA    R8,ECB         A(CP ECB)
         ST    R8,PPLECB        IN PPL
         XC    XEQANS,XEQANS  ZERO PDL ANSWER AREA
         LA    R8,XEQANS      A(PDL ANSWER AREA)
         ST    R8,PPLANS        IN PPL
         LA    R8,WORKAREA    A(WORKAREA)
         ST    R8,PPLUWA        IN PPL
 SPACE 1
         LA    R1,PPL         A(PPL) IN R1
         LINK  EP=IKJPARS     GO PARSE COMMAND BUFFER
         LR    R8,R15         PUT PARSE RETURN CODE IN R8
 SPACE 1
************************
* PARSE ERROR ANALYSIS *
************************
         B     XEQPARCK(R8) ROUTE TO ERROR HANDLER
XEQPARCK B     XEQPAROK       NO ERRORS
         B     XEQPE4         INCOMPLETE PARAMETERS AND NOPROMPT
         B     XEQRET         USER ATTENTIONED OUT (RETURN)
         B     XEQPE12        BAD PPL (ABEND)
         B     XEQPE16        INSUFFICIENT REGION SIZE FOR PARSE
         B     XEQPE20        INVALID PARAMETERS (VALIDCK FAILURE)
         B     XEQPE24        PCL CONFLICT (ABEND)
 SPACE 1
XEQPE0   ABEND 100,DUMP       A(PDL) IS ZERO AFTER SUCCESSFUL PARSE
 SPACE 1
XEQPE4   LA    R1,XEQPM4      INCOMPLETE PARAMETERS AND NOPROMPT
         BAL   R6,XEQTPUT     GO TELL
         B     XEQRET         GO RETURN
 SPACE 1
XEQPE12  ABEND 112,DUMP       BAD PPL
 SPACE 1
XEQPE16  LA    R1,XEQPM16     INSUFFICIENT REGION SIZE FOR PARSE
         BAL   R6,XEQTPUT     GO TELL
         B     XEQRET         GO RETURN
 SPACE 1
XEQPE20  LA    R1,XEQPM20     INVALID PARAMETERS (VALIDCK FAILURE)
         BAL   R6,XEQTPUT     GO TELL
         B     XEQRET         GO RETURN
 SPACE 1
XEQPE24  ABEND 124,DUMP       PCL CONFLICT
 SPACE 1
************************************
* PREPARE PARM DATA IF ANY PRESENT *
************************************
XEQPAROK L     R11,XEQANS     A(PDL) IN R11
         LA    R11,0(R11)     EXORCISE HI ORDER BYTE
         LTR   R11,R11        DOES PDL EXIST
         BZ    XEQPE0         NO, SOMETHING IS WRONG
         USING XEQPDL,R11     USING R11 AT XEQPDL
 SPACE 1
XEQPCK   LA    R8,PARMLEN     A(PARM AREA) IN R8
         ST    R8,PARMADDR    STORE IN WORKAREA
         OI    PARMADDR,X'80' INDICATE LAST IN LIST
         XC    PARMLEN,PARMLEN ZERO PARMLEN
         MVI   PARMAREA,C' '  SET BLANK
         MVC   PARMAREA+1(L'PARMAREA-1),PARMAREA PROPAGATE
         TM    PARM+6,X'80'   IS PARM PRESENT
         BZ    XEQPRLS        NO, CONTINUE
 SPACE 1
         L     R8,PARM        A(PARM DATA) IN R8
         LTR   R8,R8          IS PARM PRESENT
         BZ    XEQPRLS        NO, CONTINUE
 SPACE 1
         MVC   PARMLEN,PARM+4 SET L'PARM DATA
         LH    R9,PARMLEN     L'PARM DATA IN R9
         SH    R9,XEQONE      DECREMENT BY 1
         BM    XEQPRLS        BRANCH IF NO PARM DATA
 SPACE 1
         CH    R9,XEQPMAX     DOES PARM LENGTH EXCEED MAX
         BL    XEQPSET        NO, CONTINUE
 SPACE 1
         MVC   PARMLEN,XEQPMAX YES, SET
         LH    R9,PARMLEN           TO
         SH    R9,XEQONE            MAX
         LA    R1,XEQMM0      PARM LENGTH EXCEEDS MAX
         BAL   R6,XEQTPUT     GO TELL
 SPACE 1
XEQPSET  EX    R9,PARMOVE     MOVE PARM DATA TO WORKAREA
 SPACE 1
***********************
* CLEANUP AFTER PARSE *
***********************
XEQPRLS  IKJRLSA XEQANS       RELEASE PARSE STORAGE
 SPACE 1
**********************************
* XFER CONTROL TO TARGET PROGRAM *
**********************************
XEQXFER  MVC   MEMXFER(MEMXFERL),MEMXFERS PREP XFER LIST SKELETON
         LA    R1,PARMADDR    A(PARM LIST) IN R1
         LA    R15,MEMXFER    A(XFER LIST) IN R15
         LA    R8,BLDLNAME    A(BLDL ENTRY) IN R8
         L     R13,4(R13)     A(HISAVE) IN R13
         XCTL  (2,12),        XFER CONTROL TO TARGET PROGRAM           X
               DE=(R8),                                                X
               MF=(E,(1)),SF=(E,(15))
 SPACE 1
**********************
* CLEANUP AND RETURN *
**********************
XEQRET   LA    R1,WORKAREA    A(WORKAREA) IN R1
         LA    R0,WORKLEN     L'WORKAREA IN R0
         L     R13,4(R13)     A(HISAVE) IN R13
         FREEMAIN R,LV=(R0),A=(R1) FREE WORKAREA
         LM    R14,R12,12(R13) RESTORE R14-R12
         BR    R14            RETURN
 SPACE 1
*********************************
* SUBROUTINE TO WRITE A MESSAGE *
*********************************
XEQTPUT  SR    R0,R0          ZERO R0
         IC    R0,0(R1)       L'MESSAGE IN R0
         LA    R1,1(R1)       A(MESSAGE) IN R1
         TPUT  (R1),(R0)      TELL TERMINAL
         BR    R6             RETURN TO CALLER
 SPACE 1
**************************
* LOCAL READONLY STORAGE *
**************************
MEMXFERS XCTL  SF=L           SKELETON MEMBER XFER CONTROL LIST
MEMXFERL EQU   *-MEMXFERS     LENGTH
 SPACE 1
XEQPCLA  DC    V(XEQPCL)      A(PCL)
XEQPMAX  DC    H'100'         MAX LENGTH FOR PARMS
XEQONE   DC    H'1'           CONSTANT OF 1
MEMOVEB  MVC   0(0,R10),0(R9) MOVE MEMBER NAME TO BLDL NAME AREA
PARMOVE  MVC   PARMAREA(0),0(R8) MOVE PARM TO WORKAREA
 SPACE 1
XEQPM4   DC    AL1(XEQPM4L),C'INCOMPLETE PARAMETERS AND NOPROMPT'
XEQPM4L  EQU   *-(XEQPM4+1)   LENGTH
 SPACE 1
XEQPM16  DC    AL1(XEQPM16L),C'INSUFFICIENT REGION SIZE FOR PARSE'
XEQPM16L EQU   *-(XEQPM16+1)  LENGTH
 SPACE 1
XEQPM20  DC    AL1(XEQPM20L),C'INVALID PARAMETERS (VALIDCK FAILURE)'
XEQPM20L EQU   *-(XEQPM20+1)  LENGTH
 SPACE 1
XEQMM0   DC    AL1(XEQMM0L),C'PARM LENGTH EXCEEDS MAX, TRUNCATED'
XEQMM0L  EQU   *-(XEQMM0+1)   LENGTH
 SPACE 1
         DROP  R5,R11,R13     DROP R5, R11, R13
 SPACE 1
****************************************
* VALIDITY CHECK EXIT FOR PROGRAM NAME *
****************************************
MEMVAL   STM   R14,R12,12(R13) SAVE R14-R12
         LR    R8,R1          SAVE A(EXIT PARM LIST) IN R8
         USING MEMCOMM,R8     USING R8 AT MEMCOMM
         L     R6,USERWORD    A(WORKAREA) IN R6
         USING WORKAREA,R6    USING R6 AT WORKAREA
         LM    R3,R4,SAVEAREA+32 RESTORE MODULE BASE REG AND A(CPPL)
         L     R11,CPPLPSCB   A(PSCB) IN R11
         USING PSCB,R11       USING R11 AT PSCB
         L     R7,PDEADR      A(PDE) IN R7
         DROP  R8             DROP R8
 SPACE 1
         LA    R8,EXITSAVE    A(SAVEAREA) IN R8
         ST    R13,4(R8)      A(HISAVE) IN MYSAVE)+4
         ST    R8,8(R13)      A(MYSAVE) IN HISAVE+8
         LR    R13,R8         A(MYSAVE) IN R13
 SPACE 1
****************************************
* DO A BLDL TO VERIFY MEMBER EXISTENCE *
****************************************
MEMVALB  MVC   BLDLCNT,XEQONE SET ENTRY COUNT TO 1
         LA    R8,BLDLENT     SET ENTRY
         STH   R8,BLDLENE       LENGTH
         XC    BLDLRES,BLDLRES ZERO REST
         MVI   BLDLNAME,C' '  SET BLANK
         MVC   BLDLNAME+1(L'BLDLNAME-1),BLDLNAME PROPAGATE
 SPACE 1
         SR    R8,R8          ZERO R8
         LH    R8,4(R7)       L'MEMBER NAME IN R8
         SH    R8,XEQONE      DECREMENT BY 1
         BM    MEMVNO         BRANCH IF NO MEMBER - GO PROMPT
 SPACE 1
         L     R9,0(R7)       A(MEMBER NAME) IN R9
         LTR   R9,R9          DOES MEMBER NAME EXIST
         BZ    MEMVNO         NO, GO PROMPT
 SPACE 1
         LA    R10,BLDLNAME   A(BLDL NAME AREA) IN R10
         EX    R8,MEMOVEB     MOVE MEMBER NAME TO BLDL NAME AREA
 SPACE 1
         BLDL  0,BLDLIST      ISSUE BLDL FOR MEMBER NAME
         LTR   R15,R15        WAS MEMBER FOUND
         BZ    MEMVALA        YES, GO GET AUTHORIZATION CLEARANCE
 SPACE 1
MEMVNO   LA    R15,4          NO, SET PARAMETER INVALID INDICATOR
         B     MEMVRET        GO RETURN
 SPACE 1
**********************************
* VALIDATE AUTHORIZATION VIA MMI *
**********************************
MEMVALA  MVI   MMIREQ,MMIREQPG INDICATE PROGRAM TYPE
         MVC   MMIUSER,PSCBUSER MOVE USERID TO MMICB
         MVC   MMIPROG,BLDLNAME MOVE MEMBER NAME
         LA    R1,MMICB       A(MMICB) IN R1
*        LINK  EP=TSOMMI      LINK TO MMI
*        LTR   R15,R15        IS AUTHORIZATION CLEARED
*        BZ    MEMVRET        YES, GO RETURN
         B     MEMVRET        ALWAYS GO
 SPACE 1
         LA    R15,8          NO, SET PARAMETER INVALID INDICATOR
 SPACE 1
MEMVRET  L     R13,4(R13)     A(HISAVE) IN R13
         ST    R15,16(R13)    PLACE RETURN CODE IN R15
         LM    R14,R12,12(R13) RESTORE R14-R12
         BR    R14            RETURN TO PARSE
 SPACE 1
         DROP  R11,R6         DROP R11, R6
 SPACE 1
************************************************
* PARSE CONTROL LIST AND PARSE DESCRIPTOR LIST *
************************************************
XEQPCL   IKJPARM DSECT=XEQPDL
 SPACE 1
MEMBER   IKJIDENT 'MEMBER NAME',MAXLNTH=8,OTHER=ALPHANUM,              X
               PROMPT='MEMBER NAME',VALIDCK=MEMVAL
 SPACE 1
PARM     IKJPOSIT QSTRING
 SPACE 1
         IKJENDP
 SPACE 1
******************
* DSECT MAPPINGS *
******************
MEMCOMM  DSECT                MEMBER VALIDITY CHECK EXIT COMM
PDEADR   DS    A              A(PDE)
USERWORD DS    A              A(WORKAREA)
VALMSG   DS    A              A(MESSAGE)
 SPACE 1
         IKJPPL
PPLLEN   EQU   *-PPL          LENGTH
 SPACE 1
WORKAREA DSECT                WORKAREA
******************************
* COMMAND PROCESSOR WORKAREA *
******************************
SAVEAREA DS    18F            STD SAVEAREA (MAIN)
 SPACE 1
MEMXFER  DS    0D             MEMBER XFER LIST
         ORG   *+MEMXFERL       AREA
 SPACE 1
XEQPPL   DS    0A             PPL
         ORG   *+PPLLEN        AREA
 SPACE 1
ECB      DS    F              CP ECB
XEQANS   DS    A              A(PDL)
EXITSAVE DS    18F            STD SAVEAREA (VALIDITY CHECK EXIT)
 SPACE 1
BLDLIST  DS    0F             BLDL LIST FOR MEMBER VALIDATION
BLDLCNT  DS    H              MEMBER COUNT
BLDLENE  DS    H              ENTRY LENGTH AREA
BLDLNAME DS    CL8            MEMBER NAME
BLDLRES  DS    CL50           REST
BLDLENT  EQU   *-BLDLNAME     ENTRY LENGTH
 SPACE 1
PARMADDR DS    A              A(PARMAREA)
PARMLEN  DS    H              L'PARM DATA
PARMAREA DS    CL100          PARM DATA
 SPACE 1
MMICB    DS    0CL16          MMICB
MMIREQ   DS    XL1            REQUEST
MMIREQPG EQU   128            PROGRAM TYPE
MMIUSER  DS    CL7            USERID
MMIPROG  DS    CL8            MEMBER NAME
         DS    0D             ALIGNMENT
WORKLEN  EQU   *-WORKAREA     LENGTH
 SPACE 1
         IKJCPPL
 SPACE 1
         IKJPSCB
         END   XEQ           END ASSEMBLY
