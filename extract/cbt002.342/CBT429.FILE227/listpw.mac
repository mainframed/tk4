         MACRO
&LABEL   $EPILOG &RC
&LABEL   LR    R1,R13              GET SAVEAREA ADDRESS
         L     R13,4(R13)          GET BACK CHAIN POINTER
         L     R0,16(R13)          GET SAVEAREA LENGTH
         ST    R15,16(R13)         SAVE REGISTER 15 (RETCODE)
         FREEMAIN R,LV=(0),A=(1)   FREE SAVEAREA
         LM    R14,R12,12(R13)     RESTORE CALLERS REGS
         AIF   (T'&RC EQ 'O').SPEC
         LA    R15,&RC             SET RETURN CODE
.SPEC    ANOP
         BR    R14                 RETURN TO CALLER
         MEND
         MACRO
&LABEL   $PROLOG &LV=0
.**********************************************************************
.*
.*       THIS MACRO WILL PROVIDE ENTRY LINKAGE AND OPTIONALLY
.*       MULTIPLE BASE REGISTERS.  ALSO, VIA THE 'LV=' KEYWORD
.*       PROVIDE ADDITIONAL USER STORAGE (APPENDED TO THE
.*       SAVE AREA) ADDRESSABLE FROM REG 13.  IF NO OPERANDS
.*       ARE CODED, REG 12 IS ASSUMED THE BASE. EXAMPLE:
.*              SECTNAME $PROLOG          = STANDARD REG 12 BASE
.*              SECTNAME $PROLOG 5        = STANDARD, REG 5 BASE
.*              SECTNAME $PROLOG 10,LV=20 = ADD 20 BYTES TO SAVE AREA
.*                                             REG 10 IS BASE
.*              SECTNAME $PROLOG R10,R11  = REGS 10 AND 11 ARE BASES
.*
.**********************************************************************
         LCLA  &AA,&AB,&AC
         GBLB  &PRORG
&AC      SETA  4096
&LABEL   CSECT
         B     32(R15)             BRANCH AROUND
         DC    AL1(26)
         DC    CL8'&LABEL'         CSECT NAME
         DC    C'-'
         DC    CL8'&SYSDATE'       COMPILE DATE
         DC    C'-'
         DC    CL8'&SYSTIME'       COMPILE TIME
         CNOP  0,4                 ALIGNMENT
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             LOAD BASE REG
         USING &LABEL,R12          INFORM ASSEMBLER
         AIF   (&LV GT 4023).MERR
         LA    R0,&LV+72           LOAD REG 0 WITH LENGTH VARIABLE
         GETMAIN R,LV=(0)          GET CORE FOR SAVEAREA AND USER
         AIF   (&LV+72 LE 256).XC2
         AIF   (&LV+72 LE 512).XC1
         MVI   0(R1),X'00'         MOVE X'00' TO FIRST BYTE
         LR    R2,R1               SAVE POINTER IN EVEN REG
         LA    R4,1(R1)            SET RECEIVING POINTER
         LR    R5,R0               SET RECEIVING LENGTH
         BCTR  R5,R0               DECREMENT LENGTH
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE
         LA    R3,1                SET SENDING LENGTH
         MVCL  R4,R2               INSTRUCTION PADS WITH X'00'
         AGO   .STORE
.XC1     ANOP
         XC    256(&LV-184,R1),256(R1)  CLEAR SAVE AREA
         XC    0(256,R1),0(R1)          CLEAR SAVE AREA
         AGO   .STORE
.XC2     ANOP
         XC    0(&LV+72,R1),0(R1)       CLEAR SAVE AREA
.STORE   ANOP
         ST    R13,4(R1)           SAVE BACK CHAIN
         ST    R1,8(R13)           SET FORWARD CHAIN
         LR    R11,R1              SAVE NEW SAVEAREA ADDRESS
         L     R15,16(R13)         RESTORE REG 15
         ST    R0,16(R13)          SAVE SAVEAREA LENGTH
         LM    R0,R1,20(R13)       RESTORE REGS USED IN GETMAIN
         LR    R13,R11             SET SAVEAREA POINTER
         AIF   (N'&SYSLIST EQ 0).MEND
         AIF   ('&SYSLIST(1)' EQ 'R12').SKIPIT
         AIF   ('&SYSLIST(1)' EQ '12').SKIPIT
         LA    &SYSLIST(1),&LABEL  LOAD REQUESTED BASE REG
         DROP  R12                 DROP ASSUMED BASE REG
         USING &LABEL,&SYSLIST(1)  INFORM ASSEMBLER
.SKIPIT  ANOP
&AA      SETA  2
.LOOP    ANOP
         AIF   (&AA GT N'&SYSLIST).MEXIT
&AB      SETA  &AA-1
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AB))  LOAD NEXT BASE REG
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AA))  LOAD NEXT BASE REG
         USING &LABEL+&AC,&SYSLIST(&AA) INFORM ASSEMBLER
&AC      SETA  &AC+4096
&AA      SETA  &AA+1
         AGO   .LOOP
.MEXIT   ANOP
         AIF   (&PRORG).MEX2
         SPACE
         $REGS
         SPACE
.MEX2    ANOP
&AA      SETA  &LV+72
         MNOTE *,'TOTAL STORAGE AREA RECEIVED = &AA'
         MEXIT
.MEND    ANOP
         MNOTE *,'NO REGISTER SPECIFIED - R12 ASSUMED'
         AGO   .MEXIT
.MERR    ANOP
         MNOTE 12,'LV > 4023 - REQUEST IGNORED'
         AGO   .MEXIT
         MEND
         MACRO
         $REGS
         GBLB  &PRORG
         AIF   (&PRORG).MEX2
&PRORG   SETB  1
 SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
 SPACE
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7
REG8     EQU   8
REG9     EQU   9
REG10    EQU   10
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
 SPACE
.MEX2    ANOP
       MEND
*----------------------------------------------------------------------
*
*        BOB BECKER NOV ,1977
*
*        THIS TSO COMMAND PROCESSOR WILL DISPLAY THE CONTENTS OF THE
*        PASSWORD DATASET TO AUTHORIZED USERS (AN AUTHORIZED USER IS
*        ONE THAT HAS 'OPER' AUTHORITY IN THE UADS DATASET).  IF
*        ADDITIONAL CONTROL IS REQUIRED, THE 'PASSWORD' DATASET MAY
*        BE 'PASSWORD PROTECTED' AS WELL.
*
*        NO PARAMETERS SHOULD BE SPECIFIED WHEN INVOKED.  INSTEAD,
*        THIS ROUTINE WILL PROMPT FOR THE OPTION (DEFINED BELOW).
*
*        OPTIONS ARE: DSN= NAME OF DATASET FOR WHICH THE PASSWORD
*                          IS TO BE DISPLAYED
*                     PW=  PASSWORD FOR WHICH ASSOCIATED DATASETS
*                          ARE TO BE DISPLAYED
*                   (NULL) LIST ALL PASSWORD/DATASET ASSOCIATIONS
*
*----------------------------------------------------------------------
         EJECT
*----------------------------------------------------------------------
*
*        VALIDATE USER AUTHORITY BY CHECKING THE PSCB FOR 'OPER'
*        AND LOCATE THE VOLUME ON WHICH THE PASSWORD DATASET RESIDES
*
*----------------------------------------------------------------------
LISTPW   $PROLOG
ENTRY010 DS    0H
         L     R2,16          GET CVT POINTER
         L     R2,0(R2)       OLD/NEW POINTER
         L     R2,4(R2)       OUR TCB POINTER
         L     R15,12(R2)     OUR TIOT POINTER
*        CLI   0(R15),C'S'    IS THIS SYSTEMS USER?
*        BNE   ERROR1         NO, INFORM USER OF RESTRICTION
         L     R2,180(R2)     JSCB POINTER
         L     R2,264(R2)     PSCB POINTER
         TM    16(R2),X'80'   DO WE HAVE OPER ABILITY?
    LINK EP=AOFF
         LOCATE LOCATE        LOCATE SYS1.NUCLEUS TO GET VOLSER
         LTR   R15,R15        CHECK RETCODE
         BNZ   ERROR0         NOT FOUND, ERROR
         MVC   IPLVOL(6),LOCWORK+6 MOVE VOLSER TO DYNALOC AREA
EXIT010  EQU   *
         B     ENTRY015
         SPACE 3
*----------------------------------------------------------------------
*
*        BUILD THE DYNAMIC ALLOCATION REQUEST BLOCK AND ALLOCATE
*        THE PASSWORD DATASET (IF IT IS PASSWORD PROTECTED, THE
*        USER WILL BE PROMPTED FOR THE PASSWORD).
*
*----------------------------------------------------------------------
ENTRY015 DS    0H
         L     R0,RBSIZE          GET STORAGE FOR BUILDING
         GETMAIN R,LV=(0)         *   THE DYNAMIC ALOC REQUEST BLOCK
         ST    R1,REQBLK          SAVE ADDRESS OF STORAGE
         BAL   R14,ENTRY910       ALLOC A THE PASSWORD DATA SET
         OPEN  PASSWORD
EXIT015  EQU   *
         B     ENTRY018
         EJECT
*----------------------------------------------------------------------
*
*        ASK USER FOR TYPE OF REQUEST (ALL, PW=, OR DSN=)
*
*----------------------------------------------------------------------
ENTRY018 DS    0H
         TPUT  MSG18A,80         ASK USER FOR REQUEST TYPE
         TGET  DSN,56            GET ANSWER
         LTR   R2,R1             CHECK LENGTH OF REPLY
         BNZ   AAA018            NOT ZERO, CHECK NEXT OPTION
         OI    SWITCH,OPTALL     SET 'ALL' OPTION
         B     ENTRY020          PROCESS 'ALL'
AAA018   OC    DSN,=CL56' '      INSURE UPPER CASE
         CLC   DSN(3),=C'PW='    CHECK VALIDITY
         BNE   BBB018            NO, CONTINUE
         SH    R2,=H'4'          DECREMENT LENGTH (+1)
         BM    ENTRY018          INVALID REPLY
         LA    R3,DSN+3          COMPARE OPERAND
         LA    R4,IN#PW          COMPARE OPERAND
         B     ENTRY020          PROCESS PASSWORD REQ
BBB018   CLC   DSN(4),=C'DSN='   CHECK VALIDITY
         BNE   ENTRY018          INVALID, RETRY
         SH    R2,=H'5'          DECREMENT LENGTH
         BM    ENTRY018          INVALID REPLY
         LA    R3,DSN+4          COMPARE OPERAND
         LA    R4,IN#DSN         COMPARE OPERAND
         B     ENTRY020          PROCESS PASSWORD REQ
MSG18A   DC    CL80'ENTER DSN=, PW=, (OR NULL)'
DSN      DC    CL56' '
         EJECT
*----------------------------------------------------------------------
*
*        READ PASSWORD DATASET TO PROCESS USER REQUEST
*
*----------------------------------------------------------------------
ENTRY020 DS    0H
         READ  RDECB,SF,PASSWORD,INREC
         CHECK RDECB
         LA    R1,INREC
         CLI   IN#DSN,X'FF'   UNUSED ENTRY ?
         BE    ENTRY020  YES - GET NEXT RECORD
         CLI   IN#DSN,X'00'   UNUSED ENTRY ?
         BE    ENTRY020  YES - GET NEXT RECORD
         TM    SWITCH,OPTALL  IS THIS 'ALL' REQUEST?
         BO    AAA020         YES, DO IT ALL
         EX    R2,COMP020     CHECK FOR REQUESTED ENTRY
         BNE   ENTRY020       NOT EQUAL, IGNORE IT
AAA020   MVC   OT#DSN,IN#DSN  MOVE DSNAME TO MESSAGE
         MVC   OT#PW,IN#PW    MOVE PASSWORD TO MESSAGE
         TPUT  OT#REC,80
EXIT020  EQU   *
         B     ENTRY020      GET NEXT RECORD
COMP020  CLC   0(0,R3),0(R4) EXECUTED COMPARE
         EJECT
*----------------------------------------------------------------------
*
*
*----------------------------------------------------------------------
EOJ      DS    0H
         CLOSE PASSWORD            THIS WILL DE-ALLOCATE (FREE=CLOSE)
EOJ1     L     R0,RBSIZE
         L     R1,REQBLK
         FREEMAIN R,LV=(0),A=(1)   FREE THE ALLOCATION WORK AREA
         L     R15,RC            SET RETURN CODE
         $EPILOG
         EJECT
*----------------------------------------------------------------------
*
*        ALLOC THE PASSWORD DATA SET ON THE IPL VOLUME
*
*----------------------------------------------------------------------
ENTRY910 DS    0H
         ST    R14,ST910          SAVE RETURN ADDRESS
         L     R11,REQBLK         ADDRESS OF REQUEST BLOCK
         USING S99RBP,R11         REQ BLK POINTER DSECT
         LA    R10,S99RBP+4       ADDRESSABILITY OF RB DSECT
         USING S99RB,R10          RB DSECT
         ST    R10,S99RBPTR       MAKE RBPTR POINT TO RB
         OI    S99RBPTR,S99RBPND  TURN ON HOB IN RBPTR
         XC    S99RB(RBLEN),S99RB  CLEAR RB
         MVI   S99RBLN,RBLEN      PUT LEN IN ITS LENGTH FIELD
         MVI   S99VERB,S99VRBAL   SET VERB CODE TO ALLOCATE
         LA    R9,S99RB+RBLEN     POINT BEYOND RB (1ST TEXT PTR)
         USING S99TUPL,R9         ADDRESSABILITY OF TEXT UNIT POINTERS
         ST    R9,S99TXTPP        INIT TEXT PTR IN RB
         LA    R8,S99TUPL+32 (16) POINT BEYOND LAST TXT PTR (1ST TXT
*                                  UNIT)
         USING S99TUNIT,R8        ADDRESSABILITY OF TEXT UNITS
* DDNAME
         ST    R8,S99TUPTR        1ST PTR TO 1ST UNIT
         LA    R1,DALDDNAM        KEY FOR DDNAME
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0008'  MOVE 8 TO TXT UNIT LNGTH FLD
         MVC   S99TUPAR(8),=C'PASSWORD' DDNAME
         LA    R8,14(R8)          BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
* DSNAME
         ST    R8,S99TUPTR        2ND PTR TO 2ND UNIT
         LA    R1,DALDSNAM        KEY FOR DSNAME
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0008'  MOVE 8 TO TXT UNIT LNGTH FLD
         MVC   S99TUPAR(8),=C'PASSWORD'         DSNAME
         LA    R8,14(R8)          BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
* DATA SET STATUS - SHR
         ST    R8,S99TUPTR        PTR TO UNIT
         LA    R1,DALSTATS        KEY FOR DSN STATUS
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0001'  MOVE 1 TO TXT UNIT LNGTH FLD
         MVI   S99TUPAR,X'08'     INDICATE SHR DATA SET
         LA    R8,7(R8)           BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
* VOL SER NUMBER
         ST    R8,S99TUPTR             PTR TO UNIT
         LA    R1,DALVLSER        KEY FOR VOL SER ALLOCATION
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0006'  MOVE 6 TO TXT UNIT LNGTH FLD
         MVC   S99TUPAR(6),IPLVOL   USE IPL VOL SER
         LA    R8,12(R8)          BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
* UNIT TYPE
         ST    R8,S99TUPTR             PTR TO UNIT
         OI    S99TUPTR,S99TUPLN  TURN ON HOB TO INIDICATE LAST PTR
         LA    R1,DALUNIT         KEY FOR UNIT ALLOCATION
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0008'  MOVE 8 TO TXT UNIT LNGTH FLD
         MVC   S99TUPAR(8),=C'SYSALLDA'   ANY UNIT IS OK
         LA    R8,14(R8)          BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
* FREE = CLOSE
         ST    R8,S99TUPTR             PTR TO UNIT
         OI    S99TUPTR,S99TUPLN  TURN ON HOB TO INIDICATE LAST PTR
         LA    R1,DALCLOSE        KEY FOR CLOSE PARM
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0000'  MOVE 0 TO TXT UNIT NUMBR FLD
         LA    R8,6(R8)           BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
********
         LR    R1,R11             ADDR OF REQUEST BLOCK
         DYNALLOC
         ST    R15,RC             SAVE RETURN CODE
         LTR   R15,R15            RETURN CODE ZERO ?
         BNZ   ERROR2             NO - DECODE ERROR
EXIT910  EQU   *
         L     R14,ST910
         BR    R14
ST910    DC    F'0'
         EJECT
*----------------------------------------------------------------------
*
*        ERROR ROUTINES
*
*----------------------------------------------------------------------
ERROR0   DS    0H
         TPUT  MSGER0,80
         B     EOJ
MSGER0   DC    CL80'SYS1.NUCLEUS NOT CATALOGED, ABORT'
*----------------------------------------------------------------------
ERROR1   DS    0H
         TPUT  MSGER1,80
         B     EOJ
MSGER1   DC    CL80'SECURITY VIOLATION, PROGRAM ABORTED'
*----------------------------------------------------------------------
ERROR2   DS    0H
         L     R1,RC              PICK UP RETURN CODE
         CVD   R1,DBWRD
         UNPK  MSG2+1(3),DBWRD+6(2)
         OI    MSG2+3,X'F0'
         UNPK  MSG2+7(5),S99ERROR(3)  1 MORE THAN FIELD SIZE
         MVI   MSG2+11,X'40'   BLANK OUT GARBAGE
         TR    MSG2+7(4),TRTBL-240    MAKE HEX PRINTABLE TOO
         UNPK  MSG2+19(5),S99INFO(3)
         MVI   MSG2+23,X'40'
         TR    MSG2+19(4),TRTBL-240   MAKE HEX PRINTABLE TOO
         TPUT  MSG2,40
         B     EOJ1
DBWRD    DC    D'0' 01234567890123456789012345
MSG2     DC    CL40' XXX - XXXXX ERROR XXXXX INFO'
TRTBL    DC    C'0123456789ABCDEF' HEX TRANSLATE TABLE
*----------------------------------------------------------------------
         EJECT
OT#REC   DS    0CL80
         DC    CL4'DSN='
OT#DSN   DC    CL44' '
         DC    CL7' '
         DC    CL9'PASSWORD='
OT#PW    DC    CL8' '
         DC    CL8' '
*-------
INREC    DS   0CL132
IN#DSN   DS   CL44
IN#PW    DS   CL8
         DS   CL80
*-------
         EJECT
*----------------------------------------------------------------------
*
*        STORAGE AREAS
*
*----------------------------------------------------------------------
         LTORG
REQBLK   DC    F'0'
RBSIZE   DC    F'512'
RC       DC    F'0'
SWITCH   DC    X'00'
OPTALL   EQU   X'01'
IPLVOL   DC    CL6' '
LOCATE   CAMLST NAME,LOCNAME,,LOCWORK
LOCNAME  DC   CL44'SYS1.NUCLEUS'
         DS   0D
LOCWORK  DC   256X'00'
*-------
         EJECT
PASSWORD DCB   DDNAME=PASSWORD,DSORG=PS,MACRF=R,EODAD=EOJ,             X
               KEYLEN=52,RECFM=F,BLKSIZE=80,LRECL=80
         EJECT
*----------------------------------------------------------------------
*
*        DSECTS
*
*----------------------------------------------------------------------
         IEFZB4D0
         EJECT
         IEFZB4D2
RBLEN    EQU   (S99RBEND-S99RB)
         END
