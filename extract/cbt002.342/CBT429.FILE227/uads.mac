         MACRO
&LABEL   $EPILOG &RC
&LABEL   LR    R1,R13              GET SAVEAREA ADDRESS
         L     R13,4(R13)          GET BACK CHAIN POINTER
         L     R0,16(R13)          GET SAVEAREA LENGTH
         ST    R15,16(R13)         SAVE REGISTER 15 (RETCODE)
         FREEMAIN R,LV=(0),A=(1)   FREE SAVEAREA
         LM    R14,R12,12(R13)     RESTORE CALLERS REGS
         AIF   (T'&RC EQ 'O').SPEC
         LA    R15,&RC             SET RETURN CODE
.SPEC    ANOP
         BR    R14                 RETURN TO CALLER
         MEND
         MACRO
&LABEL   $PROLOG &LV=0
.**********************************************************************
.*
.*       THIS MACRO WILL PROVIDE ENTRY LINKAGE AND OPTIONALLY
.*       MULTIPLE BASE REGISTERS.  ALSO, VIA THE 'LV=' KEYWORD
.*       PROVIDE ADDITIONAL USER STORAGE (APPENDED TO THE
.*       SAVE AREA) ADDRESSABLE FROM REG 13.  IF NO OPERANDS
.*       ARE CODED, REG 12 IS ASSUMED THE BASE. EXAMPLE:
.*              SECTNAME $PROLOG          = STANDARD REG 12 BASE
.*              SECTNAME $PROLOG 5        = STANDARD, REG 5 BASE
.*              SECTNAME $PROLOG 10,LV=20 = ADD 20 BYTES TO SAVE AREA
.*                                             REG 10 IS BASE
.*              SECTNAME $PROLOG R10,R11  = REGS 10 AND 11 ARE BASES
.*
.**********************************************************************
         LCLA  &AA,&AB,&AC
         GBLB  &PRORG
&AC      SETA  4096
&LABEL   CSECT
         B     32(R15)             BRANCH AROUND
         DC    AL1(26)
         DC    CL8'&LABEL'         CSECT NAME
         DC    C'-'
         DC    CL8'&SYSDATE'       COMPILE DATE
         DC    C'-'
         DC    CL8'&SYSTIME'       COMPILE TIME
         CNOP  0,4                 ALIGNMENT
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             LOAD BASE REG
         USING &LABEL,R12          INFORM ASSEMBLER
         AIF   (&LV GT 4023).MERR
         LA    R0,&LV+72           LOAD REG 0 WITH LENGTH VARIABLE
         GETMAIN R,LV=(0)          GET CORE FOR SAVEAREA AND USER
         AIF   (&LV+72 LE 256).XC2
         AIF   (&LV+72 LE 512).XC1
         MVI   0(R1),X'00'         MOVE X'00' TO FIRST BYTE
         LR    R2,R1               SAVE POINTER IN EVEN REG
         LA    R4,1(R1)            SET RECEIVING POINTER
         LR    R5,R0               SET RECEIVING LENGTH
         BCTR  R5,R0               DECREMENT LENGTH
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE
         LA    R3,1                SET SENDING LENGTH
         MVCL  R4,R2               INSTRUCTION PADS WITH X'00'
         AGO   .STORE
.XC1     ANOP
         XC    256(&LV-184,R1),256(R1)  CLEAR SAVE AREA
         XC    0(256,R1),0(R1)          CLEAR SAVE AREA
         AGO   .STORE
.XC2     ANOP
         XC    0(&LV+72,R1),0(R1)       CLEAR SAVE AREA
.STORE   ANOP
         ST    R13,4(R1)           SAVE BACK CHAIN
         ST    R1,8(R13)           SET FORWARD CHAIN
         LR    R11,R1              SAVE NEW SAVEAREA ADDRESS
         L     R15,16(R13)         RESTORE REG 15
         ST    R0,16(R13)          SAVE SAVEAREA LENGTH
         LM    R0,R1,20(R13)       RESTORE REGS USED IN GETMAIN
         LR    R13,R11             SET SAVEAREA POINTER
         AIF   (N'&SYSLIST EQ 0).MEND
         AIF   ('&SYSLIST(1)' EQ 'R12').SKIPIT
         AIF   ('&SYSLIST(1)' EQ '12').SKIPIT
         LA    &SYSLIST(1),&LABEL  LOAD REQUESTED BASE REG
         DROP  R12                 DROP ASSUMED BASE REG
         USING &LABEL,&SYSLIST(1)  INFORM ASSEMBLER
.SKIPIT  ANOP
&AA      SETA  2
.LOOP    ANOP
         AIF   (&AA GT N'&SYSLIST).MEXIT
&AB      SETA  &AA-1
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AB))  LOAD NEXT BASE REG
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AA))  LOAD NEXT BASE REG
         USING &LABEL+&AC,&SYSLIST(&AA) INFORM ASSEMBLER
&AC      SETA  &AC+4096
&AA      SETA  &AA+1
         AGO   .LOOP
.MEXIT   ANOP
         AIF   (&PRORG).MEX2
         SPACE
         $REGS
         SPACE
.MEX2    ANOP
&AA      SETA  &LV+72
         MNOTE *,'TOTAL STORAGE AREA RECEIVED = &AA'
         MEXIT
.MEND    ANOP
         MNOTE *,'NO REGISTER SPECIFIED - R12 ASSUMED'
         AGO   .MEXIT
.MERR    ANOP
         MNOTE 12,'LV > 4023 - REQUEST IGNORED'
         AGO   .MEXIT
         MEND
         MACRO
         $REGS
         GBLB  &PRORG
         AIF   (&PRORG).MEX2
&PRORG   SETB  1
 SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
 SPACE
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7
REG8     EQU   8
REG9     EQU   9
REG10    EQU   10
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
 SPACE
.MEX2    ANOP
       MEND
*---------------------------------------------------------------------
*
*        THIS TSO COMMAND PROCESSOR WILL ALLOW THOSE USERS WITH
*        OPERATOR ABILITY TO DYNAMICALLY CHANGE THEIR UADS ENTRIES
*        IN THE PSCB.  ONCE CHANGED, IT WILL REMAIN IN EFFECT
*        ACCROSS A RELOGON (UNLESS THE SYSTEM CRASHES).
*
*        THOSE ENTRIES THAT MAY BE CHANGED AND THEIR ASSOCIATED
*        KEYWORDS ARE LISTED BELOW:
*
*        ID(????)/RESET    CHANGE CURRENT USER-ID TO ???? OR RESET
*                          BACK TO REAL ID. THIS OPERAND IS ALLOWED
*                          ONLY FOR USERS WITH ACCOUNT AUTHORITY,
*                          SINCE ONE MUST INSURE THAT PRIOR TO
*                          LOGOFF THE NEW ID MUST BE RESET.  IF NOT
*                          RESET, THE NEW ID SET WILL BE UPDATED
*                          WITH THE UADS ENTRY FOR THE ACTIVE USER.
*
*        DEST(????)/NODEST CHANGE THE CURRENT DEFAULT DESTINATION
*                          TO ???? OR NULLIFY IT.
*
*        UNIT(????)/NOUNIT CHANGE THE CURRENT DEFAULT DYNAMIC
*                          ALLOCATION UNITNAME TO ???? OR NULLIFY IT.
*
*        JCL/NOJCL         ALLOW OR DISALLOW BATCH JOB SUBMISSIONS.
*
*        MOUNT/NOMOUNT     ALLOW OR DISALLOW FOREGROUND TAPE/DISK
*                          MOUNT CAPABILITIES.
*
*        PASS/NOPASS       ALLOW OR DISALLOW DATASET PASSWORD CHECKING
*
*---------------------------------------------------------------------
         EJECT
UADS     $PROLOG
*
*        REGISTER USAGE:
*
*        R0-R8 =====> WORK REGISTERS
*
*        R9    =====> BASE FOR THE PSCB
*        R10   =====> BASE FOR THE PARSE PDE'S
*        R11   =====> BASE FOR THE CPPL
*        R12   =====> BASE FOR UADS (THIS ROUTINE)
*
         EJECT
*---------------------------------------------------------------------*
*
*        RETRIEVE INITIALIZATION PARMS
*
*---------------------------------------------------------------------*
ENTRY010 DS    0H
         LR    R11,R1             SAVE CPPL POINTER
         USING CPPL,R11           PROVIDE ADDRESSABILITY
         MVC   PPLUPT,CPPLUPT     COPY UPT ADDRESS
         MVC   PPLECT,CPPLECT     COPY ECT ADDRESS
         MVC   PPLCBUF,CPPLCBUF   COPY ADDRESS OF COMMAND BUF
         L     R9,CPPLPSCB        ADDR OF PSCB
         USING PSCB,R9
EXIT010  EQU   *
         EJECT
*---------------------------------------------------------------------*
*
*        LINK TO PARSE TO RETRIEVE COMMAND DATA
*        PROCESS USER-ID OPERANDS
*
*---------------------------------------------------------------------*
ENTRY020 DS    0H
         LA    R1,PPLUPT          PROVIDE LIST FOR PARSE
         LINK  EP=IKJPARS         PARSE COMMAND
         LINK  EP=AON
         MODESET KEY=ZERO
         L     R10,PPLANSR        GET POINTER TO ANSWER AREA
         USING IKJPARMD,R10       PROVIDE ADDRESSABILITY
         LH    R15,USERID         GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT020            NOT SPECIFIED, SKIP IT
         B     BBB020             RESET SPECIFIED, GO TO IT
AAA020   L     R1,UID             ADDRESS OF NEW USERID
         LH    R3,UID+4           GET SIZE OF ID
         B     DDD020             TO COMMON ROUTINE
BBB020   EQU   *
         L     R3,16              ADDR OF CVT
         L     R3,0(R3)           ADDR OF OLD/NEW PTRS
         L     R3,12(R3)          ADDR OF CURRENT ASCB
         L     R1,176(R3)         ADDR OF REAL USER-ID
         LR    R3,R1
         MVC   PSCBUSER,BLANKS
         LA    R15,7              MAX SIZE OF USERID
LOOP020  EQU   *
         CLI   0(R3),C' '         END OF ID ?
         BE    CCC020             YES - OK
         CLI   0(R3),X'00'        END OF ID ?
         BE    CCC020             YES - OK
         LA    R3,1(R3)           BUMP
         BCT   R15,LOOP020        CHECK ALL BYTES
CCC020   SR    R3,R1              LENGTH OF USERID
DDD020   EQU   *
         TM    PSCBATR1,PSCBACCT  DOES USER HAVE ACCOUNT AUTHORITY?
         BZ    ERR020             NO, INFORM USER
         STC   R3,PSCBUSRL        STORE LENGTH
         BCTR  R3,R0              DECR FOR EX
         EX    R3,MVCID
EXIT020  EQU   *
         B     ENTRY030
ERR020   TPUT  MSG020,60
         B     ENTRY030
MSG020   DC    CL60'USER-ID CHANGE NOT AUTHORIZED'
MVCID    MVC   PSCBUSER(0),0(R1)
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS UNIT OPERANDS
*
*---------------------------------------------------------------------*
ENTRY030 DS    0H
         LH    R15,UNIT           GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT030            NOT SPECIFIED, SKIP IT
         B     AAA030             NOUNIT SPECIFIED, GO TO IT
         L     R1,UNITN           UNIT SPECIFIED, DO IT
         LH    R2,UNITN+4         GET SIZE OF NAME
         BCTR  R2,R0              DECR FOR EX
         EX    R2,MVCUNIT         MOVE UNIT
         B     EXIT030
AAA030   EQU   *
         MVC   PSCBGPNM,BLANKS
EXIT030  DS    0H
         B     ENTRY040
MVCUNIT  MVC   PSCBGPNM(0),0(R1)
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS DEST OPERANDS
*
*---------------------------------------------------------------------*
ENTRY040 DS    0H
         LH    R15,DEST           GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT040            NOT SPECIFIED, SKIP IT
         B     AAA040             NODEST SPECIFIED, GO TO IT
         MVC   PSCBDEST,BLANKS
         L     R1,DESTN           DEST SPECIFIED, DO IT
         LH    R2,DESTN+4         GET SIZE OF DEST NAME
         BCTR  R2,R0              DECR FOR EX
         EX    R2,MVCDEST         MOVE DEST
         B     EXIT040
AAA040   EQU   *
         MVC   PSCBDEST,BLANKS
EXIT040  DS    0H
         B     ENTRY050
MVCDEST  MVC   PSCBDEST(0),0(R1)
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS JCL OPERANDS
*
*---------------------------------------------------------------------*
ENTRY050 DS    0H
         LH    R15,JCL            GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT050            NOT SPECIFIED, SKIP IT        0
         B     AAA050             NOJCL SPECIFIED, GO TO IT      1
         OI    PSCBATR1,PSCBJCL   ALLOW JCL                      2
         B     EXIT050
AAA050   EQU   *
         NI    PSCBATR1,X'FF'-PSCBJCL  RESET JCL
EXIT050  DS    0H
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS MOUNT OPERANDS
*
*---------------------------------------------------------------------*
ENTRY060 DS    0H
         LH    R15,MOUNT          GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT060            NOT SPECIFIED, SKIP IT          0
         B     AAA060             NOMOUNT SPECIFIED, GO TO IT        1
         OI    PSCBATR1,PSCBVMNT  ALLOW MOUNT                     2
         B     EXIT060
AAA060   EQU   *
         NI    PSCBATR1,X'FF'-PSCBVMNT  RESET MOUNT
EXIT060  DS    0H
         EJECT
*---------------------------------------------------------------------*
*
*        PROCESS PASSWORD OPERANDS
*
*---------------------------------------------------------------------*
ENTRY070 DS    0H
         L     R1,16          GET CVT POINTER
         L     R1,0(R1)       OLD/NEW POINTER
         L     R1,4(R1)       OUR TCB POINTER
         L     R2,180(R1)     JSCB POINTER
         USING IEZJSCB,R2
         OI    JSCBSWT1,JSCBPASS  NO PASSWORD CHECKING
         LH    R15,PASS           GET NUMBER OF SPECIFIED KEYWORD
         SLL   R15,2              MULTIPLY BY FOUR
         B     *+4(R15)           TO PROCESSING ROUTINE
         B     EXIT070            NOT SPECIFIED, SKIP IT          0
         B     AAA070             NOPASS SPECIFIED, GO TO IT      1
         NI    JSCBSWT1,X'FF'-JSCBPASS  NO PASSWORD CHECKING      2
         B     EXIT070
AAA070   EQU   *
         OI    JSCBSWT1,JSCBPASS  ALLOW PASSWORD CHECKING
EXIT070  DS    0H
         DROP  R2
         EJECT
*---------------------------------------------------------------------*
RETURN   DS    0H
         MODESET KEY=ZERO
         LINK  EP=AOFF
         L     R15,RC             RESTORE RETURN CODE
         $EPILOG
         EJECT
*---------------------------------------------------------------------*
BLANKS   DC    81C' '             BLANK FOLLOWED BY 80 BYTE BUFFER
ECB      DC    F'0'               DUMB ECB FOR PARSE
PPLANSR  DC    F'0'               PARSE ANSWER AREA POINTER
RC       DC    F'0'               RETURN CODE SAVE AREA
*---------------------------------------------------------------------*
PPLUPT   DC    F'0'               USER PROFILE TABLE POINTER
PPLECT   DC    F'0'               ENVIRONMENT CONTROL TABLE
PPLECB   DC    A(ECB)             ECB POINTER
PPLPCL   DC    A(IKJPCL)          ADDRESS OF IKJPARM
PPLANS   DC    A(PPLANSR)         PLACE TO PUT ANSWER
PPLCBUF  DC    F'0'               POINTER TO COMMAND BUFFER
         EJECT
*---------------------------------------------------------------------*
         LTORG
         EJECT
IKJPCL   IKJPARM
USERID   IKJKEYWD
         IKJNAME 'RESET'
         IKJNAME 'ID',SUBFLD=IDSUBF
UNIT     IKJKEYWD
         IKJNAME 'NOUNIT'
         IKJNAME 'UNIT',SUBFLD=UNITSUBF
DEST     IKJKEYWD
         IKJNAME 'NODEST'
         IKJNAME 'DEST',SUBFLD=DESTSUBF
JCL      IKJKEYWD
         IKJNAME 'NOJCL'
         IKJNAME 'JCL'
MOUNT    IKJKEYWD
         IKJNAME 'NOMOUNT'
         IKJNAME 'MOUNT'
PASS     IKJKEYWD
         IKJNAME 'NOPASS'
         IKJNAME 'PASS'
IDSUBF   IKJSUBF
UID      IKJPOSIT  USERID,PROMPT='NEW USER-ID TO BE USED'
UNITSUBF IKJSUBF
UNITN    IKJIDENT  'UNITNAME',MAXLNTH=8,FIRST=ALPHANUM,OTHER=ALPHANUM, X
               PROMPT='UNIT NAME FOR DEFAULT DEVICE ALLOCATIONS'
DESTSUBF IKJSUBF
DESTN    IKJIDENT  'DEST',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,        X
               PROMPT='DESTID FOR DEFAULT SYSOUT/JOB ROUTING'
         IKJENDP
         PRINT OFF
         IKJCPPL
         IKJPSCB
         IEZJSCB
         END
