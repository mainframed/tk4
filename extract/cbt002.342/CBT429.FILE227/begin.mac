         MACRO
&NAME    BEGIN &DATE,&BASE1=,&BASE2=,&BASE3=,&NOEQU=,&EQU=
         LCLA  &REG,&S,&SW
         LCLC  &P
         AIF   (T'&NAME EQ 'O').ONE
&NAME    CSECT
         AGO   .TWO
.ONE     ANOP
         MNOTE 12,'NO CSECT NAME SPECIFIED - MACRO NOT EXPANDED'
         AGO   .EXIT
.TWO     AIF   (T'&DATE NE 'O').THREE
         MNOTE 12,'NO DATE SPECIFIED - MACRO NOT EXPANDED'
         AGO   .EXIT
.THREE   B     20(15)
         DC    CL8'&NAME'
         DC    CL8'&DATE'
         AIF   (T'&NOEQU EQ 'O').DOEQU
&P       SETC    'BEGINM'
         AIF    (T'&BASE1 NE 'O').DONOT1
&P.3     EQU   3
         AGO   .AROUND
.DONOT1   ANOP
         AIF   (&BASE1 EQ 15).N01
         AIF   (&BASE1 EQ 12).N01
&P&BASE1 EQU   &BASE1
.N01     ANOP
         AIF   (T'&BASE2 EQ 'O').AROUND
         AIF   (&BASE2 EQ 12).N02
         AIF   (&BASE2 EQ 15).N02
&P&BASE2 EQU   &BASE2
.N02     ANOP
         AIF   (T'&BASE3 EQ 'O').AROUND
         AIF   (&BASE3 EQ 12).N03
         AIF   (&BASE3 EQ 15).N03
&P&BASE3 EQU   &BASE3
.N03     ANOP
&P.12    EQU   12
&P.13    EQU   13
&P.14    EQU   14
&P.15    EQU   15
         AGO   .AROUND
.DOEQU    ANOP
         AIF   (T'&EQU EQ 'O').FOUR
&P       SETC  '&EQU'
         AGO   .FOURA
.FOUR    ANOP
&P       SETC  'R'
.FOURA   ANOP
&P.0     EQU   0
&P.1     EQU   1
&P.2     EQU   2
&P.3     EQU   3
&P.4     EQU   4
&P.5     EQU   5
&P.6     EQU   6
&P.7     EQU   7
&P.8     EQU   8
&P.9     EQU   9
&P.10    EQU   10
&P.11    EQU   11
&P.12    EQU   12
&P.13    EQU   13
&P.14    EQU   14
&P.15    EQU   15
.AROUND  ANOP
         AIF   (T'&BASE1 EQ 'O').GOODBAS
         AIF   (&BASE1 EQ 0).BADBASE  BASE1 CANNOT BE EQUAL TO 0
         AIF   (&BASE1 EQ 1).BADBASE  BASE1 CANNOT BE EQUAL TO 1
         AIF   (&BASE1 EQ 13).BADBASE BASE1 CANNOT BE EQUAL TO 13
         AIF   (&BASE1 EQ 14).BADBASE BASE1 CANNOT BE EQUAL TO 14
         AIF   (T'&BASE2 EQ 'O').GOODBAS  IF BASE2 NOT DEFINED DONT TST
         AIF   (&BASE2 EQ 0).BADBASE BASE 2 CONNOT BE EQUAL TO 0
         AIF   (&BASE2 EQ 1).BADBASE BASE 2 CONNOT BE EQUAL TO 1
         AIF   (&BASE2 EQ 13).BADBASE BASE2 CONNOT BE EQUAL TO 13
         AIF   (&BASE2 EQ 14).BADBASE BASE2 CONNOT BE EQUAL TO 14
         AIF   (T'&BASE3 EQ 'O').GOODBAS IF BASE3 NOT DEFINED DONT TST
         AIF   (&BASE3 EQ 0).BADBASE  BASE3 CONNOT BE EQUAL TO 0
         AIF   (&BASE3 EQ 1).BADBASE  BASE3 CONNOT BE EQUAL TO 1
         AIF   (&BASE3 EQ 13).BADBASE BASE3 CONNOT BE EQUAL TO 13
         AIF   (&BASE3 NE 14).GOODBAS BASE3 CONNOT BE EQUAL TO 14
.BADBASE MNOTE 16,'BASE REGISTER  SPECIFIED MUST NOT BE 0,1,13,14 MACRO-
               NOT EXPANDED'
         AGO   .EXIT
.GOODBAS ANOP
&SW      SETA  0
&S       SETA  15
.FIVE    AIF   (T'&BASE1 EQ 'O').SIX
&REG     SETA  &BASE1
         AGO   .TEST
.SIX     ANOP
&REG     SETA  3
         AGO   .TEST
.LOOP    AIF   (&SW EQ 1).LOOP1
&SW      SETA  1
&S       SETA  2
.LOOP1   ANOP
&S       SETA  &S+1
         AIF   (&S GT 12).EXIT
.TEST    AIF   (&REG EQ &S).LOOP
         AIF   (T'&BASE2 EQ 'O').SEVEN
         AIF   (&BASE2 EQ &S).LOOP
         AIF   (T'&BASE3 EQ 'O').SEVEN
         AIF   (&BASE3 EQ &S).LOOP
.SEVEN   STM   &P.14,&P.12,12(&P.13)
         BALR  &P&REG,0
         AIF   (T'&BASE3 EQ 'O').EIGHT
         USING *,&P&BASE1,&P&BASE2,&P&BASE3
         L     &P&BASE2,*+14
         L     &P&BASE3,*+14
         B     SAVEAREA+72
         DS    0F
         DC    A(*-14+4096,*-18+8192)
         AGO   .FINI
.EIGHT   AIF   (T'&BASE2 EQ 'O').NINE
         USING *,&P&BASE1,&P&BASE2
         L     &P&BASE2,*+10
         B     SAVEAREA+72
         DS    0F
         DC    A(*-10+4096)
         AGO   .FINI
.NINE    ANOP
         USING *,&P&REG
         B     SAVEAREA+72
.FINI    ANOP
SAVEAREA DC    18F'0'
         ST    &P.13,SAVEAREA+4
         LR    &P&S,&P.13
         LA    &P.13,SAVEAREA
         ST    &P.13,8(&P&S)
.EXIT    ANOP
         MEND
