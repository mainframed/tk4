FORM     TITLE 'ARCHFORM --- ARCHIVER FUNCTION SUPPORT SUBROUTINE'
*---------------------------------------------------------------------*
*---     FUNCTION: FORMAT ALL MESSAGES INTO 'LINE' FOR THE         ---*
*---           PRINTING PROCESS.  ANY MESSAGE WHICH CONTAINS       ---*
*---           ITEM DESCRIPTORS ARE FORMATTED BY THIS ROUTINE.     ---*
*---                                                               ---*
*---     THIS ROUTINE IS INVOKED EXCLUSIVELY VIA #FORMAT MACRO.    ---*
*---                                                               ---*
*---------------------------------------------------------------------*
         SPACE 3
ARCHFORM CSECT
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         USING ARCHFORM,R15
         #STAMP
         STM   R14,R12,12(R13)     STORE ENTRY REGISTERS
         LR    R11,R15             LOAD ROUTINE BASE REGISTER
         DROP  R15                 DROP THE OLD BASE
         USING ARCHFORM,R11        DEFINE THE NEW BASE REGISTER
         LA    R14,72(,R13)        POINT DOWN THE SAVE AREA STACK
         ST    R14,8(,R13)         STORE FORWARD POINTER
         ST    R13,4(,R14)         AND BACK POINTER
         LR    R13,R14             LOAD NEW SAVE AREA POINTER
         L     R14,4(,R13)         RELOAD BACK POINTER
         L     R14,12(,R14)        RELOAD ORIG. REG 14
         USING MAINWK,R12          BASE THE MAIN WORK AREA
         LA    R15,0               CLEAR A WORK REGISTER
         ICM   R15,3,8(R14)        INSERT THE S-CON
         SRL   R15,12              SHIFT OFF THE OFFSET VALUE
         O     R15,LR-2            GET THE REST ---
         EX    R15,LR              COPY THE S-CON'S REGISTER
         LA    R15,0               RE-CLEAR THE REGISTER
         ICM   R15,3,8(R14)        RELOAD THE S-CON
         N     R15,=A(X'FFF')      TRIM TO SIMPLE OFFSET
         AR    R2,R15              POINT TO ACTUAL OUTPUT AREA
         CLC   0(8,R14),=CL8' '    IS THERE A MESSAGE NUMBER ??
         BE    FMT1                NOPE ---
         MVC   0(8,R2),0(R14)      MOVE MESSAGE NUMBER TO LINE
         LA    R2,9(,R2)           INCREMENT TRAVELING CURSOR PAST IT
FMT1     DS    0H
         LR    R3,R0               COPY DESCRIPTOR ADDRESS
         USING REC,R3              AND BASE ITS DSECT
         MVI   0(R2),C'-'
         LA    R2,1(,R2)
         MVC   0(10,R2),RECN       MOVE THE NAME OVER
         MVI   10(R2),C'-'         AND FIELD SEPARATOR
         LA    R2,11(,R2)          INCREMENT TRAVELING POINTER
         MVC   0(10,R2),RECG       MOVE THE GROUP OVER
         MVI   10(R2),C'-'         AND FIELD SEPARATOR
         LA    R2,11(,R2)          INCREMENT TRAVELING POINTER
         MVC   0(10,R2),RECSG      MOVE THE SUBGROUP OVER
         MVI   10(R2),C'-'         AND FIELD SEPARATOR
         LA    R2,11(,R2)          INCREMENT TRAVELING POINTER
         MVC   0(10,R2),RECT       MOVE THE TYPE OVER
         MVI   10(R2),C'-'         AND FIELD SEPARATOR
         LA    R2,11(,R2)          INCREMENT TRAVELING POINTER
         ICM   R0,15,RECVER        INSERT THE VERSION
         X     R0,=F'-1'           INVERT ALL BITS
         CVD   R0,DWORK            AND DECIMALIZE IT
         OI    DWORK+7,X'0F'       FORST LAST SIGN NUMERIC
         MVC   0(12,R2),FMT4       MOVE IN THE EDIT MASK
         ED    0(12,R2),DWORK+3    EDIT IN THE VERSION NUMBER
         LA    R2,13(,R2)          POINT PAST IT
         LTR   R1,R1               IS THERE A MESSAGE ??
         BZ    FMT2                NOPE ---
         LA    R7,0                CLEAR THE REGISTER
         IC    R7,0(,R1)           INSERT ITS LENGTH
         BCTR  R7,R0               DECREMENT BY ONE
         EX    R7,FMT3             NOVE TEXT INTO LINE
         LA    R2,2(R7,R2)         BUMP CURSOR PAST IT
FMT2     DS    0H
         LR    R0,R2               SET RETURN VALUE
         L     R13,4(,R13)
         L     R14,12(,R13)
         LM    R2,R12,28(R13)
         LA    R15,0
         MVI   12(R13),255
         B     10(,R14)
FMT3     MVC   0(0,R2),1(R1)       ACTUAL TEXT MOVE
         DROP  R3
         LTORG
FMT4     DC    C' ',X'202020',C',',X'202020',C',',X'202020'
         CNOP  2,4
LR       LR    R2,R0               'EX'ED WHILE CONVERTING THE S-CON
         LTORG
         COPY #RECS
         COPY ##ARCHWK
         END
