DEL1     TITLE 'ARCHIVER --- ALIAS DELETE FUNCTION  '
*---------------------------------------------------------------------*
*---     THIS SUBROUTINE OF THE DELETE FUNCTION DELETES ALL THE    ---*
*---          ALIASES FOR A PARTICULAR ITEM IN THE VSAM1 ARCHIVE   ---*
*---          CLUSTER.  IT IS THEREFORE FUNCTIONALLY EQUIVALENT    ---*
*---          TO A DELALIAS FUNCTION.                              ---*
*---------------------------------------------------------------------*
ARCHDEL1 CSECT
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
@RPL1    EQU   R2                  SOME
@RPL2    EQU   R3                    SPECIAL
@RPL3    EQU   R4                      EQUATES
@BUF1    EQU   R5                        FOR
@BUF2    EQU   R6                          VSAM
@BUF3    EQU   R7                            I/O MGMT
         COPY #ARCBITS
         USING ARCHDEL1,R15
         #STAMP
         STM   R14,R12,12(R13)     STORE ENTRY REGISTERS
         LR    R11,R15             ALIS ROUTINE BASE REGISTER
         DROP  R15                 DROP THE OLD BASE
         USING ARCHDEL1,R11        DEFINE THE NEW BASE REGISTER
         LA    R14,72(,R13)        POINT DOWN THE SAVE AREA STACK
         ST    R14,8(,R13)         STORE FORWARD POINTER
         ST    R13,4(,R14)         AND BACK POINTER
         LR    R13,R14             ALIS NEW SAVE AREA POINTER
         L     R14,4(,R13)         RECOPY BACK POINTER
         L     R14,12(,R14)        RECOPY ORIG. REG 14
         LA    R10,0(,R11)         COPY ORIGINAL BASE REGISTER
         USING MAINWK,R12          BASE THE MAIN WORK AREA
         MVC   VS1MODE,=CL8'OUTPUT' SET THE I/O MODE
         LA    R1,VSAM1DD          LOAD THE PARM POINTER
         LA    R0,0                AND THE 'OPEN' FLAG
         CALL  ARCHVSOP            CALL THE 'OPEN' FUNCTION
         LTR   R15,R15             OPEN OK ??
         BNZ   ENDITALL            NOPE; CAN'T WORK
*---------------------------------------------------------------------*
*---     DELETE ALL THE ALIASES FOR A SPECIFIC ITEM, OR GROUP OF   ---*
*---     ITEMS.  WE ARE BOUND TO A MECHANISM WHICH MAKES UP IN     ---*
*---     BRUTE FORCE WHAT IT LACKS IN ELEGANCE.  SEARCH THROUGH    ---*
*---     ALL THE HEADER RECORDS FOR ANY ALIASES THAT REFER TO THE  ---*
*---     SELECTED ITEM(S) AND DELETE THEM ALL.                     ---*
*---------------------------------------------------------------------*
         SPACE 3
         L     @RPL3,RPL3          LOAD RPL POINTER REG.
         L     @BUF3,VS1BUF3       POINT TO THE VSAM BUFFER
         USING REC,@BUF3           BASE THE RECORD DSECT
         XC    0(RECKLEN,@BUF3),0(@BUF3) START AT THE BEGINNING !!
         MVI   RECTT,#RTHDR        INITIAL RECORD TYPE
         MODCB RPL=(@RPL3),                                            X
               OPTCD=(SEQ,FKS,KGE,SYN,UPD),                            X
               ACB=(*,ACB1),                                           X
               ARG=(@BUF3),                                            X
               AREA=(@BUF3),                                           X
               AREALEN=(*,VS1LIM),                                     X
               MF=(G,GENWK)        POSITION TO CLUSTER START
         POINT RPL=(@RPL3)         GET TO IT
DA2      DS    0H
         GET   RPL=(@RPL3)         AND READ A HEADER RECORD
         LTR   R15,R15             ANYTHING LEFT ??
         BNZ   ENDITALL            NOPE; ALL DONE
         CLI   RECTT,#RTHDR        HEADER RECORD ??
         BNE   ENDITALL            NOPE; ALL DONE ---
         #IF   RSRALIAS,OFF,DA3    NOT AN ALIAS RECORD
         LA    R1,RECAN            POINT TO THE 'REAL' NAME
         LA    R0,0                USE DEFAULT QUALIFIER DATA
         CALL  ARCHKNAM            DELETE IT ??
         LTR   R15,R15             PASS THE CHECK ??
         BNZ   DA2                 NOPE ---
         ERASE RPL=(@RPL3)         ERASE THE RECORD
         LTR   R15,R15             ERASE OK ??
         BNZ   1(,R15)             NOPE; DIE HORRIBLY
         L     R15,MCNT            LOAD THE ITEM COUNTER
         LA    R15,1(,R15)         INCREMENT BY ONE
         ST    R15,MCNT            AND SAVE UPDATED COUNT
         #FORMAT RECTT,,DELD       OUTPUT THE 'ALIAS DELETED' MESSAGE
         #PUT  LINE
         B     DA2                 YES; CONTINUE
DA3      DS    0H
         #IF   RSRHALS,OFF,DA2     ANY ALIASES KNOWN ??
         LA    R1,RECN             POINT TO THE KEY
         LA    R0,0                USE DEFAULT QUALIFIER DATA
         CALL  ARCHKNAM            IS THIS ONE WE SELECTED ??
         LTR   R15,R15             CHECK R.C.
         BNZ   DA2                 NOPE ---
         #SET  RSRHALS,OFF         NO ALIASES NOW !!
         PUT   RPL=(@RPL3)         UPDATE THE RECORD
         B     DA2                 AND CONTINUE ---
         DROP  @BUF3
         TITLE 'TERMINATION LINKAGE, ETC. '
*---------------------------------------------------------------------*
*---     WRAP UP, CLOSE THE VSAM FILES, FREE THE STORAGE AND       ---*
*---           RETURN TO ARCHMAIN FOR ANOTHER FUNCTION.            ---*
*---------------------------------------------------------------------*
ENDITALL DS    0H
         LA    R1,VSAM1DD          POINT TO THE DDNAME
         LA    R0,4                SET 'CLOSE' REQUEST
         CALL  ARCHVSOP            CALL THE VSAM 'CLOSE' ROUTINE
         #PUT  ERR12
         L     R0,MCNT             COPY MEMBER COUNT
         CVD   R0,DWORK            DECIMALIZE IT
         MVC   LINE,MCMSG          COPY IN THE MESSAGE SKELETON
         ED    LINE+2(20),DWORK    ADD IN THE ACTUALS
         #PUT  LINE                AND OUTPUT THE RESULTS
         L     R13,4(,R13)         COPY BACK S.A. POINTER
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         LA    R15,0               ALWAYS RC = 0
         MVI   12(R13),255
         BR    R14                 AND RETURN TO CALLER
         LTORG
* ERROR MESSAGES, ETC.
ERR12    DC    CL133'0         ALIAS DELETIONS COMPLETED.  TOTALS FOLLOX
               W: '
MCMSG    DC    CL133' '
         ORG   MCMSG+2
         DC    X'40202020'
         DC    X'6B202020'
         DC    X'6B202020'
         DC    X'6B202020'
         DC    X'6B202120'
         DC    C' ALIAS(ES) DELETED. '
         ORG
DELD     DC    AL1(L'DELD1)
DELD1    DC    CL30' ALIAS IS DELETED.'
         LTORG
         COPY  #RECS
         COPY  ##ARCHWK
         END
