         MACRO
         IECSDSL1 &PARA
.**********************************************************************
.*
.*MODULE-TYPE = MACRO
.*
.*PROCESSOR = ASSEMBLER OR PLS
.*
.*CHANGE ACTIVITY =
.*$L1=IXVTOC,JDM1134,T790227,STLBS: INDEXED VTOC SUPPORT           @L1A
.*$L2=ICFCAT,JDM1113,T790225,STLMD: ICF CATALOG SUPPORT            @L2A
.*$01=OZ52996,JDM1113,81110,STLRRB: DATE LAST REFERENCED           @01A
.*$L3=MVS/XA,JDP1111,,STLBS:        BIT DESCRIPTIONS               @L3A
.*$P1=KKC0088,HDP2210,,STLAM:       REMOVE SEMICOLON FROM ASM REM  @P1A
.*$02=OZ97209,HDP2210,T86113,STLAM: ADD FROMAT 4 INDEX DISABLE BIT @02A
.*
.**** END OF SPECIFICATIONS *******************************************
         LCLA  &CTR
&CTR     SETA  1
.LOOP    AIF   (&CTR GT N'&PARA).ENDOFIT
         AIF   ('&PARA(&CTR)' EQ '1').FIRSTON
         AIF   ('&PARA(&CTR)' EQ '2').SECNDON
         AIF   ('&PARA(&CTR)' EQ '3').THIRDON
         AIF   ('&PARA(&CTR)' EQ '4').FORTHON
         AIF   ('&PARA(&CTR)' EQ '5').FIFTHON
         AIF   ('&PARA(&CTR)' EQ '6').SIXTHON
         AGO   .ERROR
.FIRSTON ANOP
IECSDSL1 EQU   *                   FORMAT 1 DSCB
IECSDSF1 EQU   IECSDSL1            SAME AS IECSDSL1                @P1C
DS1DSNAM DS    CL44                DATA SET NAME
DS1FMTID DS    CL1                 FORMAT IDENTIFIER (X'F1')       @L3C
DS1DSSN  DS    CL6                 DATA SET SERIAL NUMBER
*                                     (IDENTIFIES THE FIRST OR     @P1A
*                                      ONLY VOLUME CONTAINING THE  @P1A
*                                      DATA SET/SPACE)             @P1A
DS1VOLSQ DS    XL2                 VOLUME SEQUENCE NUMBER (1-9999) @P1C
DS1CREDT DS    XL3                 CREATION DATE
*                                    (DISCONTINUOUS BIN, X'YYDDDD')@P1A
DS1EXPDT DS    XL3                 EXPIRATION DATE
*                                    (DISCONTINUOUS BIN, X'YYDDDD')@P1A
DS1NOEPV DS    XL1                 NUMBER OF DATA EXTENTS ON VOLUME@P1C
DS1NOBDB DS    XL1                 NUMBER OF BYTES USED IN LAST
*                                     DIRECTORY BLOCK
         DS    XL1                 RESERVED
DS1SYSCD DS    CL13                SYSTEM CODE
DS1REFD  DS    XL3                 DATE LAST REFERENCED            @01C
*                                    (DISCONTINUOUS BIN, X'YYDDDD')@P1A
*                                    OR ZERO IF NOT MAINTAINED     @P1A
         DS    XL4                 RESERVED                    @G60ASBJ
DS1DSORG DS    XL2                 DATA SET ORGANIZATION
*
*                    FIRST BYTE OF DS1DSORG
DS1DSGIS EQU   X'80'  1... ....    IS - INDEXED SEQUENTIAL         @L1A
*                                  ORGANIZATION
DS1DSGPS EQU   X'40'  .1.. ....    PS - PHYSICAL SEQUENTIAL        @L1A
*                                  ORGANIZATION
DS1DSGDA EQU   X'20'  ..1. ....    DA - DIRECT ORGANIZATION        @L1A
DS1DSGCX EQU   X'10'  ...1 ....    CX - BTAM OR QTAM LINE GROUP    @L1A
*        EQU   X'08'  .... 1...    CQ - QTAM DIRECT ACCESS MSG QUE @P1C
*        EQU   X'04'  .... .1..    MQ - QTAM PROBLEM PROG MSG QUEUE@P1C
DS1DSGPO EQU   X'02'  .... ..1.    PO - PARTITIONED ORGANIZATION   @L1A
DS1DSGU  EQU   X'01'  .... ...1    U - UNMOVABLE, THE DATA SET     @P1C
*                                      CONTAINS LOCATION DEPENDENT
*                                      INFORMATION
*
*                     SECOND BYTE OF DS1DSORG
DS1DSGGS EQU   X'80'  1... ....    GS - GRAPHICS ORGANIZATION      @L1A
DS1DSGTX EQU   X'40'  .1.. ....    TX - TCAM LINE GROUP            @L1A
DS1DSGTQ EQU   X'20'  ..1. ....    TQ - TCAM MESSAGE QUEUE         @L1A
*        EQU   X'10'  ...X ....    RESERVED                        @P1C
DS1ACBM  EQU   X'08'  .... 1...    AM - ACCESS METHOD CONTROL BLOCK@P1C
DS1ORGAM EQU   X'08'  .... 1...    AM - VSAM DATA SET/SPACE        @P1M
DS1DSGTR EQU   X'04'  .... .1..    TR - TCAM 3705                  @L1A
*        EQU   X'02'  .... ..X.    RESERVED                        @P1C
*        EQU   X'01'  .... ...X    RESERVED                        @P1C
*                                                                  @P1C
DS1RECFM DS    XL1                 RECORD FORMAT
DS1RECFF EQU   X'80'  10.. ....    F - FIXED LENGTH                @L3A
DS1RECFV EQU   X'40'  01.. ....    V - VARIABLE LENGTH             @L3A
DS1RECFU EQU   X'C0'  11.. ....    U - UNDEFINED LENGTH            @L3A
DS1RECFT EQU   X'20'  ..1. ....    T - TRACK OVERFLOW              @L3A
DS1RECFB EQU   X'10'  ...1 ....    B - BLOCKED: MAY NOT OCCUR WITH @L3A
*                                      UNDEFINED (U).              @L3A
DS1RECFS EQU   X'08'  .... 1...    FIXED LENGTH: (X'88')           @L3A
*                                     STANDARD BLOCKS, NO TRUNCATED@L3A
*                                     BLOCKS OR UNFILLED TRACKS.   @L3A
*                                  VARIABLE LENGTH: (X'48')        @L3A
*                                     SPANNED RECORDS.             @L3A
DS1RECFA EQU   X'04'  .... .10.    ANSI CONTROL CHARACTER          @L3A
DS1RECMC EQU   X'02'  .... .01.    MACHINE CONTROL CHARACTER       @L3A
*                     .... .00.    NO CONTROL CHARACTER            @L3A
*                     .... ...0    ALWAYS ZERO                     @L3A
DS1OPTCD DS    XL1                 OPTION CODES -- INDICATES THE   @P1C
*                                  VARIOUS OPTIONS USED IN         @P1C
*                                  BUILDING THE DATA SET           @P1C
*                                                                  @P1A
* BDAM DS1OPTCD FIELD ASSIGNMENTS (APPLY ONLY WHEN DS1DSGDA IS ON) @P1A
*              X'80'  1... ....    W - WRITE VALIDITY CHECK        @P1A
*              X'40'  .1.. ....      - TRACK OVERFLOW              @P1A
*              X'20'  ..1. ....    E - EXTENDED SEARCH             @P1A
*              X'10'  ...1 ....    F - FEEDBACK                    @P1A
*              X'08'  .... 1...    A - ACTUAL ADDRESSING           @P1A
*              X'04'  .... .1..      - DYNAMIC BUFFERING           @P1A
*              X'02'  .... ..1.      - READ EXCLUSIVE              @P1A
*              X'01'  .... ...1    R - RELATIVE BLOCK ADDRESSING   @P1A
*                                                                  @P1A
* ISAM DS1OPTCD FIELD ASSIGNMENTS (APPLY ONLY WHEN DS1DSGIS IS ON) @P1A
*              X'80'  1... ....    W - WRITE VALIDITY CHECK        @P1A
*              X'40'  .1.. ....    U - ACCUMULATE TRACK INDEX ENT  @P1A
*              X'20'  ..1. ....    M - MASTER INDECIES             @P1A
*              X'10'  ...1 ....    I - INDEPENDENT OVERFLOW AREA   @P1A
*              X'08'  .... 1...    Y - CYLINDER OVERFLOW AREA      @P1A
*              X'04'  .... .X..    RESERVED                        @P1A
*              X'02'  .... ..1.    L - DELETE OPTION               @P1A
*              X'01'  .... ...1    R - REORGANIZATION CRITERIA     @P1A
*                                                                  @P1A
* BPAM/BSAM/QSAM DS1OPTCD FIELD ASSIGNMENTS                        @P1A
*                   (APPLY ONLY WHEN DS1DSGPO OR DS1DSGPS IS ON)   @P1A
*              X'80'  1... ....    W - WRITE VALIDITY CHECK        @P1A
*              X'40'  .1.. ....    U - ALLOW DATA CHECK (1403 PRTR)@P1A
*              X'20'  ..1. ....    C - CHAINED SCHEDULING USING PCI@P1A
*              X'10'  ...1 ....    H - DOS/OS INTERCHANGE FEATURE  @P1A
*              X'08'  .... 1...    B - TREAT EOF AS EOV (TAPE)     @P1A
*              X'04'  .... .1..    Z - SEARCH DIRECT               @P1A
*              X'02'  .... ..1.    T - USER LABEL TOTALING         @P1A
*              X'01'  .... ...1    J - FOR 3800 PRINTING SUBSYSTEM @P1A
*                                                                  @P1A
* VSAM DS1OPTCD FIELD ASSIGNMENTS (APPLY ONLY WHEN DS1DSGAM IS ON) @P1C
DS1OPTIC EQU   X'80'  1... ....    DATA SET CATALOGED IN ICF CTLG  @P1C
DS1OPTBC EQU   X'40'  .1.. ....    DATA SET IS ICF CATALOG         @P1C
*                     ..XX XXXX    RESERVED                        @P1C
*
DS1BLKL  DS    XL2                 BLOCK LENGTH (TYPE F RECORDS),  @L3C
*                                  OR MAX BLKSIZE (U OR V RECORDS) @L3A
DS1LRECL DS    XL2                 LOGICAL RECORD LENGTH
*                                  FIXED LENGTH: RECORD LENGTH     @L3A
*                                  UNDEFINED LENGTH: ZERO          @L3A
*                                  VARIABLE UNSPANNED: MAX REC LEN @L3A
*                                  VARIABLE SPANNED:               @L3A
*                                   < 32756 BYTES- MAX REC LEN     @L3A
*                                   > 32756 BYTES- X'8000'         @L3A
DS1KEYL  DS    XL1                 KEY LENGTH (0 - 255)
DS1RKP   DS    XL2                 RELATIVE KEY POSITION
DS1DSIND DS    XL1                 DATA SET INDICATORS
DS1IND80 EQU   X'80'  1... ....    LAST VOLUME ON WHICH THE DATA   @P1C
*                                     SET RESIDES              @G60ASBJ
DS1IND40 EQU   X'40'  .1.. ....    DATA SET IS RACF DEFINED    @G60ASBJ
DS1IND20 EQU   X'20'  ..1. ....    BLOCK LENGTH IS A MULTIPLE  @G60ASBJ
*                                     OF 8 BYTES               @G60ASBJ
DS1IND10 EQU   X'10'  ...1 ....    PASSWORD IS REQUIRED TO     @G60ASBJ
*                                  READ OR WRITE OR BOTH-SEE   @G60ASBJ
*                                     DS1IND04                 @G60ASBJ
DS1IND08 EQU   X'08'  .... X...    RESERVED                        @P1C
DS1IND04 EQU   X'04'  .... .1..    IF DS1IND10 IS 1 THEN           @P1C
*                                     IF DS1IND04 IS . . .         @P1C
*                                        1-PASSWORD REQUIRED TO    @P1C
*                                          WRITE BUT NOT TO READ   @P1C
*                                        0-PASSWORD REQUIRED TO    @P1C
*                                          WRITE AND TO READ       @P1C
DS1IND02 EQU   X'02'  .... ..1.    DATASET OPENED FOR OTHER    @G60ASBJ
*                                     THAN INPUT SINCE LAST    @G60ASBJ
*                                     BACKUP COPY MADE.        @G60ASBJ
DS1DSCHA EQU   DS1IND02            SAME USE AS BIT DS1IND02    @G60ASBJ
DS1IND01 EQU   X'01'  .... ...1    SECURE CHECKPOINT DATA SET      @01C
DS1CHKPT EQU   DS1IND01            SAME AS DS1IND01                @L3C
DS1SCALO DS    0XL4                SECONDARY ALLOCATION            @L3C
DS1SCAL1 DS    XL1                 SPACE PARAMETERS                @P1C
DS1DSPAC EQU   X'C0'  XX.. ....    BIT PATTERN FOR SPACE REQUEST   @L3A
DS1CYL   EQU   X'C0'  11.. ....    CYLINDER REQUEST                @L3A
DS1TRK   EQU   X'80'  10.. ....    TRACK REQUEST                   @L3A
DS1AVR   EQU   X'40'  01.. ....    AVERAGE BLOCK LENGTH REQUEST    @L3A
DS1AVRND EQU   X'41'  01.. ...1    AVERAGE BLOCK AND ROUND REQUEST @P1C
DS1MSGP  EQU   X'20'  ..1. ....    MASS STORAGE VOL GROUP (MSVGP)  @L3A
*                     ...X ....    RESERVED                        @L3A
DS1CONTG EQU   X'08'  .... 1...    CONTIGUOUS REQUEST              @L3A
DS1MXIG  EQU   X'04'  .... .1..    MXIG REQUEST                    @L3A
DS1ALX   EQU   X'02'  .... ..1.    ALX REQUEST                     @L3A
*              X'01'  .... ...1    ROUND REQUEST                   @P1A
DS1DSABS EQU   X'00'  0000 0000    ABSOLUTE TRACK REQUEST          @L3A
DS1SCAL3 DS    XL3                 SECONDARY ALLOCATION QUANTITY   @P1C
DS1LSTAR DS    XL3                 LAST USED TRACK                 @P1C
*                                     AND BLOCK ON TRACK (TTR)     @P1A
DS1TRBAL DS    XL2                 BYTES REMAINING ON LAST TRACK USED
         DS    XL2                 RESERVED
DS1EXT1  DS    XL10                FIRST EXTENT DESCRIPTION
*        FIRST BYTE                EXTENT TYPE INDICATOR
*              X'81'  1... ...1    EXTENT ON CYLINDER BOUNDARIES   @L3A
*              X'80'  1... ....    EXTENT DESCRIBED IS SHARING CYL.@L3A
*              X'40'  01.. ....    FIRST EXTENT DESCRIBES THE      @L3A
*                                     USER LABEL (AND IS NOT       @P1C
*                                     COUNTED IN DS1NOEPV)         @P1A
*              X'04'  .... .1..    INDEX AREA EXTENT (ISAM)        @L3A
*              X'02'  .... ..1.    OVERFLOW AREA EXTENT (ISAM)     @L3A
*              X'01'  .... ...1    USER'S DATA BLOCK EXTENT OR     @L3A
*                                     IS A PRIME AREA EXTENT (ISAM)@P1C
*              X'00'  0000 0000    FOLLOWING 9 BYTES DO NOT        @L3A
*                                     INDICATE ANY EXTENT          @P1C
*        SECOND BYTE               EXTENT SEQUENCE NUMBER
*        THIRD - SIXTH BYTES       LOWER LIMIT (CCHH)
*        SEVENTH - TENTH BYTES     UPPER LIMIT (CCHH)
DS1EXT2  DS    XL10                SECOND EXTENT DESCRIPTION
DS1EXT3  DS    XL10                THIRD EXTENT DESCRIPTION
DS1PTRDS DS    XL5                 POINTER (CCHHR) TO A FORMAT 2   @P1C
*                                     OR 3 DSCB, OR ZERO           @P1A
DS1END   EQU   *                   EQUATE AT END OF DSCB1 MAP      @P1C
&CTR     SETA  &CTR+1
         AGO   .LOOP
.SECNDON ANOP
IECSDSL2 EQU   *                   FORMAT 2 DSCB
IECSDSF2 EQU   IECSDSL2            SAME AS IECSDSL2                @P1C
         DS    XL1                 KEY IDENTIFIER  (X'02')
DS22MIND DS    XL7                 ADDRESS OF 2ND LEVEL MASTER INDEX
DS2L2MEN DS    XL5                 LAST 2ND LEVEL MASTER INDEX ENTRY
DS23MIND DS    XL7                 ADDRESS OF 3RD LEVEL MASTER INDEX
DS2L3MIN DS    XL5                 LAST 3RD LEVEL MASTER INDEX ENTRY
         DS    XL11                RESERVED
DS2LPDT  DS    XL8                 LAST PRIME TRACK ON LAST PRIME CYL
DS2FMTID DS    CL1                 FORMAT IDENTIFIER (X'F2')
DS2NOLEV DS    XL1                 NUMBER OF INDEX LEVELS
DS2DVIND DS    XL1                 HIGH LEVEL INDEX DEVELOPMENT
*                                     INDICATOR
DS21RCYL DS    XL3                 FIRST DATA RECORD IN CYLINDER
DS2LTCYL DS    XL2                 LAST DATA TRACK IN CYLINDER
DS2CYLOV DS    XL1                 NUMBER OF TRACKS FOR CYLINDER
*                                     OVERFLOW
DS2HIRIN DS    XL1                 HIGHEST 'R' ON HIGH-LEVEL INDEX TRK
DS2HIRPR DS    XL1                 HIGHEST 'R' ON PRIME DATA TRACK
DS2HIROV DS    XL1                 HIGHEST 'R' ON OVERFLOW DATA TRACK
DS2RSHTR DS    XL1                 'R' OF LAST DATA RECORD ON SHARED
*                                     TRACK
DS2HIRTI DS    XL1                 HIGHEST 'R' ON UNSHARED TRACK OF
*                                     TRACK INDEX
DS2HIIOV DS    XL1                 HIGHEST 'R' FOR INDEPENDENT OVERFLOW
*                                     DATA TRACKS
DS2TAGDT DS    XL2                 TAG DELETION COUNT
DS2RORG3 DS    XL3                 NON-FIRST OVERFLOW REFERENCE COUNT
DS2NOBYT DS    XL2                 NUMBER OF BYTES FOR HIGHEST-LEVEL
*                                     INDEX
DS2NOTRK DS    XL1                 NUMBER OF TRACKS FOR HIGHEST-LEVEL
*                                     INDEX
DS2PRCTR DS    XL4                 PRIME RECORD COUNT
DS2STIND DS    XL1                 STATUS INDICATORS
*                     .1.. ....    PREFORM KEY SEQUENCE CHECKING   @L3A
*                     ..1. ....    INITIAL LOAD HAS BEEN COMPLETED @L3A
*                     .... ..1.    LAST BLOCK FULL                 @L3A
*                     .... ...1    LAST TRACK FULL                 @L3A
*                     X..X XX..    RESERVED BITS                   @L3A
DS2CYLAD DS    XL7                 ADDRESS OF CYLINDER INDEX
DS2ADLIN DS    XL7                 ADDRESS OF LOWEST LEVEL MASTER INDEX
DS2ADHIN DS    XL7                 ADDRESS OF HIGHEST LEVEL MASTER
*                                     INDEX
DS2LPRAD DS    XL8                 LAST PRIME DATA RECORD ADDRESS
DS2LTRAD DS    XL5                 LAST TRACK INDEX ENTRY ADDRESS
DS2LCYAD DS    XL5                 LAST CYLINDER INDEX ENTRY ADDRESS
DS2LMSAD DS    XL5                 LAST MASTER INDEX ENTRY ADDRESS
DS2LOVAD DS    XL8                 LAST INDEPENDENT OVERFLOW RECORD
*                                     ADDRESS
DS2BYOVL DS    XL2                 BYTES REMAINING ON OVERFLOW TRACK
DS2RORG2 DS    XL2                 TRACKS REMAINING IN INDEPENDENT
*                                     OVERFLOW AREA
DS2OVRCT DS    XL2                 OVERFLOW RECORD COUNT
DS2RORG1 DS    XL2                 CYLINDER OVERFLOW AREA COUNT
DS2NIRT  DS    XL3                 DUMMY TRACK INDEX ENTRY ADDRESS
DS2PTRDS DS    XL5                 POINTER (CCHHR) TO FORMAT 3 DSCB@P1C
*                                     OR ZERO                      @P1A
DS2END   EQU   *                   EQUATE AT END OF DSCB2 MAP      @P1C
&CTR     SETA  &CTR+1
         AGO   .LOOP
.THIRDON ANOP
IECSDSL3 EQU   *                   FORMAT 3 DSCB
IECSDSF3 EQU   IECSDSL3            SAME AS IECSDSL3                @P1C
         DS    XL4                 KEY IDENTIFIER (X'03030303')
DS3EXTNT DS    XL40                FOUR EXTENT DESCRIPTIONS
*        FIRST BYTE                EXTENT TYPE INDICATOR
*        SECOND BYTE               EXTENT SEQUENCE NUMBER
*        THIRD - SIXTH BYTES       LOWER LIMIT (CCHH)              @P1C
*        SEVENTH - TENTH BYTES     UPPER LIMIT (CCHH)              @P1C
DS3FMTID DS    CL1                 FORMAT IDENTIFIER (X'F3')
DS3ADEXT DS    XL90                NINE ADDITIONAL EXTENT DESCRIPTIONS
DS3PTRDS DS    XL5                 FORWARD CHAIN POINTER (CCHHR)   @P1C
*                                     TO FORMAT 3 DSCB OR ZERO     @P1A
DS3END   EQU   *                   EQUATE AT END OF DSCB3 MAP      @P1C
&CTR     SETA  &CTR+1
         AGO   .LOOP
.FORTHON ANOP
IECSDSL4 EQU   *                   FORMAT 4 DSCB
IECSDSF4 EQU   IECSDSL4            SAME AS IECSDSL4                @P1C
DS4IDFMT DS    CL1                 FORMAT IDENTIFIER (X'F4')
DS4HPCHR DS    XL5                 HIGHEST ADDRESS (CCHHR) OF A    @P1C
*                                     FORMAT 1 DSCB (SET TO END OF @P1A
*                                     VTOC IF INDEXED VTOC)        @P1A
DS4DSREC DS    XL2                 NUMBER OF AVAILABLE DSCB'S (NOT @P1C
*                                     MAINTAINED IF INDEXED VTOC)  @P1A
DS4HCCHH DS    XL4                 CCHH OF NEXT AVAILABLE ALTERNATE TRK
DS4NOATK DS    XL2                 NUMBER OF REMAINING ALTERNATE TRACKS
DS4VTOCI DS    XL1                 VTOC INDICATORS
DS4DOSBT EQU   X'80'  1... ....    DOS BIT (IF ON, EITHER NO FORMAT@P1C
*                                     5 DSCBS OR INDEXED VTOC -    @P1A
*                                     SEE DS4IVTOC)                @P1A
DS4DVTOC EQU   X'40'  .1.. ....    IF ON, THE INDEX WAS DISABLED   @02A
DS4DSTKP EQU   X'10'  ...1 ....    DOS STACKED PACK                @P1C
DS4DOCVT EQU   X'08'  .... 1...    DOS CONVERTED VTOC              @P1C
DS4DIRF  EQU   X'04'  .... .1..    DIRF BIT                        @P1C
DS4DICVT EQU   X'02'  .... ..1.    DIRF RECLAIMED                  @P1C
DS4IVTOC EQU   X'01'  .... ...1    IF ON, AN INDEX HAS BEEN CREATED@P1C
*                                     FOR THE VTOC.                @P1A
*                                  IF DS4DOSBT IS ON, THE INDEX IS @P1A
*                                     VALID.                       @P1A
*                                  IF EITHER OF THESE BITS IS OFF, @P1A
*                                    THE INDEX HAS BEEN INVALIDATED@P1A
*                     ..X. ....    RESERVED BIT                    @02C
DS4NOEXT DS    XL1                 NUMBER OF EXTENTS IN THE VTOC
         DS    XL2                 RESERVED
DS4DEVCT DS    0XL14               DEVICE CONSTANTS
DS4DEVSZ DS    XL4                 DEVICE SIZE                     @P1C
*                     XL2          NUMBER OF LOGICAL CYLINDERS     @P1A
*                     XL2          NUMBER OF TRACKS IN A           @P1A
*                                     LOGICAL CYLINDER             @P1A
DS4DEVTK DS    XL2                 DEVICE TRACK LENGTH
DS4DEVOV DS    0XL2                KEYED RECORD OVERHEAD
DS4DEVI  DS    XL1                    NON-LAST KEYED RECORD OVERHEAD
DS4DEVL  DS    XL1                    LAST KEYED RECORD OVERHEAD
DS4DEVK  DS    XL1                 NON-KEYED RECORD OVERHEAD
*                                     DIFFERENTIAL
DS4DEVFG DS    XL1                 FLAG BYTE
*                     .... 1...    THE KEYED RECORD OVERHEAD FIELD @L3A
*                                  (DS4DEVOV) IS USED AS A 2-BYTE  @L3A
*                                  FIELD TO SPECIFY THE OVERHEAD   @L3A
*                                  REQUIRED FOR A KEYED RECORD     @L3A
*                     .... .1..    THE CCHH OF AN ABSOLUTE ADDRESS IS
*                                  USED AS A CONTINUOUS BINARY VALUE
*                     .... ..1.    THE CCHH OF AN ABSOLUTE ADDRESS IS
*                                  USED AS 4 SEPARATE BINARY VALUES
*                     .... ...1    A TOLERANCE FACTOR MUST BE APPLIED
*                                  TO ALL BUT THE LAST BLOCK OF THE
*                                  TRACK.                          @L3A
*                     XXXX ....    RESERVED BITS                   @L3A
DS4DEVTL DS    XL2                 DEVICE TOLERANCE
DS4DEVDT DS    XL1                 NUMBER OF DSCB'S PER TRACK
DS4DEVDB DS    XL1                 NUMBER OF DIRECTORY BLOCKS PER TRACK
DS4AMTIM DS    XL8                 VSAM TIME STAMP
DS4AMCAT DS    0XL3                VSAM CATALOG INDICATOR
DS4VSIND DS    XL1                 VSAM INDICATORS
*                     1... ....    A VSAM CATALOG REFERENCES THIS  @L3A
*                                  VOLUME.                         @L3A
*                     .1.. ....    THE VSAM DATA SETS ON THIS VOLUME
*                                  ARE UNUSUABLE BECAUSE AN MSS
*                                  CONVERTV COMMAND HAS NOT COMPLETED
*                                  SUCCESSFULLY FOR THIS VOLUME.   @L3A
*                     ..XX XXXX    RESERVED BITS                   @L3A
DS4VSCRA DS    XL2                 RELATIVE TRACK LOCATION OF THE CRA
DS4R2TIM DS    XL8                 VSAM VOLUME/CATALOG MATCH
*                                  TIME STAMP
         DS    XL5                 RESERVED
DS4F6PTR DS    XL5                 POINTER (CCHHR) TO FIRST FORMAT @P1C
*                                     6 DSCB OR ZERO               @P1A
DS4VTOCE DS    XL10                VTOC EXTENT DESCRIPTION
         DS    XL25                RESERVED
DS4END   EQU   *                   EQUATE AT END OF DSCB4          @P1C
&CTR     SETA  &CTR+1
         AGO   .LOOP
.FIFTHON ANOP
IECSDSL5 EQU   *                   FORMAT 5 DSCB
IECSDSF5 EQU   IECSDSL5            SAME AS IECSDSL5                @P1C
DS5KEYID DS    XL4                 KEY IDENTIFIER (X'05050505')
DS5AVEXT DS    XL5                 AVAILABLE EXTENT
*        BYTES 1 - 2     RELATIVE TRACK ADDRESS OF THE FIRST TRACK
*                        IN THE EXTENT
*        BYTES 3 - 4     NUMBER OF UNUSED CYLINDERS IN THE EXTENT
*        BYTE  5         NUMBER OF ADDITIONAL UNUSED TRACKS
DS5EXTAV DS    XL35                SEVEN AVAILABLE EXTENTS
DS5FMTID DS    CL1                 FORMAT IDENTIFIER (X'F5')
DS5MAVET DS    XL90                EIGHTEEN AVAILABLE EXTENTS
DS5PTRDS DS    XL5                 FORWARD CHAIN POINTER (CCHHR)   @P1C
*                                     TO FORMAT 5 DSCB OR ZERO     @P1A
DS5END   EQU   *                   EQUATE AT END OF DSCB5          @P1C
&CTR     SETA  &CTR+1
         AGO   .LOOP
.SIXTHON ANOP
IECSDSL6 EQU   *                   FORMAT 6 DSCB
IECSDSF6 EQU   IECSDSL6            SAME AS IECSDSL6                @P1C
DS6KEYID DS    XL4                 KEY IDENTIFIER (X'06060606')
DS6AVEXT DS    XL5                 SHARED EXTENT DESCRIPTION
*        BYTES 1 - 2     RELATIVE TRACK ADDRESS OF THE FIRST CYLINDER
*        BYTES 3 - 4     NUMBER OF FULL CYLINDERS BEING SHARED
*        BYTE  5         NUMBER OF DATA SETS SHARING THE EXTENT
DS6EXTAV DS    XL35                SEVEN SHARED EXTENTS
DS6FMTID DS    CL1                 FORMAT IDENTIFIER (X'F6')
DS6MAVET DS    XL90                EIGHTEEN SHARED EXTENTS
DS6PTRDS DS    XL5                 FORWARD CHAIN POINTER (CCHHR)   @P1C
*                                     TO FORMAT 6 DSCB OR ZERO     @P1A
DS6END   EQU   *                   EQUATE AT END OF DSCB6          @P1C
&CTR     SETA  &CTR+1
         AGO   .LOOP
.ERROR   MNOTE 7,'***INCORRECT FORMAT NUMBER'
.ENDOFIT MEND
