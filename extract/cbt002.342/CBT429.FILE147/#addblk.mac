         MACRO
&LBL     #ADDBLK &BUFFER=,&RECORD=,&LENGTH=,&ERR=,&RTN=,&LAST=NO
.*
.* BUFFER IS A SINGLE DIGIT, EITHER 1 OR 2, DENOTINE VS1BUF1 OR
.*        VS1BUF2
.*
.* RECORD IS THE ADDRESS OF THE PIECE TO BE ADDED, NOT INCLUDING THE
.*        ARCHIVER'S SPECIAL 3-BYTE LENGTH FIELD
.*
.* LENGTH IS THE LENGTH OF THE PIECE TO BE ADDED, AGAIN IGNORING THE
.*        ARCHIVER'S SPECIAL 3-BYTE LENGTH FIELD
.*
.* ERR IS A LOCATION TO BRANCH TO IF THE PIECE CANNOT BE ADDED TO THE
.*     CURRENT CLUSTER.
.*
.* RTN IS THE ROUTINE THAT WILL DO ALL VSAM 'PUT' ACTIVITY
.*
.* LAST=YES OR NO, TO DENOTE THE NEED FOR A 'PUT' WITHOUT ADDING A
.*       PIECE TO THE BUFFER.
.*
.* NOTE EXTENSIVE USE OF THE #LR MACRO TO LOAD OPERANDS INTO REGS.
.*
         LCLC  &A,&B,&C,&D,&E,&F,&G,&H,&I,&BUF,&LEN
&A       SETC  '&LBL'
&B       SETC  '#VSA&SYSNDX'
&C       SETC  '#VSB&SYSNDX'
         AIF   ('&A' EQ '').NOLBL
&LBL     DS    0H
.NOLBL   ANOP
         AIF   ('&BUFFER' NE '1').B2
&BUF     SETC  'VS1BUF1'
&LEN     SETC  'VS1LEN1'
         AGO   .C
.B2      AIF   ('&BUFFER' EQ '2').B3
         MNOTE 8,'BUFFER=&BUFFER IS INVALID. BUFFER=1 ASSUMED.'
&BUF     SETC  'VS1BUF1'
&LEN     SETC  'VS1LEN1'
         AGO   .C
.B3      ANOP
&BUF     SETC  'VS1BUF2'
&LEN     SETC  'VS1LEN2'
.C       ANOP
         AIF   ('&LAST' EQ 'YES' OR '&LAST' EQ 'Y').LAST
         #LR   R0,&RECORD          LOAD RECORD POINTER
         #LR   R1,&LENGTH            AND LENGTH
         ICM   R1,7,0(R1)
         ICM   R1,8,=X'00'
         LA    R15,3(,R1)          ACCOUNT FOR LENGTH FIELD
         LA    R14,RECKLEN(,R15)   ACCOUNT FOR VSAM KEY
         C     R14,VS1LIM          CAN IT FIT ??
         BH    &ERR                NOPE; ERROR ---
         LR    R14,R15             COPY THAT LENGTH
         A     R14,&LEN            ADD CURRENT LENGTH
         C     R14,VS1LIM          WILL THIS FIT ??
         BNH   &B                  YES; JUST ADD IT IN
         STM   R0,R1,BLKSAVE       SAVE THAT RECORD STUFF
         L     R1,&BUF             POINT TO THE BUFFER
         L     R0,&LEN             AND LOAD ITS LENGTH
         BAL   R14,&RTN            GO OUTPUT THE RECORD
         L     R1,&BUF             LOAD THE BUFFER ADDRESS
         ICM   R15,15,RECSEQ#-REC(R1) INSERT THE SEQUENCE NUMBER
         LA    R15,1(,R15)         ADD ONE
         STCM  R15,15,RECSEQ#-REC(R1) SAVE UPDATED VALUE
         LA    R0,RECKLEN          LOAD LENGTH OF NAKED KEY
         ST    R0,&LEN             AND SAVE IT
         LM    R0,R1,BLKSAVE       RELOAD THAT INPUT STUFF
&B       DS    0H
         L     R14,&LEN            LOAD CURRENT RECORD LENGTH
         LA    R15,3(R1,R14)       ADD THIS ONE
         ST    R15,&LEN            SAVE UPDATED VALUE
         A     R14,&BUF            POINT INTO THE BUFFER
         STCM  R1,7,0(R14)         STORE LENGTH BYTES
         LA    R14,3(,R14)         POINT TO DATA AREA
         LR    R15,R1              COPY THE LENGTH
         MVCL  R14,R0              COPY RECORD TO BUFFER
         MEXIT
.LAST    ANOP
         LA    R0,RECKLEN          LOAD THE KEY LENGTH
         C     R0,&LEN             ANYTHING BUT A KEY ??
         BE    &C                  NOPE; ALL DONE
         L     R1,&BUF             LOAD BUFFER ADDRESS
         L     R0,&LEN             AND LENGTH
         BAL   R14,&RTN            OUTPUT THE LAST RECORD
         L     R1,&BUF             LOAD THE BUFFER ADDRESS
         ICM   R15,15,RECSEQ#-REC(R1) INSERT THE SEQUENCE NUMBER
         LA    R15,1(,R15)         ADD ONE
         STCM  R15,15,RECSEQ#-REC(R1) SAVE UPDATED VALUE
         LA    R0,RECKLEN          LOAD LENGTH OF NAKED KEY
         ST    R0,&LEN             AND SAVE IT
&C       DS    0H
         MEND
