         MACRO
&LBL     $CVRECFM &OUT,&IN
.*--------------------------------------------------------------------*
.*---    GENERAL-PURPOSE MACRO                                     ---*
.*---                                                              ---*
.*---    CONVERT DCBRECFM TO PRINTABLE FORMAT                      ---*
.*---                                                              ---*
.*---    IN= IS A ONE-BYTE FIELD CONTAINING THE RECFM FIELD OF     ---*
.*---    A DCB OR FORMAT-1 DSCB.                                   ---*
.*---                                                              ---*
.*---    OUT= IS AN OUTPUT FIELD THAT WILL CONTAIN THE CHARACTER   ---*
.*---    FORM OF THE RECFM VALUE IN THE INPUT FIELD. NO CHECK IS   ---*
.*---    MADE REGARDING THE LENGTH OF EITHER THE OUTPUT OR INPUT   ---*
.*---    FIELDS.  NOTE THAT THIS MACRO CAN GENERATE REENTRANT CODE ---*
.*---    AND IS NOT ADDRESSING-MODE DEPENDANT.                     ---*
.*--------------------------------------------------------------------*
         LCLC  &LL,&LLA,&LLB,&LLC,&LLD,&LLE,&LLF,&LLG,&LLH
&LLA     SETC  '$CRA&SYSNDX'
&LLB     SETC  '$CRB&SYSNDX'
&LLC     SETC  '$CRC&SYSNDX'
&LLD     SETC  '$CRD&SYSNDX'
&LLE     SETC  '$CRE&SYSNDX'
&LLF     SETC  '$CRF&SYSNDX'
&LLG     SETC  '$CRG&SYSNDX'
&LLH     SETC  '$CRH&SYSNDX'
&LL      SETC  '&LBL'
         AIF   ('&LL' EQ '').NOLBL
&LL      DS    0H
.NOLBL   ANOP
         LA    15,&OUT             POINT TO OUTPUT FIELD
         MVC   0(8,15),=CL8' '     AND CLEAR IT OUT
         TM    &IN,X'C0'           RECFM=U ??
         BNO   &LLA                NOPE ---
         MVI   0(15),C'U'          YES ---
         LA    15,1(,15)           NEXT POSITION
         B     &LLC
&LLA     DS    0H
         TM    &IN,X'80'           RECFM=F ??
         BZ    &LLB                NOPE ---
         MVI   0(15),C'F'
         LA    15,1(,15)
         B     &LLC
&LLB     DS    0H
         TM    &IN,X'40'           RECFM = V ??
         BZ    &LLC                NOPE ---
         MVI   0(15),C'V'
         LA    15,1(,15)
&LLC     DS    0H
         TM    &IN,X'20'           TRACK OVERFLOW ??
         BZ    &LLD                NOPE ---
         MVI   0(15),C'T'
         LA    15,1(,15)
&LLD     DS    0H
         TM    &IN,X'10'           BLOCKED RECORDS ??
         BZ    &LLE
         MVI   0(15),C'B'
         LA    15,1(,15)
&LLE     DS    0H
         TM    &IN,X'08'           SPANNED/STANDARD RECORDS ??
         BZ    &LLF                NOPE ---
         MVI   0(15),C'S'
         LA    15,1(,15)
&LLF     DS    0H
         TM    &IN,X'04'        ANSI CONTROL CHARACTERS ??
         BZ    &LLG                NOPE ---
         MVI   0(15),C'A'
         LA    15,1(,15)
&LLG     DS    0H
         TM    &IN,X'02'           MACHINE CONTROL CHARACTERS ??
         BZ    &LLH                NOPE ---
         MVI   0(15),C'M'
&LLH     DS    0H
         MEND
