         MACRO
&NAME    #IF   &BIT,&COND,&GOTO
         GBLB  &ERROR              ERROR FLAG.
         GBLC  &DSWBYTE(80)        DATA SWITCH BYTE NAME ARRAY.
         GBLC  &DSWBIT(640)        DATA SWITCH BIT NAME ARRAY.
         GBLC  &SWBYTE             SWITCH BYTE NAME.
         GBLA  &DSWBCNT            SWITCH BIT COUNT
.*
         LCLA  &X,&N               COUNTERS.
         LCLC  &OPC,&LBL
         AIF   ('&COND' EQ 'ON').IFON
         AIF   ('&COND' EQ 'OFF').IFOFF
         MNOTE 8,'WHAT CONDITION TO TEST FOR ? '
         MEXIT
.IFON    ANOP
&OPC     SETC  'BO'
         AGO   .GETBIT
.IFOFF   ANOP
&OPC     SETC  'BNO'
.GETBIT  #FINDSW &BIT
         AIF   (&ERROR).NOTEST
&LBL     SETC  '&NAME'
         AIF   ('&LBL' EQ '').NOLBL
&NAME    DS    0H
.NOLBL   ANOP
         TM    &SWBYTE,&BIT        TEST THE FLAG BIT
         &OPC  &GOTO               AND BRANCH ---
         MEXIT
.NOTEST  ANOP
         MNOTE 8,'BIT &BIT IS NOT DEFINED.'
         MEND
