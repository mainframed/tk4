         MACRO     -- FREE BUFFER POOL, IF IT EXISTS --
&LABEL   #FREE
.**********************************************************************
.*       THIS LITTLE MACRO GENERATES THE CODE TO CHECK EACH OF A LIST *
.*       LIST OF DCB'S AND FREE THE BUFFER POOLS, IF ANY.             *
.*       IF THE BUFFER POOL EXISTS, A 'FREEPOOL MACRO IS ISSUED       *
.*       TO RELEASE THE ALLOCATED BUFFERS.  IF NOT, THE FREEPOOL      *
.*       MACRO IS SKIPPED.                                            *
.*                                                                    *
.*       *******  WARNING  *******  DO NOT ISSUE THIS MACRO ON AN     *
.*       OPEN DCB. THE RESULTS ARE UNPREDICTABLE.                     *
.**********************************************************************
.*       RICK FOCHTMAN, IHC. CCC., 18 NOV 74                          *
.**********************************************************************
         GBLA  &FREC
         LCLC  &DCB,&LBL
         LCLA  &KNT,&LP1
         AIF   ('&LABEL' EQ '').ISNA
         SPACE 1
&LABEL   DS    0H            DEFINE THE LABEL
.ISNA    ANOP
&LP1     SETA  1
&KNT     SETA  N'&SYSLIST
         AIF   (&KNT EQ 0).ERRA
.ISNB    AIF   (&LP1 GT &KNT).MEXIT
&DCB     SETC  '&SYSLIST(&LP1)'
         AIF   ('&DCB' NE '').ISNC
         MNOTE 8,'DUMMY/NULL OPERANDS NOT ALLOWED. IGNORED'
&LP1     SETA  &LP1+1
         AGO   .ISNB
.ISNC    ANOP
&FREC    SETA  &FREC+1
&LBL     SETC  '$FRE&FREC'
         SPACE 1
         #LR   1,&DCB        POINT OUT THE DCB
         TM    23(1),1       Q/ BUFCB ADDR INVALID
         BO    &LBL          NO BUFFER POOL FOR THIS DCB
         FREEPOOL  (1)       HAS POOL, SO FREE IT
&LBL     DS    0H
&LP1     SETA  &LP1+1        BUMP THE LOOP COUNTER
         AGO   .ISNB         AND LOOP - - -
.MEXIT   ANOP
         SPACE 1
         MEXIT
.ERRA    ANOP
         MNOTE 4,'NO OPERANDS. NO CODE GENERATED'
         MEND
