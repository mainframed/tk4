         MACRO
&L       FLO   &AREAS=,&MSG=,&MF=
.*
.*       GENERAL STATEMENT LABEL TRACE MACRO
.*
.*       EXPANSION OF THIS MACRO IS GOVERNED BY THE ARITHMATIC GLOBAL
.*       VARIABLE '&TRACE' AND/OR A '&SYSPARM' PARAMETER. '&TRACE'
.*       VALUES OVERRIDE THE '&SYSPARM' PARAMETER IF BOTH ARE PRESENT
.*       UNLESS THE '&TRACE' OR THE '&SYSPARM' LEVEL IS ZERO. IF THE
.*       '&SYSPARM' TRACE LEVEL IS ZERO(OFF), IT UNCONDITIONALLY TURNS
.*       OFF ALL TRACE CODE GENERATION. IF THE '&TRACE' LEVEL IS ZERO,
.*       THE '&SYSPARM' LEVEL (IF SPECIFIED) IS USED AS THE DEFAULT
.*       TRACE LEVEL.
.*       THE TRACE LEVEL IS USER SELECTED AS FOLLOWS:
.*
.*       &TRACE=0  NO TRACE CODE PRODUCED.
.*       &TRACE=1  STATEMENT LABEL TRACE.
.*       &TRACE=2  STATEMENT LABEL TRACE AND GENL REGISTERS.
.*       &TRACE=3  STATEMENT LABEL TRACE,GENL REGS, AND SNAP DUMP.
.*       &TRACE=4  STATEMENT LABEL TRACE,GENL REGS, AND ABEND DUMP.
.*
.*       MAIN STORAGE AREAS MAY BE TRACED AS FOLLOWS:
.*       AREAS=(A,B<,A,B>...)
.*             WHERE-
.*                 A = THE ADDRESS OF A MAIN STORAGE AREA TO BE TRACED.
.*                     THE ADDRESS MAY BE SPECIFIED AS A RELOCATABLE
.*                     SYMBOL OR AS A REGISTER (ENCLOSED BY PARENS)
.*                     WHICH CONTAINS THE ADDRESS.
.*                 B = THE LENGTH OF THE MAIN STORAGE AREA TO BE
.*                     TRACED. THE LENGTH MAY BE SPECIFIED AS AN
.*                     ABSOLUTE EXPRESSION OR AS A REGISTER (ENCLOSED
.*                     BY PARENS) WHICH CONTAINS THE LENGTH.
.*
.*       TRACE MESSAGES MAY BE PRINTED AS FOLLOWS:
.*       MSG='TEXT STRING'     OR...
.*       MSG=A
.*             WHERE-
.*                 A = THE ADDRESS OF A HALFWORD LENGTH FOLLOWED BY A
.*                     STRING. THE ADDRESS MAY BE SPECIFIED AS A
.*                     RELOCATABLE SYMBOL OR AS A REGISTER (ENCLOSED BY
.*                     PARENS) WHICH CONTAINS THE ADDRESS.
.*
         GBLA  &TRACE              IN-LINE CODE SPECIFIED TRACE LEVEL.
         LCLA  &PARM               SYSPARM SPECIFIED TRACE LEVEL.
         LCLA  &CTR,&CTR1          COUNTERS.
         LCLC  &C                  SYSPARM SPECIFIED TRACE VALUE.
         LCLC  &T                  TYPE OF AREAS LENGTH.
&L       DS    0H
.*
.*       EXTRACT TRACE LEVEL FROM SYSPARM
.*
         AIF   (K'&SYSPARM LT 7).NOPARM BR IF NO SYSPARM TRACE.
&CTR     SETA  K'&SYSPARM-6
.LOOP    AIF   (&CTR EQ 0).NOPARM  BR IF NO SYSPARM TRACE.
         AIF   ('&SYSPARM'(&CTR,6) EQ 'TRACE=').CHKPARM BR IF TRACE SPE
&CTR     SETA  &CTR-1              SCAN AL OF SYSPARM CHARACTERS.
         AGO   .LOOP
.*
.CHKPARM ANOP
&C       SETC  '&SYSPARM'(&CTR+6,1) GET TRACE VALUE.
         AIF   ('&C' EQ 'O').END   BR IF UNCONDITIONAL TRACE OFF.
         AIF   ('&C' EQ '0').END
&PARM    SETA  1
         AIF   ('&C' EQ 'L').SETPARM TRACE LABELS.
         AIF   ('&C' EQ '1').SETPARM
&PARM    SETA  2
         AIF   ('&C' EQ 'R').SETPARM TRACE REGISTERS.
         AIF   ('&C' EQ '2').SETPARM
&PARM    SETA  3
         AIF   ('&C' EQ 'S').SETPARM SNAP DUMP.
         AIF   ('&C' EQ '3').SETPARM
&PARM    SETA  4
         AIF   ('&C' EQ 'A').SETPARM ABEND DUMP.
         AIF   ('&C' EQ '4').SETPARM
         MNOTE 2,'*** ERROR ** INVALID SYSPARM TRACE VALUE'
         MEXIT
.*
.SETPARM AIF   (&TRACE NE 0).NOPARM BR IF EXPLICIT TRACE SPECIFIED.
&TRACE   SETA  &PARM               USE SYSPARM SPECIFIED TRACE LEVEL.
.NOPARM  ANOP                      SYSPARM BYPASS.
         AIF   (&TRACE EQ 0).END   BYPASS CODE GENERATION.
.*
.*       CONSTRUCT MESSAGE CALL TO TRACE MODULE
.*
         AIF   (K'&MSG EQ 0).NOMSG BYPASS MESSAGE CODE GENERATION.
         AIF   ('&MSG'(1,1) EQ '''').MSGTEXT BR IF QUOTED TEXT.
         CNOP  2,4                 .SET PARAMETER LIST ALIGNMENT.
         AIF   (('&MSG'(1,1) EQ '(') AND ('&MSG'(K'&MSG,1) EQ ')')).REG
         L     14,=V(TRACE5)       .SET TRACE MODULE ADDRESS.
         BALR  14,14               .LINK TO TRACE MODULE.
         DC    A(&MSG)             .MESSAGE ADDRESS.
         AGO   .NOMSG
.REG     ANOP
&C       SETC  '&MSG'(2,K'&MSG-2)  EXTRACT REGISTER FROM PARENS.
         ST    &C,*+10             .SET TRACE MESSAGE ADDRESS.
         AGO   .MSGCOM
.MSGTEXT ANOP
&CTR     SETA  K'&MSG-2            SET MESSAGE TEXT LENGTH.
&C       SETC  '&MSG'(2,&CTR)      EXTRACT MESSAGE FROM QUOTES.
         BAL   14,AMA&SYSNDX.M     .OBTAIN MESSAGE ADDRESS.
         DC    AL2(&CTR)           .MESSAGE LENGTH.
         DC    C'&C'               .MESSAGE TEXT.
         CNOP  2,4                 .SET PARAMETER LIST ALIGNMENT.
&C       SETC  'AMA&SYSNDX.M'
&C       ST    14,*+10             .SET MESSAGE ADDRESS.
.MSGCOM  L     14,=V(TRACE5)       .SET TRACE MODULE ENTRY ADDRESS.
         BALR  14,14               .LINK TO TRACE MODULE.
         DC    A(*-*)              .MESSAGE ADDRESS.
.*
.*       SET TRACE PARAMETER LIST VALUES
.*
.NOMSG   ANOP                      MESSAGE CODE GENERATION BYPASS.
         AIF   ('&MF' EQ 'O').END  BR IF MESSAGE ONLY FORM.
         AIF   (K'&AREAS EQ 0).SETLINK BR IF NOT AREAS.
&CTR     SETA  N'&AREAS            GET NUMBER OF AREAS TO BE TRACED.
         AIF   (&CTR/2*2 NE &CTR).BADAREA
.LOOP1   AIF   (&CTR EQ 0).SETALIN BR IF PARM SETUP COMPLETE.
&C       SETC  '&AREAS(&CTR-1)'    GET ADDRESS STRING.
         AIF   ('&C' EQ '').BADAREA
&CTR1    SETA  10+16*(&CTR/2-1)    SET ADDRESS PARAMETER OFFSET.
         AIF   (('&C'(1,1) EQ '(') AND ('&C'(K'&C,1) EQ ')')).REGAREA
         LA    14,&C               .OBTAIN AREA ADDRESS.
         ST    14,AMA&SYSNDX.A+&CTR1 .SET TRACE PARM ADDRESS.
         AGO   .SETLEN
.REGAREA ANOP
&C       SETC  '&C'(2,K'&C-2)      EXTRACT REGISTER FROM PARENS.
         ST    &C,AMA&SYSNDX.A+&CTR1 .SET TRACE PARM ADDRESS.
.SETLEN  ANOP
&C       SETC  '&AREAS(&CTR)'      GET LENGTH STRING.
&T       SETC  T'&AREAS(&CTR)      GET TYPE OF LENGTH STRING.
         AIF   ('&C' EQ '').NXTAREA
         AIF   (('&C'(1,1) EQ '(') AND ('&C'(K'&C,1) EQ ')')).EXLEN
         AIF   ('&T' EQ 'N').NXTAREA
.EXLEN   ANOP
&CTR1    SETA  &CTR1+4
         AIF   (('&C'(1,1) EQ '(') AND ('&C'(K'&C,1) EQ ')')).REGLEN
         LA    14,&C               OBTAIN AREA LENGTH.
         STH   14,AMA&SYSNDX.A+&CTR1 .SET TRACE PARM LENGTH.
         AGO   .NXTAREA
.REGLEN  ANOP
&C       SETC  '&C'(2,K'&C-2)      EXTRACT REGISTER FROM PARENS.
         STH   &C,AMA&SYSNDX.A+&CTR1 .SET TRACE PARM LENGTH.
.NXTAREA ANOP
&CTR     SETA  &CTR-2              GET NEXT AREA.
         AGO   .LOOP1
.SETALIN CNOP  2,4                 .SET PARAMETER LIST ALIGNMENT.
.*
.*       ESTABLISH LINKAGE TO TRACE MODULE
.*
.SETLINK ANOP
         L     14,=V(TRACE&TRACE)  .SET TRACE MODULE ENTRY ADDRESS.
         BALR  14,14               .LINK TO TRACE MODULE.
.*
.*       CONSTRUCT TRACE PARAMETER LIST
.*
         AIF   (K'&AREAS EQ 0).NOAREAS BR IF NO AREAS TRACE.
&CTR     SETA  N'&AREAS            GET NUMBER OF AREAS TO BE TRACED.
         SPACE 1
         DC    AL1(0)              .INDICATE AREAS TRACE.
&CTR1    SETA  &CTR/2
         DC    AL1(&CTR1)          .NUMBER OF AREAS TO TRACE.
         SPACE 1
&C       SETC  'AMA&SYSNDX.A'
&C       DS    0H
&CTR1    SETA  1
.LOOP2   AIF   (&CTR LT &CTR1).NOAREAS
         DC    CL10'&AREAS(&CTR1)' .AREA NAME.
         DC    AL4(*-*)            .AREA ADDRESS.
&C       SETC  '&AREAS(&CTR1+1)'   EXTRACT LENGTH STRING.
&T       SETC  T'&AREAS(&CTR1+1)   EXTRACT TYPE OF LENGTH STRING.
         AIF   ('&T' EQ 'N').SELFDEF
         AIF   ('&AREAS(&CTR1+1)' NE '').NOLEN
&C       SETC  'L'.''''.'&AREAS(&CTR1)'
.SELFDEF ANOP
         DC    AL2(&C)             .AREA LENGTH.
         AGO   .NEXT
.NOLEN   DC    AL2(*-*)            .AREA LENGTH.
.NEXT    ANOP
         SPACE 1
&CTR1    SETA  &CTR1+2             GET NEXT AREA.
         AGO   .LOOP2
.NOAREAS ANOP                      MAIN STORAGE TRACE BYPASS.
         DC    CL8'&L'             .TRACE IDENTIFIER.
         AGO   .END
.*
.BADAREA MNOTE 2,'*** ERROR ** INVALID AREAS= SPECIFICATION'
.END     ANOP
         MEND
