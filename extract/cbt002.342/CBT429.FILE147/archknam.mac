CNAM     TITLE 'ARCHKNAM --- ARCHIVER FUNCTION SUPPORT SUBROUTINE'
*---------------------------------------------------------------------*
*---     FUNCTION: CHECK THE NAME IN THE RECORD KEY POINTED TO     ---*
*---           BY REGISTER 1 AGAINST THE NAME(S) FROM THE          ---*
*---           CURRENT CONTROL CARD. SET A ZERO RETURN CODE FOR    ---*
*---           A MATCH OR A 4 FOR A MIS-MATCH.                     ---*
*---                                                               ---*
*---     THIS ROUTINE IS INVOKED EXCLUSIVELY VIA #CHKNAM MACRO.    ---*
*---------------------------------------------------------------------*
         EJECT
ARCHKNAM CSECT
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         USING ARCHKNAM,R15
         #STAMP
         STM   R14,R12,12(R13)     STORE ENTRY REGISTERS
         LR    R11,R15             LOAD ROUTINE BASE REGISTER
         DROP  R15                 DROP THE OLD BASE
         USING ARCHKNAM,R11        DEFINE THE NEW BASE REGISTER
         LA    R14,72(,R13)        POINT DOWN THE SAVE AREA STACK
         ST    R14,8(,R13)         STORE FORWARD POINTER
         ST    R13,4(,R14)         AND BACK POINTER
         LR    R13,R14             LOAD NEW SAVE AREA POINTER
         L     R14,4(,R13)         RELOAD BACK POINTER
         L     R14,12(,R14)        RELOAD ORIG. REG 14
         USING MAINWK,R12          BASE THE MAIN WORK AREA
         COPY  #ARCBITS
         L     R15,4(,R13)         LOAD BACK SAVE-AREA POINTER
         XC    16(4,R15),16(R15)   INITIAL RETURN CODE IS ZERO
         LA    R2,Q1               INITIALIZE NAME-LIST POINTER
         LTR   R0,R0               WAS A NAME-LIST PASSED ??
         BZ    CHK0                NOPE
         LR    R2,R0               YES; COPY ITS ADDRESS
CHK0     DS    0H
         LA    R7,30(,R2)          POINT TO LAST QUALIFIER
         LA    R6,10               AND QUALIFIER LENGTH
CHK1     DS    0H
         BAL   R9,CHKQ             GO CHECK THE QUALIFIER
         B     NOMATCH             TEST FAILED; SET NOMATCH RETURN CODE
         LA    R1,10(,R1)          POINT TO NEXT VALUE
         BXLE  R2,R6,CHK1          AND LOOP, CHECKINGLY
         #IF   VRS,OFF,MATCH       WAS VERSION NUMBER GIVEN ??
         ICM   R0,15,0(R1)         LOAD THE VERSION NUMBER
         C     R0,0(,R2)           DOES IT MATCH ??
         BNE   NOMATCH             NOPE; FAIL THE TEST
MATCH    DS    0H
         L     R13,4(,R13)         LOAD THE BACK POINTER
         LM    R14,R12,12(R13)     RESTORE ENTRY REGISTERS
         MVI   12(R13),255
         BR    R14                 AND RETURN TO CALLER
NOMATCH  DS    0H
         L     R15,4(,R13)         LOAD BACK POINTER
         MVC   16(4,R15),=F'4'     NON-ZERO RETURN CODE FOR FAILURE
         B     MATCH               AND RETURN TO CALLER
         TITLE 'GENERIC/NONGENERIC QUALIFIER COMPARISON'
*---------------------------------------------------------------------*
*---     WE DO SOME STRANGE THINGS WITH MASKS, 'AND' AND COMPARE   ---*
*---     INSTRUCTIONS HERE.  THE STEPS ARE RELATIVELY SIMPLE,      ---*
*---     ONCE THEY'RE UNDERSTOOD, BUT CAN BE A LITTLE STRANGE TO   ---*
*---     GRASP IF YOU DON'T WORK IT OUT ONCE OR TWICE WITH PAPER   ---*
*---     AND PENCIL.  THE BASIC STEPS ARE THESE:                   ---*
*---                                                               ---*
*---     1. COPY THE QUALIFIER FROM THE CONTROL STATEMENT TO THE   ---*
*---        GENWK AREA. THEN MAKE A COPY OF IT, IMMEDIATELY        ---*
*---        FOLLOWING THE FIRST.                                   ---*
*---                                                               ---*
*---     2. EVERY PLACE WE HAVE A 'GENERIC' CHARACTER, REPLACE IT  ---*
*---        WITH A BLANK. ALSO, EVERY NON-GENERIC CHARACTER IS     ---*
*---        REPLACED WITH A X'FF'.                                 ---*
*---                                                               ---*
*---     3. TO MAKE THE COMPARISON, DO THIS:                       ---*
*---                                                               ---*
*---        A. 'AND' THE INPUT VALUE WITH THE FIRST MASK. ALL      ---*
*---           CHARACTERS THAT ARE NOT GENERICS WILL BE UNCHANGED  ---*
*---           AND ALL GENERICS WILL BE REPLACED BY BLANKS.        ---*
*---                                                               ---*
*---        B. COMPARE THE RESULT WITH THE SECOND MASK.  ALL       ---*
*---           NON-GENERIC CHARACTERS WILL BE CHECKED FOR EXACT    ---*
*---           MATCHES AND ALL GENERIC CHARACTERS WILL BE BLANKS   ---*
*---           COMPARED TO BLANKS, MAKING THEM EQUAL.              ---*
*---                                                               ---*
*---     SIMPLE, ISN'T IT ??                                       ---*
*---------------------------------------------------------------------*
CHKQ     DS    0H
         MVC   GENWK(10),0(R2)     COPY THE MEMBER NAME
         MVC   GENWK+10(10),GENWK  AND DUPLICATE IT
         CLC   =CL10'%%%%%%%%%%',0(R2) ALL ITEMS ??
         BE    4(,R9)              YES; NOTHING TO FILTER
         LA    R5,GENWK+9          POINT TO STRING END
         LA    R3,GENWK            AND STRING START
         LA    R4,1                AND BXLE INCREMENT
F1       DS    0H
         CLI   0(R3),C'%'          GENERIC CHARACTER ??
         BNE   F2                  NOPE ---
         MVI   0(R3),C' '          YES; REPLACE WITH A BLANK
         MVI   10(R3),C' '         YES; REPLACE WITH A BLANK
         B     F3                  DO OTHER MASK
F2       DS    0H
         MVI   0(R3),X'FF'         ANYTHING GOES HERE
F3       DS    0H
         BXLE  R3,R4,F1            LOOP FOR MASKS
*---------------------------------------------------------------------*
*---     SEARCH MASKS ARE BUILT. NOW DO THE COMPARE                ---*
*---------------------------------------------------------------------*
TSTX     DS    0H
         MVC   GENWK+20(10),0(R1)  COPY THE MEMBER NAME
         NC    GENWK+20(10),GENWK  MASK OUT THE GENERIC CHARS
         CLC   GENWK+20(10),GENWK+10 REMAINDER MATCH ??
         BE    4(,R9)              YES; PASS THE TEST
         BNE   0(,R9)              NOPE; FAIL THE TEST
         LTORG
         COPY  #RECS               INPUT RECORD DSECT
         COPY  ##ARCHWK            ARCHIVER MAIN WORK AREA
         END
