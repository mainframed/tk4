         MACRO
&LBL     #FORMAT &SRC,&MSG,&TXT,&OUT=LINE+1,&CLEAR=YES,&TIME=NO
         LCLC  &A,&LL
&LL      SETC  '&LBL'
         SPACE 1
         AIF   ('&LL' EQ '').A
&LL      DS    0H
.A       AIF   ('&CLEAR' NE 'YES').B
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
.B       ANOP
&A       SETC  '&SRC'
         AIF   ('&A' EQ '').ERR1   &SRC MISSING - REQUIRED
         LA    R0,&A               LOAD RECORD KEY ADDRESS
&A       SETC  '&TXT'              CHECK FOR MESSAGE TEXT
         AIF   ('&A' EQ '').NOTEXT
         LA    R1,&A               LOAD TEXT ADDRESS
         AGO   .CALL
.NOTEXT  ANOP
         LA    R1,0                NO MESSAGE TEXT FOLLOWS
.CALL    ANOP
&A       SETC  '&MSG'              CHECK THE MESSAGE NUMBER
         AIF   ('&A' NE '').MSG
&A       SETC  ''' '''
.MSG     ANOP
         AIF   ('&TIME' EQ 'NO').NOTIME
         O     R1,=X'80000000'     SET 'TIME REQUIRED' FLAG
.NOTIME  ANOP
         L     R15,=V(ARCHFORM)    LOAD FORMATTER ADDRESS
         BALR  R14,R15             AND CALL IT
         DC    CL8&A               MESSAGE NUMBER
         DC    S(&OUT)             OUTPUT POINTER
         SPACE 1
         MEXIT
.ERR1    ANOP
         MNOTE 8,'REQUIRED SRC OPERAND IS MISSING.'
         MEND
