         MACRO
&LABEL   #DSW  &B80,&B40,&B20,&B10,&B08,&B04,&B02,&B01,&TYPE=,&INIT=
.*
.*       DATA SWITCH GENERATION MACRO
.*
         GBLC  &DSWBYTE(80)        DATA SWITCH BYTE NAME ARRAY.
         GBLC  &DSWBIT(640)        DATA SWITCH BIT NAME ARRAY.
         GBLA  &DSWBCNT            DATA SWITCH BIT COUNT
         LCLA  &X
         LCLC  &C
.DSW5    AIF   (&X EQ 80).DSWERR
&X       SETA  &X+1
         AIF   ('&DSWBYTE(&X)' NE '').DSW5
&C       SETC  'DSW&SYSNDX.A'
         AIF   (K'&LABEL EQ 0).NOLABEL
&C       SETC  '&LABEL'
.NOLABEL ANOP
         AIF   ('&TYPE' EQ 'NODATA').NODATA
         AIF   (K'&INIT EQ 0).NOINIT
&C       DC    &INIT               .INITIALIZED SWITCH VARIABLE.
&DSWBCNT SETA  &DSWBCNT+8
         AGO   .ENDINIT
.NOINIT  ANOP
&C       DS    B                   .SWITCH VARIABLE.
&DSWBCNT SETA  &DSWBCNT+8
.ENDINIT ANOP
.NODATA  ANOP
&DSWBYTE(&X) SETC '&C'             SAVE SWITCH VARIABLE NAME.
&X       SETA  (&X-1)*8            GET CORRESPONDING BIT SWITCH GROUP.
         AIF   ('&B80' EQ '').DSW10
&B80     EQU   B'10000000'
&DSWBIT(&X+1) SETC '&B80'
.DSW10   AIF   ('&B40' EQ '').DSW20
&B40     EQU   B'01000000'
&DSWBIT(&X+2) SETC '&B40'
.DSW20   AIF   ('&B20' EQ '').DSW30
&B20     EQU   B'00100000'
&DSWBIT(&X+3) SETC '&B20'
.DSW30   AIF   ('&B10' EQ '').DSW40
&B10     EQU   B'00010000'
&DSWBIT(&X+4) SETC '&B10'
.DSW40   AIF   ('&B08' EQ '').DSW50
&B08     EQU   B'00001000'
&DSWBIT(&X+5) SETC '&B08'
.DSW50   AIF   ('&B04' EQ '').DSW60
&B04     EQU   B'00000100'
&DSWBIT(&X+6) SETC '&B04'
.DSW60   AIF   ('&B02' EQ '').DSW70
&B02     EQU   B'00000010'
&DSWBIT(&X+7) SETC '&B02'
.DSW70   AIF   ('&B01' EQ '').DSW80
&B01     EQU   B'00000001'
&DSWBIT(&X+8) SETC '&B01'
.DSW80   MEXIT
.*
.*
.DSWERR  MNOTE 0,'** ERROR ** MAX NUMBER OF SWITCHES EXCEEDED'
         MEND
