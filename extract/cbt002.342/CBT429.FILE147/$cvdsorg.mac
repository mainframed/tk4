         MACRO
&LBL     $CVDSORG &OUT,&IN
.*--------------------------------------------------------------------*
.*---    GENERAL-PURPOSE MACRO                                     ---*
.*---                                                              ---*
.*---    CONVERT DCBDSORG TO PRINTABLE FORMAT                      ---*
.*---                                                              ---*
.*---    IN= IS A TWO-BYTE FIELD CONTAINING THE DSORG FIELD OF     ---*
.*---    A DCB OR FORMAT-1 DSCB.                                   ---*
.*---                                                              ---*
.*---    OUT= IS AN OUTPUT FIELD THAT WILL CONTAIN THE CHARACTER   ---*
.*---    FORM OF THE DSORG VALUE IN THE INPUT FIELD. NO CHECK IS   ---*
.*---    MADE REGARDING THE LENGTH OF EITHER THE OUTPUT OR INPUT   ---*
.*---    FIELDS.  NOTE THAT THIS MACRO CAN GENERATE REENTRANT CODE ---*
.*---    AND IS NOT ADDRESSING-MODE DEPENDANT.                     ---*
.*--------------------------------------------------------------------*
         LCLC  &LL,&LE
&LL      SETC  '&LBL'
         AIF   ('&LL' EQ '').NOLBL
&LL      DS    0H
.NOLBL   ANOP
&LL      SETC  '$CDA&SYSNDX'
&LE      SETC  '$CDU&SYSNDX'
         LA    15,&OUT             POINT TO OUTPUT AREA
         MVC   0(3,15),=CL3' '     AND CLEAR IT OUT
         TM    &IN,X'80'           ISAM ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'IS'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDB&SYSNDX'
         TM    &IN,X'40'           PHYSICAL SEQUENTIAL ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'PS'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDC&SYSNDX'
         TM    &IN,X'20'           DIRECT ACCESS (BDAM) ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'DA'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDD&SYSNDX'
         TM    &IN,X'10'           BTAM/QTAM LINE GROUP ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'CX'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDE&SYSNDX'
         TM    &IN,X'02'           PARTITIONED ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'PO'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDF&SYSNDX'
         TM    1(R2),X'80'         GRAPHICS ORGANIZATION ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'GS'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDG&SYSNDX'
         TM    1(R2),X'40'         TCAM LINE GROUP ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'TX'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDH&SYSNDX'
         TM    1(R2),X'20'         TCAM MESSAGE QUEUE ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'TQ'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDI&SYSNDX'
         TM    1(R2),X'08'         VSAM ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'AM'      MOVE IN CHARACTER FORM
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
&LL      SETC  '$CDJ&SYSNDX'
         TM    &IN,X'04'           TCAM/3705 ??
         BZ    &LL                 NOPE ---
         MVC   0(2,15),=C'TR'      MOVE IN CHARACTER FORM
         LA    R6,2(,R6)
         B     &LE                 GO CHECK UNMOVABLE
&LL      DS    0H
         MVC   0(2,15),=C'XX'      MOVE IN CHARACTER FORM
&LE      DS    0H
&LL      SETC  '$CDK&SYSNDX'
         TM    &IN,X'01'           UNMOVEABLE DATASET ??
         BZ    &LL                 NOPE ---
         MVI   2(15),C'U'
&LL      DS    0H
         MEND
