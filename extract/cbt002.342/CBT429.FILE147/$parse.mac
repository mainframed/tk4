         MACRO
&LBL     $PARSE &TYPE,                                                 -
               &KEYWD=,                                                -
               &RSV=0,                                                 -
               &SUBLIST=0
         GBLC  &N1,&N2,&N3
         GBLB  &PB1,&PB2,&PB3
         GBLA  &POS,&KWD
         AIF   ('&TYPE' EQ 'DSECT').DSECT
         AIF   ('&TYPE' EQ 'DEFINE').DEF00
         AIF   ('&TYPE' EQ 'POS').POS00
         AIF   ('&TYPE' EQ 'KEYWORD').KEY00
         AIF   ('&TYPE' EQ 'END').END
         MNOTE 8,'TYPE=&TYPE IS INVALID -- IGNORED'
         MEXIT
.DEF00   ANOP
&N1      SETC  'PARS&SYSNDX'
&N2      SETC  'PBRS&SYSNDX'
&N3      SETC  'PCRS&SYSNDX'
         AIF   ('&LBL' NE '').DEF01
         MNOTE 8,'PARSE TABLE MUST HAVE A LABEL'
         MEXIT
.DEF01   ANOP
&LBL     DS    0H
&N1      DS    AL2                 NUMBER OF POSITIONAL PARMS
&PB1     SETB  1
&POS     SETA  0
&KWD     SETA  0
         SPACE 1
         MEXIT
.POS00   AIF   ('&KEYWD' EQ '').POS01
         MNOTE 8,'KEYWORD INVALID ON A POSITIONAL PARAMETER'
         MEXIT
.POS01   AIF   (&PB1).POS02
         MNOTE 8,'PARSE TABLE HEADER MISSING'
         MEXIT
.POS02   AIF   (NOT &PB2).POS03
         MNOTE 8,'POSITIONAL PARMS MUST PRECEDE ALL KEYWORD PARMS'
         MEXIT
.POS03   ANOP
&POS     SETA  &POS+1
&LBL     DC    AL2(0)
         DC    AL2(&RSV)
         DC    AL4(0,&SUBLIST)
         MEXIT
.KEY00   AIF   (&PB2).KEY01
&N2      DS    AL2                 COUNT OF KEYWORD PARMS
&PB2     SETB  1
.KEY01   ANOP
&KWD     SETA  &KWD+1
&LBL     DC    CL8&KEYWD
         DC    AL2(0,&RSV)
         DC    AL4(0,&SUBLIST)
         MEXIT
.END     ANOP
&PB1     SETB  0
&PB2     SETB  0
         ORG   &N1                 POSITIONAL OPERAND HEADER
         DC    AL2(&POS)           POSITIONAL OPERAND COUNT
         AIF   (&KWD EQ 0).NOKW
         ORG   &N2                 KEYWORD TABLE HEADER
         DC    AL2(&KWD)           KEYWORD OPERAND COUNT
         ORG
         MEXIT
.NOKW    ANOP
         ORG
         DC    AL2(&KWD)           KEYWORD OPERAND COUNT
         MEXIT
.DSECT   ANOP
         SPACE 3
*---------------------------------------------------------------------*
*---     RPARSE SUBROUTINE'S PARAMETER LIST AND RETURN AREA        ---*
*---------------------------------------------------------------------*
PRSTAB   DSECT
PRSPSCT  DS    H                   POSITIONAL OPERAND COUNT
*              THE FOLLOWING 4 FIELDS ARE REPEATED PRSPSCT TIMES
PRSPSLN  DS    H                   POSITIONAL OPERAND LENGTH
PRSPSRS  DS    H                   RESERVED
PRSPSLOC DS    AL4                 OPERAND LOCATION
PRSPSSL  DS    AL4                 SUBLIST PARSE LIST OR ZEROS
PRSPSBC  EQU   *-PRSPSLN           LENGTH OF EACH ENTRY
*---------------------------------------------------------------------*
PRSKWCT  DS    H                   KEYWORD OPERAND COUNT
*              THE FOLLOWING FIELDS ARE REPEATED PRSKWCT TIMES
PRSKWKW  DS    CL8                 KEYWORD IDENTIFIER
PRSKWLN  DS    H                   VALUE LENGTH
PRSKWRS  DS    H                   RESERVED
PRSKWLOC DS    AL4                 START OF KEYWORD VALUE
PRSKWSL  DS    AL4                 SUBLIST PARSE LIST, OR ZEROS
PRSKWBC  EQU   *-PRSKWKW           LENGTH OF EACH ENTRY
*---------------------------------------------------------------------*
*---     END OF RPARSE PARAMETER LIST DESCRIPTION                  ---*
*---------------------------------------------------------------------*
         SPACE 3
         MEND
