/* REXX Build a Copy Job                                     */
address "TSO";
"ALLOC F(INPUT) DA('SYSM.TECH.COMMON.CNTLLIB(BLDCDATA)') SHR";
"EXECIO * DISKR INPUT (FINIS STEM INPUT.";
"FREE F(INPUT)";
"NEWSTACK";
QUEUE "//COPYSYS JOB 5304010530000000,'M MCCARTY',NOTIFY=IBMUSER,";
QUEUE "// CLASS=S,MSGCLASS=G,REGION=8M";
QUEUE "//COPY EXEC PGM=IEBCOPY";
QUEUE "//SYSPRINT DD SYSOUT=*";
DO I = 1 TO INPUT.0 BY 2;
  PARSE VAR INPUT.I IDSN IVSER ODSN OVSER . ;
  J = I+1;
  PARSE VAR INPUT.J RECFM LRECL DATACLAS SPACE . ;
  QUEUE "//I"I "DD DSN="IDSN",DISP=SHR,UNIT=3390,VOL=SER="IVSER;
  QUEUE "//O"I "DD DSN="ODSN",DISP=(NEW,CATLG),VOL=SER="OVSER",";
  QUEUE "// DATACLAS="DATACLAS",STORCLAS=SCSTNRDG,";
  IF OVSER="SMSSM1" THEN MGMT="NOMIG";
     ELSE MGMT="STANDARD";
  IF RECFM="U" THEN DSNT="PDS";
     ELSE DSNT="LIBRARY";
  QUEUE "// MGMTCLAS="MGMT",DSNTYPE="DSNT",";
  QUEUE "// RECFM="RECFM",SPACE="SPACE",";
  IF RECFM="U" THEN QUEUE "// BLKSIZE=23476";
     ELSE QUEUE "// LRECL="LRECL;
END;
QUEUE "//SYSIN DD *";
DO I = 1 TO INPUT.0 BY 2;
  QUEUE " COPY I=I"I",O=O"I;
END;
QUEUE "//";
QUEUE "";
"ALLOC F(OUTPT) DA('SYSM.TECH.COMMON.CNTLLIB(JSYSCOPY)') SHR";
"EXECIO * DISKW OUTPT (FINIS";
"FREE F(OUTPT)";
EXIT 0;
