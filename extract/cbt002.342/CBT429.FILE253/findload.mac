/* REXX BLDL locate element */
address "TSO";
parse upper arg bldlmem ; /* element name */
if bldlmem = "" then do;
      say "Missing member name.";
      exit 4;
      end;
bldldir = xlibload(bldlmem); /* fetch dir entry */
if bldldir = ""  then do;
      say "Locate for member" bldlmem "failed.";
      exit 4;
      end;
concat# = c2d(substr(bldldir,12,1))+1; /* bldl K = concat # */
sysloc# = c2d(substr(bldldir,13,1)); /* bldl z = locate # */
select;
   when sysloc# = 0 then do;
      say "Load module" bldlmem "located in private library.";
      exit 0;
      end;
   when sysloc# = 1 then do;
      cvtptr = storage("00000010",4); /* locate llt from cvt */
      cvtllta = d2x(c2d(cvtptr)+1244); /* mapping ihallt */
      llta = c2d(storage(cvtllta,4));
      lltacount = c2d(storage(d2x(llta+4),4));
      lltaentry = d2x(llta+9); /* entry ptr */
      dsnlist = "";
      do i = 1 to lltacount;
           dsnlist = dsnlist strip(storage(lltaentry,44));
           lltaentry = d2x(x2d(lltaentry)+45);
      end;
      say "Load module" bldlmem "located in LINKLIST library" ,
             word(dsnlist,concat#);
      exit 0;
      end;
   when sysloc# = 2 then do;
/*    dsnlist = @listalc("STEPLIB"); allocdsn list per ddname
      if dsnlist = "" then do;
         say "Locate for member" bldlmem "failed.";
         exit 8;
         end; */
      say "Load module" bldlmem "located in STEPLIB library.";
      exit 0;
      end;
   otherwise do;
      say "Load module" bldlmem "located in parent task library.";
      exit 0;
      end;
end;
exit 0;
