PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAISHOW)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - SHOW COMMAND
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAISHOW - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAISESS ZTRAIL) SHARED
IF &STR(&ZTRAIL) NE THEN DO
    SET ZCMD=&STR(&ZTRAIL)
    ISPEXEC CONTROL NONDISPL ENTER
    END
/**
ISPEXEC DISPLAY PANEL(NUAISHOW) CURSOR(ZCMD)
SET CC=&LASTCC
/**
SET SKEL=NUAISHOW
IF &CC NE 0 AND &CC NE 8 THEN +
    WRITE %NUAISHOW - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
IF &CC NE 0 THEN SET SKEL=NUAIQUIT
ELSE DO
    ISPEXEC CONTROL NONDISPL END
    ISPEXEC CONTROL DISPLAY LINE START(4)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAISHOW - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
