PROC 0 DEBUG QUIET MENU SCRIPT() TRAN(BRACKET) NUAIOPT() ISPEXEC(YES) +
    LOAD(SYS1.NETUA.V2R8M8.LOADLIB) PGM(NUACLIEN)
/**
/**  REFER: NUA.V2R8M8.TEXT(NUAUSER)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   TSO/E CLIST TO EXECUTE THE CLIENT (INITIATOR)
/**
/**  NOTE:  THIS CLIST MUST BE COPIED INTO A CLIST LIBRARY
/**         ACCESSIBLE BY TSO/E USERS AT YOUR INSTALLATION
/**
CONTROL NOFLUSH NOPROMPT MSG END(ENDO) ASIS  /* MSG REQUIRED */
IF &DEBUG EQ DEBUG THEN CONTROL PROMPT LIST CONLIST
/**
IF &MENU EQ MENU THEN SET QUIET=QUIET
ELSE IF &SYSISPF EQ ACTIVE THEN ISPEXEC CONTROL DISPLAY LINE START(1)
IF &QUIET EQ THEN WRITE %NUAUSER  - USER-Access starting
IF &STR("&SUBSTR(1:1,&STR(&SCRIPT ))") EQ "'" THEN +
    SET SCRIPT=&STR('&SCRIPT')
/**
IF &SYSENV EQ FORE THEN DO
    ALLOC F(SYSIN)    DS(*) REUSE
    ALLOC F(SYSPRINT) DS(*) REUSE RECFM(V B A)
    ALLOC F(SYSERR)   DS(*) REUSE RECFM(V B A)
    /**
    IF &TRAN NE         THEN TERM NOTRAN NOCHAR
    IF &TRAN EQ BRACKET THEN TERM CHAR((X'41',X'AD'),(X'42',X'BD'))
    ENDO
/**
IF &ISPEXEC EQ YES AND &SYSISPF EQ ACTIVE THEN DO
    ISPEXEC CONTROL ERRORS RETURN
    ISPEXEC LIBDEF ISPLLIB EXCLDATA ID('&LOAD') UNCOND
    SET &CC=&LASTCC
    IF &CC EQ 0 THEN DO
        IF &MENU EQ MENU THEN DO
            ISPEXEC VPUT (NUAIOPT) SHARED
            ISPEXEC VGET (ZAPPLID) SHARED
            IF &ZAPPLID EQ NUAI THEN SET NEWAPPL=
            ELSE SET NEWAPPL=NEWAPPL(NUAI) PASSLIB
            ENDO
        ISPEXEC SELECT PGM(&PGM) PARM('&SCRIPT') &NEWAPPL
        SET &CC=&LASTCC
        ENDO
    ISPEXEC LIBDEF ISPLLIB
    ENDO
ELSE DO
    CALL '&LOAD(&PGM)' '&SCRIPT'
    SET CC=&LASTCC
    ENDO
/**
IF &SYSENV EQ FORE AND &TRAN NE THEN TERM NOTRAN NOCHAR
/**
IF &QUIET EQ THEN WRITE %NUAUSER  - USER-Access complete - Code(&CC)
IF &DATATYPE(&CC) NE NUM THEN EXIT CODE(12)
EXIT CODE(&CC)
