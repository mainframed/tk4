PROC 0 DEBUG OPT()
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIMENU)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - MAIN MENU
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIMENU - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
IF &STR(&OPT) EQ INITIAL THEN DO
    %NUAIINIT
    IF &LASTCC NE 0 THEN DO
        WRITE %NUAIMENU - DATASET INITIALIZATION FAILED
        EXIT QUIT CODE(12)
        END
    ELSE DO
        ISPEXEC VGET (NUAIOPT) SHARED
        SET OPT=&NUAIOPT
        SET NUAIOPT=
        ISPEXEC VPUT (NUAIOPT) SHARED
        END
    END
IF &STR(&OPT) NE THEN SET OPT=OPT(&OPT)
ELSE DO
    ISPEXEC FTOPEN
    ISPEXEC FTINCL NUAIQUIT
    ISPEXEC FTCLOSE
    END
/**
ISPEXEC SELECT PANEL(NUAIMENU) &OPT
SET CC=&LASTCC
IF &DATATYPE(&CC) NE NUM OR &CC GT 4 THEN +
    WRITE %NUAIMENU - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
/**
IF &CC EQ 4 THEN DO
    ERROR DO
        ERROR DO
            ERROR OFF
            WRITE %NUAIMENU - FILE TAILORING FAILED
            EXIT QUIT CODE(12)
            END
        %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
        GOTO FT
        END
FT: +
    ISPEXEC FTOPEN
    ISPEXEC FTINCL NUAIQUIT
    ISPEXEC FTCLOSE
    ERROR OFF
    END
EXIT CODE(0)
