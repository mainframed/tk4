PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIEDTE)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - REMOTE FILE EDIT ENTRY
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIEDTE - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAISOUR NUAISWCD NUAIHOST NUAIUSER NUAICOPT) PROFILE
ISPEXEC VGET (NUAISESS NUAIUNIT) SHARED
IF &NUAIUNIT EQ THEN SET UNIT=
ELSE SET UNIT=UNIT(&NUAIUNIT)
SET MSG=
SET CURSOR=CURSOR(ZCMD)
IF &STR(&NUAISWCD) EQ THEN SET CURSOR=CURSOR(NUAISWCD)
IF &STR(&NUAISOUR) EQ THEN SET CURSOR=CURSOR(NUAISOUR)
SET NEXTOPT=E
/**
PANEL: ISPEXEC DISPLAY PANEL(NUAIEDTE) &MSG &CURSOR
SET CC=&LASTCC
SET MSG=
SET CURSOR=CURSOR(ZCMD)
IF &CC NE 0 AND &CC NE 8 THEN +
    WRITE %NUAIEDTE - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
/**
SET SKEL=NUAIEDTE
IF &NUAICMD NE EDIT THEN SET SKEL=NUAIUCMD
IF &CC NE 0 THEN SET SKEL=NUAIQUIT
ELSE DO
    ISPEXEC VPUT (NUAISOUR NUAISWCD NUAIHOST NUAIUSER NUAICOPT) PROFILE
    ALLOC DD(NUAIEDT&NUAISESS) NEW DELETE SPACE(10 30) BLOCK(4096) +
        DSORG(PS) RECFM(V B) LRECL(255) BLKSIZE(4096) REUSE &UNIT
    IF &LASTCC NE 0 THEN DO
        SETMSG=MSG(NUAI019)
        GOTO PANEL
        END
    IF &STR(&NUAIPSWD) EQ THEN SET NUAIPSWT=
    ELSE SET NUAIPSWT=XXXXXXXX
    ISPEXEC CONTROL NONDISPL END
    ISPEXEC CONTROL DISPLAY LINE START(12)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAIEDTE - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
