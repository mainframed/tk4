PROC 0 DEBUG  DATEFMT(USA)                          +
              CTMSJOB()   /* DEFAULT FOR "JOB NAME" IS ' '         */
 /* WM1286 AM 220992 SUPPORT JAPANESE DATE */

ISPEXEC CONTROL ERRORS RETURN
CONTROL MAIN NOLIST NOCONLIST NOSYMLIST NOMSG
IF &DEBUG=DEBUG THEN CONTROL MAIN LIST CONLIST SYMLIST MSG

  /*****************************/
  /*   READ FROM PROFILE       */
  /*****************************/
ISPEXEC VGET (CTMSLIB,CTMCAL) PROFILE
IF &STR(&CTMSLIB) =          +
THEN +
  SET CTMSLIB=&STR(SYSS.CTMO.V410.SCHEDULE)
IF &STR(&CTMCAL) =          +
THEN +
  SET CTMCAL=&STR(SYSS.IOA.V410.CAL)
ISPEXEC VGET (ZDAY,ZMONTH,ZYEAR)
SET YY=&ZYEAR
SET MM=&ZMONTH
SET DD=&ZDAY
SET &NOALC=&STR()
SET MM=&SUBSTR(2:3,&EVAL(&MM+100))
SET DD=&SUBSTR(2:3,&EVAL(&DD+100))
SET CTMSMEM=
SET CTMSJOB=
REDISP:+
IF &STR(&CTMSJOB)='1'  THEN SET CTMSJOB=&STR(*)
SET CTMGRP=
SET FRC=NO
IF &DATEFMT=JPN THEN +
            ISPEXEC DISPLAY PANEL(CTMPDALJ)
            ELSE +
IF &DATEFMT=USA THEN +
            ISPEXEC DISPLAY PANEL(CTMPDALA)
            ELSE ISPEXEC DISPLAY PANEL(CTMPDALW)
IF &RESP = END THEN DO
                      ISPEXEC VPUT (CTMSLIB,CTMCAL) PROFILE
                      EXIT
                    END

SET ZYEAR=&YY
SET ZMONTH=&MM
SET ZDAY=&DD
IF &ZDAY GT 1 THEN DO
             SET DD=&ZDAY-1
             SET MM=&ZMONTH
             END
      ELSE DO
          IF &ZMONTH GT 1 THEN  SET MM=&ZMONTH-1
                 ELSE DO
                    SET MM=12
                    SET YY=&ZYEAR-1
                    END
          SET DD=31
          IF &MM=4 OR &MM=6 OR &MM=9 OR &MM=11 THEN SET DD=30
          IF &MM=2  THEN SET DD=28
          IF &MM=2 AND (&YY=88 OR &YY=92 OR &YY=96) THEN SET DD=29
          END
SET MM=&SUBSTR(2:3,&EVAL(&MM+100))
SET DD=&SUBSTR(2:3,&EVAL(&DD+100))
SET ZDAY=&SUBSTR(2:3,&EVAL(&ZDAY+100))
SET ZMONTH=&SUBSTR(2:3,&EVAL(&ZMONTH+100))
IF &STR('&CTMSJOB')=&STR('*')  THEN SET CTMSJOB='1'
IF &STR('&CTMGRP') = ''   THEN SET CTMGRP='1'
IF &DATEFMT=JPN THEN +
     %CTMCDAIL  CTMSLIB(&CTMSLIB) CTMCAL(&CTMCAL) CTMSMEM(&CTMSMEM) +
           CTMSJOB(&CTMSJOB)   +
           CTMGRP(&CTMGRP)     +
           FORCE(&FRC)         +
           DBPREFA(SYSS.IOA) DBVERA(V410) +
           DBPREFM(SYSS.CTMO) DBVERM(V410) +
           &DEBUG DATE1(&YY&MM&DD) DATE2(&ZYEAR&ZMONTH&ZDAY) &NOALC
   ELSE    +
IF &DATEFMT=USA THEN +
     %CTMCDAIL  CTMSLIB(&CTMSLIB) CTMCAL(&CTMCAL) CTMSMEM(&CTMSMEM) +
           CTMSJOB(&CTMSJOB)   +
           CTMGRP(&CTMGRP)     +
           FORCE(&FRC)         +
           DBPREFA(SYSS.IOA) DBVERA(V410) +
           DBPREFM(SYSS.CTMO) DBVERM(V410) +
           &DEBUG DATE1(&MM&DD&YY) DATE2(&ZMONTH&ZDAY&ZYEAR) &NOALC
   ELSE    +
      %CTMCDAIL  CTMSLIB(&CTMSLIB) CTMCAL(&CTMCAL) CTMSMEM(&CTMSMEM) +
           CTMSJOB(&CTMSJOB)   +
           CTMGRP(&CTMGRP)     +
           FORCE(&FRC)         +
           DBPREFA(SYSS.IOA) DBVERA(V410) +
           DBPREFM(SYSS.CTMO) DBVERM(V410) +
           &DEBUG DATE1(&DD&MM&YY) DATE2(&ZDAY&ZMONTH&ZYEAR) &NOALC
SET &NOALC=NOALC
SET YY=&ZYEAR
SET DD=&SUBSTR(2:3,&EVAL(&ZDAY+100))
SET MM=&SUBSTR(2:3,&EVAL(&ZMONTH+100))
GOTO REDISP
END
