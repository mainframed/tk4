PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIBROW)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - ISPF/PDF BROWSE OF A REMOTE FILE
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIBROW - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAISESS) SHARED
ISPEXEC LMINIT DATAID(NUAIBROW) DDNAME(NUAIBRW&NUAISESS)
ISPEXEC BROWSE DATAID(&NUAIBROW) PANEL(NUAIBROW)
SET CC=&LASTCC
/**
SET SKEL=NUAIBROW
IF &CC GT 12 THEN DO
    WRITE %NUAIBROW - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
    SET SKEL=NUAIQUIT
    END
ELSE DO
    ISPEXEC LMFREE DATAID(&NUAIBROW)
    FREE DD(NUAIBRW&NUAISESS)
    ISPEXEC CONTROL NONDISPL END
    IF &CC EQ 12 THEN ISPEXEC SETMSG MSG(NUAI008)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAIBROW - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
