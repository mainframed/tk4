PROC 0 DEBUG()
CONTROL MAIN NOLIST NOCONLIST NOSYMLIST NOMSG
IF &DEBUG=DEBUG THEN CONTROL MAIN LIST CONLIST SYMLIST MSG
ISPEXEC CONTROL ERRORS RETURN

/********************************************************/
/**                                                    **/
/**    CONTROL-M QUICK SCHEDULE DEFINITION             **/
/**                                                    **/
/**    CLIST CTMCK15  - READ SKELETON TABLE            **/
/**                                                    **/
/********************************************************/

ISPEXEC VGET (LIB SCHEDTB GRP  SGRP SKELMEM)

/********************************************************/
/**    READ IN THE MODEL TABLE AND SET UP              **/
/********************************************************/

ISPEXEC LMFREE DATAID(&CTMPKIN)  /* IN CASE SOMETHING THERE */

SET &IOERROR=Y
SET &SKELVALD=N
SET &CTMPKIN=&NUL
ISPEXEC VPUT (IOERROR CTMPKIN SKELVALD)

ISPEXEC LMINIT  DATAID(CTMPKIN) DATASET('&LIB') ENQ(SHR)
SET &RC = &LASTCC
IF &RC ^= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMK003Z)
      EXIT QUIT
   END

ISPEXEC VPUT (CTMPKIN)

ISPEXEC LMOPEN  DATAID(&CTMPKIN) OPTION(INPUT)
SET &RC = &LASTCC
IF &RC ^= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMK003Z)
      EXIT QUIT
   END

ISPEXEC LMMFIND DATAID(&CTMPKIN) MEMBER(&SKELMEM)
SET &RC = &LASTCC
IF &RC ^= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMK003Z)
      EXIT QUIT
   END

/********************************************************/
/**    READ IN CARDS FROM MODEL                        **/
/********************************************************/

SET &MODEND = 0
SET &NSHOUT = 0
SET &NECARD = 0
SET &NQCARD = 0
SET &NRCARD = 0
SET &NPCARD = 0
SET &NCCARD = 0
SET &NTCARD = 0
SET &NBCARD = 0
SET &NDCARD = 0
SET &NMCARD = 0
SET &NZCARD = 0
SET &NHCARD = 0
SET &NLCARD = 0
SET &NVCARD = 0
SET &NWCARD = 0

SET &CARDV=V    /*  IN CASE SKELETON DOESNT HAVE ANY */

DO WHILE &MODEND = 0

ISPEXEC LMGET   DATAID(&CTMPKIN) MODE(INVAR) DATALEN(MODLEN) +
                   DATALOC(MODCARD) MAXLEN(80)
SET &RC = &LASTCC
IF &RC ^= 0 THEN +
  IF &RC = 8 THEN SET &MODEND = 1
     ELSE +
   DO
      ISPEXEC SETMSG MSG(CTMK003Z)
      EXIT QUIT
   END

IF &MODEND = 0 THEN +
   DO

     SET &C = &SUBSTR(1,&NRSTR(&MODCARD))

     IF (&NRSTR(&C)^=B) THEN +
       IF (&NRSTR(&C)^=C) THEN +
       IF (&NRSTR(&C)^=D) THEN +
       IF (&NRSTR(&C)^=E) THEN +
       IF (&NRSTR(&C)^=H) THEN +
       IF (&NRSTR(&C)^=I) THEN +
       IF (&NRSTR(&C)^=L) THEN +
       IF (&NRSTR(&C)^=M) THEN +
       IF (&NRSTR(&C)^=O) THEN +
       IF (&NRSTR(&C)^=P) THEN +
       IF (&NRSTR(&C)^=Q) THEN +
       IF (&NRSTR(&C)^=R) THEN +
       IF (&NRSTR(&C)^=S) THEN +
       IF (&NRSTR(&C)^=T) THEN +
       IF (&NRSTR(&C)^=V) THEN +
       IF (&NRSTR(&C)^=W) THEN +
       IF (&NRSTR(&C)^=Z) THEN +
         DO
             ISPEXEC SETMSG MSG(CTMK001S)
             EXIT QUIT
         END

     IF &C = E THEN +
       DO
         SET &NECARD = &NECARD + 1
         SET &CARDE&NECARD = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDE&NECARD)
       END

     IF &C = Q THEN +
       DO
         SET &NQCARD = &NQCARD + 1
         SET &CARDQ&NQCARD = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDQ&NQCARD)
       END

     IF &C = R THEN +
       DO
         SET &NRCARD = &NRCARD + 1
         SET &CARDR&NRCARD = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDR&NRCARD)
       END

     IF &C = P THEN +
       DO
         SET &NPCARD = &NPCARD + 1
         SET &CARDP&NPCARD = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDP&NPCARD)
       END

     IF &C = C THEN +
       DO
         SET &NCCARD = &NCCARD + 1
         SET &CARDC&NCCARD = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDC&NCCARD)
       END

     IF &C = T THEN +
       DO
         SET &NTCARD = &NTCARD + 1
         SET &CARDT&NTCARD = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDT&NTCARD)
       END

     IF &C = B THEN +
       DO
         SET &NBCARD = &NBCARD + 1
         SET &CARDB&NBCARD = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDB&NBCARD)
       END

     IF &C = S THEN +
       DO
         SET &NSHOUT = &NSHOUT + 1
         SET &CARDS&NSHOUT = &NRSTR(&MODCARD)
         ISPEXEC VPUT (CARDS&NSHOUT)
       END

     IF &C = D THEN +
       DO
         SET &NDCARD = &NDCARD + 1
         SET &TEMPU = &SUBSTR(1:8,&SYSUID&STR(        ))
         SET &CTMCKSV=MOVEUID
         ISPEXEC VPUT (MODCARD TEMPU CTMCKSV)
         CTMTQSR     /* MOVE THE USER ID TO THE CARD */
         SET &RC = &LASTCC
         IF &RC>0 THEN +
           DO
              SET &ZERRLM = ERROR EXECUTING PROGRAM CTMTQSR
              ISPEXEC SETMSG MSG(CTMK003Z)
              EXIT QUIT
           END
         ISPEXEC VGET (CARDD)
       END

     IF &C = W THEN +
       DO
         SET &NWCARD = &NWCARD + 1
         SET &CTMCKSV=MOVEWCD
         ISPEXEC VPUT (MODCARD CTMCKSV)
         CTMTQSR     /* MOVE MODCARD TO CARDW */
         ISPEXEC VGET (CARDW)
       END

     IF &C = M THEN +
       DO
         SET &NMCARD = &NMCARD + 1
         SET &CARDM = &NRSTR(&MODCARD)
       END

     IF &C = L THEN +
       DO
         SET &NLCARD = &NLCARD + 1
         SET &CARDL = &NRSTR(&MODCARD)
       END

     IF &C = V THEN +
       DO
         SET &NVCARD = &NVCARD + 1
         SET &CARDV = &NRSTR(&MODCARD)
       END

     IF &C = H THEN +
       DO
         SET &NHCARD = &NHCARD + 1
         SET &CARDH = &NRSTR(&MODCARD)
       END

     IF &C = Z THEN +
       DO
         SET &NZCARD = &NZCARD + 1
         SET &CARDZ = &NRSTR(&MODCARD)
       END

     IF &C = O THEN SET &CARDO = &NRSTR(&MODCARD)

   END
END

IF ( (&NDCARD^=1) OR (&NMCARD^=1) OR (&NZCARD^=1) OR (&NVCARD>1) OR +
     (&NHCARD^=1) OR (&NLCARD^=1) OR (&NWCARD >1) ) THEN +
       DO
             ISPEXEC SETMSG MSG(CTMK001S)
             EXIT QUIT
       END

ISPEXEC VPUT (CARDD CARDM CARDL CARDH CARDO CARDZ CARDV)
ISPEXEC VPUT -
    (NECARD NQCARD NRCARD NPCARD NCCARD NTCARD NBCARD NSHOUT)
ISPEXEC VPUT (CARDS CARDS2)
ISPEXEC VPUT (NWCARD CARDW)


ISPEXEC LMCLOSE DATAID(&CTMPKIN)
SET &RC = &LASTCC
IF &RC ^= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMK003Z)
      EXIT QUIT
   END

ISPEXEC LMFREE  DATAID(&CTMPKIN)
SET &RC = &LASTCC
IF &RC ^= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMK003Z)
      EXIT QUIT
   END

SET &IOERROR=N
SET &SKELVALD=Y
ISPEXEC VPUT (IOERROR SKELVALD)

EXIT
