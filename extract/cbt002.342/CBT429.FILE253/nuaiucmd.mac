PROC 0 DEBUG CMD() NEXTOPT(C)
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIUCMD)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - EXECUTE USER-ACCESS COMMAND
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST SYMLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIUCMD - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAISESS) SHARED
IF &STR(&CMD) EQ THEN ISPEXEC VGET NUAIUCMD PROFILE
ELSE DO
    CONTROL ASIS
    SET NUAIUCMD=&STR(&CMD)
    CONTROL CAPS
    ISPEXEC CONTROL NONDISPL ENTER
    END
SET MSG=
SET CURSOR=CURSOR(ZCMD)
/**
ISPEXEC DISPLAY PANEL(NUAIUCMD) &MSG &CURSOR
SET CC=&LASTCC
SET MSG=
SET CURSOR=CURSOR(ZCMD)
/**
SET SKEL=NUAIUCMD
IF &CC NE 0 AND &CC NE 8 THEN +
    WRITE %NUAIUCMD - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
IF &CC NE 0 THEN SET SKEL=NUAIQUIT
ELSE DO
    ISPEXEC VPUT NUAIUCMD PROFILE
    ISPEXEC CONTROL NONDISPL END
    IF &STR(&CMD) EQ THEN ISPEXEC CONTROL DISPLAY LINE START(7)
    ELSE ISPEXEC CONTROL DISPLAY LINE START(4)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAIUCMD - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
