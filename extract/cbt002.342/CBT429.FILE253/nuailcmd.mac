PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAILCMD)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - EXECUTE LOCAL HOST COMMAND
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAILCMD - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAILCMD) PROFILE
ISPEXEC VGET (NUAISESS) SHARED
SET MSG=
SET CURSOR=CURSOR(ZCMD)
/**
ISPEXEC DISPLAY PANEL(NUAILCMD) &MSG &CURSOR
SET CC=&LASTCC
SET MSG=
SET CURSOR=CURSOR(ZCMD)
/**
SET SKEL=NUAILCMD
IF &CC NE 0 AND &CC NE 8 THEN +
    WRITE %NUAILCMD - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
IF &CC NE 0 THEN SET SKEL=NUAIQUIT
ELSE DO
    ISPEXEC VPUT NUAILCMD PROFILE
    ISPEXEC CONTROL NONDISPL END
    ISPEXEC CONTROL DISPLAY LINE START(7)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAILCMD - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
