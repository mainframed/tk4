/*  REXX EXEC : ( STKVOL ).
    FUNCTION: READS THE SORTED VOLUME REPORT LISTING DATASET SORTED BY
              VOLUME AND CHECKS FOR DUPLICATE TAPE VOLSERS.
    INPUT   : DSN = ( SYSS.STKHSC.VOLREPRT.LISTING ).
    OUTPUT  : TERMINAL DISPLAY OF ANY DUPLICATE VOLSERS.
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
PARSE UPPER ARG REPORT .
IF REPORT = '' THEN REPORT = 'SYSS.STKHSC.VOLREPRT.LISTING'
X = MSG("OFF")
"FREE FILE(STKDD)"
DUPCNT = 0
SVOLUME = 'XXXXXX'
CALL READ_VOLUME_REPORT

/*     */
MAIN_ROUTINE:
DO I = 1 TO DATA.0
   VOLKEY = WORD(DATA.I,3)
   IF VOLKEY ^= 'Y' THEN ITERATE
   RVOLUME = SUBSTR(DATA.I,3,6)
   IF RVOLUME = SVOLUME THEN DO
      DUPCNT = DUPCNT + 1
      SAY '*** DUPLICATE TAPE VOLUME FOUND: ('RVOLUME'). ***'
   END
   SVOLUME = RVOLUME
END
SAY '+++ DUPLICATE TAPE VOLUME COUNT: ('DUPCNT'). +++'
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
READ_VOLUME_REPORT:
"ALLOC DA('"REPORT"') FILE(STKDD) SHR"
SRC = RC
IF SRC ^= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ( 'REPORT' ). ***'
   SAY '*** DATASET IN USE OR UNAVAILABLE - RETURN CODE: (' SRC '). ***'
   SAY '*** STKVOL EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
"EXECIO * DISKR STKDD (FINIS STEM DATA."
SRC = RC
"FREE FILE(STKDD)"
IF SRC ^= 0 THEN DO
   SAY '*** DATASET EXECIO ERROR: ( 'REPORT' ). ***'
   SAY '*** EXECIO DATASET READ RETURN CODE: (' SRC '). ***'
   SAY '*** STKVOL EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
RETURN
