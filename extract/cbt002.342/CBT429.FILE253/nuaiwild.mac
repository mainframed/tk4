PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIWILD)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - WILDCARD FILE TRANSFER
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIWILD - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAIDPAT NUAIDEST NUAIMODE NUAICREA NUAITOPT +
    NUAIRMIG NUAISWCD NUAIHOST NUAIUSER NUAICOPT) PROFILE
ISPEXEC VGET (NUAISESS) SHARED
SET MSG=
SET CURSOR=CURSOR(ZCMD)
IF &STR(&NUAISWCD) EQ THEN SET CURSOR=CURSOR(NUAISWCD)
IF &STR(&NUAIDPAT) EQ THEN SET CURSOR=CURSOR(NUAIDPAT)
SET NEXTOPT=W
/**
PANEL: ISPEXEC DISPLAY PANEL(NUAIWILD) &MSG &CURSOR
SET CC=&LASTCC
SET MSG=
SET CURSOR=CURSOR(ZCMD)
IF &CC NE 0 AND &CC NE 8 THEN +
    WRITE %NUAIWILD - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
/**
SET SKEL=NUAIWILD
IF &NUAICMD NE SEND THEN SET SKEL=NUAIUCMD
IF &CC NE 0 THEN SET SKEL=NUAIQUIT
IF &SKEL EQ NUAIWILD THEN DO
    IF &STR(&NUAIPSWD) EQ THEN SET NUAIPSWT=
    ELSE SET NUAIPSWT=XXXXXXXX
    ISPEXEC VPUT (NUAIDPAT NUAIDEST NUAIMODE NUAICREA NUAITOPT +
        NUAIRMIG NUAISWCD NUAIHOST NUAIUSER NUAICOPT) PROFILE
    ISPEXEC TBCREATE NUAIWILD NAMES(NUAIDSN) NOWRITE REPLACE
    IF &LASTCC GT 4 THEN DO
        SET MSG=MSG(NUAI011)
        GOTO PANEL
        END
    IF &NUAIRMIG EQ YES THEN SET RECALL = RECALL
    ELSE SET RECALL =
    ISPEXEC SELECT PGM(NUAIWLD) PARM('&STR(&NUAIDPAT) &RECALL')
    SET CC=&LASTCC
    IF &CC NE 0 THEN DO
        SET MSG=MSG(NUAI012)
        IF &CC EQ 100 THEN SET MSG=MSG(NUAI015) /* NO PATTERN */
        IF &CC EQ 101 THEN SET MSG=MSG(NUAI014) /* NO MATCH */
        IF &CC EQ 102 THEN SET MSG=MSG(NUAI015) /* BAD PATTERN */
        IF &CC EQ 103 THEN SET MSG=MSG(NUAI016) /* BUFFER TOO SMALL */
        IF &CC EQ 104 THEN SET MSG=MSG(NUAI017) /* SEARCH FAILED */
        GOTO PANEL
        END
    ISPEXEC TBTOP NUAIWILD
    IF &LASTCC NE 0 THEN DO
        SET MSG=MSG(NUAI012)
        GOTO PANEL
        END
    ISPEXEC TBDISPL NUAIWILD PANEL(NUAIWLST)
    IF &LASTCC NE 0 THEN DO
        SET MSG=MSG(NUAI013)
        GOTO PANEL
        END
    END
IF &SKEL NE NUAIQUIT THEN DO
    ISPEXEC VPUT (NUAIDPAT NUAIDEST NUAIMODE NUAICREA NUAITOPT +
        NUAIRMIG NUAISWCD NUAIHOST NUAIUSER NUAICOPT) PROFILE
    ISPEXEC CONTROL NONDISPL END
    ISPEXEC CONTROL DISPLAY LINE START(4)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAIWILD - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
ISPEXEC TBCLOSE NUAIWILD
EXIT CODE(0)
