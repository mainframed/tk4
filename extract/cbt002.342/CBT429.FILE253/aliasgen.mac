/*  REXX EXEC ( ALIASGEN ).
    FUNCTION: BUILD PDS MEMBERS CONTAINING THE ALIAS/RELATES FOR EACH
              ASSOCIATION TO A CONNECTED ICF USER CATALOG.
    ZEKE JOB: SYSS.ZEKE.JCLLIB(PTCAT01D).
    INPUT   : IDCAMS LSTALIAS SYSOUT DATASET ( SYSS.LSTALIAS.SYSPRINT ).
    OUTPUT  : PDS WITH ALIAS/RELATE MEMBERS ( SYSS.LSTALIAS.CNTLLIB ).
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
C = 0
UCNT = 0
/*     */
READ_LSTALIAS_OUTPUT:
"ALLOC DA('SYSS.LSTALIAS.SYSPRINT') F(ISPFILE) OLD"
SRC = RC
IF SRC ^= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ( SYSS.LSTALIAS.SYSPRINT ). ***'
   SAY '*** DATASET IN USE OR UNAVAILABLE - RETURN CODE: (' SRC '). ***'
   SAY '*** ALIASGEN EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
"EXECIO * DISKR ISPFILE (FINIS STEM DATA."
SRC = RC
"FREE F(ISPFILE)"
IF SRC ^= 0 THEN DO
   SAY '*** DATASET EXECIO ERROR: ( SYSS.LSTALIAS.SYSPRINT ). ***'
   SAY '*** EXECIO DATASET READ RETURN CODE: (' SRC '). ***'
   SAY '*** ALIASGEN EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
/*     */
BUILD_USERCAT_TABLE:
DO I = 1 TO DATA.0
   UCATKEY = POS('USERCAT--',DATA.I)
   IF UCATKEY ^= 0 THEN DO
      U = UCATKEY + 9
      UCAT = SUBSTR(DATA.I,U)
      USER_CATALOG = STRIP(UCAT)
      CALL CHECK_USERCAT_TABLE
   END
END
/*     */
GET_ALIAS_NAMES:
UCATCNT = C
C = 0
DO UCATCNT
   C = C + 1
   IF C > 1 THEN DO
      "EXECIO 0 DISKW ISPFILE (FINIS"
      "FREE F(ISPFILE)"
   END
   ALLOCSW = 'YES'
   DO I = 1 TO DATA.0
      ALIASKEY = POS('ALIAS ---------',DATA.I)
      IF ALIASKEY ^= 0 THEN ALIAS = SUBWORD(DATA.I,3,1)
      DATAKEY = POS('NONVSAM--',DATA.I)
      IF DATAKEY ^= 0 THEN ITERATE
      UCATKEY = POS('USERCAT--',DATA.I)
      IF UCATKEY ^= 0 THEN DO
         U = UCATKEY + 9
         UCAT = SUBSTR(DATA.I,U)
         USER_CATALOG = STRIP(UCAT)
         IF USER_CATALOG = USERCAT.C THEN DO
            PDSMEMB = 'USRCAT'××C
            IF ALLOCSW = 'YES' THEN CALL ALLOC_CNTLLIB
            RECORD.3 = "         RELATE("USER_CATALOG"))"
            CALL WRITE_PDSMEMB
         END
      END
   END
END
"EXECIO 0 DISKW ISPFILE (FINIS"
"FREE F(ISPFILE)"
/*     */
GET_ALIAS_DSN_RELATES:
PDSMEMB = 'RELATDSN'
CALL ALLOC_CNTLLIB
DO I = 1 TO DATA.0
   ALIASKEY = POS('ALIAS ---------',DATA.I)
   IF ALIASKEY ^= 0 THEN ALIAS = SUBWORD(DATA.I,3,1)
   UCATKEY = POS('USERCAT--',DATA.I)
   IF UCATKEY ^= 0 THEN ITERATE
   DATAKEY = POS('NONVSAM--',DATA.I)
   IF DATAKEY ^= 0 THEN DO
      U = DATAKEY + 9
      RDSN = SUBSTR(DATA.I,U)
      RELATE_DSN = STRIP(RDSN)
      RECORD.3 = "         RELATE("RELATE_DSN"))"
      CALL WRITE_PDSMEMB
   END
END
"EXECIO 0 DISKW ISPFILE (FINIS"
"FREE F(ISPFILE)"
EXIT 0
/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
CHECK_USERCAT_TABLE:
CATSW = 'NO'
SAVCNT = C
C = 0
IF SAVCNT = 0 THEN DO
   C = C + 1
   USERCAT.C = USER_CATALOG
   RETURN
END
DO SAVCNT
   C = C + 1
   IF USER_CATALOG = USERCAT.C THEN CATSW = 'YES'
END
IF CATSW = 'YES' THEN RETURN
C = C + 1
USERCAT.C = USER_CATALOG
RETURN
/*     */
ALLOC_CNTLLIB:
ALLOCSW = 'NO'
"ALLOC DA('SYSS.LSTALIAS.CNTLLIB("PDSMEMB")') F(ISPFILE) SHR"
SRC = RC
IF SRC ^= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ( SYSS.LSTALIAS.CNTLLIB ). ***'
   SAY '*** DATASET IN USE OR UNAVAILABLE - RETURN CODE: (' SRC '). ***'
   SAY '*** ALIASGEN EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
RETURN
/*     */
WRITE_PDSMEMB:
R = 0
"DELSTACK"
RECORD.1 = " DEFINE ALIAS -"
RECORD.2 = "         (NAME("ALIAS") -"
DO 3
   R = R + 1
   QUEUE RECORD.R
END
"EXECIO 3 DISKW ISPFILE"
SRC = RC
IF SRC ^= 0 THEN DO
   SAY '*** PDS WRITE ERROR: SYSS.LSTALIAS.CNTLLIB('PDSMEMB'). ***'
   SAY '*** EXECIO WRITE RETURN CODE: (' SRC '). ***'
   SAY '*** ALIASGEN EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
RETURN
