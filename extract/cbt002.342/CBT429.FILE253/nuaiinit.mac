PROC 0 DEBUG ISPF HOST OUT
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIINIT)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - WORK DATA SET INITIALIZATION
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIINIT - ISPF NOT AVAILABLE
    GOTO INITFAIL
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (ZSCREEN) SHARED
SET NUAISESS=&ZSCREEN
ISPEXEC VPUT (NUAISESS) SHARED /* UNIQUE FILE SUFFIX FOR SPLIT SCREEN */
ISPEXEC VGET (NUAIUNIT) SHARED /* DASD WORK FILE UNIT TYPE */
IF &NUAIUNIT EQ THEN SET UNIT=
ELSE SET UNIT=UNIT(&NUAIUNIT)
/**
IF &OUT EQ OUT OR &ISPF&OUT&HOST EQ THEN DO
    ALLOC DD(NUAIOUT&NUAISESS) NEW DELETE SPACE(10 30) BLOCK(4096) +
        DSORG(PS) RECFM(V B) LRECL(255) BLKSIZE(4096) REUSE &UNIT
    IF &LASTCC NE 0 THEN DO
        WRITE %NUAIINIT - UNABLE TO ALLOCATE COMMAND OUTPUT DATASET
        GOTO INITFAIL
        END
    ISPEXEC LMINIT DATAID(NUAIOUT) DDNAME(NUAIOUT&NUAISESS)
    IF &LASTCC NE 0 THEN DO
        WRITE %NUAIINIT - LMINIT FAILED FOR COMMAND OUTPUT DATASET
        GOTO INITFAIL
        END
    ISPEXEC VPUT (NUAIOUT) SHARED
    END
/**
IF &HOST EQ HOST OR &ISPF&OUT&HOST EQ THEN DO
    ALLOC DD(NUAIHST&NUAISESS) NEW DELETE SPACE(10 30) BLOCK(4096) +
        DSORG(PS) RECFM(V B) LRECL(255) BLKSIZE(4096) REUSE &UNIT
    IF &LASTCC NE 0 THEN DO
        WRITE %NUAIINIT - UNABLE TO ALLOCATE SHOW HOST OUTPUT DATASET
        GOTO INITFAIL
        END
    ERROR DO
        ERROR OFF
        WRITE %NUAIINIT - UNABLE TO INITIALIZE HOST OUTPUT DATASET
        GOTO INITFAIL
        END
    OPENFILE NUAIHST&NUAISESS OUTPUT
    CONTROL ASIS
    SET NUAIHST&NUAISESS=Enter SHOW command to obtain current +
        host information
    CONTROL CAPS
    PUTFILE NUAIHST&NUAISESS
    CLOSFILE NUAIHST&NUAISESS
    ERROR OFF
    ISPEXEC LMINIT DATAID(NUAIHOUT) DDNAME(NUAIHST&NUAISESS)
    IF &LASTCC NE 0 THEN DO
        WRITE %NUAIINIT - LMINIT FAILED FOR SHOW HOST OUTPUT DATASET
        GOTO INITFAIL
        END
    ISPEXEC VPUT (NUAIHOUT) SHARED
    END
/**
IF &ISPF EQ ISPF OR &ISPF&OUT&HOST EQ THEN DO
    ISPEXEC FTCLOSE /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    ALLOC DD(ISPFILE) NEW DELETE SPACE(10 30) BLOCK(4096) +
        DSORG(PS) RECFM(V B) LRECL(255) BLKSIZE(4096) REUSE &UNIT
    IF &LASTCC NE 0 THEN DO
        WRITE %NUAIINIT - UNABLE TO ALLOCATE FILE TAILORING DATASET
        GOTO INITFAIL
        END
    ERROR DO
        ERROR OFF
        WRITE %NUAIINIT - FILE TAILORING FAILED
        GOTO INITFAIL
        END
    ISPEXEC FTOPEN
    ISPEXEC FTINCL NUAIQUIT
    ISPEXEC FTCLOSE
    ERROR OFF
    END
/**
EXIT CODE(0)
/**
INITFAIL: +
IF &OUT EQ OUT OR &ISPF&OUT&HOST EQ THEN DO
    ISPEXEC LMFREE DATAID(&NUAIOUT)
    FREE DD(NUAIOUT&NUAISESS)
    END
IF &HOST EQ HOST OR &ISPF&OUT&HOST EQ THEN DO
    ISPEXEC LMFREE DATAID(&NUAIHOUT)
    FREE DD(NUAIHST&NUAISESS)
    END
IF &ISPF EQ ISPF OR &ISPF&OUT&HOST EQ THEN ISPEXEC FTCLOSE
EXIT CODE(12)
