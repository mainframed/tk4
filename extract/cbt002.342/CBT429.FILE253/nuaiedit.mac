PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIEDIT)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - ISPF/PDF EDIT OF A REMOTE FILE
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIEDIT - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAISWCD) PROFILE
ISPEXEC VGET (NUAISESS) SHARED
ISPEXEC LMINIT DATAID(NUAIEDIT) DDNAME(NUAIEDT&NUAISESS)
ISPEXEC EDIT DATAID(&NUAIEDIT) PANEL(NUAIEDIT)
SET CC=&LASTCC
/**
SET SKEL=NUAIEDIT
IF &CC GT 4 THEN DO
    WRITE %NUAIEDIT - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
    SET SKEL=NUAIQUIT
    END
ELSE DO
    ISPEXEC CONTROL NONDISPL END
    IF &CC EQ 0 OR &NUAISWCD EQ YES THEN +
        ISPEXEC CONTROL DISPLAY LINE START(4)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAIEDIT - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
