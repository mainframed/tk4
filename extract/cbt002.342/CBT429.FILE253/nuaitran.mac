PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAITRAN)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - FILE TRANSFER
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAITRAN - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAIMODE NUAICREA NUAITOPT NUAISOUR NUAIDEST NUAISWCD +
    NUAIHOST NUAIUSER NUAICOPT) PROFILE
ISPEXEC VGET (NUAISESS) SHARED
SET MSG=
SET CURSOR=CURSOR(ZCMD)
IF &STR(&NUAISWCD) EQ THEN SET CURSOR=CURSOR(NUAISWCD)
IF &STR(&NUAICREA) EQ THEN SET CURSOR=CURSOR(NUAICREA)
IF &STR(&NUAIMODE) EQ THEN SET CURSOR=CURSOR(NUAIMODE)
IF &STR(&NUAISOUR) EQ THEN SET CURSOR=CURSOR(NUAISOUR)
SET NEXTOPT=T
/**
ISPEXEC DISPLAY PANEL(NUAITRAN) &MSG &CURSOR
SET CC=&LASTCC
SET MSG=
SET CURSOR=CURSOR(ZCMD)
IF &CC NE 0 AND &CC NE 8 THEN +
    WRITE %NUAITRAN - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
/**
SET SKEL=NUAITRAN
IF &NUAICMD NE SEND AND &NUAICMD NE RECEIVE THEN SET SKEL=NUAIUCMD
IF &CC NE 0 THEN SET SKEL=NUAIQUIT
ELSE DO
    IF &STR(&NUAIPSWD) EQ THEN SET NUAIPSWT=
    ELSE SET NUAIPSWT=XXXXXXXX
    ISPEXEC VPUT (NUAIMODE NUAICREA NUAITOPT NUAISOUR NUAIDEST +
        NUAISWCD NUAIHOST NUAIUSER NUAICOPT) PROFILE
    ISPEXEC CONTROL NONDISPL END
    ISPEXEC CONTROL DISPLAY LINE START(7)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAITRAN - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
