PROC 0 DEBUG
/**
/**  REFER: NUA.V2R10M12.CLIST(NUAIHOST)
/**  COMP:  IBM/MVS USER-ACCESS (H213)
/**  DOC:   ISPF MENU SYSTEM - REMOTE HOST MANAGEMENT
/**
CONTROL NOFLUSH NOLIST NOMSG NOCONLIST NOPROMPT
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST
/**
IF &SYSISPF NE ACTIVE THEN DO
    WRITE %NUAIHOST - ISPF NOT AVAILABLE
    EXIT QUIT CODE(12)
    END
ELSE ISPEXEC CONTROL ERRORS RETURN
/**
ISPEXEC VGET (NUAIHNUM NUAIHOST NUAIUSER NUAICOPT) PROFILE
ISPEXEC VGET (NUAIHOUT NUAISESS) SHARED
SET NEXTOPT=H
/**
PANEL: ISPEXEC BROWSE DATAID(&NUAIHOUT) PANEL(NUAIHOST)
SET CC=&LASTCC
IF &CC EQ 12 THEN DO
    OPENFILE NUAIHST&NUAISESS OUTPUT
    CONTROL ASIS
    SET NUAIHOUT=Enter SHOW command to obtain current host information
    CONTROL CAPS
    PUTFILE NUAIHST&NUAISESS
    CLOSFILE NUAIHST&NUAISESS
    GOTO PANEL
    END
IF &STR(&NUAICMD) EQ END THEN SET &CC EQ 8
IF &CC NE 0 AND &CC NE 8 THEN +
    WRITE %NUAIHOST - ISPEXEC FAILED WITH A CONDITION CODE OF &CC.
/**
SET SKEL=NUAIHOST
IF &NUAICMD NE CONNECT AND &NUAICMD NE DISCONNECT AND +
    &NUAICMD NE SET AND &NUAICMD NE SHOW THEN DO
    CONTROL ASIS
    SET ZCMD=&STR(&NUAICMD &ZTRAIL)
    CONTROL CAPS
    SET SKEL=NUAIUCMD
    END
IF &CC NE 0 THEN SET SKEL=NUAIQUIT
ELSE DO
    IF &STR(&NUAIPSWD) EQ THEN SET NUAIPSWT=
    ELSE SET NUAIPSWT=XXXXXXXX
    ISPEXEC VPUT (NUAIHNUM NUAIHOST NUAIUSER NUAICOPT) PROFILE
    ISPEXEC CONTROL NONDISPL END
    ISPEXEC CONTROL DISPLAY LINE START(11)
    END
/**
ERROR DO
    ERROR DO
        ERROR OFF
        WRITE %NUAIHOST - FILE TAILORING FAILED
        EXIT QUIT CODE(12)
        END
    %NUAIINIT ISPF /* THIS CAN SOMETIMES HELP AFTER AN ERROR */
    GOTO FT
    END
FT: +
ISPEXEC FTOPEN
ISPEXEC FTINCL &SKEL
ISPEXEC FTCLOSE
ERROR OFF
/**
EXIT CODE(0)
