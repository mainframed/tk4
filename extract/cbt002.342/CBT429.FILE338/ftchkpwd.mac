//IBMUSERX JOB (ACCT#),FTCHKPWD,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMH EXEC PGM=IEV90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MODULE NAME = FTCHKPWD                                              *
*                                                                     *
* DESCRIPTIVE NAME = "CHECK PASSWORD" EXIT FOR FTP                    *
*                                                                     *
* STATUS = R102                                                       *
*                                                                     *
* FUNCTION = This user exit controls which user-ids can               *
*            access the FTP server.                                   *
*                                                                     *
* AUTHOR = Gilbert Saint-flour                                        *
*                                                                     *
* ENVIRONMENT = See below                                             *
*                                                                     *
*    DEPENDENCIES: TCP/IP V3R1                                        *
*                                                                     *
* MODULE TYPE = PROCEDURE, (CSECT TYPE)                               *
*                                                                     *
*    PROCESSOR = IBM HIGH LEVEL ASSEMBLER/MVS                         *
*                                                                     *
*    MODULE SIZE = 1K                                                 *
*                                                                     *
*    ATTRIBUTES = Reenterable, RMODE ANY, AMODE 31,                   *
*                 Problem State, Key 8                                *
*                                                                     *
* OPERATION = See TCP/IP Customization Guide                          *
*                                                                     *
* INSTALLATION = See Below                                            *
*                                                                     *
*    1. Link-edit FTCHKPWD into an APF-authorized link-list           *
*       library and refresh LLA.                                      *
*                                                                     *
*    2. Stop the FTP server and restart it.                           *
*                                                                     *
* PARAMETERS = See documentation for FTCHKPWD                         *
*                                                                     *
* RETURN-CODES = 0          User allowed to access FTP                *
*                16         User not allowed to access FTP            *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*  $100 Initial version                                               *
*  $101 Add list of authorized user-ids                               *
*  $102 Authorize users defined with TSO(USERDATA(8000))              *
*                                                                     *
***********************************************************************
FTCHKPWD CSECT
FTCHKPWD RMODE ANY
         BAKR  R14,0
         LR    R12,R15                 BASE REG
         USING FTCHKPWD,R12
*
         LM    R4,R7,0(R1)             LOAD PARAMETERS
         USING RETURN_CODE,R4
         USING USERID,R6
         USING PASSWORD,R7
*---------------------------------------------------------------------*
*                                                                     *
*        Check for special users which don't have a RACF TSO segment  *
*                                                                     *
*---------------------------------------------------------------------*
         BAL   R2,PUB2                 branch around the list
         DC    CL8'PUB'                Anonymous
         DC    CL8'IBMUSER'            gsf
         DC    CL8'P390'               Kal
*loop
PUB2     CLC   USERID(8),0(R2)         this user authorised?
         BE    RC0                     yes, exit with rc=0
         LA    R2,8(,R2)               next entry in the table
         CLC   PUB2,0(R2)              end of table?
         BNE   PUB2                    no, check next entry
*endloop
*---------------------------------------------------------------------*
*                                                                     *
*        Not a special user, check for TSO(USERDATA(8000))            *
*                                                                     *
*---------------------------------------------------------------------*
         GETMAIN R,LV=RACXTRTLL,SP=229
         LR    R9,R1
         USING RACXTRTL,R9
         MVC   RACXTRTL(RACXTRTLL),RACXTRTM
         RACXTRT TYPE=EXTRACT,                                         X
               ENTITY=USERID,                                          X
               RELEASE=1.8,                                            X
               MF=(E,RACXTRTL)
         LTR   R7,R15                  PASS/SAVE RC
         BNZ   FREEM2                  RACF FAILED, EXIT
         LH    R2,4(,R1)               OFFSET TO DATA RETURNED
         LA    R3,0(R2,R1)             POINT AT LENGTH OF DATA RETURNED
         CLC   =F'2',0(R3)             Two bytes of data returned?
         BNE   NOTUDATA                No, error
         TM    4(R3),X'80'             USERDATA(8000) ?
         BO    FREEM1                  yes, jump
NOTUDATA LA    R7,8                    No, RC=8
*
FREEM1   L     R0,0(,R1)               SP,LENGTH
         FREEMAIN R,LV=(0),A=(1)       FREE MEMORY
*
FREEM2   FREEMAIN R,LV=RACXTRTLL,SP=229,A=(R9)
*
         LTR   R7,R7                   any error?
         BNZ   RC16                    yes, exit with rc=16
*
RC0      XC    RETURN_CODE,RETURN_CODE RC=0 (user is authorized)
         PR                            GOBACK
*
RC16     LA    R15,16                  RC=16 (user not authorized)
         ST    R15,RETURN_CODE         PASS RETURN_CODE
         PR                            GOBACK
*
RACXTRTM RACXTRT TYPE=EXTRACT,                                         X
               ENTITY=*-*,                                             X
               FIELDS=FLDLIST,                                         X
               SEGMENT=SEGNAME,                                        X
               SUBPOOL=229,                                            X
               RELEASE=1.8,                                            X
               MF=L
SEGNAME  DC    CL8'TSO '
FLDLIST  DC    AL4(1),CL8'TUDATA'
*
RETURN_CODE DSECT
USERID      DSECT
PASSWORD    DSECT
*
DYNAM    DSECT
RACXTRTL RACXTRT TYPE=EXTRACT,RELEASE=1.8,MF=L
RACXTRTLL EQU *-RACXTRTL
         YREGS
         END
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//LKED    EXEC PGM=LINKLLA,PARM=RENT
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,DELETE)
//SYSLMOD  DD DISP=SHR,DSN=SYS1.P390.LINKLIB(FTCHKPWD)
//SYSPRINT DD SYSOUT=*
