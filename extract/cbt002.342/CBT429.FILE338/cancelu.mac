//IBMUSERK JOB (ACCT#),CANCELU,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMH EXEC PGM=IEV90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MODULE NAME = CANCELU                                               *
*                                                                     *
* DESCRIPTIVE NAME = Cancel your own TSO session                      *
*                                                                     *
* STATUS = R202                                                       *
*                                                                     *
* FUNCTION = This TSO command can be used in conjunction with a       *
*            public TSO userid to allow remote users to cancel their  *
*            own TSO session after the connection hangs or drops.     *
*                                                                     *
* AUTHOR = Gilbert Saint-flour                                        *
*                                                                     *
* ENVIRONMENT = SEE BELOW                                             *
*                                                                     *
*    DEPENDENCIES: MVS/ESA V5 OR OS/390                               *
*                  STRING Macro R504                                  *
*                                                                     *
* MODULE TYPE = PROCEDURE, (CSECT TYPE)                               *
*                                                                     *
*    PROCESSOR = IBM HIGH LEVEL ASSEMBLER/MVS                         *
*                                                                     *
*    MODULE SIZE = 2K                                                 *
*                                                                     *
*    ATTRIBUTES = NON-REUSABLE, RMODE 24, AMODE 31,                   *
*                 APF-AUTHORIZED                                      *
*                                                                     *
* OPERATION = See Below                                               *
*                                                                     *
*    1. Issue the STTMPMD macro to inactivate the ATTN and PA1 keys.  *
*                                                                     *
*    2. Issue the ESTAE macro to LOGOFF in case of an ABEND.          *
*                                                                     *
*    3. Prompt the user for a userid and a password                   *
*                                                                     *
*    4. Issue RACROUTE REQUEST=VERIFY to validate the userid and      *
*       the password.  If RACROUTE fails, issue LOGOFF.               *
*                                                                     *
*    5. Scan active address spaces to determine if the userid's       *
*       TSO session is still active. If not found, exit to the        *
*       LOGON panel.                                                  *
*                                                                     *
*    6. Scan OREs for outstanding replies for the userid's TSO        *
*       session.  Issue a message if one is found, as this may        *
*       prevent the CANCEL command from being successful.             *
*                                                                     *
*    7. Invoke the System-Initiated Cancel routine (SIC) to           *
*       cancel the userid's TSO session.                              *
*                                                                     *
*    8. Wait until the cancelled address space terminates,            *
*       or for 10 seconds, whichever comes first.                     *
*                                                                     *
*    9. Exit to the LOGON screen                                      *
*                                                                     *
* INSTALLATION = See below                                            *
*                                                                     *
*    1. Link-edit CANCELU into an APF-Authorized library.             *
*                                                                     *
*    2. Add CANCELU to the AUTHCMD table in IKJTSOnn                  *
*                                                                     *
*    3. Define the CANCELU user in RACF and TSO                       *
*                                                                     *
*       ADDUSER +                                                     *
*             CANCELU +                                               *
*             NAME(CANCELU-CANCELME) +                                *
*             DFLTGRP(TEST) +                                         *
*             PASSWORD(CANCELME) +                                    *
*             TSO(PROC(CANCELU))                                      *
*                                                                     *
*    4. Create the CANCELU LOGON procedure:                           *
*                                                                     *
*         //CANCELU EXEC PGM=IKJEFT01,PARM=CANCELU                    *
*         //STEPLIB DD DSN=<APF-authorized library>,DISP=SHR          *
*                                                                     *
* PARAMETERS = None                                                   *
*                                                                     *
* RETURN-CODES = None                                                 *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*  $100 First release                                                 *
*  $101 Replace CANCEL command with CALL to the SIC routine           *
*  $202 Replace IKJPARSE with full-screen TGET/TPUT                   *
*                                                                     *
***********************************************************************
CANCELU  CSECT
CANCELU  AMODE 31                      TPUT
         DC    C'GSF Utilities - CANCELU R202 ',0H'0'
BEGIN    BALR  R12,0                   BASE REGISTER
         USING *,R12
         LR    R11,R1                  POINT AT CPPL.
         USING CPPL,R11
         ESTAE RECOVERY,CT             INTERCEPT ABENDS
*
*        Retrieve ACEE addr and terminal name
*
         L     R4,PSAAOLD-PSA          MY ASCB
         USING ASCB,R4
         L     R5,ASCBASXB             MY ASXB
         USING ASXB,R5
         MVC   USERID,ASXBUSER         TSO USERID
         L     R6,ASXBSENV             MY ACEE
         USING ACEE,R6
         MVC   TERMNAME,ACEETRID       terminal name
         DROP  R4,R5,R6
*
         STAX  DEFER=YES               IGNORE ATTENTIONS
         STSIZE LINE=24,SIZE=80        3270 MODEL-2
***      STFSMODE ON,INITIAL=YES       Switch to full-screen mode
         STTMPMD ON,KEYS=ALL           TRAP CLEAR AND PA1 KEYS
***********************************************************************
*        Issue full-screen TPUT, get answer                           *
***********************************************************************
*loop
LOOP1    TPUT  SCREEN,SCREENL,FULLSCR
         LTR   R1,R15                  any error?
         BNZ   ABEND16                 YES, ABEND
         MVC   INBUF,BLANKS            clear input area
         TGET  INBUF,L'INBUF           READ REPLY
         LTR   R1,R15                  any error?
         BNZ   ABEND16                 YES, ABEND
         TCLEARQ INPUT
 CLI INBUF+L'USERID,X'11'    SBA?
 BNE *+10                    no, jump
 MVC INBUF+L'USERID(L'PASSWORD),INBUF+L'USERID+3
         OC    INBUF,BLANKS            UPPER CASE THE INPUT
         CLC   INBUF,BLANKS            any data entered?
         BE    LOOP1                   NO, RESHOW SCREEN
*endloop
         STLINENO LINE=10,CLEAR=NO     MIDDLE OF SCREEN
***********************************************************************
*        PROCESS PASSED PARAMETERS                                    *
***********************************************************************
         STRING (INBUF,L'USERID,T),INTO=USERID
         STC   R15,USERIDL             length of user-id
         STRING (INBUF+L'USERID,L'PASSWORD,T),INTO=PASSWORD
         STC   R15,PASSWRDL            length of password
         CLI   USERIDL,0               USERID SPECIFIED?
         BE    STACKCMD                NO, QUIT
         CLI   PASSWRDL,0              PASSWORD SPECIFIED?
         BE    STACKCMD                NO, QUIT
***********************************************************************
*        CHECK USERID/PASSWORD                                        *
***********************************************************************
         RACROUTE REQUEST=VERIFY,                                      X
               MF=(E,RACVFYL)
         LTR   R4,R15                  SAF RC
         BZ    PSWDOK                  ZERO, CONTINUE
SAFERROR LM    R5,R6,RACVFYL           RACF RC,RSN
         STRING ((R4),,X),1X,((R5),,X),1X,((R6),,X),INTO=WORK512
         TPUT  WORK512,(R15)
         B     STACKCMD
RACVFYL  RACROUTE REQUEST=VERIFY,                                      X
               ENVIR=CREATE,                                           X
               USERID=USERIDL,                                         X
               PASSWRD=PASSWRDL,                                       X
               TERMID=TERMNAME,        Terminal name                   X
               WORKA=WORK512,                                          X
               MF=L
*
*        RECOVERY ROUTINE
*
RECOVERY LA    R15,STACKCMD-RECOVERY(,R15) RETRY ADDRESS
         SETRP RETADDR=(R15),RC=4,FRESDWA=YES,RETREGS=YES
         BR    R14
***********************************************************************
*        ISSUE "CANCEL U=USERID" COMMAND                              *
***********************************************************************
PSWDOK   STRING 'LOGON ',USERID,INTO=LIST1CMD
         STTMPMD OFF                   ALLOW CLEAR AND PA1 KEYS
*
         L     R3,CVTPTR               CVT ADDRESS
         USING CVTMAP,R3
         LA    R4,2                    FIRST ASID
         L     R5,CVTASVT              POINT TO ASVT
         L     R5,ASVTMAXU-ASVT(,R5)   MAX ASID
*LOOP
LOOP2    LOCASCB ASID=(R4)
         LTR   R15,R15                 ASCB active?
         BNZ   LOOP2NXT                no, ignore
         LR    R7,R1
         USING ASCB,R7
         IAZXJSAB READ,ASCB=(R7),JOBNAME=JOBNAME,JOBID=JOBID
         LTR   R15,R15
         BNZ   LOOP2NXT
         CLI   JOBID,C'T'              TSO ADDRESS SPACE?
         BNE   LOOP2NXT                no, ignore
         CLC   JOBNAME,USERID          MY USERID?
         BE    ASIDF99                 YES, EXIT
LOOP2NXT LA    R4,1(,R4)               NEXT ASID
         BCT   R5,LOOP2                NEXT ASID
*ENDLOOP
         STRING 'USER ',(USERID,,T),' NOT LOGGED ON',INTO=WORK512
         TPUT  WORK512,(R15)
         B     STACKCMD
ASIDF99  EQU   *
         STRING 'USERID(',(USERID,,T),') ACTIVE IN ASID(',             X
               (ASCBASID,,X),')',INTO=WORK512
         TPUT  WORK512,(R15)
***********************************************************************
*        LOOK FOR OUTSTANDING WTORS                                   *
***********************************************************************
         L     R4,CVTCUCB              UCM
         ICM   R5,B'1111',UCMRPYQ-UCM(R4) FIRST ORE
         BZ    WTOR99                  NO OUTSTANDING REPLIES, EXIT
         USING OREF,R5
         MODESET KEY=ZERO
*LOOP
WTOR11   CLC   OREASID,ASCBASID        WTOR FOR MY USERID?
         BE    WTOR21                  YES, REPLY RETRY
         ICM   R4,B'1111',ORELKP       NEXT ORE
         BNZ   WTOR11                  LOOP
*ENDLOOP
         MODESET KEY=NZERO
         B     WTOR99                  EXIT
WTOR21   EQU   *
         MODESET KEY=NZERO
         STRING 'USER ',(USERID,,T),' HAS OUTSTANDING REPLIES',        X
               INTO=WORK512
         TPUT  WORK512,(R15)
WTOR99   EQU   *
***********************************************************************
*        Invoke the "System-Initiated Cancel" routine (SIC)           *
***********************************************************************
SIC      MODESET KEY=ZERO,MODE=SUP
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=STDSAVE
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,REGS=STDSAVE
         LA    R0,X'222'               abend code
         LA    R1,ASCB                 ASCB address
         L     R15,CVTSIC              system-initiated CANCEL
         BALR  R14,R15              <- CANCEL
         SETLOCK RELEASE,TYPE=ALL,REGS=STDSAVE
         MODESET KEY=NZERO,MODE=PROB   KEY=PROB
***********************************************************************
*        LOOP UNTIL ADDRESS SPACE TERMINATES                          *
***********************************************************************
         STRING 'CANCEL COMMAND ISSUED FOR USERID(',(USERID,,T),')',   X
               INTO=WORK512
         TPUT  WORK512,(R15)
         LA    R3,20                   20 STIMER ITERATIONS
*LOOP
WAIT5    STIMER WAIT,BINTVL=HALFASEC   WAIT FOR HALF A SECOND
         IAZXJSAB READ,ASCB=(R7),JOBNAME=JOBNAME,JOBID=JOBID
         LTR   R15,R15
         BNZ   WAIT5X
         STRING 'WAITING FOR ADDRESS-SPACE TO TERMINATE',              X
               INTO=WORK512
         TPUT  WORK512,(R15)
         BCT   R3,WAIT5
*ENDLOOP
WAIT5X   EQU   *
***********************************************************************
*        STACK A "LOGOFF" OR A "LOGON" COMMAND                        *
***********************************************************************
STACKCMD EQU   *
         GETMAIN R,LV=LIST1L,SP=78
         LR    R2,R1                   ADDRESS OF THE LSD
         MVC   0(LIST1L,R2),LIST1      MOVE TO SP78
         LA    R3,16(,R2)              POINT TO IN-STORAGE LIST
         ST    R3,0(,R2)               STORE INTO LSD
         ST    R3,8(,R2)               STORE INTO LSD
         L     R3,CPPLUPT              UPT
         L     R4,CPPLECT              ECT
         STACK PARM=STACKL,UPT=(R3),ECT=(R4),ECB=DYNECB,               X
               STORAGE=((R2),PROCN),MF=(E,DYNPPL)
         LTR   R0,R15
         BNZ   ABEND16
         SVC   3                       EXIT
*
ABEND16  ABEND 16,DUMP,STEP,REASON=(15)
*
LIST1    DC    A(*-*,L'LIST1CMD+4,*-*,0)
         DC    Y(L'LIST1CMD+4,0)
LIST1CMD DC    C'LOGOFF        '
LIST1L   EQU   *-LIST1
STACKL   STACK MF=L
HALFASEC DC    F'50'                   HALF A SECOND
*--------------------------------------*
*  3270 SCREEN BUFFER                  *
*--------------------------------------*
SCREEN   DC    X'27F5'                  ESC, ERASE WRITE STD
         DC    X'C1'                    WCC:
*        F3270 ROW=24,COL=80,MODEL=2    SPF START
         DC    X'115D7F'                SBA
*        F3270 ROW=1,COL=1              ...
         DC    X'114040'                SBA
*        F3270 ROW=1,COL=1,RA=00        CLEAR WHOLE SCREEN
         DC    X'3C4040',X'00'          RA
*        F3270 ROW=3,COL=1
         DC    X'11C260'                SBA
*        F3270 ATTR=(SF,PROT,NORM),INITIAL='Cancel a TSO User-ID'
         DC    X'1D60'                  SF
         DC    C'Cancel a TSO User-ID'
*        F3270 ROW=5,COL=3,INITIAL='  Userid '
         DC    X'11C5C2'                SBA
         DC    C'  Userid '             DATA
*        F3270 ATTR=(SF,PROT,BRIGHT),INITIAL='===>'
         DC    X'1DE8'                  SF
         DC    C'===>'                  DATA
*        F3270 ATTR=(SF,UNPROT),LENGTH=8
         DC    X'1D40'                  SF
         DC    CL8' '                   User-id
*        F3270 ATTR=(SF,PROT,NORM,ASKIP)
         DC    X'1DF0'                  SF
*        F3270 ROW=6,COL=3,INITIAL='Password '
         DC    X'11C6D2'                SBA
         DC    C'Password '             DATA
*        F3270 ATTR=(SF,PROT,BRIGHT),INITIAL='===>'
         DC    X'1DE8'                  SF
         DC    C'===>'                  DATA
*        F3270 ATTR=(SF,NDISP,UNPROT),LENGTH=8
         DC    X'1D4C'                  SF
         DC    CL8' '                   Password
*        F3270 ATTR=(SF,PROT,NORM)
         DC    X'1D60'                  SF
*        F3270 ROW=5,COL=18
         DC    X'11C5D1'                SBA
         DC    X'13'                    IC
SCREENL  EQU   *-SCREEN
BLANKS   DC    CL(L'INBUF)' '          blanks
*
DYNPPL   DS    8F                      PPL FOR USE WITH PARSE
DYNECB   DS    F                       ECB FOR USE WITH PARSE
TERMNAME DS    CL8                     Terminal name
USERIDL  DS    FL1'7'                  actual length of user-id
USERID   DS    CL8                     User-ID and job-name
PASSWRDL DS    AL1(L'PASSWORD)         length of password
PASSWORD DS    CL8                     password
JOBNAME  DS    CL8                     FROM JSAB
JOBID    DS    CL8                     FROM JSAB
INBUF    DS    CL24                    FROM TGET
         DS    0D
WORK512  DS    CL512
         PRINT NOGEN
         IKJCPPL                       COMMAND PROCESSOR PARM LIST
         IHAPSA                        PREFIXED STORAGE AREA
         CVT   DSECT=YES               COMMUNICATION VECTOR TABLE
         IHAASCB                       ADDRESS SPACE CONTROL BLOCK
         IHAASXB                       ADDRESS SPACE EXTENSION BLOCK
         IHAACEE                       ACCESSOR ENVIRONMENT ELEMENT
         IHAASSB                       ADDRESS SPACE SECONDARY BLOCK
         IHAASVT                       ADDRESS SPACE VECTOR TABLE
         IAZJSAB                       JOB SCHEDULER ASCB
         IEECUCM DSECT=YES             UNIT CONTROL MODULE
         IHAORE DSECT=YES              OPERATOR REPLY ELEMENT
         IHASDWA DSECT=YES,VRAMAP=NO   SDWA
         STRING GENERATE
         YREGS
         END   BEGIN
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//         DD DISP=SHR,DSN=IBMUSER.P390IPL.PDS           (STRING)
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,5)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//LKED    EXEC PGM=LINKLLA,PARM='MAP,AC=1'
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,DELETE)
//SYSLMOD  DD DISP=SHR,DSN=SYS1.P390.LINKLIB(CANCELU)
//SYSPRINT DD SYSOUT=*
//
//
//GO      EXEC PGM=IKJEFT01,REGION=2M
//STEPLIB  DD DISP=SHR,DSN=*.LKED.SYSLMOD
//SYSTSIN  DD *
CANCELU
//SYSTSPRT DD SYSOUT=*
//ABNLIGNR DD DUMMY
//SYSUDUMP DD SYSOUT=*
