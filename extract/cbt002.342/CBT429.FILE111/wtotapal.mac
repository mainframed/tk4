//S40DA01S JOB (11613109,0402,1,1,,LONG,,,104),PURDY,CLASS=H,
//            MSGCLASS=X
//ASM     EXEC PGM=IEV90,
//             PARM='NODECK,OBJ,LINECNT=99,BUFSIZE(MAX),RENT',
//             REGION=4096K
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD  DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD  DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD  SYSOUT=*,DCB=BLKSIZE=1089
//SYSPUNCH DD  DUMMY
//SYSLIN   DD  DSN=&&OBJSET,UNIT=SYSSQ,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
WTOTAPAL TITLE 'MPF EXIT FOR TAPE ALLOCATION RECOVERY'
WTOTAPAL CSECT
WTOTAPAL AMODE 31
WTOTAPAL RMODE ANY
         USING *,R15
         SPACE
***********************************************************************
*                                                                     *
* THIS EXIT IS USED TO WRITE AN ALLOCATION RECOVERY SMF RECORD FOR    *
* EVERY ALLOCATION RECOVERY MESSAGE RECIEVED FOR TAPE - IEF489        *
*                                                                     *
***********************************************************************
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3    ---  BASE REG
R4       EQU   4    ===> CTXT
R5       EQU   5    ===> CTXTATTR
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13   ===> GOTTEN REG SAVE AREA
R14      EQU   14
R15      EQU   15
         EJECT
**********************************************************************
*                                                                    *
*        SETUP AND SEE WHY WE WERE CALLED                            *
*                                                                    *
**********************************************************************
         SPACE 2
         SAVE  (14,12),,WTOTAPAL.&SYSDATE..&SYSTIME
         LR    R3,R15
         SPACE
         DROP  R15
         USING WTOTAPAL,R3
         SPACE
         L     R4,0(R1)      LOAD POINTER TO PASSED PARM LIST
         USING CTXT,R4       ESTABLISH ADDRESSABILITY TO PARM LIST
         SPACE
         GETMAIN RU,LV=AREAREGS,SP=230,LOC=(BELOW,ANY) GETMAIN FOR REGS
         SPACE
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA
         MVC   1(AREAREGS-1,R10),0(R10)
         ST    R13,4(R10)    EXCHANGE THE REGISTER SAVE AREA
         ST    R10,8(R13)    POINTERS TRICK
         LR    R13,R10
         USING WORKREGS,R13  ESTABLISH ADDRESSABILITY TO GOTTEN AREA
         SPACE
         MVC   SMFREC(SMFSLEN),SMFSKEL MOVE SKELETON SMF RECORD
         TIME  BIN           GET DATE AND TIME
         ST    R0,SMFDTIME   STORE DATE & TIME IN RECORD
         ST    R1,SMFDDATE
         L     R1,16         LOAD ADDRESS OF CVT
         L     R1,196(R1)    LOAD ADDRESS OF SMCA
         MVC   SMFDSID,16(R1) MOVE SYSTEM ID FROM SMCA
         SPACE
         L     R5,CTXTTXPJ   LOAD POINTER TO MESSAGE ATTRIBUTES
         USING CTXTATTR,R5   EXTABLISH ADDRESSABILITY TO ATTRIBUTES
         CLC   CTXTTMSG(6),=C'IEF238'  CHECK FOR CORRECT MESSAGE TYPE
         BE    OK238
         B     RETURN0
         SPACE
OK238    EQU   *
         SPACE
WRITESMF SMFWTM SMFREC
         SPACE
RETURN0  SR    R10,R10
         SPACE
RETURN   LR    R1,R13        LOAD ADDRESS OF GOTTEN AREA FOR REGS
         L     R13,4(R13)    LOAD PREVIOUS SAVE AREA ADDRESS
         LA    R0,AREAREGS   LOAD SIZE OF GOTTEN AREA FOR REGISTERS
         FREEMAIN RU,LV=(0),A=(1),SP=230  FREE GOTTEN AREA
         LR    R15,R10       PLACE RETURN CODE IN REG15
         RETURN (14,12),RC=(15) RETURN TO THE CALLER
         EJECT
         CNOP  2,4
SMFSKEL  DS    0H        SMF RECORD TO BE WRITTEN
SMFLEN   DC    H'19'     RECORD LENGTH
SMFSEG   DC    H'0'      SEGMENT INDICATOR
SMFSIND  DC    X'06'     SYSTEM INDICATOR
SMFRECTY DC    AL1(200)  RECORD TYPE
SMFTIME  DS    XL4       TIME
SMFDATE  DS    XL4       DATE
SMFSID   DS    CL4       SYSTEM ID
SMFTYPE  DC    C'A'      A - ALLOCATION RECOVERY
SMFSLEN  EQU   *-SMFSKEL
         EJECT
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP GOTTEN AREA                               *
*                                                                    *
**********************************************************************
         SPACE
WORKREGS DSECT
         SPACE
REGAREA  DS    18F       REGISTER SAVE AREA
         SPACE
         CNOP  2,4
SMFREC   DS    0H        SMF RECORD TO BE WRITTEN
SMFDLEN  DS    H         LENGTH
SMFDSEG  DS    H         SEGMENT INDICATOR
SMFDSIND DS    XL1       SYSTEM INDICATOR
SMFDRECT DS    XL1       RECORD TYPE
SMFDTIME DS    XL4       TIME
SMFDDATE DS    XL4       DATE
SMFDSID  DS    CL4       SYSTEM ID
SMFDTYPE DS    CL1       A - ALLOCATION RECOVERY
         SPACE
AREAREGS EQU   *-WORKREGS
         EJECT
         IEZVX100
         SPACE 2
         END
/*
//LKED1   EXEC PGM=IEWL,PARM='LIST,LET,MAP,XREF,RENT,REFR',
//             COND=(8,LT,ASM)
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=*,DCB=(RECFM=FB,LRECL=121,BLKSIZE=1210)
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD  DDNAME=SYSIN
//SYSLMOD  DD  DSN=SYS1.LINKLIB,DISP=SHR
//SYSIN    DD *
  SETCODE AC(1)
  NAME WTOTAPAL(R)
/*
