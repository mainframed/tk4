//S40DA01S JOB (11613109,0402,1,1,,LONG,,,104),PURDY,CLASS=H,
//            MSGCLASS=X
//ASM     EXEC PGM=IEV90,
//             PARM='NODECK,OBJ,LINECNT=99,BUFSIZE(MAX),RENT',
//             REGION=4096K
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD  DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD  DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD  SYSOUT=*,DCB=BLKSIZE=1089
//SYSPUNCH DD  DUMMY
//SYSLIN   DD  DSN=&&OBJSET,UNIT=SYSSQ,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
*          DATA SET WTOETPS2                AS OF 05/27/88
WTOETPS2 TITLE 'WTOETPS2 -- WTO EXIT TO DISPLAY TAPE UNLOADED DATA'
WTOETPS2 CSECT
WTOETPS2 AMODE 31
WTOETPS2 RMODE ANY
HDRLEN   EQU   12   CONSTANT FROM THE EXIT "WTOETPS1" MACRO
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOETPS2'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R4,0(R1)      LOAD POINTER TO PASSED PARM LIST      JWP
         SPACE
         GETMAIN RU,LV=AREAREGS,SP=230,LOC=(BELOW,ANY) GETMAIN FOR JWP
         SPACE
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA           JWP
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA                     JWP
         MVC   1(AREAREGS-1,R10),0(R10)                            JWP
         ST    R13,4(R10)    EXCHANGE THE REGISTER SAVE AREA       JWP
         ST    R10,8(R13)    POINTERS TRICK                        JWP
         LR    R13,R10                                             JWP
         USING WORKREGS,R13  ESTABLISH ADDRESSABILITY TO GOTTEN AREA
         SPACE
         L     R8,CVTPTR      FIND USERCVT
         USING CVT,R8
         L     R9,CVTUSER
         LTR   R9,R9          IS THE USERCVT VALID?
         BZ    RETURN           NO, DO NOT DO ANYTHING DUMB
         USING USERCVT,R9
         DROP  R8
         LR    R10,R4         PICK UP ADDRESS OF CTXT
         USING CTXT,R10
         L     R8,CTXTTXPJ    LOAD POINTER TO THE MAJOR LINE
         USING CTXTATTR,R8
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BO    RETURN          YES, SOMETHING IS WRONG
         CLC   =C'IEC501A',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BE    START1          YES, PROCESS IT
         CLC   =C'IEC501E',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BE    START1          YES, PROCESS IT
         CLC   =C'IEF233A',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BE    START1          YES, PROCESS IT
         CLC   =C'IEF233D',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BNE   RETURN          NO, DON'T KNOW WHY I WAS CALLED
         SPACE
START1   EQU   *
         MVC   SMFREC(SMFSLEN),SMFSKEL MOVE SKELETON SMF RECORD    JWP
         TIME  BIN           GET DATE AND TIME                     JWP
         ST    R0,SMFDTIME   STORE DATE & TIME IN RECORD           JWP
         ST    R1,SMFDDATE                                         JWP
         L     R1,16         LOAD ADDRESS OF CVT                   JWP
         L     R1,196(R1)    LOAD ADDRESS OF SMCA                  JWP
         MVC   SMFDSID,16(R1) MOVE SYSTEM ID FROM SMCA             JWP
         MVC   SMFDUNIT,CTXTTMSG+10 MOVE UNIT ADDRESS TO SMF RECORD
         MVC   SMFDVOL,CTXTTMSG+14  MOVE VOLSER TO SMF RECORD      JWP
         SMFWTM SMFREC         WRITE SMF RECORD FOR MOUNT          JWP
         SPACE
         TRT   CTXTTMSG,COMMATBL  FIND THE INFAMOUS COMMA
         CLI   0(R1),C','    DID I REALLY FIND IT
         BE    START2          YES, CONTINUE PROCESSING
         EX    1,*             NO, PROBLEM SOMEWHERE
START2   LA    R6,1(R1)      PROCESS THE VOL=SER=
         CLC   =CL6' ',0(R6)  IS THE VOLUME SERIAL NUMBER BLANK?
         BE    RETURN           YES, FORGET IT
         CLC   =C'SCRTCH',0(R6) IS THE VOLUME SERIAL NUMBER SCRTCH?
         BE    RETURN           YES, FORGET IT
         CLC   =C'PRIVAT',0(R6) IS THE VOLUME SERIAL NUMBER PRIVAT?
         BE    RETURN           YES, FORGET IT
START4   L     R11,TAPESTKR   FIND THE TAPE UNIT ADDRESS TABLE
         LTR   R11,R11        IS THERE ONE?
         BZ    RETURN           NO, FORGET IT
         USING CSATABLE,R11
         CLC   =C'WTOETPS1',CSAEYECT  IS THIS THE CORRECT TABLE?
         BE    START3                   YES, USE IT
         EX    2,*                      NO, FIND OUT WHY
START3   LA    R11,HDRLEN(R11)   SKIP PAST THE TAPE TABLE HEADER
CKVOLSER CLC   =X'FFFFFFFF',0(R11) IS THIS THE END OF THE TABLE?
         BE    RETURN                YES, FORGET IT
         LH    R4,4(R11)           PICK UP THE NUMBER OF TABLE ENTRIES
         LA    R5,6(R11)           FIND THE FIRST TABLE ENTRY
CKVOLSE1 CLC   0(6,R5),0(R6)       IS THIS THE VOLUME SERIAL NUMBER?
         BE    FOUNDIT               YES, DISPLAY IT
         LA    R5,6(R5)              NO, TRY ANOTHER
         BCT   R4,CKVOLSE1         TRY ANOTHER VOL=SER=
         LR    R11,R5              SET UP THE NEW POINTER
         B     CKVOLSER            GO TO IT
         SPACE 1
FOUNDIT  CLC   =H'108',CTXTTLMX    ENOUGH ROOM FOR THE MESSAGE?
         BH    RETURN                NO, FORGET IT
         MVC   CTXTTLEN,CTXTTLMX   GIVE THE MESSAGE ENOUGH ROOM
         MVC   CTXTTMSG+(108-L'UNITMSG)(L'UNITMSG),UNITMSG
         MVC   CTXTTMSG+103(4),0(R11) MOVE THE UNIT ADDRESS
         OI    CTXTRFB1,CTXTRCMT      TELL COMMTASK TO CHANGE THE MSG
         SPACE
RETURN   LR    R1,R13       LOAD ADDRESS OF GOTTEN AREA           JWP
         L     R13,4(R13)   LOAD PREVIOUS SAVE AREA ADDRESS       JWP
         LA    R0,AREAREGS  LOAD SIZE OF GOTTEN AREA              JWP
         FREEMAIN RU,LV=(0),A=(1),SP=230 FREE THE GOTTEN AREA     JWP
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
         LTORG
UNITMSG  DC    C'  IT MAY STILL BE ON XXXX.'
COMMATBL DC    107X'00'        FIND THE INFAMOUS FIRST COMMA
         DC    C','
         DC    148X'00'
         SPACE
         CNOP  2,4
SMFSKEL  DS    0H        SMF RECORD TO BE WRITTEN
SMFLEN   DC    H'28'     RECORD LENGTH
SMFSEG   DC    H'0'      SEGMENT INDICATOR
SMFSIND  DC    X'06'     SYSTEM INDICATOR
SMFRECTY DC    AL1(200)  RECORD TYPE
SMFTIME  DS    XL4       TIME
SMFDATE  DS    XL4       DATE
SMFSID   DS    CL4       SYSTEM ID
SMFTYPE  DC    C'M'      M - MOUNT RECORD
SMFUNIT  DS    CL3       UNIT ADDRESS
SMFVOL   DS    CL6       VOLSER
SMFSLEN  EQU   *-SMFSKEL
         SPACE
         PRINT GEN
         SPACE 3
CSATABLE DSECT
CSAEYECT DS    D              TABLE EYE-CATCHER
CSALENG  DS    F              LENGTH OF THE TABLE
TAPETBL  DS    F              TAPE UNIT ENTRIES
         SPACE 1
USERCVT  DSECT
         DS    D              EYECATCHER 'USERCVT*'
TAPESTKR DS    F              ADDRESS OF TAPE CSA TABLE
         SPACE 1
WORKREGS DSECT
         SPACE
REGAREA  DS    18F       REGISTER SAVE AREA
         SPACE
         CNOP  2,4
SMFREC   DS    0H        SMF RECORD TO BE WRITTEN
SMFDLEN  DS    H         LENGTH
SMFDSEG  DS    H         SEGMENT INDICATOR
SMFDSIND DS    XL1       SYSTEM INDICATOR
SMFDRECT DS    XL1       RECORD TYPE
SMFDTIME DS    XL4       TIME
SMFDDATE DS    XL4       DATE
SMFDSID  DS    CL4       SYSTEM ID
SMFDTYPE DS    CL1       M - MOUNT RECORD
SMFDUNIT DS    CL3       UNIT ADDRESS
SMFDVOL  DS    CL6       VOLSER
         SPACE
AREAREGS EQU   *-WORKREGS
         EJECT
         IEZVX100
         PRINT NOGEN
         REGEQU
         CVT   DSECT=YES,LIST=YES
         END   WTOETPS2
/*
//LKED1   EXEC PGM=IEWL,PARM='LIST,LET,MAP,XREF,RENT,REFR',
//             COND=(8,LT,ASM)
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=*,DCB=(RECFM=FB,LRECL=121,BLKSIZE=1210)
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD  DDNAME=SYSIN
//SYSLMOD  DD  DSN=SYS1.LINKLIB,DISP=SHR
//SYSIN    DD *
  SETCODE AC(1)
  NAME WTOETPS2(R)
/*
