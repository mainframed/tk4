         MACRO
&L       CNVTHMS &I,&O,&W
.*       PDSDOC *TIME CONVERSION MACRO - EX PYCROFT...................*
.*             INPUT IS 4 BYTES 0HHMMSSF
.*             OUTPUT IS 8 BYTES PACKED SECONDS
.*             THIS MACRO REQUIRES AN 8 BYTE WORK AREA
&L       ZAP   &W,&I               COPY 0HHMMSSF
         NC    &W+4(3),=X'00000F'  ELIMINATE HHMM
         ZAP   &O,&W               INIT OUTPUT AS SS VALUE
         ZAP   &W,&I               REFRESH INPUT
         NC    &W+5(2),=X'F00F'    ELIMINATE MM
         OI    &W+5,X'0F'          SIGN AFTER HH
         MP    &W.(6),=P'3600'     CONVERT HH
         AP    &O,&W.(6)           ADD TO RESULT
         ZAP   &W,&I               REFRESH INPUT
         NC    &W+4(2),=X'000F'    ELIMINATE HH
         OI    &W+6,X'0F'          SIGN AFTER MM
         MP    &W.(7),=P'60'       CONVERT MM
         AP    &O,&W.(7)           ADD TO RESULT
         MEND
