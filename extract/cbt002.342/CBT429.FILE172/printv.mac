***********************************************************************
*                                                                     *
*        PDSDOC *REPORT PROCESSING SKELETON - VARIABLE LENGTH LINES...*
*        ON ENTRY  R0 -------> RDW + PRINT LINE                       *
*                                                                     *
* 25MAY00 DHC CORRECT LINE COUNTING                                   *
* 05NOV98 DHC ADDED HEADER VARIABLES                                  *
*                                                                     *
***********************************************************************
         SPACE 1
PRINTV   DS    0H                  WRITE REPORT
         SUBIN SAVE=(R0,R2,R3,R4)  SUBROUTINE ENTRY
         LR    R3,R0               SAVE DATA ADDRESS
         LA    R2,PRINTSW          GET MDCB ADDRESS
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN   TEST IF NEED TO WRITE
         BNO   PRINTVX             BRANCH IF NO
         L     R1,MDCBDCB          GET DCB ADDRESS
         AP    LINECOUN,ONE        ADD ONE TO LINE COUNTER
         CLI   4(R3),C' '          JUST ONE SKIP?
         BE    *+4+6               IF MORE,COUNT TWO
         AP    LINECOUN,ONE        ADD ONE TO LINE COUNTER
         CP    LINECOUN,MAXLINE    PAGE OVERFLOW ?
         BL    PRINTVY             NO, CARRY ON NORMALLY
         SP    LINECOUN,LINECOUN   YES, ZEROISE LINE COUNTER
         AP    PAGECOUN,ONE        ADD ONE TO PAGE COUNTER
         MVC   HEADPAGE,EDITPL2    MOVE IN EDIT MASK
         ED    HEADPAGE,PAGECOUN   SHOW PAGE NUMBER
         L     R0,ATITLE1          POINT AT PAGE HEADING
         GOSUB WRITEV              GO OUTPUT HEADING
         L     R0,ASUBTIT          POINT AT SECOND HEADING
         GOSUB WRITEV              GO OUTPUT IT
         LR    R0,R3               RESTORE DATA ADDRESS
         SPACE 1
PRINTVY  DS    0H                  HEADER PROCESSING ENDED
         GOSUB WRITEV              GO OUTPUT A DETAIL LINE
PRINTVX  DS    0H                  END OF PRINTING
         SUBOUT RESTORE=(R0,R2,R3,R4) RETURN TO CALLER
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
ATITLE1  DC    A(TITLE1)           ADDRESS OF DEAFULT TITLE
ASUBTIT  DC    A(SUBTITLE)         ADDRESS OF DEAFULT SUBTITLE
