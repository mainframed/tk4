***********************************************************************
*        PDSDOC *SUBROUTINE TO PERFORM A WRITE TO OPERATOR............*
*                                                                     *
*        ON ENTRY R0   =    LENGTH OF TEXT                            *
*                 R1 -----> LINE OF TEXT                              *
*                                                                     *
* 04MAR93 CART1 INITIAL CODE CREATED                                  *
*                                                                     *
***********************************************************************
         SPACE 1
SEWTO    DS    0H                  ISSUE WTO
         RIN   (R0,R1)             SUBROUTINE ENTRY
         LA    R4,125              SET MAXIMUM LENGTH
         CR    R4,R0               TEST LENGTH SUPPLIED
         BNL   *+4+2+4             SKIP OVER IF OK
         LTR   R4,R0               FITS, USE THE LENGTH SUPPLIED
         BZ    SEWTOX              NOTHING TO SAY, STAY DUMB
         MVI   MSGLINE,C' '        CLEAR TO BLANKS ..
         MVC   MSGLINE+1(L'MSGLINE-1),MSGLINE      .. ANY PREVIOUS MSG
         BCTR  R4,0                DECREMENT FOR EXECUTE
         EX    R4,SEWTOMVC         COPY THE TEXT TO WTO SKELETON
         SPACE 2
         SR    R0,R0               CLEAR REG. ZERO FOR WTO
         WTO   MF=(E,SEWTOMSG)     GO WRITE RESULTS ON CONSOLE
         ST    R1,SEWTOID          SAVE MESSAGE IDENT
         SPACE 1
SEWTOX   DS    0H                  END OF ROUTINE
         ROUT  ,                   RETURN TO CALLER
         SPACE 2
SEWTOMVC MVC   MSGLINE(*-*),0(R1)  MOVE MESSAGE TEXT          *EXECUTE*
         EJECT ,
***********************************************************************
*        WTO SKELETONS                                                *
***********************************************************************
         SPACE 1
SEWTOID  DC    F'0'                SAVE AREA FOR MESSAGE IDENTIFIER
         SPACE 1
         PUSH PRINT                SAVE PRINT STATUS
         PRINT ON,GEN              NEW PRINT STATUS
SEWTOMSG WTO   '                                                       X
                                                                       X
                             ',    MAX TEXT LENGTH (125)               X
               ROUTCDE=(11),       ROUTE TO PROGRAMMER                 X
               DESC=(7),MF=L       MESSAGE IS FROM APPLICATION PROGRAM
         SPACE 1
*        OFFSETS IN MESSAGE DATA LINE
         SPACE 1
MSGLINE  EQU   SEWTOMSG+4,125      MESSAGE LINE
MSGNUM   EQU   SEWTOMSG+4,8        MESSAGE NUMBER
MSGDATA  EQU   SEWTOMSG+4+10,115   MESSAGE TEXT
         SPACE 1
         POP PRINT                RESTORE PRINT STATUS
