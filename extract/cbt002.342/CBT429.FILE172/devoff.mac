DEVOFFL  TITLE 'SET DEVICE OFFLINE - PROMPT WITH RC=0'
DEVOFFL  CSECT
         PRINT ON,GEN
         SPACE 2
*
***********************************************************************
*        PDSDOC *VARY A DEVICE OFFLINE AND TELL OPC/A ABOUT IT........*
*                                                                     *
*        PROGRAM ID. :             DEVOFFL                            *
*                                                                     *
*        DATE WRITTEN :            JUL 1992                           *
*                                                                     *
*        AUTHOR :                  P. LAEDERACH / IFS                 *
*                                                                     *
*        LOCATION :                SPRECHER ENERGIE AG                *
*                                  OBERENTFELDEN  (CH)                *
*                                                                     *
*        ZWECK                                                        *
*        -----                                                        *
*                                                                     *
*        DIESES PROG UEBERNIMMT DIE DEVICE-ADDR AUS PARM-FELD         *
*        EINES JOBS UND VERSUCHT, DEN DEVICE OFFLINE ZU SETZEN.       *
*        DAS RESULTAT WIRD IN FORM VON RC'S MITGETEILT.               *
*                                                                     *
*   RC = 0   -->  DEVICE IS OFFLINE                                   *
*   RC = 4   -->  DEVICE IS OFFLINE, BUT USED BY A SYST-COMPONENT     *
*   RC = 8   -->  DEVICE STILL ONLINE (JOB OUT OF TIME)               *
*   RC =12   -->  UNABLE TO SET DEV OFFL (ZB PAGE-DS,SYSRES,CONS)     *
*   RC =16   -->  DEV-ADR IS WRONG (LENGHT =0 OR >3)                  *
*   RC =20   -->  DEV-ADR INVALID OR NO UCB EXISTS                    *
*                                                                     *
***********************************************************************
*
*        ----------------------------------------------------------
*           SAVE REGISTER IN SAVE AREA
*        ----------------------------------------------------------
         SAVE  (14,12),,*                SAVE REG'S 14,15,1-12 IN LSA
         LR    R12,R15                   R15 = PROGR. ANFANG
         LR    R10,R1                    R1  = ADR DER PARM.FELD-ADR
         USING DEVOFFL,R12
         GETMAIN R,LV=WORKSIZE,SP=0      GET SAVEAREA + WORKAREA
         LR    R11,R1                    PUT LOW-SA-ADRESS IN R11
         USING SAVEAREA,R11
         ST    R13,SAVE+4                ST HIGH-SA-ADRESS IN LOW-SA
         ST    R11,8(,R13)               ST LOW -SA-ADRESS IN HIGH-SA
         LR    R13,R11                   PUT LOW-SA-ADRESS IN R13
*
*        ----------------------------------------------------------
*           KONTROLLE, OB PARM.FELD NICHT LEER UND NICHT > 3
*        ----------------------------------------------------------
         SR    R8,R8
         SR    R9,R9
         L     R8,0(0,R10)               LAEDT PARM.FELD-ADR -> R8
         LH    R9,0(0,R8)                2BYTES (PARM.LAENGE) -> R9
         LTR   R9,R9                     IST PARM.LAENGE = 0 ?
         BC    8,ERROR4                  FALLS JA, GOTO ERROR
         CH    R9,=H'3'                  1ST OP. HIGH ? -> CC=2
         BC    2,ERROR4                  FALLS JA, GOTO ERROR
*
*        ----------------------------------------------------------
*           PARAMETER AUS PARM.FELD IN FELD 'DEVADR' UEBERTRAGEN
*        ----------------------------------------------------------
         BCTR  R9,0                      PARM.LAENGE - 1
         EX    R9,MOVE                   PARM.LAENGE -->  A(MOVE)
         MVC   CONSCMD+6(3),DEVADR       SAVE IN COMMAND
*
*        ----------------------------------------------------------
*           GET UCB-ADR
*        ----------------------------------------------------------
         GETAUTH TYPE=GET                AUTHORIZE FOR PRIV. INSTR
         SR    R6,R6
         SR    R7,R7
         TR    DEVADR,TBLA               TRANSLATE DEVICE ADDRESS
         PACK  PDEVADR,DEVADR            DEV.ADR(C'') -> PACKEN(P'')
         L     R6,PDEVADR+4              DIE RECHTEN 4 BYTES -> R6
         SRL   R6,4                      4 BITS->RECHTS, SHIFT OUT C
         STH   R6,HDEVADR                SAVE THIS ADDRESS
         GOSUB SUPUSER                   -> SUP.MODE, USER PROT.KEY
         LR    R5,R13                    SAVE R13 DURING IOSLOOK
         LA    R13,IOSLSAVE              SA-ADR FOR IOSLOOK-MAKRO
         IOSLOOK DEV=(6),UCB=(7)         MACRO, UCB-ADR RETURNED IN R7
         LR    R13,R5                    RESTORE PROGR SA-ADRESS
*        DC    A(0)                      ABEND S0C1
         LR    R9,R15                    SAVE RETURN CODE
         GOSUB PROBUSER                  -> PROBL MODE, USER PROT.KEY
         LTR   R15,R9                    IST RC=0 ?
         BC    2,ERROR5                  NEIN -> SET RC=20, NO UCB
*
*        ----------------------------------------------------------
*           GET UCB AND CHECK FLAG-BITS
*        ----------------------------------------------------------
         SR    R2,R2                     INTERVAL-ZAEHLER
TESTSTAT EQU   *
         USING UCBCMSEG,R7
         TM    UCBSTAT,UCBONLI           IST STATUS ONLINE?
         BO    ONLINE                    BIT ON, DEV ONL
         TM    UCBFL5,UCBNALOC           IS OFFL DEV IN USE BY SYSTEM?
         BO    ERROR1                    SET RC=4
         B     JOBEND                        RC=0  DEV OFFLINE ××
ONLINE   TM    UCBSTAB,UCBPGFL           UCB USED AS PAGE FILE?
         BO    ERROR3                    SET RC=12
         TM    UCBSTAT,UCBSYSR           IS DEV SYSRES OR ACT CONSOLE?
         BO    ERROR3                    SET RC=12
         TM    UCBSTAT,UCBCHGS           IS OFFLINE-CMD PENDING?
         BO    WAIT10
         CLI   FLAG,X'1'                 HAS CMD BEEN ISSUED?
         BE    WAIT10                    JA -> WAIT10
*
*        ----------------------------------------------------------
*           ISSUE VARY-CMD WITH MGCR-MACRO  (SUPERV. MODE KEY 0)
*        ----------------------------------------------------------
         SR    R0,R0
         GOSUB SUPKEY0                   -> SUPV. STATE, KEY 0
         MGCR  CONSCMD
         GOSUB PROBUSER                  -> PROBL. STATE, USER KEY
         MVC   FLAG,FLAG1                SET FLAG TO 1
*
*        ----------------------------------------------------------
*           WAIT 10''
*        ----------------------------------------------------------
WAIT10   EQU   *
         STIMER WAIT,BINTVL=WAITTIME
         LA    R2,1(0,R2)                INHALT VON R2 + 1
*
*        ----------------------------------------------------------
*           JOB OUT OF TIME ?    (RC=8, FALLS ELAPSED TIME > 30')
*                                 30' SIND CA 180 INTERVALLS
*        ----------------------------------------------------------
         CH    R2,=H'180'                1ST OP. HIGH ? -> CC=2
         BC    2,ERROR2                  SET RC=8
         B     TESTSTAT
*
*        ----------------------------------------------------------
*           ZUORDNUNG DER RETURN CODES
*        ----------------------------------------------------------
ERROR1   DS    0H
         MVC   CONDCODE,COND4
         B     JOBEND
         SPACE 1
ERROR2   DS    0H
         MVC   CONDCODE,COND8
         B     JOBEND
         SPACE 1
ERROR3   DS    0H
         MVC   CONDCODE,COND12
         B     JOBEND
         SPACE 1
ERROR4   DS    0H                        PARM IST =0 OR >3
         MVC   CONDCODE,COND16
         B     JOBEND
         SPACE 1
ERROR5   DS    0H                        IOSLOOK GIBT RC=4, -> DEVADR
         MVC   CONDCODE,COND20           NOT FOUND OR NO UCB EXISTS
         B     JOBEND
         SPACE 1
JOBEND   EQU   *
         GETAUTH TYPE=OFF                STANDARD AUTHORIZATION
*
*        ----------------------------------------------------------
*           FREEMAIN SAVE-AREA FROM EXIT AND RETURN TO CALLER
*        ----------------------------------------------------------
         LR    R1,R13                    L A(NEW SA) IN R1 FOR FREEMAIN
         L     R13,SAVE+4                L A(OLD SA) IN R13
         LH    R15,CONDCODE              LOAD COND CODE IN REG 15
         LR    R11,R15                   SAVE REG 15 DURING FREEMAIN
         FREEMAIN R,A=(1),LV=WORKSIZE,SP=0
         LR    R15,R11                   SAVE REG 15 DURING FREEMAIN
         RETURN (14,12),RC=(15)          RETURN WITH COND CODE IN R15
         SPACE 10
*
*
*
*
*
*        ----------------------------------------------------------
*          PARAMETER LIST
*        ----------------------------------------------------------
         DS    0F
CONSCMD  DC    X'0'
         DC    AL1(TOKEN-CONSCMD)
         DC    X'0000'
         DC    C'V ADR,OFFLINE'
TOKEN    EQU   *
         DS    0F
IOSLSAVE DC    16F'0'                    SAVE-AREA FOR IOSLOOK-MAKRO
         DS    0D
PDEVADR  DC    PL8'0'
HDEVADR  DC    H'0'                BINARY DEVICE ADDRESS
DEVADR   DC    CL3'0'
         DS    0F
CONDCODE DC    H'0'
COND4    DC    H'4'
COND8    DC    H'8'
COND12   DC    H'12'
COND16   DC    H'16'
COND20   DC    H'20'
FLAG     DC    X'0'
FLAG1    DC    X'1'
WAITTIME DC    F'1000'             INTERVALL = 1000*1/100S
*
HEXADDR  DC    XL4'00000000'       DEVICE ADDRESS
*
TBLA     DS    0F                  VALID DISPLAY-HEX CHARACTER TABLE
         DC    193X'00'            HEX 00 TO C0
         DC    XL6'FAFBFCFDFEFF'       C1 TO C6 (ALPHA A-F)
         DC    41X'00'                 C7 TO EF
         DC    CL10'0123456789'        F0 TO F9 (NUMERIC 0-9)
         DC    6X'00'                  FA TO FF
*
*
*       ----------------------------------------------------------
*          EXECUTE ANWEISUNG
*       ----------------------------------------------------------
MOVE     MVC   DEVADR(0),2(R8)     FELD ÖA(R7)+2! IN LAENGE
*                                  R9 NACH DEVADR UEBERTRAGEN
*
*       ----------------------------------------------------------
*         ROUTINE TO GET INTO SUPERVISOR MODE, PROTECT KEY ZERO
*       ----------------------------------------------------------
SUPKEY0  DS    0H                  SET SUPERVISOR MODE
         SUBIN ,                   SUBROUTINE ENTRY
         MODESET MODE=SUP,KEY=ZERO GET INTO SUPERVISOR MODE,PK0
         SUBOUT ,                  SUBROUTINE EXIT
*
*       ----------------------------------------------------------
*         ROUTINE TO GET INTO SUPERVISOR MODE, USER PROTECT KEY
*       ----------------------------------------------------------
SUPUSER  DS    0H                  SET SUPERVISOR MODE
         SUBIN ,                   SUBROUTINE ENTRY
         MODESET MODE=SUP,KEY=NZERO GET INTO SUPERVISOR MODE,USER PK
         SUBOUT ,                  SUBROUTINE EXIT
*
*       ----------------------------------------------------------
*         ROUTINE TO GET INTO PROBLEM STATE, USER PROTECT KEY
*       ----------------------------------------------------------
PROBUSER DS    0H                  SET PROBLEM PROGRAM MODE
         SUBIN ,                   SUBROUTINE ENTRY
         MODESET MODE=PROB,KEY=NZERO GET OUT OF SUPERVISOR MODE
         SUBOUT ,                  SUBROUTINE EXIT
*
*       ----------------------------------------------------------
*          REGISTER EQUATES AND USAGES.
*       ----------------------------------------------------------
R0       EQU   0
R1       EQU   1    POINTER ZU ADR VON PARM.FELD, LOW-SA (GETMAIN)
R2       EQU   2
R3       EQU   3    INTERVAL-DURCHGANGSZAEHLER
R4       EQU   4
R5       EQU   5
R6       EQU   6    UEBERGIBT DEV.ADR AN MACRO IOSLOOK
R7       EQU   7    BEKOMMT UCB-ADR VON MACRO IOSLOOK
R8       EQU   8    PARM.FELD
R9       EQU   9    LAENGE DES PARMFELDES
R10      EQU   10   POINTER ZU ADR VON PARM.FELD
R11      EQU   11   BR DSECT LOW-SA
R12      EQU   12   BR PROGR
R13      EQU   13   HIGH-SA, LOW-SA
R14      EQU   14
R15      EQU   15   PROGR-ANFANG, RC'S
*
*       ----------------------------------------------------------
*           DSECTS
*       ----------------------------------------------------------
SAVEAREA DSECT
SAVE     DS    18F
SAVESIZE EQU   *-SAVEAREA                GROESSE DER SA
         DS    0D                        AUF NAECHSTE D-GRENZE
WORKSIZE EQU   *-SAVEAREA                WORKSIZE HIER = 24 BYTE
         EJECT
*
*        PRINT NOGEN
*
UCB      DSECT                           DSECT FUER UCB
         IEFUCBOB ,
         EJECT
CVT      DSECT                           DSECT FUER CVT
         CVT DSECT=YES
         EJECT
IOCOM    DSECT                           DSECT FUER IOCOM
         IECDIOCM ,
         EJECT
*
         CSECT
         END
