DEMEMBER TITLE 'DELETE MEMBERS SPECIFIED BY PARM FIELD'
***********************************************************************
*        PDSDOC *DELETE MEMBERS WITH NON-STANDARD NAMES...............*
*                                                                     *
*        PROGRAM ID. :             DEMEMBER                           *
*                                                                     *
*        DATE WRITTEN :            JANUARY   2000                     *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*        COPYRIGHT:                AGCO UK LTD.                       *
*                                  LICENCE IS GRANTED FOR FREE        *
*                                  DISTRIBUTION WITHIN THE TERMS OF   *
*                                  USE OF THE CBT TAPE.               *
*                                                                     *
*        LOCATION :                DESFORD, LEICESTERSHIRE            *
*                                  ENGLAND                            *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 24JAN00  DC   STARTED WRITING                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS PROGRAM WILL DELETE THE MEMBER OF SYSUT2 NAMED IN THE PARM     *
* FIELD EVEN IF THAT NAME DOES NOT CONFORM TO NORMAL RULES I.E.       *
* UPPERCASE A-Z, 0-9 OR NATIONAL CHARACTERS. THIS IS REQUIRED FOR     *
* HOUSEKEEPING ON IMS FORMAT DATASETS.                                *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
* ONLY ONE MEMBER MAY BE PROCESSED FOR EACH INVOCATION OF THIS PROGRAM*
*                                                                     *
* THE USER MUST HAVE EXCLUSIVE CONTROL OF SYSUT2, OTHERWISE A 213-30  *
* ABEND MAY BE EXPERIENCED.                                           *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* 2)PARM= THE NAME OF A MEMBER OF SYSUT2 TO BE DELETED.               *
*         1 TO 8 CHARACTERS, IN QUOTES IF NECESSSARY.                 *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* 1)SYSUT2 - DEFINES THE PDS FROM WHICH MEMBERS WILL BE DELETED       *
*                                                                     *
* 2)SYSPRINT - MESSAGE DATASET. VBA RECORDS.                          *
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
DEMEMBER CSECT ,                   MAIN CSECT
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   WORK
R2       EQU   2                   DCB TABLE POINTER
REG2     EQU   R2                  WORK REGISTER IN 'SCAN' MACRO
R3       EQU   3                   .
R4       EQU   4                   LINKAGE RETURN, WORK
R5       EQU   5                   .
R6       EQU   6                   .
R7       EQU   7                   .
R8       EQU   8                   .
R9       EQU   9                   .
R10      EQU   10                  .
R11      EQU   11                  .
R12      EQU   12                  BASE REGISTER 2
R13      EQU   13                  SAVE AREA / BASE REG. 1
R14      EQU   14                  .
R15      EQU   15                  .
         SPACE 1
***********************************************************************
*        MAIN PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 2
         PGMIN EQUATES=NO,USING=(R13,R12) STANDARD HOUSEKEEPING
         L     R5,0(R1)            LOAD ADDRESS OF PARM INTO R5
         ST    R5,PARMADDR         STORE ADDRESS OF PARM INFO'S
         GOSUB INITIAL             PERFORM INITIALISATION
         GOSUB PARMPROC            CHECK IF PARM SUPPLIED
         ICM   R4,B'0011',RETCODE  OK SO FAR?
         BNZ   STOPPGM             NO, BOMB OUT
         LA    R1,MEMBNAME         SET MEMBER POINTER
         LA    R2,SYSUT2SW         POINT TO SYSUT2
         BAL   R4,SCRATCH          GO DELETE MEMBER
         EJECT ,
***********************************************************************
*        END OF PROCESSING. TIDY UP, REPORT AND LEAVE.                *
***********************************************************************
         SPACE 1
STOPPGM  DS    0H                  NOW SHUTTING DOWN
         SPACE 1
         GOSUB CLOSEM              GO CLOSE ALL FILES
         SPACE 2
ENDOFPGM DS    0H                  NOW SHUTTING DOWN
         PGMOUT RC=RETCODE         EXIT PROGRAM
         TITLE 'INITIALISATION PROCESSING'
***********************************************************************
*        OPEN SUPPLIED DD CARDS                                       *
***********************************************************************
         SPACE 1
INITIAL  DS    0H                  PROGRAM INITIALISATION
         RIN   ,                   SUBROUTINE ENTRY
         SR    R1,R1               ASK FOR CURRENT TIME
         GOSUB TODDATE             GO FORMAT TOD-CLOCK
         DATE  HEADDATE,FORMAT='DD MMM YY',FROM=TODPJUL FORMAT DATE ..
         MVC   HEADTIME,TODTIME    .. AND TIME IN HEADING
         BAL   R4,OPENEM           GO OPEN SUPPLIED DD CARDS
         SPACE 2
***********************************************************************
*        GET DETAILS OF SYSUT2                                        *
***********************************************************************
         SPACE 2
         LA    R2,SYSUT2SW         POINT TO SYSUT2
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         MVC   DSNDDN,MDCBDDNM     COPY DDNAME
         TM    MDCBFLAG,DDPRES     IS IT PRESENT?
         BNO   INITIAL2            NO, NOTHING TO REPORT
         GOSUB JFCBREAD            GO GET DETAILS
         MVC   DSNDSN,JFCBDSNM     COPY DATASET NAME
         MVC   DSNVOL,JFCBVOLS     COPY FIRST VOLUME SERIAL
         LA    R0,DSNLINE1         POINT TO MESSAGE
         GOSUB PRINTV              OUTPUT INFORMATION LINE
         SPACE 1
INITIAL2 DS    0H                  CARRY ON WITH INITIALISATION
         LA    R0,RPTAHDR2         POINT TO BLANK LINE
         GOSUB PRINTV              OUTPUT SPACES
         SPACE 3
INITIALX DS    0H                  END OF INITIALISATION
         ROUT  ,                   RETURN TO CALLER
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         TITLE 'SUB - ROUTINES'
*******************************************************************
*        PROCESS PARM FIELD, CHECK FOR MEMBER                     *
*******************************************************************
         SPACE 1
PARMPROC DS    0H                  PROCESS PARM FIELD
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         L     R5,PARMADDR         RESTORE ADDRESS OF PARM INFO'S
         LA    R2,SYSUT2SW         POINT TO SYSUT2
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN   TEST IF POSSIBLE
         BNO   ERROR01             BRANCH IF NO
         LH    R6,0(R5)            GET LENGTH OF PARM INFO'S
         ST    R6,PARMLGTH         SAVE PARM LENGTH
         LTR   R6,R6               TEST FOR A PARM
         BZ    ERROR02             NONE, ERROR HANDLE
         CH    R6,=H'8'            CHECK LENGTH
         BH    ERROR03             LONG, ERROR HANDLE
         BCTR  R6,0                DECREMENT FOR EXECUTE
         EX    R6,MEMBMOVE      MOVE MEMBER NAME
*
*        AS STOW WILL DETECT THAT THE MEMBER DOES NOT EXIST THE
*        FOLLOWING CODE IS REDUNDANT, BUT MAY BE LEFT FOR POSTERITY.
*
*        B     PARMDONE            SKIP REDUNDANT CODE
         SPACE 1
         ICM   R1,B'0111',MDCBDCB+1  GET DCB ADDRESS
         BLDL  (1),BLDLLIST     DOES MEMBER NAME EXIST?
         LTR   R15,R15          CHECK RETCODE
         BNZ   ERROR04          NO, INVALID PARM
         B     PARMDONE            FINISHED PROCESSING PARM
         SPACE 1
PARMCOMP DS    0H                  ISSUE INFORMATION MESSAGE
         GOSUB PRINTV              OUTPUT VARIABLE LENGTH LINE
         SPACE 1
PARMDONE DS    0H                  PARM FIELD PROCESSED
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         SPACE 2
MEMBMOVE MVC   MEMBNAME(0),2(R5) MOVE MODULE NAME
BLDLLIST DC    H'1',H'12'      NUMBER AND SIZE OF ENTRIES
MEMBNAME DC    CL8' '          NAME OF MODULE
TTRC     DC    CL12' '         TTRC AREA
         EJECT ,
***********************************************************************
*                                                                     *
* ERROR HANDLING                                                      *
*                                                                     *
***********************************************************************
         SPACE 1
ERROR01  DS    0H                  SYSUT2 NOT SUPPLIED
         LA    R0,PARMLN01         POINT TO MESSAGE
         MVC   RETCODE,=H'64'      SET ERROR CODE
         B     PARMCOMP            GO OUTPUT MESSAGE
         SPACE 1
ERROR02  DS    0H                  PARM NOT SPECIFIED
         LA    R0,PARMLN02         POINT TO MESSAGE
         MVC   RETCODE,=H'65'      SET ERROR CODE
         B     PARMCOMP            GO OUTPUT MESSAGE
         SPACE 1
ERROR03  DS    0H                  PARM FIELD TOO LONG
         MVC   RETCODE,=H'66'      SET ERROR CODE
         LA    R0,PARMLN03         POINT TO MESSAGE
         B     PARMCOMP            GO OUTPUT MESSAGE
         SPACE 1
ERROR04  DS    0H                  MEMBER NOT FOUND
         MVC   RETCODE,=H'67'      SET ERROR CODE
         LA    R0,PARMLN04         POINT TO MESSAGE
         MVC   PARML4M,2(R5)       COPY IN MEMBER NAME
         B     PARMCOMP            GO OUTPUT MESSAGE
         EJECT ,
***********************************************************************
*        INFORMATION LINES FOR PARM PROCESSING                        *
***********************************************************************
         SPACE 2
*                                                             OFFSET
PARMLN01 DC    AL2(PARML1L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL1' '              PADDING                      5
         DC    CL26'SYSUT2 COULD NOT BE OPENED' INFORMATION     6
         SPACE 1
PARML1L  EQU   *-PARMLN01          LENGTH OF DETAIL RECORD     32
         SPACE 2
*                                                             OFFSET
PARMLN02 DC    AL2(PARML2L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL1' '              PADDING                      5
         DC    CL16'NO PARM SUPPLIED' INFORMATION               6
         SPACE 1
PARML2L  EQU   *-PARMLN02          LENGTH OF DETAIL RECORD     22
         SPACE 2
*                                                             OFFSET
PARMLN03 DC    AL2(PARML3L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL1' '              PADDING                      5
         DC    CL27'PARM TOO LONG >8 CHARACTERS' INFORMATION    6
         SPACE 1
PARML3L  EQU   *-PARMLN03          LENGTH OF DETAIL RECORD     33
         SPACE 2
*                                                             OFFSET
PARMLN04 DC    AL2(PARML4L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
PARML4M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL26'MEMBER NOT FOUND IN SYSUT2'    INFORMATION 22
         SPACE 1
PARML4L  EQU   *-PARMLN04          LENGTH OF DETAIL RECORD     48
         EJECT ,
***********************************************************************
*                                                                     *
*        DELETE A MEMBER                                              *
*                                                                     *
* THIS CODE EXTRACTED FROM 'PDSPROGM' OFF THE CBT TAPE WITH SOME MODS *
* WRITTEN BY BILL GODFREY,  PLANNING RESEARCH CORPORATION.            *
* INSTALLATION; PRC COMPUTER CENTER INC, MCLEAN VA.                   *
* WRITTEN MAY 24 1979, UPDATED JUNE 16 1980.                          *
*                                                                     *
* ON ENTRY;                                                           *
*        R1 --> 8 BYTE MEMBER NAME TO DELETE                          *
*        R2 --> MDCB FOR A PDS                                        *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE
SCRATCH  DS    0H                  DELETE MEMBER
         SUBIN SAVE=(R1,R2,R4)     SUB-ROUTINE ENTRY
         LR    R0,R1               SET MEMBER POINTER
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         ICM   R1,B'0111',MDCBDCB+1  GET DCB ADDRESS
         SPACE 1
         STOW  (1),(0),D           TRY TO DELETE THE MEMBER
         SPACE 1
         STH   R15,RETCODE         SAVE COMPLETION CODE
         B     STOWDR(R15)
STOWDR   B     STOWDR00
         B     STOWDR04
         B     STOWDR08
         B     STOWDR0C
         B     STOWDR10
         B     STOWDR14
         B     STOWDR18
         SPACE 2
STOWDR00 DS    0H                  MEMBER SUCCESSFULLY DELETED
         LA    R0,STOWLN01         POINT TO MESSAGE
         MVC   STOWL1M,MEMBNAME    COPY IN MEMBER NAME
         B     STOWCOMP            GO OUTPUT MESSAGE
         SPACE 1
STOWDR04 DS    0H                  INVALID RETURN CODE
         LA    R0,STOWLN02         POINT TO MESSAGE
         MVC   STOWL2M,MEMBNAME    COPY IN MEMBER NAME
         B     STOWCOMP            GO OUTPUT MESSAGE
         SPACE 1
STOWDR08 DS    0H                  MEMBER NOT FOUND
         LA    R0,STOWLN03         POINT TO MESSAGE
         MVC   STOWL3M,MEMBNAME    COPY IN MEMBER NAME
         B     STOWCOMP            GO OUTPUT MESSAGE
         SPACE 1
STOWDR0C DS    0H                  LOGICAL ERROR
         LA    R0,STOWLN04         POINT TO MESSAGE
         MVC   STOWL4M,MEMBNAME    COPY IN MEMBER NAME
         B     STOWCOMP            GO OUTPUT MESSAGE
         SPACE 1
STOWDR10 DS    0H                  I/O  ERROR
         LA    R0,STOWLN05         POINT TO MESSAGE
         MVC   STOWL5M,MEMBNAME    COPY IN MEMBER NAME
         B     STOWCOMP            GO OUTPUT MESSAGE
         SPACE 1
STOWDR14 DS    0H                  DCB  ERROR
         LA    R0,STOWLN06         POINT TO MESSAGE
         MVC   STOWL6M,MEMBNAME    COPY IN MEMBER NAME
         B     STOWCOMP            GO OUTPUT MESSAGE
         SPACE 1
STOWDR18 DS    0H                  INSUFFICIENT STORAGE
         LA    R0,STOWLN07         POINT TO MESSAGE
         MVC   STOWL7M,MEMBNAME    COPY IN MEMBER NAME
         B     STOWCOMP            GO OUTPUT MESSAGE
         SPACE 1
STOWCOMP DS    0H                  ISSUE INFORMATION MESSAGE
         GOSUB PRINTV              OUTPUT VARIABLE LENGTH LINE
         SPACE 1
SCRATCHX DS    0H                  END OF MEMBER PROCESSING
         SUBOUT RESTORE=(R1,R2,R4) RETURN TO CALLER
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        INFORMATION LINES FOR STOW PROCESSING                        *
***********************************************************************
         SPACE 2
*                                                             OFFSET
STOWLN01 DC    AL2(STOWL1L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
STOWL1M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL22'SUCCESSFULLY SCRATCHED' INFORMATION        22
         SPACE 1
STOWL1L  EQU   *-STOWLN01          LENGTH OF DETAIL RECORD     44
         SPACE 2
*                                                             OFFSET
STOWLN02 DC    AL2(STOWL2L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
STOWL2M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL25'LOGICAL ERROR. STOW RC=04'     INFORMATION 22
         SPACE 1
STOWL2L  EQU   *-STOWLN02          LENGTH OF DETAIL RECORD     47
         SPACE 2
*                                                             OFFSET
STOWLN03 DC    AL2(STOWL3L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
STOWL3M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL22'** MEMBER NOT FOUND **'        INFORMATION 22
         SPACE 1
STOWL3L  EQU   *-STOWLN03          LENGTH OF DETAIL RECORD     44
         SPACE 2
*                                                             OFFSET
STOWLN04 DC    AL2(STOWL4L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
STOWL4M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL25'LOGICAL ERROR. STOW RC=12'     INFORMATION 22
         SPACE 1
STOWL4L  EQU   *-STOWLN04          LENGTH OF DETAIL RECORD     47
         SPACE 2
*                                                             OFFSET
STOWLN05 DC    AL2(STOWL5L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
STOWL5M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL17'*** I/O ERROR ***'             INFORMATION 22
         SPACE 1
STOWL5L  EQU   *-STOWLN05          LENGTH OF DETAIL RECORD     39
         SPACE 2
*                                                             OFFSET
STOWLN06 DC    AL2(STOWL6L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
STOWL6M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL17'*** DCB ERROR ***'             INFORMATION 22
         SPACE 1
STOWL6L  EQU   *-STOWLN06          LENGTH OF DETAIL RECORD     39
         SPACE 2
*                                                             OFFSET
STOWLN07 DC    AL2(STOWL7L)        LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL8' '              PADDING                      5
STOWL7M  DC    CL8' '              MEMBER NAME                 13
         DC    CL1' '              PADDING                     21
         DC    CL43'** INSUFFICIENT STORAGE. INCREASE REGION **' 22
         SPACE 1
STOWL7L  EQU   *-STOWLN07          LENGTH OF DETAIL RECORD     65
         SPACE 2
STOWOK   DC    F'0'                COUNT OF MEMBERS DELETED
STOWNOK  DC    F'0'                COUNT OF FAILED DELETES
         TITLE 'COPY BLOCKS'
         COPY  BIOS                STANDARD I/O PROCESSING
         EJECT ,
         SPACE 2
DEMEMBER CSECT ,                   MAIN CSECT
         EJECT ,
         COPY  WRITEV              STANDARD I/O
         SPACE 2
         COPY  PUTMOVE             STANDARD I/O
         SPACE 2
         COPY  PRINTV              VARIABLE LENGTH PRINT LINES
         EJECT ,
         COPY  TODDATE             STANDARD DATE FORMATTING
         TITLE 'DATA AREAS'
BEGSNAP  DS    0D                  SNAP DATA AREAS FOLLOWING
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 2
         DS    0F                  GET ADDRESSES ALIGNED
EYECATCH DC    CL8'EYECATCH'       EYE CATCHER
         SPACE 1
DWORK    DC    D'0'                DOUBLEWORD WORK AREA
ZEROWORD DC    F'0'                A LOT OF NOTHING
COMPCODE DC    H'0'                PROGRAM COMPLETION CODE
RETCODE  DC    H'0'                MACRO RETURN CODE SAVE AREA
         SPACE 1
PARMADDR DC    A(0)                ADDRESS OF PARM FIELD
PARMLGTH DC    A(0)                LENGTH OF PARM FIELD
         SPACE 1
CONVWORK DC    CL15' '             DECIMAL WORK AREA
ONE      DC    PL1'1'              COUNT INCREMENT
SIGN     EQU   X'F0'               POSITIVE PACKED SIGN
         SPACE 1
         RIN   GENERATE            SUBROUTINE SAVE AREA
         TITLE 'REPORT LINES'
***********************************************************************
*        REPORT LINE SKELETONS                                        *
***********************************************************************
         SPACE 1
TITLE1   EQU   *                   STANDARD NAME
RPTAHDR1 DC    AL2(RPTAH1LN)       LENGTH OF HEADER RECORD     CC
         DC    AL2(0)
RPTAEJCT DC    C'1'                SKIP TO TOP OF FORM          1
         DC    CL13' '             PADDING                      2
RPTAFAST DC    CL4' '              RUN TYPE                    15
         DC    CL1' '              PADDING                     19
         DC    CL35'DELETE MEMBERS FROM PARM FIELD'      TITLE 20
         DC    CL10' '             PADDING                     55
HEADDATE DC    CL9' '              REPORT DATE                 65
         SPACE 1
RPTAH1LN EQU   *-RPTAHDR1          LENGTH OF HEADER RECORD     74
         SPACE 1
         DC    CL8' '              PADDING                     75
HEADTIME DS    0CL5                TIME OF REPORT              83
HEADHH   DC    CL2' '              HOUR OF REPORT              83
         DC    CL1'.'              SEPARATOR                   84
HEADMM   DC    CL2' '              MINUTE OF REPORT            85
         DC    CL6' '              PADDING                     87
         DC    CL4'PAGE'                                       23
HEADPAGE DC    CL4'0000'           PAGE NUMBER                127
         SPACE 2
SUBTITLE EQU   *                   STANDARD NAME, DUMMY RECORD
RPTAHDR2 DC    AL2(RPTAH2LN)       LENGTH OF TITLE  RECORD     CC
         DC    AL2(0)
         DC    C'-'                ASA SKIP 3                   1
         DC    CL1' '              PADDING                      2
         SPACE 1
RPTAH2LN EQU   *-RPTAHDR2          LENGTH OF HEADER RECORD      .
         EJECT ,
***********************************************************************
*        HERE ARE STATISTICS MESSAGES SKELETONS                       *
***********************************************************************
         SPACE 1
*                                                             OFFSET
DSNLINE1 DC    AL2(DSNL1L)         LENGTH OF DETAIL RECORD      0
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
DSNDDN   DC    CL8' '              DDNAME                       5
         DC    CL8' IS DSN='       PADDING                     13
DSNDSN   DC    CL44' '             DATASET NAME                21
         DC    CL14' ON VOLUME = ' PADDING                     65
DSNVOL   DC    CL6' '              FIRST VOLUME SERIAL         79
         SPACE 1
DSNL1L   EQU   *-DSNLINE1          LENGTH OF DETAIL RECORD     81
         SPACE 1
*                                                             OFFSET
REPTCOUN DC    PL2'0'              COUNT OF REPORTS
PAGECOUN DC    PL2'0'              COUNT OF PAGES
LINECOUN DC    PL2'60'             COUNT OF LINES
MAXLINE  DC    PL2'56'             MAXIMUM NUMBER OF LINES PER PAGE
         SPACE 1
EDITPL2  DC    X'40202120'         COPIES EDIT MASK
EDITPL4  DC    X'4020202020202120' COUNT EDIT MASK
EDITPL8  DC    X'40202020202020202020202020202120' COUNT EDIT MASK
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         TITLE 'DCB''S AND I/O CONTROL TABLES'
***********************************************************************
*        DCB'S TABLE                                                  *
***********************************************************************
         SPACE 1
DCBTABLE DS    0F
SYSUT2SW DC    X'00',AL3(SYSUT2),CL8'SYSUT2',CL8'OUTPUT',CL8'LEAVE'
         DC    PL8'0'              RECORD COUNT
DCBNTRY  EQU   *-DCBTABLE          LENGTH OF ONE TABLE ELEMENT
         SPACE 1
PRINTSW  DC    X'00',AL3(SYSPRINT)
         DC    CL8'SYSPRINT'
         DC    CL8'OUTPUT'
         DC    CL8'LEAVE'
         DC    PL8'0'              RECORD COUNT
         SPACE 1
OPENSIZE EQU   *-DCBTABLE          TABLE SIZE OF FULL TIME FILES
MAXOPEN  EQU   OPENSIZE/DCBNTRY    MAXIMUM NUMBER OF DCB'S TO OPEN
         SPACE 1
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUM NUMBER OF DCB'S
         SPACE 2
         EJECT ,
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
***********************************************************************
*        DATA CONTROL BLOCKS                                          *
***********************************************************************
         SPACE 1
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PO,                                               *
               EXLST=EXITLIST,                                         *
               MACRF=W
         SPACE 2
SYSPRINT DCB   DDNAME=SYSPRINT,                                        *
               DSORG=PS,                                               *
               RECFM=VBA,LRECL=137,                                    *
               EXLST=EXITLIST,                                         *
               MACRF=PM
         SPACE 2
SNAPDCB  DCB   DDNAME=SNAP,DSORG=PS,MACRF=W,                           *
               RECFM=VBA,LRECL=125,BLKSIZE=882
EOSNAP   DS    0H                  END OF STORAGE AREA TO BE SNAPPED
         SPACE 3
         POP   PRINT               RESTORE PRINT STATUS
         END   ,
