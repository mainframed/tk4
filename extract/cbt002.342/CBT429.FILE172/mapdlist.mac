MAPDLIST TITLE 'MAPDISK LISTING PROGRAM'
*
*        PDSDOC *PRINT MAPDISK SUMMARY RECORDS IN MAPDISK FORMAT......*
*
* ..OCT90 CART1 WROTE PROGRAM                                      R1.0
* ..MAY91 CART1 ADD OPTION OF STATS OR VOLUME REPORT               R2.0
* 22MAY92 CART1 CHANGED FORMAT OF SUMMARY RECORD                   R2.1
* 25MAY92 CART1 GIVE ALLOCATION TOTALS IN END MESSAGE              R2.2
*
*
*
*
*
*
*********************************************************************
*
*         REGISTER EQUATES
*
*********************************************************************
R0       EQU   0                   SYSTEMS
R1       EQU   1                   SYSTEMS
R2       EQU   2                   WORK & TEMP
R3       EQU   3                   WORK & TEMP
R4       EQU   4                   SUB-ROUTINE LINKAGE
R5       EQU   5                   WORK & TEMP
R6       EQU   6
R7       EQU   7
R8       EQU   8                   PRINT LINE POINTER
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                  SAVE AND BASE #1
R14      EQU   14                  RETURN
R15      EQU   15
         EJECT
*********************************************************************
*
*         INITIALIZATION
*
*********************************************************************
MAPDLIST CSECT ,
         PRINT GEN
         PGMIN USING=(R13),EQUATES=NO STANDARD HOUSEKEEPING
         EJECT
*******************************************************************
*        SAVE ADDRESS AND LENGTH OF PARM OF EXEC CARD             *
*******************************************************************
         SPACE 1
         L     R1,0(R1)            LOAD ADDRESS OF PARM INTO R1
         ST    R1,PARMADDR         STORE ADDRESS OF PARM INFO'S
         LH    R2,0(R1)            GET ADDRESS OF PARM INFO'S
         ST    R2,PARMLGTH         SAVE PARM LENGTH
         LTR   R2,R2               TEST FOR A PARM
         BZ    GETDATE             NONE, FORGET IT
         BCTR  R2,0                DECEMENT FOR EXECUTE
         EX    R2,PARMCLC          TEST FOR PARM=STAT(S)
         BNE   GETDATE             NO, DEFAULT IS VOLUME
         OI    PARMSWCH,PARMSTAT   YES, SET FLAG
         B     GETDATE             GO ROUND EXECUTED INSTRUCTION
         SPACE 1
PARMCLC  CLC   2(*-*,R1),STATPARM  TEST FOR PARM=STAT(S)   *EXECUTED*
         EJECT
*********************************************************************
*        DATE ROUTINE
*********************************************************************
         SPACE 1
GETDATE  DS    0H                  GET TIME AND DATE OF RUN
         SR    R1,R1               ASK FOR CURRENT DATE
         GOSUB TODDATE             GO FORMAT TOD-CLOCK
         DATE  HEADDATE,FORMAT='DD MMM YY',FROM=TODPJUL FORMAT DATE ..
         MVC   HEADTIME,TODTIME    .. AND TIME IN HEADING
         EJECT
*********************************************************************
*         OPEN FILES
*********************************************************************
         SPACE 2
         LA    R3,MAXOPEN          SET NUMBER OF FILES TO OPEN
         LA    R2,SYSUT1SW         POINT AT MDCB LIST
         USING MDCB,R2             ESTABLISH ITS ADDRESSSABILITY
OPENLOOP DS    0H                  OPEN ALL FILES
         OI    MDCBFLAG,DDINT      EXPRESS INTEREST
         BAL   R4,TESTIOT          GO FIND IF IT IS PRESENT
         BAL   R4,OPEN             GO OPEN IT
         LA    R2,DCBNTRY(R2)      INCREMENT POINTER
         BCT   R3,OPENLOOP         GO ROUND ALL FILES
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         EJECT
*********************************************************************
*
*        MAINLINE PROCESSING
*        READ AND PRINT SUMMARY RECORDS
*
*********************************************************************
         SPACE 2
         BAL   R14,HEADING         GO OUTPUT HEADING
         SPACE 1
MAINLOOP DS    0H                  MAINLINE PROCESSING
         BAL   R4,SYSUT1RD         READ A RECORD
         BAL   R4,SUMMARY          COPY DATA TO REPORT
         LA    R8,OUTBIN           POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
         MVI   DATANAME,BLANK      CLEAR PRINT LINE
         MVC   DATANAME+1(132),DATANAME  CLEAR PRINT LINE
         BAL   R14,LINETEST        TEST PAGE EJECT
         B     MAINLOOP            GO ROUND UNTIL EOF
         EJECT
*********************************************************************
*
*        ALL VOLUMES PROCESSED. TIDY UP AND LEAVE.
*
*********************************************************************
         SPACE 2
ALLDONE  DS    0H                 END OF PROCESSING
         LA    R8,DONEMSG          POINT TO OUTPUT LINE
         TM    PARMSWCH,PARMSTAT   DO WE WANT TOTALS?
         BO    ALLDONE1            NO,USE DEFAULT MESSAGE
         LA    R15,DONEMEND        POINT TO TEXT AREA
         L     R1,CNTUT1           GET INPUT COUNT
         GOSUB TRIM                OUTPUT THE NUMBER
         MVC   0(L'DONEICNT,R15),DONEICNT OUTPUT COUNT TEXT
         LA    R15,L'DONEICNT(,R15) UPDATE OUTPUT POINTER
         CVB   R1,PTRKALC          GET TRACKS ALLOCATED
         GOSUB TRIM                OUTPUT THE NUMBER
         MVC   0(L'DONETALC,R15),DONETALC OUTPUT ALLOCATED TEXT
         LA    R15,L'DONETALC(,R15) UPDATE OUTPUT POINTER
         CVB   R1,PTRKWST          GET TRACKS WASTED
         GOSUB TRIM                OUTPUT THE NUMBER
         MVC   0(L'DONETWST,R15),DONETWST OUTPUT ALLOCATED TEXT
         SPACE 1
ALLDONE1 DS    0H                  END MESSAGE HAS BEEN BUILT
         BAL   R4,SYSPRTWT         GO PRINT LINE
         LA    R3,MAXOPEN          SET NUMBER OF FILES TO CLOSE
         LA    R2,SYSUT1SW         POINT AT MDCB LIST
         USING MDCB,R2             ESTABLISH ITS ADDRESSSABILITY
         SPACE 1
CLOSEM   DS    0H                  CLOSE ALL FILES
         BAL   R4,CLOSE            GO CLOSE IT
         LA    R2,DCBNTRY(R2)      INCREMENT POINTER
         BCT   R3,CLOSEM           GO ROUND ALL FILES
         SPACE 1
RETURN   DS    0H
         L     R15,CNTUT1          SET RETURN CODE INPUT COUNT
         L     13,4(13)            RESTORE REG 13 TO CALLER'S SAVE
         L     14,12(13)           RESTORE REGS
         LM    0,12,20(13)         RESTORE REGS
         BR    14                  RETURN TO CALLER
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         EJECT
*********************************************************************
*         PAGE COUNT PROCESSING FOR SYSPRINT
*********************************************************************
         SPACE 2
LINETEST DS    0H
         SUBIN SAVE=(R14)          SUB-ROUTINE ENTRY
         CP    LINEKNT,=P'56'
         BL    NOEJE
         BAL   R14,HEADING
NOEJE    AP    LINEKNT,=P'1'
         SUBOUT RESTORE=(R14),RETURN=(R14) RETURN TO CALLER
         EJECT
*********************************************************************
*         PAGE HEADING PROCESSING
*********************************************************************
         SPACE 2
HEADING  DS    0H
         SUBIN SAVE=(R14)          SUB-ROUTINE ENTRY
         AP    PAGEKNT,=P'1'       ADD ONE TO PAGE COUNT
         MVC   PAGEPLC,PATTERN     PUT IN EDIT MASK
         ED    PAGEPLC,PAGEKNT     NUMBER FOR PAGE
         LA    R8,HEAD1            POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
         ZAP   LINEKNT,=P'2'       INITIATE 3 LINES PRINTED
         TM    PARMSWCH,PARMSTAT   DO WE WANT STATISTICS?
         BNO   HEADINGV            NO, WANT VOLUME INFORMATION
         LA    R8,HEAD3S           POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
         LA    R8,HEAD4S           POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
         LA    R8,HEAD5S           POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
         B     HEADTXT             EXIT SUB-ROUTINE
         SPACE 1
HEADINGV DS    0H                  HEADERS FOR VOLUME INFORMATION
         LA    R8,HEAD3V           POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
         LA    R8,HEAD4V           POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
         LA    R8,HEAD5V           POINT TO OUTPUT LINE
         BAL   R4,SYSPRTWT         GO PRINT LINE
HEADTXT  DS    0H
         AP    LINEKNT,=P'3'       3 MORE LINES PRINTED
         SUBOUT RESTORE=(R14),RETURN=(R14) RETURN TO CALLER
         EJECT
*******************************************************************
*        SUMMARY RECORD PROCESSING
*******************************************************************
         SPACE 1
SUMMARY  DS    0H                  READ A RECORD FROM SYSUT1
         SUBIN SAVE=(R2,R3,R4)     SUBROUTINE ENTRY
         SPACE 1
         LA    R2,SRSPACE          POINT TO RECORD AREA
         USING SRRECORD,R2         ESTABLISH ADDRESSABILITY
         MVC   DATANAME,SRDSNAME   COPY DATASET NAME
         MVC   CRDATE,SRCRDATE     ..CREATE DATE
         MVC   PURGDATE,SREXPDT    ..EXPIRE DATE
         MVC   REFDATE,SRREFDT     ..REFERENCE DATE
         MVC   DSORG,SRDSORG       ..DSORG
         MVC   DSCHA,SRDSCHA       ..DATA SET CHANGED
         MVC   RECFM,SRRECFM       ..RECFM
         MVC   LRECL,SRLRECL       ..LRECL
         MVC   BLKSIZE,SRBLKSZE    ..BLOCKSIZE
         MVC   ALUNIT,SRALUNIT     ..UNIT OF ALLOCATION
         MVC   SPALLOC,SRSPALC     ..SPACE ALLOC
         MVC   SPUSED,SRSPUSED     ..SPACE USED
         MVC   ALL2ND,SRALL2ND     ..SECONDARY ALLOCATION
         MVC   EXTENTS,SREXTENT    ..NO. EXTENTS
         TM    PARMSWCH,PARMSTAT   DO WE WANT STATISTICS?
         BNO   SUMMARYV            NO, SHOW VOLUME INFORMATION
         MVC   ISRKP,SRISRKP       COPY RELATIVE KEY POSITION
         MVC   ISKEYLEN,SRISKLN    ..AND KEY LENGTH
         CLC   CHARVSAM,SRDSORG    IS IT A VSAM FILE?
         BNE   LSUMMPAM            NO, TREAT AS PDS
         MVC   VSCASPLT,SRCASPLT   COPY CA SPLIT PERCENT
         MVC   VSCISPLT,SRCISPLT   .. AND CI SPLIT PERCENT
         B     SUMMARYX            EXIT SUB-ROUTINE
         SPACE 1
LSUMMPAM DS    0H                  SHOW PDS STATISTICS
         MVC   DRBLOCKS,SRDRBALC   COPY DIRECTORY BLOCKS
         MVC   DRBLUSED,SRDRBUSE   ..AND USED
         B     SUMMARYX            EXIT SUB-ROUTINE
         SPACE 1
SUMMARYV DS    0H                  WANT VOLUME INFORMATION
         MVC   VOLSER,SRVOLSER     COPY VOLUME SERIAL
         MVC   SPWASTE,SRSPWST     ..SPACE WASTED
         MVC   PCTUSED,SRSP100     ..PERCENT UTILISATION
         PACK  DW,SRSPALC          CONVERT TRACKS ALLOCATED
         AP    PTRKALC,DW          AND TOTAL THEM
         PACK  DW,SRSPWST          CONVERT TRACKS WASTED
         AP    PTRKWST,DW          AND TOTAL THEM
         SPACE 1
SUMMARYX DS    0H                  RETURN TO CALLER
         SUBOUT  RESTORE=(R2,R3,R4)  SUBROUTINE EXIT
         EJECT
*******************************************************************
*        READ SUBROUTINE FOR SYSUT1                               *
*******************************************************************
         SPACE 1
SYSUT1RD DS    0H                  WRITE A RECORD TO SYSUT2
         SUBIN SAVE=(R4,R3)        SUBROUTINE ENTRY
         LR    R3,R2               SAVE RECORD ADDRESS
         LA    R2,SYSUT1SW         POINT TO FILE CONTROL BLOCK
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         TM    MDCBFLAG,DDPRES+DDOPEN  TEST IF ABLE TO WRITE
         BO    SYSUT1WU            YES, CONTINUE
         TM    MDCBFLAG,DDPRES+DDINT  HAVE WE ALLOCATED IT?
         BM    SYSUT1WX            UNABLE TO, FORGET IT
         BAL   R4,OPEN             NO, GO OPEN IT
         SPACE 1
SYSUT1WU DS    0H                  WRITE A RECORD TO SYSUT2
         L     R1,MDCBDCB          POINT TO DCB
         GET   (R1),SRSPACE        GO READ RECORD
         L     R4,CNTUT1           GET RECORD COUNT
         LA    R4,1(R4)            INCREMENT
         ST    R4,CNTUT1           SAVE NEW COUNT
         SPACE 1
SYSUT1WX DS    0H                  RETURN TO CALLER
         SUBOUT  RESTORE=(R4,R3)   SUBROUTINE EXIT
         SPACE 1
         DROP  R2                  FINISHED WITH MDCB
         EJECT
*******************************************************************
*        WRITE SUBROUTINE FOR SYSPRINT                            *
*        ON ENTRY    R8 --> DATA                                  *
*******************************************************************
         SPACE 1
SYSPRTWT DS    0H                  WRITE A RECORD TO SYSPRINT
         SUBIN SAVE=(R4,R3)        SUBROUTINE ENTRY
         LA    R2,SYSPRTSW         POINT TO FILE CONTROL BLOCK
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         TM    MDCBFLAG,DDPRES+DDOPEN  TEST IF ABLE TO WRITE
         BO    SYSPRTWU            YES, CONTINUE
         TM    MDCBFLAG,DDPRES+DDINT  HAVE WE ALLOCATED IT?
         BM    SYSPRTWX            UNABLE TO, FORGET IT
         BAL   R4,OPEN             NO, GO OPEN IT
         SPACE 1
SYSPRTWU DS    0H                  WRITE A RECORD TO SYSPRINT
         L     R1,MDCBDCB          POINT TO DCB
         LR    R2,R8               AND RECORD
         BAL   R4,PUTMOVE          GO OUTPUT RECORD
         SPACE 1
SYSPRTWX DS    0H                  RETURN TO CALLER
         SUBOUT  RESTORE=(R4,R3)   SUBROUTINE EXIT
         SPACE 1
         DROP  R2                  FINISHED WITH MDCB
         EJECT
*******************************************************************
*        GENERALISED PUT/MOVE SUBROUTINE                          *
*        ON ENTRY    R1 --> DCB                                   *
*                    R2 --> DATA                                  *
*******************************************************************
         SPACE 1
PUTMOVE  DS    0H                  WRITE A RECORD TO SYSPRINT
         SUBIN SAVE=(R1,R2)        SUBROUTINE ENTRY
         LR    R9,R4               SAVE LAST BRANCH  *DEBUG*
         PUT   (R1),(R2)           WRITE A RECORD
         SPACE 1
         SUBOUT  RESTORE=(R1,R2)   SUBROUTINE EXIT
         EJECT
         COPY  BLKSZRTN            INCLUDE DEFAULT BLOCKSIZE RROUTINE
         EJECT
         COPY  TESTDD              INCLUDE DD CARD CHECK CODE
         EJECT
         COPY  OPENCLOS            INCLUDE STANDARD OPEN/CLOSE CODE
         EJECT ,
         COPY  TRIM                INCLUDE STANDARD DISPLAY CODE
         SPACE 2
         COPY  CONVWORD            INCLUDE STANDARD CONVERSION CODE
         EJECT
         COPY  TODDATE             COPY TOD-CLOCK ROUTINE
         EJECT
*********************************************************************
*
*         VARIOUS CONSTANTS, EQUATES , ETC
*
*********************************************************************
         SPACE 1
EDITWA   DS    D                   DOUBLE-WORD WORK AREA FOR NEW EDIT
PATTERN  DC    X'4020202020202120'  NEW EDITTING PATTERN
DWORK    DS    0D                  DOUBLEWORD WORK AREA
DW       DS    0D
         DS    F
FW       DS    0F
         DS    X
CL3      DS    XL3
PTRKALC  DC    PL8'0'              COUNT OF TRACKS ALLOCATED
PTRKWST  DC    PL8'0'              COUNT OF TRACKS WASTED
         SPACE 1
DATE     DS    F
TIME     DS    F
CNTUT1   DC    F'0'                INPUT RECORD COUNT
LINEKNT  DC    PL2'0'              KEEP TRACK OF LINES PER PAGE
PAGEKNT  DC    PL2'0'
GPAGEKNT DC    PL2'0'
GLINEKNT DC    PL2'0'
         SPACE 1
PARMLGTH DC    F'0'                LENGTH OF PARM OF EXEC CARD
PARMADDR DS    F                   ADDRESS OF PARM INFO'S
HW       DS    H
PARMSWCH DC    X'00'               SWITCH FOR 'FULL' PARM
PARMSTAT EQU   X'08'               SWITCH FOR STATS REQUEST
SWIT     DC    X'FF'
STATPARM DC    C'STATS'            PARM VALUE TO SELECT STATS
         SPACE 1
BLANK    EQU   X'40'
CONVWORK DC    CL15' '             CONVERSION WORK AREA
         SPACE 1
CHARVSAM DC    C'VS'               LITERAL VSAM IDENTIFIER
GSKIP2   DC    CL133'0'
TOPMSG   DS    0C
SKIP2    DC    C'0'                ONE SPACE PLUS LINE
         SPACE 2
SRSPACE  DC    CL(SRRECLEN)' '     AREA FOR SUMMARY RECORD
         EJECT
*********************************************************************
*
*               PAGE AND COLUMN HEADINGS
*
*********************************************************************
HEAD1    DC    CL1'1'              SKIP TO TOP OF PAGE
HEAD2    DC    CL37'SPRECHER ENERGIE AG (VER 2.2)'
         DC    CL35' * MAP OF DISK CONTENTS * '
SAVESER  DC    CL6' '
         DC    CL14'       DATE : '
HEADDATE DS    CL9
         DC    CL9'  TIME :'
HEADTIME DC    CL8' '              TIME HH:MM:SS
         DC    CL5'     '
         DC    CL5'PAGE'
PAGEPLC  DC    CL4' '              PAGE COUNT HERE
         SPACE 2
HEAD3V   DC    CL1'0'
         DC    CL44'*-------------- DATA SET NAME -------------*'
         DC    CL1' '
         DC    CL17'*---- DATES ----*'
         DC    CL1' '
         DC    CL23'*-------- DCB --------*'
         DC    CL1' '
         DC    CL45'*--------- ALLOCATION INFORMATION ----------*'
         SPACE 2
HEAD4V   DC    CL77' '
         DC    CL5'MAX  '
         DC    CL6'CISIZ '
         DC    CL5'ALOC '
         DC    CL6'TRACK '
         DC    CL6'TRACK '
         DC    CL5'2NDY '
         DC    CL4'NO. '
         DC    CL5'TRACK'
         DC    CL1' '
         DC    CL3'PCT'
         DC    CL5' '
         DC    CL5'DISK '
         SPACE 2
HEAD5V   DC    CL1' '
         DC    CL34' '
         DC    CL16'           CREDT'
         DC    CL6' EXPDT'
         DC    CL6' REFDT'
         DC    CL6' DSORG'
         DC    CL6' RECFM'
         DC    CL6' LRECL'
         DC    CL6' BLKSZ'
         DC    CL5' UNIT'
         DC    CL6' ALLOC'
         DC    CL6'  USED'
         DC    CL5' ALOC'
         DC    CL4' EXT'
         DC    CL1' '
         DC    CL5'WASTE'
         DC    CL1' '
         DC    CL3'USE'
         DC    CL4' '
         DC    CL6'VOLSER'
         EJECT
*********************************************************************
*
*               STATISTICS HEADERS
*
*********************************************************************
         SPACE 2
HEAD3S   DC    CL1'0'
         DC    CL44'*-------------- DATA SET NAME -------------*'
         DC    CL1' '
         DC    CL17'*---- DATES ----*'
         DC    CL1' '
         DC    CL23'*-------- DCB --------*'
         DC    CL1' '
         DC    CL25'*----- ALLOCATION ------*'
         DC    CL1' '
         DC    CL19'*-- VS/PO STATS --*'
         SPACE 2
HEAD4S   DC    CL77' '
         DC    CL5'MAX  '
         DC    CL6'CISIZ '
         DC    CL5'ALOC '
         DC    CL6'TRACK '
         DC    CL6'TRACK '
         DC    CL5'2NDY '
         DC    CL3'NO.'
         DC    CL1' '
         DC    CL5' CA% '
         DC    CL5' CI% '
         DC    CL9'VS RKP-KL'
         SPACE 2
HEAD5S   DC    CL1' '
         DC    CL34' '
         DC    CL16'           CREDT'
         DC    CL6' EXPDT'
         DC    CL6' REFDT'
         DC    CL6' DSORG'
         DC    CL6' RECFM'
         DC    CL6' LRECL'
         DC    CL6' BLKSZ'
         DC    CL5' UNIT'
         DC    CL6' ALLOC'
         DC    CL6'  USED'
         DC    CL5' ALOC'
         DC    CL4' EXT'
         DC    CL1' '
         DC    CL4' DIR'
         DC    CL5' -USE'
         DC    CL1' '
         DC    CL9'PO MEMBS'
         EJECT ,
*********************************************************************
*
*               DETAIL LINE INFORMATION
*
*********************************************************************
OUTBIN   DC    C' '                OUTPUT BIN -  NO  SPACE (PRINT)
DATANAME DC    CL44'WHO'           DATASET NAME
         DC    CL1' '
CRDATE   DC    CL5'01234'          CREATION DATE
         DC    CL1' '
PURGDATE DC    CL5'99350'          PURGE DATE
         DC    CL1' '
REFDATE  DC    CL5' '              REFERRED-TO DATE
DSCHA    DC    CL1' '              DATA SET CHANGED
         DC    CL1' '
DSORG    DC    CL4' '              DATA SET ORGANISATION
         DC    CL3' '
RECFM    DC    CL3' '              RECORD FORMAT
         DC    CL1' '
LRECL    DC    CL5' '              LOGICAL RECORD LENGTH
         DC    CL1' '
BLKSIZE  DC    CL5' '              BLOCK SIZE
         DC    CL1' '
ALUNIT   DC    CL5' '              ALLOC UNITS--(TRK,CYL,BLK)
SPALLOC  DC    CL5' '              SPACE ALLOCATED TRACKS TOTAL
         DC    CL1' '
SPUSED   DC    CL5' '              SPACE USED - TOTAL TRACKS
         DC    CL1' '
ALL2ND   DC    CL4' '              SECONDARY ALLOCATION
         DC    CL1' '
EXTENTS  DC    CL3' '              NUMBER OF EXTENTS
         DC    CL1' '
SPWASTE  DC    CL5' '              TRACKS WASTED
         DC    CL1' '
PCTUSED  DC    CL3' '              PERCENT UTILISATION
         DC    CL4' '
VOLSER   DC    CL6' '              VOLUME SERIAL
         SPACE 2
CALCU    DC    CL10' '
         SPACE 2
         ORG   SPWASTE             ALTERNATIVE FORMAT
VSCASPLT DC    0CL3' '             CA SPLITS PERCENT
DRBLOCKS DC    CL4' '              NO. DIRECTORY BLOCKS ALLOCATED
         DC    CL1' '
VSCISPLT DC    0CL3' '             CI SPLITS PERCENT
DRBLUSED DC    CL4' '              NUMBER OF DIRECTORY BLOCKS USED
         DC    CL1' '
ISRKP    DC    CL5' '              I.S. R.K.P.(RELATIVE KEY POSITION)
         DC    CL1' '
ISKEYLEN DC    CL3' '              I.S. KEYLENGTH
         SPACE 1
         ORG   ,                   RESTORE POINTER
         SPACE 2
DONEMSG  DS    0CL133              PROGRAM ENDED MESSAGE
         DC    CL1'-'              SKIP TWO
         DC    CL18' END OF UTILITY - '  SELF EVIDENT MESSAGE
DONEMEND EQU   *                   BUILD END MESSAGE HERE
         DC    CL21'DISK PACKS ARE MAPPED' SIMPLISTIC END MESSAGE
         DC    CL93' '             REST OF PRINT LINE
DONEICNT DC    CL18' DATASETS LISTED. ' COUNT TEXT
DONETALC DC    CL19' TRACKS ALLOCATED. ' ALLOCATED TEXT
DONETWST DC    CL15' TRACKS WASTED.' WASTED TEXT
         TITLE 'DCB''S AND I/O CONTROL TABLES'
***********************************************************************
*        DCB'S TABLE                                                  *
***********************************************************************
         SPACE 1
DCBTABLE DS    0F
SYSUT1SW DC    X'00',AL3(SYSUT1),CL8'SYSUT1',CL8'INPUT',CL8' '
DCBNTRY  EQU   *-DCBTABLE          LENGTH OF ONE TABLE ELEMENT
SYSPRTSW DC    X'00',AL3(SYSPRINT),CL8'SYSPRINT',CL8'OUTPUT',CL8' '
OPENSIZE EQU   *-DCBTABLE          TABLE SIZE OF FULL TIME FILES
MAXOPEN  EQU   OPENSIZE/DCBNTRY    MAXIMUM NUMBER OF DCB'S TO OPEN
         SPACE 1
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUM NUMBER OF DCB'S
         SPACE 1
DDBYPASS EQU   128                 DD NON-PROCESS
DDINT    EQU   16                  DD INTEREST
DDPRES   EQU   8                   DD PRESENT
DDOPEN   EQU   1                   DATA SET IS OPEN
FF       EQU   255                 ALL BITS ON
         SPACE 2
EXITLIST DC    X'85',AL3(BLKSZRTN)  ENTRY FOR DCB MODIFICATION
         EJECT
*******************************************************************
*        DATA CONTROL BLOCKS                                      *
*******************************************************************
         PRINT NOGEN
         SPACE 3
*********************************************************************
*              SYSUT1 - INPUT SUMMARY FILE
*********************************************************************
SYSUT1   DCB   DDNAME=SYSUT1,                                          *
               DSORG=PS,                                               *
               EODAD=ALLDONE,                                          *
               EXLST=EXITLIST,                                         *
               MACRF=GM
*********************************************************************
*               PRINT
*********************************************************************
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BFTEK=S,RECFM=FA,     1
               BLKSIZE=133,LRECL=133,BUFNO=14
         EJECT
*********************************************************************
*         LITERALS ETC.
*********************************************************************
         DS    0D
         LTORG
         SPACE 2
         RIN   GENERATE            REGISTER SAVE AREAS
         EJECT
         TITLE 'DSECT MAPS'
***********************************************************************
*        DD TABLE ENTRY FORMAT                                        *
***********************************************************************
         SPACE 1
MDCB     DSECT ,                   FORMAT OF A DCB TABLE ENTRY
MDCBDCB  DS    0F                  DCB POINTER
MDCBFLAG DS    XL1                 FLAGS
         DS    AL3                 ADDRESS OF DCB
MDCBDDNM DS    CL8'DDNAME'         DDNAME
MDCBOPEN DS    CL8'INPUT'          TYPE OF OPEN REQUIRED
MDCBCLOS DS    CL8'REREAD'         TYPE OF CLOSE REQUIRED
         SPACE 1
MDCBLEN  EQU   *-MDCB              LENGTH OF ONE TABLE ENTRY
         SPACE 2
*********************************************************************
*
*         DCBD MACRO (IHADCB DSECT)
*
*********************************************************************
         DCBD  DSORG=PS,DEVD=DA    DCB DSECT
         EJECT
         COPY  MAPDISKF            FORMAT OF SUMMARY RECORDS
         SPACE 2
         END   MAPDLIST
*
