***********************************************************************
*                                                                     *
*        PDSDOC *LOCATE RACF ACEE.....................................*
*                                                                     *
*        ON EXIT R1 --> ACEE IF FOUND                                 *
*                R1 = 0 IF NOT FOUND                                  *
*                                                                     *
***********************************************************************
         SPACE 1
GETACEE  DS    0H                  SUBROUTINE ENTRY
         SUBIN SAVE=(R4)           SAVE THE REGISTERS WE USE
         USING PSA,R0              FRIG PSA ADDRESSABILITY
         L     R4,PSAAOLD                 GET ASCB
         USING ASCB,R4                    ADDRESS IT
         L     R4,ASCBASXB                GET ASXB
         USING ASXB,R4                    ADDRESS IT
         ICM   R1,B'1111',ASXBSENV GET ACEE
         BZ    GOTACEE             EXIT IF NONE
         USING ACEE,R1             ESTABLISH ACEE ADDRESSABILITY
         CLC   =C'ACEE',ACEEACEE   CONFIRM WE HAVE AN ACEE
         BE    GOTACEE             YES, RETURN ITS ADDRESS
         XR    R1,R1               NO, RETURN ZERO
         SPACE 1
GOTACEE  DS    0H                  END OF SUB-ROUTINE
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 1
         DROP  R1,R4               END ADDRESSABILITY
         TITLE 'DSECT MAPS'
         SPACE 1
         IHAPSA DSECT=YES,LIST=NO MAP PREFIXED STORAGE AREA
         EJECT ,
         IHAASCB DSECT=YES,LIST=NO MAP ASCB
         EJECT ,
         IHAASXB DSECT=YES,LIST=NO MAP ASXB
         EJECT ,
         IHAACEE ,                 MAP ACEE
*
*        MAY NEED A CSECT STATEMENT AFTER COPYING THIS CODE
*
