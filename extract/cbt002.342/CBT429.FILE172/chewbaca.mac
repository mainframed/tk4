         MACRO
&NAME    CHEWBACA &TYPE=
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT ON,GEN              EXPAND THINGS
         AIF   (T'&NAME EQ 'O').NONAME
         AIF   ('&TYPE' EQ 'CSECT').CSECT
         AIF   ('&TYPE' EQ 'DSECT').DSECT
         MNOTE 12,'INVALID TYPE'
         MEXIT
.CSECT   ANOP
CHEWBACA CSECT ,                   START OF COMMON AREA
***********************************************************************
*        CSECT CODE                                                   *
* 10APR00 DHC ADDED JESIO CODE TO IGNORE JES2 FILES WITH ERRORS       *
***********************************************************************
         AGO   .NODSECT
.DSECT   ANOP
CHEWBACA DSECT ,                   COMMON AREA MAPPING
.NODSECT ANOP
         TITLE 'CHEWBACA - BASIC ADDRESSABLE COMMON AREA'
***********************************************************************
*                                                                     *
*          COMMON AREA MAPPING MACRO                                  *
*                                                                     *
*     THIS MACRO WILL GENERATE EITHER THE 'CHEWBACA' CONSTANTS OR     *
*     A DSECT TO MAP THE 'CHEWBACA' AREA. 'CHEWBACA' IS USED FOR      *
*     CONSTANTS AND WORK AREAS THAT ARE REQUIRED BY SEVERAL MODULES   *
*     ALLOWING SOME ELIMINATION OF DUPLICATED DATA AND TO ALLOW       *
*     INTER-MODULE COMMUNICATION.                                     *
*                                                                     *
*                                                                     *
***********************************************************************
&NAME    EQU   *                   START OF COMMON AREA
         SPACE 3
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0                   WORK
R1       EQU   1                   WORK
R2       EQU   2                   DCB TABLE POINTER
REG2     EQU   R2                  WORK REGISTER IN 'SCAN' MACRO
R3       EQU   3                   .
R4       EQU   4                   LINKAGE RETURN
R5       EQU   5                   INPUT RECORD ADDRESS
R6       EQU   6                   INPUT RECORD LENGTH - 1
R7       EQU   7                   .
R8       EQU   8                   CIB POINTER
R9       EQU   9                   BASE REG FOR SVC 99 PARAMETER AREA
R10      EQU   10                  BASE REG FOR SSOB
R11      EQU   11                  MAIN WORK AREA (CHEWBACA) BASE
R12      EQU   12                  CHEWBACA BASE REGISTER 2
R13      EQU   13                  SAVE AREA / PROGRAM BASE REGISTER
R14      EQU   14                  PROGRAM RETURN / WORK
R15      EQU   15                  PROGRAM LINKAGE / WORK
         EJECT ,
***********************************************************************
*                                                                     *
*          GLOBAL DATA                                                *
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 1
         DS    0F                  GET ADDRESSES ALIGNED
BEGSNAP  DS    0D                  SNAP DATA AREAS FOLLOWING
         DC    2F'0'               BASE REGISTERS SAVE AREA
         DC    CL8'CHEWBACA'       EYECATCHER
COMMLPTR DC    A(0)                PTR FROM EXTRACT (TO ECB & CIB PTRS)
CMNDECBP DC    A(0)                PTR TO ECB POSTED WHEN CMND ISSUED
ECBL2PTR DC    A(CMDECBP2)         POINTER TO SECOND ECBLIST
CMDECBP2 DC    A(0)                POINTER TO COMMAND ECB
TIMRECBP DC    X'80',AL3(STIMRECB) POINTER TO ECB POSTED BY STIMER EXIT
STIMRECB DC    F'0'                ECB POSTED BY STIMER EXIT
WTORECB  DC    F'0'                ECB POSTED BY OPERATOR REPLY
         AIF   ('&TYPE' NE 'CSECT').DE2
         ENTRY WTORECB             SHOW EXTERNAL REFERENCE
         SPACE 1
.DE2     ANOP
LASTTYPE DC    XL1'00'             TYPE OF CC LAST ENCOUNTERED
LASTCC   DC    XL1'00'             LAST CC ENCOUNTERED
         SPACE 3
JOBCOUNT DC    PL4'0'              NUMBER OF JOBS PROCESSED
JESCOUNT DC    PL4'0'              NUMBER OF DATASETS PROCESSED
CNTSUT2  DC    PL8'0'              OUTPUT RECORD COUNT FIELD
CNTSUT3  DC    PL8'0'              OUTPUT RECORD COUNT FIELD
CNTSUTR3 DC    PL8'0'              INPUT RECORD COUNT FIELD
ONE      DC    PL1'1'              COUNT INCREMENT
SIGN     EQU   X'F0'               POSITIVE PACKED SIGN
         SPACE 1
REPTCOUN DC    PL2'0'              COUNT OF REPORTS
PAGECOUN DC    PL2'0'              COUNT OF PAGES
LINECOUN DC    PL2'60'             COUNT OF LINES
MAXLINE  DC    PL2'56'             MAXIMUM NUMBER OF LINES PER PAGE
         SPACE 1
LINEBLNK DC    F'0'                ADDRESS OF LAST CHAR. IN LINE
LINEBNUM DC    F'0'                NO. OF CHARACTERS IN LINE
         SPACE 1
EDITPL2  DC    X'40202120'         COPIES EDIT MASK
EDITPL4  DC    X'4020202020202120' COUNT EDIT MASK
EDITPL8  DC    X'40202020202020202020202020202120' COUNT EDIT MASK
         EJECT ,
UCMID    DC    X'00'               ID OF CONSOLE TALKING TO ME
COMPCODE DC    H'0'                PROGRAM COMPLETION CODE
SCANBLKS DS    20F                 WORK AREA FOR SCAN MACRO
WORKKEY  DS    D                   WORK AREA FOR CIB PROCESSING
ZEROWORD DC    F'0'                A LOT OF NOTHING
WTIMEONE DC    F'06000'            1   MINUTE INTERVAL FOR STIMER
WTIMETWO DC    F'12000'            2   MINUTE INTERVAL FOR STIMER
*
*        RETURN CODES FROM VARIOUS MODULES
*
RETCODE  DC    H'0'                MACRO RETURN CODE SAVE AREA
RETDYNA  DC    H'0'                RETURN CODE FROM CHEWDYNA
RETPARS  DC    H'0'                RETURN CODE FROM CHEWPARS
RETREPT  DC    H'0'                RETURN CODE FROM CHEWREPT
RETSKIP  DC    H'0'                RETURN CODE FROM CHEWSKIP
         EJECT ,
***********************************************************************
*        OPERATOR DEFINABLE VARIABLES                                 *
***********************************************************************
         SPACE 1
PREFIX   DC    CL8'OUT'            HIGH LEVEL QUALIFIER
UNIT     DC    CL8'SYSALLDA'       UNIT NAME
         SPACE 1
CHEWCLAS DC    CL1'A'              CLASS FOR RE-QUEUEING OUTPUT
CHEWDEST DC    CL8' '              DESTINATION FOR SYSOUT SELECTION
CHEWCLSL DC    CL8'Z'              CLASS LIST FOR SYSOUT SELECTION
         SPACE 3
JOBWMAX  DC    H'5'                NO. TIMES AWAIT JOB END
JOBWCNT  DC    H'0'                STILL WAITING
PURGE    DC    CL5'PURGE'          PURGE REPLY IDENTIFIER
BLANKS   DC    CL5'     '          BLANK REPLY TEST
TSUID    DC    CL3'TSU'            TSU JOB IDENTIFIER
         SPACE 1
RUNFLAG  DC    XL1'00'             FLAGS FOR PROCESSING THIS RUN
RUNNOWTO EQU   X'80'               DO NOT ISSUE WTO'S
RUNNOTSU EQU   X'40'               DO NOT ISSUE WTOR'S FOR TSU
PRINTALL EQU   X'20'               REQUEUE ALL JOBS FOR PRINTING
RUNFSEOF EQU   X'10'               STOP AT END OF DATA
RUNCONT  EQU   X'08'               NEVER STOP - CONTINUOUS RUNNING
JOBFLAG  DC    XL1'00'             JOB LEVEL FLAGS
JOBGOT   EQU   X'04'               A JOB IS BEING PROCESSED
JOBMSGS  EQU   X'01'               JOB MESSAGE DATASETS BEEN PROCESSED
DSFLAG   DC    XL1'00'             DATASET FLAGS
DSREQ    EQU   X'80'               DATASET TO BE RE-QUEUED FOR OUTPUT
DSWTOR   EQU   X'40'               OPERATOR TO BE NOTIFIED
DSWTED   EQU   X'20'               OPERATOR HAS BEEN NOTIFIED
DSSCAN   EQU   X'10'               DATASET TO BE SCANNED
NULL     EQU   X'00'               RESET
         SPACE 1
RDRSTIME DC    XL8'00'             RDR START TIME/DATE OF JOB
         EJECT ,
***********************************************************************
*        ADDRESS POINTERS                                             *
***********************************************************************
         SPACE 1
         DS    0F
ACOMMAND DC    A(0)                ADDRESS OF COMMAND LENGTH AND TEXT
ACC      DC    A(0)                ADDRESS OF CARRIAGE CONTROL ENTRY
APRINT   DC    A(JESLINE)          ADDRESS OF PRINT LINE
         SPACE 2
CPBPARS  CPB   CHEWPARS            CPB FOR COMMAND PARSER
CPBREPT  CPB   CHEWREPT            CPB FOR REPORT WRITER
CPBDYNA  CPB   CHEWDYNA            CPB FOR DYNAMIC ALLOCATOR
CPBSKIP  CPB   CHEWSKIP            CPB FOR CARRIAGE CONTROL
         EJECT ,
***********************************************************************
*        ERROR MESSAGES                                               *
***********************************************************************
         SPACE 1
MSG10    DS    0F
MSG10RPL DC    AL1(L'OUTNOTE)      LENGTH OF REPLY AREA
MSG10RPA DC    AL3(OUTNOTE)        ADDRESS OF REPLY AREA
MSG10ECB DC    V(WTORECB)          ADDRESS OF WTOR ECB
MSG10WTO EQU   *                   START OF WTO TEXT
         DC    AL2(MSG10LEN)       LENGTH OF MESSAGE
         DC    H'0'                MCS FLAGS
WTOLEN   EQU   *-MSG10WTO          LENGTH OF TEXT
         DC    C'CHEW:'            MESSAGE IDENTIFIER
MSG10JID DC    CL8'J000000'        JOB IDENT
         DC    CL1' '              PADDING
MSG10JNM DC    CL8'JOBNAME'        JOB NAME
         DC    CL1' '              PADDING
MSG10TXT DC    C'RE-QUEUED FOR PRINTING' MESSAGE INFORMATION
         SPACE 1
MSG10LEN EQU   *-MSG10WTO          LENGTH OF TEXT
         SPACE 2
MSG11    DS    0F
         DC    AL2(WTOLEN+L'TEXT11),H'0'
TEXT11   DC    C'CHEW: ANOTHER ERROR MESSAGE WTO SAMPLE'
         EJECT ,
***********************************************************************
*        OUTPUT RECORD AREA - PRINT LINE, SSOB AND TIME STAMP.        *
*        SUMMARY RECORD AREA IS THE SSOB, TIMESTAMP AND JFCB.         *
***********************************************************************
         SPACE 1
         DS    0F
SSPARM   DC    X'80',AL3(SSAREA)   ADDRESS OF SSOB
         SPACE 1
OUTRDW   DC    XL2'00'             RECORD LEGTH
         DC    XL2'00'
OUTREC   EQU   *                   START OF OUTPUT RECORD
JESLINE  DS    0CL133' '           PRINT LINE IMAGE
JESCC    DC    CL1' '              ASA CONTROL CHARACTER
         DC    CL132' '            PRINT LINE
         DC    CL1' '              POSSIBLE LAST CHAR
         SPACE 1
SUMREC   DS    0F                  START OF SUMMARY RECORD
SSAREA   DC    XL(SSOBLEN1)'00'    AREA FOR SSOB
         ORG   SSAREA              GO BACK OVER IT
         DC    C'SSOB'             CONTROL BLOCK IDENTIFIER
         DC    AL2(SSOBHSIZ)       LENGTH OF SSOB HEADER
         DC    AL2(SSOBSOUT)       FUNCTION ID
         DC    A(0)                ADDRESS OF SSIB OR ZERO
         DC    F'0'                RETURN CODE FROM SUBSYSTEM
         DC    A(SSFUNC)           FUNCTION DEPENDENT AREA POINTER
         DC    A(0)                SSI SAVED RETURN ADDRESS
         DC    XL4'00'             RESERVED
*
*        SSOB SYSOUT EXTENSION
*
SSFUNC   DC    AL2(SSSOSIZE)       SYSOUT EXTENSION LENGTH
         DC    X'00'               SSSOUFLG
         DC    AL1(SSSOCVER)       SSOB VERSION NUMBER
         DC    AL1(SSSOSCLS)       DS SELECTION CNTL FLAGS - CLASS ONLY
         DC    XL23'00'            FILLER
         DC    XL8'00'             DEST. - NOT A SELECTION CRITERION
         DC    XL64'00'            FILLER
         DC    CL8'Z'              SYSOUT CLASS SELECTION LIST
         ORG   ,                   END OF SSOB
         SPACE 1
OUTCC    DC    CL1' '              TYPE OF FORMS CONTROL IN DATASET
OUTDATE  DC    CL6'YYMMDD'         DATE PROCESSED
OUTTIME  DC    CL6'HHMMSS'         TIME DATASET DE-ALLOCATED
OUTCOUNT DC    PL4'0'              RECORD COUNT
OUTNOTE  DC    CL80' '             MESSAGE FROM OPERATOR
         SPACE 1
OUTRECL  EQU   *-OUTREC            LENGTH OF OUTPUT RECORD
         SPACE 1
AJFCB    DS    CL176' '            JFCB AREA
SUMRECL  EQU   *-SUMREC            LENGTH OF SUMMARY RECORD
         SPACE 1
         TITLE 'DYNAMIC ALLOCATION CONTROLS'
***********************************************************************
*        DYNAMIC ALLOCATION TEXT BLOCKS                               *
***********************************************************************
         SPACE 1
         DS    0F
S99PARM  DC    X'80',AL3(S99AREA)  ADDRESS OF SVC 99 REQUEST BLOCK
S99AREA  DC    AL1(S99RBEND-S99RB) LENGTH OF REQUEST BLOCK
         DC    X'01'               VERB CODE
         DC    XL6'00'
         DC    A(ALTUPTR)          POINTER TO LIST OF TEXT UNIT PTRS
         DC    XL8'00'
         SPACE 1
ALTUPTR  DC    A(ALDSTU)           ADDR OF DSNAME TEXT UNIT
         DC    A(ALSSTU)           ADDR OF SUBSYSTEM TEXT UNIT
         DC    X'80',AL3(ALDDTU)   ADDR OF DDNAME TEXT UNIT
         SPACE 1
ALDSTU   DC    X'0002'             ALLOCATION BY DSNAME
         DC    X'0001'             ONE PARAMETER
         DC    X'002C'             LENGTH OF PARAMETER
ALDSN    DC    CL44' '             DSNAME
         DS    0H
ALSSTU   DC    X'005C'             SUBSYSTEM DATA SET ALLOCATION
         DC    X'0001'             ONE PARAMETER
         DC    X'0004'             LENGTH OF PARAMETER
ALSSN    DC    CL4'JES2'           SUBSYSTEM NAME
         DS    0H
ALDDTU   DC    X'0055'             RETURN WITH DDNAME
         DC    X'0001'             ONE PARAMETER
         DC    X'0008'             LENGTH OF PARAMETER
ALDDNAM  DC    CL8' '              AREA FOR DDNAME
         EJECT ,
***********************************************************************
*        DYNAMIC UNALLOCATION TEXT BLOCKS                             *
***********************************************************************
         SPACE 1
UALTUPTR DC    A(UALDDTU)          ADDR OF UNALLOCATION DDNAME TXT UNIT
         DC    X'80',AL3(UALOVDSP) ADDR OF UNALLOC OVERIDING DISP TXT U
         SPACE 1
UALDDTU  DC    X'0001'             UNALLOCATION BY DDNAME
         DC    X'0001'             ONE PARAMETER
         DC    X'0008'             LENGTH OF PARAMETER
UALDDNAM DC    CL8' '              DDNAME TO BE UNALLOCATED
         DS    0H
UALOVDSP DC    X'0005'             OVERIDING DISPOSITION SPECIFICATION
         DC    X'0001'             ONE PARAMETER
         DC    X'0001'             LENGTH OF PARAMETER
UALODISP DC    X'08'               KEEP DATA SET
         SPACE 1
UALODELT EQU   X'04'               DISP=DELETE CODE
UALOKEEP EQU   X'08'               DISP=KEEP
         EJECT ,
***********************************************************************
*        DYNAMIC ALLOCATION TEXT BLOCKS FOR DISK FILE                 *
***********************************************************************
         SPACE 1
         DS    0F
S99PARM3 DC    X'80',AL3(S99AREA3) ADDR OF SVC 99 RB FOR SYSOUT ALLOC
S99AREA3 DC    AL1(S99RBEND-S99RB) LENGTH OF REQUEST BLOCK
         DC    X'01'               VERB CODE - ALLOCATE
         DC    XL6'00'
         DC    A(DISKUPTR)         POINTER TO LIST OF TEXT UNIT PTRS
         DC    XL8'00'
         SPACE 1
DISKUPTR DC    A(ALDDTU)           RETURN DDNAME TEXT UNIT
         DC    A(DSKDSN)           ADDR OF DSNAME TEXT UNIT
         DC    A(DSKUNIT)          ADDR OF UNIT= TEXT UNIT
         DC    A(DSKTRK)           ADDR OF SPACE TEXT UNIT
         DC    A(DSKPRI)           ADDR OF SPACE TEXT UNIT
         DC    A(DSKSEC)           ADDR OF SPACE TEXT UNIT
         DC    A(DSKRLSE)          ADDR OF SPACE TEXT UNIT
         DC    A(DSKNEW)           ADDR OF DISPOSITION TEXT UNIT
         DC    A(DSKCAT)           ADDR OF DISPOSITION TEXT UNIT
         DC    A(DSKDEL)           ADDR OF DISPOSITION TEXT UNIT
         DC    X'80',AL3(DSKFREE)  ADDR OF FREE=CLOSE TEXT UNIT
         SPACE 1
         DS    0H
DSKUNIT  DC    X'0015'             UNIT NAME
         DC    X'0001'             ONE PARAMETER
         DC    X'0008'             LENGTH OF UNIT NAME
         DC    CL8'SYSALLDA'       DIRECT ACCESS DEVICE
*
*        SPACE PARAMETERS
*
DSKTRK   DS    0H
         DC    X'0007'             ALLOCATE IN TRACKS
         DC    X'0000'             NO PARAMETER
         DS    0H
DSKPRI   DC    X'000A'             PRIMARY SPACE
         DC    X'0001'             ONE PARAMETER
         DC    X'0003'             LENGTH
         DC    AL3(90)             90 TRACKS
DSKSEC   DS    0H
         DC    X'000B'             SECONDARY SPACE
         DC    X'0001'             ONE PARAMETER
         DC    X'0003'             LENGTH
         DC    AL3(300)            300 TRACKS
DSKRLSE  DS    0H
         DC    X'000D'             RELEASE UNUSED SPACE
         DC    X'0000'             NO PARAMETER
         EJECT 1
*
*        DISPOSITION
*
DSKNEW   DS    0H
         DC    X'0004'             DATASET STATUS
         DC    X'0001'             ONE PARAMETER
         DC    X'0001'             LENGTH
         DC    XL1'04'             NEW
DSKCAT   DS    0H
         DC    X'0005'             NORMAL DISPOSITION
         DC    X'0001'             ONE PARAMETER
         DC    X'0001'             LENGTH
         DC    XL1'02'             CATALOG
DSKDEL   DS    0H
         DC    X'0006'             CONDITIONAL DISPOSITION
         DC    X'0001'             ONE PARAMETER
         DC    X'0001'             LENGTH
         DC    XL1'04'             DELETE
DSKFREE  DS    0H
         DC    X'001C'             FREE=CLOSE
         DC    X'0000'             NO PARAMETERS
*
*        DATASET NAME IN THE FORMAT :-
*
*        PREFIX.GROUP.JOBNAME.AYYMMDD.XCHHMMSS.JOBID
*                 WHERE A IS A CENTURY INDICATOR (A=20,B=21 ETC.)
*                           AND X IS J (JOB)
*                                    S (STC)
*                                    T (TSU)
*                                C IS THE SYSOUT CLASS
*
DSKDSN   DS    0H
         DC    X'0002'             DATASET NAME
         DC    X'0001'             ONE PARAMETER
DSKDSNLN DC    AL2(DSKDSNL)        LENGTH
DSKDSNA  DS    0CL44               AREA FOR BUILDING DSN
DSKDSNPR DC    CL3'OUT'            HIGH LEVEL QUALIFIER         ..
DSKDSND1 DC    C'.'                DOT                          ..
DSKDSNGP DC    CL3'GRP'            GROUP                        ..
DSKDSND2 DC    C'.'                DOT                          ..
DSKDSNJB DC    CL8'JOBNAME'        JOBNAME                      ..
DSKDSND3 DC    C'.'                DOT                          ..
DSKDSNCY DC    CL1'A'              CENTURY FLAG                 ..
DSKDSNDT DC    CL7'YYMMDD'         DATE                         ..
DSKDSND4 DC    C'.'                DOT                          ..
DSKDSNTY DC    CL1'J'              JOB TYPE - JOB, STC, TSU     ..
DSKDSNCL DC    CL1' '              SYSOUT CLASS                 ..
DSKDSNTM DS    0CL6'HHMMSS'        TIME                         ..
DSKDSNHH DC    CL2'HH'             HOURS                        ..
DSKDSNMM DC    CL2'MM'             MINUTES                      ..
DSKDSNSS DC    CL2'SS'             SECONDS                      ..
DSKDSND5 DC    C'.'                DOT                          ..
DSKDSNJN DC    CL8'JOBNUM'         INCREMENTAL COUNTER          ..
DSKDSNL  EQU   *-DSKDSNA           LENGTH OF DATASET NAME AREA
         SPACE 1
DSKDSNNO DC    CL2'NO'             ALPHA CHARACTERS
         TITLE 'DCB''S AND I/O CONTROL TABLES'
***********************************************************************
*        DCB'S TABLE                                                  *
***********************************************************************
         SPACE 1
DCBTABLE DS    0F
SYSUT2SW DC    X'00',AL3(SYSUT2),CL8'SYSUT2',CL8'OUTPUT',CL8'LEAVE'
         DC    PL8'0'              RECORD COUNT
DCBNTRY  EQU   *-DCBTABLE          LENGTH OF ONE TABLE ELEMENT
SYSUT3SW DC    X'00',AL3(SYSUT3),CL8'SYSUT3',CL8'OUTPUT',CL8' '
         DC    PL8'0'              RECORD COUNT
SNAPSW   DC    X'00',AL3(SNAPDCB),CL8'SNAPDCB',CL8'OUTPUT',CL8' '
         DC    PL8'0'              RECORD COUNT
OPENSIZE EQU   *-DCBTABLE          TABLE SIZE OF FULL TIME FILES
MAXOPEN  EQU   OPENSIZE/DCBNTRY    MAXIMUM NUMBER OF DCB'S TO OPEN
         SPACE 1
PRINTSW  DC    X'00',AL3(SYSOUT),CL8'SYSOUT',CL8'OUTPUT',CL8' '
         DC    PL8'0'              RECORD COUNT
DISKSW   DC    X'00',AL3(PRINTJOB),CL8'PRINTJOB',CL8'OUTPUT',CL8' '
         DC    PL8'0'              RECORD COUNT
INSW     DC    X'00',AL3(INDCB),CL8' ',CL8'INPUT',CL8' '
         DC    PL8'0'              RECORD COUNT
         SPACE 1
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUM NUMBER OF DCB'S
WTOIOE   DS    0F
         DC    AL2(WTOEND-*),H'0',CL109'******** - I/O ERROR -'
WTOEND   EQU   *
         EJECT ,
*******************************************************************
*        DATA CONTROL BLOCKS                                      *
*                                                                 *
* 26OCT98 DHC USE SYSTEM DETERMINED BLOCKSIZE                     *
*                                                                 *
*******************************************************************
         SPACE 1
SYSOUT   DCB   DDNAME=SYSOUT,                                          *
               DSORG=PS,                                               *
               SYNAD=IOERROR,                                          *
               EXLST=EXITLIST,                                         *
               RECFM=FBA,                                              *
               MACRF=PM,                                               *
               LRECL=133
         SPACE 3
PRINTJOB DCB   DDNAME=PRINTJOB,                                        *
               DSORG=PS,                                               *
               SYNAD=IOERROR,                                          *
               EXLST=EXITLIST,                                         *
               RECFM=VBA,                                              *
               MACRF=PM,                                               *
               LRECL=137,                                              *
               BLKSIZE=0
         SPACE 3
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               SYNAD=IOERROR,                                          *
               EXLST=EXITLIST,                                         *
               RECFM=FB,                                               *
               MACRF=PM,                                               *
               LRECL=OUTRECL,                                          *
               BLKSIZE=0
         SPACE 3
SYSUT3   DCB   DDNAME=SYSUT3,                                          *
               DSORG=PS,                                               *
               SYNAD=IOERROR,                                          *
               EXLST=EXITLIST,                                         *
               RECFM=FB,                                               *
               MACRF=(PM,GM),                                          *
               LRECL=SUMRECL,                                          *
               BLKSIZE=0
         SPACE 3
INDCB    DCB   DDNAME=DUMMY,DSORG=PS,MACRF=GL,EROPT=ACC,               X
               SYNAD=JESIO
         SPACE 2
SNAPDCB  DCB   DDNAME=SNAP,DSORG=PS,MACRF=W,                           *
               RECFM=VBA,LRECL=125,BLKSIZE=882
         EJECT ,
***********************************************************************
*        COMMON CODE                                                  *
***********************************************************************
         SPACE 1
***********************************************************************
*        I/O ERROR ROUTINE                                            *
***********************************************************************
         SPACE 1
IOERROR  DS    0H                  ANALYSE I/O ERROR
         MVC   COMPCODE,=H'20'     SET COMPLETION CODE
         LA    R4,WTOIOE           POINT TO MESSAGE SKELETON
         SYNADAF ACSMETH=QSAM      CALL ANALYSIS ROUTINE
         MVC   27(86,R4),50(R1)    ADD RESULT OF ANALYSIS TO LINE
         SYNADRLS ,                RELEASE OS BUFFER
         WTO   MF=(E,WTOIOE)       TELL OPERATOR OF ERROR
         DC    A(0)                ABEND S0C1                 *TESTING*
         SPACE 3
***********************************************************************
*        JES2 ERROR ROUTINE                                           *
***********************************************************************
         SPACE 1
JESERROR SUBIN ,                   HANDLE I/O ERROR FROM JES2
         MVC   COMPCODE,=H'4'      SET COMPLETION CODE
         OI    JOBFLAG,DSREQ       ENSURE JOB IS REQUEUED
         DC    A(0)                ABEND S0C1                 *TESTING*
         SPACE 2
***********************************************************************
*        JES2 I/O ERROR ROUTINE                                       *
***********************************************************************
         SPACE 1
JESIO    DS    0H                  HANDLE I/O ERROR FROM JES2
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         L     R3,MDCBDCB          GET DCB ADDRESS
         USING IHADCB,R3           ESTABLISH DCB ADDRESSABILITY
         ICM   R14,B'0111',DCBEODA GET 24-BIT EOD ADDRESS
         DROP  R2,R3               FINISHED WITH MDCB AND DCB
         RETURN ,                  RETURN TO CLOSE FILE
         SPACE 2
***********************************************************************
*        COMMMON PROGRAM TERMINATION CODE                             *
***********************************************************************
         SPACE 1
ENDOFPGM DS    0H                  END OF PROGRAM (COMMON)
         SPACE 1
         PGMOUT RC=(R15)           RETURN TO CALLER
         TITLE 'COPY BLOCKS'
         COPY  BLANK               CLEAR STORAGE
         EJECT ,
         COPY  WRITING             STANDARD OUTPUT
         SPACE 2
         COPY  PUTMOVE             STANDARD QSAM
         EJECT ,
         COPY  GETTINV             STANDARD VARIABLE QSAM
         EJECT ,
         COPY  SETEOD              SET EOD ROUTINE
         EJECT ,
         COPY TODDATE              INCLUDE CALENDAR FUNCTION
         SPACE 2
         COPY  BIOS                STANDARD ACCESS STUFF
         AIF   ('&TYPE' NE 'DSECT').CS2
         SPACE 3
CHEWBACA DSECT ,                   RESUME COMMON STORAGE DSECT
         AGO   .COMM2
.CS2     ANOP
         SPACE 3
CHEWBACA CSECT ,                   RESUME COMMON STORAGE DSECT
.COMM2   ANOP
         EJECT ,
         LTORG *                   COMMON LITERAL POOL
         SPACE 3
CHEWBACL EQU   *-CHEWBACA          LENGTH OF COMMON STORAGE
         TITLE 'COMMON DSECT MAPS'
***********************************************************************
*        CONTROL CHARACTER TABLE ENTRY FORMAT - TABLE IS IN CHEWSKIP  *
***********************************************************************
         SPACE 1
CCTAB    DSECT ,                   FORMAT OF CONTROL CHARACTER TABLE
CC       DS    XL1'19'             CONTROL CHARACTER
CCTYPE   DS    AL1(0)              TYPE OF CC - A OR M
M        EQU   X'10'               MACHINE CONTROL
A        EQU   X'01'               ANSI CONTROL
PAGESKIP EQU   X'F1'               ANSI TOP OF FORM
IMMED    EQU   X'80'               IMMEDIATE CONTROL
CCTHIS   DS    XL1'00'             CARRIAGE CONTROL THIS LINE
CCNEXT   DS    XL1'00'             CARRIAGE CONTROL NEXT LINE
         SPACE 1
CCTABLEN EQU   *-CCTAB             LENGTH OF ONE TABLE ENTRY
         SPACE 2
         CPB   DSECT               CALLED PROGRAM BLOCK
         EJECT ,
         IEFJESCT ,                JES COMMUNICATION TABLE DSECT
         EJECT ,
         IEFJSSOB (SO)             SSOB DSECT WITH PROCESS SYSOUT FUNC
         EJECT ,
         IEFZB4D0 ,                SVC 99 REQUEST BLOCK DSECT
         SPACE 3
         POP   PRINT               RESTORE PRINT STATUS
         MEXIT
.NONAME  ANOP
         MNOTE 12,'NAME IS REQUIRED WHEN TYPE=DSECT'
.MEXIT   ANOP
         MEXIT
         MEND
