LASSOO   TITLE 'LET ADDRESS SPACE SWAP - OR OTHERWISE'
***********************************************************************
*        PDSDOC *PROGRAM TO SET ADDRESS SPACE SWAPPABLE OR NOT........*
*                                                                     *
*        PROGRAM ID     :          LASSOO                             *
*                                                                     *
*        AUTHOR         :          D.H.CARTWRIGHT                     *
*        COPYRIGHT                 D.H.CARTWRIGHT 1993                *
*                                                                     *
*        LOCATION       :          SUTTON COLDFIELD                   *
*                                                                     *
*        HISTORY        :                                             *
*                                                                     *
* 02AUG93 CART1 STARTED WRITING TO SATISFY NEED EXPRESSED BY NISSAN   *
* 23MAR94 CART1 ADD TEST OF ASCBJBNS FOR STARTED TASKS                *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*        -------                                                      *
*                                                                     *
* THIS PROGRAM WAS WRITTEN AFTER NISSAN UK EXPRESSED A NEED TO BE     *
* ABLE TO MAKE A NON-SWAPPABLE ADDRESS SPACE SWAPPABLE. THIS CAN BE   *
* DONE WITH THE LIKES OF 'OMEGAMON', BUT THEY DO NOT HAVE SUCH        *
* LUXURIES. I WENT AWAY TO FIND OUT HOW 'OMEGAMON' MIGHT PERFORM THIS *
* FUNCTION, AND WAS LUCKY ENOUGH TO FIND THE '# COMMAND SUBSYSTEM' ON *
* THE CBT TAPE. THE #D COMMAND (DISPLAY/ZAP CORE) CONTAINS CODE WHICH *
* DOES THIS FUNCTION. I COPIED THE IDEAS FROM THAT CODE AND ADDED     *
* SOME STUFF TO MAKE IT USABLE AS A BATCH JOB.                        *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*        -----------                                                  *
*                                                                     *
* 1) MUST RUN AUTHORISED FROM AN AUTHORISED LIBRARY                   *
*                                                                     *
* 2) RUNS IN 31 BIT ADDRESSING MODE                                   *
*                                                                     *
* 3) DOES NOT APPEAR TO WORK FOR STC'S. NEED TO CHANGE THE ASCB       *
*    LOCATE ROUTINE, PERHAPS TO BE ABLE TO SPECIFY THE ADDRESS        *
*    SPACE ID.                                                        *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        INPUT                                                        *
*        -----                                                        *
*                                                                     *
* 1) THE PARM IS THE ONLY INPUT.                                      *
*    THE FORMAT IS '+JOBNAME' TO SET 'JOBNAME' NON-SWAPPABLE          *
*                  '-JOBNAME' TO SET 'JOBNAME' SWAPPABLE              *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        OUTPUT                                                       *
*        ------                                                       *
*                                                                     *
* 1) THE RETURN CODE IS THE ONLY EXTERNAL OUTPUT FROM THE PROGRAM     *
*        0     REQUESTED FUNCTION COMPLETED                           *
*        4     ADDRESS SPACE WAS ALREADY IN REQUESTED STATE           *
*        8     JOBNAME NOT FOUND ON ASCB READY QUEUE (SWAPPED?)       *
*       12     JOB COULD NOT BE ALTERED (SWAPPED OUT)                 *
*       16     FORMAT ERROR IN PARM                                   *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
LASSOO   CSECT ,
LASSOO   AMODE 31                  EXTENDED ADDRESSING
LASSOO   RMODE ANY                 ANY RESIDENCY MODE
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES                                  *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   PARAMETER PASSING
R2       EQU   2                   WORK
R3       EQU   3                   CVT POINTER
R4       EQU   4                   SUB-ROUTINE LINKAGE
R5       EQU   5                   PARM POINTER
R6       EQU   6                   LENGTH REGISTER
R7       EQU   7                   ASCB POINTER
R8       EQU   8                   OUCB POINTER
R9       EQU   9                   WORK
R10      EQU   10                  WORK
R11      EQU   11                  WORK
R12      EQU   12                  WORK
R13      EQU   13                  SAVE AREA POINTER AND BASE 1
R14      EQU   14                  SYSTEM LINKAGE RETURN
R15      EQU   15                  SYSTEM LINKAGE AND RETURN CODE
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) STANDARD HOUSEKEEPING
         L     R5,0(,R1)           GET ADDRESS OF PARM FIELD
         EJECT ,
***********************************************************************
*        PROGRAM MAIN LINE                                            *
***********************************************************************
         SPACE 1
         BAL   R4,PARMPROC         GO PROCESS THE PARM
         GETAUTH TYPE=GET          ENSURE WE ARE AUTHORISED
         MODESET MODE=SUP,KEY=ZERO GET INTO SUPERVISOR MODE, PK0
         SETLOCK OBTAIN,TYPE=DISP,MODE=UNCOND,REGS=USE,                X
               RELATED=('ENSURE ASCB CHAIN DOES NOT CHANGE') LOCK
         BAL   R4,ASCBSCAN         GO FIND THE ASCB
         USING ASCB,R7             ESTABLISH ASCB ADDRESSABILITY
         L     R8,ASCBOUCB         PICK UP OUCB POINTER
         USING OUCB,R8             ESTABLISH OUCB ADDRESSABILITY
         SPACE 1
         TM    OUCBQFL,OUCBOUT     IS JOB SWAPPED OUT?
         BO    ERROR12             YES, CANNOT PROCEED
         BAL   R4,SETSWAP          GO SET SWAPPABILITY
         SPACE 1
         DROP  R7                  END ASCB ADDRESSABILITY
         DROP  R8                  END OUCB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        PROGRAM TERMINATION                                          *
***********************************************************************
         SPACE 1
GOODEND  DS    0H                  PARM WAS AT LEAST VALID
         SETLOCK RELEASE,TYPE=DISP,REGS=USE,                           X
               RELATED=('FREE LOCK AFTER SCAN OF ASCB CHAIN') UNLOCK
         MODESET MODE=PROB,KEY=NZERO RETURN TO OUR OWN PK
         SPACE 2
PGMEND   DS    0H                  PROGRAM CAME TO SOME END
         PGMOUT RC=RETCODE         RETURN TO CALLER
         TITLE 'FUNCTIONAL SUB-ROUTINES'
***********************************************************************
*        PARM PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 1
PARMPROC DS    0H                  PROCESS PARM FIELD
         LH    R6,0(,R5)           PICK UP PARM LENGTH
         LA    R5,2(,R5)           UPDATE PARM POINTER
         LTR   R6,R6               TEST FOR PARMS
         BZ    ERROR16             EXIT IF NONE
         CH    R6,=H'9'            TEST FOR EXCESS LENGTH
         BH    ERROR16             TOO LONG, BLOW UP
         CLI   0(R5),C'+'          REQUEST NON-SWAPPABLE?
         BE    REQNSWAP            YES, PROCESS IT
         CLI   0(R5),C'-'          REQUEST SWAPPABLE?
         BNE   ERROR16             NO, PARM WRONG.
         OI    RUNFLAG,RUNYSWAP    YES, SET FLAG
         B     REQJNAME            GO FIND JOB NAME
         SPACE 1
REQNSWAP DS    0H                  '+' OPTION FOUND
         OI    RUNFLAG,RUNNSWAP    SET FLAG
         SPACE 1
REQJNAME DS    0H                  ISOLATE JOBNAME
         LA    R5,1(,R5)           UPDATE PARM POINTER
         BCT   R6,*+4+4            REDUCE PARM FOR FUNCTION CHOICE
         B     ERROR16             ERROR IF THAT IS ALL
         BCTR  R6,0                REDUCE AGAIN FOR EXECUTE
         EX    R6,MOVEJOB          COPY THE JOBNAME FROM PARM
         SPACE 1
         BR    R4                  RETURN TO CALLER
         SPACE 1
MOVEJOB  MVC   JOBNAME(*-*),0(R5)  MOVE JOBNAME              *EXECUTED*
         EJECT ,
***********************************************************************
*        ROUTINE TO FIND REQUIRED ASCB                                *
*        ON EXIT R7 --> ASCB FOR 'JOBNAME'                            *
***********************************************************************
         SPACE 1
ASCBSCAN DS    0H                  RUN THE CHAIN OF ASCB'S
         L     R3,CVTPTR           GET CVT POINTER
         USING CVT,R3              ESTABLISH CVT ADDRESSABILITY
         ICM   R7,B'1111',CVTASCBH LOAD ADDRESS OF TOP ASCB
         USING ASCB,R7             ESTABLISH ASCB ADDRESSABILITY
         B     *+4+4               SKIP FIRST LOAD OF LOOP
         SPACE 1
ASCBLOOP DS    0H                  LOOP THROUGH ASCB CHAIN
         ICM   R7,B'1111',ASCBFWDP PICK UP FORWARD POINTER
         BZ    ERROR8              JOB NOT FOUND IF END
         ICM   R1,B'1111',ASCBJBNI GET ADDRESS OF JOBNAME INDICATOR
         BNZ   ASCBTEST            GOT IT, GO SEE IF WANTED
         ICM   R1,B'1111',ASCBJBNS GET ADDRESS OF STC NAME
         BZ    ASCBLOOP            TRY NEXT ASCB IF THIS ONE INVALID
ASCBTEST DS    0H                  FOUND JOBNAME, COMPARE IF WANTED
         CLC   JOBNAME,0(R1)       COMPARE JOBNAMES
         BNE   ASCBLOOP            TRY NEXT IF NOT THIS
         SPACE 1
ASCBSCNX DS    0H                  END OF SCAN
         BR    R4                  RETURN TO CALLER
         SPACE 1
         DROP  R3                  END CVT ADDRESSABILITY
         DROP  R7                  END ASCB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        ROUTINE TO SET SWAPPABILITY OR OTHERWISE                     *
*        ON ENTRY R8 --> OUCB FOR 'JOBNAME'                           *
***********************************************************************
         SPACE 1
SETSWAP  DS    0H                  SET JOBNAME SWAPPABLE AS REQUIRED
         USING OUCB,R8             ESTABLISH OUCB ADDRESSABILITY
         TM    OUCBSFL,OUCBNSW     IS THE JOB NON-SWAPPABLE?
         BNO   SETSWAPY            NO, SEE ABOUT MAKING IT SO
         TM    RUNFLAG,RUNNSWAP    WAS NON-SWAPPABILITY REQUESTED?
         BO    ERROR4              YES, SO REDUNDANT REQUEST
         B     SETSWAPZ            GO REVERSE THE BIT
         SPACE 1
SETSWAPY DS    0H                  JOBNAME IS CURRENTLY SWAPPABLE
         TM    RUNFLAG,RUNYSWAP    WAS SWAPPABILITY REQUESTED?
         BO    ERROR4              YES, SO REDUNDANT REQUEST
         SPACE 1
SETSWAPZ DS    0H                  REVERSE THE NON-SWAPPABILITY BIT
         SETLOCK OBTAIN,TYPE=CPU,REGS=USE,                             X
               RELATED=ZAPIT       DISABLE INTERRUPTS FOR CHANGE
         XI    OUCBSFL,OUCBNSW     REVERSE THE BIT
         SETLOCK RELEASE,TYPE=CPU,REGS=USE,                            X
               RELATED=ZAPIT       ENABLE INTERRUPTS AFTER CHANGE
         SPACE 1
SETSWAPX DS    0H                  END OF ROUTINE
         BR    R4                  RETURN TO CALLER
         SPACE 1
         DROP  R8                  END OUCB ADDRESSABILITY
         TITLE 'ERROR HANDLING'
ERROR4   DS    0H                  REDUNDANT REQUEST
         MVC   RETCODE,=H'4'       SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR8   DS    0H                  JOBNAME NOT FOUND
         MVC   RETCODE,=H'8'       SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR12  DS    0H                  JOBNAME SWAPPED OUT
         MVC   RETCODE,=H'12'      SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR16  DS    0H                  INVALID REQUEST
         MVC   RETCODE,=H'16'      SET RETURN CODE
         B     PGMEND              GO TO EXIT
         SPACE 1
         TITLE 'DATA AREAS'
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 1
RETCODE  DC    H'0'                PROGRAM RETURN CODE
JOBNAME  DC    CL8' '              AREA FOR JOBNAME
RUNFLAG  DC    X'00'               RUN TIME FLAG
RUNNSWAP EQU   X'80'               NON-SWAPPING REQUESTED
RUNYSWAP EQU   X'40'               SWAPPABILITY REQUESTED
         EJECT ,
         IRAOUCB DSECT=YES         MAP SRM USER CONTROL BLOCK
         EJECT ,
         CVT   DSECT=YES,LIST=NO   MAP COMMUNICATIONS VECTOR TABLE
         EJECT ,
         IHAPSA DSECT=YES,LIST=NO   MAP PSA
         EJECT ,
         IHAASCB DSECT=YES,LIST=NO  MAP ASCB
         END ,
