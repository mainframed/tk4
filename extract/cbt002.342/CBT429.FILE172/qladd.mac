***********************************************************************
*                                                                     *
*        PDSDOC *CODE TO ADD AN ELEMENT TO A CHAIN OF STORAGE AREAS...*
*                                                                     *
* ELEMENTS ARE CHAINED BY THE FIRST FULLWORD OF THE AREA, AND ARE     *
* CHAINED IN A FORWARD DIRECTION ONLY                                 *
*                                                                     *
*        ON ENTRY:                                                    *
*              R0   =  LENGTH OF STORAGE ELEMENT                      *
*              R1   -> ANCHOR ADDRESS OF THE CHAIN                    *
*                                                                     *
*        ON EXIT:                                                     *
*              R1   -> NEW ELEMENT                                    *
*                                                                     *
***********************************************************************
         SPACE 2
QLADD    SUBIN SAVE=(R3,R4)        SUBROUTINE ENTRY
         LR    R3,R1               SAVE CHAIN HEADER
         GETMAIN R,LV=(0)          GET STORAGE ELEMENT
         BAL   R4,CLEAR            GO CLEAR TO ZEROES
         SPACE 1
QLADDIN  DS    0H                  CHAIN ELEMENT OFF THE END
         SR    R4,R4               SET OPERAND1 TO ZERO
         CS    R4,R1,0(R3)         IF CHAIN FIELD ZERO, ADD NEW ELEMENT
         BE    QLADDX              QUEUED, EXIT
         LR    R3,R4               NO, GET ADDRESS OF NEXT ELEMENT
         B     QLADDIN             THEN RUN UP THE CHAIN
         SPACE 2
QLADDX   DS    0H                  END OF SUBROUTINE
         SUBOUT RESTORE=(R3,R4)    RETURN TO CALLER
         EJECT ,
         COPY  CLEAR               COPY CODE TO CLEAR STORAGE
