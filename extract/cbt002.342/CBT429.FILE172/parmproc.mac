*******************************************************************
*        PDSDOC *SAVE ADDRESS AND LENGTH OF PARM OF EXEC CARD.....*
*                                                                 *
* REQUIRES THE FOLLOWING DATA ITEMS TO BE DEFINED;                *
*                                                                 *
* PARMADDR DC    A(0)                ADDRESS OF PARM FIELD        *
* PARMLGTH DC    A(0)                LENGTH OF PARM FIELD         *
* PARMFLAG DC    XL1'00'             FLAGS FOR PARM VALUES        *
* GOODPARM DC    C'VALUE'            VALID PARM OPTION            *
* PARMGOOD EQU   X'80'               AND ITS FLAG VALUE           *
*                                                                 *
* 06NOV98 DHC COPIED CODE FROM 'DEMANDS', GENERALISED IT          *
*******************************************************************
         SPACE 1
PARMPROC DS    0H                  PROCESS PARM FIELD
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         L     R1,0(R1)            LOAD ADDRESS OF PARM INTO R1
         ST    R1,PARMADDR         STORE ADDRESS OF PARM INFO'S
         LH    R2,0(R1)            GET ADDRESS OF PARM INFO'S
         ST    R2,PARMLGTH         SAVE PARM LENGTH
         LTR   R2,R2               TEST FOR A PARM
         BZ    PARMDONE            NONE, FORGET IT
         BCTR  R2,0                DECREMENT FOR EXECUTE
         EX    R2,PARMCLC          TEST FOR PARM=GOOD
         BNE   PARMDONE            NO, DEFAULT IS PRETTY
         OI    PARMFLAG,PARMGOOD   YES, SET FLAG
         SPACE 1
PARMDONE DS    0H                  PARM FIELD PROCESSED
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 2
PARMCLC  CLC   2(*-*,R1),GOODPARM  TEST FOR PARM=GOOD      *EXECUTED*
