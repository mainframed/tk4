         TITLE 'DCS14DE1 - EXTRACT PDS FILE USAGE INFORMATION'
DCS14DE1 CSECT
         PRINT NOGEN               DON'T EXPAND MACROS
         PRINT GEN                 EXPAND MACROS
***********************************************************************
*                                                                     *
* MODULE NAME:    DCS14DE1                                            *
*                                                                     *
* TITLE:          DARNICK COMPUTING SMF 14 DATA EXTRACT PROGRAM #1    *
*                                                                     *
* PARAMETERS:     NONE                                                *
*                                                                     *
* INPUT:          SMF DATA CONTAINING TYPE 14 RECORDS                 *
*                                                                     *
* OUTPUT:         PDSUSE   - PDS FILE USAGE RECORDS                   *
*                                                                     *
* EXIT NORMAL:    AT PROGRAM END - VIA R14                            *
*                                                                     *
*                 RETURN CODE: 0  - FUNCTION COMPLETED OK             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* 13OCT98 DHC RE-ARRANGE SUMMARY RECORD FOR BETTER COMPRESSABILITY    *
* 06OCT98 DHC ADD JOBNAME                                             *
* 25AUG98 DHC ONLY PROCESS LOAD LIBRARIES FOR LLA                     *
* 21AUG98 DHC STARTED TO WRITE.                                       *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* REGISTER USAGE:                                                     *
*                                                                     *
R0       EQU   0               PARAMETER
R1       EQU   1               PARAMETER
R2       EQU   2               WORK REG
R3       EQU   3               WORK REG
R4       EQU   4               INTERNAL LINKAGE
R5       EQU   5               WORK REG
R6       EQU   6               WORK REG
R7       EQU   7               WORK REG
R8       EQU   8               SECOND PGM BASE
R9       EQU   9               SUBROUTINE LINK
R10      EQU   10              INPUT RECORD POINTER
R11      EQU   11              OUTPUT RECORD POINTER
R12      EQU   12              .
R13      EQU   13              CURRENT SAVE AREA AND BASE REGISTER 1
R14      EQU   14              STD LINKAGE/WORK
R15      EQU   15              STD LINKAGE/WORK
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        NORMAL ENTRY- AND LINKAGE CONVENTION CODE.                   *
*                                                                     *
***********************************************************************
         USING *,R15
         B     DCLSTART        BRANCH AROUND EYECATCHER
SAVEAREA DC    18F'0'              REGISTER SAVE AREA
         DC    CL8'DCS14DE1'   MODULE NAME
         DC    CL8'R1.0'       RELEASE
         DC    CL8'&SYSDATE'   ASSEMBLY DATE
         DC    CL8'  &SYSTIME' ASSEMBLY TIME
DCLSTART DS    0F
         SAVE  (14,12)
         LA    R10,SAVEAREA        POINT TO SAVE AREA
         ST    R10,8(,R13)         SET SAVE AREA
         ST    R13,4(,R10)         CHAIN SAVE AREAS
         LR    R13,R10             SET NEW SAVEAREA POINTER
         DROP  R15                 END TEMPORARY ADDRESSABILITY
         USING SAVEAREA,R13        ESTABLISH PROGRAM ADDRESSABILITY
         SPACE 2
         PUSH  PRINT
*        PRINT NOGEN
         SPACE 2
         OPEN  (SMFIN,(INPUT),PDSUSE,(OUTPUT)) OPEN FILES
         TITLE 'DCS14DE1 - PROCESS SMF 14 RECORDS'
***********************************************************************
*                                                                     *
*        MAIN PROCESSING - SMF 14                                     *
*                                                                     *
***********************************************************************
         SPACE 2
GETSMF   DS    0H
         GET   SMFIN               GET A SMF RECORD
         LR    R10,R1              POINT TO RECORD
         USING SMF14,R10           ESTABLISH SMF RECORD ADDRESSABILITY
         CLI   SMF14RTY,14         IS IT A DATA READ RECORD?
         BE    GOTSMF              YES, PROCESS IT
         CLI   SMF14RTY,15         IS IT A DATA WRITE RECORD?
         BNE   GETSMF              NO, TRY THE NEXT ONE
GOTSMF   DS    0H                  PROCESS THIS SMF RECORD
         BAL   R4,SUMMOUT          GO CREATE SUMMARY RECORD
         B     GETSMF              THEN GO ROUND AGAIN
         TITLE 'PROGRAM TERMINATION PROCESSING'
***********************************************************************
*                                                                     *
*        RETURN                                                       *
*                                                                     *
***********************************************************************
ENDPGM   DS    0H                  END OF PROCESSING
         SPACE 2
         CLOSE (SMFIN,,PDSUSE)     CLOSE FILES
         L     R15,RETNCODE        GET RETURN CODE
         L     R13,SAVEAREA+4      HIGHER SAVE AREA
         LM    R0,R12,20(R13)      RESTORE R0-R12
         L     R14,12(R13)         RESTORE R14
         BR    R14                 RETURN TO CALLER
         TITLE 'SUMMARY RECORD CREATION SUB-ROUTINE'
***********************************************************************
*                                                                     *
*        SUB ROUTINE TO CREATE SIMPLE SUMMARY RECORD                  *
*                                                                     *
***********************************************************************
         SPACE 2
SUMMOUT  DS    0H                  SUBROUTINE ENTRY
         TM    SMFDCBOR,DCBDSGPO   IS IT A PDS?
         BNO   SUMMOUTX            NO, LOSE INTEREST
         LA    R11,SMFJFCB1        POINT TO THE JFCB AREA
         USING INFMJFCB,R11        ESTABLISH ADDRESSABILITY
         TM    JFCRECFM,JFCUND     COULD IT BE A LOAD LIBRARY?
         BNO   SUMMOUTX            NO, SKIP IT
         MVC   SUMDSN,JFCBDSNM     COPY DATASET NAME
         SPACE 1
         MVC   SUMJOBN,SMF14JBN    COPY JOB NAME
         MVC   SUMEXCP,SMFEXCP     COPY EXCP COUNT OF FIRST UCB
         SR    R1,R1               CLEAR COUNT
         CLI   SMF14RTY,14         IS IT A DATA READ RECORD?
         BE    *+4+4               YES, UPDATE COUNT NOT INCREASED
         LA    R1,1                ADD ONE TO UPDATES
         STCM  R1,B'1111',SUMUPDT  SAVE UPDATE COUNT
         PUT   PDSUSE,SUMREC       OUTPUT SUMMARY RECORD
         SPACE 1
SUMMOUTX DS    0H                  RETURN TO CALLER
         BR    R4                  RETURN TO CALLER
         DROP  R11                 END JFCB ADDRESSABILITY
         DROP  R10                 END SMF ADDRESSABILITY
         POP   PRINT               RESTORE PRINT OPTIONS
         TITLE 'DCS14DE1 - WORK AREAS AND DSECTS'
***********************************************************************
*                                                                     *
*        VARIOUS WORK AREAS                                           *
*                                                                     *
***********************************************************************
         SPACE 2
DWORK    DS    D                   DOUBLE WORD WORK AREA
MSUB14   DC    F'0'                MESSAGE SUBROUTINE SAVE AREA
RETNCODE DC    F'0'                PROGRAM RETURN CODE
         EJECT
* LITERAL CONSTANTS
         LTORG
         EJECT
* DCB DEFINITIONS
         PUSH  PRINT
         PRINT NOGEN
SMFIN    DCB   DDNAME=SMFIN,MACRF=GL,DSORG=PS,EODAD=ENDPGM   SMF
         SPACE 2
PDSUSE   DCB   DDNAME=PDSUSE,MACRF=PM,DSORG=PS,RECFM=FB,LRECL=SUMRECL
         POP   PRINT
         EJECT
***********************************************************************
*                                                                     *
*  SUMMARY RECORD AREA                                                *
*                                                                     *
***********************************************************************
         SPACE 2
SUMREC   DS    0H                                                   CC
SUMDSN   DC    CL44' '             DATASET NAME                      1
SUMUPDT  DC    AL4(0)              NUMBER OF TIMES UPDATED          45
SUMONE   DC    AL4(1)              CONSTANT FOR COUNTING            49
SUMJOBN  DC    CL8' '              JOB NAME                         53
SUMEXCP  DC    AL4(0)              EXCP COUNT                       61
SUMRECL  EQU   *-SUMREC            LENGTH OF SUMMARY RECORD         64
         EJECT
***********************************************************************
*                                                                     *
*  SYSTEM DATA AREAS                                                  *
*                                                                     *
***********************************************************************
         SPACE 2
         PUSH  PRINT
         PRINT NOGEN
*        PRINT GEN
         DCBD  DSORG=PS,DEVD=DA
         POP   PRINT
         EJECT
***********************************************************************
*                                                                     *
*  RECORD AREA DSECTS                                                 *
*                                                                     *
***********************************************************************
         SPACE 2
         PUSH  PRINT
         PRINT GEN
         DS    0F                  ALIGNMENT
SMF14    DSECT ,                   LOGICAL DEFINITION ONLY
         IFASMFR (14)              SMF RECORD MAPPING
         EJECT
JFCB     DSECT ,                   MAP THE JFCB AREA
         IEFJFCBN LIST=YES         JFCB WITH LISTING
         SPACE 2
         POP   PRINT
DCS14DE1 CSECT
         SPACE 2
         END   DCS14DE1
