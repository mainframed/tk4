         TITLE 'FINDVOL  ==> SUBROUTINE TO CHECK FOR ONLINE VOLSER'
***********************************************************************
*                                                                     *
* THIS PROGRAM IS ADAPTED FROM "FINDVOL", A ROUTINE WHICH IS BELIEVED *
* TO COME FROM THE GERMAN OR SWISS GUIDE GOODIES TAPE.                *
* IT WAS ADAPTED FOR OS/390 2.6 BY DAVID CARTWRIGHT AT AGCO UK LTD.,  *
* DESFORD ENGLAND. THIS VERSION IS NOT RE-ENTRANT.                    *
*                                                                     *
* ON ENTRY R1 --> 6 CHARACTER VOLUME SERIAL                           *
*                                                                     *
*                                                                     *
* 03NOV99 DHC REPLACED UCB SCAN IN "FINDVOL".                         *
*                                                                     *
***********************************************************************
FINDVOL  RMODE ANY
FINDVOL  AMODE 31                  EXTENDED ADDRESSING
FINDVOL  CSECT  ,
         PGMIN EQUATES=YES,USING=(R12)
         SPACE 3
         LR    R6,R1               SAVE PARM ADDRESS
         GOSUB USERSUP             GET SUPERVISOR STATE
         SPACE 1
         UCBLOOK VOLSER=(6),NOPIN,DEVCLASS=DASD,  FIND UCB             X
               DYNAMIC=YES,RANGE=ALL,UCBPTR=ADDRUCB WHEREVER IT IS
         LR    R5,R15              SAVE RETURN CODE
         GOSUB MODEPROB            RETURN TO PROBLEM PROGRAM MODE
         TITLE 'FINDVOL  ==> CHECK RESULTS - ONLINE => RETURN'
SETUCB2  LTR   R15,R5              RESTORE RETURN CODE
         BNZ   SETUCB4
         SPACE
         L     R5,ADDRUCB          GET UCB ADDRESS
         USING UCBOB,R5              AND ESTABLISH ADDRESSABILITY
         SPACE
         TM    UCBSTAT,UCBONLI     IS DEVICE ONLINE ?
         BZ    SETUCB4             NO => REJECT
         SPACE
         SR    R15,R15             SET RC = 0
         B     ENDE01                      RETURN
SETUCB4  DS    0H
*        DC    A(0)                ABEND S0C1          *DEBUG*
         LA    R15,12              VOLSER NOT FOUND/OFFLINE
         XR    R5,R5               R1 = ZERO
         SPACE 2
ENDE01   DS    0H
         SPACE
         LR    R1,R5               LOAD UCB ADDRESS
         PGMOUT RC=(15)            RETURN TO CALLER
         EJECT ,
         COPY  SETMODE             STANDARD MODESET
         TITLE 'WORK AREAS'
         LTORG
         SPACE 3
ADDRUCB  DS    F        ADDRESS OF UCB
         SPACE
         PRINT NOGEN
         EJECT
MAPPING  DSECT ,                  DON'T CONFUSE THE ASSEMBLER
         IEFUCBOB DEVCLAS=DA      UCB MAPPING MACRO
         EJECT
         CVT   DSECT=YES
         END
