         TITLE 'O C X  -  OPERATOR COMMAND EXECUTOR'
*----------------------------------------------------------------------
*
*        THIS ROUTINE PROVIDES FOR JOB SUBMISSION AND AUTOMATIC
*        COMMANDS IN AN MVS/JES2 ENVIRONMENT.  IT'S FUNCTIONS
*        ARE THREE-FOLD AND ARE EXPLAINED BELOW.
*
*
*        1=RDR  ALLOW AN EASY WAY OF PRESENTING JOBS TO JES2 VIA AN
*               OPERATOR MODIFY COMMAND.
*
*        2=CMD  ALLOW AN EASY WAY OF PRESENTING MULTIPLE COMMANDS TO
*               MVS/JES2 VIA AN OPERATOR MODIFY COMMAND.
*
*        3=AUTO ALLOW FOR TIME-OF-DAY/DAY-OF-WEEK AUTOMATIC COMMANDS
*
*
*        THE USER MUST PROVIDE A STARTED TASK PROCEDURE IN SYS1.PROCLIB
*        WHICH IS STARTED AT IPL TIME.  ONCE ACTIVE, IT WILL PROCESS
*        USER DEFINED COMMANDS BY DAY OF WEEK (USER DEFINED).
*        ALSO, IT WILL SUPPORT OPERATOR MODIFY COMMANDS (DEMAND)
*        EITHER SUBMIT JOBS (MEMBERS) TO THE INTERNAL READER OR
*        EXECUTE COMMANDS (MEMBER LISTS).
*
*        FOR THE JOB SUBMISSION FEATURE, ONE OR MORE PARTITIONED
*        DATASET(S) ARE USED.  FOR THE COMMAND/AUTO-COMMAND
*        FUNCTIONS, ANOTHER PARTITIONED DATASET IS USED.  EACH
*        ARE DESCRIBED BELOW (BY DDNAME).
*
***********************************************************************
*        THIS CODE WAS ORIGINALLY ON THE SHARE MODS TAPE AS A
*        PGM NAMED "AUTO".  SUPPORT FOR THE % CONTROL COMMANDS
*        HAVE BEEN ADDED TO THAT VERSION.
***********************************************************************
         EJECT
***********************************************************************
*
*
*
*        DDNAME     USE AND FUNCTION
*        --------   -------------------------------------------------
*
*
*        INTRDR     REQUIRED IF THE JOB-SUBMISSION FUNCTION IS TO
*                   BE USED.  IT SHOULD SPECIFY THE JES2 INTERNAL
*                   READER.
*
*        JOB00      REQUIRED IF THE JOB-SUBMISSION FUNCTION IS TO
*                   BE USED.  IT SHOULD SPECIFY THE NAME OF A
*                   PARTITIONED DATASET WHICH CONTAINS MEMBERS
*                   TO BE SUBMITTED TO JES2.
*
*        JOB??      OPTIONAL WHEN THE JOB-SUBMISSION FUNCTION IS USED.
*                   ANY NUMBER OF ADDITIONAL 'JOB??' DDCARDS MAY BE
*                   USED TO ALLOW FOR SPECIALIZED FUNCTION (WITH
*                   DUPLICATE MEMBER NAMES).  THIS OPTION IS SIMILAR
*                   TO THE MULTIPLE 'PROC??' DDCARDS WITHIN THE JES2
*                   STARTUP PROC.  USE OF THESE OPTIONAL LIBRARIES
*                   IS CONTROLED BY THE OPERATOR (VIA THE 'D=??'
*                   KEYWORD, AS DETAILED BELOW).
*
*        COMMANDS   THIS DD STATEMENT IS REQUIRED FOR EITHER THE
*                   OPERATOR COMMAND-LIST OR THE AUTO-COMMAND
*                   FUNCTION.  IT SHOULD CONTAIN MEMBERS (LISTS
*                   OF COMMANDS) IN THE FORMAT DESCRIBED UNDER THE
*                   HEADING 'COMMAND RECORD SYNTAX' BELOW.
*
*
*        THE FOLLOWING IS AN EXAMPLE OF THE TYPE OF PROC TO
*        BE ADDED TO YOUR SYS1.PROCLIB.  NOTE THAT THE 'JOB00'
*        DDNAME IS REQUIRED TO USE THE JOB SUBMISSION FUNCTION
*        WHILE 'JOB01' AND 'JOB02' ARE OPTIONAL (USED IN THIS
*        CASE AS REMOTE JOB CONTROL LIBRARIES).
*
*        //OCX      EXEC PGM=OCX,TIME=1440
*        //STEPLIB  DD DISP=SHR,DSN=USER.LIBRARY    <---- AUTHORIZED
*        //INTRDR   DD SYSOUT=(A,INTRDR)
*        //COMMANDS DD DISP=SHR,DSN=SYS1.COMMANDS
*        //JOB00    DD DISP=SHR,DSN=RMT0.JCL.LIB
*        //JOB01    DD DISP=SHR,DSN=RMT1.JCL.LIB
*        //JOB02    DD DISP=SHR,DSN=RMT2.JCL.LIB
*
*        USE OF THE OPTIONAL 'JOB??' DDNAMES IS SPECIFIED BY THE
*        OPERATOR USING THE 'D=' OPERAND (DETAILED BELOW).
*
***********************************************************************
         EJECT
***********************************************************************
*
*        THE FOLLOWING ARE EXAMPLES OF NORMAL OPERATOR COMMANDS
*        JOB SUBMISSION USE:
*
*        FUNCTION  COMMAND      OPERATION
*        --------  ------------ --------------------------------------
*
*        START     S OCX.OCX    NORMAL OPERATOR START-UP.  THE FIRST
*                               AUTOMATIC COMMAND TO BE PROCESSED
*                               WILL BE THE MEMBER WITH A NAME THAT
*                               SPECIFIED A TIME GREATER THAN CURRENT.
*                               HOWEVER, DEMAND COMMAND EXECUTION
*                               AS WELL AS JOB SUBMISSION IS AVAILABLE
*                               IMMEDIATELY.
*
*        IPL       S OCX.OCX,PARM=IPLMEM
*                               SAME AS ABOVE, EXCEPT THAT THE COMMAND
*                               MEMBER NAMED IN THE PARM FIELD WILL BE
*                               EXECUTED BEFORE ANY OTHER REQUESTS
*                               (AUTOMATIC OR OPERATOR REQUESTED
*                               COMMAND).  THIS PROVIDES AN ALTERNATIVE
*                               TO THE 'SYS1.PARMLIB(COMMND??)' OPTION
*                               PROVIDED BY MVS.  IT ALSO PROVIDES FOR
*                               ANY TYPE OF COMMAND (JES2 COMMANDS AS
*                               WELL AS MVS COMMANDS).
*
*        SUBMIT  F OCX,-MEMBER1  SUBMIT 'MEMBER1' TO THE JES2 INTERNAL
*                               READER.  SINCE THE 'D=??' OPTION WAS
*                               NOT USED, THE MEMBER WILL BE READ FROM
*                               THE 'JOB00' DD (WHICH WILL REMAIN OPEN
*                               OPEN UNTIL SOMETHING OTHER UNTIL 'D=??'
*                               IS SPECIFIED AND IS NOT THE SAME AS THE
*                               ONE USED PREVIOUSLY).
*
*        SUBMIT  F OCX,-MEMBER1,MEMBER2,....,MEMBERN
*                               SUBMIT MULTIPLE MEMBERS TO THE JES2
*                               INTERNAL READER.  SINCE 'D=??' WAS NOT
*                               SPECIFIED, THE 'JOB00' IS AGAIN USED.
*
*        SUBMIT  F OCX,-MEMBER1,D=01
*                               SUBMIT A MEMBER (JOB) TO THE JES2
*                               INTERNAL READER.  SINCE 'D=01' WAS
*                               SPECIFIED, THE 'JOB00' FILE IS CLOSED
*                               AND THE 'JOB01' FILE IS OPENED TO
*                               READ THE MEMBER.  THIS FILE WILL
*                               REMAIN OPEN UNTIL THE 'D=??' IS
*                               CHANGED (FROM 'D=01') OR IS NOT
*                               SPECIFIED ('D=00' IS THE DEFAULT).
*
*        SUBMIT  F OCX,-MEMBER1,MEMBER02,.....,MEMBERN,D=01
*                               SAME AS THE ABOVE, HOWEVER, MULTIPLE
*                               MEMBERS WILL BE READ FROM THE 'JOB01'
*                               FILE AND SUBMITTED.  NOTE THAT THE
*                               'D=??' KEYWORD MUST BE SPECIFIED AS
*                               THE LAST OPTION.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        THE FOLLOWING ARE EXAMPLES OF OPERATOR COMMANDS TO SCHEDULE
*        COMMAND LISTS.
*
*        FUNCTION  COMMAND      OPERATION
*        --------  ------------ --------------------------------------
*
*        COMMAND F OCX,MEMBER1  CAUSE A LIST OF COMMANDS TO BE EXECUTED
*                               BY MVS (OR JES2).  NOTE THAT THE DASH
*                               (HYPHEN) AS A PREFIX TO THE MEMBER
*                               NAME IDENTIFIES THE REQUEST AS A
*                               COMMAND REQUEST (DEMAND).  THE NAMED
*                               MEMBER WILL BE READ FROM THE FILE
*                               IDENTIFIED BY THE DD 'COMMANDS'.  EACH
*                               COMMAND READ WILL BE EXECUTED IF RE-
*                               QUESTED FOR THE CURRENT DAY (THE SYNTAX
*                               SYNTAX OF THE 'COMMANDS' RECORDS ARE
*                               DEFINED BELOW UNDER 'COMMAND RECORD
*                               SYNTAX').
*
*        COMMAND F OCX,MEMBER1,MEMBER2,.....,MEMBERN
*                               CAUSE A LIST OF MEMBERS TO BE PROCESSED
*                               BY MVS (OR JES2).  EACH MEMBER MAY
*                               CONTAIN MULTIPLE COMMANDS. AGAIN NOTE
*                               THAT THE DASH IDENTIFIES THE REQUEST
*                               AS A DEMAND COMMAND REQUEST.
*
*        SHUTDOWN  P OCX        TO TERMINATE THIS ROUTINE, SIMPLY
*                               ENTER AN OPERATOR STOP COMMAND.
*
*        SHUTDOWN  F OCX,SHUTDOWN
*                               TO ALLOW A LIST OF 'SHUTDOWN' COMMANDS
*                               TO BE EXECUTED PRIOR TO STOPPING THIS
*                               ROUTINE (SUCH AS $PI, I SMF, P RMF ETC)
*                               THE 'P A' COMMAND MAY BE ADDED TO THE
*                               END OF THE SHUTDOWN MEMBER.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        THE AUTOMATIC COMMAND FUNCTION IS NOT CONTROLLED BY THE
*        OPERATOR.  INSTEAD, IT OPERATES ON A 1440 MINUTE CYCLE
*        (60 MINUTES/HOUR AND 24 HOURS/DAY) WITHIN DAY-OF-WEEK
*        AND MONTH-OF-YEAR CYCLES.  THIS MEANS THAT COMMANDS MAY
*        BE SCHEDULED AT A SPECIFIED TIME OF DAY ON ANY (OR ALL)
*        DAY(S) OF THE WEEK OR ANY DAY OF THE MONTH.
*
*        COMMANDS TO BE EXECUTED ARE CONATAINED IN MEMBERS THAT
*        RESIDE IN THE 'COMMANDS' LIBRARY.  EACH MEMBER TO BE
*        AUTOMATICALLY PROCESSED IS NAMED AS FOLLOWS.
*
*            @0100   THIS LIST OF COMMANDS IS TO BE PROCESSED AT
*                    1:00 IN THE MORNING.
*            @1423   THIS MEMBER WILL BE PROCESSED AT 2:23 PM.
*
*            @0000   THIS MEMBER WILL BE PROCESSED AT MIDNIGHT.
*
*        NOTE THAT THE MEMBER NAMES TO BE PROCESSED AUTOMATICALLY
*        MUST BE PREFIXED WITH THE 'AT SIGN' (@), HOWEVER, THESE
*        MEMBERS ARE NOT RESTRICTED FROM 'DEMAND' USE.
*
*
*        COMMAND RECORD SYNTAX
*
*        EACH MEMBER CONTAINED IN THE 'COMMANDS' LIBRARY MUST FOLLOW
*        THE SYNTAX DEFINED BELOW.  THIS IS TRUE FOR BOTH DEMAND
*        COMMAND LISTS AS WELL AS AUTOMATIC COMMAND LISTS.
*
*        AN ASTERISK (*) IN COLUMN 1 DENOTES A COMMENT RECORD AND IS
*        IGNORED BY THE PROCESSOR.  COMMANDS (RECORDS) ARE PROCESSED
*
*        A  PERCENT (%) IN COLUMN 16 DENOTES AN EXTENDED CONTROL COM-
*        MAND.  THIS ALLOWS SOME MINIMAL LOGIC AND OPERATOR INTERAC-
*        TION.
*
*        A NON-BLANK IN THE 'DAY-OF-THE-WEEK' FIELD REQUESTS PROCESSING
*        ON THAT DAY.
*
*        FORMAT:
*
*        1    ===>BLANK            (AN ASTERISK SIGNIFIES A COMMENT)
*        2-6  ===>MM/DD            (MONTH/DAY SPEC - SLASH REQ'D!)
*        15   ===>(BLANK)
*        8-14 ===>MTWTFSS          (DAY-OF-WEEK SPECIFICATION)
*        15   ===>(BLANK)
*        16   ===>COMMAND DATA     (THROUGH COLUMN 71)
*        16   ===>%CONTROL CMD     (THROUGH COLUMN 71)
*        72   ===>CONTINUATION     (ONLY ONE CONTINUATION ALLOWED)
*
*        AN ASTERISK IN EITHER THE MM FIELD OR THE DD FIELD ALLOWS
*        EXECUTION FOR ALL MONTHS OR ALL DAYS.  ASTERISKS IN BOTH
*        FIELDS EFFECTIVELY NULLIFIES DAY-WITHIN-MONTH CRITERIA
*        (IF '**/**' IS SPECIFIED, THE COMMAND IS SUBJECT ONLY TO
*        DAY OF WEEK CRITERIA WHICH IS THE SAME AS ALL BLANKS IN
*        THE MM/DD FIELD).  IF THE MM/DD FIELD IS NON-BLANK,
*        THE REQUEST WILL BE PROCESSED ON THE MONTH/DAY SPECIFIED
*        BUT IS ALSO SUBJECT TO DAY OF WEEK SPECIFICATIONS.
*        IF A REQUEST IS TO BE PROCESSED ON THE SAME DAY OF EVERY
*        MONTH, SPECIFY THE MONTH AS ASTERISKS ('**/05').  IF A
*        REQUEST IS TO BE PROCESSED EVERY DAY OF A GIVEN MONTH,
*        SPECIFY THE DAY AS ASTERISKS ('09/**').
*
*        CONTINUATION IS SUPPORTED ONLY IF THE SUBSEQUENT RECORD
*        HAS 'ALL' BLANKS IN COLUMNS 1 THROUGH 15 AND CONTINUED
*        DATA BEGINS IN COLUMN 16.
*
*
*   CONTROL COMMANDS:
*
*   COLUMNS:  1      8       16   21        31        41          71
*   COL-1---->*MM/DD MTWTFSS %IF TIME HHMM-HHMM
*        ALLOW A TIME RANGE EVALUATION.
*        DETERMINE IF CURRENT TIME FALLS WITHIN SPECIFIED RANGE AND -
*        DO THE FOLLOWING COMMANDS UNTIL %END IF IT DOES
*   COL-1---->*MM/DD MTWTFSS %WTO MESSAGE------------------------>71
*        ISSUE A HIGHLIGHTED WTO MESSAGE TO THE OPERATOR CONSOLE
*   COL-1---->*MM/DD MTWTFSS %DOM
*        DELETE THE LAST MESSAGE ISSUE VIA %WTO
*        NOTE: %ASK WILL AUTOMATICALLY DELETE THE LAST %WTO MSG
*   COL-1---->*MM/DD MTWTFSS %WAT NN
*        WAIT "NN" SECONDS BEFORE CONTINUING
*   COL-1---->*MM/DD MTWTFSS %ASK OPTION1   OPTION2   OPTION3
*        ASK OPERATOR TO RESPOND WITH ONE OF THE SPECIFIED OPTIONS
*        (UP TO 5 OPTIONS CAN BE SPECIFIED)
*   COL-1---->*MM/DD MTWTFSS %RSP REPLY     DO
*        COMPARE RESPONSE FROM LAST %ASK WITH "REPLY" .  EXECUTE ALL
*        THE FOLLOWING LOWING COMMANDS UNTIL %END IF REPLY MATCHES.
*   COL-1---->*MM/DD MTWTFSS %RSP REPLY     STOP
*        COMPARE RESPONSE FROM LAST %ASK WITH "REPLY" .  STOP PROCESS-
*        ING ALL REMAINING COMMANDS IN THIS MEMBER IF REPLY MATCHES.
*   COL-1---->*MM/DD MTWTFSS %END
*        INDICATE THE END OF A %RSP ... DO STRUCTURE
*        INDICATE THE END OF A %IF TIME RANGE STRUCTURE
*
***********************************************************************
         EJECT
***********************************************************************
*
*        EXAMPLES OF AUTOMATIC COMMANDS:
*
*   COLUMNS:  1      8       16   21        31        41          71
*             ..     .       .    .         .         .           .
*   COL-1---->*MM/DD MTWTFSS COMMAND-- --------------------------->72
*   COL-1---->*MM/DD MTWTFSS %IF TIME HHMM-HHMM
*   COL-1---->*MM/DD MTWTFSS %WTO MESSAGE------------------------>71
*   COL-1---->*MM/DD MTWTFSS %DOM
*   COL-1---->*MM/DD MTWTFSS %WAT NN
*   COL-1---->*MM/DD MTWTFSS %ASK OPTION1   OPTION2   OPTION3---->71
*   COL-1---->*MM/DD MTWTFSS %RSP REPLY     DO
*   COL-1---->*MM/DD MTWTFSS %RSP REPLY     STOP
*   COL-1---->*MM/DD MTWTFSS %END
*
* MEMBER=@0300
*   COL-1---->       XXXXXX  F OCX,-BACKUP1,BACKUP2,BACKUP3
*   COL-1---->             X F OCX,-BACKUPWK
*   COL-1----> **/01 ******* F OCX,-MONTH01
*   COL-1----> **/01      S  F OCX,-FIRSTSAT SCHEDULE JOB ONLY ON THE
*   COL-1----> **/02      S  F OCX,-FIRSTSAT 1ST SATURDAY OF THE MONTH
*   COL-1----> **/03      S  F OCX,-FIRSTSAT          "
*   COL-1----> **/04      S  F OCX,-FIRSTSAT          "
*   COL-1----> **/05      S  F OCX,-FIRSTSAT          "
*   COL-1----> **/06      S  F OCX,-FIRSTSAT          "
*   COL-1----> **/07      S  F OCX,-FIRSTSAT          "
* MEMBER=@0755
*   COL-1---->       MTWTF   START VTAM
* MEMBER=@0800
*   COL-1---->       MTWTF   START TSO
*   COL-1---->       MTWTF   $HQ,P
*   COL-1---->       MTWTF   $AQ,T
* MEMBER=@0900
*   COL-1---->       -       SEND 'TODAY IS BLUE MONDAY...KEEP SMILING*
*   COL-1---->               '
*   COL-1---->        -      SEND 'TODAY IS TUESDAY...FOUR MORE TO GO'
*   COL-1---->         -     SEND 'TODAY IS WEDNESDAY...YOUR HALF WAY'
*   COL-1---->          -    SEND 'TODAY IS THURSDAY...HANG IN THERE'
*   COL-1---->           -   SEND 'THANK GOD IT''S FRIDAY'
*   COL-1---->            -  SEND 'IT''S SATURDAY... DIDN''T YOU FINIS*
*   COL-1---->               H?'
*   COL-1---->             - SEND 'WOW... EVEN SUNDAY YOU WORK!!!!!!'
* MEMBER=@1645
*   COL-1---->       MTWTF   SE 'TSO WILL BE GOING DOWN AT FIVE SHARP'
* MEMBER=@1655
*   COL-1---->       MTWTF   SE 'TSO WILL BE GOING DOWN IN FIVE MINUTE*
*   COL-1---->               S'
* MEMBER=@1659
*   COL-1---->       MTWTF   SE '**** TSO IS GOING DOWN IN ONE MINUTE X
*   COL-1---->               ****'
*   COL-1---->       MTWTF   SE '****   YOU WILL RECEIVE NO WARNING   X
*   COL-1---->               ****'
* MEMBER=@1700
*   COL-1---->       MTWTF   P TSO
*   COL-1---->       MTWTF   $HQ,T
*   COL-1---->       MTWTF   $AQ,P
*   COL-1----> 05/17 ******* F OCX,TESTJOB SCHEDULE ONLY ON MAY 17
*   COL-1----> 05/16 *****   F OCX,TESTWKDY SCHEDULE ONLY ON MAY 16 WD
*   COL-1----> 05/16      ** F OCX,TESTWKND SCHEDULE ONLY ON MAY 16 WE
* MEMBER=@1702
*   COL-1---->       MTWTF   Z NET,QUICK
*
***********************************************************************
         EJECT
***********************************************************************
*
*        EXAMPLES OF OPERATOR 'DEMAND' COMMANDS:
*
*   COL-1---->*      MTWTFSS COMMAND--------------------------->72
*
* MEMBER=IPL                            (  S OCX,PARM=IPL  )
*   COL-1---->       XXXXXXX S RMF.RMF
*   COL-1---->       XXXXXXX %WTO  ENTER TYPE OF NETWRK STARTUP DESIRED
*   COL-1---->       XXXXXXX %ASK  FULL      TECH      NONE
*   COL-1---->       XXXXXXX %DOM
*   COL-1---->       XXXXXXX %RSP  FULL      DO
*   COL-1---->       XXXXXXX F OCX,STVTAM
*   COL-1---->       XXXXXXX %WAT  15
*   COL-1---->       XXXXXXX F OCX,STCICS,STTSO
*   COL-1---->       XXXXXXX %END
*   COL-1---->       XXXXXXX %RSP  TECH      DO
*   COL-1---->       XXXXXXX F OCX,STVTAM
*   COL-1---->       XXXXXXX %WAT  15
*   COL-1---->       XXXXXXX %WTO  NO APPLICATIONS STARTED... ONLY VTAM
*   COL-1---->       XXXXXXX %END
*   COL-1---->       XXXXXXX %RSP  NONE      DO
*   COL-1---->       XXXXXXX %WTO  NETWORK NOT STARTED
*   COL-1---->       XXXXXXX %END
*   COL-1---->       XXXXXXX S OPSWTR.OPSWTR
*   COL-1---->       XXXXXXX %IF TIME 0800-1859
*   COL-1---->       XXXXXXX F OCX,DINITS    .... SET DAY TIME INITS
*   COL-1---->       XXXXXXX %END
*   COL-1---->       XXXXXXX %IF TIME 1900-0759
*   COL-1---->       XXXXXXX F OCX,NINITS    .... SET NITE TIME INITS
*   COL-1---->       XXXXXXX %END
*   COL-1---->       XXXXXXX F OCX,VARYCONS,VARYALL,SETJES
* MEMBER=SETJES                         (  F OCX,SETJES )
*   COL-1---->       XXXXXXX $TPRT1,Q=A
*   COL-1---->       XXXXXXX $TI9,Z
*   COL-1---->       XXXXXXX $SI9
*   COL-1---->       XXXXXXX $SPRT1
* MEMBER=SHUTDOWN                       (  F OCX,SHUTDOWN )
*   COL-1---->       XXXXXXX $P
*   COL-1---->       XXXXXXX $IPRT1
*   COL-1---->       XXXXXXX $PLNE1
*   COL-1---->       XXXXXXX $ELNE1
*   COL-1---->       XXXXXXX P OPSWTR
*   COL-1---->       XXXXXXX P RMF
*   COL-1---->       XXXXXXX P A
* MEMBER=UNLOAD                         (  F OCX,UNLOAD )
*   COL-1---->       XXXXXXX U 180
*   COL-1---->       XXXXXXX U 181
*   COL-1---->       XXXXXXX U 182
* MEMBER=VARYALL                        (  F OCX,VARYALL )
*   COL-1---->       XXXXXXX V 180-188,ONLINE
*   COL-1---->       XXXXXXX V 150-158,OFFLINE
*   COL-1---->       XXXXXXX V 160-16F,ONLINE
* MEMBER=VARYCONS                       (  F OCX,VARYCONS )
*   COL-1---->       XXXXXXX V 120,CONSOLE,ROUT=(1,2,8,9,10,12,13,14,1*
*   COL-1---->               5)
*   COL-1---->       XXXXXXX V 121,CONSOLE,ROUT=(3,4,5,6)
*   COL-1---->       XXXXXXX V 122,CONSOLE,ROUT=(7)
*   COL-1---->       XXXXXXX V O-123,CONSOLE,ROUT=ALL
* MEMBER=VARYOFF                        (  F OCX,VARYOFF )
*   COL-1----> 09/**      XX V 150-158,OFFLINE  ONLY FOR SEPT WKEND
*   COL-1---->       XXXXXXX V 160-168,OFFLINE
*   COL-1---->       XXXXXXX V 170-178,OFFLINE
*
*  NOTE THAT THE COMMAND-LIST MEMBER MAY INCLUDE COMMANDS FOR THIS
*  ROUTINE AS WELL.  THIS MAY OR MAY NOT BE DESIRABLE SINCE IF NOT
*  CAUTIOUS, A LOOP COULD RESULT.
*
***********************************************************************
         EJECT
          SPACE 1
*
*-------------------------------------------------------------------*
*                                                                   *
*                      D I S C L A I M E R                          *
*                                                                   *
*  BELL AND HOWELL COMPANY MAKES NO WARRANTY EXPRESSED OR IMPLIED   *
*  AS TO THE FITNESS OF THIS CODE UNDER YOUR ENVIRONMENT.  THIS     *
*  EXIT SHOULD BE COMPREHENSIVELY TESTED BEFORE IT IS PLACED INTO   *
*  YOUR PRODUCTION ENVIRONMENT.  IF YOU HAVE ANY QUESTIONS CONTACT: *
*                                                                   *
*           ROBERT M. SIRKIS                                        *
*           TECHNICAL SUPPORT SPECIALIST                            *
*           BELL AND HOWELL COMPANY                                 *
*           2231 WEST HOWARD STREET                                 *
*           EVANSTON, IL 60202                                      *
*           (312) 570-4687                                          *
*                                                                   *
*-------------------------------------------------------------------*
*
          SPACE 1
***********************************************************************
*
*        THIS ROUTINE IS 'NOT' RE-ENTRANT AND MUST BE LINKED AS
*        'AUTHORIZED'.
*
*
*
*        REGISTER USAGE
*
*        R0   - WORK
*        R1   - WORK
*        R2   - WORK
*        R3   - WORK
*        R4   - WORK
*        R5   - WORK
*        R6   - WORK
*        R7   - WORK
*        R8   - UNUSED
*        R9   - QEDIT ORIGIN BASE
*        R10  - DCBS
*        R11  - BASE # 1
*        R12  - BASE # 2
*        R13  - SAVE AREA
*        R14  - BAL
*        R15  - WORK
*
*----------------------------------------------------------------------
         EJECT
*----------------------------------------------------------------------
*
*        INITIALIZATION
*
*----------------------------------------------------------------------
OCX      $PROLOG R11,R12          ENTRY LINKAGE
         USING IHADCB,R10         DCB DSECT
* VERIFY THAT PGM WAS STARTED FROM OS CONSOLE
         L     R2,CVTPTR      GET ADDRESS OF CVT
         L     R2,0(R2)       GET ADDR OF TCBWORDS
         L     R2,4(R2)       GET ADDR OF OUR TCB
         ICM   R2,15,TCBJSCB(R2) GET ADDR OF JSCB
         BZ    NOTAUTH        NO JSCB!
         ICM   R2,15,JSCBSSIB(R2) GET ADDR OF SSIB
         BZ    NOTAUTH        NO SSIB!
         CLC   =C'SSIB',SSIBID(R2) IS THIS THE SSIB?
         BNE   NOTAUTH        NOPE!
         CLC   =C'STC',SSIBJBID(R2)   STARTED TASK?
         BNE   NOTAUTH        NOPE!
         B     ENTRY010
NOTAUTH  DS    0H
         WTO   '** OCX ** - INVALID INITIALIZATION ENVIRONMENT'
         ABEND 999
         SPACE 2
ENTRY010 DS    0H
         L     R9,0(R1)           PARM POINTER
         XR    R2,R2              CLEAR R2
         ICM   R2,3,0(R9)         ANY PARM ?
         BZ    BBB010             NO - NOT STARTED AT IPL
         CH    R2,=H'8'           MAX PARM LENGTH ?
         BNH   AAA010             NO - OK
         B     BBB010
AAA010   EQU   *
         BCTR  R2,R0              DECR FOR EXECUTE
         EX    R2,MVC010          MOVE PARM TO MEMBER
BBB010   EQU   *
         OPEN  (COMMANDS,,DIR,,INTRDR,(OUTPUT))
         TM    COMMANDS+DCBOFLGS-IHADCB,DCBOFOPN  IS IT OPEN ?
         BZ    ERROR1             NO - ERROR
         BAL   R14,ENTRY999       SET UP THE DATE/TIME/DAY FIELDS
         L     R0,BUFSIZE         MAX BLKSIZE
         GETMAIN R,LV=(0)
         ST    R1,BUFFER          STORE FOR USE AS READ BUFFER
EXIT010  EQU   *
         B     ENTRY020
         LTORG
MVC010   MVC   MEMBER(0),2(R9)
         EJECT
*----------------------------------------------------------------------
*
*        INITIALIZE CONSOLE COMMUNICATION INTERFACE
*
*----------------------------------------------------------------------
ENTRY020 DS    0H
         EXTRACT TIOT,'S',FIELDS=(TIOT)
         L     R1,TIOT            GET ADDRESS OF TIOT
         MVC   WTORASK+13(3),0(R1) COPY JOBNAME TO MSG
         MVC   WTOQSTN+5(3),0(R1) COPY JOBNAME TO MSG
         MVC   WTOINVLD+5(3),0(R1) COPY JOBNAME TO MSG
         MVC   WTO900+11(3),0(R1) COPY JOBNAME TO MSG
         EXTRACT COMM,'S',FIELDS=(COMM)
         L     R1,COMM            GET ADDRESS OF COMMUNICATIONS FIELDS
         L     R0,0(R1)           GET COMMUNICATIONS ECB ADDRESS
         ST    R0,COMMECB         SAVE ECB ADDRESS IN WAIT LIST
         LA    R9,4(R1)           GET ORIGIN ADDRESS FOR QEDIT
         ICM   R1,15,0(R9)        IS THERE A START COMMAND CIB WAITING
         BZ    AAA020             NO, MUST BE BATCH EXECUTION
         QEDIT ORIGIN=(R9),BLOCK=(R1)  YES, FREE START COMMAND CIB
AAA020   EQU   *
         QEDIT ORIGIN=(R9),CIBCTR=255  ALLOW STACKED COMMANDS (255)
EXIT020  EQU   *
         B     ENTRY030
COMM     DC    F'0'
TIOT     DC    F'0'
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        IPL COMMAND EXECUTION
*
*----------------------------------------------------------------------
ENTRY030 DS    0H
         CLC   MEMBER,BLANKS      ANY PARM ?
         BE    EXIT030            NO - THIS IS NOT IPL START
         BAL   R14,ENTRY900       PROCESS IPL MEMBER
EXIT030  EQU   *
         B     ENTRY040
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        SET UP TIMER
*
*----------------------------------------------------------------------
ENTRY040 DS    0H
         POINT DIR,=X'00000100'   POINT TO FIRST RECORD IN DIRECTORY
         USING DIRDSECT,R3
         TIME  DEC
         STCM  R0,12,HHMM         STORE TIME HHMM
         UNPK  @TIME(5),HHMM      MOVE TO STIMER TIME FIELD
         MVC   @TIME+4(2),=2C'0'
AAA040   EQU   *
         L     R3,BUFFER          ADDR OF BUFFER
         READ  DIRDECB,SF,DIR,(R3),'S'  READ DIRECTORY
         CHECK DIRDECB            AND OF COURSE WAIT
         XR    R1,R1              CLEAR A WORK REG
         ICM   R1,3,0(R3)         LNGTH OF DATA IN DIR BLK
         LA    R5,0(R1,R3)        END OF DATA IN DIR BLK
         LA    R3,2(R3)           INCR TO FIRST MEMBER NAME
LOOP040A EQU   *
         CLC   DIRNAME,=8X'FF'    END OF MEMBERS ?
         BE    DDD040             YES - CHECK FOR LO MEMBER
         CLI   DIRNAME,C'@'       TOD MEMBER ?
         BNE   CCC040             NO -SKIP IT
         LA    R1,DIRNAME+1       SECOND BYTE OF DIRNAME
         LA    R15,4              LOOP CONTROL - HHMM
LOOP040B EQU   *
         TM    0(R1),X'F0'        NUMERIC ?
         BNO   CCC040             NO - SKIP THIS MEMBER
         LA    R1,1(R1)           BUMP TO NEXT BYTE
         BCT   R15,LOOP040B
         CLC   DIRNAME+6(2),BLANKS  MUST BE BLANKS
         BE    BBB040             YES - OK
         MVC   WTO040+28(8),DIRNAME  NO - INFORM OPER
WTO040   WTO   '** OCX  ** - MEMBER //////// INVALID FOR TOD PROCESSINGX
               , IGNORED'
         B     CCC040
BBB040   EQU   *
         CLC   DIRNAME+1(4),@TIME  MUST BE HIGHER
         BH    GGG040             PROCESS THIS MEMBER
CCC040   EQU   *
         NI    DIRC,X'1F'         TURN OFF UNNEEDED BITS
         XR    R1,R1              CLEAR R1
         IC    R1,DIRC            NUMBER OF USER HALFWORDS
         SLL   R1,1               NUMBER OF USER BYTES
         LA    R3,12(R1,R3)       INCR TO NEXT DIR ENTRY
         CR    R3,R5              PAST END OF DIR BLOCK ?
         BL    LOOP040A           NO - CHECK THIS DIR ENTRY
         B     AAA040             YES - GET NEXT DIR BLOCK
DDD040   EQU   *                  ALSO EODAD FOR DIR READS
         AP    HHMM,=P'1000'      ADD 1 HOUR
         MVI   HHMM+1,X'00'       CLEAR MM
         CP    HHMM,=P'24000'     MIDNITE ?
         BL    EEE040             NO - OK
         MVC   @TIME,=C'23595999' SET MIDNIGHT AS TIMER VALUE
         B     III040             GO TO IT
EEE040   EQU   *
         UNPK  @TIME(5),HHMM      SET DEFAULT TO CHECK AGAIN NEXT HOUR
         B     HHH040
GGG040   EQU   *
         MVC   @TIME(4),DIRNAME+1  YES - NEW TIMER VALUE
HHH040   EQU   *
         MVC   @TIME+4(4),=4C'0'
III040   EQU   *
         XC    TIMEECB,TIMEECB
         STIMER  REAL,ENTRY950,TOD=@TIME
EXIT040  EQU   *
         B     ENTRY050
         DROP  R3
         LTORG
HHMM     DC    PL3'0'
@TIME    DC    D'0'
         EJECT
*----------------------------------------------------------------------
*
*        WAIT FOR MODIFY OR TIMER POP
*
*----------------------------------------------------------------------
ENTRY050 DS    0H
         WAIT  1,ECBLIST=ECBLIST,LONG=YES  WAIT UNTIL AN ECB IS POSTED
         L     R1,COMMECB         ADDR OF COMMUNICATIONS ECB
         CLI   0(R1),X'00'        CHECK FOR OPERATOR REQUEST
         BNE   ENTRY060           YES--GO TO PROCESS OPER REQ
         CLI   TIMEECB,X'00'      DID THE TIMER POP ?
         BNE   ENTRY300           YES--GO PROCESS TIME REQUEST
         B     ENTRY050           HMMM.. DON'T KNOW, RETRY
         LTORG
ECBLIST  DS    0F
COMMECB  DC    A(0)
         DC    X'80',AL3(TIMEECB)
         EJECT
*----------------------------------------------------------------------
*
*        CHECK FOR OPERATOR REQUEST
*
*----------------------------------------------------------------------
ENTRY060 DS    0H
         L     R3,0(R9)           GET ADDRESS OF THE CIB
         USING CIBSECT,R3         ALLOW ADDRESSABILITY
         CLI   CIBVERB,X'40'      IS IT THE STOP COMMAND ?
         BE    RETURN             YES - TO CLEAN UP ROUTINE
         MVC   COMMDATA,BLANKS    CLEAR THE DATA BUFFER
         IC    R15,CIBDSIZE+1     GET SIZE OF MODIFY DATA
         BCTR  R15,R0             DECREMENT FOR EXECUTE
         EX    R15,MOVE060        MOVE TO COMMDATA
         QEDIT ORIGIN=(R9),BLOCK=(R3)  FREE THE CIB
         OC    COMMDATA,BLANKS    UPPER CASE
         CLI   COMMDATA,C'='      IS IT A COMMAND REQUEST ?
         BE    ENTRY200           YES - PROCESS IT
         CLI   COMMDATA,C'-'      IS IT A JOB REQUEST ?
         BE    ENTRY100           YES - PROCESS IT
         B     ENTRA200           ASSUME COMMAND REQUEST
         LTORG
MOVE060  MVC   COMMDATA(0),CIBDATA  MOVE DATA TO WORK AREA
         DROP  R3                 FREE THE USING REGISTER
         EJECT
*----------------------------------------------------------------------
*
*        DETERMINE WHICH DDNAME TO USE FOR SUBMIT
*
*----------------------------------------------------------------------
ENTRY100 DS    0H
         LA    R10,JOBDCB         ADDR OF JOB DCB
         LA    R3,COMMDATA+1      ADDR OF INPUT DATA
LOOP100A EQU   *
         CLI   0(R3),C' '         END OF DATA ?
         BE    AAA100             YES - DEFAULT DDNAME
         CLC   0(3,R3),=C',D='    IS THIS DDNAME OVERRIDE?
         BE    BBB100             NO, CONTINUE UNTIL ONE IS FOUND
         LA    R3,1(R3)           BUMP TO NEXT CHARACTER
         B     LOOP100A           BRANCH BACK
AAA100   EQU   *
         MVC   0(5,R3),=C',D=00'  ASSUME DEFAULT
BBB100   EQU   *
         TM    DCBOFLGS,DCBOFOPN  JOB CNTL DCB OPEN ?
         BZ    CCC100             NO - OPEN IT
         CLC   OPENDCB,3(R3)      SAME AS LAST ONE ?
         BE    EXIT100            YES - OK
         CLOSE ((R10))            NO - CLOSE IT
         XC    OPENDCB,OPENDCB    CLEAR LAST OPENED
CCC100   EQU   *
         MVC   DCBDDNAM+3(2),3(R3)  MODIFY DDNAME
         OPEN  ((R10))
         TM    DCBOFLGS,DCBOFOPN  DID IT OPEN THIS TIME ?
         BZ    ERROR2             NO - ERROR
         MVC   OPENDCB,3(R3)      SAVE LAST OPENED
EXIT100  EQU   *
         MVC   0(5,R3),=CL5' '    CLEAR THE 'D=XX' PARM
         B     ENTRY110
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        SUBMIT EACH MEMBER SPECIFIED IN THE PARMLIST
*
*----------------------------------------------------------------------
ENTRY110 DS    0H
         TM    INTRDR+DCBOFLGS-IHADCB,DCBOFOPN  IS THE INTRDR OPEN ?
         BZ    ENTRY050           NO, IGNORE REQUEST
         LA    R3,COMMDATA        ADDRESS OF FIRST (OR ONLY) OPERAND
AAA110   EQU   *
         LR    R4,R3              COPY TO WORK REG
         SR    R15,R15            CLEAR WORK REGISTER
         LA    R14,9              SET LOOP CONTROL
LOOP110  EQU   *
         CLI   0(R3),C' '         END OF MEMBER NAME?
         BE    BBB110             YES, PROCESS ONLY (LAST) MEMBER
         CLI   0(R3),C','         DELIMITER ?
         BE    BBB110             YES, PROCESS THIS ENTRY
         LA    R3,1(R3)           BUMP TO NEXT CHARACTER
         LA    R15,1(R15)         BUMP CHAR COUNT REG
         BCT   R14,LOOP110        BACK TO CHECK NEXT CHAR
         B     ERROR3             MEMBER NAME LONGER THAN 8 CHARS
BBB110   EQU   *
         BCTR  R15,R0             DECREMENT FOR MOVE
         MVC   MEMBER(8),BLANKS   CLEAR MEMBER NAME AREA
         EX    R15,MOVE110        MOVE MEMBER NAME
         FIND  ((R10)),MEMBER,D
         LTR   R15,R15            MEMBER FOUND ?
         BZ    CCC110             YES - OK
         MVC   WTO110+25(8),MEMBER
WTO110   WTO   '** OCX  ** - JOB //////// NOT FOUND'
         B     EEE110             GO TO EODAD ROUTINE
CCC110   EQU   *
         L     R5,BUFFER          ADDR OF BUFFER
         READ  READDECB,SF,(R10),(R5),'S'
         CHECK READDECB
         LH    R6,DCBLRECL        LRECL
         LH    R7,DCBBLKSI        BLOCK SIZE
         LA    R7,0(R7,R5)        END OF FULL BLOCK
         L     R1,READDECB+16     ADDR OF IOB
         LH    R1,14(R1)          RESIDUAL COUNT FOR SHORT BLOCK
         SR    R7,R1              ADJUSTED BLOCK LENGTH
         SR    R7,R6              DECR TO BEGINNING OF LAST RECORD    *
DDD110   EQU   *
         PUT   INTRDR,(R5)        SUBMIT JCL
         BXLE  R5,R6,DDD110       BUMP TO NEXT REC IN BLOCK AND PUT
         B     CCC110             READ ANOTHER BLOCK
EEE110   EQU   *
         CLI   0(R3),C','         IS THERE ANOTHER MEMBER TO PROCESS ?
         BNE   FFF110             NO, FLUSH THE INTERNAL READER
         LA    R3,1(R3)           BUMP TO START OF NEXT MEMBER
         CLI   0(R3),C' '         NULL MEMBER NAME ?
         BNE   AAA110             NO, PROCESS IT
FFF110   EQU   *
         PUT   INTRDR,EOF         /*EOF
EXIT110  EQU   *                  END OF ALL SUBMITS
         B     ENTRY050           CHECK FOR ANY MORE COMMANDS
MOVE110  MVC   MEMBER(0),0(R4)    EXECUTED MOVE OF MEMBER NAME
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        PROCESS COMMAND EXECUTION REQUEST BY OPERATOR
*
*----------------------------------------------------------------------
ENTRY200 DS    0H
         LA    R3,COMMDATA+1      ADDRESS OF FIRST (OR ONLY) OPERAND
         B     AAA200
ENTRA200 DS    0H
         LA    R3,COMMDATA        ADDRESS OF FIRST (OR ONLY) OPERAND
AAA200   EQU   *
         LR    R4,R3              COPY TO WORK REG
         SR    R15,R15            CLEAR WORK REGISTER
         LA    R14,9              SET LOOP CONTROL
LOOP200  EQU   *
         CLI   0(R3),C' '         END OF MEMBER NAME?
         BE    BBB200             YES, PROCESS ONLY (LAST) MEMBER
         CLI   0(R3),C','         DELIMITER ?
         BE    BBB200             YES, PROCESS THIS ENTRY
         LA    R3,1(R3)           BUMP TO NEXT CHARACTER
         LA    R15,1(R15)         BUMP CHAR COUNT REG
         BCT   R14,LOOP200        BACK TO CHECK NEXT CHAR
         B     ERROR4             MEMBER NAME LONGER THAN 8 CHARS
BBB200   EQU   *
         BCTR  R15,R0             DECREMENT FOR MOVE
         MVC   MEMBER(8),BLANKS   CLEAR MEMBER NAME AREA
         EX    R15,MOVE200        MOVE MEMBER NAME
         BAL   R14,ENTRY900       PROCESS COMMAND
         CLI   0(R3),C','         IS THERE ANOTHER MEMBER TO PROCESS ?
         BNE   EXIT200            NO, DONE
         LA    R3,1(R3)           BUMP TO START OF NEXT MEMBER
         CLI   0(R3),C' '         NULL MEMBER NAME ?
         BNE   AAA200             NO, PROCESS IT
EXIT200  EQU   *
         B     ENTRY050
         LTORG
MOVE200  MVC   MEMBER(0),0(R4)    EXECUTED MOVE OF MEMBER NAME
         EJECT
*----------------------------------------------------------------------
*
*        PROCESS TIME INTERVAL REQUESTS (THE TIMER HAS POPPED)
*
*----------------------------------------------------------------------
ENTRY300 DS    0H
         MVC   MEMBER,BLANKS      CLEAR
         MVI   MEMBER,C'@'        PREFIX
         MVC   MEMBER+1(4),@TIME  BUILD NAME
         FIND  COMMANDS,MEMBER,D
         LTR   R15,R15            MEMBER FOUND ?
         BNZ   EXIT300            NO, MUST BE NULL TIMER
         BAL   R14,ENTRY900
EXIT300  EQU   *
         B     ENTRY040
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        PROCESS COMMANDS
*
*----------------------------------------------------------------------
ENTRY900 DS    0H
         STM   R0,R15,REGSAVE     SAVE ALL THE REGS
         LA    R10,COMMANDS       DCB ADDR
         TM    DCBOFLGS,DCBOFOPN  IS THE COMMANDS OPEN ?
         BZ    EXIT900            NO, IGNORE REQUEST
         BAL   R14,ENTRY999       RE-ESTABLISH DATE, TIME, AND DAY
         FIND  COMMANDS,MEMBER,D
         LTR   R15,R15            MEMBER FOUND ?
         BZ    AAA900             YES - PROCESS IT
         MVC   WTO900+25(8),MEMBER
WTO900   WTO   '** OCX  ** - CMD //////// NOT FOUND'
         B     EXIT900            GO TO EODAD ROUTINE
AAA900   EQU   *
         L     R5,BUFFER          ADDR OF BUFFER
         READ  CMDDECB,SF,(R10),(R5),'S'
         CHECK CMDDECB
         LH    R6,DCBLRECL        LRECL
         LH    R7,DCBBLKSI        BLOCK SIZE
         LA    R7,0(R7,R5)        END OF FULL BLOCK
         L     R1,CMDDECB+16      ADDR OF IOB
         LH    R1,14(R1)          RESIDUAL COUNT FOR SHORT BLOCK
         SR    R7,R1              ADJUSTED BLOCK LENGTH
         SR    R7,R6              DECR TO BEGINNING OF LAST RECORD    *
BBB900   EQU   *
         CLI   CONTFLAG,X'FF'     IS THE CONTINUATION FLAG ON ?
         BNE   EEE900             NO, NORMAL PROCESSING
         CLC   0(15,R5),BLANKS    IS IT A VALID CONT RECORD ?
         BE    DDD900             YES, OK
CCC900   EQU   *
         MVC   WTO900A+25(8),MEMBER MOVE MEMBER NAME
WTO900A  WTO   '** OCX  ** - CMD //////// INVALID CONTINUATION, SKIP'
         MVI   CONTFLAG,X'00'     RESET CONT FLAG
         B     JJJ900             SKIP THE RECORD
DDD900   EQU   *
         MVC   CMDBUF+56(56),15(R5)  MOVE COMMAND DATA TO BUFR
         CLI   71(R5),C' '        IS THIS RECORD CONTINUED ?
         BNE   CCC900             YES - ERROR ONLY ONE CONT ALLOWED
         MVI   CONTFLAG,X'00'     RESET THE CONTINUATION FLAG
         B     III900             PROCESS COMPLETE COMMAND
EEE900   EQU   *
         CLI   0(R5),C'*'         COMMENT ?
         BE    KKK900             YES - SKIP IT
         CLC   =C'%END ',0(R5)    END OF IF CHECK
         BNE   *+12               NO, SKIP AROUND RESET
         MVI   IFFLAG,X'00'       RESET FLAG TO ALLOW ALL CMDS
         B     KKK900             GO GET NEXT RECORD
         CLC   =C'%IF TIME ',0(R5) IS THIS A TIME RANGE CHECK?
         BNE   MMM900             NO
         MVI   IFFLAG,X'00'       RESET FLAG TO ALLOW ALL CMDS
         MVC   WKTIME,9(R5)       COPY START TIME
         PACK  STIME,WKTIME(7)    PACK START TIME
         MVC   WKTIME,14(R5)      COPY END TIME
         PACK  ETIME,WKTIME(7)    PACK END TIME
         TIME  DEC                GET THE NOW TIME
         ST    R0,NOWTIME
         CLC   STIME(2),ETIME     IS BEGINNING TIME LT END TIME
         BH    STH900             YES, CHECK A DIFFERENT WAY
         CLC   NOWTIME(2),ETIME   IS NOWTIME LT END TIME
         BH    SKP900             NO, SET FLAG TO SKIP
         CLC   NOWTIME(2),STIME   IS NOWTIME GT START TIME
         BL    SKP900             NO, SET FLAG TO SKIP
         CLC   =C'STOP',19(R5)    IS IT STOP PROCESSING MEMBER?
         BE    EXIT900            YES
         B     KKK900             NO, GO PROCESS THIS STUFF
STH900   CLC   NOWTIME(2),ETIME   IS NOWTIME LT END TIME
         BL    STP900             YES, THEN PROCESS THIS GUY
         CLC   NOWTIME(2),STIME   IS NOWTIME GT START TIME
         BH    STP900             NO, SET FLAG TO SKIP
SKP900   MVI   IFFLAG,X'FF'       TIME CHECK FAILED, SKIP PROCESSING
         B     JJJ900             GO SKIP THIS RECORD
STP900   CLC   =C'STOP',19(R5)    IS IT STOP PROCESSING MEMBER?
         BE    EXIT900            YES
         B     JJJ900             NO, GO PROCESS THIS STUFF
MMM900   CLI   IFFLAG,X'FF'       SKIPPING COMMANDS?
         BE    JJJ900             YES - SKIP IT
         CLC   1(5,R5),BLANKS     ANY MONTH/DAY PROCESSING?
         BE    HHH900             NO, PROCESS DAY OF WEEK ONLY
         CLI   3(R5),C'/'         VALID MONTH/DAY SPECIFICATION?
         BE    FFF900             YES - OK
         MVC   WTO900B+25(8),MEMBER MOVE MEMBER NAME
WTO900B  WTO   '** OCX  ** - CMD //////// INVALID MONTH/DAY, SKIPPED'
         B     KKK900             SKIP THE RECORD
FFF900   EQU   *
         CLC   1(5,R5),=C'**/**'  ANY MONTH/DAY PROCESSING?
         BE    HHH900             YES, PROCESS DAY OF WEEK ONLY
         CLC   1(5,R5),DATE       IS THIS A HIT?
         BE    HHH900             YES, CHECK FOR CORRECT DAY OF WEEK
         CLC   4(2,R5),=C'**'     WAS ANY DAY SPECIFIED ?
         BNE   GGG900             NO, CHECK FOR ANY MONTH
         CLC   1(2,R5),DATE       YES, IS THIS THE CORRECT MONTH
         BE    HHH900             YES, PROCESS DAY OF WEEK NEXT
         B     KKK900             NO - SKIP IT
GGG900   EQU   *
         CLC   1(2,R5),=C'**'     WAS ANY MONTH SPECIFIED ?
         BNE   KKK900             NO - SKIP IT
         CLC   4(2,R5),DATE+3     YES, IS THIS THE CORRECT DAY
         BNE   KKK900             NO - SKIP IT
HHH900   EQU   *
         SR    R1,R1              CLEAR FOR DAY INDEX
         IC    R1,DAY             GET DAY INDEX
         LA    R1,7(R5,R1)        POINT TO TODAYS PROCESS FLAG
         CLI   0(R1),C' '         PROCESS FLAG ON FOR TODAY ?
         BE    KKK900             NO -SKIP IT
         MVC   CMDBUF(56),15(R5)  MOVE COMMAND DATA TO BUFR
         CLI   71(R5),C' '        IS THIS RECORD CONTINUED ?
         BE    III900             NO - PROCESS COMMAND
         MVI   CONTFLAG,X'FF'     SET THE CONTINUATION FLAG
         B     KKK900             GET NEXT RECORD
III900   EQU   *
         CLC   =C'%END ',15(R5)    IS THIS A END REQUEST?
         BE    END0000            YES, GO PROCESS
         CLI   IFFLAG2,X'FF'      ARE WE SKIPPING COMMANDS?
         BE    JJJ900             YES
         CLI   15(R5),C'%'        IS THIS A CONTROL REQUEST?
         BE    AAAA00             YES, GO DO SPECIAL PROCESSING
         MODESET KEY=ZERO         GET KEY ZERO AND AUTH
         LM    R0,R1,SVCREGS      GET SVC 34 REGS
         SVC   34                 SCHEDULE COMMAND
         MODESET KEY=NZERO        RELEASE AUTHORIZATION
JJJ900   EQU   *
KKK900   EQU   *
         MVC   CMDBUF(115),BLANKS CLEAR THE COMMAND BUFFER
         BXLE  R5,R6,BBB900       BUMP TO NEXT REC IN BLOCK
         B     AAA900             READ ANOTHER BLOCK
EXIT900  EQU   *                  EODAD
         MVI   IFFLAG,X'00'       RESET FLAG TO ALLOW ALL CMDS
         MVI   IFFLAG2,X'00'      RESET FLAG TO ALLOW ALL CMDS
         LM    R0,R15,REGSAVE     RESTORE REGS
         BR    R14
         SPACE 1
AAAA00   EQU   *                  PROCESS % CONTROL REQUESTS
         CLC   =C'%WAT ',15(R5)   IS THIS A WAIT REQUEST?
         BE    WAT0000            YES, GO PROCESS
         CLC   =C'%END ',15(R5)    IS THIS A END REQUEST?
         BE    END0000            YES, GO PROCESS
         CLC   =C'%ASK ',15(R5)    IS THIS AN ASK REQUEST?
         BE    ASK0000            YES, GO PROCESS
         CLC   =C'%RSP ',15(R5)    IS THIS A RSP REQUEST?
         BE    RSP0000            YES, GO PROCESS
         CLC   =C'%WTO ',15(R5)    IS THIS A WTO REQUEST?
         BE    WTO0000            YES, GO PROCESS
         CLC   =C'%DOM ',15(R5)    IS THIS A DOM REQUEST?
         BE    DOM0000            YES, GO PROCESS
         BNE   WTOA00A            NO, ISSUE ERROR MSG
*** PROCESS %RSP  CONTROL CARD
RSP0000  CLC   =C'STOP ',30(R5)   WANT TO STOP ALL PROCESSING?
         BNE   RSP0010            NO, CHECK RESPONSE
         CLC   ASKRPLY,20(R5)     COMPARE RESPONSE
         BE    EXIT900            STOP PROCESSING OF THIS MEMBER
         B     JJJ900             GO READ NEXT RECORD
RSP0010  CLC   ASKRPLY,20(R5)     COMPARE RESPONSE
         BE    JJJ900             MATCH, PROCESS NEXT RECORD
         MVI   IFFLAG2,X'FF'      SET TO SKIP THIS STUFF
         B     JJJ900             GO PROCESS NEXT RECORD
*** PROCESS %ASK CONTROL CARD
ASK0000  MVC   ASKOPTS,20(R5)     COPY ASK RESPONSES TO HOLD AREA
ASK0010  XC    ASKECB,ASKECB      ZERO THE ECB
         MVC   ASKRPLY,=CL8' '    BLANK OUT REPLY AREA
         WTOR  ,ASKRPLY,,ASKECB,MF=(E,WTORASK) ASK OPER
         WAIT  1,ECB=ASKECB,LONG=YES   WAIT FOR RESPONSE
         ICM   R1,15,WTOQMID      GET WTO MSG ID
         BZ    ASK0012
         DOM   MSG=(1)            DELETE IT
ASK0012  OC    ASKRPLY,BLANKS     UPCASE IT
         CLC   =C'? ',ASKRPLY     OPER WANT IT REPEATED?
         BNE   ASK0020            NO
ASK0015  WTO   MF=(E,WTOQSTN)     WTO THE QUESTION
         ST    R1,WTOQMID         SAVE THE WTO ID
         B     ASK0010            ASK AGAIN
ASK0020  LA    R8,5               SET MAX NUMBER OF OPTIONS
         LA    R9,20(R5)          POINT TO FIRST OPTION
ASK0030  CLC   ASKRPLY,0(R9)      IS THIS THE REPLY?
         BE    JJJ900             YES, WE GOT ONE
         LA    R9,10(R9)          POINT TO NEXT OPTION
         BCT   R8,ASK0030         GO CHECK IT
         WTO   MF=(E,WTOINVLD)    GO ISSUE INVALID REPLY MSG
         B     ASK0015            ASK AGAIN
*** PROCESS %DOM CONTROL CARD
DOM0000  L     R1,WTOQMID         GET WTO MSG ID
         DOM   MSG=(1)            DELETE IT
         B     JJJ900             GO GET NEXT RECORD
*** PROCESS %WTO CONTROL CARD
WTO0000  MVC   WTOQSTXT,20(R5)    COPY QUESTION TO WTOR
         WTO   MF=(E,WTOQSTN)     WTO THE QUESTION
         ST    R1,WTOQMID         SAVE THE WTO ID
         B     JJJ900             GO GET NEXT RECORD
*** PROCESS %END CONTROL CARD
END0000  MVI   IFFLAG2,X'00'      SET FLAG TO ALLOW COMMANDS
         B     JJJ900             GO GET NEXT RECORD
*** PROCESS %WAT CONTROL CARD
*        VERIFY THAT PARM IS NUMERIC
WAT0000  CLI   20(R5),F0    IS THIS CHAR NUMERIC?
         BL    WTOA00B       NO
         CLI   20(R5),F9
         BH    WTOA00B       NO
         CLI   21(R5),F0     IS THIS DIGIT MUNERIC?
         BL    WTOA00B       NO
         CLI   21(R5),F9
         BH    WTOA00B       NO
         SPACE 2
**       CONVERT RESPONSE TO TIMER UNITS
         PACK  WTDBLWRD,20(2,R5) PACK WAIT TIME
         CVB   R3,WTDBLWRD      AND GET IN BINARY FORMAT
         SLR   R2,R2
         M     R2,SECONDS    MULTIPLY TO GET REAL TIME
         ST    R3,WAITTIME   SAVE THE INTERVAL
         SPACE 1
STIMER   STIMER WAIT,BINTVL=WAITTIME
         SPACE 1
         B     JJJ900
         SPACE 1
WTOA00B  WTO   '** OCX  ** - INVALID %WAIT TIME INTERVAL'
         B     EXIT900            STOP MEMBER PROCESSING
         SPACE 1
WTOA00A  WTO   '** OCX  ** - INVALID % CONTROL COMMAND ENCOUNTERED'
         B     EXIT900            STOP MEMBER PROCESSING
         LTORG
         SPACE 2
REGSAVE  DC    18F'-1'
CONTFLAG DC    X'00'
IFFLAG   DC    X'00'
IFFLAG2  DC    X'00'
SVCREGS  DC    F'0'               REG 0 FOR SVC 34
         DC    A(SVCCIB)          REG 1 FOR SVC 34
SVCCIB   DC    AL2(118)           MAXIMUM LENGHT OF COMMAND
         DC    H'0'               SVC 34 PADDING
CMDBUF   DC    CL120' '           COMMAND BUFFER
         EJECT
*----------------------------------------------------------------------
*
*        TIMER EXIT
*
*----------------------------------------------------------------------
ENTRY950 DS    0H
         USING *,R15
         STM   R14,R12,4(R13)     SAVE REGS
         L     R12,=A(OCX)
         DROP  R15
         POST  TIMEECB
         LM    R14,R12,4(R13)     RESTORE REGS
EXIT950  EQU   *
         BR    R14
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        DAY/DATE ROUTINE (FROM THE MACHINE CLOCK)
*
*----------------------------------------------------------------------
ENTRY999 DS    0H
         ST    R14,SAVE999        SAVE BAL REG
         $STCK NOGEN              GENERATE THE DATE, TIME AND DAY
EXIT999  EQU   *
         L     R14,SAVE999        RELOAD BAL REG
         BR    R14
         LTORG
SAVE999  DC    F'-1'
DAY      DC    X'99'              RELATIVE DAY OF WEEK (0=MONDAY)
DATE     DC    D'0'               DATE MM/DD/YY
TIME     DC    D'0'               TIME HH:MM:SS
         EJECT
*----------------------------------------------------------------------
*
*        END OF JOB
*
*----------------------------------------------------------------------
RETURN   DS    0H
         TM    DCBOFLGS,DCBOFOPN  IS THE JCL DCB OPEN?
         BZ    AAARET             NO, SKIP THE CLOSE
         CLOSE ((10))             CLOSE THE DCB
AAARET   EQU   *
         TM    COMMANDS+DCBOFLGS-IHADCB,DCBOFOPN  COMMANDS OPEN ?
         BZ    BBBRET             NO, SKIP THE CLOSE
         CLOSE (COMMANDS,,DIR)    CLOSE BOTH DCBS
BBBRET   EQU   *
         TM    INTRDR+DCBOFLGS-IHADCB,DCBOFOPN  HOW ABOUT THE INTRDR ?
         BZ    CCCRET             NO, SKIP THAT CLOSE
         CLOSE (INTRDR)           CLOSE THE INTERNAL READER
CCCRET   EQU   *
         L     R1,BUFFER
         L     R0,BUFSIZE
         FREEMAIN  R,LV=(0),A=(1)
         SR    R15,R15            CLEAR RETCODE
         $EPILOG
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        ERROR ROUTINES
*
*----------------------------------------------------------------------
ERROR1   DS    0H
         WTO   '** OCX  ** - COMMANDS DD CARD MISSING, TERMINATING'
         B     RETURN
*----------------------------------------------------------------------
ERROR2   DS    0H
         MVC   WTO2+23(2),3(R3)
WTO2     WTO   '** OCX  ** - D=// OPEN ERROR, JOB NOT SUBMITTED'
         B     ENTRY050           PROCESS NEXT COMMAND
*----------------------------------------------------------------------
ERROR3   DS    0H
         MVC   WTO3+25(8),0(R4)
WTO3     WTO   '** OCX  ** - JOB //////// AND ALL FOLLOWING NOT PROCESSX
               ED, LENGTH EXCEEDED'
         B     ENTRY050           PROCESS NEXT COMMAND
*----------------------------------------------------------------------
ERROR4   DS    0H
         MVC   WTO4+25(8),0(R4)
WTO4     WTO   '** OCX  ** - CMD //////// AND ALL FOLLOWING NOT PROCESSX
               ED, LENGTH EXCEEDED'
         B     ENTRY050           PROCESS NEXT COMMAND
         EJECT
*----------------------------------------------------------------------
*
*        STORAGE AREAS
*
*----------------------------------------------------------------------
BUFSIZE  DC    F'32760'
TIMEECB  DC    F'0'
BUFFER   DC    F'0'
MEMBER   DC    CL8' '
OPENDCB  DC    CL2' '
HOLDCIB  DC    CL256' '
COMMDATA DC    CL256' '
BLANKS   DC    CL256' '
EOF      DC    CL80'/*EOF'
WKTIME   DC    CL4'0000',C'000'
         DS    0F
NOWTIME  DC    PL4'0'
STIME    DC    PL4'0'
ETIME    DC    PL4'0'
WTDBLWRD  DS    D
WAITTIME DC    A(0)
SECONDS  DC    A(100)   FUDGE FACTOR FOR BINTVL
ASKECB   DC    A(0)
WTOQMID  DC    A(0)
ASKRPLY  DC    CL8' '
         SPACE 1
WTORASK  WTOR  '*OCX : VALID RESPONSES ARE: 123456789012345678901234567X
               8901234567890123456789',,8,ROUTCDE=2,DESC=3,MF=L
ASKOPTS  EQU   WTORASK+12+28,50,C'C'
         SPACE 1
WTOQSTN  WTO   '*OCX: 123456789012345678901234567890123456789012345 ', X
               ROUTCDE=2,DESC=2,MF=L
WTOQSTXT EQU   WTOQSTN+4+6,45,C'C'
WTOINVLD WTO   '*OCX: INVALID RESPONSE ENTERED - PLEASE REENTER',      X
               ROUTCDE=2,MF=L
         SPACE 1
F0       EQU   C'0'
F9       EQU   C'9'
         EJECT
         PRINT NOGEN
COMMANDS DCB   DDNAME=COMMANDS,DSORG=PO,MACRF=R,EODAD=EXIT900
DIR      DCB   DDNAME=COMMANDS,DSORG=PS,MACRF=RP,EODAD=DDD040,         X
               LRECL=256,BLKSIZE=256,RECFM=FB
JOBDCB   DCB   DDNAME=JOB00,DSORG=PO,MACRF=R,EODAD=EEE110
INTRDR   DCB   DDNAME=INTRDR,DSORG=PS,MACRF=PM,                        X
               LRECL=80,BLKSIZE=80,RECFM=F
         SPACE 2
CVTPTR   EQU   16
TCBJSCB  EQU   X'B4'
JSCBSSIB EQU   X'13C'
SSIBID   EQU   0
SSIBJBID EQU   X'0C'
         EJECT
CIBSECT  DSECT ,                  COMMAND INPUT BUFFER MAPPING
CIBCIB   DS    F                  ADDRESS OF NEXT CIB IN CHAIN
CIBVERB  DS    C                  CIB VERB CODE
CIBSIZE  DS    C                  SIZE OF THE COMMAND INPUT BUFFER
CIBRSVD1 DS    H                  RESERVED
CIBRSVD2 DS    C                  RESERVED
CIBTSOID DS    CL3                TSO TERMINAL ID
CIBCONID DS    C                  CONSOLE ID OF STARTED TASK
CIBRSVD3 DS    C                  RESERVED
CIBDSIZE DS    H                  SIZE OF USER DATA WITHIN CIB
CIBDATA  DS    C                  COMMAND DATA AS ENTERED BY OPERATOR
DIRDSECT DSECT
DIRNAME  DS    CL8
DIRTTR   DS    CL3
DIRC     DS    CL1
         DCBD  DSORG=PO
         END   OCX
