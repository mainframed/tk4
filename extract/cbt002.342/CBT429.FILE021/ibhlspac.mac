         TITLE 'I B H L S P A C    DIRECT ACCESS VOLUME SPACE INFORMATIX
               ON'
IBHLSPAC CSECT
         SPACE 1
***********************************************************************
*                                                                     *
*        THIS ROUTINE USES THE SMF LSPACE ROUTINE (SVC 78)            *
*        TO COMPUTE VOLUME SPACE STATISTICS FOR ALL ONLINE            *
*        DA DEVICES.                                                  *
*                                                                     *
*        DDNAMES:  SYSPRINT - TO SYSOUT                               *
*                  VATLST - ALLOCATE TO SYS1.PARMLIB(VATLSTXX)        *
*                YOUR VATLST SHOULD HAVE COMMENTS STARTING COL        *
*                30 WHICH ARE THEN DISPLAYED IN THE REPORT.           *
*                                                                     *
*                           WRITTEN BY G M SCHLESAK / BELL & HOWELL   *
***********************************************************************
         SPACE 1
*
*-------------------------------------------------------------------*
*                                                                   *
*                      D I S C L A I M E R                          *
*                                                                   *
*  BELL AND HOWELL COMPANY MAKES NO WARRANTY EXPRESSED OR IMPLIED   *
*  AS TO THE FITNESS OF THIS CODE UNDER YOUR ENVIRONMENT.  THIS     *
*  EXIT SHOULD BE COMPREHENSIVELY TESTED BEFORE IT IS PLACED INTO   *
*  YOUR PRODUCTION ENVIRONMENT.  IF YOU HAVE ANY QUESTIONS CONTACT: *
*                                                                   *
*           ROBERT M. SIRKIS                                        *
*           TECHNICAL SUPPORT SPECIALIST                            *
*           BELL AND HOWELL COMPANY                                 *
*           2231 WEST HOWARD STREET                                 *
*           EVANSTON, IL 60202                                      *
*           (312) 570-4687                                          *
*                                                                   *
*-------------------------------------------------------------------*
*
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
***  P E R F O R M   L I N K A G E   A N D   O P E N   F I L E S
         SAVE (14,12),,*
         LR    R12,R15
         USING IBHLSPAC,R12
         ST    R13,SAVEAREA+4
         LA    R11,SAVEAREA
         ST    R11,8(R13)
         LR    R13,R11
         SPACE 1
         OPEN  (SYSPRINT,(OUTPUT))
         LTR   R15,R15             DID FILE OPEN?
         BNZ   RETURN              NO
         OPEN  (VATLST,(INPUT))
         LTR   R15,R15             DID FILE OPEN?
         BNZ   GETCVT              NO
         SPACE 2
***  FILL IN COMMENT TABLE FROM VATLST FILE
         SLR   R4,R4               ZERO TABLE ENTRY COUNTER
         LA    R5,CMNTABLE         POINT AT BEGINNING OF COMMENT TABLE
         USING CMNTENTY,R5         ADDRESS TABLE ENTRY SYMBOLICALLY
GETCMNTS GET   VATLST,VATRCRD
         MVC   CMNTVOLS,VATRVOLS   PUT VOLSER INTO TABLE
         MVC   CMNTCMNT,VATRCMNT   PUT COMMENT FILED INTO TABLE
         LA    R4,1(R4)            BUMP TABLE ENTRY COUNTER
         LA    R5,CMNTENTL(R5)     POINT TO NEXT TABLE ENTRY
         B     GETCMNTS            GO GET NEXT RECORD
         DROP  R5
         SPACE 1
EOFCMNTS CLOSE (VATLST)
         S     R5,=A(CMNTENTL)     POINT BACK TO THE LAST ENTRY
         ST    R5,TOTENTP          SAVE LAST ENTRY POINTER
         ST    R4,TOTENTN          SAVE TOTAL NUMBER OF ENTRIES
         SPACE 2
***  F I L L   I N   &   P R I N T   P A G E   H E A D I N G
GETCVT   L     R2,CVTPTR           GET ADDR OF CVT
         L     R3,CVTSMCA(R2)      GET ADDR OF SMCA
         LTR   R3,R3               IS SMF IN SYSTEM?
         BZ    *+10                NO, DO NOT MOVE SYSID
         MVC   HEADING1,SMCASID(R3) MOVE SYSID TO HEADING
         ED    HEADING2,CVTDATE+1(R2) PUT DATE IN HEADING
         TIME  DEC                 GET TIME OF DAY
         ST    R0,DECTIME
         ED    HEADING3,DECTIME    PUT TIME IN HEADING
         SPACE 1
* PRINT PAGE HEADINGS
         PUT   SYSPRINT,HEADINGA
         PUT   SYSPRINT,HEADINGB
         PUT   SYSPRINT,HEADINGC
         PUT   SYSPRINT,OUTLINE
         SPACE 2
***  S E A R C H   U C B S   F O R   D A   D E V I C E S
         L     R2,CVTPTR           GET ADDR OF CVT
         L     R2,CVTILK2(R2)      GET ADDR OF UCB ADDR LIST
CHECKUCB SR    R3,R3
         CH    R3,0(R2)            IS THIS A ZERO ENTRY
         BE    NEXTUCB             YES, GOTO NEXT ENTRY
         LH    R3,0(R2)            GET UCB ADDR
         CH    R3,XFFFF            END OF LIST?
         BE    SPACETOT            YES, GO PRINT TOTAL LINE
         BAL   R11,VOLINFO         PROCESS THIS UCB
         SPACE 1
NEXTUCB  LA    R2,2(R2)            GET NEXT UCB ADDR
         B     CHECKUCB
         SPACE 2
***  G E T   V O L U M E   I N F O R M A T I O N
VOLINFO  EQU   *
         USING UCBDSECT,R3
         CLI   UCBTYP+2,TYPDA      IS IT DA?
         BNER  R11                 NO, GOTO NEXT UCB
         CLI   UCBTYP+3,TYP2321    IS IT 2321 DATA CELL?
         BER   R11                 YES, GOTO NEXT UCB
         TM    UCBSTAT,UCBONLI     IS DEVICE ONLINE
         BNOR  R11                 NO, GOTO NEXT UCB
         LR    R0,R3               SET R0 TO UCB IN QUESTION.
         L     R15,=V(OPTCHK)      GET OPTCHK ADDRESS.
         BALR  R14,R15             CALL OPTCHK ROUTINE.
         LTR   R15,R15             HAS UCB BEEN PROCESSED VIA OPTCHAN?
         BNER  R11                 YES-GOTO NEXT UCB.
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE  CLEAR PRINT LINE
         MVC   OUTLINE1,UCBVOLI    MOVE VOLSER
         MVC   OUTLINE4,UCBNAME    MOVE UNIT ADDRESS
         SPACE 2
***  PUT COMMENT FROM VATLST INTO PRINT LINE
         ICM   R4,15,TOTENTN       GET TOTAL NUMBER OF ENTRIES
         BZ    DOLSPACE            NO TABLE ENTRIES
         L     R5,TOTENTP          POINT TO LAST TABLE ENTRY
         USING CMNTENTY,R5         ADDRESS TABLE ENTRY SYMBOLICALLY
FNDCLUP1 CLC   UCBVOLI,CMNTVOLS    IS THIS THE RIGHT VOLSER
         BE    FNDCLUP2            YES, WE GOT IT
         S     R5,=A(CMNTENTL)     BUMP DOWN TO NEXT ENTRY
         BCT   R4,FNDCLUP1         GO CHECK IT
         B     DOLSPACE            NO MATCHING ENTRY
FNDCLUP2 MVC   OUTLINE8,CMNTCMNT   PUT COMMENT INTO OUTPUT
         DROP  R5
         SPACE 2
***  G O   T O   L S P A C E   R O U T I N E
DOLSPACE LR    R0,R3               SET UP UCB ADDRESS
         LA    R1,PARMAREA         30 BYTE MESSAGE AREA
         SVC   78                  ISSUE LSPACE SVC
         LTR   R15,R15             TEST RETURN CODE
         BNZ   SETERR
         SPACE 1
* PACK VALUES RETURNED BY LSPACE
         PACK  PRMFSCTP,PRMFSCTC   PACK MAX CONTIG TRKS
         PACK  PRMFSCCP,PRMFSCCC   PACK MAX CONTIG CYLS
         PACK  PRMFSEXP,PRMFSEXC   PACK NUMBER OF EXTENTS
         PACK  PRMFSTTP,PRMFSTTC   PACK TOTAL FREE TRKS
         PACK  PRMFSTCP,PRMFSTCC   PACK TOTAL FREE CYLS
         SPACE 1
* PUT VALUES IN PRINT LINE
         MVC   OUTLINE2,PATTERN1
         ED    OUTLINE2,FREESPAC
         CP    PRMFSEXP,=P'50'     MORE THAN 50 EXTENTS
         BNH   *+8
         MVI   OUTLINE2+20,C'*'    HI-LIGHT
         CP    PRMFSTTP,=P'200'    MORE THAN 200 FREE TRACKS
         BNH   *+8
         MVI   OUTLINE2+13,C'*'    HI-LIGHT
         B     COMPDEV             GO COMPUTE DEVICE TYPE
         SPACE 1
* MOVE ERROR MSG TO OUTPUT LINE
SETERR   EQU   *
         SLA   R15,2
         AL    R15,=A(MSGTBL-16)   ADDRESS MSG TABLE
         MVC   OUTLINE3,0(R15)     MOVE MSG INTO LINE
         OI    ERRINDIC,LSPACERR   INDICATE ERROR RETURNED
         SPACE 2
***  C O M P U T E   D E V   T Y P E   A N D   U P D A T E
***  D E V   T Y P E   S P A C E   T O T A L S
COMPDEV  LA    R4,DEVTBL           GET ADDR OF DEVICE TABLE
         USING TBLDSECT,R4
CHKDEV   CLI   TBLDEV,ENDTBL       IS THIS THE LAST ENTRY?
         BE    MVDVTYP             YES, WE MUST USE THIS ENTRY
         CLC   UCBTYP+3(1),TBLDEV  UCB MATCH TABLE ENTRY?
         BNE   NXTNTRY             NO, GO TO NEXT DEVICE ENTRY
MVDVTYP  MVC   OUTLINE5,TBLNAME    MOVE DEV NAME TO OUTPUT LINE
         MVI   TBLUSE,TBLUSED      INDICATE DEVICE HAS BEEN FOUND
         SPACE 1
* CHECK FOR VOL CATAEGORY
CHKVPVT  TM    ERRINDIC,LSPACERR   LSPACE ERROR?
         BO    VOLCAT              YES
         CLC   UCBVOLI(3),VOLVPVT  IS IT A PVT VOLUME?
         BNE   CHKVSTG             NO
         AP    TOTCVPVT,PRMFSTCP   ADD FREE CYLS TOTAL
         AP    TOTTVPVT,PRMFSTTP   ADD FREE TRKS TOTAL
         B     VOLCAT              GO GET VOLUME CATEGORY
         SPACE 1
CHKVSTG  CLC   UCBVOLI(3),VOLVSTG  IS IT A PVT VOLUME?
         BNE   CHKVSCP             NO
         AP    TOTCVSTG,PRMFSTCP   ADD FREE CYLS TOTAL
         AP    TOTTVSTG,PRMFSTTP   ADD FREE TRKS TOTAL
         B     VOLCAT              GO GET VOLUME CATEGORY
         SPACE 1
CHKVSCP  CLC   UCBVOLI(3),VOLVSCP  IS IT A SCP VOLUME?
         BNE   CHKVTSO             NO
         AP    TOTCVSCP,PRMFSTCP   ADD FREE CYLS TOTAL
         AP    TOTTVSCP,PRMFSTTP   ADD FREE TRKS TOTAL
         B     VOLCAT              GO GET VOLUME CATEGORY
         SPACE 1
CHKVTSO  CLC   UCBVOLI(3),VOLVTSO  IS IT A TSO VOLUME?
         BNE   CHKVPUB             NO
         AP    TOTCVTSO,PRMFSTCP   ADD FREE CYLS TOTAL
         AP    TOTTVTSO,PRMFSTTP   ADD FREE TRKS TOTAL
         B     VOLCAT              GO GET VOLUME CATEGORY
         SPACE 1
CHKVPUB  CLC   UCBVOLI(3),VOLVPUB  IS IT A PUB VOLUME?
         BNE   CHKVOTH             NO
         AP    TOTCVPUB,PRMFSTCP   ADD FREE CYLS TOTAL
         AP    TOTTVPUB,PRMFSTTP   ADD FREE TRKS TOTAL
         B     VOLCAT              GO GET VOLUME CATEGORY
         SPACE 1
CHKVOTH  AP    TOTCVOTH,PRMFSTCP   ADD FREE CYLS TOTAL
         AP    TOTTVOTH,PRMFSTTP   ADD FREE TRKS TOTAL
         B     VOLCAT              GO GET VOLUME CATEGORY
         SPACE 1
* CHECK FOR VOL TYPE
VOLCAT   TM    UCBSTAB,UCBBPRV     IS IT A PRV VOLUME?
         BNO   CHKPUB              NO
         MVC   OUTLINE6,VOLTPRV    MOVE VOL TYPE TO OUTPUT LINE
         TM    ERRINDIC,LSPACERR   DO WEE HAVE NUMBERS?
         BO    VOLSTAT             NO
         AP    TBLCPRV,PRMFSTCP    ADD FREE CYLS
         AP    TBLTPRV,PRMFSTTP    ADD FREE TRKS
         AP    TOTCPRV,PRMFSTCP    ADD FREE CYLS TOTAL
         AP    TOTTPRV,PRMFSTTP    ADD FREE TRKS TOTAL
         B     VOLSTAT             GO GET VOLUME STATUS
         SPACE 1
CHKPUB   TM    UCBSTAB,UCBBPUB     IS IT A PUB VOLUME?
         BNO   CHKSTR              NO
         TM    ERRINDIC,LSPACERR   DO WEE HAVE NUMBERS?
         BO    VOLSTAT             NO
         MVC   OUTLINE6,VOLTPUB    MOVE VOL TYPE TO OUTPUT LINE
         AP    TBLCPUB,PRMFSTCP    ADD FREE CYLS
         AP    TBLTPUB,PRMFSTTP    ADD FREE TRKS
         AP    TOTCPUB,PRMFSTCP    ADD FREE CYLS TOTAL
         AP    TOTTPUB,PRMFSTTP    ADD FREE TRKS TOTAL
         B     VOLSTAT             GO GET VOLUME STATUS
         SPACE 1
CHKSTR   TM    UCBSTAB,UCBBSTR     IS IT A STR VOLUME?
         BNO   VOLSTAT             GO GET VOLUME STATUS
         TM    ERRINDIC,LSPACERR   DO WEE HAVE NUMBERS?
         BO    VOLSTAT             NO
         MVC   OUTLINE6,VOLTSTR    MOVE VOL TYPE TO OUTPUT LINE
         AP    TBLCSTR,PRMFSTCP    ADD FREE CYLS
         AP    TBLTSTR,PRMFSTTP    ADD FREE TRKS
         AP    TOTCSTR,PRMFSTCP    ADD FREE CYLS TOTAL
         AP    TOTTSTR,PRMFSTTP    ADD FREE TRKS TOTAL
         B     VOLSTAT             GO GET VOLUME STATUS
         SPACE 1
NXTNTRY  CLI   TBLDEV,ENDTBL       IS THIS END OF TABLE
         BE    VOLSTAT             YES, GO COMPUTE VOL STATUS
         LA    R4,NXTDEV           POINT TO NEXT DEVICE ENTRY
         B     CHKDEV              GO SEE IF MATCH
         DROP  R4
         SPACE 2
***  C O M P U T E   V O L U M E   S T A T U S
VOLSTAT  MVC   OUTLINE7,STATRMOV   DEFAULT STATUS IS REMOVABLE
         TM    UCBSTAT,UCBRESV     IS IT RESERVED?
         BNO   *+10
         MVC   OUTLINE7,STATRESV   MOVE VOL STAT TO OUTPUT LINE
         TM    UCBSTAT,UCBPRES     IS IT PERM RESIDENT?
         BNO   *+10
         MVC   OUTLINE7,STATPRES   MOVE VOL STAT TO OUTPUT LINE
         DROP  R3
         SPACE 2
***  P R I N T   O U T P U T    L I N E
PUTLINE  PUT   SYSPRINT,OUTLINE
         NI    ERRINDIC,X'FF'-LSPACERR
         BR    R11                 GO TO NEXT UCB
         SPACE 2
***  P R I N T   S P A C E   T O T A L S   B Y
***  D E V   T Y P E   &   V O L   T Y P E
SPACETOT EQU   *
         SPACE 1
* PRINT VOL TYPE HEADINGS
         PUT   SYSPRINT,HEADINGH
         PUT   SYSPRINT,HEADINGF
         PUT   SYSPRINT,HEADINGG
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         PUT   SYSPRINT,OUTLINE
         SPACE 1
         MVC   OUTLINEA,VOLVPVT    MOVE VOL TYPE TO OUTPUT LINE
         MVC   OUTLINEB,PATTERN3   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,TOTCVPVT   EDIT FREE SPACE VALUES
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         SPACE 1
         MVC   OUTLINEA,VOLVSCP    MOVE VOL TYPE TO OUTPUT LINE
         MVC   OUTLINEB,PATTERN3   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,TOTCVSCP   EDIT FREE SPACE VALUES
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         SPACE 1
         MVC   OUTLINEA,VOLVTSO    MOVE VOL TYPE TO OUTPUT LINE
         MVC   OUTLINEB,PATTERN3   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,TOTCVTSO   EDIT FREE SPACE VALUES
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         SPACE 1
         MVC   OUTLINEA,VOLVSTG    MOVE VOL TYPE TO OUTPUT LINE
         MVC   OUTLINEB,PATTERN3   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,TOTCVSTG   EDIT FREE SPACE VALUES
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         SPACE 1
         MVC   OUTLINEA,VOLVPUB    MOVE VOL TYPE TO OUTPUT LINE
         MVC   OUTLINEB,PATTERN3   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,TOTCVPUB   EDIT FREE SPACE VALUES
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         SPACE 1
         MVC   OUTLINEA,VOLVOTH    MOVE VOL TYPE TO OUTPUT LINE
         MVC   OUTLINEB,PATTERN3   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,TOTCVOTH   EDIT FREE SPACE VALUES
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         SPACE 1
* PRINT DEV TYPE HEADINGS
         PUT   SYSPRINT,HEADINGH
         PUT   SYSPRINT,HEADINGD
         PUT   SYSPRINT,HEADINGE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
         PUT   SYSPRINT,OUTLINE
         SPACE 1
* PRINT THE TOTALS
         LA    R4,DEVTBL
         USING TBLDSECT,R4
NEWNTRY  CLI   TBLUSE,TBLUSED      WAS THIS ENTRY USED?
         BNE   UPDTPTR             NO, GO TO NEXT DEVICE ENTRY
         MVC   OUTLINEA,TBLNAME    MOVE DEVICE NAME TO OUTPUT LINE
         MVC   OUTLINEB,PATTERN2   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,DEVTOTS    EDIT FREE SPACE VALUES
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE CLEAR PRINT LINE
UPDTPTR  CLI   TBLDEV,ENDTBL       IS THIS THE LAST ENTRY?
         LA    R4,NXTDEV           POINT TO NEXT DEVICE ENTRY
         BNE   NEWNTRY             THERE ARE MORE ENTRIES
         MVC   OUTLINE(L'TOTAL),TOTAL
         MVC   OUTLINEB,PATTERN2   MOVE EDIT PATTERN TO OUTPUT LINE
         ED    OUTLINEB,FREETOTS   EDIT FREE SPACE TOTALS
         PUT   SYSPRINT,OUTLINE
         SPACE 2
         CLOSE (SYSPRINT)
         SPACE 2
***  R E T U R N   T O    S Y S T E M
RETURN   EQU   *
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE 2
***  D A T A   A R E A S   A N D   C O N S T A N T S
CVTPTR   EQU   16                  ADDR OF PTR TO CVT
CVTSMCA  EQU   196                 ADDR OF SMCA
CVTDATE  EQU   56                  DATE
CVTILK2  EQU   40                  ADDR OF UCB ADDR LIST
SMCASID  EQU   16                  SYSTEM ID
ENDTBL   EQU   X'FF'               END OF TABLE INDICATOR
TBLUSED  EQU   X'FF'               ENTRY HAS BEEN USED INDICATOR
ERRINDIC DC    X'00'               ERROR INDICATOR BYTE
LSPACERR EQU   X'80'               ERROR RETURNED FROM LSPACE FOR VOL
         SPACE 1
VOLTSTR  DC    CL4'STRG'
VOLTPRV  DC    CL4'PRIV'
VOLTPUB  DC    CL4'PUB '
VOLVPVT  DC    CL7'PVT*** '
VOLVTSO  DC    CL7'TSO*** '
VOLVSCP  DC    CL7'SCP*** '
VOLVSTG  DC    CL7'STG*** '
VOLVPUB  DC    CL7'PUB*** '
VOLVOTH  DC    CL7'OTHER  '
STATRMOV DC    CL5'REMOV'
STATRESV DC    CL5'RESRV'
STATPRES DC    CL5'RSDNT'
         SPACE 1
DECTIME  DC    F'0'
TOTENTP  DC    A(0)               POINTER TO LAST COMMENT ENTRY
TOTENTN  DC    F'0'               TOTAL NUMBER OF COMMENT ENTRIES
XFFFF    DC    X'FFFF'
TOTAL    DC    C'0TOTALS'
SAVEAREA DS    18F
         SPACE 1
LSPACPRM EQU   *
         DS    10F
         ORG   LSPACPRM+8
PARMAREA EQU   *                   LSPACE INFO RETURN FIELD
         ORG   PARMAREA+6          BEGINNNING OF CHAR FILEDS
PRMFSTCC DS    CL4,CL1             FREE TOTAL CYL (CHAR)
PRMFSTTC DS    CL4,CL1             FREE TOTAL TRK (CHAR)
PRMFSEXC DS    CL4,CL1             FREE EXTENTS   (CHAR)
PRMFSCCC DS    CL4,CL1             LARGEST CONTIG CYL (CHAR)
PRMFSCTC DS    CL4                 LARGEST CONTIG TRK (CHAR)
         ORG   PARMAREA+15         BEGINNING OF PACKED FIELDS
FREESPAC EQU   *
PRMFSTCP DS    CL3                 FREE TOT CYL (PACKED)
PRMFSTTP DS    CL3                 FREE TOT TRK (PACKED)
PRMFSEXP DS    CL3                 FREE EXTENTS   (PACKED)
PRMFSCCP DS    CL3                 LARGEST CONTIG CYL (PACKED)
PRMFSCTP DS    CL3                 LARGEST CONTIG TRK (PACKED)
         ORG
         SPACE 1
VATRCRD  DS    0CL80               VATLST RECORD LAYOUT
VATRVOLS DS    CL6                 VOLSER
         DS    CL23                FILLER
VATRCMNT DS    CL30                COMMENT FIELD
         DS    CL21                FILLER
         SPACE 1
MSGTBL   EQU   *
         DC    CL16'PERM I/O ERROR' RC=4
         DC    CL16'NON-STANDARD VOL' RC=8
         DC    CL16'UCB NOT READY' RC=12
         DC    CL16'LSPACE RC=16'  RC=16
         SPACE 1
DEVTBL   EQU   *
DEV23051 DC    X'06',X'00',CL6'2305-1',6PL4'0'
DEV23052 DC    X'07',X'00',CL6'2305-2',6PL4'0'
DEV2314  DC    X'08',X'00',CL6' 2314 ',6PL4'0'
DEV3330  DC    X'09',X'00',CL6' 3330 ',6PL4'0'
DEV3340  DC    X'0A',X'00',CL6' 3340 ',6PL4'0'
DEV3350  DC    X'0B',X'00',CL6' 3350 ',6PL4'0'
DEV33301 DC    X'0D',X'00',CL6'3330-1',6PL4'0'
DEV3380  DC    X'0E',X'00',CL6' 3380 ',6PL4'0'
DEVUNKWN DC    X'FF',X'00',CL6'UNKNWN',6PL4'0'
         SPACE 1
FREETOTS EQU   *                   TOTAL FREE SPACE COUNTERS
TOTCPRV  DC    PL4'0'              PRV CYL TOTAL
TOTTPRV  DC    PL4'0'              PRV TRK TOTAL
TOTCPUB  DC    PL4'0'              PUB CYL TOTAL
TOTTPUB  DC    PL4'0'              PUB TRK TOTAL
TOTCSTR  DC    PL4'0'              STR CYL TOTAL
TOTTSTR  DC    PL4'0'              STR TRK TOTAL
TOTCVPVT DC    PL4'0'              VOL=PVT CYL TOTAL
TOTTVPVT DC    PL4'0'              VOL=PVT TRK TOTAL
TOTCVSCP DC    PL4'0'              VOL=SCP CYL TOTAL
TOTTVSCP DC    PL4'0'              VOL=SCP TRK TOTAL
TOTCVTSO DC    PL4'0'              VOL=TSO CYL TOTAL
TOTTVTSO DC    PL4'0'              VOL=TSO TRK TOTAL
TOTCVSTG DC    PL4'0'              VOL=STG CYL TOTAL
TOTTVSTG DC    PL4'0'              VOL=STG TRK TOTAL
TOTCVPUB DC    PL4'0'              VOL=PUB CYL TOTAL
TOTTVPUB DC    PL4'0'              VOL=PUB TRK TOTAL
TOTCVOTH DC    PL4'0'              VOL=OTHER CYL TOTAL
TOTTVOTH DC    PL4'0'              VOL=OTHER TRK TOTAL
         SPACE 1
PATTERN1 DC    X'402020202120222020202120224020202021202240404040404040X
               202020212022404040402020202120'
PATTERN2 DC    X'402020202020212022402020202020212022404040202020202021X
               20222020202020212022404040202020202021202240202020202021X
               20'
PATTERN3 DC    X'4020202020202120224020202020202120'
         SPACE 1
HEADINGA DC    CL121' '
         ORG   HEADINGA
         DC    C'1IBHLSPAC  V2L4      D I R E C T    A C C E S S    S PX
                A C E      '
HEADING1 DC    CL4' ',CL3' '
HEADING2 DC    X'4020204B202020',CL3' '
HEADING3 DC    X'4021204B20204B2020'
         ORG
HEADINGB DC    CL121'-VOLSER  FREE  FREE   FREE    LARGEST CONTIG FREE X
               AREA  UNIT    DEVICE    ---STATUS--    ----COMMENTS----'
HEADINGC DC    CL121'         CYLS  TRKS  EXTENTS     CYLINDERS  TRACKSX
                              TYPE'
HEADINGD DC    CL121'-DEVICE     ---PRIVATE---       ---PUBLIC---      X
                ---STORAGE---'
HEADINGE DC    CL121'  TYPE      CYLS     TRKS       CYLS    TRKS      X
                CYLS     TRKS'
HEADINGF DC    CL121'-VOLUME     FREE     FREE '
HEADINGG DC    CL121'  TYPE      CYLS     TRKS '
HEADINGH DC    CL121'0'
         ORG   HEADINGH+1
         DC    91C'-'
         ORG
OUTLINE  DC    CL121' '
         ORG   OUTLINE+1
OUTLINE1 DS    CL6' '              VOL SER
OUTLINE2 DS    CL(L'PATTERN1)' '   FREE SPACE VALUES
         ORG   OUTLINE2+4
OUTLINE3 DS    CL16' '             ERROR MSG
         DS    CL29' '
OUTLINE4 DS    CL3' '              UNIT ADDRESS
         DS    CL5' '
OUTLINE5 DS    CL6' '              DEVICE TYPE
         DS    CL4' '
OUTLINE6 DS    CL4' '              VOL TYPE
         DS    CL2' '
OUTLINE7 DS    CL5' '              VOL STATUS
         DS    CL5' '
OUTLINE8 DS    CL30' '             VOL COMMENTS
         ORG   OUTLINE+1
OUTLINEA DS    CL6' '              DEVICE TYPE
         DS    CL1' '
OUTLINEB DS    CL(L'PATTERN2)' '   FREE SPACE TOTALS
         ORG
         SPACE 2
         PRINT NOGEN
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,LRECL=121,DSORG=PS,            X
               BLKSIZE=4840,RECFM=FBA
VATLST   DCB   DDNAME=VATLST,MACRF=GM,LRECL=80,DSORG=PS,               X
               EODAD=EOFCMNTS
         SPACE 2
         LTORG
         SPACE 2
CMNTABLE DS    200XL36     VATLST VOLSER COMMENT TABLE
         SPACE 2
         PRINT GEN
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*  OPTCHAN - OPTIONAL CHANNEL ROUTINE                                 *
*                                                                     *
*  THIS ROUTINE IS TO DETERMINE IF A UCB HAS ALREADY BEEN PROCESSED   *
*  BY THE PROGRAM.  OPTCHAN WILL MAINTAIN A TABLE OF SERIAL NUMBERS   *
*  AND BASED ON THIS INFORMATION DECIDE IF UCB HAS ALREADY BEEN       *
*  PROCESSED.                                                         *
*                                                                     *
*  ENTRY - REGISTER 0 CONTAINS ADDRESS OF UCB UNDER CONSIDERATION.    *
*                                                                     *
*  EXIT  - REGISTER 15 IS RETURN CODE:                                *
*          0 - UCB HAS NOT BEEN PROCESSED.                            *
*          4 - UCB HAS BEEN PROCESSED.                                *
*                                                                     *
*  ABNORMAL CONDITIONS:                                               *
*  U100 - NUMBER OF UNIQUE UCBS HAS EXCEEDED 100.                     *
*  U200 - REGISTER 0 WAS INVALID.                                     *
*                           ADDED R. PIEPENBRINK / WALGREENS 04/77    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
OPTCHK   CSECT
         SAVE (14,12),,*
         LR    R12,R15        BASE FOR ROUTINE
         USING OPTCHK,R12
         ST    R13,OPTSAVE+4
         LA    R11,OPTSAVE
         ST    R11,8(R13)
         LR    R13,R11             LINKAGE COMPLETE
         SPACE 2
* IS VOLSER IN TABLE?
         LR    R9,R0               R9 IS BASE FOR UCB
         USING UCBDSECT,R9
         CLI   UCBID,X'FF'         IS IT A UCB?
         BNE   OPTU200             NO - ABEND U200.
         LA    R4,OPTABLE          ADDRESS TABLE
         USING OPDSECT,R4
         LA    R6,OPEND-OPDSECT    SET INDEXING VALUE FOR BXH
         LA    R7,OPTLAST          LAST ENTRY IN TABLE
         SPACE
OPLOOP1  EQU   *
         CLI   OPDSECT,X'FF'       END OF ENTRIES USED?
         BE    OPNOTIN             YES - VOLSER NOT USED.
         CLC   OPVOLI,UCBVOLI      HAS IT BENN PROCESSED?
         BE    OPVOLIN             YES - VOLER WAS PROCESSED.
         BXLE  R4,R6,OPLOOP1       FINISH TABLE.
         B     OPTU100
         SPACE 2
* VOLSER WAS PROCESSED.
OPVOLIN  DS    0H
         LA    R15,4               SET RETURN CODE. UCB PROCESSED
         B     OPOUT               GO TO EXIT.
         SPACE 2
* VOLSER WAS NOT IN TABLE.
OPNOTIN  DS    0H
         CR    R4,R7               IS TABLE FULL?
         BL    OPSKIP1             NO - BRANCH AROUND ABEND.
         SPACE
* THE TABLE IS FULL
OPTU100  EQU   *
         ABEND 100,DUMP            TABLE FULL. ABEND U100.
         SPACE 1
*  REGISTER 0 DOES NOT REFERENCE A VALID UCB.
OPTU200  DS    0H
         ABEND 200,DUMP
         SPACE
OPSKIP1  DS    0H
         MVC   OPVOLI,UCBVOLI      MOVE IN VOLSER
         MVI   OPEND,X'FF'         INDICATE LAST ENTRY USED
         SPACE 2
* VOLSER WAS NOT PROCESSED
         SR    R15,R15             SET RETURN CODE. UCB NOT PROCESSED.
         SPACE 2
* EXIT FROM OPTCHK
OPOUT    EQU   *
         L R13,4(R13)              RESTORE R13
         RETURN (14,12),RC=(15)
         BR    R14                 EXIT.
         SPACE 2
         LTORG
         SPACE 2
* THIS IS THE TABLE OF VOLUME SERIAL NUMBERS. THE LAST ENTRY IS
* INDICATED BY THE FIRST BYTE BEING X'FF'  EACH ENTRY IS 6 BYTES.
OPTABLE  DC    X'FF',CL5' '        FIRST ENTRY INDICATED AS LAST.
         DC    98CL6' '            OTHER ENTRIES.
OPTLAST  DC    CL6' '              LAST ENTRY
OPTSAVE  DS    18F                 REGISTER SAVEAREA
         EJECT
UCBDSECT DSECT
         DS    2X
UCBID    DS    X                   UCB ID. SHOULD BE X'FF'
UCBSTAT  DS    XL1                 DEVICE STATUS
UCBONLI  EQU   X'80'               DEVICE IS ONLINE
UCBRESV  EQU   X'20'               VOL IS RESERVED
UCBPRES  EQU   X'04'               VOL IS PERM RESIDENT
         DS    9X
UCBNAME  DS    CL3                 UNIT NAME
UCBTYP   DS    XL4                 DEVICE TYPE
TYPDA    EQU   X'20'               DIRECT ACCESS
TYP2321  EQU   X'05'               2321 DATA CELL
         DS    8X
UCBVOLI  DS    CL6                 DEVICE VOLSER
UCBSTAB  DS    XL1                 VOLUME STATUS
UCBBPRV  EQU   X'10'               PRIVATE VOLUME
UCBBPUB  EQU   X'08'               PUBLIC VOLUME
UCBBSTR  EQU   X'04'               STORAGE VOLUME
         SPACE 1
CMNTENTY DSECT
CMNTVOLS DS    CL6                 VOLSER
CMNTCMNT DS    CL30                COMMENT FIELD
CMNTENTL EQU   *-CMNTENTY
         SPACE 1
TBLDSECT DSECT
TBLDEV   DS    XL1                 DEVICE TYPE ENTRY
TBLUSE   DS    XL1                 USE INDICATOR
TBLNAME  DS    CL6                 DEVICE NAME
DEVTOTS  EQU   *
TBLCPRV  DS    PL4                 PRV CYL COUNTER
TBLTPRV  DS    PL4                 PRV TRK COUNTER
TBLCPUB  DS    PL4                 PUB CYL COUNTER
TBLTPUB  DS    PL4                 PUB TRK COUNTER
TBLCSTR  DS    PL4                 STR CYL COUNTER
TBLTSTR  DS    PL4                 STR TRK COUNTER
NXTDEV   EQU   *
         SPACE 1
OPDSECT  DSECT
OPVOLI   DS    CL6                 VOLUME SERIAL ENTRY
OPEND    DS    0C
         END   IBHLSPAC
