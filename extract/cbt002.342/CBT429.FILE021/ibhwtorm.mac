         MACRO
         DEBUG
         BALR  R8,R0
         ST    R8,DEBUGG
         MEND
IBHWTORM CSECT
         SPACE 1
***********************************************************************
*                                                                     *
***********************************************************************
         SPACE 2
** STANDARD OS LINKAGE CONVENTIONS
         SAVE  (14,12),,*
         LR    R12,R15
         USING IBHWTORM,R12
         ST    R13,SAVEAREA+4
         LA    R11,SAVEAREA
         ST    R11,8(R13)
         LR    R13,R11
*                                                                  *
* METHOD OF OPERATION                                              *
*                                                                  *
* 1) OBTAIN PARAMETER LIST IN THE FORM:                            *
*                                                                  *
*                                                                  *
* R1==> A(PARM1,PARM2)                                             *
* PARM1 ==> BEGINNING REPLY STRING FOR 8 BYTES
* PARM2 ==> JESJOBID 8 BYTES +REPLY NUM 2 BYTES +REPLY TEXT 128 BYTES
*
* 2) OBTAIN INFORMATION ABOUT THE CONSOLE ADDRESS SPACE, SUCH AS   *
*    ASID, ASCB AND CONSOLE ID OF MASTER.
*
* 3) SSAR TO CONSOLE ASID
*
* 4) SAC TO CONSOLE ASID
*
* 5) SEARCH THE ORE AND  WQE CONTROL BLOCKS UNTIL EITHER THE IMS   *
*    COPY, OR MESSAGE ID IS LOCATED...
*
* 6) FORMAT AND ISSUE THE REPLY COMMAND, OR SET RETURN CODE SAYING *
*    THIS COULD NOT BE DONE.
*
*
* 7) RETURN TO THE SAFETY OF THE REPLY MODULE, AFTER RETURNING TO  *
*    KEY OF EIGHT.
*
********************************************************************
          SPACE 1
*
*-------------------------------------------------------------------*
*                                                                   *
*                      D I S C L A I M E R                          *
*                                                                   *
*  BELL AND HOWELL COMPANY MAKES NO WARRANTY EXPRESSED OR IMPLIED   *
*  AS TO THE FITNESS OF THIS CODE UNDER YOUR ENVIRONMENT.  THIS     *
*  EXIT SHOULD BE COMPREHENSIVELY TESTED BEFORE IT IS PLACED INTO   *
*  YOUR PRODUCTION ENVIRONMENT.  IF YOU HAVE ANY QUESTIONS CONTACT: *
*                                                                   *
*           ROBERT M. SIRKIS                                        *
*           TECHNICAL SUPPORT SPECIALIST                            *
*           BELL AND HOWELL COMPANY                                 *
*           2231 WEST HOWARD STREET                                 *
*           EVANSTON, IL 60202                                      *
*           (312) 570-4687                                          *
*                                                                   *
*-------------------------------------------------------------------*
*
          SPACE 1
         L     R2,0(R1)           GET MSGID PTR
         MVC   REPMSGID,0(R2)
         L     R2,4(R1)           GET RETURN BUFFERR ADDR
         ST    R2,PARMADDR        SAVE RETURN BUFFER ADDR
********************************************************************
*                                                                  *
* THE PARAMETERS ARE NOW SAFELY STORED AWAY, THE REST OF THIS CODE *
* MUST RUN IN THE CONSOLE ADDRESS SPACE. WE WILL SSAR AND SAC TO   *
* ACHIEVE THIS.                                                    *
* FIRST, WE MUST DETERMINE THE ASID OF THE COMMTASK ADDRESS SPACE. *
********************************************************************
         DEBUG
         L     R2,16              CVT
         USING CVT,R2
         L     R3,CVTCUCB         UCMBASE
         USING UCM,R3
         LH    R4,UCMCTID         CONSOLE ADDRESS SPACE
         STH   R4,CONSASID        PUT AWAY
         LR    R4,R3
         AH    R4,=H'-4'          GO BACK 4 SPACES
         L     R4,0(R4)           UCM PREFIX
         USING UCMPRFX,R4
         L     R14,UCMMCENT       ADDRESS OF UCM FOR MASTER CONSOLE
         USING UCMLIST,R14
         XR    R15,R15
         IC    R15,UCMID
         STH   R15,MASTCONS
         L     R15,UCMASCB        GET ASCB OF COMMTASK ADDRESS SPACE
         ST    R15,COMMASCB       AND PUT IT AWAY
         DEBUG
         DROP  R2,R14
         MODESET MF=(E,SUPRMOD)
         ESAR  R2                 GET SECONDARY ADDRESS SPACE
         DEBUG
         ST    R2,SASID           STORE IT AWAY FOR LATER
         AXSET AX=ONE             GET AUTHORIZATION ANYWHERE
         LH    R2,CONSASID        LOAD UP ASID OF CONSOLE ADDRESS SPACE
         SSAR  R2
         DEBUG
* GET CONSOLE ADDRESS SPACE LOCAL LOCK FOR REPLY FUNCTION.
         L     R11,COMMASCB       GET ASCB OF COMMTASK
         ST    R13,R13SAVE        SAVE REG 13
         LA    R13,LOCKSAVE       PROVIDE SETLOCK WITH SAVEAREA
GETLOCK  SETLOCK OBTAIN,TYPE=CML,ASCB=(11),REGS=USE,MODE=UNCOND,       X
               RELATED=(CONASCB,RELLOCK)
         DEBUG
         SAC   X'100'             GO INTO SECONDARY ADDRESS SPACE
         DEBUG
*                                 MODE (CONSOLE ASID)
REPLYIT  DS    0H
* WE WILL SEARCH THE CHAIN OF ORE ELEMENTS, LOOKING FOR ONE WITH A
* WQE TO MATCH WHAT WE WANT.
         L     R4,UCMRPYQ         LOAD ADDRESS OF FIRST ORE ENTRY
         LA    R4,0(R4)           ** CLEAR HIGH ORDER BYTE **
         LTR   R4,R4              IF NO OUTSTANDING WTORS
         BZ    ENDLOOP            THEN GET TO END OF LOOP
         LA    R7,20              OTHERWISE, ASSUME AT MOST 20.
ORELOOP  DS    0H
         DEBUG
         USING OREF,R4
         L     R5,ORERWQE         GET ASSOCIATED WQE
         USING WQE,R5             ADDRESS IT
********************************************************************
********************************************************************
* HERE WE KNOW WE ARE LOOKING FOR MESSAGE TEXT
********************************************************************
LOOKMSG  DS    0H
         CLC   REPMSGID,WQETXT+4  IS THIS THE CORRECT MSGID
         BE    GOTMSG             GOT THE MESSAGE
TRYNEXT  DS    0H
         DEBUG
         L     R4,ORELKP          TRY NEXT ORE
         LA    R4,0(R4)           ** CLEAR HIGH ORDER BYTE **
         LTR   R4,R4
         BZ    ENDLOOP
         BCT   R7,ORELOOP
         MVC   EXITRC(2),=H'8'    SET RETURN CODE FROM LOOP
         B     XMEMOFF            AND GET OUT OF CROSS MEM LOOP
ENDLOOP  DS    0H
         DEBUG
         MVC   EXITRC(2),=H'8'
         B     XMEMOFF
GOTMSG   DS    0H
         DEBUG
         MVC   REPJOBID,WQEJOBNM
         MVC   REPLYNUM,OREID
         MVC   REPLYTXT,WQETXT       COPY REPLY TEXT
         DEBUG
         SAC   X'000'                    OUT OF XMEM MODE
         L     R2,SASID                  GET BACK ID OF SAVED ASID
         SSAR  R2
         L     R11,COMMASCB              GET COMTASK ASCB
RELLOCK  SETLOCK RELEASE,TYPE=CML,ASCB=(11),                           X
               RELATED=(CONASCB,GETLOCK)
         DEBUG
         L     R13,R13SAVE
         MVC   EXITRC(2),=H'0'
         L     R2,PARMADDR
         MVC   0(8,R2),REPJOBID
         MVC   8(2,R2),REPLYNUM
         MVC   10(128,R2),REPLYTXT
         B     KEYEIGHT
         DROP  R3,R4
XMEMOFF  DS    0H
         DEBUG
         L     R11,COMMASCB              GET COMTASK ASCB
         SETLOCK RELEASE,TYPE=CML,ASCB=(11),                           X
               RELATED=(CONASCB,GETLOCK)
* HERE, IF EXITRC=0 THEN WE HAVE REPLIED, OTHERWISE, THE MESSAGE
* WAS NOT OUTSTANDING.
         SAC   X'000'                   GO BACK TO HOME ADDRESS SPACE
         L     R2,SASID                 GET ID OF SAVED ASID
         SSAR  R2
         L     R13,R13SAVE
KEYEIGHT DS    0H
         DEBUG
         MODESET MF=(E,PROBMOD)
ENDPROG  DS    0H
RETURN   EQU   *
*        EX    0,*      FOR DEBUGGING
         LH    R15,EXITRC
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
SUPRMOD  MODESET MODE=SUP,KEY=ZERO,MF=L
PROBMOD  MODESET KEY=NZERO,MODE=PROB,MF=L
LOCKSAVE DS    6F            SAVE AREA FOR SETLOCK
ONE      DC    H'1'
R13SAVE  DS    F             SAVE R13 FOR SETLOCK
PARMADDR DS    F
SAVEAREA DS    18F
SASID    DS    F
CONSASID DS    H
EXITRC   DS    H
COMMASCB DS    F
MASTCONS DS    H
REPMSGID DC    CL8' '
         DC    C'DEBUGG'
DEBUGG   DC    F'0'
REPJOBID DC    CL8' '
REPLYNUM DC    CL2' '
REPLYTXT DC    CL128' '
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
         CVT   DSECT=YES
         IHAORE
UCM      DSECT
         IEECUCM
         IHAWQE
         IHAPSA
         END
