         MACRO
&LABEL   ENTER &SA=,        SAVEAREA ADDRESS                           X
               &BASE=12,    BASE REGISTER                              X
               &BASE2=,     SECOND BASE REGISTER                       X
               &BASE3=,     THIRD BASE REGISTER                        X
               &ID=,        MODULE IDENTIFIER                          X
               &TITLE=,     ASSEMBLY LISTING TITLE                     X
               &RENT=NO,    REENTERANT OR NONREENTERANT SAVEAREAS      X
               &CSECT=YES,  CSECT OR NOT                    9/3/81     X
               &WKBYTES=0,  BYTES TO BE GETMAIN'D BESIDES 72 FOR SA    X
               &ID2=,       CONCATENATED TO MODULE ID         10/27/82 X
               &GETFAIL=    BRANCH-TO ADDRESS IF GETMAIN FAILS
.*                                                      RON MURA, TPD
.*               NO PARAMETERS REQUIRED
.*               SA IGNORED IF RENT=YES SPECIFIED
.*               WKBYTES AND GETFAIL IGNORED UNLESS RENT=YES SPECIFIED
         MNOTE *,'*** ENTER - STANDARD OS LINKAGE ***'
         GBLB  &RENTSW1     1 = REENTERANT SAVEAREA
         GBLC  &WKLNTH
&RENTSW1 SETB  0            TURN OFF IN CASE MULT. MACROS PER ASSEMBLY
&WKLNTH  SETC  '&WKBYTES'   KEEP FOR FREEMAIN IN LEAVE MACRO
         LCLC  &CSECTNM,&WID,&NDX,&SAVE,&GETTYPE              10/27/82
&NDX     SETC  '&SYSNDX'
         AIF   ('&TITLE' EQ '').NOTITLE
         TITLE &TITLE
.*                          TITLE MUST BE ENCLOSED BY QUOTATION MARKS
.NOTITLE AIF   ('&LABEL' EQ '').NOLABEL
&CSECTNM SETC  '&LABEL'
         AGO   .CHKID
.NOLABEL AIF   ('&CSECT' NE 'YES').CHKID                    9/3/81
&CSECTNM SETC  '$PRIVATE'
         MNOTE *,'*** NO LABEL ON ENTER MACRO, CSECT CALLED ''$PRIVATE'X
               ' ***'
.CHKID   AIF   (T'&ID EQ 'O').NOID
&WID     SETC  '&ID'                                          10/27/82
         AGO   .CHKID2                                        10/27/82
.NOID    ANOP
&WID     SETC  '&CSECTNM-&SYSDATE-&SYSTIME'                   10/27/82
.CHKID2  AIF   (T'&ID2 EQ 'O').CHKSA                          10/27/82
&WID     SETC  '&WID-&ID2'    CONCATENATE ID2 TO ID STRING    10/27/82
.CHKSA   ANOP
         AIF   ('&CSECT' EQ 'YES').CS                       9/3/81
&LABEL   SAVE  (14,12),,&WID                           9/3/81 10/27/82
         AGO   .LR
.CS      ANOP                                               9/3/81
&CSECTNM CSECT
         SAVE  (14,12),,&WID                                  10/27/82
.LR      ANOP                                               9/3/81
         LR    &BASE,15               ESTABLISH BASE
         USING &CSECTNM,&BASE         INFORM ASSEMBLER
         AIF   (T'&BASE2 EQ 'O').CHKRENT
         LA    &BASE2,2048(&BASE)
         LA    &BASE2,2048(&BASE2)    ESTABLISH SECOND BASE REG
         USING &CSECTNM+4096,&BASE2
         AIF   (T'&BASE3 EQ 'O').CHKRENT
         LA    &BASE3,2048(&BASE2)
         LA    &BASE3,2048(&BASE3)    ESTABLISH THIRD BASE REG
         USING &CSECTNM+8192,&BASE3
.CHKRENT AIF   ('&RENT' EQ 'YES').REENT
         AIF   (T'&SA EQ 'O').NOSA
&SAVE    SETC  '&SA'
         AGO   .SAOK
.NOSA    ANOP
&SAVE    SETC  'SAVE&NDX'
.SAOK    ANOP
         ST    13,&SAVE+4             CHAIN UP TO HSA
         LA    15,&SAVE
         ST    15,8(13)               CHAIN DOWN TO LSA
         LR    13,15                  POINT TO SAVEAREA
         AGO   .CHKB
.REENT   ANOP
&RENTSW1 SETB  1         INDICATE REENTERANT SAVEAREA FOR LEAVE MACRO
         LR    14,1                   SAVE PTR TO PARAM LIST
         LR    15,0                   SAVE REG 0
         AIF   (T'&GETFAIL EQ 'O').UNCOND
         MNOTE '         GETMAIN RC,LV=72+&WKBYTES  REENT. SA FMT USED'
         GETMAIN RC,LV=72+&WKBYTES    GET 72 BYTES FOR SAVEAREA
         LTR   15,15                  TEST FOR SUCCESS
         BNZ   &GETFAIL
         AGO   .CHAIN
.UNCOND  MNOTE '         GETMAIN R,LV=72+&WKBYTES  REENTERANT SAVEAREA X
               FORMAT USED'
         GETMAIN R,LV=72+&WKBYTES     GET 72 BYTES FOR SAVEAREA
.CHAIN   ANOP
         ST    13,4(1)                CHAIN UP TO HSA
         ST    1,8(13)                CHAIN DOWN TO LSA
         LR    0,15                   RESTORE REG 0
         LR    13,1                   POINT TO SAVEAREA
         LR    1,14                   RESTORE REG 1
.*    SEE IF WE NEED TO BRANCH AROUND CONSTANTS:
.CHKB    AIF   (T'&SA NE 'O' OR '&RENT' EQ 'YES').MEND
         B     SAVE&NDX.+72           BRANCH AROUND SAVEAREA
SAVE&NDX DS    18F
.MEND    MEND
