++ USERMOD(ACFW002) /* ADD ACF2 VIOLATION EXIT ROUTINE

       1.  THE VIOEXIT ROUTINE WILL DETERMINE IF A RETRY SHOULD BE
           DONE ON A RULE.  IF DATASET IS A TSO TYPE (BEGINS WITH A $),
           THEN RETRY IF FIRST FOUR CHARACTER RULE WAS NOT CHECKED
           ($DDX INSTEAD OF $DDXAAA).
       2.  ALSO IF A BLP TAPE VIOLATON OCCURS, THEN THE VOLSER IS
           CHECKED TO SEE IF IT IS NUMERIC. IF SO, THE DATASET IS
           CONSIDERED A VALID DSD TAPE AND ACCESS IS DENIED.  IF
           VOLSER IS NOT ALL NUMERICS, ACCESS IS ALLOWED.
                                                                 */ .
++ VER(Z038) FMID(ACF5000) .
++ JCLIN .
//GEN49 JOB WC07N0044,'531-1 CM SINGER'
//SG21  EXEC LINKS,
// PARM='XREF,LET,LIST,RENT',
//    UNIT='3350',SER=IPLXXX,N=SYS1,NAME=LPALIB,P1=' ',
//    MOD=,P2=' ',CLASS=T
//LK.SYSPUNCH DD DUMMY
//INCLUDE DD DSN=MVS3.INCLUDE,DISP=SHR
//SYSLIN DD *
 INCLUDE INCLUDE(VIOEXIT)
 NAME VIOEXIT(R)
/*
++ SRC(VIOEXIT) DISTLIB(ASRCLIB) .
         TITLE     'VIOEXIT -- ACF RULE VIOLATION EXIT'        ACFW002
         SPACE     1                                           ACFW002
         SPACE 3                                               ACFW002
         ACSXP                    MAP EXIT PARAMETER LIST      ACFW002
         ACFREGS                  REGISTER EQUATES             ACFW002
WORK     DSECT                                                 ACFW002
VOLID    DS    CL6                EBCDIC VOLUME ID             ACFW002
WORKLEN  EQU   *-WORK             LENGTH OF WORK AREA          ACFW002
         EJECT                                                 ACFW002
VIOEXIT  CSECT                                                 ACFW002
         SAVE  (14,12),,*         SAVE FOR RETURN              ACFW002
         LR    R11,R15            COPY BASE REGISTER           ACFW002
         USING VIOEXIT,R11        ANNOUNCE ADDRESSIBILITY      ACFW002
         LR    R10,R1             COPY PARAMETER LIST POINTER  ACFW002
         USING ACSXP,R10          ANNOUNCE ADDERSSIBILITY      ACFW002
         CLI   ACUUFLG3,ACUU3PGM  PROGRAM NAME VALIDATION?     ACFW002
         BE    RETURN0            ..YES, ABORT                 ACFW002
         TM    ACUUFLG1,ACUU1PGM  ACCESSED BY RESTRICTED PGM?  ACFW002
         BO    RETURN0            ..YES, ABORT                 ACFW002
         TM    ACUUFLG2,ACUU2BLP  BLP VIOLATION?               ACFW002
         BO    CHKBLP             ..YES, GO CHECK VOLSER       ACFW002
         CLI   ACUURRET,ACUURNAC  RULE FOUND?                  ACFW002
         BE    RETURN0            ..YES, ABORT                 ACFW002
         B     DOLLAR             GO LOOK FOR TSO TYPE DATASET ACFW002
         SPACE 3                                               ACFW002
CHKBLP   DS    0H                                              ACFW002
         GETMAIN RU,LV=WORKLEN    GET SOME STORAGE             ACFW002
         LR    R3,R1              GET POINTER TO WORKAREA      ACFW002
         USING WORK,R3            POINT AT WORK AREA           ACFW002
         SR    R2,R2              ZERO R2 TO USE AS SWITCH     ACFW002
         L     R1,ACUUVOL         POINT AT VOLSER FOR TAPE     ACFW002
         MVC   VOLID,0(R1)        MOVE VOLSER TO WORK AREA     ACFW002
         NC    VOLID,=C'000000'   IS VOLUME ID IN RANGE        ACFW002
         CLC   VOLID,=C'000000'   IS IT 000000 THRU 999999?    ACFW002
         BE    FREESTOR           ..YES, PROCESS AS NORMAL     ACFW002
         LA    R2,4               SET SWITCH TO ALLOW ACCESS   ACFW002
FREESTOR DS    0H                                              ACFW002
         LR    R1,R3              GET POINTER TO WORK AREA     ACFW002
         FREEMAIN RU,LV=WORKLEN,  FREE                         ACFW002 X
               A=(1)              STORAGE                      ACFW002
         LTR   R2,R2              CHECK R2 FOR ZEROS           ACFW002
         BZ    RETURN0            ..YES, DO NORMAL PROCESS     ACFW002
         B     RETURN4            ..NO, ALLOW ACCESS           ACFW002
         DROP  R3                                              ACFW002
         SPACE 3                                               ACFW002
DOLLAR   DS    0H                                              ACFW002
         L     R5,ACUUDSN         POINT AT BEGINNING OF DSN    ACFW002
         L     R6,ACUUPFX         POINT AT RULE PREFIX         ACFW002
         CLI   0(R5),C'$'         IS IT A TSO DATASET?         ACFW002
         BNE   RETURN0            ..NO, ALL DONE               ACFW002
         L     R4,ACUUPFX         SAVE ENTRY TO RULE PREFIX    ACFW002
         LA    R7,8               MAX LGHT 1ST LEVEL QUALIFIER ACFW002
         SPACE 1                                               ACFW002
MLOOP    DS    0H                 LOOP LOOKING FOR BLANK       ACFW002
         CLI   0(R6),C' '         BLANK?                       ACFW002
         BE    ENDPFX             ..YES, ALL DONE              ACFW002
         LA    R6,1(R6)           BUMP POINTER TO PREFIX       ACFW002
         BCT   R7,MLOOP           KEEP LOOKING, MAX OF 8 TIMES ACFW002
         SPACE 1                                               ACFW002
ENDPFX   DS    0H                 TSO TYPE DATASET             ACFW002
         CH    R7,=H'04'          MAX LGTH 1ST LEVEL QUALIFIER ACFW002
         BL    NEWPFX             GO TRY $DDX PREFIX           ACFW002
         B     RETURN0            ALREADY TRIED $DDX (ABORT)   ACFW002
         SPACE 1                                               ACFW002
NEWPFX   DS    0H                 TSO TYPE DATASET             ACFW002
         MVC   0(8,R4),=CL8' '    BLANK OUT PREFIX (ACUUPFX)   ACFW002
         MVC   0(4,R4),0(R5)      MOVE 4 CHAR PREFIX           ACFW002
         B     RETURN24           TRY $DDX PREFIX (RETRY)      ACFW002
         SPACE 1                                               ACFW002
RETURN0  DS    0H                 FINI                         ACFW002
         RETURN (14,12),RC=0      GIVE RETURN CODE 0 (ABORT)   ACFW002
RETURN4  DS    0H                 FINI                         ACFW002
         RETURN (14,12),RC=4      GIVE RETURN CODE 4 (ALLOW)   ACFW002
RETURN24 DS    0H                 FINI                         ACFW002
         RETURN (14,12),RC=24     GIVE RETURN CODE 24 (RETRY)  ACFW002
         END   VIOEXIT                                         ACFW002
