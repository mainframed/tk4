++ USERMOD(ADPW008) /* ADD DFDS DASD EXIT ROUTINE

   A. FOR ALL ALLOCATIONS AND EXTENDS THIS EXIT WILL FORCE
      THE DS1REFD FIELD TO BE UPDATED WITH THE CURRENT DATE.

   B. WILL NOT ALLOW VSAM OR ISAM DATASETS ON ANY PACK FOR TSO TYPE
      DATASETS (THOSE BEGINNING WITH A DOLLOR SIGN).

   C. THIS PROGRAM WILL PERFORM THE FOLLOWING FUNCTIONS ON
      THE TSO8XX PACKS:
      1.  WILL LIMIT DA TYPE DATASETS TO 1 EXTENT.
      2.  WILL FORCE MAX ALLOCATION PER DATASET OF:
          A. 10 CYLINDERS.
          B. 150 TRACKS.
      3.  WILL FORCE ALLOCATION OF 5,5 CYLINDERS TO 5,5 TRACKS
          (5,5 CYLINDERS IS NORMAL ALLOCATION FOR SYSDA, THIS SHOULD
          NOT BE USED FOR TSO VOLUMES).
      4.  IF ALLOCATION IS GREATER THAN ONE OF THE ABOVE, THEN
          THE ALLOCATION IS CHANGED TO THE MAXIMUM ALLOWED (I.E.
          REQUESTED AMOUNT OF 30 CYL, ACTUAL AMOUNT GIVEN IS 10 CYL).
      5.  IF ALLOCATION HAS SECONDARY EXTENTS, THE RELEASE BIT
          IS TURNED ON.

   D. THIS PROGRAM WILL PERFORM THE FOLLOWING FUNCTIONS ON
      THE WRKXXX PACKS:
      1.  IF ALLOCATION HAS SECONDARY EXTENTS, THE RELEASE BIT
          IS TURNED ON.
      2.  WILL SETUP TWO WORK POOLS (I.E. ONE SET OF VOLUMES FOR
          PRODUCTION AND ONE SET FOR EVERYBODY ELSE) .

   E. THIS PROGRAM WILL PERFORM THE FOLLOWING FUNCTIONS ON
      THE MKWXXX PACKS:
      1.  WILL FORCE MAX ALLOCATION PER DATASET OF:
          A. 50 CYLINDERS.
          B. 750 TRACKS.
      2.  IF ALLOCATION IS GREATER THAN ONE OF THE ABOVE, THEN
          THE ALLOCATION IS CHANGED TO THE MAXIMUM ALLOWED (I.E.
          REQUESTED AMOUNT OF 90 CYL, ACTUAL AMOUNT GIVEN IS 50 CYL).
      3.  WILL FORCE ALLOCATION OF 5,5 CYLINDERS TO 5,5 TRACKS.
      4.  IF ALLOCATION HAS SECONDARY EXTENTS, THE RELEASE BIT
          IS TURNED ON.

   F. THIS PROGRAM WILL PERFORM THE FOLLOWING FUNCTIONS ON
      THE MPRXXX PACKS:
      1.  WILL ALLOW ONLY DATASETS THAT BEGIN WITH DCWX (EXCEPT FOR
          DCWT), $WC4, AND $WC3 TO BE ALLOCATED.
                                                                 */ .
++ VER(Z038) FMID(HDP2230) .
++ JCLIN .
//GEN49 JOB WC07N0323,'531-0 CM SINGER'
//SG21  EXEC LINKS,
// PARM='XREF,LET,LIST,RENT',
//    UNIT='3380',SER=IPLXXX,N=SYS1,NAME=LPALIB,P1=' ',
//    MOD=,P2=' ',CLASS=T
//LK.SYSPUNCH DD DUMMY
//INCLUDE DD DSN=MVS3.INCLUDE,DISP=SHR
//SYSLIN DD *
 INCLUDE INCLUDE(IGGPRE00)
 NAME IGGPRE00(R)
/*
++ SRC(IGGPRE00) DISTLIB(ASRCLIB) .
IGGPRE00 TITLE '*** DFDS PREPROCESSING EXIT *** IGGPRE00'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  ROUTINE NAME - IGGPRE00                                            *
*          DATE - 10 MAY 1983                                         *
*          BRUCE R. MINTON                                            *
*          GENERAL DYNAMICS - DATA SYSTEMS DIVISION                   *
*          P.O. BOX 85808                                             *
*          SAN DIEGO, CALIFORNIA 92138                                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 4
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
IGGPRE00 CSECT
         SAVE  (14,12),,*          SAVE FOR RETURN
         LR    R10,R15             COPY BASE REGISTER
         USING IGGPRE00,R10        SET ADDRESABILITY
         LR    R2,R1               GET POINTER TO PARAMETER LIST
         USING IGGIEPL,R2          POINT AT PARAMETER LIST
*
         TM    IEXFLAG,IEXVIO      IS IT VIO DATASET?
         BO    RETURN0             ..YES, DONT DO ANYTHING
         CLI   IEXFUNC,IEXALL      IS THIS ALLOCATE
         BE    GETDATE             ..YES - DATE STAMP
         CLI   IEXFUNC,IEXEXT      IS THIS EXTEND
         BNE   RETURN0             ..NO, DONT DO ANYTHING
*
GETDATE  L     R3,16               GET POINTER TO CVT
         LM    R4,R5,IEXFMT1       SAVE DATA FOR 8 BYTE WORK AREA
         XC    IEXFMT1(8),IEXFMT1 CLEAR WORK AREA
         XC    IEXRSVWD(4),IEXRSVWD CLEAR RESERVED WORD
         SR    R6,R6               CLEAR WORK REG
         IC    R6,X'39'(R3)        PICK UP YEAR FROM CVTDATE
         SLL   R6,4                SHIFT OVER 4 BITS
         O     R6,=X'0000000F'     MAKE PACKED DECIMAL
         ST    R6,IEXFMT1+4        SAVE DOWN
         CVB   R6,IEXFMT1          MAKE BINARY
         SR    R7,R7               CLEAR WORK REG
         MVC   IEXFMT1+6(2),X'3A'(R3) MOVE IN DAY NUMBER FROM CVTDATE
         CVB   R7,IEXFMT1          MAKE BINARY
         STM   R4,R5,IEXFMT1       RESTORE DATA FROM 8 BYTE WORK AREA
*
         TM    IEXFLAG,IEXMF1      WAS PARTIAL DSCB SUPPLIED
         BZ    GETDSCB             ..NO, MUST BUILD ONE
         L     R1,IEXFMT1          GET POINTER TO PARTIAL FMT1 DSCB
         STC   R6,X'1F'(R1)        SAVE DOWN YEAR
         STCM  R7,3,X'20'(R1)      SAVE DOWN DAY
         B     RETURN0             AND RETURN (NO JFCB AVAILABLE)
*
GETDSCB  LA    R5,96               LENGTH OF MODEL DSCB
       GETMAIN R,LV=(R5)           GETMAIN MODEL
         ST    R1,IEXFMT1          SAVE FOR ALLOCATE
         ST    R1,IEXRSVWD         SAVE FOR POST EXIT
         MVI   0(R1),X'00'         ZERO IT
         MVC   1(95,R1),0(R1)      ALL OUT
         STC   R6,X'1F'(R1)        SAVE DOWN YEAR
         STCM  R7,3,X'20'(R1)      SAVE DOWN DAY
*
         L     R3,IEXUCB           GET POINTER TO UCB
         USING IGGUCB,R3           POINT AT UCB
         L     R4,IEXPTR1          GET POINTER TO JFCB
         USING IGGJFCB,R4          POINT AT JFCB
         CLI   IEXFUNC,X'02'       IS IT EXTEND FUNCTION?
         BE    CHKEXTA             ..YES, GO CHECK EXTENTS
         CLI   IEXFUNC,X'01'       IS IT ALLOCATE FUNCTION?
         BNE   RETURN0             ..NO, DONT DO ANYTHING
         BAL   R11,CHKWORKA        GO CHECK WORK PACK PARTITION
         BAL   R11,VSAMTSO         GO CHECK TSO TYPE VSAM DATASETS
         CLC   UCBVOLI(3),MSSVOL   IS IT A MSS PACK?
         BE    CHKMSS              ..YES, GO PROCESS IT
         CLC   UCBVOLI(3),MPRXXX   IS IT MPRXXX VOLUME (I.E. PROD)?
         BE    MPRCHK              ..YES, GO CHECK MPRXXX PACKS
         CLC   UCBVOLI(4),TSO8XX   IS IT TSO8XX (3380)?
         BE    DSORG               ..YES, GO CHECK DSORG
         B     RETURN0             ALL DONE
         SPACE 4
CHKWORKA DS    0H
         CLC   UCBVOLI(3),WRKXXX   IS IT A WORK PACK?
         BNER  R11                 ..NO, KEEP CHECKING
         CLC   JFCBDSNM(3),SYS1    IS IT SYSTEM NAME?
         B     CHKWORKB            ..YES, SKIP ALL SEPARATING <--- TEMP
         CLC   JFCBDSNM(4),VS01    IS IT VSAM WORK FILE?
         BE    CHKWORKB            ..YES, SKIP ALL SEPARATING
         CLC   JFCBDSNM(4),VSW1    IS IT VSAM WORK FILE?
         BE    CHKWORKB            ..YES, SKIP ALL SEPARATING
         CLI   UCBVOLI+5,X'F0'     IS LAST BYTE OF VOLUME ZERO?
         BE    PRODONLY            ..YES, ONLY PROD DATASETS
         CLI   UCBVOLI+5,X'F2'     IS LAST BYTE OF VOLUME TWO?
         BE    PRODONLY            ..YES, ONLY PROD DATASETS
         CLI   UCBVOLI+5,X'F4'     IS LAST BYTE OF VOLUME FOUR?
         BE    PRODONLY            ..YES, ONLY PROD DATASETS
         CLI   UCBVOLI+5,X'F6'     IS LAST BYTE OF VOLUME SIX?
         BE    PRODONLY            ..YES, ONLY PROD DATASETS
         CLI   UCBVOLI+5,X'F8'     IS LAST BYTE OF VOLUME EIGHT?
         BE    PRODONLY            ..YES, ONLY PROD DATASETS
         CLC   JFCBDSNM(4),DCWT    IS IT DCWT TEMP DATASET?
         BE    CHKWORKB            ..YES, ALLOW ON THIS VOLUME
         CLC   JFCBDSNM(3),DCWT    IS IT DCWX PROD DATASET?
         BE    RETURN4             ..YES, DO NOT ALLOW ON THIS VOLUME
         CLC   JFCBDSNM(4),PROD    IS IT PROD DATASET?
         BE    RETURN4             ..YES, DO NOT ALLOW ON THIS VOLUME
         B     CHKWORKB            EVERYBODY ELSE IS ALLOWED
PRODONLY DS    0H
         CLC   JFCBDSNM(4),DCWT    IS IT DCWT TEMP DATASET?
         BE    RETURN4             ..YES, DO NOT ALLOW ON THIS VOLUME
         CLC   JFCBDSNM(3),DCWT    IS IT DCWX PROD DATASET?
         BE    CHKWORKB            ..YES, ALLOW AND CONTINUE PROCESSING
         CLC   JFCBDSNM(4),PROD    IS IT PROD DATASET?
         BE    CHKWORKB            ..YES, ALLOW AND CONTINUE PROCESSING
         B     RETURN4             EVERYBODY ELSE NOT ALLOWED
         SPACE 5
CHKWORKB DS    0H
         CLC   JFCBDSNM(4),PROD    IS IT A PRODUCTION DATASET?
         BER   R11                 ..YES, DONT DO ANYTHING
         CLC   JFCBSQTY,ZEROS      IS THERE SECONDARY ALLOCATION?
         BER   R11                 ..NO, DONT DO ANYTHING
         OI    JFCBIND1,JFCRLSE    TURN ON RELEASE FLAG
         BR    R11                 ALL DONE
         EJECT
MPRCHK   DS    0H
         CLC   JFCBDSNM(8),HSM     IS IT AN HSM RECALL DATASET?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(4),SYS1    IS IT SYS1?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(4),DCWT    IS IT DCWT DATASET?
         BE    RETURN4             ..YES, DONT ALLOW ON MPRXXX PACK
         CLC   JFCBDSNM(3),DCWT    IS IT DCW1, DCW2, ETC?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(4),$WC3    IS IT HSS NAME?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(4),$WC4    IS IT PC TYPE DATASET?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         B     RETURN4             ..NO, DONT ALLOW ON MPRXXX PACK
         EJECT
DSORG    DS    0H
         CLC   JFCBDSNM(8),HSM     IS IT AN HSM RECALL DATASET?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(4),SYS1    IS IT SYS1?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(3),SYS1    IS IT SYSTEM NAME?
         BE    CHKDSORG            ..YES, FOLLOW NORMAL RULES
         CLC   JFCBDSNM(1),DOLLAR  IS IT A TSO TYPE DATASET?
         BNE   RETURN8             ..NO, DSN NOT ALLOWED ON TSO PACK
         SPACE 2
CHKDSORG DS    0H
         TM    JFCDSRG1,JFCORGDA   IS DSORG DA?
         BO    DATYPE              ..YES, GO PROCESS IT
         B     SPACE               GO CHECK PRIMARY ALLOCATION
         SPACE 2
DATYPE   DS    0H
         MVC   JFCBSQTY,ZEROS      MAKE SECONDARY QAUNTITY ZERO FOR DA
         SPACE 2
SPACE    DS    0H
         CLC   JFCBSQTY,ZEROS      IS THERE SECONDARY ALLOCATION?
         BE    CHKTYPE             ..NO, DONT TURN ON RELEASE FLAG
         OI    JFCBIND1,JFCRLSE    TURN ON RELEASE FLAG
CHKTYPE  DS    0H
         TM    JFCBCTRI,JFCBCYL    IS ALLOCATION BY CYLINDERS?
         BO    CYLINDER            ..YES, GO DO CHECKING FOR CYLINDERS?
         TM    JFCBCTRI,JFCBTRK    IS ALLOCATION BY TRACKS?
         BO    TRACKS              ..YES, GO DO CHECKING FOR TRACKS?
         TM    JFCBCTRI,JFCBAVR    IS ALLOCATION BY BLOCKS?
         BO    BLOCKS              ..YES, GO DO CHECKING FOR BLOCKS?
         B     RETURN0             DONT CHECK ANYTHING ELSE
         SPACE 2
CYLINDER DS    0H
         CLC   JFCBPQTY,CYL5       IS PRIMARY CYLINDERS EQUAL TO 5?
         BNE   CYLA                ..NO, GO DO NORMAL CHECKING
         CLC   JFCBSQTY,CYL5       IS SECONDARY CYLINDERS EQUAL TO 5?
         BNE   CYLA                ..NO, GO DO NORMAL CHECKING
         XI    JFCBCTRI,JFCBCYL    TURN OFF ALLOCATION BY CYLINDERS
         XI    JFCBCTRI,JFCBTRK    TURN ON ALLOCATION BY TRACKS
         B     RETURN0             ALL DONE
CYLA     DS    0H
         MVC   JFCBSQTY,CYL1       MAKE EXTENT INFORMATION 1 CYL
         CLC   JFCBPQTY,CYLNUM     IS NUMBER CYLINDERS MORE THAN MAX?
         BNH   RETURN0             ..NO, ALLOW ALLOCATION
         MVC   JFCBPQTY,CYLNUM     MORE IN MAX NUMBER OF CYLINDERS
         MVC   JFCBSQTY,ZEROS      MAKE SECONDARY QAUNTITY ZERO
         B     RETURN0             ALL DONE
         SPACE 2
TRACKS   DS    0H
         MVC   JFCBSQTY,TRK15      FORCE EXTENT ALLOCATION TO 15TRKS
         CLC   JFCBPQTY,TRK3380    IS NUMBER OF TRACKS MORE THAN MAX?
         BNH   RETURN0             ..NO, ALLOW ALLOCATION
         MVC   JFCBPQTY,TRK3380    MORE IN MAX NUMBER OF TRACKS
         MVC   JFCBSQTY,ZEROS      MAKE SECONDARY QAUNTITY ZERO
         B     RETURN0             ALL DONE
         SPACE 2
BLOCKS   DS    0H
         L     R5,BYTES            GET MAX NUMBER OF BYTES
         SR    R6,R6               CLEAR R6
         ICM   R6,7,JFCBDRLH       GET BLOCK SIZE
         LR    R8,R6               SAVE BLOCK SIZE FOR LATER
         SR    R7,R7               CLEAR R7
         ICM   R7,7,JFCBPQTY       GET NUMBER OF BLOCKS
         MR    R6,R6               GET TOTAL NUMBER OF BYTES REQUESTED
         CR    R7,R5               IS AMOUNT TOO BIG?
         BL    RETURN0             ..NO, ALLOW REQUEST
         LR    R6,R5               GET MAX NUMBER OF BYTES
         SRDA  R6,32               SHIFT IT FOR DIVIDE
         DR    R6,R8               DIVIDE MAX BYTES BY BLOCK SIZE
         STCM  R7,7,JFCBPQTY       MORE IN MAX NUMBER OF BLOCKS
         MVC   JFCBSQTY,ZEROS      MAKE SECONDARY QAUNTITY ZERO
         B     RETURN0             ALL DONE
         EJECT
VSAMTSO  DS    0H
         CLC   JFCBDSNM(1),DOLLAR  IS IT A TSO TYPE DATASET?
         BNER  R11                 ..NO, RETURN TO NORMAL PROCESSING
         TM    JFCDSRG2,JFCORGAM   IS DSORG VSAM?
         BO    RETURN8             ..YES, DO NOT ALLOW VSAM
         TM    JFCDSRG1,JFCORGIS   IS DSORG ISAM?
         BO    RETURN8             ..YES, DO NOT ALLOW ISAM
         BR    R11                 GO DO NORMAL PROCESSING
         EJECT
CHKMSS   DS    0H
         CLC   JFCBDSNM(8),HSM     IS IT AN HSM RECALL DATASET?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(4),SYS1    IS IT SYS1?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBSQTY,ZEROS      IS THERE SECONDARY ALLOCATION?
         BE    CHKMSSA             ..NO, DONT TURN ON RELEASE FLAG
         OI    JFCBIND1,JFCRLSE    TURN ON RELEASE FLAG
         SPACE 2
CHKMSSA  DS    0H
         TM    JFCBCTRI,JFCBCYL    IS ALLOCATION BY CYLINDERS?
         BO    MSSCYLA             ..YES, GO DO CHECKING FOR CYLINDERS?
         TM    JFCBCTRI,JFCBTRK    IS ALLOCATION BY TRACKS?
         BO    MSSTRKS             ..YES, GO DO CHECKING FOR TRACKS?
         TM    JFCBCTRI,JFCBAVR    IS ALLOCATION BY BLOCKS?
         BO    MSSBLKS             ..YES, GO DO CHECKING FOR BLOCKS?
         B     RETURN0             DONT CHECK ANYTHING ELSE
         SPACE 2
MSSCYLA  DS    0H
         CLC   JFCBPQTY,CYL5       IS PRIMARY CYLINDERS EQUAL TO 5?
         BNE   MSSCYLB             ..NO, GO DO NORMAL CHECKING
         CLC   JFCBSQTY,CYL5       IS SECONDARY CYLINDERS EQUAL TO 5?
         BNE   MSSCYLB             ..NO, GO DO NORMAL CHECKING
         XI    JFCBCTRI,JFCBCYL    TURN OFF ALLOCATION BY CYLINDERS
         XI    JFCBCTRI,JFCBTRK    TURN ON ALLOCATION BY TRACKS
         B     RETURN0             ALL DONE
MSSCYLB  DS    0H
         CLC   JFCBPQTY,MSSCYL     IS NUMBER CYLINDERS MORE THAN MAX?
         BNH   RETURN0             ..NO, ALLOW ALLOCATION
         MVC   JFCBPQTY,MSSCYL     MORE IN MAX NUMBER OF CYLINDERS
         MVC   JFCBSQTY,ZEROS      MAKE SECONDARY QAUNTITY ZERO
         B     RETURN0             ALL DONE
         SPACE 2
MSSTRKS  DS    0H
         CLC   JFCBPQTY,MSSTRK     IS NUMBER OF TRACKS MORE THAN MAX?
         BNH   RETURN0             ..NO, ALLOW ALLOCATION
         MVC   JFCBPQTY,MSSTRK     MORE IN MAX NUMBER OF TRACKS
         MVC   JFCBSQTY,ZEROS      MAKE SECONDARY QAUNTITY ZERO
         B     RETURN0             ALL DONE
         SPACE 2
MSSBLKS  DS    0H
         L     R5,MSSBYTES         GET MAX NUMBER OF BYTES
         SR    R6,R6               CLEAR R6
         ICM   R6,7,JFCBDRLH       GET BLOCK SIZE
         LR    R8,R6               SAVE BLOCK SIZE FOR LATER
         SR    R7,R7               CLEAR R7
         ICM   R7,7,JFCBPQTY       GET NUMBER OF BLOCKS
         MR    R6,R6               GET TOTAL NUMBER OF BYTES REQUESTED
         CR    R7,R5               IS AMOUNT TOO BIG?
         BL    RETURN0             ..NO, ALLOW REQUEST
         LR    R6,R5               GET MAX NUMBER OF BYTES
         SRDA  R6,32               SHIFT IT FOR DIVIDE
         DR    R6,R8               DIVIDE MAX BYTES BY BLOCK SIZE
         STCM  R7,7,JFCBPQTY       MORE IN MAX NUMBER OF BLOCKS
         MVC   JFCBSQTY,ZEROS      MAKE SECONDARY QAUNTITY ZERO
         B     RETURN0             ALL DONE
         EJECT
CHKEXTA  DS    0H
         CLC   JFCBDSNM(8),HSM     IS IT AN HSM RECALL DATASET?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         CLC   JFCBDSNM(4),SYS1    IS IT SYS1?
         BE    RETURN0             ..YES, DONT DO ANYTHING
         TM    JFCDSRG1,JFCORGIS   IS DSORG ISAM?
         BO    RETURN0             ..YES, DONT DO ANYTHING
         SR    R6,R6               CLEAR R6
         SR    R7,R7               CLEAR R7
         CLC   UCBVOLI(3),MSSVOL   IS IT A MSS PACK?
         BE    CHKEXTB             ..YES, GO PROCESS IT
         CLC   UCBVOLI(4),TSO8XX   IS IT A TSO PACK?
         BNE   RETURN0             ..NO, ALL DONE
         ICM   R7,7,TRK3380        GET MAX ALLOCATION FOR TSO
         B     CHKEXTC             GO PROCESS DEB INFO
         SPACE 2
CHKEXTB  DS    0H
         ICM   R7,7,MSSTRK         GET MAX ALLOCATION FOR MSS
         SPACE 2
CHKEXTC  DS    0H
         L     R9,IEXPTR2          GET POINTER TO DEB
         USING DEB,R9              SET ADDRESABILITY
         SR    R5,R5               CLEAR R5
         ICM   R5,1,16(R9)         GET NUMBER OF EXTENTS
         LA    R9,32(R9)           BUMP PAST BASIC SECTION TO DASD
         DROP  R9                  DROP ADDRESSABILITY
         USING DEBDASD,R9          SET ADDRESABILITY
         SPACE 2
CHKEXTD  DS    0H
         AH    R6,DEBNMTRK         ADD NUMBER OF TRACKS FOR THIS EXTENT
         CR    R6,R7               IS ALLOCATION OVER THE MAX?
         BH    RETURN8             ..YES, DO NOT ALLOW ANY MORE EXTENTS
         LA    R9,16(R9)           BUMP TO NEXT EXTENT SECTION
         BCT   R5,CHKEXTD          LOOP THROUGH ALL EXTENTS
         SPACE 2
RETURN0  DS    0H
         RETURN (14,12),RC=0       RETURN - EVERYTHING OK
RETURN4  DS    0H
         RETURN (14,12),RC=4       RETURN - NOT ALLOWED ON THIS VOLUME
RETURN8  DS    0H
         RETURN (14,12),RC=8       RETURN - NOT ALLOWED ON ANY VOLUME
         EJECT
         LTORG
ZEROS    DC    X'00000000'         HEX ZEROES
TSO8XX   DC    C'TSO8'             VOLSER TSO8XX
WRKXXX   DC    C'WRKXXX'           WORK PACK
MSSVOL   DC    C'MWKXXX'           MSS PACK
MPRXXX   DC    C'MPRXXX'           PROD PACK
PROD     DC    C'PROD'             PRODUCTION WORK DATASET
HSM      DC    C'HSM.MDB.'         HSM RECALL DATASET NAME
SYS1     DC    C'SYS1'             SYS1 DATASET
VS01     DC    C'VS01'             VS01 DATASET
VSW1     DC    C'VSW1'             VSW1 DATASET
DCWT     DC    C'DCWT'             DCWT DATASET
$WC3     DC    C'$WC3'             HSS DATASET
$WC4     DC    C'$WC4'             PC DATASET
DOLLAR   DC    C'$'                TSO DSN MUST START WITH $
CYL5     DC    X'000005'           SPACE OF 5 CYLINDERS
CYL1     DC    X'000001'           SPACE OF 1 CYLINDER FOR EXTENT INFO
CYLNUM   DC    X'00000A'           SPACE OF 10 CYLINDERS
BLKNUM   DC    X'0003E8'           SPACE OF 1000 BLOCKS
TRK15    DC    X'00000F'           SPACE OF 15 TRACKS FOR EXTENT INFO
TRK3380  DC    X'000096'           SPACE OF 150 TRACKS
BYTES    DC    F'6000000'          6 MEGA BYTES
MSSCYL   DC    X'000032'           SPACE OF 50 CYLINDERS
MSSTRK   DC    X'0002EE'           SPACE OF 750 TRACKS
MSSBYTES DC    F'35000000'         35 MEGA BYTES
         EJECT
IGGIEPL  IECIEXPL                  PARAMETER LIST MAP IEPL
         EJECT
IGGJFCB  DSECT
         IEFJFCBN LIST=YES         JFCB DSECT
         EJECT
IGGUCB   DSECT
         IEFUCBOB LIST=NO          UCB DSECT
         EJECT
         IEZDEB LIST=YES           DEB DSECT
         END   IGGPRE00
