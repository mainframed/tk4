++USERMOD (A$$W008)   /*
      THIS USERMOD IS TO INSTALL THE
      SVC THAT OBTAINS THE TERMINAL ID FOR VTAM

      SVC = 233 - TYPE 4
                                                     07/26/88 BRM  */.
++VER (Z038) FMID(A$$SVCS) .
++JCLIN .
//A$$W008 JOB WC07N0323,'W1-5311  CR SINGER'
//LPASVC  EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,REUS,RENT',
//    UNIT='3380',SER=IPLXXX,N=SYS2,NAME=HSSLPA,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//SYSLIN   DD  *
   INCLUDE SYSPUNCH(WCSVC233)
   NAME WCSVC233(R)
/*
++SRC (WCSVC233) DISTLIB(ASRCLIB)  .
         TITLE 'SVC 233 - OBTAIN TERMINAL ID'
WCSVC233 CSECT
*
*  THIS SVC OBTAINS THE TERMINAL ID FOR AN ADDRESS SPACE (CURRENT OR
*  OTHERWISE) FROM THE TSB.  FOR VTAM, THIS IS THE FIELD 'TSBXSYM'.
*  FOR TCAM, THIS IS THE FIELD 'TSBLINE', TRANSLATED TO CHARACTER
*  FORMAT (E.G., X'0A12' BECOMES C'0A12') AND PRECEDED BY THE
*  CHARACTERS 'TCAM'.
*
*  AT ENTRY, REG 1 MUST POINT TO A PARAMETER LIST CONTAINING:
*       WORD 1:  ADDRESS OF THE ASCB TO BE USED
*                           - OR -
*                0 (IN WHICH CASE THE CURRENT ASCB WILL BE USED)
*       WORD 2:  ADDRESS OF 8-CHARACTER AREA IN WHICH THE TERMINAL
*                    ID WILL BE RETURNED
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING WCSVC233,R12
         USING WORK,R13       ADDRESSABILITY FOR WORK AREA
         LR    R12,R6
         LR    R2,R1          GET ADDRESS OF PARMS
         USING PARMS,R2       ADDRESSABILITY FOR PARMS
*
         LA    R0,WORKLEN
         GETMAIN R,LV=(0)     GET AREA FOR SAVE
         LR    R13,R1         R13 PTS TO SAVE AREA
*
         ST    R14,SAVE       SAVE RETURN ADDRESS
*
         L     R7,OUTADDR     GET ADDRESS OF OUTPUT AREA
         MVI   0(R7),C' '     CLEAR OUTPUT AREA
         MVC   1(7,R7),0(R7)
         L     R8,ASCBADDR    GET ASCB ADDRESS
         LTR   R8,R8          ZERO?
         BNZ   GOTASCB
*                             YES, GET CURRENT ASCB ADDRESS
         L     R8,16(0,0)     CVT
         L     R8,0(0,R8)     PTR TO CURRENT TCB'S, ASCB'S
         L     R8,12(0,R8)    CURRENT ASCB
GOTASCB  EQU   *              GET TERMINAL ID
         L     R8,60(0,R8)    TSB
         TM    25(R8),X'01'   VTAM?
         BO    VTAM
TCAM     EQU   *
         MVC   0(4,R7),=C'TCAM'  FLAG AS TCAM
         MVC   4(1,R7),82(R8) PUT FIRST HEX DIGIT IN OUTPUT AREA
         IC    R9,83(R8)      GET 2ND AND 3RD DIGITS
         STC   R9,6(,R7)      PUT 3RD DIGIT IN OUTPUT AREA
         SRL   R9,4           GET 2ND DIGIT
         STC   R9,5(,R7)      PUT 2ND DIGIT IN OUTPUT AREA
         NC    4(3,R7),=X'0F0F0F'    PREPARE TO TRANSLATE
         TR    4(3,R7),=C'0123456789ABCDEF'    TRANSLATE
         B     EXIT
*
VTAM     EQU   *
         MVC   0(8,R7),104(R8) YES, PUT VTAM TERMID IN OUTPUT AREA
*
EXIT     L     R14,SAVE       RESTORE R14
         LR    R1,R13
         LA    R0,WORKLEN
         FREEMAIN R,LV=(0),A=(R1)  FREE GETMAINED AREA
         BR    R14
         LTORG
WORK     DSECT
         DS    2F
SAVE     DS    16F
WORKLEN  EQU   *-WORK
PARMS    DSECT
ASCBADDR DS    F
OUTADDR  DS    F
         END   WCSVC233
