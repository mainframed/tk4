++ USERMOD(AVTW001) /* ADD SOURCE ISTAUCAG EXIT ROUTINE

           VTAM USER EXIT ROUTINE FOR FRANK NASH.
                                                                 */ .
++ VER(Z038) FMID(HVT3205) .
++ JCLIN .
//GEN49 JOB Y51024509,'531-0 CR SINGER'
//SG21  EXEC LINKS,
// PARM='NCAL,XREF,LET,LIST,RENT,REFR',
//    UNIT='3350',SER=IPLXXX,N=SYS2,NAME=HSSLPA,P1=' ',
//    MOD=,P2=' ',CLASS=T,OBJ=OBJPDS01
//AOST3   DD DISP=SHR,VOLUME=(,RETAIN),DSN=SYS1.AOST3
//SYSLIN DD *
 INCLUDE AOST3(ISTAUCAG)
 NAME  ISTAUCAG(R)
/*
++ SRC(ISTAUCAG) DISTLIB(ASRCLIB) .
ISTAUCAG CSECT
***********************************************************************
**  VTAM EXIT (ACCOUNTING) ROUTINE                                   **
**     THIS ROUTINE IS CALLED EACH TIME A SESSION IS ESTABLISHED     **
**   OR TERMINATED.  A SEPERATE INVOCATION IS MADE FOR EACH SESSION. **
**   THE PURPOSE OF THIS ROUTINE IS TO KEEP STATISTICS IN ORDER      **
**   TO APPROPRIATLY CHARGE USERS OF APPLICATION PROGRAMS AND LU'S.  **
**  INITIAL REGISTER CONTENTS                                        **
**   REGISTER 0  -  POSITIVE IF SESSION HAS BEEN ESTABLISHED;        **
**                  NEGATIVE IF SESSION HAS BEEN TERMINATED          **
**   REGISTER 7  -  POINTER TO A DOUBLEWORD CONTAINING THE NAME      **
**                  OF THE PRIMARY LOGICAL UNIT (LU)                 **
**   REGISTER 11 -  POINTER TO A DOUBLEWORD CONTAINING THE NAME      **
**                  OF THE SECONDARY LOGICAL UNIT                    **
**   REGISTER 13 -  ADDRESS OF THE SAVE AREA FOR USE BY THIS ROUTINE **
**   REGISTER 14 -  RETURN ADDRESS                                   **
**   REGISTER 15 -  ADDRESS OF THE ENTRY POINT TO THIS ROUTINE       **
**                                                                   **
**    REFERENCE: ADV COMM FUNCTION FOR VTAM - INSTALLATION           **
***********************************************************************
         SAVE  (14,12),,*
         LA    12,0(15)
         USING ISTAUCAG,12       SETUP ADDRESSING FOR CSECT
         MODESET  KEY=ZERO
*  DO GETMAIN FOR WORK AREA SPACE FOR SMF RECORD AND SAVE AREA
GETMAIN  GETMAIN  R,LV=300       GET STORAGE FOR WORK AREA
         ST    13,4(1)           SET BACKWARD PTR
         ST    1,8(13)           SET FORWARD PTR
         LR    13,1              PUT ADDR OF SAVE AREA IN R13
         L     1,4(13)
         L     0,20(1)           RESTORE R0
         L     1,24(1)           RESTORE R1
         USING SAVEAREA,13       SETUP ADDRESSING FOR DSECT
*  FIRST SIX BYTES OF RECORD ARE FIXED
SETCONST MVC   LEN(6),CONST      LOAD CONSTANT DATA INTO SMF RECORD
*  SET THE CONNECT/DISCONNECT FLAG
SETDFLG  C     0,=F'-1'          TEST FOR CONNECT/DISCONNECT
         BH    HIGH              IF HIGH, THEN CONNECT
         MVI   DISCON,C'1'       IS DISCONNECT
         B     SETLUS
HIGH     MVI   DISCON,C'0'       IS CONNECT
SETLUS   MVC   LU1(8),0(7)       GET APPLICATION NAME
         MVC   LU2(8),0(11)      GET LOGICAL UNIT/TERMINAL NAME
*  CALL TIME SVC TO GET TIME AND DATE
SETTMDT  TIME  BIN               GET TIME AND DATE FOR WRITE
         STCM  0,15,TME
         STCM  1,15,DTE
         L     4,16              GET ADDRESS OF CVT
         L     4,196(4)          GET ADDRESS OF SMCA
SETSID   MVC   SID(4),16(4)      GET SYSTEM ID
*  CALL SMF SVC TO WRITE A SMF RECORD
WRREC    SMFWTM OUTREC           WRITE SMF RECORD
         LTR   15,15             TEST FOR ZERO RETURN CODE
         BZ    EXIT1
         WTO   MF=(E,WTO1)       ERROR - WRITE WTO
*  DO CLEANUP BEFORE EXITING
EXIT1    LR    1,13              RESTORE R1 FOR FREEMAIN
         L     13,4(13)          RESTORE R13
         FREEMAIN R,LV=300,A=(1) FREE WORK AREA
EXIT2    EQU      *
         MODESET  KEY=NZERO
RETURN   RETURN   (14,12),RC=0   RETURN TO VTAM
*  CONSTANT DATA
         DS    0F
CONST    DC    X'0023000000E6'
VTAMKEY  DC    X'60'
FC22     DC    C'FC22'
FC23     DC    C'FC23'
WTO1     WTO   ' ERROR IN ISTAUCAG - F NASH',ROUTCDE=(1,2,8,10),MF=L
*  DSECT TO DEFINE WORK AREAS FOR GETMAIN SPACE
         DSECT
SAVEAREA DS    18F
OUTREC   DS    0CL35
*           REQUIRED HEADER FOR ALL SMF RECORDS
LEN      DS    BL2             RECORD LENGTH
SEG      DS    BL2             SPAN FIELD
FLG      DS    BL1
TYPE     DS    BL1             RECORD TYPE (BINARY)
TME      DS    BL4             TIME (BINARY)
DTE      DS    BL4             DATE (PACKED DECIMAL)
SID      DS    CL4             SYSTEM ID
*           REF: MVS SYS MNGT FACILITIES
DISCON   DS    CL1             LOGON/LOGOFF FLAG
LU1      DS    CL8             APPLICATION ID
LU2      DS    CL8             TERMINAL ID
*
         END
