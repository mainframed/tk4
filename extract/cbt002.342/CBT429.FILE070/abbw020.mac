++ USERMOD(ABBW020) /* INSTALL IEFUTL EXIT:

  THIS MODIFICATION INSTALLS THE IEFUTL SMF EXIT WHICH WILL:
   1) CHECK ACF2 FLAG TSOWAIT TO SEE IF USER WAIT TIME WILL BE
      EXTENDED.
   2) IF THE FLAG IS ON, SET EXTENSION TO 5 HOURS AND TURN THE FLAG
      OFF (THIS GIVES USER ONE TIME EXTENSION OF 5 HOURS).
   3) IF FLAG IS OFF, DON'T DO ANYTHING.
                                                    */ .
++ VER(Z038) FMID(HBB2102)     /*   MVSXA VER 2     */ .
++ JCLIN .
//GEN11 JOB WC07N0323,'W1-5311  CR SINGER'
//SG21 EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,REUS,RENT',
//    UNIT='3350',SER=IPLXXX,N=GEN1,NAME=LPALIB,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//SYSLIN DD *
  ENTRY  IEFUTL
  NAME IEFUTL(R)
/*
++ SRC(IEFUTL) DISTLIB(ASAMPLIB) DISTOBJ(AOSB3).
IEFUTL TITLE '*** SMF WAIT TIME EXIT *** IEFUTL'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  ROUTINE NAME - IEFUTL                                              *
*          BRUCE R. MINTON                                            *
*          GENERAL DYNAMICS - DATA SYSTEMS DIVISION                   *
*          P.O. BOX 85808                                             *
*          SAN DIEGO, CALIFORNIA 92138                                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 4
         ACFREGS
IEFUTL   CSECT
         SAVE  (14,12),,*              SAVE REGS
         LR    R10,R15                 BASE REGISTER
         USING IEFUTL,R10              SET UP ADDRESSABILITY
         LR    R2,R0                   SAVE PARM INFO
         LR    R8,R1                   SAVE PARM INFO
         USING SMFEPLA,R8              ADDR SMF INFO
         LA    R6,0                    SET DEFAULT TIME EXTENSION
         LA    R12,0                   SET DEFAULT RETURN CODE
         C     R2,=F'08'               HAS WAIT TIME EXPIRED?
         BNE   FINI                    ..NO, DONT DO ANYTHING
         GETMAIN RU,LV=WORKLEN         GET WORKING STORAGE
         LR    R9,R1                   SAVE POINTER
         USING WORKAREA,R9             SET UP ADDRESSABILITY
         ST    R1,8(R13)               <= CHAIN THE
         ST    R13,4(R1)               <= SAVE
         LR    R13,R1                  <= AREAS
         L     R3,16                   GET CVT POINTER
         L     R4,0(R3)                GET TCB WORDS
         L     R4,12(R4)               GET CURRENT ASCB POINTER
         L     R5,172(R4)              GET POINTER TO JOBNAME FIELD
         LTR   R5,R5                   IS IT A BATCH JOB?
         BZ    CHKTSO                  ..NO, SEE IF IS TSO
         B     RETURN                  ..YES, DONT DO ANYTHING
CHKTSO   DS    0H
         L     R5,144(R4)              GET POINTER TO OUCB
         TM    X'74'(R3),X'80'         IS THIS MVS/XA?
         BZ    SP13                    ..NO, MUST BE MVS/SP 1.3
         L     R15,=A(X'80000000'+CKTASK) SET UP FOR 31 BIT MODE
         BSM   R0,R15                  SWITCH TO 31 BIT MODE
CKTASK   DS    0H
         TM    18(R5),X'20'            IS IT A TSO TASK?
         BO    OFF31BIT                ..YES, CONTINUE
         B     A24BIT                  ..NO, DONT DO ANYTHING
OFF31BIT DS    0H
         L     R15,=A(X'00000000'+ACF2CVT) SET UP FOR 24 BIT MODE
         BSM   R0,R15                  SWITCH TO 24 BIT MODE
         B     ACF2CVT                 GO CHECK ACF2 INFO
SP13     DS    0H
         TM    18(R5),X'20'            IS IT A TSO TASK?
         BZ    RETURN                  ..NO, DONT DO ANYTHING
ACF2CVT  DS    0H
         ACFINCVT R11,NONE=RETURN      FIND ACF2 CVT
         USING ACCVT,R11               ADDRESS IT
         ACFGUCB R7,NONE=RETURN,       GET ACF2 ACUCB                  X
               INLINE=YES,SYS=AOS2
         USING ACUCB,R7                ADDRESS ACUCB
         L     R3,ACULRECP             POINT AT LOGONID RECORD
         USING LIDREC,R3               ADDRESS LOGONID
         TM    LIDI2FLG,LIDI2F1        IS USER TO WAIT TIME EXTENDED?
         BNO   RETURN                  ..NO, DONT DO ANYTHING
EXTENDTM DS    0H
         NI    LIDI2FLG,X'7F'          TURN OFF TSO WAIT FLAG
         L     R12,=F'08'              SET RC TO EXTEND WAIT
         L     R6,WAITTIME             LOAD TIME EXTENSION TIMER
         B     RETURN                  ALL DONE
A24BIT   DS    0H
         L     R15,=A(X'00000000'+RETURN) SET UP FOR 24 BIT MODE
         BSM   R0,R15                  SWITCH TO 24 BIT MODE
RETURN   DS    0H
         LR    R1,R9                   COPY WORKAREA ADDR
         L     R13,SAVEAREA+4          POINT AT SAVE AREA
         FREEMAIN RU,LV=WORKLEN,A=(1)  FREE STORAGE
FINI     DS    0H
         LR    R1,R6                   GET TIMER EXTENSION
         LR    R15,R12                 GET RETURN CODE
         L     R14,12(,R13)            RESTORE R14
         LM    R2,R12,28(R13)          RESTORE REGS EXCEPT R0,R1,R15
         BR    14                      ALL DONE
         EJECT
         LTORG
WAITTIME DC    F'18000'                18,000 SECONDS OR 5 HOURS
WORKAREA DSECT
SAVEAREA DS    9D
WORKLEN  EQU   *-WORKAREA
         SPACE 4
SMFEPLA  DSECT
SMFJOBN  DS    CL8                     JOBNAME
SMFTIME  DS    CL4                     TIME JOB ENTERED SYSTEM
SMFDATE  DS    CL4                     DATE JOB ENTERED SYSTEM
SMFSID   DS    CL4                     SMF SYSTEM ID
SMFUSID  DS    CL8                     SMF USER IDENTIFICATION
SMFSTEP  DS    CL1                     NUMBER OF STEP BEING PROCESSED
SMFOPT   DS    CL1                     SMF OPTIONS
SMFREST  DS    CL1                     SMF RESTART INDICATOR
SMFCLAS  DS    CL1                     JOB CLASS
SMFUSER  DS    CL4                     USER COMMUNICATION FIELDS
         EJECT
         LIDREC
         EJECT
         ACUCB
         EJECT
         ACFASVT
         EJECT
         ACCVT
         EJECT
         IHAPSA
         EJECT
         IHAASCB
         END IEFUTL
