++ USERMOD(AJSW016) /*

   PRINT JOB STATS ON JOB TRAILER PAGE
                                       */.
++ VER (Z038) FMID(HJS2215).
++ SRCUPD(IATUX23) DISTLIB(AJES3SRC) .
./ CHANGE NAME=IATUX23
*        USER MODS =                                            AJSW016
*                                                               AJSW016
*             - JOB TRAILER PAGE INFORMATION                    AJSW016
*                                                               AJSW016
**************************************************************  AJSW016
*                                                            *  AJSW016
*        IATUX23 - PRINT TRAILER PAGE INFORMATION            *  AJSW016
*                                                            *  AJSW016
**************************************************************  AJSW016
         IATYJCT ,                                              AJSW016
         IATYJMR ,                                              AJSW016
         EJECT                                                  AJSW016
         AIF   ('&TYPE' NE 'DSECT').NUAXX                       AJSW016
UX23WKST DC    0X'00'              START OF WORK SPACE          AJSW016
UX23WORK DC    2D'0'               WORK SPACE                   AJSW016
UX23GDLN DS    CL(GDLINESL)        PRINT BUILD AREA             AJSW016
UX23WKND DC    0D'0'               END OF WORK SPACE            AJSW016
UX23WKLN EQU   *-UX23WKST          SIZE OF USER WORK SPACE      AJSW016
.NUAXX   ANOP                                                   AJSW016
         A     R0,=A(UX23WKLN)     ADD SIZE OF USER WORK AREA   AJSW016
         LA    R2,UX23GDLN         INITIALIZE ALL LINES         AJSW016
         LM    R3,R5,=A(555,GDLINES,555)    ..                  AJSW016
         MVCL  R2,R4                 ..                         AJSW016
         LA    R6,UX23GDLN+222       ..                         AJSW016
         IATXTOD ,                 GET -> TIME OF DAY           AJSW016
         MVC   58(2,R6),0(R1)      HH TO PRINT LINE             AJSW016
         MVC   61(2,R6),2(R1)       MM TO PRINT LINE            AJSW016
         MVC   64(2,R6),4(R1)        SS TO PRINT LINE           AJSW016
         IATXTOD BIN                                            AJSW016
         ST    R1,UX23WORK+12      SAVE RETURNED DATE           AJSW016
         MVI   FEB+3,28            ASSUME NOT A LEAP YEAR       AJSW016
         TM    UX23WORK+13,X'01'   IS IT AN ODD YEAR?           AJSW016
         BC    ALLON,AJS001        YES, CONTINUE                AJSW016
         TM    UX23WORK+13,X'12'   IS IT A LEAP YEAR?           AJSW016
         BC    MIXED,AJS001        NO, CONTINUE                 AJSW016
         MVI   FEB+3,29            SET FEBRUARY FOR 29 DAYS     AJSW016
AJS001   DS    0H                  DATE 00YYDDD                 AJSW016
         UNPK  53(5,R6),UX23WORK+12(4) YYDDD TO ZONED           AJSW016
         MVC   55(3,R6),TVTBLANK   BLANK OUT ZONED DDD          AJSW016
         MVC   UX23WORK+8(6),TVTZERO CLEAR YY & 4 PRECEEDING    AJSW016
         CVB   R3,UX23WORK+8       DDD TO BINARY                AJSW016
         LA    R4,GDTABL           -> DAYS-IN-MONTH TABLE       AJSW016
GDLOOKUP S     R3,0(R4)            DDD LESS DAYS IN MONTH       AJSW016
         BNP   GDFND               GOT DAY IN THIS MONTH        AJSW016
         CLC   GDTABLND,4(R4)      END OF DAYS-IN-MONTH TABLE?  AJSW016
         BE    GDNOTFND            YES, AN ERROR                AJSW016
         LA    R4,8(R4)            NO, STEP TO NEXT MONTH       AJSW016
         B     GDLOOKUP            AND LOOP FOR DATE            AJSW016
GDFND    A     R3,0(R4)            ADD BACK THIS MONTH'S DAYS   AJSW016
         MVC   43(3,R6),4(R4)      SET NAME OF MONTH IN PRINT   AJSW016
         CVD   R3,UX23WORK+8       DAY OF MONTH TO PACKED       AJSW016
         UNPK  47(2,R6),UX23WORK+8(8) DAY OF MONTH TO ZONED PRT AJSW016
         OI    48(R6),X'F0'        SET ALL SIGN BITS            AJSW016
         B     GDGETJCT              ..                         AJSW016
GDNOTFND MVC   44(6,R6),=C'UNK XX' ERROR CONVERTING DATE        AJSW016
GDGETJCT PACK  UX23WORK+8(8),WTRDJNO PACK JOB NUMBER            AJSW016
         CVB   R1,UX23WORK+8       JOB NUMBER TO DECIMAL  ..    AJSW016
         IATXJCT TYPE=RW,JOBNUM=(R1),ERROR=GDPRINT  GET JCT     AJSW016
         LR    R4,R1               COPY -> TO JCT               AJSW016
         USING JCTSTART,R4         JCT MAP ADDRESSABILITY       AJSW016
         OI    JCTFL8,JCTNCHNG     NO CHANGE MADE TO JCT        AJSW016
         MVI   UX23WORK,0          SET DIDN'T READ JMR FLAG     AJSW016
         LA    R1,JCTJMRFD         GET -> JMR FDB               AJSW016
         USING FDBSTART,R1                                      AJSW016
         TM    FDBSPFL1,FDBONSP    IS IT ON SPOOL?              AJSW016
         BC    ALLOFF,GDSETJMR     NO, DONT READ IT             AJSW016
         DROP  R1                                               AJSW016
         MVI   UX23WORK,128        YES, SET DID READ JMR FLAG   AJSW016
         JESREAD FDB=(R1),ID=JMR   READ JMR                     AJSW016
GDSETJMR L     R5,JCTJMRFD         FINISH UP HEADER LINE        AJSW016
         USING JMR,R5                                           AJSW016
         MVC   4(8,R6),SMF26DEV    JOB ORIGIN TO PRINT LINE     AJSW016
         MVC   101(8,R6),SMF26DEV    ..                         AJSW016
         MVC   15(20,R6),SMF26NAM  PROGRAMMER NAME TO PRINT LN  AJSW016
         MVC   78(20,R6),SMF26NAM    ..                         AJSW016
         CLI   UX23WORK,128        DID WE READ THE JMR          AJSW016
         BNE   GDRELJCT            NO, DON'T RELEASE IT         AJSW016
         LA    R1,JCTJMRFD         YES, MUST RELEASE JMR        AJSW016
         ARELEASE FDB=(R1)         RELEASE JMR                  AJSW016
         MVI   UX23WORK,0          CLEAR THE FLAG               AJSW016
GDRELJCT PACK  UX23WORK+8(8),WTRDJNO RELEASE THE JCT            AJSW016
         CVB   R1,UX23WORK+8       JOBNUMBER TO BINARY          AJSW016
         IATXJCT TYPE=REL,JOBNUM=(R1),ERROR=GDPRINT  ..         AJSW016
GDPRINT  LA    R3,5                # LINES TO BE PRINTED        AJSW016
         LA    R2,UX23GDLN         -> LINES TO PRINT            AJSW016
         L     R15,=A(UX23S150)    -> MIDDLE OF SUBROUTINE      AJSW016
         L     R10,=A(UX23SUB)     SET SUBROUTINE BASE ADDR     AJSW016
         BALR  R9,R15              JUMP TO MIDDLE OF SUBROUTINE AJSW016
         A     R0,=A(UX23WKLN)     ADD SIZE OF USER WORK AREA   AJSW016
GDTABL   DC    A(31)               JANUARY                      AJSW016
         DC    C'JAN'                ..                         AJSW016
FEB      DC    A(28)               FEBRUARY                     AJSW016
         DC    C'FEB'                ..                         AJSW016
         DC    A(31)               MARCH                        AJSW016
         DC    C'MAR'                ..                         AJSW016
         DC    A(30)               APRIL                        AJSW016
         DC    C'APR'                ..                         AJSW016
         DC    A(31)               MAY                          AJSW016
         DC    C'MAY'                ..                         AJSW016
         DC    A(30)               JUNE                         AJSW016
         DC    C'JUN'                ..                         AJSW016
         DC    A(31)               JULY                         AJSW016
         DC    C'JUL'                ..                         AJSW016
         DC    A(31)               AUGUST                       AJSW016
         DC    C'AUG'                ..                         AJSW016
         DC    A(30)               SEPTEMBER                    AJSW016
         DC    C'SEP'                ..                         AJSW016
         DC    A(31)               OCTOBER                      AJSW016
         DC    C'OCT'                ..                         AJSW016
         DC    A(30)               NOVEMBER                     AJSW016
         DC    C'NOV'                ..                         AJSW016
         DC    A(31)               DECEMBER                     AJSW016
GDTABLND DC    C'DEC'                ..                         AJSW016
GDLINES  DC    C' '                                             AJSW016
         DC    110C'*'                                          AJSW016
         DC    CL110' *'                                        AJSW016
         DC    C'*'                                             AJSW016
         DC    CL49' *'                                         AJSW016
         DC    CL11', 19'                                       AJSW016
         DC    CL50'.  .'                                       AJSW016
         DC    C'*'                                             AJSW016
         DC    CL110' *'                                        AJSW016
         DC    C'*'                                             AJSW016
         DC    C' '                                             AJSW016
         DC    110C'*'                                          AJSW016
GDLINESN DC    0D'0'               END OF TRLR PRINT DATA       AJSW016
GDLINESL EQU   GDLINESN-GDLINES    SIZE OF TRLR PRINT DATA      AJSW016
UMODNUM  DC    CL7'AJSW016'        USER NUMBER
