++ USERMOD(ACFW009) /* ADD ACF2/JES3 USER EXIT 02

           THE ACFUSX02 ROUTINE WILL ADD TO ACCOUNT FIELDS IN
           THE TYPE 26 SMF RECORD.  IT WILL ADD A DUMMY 2ND ACCT
           FIELD AS WELL AS THE USERS LOGONID AND DIVISION
           CODE TO THE 3RD AND 4TH ACCOUNTING FIELDS.
                                                                 */ .
++ VER(Z038) FMID(ACF5000) PRE(TS75402,TS75397) .
++ SRCUPD(ACFUSX02) DISTLIB(ACFAMAC) SYSLIB(ACFMAC) .
./ CHANGE NAME=ACFUSX02
*  USER MODS =                                                  ACFW009
*       ADD INFO TO TYPE 26 SMF RECORD.                         ACFW009
*                                                               ACFW009
         EJECT                                                  ACFW009
         IATYJMR  ,                                             ACFW009
         EJECT                                                  ACFW009
         ACCVT    ,                                             ACFW009
         LR    R2,R1              *GET POINTER TO IATISDT       ACFW009
         USING IATISDT,R2         *SET UP ADDRESSABILITY        ACFW009
         GETMAIN RU,LV=WORKLEN    *GET WORK AREA                ACFW009
         ST    R13,4(R1)          *SAVE AREA CHAIN              ACFW009
         ST    R1,8(R13)          *SAVE GETMAINED AREA          ACFW009
         LR    R3,R1              *POINT AT GETMAIN AREA        ACFW009
         USING WORKAREA,R3        *ADDRESS GETMAINED AREA       ACFW009
         TM    ISFLAG9,ISDSEL     *IS THIS A DEMAND SELECT JOB? ACFW009
         BC    ALLOFF,GETCVT      *NO, GO PUT IN ACCT DATA      ACFW009
         TM    ISFLAG10,ISMVSTSO  *IS IT A TSO JOB?             ACFW009
         BC    ALLOFF,RETURN      *NO, RETURN TO CALLER         ACFW009
         SPACE 3                                                ACFW009
GETCVT   ACFINCVT 6,NONE=RETURN   *GO GET ACF2 CVT              ACFW009
         USING ACCVT,R6           *ADDRESS ACF2 CVT             ACFW009
         XC    ACALT(ACALEN),ACALT *ZERO PARM BLOCK             ACFW009
         SPACE 2                                                ACFW009
         MVI   ACAFCN,X'02'       *SET UP ACF2 SVC CALL         ACFW009
         MVI   ACASFCN,ACASURT    *RETURN UNFORMATTED LOGONID   ACFW009
         LA    R1,ERRINFO         *MESSAGE AREA ADDRESS         ACFW009
         ST    R1,ACAMSG          *STORE IT                     ACFW009
         MVC   ACALID,ACVLID      *GET LOGONID TO RETURN        ACFW009
         DROP  R5                                               ACFW009
         LA    R7,LOGONREC        *POINT AT DATA RETURN AREA    ACFW009
         USING LIDREC,R7          *SETUP ADDRESSABILITY         ACFW009
         ST    R7,ACARETB         *STORE ADDR OF RETURN AREA    ACFW009
         MVC   ACARETL,=A(1024)   *GET LOGONID LENGTH           ACFW009
         SPACE 2                                                ACFW009
         ACFSVC ACALT,TYPE=A,     *GO FIND ACF2 LOGONID         ACFW009X
               NONE=RETURN,                                     ACFW009X
               CVT=HAVE                                         ACFW009
         LTR   R15,R15            *EVERYTHING OK?               ACFW009
         BZ    GETSMF             *YES, GO GET LOGONID INFO     ACFW009
         SPACE 2                                                ACFW009
ERROR    DS    0H                                               ACFW009
         LH    R1,ERRINFO         *GET MESSAGE LENGTH           ACFW009
         LA    R1,ERRINFO(R1)     *NEXT AVAILABLE BYTE IN MSG   ACFW009
         MVC   0(L'RCODE,R1),RCODE *ADD ROUTING CODES           ACFW009
         MVI   ERRINFO+1,X'80'    *AND MCS FLAG                 ACFW009
         WTO   MF=(E,ERRINFO)     *ISSUE THE WTO                ACFW009
         B     RETURN             *ALL DONE                     ACFW009
         SPACE 2                                                ACFW009
GETSMF   DS    0H                                               ACFW009
         L     R4,JDABADDR        *GET JDAB POINTER             ACFW009
         USING JDABSTRT,R4        *SET UP BASE REG              ACFW009
         BAL   R14,SMF26          *GET ADDR OF SMF26JAF         ACFW009
         B     RETURN             *ACTUAL SECT DOES NOT EXIST   ACFW009
         MVI   0(R1),X'04'        *SPECIFY FOUR ACCT FIELDS     ACFW009
         SR    R15,R15            *CLEAR R15                    ACFW009
         IC    R15,1(R1)          *GET LENGTH OF 1ST ACCT FLD   ACFW009
         LA    R15,2(R15)         *ADD 2 FOR 1ST TWO FIELDS     ACFW009
         LA    R1,0(R15,R1)       *GET POINTER TO 2ND ACCT FLD  ACFW009
         MVC   0(2,R1),INIT       *MOVE IN DUMMY 2ND ACCT FLD   ACFW009
         LA    R1,2(R1)           *POINT TO LOGONID FIELD       ACFW009
         LA    R14,8              *MAXIMUM LENGTH OF LOGONID    ACFW009
         LA    R15,LIDLID         *POINT TO LOGONID             ACFW009
LIDLOOP  CLI   0(R15),C' '        *BLANK YET                    ACFW009
         BE    LIDBLNK            *YES - STOP SCAN              ACFW009
         LA    R15,1(R15)         *POINT TO NEXT CHARACTER      ACFW009
         BCT   R14,LIDLOOP        *AND AROUND AGAIN             ACFW009
LIDBLNK  LA    R14,LIDLID         *POINT TO LOGONID             ACFW009
         SR    R15,R14            *COMPUTE LENGTH OF LOGONID    ACFW009
         LTR   R15,R15            *TEST FOR ZERO                ACFW009
         BZ    LIDNULL            *YES - IT WAS ZERO            ACFW009
         STC   R15,0(R1)          *MOVE IN LOGONID LENGTH       ACFW009
         BCTR  R15,R0             *SUBTRACT ONE FOR EX INST     ACFW009
         EX    R15,LIDMOVE        *MOVE IN LOGONID              ACFW009
         B     *+10               *SKIP MOVE FOR DUMMY LOGONID  ACFW009
LIDNULL  MVC   0(2,R1),INIT       *MOVE IN DUMMY 3ND ACCT FLD   ACFW009
         LA    R1,2(R15,R1)       *POINT TO DIVISION FIELD      ACFW009
         MVI   0(R1),X'02'        *MOVE IN LENGTH OF DIVISON    ACFW009
         MVC   1(2,R1),LIDDIV     *MOVE IN DIVISION CODE        ACFW009
         B     RETURN             *ALL DONE                     ACFW009
LIDMOVE  MVC   1(0,R1),LIDLID     *MOVE IN LOGONID              ACFW009
*-------------------------------------------------------------* ACFW009
*  SMF26 - POINT AT ACCOUNTING FIELDS IN TYPE 26 SMF RECORD   * ACFW009
*-------------------------------------------------------------* ACFW009
SMF26    L     R1,JMRADDR         *GET JMR ADDR                 ACFW009
         USING JMR,R1             *ADDRESSABILITY               ACFW009
         TM    SMF26IND,X'20'     *ACTUAL SECTION EXIST?        ACFW009
         BZR   R14                *..NO, RETURN TO CALLER       ACFW009
         TM    SMF26IND,X'C0'     *DOES DESCR OR EVENT EXIST?   ACFW009
         BZ    SMFACT             *..NEITHER, JUST ACTUAL       ACFW009
         BM    SMFONE             *..JUST ONE OF THEM EXISTS    ACFW009
         AH    R1,SMF26LN1        *ALLOW FOR DESCR SECT         ACFW009
SMFONE   AH    R1,SMF26LN1        *ALLOW FOR EVENT (OR DESCR)   ACFW009
SMFACT   LA    R1,SMF26JAF        *POINT TO JOB ACCTNG FLDS     ACFW009
         B     4(R14)             *NORMAL RETURN                ACFW009
         SPACE 3                                                ACFW009
RETURN   DS    0H                                               ACFW009
         LR    R1,R3              *UNCHAIN SAVE AREAS           ACFW009
         L     R13,4(R3)          *POINT BACK AT SAVE AREA      ACFW009
         FREEMAIN RU,LV=WORKLEN,A=(1) *FREE STORAGE             ACFW009
*-------------------------------------------------------------* ACFW009
*        C O N S T A N T S                                    * ACFW009
*-------------------------------------------------------------* ACFW009
INIT     DC    XL2'01F0'          *LENGTH=1, ACCT=0             ACFW009
RCODE    DC    X'000080A0'        *WTO ROUTING CODES            ACFW009
WORKAREA DSECT                                                  ACFW009
SAVEREGS DS    9D                 *SAVE AREA                    ACFW009
         ACALT DSECT=NO           *RESOURCE PARM BLOCK          ACFW009
ACALEN   EQU       *-ACALT        *LENGTH OF CONTROL BLOCK      ACFW009
ERRINFO  DS    CL128              *MESSAGE AREA                 ACFW009
LOGONREC DS    CL1024             *LOGONID RECORD               ACFW009
WORKLEN  EQU   *-WORKAREA         *WORK AREA LENGTH             ACFW009
UMODNUM  DC    CL7'ACFW009'       *USER NUMBER                  ACFW009
