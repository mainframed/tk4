++USERMOD (A$$W006)   /*
      THIS USERMOD IS TO INSTALL THE
      AUTHORIZE IEBCOPY SVC

      SVC = 212 - TYPE 3
                                                     07/26/88 BRM  */.
++VER (Z038) FMID(A$$SVCS) .
++JCLIN .
//A$$W006 JOB WC07N0323,'W1-5311  CR SINGER'
//LPASVC  EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,REUS,RENT',
//    UNIT='3380',SER=IPLXXX,N=SYS2,NAME=HSSLPA,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//SYSLIN   DD  *
   INCLUDE SYSPUNCH(WCSVC212)
   NAME WCSVC212(R)
/*
++SRC (WCSVC212) DISTLIB(ASRCLIB)  .
WCSVC212 CSECT                    AUTHORIZATION ON/OFF SVC
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*   ROUTINE NAME - WCSVC212                                           *
*           DATE - 21 OCTOBER 1982                                    *
*           BRUCE R. MINTON                                           *
*           GENERAL DYNAMICS - DATA SYSTEMS DIVISION                  *
*           P.O. BOX 85808                                            *
*           SAN DIEGO, CALIFORNIA 92138                               *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 4
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   THIS SVC TURNS ON THE AUTHORIZATION BIT FROM CALLER. IT WAS SETUP *
*   FOR PROGRAM ISPFCOPY BECAUSE IT NEEDS TO CALL IEBCOPY.            *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         BALR  12,0               PROGRAM ADDRESSABILITY
         USING *,12
         L     2,28(5)            ADDRESS OF CALLERS RB TO R2
         CLC   21(3,2),361(3)     CHECK RESUME PSW
         BL    RETURN             EXIT, CALLER NOT IN PLPA
         L     2,180(4)           ADDRESS JSCB TO R2
         BCT   0,AUTHOFF          R0 NOT EQUAL 1 REQUESTS AUTH OFF
AUTHON   OI    236(2),X'01'       SET JSCBAUTH BIT
         B     RETURN             EXIT WITH AUTHORIZATION ON
AUTHOFF  NI    236(2),X'FE'       CLEAR JSCBAUTH BIT
RETURN   BR    14                 RETURN
         END   WCSVC212
