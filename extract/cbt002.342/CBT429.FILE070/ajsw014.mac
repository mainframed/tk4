++ USERMOD(AJSW014) /*

       THIS MODIFICATION:

       1. EXAMINES JOB CLASS AND IF IT IS EXPRESS IT CHECKS THE
          TOTAL REQUESTED CPU TIME FOR THE JOB. IF IT IS IN EXCESS
          OF 30 SECONDS IT WILL CHANGE THE CLASS TO JS3BATCH.
       2. EXAMINES JOB CLASS AND IF IT IS JS3BATCH IT WILL CALCULATE
          THE (NEW) JOB PRIORITY USING A LINEAR COMBINATION OF TOTAL
          CPU TIME, ESTIMATED NUMBER OF PRINT LINES, NUMBER OF EST-
          IMATED TAPE MOUNTS, NUMBER OF NEW DISK DATASETS AND THE
          LARGEST STEPS REGION REQUESTED. THE 'SMALLER' JOBS GET
          THE HIGHER PRIORITY (HIGEST = 9) AND THE 'LARGER' JOBS GET
          THE LOWER PRIORITY (LOWEST = 1).
                                                                 */ .
++ VER(Z038) FMID(HJS2215).
++ SRCUPD(IATIICM) .
./ CHANGE NAME=IATIICM
* USER MODS:                                                    AJSW014
*                                                               AJSW014
*      - COMPUTE JOB PRIORITY AND CHANGE CLASS                  AJSW014
*                                                               AJSW014
         EJECT                                                  AJSW014
         IATYRSQ                                                AJSW014
         EJECT                                                  AJSW014
         IATYMCL ,                                              AJSW014
         EJECT                                                  AJSW014
         IATYJQE ,                                              AJSW014
GDEOJ    B     GDEOJ1                                           AJSW014
         XC    GDXTM(4),GDXTM     *CLEAR TIME FIELD             AJSW014
GDSTEP   B     GDSTEP1                                          AJSW014
GDUNIT   B     GDUNIT1                                          AJSW014
         EJECT                                                  AJSW014
*************************************************************** AJSW014
* ENTRY IS FROM GDSTEP AT 04920010                            * AJSW014
*                                                             * AJSW014
* 1) ACCUM STEP ESTIMATED EXECUTION TIME                      * AJSW014
* 2) SAVE LARGEST STEP REGION REQUEST                         * AJSW014
*************************************************************** AJSW014
         USING SCT,R6                                           AJSW014
GDSTEP1  STM   R2,R9,GDSAVE       *SAVE REGISTERS               AJSW014
         BALR  R9,0               *SET BASE                     AJSW014
         USING *,R9               *  ..                         AJSW014
         SR    R5,R5              *ACCUM STEP EST. XTM          AJSW014
         ICM   R5,7,SCTSTIME      *  ..                         AJSW014
         BNZ   GDSTEP2            *  ..                         AJSW014
         L     R5,GDDEFTME        *  ..                         AJSW014
GDSTEP2  SR    R4,R4              *  ..                         AJSW014
         D     R4,GD100           *  ..                         AJSW014
         A     R5,GDXTM           *  ..                         AJSW014
         ST    R5,GDXTM           *  ..                         AJSW014
         SR    R5,R5              *SAVE LARGEST STEP RGN REQ    AJSW014
         ICM   R5,3,SCTMSSZE      *  ..                         AJSW014
         BNZ   GDSTEP3            *  ..                         AJSW014
         L     R5,GDDEFRGN        *  ..                         AJSW014
GDSTEP3  C     R5,GDRGN           *  ..                         AJSW014
         BNH   GDSTEPND           *  ..                         AJSW014
         ST    R5,GDRGN           *  ..                         AJSW014
GDSTEPND LM    R2,R9,GDSAVE       *RETURN                       AJSW014
         B     GDSTEP+4           *  ..                         AJSW014
         DROP  R6                                               AJSW014
         SPACE 3                                                AJSW014
*************************************************************** AJSW014
* ENTRY IS FROM GDUNIT AT 08070010                            * AJSW014
*                                                             * AJSW014
* 1) ACCUM TOTAL NUMBER OF TAPE REQUESTS                      * AJSW014
* 2) ACCUM TOTAL NUMBER OF NEW DISK REQUESTS                  * AJSW014
*************************************************************** AJSW014
GDUNIT1  STM   R2,R9,GDSAVE       *SAVE REGISTERS               AJSW014
         BALR  R9,0               *SET BASE                     AJSW014
         USING *,R9               *  ..                         AJSW014
         CLC   GDTAPE,SCTUTYPE    *COUNT # TAPE REQUESTS        AJSW014
         BE    GDUNIT2            *  ..                         AJSW014
         CLC   GD3400,SCTUTYPE    *  ..                         AJSW014
         BNE   GDUNIT3            *  ..                         AJSW014
GDUNIT2  L     R5,GDTPE           *  ..                         AJSW014
         LA    R5,1(R5)           *  ..                         AJSW014
         ST    R5,GDTPE           *  ..                         AJSW014
         B     GDUNITND           *  ..                         AJSW014
GDUNIT3  TM    DDLFLG2,DDLNEW     *COUNT # NEW DISK REQUESTS    AJSW014
         BNO   GDUNITND           *  ..                         AJSW014
         CLC   GDSYS,SCTUTYPE     *  ..                         AJSW014
         BE    GDUNIT4            *  ..                         AJSW014
         CLC   GDDISK,SCTUTYPE    *  ..                         AJSW014
         BE    GDUNIT4            *  ..                         AJSW014
         CLC   GDPROD(4),SCTUTYPE *  ..                         AJSW014
         BE    GDUNIT4            *  ..                         AJSW014
         CLC   GD33,SCTUTYPE      *  ..                         AJSW014
         BE    GDUNIT4            *  ..                         AJSW014
         CLC   GD2305,SCTUTYPE    *  ..                         AJSW014
         BE    GDUNIT4            *  ..                         AJSW014
         CLC   GDVIO,SCTUTYPE     *  ..                         AJSW014
         BNE   GDUNITND           *  ..                         AJSW014
GDUNIT4  L     R5,GDDSK           *  ..                         AJSW014
         LA    R5,1(R5)           *  ..                         AJSW014
         ST    R5,GDDSK           *  ..                         AJSW014
GDUNITND LM    R2,R9,GDSAVE       *RETURN                       AJSW014
         B     GDUNIT+4           *  ..                         AJSW014
         SPACE 3                                                AJSW014
*************************************************************** AJSW014
* ENTRY IS FROM GDEOJ AT 01850010                             * AJSW014
*                                                             * AJSW014
* 1) SAVE ESTIMATED NUMBER OF PRINT LINES                     * AJSW014
* 2) REPLACE STEP REGION SIZE WITH JOB (IF PRESENT)           * AJSW014
* 3) REPLACE STEP ESTIMATED TIME WITH JOB (IF PRESENT)        * AJSW014
* 4) CHANGE CLASS AND PRIORITY AS REQUIRED                    * AJSW014
*************************************************************** AJSW014
GDEOJ1   STM   R2,R9,GDSAVE       *SAVE REGISTERS               AJSW014
         BALR  R9,0               *SET BASE                     AJSW014
         USING *,R9               *  ..                         AJSW014
         L     R2,FCTRQAD                                       AJSW014
         USING RQSTART,R2                                       AJSW014
         L     R3,IDDJMR          *ESTABLISH IATYJMR BASE       AJSW014
         MVC   GDLNE,SMF26ELN(R3) *SAVE ESTIMATED LINE COUNT    AJSW014
         SR    R6,R6              *REPLACE STEP REGION SIZE     AJSW014
         ICM   R6,3,RQMVTSIZ      *  ..                         AJSW014
         BZ    GDEOJ2             *  ..                         AJSW014
         C     R6,GDDEFRGN        *  ..                         AJSW014
         BE    GDEOJ2             *  ..                         AJSW014
         ST    R6,GDRGN           *  ..                         AJSW014
GDEOJ2   ICM   R7,15,IDDCJXTM     *REPLACE STEP EST.EXEC TIME   AJSW014
         BZ    GDEOJ3             *  ..                         AJSW014
         SR    R6,R6              *  ..                         AJSW014
         D     R6,GD100           *  ..                         AJSW014
         ST    R7,GDXTM           *  ..                         AJSW014
GDEOJ3   MVC   SMF26XTM(4,R3),GDXTM                             AJSW014
         LA    R4,30              *LIMIT TO 30 SECONDS          AJSW014
         C     R4,GDXTM           *  ..                         AJSW014
         BL    GDEOJ4             *  ..                         AJSW014
         LA    R8,GD05            *  ..  X, EXPRESS             AJSW014
         CLC   SMF26CLN(8,R3),GDEXP                             AJSW014
         BE    GDEOJ7                                           AJSW014
         LA    R8,GD09              *..  W, DATALOAD            AJSW014
         CLC   SMF26CLN(8,R3),GDLOAD                            AJSW014
         BE    GDEOJ7                                           AJSW014
GDEOJ4   LA    R8,GD07            *LOOK FOR 'PRODUCTION'        AJSW014
         CLC   SMF26CLN(8,R3),GDPROD *PRODUCTION CLASS?         AJSW014
         BE    GDEOJ7P               *YES - LEAVE PRTY          AJSW014
         CLC   SMF26CLN(8,R3),GDBMP  *BMP CLASS                 AJSW014
         BE    GDEOJ7                                           AJSW014
         LA    R8,GD09                                          AJSW014
GDEOJ5   CLC   SMF26CLN(8,R3),GDTP *LOOK FOR 'TP'               AJSW014
         BE    GDEOJ7                                           AJSW014
         LA    R8,GD11             *LOOK FOR 'URGENT'           AJSW014
         CLC   SMF26CLN(8,R3),GDURG                             AJSW014
         BE    GDEOJ7                                           AJSW014
         CLC   SMF26CLN(8,R3),GDSDE *LOOK FOR SD ESTIMATING     AJSW014
         BE    GDEOJ6             *  ..                         AJSW014
         CLC   SMF26CLN(8,R3),GDPDE *LOOK FOR PD ESTIMATING     AJSW014
         BE    GDEOJ6             *  ..                         AJSW014
         CLC   SMF26CLN(8,R3),GDONITE OVERNIGHT CLASS?          AJSW014
         BE    GDEOJ6              GO COMPUTE PRIORITY          AJSW014
         MVI   SMF26CLS(R3),C' '  *CLEAR OUT ANY GARBAGE        AJSW014
         MVI   JMRCLAS(R3),C' '   *  ..  ..                     AJSW014
         MVI   IDDJCLAS,C' '      *  ..  ..                     AJSW014
         TM    IDDGTRC2,IDDGDSEL  *DEMAND SELECT?               AJSW014
         BZ    GDEOJ5B            *NO - CHANGE TO JS3BATCH      AJSW014
         TM    IDDGTRC1,IDDGTSOL  *TSO LOGON?                   AJSW014
         BZ    GDEOJ5A            *NO - MUST BE STC             AJSW014
         LA    R8,GD11            *SET PRIORITY                 AJSW014
         MVC   SMF26CLN(8,R3),GDTSO *CHANGE TO TSO              AJSW014
         B     GDEOJ5C              *AND GO ON                  AJSW014
GDEOJ5A  LA    R8,GD14              *SET PRIORITY               AJSW014
         MVC   SMF26CLN(8,R3),GDSTC *CHANGE TO STC              AJSW014
         B     GDEOJ5C              *AND GO ON                  AJSW014
GDEOJ5B  MVC   SMF26CLN(8,R3),JOBCLS *CHANGE TO JS3BATCH        AJSW014
GDEOJ5C  LH    R1,RQJOBNO                                       AJSW014
         IATXJCT TYPE=RW,JOBNUM=(R1),ERROR=GDEOJ5D              AJSW014
         MVC   JCTSCHCL(8,R1),SMF26CLN(R3) *CHANGE TO CORRECT   AJSW014
         LH    R1,RQJOBNO                                       AJSW014
         IATXJCT TYPE=REL,JOBNUM=(R1),ERROR=GDEOJ5C1            AJSW014
GDEOJ5C1 LH    R1,RQJOBNO                                       AJSW014
         IATXJQE JOBNUM=(R1),ERROR=GDEOJ5D                      AJSW014
         L     R5,MCLASS          *GET MCLASS TABLE ADDRESS     AJSW014
         USING MCNAME,R5          *ESTABLISH ADDRESSABILITY     AJSW014
         LH    R6,MCLNG           *GET LENGTH OF ENTRY          AJSW014
         LA    R7,1               *COUNT OF CLASSES             AJSW014
GDEOJ5C2 CLC   MCNAME(4),=XL4'FFFFFFFF' *LAST ONE?              AJSW014
         BE    GDEOJ5D            *YES IT IS                    AJSW014
         CLC   SMF26CLN(8,R3),MCNAME *IS THIS IT?               AJSW014
         BE    GDEOJ5C3           *YES IT IS                    AJSW014
         LA    R5,0(R5,R6)        *BUMP TO NEXT ONE             AJSW014
         LA    R7,1(R7)           *BUMP TO NEXT ONE             AJSW014
         B     GDEOJ5C2           *AND AROUND AGAIN             AJSW014
GDEOJ5C3 STC   R7,JQECLS-JQESTART(0,R1) NEW JQE CLASS           AJSW014
         DROP  R5                                               AJSW014
GDEOJ5D  CLC   SMF26CLN(8,R3),JOBCLS *DEFAULT?                  AJSW014
         BNE   GDEOJ7             *NO - DON'T COMPUTE PRTY      AJSW014
GDEOJ6   SR    R8,R8              *COMPUTE PRIORITY             AJSW014
         L     R5,GDXTM           *ESTIMATED EXEC TIME          AJSW014
         LA    R6,GDXTMT          *  ..  ..                     AJSW014
         BAL   R7,GDEOJLU         *  ..  ..                     AJSW014
         L     R5,GDRGN           *REGION REQUEST               AJSW014
         LA    R6,GDRGNT          *  ..  ..                     AJSW014
         BAL   R7,GDEOJLU         *  ..  ..                     AJSW014
         L     R5,GDLNE           *ESTIMATED PRINT LINES        AJSW014
         LA    R6,GDLNET          *  ..  ..                     AJSW014
         BAL   R7,GDEOJLU         *  ..  ..                     AJSW014
         L     R5,GDTPE           *NUMBER TAPE REQUESTS         AJSW014
         LA    R6,GDTPET          *  ..  ..                     AJSW014
         BAL   R7,GDEOJLU         *  ..  ..                     AJSW014
         L     R5,GDDSK           *NUMBER NEW DISK REQ'S        AJSW014
         LA    R6,GDDSKT          *  ..  ..                     AJSW014
         BAL   R7,GDEOJLU         *  ..  ..                     AJSW014
         LA    R8,GDPRTYT(R8)     *  ..                         AJSW014
         SPACE 1                                                AJSW014
         PACK  GDCOM(8),0(2,R8)   *PACK COMPUTED PRIORITY       AJSW014
         MVC   TVTWORKD,GDCOM     *MOVE FOR CONVERSION          AJSW014
         CVB   R5,TVTWORKD        *MAKE BINARY                  AJSW014
         ST    R5,GDPRTY          *AND SAVE FOR TRACE           AJSW014
         MVC   GDCOM(GTRACEL),GTRACE *MOVE LIST FORM OF GTRACE  AJSW014
         LA    R5,GDCOM           *AND POINT TO IT              AJSW014
         LA    R6,GDXTM           *POINT TO TRACE DATA          AJSW014
        GTRACE MF=(E,(R5)),ID=3,DATA=(R6),PAGEIN=YES            AJSW014
         SPACE 1                                                AJSW014
GDEOJ7   MVC   GDCOM,GDMSG        *CHANGE PRIORITY              AJSW014
         MVC   GDCOM+6(4),IDDJOBNO * ..                         AJSW014
         MVC   GDCOM+13(2),0(R8)   * ..                         AJSW014
         INTERCOM CONS=DUMMY,TEXT=GDCOM,MSG=NO                  AJSW014
GDEOJ7P  LM    R2,R9,GDSAVE       *RETURN                       AJSW014
         B     GDEOJ+4            *  ..                         AJSW014
GDEOJLU  CLC   GDTBLND,0(R6)      *PRIORITY TABLE LOOKUP        AJSW014
         BER   R7                 *  ..                         AJSW014
         C     R5,0(R6)           *  ..                         AJSW014
         BNHR  R7                 *  ..                         AJSW014
         A     R8,4(R6)           *  ..                         AJSW014
         LA    R6,8(R6)           *  ..                         AJSW014
         C     R8,GDPRTYND        *  ..                         AJSW014
         BL    GDEOJLU            *  ..                         AJSW014
         L     R8,GDPRTYND        *  ..                         AJSW014
         BR    R7                 *  ..                         AJSW014
         DROP  R2                                               AJSW014
         EJECT                                                  AJSW014
*************************************************************** AJSW014
*              WORK AREAS AND CONSTANTS                       * AJSW014
*************************************************************** AJSW014
JMRCLAS  EQU   X'3B'              *JOB CLASS                    AJSW014
JCTSCHCL EQU   X'C2'              *JOB NUMBER IN BINARY         AJSW014
SMF26CLS EQU   X'D5'              *JOB CLASS                    AJSW014
SMF26DEV EQU   X'DC'              *JOB ORIGIN                   AJSW014
SMF26XTM EQU   X'EC'              *ESTIMATED EXECUTION TIME     AJSW014
SMF26ELN EQU   X'F0'              *ESTIMATED PRINT LINES        AJSW014
SMF26CLN EQU   X'12C'             *CLASS NAME                   AJSW014
GDXTM    EQU   IDDUSAVE+0,4       *ESTIMATED EXECUTION TIME     AJSW014
GDRGN    EQU   IDDUSAVE+4,4       *REGION REQUEST               AJSW014
GDLNE    EQU   IDDUSAVE+8,4       *ESTIMATED NUMBER OF LINES    AJSW014
GDTPE    EQU   IDDUSAVE+12,4      *NUMBER OF TAPE REQUEST'S     AJSW014
GDDSK    EQU   IDDUSAVE+16,4      *NUMBER OF NEW DISK REQ'S     AJSW014
GDPRTY   EQU   IDDUSAVE+20,4      *COMPUTED PRIORITY            AJSW014
GDSAVE   EQU   IDDUSAVE+24,32     *REGISTER 2-9 SAVE AREA       AJSW014
GDCOM    EQU   IDDUSAVE+56,16     *INTERCOM MSG WORK AREA       AJSW014
         SPACE 1                                                AJSW014
GTRACE  GTRACE MF=L,LNG=24,FID=3  *LIST FORM OF GTRACE          AJSW014
GTRACEL  EQU   *-GTRACE           *LENGTH OF GTRACE DATA LIST   AJSW014
         SPACE 1                                                AJSW014
GDDEFTME DC    A(5*60*100)        *5 MINUTE TIME DEFAULT        AJSW014
GDDEFRGN DC    A(192)             *192K REGION DEFAULT          AJSW014
GD100    DC    A(100)             *100 CONSTANT                 AJSW014
GDXTMT   DC    A(300,20,600,20,900,20)   *EST.XTM TABLE         AJSW014
         DC    A(1800,20,2400,20,3600,20) * ..                  AJSW014
         DC    C'END-'                    * ..                  AJSW014
         DC    A(0,0,0,0,0,0)             * ..                  AJSW014
GDRGNT   DC    A(192,20,512,20,1024,20)  *REGION REQ TABLE      AJSW014
         DC    C'END-'                   *  ..                  AJSW014
         DC    A(0,0,0,0,0,0)            *  ..                  AJSW014
GDLNET   DC    A(10000,2,50000,2,100000,2) *PRINT LINES TABLE   AJSW014
         DC    C'END-'                   *  ..                  AJSW014
         DC    A(0,0,0,0,0,0)            *  ..                  AJSW014
GDTPET   DC    A(0,2,2,2,4,2)            *TAPE COUNT TABLE      AJSW014
         DC    C'END-'                   *  ..                  AJSW014
         DC    A(0,0,0,0,0,0)            *  ..                  AJSW014
GDDSKT   DC    A(0,2,2,2,4,2)            *NEW DISK CNT TABLE    AJSW014
         DC    C'END-'                   *  ..                  AJSW014
         DC    A(0,0,0,0,0,0)            *  ..                  AJSW014
GDTBLND  DC    C'END-'                   *END OF TABLE FLAG     AJSW014
GDPRTYT  DC    CL20'09090908080807070706' *PRIORITY TABLE       AJSW014
         DC    CL20'08080807070706060605' * ..                  AJSW014
         DC    CL20'07070706060605050504' * ..                  AJSW014
         DC    CL20'06060605050504040403' * ..                  AJSW014
         DC    CL20'05050504040403030302' * ..                  AJSW014
         DC    CL20'04040403030302020201' * ..                  AJSW014
         DC    CL20'03030302020201010101' * ..                  AJSW014
         DC    CL20'02020201010101010101' * ..                  AJSW014
         DC    CL20'01010101010101010101' * ..                  AJSW014
         DC    CL20'01010101010101010101' * ..                  AJSW014
GDPRTYND DC    A(198)             *MAXIMUM TABLE INCREMENT      AJSW014
GDTAPE   DC    C'TAPE'                                          AJSW014
GD3400   DC    C'3400'                                          AJSW014
GDDISK   DC    C'DISK'                                          AJSW014
GD2305   DC    C'2305'                                          AJSW014
GDVIO    DC    C'VIODISK'                                       AJSW014
GDSYS    DC    C'SYS'             *SYSDA,SYSTSO,ETC.....        AJSW014
GD33     DC    C'33'              *3330,3340,3350               AJSW014
GD14     DC    C'14'              *PRIORITY 14                  AJSW014
GD11     DC    C'11'              *PRIORITY 11                  AJSW014
GD09     DC    C'09'              *PRIORITY 9                   AJSW014
GD07     DC    C'07'              *PRIORITY 7                   AJSW014
GD05     DC    C'05'              *PRIORITY 5                   AJSW014
GDTP     DC    CL8'TP'            *CLASS=TP                     AJSW014
GDPROD   DC    CL8'PROD'          *CLASS=PROD                   AJSW014
GDBMP    DC    CL8'BMP'           *CLASS=BMP'                   AJSW014
GDSDE    DC    CL8'SDEST'         *CLASS=SDEST                  AJSW014
GDPDE    DC    CL8'PDEST'         *CLASS=PDEST                  AJSW014
GDLOAD   DC    CL8'DATALOAD'      *CLASS=DATALOAD               AJSW014
GDURG    DC    CL8'URGENT'        *CLASS=URGENT                 AJSW014
GDEXP    DC    CL8'EXPRESS'       *CLASS=EXPRESS                AJSW014
GDONITE  DC    CL8'OVERNITE'       CLASS=OVERNITE               AJSW014
GDSTC    DC    CL8'STC'           *CLASS=STC                    AJSW014
GDTSO    DC    CL8'TSO'           *CLASS=TSO                    AJSW014
GDDR     DC    CL8'DR'            *SMF26DEV=DR (DISK-RDR)       AJSW014
GDPC     DC    CL8'$PRDCTL'       *SMF26DEV=$PRDCTL (TSO)       AJSW014
GDINR    DC    CL8'INTRDR'        *SMF26DEV=INTRDR              AJSW014
GDMSG    DC    AL1(15)            *INTERCOM MESSAGE             AJSW014
         DC    CL15'*F,J=NNNN,P=NN'  ..                         AJSW014
         LTORG ,                                                AJSW014
UMODNUM  DC    CL7'AJSW014'       *USER NUMBER
