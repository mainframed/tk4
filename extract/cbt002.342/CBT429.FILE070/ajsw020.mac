++ USERMOD(AJSW020) /* ALLOW GENERIC STATUS COMMAND AND OTHER THINGS

       THIS MODIFICATION WILL ALLOW "GENERIC" STATUS COMMANDS:
       THE USER CAN ENTER ST .....* (* CAN BE @,#, OR $) AND
       JES3 WILL RESPOND AS IF AN 8I J/.....* HAD BEEN DONE.
       MOD WILL ALSO SEARCH FOR ALL JOBS SUBMITTED BY A USER
       (RATHER THAN JUST BEGINNING WITH USERID) AND WILL ALSO
       PREVENT CANCELLING ANY JOB NOT SUBMITTED BY THAT USERID.
                                                                 */ .
++VER (Z038) FMID(HJS2215) .
++SRCUPD(IATUX30) DISTLIB(AJES3SRC) .
./ CHANGE NAME=IATUX30
* USER MODS:                                                    AJSW020
*                                                               AJSW020
*      - ALLOW GENERIC STATUS AND CHECK JOB OWNERSHIP           AJSW020
*                                                               AJSW020
         IATYMCL ,                                              AJSW020
         EJECT                                                  AJSW020
*        LA    R15,TSJEFFOK       *THIS LINE COMMENTED BY       AJSW020
*        ARETURN                  *THIS LINE COMMENTED BY       AJSW020
         LR    R9,R1              *ESTABLISH PARM LIST BASE     AJSW020
         USING TSUXLST,R9                                       AJSW020
         LM    R5,R8,TSUXLST      *ESTABLISH PTRS. AND BASES    AJSW020
         USING SSCSBGN,R6                                       AJSW020
         LTR   R7,R7              *REQUEST SELECTION ENTRY?     AJSW020
         BC    NZERO,IATUX30P     *NO-JOB SELECTION             AJSW020
         SPACE 3                                                AJSW020
         NOP   IATUX301 ALLOW ALL STATUS, CANCEL, AND OUTPUT    AJSW020
         SPACE 3                                                AJSW020
         CLI   0(R5),MODSTS       *STATUS REQUEST?              AJSW020
         BNE   IATUX301      *NO, EXIT..ALLOW ALL CANCEL/OUTPUT AJSW020
         TM    SSCSFLGS,SSCSUSID  *USER ID IN JOBNAME FIELD?    AJSW020
         BO    IATUX301           *YES..EXIT                    AJSW020
         LA    R15,SSCSJOBN       *JOBNAME                      AJSW020
         LA    R14,L'SSCSJOBN-1   *LENGTH MINUS 1 FOR EX INST   AJSW020
SRCHNAME DS    0H                                               AJSW020
         EX    R14,TESTNAME       *ANY SPECIAL CHARACTERS       AJSW020
         BZ    IATUX301           *NO, EXIT IT IS GENERIC SRCH  AJSW020
         LA    R2,SSCSJOBN+7      *POINT TO LAST CHAR IN NAME   AJSW020
         CR    R1,R2              *CHAR IN LAST COL?            AJSW020
         BE    SETSTAR            *YES, MAKE IT A STAR          AJSW020
         CLI   1(R1),C' '         *IS NEXT CHAR A SPACE?        AJSW020
         BE    SETSTAR            *YES, MAKE IT A STAR          AJSW020
         LR    R0,R1              *SAVE ADDR OF SPECIAL CHAR    AJSW020
         SLR   R0,R15             *FIND POSITION IN JOBNAME     AJSW020
         LA    R15,1(R1)          *SETUP FOR NEXT PASS          AJSW020
         SR    R14,R0             *CALC CHARS LEFT TO SCAN      AJSW020
         BCTR  R14,0                                            AJSW020
         B     SRCHNAME           *TRY AGAIN                    AJSW020
TESTNAME TRT   0(1,R15),TRTABLE   *RESULT OF AN EXECUTE         AJSW020
SETSTAR  MVI   0(R1),C'*'         *SET FOR GENERIC SEARCH       AJSW020
         B     IATUX301           *NOW EXIT                     AJSW020
IATUX304 LA    R15,TSJESREJ       *NO-SET REJECT REQUEST        AJSW020
         B     IATUX302           *EXIT                         AJSW020
         USING SSSOBGN,R6                                       AJSW020
IATUX303 TM    SSSOFLG1,SSSOSJBI  *JOBID SPECIFIED?             AJSW020
         BC    ALLOFF,IATUX304    *NO-REJECT REQUEST            AJSW020
         USING SSCSBGN,R6                                       AJSW020
IATUX301 LA    R15,TSJESRTN       *SET JES EXIT AND REENTER     AJSW020
IATUX302 ARETURN                                                AJSW020
*        RESTRICT CANCEL TO CANCEL REQUESTS FOR               * AJSW020
IATUX30P CLI   0(R5),MODSTS       *STATUS REQUEST?              AJSW020
         BC    NE,IATUX30S        *NO-PROCESS CANCEL/OUTPUT     AJSW020
         TM    SSCSFLGS,SSCSUSID  *YES-ANY OPERANDS ON REQUEST? AJSW020
         BC    ALLON,IATUX30R     *NO-GO AUTHORIZE USER         AJSW020
IATUX30Q LA    R15,TSJOBOK        *YES-IND PROCESS THIS JOB     AJSW020
         B     IATUX302           *EXIT                         AJSW020
         USING JQESTART,R7                             @ZA27008 AJSW020
IATUX30R CLC   JQETUSID,0(R8)     *REQUESTOR=OWNER?    @ZA27008 AJSW020
         BC    NE,IATUX3OR        *NO THEN REJECT THE JOB       AJSW020
         SR    R2,R2              *CLEAR REG                    AJSW020
         IC    R2,JQECLS          *GET JOB CLASS                AJSW020
         LTR   R2,R2              *IS THERE ONE THERE           AJSW020
         BZ    IATUX30Q           *NO - ASSUME IT IS A JOB      AJSW020
         BCTR  R2,0               *MAKE AN OFFSET               AJSW020
         L     R3,MCLASS          *GET CLASS TABLE ADDRESS      AJSW020
         USING MCNAME,R3          *ESTABLISH ADDRESSABILITY     AJSW020
         MH    R2,MCLNG           *POINT INTO MCLASS ARRAY      AJSW020
         LA    R3,0(R3,R2)        *POINT TO CLASS ENTRY         AJSW020
         CLC   MCNAME,=CL8'TSO'   *IS THIS ENTRY TSO ?          AJSW020
         BC    NE,IATUX30Q        *NO  - THEN IT IS A JOB       AJSW020
         DROP  R3                                               AJSW020
IATUX3OR DS    0H                                               AJSW020
         LA    R15,TSJOBREJ       *NO-SET REJECT-CONTINUE SCAN  AJSW020
         B     IATUX302           *EXIT                         AJSW020
         USING JCTSTART,R7                             @ZA27008 AJSW020
IATUX30S CLI   0(R5),MODOTS       *OUTPUT REQUEST?              AJSW020
         BC    EQ,IATUX30U        *YES-GO PROCESS OUTPUT        AJSW020
         CLC   JCTTUSID,0(R8)     *NO-REQUESTOR=OWNER?          AJSW020
         BC    NE,IATUX30T        *YES..REJECT                  AJSW020
         SLR   R2,R2              *CLEAR R2                     AJSW020
         IC    R2,CANCLMSG        *LENGTH OF MSG                AJSW020
         LA    R2,7(R2)           *ALLOW FOR LIST FORM OF MSG   AJSW020
GETMAIN  AGETMAIN SIZE=(R2),LOC=ANY BUSY=BUSY *GET STORAGE      AJSW020
*USY     AWAIT TYPE=ON,ECFMASK=FCTAGET,ECFADD=FCTFLAG1          AJSW020
*        B     GETMAIN            *TRY AGAIN                    AJSW020
GOT      SH    R2,=H'7'           *GET LENGTH OF MSG ONLY       AJSW020
         EX    R2,UX30MMSG        *MOVE MSG TO TEMP AREA        AJSW020
         MVC   37(8,R1),JCTJBNAM  *JOB NAME TO BE CANCELED      AJSW020
         MVC   46(8,R1),SSCSJOBI  *JOB ID OR BLANKS             AJSW020
         MVC   16(7,R1),0(R8)     *TSO USER ID                  AJSW020
         LA    R0,6(R1)           *ADDR OF MSG                  AJSW020
         ST    R0,0(R1)           *SAVE IN LIST FORM OF MSG     AJSW020
         LR    R3,R1              *SAVE ADDR OF GETMAIN AREA    AJSW020
         MESSAGE CLASS=MLG,MF=(E,(R1)) *WRITE MSG TO MLG        AJSW020
         LA    R0,7(R2)           *GET LENGTH OF GETMAIN AREA   AJSW020
         APUTMAIN SIZE=(R0),AREA=(R3) *RELEASE STORAGE          AJSW020
         B     IATUX30Q           *NOW CANCEL THE JOB           AJSW020
UX30MMSG MVC   6(1,R1),CANCLMSG   *MOVE MSG                     AJSW020
IATUX30T LA    R15,TSREQREJ       *NO-INDICATE REQUEST REJECT   AJSW020
         B     IATUX302           *EXIT                         AJSW020
         USING RQSTART,R7                                       AJSW020
IATUX30U B     IATUX30Q                                         AJSW020
         SPACE 3                                                AJSW020
CANCLMSG DC     AL1(CANCLEND-CANCLMSG)                          AJSW020
         DC     C'TSO USER XXXXXXX CANCELED JOB XXXXXXXX(XXXXXXXX)......
               .............................'                   AJSW020
CANCLEND EQU   *-1                                              AJSW020
         SPACE 3                                                AJSW020
TRTABLE  DC   256X'00'                                          AJSW020
         ORG  TRTABLE+C'@'                                      AJSW020
         DC   C'@'                                              AJSW020
         ORG  TRTABLE+C'#'                                      AJSW020
         DC   C'#'                                              AJSW020
         ORG  TRTABLE+C'$'                                      AJSW020
         DC   C'$'                                              AJSW020
         ORG  ,                                                 AJSW020
         SPACE 3                                                AJSW020
UMODNUM  DC   CL7'AJSW020'        *USER NUMBER
