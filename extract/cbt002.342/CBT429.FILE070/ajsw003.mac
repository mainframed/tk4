++ USERMOD(AJSW003) /*

    THIS MODIFICATION TO IATUX28 CONTAINS THE FOLLOWING:
    1.  ACCT VALIDATION CODE FOR BATCH JOBS AND TSO.
    2.  CHANGE PROCID FOR STC.
    3.  ADD ACF2 SUPPORT CODE.
    4.  CAUSE DEFAULT OUTPUT LIMIT FOR STARTED TASKS AND TSO USERS TO
        BE EQUAL TO THE SYSTEM OUTLIM DEFAULT (THIS IS USUALLY QUITE
        HIGH - 2 MILLION LINES AT WC - AND IS SET HIGH SIMPLY BECAUSE
        IT IS NOT POSSIBLE TO SUPPLY A //*MAIN CARD FOR STARTED TASKS
        AND TSO USERS).

        ******* THIS IS PRE FOR AJSW008 ACCT CODE *******
                                                                 */ .
++ VER(Z038) FMID(HJS2215) PRE(AJSW013).
++ SRCUPD(IATUX28) .
./ CHANGE NAME=IATUX28
* USER MODS:                                                    AJSW003
*                                                               AJSW003
*      - ACCT VALIDATION CODE                                   AJSW003
*      - CHANGE PROCID FOR STC                                  AJSW003
*      - ADD ACF2 CODE                                          AJSW003
*      - SET OUTPUT LIMIT FOR STC AND TSO TO SYSTEM OUTLIM      AJSW003
*                                                               AJSW003
         IATYJDA ,                                              AJSW003
         EJECT                                                  AJSW003
         IATYJMR ,                                              AJSW003
         EJECT                                                  AJSW003
         IATYSVT ,                                              AJSW003
         EJECT                                                  AJSW003
         IATYRSQ ,                                              AJSW003
         EJECT                                                  AJSW003
*-------------------------------------------------------------* AJSW003
*            USRAREA DSECT FOR USER RELATED DATA              * AJSW003
*-------------------------------------------------------------* AJSW003
USRAREA  DSECT                                                  AJSW003
USRLGTH  DS    F                  *LENGTH OF ACCT NUMBER        AJSW003
USRACCT  DS    CL15               *USER AREA FOR ACCT NUMBER    AJSW003
USRERR   DC    F'0'               *USER ERROR FIELD             AJSW003
USRRPL   DS    CL76               *USER RPL                     AJSW003
USRGENA  DS    CL(USRGENL)        *USER GENCB WORK AREA         AJSW003
USRREC   DS    CL64                USER INPUT RECORD            AJSW003
USRMSG   DS    CL70                USER OUTPUT MESSAGE AREA     AJSW003
USRSHWA  DS    CL(USRSHWL)         USER SHOWCB WORK AREA        AJSW003
USRSHW   DS    F                   USER SHOW AREA FDBK FIELD    AJSW003
USRSAVE  DS    18F                 USER SAVE AREA FOR OS CALLS  AJSW003
TSOFLAG  DS    CL1                 TSO FLAG                     AJSW003
USRBSY   DC    XL1'00'             X'FF' IN USE, X'00' FREE     AJSW003
USROPN   DC    F'0'                STORAGE FOR OPEN LIST        AJSW003
ACCTACB  ACB   DDNAME=ACCTFILE,MACRF=(KEY,DIR,IN,AMODE31)       AJSW003
USREND   DC    0D'0'               ALIGNMENT TO DWD BNDRY       AJSW003
USRLEN   EQU   USREND-USRAREA      LENGTH OF USER DSECT         AJSW003
         EJECT                                                  AJSW003
         IATXCUE EP=ACFJBINT,ALOAD=YES                          AJSW003
         SR    R15,R15            *SET ACF2 RETURN CODE TO 0    AJSW003
         SPACE 1                                                AJSW003
*                                                               AJSW003
         ICM   R9,B'1111',ISDWORK  GET -> UX28 WORK SPACE       AJSW003
         BC    NZERO,GOTWORK       WORK SPACE ALREADY ACQUIRED  AJSW003
         LA    R0,USRLEN          *GET LENGTH FOR GETMAIN       AJSW003
GETSTOR  DS    0H                                               AJSW003
         AGETMAIN SIZE=(R0),       GET WORKAREA STORAGE         AJSW003*
               LOC=(BELOW,ANY)     BELOW THE LINE.              AJSW003
* NOTE: WITH 2.1.5, ECF & MASK NO LONGER RETURNED, IF NO BUSY=  AJSW003
WAITSTOR DS    0H                  SERVICE RTN AWAITS.  IF      AJSW003
*        AWAIT TYPE=ON,            LOCAL WAIT DESIRED, UNCOMMNT AJSW003
*              ECFMASK=FCTAGET     MACRO, ADD BUSY= PARM TO     AJSW003
*              ECFADD=FCTFLAG1     GETMAIN.                     AJSW003
GOTSTOR  DS    0H                                               AJSW003
         LR    R9,R1                                            AJSW003
         LA    R1,USRLEN           SIZE OF STORAGE OBTAINED     AJSW003
         XR    R15,R15             CLEAR DEST LENGTH SET PAD    AJSW003
         MVCL  R0,R14              CLEAR ACQUIRED STORAGE       AJSW003
         ST    R9,ISDWORK          -> WORKSPACE IN DRVR DATA    AJSW003
         USING USRAREA,R9          ESTABLISH ADDRESSABILITY     AJSW003
         MVC   ACCTACB(ACBLEN),ACCTACBP COPY ACB TO WORK AREA   AJSW003
         MVI   USROPN,X'80'        SET END OF LIST FLAG         AJSW003
GOTWORK  DC    0H'0'                                            AJSW003
         TM    USRBSY,XFF          WORK SPACE IN USE?           AJSW003
         BZ    HOLDWORK            NO, FLAG AS IN USE           AJSW003
         AWAIT TYPE=OFF,ECFMASK=X'FF',ECFADD=USRBSY             AJSW003
HOLDWORK DC    0H'0'                                            AJSW003
         MVI   USRBSY,XFF          SET WORK AREA BUSY FLAG      AJSW003
         L     R4,LOCADDR          GET JOB CARD POINTER         AJSW003
         SPACE 1                                                AJSW003
NOLJOB   DS    0H                                               AJSW003
         TM    TVTUFLG1,TVTUCLOS  *SHOULD WE STOP VALIDATION?   AJSW003
         BC    ALLON,STOPIT       *YES, CLOSE FILE AND END      AJSW003
         CLC   2(8,R4),=C'ACCTSTOP' *LOOK FOR STOP JOB?         AJSW003
         BC    NE,NOSJOB          *NO, IS NOT ACCT STOP JOB     AJSW003
         CLC   62(8,R4),=C'*/$-STOP' *CHECK VALIDITY?           AJSW003
         BC    NE,NOSJOB          *NO, IS NOT ACCT STOP JOB     AJSW003
STOPIT   DS    0H                                               AJSW003
         MVI   TVTUFLG1,TVTUSTOP  *SET STOP SWITCH              AJSW003
         TM    ACCTACB+48,X'10'   *IS FILE OPEN?                AJSW003
         BC    ALLOFF,STFREE      *NO, DONT NEED TO CLOSE IT    AJSW003
         IATXCSF ENTER=CLOSEACB,  *GO CLOSE ACB                 AJSW003X
               ID=152                                           AJSW003
         LTR   R15,R15            *CLOSE WORK OK?               AJSW003
         BC    NZERO,CLOSFAIL     *NO, PROCESS ERROR            AJSW003
         MESSAGE TEXT=CLOSOK,     *WRITE OUT MESSAGE            AJSW003X
               CLASS=S2                                         AJSW003
STFREE   DC    0H'0'                                            AJSW003
         LTR   R1,R9               HAVE ACQUIRED WORK SPACE?    AJSW003
         BC    ZERO,STRETURN       NO, JUST RETURN              AJSW003
         LA    R0,USRLEN           YES, SET LENGTH TO FREE      AJSW003
         APUTMAIN SIZE=(R0),       FREE WORK SPACE              AJSW003X
               AREA=(R1)                                        AJSW003
         XC    ISDWORK,ISDWORK     CLEAR -> WORK SPACE          AJSW003
         B     STRETURN            ALL DONE                     AJSW003
         SPACE 1                                                AJSW003
NOSJOB   DS    0H                                               AJSW003
         CLC   2(8,R4),=C'ACCTSTRT' *IS THIS ACCT START JOB?    AJSW003
         BC    NE,CKOPEN          *NO, IS NOT START JOB         AJSW003
         CLC   62(8,R4),=C'*/$-STRT' *CHECK VALIDITY?           AJSW003
         BC    NE,CKOPEN          *NO, IS NOT START JOB         AJSW003
         MVI   TVTUFLG1,TVTUOPEN  *TURN ON OPEN SWITCH          AJSW003
         B     OPEN               *GO DO OPEN                   AJSW003
         SPACE 1                                                AJSW003
CKOPEN   DS    0H                                               AJSW003
         TM    TVTUFLG1,TVTUSTRT  *SHOULD VALIDATION STARTUP?   AJSW003
         BC    ALLON,OPENIT       *YES, START IT UP             AJSW003
         TM    TVTUFLG1,TVTUSTOP  *ACCT VALIDATION STOPPED?     AJSW003
         BC    ALLON,STRETURN     *BRANCH IF YES                AJSW003
         TM    TVTUFLG1,TVTUOPEN  *IS THIS 1ST PASS AFTER IPL?  AJSW003
         BC    ALLON,NOOPEN       *NO, FILE OPEN                AJSW003
OPENIT   DS    0H                                               AJSW003
         MVI   TVTUFLG1,TVTUOPEN  *SET FILE TO OPEN STATUS      AJSW003
         SPACE 1                                                AJSW003
OPEN     DS    0H                                               AJSW003
         TM    ACCTACB+48,X'10'   *IS ACB OPEN?                 AJSW003
         BC    ALLON,NOOPEN        YES, NO OPEN REQUIRED        AJSW003
         IATXCSF ENTER=OPENACB,    CALL MVS OPEN FOR ACB        AJSW003X
               ID=152                                           AJSW003
         LTR   R15,R15             OPEN SUCCESSFUL?             AJSW003
         BC    NZERO,OPENFAIL      NO, OPEN FAILED              AJSW003
         MVI   TVTUFLG1,TVTUOPEN   TURN ON OPEN SWITCH          AJSW003
         MESSAGE TEXT=OPENOK,      WRITE OUT MESSAGE            AJSW003X
               CLASS=S2                                         AJSW003
         GENCB BLK=RPL,            CONSTRUCT AN RPL             AJSW003X
               WAREA=(S,USRRPL),   IN THIS AREA OF STORAGE      AJSW003X
               LENGTH=76,          WHICH IS 76 BYTES LONG       AJSW003X
               ACB=(S,ACCTACB),    CONNECT RPL W/THIS ACB       AJSW003X
               AREA=(S,USRREC),    READ RECORDS INTO THIS AREA  AJSW003X
               RECLEN=64,          RECORDS ARE 64 BYTES LONG    AJSW003X
               AREALEN=64,         AND THE BUFFER IS 64 BYTES!  AJSW003X
               ARG=(S,USRACCT),    THE RETRIEVAL KEY WILL BE H  AJSW003X
               OPTCD=(KEY,DIR),    READ KSDS BY KEY, NONSEQ.    AJSW003X
               MF=(L,USRGENA,USRGENL) BUILD PARMLIST HERE       AJSW003
         IATXCSF ENTER=GENACB,     GO DO GENCB                  AJSW003X
               ID=152                                           AJSW003
         LTR   R15,R15             GENCB SUCCESSFUL?            AJSW003
         BC    NZERO,GENCBAD       NO, ISSUE ERROR MESSAGE      AJSW003
NOOPEN   DS    0H                                               AJSW003
         LA    R6,3(R4)            R6=CURRENT SEARCH POSITION   AJSW003
         LA    R7,61(R4)           R7=END SEARCH POSITION       AJSW003
         L     R4,JDABADDR         GET JDAB POINTER             AJSW003
         USING JDABSTRT,R4         SET UP BASE REG              AJSW003
         TM    ISFLAG9,ISDSEL      IS THIS A DEMAND SELECT JOB? AJSW003
         BC    ALLOFF,JOBSCAN      NO, GO VALIDATE IT           AJSW003
         CLC   8(13,R6),JOBINFO    IS IT A STC JOB?             AJSW003
         BC    EQ,STCORMNT         YES, GO PROCESS STC JOB      AJSW003
         MVI   TSOFLAG,TSO         MUST BE TSO JOB              AJSW003
JOBSCAN  DS    0H                                               AJSW003
         CLC   =C' JOB ',0(R6)     LOOKING FOR ' JOB '          AJSW003
         BC    EQ,JOBFOUND         YES, GO PROCESS IT           AJSW003
         CLC   =C' NJB ',0(R6)     LOOKING FOR ' NJB '          AJSW003
         BC    NE,NEXTCHAR         NO, LOOK AT NEXT ONE         AJSW003
         B     JOBFOUND            WE MAY NOT EVER GET HERE     AJSW003
         SPACE 2                                                AJSW003
MOVESMF  MVC   2(0,R1),0(R2)       MOVE IN ACCTNG DATA          AJSW003
NEXTCHAR DS    0H                                               AJSW003
         CR    R6,R7               POSSIBILITY OF JOB EXCEEDED? AJSW003
         BC    GT,RETURN           YES, IGNORE IT               AJSW003
         LA    R6,1(R6)            TRY NEXT POSITION            AJSW003
         B     JOBSCAN             TRY AGAIN                    AJSW003
JOBFOUND DS    0H                                               AJSW003
         LA    R6,5(R6)            SKIP ' JOB '                 AJSW003
JCLSCAN  DS    0H                                               AJSW003
         CLI   0(R6),X'4D'         OPEN PAREN?                  AJSW003
         BC    EQ,ACCTNEXT         YES, ACCT SHOULD BE NEXT     AJSW003
         CLI   0(R6),X'7D'         QUOTE?                       AJSW003
         BC    EQ,ACCTNEXT         YES, ACCT SHOULD BE NEXT     AJSW003
         CLI   0(R6),X'40'         BLANK?                       AJSW003
         BC    NE,ACCTDSD          NO, MUST BE ACCT NUMBER      AJSW003
         LA    R6,1(R6)            YES, LET'S TRY NEXT POSITION AJSW003
         CR    R6,R7               POSSIBILITY ACCT EXCEEDED?   AJSW003
         BC    GT,ACCTBAD          YES, GO TO BAD ACCT MSG      AJSW003
         B     JCLSCAN             TRY AGAIN                    AJSW003
ACCTNEXT DS    0H                                               AJSW003
         LA    R6,1(R6)            POINT AT ACCT                AJSW003
ACCTDSD  DS    0H                                               AJSW003
         LR    R5,R6               GET POINTER TO ACCT          AJSW003
         SR    R8,R8               USE R8 AS COUNTER            AJSW003
         LA    R7,12(R6)           END OF ACCT NUMBER           AJSW003
ACCTSCAN DS    0H                                               AJSW003
         CLI   1(R6),C','          COMMA FOLLOWS ACCT?          AJSW003
         BC    EQ,READ             YES, END OF ACCT NMBR        AJSW003
         CLI   1(R6),X'5D'         CLOSE PAREN FOLLOWS ACCT?    AJSW003
         BC    EQ,READ             YES, END OF ACCT NMBER       AJSW003
         CLI   1(R6),X'7D'         QUOTE FOLLOWS ACCT?          AJSW003
         BC    EQ,READ             YES, END OF ACCT NMBR        AJSW003
         CLI   1(R6),X'40'         BLANK FOLLOWS ACCT?          AJSW003
         BC    EQ,READ             YES, END OF ACCT NMBR        AJSW003
         CR    R6,R7               ARE WE AT END?               AJSW003
         BC    EQ,READ             YES, END OF ACCT NMBR        AJSW003
         LA    R6,1(R6)            GO TO NEXT POSITION          AJSW003
         LA    R8,1(R8)            BUMP COUNTER BY 1            AJSW003
         B     ACCTSCAN            KEEP LOOKING                 AJSW003
READ     DS    0H                                               AJSW003
         LA    R2,USRACCT          POINT AT ACCT NUMBER         AJSW003
         MVC   USRACCT,BLANKS      ZERO OUT ACCT NUMBER         AJSW003
         EX    R8,MOVEACCT         SAVE ACCT NUMBER             AJSW003
         LA    R8,1(R8)            BUMP TO GET TRUE LENGTH      AJSW003
         ST    R8,USRLGTH          SAVE IT                      AJSW003
*                                                               AJSW003
*                                                               AJSW003
         CLI   0(R2),C'A'          START WITH 'A' FOR CC?       AJSW003
         BC    NE,DOGENCB          NO, PROCESS AS NORMAL        AJSW003
         C     R8,=F'6'            IS ACCT 6 BYTES LONG?        AJSW003
         BC    EQ,GOODREAD         YES, MUST BE CC ACCOUNT      AJSW003
DOGENCB  DS    0H                                               AJSW003
         IATXCSF ENTER=READACB,    GO DO VSAM READ              AJSW003X
               ID=152                                           AJSW003
         LTR   R15,R15             READ SUCCESSFUL?             AJSW003
         BC    NZERO,READBAD       NO, ISSUE ERROR MESSAGE      AJSW003
GOODREAD DS    0H                                               AJSW003
         BAL   R14,SMF26           GET ADDR OF SMF26JAF         AJSW003
         B     RETURN              ACTUAL SECT DOES NOT EXIST   AJSW003
         MVI   0(R1),X'01'         SPECIFY ONE ACCT FIELD       AJSW003
         L     R15,USRLGTH         GET LENGTH OF ACCT NUMBER    AJSW003
         STC   R15,1(R1)           MOVE IN LENGTH OF ACCT FLD   AJSW003
         BCTR  R15,R0              SUBTRACT ONE FOR EXECUTE     AJSW003
         EX    R15,MOVESMF         MOVE TO SMF RECORD           AJSW003
         LA    R15,0               SET RETURN CODE TO 0 - AND   AJSW003
         ST    R15,USRERR          ALLOW EXECUTION              AJSW003
         B     RETURN              ALL DONE                     AJSW003
MOVEACCT MVC   USRACCT(0),0(R5)    MOVE IN ACCTNG DATA          AJSW003
ACCTBAD  DS    0H                                               AJSW003
         BAL   R14,SMF26           GET ADDR OF SMF26JAF         AJSW003
         B     MAINCODE            NO ACT SECT RET POINT        AJSW003
         MVC   0(24,R1),BADACCT    MOVE IN BAD ACCT MSG         AJSW003
         B     MAINCODE            GET BACK TO MAIN LOGIC       AJSW003
BADACCT  DC    X'01',X'09',C'WC05N0007'                    @JBD AJSW003
MAINCODE DS    0H                  RESUME POINT                 AJSW003
         TM    TSOFLAG,TSO         IS THIS A TSO JOB?           AJSW003
         BC    ALLON,SETTSO        YES, GO CANCEL IN IATUX04    AJSW003
         LA    R15,4               SET RETURN CODE TO 4 AND...  AJSW003
         ST    R15,USRERR          ..REJECT THE JOB             AJSW003
         MVC   USRMSG(70),MSG3     MOVE IN MESSAGE              AJSW003
         MVC   USRMSG+16(8),JDABJNAM  MOVE IN JOB NAME          AJSW003
         MVC   USRMSG+25(4),JDABJNUM  MOVE IN JOB NUMBER        AJSW003
         BAL   R7,USERMSG          GO PRINT MESSAGE             AJSW003
         LA    R1,USRMSG           POINT TO MESSAGE             AJSW003
         MESSAGE TEXT=(R1),        WRITE IT TO OPER             AJSW003X
               CLASS=S2                                         AJSW003
         B     RETURN              ALL DONE                     AJSW003
SETTSO   DS    0H                                               AJSW003
         MVI   JDABUFLG,JDABFTSO   SET TO CANCEL JOB LATER      AJSW003
         LA    R15,0               SET RETURN CODE TO ZERO      AJSW003
         STC   R15,USRERR          SAVE RETURN CODE             AJSW003
         B     RETURN              ALL DONE                     AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*  SMF26 - POINT AT ACCOUNTING FIELDS IN TYPE 26 SMF RECORD   * AJSW003
*-------------------------------------------------------------* AJSW003
SMF26    DS    0H                                               AJSW003
         L     R1,JMRADDR          GET JMR ADDR                 AJSW003
         USING JMR,R1              ADDRESSABILITY               AJSW003
         TM    SMF26IND,X'20'      ACTUAL SECTION EXIST?        AJSW003
         BC    ALLOFF,R14          NO, RETURN TO CALLER         AJSW003
         TM    SMF26IND,X'C0'      DOES DESCR OR EVENT EXIST?   AJSW003
         BC    ALLOFF,SMFACT       NEITHER, JUST ACTUAL         AJSW003
         BC    MIXED,SMFONE        JUST ONE OF THEM EXISTS      AJSW003
         AH    R1,SMF26LN1         ALLOW FOR DESCR SECT         AJSW003
SMFONE   DS    0H                                               AJSW003
         AH    R1,SMF26LN1         ALLOW FOR EVENT (OR DESCR)   AJSW003
SMFACT   DS    0H                                               AJSW003
         LA    R1,SMF26JAF         POINT TO JOB ACCTNG FLDS     AJSW003
         B     4(R14)              NORMAL RETURN                AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*           OPENACB - OPEN ACB FOR ACCT NUMBER FILE           * AJSW003
*-------------------------------------------------------------* AJSW003
OPENACB  DS    0H                                               AJSW003
         LR    R6,R14              SAVE R14                     AJSW003
         OPEN  ACCTACB,MF=(E,USROPN) OPEN ACB                   AJSW003
         LR    R14,R6              RESET R14                    AJSW003
         BR    R14                 RETURN TO CALLER             AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*          CLOSEACB - CLOSE ACB FOR ACCT NUMBER FILE          * AJSW003
*-------------------------------------------------------------* AJSW003
CLOSEACB DS    0H                                               AJSW003
         LR    R6,R14              SAVE R14                     AJSW003
         CLOSE ACCTACB,MF=(E,USROPN) CLOSE ACB                  AJSW003
         LR    R14,R6              RESET R14                    AJSW003
         BR    R14                 RETURN TO CALLER             AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*    READACB - EXECUTE OS GET FOR VSAM ACCT NUMBER RECORD     * AJSW003
*-------------------------------------------------------------* AJSW003
READACB  DS    0H                                               AJSW003
         LR    R6,R14              SAVE R14                     AJSW003
         LR    R8,R13              SAVE R13                     AJSW003
         LA    R13,USRSAVE         POINT AT USER SAVE AREA      AJSW003
         GET   RPL=USRRPL          EXECUTE VSAM GET             AJSW003
         LR    R14,R6              RESTORE R14                  AJSW003
         LR    R13,R8              RESTORE R13                  AJSW003
         BR    R14                 RETURN TO CALLER             AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*             OPENFAIL - OPEN FAILED FOR ACB                  * AJSW003
*-------------------------------------------------------------* AJSW003
OPENFAIL DS    0H                                               AJSW003
         MESSAGE TEXT=MSG1,        ISSUE FAIL                   AJSW003X
               CLASS=S2            MESSAGE                      AJSW003
         MVI   TVTUFLG1,TVTUSTOP   STOP VALIDATION              AJSW003
         LA    R15,0               SET RETURN CODE TO 0         AJSW003
         ST    R15,USRERR          SAVE RETURN CODE             AJSW003
         TM    ISFLAG9,ISDSEL      IS IT DEMAND SELECT JOB?     AJSW003
         BC    ALLON,STCORMNT      YES, GO DO STC PROCESSING    AJSW003
         B     RETURN              ALL DONE                     AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*            CLOSFAIL - CLOSE FAILED FOR ACB                  * AJSW003
*-------------------------------------------------------------* AJSW003
CLOSFAIL DS    0H                                               AJSW003
         MESSAGE TEXT=MSG9,        ISSUE FAIL                   AJSW003X
               CLASS=S2            MESSAGE                      AJSW003
         MVI   TVTUFLG1,TVTUSTOP   STOP VALIDATION              AJSW003
         LA    R15,0               SET RETURN CODE TO 0         AJSW003
         ST    R15,USRERR          SAVE RETURN CODE             AJSW003
         B     RETURN              ALL DONE                     AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*  SHOWCB - EXECUTE SHOWCB FOR USER RPL AFTER READ ERROR      * AJSW003
*-------------------------------------------------------------* AJSW003
SHOWCB   DS    0H                                               AJSW003
         LR    R6,R14              SAVE R14                     AJSW003
         LR    R8,R13              SAVE R13                     AJSW003
         LA    R13,USRSAVE         POINT AT SAVE AREA           AJSW003
         SHOWCB AREA=(R0),         EXECUTE                      AJSW003X
               MF=(E,USRSHWA)      SHOWCB                       AJSW003
         LR    R14,R6              RESTORE R14                  AJSW003
         LR    R13,R8              RESTORE R13                  AJSW003
         BR    R14                 RETURN TO CALLER             AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*            GENACB - EXECUTE GENCB FOR USER RPL              * AJSW003
*-------------------------------------------------------------* AJSW003
GENACB   DS    0H                                               AJSW003
         LR    R6,R14              SAVE R14                     AJSW003
         LR    R8,R13              SAVE R13                     AJSW003
         LA    R13,USRSAVE         POINT AT SAVE AREA           AJSW003
         GENCB BLK=RPL,            EXECUTE                      AJSW003X
               MF=(E,USRGENA)      GENCB                        AJSW003
         LR    R14,R6              RESTORE R14                  AJSW003
         LR    R13,R8              RESTORE R13                  AJSW003
         BR    R14                 RETURN TO CALLER             AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*             GENCBAD - GENCB FAILED FOR USER                 * AJSW003
*-------------------------------------------------------------* AJSW003
GENCBAD  DS    0H                                               AJSW003
         MVC   USRMSG(70),MSG2     GET ERROR MESSAGE            AJSW003
         MVC   USRMSG+16(8),JDABJNAM  MOVE IN JOB NAME          AJSW003
         MVC   USRMSG+25(4),JDABJNUM  MOVE IN JOB NUMBER        AJSW003
         BAL   R7,USERMSG          BRANCH TO WRITE ROUTINE      AJSW003
         LA    R1,USRMSG           POINT TO USER MESSAGE        AJSW003
         MESSAGE TEXT=(R1),        INFORM USR OF GENCB FAILURE  AJSW003X
               CLASS=S2                                         AJSW003
         LA    R15,0               SET RETURN CODE TO 0 - AND   AJSW003
         ST    R15,USRERR          ALLOW EXECUTION              AJSW003
         B     GOODREAD            PROCESS LIKE ITS OK          AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*             READBAD - READ FAILED FOR USER                  * AJSW003
*-------------------------------------------------------------* AJSW003
READBAD  DS    0H                                               AJSW003
         C     R15,=F'08'          LOGICAL ERROR?               AJSW003
         BC    NE,READERR1         NO, KEEP PROCESSING ERROR    AJSW003
         CLI   USRRPL+15,X'10'     RECORD NOT FOUND?            AJSW003
         BC    EQ,ACCTBAD          YES, MEANS INVALID ACCT      AJSW003
READERR1 DS    0H                                               AJSW003
         LA    R6,USRSHW           POINT TO SHOWCB AREA         AJSW003
         LR    R2,R15              SAVE RETURN CODE             AJSW003
         SHOWCB RPL=(S,USRRPL),    SHOWCB LIST FORM             AJSW003X
               AREA=(R6),                                       AJSW003X
               LENGTH=4,                                        AJSW003X
               FIELDS=(FDBK),                                   AJSW003X
               MF=(L,USRSHWA,USRSHWL)                           AJSW003
         IATXCSF ENTER=SHOWCB,     EXECUTE FORM                 AJSW003X
               ID=152              OF SHOWCB                    AJSW003
         LTR   R15,R15             SHOWCB SUCCESSFUL?           AJSW003
         BC    NZERO,SHOWERR       NO, GOT REAL PROBLEMS        AJSW003
         LR    R15,R2              RELOAD ORIGINAL REG15        AJSW003
         C     R15,=F'04'          RPL ERROR?                   AJSW003
         BC    EQ,READERR4         YES, GO PROCESS ERROR        AJSW003
         C     R15,=F'12'          PHYSICAL ERROR?              AJSW003
         BC    EQ,READERRC         YES, GO PROCESS ERROR        AJSW003
         C     R15,=F'08'          LOGICAL ERROR?               AJSW003
         BC    EQ,READERR8         YES, GO PROCESS ERROR        AJSW003
         MVC   USRMSG(70),MSG5     UNKNOWN ERROR MESSAGE        AJSW003
         B     OUTMSG              GO PUT OUT MESSAGE           AJSW003
         SPACE 1                                                AJSW003
SHOWERR  DS    0H                                               AJSW003
         MVC   USRMSG(70),MSG4     SHOWCB ERROR MESSAGE         AJSW003
         B     OUTMSG              GO PUT OUT MESSAGE           AJSW003
         SPACE 1                                                AJSW003
READERR4 DS    0H                                               AJSW003
         MVC   USRMSG(70),MSG6     RPL ERROR MESSAGE            AJSW003
         B     OUTMSG              GO PUT OUT MESSAGE           AJSW003
         SPACE 1                                                AJSW003
READERRC DS    0H                                               AJSW003
         MVC   USRMSG(70),MSG7     PHYSICAL ERROR MESSAGE       AJSW003
         B     OUTMSG              GO PUT OUT MESSAGE           AJSW003
         SPACE 1                                                AJSW003
READERR8 DS    0H                                               AJSW003
         MVC   USRMSG(70),MSG8     LOGICAL ERROR MESSAGE        AJSW003
         B     OUTMSG              GO PUT OUT MESSAGE           AJSW003
         SPACE 1                                                AJSW003
OUTMSG   DS    0H                                               AJSW003
         MVC   USRMSG+16(8),JDABJNAM  MOVE IN JOB NAME          AJSW003
         MVC   USRMSG+25(4),JDABJNUM  MOVE IN JOB NUMBER        AJSW003
         BAL   R7,USERMSG          TELL USER ABOUT ERROR        AJSW003
         LA    R1,USRMSG           POINT AT ERROR MESSAGE       AJSW003
         MESSAGE TEXT=(R1),        INFORM OPERATOR OF ERROR     AJSW003X
               CLASS=S2                                         AJSW003
         LA    R15,0               SET RETURN CODE TO ALLOW     AJSW003
         ST    R15,USRERR          SAVE RETURN CODE             AJSW003
         B     GOODREAD            GO FINISH PROCESSING         AJSW003
         SPACE 1                                                AJSW003
*-------------------------------------------------------------* AJSW003
*  USERMSG - WRITE A MESSAGE TO THE USER'S JESMSG DATASET     * AJSW003
*-------------------------------------------------------------* AJSW003
USERMSG  DS    0H                                               AJSW003
         L     R1,MSGCURR          LOAD POINTER TO MESSAGE      AJSW003
         ALOCATE FDB=(R1),COUNT=70  AREA FOR NEW MESSAGE        AJSW003
         MVC   0(70,R1),USRMSG     MESSAGE TO ALOCATE AREA      AJSW003
         L     R1,MSGCURR          RELOAD MESSAGE POINTER       AJSW003
         ABLOCK FDB=(R1),COUNT=70  WRITE MESSAGE                AJSW003
         BR    R7                  RETURN TO CALLER             AJSW003
         SPACE 1                                                AJSW003
         DROP  R4                  DROP BASE REG                AJSW003
*-------------------------------------------------------------* AJSW003
*           DEFAULT LINES/CARDS FOR STC AND TSO               * AJSW003
*-------------------------------------------------------------* AJSW003
         USING JCTSTART,R4                                      AJSW003
         USING JDABSTRT,R5                                      AJSW003
STCORMNT DS    0H                                               AJSW003
         L     R4,ISJCTBLD         PTR TO JCT                   AJSW003
         L     R5,JDABADDR         PTR TO JDAB                  AJSW003
         L     R3,TVTSSVT          PTR TO SSVT                  AJSW003
         USING SSVT,R3                                          AJSW003
         L     R3,SVTOLIM          OUTLIM FROM OUTSERV CARD     AJSW003
         ST    R3,JDABMAXC         JDAB CARD ESTIMATE           AJSW003
         ST    R3,JDABMAXL         JDAB LINE ESTIMATE           AJSW003
         L     R2,JMRADDR          GET JMR ADDRESS              AJSW003
         USING JMR,R2                                           AJSW003
         STCM  R3,15,SMF26EPU      JMR CARD ESTIMATE            AJSW003
         STCM  R3,15,SMF26ELN      JMR LINE ESTIMATE            AJSW003
         SR    R2,R2               CLEAR R2                     AJSW003
         D     R2,=F'1000'         DIVIDE LINE COUNT            AJSW003
         STH   R3,JCTMAXC          JCT CARD ESTIMATE            AJSW003
         STH   R3,JCTMAXL          JCT LINE ESTIMATE            AJSW003
         TM    ISFLAG10,ISMVSTSO   IS THIS A TSO JOB?           AJSW003
         BC    ALLON,FINI          BIY                          AJSW003
         MVC   JDABPROC,=C'SS'     PROC=SS FOR START OR MOUNT   AJSW003
         MVC   JCTPROC,=C'SS'      DITTO                        AJSW003
         B     FINI                ALL DONE                     AJSW003
STRETURN DS    0H                                               AJSW003
         TM    ISFLAG9,ISDSEL      IS THIS A DEMAND SELECT JOB? AJSW003
         BC    ALLON,STCORMNT      YES, GO SET LINE DEFAULTS    AJSW003
         B     FINI                NO, RETURN                   AJSW003
RETURN   DS    0H                                               AJSW003
         TM    TSOFLAG,TSO         IS THIS A TSO JOB?           AJSW003
         BC    ALLON,STCORMNT      YES, GO FIX OUTPUT LINES     AJSW003
FINI     DS    0H                                               AJSW003
         MVI   TSOFLAG,X00         CLEAR TSO JOB FLAG           AJSW003
         MVI   USRBSY,X00          CLEAR USER WORKSPACE LOCK    AJSW003
         L     R15,USRERR          LOAD RETURN CODE             AJSW003
         XC    USRERR,USRERR       CLEAR RETURN CODE            AJSW003
*        LA    R15,12              COMMENT DUMMY USER EXIT      AJSW003
         SPACE 1                                                AJSW003
TSO      EQU   1                                                AJSW003
X00      EQU   0                   USEFUL CONSTANT              AJSW003
XFF      EQU   X'FF'               USEFUL CONSTANT              AJSW003
BLANKS   DC    CL20' '             BLANKS                       AJSW003
JOBINFO  DC    CL13'JOB MSGLEVEL=' STC JOB CARD                 AJSW003
         SPACE 2                                                AJSW003
OPENOK   DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0004 ***** ACCOUNT VALIDATION FI'        AJSW003
         DC    CL34'LE SUCCESSFULLY OPENED *****      '         AJSW003
CLOSOK   DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0104 ***** ACCOUNT VALIDATION FI'        AJSW003
         DC    CL34'LE SUCCESSFULLY CLOSED *****      '         AJSW003
MSG1     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0204 ***** OPEN FAILED FOR ACCOU'        AJSW003
         DC    CL34'NT VALIDATION FILE *****          '         AJSW003
MSG2     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0304 *****          (    ) GENCB'        AJSW003
         DC    CL34' FAILED FOR USER *****            '         AJSW003
MSG3     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0404 *****          (    ) REJEC'        AJSW003
         DC    CL34'TED --- INVALID ACCT NUMBER ***** '         AJSW003
MSG4     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0504 *****          (    ) READ '        AJSW003
         DC    CL34'AND SHOWCB FAILED *****           '         AJSW003
MSG5     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0604 *****          (    ) UNDET'        AJSW003
         DC    CL34'ERMINABLE ERROR - REG15 = 04 *****'         AJSW003
MSG6     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0704 *****          (    ) RPL E'        AJSW003
         DC    CL34'RROR - REG15 = 04 *****           '         AJSW003
MSG7     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0804 *****          (    ) PHYSI'        AJSW003
         DC    CL34'CAL ERROR READ - REG15 = 04 ***** '         AJSW003
MSG8     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0005 *****          (    ) UNDET'        AJSW003
         DC    CL34'ERMINED LOGICAL ERROR *****       '         AJSW003
MSG9     DS    0F                                               AJSW003
         DC    AL1(69)                                          AJSW003
         DC    CL35'IAT0105 ***** CLOSE FAILED FOR ACCO'        AJSW003
         DC    CL34'UNT VALIDATION FILE *****         '         AJSW003
ACCTACBP ACB   DDNAME=ACCTFILE,MACRF=(KEY,DIR,IN,AMODE31)       AJSW003
ACBEND   DC    0X'00'                                           AJSW003
ACBLEN   EQU   ACBEND-ACCTACBP     LENGTH OF ACB                AJSW003
UMODNUM  DC    CL7'AJSW003'       *USER NUMBER                  AJSW003
++MACUPD(IATYJDA).
./ CHANGE NAME=IATYJDA
JDABUFLG EQU   JDABRSV2,1         *U S E R   F L A G  1         AJSW003
JDABFTSO EQU   X'01'              *CANCEL TSO JOB               AJSW003
