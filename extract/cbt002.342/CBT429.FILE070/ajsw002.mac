++ USERMOD(AJSW002)  /*

           THIS MODIFICATION PERFORMS THESE FUNCTIONS IN IATUX29:

    1. - CHANGES THE JES3 DEFAULTS TO BE NORINGCK AND NO EXPDTCK.
                 THIS IS NECESSARY SINCE ALL OUR TAPES HAVE RINGS
                 IN THEM AND OUR TAPE LIBRARY SYSTEM USES THE EXPDT
                 FIELD FOR OTHER THINGS (AND IT MAY BE HIGH).
    2. - CAUSES STARTED TASKS BEGINING WITH 'IMS', 'CIC', AND 'NET'
                TO BE ASSIGNED 50 STAGING AREAS RATHER THAN THE
                (NORMAL) DEFAULT OF 10.
    3. - INSTALLS THE ACF2 CODE FOR JOB VALIDATION.
    4. - DOES ACCOUNT VALIDATION FOR USAGE CHARGES.
                                                           */ .
++ VER(Z038) FMID(HJS2215).
++ SRCUPD(IATUX29) .
./ CHANGE NAME=IATUX29
* USER MODS:                                                    AJSW002
*            - CHANGE DEFAULTS TO NORINGCK AND NO EXPDTCK.      AJSW002
*                                                               AJSW002
*            - GIVE 50 STARS TO STARTED TASKS BEGINNING WITH    AJSW002
*              'IMS', 'CICS', 'NET', 'IDMS', 'M204', 'WCRMO',   AJSW002
*              AND 'HSM' (DEFAULT IS 10).                       AJSW002
*                                                               AJSW002
*            - INSTALL ACF2 CODE FOR JOB VALIDATION.            AJSW002
*                                                               AJSW002
*            - ACCOUNT VALIDATION FOR TSO LOGONS AND SUBMITS.   AJSW002
*                                                               AJSW002
*-------------------------------------------------------------* AJSW002
*           IATUX29 - DEFAULT NORINGCK AND NOEXPDTCK          * AJSW002
*-------------------------------------------------------------* AJSW002
         IATYJDA ,                                              AJSW002
         IATYJMR ,                                              AJSW002
         IATYRSQ                                                AJSW002
         IATYMPC                                                AJSW002
         IATYDST                                                AJSW002
         IATYJQE                                                AJSW002
*        LA    R15,8               SET FOR DUMMY USER EXIT 29   AJSW002
*        ARETURN                                                AJSW002
*        IATXPTCH LT                                            AJSW002
         L     R8,ISJCTBLD                                      AJSW002
         USING JCTSTART,R8                                      AJSW002
         L     R6,JDABADDR                                      AJSW002
         TM    TVTUFLG1,TVTUSTOP ACCT. VALIDATION TURNED OFF    AJSW002
         BC    ALLON,ACCEPT      YES, NOTHING TO DO HERE        AJSW002
         USING JDABSTRT,R6                                      AJSW002
         OI    JDABFLG3,JDABNOXP  *NO EXPDT CHECK               AJSW002
         OI    JDABFLG2,JDABRNGC  *NO RING CHECK                AJSW002
*-------------------------------------------------------------* AJSW002
*     THIS WILL SET STAR LIMIT TO 50 FOR 'IMS' 'CIC' 'NET'    * AJSW002
*-------------------------------------------------------------* AJSW002
         TM    ISFLAG9,ISDSEL     *IS THIS A DEMAND SELECT?     AJSW002
         BNO   USR010             *IF NOT, USE DEFAULT LIMIT    AJSW002
         TM    ISFLAG10,ISMVSTSO  *IS THIS A TSO USER?          AJSW002
         BO    USR010             *IF YES, USE DEFAULT LIMIT    AJSW002
         CLC   JDABJNAM(3),=C'IMS'   *IS IT IMS?                AJSW002
         BE    SETSTAR               *YES                       AJSW002
         CLC   JDABJNAM(4),=C'CICS'  *IS IT CICS?               AJSW002
         BE    SETSTAR               *YES                       AJSW002
         CLC   JDABJNAM(5),=C'WCMRO' *IS IT CICS?               AJSW002
         BE    SETSTAR               *YES                       AJSW002
         CLC   JDABJNAM(4),=C'IDMS'  *IS IT IDMS?               AJSW002
         BE    SETSTAR               *YES                       AJSW002
         CLC   JDABJNAM(4),=C'M204'  *IS IT M204?               AJSW002
         BE    SETSTAR               *YES                       AJSW002
         CLC   JDABJNAM(3),=C'HSM'   *IS IT HSM?                AJSW002
         BE    SETSTAR               *YES                       AJSW002
         CLC   JDABJNAM(3),=C'NET'   *IS IT NET?                AJSW002
         BNE   USR010                *NO                        AJSW002
SETSTAR  DS    0H                                               AJSW002
         LA    R0,50              *SET LIMIT TO 50              AJSW002
         STH   R0,JDASALIM        *IN JDAB                      AJSW002
USR010   DS    0H                                               AJSW002
         LA    R2,JDABJBCD+4      *START OF SEARCH              AJSW002
         LA    R4,71-4            *MAXIMUM SEARCH LENGTH        AJSW002
USR020   DS    0H                                               AJSW002
         CLC   0(4,R2),=C'JOB '                                 AJSW002
         BC    EQ,USR030          *FOUND ACCT FLD - GO PROCESS  AJSW002
         LA    R2,1(R2)           *BUMP TO NEXT CHARACTER       AJSW002
         BCT   R4,USR020          *CONTINUE CHECKING            AJSW002
         B     ACCEPT             *ACCT FLD NEVER FOUND         AJSW002
USR030   DS    0H                                               AJSW002
         CLC   0(13,R2),JOBINFO   *DEMAND SELECT?               AJSW002
         BC    EQ,ACCEPT          *YES, ACCEPT JOB              AJSW002
         LA    R2,4(R2)           *POINT TO ACCOUNT FIELD       AJSW002
         LA    R7,60              *NUMBER OF BYTES TO SEARCH    AJSW002
USR032   DS    0H                                               AJSW002
         CLI   0(R2),C'('         *START OF ACCOUNT FIELD       AJSW002
         BC    EQ,USR035          *YES, GO PROCESS              AJSW002
         CLI   0(R2),C' '         *START OF ACCOUNT FIELD       AJSW002
         BC    NE,USR037          *YES, GO PROCESS              AJSW002
         LA    R2,1(R2)           *POINT TO NEXT CHAR           AJSW002
         BCT   R7,USR032          *CHECK AGAIN                  AJSW002
         TM    ISFLAG9,ISDSEL     *STARTED TASK?                AJSW002
         BC    ALLOFF,FLUSH       *NO, FLUSH JOB - NO ACCT CODE AJSW002
         B     ACCEPT             *YES, DON'T NEED ACCOUNT CODE AJSW002
JOBINFO  DC    CL13'JOB MSGLEVEL='                              AJSW002
*-------------------------------------------------------------* AJSW002
*            DETERMINE IF JOB IS TSO OR BATCH                 * AJSW002
*-------------------------------------------------------------* AJSW002
USR035   DS    0H                                               AJSW002
         LA    R2,1(R2)           *POINT TO ACCOUNT CODE        AJSW002
USR037   DS    0H                                               AJSW002
         TM    ISFLAG10,ISMVSTSO  *IS THIS A TSO USER?          AJSW002
         BC    ALLON,USR040       *YES, DO CHECKING             AJSW002
         TM    ISFLAG9,ISDSEL     *IS THIS A DEMAND SELECT JOB? AJSW002
         BC    ALLOFF,ACCEPT      *NO, SKIP CHECKING            AJSW002
         CLC   JDABUSID,=CL8'0'   *TSO LOGON?                   AJSW002
         BC    EQ,ACCEPT          *NO, SKIP CHECKING            AJSW002
*-------------------------------------------------------------* AJSW002
*  PREVENT TSO JOBS FROM USING PRODUCTION PUCS  DDAAYSS8      * AJSW002
*   WHERE: DD = DIVISION                                      * AJSW002
*          AA = APPLICATION                                   * AJSW002
*           Y = ALWAYS 'P'                                    * AJSW002
*          SS = PROJECT SERIES 88-99                          * AJSW002
*           8 = ALWAYS '8'                                    * AJSW002
*-------------------------------------------------------------* AJSW002
USR040   DS    0H                                               AJSW002
         CLI   4(R2),C'P'         *PRODUCTION PUCS?             AJSW002
         BC    NE,USR050          *NO, CONTINUE CHECK           AJSW002
         CLI   7(R2),C'8'         *DOUBLE CHECK. PROD PUCS?     AJSW002
         BC    EQ,FLUSH           *YES, NOT ALLOWED - FLUSH JOB AJSW002
USR050   DS    0H                                               AJSW002
         LA    R5,IDTABLE         *ADDRESS OF DSD ID'S          AJSW002
USR060   DS    0H                                               AJSW002
         CLC   JDABUSID(3),0(R5)  *IS THIS A DSD ID?            AJSW002
         BC    EQ,USR100          *YES, GO CHECK ACCOUNT NUM    AJSW002
         LA    R5,3(R5)           *POINT TO NEXT TABLE ENTRY    AJSW002
         CLC   TABLEND(3),0(R5)   *END OF DSD ID TABLE?         AJSW002
         BC    NE,USR060          *NO, CHECK NEXT ENTRY         AJSW002
         LA    R5,FCIDS           *ADDR OF FORT WORTH DSD IDS   AJSW002
USR065   DS    0H                                               AJSW002
         CLC   JDABUSID+1(3),0(R5) *IS THIS A DSD ID?           AJSW002
         BC    EQ,USR100          *YES, GO CHECK ACCOUNT NUM    AJSW002
         LA    R5,3(R5)           *POINT TO NEXT TABLE ENTRY    AJSW002
         CLC   TABLEND(3),0(R5)   *END OF DSD ID TABLE?         AJSW002
         BC    NE,USR065          *NO, CHECK NEXT ENTRY         AJSW002
*-------------------------------------------------------------* AJSW002
* CHECK NON-DSD ID FOR VALID ACCOUNT NUMBER (PUCS)  DDAAZXXXX * AJSW002
*   WHERE: DD = DIVISION                                      * AJSW002
*          AA = APPLICATION                                   * AJSW002
*           Z = ALWAYS 'Z' - OPEN SHOP INDICATOR              * AJSW002
*        XXXX = 2, 3, OR 4 NUMERICS                           * AJSW002
*-------------------------------------------------------------* AJSW002
USR070   DS    0H                                               AJSW002
         CLI   4(R2),C'Z'         *OPEN SHOP INDICATOR PRESENT? AJSW002
         BC    EQ,ACCEPT          *YES, LET JOB RUN             AJSW002
         B     FLUSH              *INVALID ACCT NUM - FLUSH JOB AJSW002
         SPACE 1                                                AJSW002
IDTABLE  DC    C'$WC$EC$WP'                                     AJSW002
TABLEND  DC    C'FFF'                                           AJSW002
FCIDS    DC    C'FCAFCDFCEFCFFCGFCHFCIFCJFCM'                   AJSW002
         DC    C'FCOFCPFCQFCRFCSFCTFCWFCZ'                      AJSW002
         DC    C'FFF'                                           AJSW002
         SPACE 1                                                AJSW002
*-------------------------------------------------------------* AJSW002
*   CHECK DSD ID FOR VALID PCS ACCOUNT NUMBER  DDAAYSSPTTSS   * AJSW002
*                                            (ANY 12 DIGITS)  * AJSW002
*   OR VALID PUCS NUMBER  DDAAZXXXX                           * AJSW002
*     WHERE: DD = DIVISION - DSD ID (EG: WC, EC, FC )         * AJSW002
*            AA = APPLICATION (01 THRU 09)                    * AJSW002
*             Z = 'N' FOR NON-BILLABLE                        * AJSW002
*          XXXX = 2, 3, OR 4 NUMERICS                         * AJSW002
*-------------------------------------------------------------* AJSW002
USR100   DS    0H                                               AJSW002
         CLI   4(R2),C'N'         *PUCS NON-BILLABLE?           AJSW002
         BC    NE,USR120          *NO SEE IF PCS NUMBER         AJSW002
         LA    R5,IDTABLE         *ADDRESS OF DSD ID'S          AJSW002
USR110   DS    0H                                               AJSW002
         CLC   0(2,R2),1(R5)      *IS THIS A DSD ID?            AJSW002
         BC    EQ,ACCEPT          *YES, ACCEPT JOB              AJSW002
         LA    R5,3(R5)           *POINT TO NEXT TABLE ENTRY    AJSW002
         CLC   TABLEND(3),0(R5)   *END OF DSD ID TABLE?         AJSW002
         BC    NE,USR110          *NO, CHECK NEXT ENTRY         AJSW002
         LA    R5,FCIDS           *ADDR OF FORT WORTH DSD IDS   AJSW002
USR115   DS    0H                                               AJSW002
         CLC   0(2,R2),0(R5)      *IS THIS A DSD ID?            AJSW002
         BC    EQ,ACCEPT          *YES, ACCEPT JOB              AJSW002
         LA    R5,3(R5)           *POINT TO NEXT TABLE ENTRY    AJSW002
         CLC   TABLEND(3),0(R5)   *END OF DSD ID TABLE?         AJSW002
         BC    NE,USR115          *NO, CHECK NEXT ENTRY         AJSW002
*-----     CHECK IF ACCOUNT NUMBER IS A 12 DIGIT PCS     -----* AJSW002
USR120   DS    0H                                               AJSW002
         SLR   R7,R7              *PREPARE FOR COUNT            AJSW002
USR130   DS    0H                                               AJSW002
         CLI   0(R2),C','         *END OF ACCOUNT NUMBER?       AJSW002
         BC    EQ,USR140          *YES, GO CHECK COUNT          AJSW002
         CLI   0(R2),C' '         *END OF ACCOUNT NUMBER?       AJSW002
         BC    EQ,USR140          *YES, GO CHECK COUNT          AJSW002
         LA    R7,1(R7)           *ADD TO COUNT                 AJSW002
         LA    R2,1(R2)           *POINT TO NEXT ACCT CHAR      AJSW002
         B     USR130             *CONTINUE CHECKING FOR END    AJSW002
USR140   DS    0H                                               AJSW002
         LA    R9,12              *NUMBER OF CHARS IN VALID PCS AJSW002
         CR    R7,R9              *VALID PCS?                   AJSW002
         BC    EQ,ACCEPT          *YES, LET JOB RUN             AJSW002
FLUSH    DS    0H                                               AJSW002
         MVC   WTO+16(8),JDABJNAM *PUT JOBNAME IN MSG           AJSW002
         MVC   WTO+25(4),JDABJNUM *PUT JOB NUMBER IN MSG        AJSW002
WTO     WTO 'IAT0205 JOBNAME (    ) UNAUTHORIZED ACCOUNT CODE', AJSW002X
               ROUTCDE=(11)                                     AJSW002
         B     USR999             *GO FLUSH JOB                 AJSW002
         SPACE 1                                                AJSW002
         DROP R6                                                AJSW002
         DROP R8                                                AJSW002
ACCEPT   DS    0H                 *INDICATE PROCESS JOB OK      AJSW002
         IATXCUE EP=ACFJBEOR,ALOAD=YES                          AJSW002
         ICM   R15,15,ACFFDB      *R15 = ACF2 RETURN CODE       AJSW002
         XC    ACFFDB,ACFFDB      *MUST CLEAR ACFFDB            AJSW002
         ARETURN                                                AJSW002
USR999   DS    0H                                               AJSW002
         IATXCUE EP=ACFJBEOR,ALOAD=YES                          AJSW002
         ICM   R15,15,ACFFDB      *R15 = ACF2 RETURN CODE       AJSW002
         XC    ACFFDB,ACFFDB      *MUST CLEAR ACFFDB            AJSW002
         LA    R15,4              *INDICATE JOB NO GOOD         AJSW002
         ARETURN                                                AJSW002
         IATXPTCH LT                                            AJSW002
UMODNUM  DC    CL7'AJSW002'
