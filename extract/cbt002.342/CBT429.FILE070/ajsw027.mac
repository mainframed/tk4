++ USERMOD(AJSW027) /* ALLOW USE OF '*' IN NET FIELD OF JESDJC MSG.

       THIS MODIFICATION WILL CAUSE JES3 TO USE THE ISSUERS DJC
       NET ID (RQMNETID FIELD) IF THE MESSAGE 'JESDJC' IS ISSUED
       WITH AN '*NET-ID*' IN THE NET-ID FIELD. PROGRAM 'JESDJC1'
       WILL THEN PERFORM 'FREEJOB' LIKE FUNCTION FOR JES3 DJC JOB
       NETS. TIME DELAY IN 'JESDJC1' IS TO ALLOW SUCCESSOR JOB(S)
       TO RESCHEDULE THROUGH CI AND BE WAITING IN MAIN WHEN THE
       CURRENT JOB ACTUALLY ENDS.
                                                                 */ .
++ VER(Z038) FMID(HJS2215).
++ JCLIN .
//GEN11 JOB Y51024508,'531-0  SINGER '
//SG22 EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET',
//    UNIT='3350',SER=IPLXXX,N=GEN1,NAME=JES3LIB,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//INCLUDE DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=MVS3.INCLUDE
//SYSLIN DD *
 INCLUDE INCLUDE(JESDJC1)
 NAME JESDJC1(R)
/*
++ SRC(JESDJC1) DISTLIB(ASRCLIB) .
         TITLE 'JES3 DJC EARLY NET RELEASE PROGRAM'
JESDJC1  CSECT
         STM   14,12,12(13)       *SAVE REGS
         BALR  12,0               *SET UP BASE
         USING *,12               *REGISTER
         ST    13,SAVE+4          *SAVE REG 13
         LA    13,SAVE            *GET SAVE AREA ADDRESS
         SPACE 1
       EXTRACT TIOT,'S',FIELDS=(TIOT) *GET TIOT ADDRESS
         L     3,TIOT             *GET TIOT ADDRESS
         MVC   JOBNAME(8),0(3)    *MOVE JOBNAME TO WTO
         SPACE 1
***  NOTE:                                                       ***
***  FOR NETNAME = '*NET-ID*' TO WORK AJSW027 MUST BE INSTALLED  ***
         SPACE 1
         MVC   NETNAME(8),=CL8'*NET-ID*' SET TO DEFAULT NAME
         SPACE 1
WTO      WTO   'IAT0008 JOBNAME  NETNAME  ** JES3 DJC HOLD COUNTS DECREX
               MENTED **',ROUTCDE=(2,11),DESC=(7)
         SPACE 1
        STIMER WAIT,BINTVL=AWHILE *WAIT FOR AWHILE
         SPACE 1
         SR    15,15              *SET GOOD RETURN CODE
         L     13,SAVE+4          *GET OLD SAVE AREA POINTER
         L     14,12(13)          *GET RETURN ADDRESS
         LM    0,12,20(13)        *RESTORE REGS
         BR    14                 *AND RETURN
         SPACE 1
JOBNAME  EQU   WTO+16             *JOBNAME IN WTO
NETNAME  EQU   WTO+25             *NETNAME IN WTO
TIOT     DC    F'0'               *TIOT ADDRESS
SAVE     DC    9D'0'              *SAVE AREA
AWHILE   DC    F'3000'            *WAIT TIME (30 SEC)
UMODNUM  DC    CL7'AJSW027'       *USER NUMBER
         END   JESDJC1
++ SRCUPD(IATCNSV) DISTLIB(AJES3SRC) .
./ CHANGE NAME=IATCNSV
*        USER MODS =                                            AJSW027
*                                                               AJSW027
*             - USE RQNETID IF NET=*NET-ID* FOR DJC WTO         AJSW027
*                                                               AJSW027
         CLC   DJCCONNI,=CL8'*NET-ID*' *IS IT TO BE DEFAULT?    AJSW027
         BNE   GDW010             *NO - USE NAME SUPPLIED       AJSW027
         MVC   DJCCONNI,RQMNETID  *YES - GET ISSUERS NETID      AJSW027
GDW010   DS    0H                 *SKIP CHANGING NETID          AJSW027
UMODNUM  DC    CL7'AJSW027'       *USER NUMBER                  AJSW027
