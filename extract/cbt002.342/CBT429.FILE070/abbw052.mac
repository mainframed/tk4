++ USERMOD(ABBW052) /* SUPPLY OUR IEAVMXIT ROUTINE

         SUPPLIES OUR IEAVMXIT TO CHANGE ROUTE AND DESC CODES.
         MAJOR PURPOSES OF OUR IEAVMXIT ARE:
         1. ADD ROUTE CODE OF 14 TO ALL JOB ABEND MESSAGES.
         2. CHANGE DESCRIPTOR CODE FOR SOME MESSAGES WHICH WE
            WOULD REALLY NOT LIKE JES3 TO KEEP AROUND FOREVER.

 CHANGE ACTIVITY
 10/20/88 J DAILEY       CONVERTED TO USER EXIT IEAVMXIT.
                         REMOVED CHECKING FOR MASS STOR MESSAGES
                           */ .
++ VER(Z038) FMID(JBB2220)              /* XA BASE*/ .
++ JCLIN .
//GEN49 JOB WC07N0044,'531-1 CM SINGER'
//SG21  EXEC LINKS,
// PARM='XREF,LET,LIST,RENT',
//    UNIT='3380',SER=IPLXXX,N=SYS2,NAME=HSSLINK,P1=' ',
//    MOD=,P2=' ',CLASS=T
//LK.SYSPUNCH DD DUMMY
//INCLUDE DD DSN=MVS3.INCLUDE,DISP=SHR
//SYSLIN DD *
 INCLUDE INCLUDE(IEAVMXIT)
 ENTRY IEAVMXIT
 NAME IEAVMXIT(R)
++ SRC(IEAVMXIT) DISTLIB(ASRCLIB) .
IEAVMXIT TITLE 'DSD WESTERN CENTER GENERAL WTO/WTOR EXIT'
***********************************************************************
*                                                                     *
* MODULE NAME : IEAVMXIT                                              *
*                                                                     *
* DESCRIPTION : GENERALIZED WTO/WTOR USER EXIT                        *
*                                                                     *
* COPYRIGHT (C) GENERAL DYNAMICS                                      *
*               DATA SYSTEMS DIVISION - WESTERN CENTER                *
*                                                                     *
* FUNCTION : THIS ROUTINE INTERCEPTS AND CHANGES THE ROUTING AND      *
*            OR DESCRIPTOR CODES FOR CERTAIN SYSTEM MESSAGES.         *
*            A COPY OF ALL ACF MESSAGES WILL BE FORCED TO SYSLOG.     *
*                                                                     *
*                                                                     *
* DEPENDENCIES : NONE                                                 *
*                                                                     *
* RESTRICTIONS : CAN NOT ENTER AN 'MVS WAIT' OR INVOKE ANY SERVICE    *
*                ISSUING AN 'MVS WAIT'.                               *
*                                                                     *
*                THIS EXIT MUST RESIDE IN A LIBRARY THAT IS IN THE    *
*                'LNKLST' CONCATENATION.                              *
*                                                                     *
* PROCESSOR : ASSEMBLER H                                             *
*                                                                     *
* ATTRIBUTES : RE-ENTRANT, RE-USABLE, AMODE 31, RMODE ANY             *
*                                                                     *
* ENTRY POINT : IEAVMXIT                                              *
*                                                                     *
* ENTRY PURPOSE : MAIN ENTRY POINT                                    *
*                                                                     *
* LINKAGE : BALR FROM THE SYSTEMS COMMUNICATIONS TASK                 *
*                                                                     *
* INPUT : REGISTER 1 CONTAINS THE ADDRESS OF A POINTER TO THE         *
*         COMMUNICATIONS TASK EXIT PARAMETER LIST (CTXT)              *
*                                                                     *
* OUTPUT : CERTAIN MESSAGES WILL HAVE THEIR DESCRIPTOR AND/OR         *
*          ROUTING CODE CHANGED. THE CTXT WILL CONTAIN THE NEW        *
*          ROUTING AND DESCRIPTOR CODES. FLAGS WILL BE SET TO         *
*          TELL THE COMMUNICATIONS TASK THAT THIS EXIT MADE           *
*          CHANGES TO THE MESSAGE.                                    *
*                                                                     *
* EXIT NORMAL : VIA CALLERS REGISTER 14                               *
*                                                                     *
* EXIT ERROR  : NONE                                                  *
*                                                                     *
* EXTERNAL ROUTINES : NONE                                            *
*                                                                     *
* REGISTER USAGE: R2  = COMMUNICATIONS TASK EXIT PARAMETER LIST       *
*                 R3  = MESSAGE ATTRIBUTES AND TEXT                   *
*                 R4  = -> MESSAGE ROUTING CODES                      *
*                 R5  = -> MESSAGE DESCRIPTOR CODES                   *
*                 R12 = MODULE BASE REGISTER                          *
*                                                                     *
* CONTROL BLOCKS  :                                                   *
*                     IEZVX100                                        *
*                                                                     *
* EXECUTABLE MACROS : NONE                                            *
*                                                                     *
* MESSAGES: NONE                                                      *
*                                                                     *
* ABEND CODES : NONE                                                  *
*                                                                     *
* CHANGE ACTIVITY :                                                   *
*                                                                     *
* 10/20/88 J DAILEY        INITIAL IMPLEMENTATION                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
IEAVMXIT AMODE 31
IEAVMXIT RMODE ANY
IEAVMXIT CSECT ,
         USING IEAVMXIT,R15
         B     START
         DC    CL9'IEAVMXIT'       MODULE NAME
         DC    CL9'&SYSDATE'       ASSEMBLE DATE
         DC    CL6'&SYSTIME'       ASSEMBLE TIME
         DROP  R15
*
START    DS    0H
         STM   14,12,12(R13)       SAVE CALLERS REGISTERS
         LR    R12,R15             SET LOCAL BASE REGISTER
         USING IEAVMXIT,R12        TELL THE ASSEMBLER ABOUT IT.
*
         L     R2,0(,R1)           SET PARAMETER LIST BASE
         USING CTXT,R2
         L     R3,CTXTTXPJ         MESSAGE TEXT BASE
         USING CTXTATTR,R3
         L     R4,CTXTRCP          MESSAGE ROUTING CODES
         USING CTXTROUT,R4
         L     R5,CTXTDCP          MESSAGE DESCRIPTOR CODES
         USING CTXTDESC,R5
         EJECT
*********************************************************************
*                                                                   *
*        TURN OFF SECURITY ROUTING FOR ALL ACF MESSAGES             *
*                                                                   *
*********************************************************************
*
         CLC   =C'ACF',MSGID
         BNE   NOTACF
         NI    CTXTR002,TURNOFF-CTXTR09
         B     SETRCHNG
*
*********************************************************************
*                                                                   *
*        FOLLOWING MESSAGES HAVE ROUTE CODE 14 ADDED                *
*                                                                   *
*********************************************************************
*
NOTACF   DS    0H
         CLC   =C'IEF450I',MSGID   LOOK FOR (JOB ABENDED)
         BE    SETR14              YES - GO ADD ROUTE CODE 14
         CLC   =C'IEF451I',MSGID   LOOK FOR (JOB ENDED BY CC)
         BE    SETR14              YES - GO ADD ROUTE CODE 14
         CLC   =C'IEF452I',MSGID   LOOK FOR (JOB NOT RUN - JCL ERROR)
         BE    SETR14              YES - GO ADD ROUTE CODE 14
         CLC   =C'IEF453I',MSGID   LOOK FOR (JOB FAILED - JCL ERROR)
         BNE   DESCODES            NO - GO DO DESC CODE CHANGES
*
SETR14   DS    0H
         OI    CTXTR002,CTXTR14    ADD ROUTE CODE 14
*
SETRCHNG DS    0H
         OI    CTXTRFB1,CTXTRCRC   INDICATE ROUTING CODE CHANGED
         EJECT
*********************************************************************
*                                                                   *
*        CHECK FOR DESCRIPTOR CODE CHANGE MESSAGES                  *
*                                                                   *
*********************************************************************
*
DESCODES DS    0H
         CLC   =C'IEA000A',MSGID LOOK FOR INTERVENTION REQ MESSAGE
         BE    SETD4A              THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'IOS003A',MSGID LOOK FOR INTERVENTION REQ MESSAGE
         BE    SETD4A              THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'IEA000I',MSGID LOOK FOR INTERVENTION REQ MESSAGE
         BNE   SETD4C              THEN CHANGE DESCRIPTOR CODE TO 4
*
SETD4A   DS    0H
         CLC   =C'00E',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'00F',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'011',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'610',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'620',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'630',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'640',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'650',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'660',MSGID+8     PRINTER ????
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
*
SETD4C   DS    0H
         CLC   =C'IEA911E',MSGID   LOOK FOR 'SYS1.DUMP' MESSAGE
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'IEA994A',MSGID   LOOK FOR 'ALL DUMPS FULL' MESSAGE
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'IST129I',MSGID   LOOK FOR VTAM ERROR MESSAGE
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'IST852I',MSGID   LOOK FOR VTAM ERROR MESSAGE
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'IAT6360',MSGID   LOOK FOR JES3 CHECK POINT MESSAGE
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'ACF0B900',MSGID  LOOK FOR ACF2 ERROR MESSAGE
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'ACF0B906',MSGID  LOOK FOR ACF2 ERROR MESSAGE
         BE    SETD4               THEN CHANGE DESCRIPTOR CODE TO 4
         CLC   =C'ACF999',MSGID    LOOK FOR ACF2 ERROR MESSAGE
         BNE   RETURN              NO, TIME TO EXIT...STAGE RIGHT
*
SETD4    DS    0H
         MVI   CTXTDC1,CTXTDC04    SET DESCRIPTOR CODE 4
         OI    CTXTRFB1,CTXTRCDC   INDICATE DESCRIPTOR CODE CHANGED
         OI    CTXTRFB2,CTXTRNRT   DO NOT RETAIN IN AMR FACILITY
*
RETURN   DS    0H
         LM    R14,R12,12(R13)     RESTORE CALLERS REGISTER
         BR    R14                 RETURN TO CALLER
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
TURNOFF  EQU   255
*
         LTORG
         TITLE 'COMMUNICATIONS TASK EXIT PARAMETER LIST'
         IEZVX100
CTXTATTR DSECT ,
MSGID    EQU   CTXTTMSG            START OF MESSAGE ID
IEAVMXIT CSECT ,
         END
