++USERMOD (AJSW057) REWORK(1988190)  /* INSTALL SILO JES3 CODE: */ .
++VER (Z038) FMID(HJS2215)
             PRE(SLS1033)             /*

       THIS MODIFICATION INSTALLS ALL SILO CODE FOR JES3
       INCLUDING IATUX09, IATUX31, AND IATUX32.

   THE SLSEHOOK MACRO MUST BE INSERTED INTO MODULE IATMDAL AFTER THE
COMPARE INSTRUCTION LABELED MDA744; FOR IATMDAL AT APAR LEVEL OY08531
IT SHOULD BE INSERTED AFTER LINE 08920000;
       VS2.SILO.SAMPLIB(IATUX09) (IATUX31) (IATUX32)

CHANGE HISTORY:
12/12/88 J. DAILEY       CHANGE IATUX31 TO CALL VENDOR EXITS THAT ARE
                         IN A MODULE TABLE.

        */ .
++SRCUPD (IATMDAL) DISTLIB (AJES3SRC) .
./ CHANGE NAME=IATMDAL
         SLSEHOOK ,
./ ENDUP
++SRCUPD (IATUX09) DISTLIB (AJES3SRC) .
./ CHANGE NAME=IATUX09
         IATYTVT TYPE=DSECT                                     AJSW057
         B     USRUX09                                          AJSW057
USRUX09  DS    0H
***********************************************************************
*  THIS IS AN EXAMPLE OF A JES3 USER EXIT 09 WHICH INVOKES THE        *
*  AUTOMATED CARTRIDGE SYSTEM SUPPORT ROUTINE PROVIDED BY STORAGETEK. *
*  THE STORAGETEK ACS SUPPORT ROUTINE NEVER FAILS A JOB;              *
*  IT ALWAYS SETS A RETURN CODE OF 0.                                 *
*  SEE MANUAL MVS/XA SPL: JES3 USER MODIFICATIONS AND MACROS          *
*  (LC28-1372) FOR A DESCRIPTION OF USER EXIT 09'S ENTRY AND EXIT     *
*  ENVIRONMENT.                                                       *
*  IF THE INSTALLATION ALREADY UTILIZES JES3 USER EXIT 09, THEN THE   *
*  EXISTING (OLD) EXIT SHOULD BE RENAMED AND THE LOAD MODULE'S NEW    *
*  NAME SHOULD BE PLACED INSIDES THE QUOTES OF THE SETC STATEMENT     *
*  BELOW.                                                             *
***********************************************************************
         LCLC  &OLDUX09
&OLDUX09 SETC  ' ' <=== YOUR OLD IATUX09'S NEW NAME GOES INSIDE QUOTES
*
         AGO   .BYP01                                           AJSW057
         TITLE 'IATYEQU - JES3 GENERAL EQUATES'
         IATYEQU ,
         TITLE 'IATYFCT - JES3 FUNCTION CONTROL TABLE'
         IATYFCT ,
         TITLE 'IATYREG - JES3 REGISTER EQUATES'
         IATYREG ,
         TITLE 'IATYTVT - JES3 TRANSFER VECTOR TABLE'
         IATYTVT TYPE=DSECT
*
         TITLE 'JES3 EXAMINE FINAL JOB STATUS USER EXIT'
IATUX09  CSECT ,
         USING *,R15
         IATYMOD BR=YES
.BYP01   ANOP                                                   AJSW057
         DROP  R15
         USING IATUX09,R10         SPECIFY CSECT'S BASE REGISTER
         LR    R10,R15             SET CSECT'S BASE ADDRESS
*
         XR    R5,R5               INITIALIZE IATUX09 RETURN CODE TO 0
*
         AIF   ('&OLDUX09' EQ ' ').DONEXIT
         ALOAD EP=&OLDUX09,        BRING OLD USER EXIT RTN INTO STORAGE+
               ERROR=USRNOXIT
         LR    R15,R0              SAVE EXIT ROUTINE'S EPA
         ACALL ENTER=(R15)         INVOKE THE EXIT ROUTINE
         LR    R5,R15              SAVE EXIT ROUTINE'S RETURN CODE
USRMSGOK DS    0H
         ADELETE EP=&OLDUX09       DELETE OLD USER EXIT FROM STORAGE
         LTR   R5,R5               CHECK EXIT ROUTINE'S RETURN CODE
         BC    NZERO,UX09DONE      IF RC NOT 0, SKIP STK EXIT.
USRNOXIT DS    0H
.DONEXIT ANOP
*
         ALOAD EP=SLEEUX09,        BRING STK EXIT ROUTINE INTO STORAGE +
               ERROR=STKNOXIT
         LR    R15,R0              SAVE EXIT ROUTINE'S EPA
         ACALL ENTER=(R15)         INVOKE THE EXIT ROUTINE
         LR    R5,R15              SAVE EXIT ROUTINE'S RETURN CODE
         ADELETE EP=SLEEUX09       DELETE EXIT ROUTINE FROM STORAGE
STKNOXIT DS    0H
*
UX09DONE DS    0H
         LR    R15,R5              SET IATUX09 RETURN CODE
         ARETURN RC=(R15)          RETURN TO IATIIDR
         LTORG ,                   COLLECT LITERALS HERE
         AGO   .BYP02                                           AJSW057
APARNUM  DC    CL5'     '          APAR NUMBER
PTFNUM   DC    CL5'SP215'          PTF NUMBER
*        END   ,                    **************  DELETED BY  AJSW057
.BYP02   ANOP                                                   AJSW057
++SRCUPD (IATUX31) .
./ CHANGE NAME=IATUX31                                          AJSW057
         B     GDUX31             GOTO OUR UX31 CODE            AJSW057
         EJECT                                                  AJSW057
****************************************************************AJSW057
*                                                               AJSW057
* THIS USER MOD PERFORMS NO EXAMINATION OF MESSAGES. INSTEAD    AJSW057
* IT ACTS AS A DRIVE ROUTINE INVOKING OTHER MODULES MOSTLY      AJSW057
* SUPPLIED BY VENDORS.                                          AJSW057
*                                                               AJSW057
* IF A ROUTINE RETURNS A RETURN CODE OF 8 OR 12 NO OTHER        AJSW057
* ROUTINES WILL BE CALLED. A RETURN CODE OF 12 WILL BE CHANGED  AJSW057
* TO A RETURN CODE OF 8. THIS ALLOWS THE MESSAGES TO AT LEAST   AJSW057
* BE RECORDED IN THE JES3 MLOG.                                 AJSW057
*                                                               AJSW057
* THE FOLLOWING REGISTERS ARE RESERVED AND MUST NOT BE          AJSW057
* DISTURBED:                                                    AJSW057
*                                                               AJSW057
*          R6 = POINTER TO TABLE OF ROUTINE NAMES.              AJSW057
*          R7 = CONSOLE NUMBER/MESSAGE DEST CLASS               AJSW057
*          R8 = ADDRESS OF COUNT AND TEXT.                      AJSW057
*          R9 = HIGHEST RETURN CODE.                            AJSW057
*                                                               AJSW057
*                                                               AJSW057
****************************************************************AJSW057
*                                                               AJSW057
GDUX31   DS    0H                 START OF GD/WC USERMOD CODE   AJSW057
         LR    R7,R0              SAVE CONSOLE/MSG DEST         AJSW057
         LR    R8,R1              SAVE ADDRESS OF COUNT/TEXT    AJSW057
         SLR   R9,R9              INITIALIZE RETURN CODE        AJSW057
         LA    R6,UX31RTNS        POINTER TO FIRST ROUTINE      AJSW057
*                                                               AJSW057
UX31LOOP DS    0H                                               AJSW057
         CLI   0(R6),X'FF'        END OF TABLE                  AJSW057
         BE    UX31END            YES, RETURN TO JES3           AJSW057
         LR    R0,R6              SET PARM REG                  AJSW057
         ALOAD EPLOC=(R0),ERROR=UX31INCR LOAD THE ROUTINE       AJSW057
         LR    R15,R0             GET EPA OF ROUTINE            AJSW057
         LR    R0,R7              SET CONSOLE PARM REG          AJSW057
         LR    R1,R8              SET MESSAGE PARM REG          AJSW057
         ACALL ENTER=(R15)        HAVE SCOTTY BEAM US THERE     AJSW057
*                                                               AJSW057
         B     UX31DEL            GET NEXT ROUTINE              AJSW057
         B     UX31RC4            MESSAGE CHANGED               AJSW057
         B     UX31RC8            MESSAGE TO DLOG/MLOG ONLY     AJSW057
         B     UX31RC8            RC 12 CHANGE TO RC 8          AJSW057
*                                                               AJSW057
UX31RC4  DS    0H                                               AJSW057
         LR    R8,R1              COUNT/TEXT                    AJSW057
         CR    R9,R15             NEW RC > MAX RC               AJSW057
         BNL   UX31DEL            NO, CONTINUE                  AJSW057
         LR    R9,R15             SET HIGHEST RETURN CODE       AJSW057
         B     UX31DEL            CONTINUE                      AJSW057
*                                                               AJSW057
UX31RC8  DS    0H                                               AJSW057
         LA    R9,8               SET RETURN CODE               AJSW057
*                                                               AJSW057
UX31DEL  DS    0H                                               AJSW057
         LR    R0,R6              SET PARM REG                  AJSW057
         ADELETE EPLOC=(R0)       DELETE THE ROUTINE            AJSW057
*                                                               AJSW057
UX31INCR DS    0H                                               AJSW057
         LA    R6,8(,R6)                                        AJSW057
         B     UX31LOOP           LOOP BACK FOR NEXT ROUTINE    AJSW057
*                                                               AJSW057
UX31END  DS    0H                                               AJSW057
         LR    R0,R7              SET CONSOLE PARM REG          AJSW057
         LR    R1,R8              SET MESSAGE PARM REG          AJSW057
         LR    R15,R9             SET RETURN CODE               AJSW057
         ARETURN RC=(R15)                                       AJSW057
*                                                               AJSW057
UX31RTNS DS    0C          **** UX31 ROUTINE TABLE ****         AJSW057
         DC    CL8'SLEEUX31'      SILO'S UX31                   AJSW057
         DC    CL8'AOEXIT31'      AFOPERATOR UX31               AJSW057
         DC    8X'FF'             DUMMY ENTRY                   AJSW057
         DC    8X'FF'             DUMMY ENTRY                   AJSW057
         DC    8X'FF'             DUMMY ENTRY                   AJSW057
UXRTNSE  DC    8X'FF'             **** END OF ROUTINE TABLE ****AJSW057
*                                                               AJSW057
UMODNUM  DC    C'AJSW057'                                       AJSW057
++SRCUPD (IATUX32) .
./ CHANGE NAME=IATUX32                                          AJSW057
         B     USRUX32-IATUX32(R15)                             AJSW057
USRUX32  DS    0H                                               AJSW057
         AGO   .BYP01                                           AJSW057
UX32     TITLE 'JES3 DYNALDSN OVERRIDE USER EXIT'
IATUX32  AMODE 31
IATUX32  RMODE ANY
         IATYASM ,                 INSURE CORRECT SPLEVEL FOR MACROS
.BYP01   ANOP                                                   AJSW057
***********************************************************************
*  THIS IS AN EXAMPLE OF A JES3 USER EXIT 32 WHICH INVOKES THE        *
*  AUTOMATED CARTRIDGE SYSTEM SUPPORT ROUTINE PROVIDED BY STORAGETEK. *
*  THE STORAGETEK ACS SUPPORT ROUTINE ALWAYS SETS A RETURN CODE OF 0. *
*  SEE MANUAL MVS/XA SPL: JES3 USER MODIFICATIONS AND MACROS          *
*  (LC28-1372) FOR A DESCRIPTION OF USER EXIT 32'S ENTRY AND EXIT     *
*  ENVIRONMENT.                                                       *
*  IF THE INSTALLATION ALREADY UTILIZES JES3 USER EXIT 32, THEN THE   *
*  EXISTING (OLD) EXIT SHOULD BE RENAMED AND THE LOAD MODULE'S NEW    *
*  NAME SHOULD BE PLACED INSIDES THE QUOTES OF THE SETC STATEMENT     *
*  BELOW.                                                             *
*  NOTE:  THE SUPPORT ROUTINE PROVIDED BY STORAGETEK (SLEEUX32) IS    *
*  NORMALLY INSTALLED INTO LINKLIB.  IN ORDER TO AVOID FETCHING       *
*  SLEEUX32 INTO STORAGE FROM LINKLIB EACH TIME DYNALLOC IS ISSUED,   *
*  THE INSTALLATION MAY CONSIDER ADDING SLEEUX32 TO THE MODIFIED LPA  *
*  LIST.  SEE MANUAL MVS/XA SPL: INITIALIZATION AND TUNING (GC28-1149)*
*  FOR DETAILS ABOUT PARMLIB MEMBER IEALPAXX.                         *
***********************************************************************
         LCLC  &OLDUX32
&OLDUX32 SETC  ' ' <=== YOUR OLD IATUX32'S NEW NAME GOES INSIDE QUOTES
*
         TITLE 'CVT - MVS COMMUNICATIONS VECTOR TABLE'
         CVT   DSECT=YES
         TITLE 'IATYAWA - JES3 ALLOCATION WORK AREA'
         IATYAWA ,
         AGO   .BYP02                                           AJSW057
         TITLE 'IATYEQU - JES3 GENERAL EQUATES'
         IATYEQU ,
         TITLE 'IATYREG - JES3 REGISTER EQUATES'
         IATYREG ,
*
         TITLE 'JES3 DYNALDSN OVERRIDE USER EXIT'
.BYP02   ANOP                                                   AJSW057
IATUX32  CSECT ,
*        USING *,R15               **************   REPLACED BY AJSW057
         USING IATUX32,R15                                      AJSW057
         IATYMOD BR=YES
         DROP  R15
         USING AWA,R13             SPECIFY ALLOCATION WORK AREA BASE
*
         LR    R2,R10              SAVE ENTRY VALUE OF R10
         LR    R3,R14              SAVE ENTRY VALUE OF R14
*
         USING IATUX32,R10         SPECIFY CSECT'S BASE REGISTER
         LR    R10,R15             SET CSECT'S BASE ADDRESS
*
         L     R1,CVTPTR           GET ADDRESS OF CVT
         L     R1,CVTLINK-CVT(,R1) GET ADDRESS OF LINKLIB'S DCB
         LOAD  EP=SLEEUX32,        GET EPA OF STK EXIT ROUTINE         +
               DCB=(R1),                                               +
               ERRET=STKNOXIT
         LR    R15,R0              SAVE EXIT ROUTINE'S EPA
***
*  USE SELSSIW, IN THE SERVICE ENTRANCE LIST IN THE AWA, AS THE
*  AREA IN WHICH TO BUILD THE SYNCH PARAMETER LIST.
***
         LA    R1,AWASEL           GET ADR OF SERVICE ENTRANCE LIST
         LA    R1,SELSSIW-SELSTART(,R1) GET ADR OF MODIFIABLE STORAGE
         MVC   0(LENSYNCH,R1),LSYNCH INITIALIZE SYNCH PLIST
         SYNCH (R15),              INVOKE THE EXIT ROUTINE             +
               RESTORE=YES,        ..RESTORE R2-R13 WHEN EXIT RETURNS  +
               STATE=SUPV,         ..INVOKE EXIT IN SUPERVISOR STATE   +
               KEYADDR=EXITKEY,    ..INVOKE EXIT IN KEY 1              +
               AMODE=CALLER,       ..INVOKE EXIT IN OUR AMODE          +
               MF=(E,(R1))
*
         DELETE EP=SLEEUX32        DECREMENT EXIT'S USE COUNT
STKNOXIT DS    0H
         XR    R15,R15             SET RETURN CODE TO 0
*
         AIF   ('&OLDUX32' EQ ' ').DONEXIT
         L     R1,CVTPTR           GET ADDRESS OF CVT
         L     R1,CVTLINK-CVT(,R1) GET ADDRESS OF LINKLIB'S DCB
         LOAD  EP=&OLDUX32,        GET EPA OF OLD USER EXIT ROUTINE    +
               DCB=(R1),                                               +
               ERRET=USRNOXIT
         LR    R15,R0              SAVE EXIT ROUTINE'S EPA
***
*  USE SELSSIW, IN THE SERVICE ENTRANCE LIST IN THE AWA, AS THE
*  AREA IN WHICH TO BUILD THE SYNCH PARAMETER LIST.
***
         LA    R1,AWASEL           GET ADR OF SERVICE ENTRANCE LIST
         LA    R1,SELSSIW-SELSTART(,R1) GET ADR OF MODIFIABLE STORAGE
         MVC   0(LENSYNCH,R1),LSYNCH INITIALIZE SYNCH PLIST
         SYNCH (R15),              INVOKE THE EXIT ROUTINE             +
               RESTORE=YES,        ..RESTORE R2-R13 WHEN EXIT RETURNS  +
               STATE=SUPV,         ..INVOKE EXIT IN SUPERVISOR STATE   +
               KEYADDR=EXITKEY,    ..INVOKE EXIT IN KEY 1              +
               AMODE=CALLER,       ..INVOKE EXIT IN OUR AMODE          +
               MF=(E,(R1))
*
         LR    R14,R15             SAVE EXIT'S RETURN CODE ACROSS SVC 9
         DELETE EP=&OLDUX32        DECREMENT EXIT'S USE COUNT
         LR    R15,R14             RESTORE EXIT'S RETURN CODE TO R15
         B     USRDONE
USRNOXIT DS    0H                  EXIT ROUTINE NOT FOUND
         XR    R15,R15             SET RETURN CODE TO 0
USRDONE  DS    0H
.DONEXIT ANOP
*
         LR    R10,R2              RESTORE R10 TO ENTRY VALUE
         LR    R14,R3              RESTORE R14 TO ENTRY VALUE
         BR    R14                 RETURN TO IATSICA
***
*  CSECT CONSTANTS
***
LSYNCH   SYNCH MF=L                PARAMETER LIST FOR SVC 12 (SYNCH)
LENSYNCH EQU   *-LSYNCH            LENGTH OF SYNCH PARAMETER LIST
EXITKEY  DC    XL1'10'             KEY IN WHICH TO INVOKE EXIT (KEY 1)
*
         LTORG ,                   COLLECT LITERALS HERE
         AGO   .BYP03                                           AJSW057
APARNUM  DC    CL5'     '          APAR NUMBER
PTFNUM   DC    CL5'SP215'          PTF NUMBER
*        END   ,              ********************** DELETED BY AJSW057
.BYP03   ANOP                                                   AJSW057
