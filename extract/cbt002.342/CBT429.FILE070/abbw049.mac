++ USERMOD(ABBW049) /*   WDSC SUBMIT EXIT ROUTINE

   THIS PROGRAM WILL FORCE ALL JOBS SUBMITTED VIA TSO TO CONFORM
   TO STANDARD JOB NAME, I.E. JOB NAME TO BEGIN WITH VALID DIVISION
   CODE OR TSO USER ID.  IF JOB DOES NOT MEET THIS SPECIFICATION,
   THEN IT WILL NOT BE SUBMITTED AND A MESSAGE WILL BE SENT TO THE
   USER.
                                                         */ .
++ VER(Z038) FMID(HTE1402)  /* TSO/E REL 4 BASE */ .
++ JCLIN .
//GEN49 JOB Y51024509,'531-0 C SINGER'
//SG21  EXEC LINKS,
// PARM='XREF,LET,LIST,RENT,REUS,REFR',
//    UNIT='3350',SER=IPLXXX,N=SYS1,NAME=CMDLIB,P1=' ',
//    MOD=,P2=' ',CLASS=T
//LK.SYSPUNCH DD DUMMY
//SYSLIN DD *
 NAME IKJEFF10(R)
/*
++ SRC(IKJEFF10) DISTLIB(ACMDLIB) .
         TITLE 'IKJEFF10 -- WDSC SUBMIT EXIT ROUTINE'
IKJEFF10 CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
START    DS    0H                 START IT UP
         STM   R14,R12,12(R13)    SAVE REGISTERS
         BALR  R10,0              LOAD R10
         USING *,R10              USE R10 AS BASE REG
         LR    R9,R1              SAVE POINTER TO POINTER
         GETMAIN R,LV=SAVLGTH     GET SOME CORE
         LR    R11,R1             SET UP ADRESSABILITY
         USING WORK,R11           R8 POINTS TO SAVE AREA
         ST    R13,SAV+4          STORE R13
         LA    R14,SAV            GET ADDRESS TO SAVE AREA
         ST    R14,8(R13)         STORE IT AWAY
         LR    R13,R14            RESET R13
         SPACE 2
         L     R3,16              GET CVT POINTER
         L     R3,0(R3)           GET TCB WORDS
         L     R3,12(R3)          GET CURRENT ASCB POINTER
         L     R3,172(R3)         GET POINTER TO JOBNAME FIELD
         LTR   R3,R3              IS THIS A BATCH JOB?
         BNZ   RETURN             ..YES, ALLOW SUBMIT
         SPACE 2
         L     R9,0(R9)           SAVE POINTER TO SUBMIT DSECT
         USING IEDSECTD,R9        SET R9 POINTER TO SUBMIT DATA
         L     R7,IESUBCTP        LOAD R7 WITH ADDR SUB POINTER
         USING IESUBCTD,R7        MAKE R7 POINTER TO 2ND DSECT
         L     R5,IEMSGP          LOAD R5 WITH MESSAGE TO REPLY
         LTR   R5,R5              IS THERE A MESSAGE THERE?
         BZ    CHKJOB             KEEP LOOKING
         FREEMAIN R,LV=WORKLTH,A=(5)  FREE SAVE AREA
         LA    R15,IEABORT        TERMINATE SUBMIT COMMAND RC=16
         MVI   0(R7),IETJOB       RETURN ONLY JOB CARDS
         B     RETURN             GET OUT OF HERE
CHKJOB   DS    0H
         TM    IESTMTYP,IESCONTN  IS THIS A CONTINUATION?
         BO    ZERO               ..YES, DONT WANT TO SEE
         TM    IETAKEEX,IETJOB    IS THIS A JOB CARD?
         BO    CHKJOB1            ..YES, GO SEE IF VALID JOB NAME
ZERO     DS    0H
         MVI   0(R7),IETJOB       RETURN ONLY JOB CARDS
         LA    R15,IECONTIN       RETURN CODE OF ZERO
         B     RETURN             GET OUT OF HERE
CHKJOB1  DS    0H
         L     R3,IECARDP         LOAD R3 POINTER TO JCL CARD
         LR    R6,R3              SAVE POINTER TO JCL CARD
         LA    R3,2(R3)           BUMP POINTER TO JOBNAME
         LA    R4,PREEND          LOAD R4 TO LENGTH OF TABLE
         LA    R5,PERF            LOAD R5 TO START OF TABLE
         CLI   2(R6),C'$'         IS START OF JOB NAME $ FOR TSOID?
         BNE   CHKJOB2            ..NO, GO SEE IF VALID DIVISION CODE
         L     R2,IEUSRIDP        PUT ADDR OF USERID INTO R2
         CLC   0(3,R2),2(R6)      COMPARE JOBNAME AND USERID (3 CHAR)
         BE    ZERO               ..YES, LET JOB BE SUBMITTED
         B     INVALID            ..NO, GOT INVALID JOBNAME
CHKJOB2  DS    0H
         CLC   2(2,R6),0(R5)      IS DIVISION CODE IN TABLE?
         BE    ZERO               ..YES, LET JOB BE SUBMITTED
         LA    R5,2(R5)           BUMP TABLE POINTER UP NEXT ENTRY
         S     R4,=F'02'          SUBTRACT 2 BYTES FROM LGTH OF TBL
         LTR   R4,R4              SEE IF R4 ZERO
         BZ    INVALID            DIV CODE NOT IN TABLE
         B     CHKJOB2            LOOP
INVALID  DS    0H
         GETMAIN R,LV=WORKLTH     GET SOME STORAGE
         LR    R8,R1              SET R8 TO POINT AT STORAGE
         USING WORKAREA,R8        WORKAREA IS DSECT
         MVC   WORKLGTH,=X'0038'  MOVE LENGTH OF 56 FOR MESSAGE
         LA    R5,8               LOAD MAX LENGTH OF JOBNAME
         LR    R2,R3              LOAD R2 TO BEGINNING OF JOBNAME
LOOP     DS    0H
         CLI   0(R2),C' '         FIND A BLANK?
         BE    PRINT              ..YES, WRITE OUT INVALID JOBNAME
         LA    R2,1(R2)           BUMP POINTER BY ONE
         BCT   R5,LOOP            KEEP LOOPING
PRINT    DS    0H
         LA    R2,8               LOAD WITH MAXIMUM JOBNAME LENGTH
         SR    R2,R5              SUBTRACT # BYTES USED
         LTR   R2,R2              MAKE SURE R2 HAS SOMETHING IN IT
         BZ    WRITEIT            NO, IT IS EMPTY
         S     R2,=F'01'          SUBTRACT ONE FOR EXECUTE
WRITEIT  DS    0H
         MVC   WORKMSG,MSGCONST   MOVE IN INVALID MSG
         EX    R2,MOVE            MOVE JOBNAME TO MESSAGE
         ST    R8,IEMSGP          STORE POINTER TO MESSAGE AWAY
         LA    R15,IEMSG          SET RETURN CODE
         B     RETURN             RETURN
MOVE     MVC   WORKMSG+46(0),0(R3) MOVE IN JOBNAME
RETURN   DS    0H
         L     R13,SAV+4          RESTORE R13
         ST    R15,16(R13)        STORE RETURN CODE
         FREEMAIN R,LV=SAVLGTH,A=(11) FREE SAVE AREA
         LM    R14,R12,12(R13)    RESTORE REGISTERS
         BR    R14                RETURN
         EJECT
         DS    0F
PERF     DC    CL2'WC'           WESTERN CENTER
         DC    CL2'SD'           SAN DIEGO
         DC    CL2'PD'           POMONA
         DC    CL2'ED'           ELECTRONICS
         DC    CL2'DX'           DATAGRAPHIX
         DC    CL2'CW'           CORPORATE WIDE
         DC    CL2'GF'           GENERAL FIELD
         DC    CL2'GS'           GENERAL SERVICES
         DC    CL2'DS'           DSS
         DC    CL2'OH'           HOME OFFICE - DSS
         DC    CL2'FC'           CENTRAL CENTER
         DC    CL2'FS'           FINANCIAL SERVICES
         DC    CL2'FW'           FORT WORTH
         DC    CL2'VS'           VALLEY SYSTEMS
         DC    CL2'SS'           SPACE SYSTEMS
         DC    CL2'EC'           EASTERN CENTER
         DC    CL2'EB'           ELECTRIC BOAT
         DC    CL2'GD'           CORPORATE OFFICE
         DC    CL2'XC'           WDSC AND CONVAIR TOGETHER
         DC    CL2'YF'           CDSC AND FORT WORTH TOGETHER
         DC    CL2'ZE'           EDSC AND ELECTIC BOAT TOGETHER
         DC    CL2'AM'           AMSEA
         DC    CL2'AS'
         DC    CL2'C0'           SAN DIEGO
         DC    CL2'C1'           SAN DIEGO
         DC    CL2'C2'           SAN DIEGO
         DC    CL2'C3'           SAN DIEGO
         DC    CL2'C4'           SAN DIEGO
         DC    CL2'C5'           SAN DIEGO
         DC    CL2'C6'           SAN DIEGO
         DC    CL2'C7'           SAN DIEGO
         DC    CL2'C8'           SAN DIEGO
         DC    CL2'C9'           SAN DIEGO
         DC    CL2'CM'           SPACE SYSTEMS COMMERICAL LAUNCH
         DC    CL2'EP'           EL PASO SAND
         DC    CL2'FA'           ABILENE
         DC    CL2'FP'           FREEMAN UNITED COAL - PVT
         DC    CL2'LS'           LAND SYSTEMS
         DC    CL2'LL'           LASER SYSTEMS
         DC    CL2'MP'           MARBLE HEAD LIME - PVT
         DC    CL2'PP'           EL PASO SAND - PVT
         DC    CL2'RR'           FORE RIVER RAILROAD
         DC    CL2'SP'           MATERIAL SERVICE - PVT
         DC    CL2'TM'
         DC    CL2'CL'
         DC    CL2'CF'
         DC    CL2'CV'
         DC    CL2'ET'
         DC    CL2'WT'
         DC    CL2'AV'
         DC    CL2'EG'
         DC    CL2'EQ'
         DC    CL2'EL'
         DC    CL2'DY'
         DC    CL2'ER'
         DC    CL2'FR'
         DC    CL2'WF'
         DC    CL2'ML'
         DC    CL2'DB'
         DC    CL2'MS'
         DC    CL2'QY'
         DC    CL2'BP'
         DC    CL2'SC'
         DC    CL2'RC'
         DC    CL2'SF'
         DC    CL2'CA'           CAMDEN (ARKANSAS)
         DC    CL2'AR'
         DC    CL2'CH'
         DC    CL2'UE'
         DC    CL2'SM'
         DC    CL2'SL'
         DC    CL2'GA'
         DC    CL2'LC'
         DC    CL2'WP'
         DC    CL2'ES'
PREEND   EQU   *-PERF
MSGCONST DC    CL54'*** NON STANDARD JOBNAME, JOB NOT SUBMITTED - '
         EJECT
         LTORG
         EJECT
WORK     DSECT
         DS    0F
SAV      DS    18F   *** OS SAVE AREA ***
SAVLGTH  EQU   *-WORK
WORKAREA DSECT
WORKLGTH DS    CL2
WORKMSG  DS    CL54
WORKLTH  EQU   *-WORKLGTH
         IKJEFFIE IETYPE=SUBMIT   SUBMIT DSECT
         END
