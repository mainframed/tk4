++ USERMOD(ACFW003) /* ADD ACF2 LOGON POST VALIDATION EXIT

           THE LGNPXIT ROUTINE WILL GET THE VTAM NODE NAME OR
           TCAM LINE NAME AND ADD IT TO JOB CARD FOR A TSO USER
           SESSION. THIS WILL ALLOW THE SMF TYPE 35 RECORD
           TO CONTAIN INFORMATION ABOUT TERMINAL UTIIZATION.
                                                                 */ .
++ VER(Z038) FMID(ACF5000) .
++ JCLIN .
//GEN49 JOB WC07N0044,'531-1 CM SINGER'
//SG21  EXEC LINKS,
// PARM='XREF,LET,LIST,RENT',
//    UNIT='3350',SER=IPLXXX,N=SYS1,NAME=LPALIB,P1=' ',
//    MOD=,P2=' ',CLASS=T
//LK.SYSPUNCH DD DUMMY
//INCLUDE DD DSN=MVS3.INCLUDE,DISP=SHR
//SYSLIN DD *
 INCLUDE INCLUDE(LGNPXIT)
 NAME LGNPXIT(R)
/*
++ SRC(LGNPXIT) DISTLIB(ASRCLIB) .
LGNPXIT  TITLE '*** ACF2 LOGON POST VALIDATION EXIT *** LGNPXIT'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  ROUTINE NAME - LGNPXIT                                             *
*          DATE - 1 MAY 1981                                          *
*          BRUCE R. MINTON                                            *
*          GENERAL DYNAMICS - DATA SYSTEMS DIVISION                   *
*          P.O. BOX 85808                                             *
*          SAN DIEGO, CALIFORNIA 92138                                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 4
LGNPXIT  CSECT
         SAVE  (14,12),,*         SAVE FOR RETURN
         LR    R10,R15            COPY BASE REGISTER
         USING LGNPXIT,R10        ANNOUNCE ADDRESSIBILITY
         L     R12,4(R1)          GET LWA BUFFER ADDRESS
         L     R11,28(R1)         GET POINTER TO LWAJCL
         L     R12,0(R12)         GET POINTER TO LWA BUFFER
         LA    R3,1               SET COUNTER TO BEGINING
         LA    R2,0               SET DEFAULT RETURN CODE TO ZERO
LWACHK   DS    0H
         CLC   0(5,R12),RECON     IS THIS A RECONNECT?
         BE    FINI               ..YES, DONT DO ANYTHING
         LA    R12,1(R12)         BUMP POINTER BY ONE
         LA    R3,1(R3)           BUMP COUNTER BY ONE
         C     R3,=F'250'         ARE WE AT END OF SEARCH?
         BNE   LWACHK             ..NO, KEEP TRYING
         LR    R3,R0              GET POINTER TO ACVALD
         USING ACVALD,R3          ANNOUNCE ADDRESSIBILITY
         LA    R2,SMFWSIZ         GET SIZE OF WORKAREA
         A     R2,=F'7'           ROUND UP TO
         N     R2,=X'FFFFFFF8'    A DOUBLE WORD
         GETMAIN R,LV=(R2)        GETMAIN WORK AREA
         LR    R9,R1              SAVE POINTER
         USING SMFXLEN,R9         SET UP ADDRESSABILITY
         ST    R2,WORKLEN         SAVE GETMAINED LENGTH
         LA    R1,SMFSAVE         CHAIN
         ST    R1,8(R13)                THE
         ST    R13,4(R1)                    SAVE
         LR    R13,R1                            AREAS
         LA    R1,SMFXTME         BORROW TIME OF DAY FOR SVC 233
         XC    SMFXTME,SMFXTME    CLEAR (USE CURRENT ASCB)
         LA    R5,SMFXTID         GET ADDRESS OF TERMINAL ID
         ST    R5,SMFXDTE         SAVE IN ARG LIST
         MVI   SMFXTID,C' '       BLANK IT OUT JUST IN CASE
         MVC   SMFXTID+1(7),SMFXTID SVC 233 DOESNT WORK
         SVC   233                GO GET TERMINAL ID
         MVC   TERMID,SMFXTID     SAVE TERMINAL ID
         SPACE 3
LOGON    DS    0H
         L     R4,ACVRECB         POINT AT LOGONID BUFFER
         USING LIDREC,R4          ANNOUNCE ADDRESSIBILITY
         L     R6,0(R11)          POINT AT JCL
         L     R15,16             CVT POINTER
         L     R15,196(0,R15)     SMCA
         CLC   SYSID,16(R15)      IS IT SY1?
         BNE   JCLCARD2           ..NO, KEEP GOING
         TM    LIDU2FLG,LIDU2F4   IS TSOSY1 FLAG SET?
         BO    JCLCARD2           ..YES, KEEP GOING
         CLC   2(4,R6),OPER       IS IT OPERATIONS?
         BE    JCLCARD2           ..YES, KEEP GOING
         CLC   2(4,R6),HSS        IS IT HSS?
         BE    JCLCARD2           ..YES, KEEP GOING
         CLC   2(4,R6),IBM        IS IT IBM?
         BE    JCLCARD2           ..YES, KEEP GOING
         CLC   2(7,R6),USER1      IS IT SPECIAL USER?
         BE    JCLCARD2           ..YES, KEEP GOING
         CLC   2(7,R6),USER2      IS IT SPECIAL USER?
         BE    JCLCARD2           ..YES, KEEP GOING
         CLC   2(7,R6),USER3      IS IT SPECIAL USER?
         BE    JCLCARD2           ..YES, KEEP GOING
         CLC   2(7,R6),USER4      IS IT SPECIAL USER?
         BE    JCLCARD2           ..YES, KEEP GOING
         CLC   2(7,R6),USER5      IS IT SPECIAL USER?
         BE    JCLCARD2           ..YES, KEEP GOING
         TPUT  MSG1,72            WRITE ERROR MESSAGE
         LA    R2,4               SET RETURN CODE 4 FOR CANCEL
         B     RETURN             FINI, DO NOT WRITE SMF RECORD
         SPACE 2
JCLCARD2 DS    0H
         LA    R7,80(R6)          POINT AT 2ND JCL CARD
         LA    R8,80(R7)          POINT AT 3RD JCL CARD
         LA    R6,2(R6)           POINT AT JOBNAME ON JOBCARD
         LA    R5,61              MAX SEARCH LENGTH
         LA    R15,7              MAX NUMBER OF SEARCHES FOR JOBNAME
         MVC   SMFXUID,=CL8' '    BLANK OUT USERID JUST IN CASE
         LA    R1,SMFXUID         POINT AT USERID FOR SMF RECORD
JOBLOOP  DS    0H
         CLI   0(R6),C' '         IS IT A BLANK?
         BE    FINDJOB            ..YES, FOUND END OF JOBNAME
         MVC   0(1,R1),0(R6)      MOVE ONE BYTE AT A TIME
         LA    R6,1(R6)           BUMP POINTER TO NEXT BYTE
         LA    R1,1(R1)           BUMP COUNTER BY ONE
         BCT   R15,JOBLOOP        KEEP LOOKING FOR JOBNAME
FINDJOB  DS    0H
         CLC   0(6,R6),=C' JOB (' LOOKING FOR JOB CARD
         BE    CHCKACCT           GO CHECK ACCT INFO
         LA    R6,1(R6)           BUMP POINTER BY ONE
         BCT   R5,FINDJOB         KEEP LOOKING FOR JOB
         B     RETURN             CANNT FIND IT, FORGET IT
CHCKACCT DS    0H
         LA    R5,52              SET MAX LENGTH OF LINE TO CLEAR
         LA    R6,6(R6)           BUMP POINTER PAST JOB PARAMETER
         MVC   SMFXACT(8),0(R6)   GIVE 1ST 8 BYTES OF ACCT NUMBER
         LA    R15,14             SET MAX LENGTH TO FIND END OF ACCT
         LA    R1,0               CLEAR R1
NUMACCT  DS    0H
         CLC   0(2,R6),=C'),'     IS IT END OF ACCT NUMBER?
         BE    MOVCARDS           ..YES, GO MOVE JCL CARDS
         LA    R6,1(R6)           BUMP TO NEXT CHARACTER
         LA    R1,1(R1)           BUMP COUNTER BY 1
         BCT   R15,NUMACCT        MAX LENGTH OF NUMERIC PART
         B     RETURN             DID NOT FIND END OF ACCT
MOVCARDS DS    0H
         MVC   0(80,R8),0(R7)     MOVE 2ND JCL CARD TO 3RD JCL CARD
         MVI   2(R7),C' '         CLEAR FIRST BYTE
         MVC   3(77,R7),2(R7)     CLEAR 2ND JCL CARD
         SR    R5,R1              SUBTRACT LENGTH OF ACCT FIELD
         EX    R5,MOVENAM         MOVE NAME FIELD TO 2ND JCL CARD
         MVI   2(R6),C' '         CLEAR FIRST BYTE
         EX    R5,CLRNAME         CLEAR REST OF JCL CARD
         MVC   0(2,R6),=X'6B7D'   MOVE IN ,'
         MVC   2(8,R6),TERMID     MOVE TERMID FROM SVC 233
         MVC   10(3,R6),=X'7D5D6B' MOVE IN '),
DIALUP   DS    0H
         LA    R2,0               SET DEFAULT RETURN CODE TO ZERO
         CLC   TERMID(4),=C'TCAM' IS TERMINAL A DIALUP?
         BE    CHKDIAL            ..YES, SEE IF DIAL-UP AUTHORIZED
         CLC   TERMID(5),=C'WCK41' IS TERMINAL A DIALUP?
         BE    CHKDIAL            ..YES, SEE IF DIAL-UP AUTHORIZED
         CLC   TERMID(4),=C'SDX6' IS TERMINAL A DIALUP?
         BE    CHKDIAL            ..YES, SEE IF DIAL-UP AUTHORIZED
         CLC   TERMID(4),=C'SDX7' IS TERMINAL A DIALUP?
         BE    CHKDIAL            ..YES, SEE IF DIAL-UP AUTHORIZED
         B     SMFREC             GO PUMP OUT SMF RECORD
CHKDIAL  DS    0H
         TM    LIDI1FLG,LIDI1F1   IS USER DIAL-UP AUTHORIZED?
         BO    SMFREC             ..YES, GO PUT OUT SMF RECORD
         TPUT  MSG,72             WRITE ERROR MESSAGE
         LA    R2,4               SET RETURN CODE 4 FOR CANCEL
         B     RETURN             FINI, DO NOT WRITE SMF RECORD
SMFREC   DS    0H
         GTSIZE
         LTR   R0,R0              IS IT A 3270?
         BZ    RETURN             ..NO, ALL DONE
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        INITIALIZE SMF HEADER AND DATA AREA                          *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         NOP   RETURN             ESCAPE IF PROBLEMS
         LA    R3,SMFXSIZ         GET SMF RECORD LENGTH
         STH   R3,SMFXLEN         INITIALIZE RECORD LENGTH
         XC    SMFXSEG,SMFXSEG    CLEAR SEG DESCRIPTOR
         MVI   SMFXFLG,X'02'      INITILIZE SYSTEM INDICATOR
         MVI   SMFXRTY,201        INITIALIZE RECORD TYPE
         TIME  BIN                GET TIME
         ST    R0,SMFXTME         SAVE TIME OF DAY
         ST    R1,SMFXDTE         SAVE DATE
         L     R3,16              GET CVT POINTER
         L     R4,196(R3)         GET SMCA POINTER
         MVC   SMFXSID,16(R4)     MOVE IN SYSTEM ID
         LA    R0,1               REQUEST AUTHORIZATION ON
         SVC   212                SET AUTHORIZATION ON
         TESTAUTH FCTN=1          ARE WE AUTHORIZATED?
         LTR   R15,R15            CHECK RETURN
         BNZ   RETURN             ..NO, ALL DONE
         SPACE 2
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        MAKE THIS ADDRESS SPACE AS "NON INTERUPTABLE" AS POSSIBLE    *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SYSEVENT DONTSWAP        ISSUE SYSEVENT MACRO NOT TO SWAP
         SPACE 2
         STCK  SMFXTIM            CAPTURE CURRENT CLOCK
         LM    R4,R5,SMFXTIM      PUT IT IN REGS 4,5
         TPUT  CMD3270,80,FULLSCR,WAIT,HOLD   CLEAR THE SCREEN
         STCK  SMFXTIM            CAPTURE CURRENT CLOCK
         LTR   R15,R15            WAS TPUT ANY GOOD ?
         BNZ   OKSWAP             NO - DON'T WRITE SMF RECORD
         SPACE 2
         LM    R6,R7,SMFXTIM      PUT IT IN REGS 6,7
         SLR   R6,R4              SUBTRACT MOST SIGNIFICANT HALF
         SLR   R7,R5              SUBTRACT LEASE SIGNIFICANT HALF
         BNM   *+8                RESULT > 0 SO WE DON'T NEED TO ADJUST
         SL    R6,=F'1'           ADJUST FOR NEGATIVE VALUE
         SRDL  R6,12              CONVERT TO MICROSECONDS
         AL    R7,=F'500'         SETUP FOR ROUND UP
         D     R6,=F'1000'        CONVERT TO MILLISECONDS
         ST    R7,SMFXTIM         SAVE TIME DELTA
         SPACE 2
         SMFWTM SMFXLEN           WRITE THE SMF RECORD
OKSWAP   DS    0H
         SYSEVENT OKSWAP          ISSUE SYSEVENT MACRO TO ALLOW SWAP
         XR    R0,R0              REQUEST AUTHORIZATION OFF
         SVC   212                SET AUTHORIZATION OFF
         EJECT
RETURN   DS    0H                 FINI
         L     R13,4(R13)         BACK CHAIN SAVE AREAS
         L     R0,WORKLEN         LOAD GOTTEN CORE LENGTH
         FREEMAIN R,LV=(0),A=(9)  FREE GOTTEN CORE
FINI     DS    0H
         LR    R15,R2             LOAD RETURN CODE
         RETURN (14,12),RC=(15)   GIVE RETURN CODE ZERO
MOVENAM  MVC   3(0,R7),2(R6)      MOVE NAME TO THIRD JCL CARD
CLRNAME  MVC   3(0,R6),2(R6)      MOVE SPACES TO REST OF JCL CARD
         LTORG
ZEROS    DC    PL8'0'             DEFAULT ZERO ACCT NUMBER
OPER     DC    CL4'$WC4'          OPERATIONS
HSS      DC    CL4'$WC3'          HSS
IBM      DC    CL4'$WCI'          IBM
SYSID    DC    CL4'W190'          SYSTEM ID
USER1    DC    CL8'$WC9JWW '      SPECIAL USER
USER2    DC    CL8'$WCPCMS '      SPECIAL USER
USER3    DC    CL8'$WC6RMC '      SPECIAL USER
USER4    DC    CL8'$WC2DBA '      SPECIAL USER
USER5    DC    CL8'$WC2DB2 '      SPECIAL USER
RECON    DC    CL5'RECON'         RECONNECT PARM
MSG      DC    CL72'USER NOT AUTHORIZED FOR DIAL-UP, SESSION CANCELLED'
MSG1     DC    CL72'NOT AUTHORIZED FOR TSO ON SY1, SESSION CANCELLED  '
CMD3270  DC    XL8'F31140403C404000' TPUT PARMS
         DC    24XL3'114040'         TPUT PARMS
         ACFREGS                  REGISTER EQUATES
         ACVALD                   SVC PARAMETER LIST
         LIDREC                   LOGONID RECORD
SMFWORK  DSECT
SMFXLEN  DS    H                  RECORD LENGTH
SMFXSEG  DS    H                  SEGMENT DESCRIPTOR
SMFXFLG  DS    XL1                SYSTEM INDICATOR (X'02')
SMFXRTY  DS    XL1                RECORD TYPE 201 (X'C9')
SMFXTME  DS    XL4                TIME IN 1/100 SEC RECORD WRITTEN
SMFXDTE  DS    XL4                DATE RECORD WRITTEN
SMFXSID  DS    XL4                SYSTEM IDENTIFICATION
*
SMFXUID  DS    XL8                USERID
SMFXACT  DS    XL8                DPAP NUMBER (PACKED DECIMAL)
SMFXTID  DS    XL8                TERMINAL ID
SMFXTIM  DS    XL4                TIME DELTA (IN MILLISECONDS)
*
SMFXSIZ  EQU   *-SMFXLEN          LENGTH OF SMF RECORD
SMFSAVE  DS    18F                SAVE AREA
TERMID   DS    CL8                TERMID FROM SVC 233
WORKLEN  DS    1F                 LENGTH OF GETMAINED AREA
SMFWSIZ  EQU   *-SMFXLEN          LENGTH OF WORK AREA
         END   LGNPXIT
