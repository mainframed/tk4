++ USERMOD(ABBW019) /* SMF STEP INITIATION EXIT:

  THIS MODIFICATION INSTALLS THE IEFUSI SMF EXIT WHICH WILL
  SUPPORT REGION LIMIT VALUES INSTEAD OF USING IEALIMIT.
                                                    */ .
++ VER(Z038) FMID(HBB2102)     /*   MVSXA VER 2     */ .
++ JCLIN .
//GEN11 JOB WC07N0323,'W1-5311  CR SINGER'
//SG21 EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,REUS,RENT',
//    UNIT='3350',SER=IPLXXX,N=SYS1,NAME=LPALIB,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//SYSLIN DD *
  MODE AMODE(31) RMODE(ANY)
  ENTRY  IEFUSI
  NAME IEFUSI(R)
/*
++ SRC(IEFUSI) DISTLIB(ASAMPLIB) DISTOBJ(AOSB3).
IEFUSI TITLE '*** SMF STEP INITIATION EXIT *** IEFUSI'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  ROUTINE NAME - IEFUSI                                              *
*          DATE - 12 JAN 1988                                         *
*          JERRY BECKSTRAND                                           *
*          GENERAL DYNAMICS - DATA SYSTEMS DIVISION                   *
*          P.O. BOX 85808                                             *
*          SAN DIEGO, CALIFORNIA 92138                                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 4
         ACFREGS
IEFUSI CSECT
       AMODE 31
       RMODE ANY
         SAVE  (14,12),,IEFUSI_&SYSDATE._&SYSTIME.
*        R1 ADDRESS OF INPUT PARAMETERS
*        R3 ADDR OF COMMON EXIT PARAMETER AREA
*        R4 ADDR OF AN EIGHT BYTE AREA CONTAINING JOB STEP NAME
*        R5 ADDR OF AN EIGHT BYTE AREA CONTAINING JOB PROGRAM NAME
*        R6 ADDR OF AREA CONTAINING ACCOUNT INFO
*        R7 SIX WORD AREA CONTAINING REGION LIMIT VALUES
*        R8 ADDR OF A WORD CONTAINING V=R FLAG
*
         LR    R10,R15                 BASE REGISTER
         USING IEFUSI,R10              SET UP ADDRESSABILITY
         LM    R3,R8,0(R1)             PARM LIST ADDRESSES
*
* GET BEGINNING ADDRESS OF VSM PARAMETER LIST
*     VSM PARAMETER LIST POINTED TO BY R7
*
         USING VSMPARM,R7              LOAD R7 ADDR VSM PARAMETE LIST
         OI    VSMFLAG,X'80'           SET HI-ORDER BIT TO 1
*
*  SET BATCH DEFAULTS ABOVE 16 MEGABYTES TO 32 MEGABYTES .
*
         MVC   VSMSIZA,RMAX32MG        REGION SIZE  ABOVE 16MG TO 32MG
         MVC   VSMLIMA,RMAX32MG        REGION LIMIT ABOVE 16MG TO 32MG
*
         SLR   R9,R9                   GET PSA POINTER AT LOCATION 0
         USING PSA,R9                  ADDRESSABILITY
         L     R9,PSAAOLD              GET ASCB POINTER
         USING ASCB,R9                 ADDRESSABILITY
         L     R2,ASCBJBNI             GET POINTER TO JOBNAME FIELD
         LTR   R2,R2                   IS IT A BATCH JOB?
         BZ    CHKTSO                  ..NO, SEE IF IS TSO
         B     RETURN                  ..YES, RETURN
CHKTSO   DS    0H
         L     R9,ASCBOUCB             GET POINTER TO OUCB
         USING OUCB,R9                 ADDRESSABILITY
         TM    OUCBYFL,X'20'           IS IT A TSO TASK?
         BZ    RETURN
TSO      DS    0H
*
*   SET TSO   REGION ABOVE 16MG TO  4MG
*
         MVC   VSMSIZA,RMAX4MG         REGION SIZE  ABOVE 16MG TO 4MG
         MVC   VSMLIMA,RMAX4MG         REGION LIMIT ABOVE 16MG TO 4MG
*
RETURN   DS    0H
*
*   ADD 128K TO REQUEST REGION SIZE
*
         L     R2,VSMSZREQ          GET REQUEST REGION SIZE
         AL    R2,N128K          ADD 128K TO REQUESTED REGION SIZE
         ST    R2,VSMSZREQ          STORE NEW REGION SIZE
*
         DS    0H
         LA    R2,0                    INSTRUCTION TO ZAP FOR TESTING
         RETURN (14,12),RC=0           ALL DONE
*
*        DATA AREA
*
RMAX4MG  DC    F'4194304'
RMAX32MG DC    F'33554432'
N128K    DC    F'131072'
         DS    0F
VSMPARM  DSECT
VSMFLAG  DS    F   VSM FLAG WORK
VSMSZREQ DS    F   REQUEST REGION
VSMLIMB  DS    F   REGION LIMIT BELOW 16MG
VSMSIZB  DS    F   REGION SIZE BELOW 16MG
VSMLIMA  DS    F   REGION LIMIT ABOVE 16MG
VSMSIZA  DS    F   REGION SIZE ABOVE 16MG
         EJECT
         LTORG
         IHAPSA
         IHAASCB
         IRAOUCB
         END IEFUSI
