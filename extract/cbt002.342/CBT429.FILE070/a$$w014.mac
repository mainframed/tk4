++USERMOD (A$$W014)   /*

   THIS USERMOD IS TO INSTALL THE TSO CLR3270 COMMAND (ALIAS CLEAR).
   THIS PROGRAM WILL CLEAR ANY 3270 SCREEN AND THEN WRITE OUT AN
   SMF RECORD (TYPE 201) REPORTING THE TIME REQUIRED TO DO IT.
                                                   09/01/88 BRM  */ .
++VER (Z038) FMID(A$$PGMS) PRE(A$$W006,A$$W008) .
++JCLIN .
//A$$W014 JOB WC07N0323,'W1-5311  CR SINGER'
//LPAPGM  EXEC LINKS,
// PARM='NCAL,LIST,XREF,LET,REUS,RENT,AC=1',
//    UNIT='3380',SER=IPLXXX,N=SYS2,NAME=HSSLPA,P1=' ',
//    MOD=,P2=' ',OBJ=OBJPDS01,CLASS=T
//SYSLIN   DD  *
   INCLUDE SYSPUNCH(CLR3270)
   ALIAS CLEAR
   NAME CLR3270(R)
/*
++SRC (CLR3270)  DISTLIB(ASRCLIB) .
         MACRO
         REGS
         LCLA  &R
.R       AIF   (&R EQ 16).END
R&R      EQU   &R
&R       SETA  &R+1
         AGO   .R
.END     MEND
         EJECT
***********************************************************************
*                                                                     *
*        CLR3270 :                                                    *
*                                                                     *
*        FUNCTION OF THIS COMMAND IT TWO FOLD: (1) PERFORM A          *
*        SCREEN CLEAR ON ANY 3270 AND (2) REPORT WITH AN SMF          *
*        RECORD (TYPE 201) THE TIME REQUIRED TO DO IT (A MEA-         *
*        SURE OF THE SO CALLED "NETWORK DELAY" SINCE THE TPUT         *
*        IS DONE WITH THE "HOLD" OPTION).                             *
*                                                                     *
***********************************************************************
*
CLR3270  CSECT
         PRINT NOGEN
         REGS
         SAVE  (14,12),,*         SAVE REGS
         LR    R10,R15            SET UP BASE REGISTER
         USING CLR3270,R10        GIVE ADDRESSABILITY
         GTSIZE
         LTR   R0,R0              IS IT A 3270?
         BZ    EXIT               ..NO, RETURN
         SR    R8,R8              SET TPUT SWITCH
*
         LA    R2,SMFWSIZ         GET SIZE OF WORKAREA
         A     R2,=F'7'           ROUND UP TO
         N     R2,=X'FFFFFFF8'    A DOUBLE WORD
       GETMAIN R,LV=(R2)          GETMAIN WORK AREA
         LR    R9,R1              SAVE POINTER
         USING SMFXLEN,R9         SET UP ADDRESSABILITY
         LA    R1,SMFSAVE         CHAIN
         ST    R1,8(R13)                THE
         ST    R13,4(R1)                    SAVE
         LR    R13,R1                            AREAS
*
*        INITIALIZE SMF HEADER AND DATA AREA
*
         LA    R3,SMFXSIZ         GET SMF RECORD LENGTH
         STH   R3,SMFXLEN         INITIALIZE RECORD LENGTH
         XC    SMFXSEG,SMFXSEG    CLEAR SEG DESCRIPTOR
         MVI   SMFXFLG,X'02'      INITILIZE SYSTEM INDICATOR
         MVI   SMFXRTY,201        INITIALIZE RECORD TYPE
         LA    R1,SMFXTME         BORROW TIME OF DAY FOR SVC 233
         XC    SMFXTME,SMFXTME    CLEAR (USE CURRENT ASCB)
         LA    R3,SMFXTID         GET ADDRESS OF TERMINAL ID
         ST    R3,SMFXDTE         SAVE IN ARG LIST
         MVI   SMFXTID,C' '       BLANK IT OUT JUST IN CASE
         MVC   SMFXTID+1(7),SMFXTID SVC 233 DOESNT WORK
         SVC   233                GO GET TERMINAL ID
         TIME  BIN                GET TIME
         ST    R0,SMFXTME         SAVE TIME OF DAY
         ST    R1,SMFXDTE         SAVE DATE
         L     R3,16              GET CVT POINTER
         L     R4,196(R3)         GET SMCA POINTER
         MVC   SMFXSID,16(R4)     MOVE IN SYSTEM ID
         L     R3,0(R3)           GET TCB WORDS
         L     R4,12(R3)          GET CURRENT ASCB POINTER
         L     R4,176(R4)         GET TSONAME POINTER
         MVC   SMFXUID,0(R4)      MOVE IN USERID
         L     R4,4(R3)           GET CURRENT TCB POINTER
         L     R4,164(R4)         GET TCT POINTER
         L     R4,28(R4)          GET JMR POINTER
         MVC   SMFXACT,20(R4)     MOVE IN DPAP NUMBER
*
*        MAKE THIS ADDRESS SPACE AS "NON INTERUPTABLE" AS POSSIBLE
*
         XC    SMFAUTH,SMFAUTH    CLEAR AUTH (DEFAULT IS OFF)
      TESTAUTH FCTN=1             ARE WE AUTHORIZED ALREADY ?
         LTR   R15,R15            CHECK RETURN
         BNZ   *+10               NO - WE MUST UNAUTHORIZE IT AT END
         MVC   SMFAUTH,=F'1'      SET AUTH STATE FLAG TO ON
*
         STAX  DEFER=YES          DEFER ATTENTION INTERRUPTS
         LTR   R15,R15            CHECK FOR STAX ERROR
         BNZ   BYPASS             EXIT, STAX ERROR
*
         ESTAE ESTAEX,PARAM=(R9),MF=(E,SMFESTL) ESTABLISH ESTAE
         LTR   R15,R15            CHECK ESTAE ERROR
         BNZ   STAXOFF            EXIT, ESTAE ERROR
*
         LA    R0,1               REQUEST AUTHORIZATION ON
         SVC   212                SET AUTHORIZATION ON
      TESTAUTH FCTN=1             ARE WE AUTHORIZED ?
         LTR   R15,R15            CHECK RETURN
         BNZ   ESTAEOFF           NO - SOMETHING WRONG - BYPASS
*
      SYSEVENT DONTSWAP           ISSUE SYSEVENT MACRO
*
         L     R0,SMFAUTH         RESTORE AUTHORIZATION
         SVC   212                SET AUTHORIZATION
*
*        ISSUE TPUT (HOLD) AND TIME IT
*
         STCK  SMFXTIM            CAPTURE CURRENT CLOCK
         LM    R4,R5,SMFXTIM      PUT IT IN REGS 4,5
         TPUT  CMD3270,80,FULLSCR,WAIT,HOLD   CLEAR THE SCREEN
         STCK  SMFXTIM            CAPTURE CURRENT CLOCK
         LA    R8,1               SET SWITCH TO INDICATE TPUT DONE
         LTR   R15,R15            WAS TPUT ANY GOOD ?
         BNZ   ESTAEOFF           NO - DON'T DROP SMF RECORD
*
         LM    R6,R7,SMFXTIM      PUT IT IN REGS 6,7
         SLR   R6,R4              SUBTRACT MOST SIGNIFICANT HALF
         SLR   R7,R5              SUBTRACT LEASE SIGNIFICANT HALF
         BNM   *+8                RESULT > 0 SO WE DON'T NEED TO ADJUST
         SL    R6,=F'1'           ADJUST FOR NEGATIVE VALUE
         SRDL  R6,12              CONVERT TO MICROSECONDS
         AL    R7,=F'500'         SETUP FOR ROUND UP
         D     R6,=F'1000'        CONVERT TO MILLISECONDS
         ST    R7,SMFXTIM         SAVE TIME DELTA
*
*        WRITE OUT SMF RECORD AND SET OKSWAP
*
         LA    R0,1               REQUEST AUTHORIZATION ON
         SVC   212                SET AUTHORIZATION ON
        SMFWTM SMFXLEN            WRITE THE SMF RECORD
      SYSEVENT OKSWAP             ISSUE SYSEVENT MACRO
         L     R0,SMFAUTH         RESTORE AUTHORIZATION
         SVC   212                SET AUTHORIZATION
ESTAEOFF ESTAE 0                  CANCEL ESTAE ENVIRONMENT
STAXOFF  STAX  DEFER=NO           ALLOW ATTENTION INTERRUPTS
*
BYPASS   L     R13,4(R13)         BACK CHAIN SAVE AREAS
      FREEMAIN R,LV=(R2),A=(R9)   FREEMAIN WORK AREA
         DROP  R9
         LTR   R8,R8              TEST TPUT SWITCH
         BNZ   EXIT               YES - TPUT WAS DONE
         TPUT  CMD3270,8,FULLSCR,WAIT,HOLD   TPUT NOT DONE - DO IT NOW
EXIT    RETURN (14,12),RC=0       RETURN
         DROP  R10
         EJECT
*
*        ESTAE EXIT ROUTINE
*
ESTAEX   DS    0H                 ESTAE ADDRESSABILITY
         LR    R10,R15            SET UP BASE REGISTER
         USING ESTAEX,R10         GIVE ADDRESSABILITY
         LR    R11,R14            SAVE RETURN ADDRESS
         C     R0,=F'12'          IS THERE A SDWA
         BNE   SDWA               YES - USE IT
         LR    R9,R2              SAVE POINTER
         B     *+8                AND SKIP OVER SDWA
SDWA     L     R9,0(R1)           LOAD REG 9 FROM SDWAPARM
         USING SMFXLEN,R9         SET UP ADDRESSABILITY
         LA    R0,1               REQUEST AUTHORIZATION ON
         SVC   212                SET AUTHORIZATION ON
      TESTAUTH FCTN=1             ARE WE AUTHORIZED ?
         LTR   R15,R15            CHECK RETURN
         BNZ   AUTHOFF            NO - BYPASS
      SYSEVENT OKSWAP             ISSUE SYSEVENT MACRO
AUTHOFF  LA    R0,0               SET DEFAULT TO UNAUTHORIZE
         LTR   R9,R9              DID WE GET A PARM AREA ?
         BZ    *+8                NO - THEN ASSUME UNAUTHORIZED
         L     R0,SMFAUTH         RESTORE AUTHORIZATION
         SVC   212                SET AUTHORIZATION
         STAX  DEFER=NO           ALLOW ATTENTION INTERRUPTS
         XR    R15,R15            CONTINUE WITH ABEND
         LR    R14,R11            RESTORE REG 14
         B     R14                RETURN
         DROP  R10,R9
*
CMD3270  DC    XL8'F31140403C404000' TPUT PARMS
         DC    24XL3'114040'         TPUT PARMS
         LTORG
         EJECT
SMFWORK  DSECT
SMFXLEN  DS    H                  RECORD LENGTH
SMFXSEG  DS    H                  SEGMENT DESCRIPTOR
SMFXFLG  DS    XL1                SYSTEM INDICATOR (X'02')
SMFXRTY  DS    XL1                RECORD TYPE 201 (X'C9')
SMFXTME  DS    XL4                TIME IN 1/100 SEC RECORD WRITTEN
SMFXDTE  DS    XL4                DATE RECORD WRITTEN
SMFXSID  DS    XL4                SYSTEM IDENTIFICATION
*
SMFXUID  DS    XL8                USERID
SMFXACT  DS    XL8                DPAP NUMBER (PACKED DECIMAL)
SMFXTID  DS    XL8                TERMINAL ID
SMFXTIM  DS    XL4                TIME DELTA (IN MILLISECONDS)
*
SMFXSIZ  EQU   *-SMFXLEN          LENGTH OF SMF RECORD
SMFSAVE  DS    18F                SAVE AREA
SMFESTL  ESTAE ESTAEX,MF=L        ESTAE PARAMETER LIST
SMFAUTH  DS    F                  AUTHORIZATION CODE SAVE AREA
SMFWSIZ  EQU   *-SMFXLEN          LENGTH OF WORK AREA
         END   CLR3270
