++ USERMOD(ACFW001) /* ADD ACF2 PREVALIDATION  EXIT ROUTINE

        1. THIS ROUTINE WILL VALIDATE A USER'S AUTHORITY TO ALLOCATE
           A NEW DATASET ON A GIVEN VOLUME VIA THE GENERALIZED
           RESOURCE FACILITY TYPE DSD RESOURCE RULES.  FOR EXAMPLE,
           TO ALLOW ALL TSO USERS TO ALLOCATE ON THE TSO PACKS, THE
           FOLLOWING RULE WOUD APPLY:
            $KEY(TSO***) TYPE(DSD)
             UID($) ALLOW
        2. THIS ROUTINE WILL ALSO ALLOW ALL STARTED TASKS WITH THE
           DEFAULT ID OF @STC TO HAVE ACCESS WITHOUT LOGGING.
                                                                 */ .
++ VER(Z038) FMID(ACF5000) .
++ JCLIN .
//GEN49 JOB WC07N0044,'531-1 CM SINGER'
//SG21  EXEC LINKS,
// PARM='XREF,LET,LIST,RENT',
//    UNIT='3350',SER=IPLXXX,N=SYS1,NAME=LPALIB,P1=' ',
//    MOD=,P2=' ',CLASS=T
//LK.SYSPUNCH DD DUMMY
//INCLUDE DD DSN=MVS3.INCLUDE,DISP=SHR
//SYSLIN DD *
 INCLUDE INCLUDE(VLDEXIT)
 NAME VLDEXIT(R)
/*
++ SRC(VLDEXIT) DISTLIB(ASRCLIB) .
         TITLE     'VLDEXIT -- ACF RULE PRE VALIDATION EXIT'
         ACSXP                    MAP EXIT PARAMETER LIST
         ACUCB                    USER CONTROL BLOCK
         ACFREGS                  REGISTER EQUATES
WORKAREA DSECT
         ACGRSRC  DSECT=NO        GENERALIZED RESOURCE PARM LIST
WORKMSG  DS    CL128              ERROR MESSAGE AREA
WORKLEN  EQU   *-WORKAREA         MAY NOT EXCEED 256 BYTES
         SPACE 2
         ACCVT                    ACF2 CVT
         EJECT
VLDEXIT  CSECT
         SAVE  (14,12),,*         SAVE FOR RETURN
         LR    R11,R15            COPY BASE REGISTER
         USING VLDEXIT,R11        ANNOUNCE ADDRESSIBILITY
         LR    R10,R1             COPY PARAMETER LIST POINTER
         USING ACSXP,R10          ANNOUNCE ADDRESSIBILITY
         TM    ACUUFLG1,ACUU1ABE  ERROR CONDITION?
         BO    RETURN0            ..YES, FINI
         CLI   ACUUFLG3,ACUU3PGM  PROGRAM NAME VALIDATION?
         BE    RETURN0            ..YES, FINI
         L     R5,ACUUAUCB        POINT AT ACUCB
         USING ACUCB,R5           ANNOUNCE ADDRESSIBILITY
LOOKUP   DS    0H
         LA    R6,TABLE           LOAD TABLE ADDRESS
CHKID    DS    0H
         CLC   ACULID(8),DFLTLID      CHECK FOR STC DFLT LID=@STC
         BNE   TABEND             ..NO,PROCEED AS NORMAL ACCESS
LOOP     CLI   0(R6),X'FF'        TEST FOR END OF TABLE
         BE    TABEND             BRANCH IF END OF TABLE
CKNAME   CLC   ACUSJOBN(8),0(R6)    COMPARE JES NAME WITH TABLE ENTRIES
         BE    RETURN4            ..YES, ALLOW IMMEDIATE ACCESS
         LA    R6,8(R6)           INCREMENT INTO TABLE
         B     LOOP
TABEND   DS    0H
         DROP  R5                 DROP ADDRESSIBILITY
         CLI   ACUUFLG3,ACUU3DSD  NEW DASDM ALLOCATION?
         BNE   RETURN0            ..NO, PROCEED AS NORMAL
         SPACE 3
DASDCHK  DS    0H
         L     R4,ACUUWORK        POINT AT WORKAREA ADDRESS
         USING WORKAREA,R4        ADDRESS TO BUILD VALIDATION PARM LIST
         XC    ACGRSRC(ACGRSLEN),ACGRSRC  CLEAR IT
         MVI   ACGFCN,4           GENERALIZED RSRC FCTN CODE
         MVI   ACGSFCN,ACGSINP    INTERPRET CALL SUB FCTN
         LA    R0,WORKMSG         POINT TO ERROR MESSAGE AREA
         ST    R0,ACGMSG          PUT ADDR INTO PARM BLOCK
         MVC   ACGRTYPE,=C'RDSD'  RSRC TYPE = DSD
         MVI   ACGRNAME,C' '      BLANK NAME FIELD
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME
         L     R1,ACUUVOL         GET VOLSER ADDRESS
         MVC   ACGRNAME(6),0(R1)  MAKE VOLSER RSRC NAME
         MVC   ACGUCB,ACUUAUCB    PASS ON USER IDENT
         MVC   ACGMODID,=CL8'VLDEXIT '  MODULE NAME FOR RPTS
         SPACE 1
         L     R3,ACUUACVT        GET ACF2 CVT ADDRESS
         USING ACCVT,R3           ADDRESS ACF2 CVT
         ACFSVC ACGRSRC,TYPE=A,CVT=HAVE  ISSUE SVC VALIDATION CALL
         LTR   R15,R15            IS ALLOCATION OK?
         BZ    RETURN0            ..YES, ALL DONE
         SPACE 1
         MVI   WORKMSG+2,X'80'    MCS FLAG
         LH    R1,WORKMSG         MESSAGE TEXT LENGTH
         LA    R1,WORKMSG(R1)     NEXT AVAILABLE CHARACTER
         MVC   0(4,R1),ERROUTE    COPY ROUTE AND DESC CODES
         WTO   MF=(E,WORKMSG)     WRITE ACF2 ERROR MSG
         MVC   WORKMSG(ERRMSGL),ERRMSG LOCAL MESSAGE PROTOTYPE
         MVC   ERRVOL-ERRMSG+WORKMSG,ACGRNAME  INCLUDE VOLSER
         WTO   MF=(E,WORKMSG)     WRITE ADDL ERROR MESSAGE
         B     RETURN8            TERMINATE..NOTE LOG DONE BY RSRC CALL
         SPACE 3
RETURN0  DS    0H                 FINI
         RETURN (14,12),RC=0      GIVE RETURN CODE ZERO
         SPACE 3
RETURN4  DS    0H                 FINI
         RETURN (14,12),RC=4      GIVE RETURN CODE FOUR - ALLOW STC
         SPACE 3
RETURN8  DS    0H                 FINI
         RETURN (14,12),RC=8      GIVE RETURN CODE EIGHT FOR ABORT
         SPACE 3
ERRMSG   DC    AL2(ERRMSGL-4),X'8000'  MESSAGE HEADER
         DC    C'         NEW DATASET ALLOCATION FOR VOLUME '
ERRVOL   DC    CL6'XXXXXX'        ROOM FOR VOLUME SERIAL NUMBER
         DC    C' DENIED'
ERROUTE  DC    X'00000020'        DESC=0, ROUTE=11
ERRMSGL  EQU   *-ERRMSG           LENGTH OF MESSAGE
         SPACE 2
DFLTLID  DC    CL8'@STC    '
TABLE    DS    0D
         DC    CL8'ACF2'
         DC    CL8'ADMPRINT'
         DC    CL8'AP1 '
         DC    CL8'APSWPROC'
         DC    CL8'ARTSINIU'
         DC    CL8'CLRDUMP'
         DC    CL8'CMD1'
         DC    CL8'DB2TDBM1'
         DC    CL8'DB2TMSTR'
         DC    CL8'D'
         DC    CL8'DEALLOC'
         DC    CL8'DUMPSRV'
         DC    CL8'HSMS1 '
         DC    CL8'HSMS2 '
         DC    CL8'HSMS3 '
         DC    CL8'HSMS4 '
         DC    CL8'IEESYSAS'
         DC    CL8'IEEVMPCR'
         DC    CL8'IKJACCNT'
         DC    CL8'INIT'
         DC    CL8'INTRDR'
         DC    CL8'INCORZAP'
         DC    CL8'IRLM'
         DC    CL8'JES2 '
         DC    CL8'JES3 '
         DC    CL8'JES4 '
         DC    CL8'JES5 '
         DC    CL8'JES7 '
         DC    CL8'JESN '
         DC    CL8'JRLMPROC'
         DC    CL8'M204PJCL'
         DC    CL8'M204PROD'
         DC    CL8'M204TJCL'
         DC    CL8'M204TEST'
         DC    CL8'LLA  '
         DC    CL8'LOCAL'
         DC    CL8'NET  '
         DC    CL8'PAGEADD'
         DC    CL8'PANEIPL'
         DC    CL8'RMF  '
         DC    CL8'RMFREP'
         DC    CL8'RMFGAT'
         DC    CL8'SETCACHE'
         DC    CL8'SMFDMPS1'
         DC    CL8'SMFDMPS2'
         DC    CL8'SMFDMPS3'
         DC    CL8'SMFDMPS4'
         DC    CL8'SYS1DUMP'
         DC    CL8'TSMSHOW '
         DC    CL8'TSMSTART'
         DC    CL8'TSMSTOP '
         DC    CL8'TSO  '
         DC    CL8'TSSO '
         DC    CL8'VFETCH'
         DC    CL8'VFETCHP'
         DC    CL8'VFETCHT'
         DC    CL8'WCICOM  '
         DC    CL8'WCLOGON '
         DC    CL8'WCLOGON1'
         DC    CL8'WCLOGON2'
         DC    CL8'WCLOGON3'
         DC    CL8'WCLOGON4'
         DC    CL8'WC0TA07'
         DC    CL8'WC0TA0B'
         DC    CL8'WC0TA0G'
         DC    CL8'WC0TA02'
         DC    CL8'WCUCC7 '
         DC    CL8'NETEXTST'
         DC    X'FF'
         END   VLDEXIT
