TSSYSTEM TITLE 'S Y S T E M   C O N F I G U R A T I O N   DISPLAY'
*FROM CBT#213 FILE300 30NOV83
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                                                                     *
*TSSYSTEM IS THE OS/MVS VERSION OF THE SYSTEM CONFIGURATION DISPLAY   *
*     COMMAND.  IT SHOWS A TSO USER THE OPERATING SYSTEM, RELEASE,    *
*     CPU MODEL, SERIAL NUMBER(S), AND MAIN STORAGE SIZE, AND DATE.   *
*     TIME SINCE IPL (OR LAST SET IPS)
*NOTE: FOR MVS/XA AMODE SHOULD BE 31 C.HUNTER 5SEP84
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
         MACRO
         $MSG  &P=B
.*       ***** MACRO USED TWICE, ONCE TO MAP CONSTANT DATA
.*       ***** THEN IN DSECT FOR RENT-DYNAMIC STOR
&P.TYPE  DC    CL1'R'
&P.RLSE  DC    CL2'03'
         DC    C'.'
&P.LEVL  DC    CL2'0A'
         DC    C' '
&P.MODL  DC    CL4'S370'
         DC    C' #'
&P.SNUM  DC    CL6'/UNKN'
&P.CORE  DC    X'402020202120',C'K '
         DC    C'IPL-'
&P.IPLV  DC    CL6'SYSRES'
&P.HH    DC    X'40202120'         _HHH
         DC    C'=HRS(MASTER TRAN TIME) '
&P.DATE  DC    X'21204B202020' YY.DDD
         DC    C'=TODAY'
         MEND
         SPACE 2
TSSYSTEM @ENT  R11,(,DSAEND-DSA),DSA=YES
TSSYSTEM AMODE 31                      RMCT/OUCB ABOVE 16M
*SYSMVS  CSECT
*        SPACE
*        JVDENTER DSA=(DSAEND-DSAWRK),VERSION=&SYSPARM
*        USING    DSAWRK,R13
         EJECT
         L     R10,CVTPTR
         USING CVT,R10
         L     R3,CVTSYSAD
         LA    R5,0(R10)
         S     R5,F8
         MVC   BUFFER(BUFEND-BUFFER),MASK
         MVC   BRLSE,4(R5)
         MVC   BLEVL,6(R5)
         LA    R15,16
         IC    R14,CVTDCB
         N     R14,VSMASK
         SRL   R14,1
         L     R9,CVTEORM
         SRA   R9,10
         LA    R9,1(R9)
         CVD   R9,DWD
         ED    BCORE,DWD+5
         MVC   BIPLV,UCBVOLI-UCBOB(R3)
         ED    BDATE,CVTDATE+1
         L     R14,CVTPCCAT            PCCAVT (+2FC)
         ICM   R4,15,0(R14)            UNIPROCESSOR ONLY!!
         BNZ   MCPUINFO                MOVE CPU INFO           DDMMM84
         ICM   R4,15,4(R14)            TRY CPU 1
         BNZ   MCPUINFO                MOVE CPU INFO           DDMMM84
         ICM   R4,15,8(R14)            TRY CPU 2
         BNZ   MCPUINFO                MOVE CPU INFO           DDMMM84
         ICM   R4,15,12(R14)           TRY CPU 3 (3084)
         BNZ   MCPUINFO                MOVE CPU INFO           DDMMM84
         B     MCPUSKIP                SKIP IT                 DDMMM84
MCPUINFO DS    0H                                              DDMMM84
         MVC   BSNUM,4+2(R4)           PCCACPID SERIAL # (6)
         MVC   BMODL,4+8(R4)           "        MODEL  # (4)
*        MVC   BVERS,4+0(R4)           "        VERSION#(2)    DDMMM84
MCPUSKIP DS    0H                                              DDMMM84
*
**             CALC TIME FROM IPL  (OR SET IPS)
*
         L     R14,CVTOPCTP        SRM'S RMCT
         USING RMCT,R14
         L     R15,CVTASVT         ASVT
         USING ASVT,R15
         L     R2,ASVTFRST+4       MASTER SCHEDULER ASCB(I HOPE)
         USING ASCB,R2
         L     R1,ASCBOUCB         *MASTER* OUCB
         USING OUCB,R1
         L     R0,RMCTTOD          CURRENT TOD
         S     R0,OUCBTMO          GET DELTA = (TIME SINCE IPL)
         DROP  R1
         SRDL  R0,32               CONVER TO SECONDS
         D     R0,=F'1024'         CONVERT TO SECONDS.
         SR    R0,R0               IGNORE REMAINDER.
         A     R1,=F'1800'         ROUND
         D     R0,=F'3600'         DIVIDE TO GET HOURS.
         CVD   R1,DWD              CONVERT HOURS -
         ED    BHH,DWD+6           - TO PRINTABLE FORMAT.
         DROP  R14,R15,R2
         EJECT
         LA    R7,BUFEND-BUFFER
         TPUT  BUFFER,(R7)
         SPACE
*        JVDLEAVE
         @RET  EQ,RC=0
         EJECT
VSMASK   DC    F'2'
F8       DC    F'8'
MASK     EQU   *                   KEEP IN SYNC WITH B.... DSA FIELDS
         $MSG  P=M                 MAP CONSTANTS FOR MESSAGE
         LTORG
*
**
*
DSA      DSECT
*SSA     DS   18F
BUFFER   EQU  *
         $MSG  P=B                 MAP MESSAGE AREA
BUFEND   EQU   *
DWD      DS    D
DSAEND   EQU   *
*        SPACE
*        JVDEQU
         EJECT
CVT      DSECT
         CVT   LIST=YES,DSECT=YES
         IHAASVT
         IHAASCB
         IRAOUCB
*        IRARMCT
RMCT     DSECT
         ORG   RMCT+X'7C'
RMCTTOD  DS    F
         IEFUCBOB
         END
