         MACRO
&LABEL   #CONVDAT  &REG=2,&MLABEL=MONTH,&DLABEL=DAY,&YLABEL=YEAR,      X
               &ALABEL=MNTHABRE,&RESTORE=NO,&RENT=YES
.*
.*  NAME     $CONVDAT
.*  DATE     25 NOVEMBER 1986
.*  AUTHOR   JOHN D. SOSTROM
.*           DAYCO PRODUCTS INCORPORATED
.*           333 W. FIRST ST.
.*           DAYTON, OHIO
.*           (513)226-5747
.*
.*  THIS MACRO CONVERTS A PACKED JULIAN FORMAT DATE PASSED IN A
.*  TO A GREGORIAN DATE FORMAT POINTED TO BY THE REGISTER USED TO
.*  SUPPLY THE JULIAN DATE IN THE FIRST PLACE.
.*
.*  THE REGISTER PASSED BY THE REG= PARAMETER MAY NOT BE REGISTER
.*  ZERO(0).  IF THE FIRST INVOKATION OF $CONVDAT IS SPECIFIED WITH
.*  RENT=YES THEN ALL SUBSIQUENT INVOCATIONS WILL BE HANDELED THE SAME.
.*
.*  IF RENT=YES IS SPECIFIED THEN REGISTER ZERO MUST CONTAIN THE
.*  ADDRESS OF A 160 BYTE WORK AREA.
.*
         GBLB  &CONV,&LABPASS,&GRENT,&NGRENT
         GBLC  &CONVL1,&CONVL2,&CONVL3,&CONVL4,&CONVL5
         GBLC  &MLAB01,&MLAB02,&MLAB03,&MLAB04,&MLAB05,&MLAB06
         GBLC  &MLAB07,&MLAB08,&MLAB09,&MLAB10,&MLAB11,&MLAB12,&MLAB13
         GBLC  &MLAB14,&MLAB15,&MLAB16,&MLAB17,&MLAB18,&MLAB19,&MLAB20
         GBLC  &WLAB02,&WLAB03,&WLAB04,&WLAB05,&WLAB06,&WLAB07,&MLAB99
         GBLC  &WLAB08,&WLAB09,&WLAB10,&WLAB11,&WLAB12,&WLAB13,&WLAB14
         GBLC  &WLAB15,&WLAB16
         GBLC  &COVSECT,&DSLABEL                .CSECT NAME WHERE AREAS
         LCLB  &LAB1,&LAB2,&LAB3,&LAB4
         LCLB  &NSECT,&GPAS1                    .NOT CURRENT CSECT
         LCLC  &LSECT,&LXLAB1,&LXLAB2,&LXLAB3,&LXLAB4
   AIF   ('&REG' NE '0' AND '&REG' NE 'R0' AND '&REG' NE 'REG0').CONT1
         MNOTE 8,'REGISTER 0 CANNOT BE USED SINCE IT IS DESTROYED'
         MEXIT
.CONT1   ANOP
&LSECT   SETC  '&SYSECT'
         AIF   ('&RENT' EQ 'NO').NSETR1
&GRENT   SETB  1
.NSETR1  ANOP
         AIF   (&NGRENT AND NOT &GRENT).NSETR3
         AIF   (&GRENT AND NOT &NGRENT).NSETR3
         AIF   (&NGRENT AND &GRENT).NSETR2
&NGRENT  SETB  1
         AGO   .NSETR3
.NSETR2  ANOP
         MNOTE 4,'PREVIOUS INVOCATION OF $CONVDAT SPECIFIED RENT=NO. SPX
               ECIFICATION OF RENT=YES AT THIS IS IGNORED.'
&GRENT   SETB  0
.NSETR3  ANOP
&LXLAB1  SETC  'Z&SYSNDX.S'
&LXLAB2  SETC  'Z&SYSNDX.T'
&LXLAB3  SETC  'Z&SYSNDX.U'
&LXLAB4  SETC  'Z&SYSNDX.V'
&LAB1    SETB  0
&LAB2    SETB  0
&LAB3    SETB  0
&LAB4    SETB  0
&NSECT   SETB  0
         AIF   (&CONV).CHKLAB
&CONV    SETB  1
&LABPASS SETB  0
.* GLOBAL LABELS
&COVSECT SETC  '&SYSECT'
&CONVL1  SETC  '&MLABEL'
&CONVL2  SETC  '&DLABEL'
&CONVL3  SETC  '&YLABEL'
&CONVL4  SETC  '&ALABEL'
&CONVL5  SETC  'D&SYSNDX.A'
&MLAB01  SETC  'M&SYSNDX.A'
&MLAB02  SETC  'M&SYSNDX.B'
&MLAB03  SETC  'M&SYSNDX.C'
&MLAB04  SETC  'M&SYSNDX.D'
&MLAB05  SETC  'M&SYSNDX.E'
&MLAB06  SETC  'M&SYSNDX.F'
&MLAB07  SETC  'M&SYSNDX.J'
&MLAB08  SETC  'M&SYSNDX.K'
&MLAB09  SETC  'M&SYSNDX.O'
&MLAB10  SETC  'M&SYSNDX.P'
&MLAB11  SETC  'M&SYSNDX.R'
&MLAB12  SETC  'M&SYSNDX.S'
&MLAB13  SETC  'M&SYSNDX.T'
&MLAB14  SETC  'C&SYSNDX.A'
&MLAB15  SETC  'C&SYSNDX.B'
&MLAB16  SETC  'C&SYSNDX.C'
&MLAB17  SETC  'C&SYSNDX.D'
&MLAB18  SETC  'C&SYSNDX.E'
&MLAB19  SETC  'C&SYSNDX.F'
&MLAB20  SETC  'C&SYSNDX.G'
&MLAB99  SETC  'M&SYSNDX.U'
&WLAB02  SETC  'W&SYSNDX.B'
&WLAB03  SETC  'W&SYSNDX.C'
&WLAB04  SETC  'W&SYSNDX.D'
&WLAB05  SETC  'W&SYSNDX.E'
&WLAB06  SETC  'W&SYSNDX.F'
&WLAB07  SETC  'W&SYSNDX.G'
&WLAB08  SETC  'W&SYSNDX.H'
&WLAB09  SETC  'W&SYSNDX.I'
&WLAB10  SETC  'W&SYSNDX.J'
&WLAB11  SETC  'W&SYSNDX.K'
&WLAB12  SETC  'W&SYSNDX.L'
&WLAB13  SETC  'W&SYSNDX.M'
&WLAB14  SETC  'W&SYSNDX.N'
&WLAB15  SETC  'W&SYSNDX.P'
&WLAB16  SETC  'W&SYSNDX.Q'
&DSLABEL SETC  'D&SYSNDX.O'
         AGO   .NOCHK
.CHKLAB  ANOP
         AIF   ('&MLABEL' EQ '&CONVL1').LAB1GO
&LAB1    SETB  1
.LAB1GO  ANOP
         AIF   ('&DLABEL' EQ '&CONVL2').LAB2GO
&LAB2    SETB  1
.LAB2GO  ANOP
         AIF   ('&YLABEL' EQ '&CONVL3').LAB3GO
&LAB3    SETB  1
.LAB3GO  ANOP
         AIF   ('&ALABEL' EQ '&CONVL4').LAB4GO
&LAB4    SETB  1
.LAB4GO  ANOP
         AIF   ('&COVSECT' EQ '&SYSECT').SAMSECT
&NSECT   SETB  1
.SAMSECT ANOP
         AIF   (NOT &NSECT).NMSG01
*
*        REG 0 AND 1 ARE DESTROYED BY THIS ROUTINE
.NMSG01  ANOP
         AIF   (NOT &GRENT).NMSG02
*
*       REGISTER 1 MUST CONTAIN THE ADDRESS OF A 274 BYTE WORK AREA ON
*              A FULL WORD BOUNDRY FOR THIS ROUTINE
*
*        THE MLABEL=, DLABEL=, YLABEL=, ALABEL= AND RETURN= PARAMETERS
*        ARE IGNORED WHEN RENT=YES IS SPECIFIED.
*
*
*
*
*
*
*
*
.NMSG02  ANOP
*
*        REG      = THE REGISTER CONTAING THE PACKED JULIAN DATE
*        MLABEL   = THE MONTH LABEL YOU WANT. DEFAULT = MONTH
*        DLABEL   = THE DAY OF THE MONTH LABEL YOU WANT. DEFAULT = DAY
*              DLABEL VALUE COMES OUT WRONG - NOT FIXED YET
*        YLABEL   = THE YEAR LABEL YOU WANT. DEFAULT = YEAR
*        ALABEL   = THE MONTH NAME LABEL YOU WANT
*        RENT     = SPECIFIES IF THIS MACRO IS TO BE REENTRANT FORMAT
*                   THE ADDRESS POINTED TO REG 0 MUST BE THE WORK AREA
*                   TO BE USED ON A DOUBLE WORK BOUNDRY
*
*        THIS ROUTINE ALSO USES REGISTERS 2, 3 AND 4 WHICH IT RESTORES
*        PRIOR TO LOADING THE POINTER REGISTER ADDRESS
*
*        THIS ROUTINE CONVERTS THE JULIAN DATE INTO THE CALENDAR DATE
*
*        EX.   FROM 75339 TO 12/05/75
*
         AIF   (NOT &GRENT).NMSG03
*
*        THE REGISTER SPECIFIED IN THE REG= KEYWORD WILL CONTAIN THE
*        ADDRESS OF THE CONVERTED DATE.
*
.NMSG03  ANOP
*        IF RESTORE=NO IS SPECIFIED (DEFAULT) THEN
*        THEN THE REGISTER SPECIFIED BY THE REG= KEYWORD IS LOADED WITH
*        THE ADDRESS OF THE CONVERTED DATE FIELD ADDRESS POINTER.
*
*        IF RESTORE=YES IS SPECIFIED THEN
*        THE REGISTER VALUE PASSED IN THE REGISTER SPECIFIED IN THE
*        REG= OPERAND IS RESTORED AS OBTAINED
*
.NOCHK   ANOP
&LABEL   DS    0H
         AIF   (&GRENT).RGO001
         AIF   (&NSECT).REMOTE
.RGO001  ANOP
         AIF   (NOT &GRENT).RGO002
         USING &DSLABEL,1
         MVC   &MLAB05.(&LXLAB3),&LXLAB1 INITIALIZE AREA
         MVC   &MLAB04.(&LXLAB4),&LXLAB2 INITIALIZE AREA
.RGO002  ANOP
         STM   2,4,&MLAB12             SAVE REGISTERS 1, 2 AND 3
         ZAP   &MLAB01,=P'01'          INITIALIZE MONTH
         ZAP   &MLAB02+2(2),=P'28'     INITIALIZE FEB
         MVC   &MLAB03.(4),=CL4' '      INITIALIZE MONTH ABREV.
         ST    &REG,&MLAB04            STORE JULIAN DATE
         ST    &REG,&MLAB13            STORE JULIAN DATE FOR RESTORE
         XC    &MLAB05,&MLAB05       ZERO WORK AREA
         MVC   &MLAB05+3(1),&MLAB04+1 MOVE YEAR INTO WORK
         L     &REG,&MLAB05            LOAD YEAR INTO REGISTER
         SLL   &REG,4                  SHIFT LEFT 1/2 BYTE
         ST    &REG,&MLAB05            STORE YEAR INTO WORK AGAIN
         OI    &MLAB05+3,X'0F'        CONVERT YEAR INTO PACKED FORMAT
         ZAP   &MLAB06,&MLAB05         SAVE YEAR
         DP    &MLAB05,=P'04'         DIVIDE YEAR BY 4
         CP    &MLAB05+2(2),=P'00'    IF REMAINDER THEN LEAP YEAR
         BNE   M&SYSNDX.G              NOT A LEAP YEAR
         ZAP   &MLAB02+2(2),=P'29'     SET MONTH 2 EQUAL TO 29 DAYS
M&SYSNDX.G LA  2,&MLAB02               SET POINTER TO MONTH TABLE
         L     3,=F'02'                SET COUNTER
         LA    4,&MLAB01-1             SET POINTER TO END OF TABLE
M&SYSNDX.I CP  &MLAB04+2(2),0(2,2)     COMPARE DAYS TO MONTH TABLE
         BNH   M&SYSNDX.H              LESS THAN TABLE
         AP    &MLAB01,=P'01'          INCRIMENT MONTH COUNTER
         SP    &MLAB04+2(2),0(2,2)     SUB TABLE DAY FORM JULIAN DAY
         LA    2,2(2)                  BUMP POINTER
         CLC   0(2,2),=X'FFFF'         CONTINUE SCAN
         BE    M&SYSNDX.H
         B     M&SYSNDX.I         DO IT AGAIN
M&SYSNDX.H ZAP &MLAB08,&MLAB04+2(2) SET DAY
         UNPK  &MLAB05,&MLAB08        UNPACK DAY
         OI    &MLAB05+3,X'F0'        RESET SIGN
         MVC   &MLAB11.(2),&MLAB05+2 MOVE DAY TO O/P AREA
         UNPK  &MLAB05,&MLAB01        UNPACK MONTH
         OI    &MLAB05+3,X'F0'        RESET SIGN
         MVC   &MLAB09.(2),&MLAB05+2 MOVE MONTH TO O/P AREA
         UNPK  &MLAB05,&MLAB06        UNPACK YEAR
         OI    &MLAB05+3,X'F0'        RESER SIGN
         MVC   &MLAB10.(2),&MLAB05+2 MOVE YEAR TO O/P AREA
         LA    &REG,&MLAB07-10         SET TABLE POINTER
         SPACE 1
*
**
***  FIND WHICH MONTH THIS IS
**
*
         SPACE 1
M&SYSNDX.L LA  &REG,10(&REG)           BUMP POINTER
         CLC   0(4,&REG),=XL4'FFFFFFFF' END OF TABLE
         BE    M&SYSNDX.M              MONTH NOT FOUND
         CLC   &MLAB09.(2),0(&REG)      THIS MONTH?
         BNE   M&SYSNDX.L              YES, THEN GO MOVE IT
         MVC   &MLAB03.(4),2(&REG)      MOVE IN MONTH ABREVIATION
         EJECT
*
**
***  FIND WHICH DAY OF THE WEEK USING ZELLER CONGRUENCE FORMULA
***       INIT WORK AREAS
**
*
         SPACE 3
*  DY=DATE
         ZAP   &WLAB02.(4),&MLAB08.(2) SET DATE   (DY)
*  MI=MONTH
         ZAP   &WLAB03.(4),&MLAB01.(2) SET MONTH  (MI)
*  YR=YEAR
         ZAP   &WLAB04.(4),&MLAB06.(2) SET YEAR   (YR)
         OI    &WLAB02+3,X'0F'    SET TO A POSITIVE SIGN
         OI    &WLAB03+3,X'0F'    SET TO A POSITIVE SIGN
         OI    &WLAB04+3,X'0F'    SET TO A POSITIVE SIGN
         SPACE 3
*  IF MI>2 THEN MA=MI-2 AND YA=YR
*     ELSE MA=MI+10 AND YA=YR-1
         SPACE 1
         MVC   &WLAB05.(4),&WLAB02
         MVC   &WLAB06.(4),&WLAB04                       YA=YR
         CP    &WLAB05.(4),=PL4'+2' GREATER THA FEB?      IF MI>2
         BH    ZA&SYSNDX          YES
         AP    &WLAB05.(4),=PL4'+10' ADD 10 TO MONTH     MA=MI+10
         SP    &WLAB06.(4),=PL4'+1' SUBTRACT 1 FROM YEAR YA=YR-1
         B     ZB&SYSNDX
ZA&SYSNDX DS 0H
         SP    &WLAB05.(4),=PL4'+2' SUBTRACT 2           MA=MI-2
         SPACE 3
* SD=INT(((13*MA)-1)/5)+DY+CY+INT(CY/4)+INT(CN/4)+(5*CN)
         SPACE 1
ZB&SYSNDX DS 0H
         MVC   &WLAB07.(4),&WLAB06
         MVC   &WLAB09.(4),&WLAB05
         MP    &WLAB09.(4),=PL2'+13'
         SP    &WLAB09.(4),=PL4'+1'
         DP    &WLAB09.(4),=PL2'+5'                 INT(((13*MA)-1)/5)
         LH    2,&WLAB09
         ST    2,&WLAB09
         MVC   &WLAB10.(4),&WLAB07
         DP    &WLAB10.(4),=PL2'+4'                 INT(CY/4)
         LH    2,&WLAB10
         ST    2,&WLAB10
         MVC   &WLAB11.(4),&WLAB08
         DP    &WLAB11.(4),=PL2'+4'                 INT(CN/4)
         LH    2,&WLAB11
         ST    2,&WLAB11
         MVC   &WLAB12.(4),&WLAB08
         MP    &WLAB12.(4),=PL2'+5'                 (CN*5)
         AP    &WLAB13.(4),&WLAB02       DY+
         AP    &WLAB13.(4),&WLAB07         CY+
         AP    &WLAB13.(4),&WLAB09           INT(((13*MA)-1)/5)+
         AP    &WLAB13.(4),&WLAB10              INT(CY/4)+
         AP    &WLAB13.(4),&WLAB11                 INT(CN/4)+
         AP    &WLAB13.(4),&WLAB12                    (CN*5)=(SD)
         SPACE 3
*  SN=(SD-(7*INT(SD/7)))+1
*
*    SN WILL EQUAL A NUMBER FROM 1 TO 7 THAT CORRESPONDS TO THE DAY
*    OF THE WEEK WITH SUNDAY = 1 AND SATURDAY = 7
         MVC   &WLAB14.(4),&WLAB13
         MVC   &WLAB15.(4),&WLAB13
         DP    &WLAB15.(4),=PL2'7'            INT(SD/7)
         LH    2,&WLAB15
         ST    2,&WLAB15
         MP    &WLAB15.(4),=PL2'7'            7*INT(SD/7)
         SP    &WLAB14.(4),&WLAB15            SD-(7*INT(SD/7))
         AP    &WLAB14.(4),=PL4'+1'           (SD-(7*INT(SD/7)))+1=SN
         SPACE 3
*  FIND DAY OF WEEK IN TABLE
         LA    2,&MLAB07-10
         UNPK  &WLAB16.(2),&WLAB14+2(2)
         OI    &WLAB16+1,X'F0'
ZC&SYSNDX DS 0H
         LA    2,10(2)            BUMP TO NEXT ENTRY
         CLC   &WLAB16.(2),0(2)  THIS IT?
         BE    ZD&SYSNDX          YES
         CLC   0(2,2),=X'FFFF'    END OF TABLE?
         BE    ZE&SYSNDX          YES, NOT FOUND
         B     ZC&SYSNDX          TRY NEXT
         SPACE 1
ZD&SYSNDX DS 0H
         MVC   &MLAB99.(4),6(2)
ZE&SYSNDX DS 0H
         AGO   .LOCALS
.REMOTE  ANOP
         L     1,=V(&MLAB12)           POINT TO REGISTER SAVE AREA
         STM   2,4,0(1)                SAVE REGISTERS 1, 2 AND 3
         L     1,=V(&MLAB01)           INITIALIZE AREAS
         ZAP   0(2,1),=P'01'           INITIALIZE MONTH
         L     1,=V(&MLAB02)
         ZAP   2(2,1),=P'28'            INITIALIZE FEB
         L     1,=V(&MLAB03)
         MVC   0(4,1),=CL4' '          INITIALIZE MONTH ABREV.
         L     1,=V(&MLAB04)           GET ADDRESS
         ST    &REG,0(1)               STORE JULIAN DATE
         L     1,=V(&MLAB13)           GET ADDRESS
         ST    &REG,0(1)               STORE JULIAN DATE FOR RESTORE
         L     1,=V(&MLAB05)           GET ADDRESS
         XC    0(4,1),0(1)             ZERO WORK AREA
         L     &REG,=V(&MLAB04)        GET ADDRESS
         MVC   3(1,1),1(&REG)          MOVE YEAR INTO WORK
         L     &REG,0(1)               LOAD YEAR INTO REGISTER
         SLL   &REG,4                  SHIFT LEFT 1/2 BYTE
         ST    &REG,0(1)               STORE YEAR INTO WORK AGAIN
         OI    3(1),X'0F'              CONVERT YEAR INTO PACKED FORMAT
         L     &REG,=V(&MLAB06)        GET ADDRESS
         ZAP   0(2,&REG),0(4,1)        SAVE YEAR
         DP    0(4,1),=P'04'           DIVIDE YEAR BY 4
         CP    2(2,1),=P'00'           IF REMAINDER THEN LEAP YEAR
         BNE   M&SYSNDX.G              NOT A LEAP YEAR
         L     &REG,=V(&MLAB02)
         ZAP   2(2,&REG),=P'29'        SET MONTH 2 EQUAL TO 29 DAYS
M&SYSNDX.G L   2,=V(&MLAB02)           SET POINTER TO MONTH TABLE
         L     3,=F'02'                SET COUNTER
         L     4,=V(&MLAB01)           SET POINTER TO END OF TABLE
         S     4,=F'1'                 SET POINTER TO END OF TABLE
         L     &REG,=V(&MLAB04)
         L     1,=V(&MLAB01)
M&SYSNDX.I CP  2(2,&REG),0(2,2)        COMPARE DAYS TO MONTH TABLE
         BNH   M&SYSNDX.H              LESS THAN TABLE
         AP    0(2,1),=P'01'           INCRIMENT MONTH COUNTER
         SP    2(2,&REG),0(2,2)        SUB TABLE DAY FORM JULIAN DAY
         LA    2,2(2)
         CLC   0(2,2),=XL2'FFFF'       CONTINUE SCAN
         BE    M&SYSNDX.H
         B     M&SYSNDX.I
M&SYSNDX.H DS  0H
         L     1,=V(&MLAB08)
         ZAP   0(2,1),2(2,&REG)       SET DAY
         L     &REG,=V(&MLAB05)
         UNPK  0(4,&REG),0(2,1)       UNPACK DAY
         OI    3(&REG),X'F0'          RESET SIGN
         L     1,=V(&MLAB11)
         MVC   0(2,1),2(&REG)         MOVE DAY TO O/P AREA
         L     1,=V(&MLAB01)
         UNPK  0(4,&REG),0(2,1)       UNPACK MONTH
         OI    3(&REG),X'F0'          RESET SIGN
         L     1,=V(&MLAB09)
         MVC   0(2,1),2(&REG)         MOVE MONTH TO O/P AREA
         L     1,=V(&MLAB06)
         UNPK  0(4,&REG),0(2,1)       UNPACK YEAR
         OI    3(&REG),X'F0'          RESER SIGN
         L     1,=V(&MLAB10)
         MVC   0(2,1),2(&REG)         MOVE YEAR TO O/P AREA
         L     &REG,=V(&MLAB07)       SET TABLE POINTER
         S     &REG,=F'10'            SET TABLE POINTER
         SPACE 1
*
**
***  FIND WHICH MONTH THIS IS
**
*
         SPACE 1
M&SYSNDX.L LA  &REG,10(&REG)          BUMP POINTER
         CLC   0(4,&REG),=XL4'FFFFFFFF' END OF TABLE
         BE    M&SYSNDX.M              MONTH NOT FOUND
         L     1,=V(&MLAB09)
         CLC   0(2,1),0(&REG)          THIS MONTH?
         BNE   M&SYSNDX.L              YES, THEN GO MOVE IT
         L     1,=V(&MLAB03)
         MVC   0(4,1),2(&REG)          MOVE IN MONTH ABREVIATION
         EJECT
*
**
***  FIND WHICH DAY OF THE WEEK USING ZELLER CONGRUENCE FORMULA
***       INIT WORK AREAS
**
*
         SPACE 3
*  DY=DATE
         L     1,=V(&WLAB02)
         L     2,=V(&MLAB08)
         ZAP   0(4,1),0(2,2) SET DATE   (DY)
         OI    0(1),X'0F'    SET TO A POSITIVE SIGN
*  MI=MONTH
         L     1,=V(&WLAB03)
         L     2,=V(&MLAB01)
         ZAP   0(4,1),0(2,2) SET MONTH  (MI)
         OI    0(1),X'0F'    SET TO A POSITIVE SIGN
*  YR=YEAR
         L     1,=V(&WLAB04)
         L     2,=V(&MLAB06)
         ZAP   0(4,1),0(2,2) SET YEAR   (YR)
         OI    0(1),X'0F'    SET TO A POSITIVE SIGN
         SPACE 3
*  IF MI>2 THEN MA=MI-2 AND YA=YR
*     ELSE MA=MI+10 AND YA=YR-1
         SPACE 1
         L     1,=V(&WLAB06)
         L     2,=V(&WLAB04)
         MVC   0(4,1),0(2)                          YA=YR
         L     1,=V(&WLAB05)
         L     2,=V(&WLAB03)
         MVC   0(4,1),0(2)
         CP    0(4,1),=PL4'+2' GREATER THA FEB?      IF MI>2
         BH    ZA&SYSNDX          YES
         AP    0(4,1),=PL4'+10' ADD 10 TO MONTH     MA=MI+10
         L     2,=V(&WLAB06)
         SP    0(4,2),=PL4'+1' SUBTRACT 1 FROM YEAR YA=YR-1
         B     ZB&SYSNDX
ZA&SYSNDX DS 0H
         SP    0(4,1),=PL4'+2' SUBTRACT 2           MA=MI-2
         SPACE 3
* SD=INT(((13*MA)-1)/5)+DY+CY+INT(CY/4)+INT(CN/4)+(5*CN)
         SPACE 1
ZB&SYSNDX DS 0H
         L     1,=V(&WLAB07)
         L     2,=V(&WLAB06)
         MVC   0(4,1),0(2)
         L     1,=V(&WLAB09)
         L     2,=V(&WLAB05)
         MVC   0(4,1),0(2)
         MP    0(4,1),=PL2'+13'
         SP    0(4,1),=PL4'+1'
         DP    0(4,1),=PL2'+5'                 INT(((13*MA)-1)/5)
         LH    2,0(1)
         ST    2,0(1)
         L     1,=V(&WLAB10)
         L     2,=V(&WLAB07)
         MVC   0(4,1),0(2)
         DP    0(4,1),=PL2'+4'                 INT(CY/4)
         LH    2,0(1)
         ST    2,0(1)
         L     1,=V(&WLAB11)
         L     2,=V(&WLAB08)
         MVC   0(4,1),0(2)
         DP    0(4,1),=PL2'+4'                 INT(CN/4)
         LH    2,0(1)
         ST    2,0(1)
         L     1,=V(&WLAB12)
         L     2,=V(&WLAB08)
         MVC   0(4,1),0(2)
         MP    0(4,1),=PL2'+5'                 (CN*5)
         L     1,=V(&WLAB13)
         L     2,=V(&WLAB02)
         AP    0(4,1),0(4,2)         DY+
         L     2,=V(&WLAB07)
         AP    0(4,1),0(4,2)          CY+
         L     2,=V(&WLAB09)
         AP    0(4,1),0(4,2)            INT(((13*MA)-1)/5)+
         L     2,=V(&WLAB10)
         AP    0(4,1),0(4,2)               INT(CY/4)+
         L     2,=V(&WLAB11)
         AP    0(4,1),0(4,2)                  INT(CN/4)+
         L     2,=V(&WLAB12)
         AP    0(4,1),0(4,2)                     (CN*5)=(SD)
         SPACE 3
*  SN=(SD-(7*INT(SD/7)))+1
*
*    SN WILL EQUAL A NUMBER FROM 1 TO 7 THAT CORRESPONDS TO THE DAY
*    OF THE WEEK WITH SUNDAY = 1 AND SATURDAY = 7
         L     1,=V(&WLAB14)
         L     2,=V(&WLAB13)
         MVC   0(4,1),0(2)
         L     1,=V(&WLAB15)
         MVC   0(4,1),0(2)
         DP    0(4,1),=PL2'7'            INT(SD/7)
         LH    2,0(1)
         ST    2,0(1)
         MP    0(4,1),=PL2'7'            7*INT(SD/7)
         L     2,=V(&WLAB14)
         SP    0(4,2),0(4,1)             SD-(7*INT(SD/7))
         AP    0(4,2),=PL4'+1'           (SD-(7*INT(SD/7)))+1=SN
         SPACE 3
*  FIND DAY OF WEEK IN TABLE
         L     1,=V(&WLAB16)
         L     2,=V(&WLAB16)
         LA    2,2(0,2)
         UNPK  0(2,1),0(2,2)
         OI    1(1),X'F0'
         L     2,=V(&MLAB07)
         S     2,=F'10'
ZC&SYSNDX DS 0H
         LA    2,10(2)            BUMP TO NEXT ENTRY
         CLC   0(2,1),0(2)  THIS IT?
         BE    ZD&SYSNDX          YES
         CLC   0(2,2),=X'FFFF'    END OF TABLE?
         BE    ZE&SYSNDX          YES, NOT FOUND
         B     ZC&SYSNDX          TRY NEXT
         SPACE 1
ZD&SYSNDX DS 0H
         L     1,=V(&MLAB99)
         MVC   0(4,1),6(2)
ZE&SYSNDX DS 0H
.LOCALS  ANOP
         SPACE 1
M&SYSNDX.M B   X&SYSNDX.Z              BRANCH AROUND WOKK AREA
         LTORG                         LITERAL POOL
         DS    0F
         AIF   (&LABPASS).NOORG1
         AIF   (NOT &GRENT).NORM01
&LABPASS SETB  1
&DSLABEL DSECT
&MLAB05  DC    F'00'
         SPACE 3
&WLAB02  DC    PL4'+0'                WORK AREA DAY (DY)
&WLAB03  DC    PL4'+0'                WORK AREA MONTH
&WLAB04  DC    PL4'+0'                WORK AREA YEAR
.*  IF MONTH <= 2 THEN MA=MONTH-2 AND YA=YEAR-1 ELSE
.*                     MA=MONTH+10 AND YA=YEAR
&WLAB05  DC    PL4'+0'                WORK AREA (MA)
&WLAB06  DC    PL4'+0'                WORK AREA (YA)
.*   CY=YA-(100*CN)
&WLAB07  DC    PL4'+0'                WORK AREA (CY)
.*  CN=INT(YA/100)
&WLAB08  DC    PL4'+19'           THIS IS A CONSTANT (CN)
.*  INT(((13*MA)-1)/5)
&WLAB09  DC    PL4'+0'            INTERMEDIATE WORK AREA 1
.*  INT(CY/4)
&WLAB10  DC    PL4'+0'            INTERMEDIATE WORK AREA 2
.*  INT(CN/4)
&WLAB11  DC    PL4'+0'            INTERMEDIATE WORK AREA 3
.*  CN*5
&WLAB12  DC    PL4'+0'            INTERMEDIATE WORK AREA 4
.*  SD=INT(((13*MA)-1)/5)+DY+CY+INT(CY/4)+INT(CN/4)+(5*CN)
&WLAB13  DC    PL4'+0'            INTERMEDIATE WORK AREA 5 (SD)
.*  SD-(7*INT(SD/7))
&WLAB14  DC    PL4'+0'            INTERMEDIATE WORK AREA 6 (SN)
&WLAB15  DC    PL4'+0'            INTERMEDIATE WORK AREA 7
&WLAB16  DC    PL4'+0'            INTERMEDIATE WORK AREA 8
         SPACE 3
&MLAB02  EQU   *
         DC    PL2'31'
         DC    PL2'28'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    X'FFFF'
&MLAB01  DC    PL2'00'                 MONTH WORK AREA
&MLAB04  DC    F'00'                   REGISTER SAVE AREA FOR REG
&MLAB06  DC    PL2'00'                 YEAR WORK AREA
&MLAB08  DC    PL2'00'                 DAY WORK AREA
&MLAB09  DC    CL2'  '                 MONTH O/P AREA
         DC    C'/'
&MLAB11  DC    CL2'  '                 DAY O/P AREA
         DC    C'/'
&MLAB10  DC    CL2'  '                 YEAR O/P AREA
&MLAB03  DC    CL4' '                  MONTH ABREIVIATION
&MLAB99  DC    CL4' '                  MONTH ABREIVIATION
         ORG   &MLAB09
&CONVL1  DC    CL2' '
         DC    C'/'
&CONVL2  DC    CL2' '
         DC    C'/'
&CONVL3  DC    CL2' '
&CONVL4  DC    CL4' '
&CONVL5  DC    CL4' '
         ORG
&MLAB07  DC    CL2'01',CL4'JAN',CL4'MON'
         DC    CL2'02',CL4'FEB',CL4'TUE'
         DC    CL2'03',CL4'MAR',CL4'TUE'
         DC    CL2'04',CL4'APR',CL4'WED'
         DC    CL2'05',CL4'MAY',CL4'THU'
         DC    CL2'06',CL4'JUN',CL4'FRI'
         DC    CL2'07',CL4'JUL',CL4'SAT'
         DC    CL2'08',CL4'AUG',CL4' '
         DC    CL2'09',CL4'SEP',CL4' '
         DC    CL2'10',CL4'OCT',CL4' '
         DC    CL2'11',CL4'NOV',CL4' '
         DC    CL2'12',CL4'DEC',CL4' '
         DC    XL10'FFFFFFFFFFFFFFFFFFFF' END OF TABLE
&MLAB12  DC 3F'0'                       REGISTER SAVE AREA
&MLAB13  DC F'0'                        REG= REGISTER SAVE AREA
&SYSECT  CSECT
&GPAS1   SETB  1
&LXLAB1  DC    F'00'
         DC    PL4'+0'                WORK AREA DAY (DY)
         DC    PL4'+0'                WORK AREA MONTH
         DC    PL4'+0'                WORK AREA YEAR
         DC    PL4'+0'                WORK AREA (MA)
         DC    PL4'+0'                WORK AREA (YA)
         DC    PL4'+0'                WORK AREA (CY)
         DC    PL4'+19'           THIS IS A CONSTANT (CN)
         DC    PL4'+0'            INTERMEDIATE WORK AREA 1
         DC    PL4'+0'            INTERMEDIATE WORK AREA 2
         DC    PL4'+0'            INTERMEDIATE WORK AREA 3
         DC    PL4'+0'            INTERMEDIATE WORK AREA 4
         DC    PL4'+0'            INTERMEDIATE WORK AREA 5 (SD)
         DC    PL4'+0'            INTERMEDIATE WORK AREA 6 (SN)
         DC    PL4'+0'            INTERMEDIATE WORK AREA 7
         DC    PL4'+0'            INTERMEDIATE WORK AREA 8
         DC    PL2'31'
         DC    PL2'28'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    X'FFFF'
         DC    PL2'00'                 MONTH WORK AREA
&LXLAB3  EQU   *-&LXLAB1               MOVE LENGTH #1
&LXLAB2  DC    F'00'                   REGISTER SAVE AREA FOR REG
         DC    PL2'00'                 YEAR WORK AREA
         DC    PL2'00'                 DAY WORK AREA
         DC    CL2'  '                 MONTH O/P AREA
         DC    C'/'
         DC    CL2'  '                 DAY O/P AREA
         DC    C'/'
         DC    CL2'  '                 YEAR O/P AREA
         DC    CL4' '                  MONTH ABREIVIATION
         DC    CL4' '                  DAY OF WEEK ABREVIATION
         DC    CL2'01',CL4'JAN',CL4'SUN'
         DC    CL2'02',CL4'FEB',CL4'MON'
         DC    CL2'03',CL4'MAR',CL4'TUE'
         DC    CL2'04',CL4'APR',CL4'WED'
         DC    CL2'05',CL4'MAY',CL4'THU'
         DC    CL2'06',CL4'JUN',CL4'FRI'
         DC    CL2'07',CL4'JUL',CL4'SAT'
         DC    CL2'08',CL4'AUG',CL4' '
         DC    CL2'09',CL4'SEP',CL4' '
         DC    CL2'10',CL4'OCT',CL4' '
         DC    CL2'11',CL4'NOV',CL4' '
         DC    CL2'12',CL4'DEC',CL4' '
         DC    XL10'FFFFFFFFFFFFFFFFFFFF' END OF TABLE
         DC 3F'0'                       REGISTER SAVE AREA
         DC F'0'                        REG= REGISTER SAVE AREA
&LXLAB4  EQU   *-&LXLAB2
         AGO   .NOORG2
.NORM01  ANOP
         AIF   (&GRENT).NOORG2
&MLAB05  DC    F'00'
         ENTRY &MLAB05
         SPACE 3
&WLAB02  DC    PL4'+0'                WORK AREA DAY (DY)
         ENTRY &WLAB02
&WLAB03  DC    PL4'+0'                WORK AREA MONTH
         ENTRY &WLAB03
&WLAB04  DC    PL4'+0'                WORK AREA YEAR
         ENTRY &WLAB04
.*  IF MONTH <= 2 THEN MA=MONTH-2 AND YA=YEAR-1 ELSE
.*                     MA=MONTH+10 AND YA=YEAR
&WLAB05  DC    PL4'+0'                WORK AREA (MA)
         ENTRY &WLAB05
&WLAB06  DC    PL4'+0'                WORK AREA (YA)
         ENTRY &WLAB06
.*   CY=YA-(100*CN)
&WLAB07  DC    PL4'+0'                WORK AREA (CY)
         ENTRY &WLAB07
.*  CN=INT(YA/100)
&WLAB08  DC    PL4'+19'           THIS IS A CONSTANT (CN)
         ENTRY &WLAB08
.*  INT(((13*MA)-1)/5)
&WLAB09  DC    PL4'+0'            INTERMEDIATE WORK AREA 1
         ENTRY &WLAB09
.*  INT(CY/4)
&WLAB10  DC    PL4'+0'            INTERMEDIATE WORK AREA 2
         ENTRY &WLAB10
.*  INT(CN/4)
&WLAB11  DC    PL4'+0'            INTERMEDIATE WORK AREA 3
         ENTRY &WLAB11
.*  CN*5
&WLAB12  DC    PL4'+0'            INTERMEDIATE WORK AREA 4
         ENTRY &WLAB12
.*  SD=INT(((13*MA)-1)/5)+DY+CY+INT(CY/4)+INT(CN/4)+(5*CN)
&WLAB13  DC    PL4'+0'            INTERMEDIATE WORK AREA 5 (SD)
         ENTRY &WLAB13
.*  SD-(7*INT(SD/7))
&WLAB14  DC    PL4'+0'            INTERMEDIATE WORK AREA 6 (SN)
         ENTRY &WLAB14
&WLAB15  DC    PL4'+0'            INTERMEDIATE WORK AREA 7
         ENTRY &WLAB15
&WLAB16  DC    PL4'+0'            INTERMEDIATE WORK AREA 8
         ENTRY &WLAB16
         SPACE 3
&MLAB02 EQU *                          MONTH TABLE
         ENTRY &MLAB02
         DC    PL2'31'
         DC    PL2'28'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    X'FFFF'
&MLAB01  DC    PL2'00'                 MONTH WORK AREA
         ENTRY &MLAB01
&MLAB04  DC    F'00'                   REGISTER SAVE AREA FOR REG
         ENTRY &MLAB04
&MLAB06  DC    PL2'00'                 YEAR WORK AREA
         ENTRY &MLAB06
&MLAB08  DC    PL2'00'                 DAY WORK AREA
         ENTRY &MLAB08
&MLAB09  DC    CL2'  '                 MONTH O/P AREA
         ENTRY &MLAB09
         DC    C'/'
&MLAB11  DC    CL2'  '                 DAY O/P AREA
         ENTRY &MLAB11
         DC    C'/'
&MLAB10  DC    CL2'  '                 YEAR O/P AREA
         ENTRY &MLAB10
&MLAB03  DC    CL4' '                  MONTH ABREIVIATION
&MLAB99  DC    CL4' '                  MONTH ABREIVIATION
         ENTRY &MLAB03
         ORG   &MLAB09
&CONVL1  DC    CL2' '
         ENTRY &CONVL1
         DC    C'/'
&CONVL2  DC    CL2' '
         ENTRY &CONVL2
         DC    C'/'
&CONVL3  DC    CL2' '
         ENTRY &CONVL3
&CONVL4  DC    CL4' '
         ENTRY &CONVL4
&CONVL5  DC    2CL4' '
         ENTRY &CONVL5
         ORG
         AGO   .RLAB01
.NOORG1  ANOP
         AIF   (&GRENT).NOORG2
         AIF   (NOT &LAB1).LOCLAB1
&MLABEL  DC    CL2' '
         DC    C'/'
.LOCLAB1 ANOP
         AIF   (NOT &LAB2).LOCLAB2
&DLABEL  DC    CL2' '
         DC    C'/'
.LOCLAB2 ANOP
         AIF   (NOT &LAB3).LOCLAB3
&YLABEL  DC    CL2' '
.LOCLAB3 ANOP
         AIF   (NOT &LAB4).LOCLAB4
&ALABEL  DC    2CL4' '
.LOCLAB4 ANOP
         AIF   (&LABPASS).NOORG2
.RLAB01  ANOP
&LABPASS SETB  1
&MLAB07  DC     CL2'01',CL4'JAN',CL4'SUN'
         ENTRY &MLAB07
         DC    CL2'02',CL4'FEB',CL4'MON'
         DC    CL2'03',CL4'MAR',CL4'TUE'
         DC    CL2'04',CL4'APR',CL4'WED'
         DC    CL2'05',CL4'MAY',CL4'THU'
         DC    CL2'06',CL4'JUN',CL4'FRI'
         DC    CL2'07',CL4'JUL',CL4'SAT'
         DC    CL2'08',CL4'AUG',CL4' '
         DC    CL2'09',CL4'SEP',CL4' '
         DC    CL2'10',CL4'OCT',CL4' '
         DC    CL2'11',CL4'NOV',CL4' '
         DC    CL2'12',CL4'DEC',CL4' '
         DC    XL10'FFFFFFFFFFFFFFFFFFFF' END OF TABEL
&MLAB12  DC 3F'0'                       REGISTER SAVE AREA
         ENTRY &MLAB12
&MLAB13  DC F'0'                        REG= REGISTER SAVE AREA
         ENTRY &MLAB13
.NOORG2  ANOP
         AIF   (NOT &GRENT).NOORG3
         AIF   (&GPAS1).NOORG3
&LXLAB1  DC    F'00'
         DC    PL4'+0'                WORK AREA DAY (DY)
         DC    PL4'+0'                WORK AREA MONTH
         DC    PL4'+0'                WORK AREA YEAR
         DC    PL4'+0'                WORK AREA (MA)
         DC    PL4'+0'                WORK AREA (YA)
         DC    PL4'+0'                WORK AREA (CY)
         DC    PL4'+19'           THIS IS A CONSTANT (CN)
         DC    PL4'+0'            INTERMEDIATE WORK AREA 1
         DC    PL4'+0'            INTERMEDIATE WORK AREA 2
         DC    PL4'+0'            INTERMEDIATE WORK AREA 3
         DC    PL4'+0'            INTERMEDIATE WORK AREA 4
         DC    PL4'+0'            INTERMEDIATE WORK AREA 5 (SD)
         DC    PL4'+0'            INTERMEDIATE WORK AREA 6 (SN)
         DC    PL4'+0'            INTERMEDIATE WORK AREA 7
         DC    PL4'+0'            INTERMEDIATE WORK AREA 8
         DC    PL2'31'
         DC    PL2'28'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    PL2'30'
         DC    PL2'31'
         DC    X'FFFF'
         DC    PL2'00'                 MONTH WORK AREA
&LXLAB3  EQU   *-&LXLAB1
&LXLAB2  DC    F'00'                   REGISTER SAVE AREA FOR REG
         DC    PL2'00'                 YEAR WORK AREA
         DC    PL2'00'                 DAY WORK AREA
         DC    CL2'  '                 MONTH O/P AREA
         DC    C'/'
         DC    CL2'  '                 DAY O/P AREA
         DC    C'/'
         DC    CL2'  '                 YEAR O/P AREA
         DC    CL4' '                  MONTH ABREIVIATION
         DC    CL4' '                  MONTH ABREIVIATION
         DC    CL2'01',CL4'JAN',CL4'SUN'
         DC    CL2'02',CL4'FEB',CL4'MON'
         DC    CL2'03',CL4'MAR',CL4'TUE'
         DC    CL2'04',CL4'APR',CL4'WED'
         DC    CL2'05',CL4'MAY',CL4'THU'
         DC    CL2'06',CL4'JUN',CL4'FRI'
         DC    CL2'07',CL4'JUL',CL4'SAT'
         DC    CL2'08',CL4'AUG',CL4' '
         DC    CL2'09',CL4'SEP',CL4' '
         DC    CL2'10',CL4'OCT',CL4' '
         DC    CL2'11',CL4'NOV',CL4' '
         DC    CL2'12',CL4'DEC',CL4' '
         DC    XL10'FFFFFFFFFFFFFFFFFFFF' END OF TABLE
         DC 3F'0'                       REGISTER SAVE AREA
         DC F'0'                        REG= REGISTER SAVE AREA
&LXLAB4  EQU   *-&LXLAB2
.NOORG3  ANOP
X&SYSNDX.Z DS  0H
         AIF   (NOT &GRENT).GCONT01
         LM    2,4,&MLAB12             RESTORE REGISTERS 1, 2 AND 3
         LA    &REG,&MLAB09            POINT REGISTER TO CONVERTED DATE
         DROP  1
         MEXIT
.GCONT01 ANOP
         AIF   (&NSECT).RMTCDE8
         AIF   (NOT &LAB1).NEXT01
         MVC   &MLABEL,&CONVL1         MOVE IN MONTH
.NEXT01  ANOP
         AIF   (NOT &LAB2).NEXT02
         MVC   &DLABEL,&CONVL2         MOVE IN DAY
.NEXT02  ANOP
         AIF   (NOT &LAB3).NEXT03
         MVC   &YLABEL,&CONVL3         MOVE IN YEAR
.NEXT03  ANOP
         AIF   (NOT &LAB4).NEXT04
         MVC   &ALABEL,&CONVL4         MOVE IN MONTH ABREVIATION
         MVC   &ALABEL+4(4),&CONVL5    MOVE IN DAY OF WEEK
.NEXT04  ANOP
         LM    2,4,&MLAB12             RESTORE REGISTERS 1, 2 AND 3
         AIF   ('&RESTORE' EQ 'YES').YESREST
         LA    &REG,&MLAB09            POINT REGISTER TO CONVERTED DATE
         MEXIT
.YESREST ANOP
         L     &REG,&MLAB13            RESTORE REGISTER PASSED
         MEXIT
.RMTCDE8 ANOP
         AIF   (NOT &LAB1).NEXT11
         L     &REG,=V(&CONVL1)
         MVC   &MLABEL,0(&REG)        MOVE IN MONTH
.NEXT11  ANOP
         AIF   (NOT &LAB2).NEXT12
         L     &REG,=V(&CONVL2)
         MVC   &DLABEL,0(&REG)         MOVE IN DAY
.NEXT12  ANOP
         AIF   (NOT &LAB3).NEXT13
         L     &REG,=V(&CONVL3)
         MVC   &YLABEL,0(&REG)         MOVE IN YEAR
.NEXT13  ANOP
         AIF   (NOT &LAB4).NEXT14
         L     &REG,=V(&CONVL4)
         MVC   &ALABEL,0(&REG)         MOVE IN MONTH ABREVIATION
         L     &REG,=V(&CONVL5)
         MVC   &ALABEL+4(4),0(&REG)    MOVE IN MONTH ABREVIATION
.NEXT14  ANOP
         L     &REG,=V(&MLAB12)
         LM    2,4,0(&REG)             RESTORE REGISTERS 1, 2 AND 3
         AIF   ('&RESTORE' EQ 'YES').XESREST
         L     &REG,=V(&MLAB09)        POINT TO LABEL
         MEXIT
.XESREST ANOP
         L     &REG,=V(&MLAB13)
         L     &REG,0(&REG)            RESTORE REGISTER PASSED
         MEND
