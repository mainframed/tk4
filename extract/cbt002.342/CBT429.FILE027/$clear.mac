         MACRO
&LABEL   $CLEAR
         LCLA  &AA,&BB,&CC,&GG
         LCLC  &DD,&EE,&FF
.*  THIS MACRO IS USED IN CONJUNCTION WITH THE $DFLAG, $FLAG AND $TEST
.*  MACROS TO CLEAR THE FLAG BITS OF A FLAG BYTE DEFINED IN THE $DFLAG
.*  MACRO THAT MAY HAVE BEEN SET BY THE USE OF THE $FLAG MACRO.
.*  MULTIPLE FLAG BITS FROM MULTIPLE FLAG BYTES MAY BE CLEARED IN ONE
.*  CALL OF THIS MACRO.
.*  TO CLEAR MUTIPLE BITS OF A SINGLE FLAG BYTE SPECIFY
.*       LABEL $CLEAR FLAGB1-FLAGB2-FLAGB3.....
.*  TO CLEAR MULTIPLE BITS OF MULTIPLE FLAG BYTES SPECIFY
.*       LABEL $CLEAR FLAGB1,BFLAGB2,CLFAGB1,......
.*  A COMBINATION OF THE ABOVE TWO FUNCTION MAY BE ACCOMPLISHED AS
.*  FOLLOWS
.*       LABEL $CLEAR FLAGB1,AFLAGB1-AFLAGB4,......
         AIF   (N'&SYSLIST GT 0).P1
         MNOTE 8,'AT LEAST ONE FLAG NAME TO SET MUST BE SPECIFIED'
         MEXIT
.P1      ANOP
&AA      SETA  1
&LABEL   DS    0H
&BB      SETA  N'&SYSLIST
         AIF   (N'&SYSLIST GT 1).P2
&DD      SETC  '&SYSLIST(&AA)'
&CC      SETA  K'&DD
&GG      SETA  1
.P1A     ANOP
         AIF   ('&DD'(&GG,1) NE '+').P1B
         MNOTE 8,'OPERAND &DD IS INVALID.'
         MEXIT
.P1B     ANOP
         AIF   ('&GG' EQ '&CC').P1C
&GG      SETA  &GG+1
         AGO   .P1A
.P1C     ANOP
&GG      SETA  1
.P1D     ANOP
         AIF   ('&DD'(&GG,1) EQ '-').P1E
         AIF   ('&GG' EQ '&CC').P1F
&GG      SETA  &GG+1
         AGO   .P1D
.P1E     ANOP
&GG      SETA  &GG-1
&EE      SETC  '&DD'(1,&GG)
         AGO   .P1G
.P1F     ANOP
&EE      SETC  '&DD'
.P1G     ANOP
         NI    #&EE,255-&DD
         MEXIT
.P2      ANOP
&AA      SETA  1
.P3      ANOP
&DD      SETC  '&SYSLIST(&AA)'
&CC      SETA  K'&DD
&GG      SETA  1
.P3A     ANOP
         AIF   ('&DD'(&GG,1) NE '+').P3B
         MNOTE 8,'OPERAND &DD IS INVALID.'
         MEXIT
.P3B     ANOP
         AIF   ('&GG' EQ '&CC').P3C
&GG      SETA  &GG+1
         AGO   .P3A
.P3C     ANOP
&GG      SETA  1
.P3D     ANOP
         AIF   ('&DD'(&GG,1) EQ '-').P3E
         AIF   ('&GG' EQ '&CC').P3F
&GG      SETA  &GG+1
         AGO   .P3D
.P3E     ANOP
&GG      SETA  &GG-1
&EE      SETC  '&DD'(1,&GG)
         AGO   .P3G
.P3F     ANOP
&EE      SETC  '&DD'
.P3G     ANOP
         NI    #&EE,255-&DD
         AIF   ('&AA' EQ '&BB').P4
&AA      SETA  &AA+1
         AGO   .P3
.P4      ANOP
         MEND
