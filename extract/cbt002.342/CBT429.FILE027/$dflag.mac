         MACRO
&LABEL   $DFLAG
         LCLA  &CNT1,&CNT2,&LNG
         LCLC  &NM,&#NM
         LCLC  &XV(8)
.*  THIS MACRO BUILDS A FLAG BYTE AND EQUATES FOR TESTING SETTING AND
.*  CLEARING THE BITS.  IT IS USED ALONG WITH THE $FLAG, $CLEAR AND
.*  $TEST MACROS.  UP TO 8 FLAGS MAY BE DEFINED.  EACH FLAG NAME HAS
.*  A MAXIMUM OF 7 CHARACTERS ALLOWED.  THE FLAG BYTES ARE ASSIGNED
.*  VALUES IN SEQUENCE WITH THE FIRST FLAG = X'80' THE SECOND FLAG
.*  = X'40' AND SO ON SO THAT EACH FLAG DEFINES ONE BIT IN THE FLAG
.*  BYTE.
.*  EACH FLAG MUST BE SEPERATED BY A COMMA.
&XV(1)   SETC  '80'
&XV(2)   SETC  '40'
&XV(3)   SETC  '20'
&XV(4)   SETC  '10'
&XV(5)   SETC  '08'
&XV(6)   SETC  '04'
&XV(7)   SETC  '02'
&XV(8)   SETC  '01'
         AIF   (N'&SYSLIST GT 0).P1
         MNOTE 8,'AT LEAST ONE FLAG NAME REQUIRED'
         MEXIT
.P1      ANOP
         AIF   (N'&SYSLIST LE 8).P2
         MNOTE 8,'A MAXIMUM OF 8 FLAG NAMES ARE ALLOWED'
         MEXIT
.P2      ANOP
&CNT1    SETA  N'&SYSLIST         GET THE NUMBER OF NAME ENTRIES
&CNT2    SETA  0                  SET START LOOP COUNTER
.P3      ANOP
         AIF   ('&CNT1' EQ '&CNT2').P4
&CNT2    SETA  &CNT2+1            ADD ONE TO COUNTER
&NM      SETC  '&SYSLIST(&CNT2)'
&LNG     SETA  K'&NM
         AIF   ('&LNG' LT '8').P3A
         MNOTE 8,'LABEL &NM IS TOO LONG.  MAXIMUM OF 7 CHARACTERS ALLOWX
               ED.'
         MEXIT
.P3A     ANOP
         AIF   ('&LNG' GT '0').P3B
         MNOTE 8,'LABEL IS TOO SHORT. A MINIMUM OF 1 CHARACTER REQUIREDX
               .'
         MEXIT
.P3B     ANOP
&#NM     SETC  '#&SYSLIST(&CNT2)'
&#NM     DS    0XL1
&NM      EQU   X'&XV(&CNT2)'
         AGO   .P3
.P4      ANOP
&LABEL   DC    XL1'00'            INITIAL VALUES SET TO ZERO
         MEND
