//JEFFKZ   JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=JEFFK,REGION=6M
//S EXEC ASMHCL,PARM.L='XREF,LIST'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSIN  DD *
LIBALLOC TITLE 'ALLOCATE LIBMASTER AS PS FILE'
LIBALLOC CSECT , /* LIBALLOC */
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K24      EQU   24
K28      EQU   28
K32      EQU   32
K36      EQU   36
K40      EQU   40
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K255     EQU   255
K256     EQU   256
K512     EQU   512
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
LIBALLOC AMODE 24
LIBALLOC RMODE 24
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         SAVE  (14,12),,LIBALLOC*&SYSDATE*&SYSTIME
         LR    RA,RF /*ADDRESSABILITY*/
         USING LIBALLOC,RA,RB,RC
         LA    RB,K4095(,RA)
         LA    RB,K1(,RB)
         LA    RC,K4095(,RB)
         LA    RC,K1(,RC)
         LR    RF,RD CHAIN
         CNOP  0,4
         BAL   RD,*+K76
         DC    (K18)A(K0)
         ST    RD,K8(,RF)
         ST    RF,K4(,RD)
         SLR   R9,R9 RC
         USING EFPL,R1
         L     R2,EFPLARG ARGUMENTS TABLE
         L     R3,EFPLEVAL EVALBLOCK PTR
         L     R3,K0(,R3) ACTUAL EVALBLOCK PTR
         DROP  R1
         LM    R4,R8,K0(R2) A(DSN) L(DSN) A(MEMBER) L(MEM) XFF
         CL    R8,=A(X'FFFFFFFF') PARMLIST END
         BE    FMEM YES
         CL    R6,=A(X'FFFFFFFF') PARMLIST END
         BNE   RC4  YES
         C     R5,=A(K44) MAX DSN LEN
         BH    RC8
         LTR   R5,R5 NULLS
         BZ    RC16
         STCM  R5,K3,PDSN+K4 DSN LEN
         BCTR  R5,K0 PER EX
         EX    R5,MOVEDSN
         OI    DSNONLY,X80 SHORTEN SVC 99 LIST
         B     DYNAM ...
FMEM     C     R5,=A(K44) MAX DSN LEN
         BH    RC8
         C     R7,=A(K8) MAX MEM NAME LEN
         BH    RC12
         LTR   R5,R5 NULLS
         BZ    RC16
         LTR   R7,R7 NULLS
         BZ    RC20
         STCM  R5,K3,PDSN+K4 DSN LEN
         STCM  R7,K3,PMEMB+K4 MEMBER NAME LEN
         BCTR  R5,K0 PER EX
         BCTR  R7,K0 PER EX
         EX    R5,MOVEDSN
         EX    R7,MOVEMEM
DYNAM    LA    R1,DYNPARM SVC 99
         SVC   99
         LTR   R9,RF
         BNZ   RC24 ERROR
         USING EVALBLOCK,R3
         LA    R0,K8 DDNAME LEN
         ST    R0,EVALBLOCK_EVLEN
         MVC   EVALBLOCK_EVDATA(K8),PDDN+K6
         B     EXIT
EXIT     LR    RF,R9 RC
         L     RD,K4(,RD)
         RETURN (14,12),RC=(15)
RC24     LA    R9,K4(,R9)
RC20     LA    R9,K4(,R9)
RC16     LA    R9,K4(,R9)
RC12     LA    R9,K4(,R9)
RC8      LA    R9,K4(,R9)
RC4      LA    R9,K4(,R9)
         B     EXIT
MOVEDSN  MVC   PDSNN(*-*),K0(R4)
MOVEMEM  MVC   PMEMBN(*-*),K0(R6)
DYNPARM  DC    X'80',AL3(S99RBP)
S99RBP   DC    X'14010000'
         DC    X'00000000'
         DC    A(S99TXTP)
         DC    8X'00'
S99TXTP  DC    A(PDSN)
         DC    A(PDISP)
         DC    A(PSUBS)
         DC    A(PDDN)
DSNONLY  DC    A(PUNAL)
         DC    X'80',AL3(PMEMB)
PDDN     DC    X'005500010008',C'XXXXXXXX'
PDSN     DC    X'00020001',AL2(*-*)
PDSNN    DC    CL44'  '
PDISP    DC    X'00040001000108'
PSUBS    DC    X'005F00010003',C'LAM'
PMEMB    DC    X'00030001',AL2(*-*)
PMEMBN   DC    CL8' '
PUNAL    DC    X'001C0000'
         LTORG
         IRXEFPL
         IRXARGTB
         IRXEVALB
         IRXSHVB
         END
/*
//L.SYSLMOD DD DSN=IPO1.LINKLIB(XLIBALLC),DISP=SHR,
//  UNIT=,SPACE=
