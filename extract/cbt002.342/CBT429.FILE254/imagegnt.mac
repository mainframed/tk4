//TECH110Z JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110,REGION=6M
/*JOBPARM S=SYSA
//S EXEC ASMHCLG,PARM.L='LIST,LET,XREF,MAP'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSIN  DD *
IMG      TITLE 'IMAGING SYSTEM DOWNLOAD PREP'
IMAGEGEN CSECT ,
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K21      EQU   21
K23      EQU   23
K24      EQU   24
K26      EQU   26
K27      EQU   27
K29      EQU   29
K28      EQU   28
K32      EQU   32
K34      EQU   34
K36      EQU   36
K38      EQU   38
K40      EQU   40
K43      EQU   43
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K57      EQU   57
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K133     EQU   133
K136     EQU   136
K144     EQU   144
K152     EQU   152
K176     EQU   176
K196     EQU   196
K208     EQU   208
K212     EQU   212
K255     EQU   255
K256     EQU   256
K272     EQU   272
K320     EQU   320
K550     EQU   550
K2047    EQU   2047
K2048    EQU   2048
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
IMAGEGEN AMODE 24
IMAGEGEN RMODE 24
         SAVE  (14,12),,IMAGEGEN*&SYSDATE*&SYSTIME
         LR    RA,RF /*ADDRESSABILITY*/
         USING IMAGEGEN,RA,RB,RC
         LA    RB,K4095(,RA)
         LA    RB,K1(,RB)
         LA    RC,K4095(,RB)
         LA    RC,K1(,RC)
         LR    RF,RD /*SAVEAREA HOUSEKEEPING*/
         CNOP  K0,K4
         BAL   RD,*+K76
         DC    (K18)AL4(K0)
         ST    RD,K8(,RF)
         ST    RF,K4(,RD)
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         STCK  CLOCK
         UNPK  WORK(K7),CLOCK+K2(K4) PROVIDE UNIQUE SERIAL#
         TR    WORK+K0(K5),HEXCHAR-C'0'
         MVC   XPRM1+K6(K5),WORK+K0
         MVC   XPRM2+K18(K5),WORK+K0
OPENLIST OPEN  (INOUT,(UPDAT),BACKUP,(OUTPUT),REPORT,(OUTPUT))
         LTR   RF,RF /* OPEN OKAY */
         BNZ   FOPEN /*NO*/
         LM    R6,R8,OPENLIST+K4 /* PICKUP DCB PTRS */
         USING IHADCB,R6
         TM    DCBOFLGS,DCBOFOPN
         BZ    FOPEN
         DROP  R6
         USING IHADCB,R7
         TM    DCBOFLGS,DCBOFOPN
         BZ    FOPEN
         DROP  R7
         USING IHADCB,R8
         TM    DCBOFLGS,DCBOFOPN
         BZ    FOPEN
         DROP  R8
         GET   INOUT GET FIRST RECORD FOR REPORT ID
         MVC   XPRM1+K3(K3),K1(R1)
         MVC   XPRM2+K15(K3),K1(R1)
         B     LOOP2
LOOP     GET   INOUT
LOOP2    LR    R2,R1 SAVE RECORD PTR
         LR    R0,R1 FOR OUTPUT
         PUT   BACKUP,(0)
         PUT   REPORT,(0)
         LA    R6,INOUT PTR DCB
         USING IHADCB,R6
         LH    R6,DCBLRECL LRECL
         DROP  R6
         BCTR  R6,0 FOR EX
         EX    R6,TRASCII CONVERT TO ASCII
         LR    R0,R2 OUTPUT
         PUTX  INOUT
         B     LOOP
TRASCII  TR    K0(*-*,R2),EBCASCII /* CHANGE TO ASCII */
EOD      CLOSE (INOUT,,BACKUP,,REPORT)
         FREEPOOL INOUT   ,
         FREEPOOL BACKUP  ,
         FREEPOOL REPORT  ,
OPENSYSI OPEN  (SYSIN01,(OUTPUT))
         LTR   RF,RF /* OPEN OKAY */
         BNZ   FOPEN /*NO*/
         L     R6,OPENSYSI+K4 /* PICKUP DCB PTRS */
         USING IHADCB,R6
         TM    DCBOFLGS,DCBOFOPN
         BZ    FOPEN
         DROP  R6
         LA    R6,XPRMCTR RECORD CTR
         LA    R7,XPRM PTR
XPRMLOOP PUT   SYSIN01,(R7)
         LA    R7,K80(,R7)
         BCT   R6,XPRMLOOP
         CLOSE (SYSIN01)
         FREEPOOL SYSIN01 ,
         LOAD  EPLOC=XCOMMEP
         LR    RF,R0 /* EP */
         LA    R1,=A(XCOMMPRM) /* EXEC PARM */
         BALR  RE,RF /* GO SCHEDULE */
         LTR   RF,RF /* RC */
         BNZ   MAINEXIT /* SCHEDULE FAILURE */
         DELETE EPLOC=XCOMMEP
         SLR   RF,RF
MAINEXIT L     RD,K4(,RD)
         RETURN (14,12),RC=(15)
FOPEN    LA    RF,K24 /* GENERAL OPEN FAILURE */
         B     MAINEXIT
CLOCK    DC    D'0'
WORK     DC    D'0'
INOUT    DCB   DDNAME=INOUT,DSORG=PS,EODAD=EOD,MACRF=(GL,PM),          X
               RECFM=FB
BACKUP   DCB   DDNAME=BACKUP,DSORG=PS,MACRF=(PM),                      X
               RECFM=FB
REPORT   DCB   DDNAME=REPORT,DSORG=PS,MACRF=(PM),                      X
               RECFM=FB
SYSIN01  DCB   DDNAME=SYSIN01,DSORG=PS,MACRF=(PM)
XCOMMEP  DC    0F'0',CL8'XCOMJOB'
XCOMMPRM DC    AL2(L'XCOMMPRL)
XCOMMPRL DC    C'TYPE=SCHEDULE,GROUP=IMAGESYS'
HEXCHAR  DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
XPRM     EQU   * *
XPRM1    DC    CL80'ID=XXXXXXXX'
XPRM2    DC    CL80'FILE=R:\DENISE\XXXXXXXX.INP'
XPRM3    DC    CL80'FILETYPE=FILE'
XPRM4    DC    CL80'TYPE=SEND'
XPRM5    DC    CL80'CODE=ASCII'
XPRM6    DC    CL80'PACK=NO'
XPRM7    DC    CL80'COMPRESS=YES'
XPRM8    DC    CL80'RECSEP=NO'
XPRMCTR  EQU   (*-XPRM)/80
EBCASCII DC    (K256)AL1(K32) /* DEFAULT TO ASCII BLANK */
         ORG   EBCASCII+C' '
         DC    X'20'
         ORG   EBCASCII+C','
         DC    X'2C255F3E3F'
         ORG   EBCASCII+C'.'
         DC    X'2E3C282B2026'
         ORG   EBCASCII+C':'
         DC    X'3A2340273D22'
         ORG   EBCASCII+C'!'
         DC    X'21242A293B202D2F'
         ORG   EBCASCII+X'81'
         DC    X'616263646566676869'
         ORG   EBCASCII+X'91'
         DC    X'6A6B6C6D6E6F707172'
         ORG   EBCASCII+X'A2'
         DC    X'737475767778797A'
         ORG   EBCASCII+C'A'
         DC    X'414243444546474849'
         ORG   EBCASCII+C'J'
         DC    X'4A4B4C4D4E4F505152'
         ORG   EBCASCII+C'S'
         DC    X'535455565758595A'
         ORG   EBCASCII+C'0'
         DC    X'30313233343536373839'
         ORG
         DCBD  DSORG=(PO,QS),DEVD=DA
         END
/*
//*L.SYSLMOD DD DSN=SYS1.TECH.COMMON.LOADLIB(IMAGEGEN),DISP=SHR,
//* UNIT=,SPACE=
//G.INOUT DD DSN=TECH110.TEST.IMAGE,DISP=SHR,FREE=CLOSE
//G.LCLDS01 DD DSN=TECH110.TEST.IMAGE,DISP=SHR
//G.BACKUP DD DUMMY,UNIT=CART,DISP=(NEW,KEEP),FREE=CLOSE,
//     DSN=TECH110.TEST.BACKUP,LRECL=133,BLKSIZE=32718
//G.SYSIN01 DD DSN=&&XCOMPRM,DISP=(NEW,DELETE),UNIT=VIO,
//   SPACE=(TRK,(1,1)),RECFM=FB,LRECL=80
//G.REPORT   DD SYSOUT=Z,LRECL=133,FREE=CLOSE
//G.SYSUDUMP DD SYSOUT=*
