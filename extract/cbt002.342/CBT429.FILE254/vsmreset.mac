//JEFFKZ   JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=JEFFK,REGION=6M
//S EXEC ASMHCL
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSIN  DD *
VSMRESET CSECT , /*RESET REUSABLE VSAM CLUSTER*/
M0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
VSMRESET AMODE 24
VSMRESET RMODE 24
         SAVE  (14,12),,VSMRESET*&SYSDATE*&SYSTIME
         LR    RC,RF ADDRESSABILITY
         USING VSMRESET,RC
         LR    RF,RD SAVEAREA HOUSEKEEPING
         CNOP  0,4
         BAL   RD,*+76
         DC    18F'0'
         ST    RD,8(RF)
         ST    RF,4(RD)
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         OI    GFBITS,GFWTPSW IKJEFF19 BATCH SWITCH
         MVC   GFPGMNP,=A(PGMNAME) PROGRAM NAME FOR ERROR MSG
         MVC   GFPGMNL,=AL2(8) PROGNAME LEN
         GENCB BLK=ACB,AM=VSAM,DDNAME=VSMRESET,                        X
               MACRF=(RST,NSR,OUT),STRNO=1
         LTR   RF,RF OK
         BNZ   DEFACBER
         LR    R2,R1 SAVE ACB PTR
         ST    R2,GFCBPTR ACB PTR FOR IKJEFF19
         MVC   GFCALLID,=AL2(GFOPEN) OPEN FAILURE ID
         OPEN  ((R2)),MODE=24 OPEN AND CLOSE TO RESET
         LTR   RF,RF OK
         BNZ   OPENERR
         MVC   GFCALLID,=AL2(GFCLOSE) CLOSE FAILURE
         CLOSE ((R2)),MODE=24
         LTR   RF,RF OK
         BNZ   CLOSERR
         L     RD,4(RD)
         RETURN (14,12),RC=0
DEFACBER L     RD,4(RD)
         RETURN (14,12),RC=24
OPENERR  ST    RF,GFRCODE RETURN CODE
         LINK  EP=IKJEFF19,PARAM=GFPARMS
         L     RD,4(RD)
         RETURN (14,12),RC=28
CLOSERR  ST    RF,GFRCODE RETURN CODE
         LINK  EP=IKJEFF19,PARAM=GFPARMS
         L     RD,4(RD)
         RETURN (14,12),RC=32
         IKJEFFGF
PGMNAME  DC    CL8'VSMRESET' PROGRAM NAME
         LTORG
         END
/*
//L.SYSLMOD DD DSN=TD.TEST1.BATCH.LOADLIB(VSMRESET),DISP=SHR,
// UNIT=,SPACE=
