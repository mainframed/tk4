//TECH110G JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110,REGION=6M
//S EXEC ASMHCL
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
// DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN  DD *
PMON     CSECT
* COBOL V2 DYNAMIC CALL USING 8 CHAR TRACE ID POINT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
PMON     AMODE 24
PMON     RMODE 24
         SAVE  (14,12),,PMON*&SYSDATE*&SYSTIME
         LR    RA,RF ADDRESSABILITY
         USING PMON,RA,RB,RC
         LA    RB,4095(RA)
         LA    RB,1(RB)
         LA    RC,4095(RB)
         LA    RC,1(RC)
         LR    RF,RD SAVEAREA HOUSEKEEPING
         CNOP  0,4
         BAL   RD,*+76
         DC    18F'0'
         ST    RD,8(RF)
         ST    RF,4(RD)
         LR    R2,R1 SAVE PASSED PARAM PTR
         LA    R0,256
         GETMAIN RU,LV=(0)
         LR    R3,R1 SAVE AREA PTR
*        L     R5,0(,R2)
*        LA    R5,0(,R5)
*        MVC   WTO+8(8),0(R5) TRACEID 8 BYTES
         STCK  0(R3)
         UNPK  8(13,R3),0(7,R3)
         TR    8(12,R3),TRHEX
         MVC   WTO+17(12),8(R3)
         L     R5,X'224' ASCB PTR
         MVC   0(8,R3),X'40'(R5) ASCBEJST
         UNPK  8(13,R3),0(7,R3)
         TR    8(12,R3),TRHEX
         MVC   WTO+30(12),8(R3)
         MVC   0(8,R3),X'C8'(R5) ASCBSRBT
         UNPK  8(13,R3),0(7,R3)
         TR    8(12,R3),TRHEX
         MVC   WTO+43(12),8(R3)
WTO      WTO   'XXXXXXXX XXXXXXXXXXXX XXXXXXXXXXXX XXXXXXXXXXXX'
         LA    R0,256
         FREEMAIN RU,LV=(0),A=(R3)
         L     RD,4(RD)
         RETURN (14,12),RC=0
         LTORG
TRHEX    DC    256X'00'
         ORG   TRHEX+C'0'
         DC    C'0123456789ABCDEF'
         ORG
         END
/*
//L.SYSLMOD DD DSN=SYS1.TEST.BATCH.LOADLIB(PMON),DISP=SHR,
// UNIT=,SPACE=
