//JEFFKZ   JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=JEFFK,REGION=6M
//S EXEC ASMHCL
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.AMODGEN,DISP=SHR,UNIT=3380,VOL=SER=MVSDL1
//         DD DSN=SYS1.AMACLIB,DISP=SHR,UNIT=3380,VOL=SER=MVSDL1
//C.SYSIN  DD *
VTOCSCAN CSECT , /*VTOC ANALYSIS*/
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
VTOCSCAN AMODE 24
VTOCSCAN RMODE 24
         SAVE  (14,12),,VTOCSCAN*&SYSDATE*&SYSTIME
         LR    RA,RF ADDRESSABILITY
         USING VTOCSCAN,RA,RB,RC
         LA    RB,4095(RA)
         LA    RB,1(RB)
         LA    RC,4095(RB)
         LA    RC,1(RC)
         LR    RF,RD SAVEAREA HOUSEKEEPING
         CNOP  0,4
         BAL   RD,*+76
         DC    18F'0'
         ST    RD,8(RF)
         ST    RF,4(RD)
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         EXTRACT CSCLPTR,FIELDS=(COMM)
         L     R4,CSCLPTR /* COMMUNIC AREA PTR */
         USING COMLIST,R4
         L     R0,COMECBPT /* PTR COMM ECB */
         ST    R0,ECBPOPER /* PLACE INTO ECBLIST */
         LA    R4,COMCIBPT /* PTR CIB CHAIN PTR */
         DROP  R4
         ST    R4,CIBCHPTR /* SAVE PTR CHAIN PTR */
         ICM   R5,15,0(R4) /* CURRENT CIB CHAIN PTR */
         BZ    NOSTART /* NOT STARTED TASK */
         USING CIBNEXT,R5
         CLI   CIBVERB,CIBSTART /* STARTED TASK CIB */
         BNE   NOSTART /* NO */
         QEDIT ORIGIN=(R4),BLOCK=(R5) /* UNCHAIN START CIB */
         LTR   RF,RF
         BNZ   FQEDIT
NOSTART  QEDIT ORIGIN=(R4),CIBCTR=0 /* DISABLE OPER MODIFIES */
         LTR   RF,RF
         BNZ   FQEDIT
         DROP  R5
         LA    R7,4095 MAX SAMPLE COUNT
         OPEN  (WORKDCB,(OUTPUT))
         LA    R2,WORKDCB
         USING IHADCB,R2
         TM    DCBOFLGS,DCBOFOPN OPEN OKAY
         BZ    FWORK
         DROP  R2
IOSVSCAN TIME  DEC
         ST    R0,WORK TIME IS HHMMSSTT - PD
         UNPK  WORK+3(5),WORK+0(3)
         MVC   WORKOUT+22(4),WORK+3
         LA    R1,IOSVPARM /* SCAN UCBS */
         L     RF,CVTPTR
         USING CVTMAP,RF
         L     RF,CVTUCBSC /* IOSVSUCB EP PTR */
         DROP  RF
         BALR  RE,RF /* SCAN FOR DASD ONLY */
         LTR   RF,RF /* SCAN END */
         BNZ   ENDIOSV
         L     R8,IOSVPARM+12 /* PICKUP UCB PTR */
         USING UCBOB,R8
         TM    UCBSTAT,UCBONLI ONLINE
         BZ    IOSVSCAN NO
         MVC   WORKOUT(6),UCBVOLI  /* VOLSER */
         ST    R8,UCBLIST FOR DEVTYPE
         DEVTYPE ,(DEVTYPE,24),UCBLIST=(UCBLIST,1)
         LTR   RF,RF
         BNZ   FDEVT
         LH    RF,DEVTYPE+8 3380 PHYS CYL COUNT
         BCTR  RF,0 SUBTRACT CE CYL
         CVD   RF,WORK
         UNPK  WORK+0(5),WORK+5(3)
         OI    WORK+4,C'0'
         MVC   WORKOUT+7(4),WORK+1
         LSPACE UCB=((R8)),DATA=LSPACE
         LTR   RF,RF
         BNZ   FLSPACE
         L     RF,LSPACE+8 TOTAL FREE CYL
         CVD   RF,WORK
         UNPK  WORK+0(5),WORK+5(3)
         OI    WORK+4,C'0'
         MVC   WORKOUT+12(4),WORK+1
         L     RF,LSPACE+32 FRAGMENTATION INDEX 0-999 (HIGH=BAD FRAG)
         CVD   RF,WORK
         UNPK  WORK+0(5),WORK+5(3)
         OI    WORK+4,C'0'
         MVC   WORKOUT+17(4),WORK+1
         PUT   WORKDCB,WORKOUT SAVE DATA
         B     IOSVSCAN CONTINUE
         DROP  R8
ENDIOSV  EQU   * *
         STIMER WAIT,DINTVL=DINTVL
         L     RF,ECBPOPER DID OPER STOP US
         TM    0(RF),X'40' POSTED
         BO    BREAKOUT
         BCT   R7,IOSVSCAN LOOP
BREAKOUT CLOSE (WORKDCB)
         L     RD,4(RD)
         RETURN (14,12),RC=0
FQEDIT   L     RD,4(RD)
         RETURN (14,12),RC=24
FWORK    L     RD,4(RD)
         RETURN (14,12),RC=28
FDEVT    L     RD,4(RD)
         RETURN (14,12),RC=32
FLSPACE  L     RD,4(RD)
         RETURN (14,12),RC=36
WORK     DC    D'0'
DINTVL   DC    C'00300000' 30 MIN WAIT
LSPACE   DC    36X'00'
CIBCHPTR DC    A(*-*)
CSCLPTR  DC    A(*-*)
MECBLIST DC    0F'0'
ECBPOPER DC    A(*-*)
TIMERPOP DC    X'80',AL3(*-*)
IOSVPARM DC    A(IOSVWORK,IOSVDEVT),X'80',AL3(IOSVPARM+12),A(0)
IOSVWORK DC    512X'00'
UCBLIST  DC    A(*-*) FOR DEVTYPE
DEVTYPE  DC    24X'00' DEVTYPE RESPONSE AREA
WORKOUT  DC    CL26' '
IOSVDEVT DC    AL1(UCB3DACC) DASD CLASS
         LTORG
         PRINT NOGEN
WORKDCB  DCB   DDNAME=WORK,DSORG=PS,MACRF=(GM,PM),RECFM=FB,LRECL=26
         CVT   DSECT=YES
         DSECT
         IEFUCBOB
         DSECT
         IEZCOM
         DSECT
         IEZCIB
         DCBD  DSORG=QS,DEVD=DA
         END
/*
//L.SYSLMOD DD DSN=TD.TEST1.BATCH.LOADLIB(VTOCSCAN),DISP=SHR,
// UNIT=,SPACE=
//*G.WORK DD DSN=JEFFK.VTOC.DATA,DISP=SHR
//*G.SYSUDUMP DD SYSOUT=*
