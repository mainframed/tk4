//TECH110Z JOB (530000000000),'TECH.JEFF.KAPLAN',REGION=6M,
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110
/*JOBPARM S=SYSA
//S EXEC ASMHCLG
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
// DD DSN=SYSM.RMF.V4R2M1.MAC01,DISP=SHR
//C.SYSIN DD *
SMFPROC  CSECT ,
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         SAVE  (14,12),,SMFPROC*&SYSDATE*&SYSTIME
         LR    RA,RF
         USING SMFPROC,RA,RB,RC
         LA    RB,4095(,RA)
         LA    RB,1(,RB)
         LA    RC,4095(,RB)
         LA    RC,1(,RC)
         LR    RF,RD
         CNOP  0,4
         BAL   RD,*+76
         DC    18A(0)
         ST    RD,8(,RF)
         ST    RF,4(,RD)
         OPEN  (SMFIN,(INPUT),SMFCPU,(OUTPUT),SMFWRK,(OUTPUT),SMFJOB,(OX
               UTPUT),SMFWTR,(OUTPUT))
LOOPIN   XC    INPUT(256),INPUT
         XC    INPUT+256(256),INPUT+256
         GET   SMFIN,INPUT
         LA    R4,INPUT
         CLI   INPUT+5,72 TYPE
         BE    TYPE72
         CLI   INPUT+5,30 TYPE
         BE    TYPE30
         CLI   INPUT+5,06 TYPE
         BE    TYPE06
         CLI   INPUT+5,70 TYPE
         BNE   LOOPIN
         USING SMF70HDR,R4
TYPE70   MVC   CPUSYSID(4),SMF70SID
         LA    R4,INPUT
         A     R4,INPUT+28
         DROP  R4
         USING SMF70PRO,R4
         MVC   CPUDATE(10),=CL10'19YY-MM-DD'
         UNPK  WORK(3),SMF70DAT+1(2)
         MVC   CPUDATE+2(2),WORK
         CLC   WORK(2),=C'92'
         BE    DLEAP70
         CLC   WORK(2),=C'96'
         BE    DLEAP70
         LA    R5,NLEPYEAR
         B     CALC70
DLEAP70  LA    R5,LEAPYEAR
CALC70   CP    SMF70DAT+2(2),0(2,R5)
         BNL   FC70
         LA    R5,4(,R5)
         B     CALC70
FC70     MVC   CPUDATE+5(2),2(R5)
         SP    SMF70DAT+2(2),0(2,R5)
         AP    SMF70DAT+2(2),=PL1'1'
         UNPK  WORK(3),SMF70DAT+2(2)
         MVC   CPUDATE+8(2),WORK+1
         OI    CPUDATE+9,C'0'
         UNPK  WORK(5),SMF70IST(3)
         MVC   CPUTIME(2),WORK+1
         CLI   SMF70INT,X'59' 1 HOUR INTERVAL
         BE    GOODINT0
         CLI   SMF70INT,X'60' 1 HOUR INTERVAL
         BNE   LOOPIN IGNORE
         DROP  R4
         USING SMF70CPU,R4
GOODINT0 LA    R4,INPUT
         A     R4,INPUT+44
         LH    R5,INPUT+48 LEN OF CPUDATA SEC
         LH    R6,INPUT+50 NMBR OF CPU DATA SEC
         LR    RF,R6 SAVE CPUSER CTR
         MH    RF,=H'3432' NUMBR SEC
         XC    CPUUTIL(4),CPUUTIL
NEXTCPUS L     R0,SMF70WAT
         AL    R0,CPUUTIL
         ST    R0,CPUUTIL
         ALR   R4,R5 NEXT CPUSEC
         BCT   R6,NEXTCPUS ...
         LR    R0,RF CALC CPUUTIL
         SL    R0,CPUUTIL
         ST    R0,CPUUTIL
         ST    RF,CPUINTVL
         PUT   SMFCPU,OUTCPU
         B     LOOPIN ..
         DROP  R4
         USING SMF72HDR,R4
TYPE72   MVC   WRKSYSID(4),SMF72SID
         CLC   =H'1',SMF72STY SUBTYPE
         BNE   LOOPIN
         LA    R4,INPUT
         A     R4,INPUT+28
         DROP  R4
         USING SMF72PRO,R4
         MVC   WRKDATE(10),=CL10'19YY-MM-DD'
         UNPK  WORK(3),SMF72DAT+1(2)
         MVC   WRKDATE+2(2),WORK
         CLC   WORK(2),=C'92'
         BE    DLEAP72
         CLC   WORK(2),=C'96'
         BE    DLEAP72
         LA    R5,NLEPYEAR
         B     CALC72
DLEAP72  LA    R5,LEAPYEAR
CALC72   CP    SMF72DAT+2(2),0(2,R5)
         BNL   FC72
         LA    R5,4(,R5)
         B     CALC72
FC72     MVC   WRKDATE+5(2),2(R5)
         SP    SMF72DAT+2(2),0(2,R5)
         AP    SMF72DAT+2(2),=PL1'1'
         UNPK  WORK(3),SMF72DAT+2(2)
         MVC   WRKDATE+8(2),WORK+1
         OI    WRKDATE+9,C'0'
         UNPK  WORK(5),SMF72IST(3)
         MVC   WRKTIME(2),WORK+1
         CLI   SMF72INT,X'59' 1 HOUR INTERVAL
         BE    GOODINT2
         CLI   SMF72INT,X'60' 1 HOUR INTERVAL
         BNE   LOOPIN IGNORE
         DROP  R4
         USING SMF72CTL,R4
GOODINT2 LA    R4,INPUT
         A     R4,INPUT+36
         CLC   =AL2(100),SMF72SUB PGN>=100
         BH    LOOPIN IGNORE
         MVC   WRKXNAME(8),=CL8' '
         CLI   SMF72SYS,C' '
         BE    LOOPIN
         CLI   SMF72SYS,C'J'
         BE    ISJOB
         CLI   SMF72SYS,C'T'
         BE    ISTSU
         MVC   WRKXNAME(8),SMF72NAM
         CLI   WRKXNAME,C' '
         BE    LOOPIN
         B     COMMON
ISTSU    MVC   WRKXNAME(3),=C'TSU'
         MVC   WRKXNAME+3(5),SMF72USR
         B     COMMON
ISJOB    MVC   WRKXNAME(6),=C'JCLASS'
         MVC   WRKXNAME+6(1),SMF72CLS
         CLI   WRKXNAME+6,C' '
         BE    LOOPIN
         DROP  R4
         USING SMF72WKL,R4
COMMON   LA    R4,INPUT
         A     R4,INPUT+44
         MVC   WRKXCOUN(4),SMF72TTX
         L     RF,SMF72CTS
         LTR   RF,RF
         BM    LOOPIN
         A     RF,SMF72STS
         LTR   RF,RF
         BM    LOOPIN
         A     RF,SMF72ITS
         LTR   RF,RF CHECK
         BZ    LOOPIN
         BM    MAX72
PUTT72   ST    RF,WRKSERV
         PUT   SMFWRK,OUTWRK
         B     LOOPIN
MAX72    L     RF,=F'2147483647' MAX
         B     PUTT72
         DROP  R4
         USING SMFRCD06,R4
TYPE06   MVC   WTRLREC(4),SMF6NLR    RECORD CTR
         MVC   WTRUID(8),=CL8' '
         TM    SMF6PAD1,X'40' COMMON SECT
         BZ    WRT06 NO
         TM    SMF6PAD1,X'80' 3800   SECT
         BZ    NO3800
         LA    R4,SMF6LN1 POSITION VAR SECT
         AH    R4,0(,R4)  ...
         AH    R4,0(,R4)
         B     T6COMM
NO3800   LA    R4,SMF6LN1 POSITION VAR SECT
         AH    R4,0(,R4)  ...
T6COMM   MVC   WTRUID(8),SMF6USID-SMF6LN3(R4)
WRT06    PUT   SMFWTR,OUTWTR
         B     LOOPIN
         DROP  R4
         USING SMFRCD30,R4
TYPE30   MVC   JOBSYSID(4),SMF30SID
         MVC   JOBDATE(10),=CL10'19YY-MM-DD'
         UNPK  WORK(3),SMF30DTE+1(2)
         MVC   JOBDATE+2(2),WORK
         CLC   WORK(2),=C'92'
         BE    DLEAP30
         CLC   WORK(2),=C'96'
         BE    DLEAP30
         LA    R5,NLEPYEAR
         B     CALC30
DLEAP30  LA    R5,LEAPYEAR
CALC30   CP    SMF30DTE+2(2),0(2,R5)
         BNL   FC30
         LA    R5,4(,R5)
         B     CALC30
FC30     MVC   JOBDATE+5(2),2(R5)
         SP    SMF30DTE+2(2),0(2,R5)
         AP    SMF30DTE+2(2),=PL1'1'
         UNPK  WORK(3),SMF30DTE+2(2)
         MVC   JOBDATE+8(2),WORK+1
         OI    JOBDATE+9,C'0'
         SLR   R6,R6
         ICM   R7,15,SMF30TME
         D     R6,=F'360000'
         CVD   R7,WORK
         UNPK  WORK(3),WORK+6(2)
         OI    WORK+2,C'0'
         MVC   JOBTIME(2),WORK+1
         DROP  R4
         USING SMF30PSS,R4
         LA    R4,INPUT
         A     R4,INPUT+24
         CLC   =H'5',SMF30TYP JOBTERM ONLY
         BNE   LOOPIN
         DROP  R4
         USING SMF30ID,R4
         LA    R4,INPUT
         A     R4,INPUT+32
         MVC   JOBCLASS(1),SMF30CLS
         CLI   JOBCLASS,C' ' BLANK FOR STC TSU
         BE    LOOPIN IGNORE
         MVC   JOBRGRP(8),SMF30GRP
         MVC   JOBRUID(8),SMF30RUD
         DROP  R4
         USING SMF30OPS,R4
         LA    R4,INPUT
         A     R4,INPUT+88
         L     R0,SMF30PTM
         AL    R0,SMF30TPR
         STCM  R0,15,JOBMOUNT
         DROP  R4
         USING SMF30URA,R4
         LA    R4,INPUT
         A     R4,INPUT+40
         L     R0,SMF30TEP
         LTR   R0,R0
         L     RF,=F'2147483647' MAX
         BM    SETEXCP
         LR    RF,R0 SET EXCP CTR
SETEXCP  STCM  RF,15,JOBIOCNT ...
         DROP  R4
         USING SMF30ACS,R4
         LA    R4,INPUT
         A     R4,INPUT+64
         MVC   JOBACCT(9),=CL9' '
         CLI   SMF30ACL,X'09'
         BL    DO30PRF
         MVC   JOBACCT(9),SMF30ACT
         DROP  R4
         USING SMF30PRF,R4
DO30PRF  LA    R4,INPUT
         A     R4,INPUT+80
         L     RF,SMF30CSU
         LTR   RF,RF
         BM    LOOPIN
         A     RF,SMF30SRB
         LTR   RF,RF
         BM    LOOPIN
         A     RF,SMF30IO
         LTR   RF,RF
         BZ    LOOPIN
         BM    MAX30
PUT30    STCM  RF,15,JOBSERV
         PUT   SMFJOB,OUTJOB
         B     LOOPIN
MAX30    L     RF,=F'2147483647' MAX
         B     PUT30
EODAD    EQU   * *
         CLOSE (SMFIN,,SMFCPU,,SMFWRK,,SMFJOB,,SMFWTR)
         FREEPOOL SMFIN
         FREEPOOL SMFCPU
         FREEPOOL SMFWRK
         FREEPOOL SMFJOB
         FREEPOOL SMFWTR
         SLR   R9,R9
MAINEXIT L     RD,4(,RD)
         LR    RF,R9
         RETURN (14,12),RC=(15)
WORK     DS    D
SMFIN    DCB   DDNAME=SMFIN,DSORG=PS,MACRF=(GM),EODAD=EODAD
SMFCPU   DCB   DDNAME=SMFCPU,DSORG=PS,MACRF=(PM)
SMFWRK   DCB   DDNAME=SMFWRK,DSORG=PS,MACRF=(PM)
SMFJOB   DCB   DDNAME=SMFJOB,DSORG=PS,MACRF=(PM)
SMFWTR   DCB   DDNAME=SMFWTR,DSORG=PS,MACRF=(PM)
LEAPYEAR DC    PL2'336',CL2'12'
         DC    PL2'306',CL2'11'
         DC    PL2'275',CL2'10'
         DC    PL2'245',CL2'09'
         DC    PL2'214',CL2'08'
         DC    PL2'183',CL2'07'
         DC    PL2'153',CL2'06'
         DC    PL2'122',CL2'05'
         DC    PL2'092',CL2'04'
         DC    PL2'061',CL2'03'
         DC    PL2'032',CL2'02'
         DC    PL2'001',CL2'01'
NLEPYEAR DC    PL2'335',CL2'12'
         DC    PL2'305',CL2'11'
         DC    PL2'274',CL2'10'
         DC    PL2'244',CL2'09'
         DC    PL2'213',CL2'08'
         DC    PL2'182',CL2'07'
         DC    PL2'152',CL2'06'
         DC    PL2'121',CL2'05'
         DC    PL2'091',CL2'04'
         DC    PL2'060',CL2'03'
         DC    PL2'032',CL2'02'
         DC    PL2'001',CL2'01'
OUTCPU   DS    0F'0'
CPUSYSID DS    CL4
CPUDATE  DS    CL10 YYYY-MM-DD
CPUTIME  DS    CL2  HH
CPUUTIL  DS    FL4
CPUINTVL DS    FL4
OUTWRK   DS    0F'0'
WRKXNAME DS    CL8 STCNAME,TSUXXXXX,CLASSX
WRKSYSID DS    CL4
WRKXCOUN DS    FL4 TRANSACTION ENDED COUNT
WRKSERV  DS    FL4
WRKDATE  DS    CL10 YYYY-MM-DD
WRKTIME  DS    CL2  HH
OUTJOB   DS    0F'0'
JOBRGRP  DS    CL8  RACF GROUP
JOBRUID  DS    CL8  RACF UID
JOBCLASS DS    C    JOBCLASS
JOBSERV  DS    FL4
JOBSYSID DS    CL4
JOBDATE  DS    CL10 YYYY-MM-DD
JOBTIME  DS    CL2  HH
JOBACCT  DS    CL9  FIRST ACCT FIELD
JOBIOCNT DS    FL4  JOB IO COUNTER
JOBMOUNT DS    FL4  JOB MOUNT CTR
OUTWTR   DS    0F'0'
WTRUID   DS    CL8
WTRLREC  DS    FL4
TRHEX    DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         LTORG
INPUT    DS    0D'0',32767C
         ERBSMFR (70,72)
SMFRCD06 DSECT
         IFASMFR (6,30)
         END
/*
//G.SMFIN DD DSN=PD.SMFDUMP.WEEKLY.BACKUP(0),DISP=SHR
//*       DD DSN=PD.SMFDUMP.WEEKLY.BACKUP(-1),DISP=SHR,
//*   UNIT=AFF=SMFIN
//G.SYSUDUMP DD SYSOUT=*
//G.SMFCPU DD DSN=&&SMFCPU,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=24,RECFM=FB
//G.SMFWRK DD DSN=&&SMFWRK,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=32,RECFM=FB
//G.SMFJOB DD DSN=&&SMFJOB,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=54,RECFM=FB
//G.SMFWTR DD DSN=&&SMFWTR,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=12,RECFM=FB
//SORTCPU EXEC PGM=SORT
//SYSPRINT DD SYSOUT=*
//SYSOUT DD SYSOUT=*
//SORTIN DD DSN=&&SMFCPU,DISP=(OLD,DELETE)
//*SORTOUT DD DSN=TECH110.USER.SRTCPU,DISP=MOD
//SORTOUT DD DSN=&&SRTCPUM,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=24,RECFM=FB
//SYSIN DD *
  SORT FIELDS=(1,24,BI,A)
/*
//SORTWRK EXEC PGM=SORT
//SYSPRINT DD SYSOUT=*
//SYSOUT DD SYSOUT=*
//SORTIN DD DSN=&&SMFWRK,DISP=(OLD,DELETE)
//*SORTOUT DD DSN=TECH110.USER.SRTWRK,DISP=MOD
//SORTOUT DD DSN=&&SRTWRKM,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=32,RECFM=FB
//SYSIN DD *
  SORT FIELDS=(1,32,BI,A)
/*
//SORTJOB EXEC PGM=SORT
//SYSPRINT DD SYSOUT=*
//SYSOUT DD SYSOUT=*
//SORTIN DD DSN=&&SMFJOB,DISP=(OLD,DELETE)
//*SORTOUT DD DSN=TECH110.USER.SRTJOB,DISP=MOD
//SORTOUT DD DSN=&&SRTJOBM,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=54,RECFM=FB
//SYSIN DD *
  SORT FIELDS=(1,54,BI,A)
/*
//SORTWTR EXEC PGM=SORT
//SYSPRINT DD SYSOUT=*
//SYSOUT DD SYSOUT=*
//SORTIN DD DSN=&&SMFWTR,DISP=(OLD,DELETE)
//*SORTOUT DD DSN=TECH110.USER.SRTWTR,DISP=MOD
//SORTOUT DD DSN=&&SRTWTRM,DISP=(NEW,PASS,DELETE),
// SPACE=(CYL,(10,5)),LRECL=12,RECFM=FB
//SYSIN DD *
  SORT FIELDS=(1,12,BI,A)
/*
//UTIL EXEC DSNUPROC,SYSTEM=DSNA,UID='TEMPJ',UTPROC=''
//DSNUPROC.SYSREC DD DSN=&&SRTCPUM,DISP=(OLD,PASS)
//*
//DSNUPROC.SYSUT1 DD DSN=&&SYSUT1,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//*
//DSNUPROC.SORTOUT DD DSN=&&SORTOUT,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//DSNUPROC.SYSERR DD SYSOUT=*
//DSNUPROC.SYSMAP DD SYSOUT=*
//*
//DSNUPROC.SYSIN    DD *
  LOAD DATA INDDN(SYSREC) RESUME(YES) LOG(NO)
  INTO TABLE BUILD.TRMFCPU
  (SYSID            POSITION (001:004) CHAR,
   INTVDATE         POSITION (005:014) DATE EXTERNAL(10),
   INTVTIME         POSITION (015:016) CHAR,
   CPUUTIL          POSITION (017:020) INTEGER,
   CPUINTV          POSITION (021:024) INTEGER)
/*
//UTIL EXEC DSNUPROC,SYSTEM=DSNA,UID='TEMPK',UTPROC=''
//DSNUPROC.SYSREC DD DSN=&&SRTWRKM,DISP=(OLD,PASS)
//*
//DSNUPROC.SYSUT1 DD DSN=&&SYSUT1,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//*
//DSNUPROC.SORTOUT DD DSN=&&SORTOUT,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//DSNUPROC.SYSERR DD SYSOUT=*
//DSNUPROC.SYSMAP DD SYSOUT=*
//*
//DSNUPROC.SYSIN    DD *
  LOAD DATA INDDN(SYSREC) RESUME(YES) LOG(NO)
  INTO TABLE BUILD.TRMFWRK
  (XNAME            POSITION (001:008) CHAR,
   SYSID            POSITION (009:012) CHAR,
   XCOUNT           POSITION (013:016) INTEGER,
   SERV             POSITION (017:020) INTEGER,
   INTVDATE         POSITION (021:030) DATE EXTERNAL(10),
   INTVTIME         POSITION (031:032) CHAR)
/*
//UTIL EXEC DSNUPROC,SYSTEM=DSNA,UID='TEMPL',UTPROC=''
//DSNUPROC.SYSREC DD DSN=&&SRTJOBM,DISP=(OLD,PASS)
//*
//DSNUPROC.SYSUT1 DD DSN=&&SYSUT1,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//*
//DSNUPROC.SORTOUT DD DSN=&&SORTOUT,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//DSNUPROC.SYSERR DD SYSOUT=*
//DSNUPROC.SYSMAP DD SYSOUT=*
//*
//DSNUPROC.SYSIN    DD *
  LOAD DATA INDDN(SYSREC) RESUME(YES) LOG(NO)
  INTO TABLE BUILD.TRMFJOB
  (RACGROUP         POSITION (001:008) CHAR,
   RACUID           POSITION (009:016) CHAR,
   JOBCLASS         POSITION (017:017) CHAR,
   SERV             POSITION (018:021) INTEGER,
   SYSID            POSITION (022:025) CHAR,
   SMFDATE          POSITION (026:035) DATE EXTERNAL(10),
   SMFTIME          POSITION (036:037) CHAR,
   JOBACCT          POSITION (038:046) CHAR,
   JOBIOCNT         POSITION (047:050) INTEGER,
   JOBMOUNT         POSITION (051:054) INTEGER)
/*
//UTIL EXEC DSNUPROC,SYSTEM=DSNA,UID='TEMPM',UTPROC=''
//DSNUPROC.SYSREC DD DSN=&&SRTWTRM,DISP=(OLD,PASS)
//*
//DSNUPROC.SYSUT1 DD DSN=&&SYSUT1,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//*
//DSNUPROC.SORTOUT DD DSN=&&SORTOUT,
//     DISP=(NEW,DELETE),
//     SPACE=(CYL,(5,5)),
//     UNIT=VIO
//DSNUPROC.SYSERR DD SYSOUT=*
//DSNUPROC.SYSMAP DD SYSOUT=*
//*
//DSNUPROC.SYSIN    DD *
  LOAD DATA INDDN(SYSREC) RESUME(YES) LOG(NO)
  INTO TABLE BUILD.TRMFWTR
  (WTRUID           POSITION (001:008) CHAR,
   WTRLREC          POSITION (009:012) INTEGER)
/*
//G1 EXEC PGM=IEBGENER,REGION=6M
//SYSPRINT DD SYSOUT=*
//SYSUT1 DD DSN=&&SRTWTRM,DISP=(OLD,DELETE)
//SYSUT2 DD DSN=TECH110.USER.SRTWTR,DISP=(MOD,KEEP)
//SYSIN  DD DUMMY
//G2 EXEC PGM=IEBGENER,REGION=6M
//SYSPRINT DD SYSOUT=*
//SYSUT1 DD DSN=&&SRTJOBM,DISP=(OLD,DELETE)
//SYSUT2 DD DSN=TECH110.USER.SRTJOB,DISP=(MOD,KEEP)
//SYSIN  DD DUMMY
//G3 EXEC PGM=IEBGENER,REGION=6M
//SYSPRINT DD SYSOUT=*
//SYSUT1 DD DSN=&&SRTWRKM,DISP=(OLD,DELETE)
//SYSUT2 DD DSN=TECH110.USER.SRTWRK,DISP=(MOD,KEEP)
//SYSIN  DD DUMMY
//G4 EXEC PGM=IEBGENER,REGION=6M
//SYSPRINT DD SYSOUT=*
//SYSUT1 DD DSN=&&SRTCPUM,DISP=(OLD,DELETE)
//SYSUT2 DD DSN=TECH110.USER.SRTCPU,DISP=(MOD,KEEP)
//SYSIN  DD DUMMY
//
