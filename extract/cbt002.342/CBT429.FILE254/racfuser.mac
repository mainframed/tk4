//TECH110Z JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110
//S EXEC ASMHCL,PARM.L='XREF,LIST,AC=1'
//C.SYSLIB DD DSN=SYSM.MACLIB,DISP=SHR
//         DD DSN=SYSM.MODGEN,DISP=SHR
//C.SYSIN  DD *
RCF      TITLE 'REXX RACFUSER PROFILE RETRIEVAL'
RACFUSER CSECT , /* RACFUSER */
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K24      EQU   24
K28      EQU   28
K32      EQU   32
K36      EQU   36
K40      EQU   40
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K255     EQU   255
K256     EQU   256
K264     EQU   264
K424     EQU   424
K512     EQU   512
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
RACFUSER AMODE 24
RACFUSER RMODE 24
         SAVE  (14,12),,RACFUSER*&SYSDATE*&SYSTIME
         LR    R3,R1 /* CPPL */
         USING CPPL,R3
         LR    RA,RF /*ADDRESSABILITY*/
         USING RACFUSER,RA,RB,RC
         LA    RB,K4095(,RA)
         LA    RB,K1(,RB)
         LA    RC,K4095(,RB)
         LA    RC,K1(,RC)
         LR    R2,RD /*SAVEAREA HOUSEKEEPING*/
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         SLR   R9,R9 RC
         LA    R0,DWORDS
         GETMAIN R,LV=(0)
         LR    RD,R1
         ST    RD,K8(,R2)
         ST    R2,K4(,RD)
         USING RACFWMAP,RD
         XC    PARSEPPL(K28),PARSEPPL /* PREP PARAM SCAN */
         XC    CMDPANS,CMDPANS
         XC    CMDPECB,CMDPECB
         LA    R8,PARSEPPL MAP
         USING PPL,R8
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         LA    R0,CMDPECB
         ST    R0,PPLECB
         LA    R0,CMDPANS
         ST    R0,PPLANS
         L     R0,=A(IKJPARM)
         ST    R0,PPLPCL
         DROP  R8
         CALLTSSR EP=IKJPARS,MF=(E,(R8))
         LTR   R9,RF
         BNZ   MAINEXIT
         L     R4,CMDPANS /* RESPONSE */
         USING IKJPARMD,R4
         L     R5,USERPARM
         LH    R6,USERPARM+K4
         TM    USERPARM+K6,X80 UID FOUND
         BZ    UIDERROR
         STC   R6,USERID /* FOR RACF */
         BCTR  R6,K0
         EX    R6,MOVEUID
         LA    R1,CMDPANS
         IKJRLSA (R1)
         LA    R5,RACFWORK
         MVC   RACFWORK(K4),=A(K96)
         LA    R6,LXTEINTY
         MVC   K0(LLLEINTY,R6),LSTEINTY MAKE RENT
         LA    R7,LXTFNAME
         MVC   K0(LLLFNAME,R7),FNAME
         LA    R7,LXTFINT
         MVC   K0(LLLFINT,R7),FINT
         LA    R7,LXTFAUDT
         MVC   K0(LLLFAUDT,R7),FAUDT
         LA    R7,LXTFSPEC
         MVC   K0(LLLFSPEC,R7),FSPEC
         LA    R7,LXTFOPER
         MVC   K0(LLLFOPER,R7),FOPER
         LA    R7,LXTFREVK
         MVC   K0(LLLFREVK,R7),FREVK
         LA    R7,LXTFGACC
         MVC   K0(LLLFGACC,R7),FGACC
         LA    R7,DOUTLINE
         MVC   K0(CULEN,R7),COUTLINE
         DROP  R4
         LA    R4,USERID
         ICHEINTY LOCATE,TYPE='USR',ENTRY=(R4),RELEASE=1.8.1,          X
               WKAREA=(R5),                                            X
               ACTIONS=(FNAME,FINT,FAUDT,FSPEC,FOPER,FREVK,FGACC),     X
               OPTIONS=(ACTION),MF=(E,(R6))
         LTR   R9,RF
         BNZ   NOUSER
         LA    R1,RACFWORK+K32
         CLI   K0(R1),C' ' ANYTHING THERE
         BE    NODATA
         MVC   DUID(K20),K0(R1)
         LA    R1,RACFWORK+K56
         CLI   K0(R1),C' ' ANYTHING THERE
         BE    DOATTRIB
         CLI   K0(R1),X'FF' ANYTHING THERE
         BE    DOATTRIB
         SLR   R0,R0
         IC    R0,RACFWORK+K56
         CVD   R0,WORK
         UNPK  WORK(K3),WORK+K6(K2)
         MVC   DPSWDINT(K2),WORK+K1
         OI    DPSWDINT+K1,C'0'
DOATTRIB EQU * *
         LA    RF,DATTRIB
         LA    R1,RACFWORK+61
         CLI   K0(R1),X'00' ANYTHING THERE
         BE    DA1
         MVC   K0(K8,RF),=C'Auditor '
         LA    RF,K8(,RF)
DA1      EQU   * *
         LA    R1,RACFWORK+66
         CLI   K0(R1),X'00' ANYTHING THERE
         BE    DA2
         MVC   K0(K8,RF),=C'Special '
         LA    RF,K8(,RF)
DA2      EQU   * *
         LA    R1,RACFWORK+71
         CLI   K0(R1),X'00' ANYTHING THERE
         BE    DA3
         MVC   K0(K11,RF),=C'Operations '
         LA    RF,K11(,RF)
DA3      EQU   * *
         LA    R1,RACFWORK+76
         CLI   K0(R1),X'00' ANYTHING THERE
         BE    DA4
         MVC   K0(K8,RF),=C'Revoked '
         LA    RF,K8(,RF)
DA4      EQU   * *
         LA    R1,RACFWORK+81
         CLI   K0(R1),X'00' ANYTHING THERE
         BE    DA5
         MVC   K0(K6,RF),=C'GRPACC'
         LA    RF,K6(,RF)
DA5      EQU   * *
         LA    R0,DULEN
         LA    R1,DOUTLINE
         STM   R0,R1,VALUELEN ...
         LINK  EP=IKJCT441,PARAM=(ECODE,NAMEPTR,NAMELEN,VALUEPTR,      X
               VALUELEN,TOKEN),VL=1
         LTR   R9,RF is rexx env active
         BZ    MAINEXIT yes leave
         LA    R0,DULEN
         LA    R1,DOUTLINE
         TPUT  (1),(0)
         B     MAINEXIT
NOUSER   LA    R0,L'NOUSERM
         LA    R1,NOUSERM
         STM   R0,R1,VALUELEN ...
         LINK  EP=IKJCT441,PARAM=(ECODE,NAMEPTR,NAMELEN,VALUEPTR,      X
               VALUELEN,TOKEN),VL=1
         LTR   R9,RF is rexx env active
         BZ    MAINEXIT yes leave
         LA    R0,L'NOUSERM
         LA    R1,NOUSERM
         TPUT  (1),(0)
         B     MAINEXIT
NODATA   LA    R0,L'NODATAM
         LA    R1,NODATAM
         STM   R0,R1,VALUELEN ...
         LINK  EP=IKJCT441,PARAM=(ECODE,NAMEPTR,NAMELEN,VALUEPTR,      X
               VALUELEN,TOKEN),VL=1
         LTR   R9,RF is rexx env active
         BZ    MAINEXIT yes leave
         LA    R0,L'NODATAM
         LA    R1,NODATAM
         TPUT  (1),(0)
         B     MAINEXIT
UIDERROR LA    R9,K255 /* UID NOT PRESENT */
MAINEXIT L     R2,K4(,RD)
         LR    R1,RD
         LA    R0,DWORDS
         FREEMAIN R,A=(1),LV=(0)
         LR    RD,R2
         LR    RF,R9
         RETURN (14,12),RC=(15)
MOVEUID  MVC   USERID+K1(*-*),K0(R5) ACQUIRE USERID
NOUSERM  DC    C'Invalid TSO/E userid entered.'
NODATAM  DC    C'RACF database programmer name field missing.'
         LTORG
FNAME    ICHEACTN FIELD=PGMRNAME,RELEASE=1.8.1
LLLFNAME EQU   *-FNAME
FINT     ICHEACTN FIELD=PASSINT,RELEASE=1.8.1
LLLFINT  EQU   *-FINT
FAUDT    ICHEACTN FIELD=FLAG6,RELEASE=1.8.1
LLLFAUDT EQU   *-FAUDT
FSPEC    ICHEACTN FIELD=FLAG2,RELEASE=1.8.1
LLLFSPEC EQU   *-FSPEC
FOPER    ICHEACTN FIELD=FLAG3,RELEASE=1.8.1
LLLFOPER EQU   *-FOPER
FREVK    ICHEACTN FIELD=FLAG4,RELEASE=1.8.1
LLLFREVK EQU   *-FREVK
FGACC    ICHEACTN FIELD=FLAG5,RELEASE=1.8.1
LLLFGACC EQU   *-FGACC
LSTEINTY ICHEINTY LOCATE,TYPE='USR',ENTRY=*-*,RELEASE=1.8.1,           X
               WKAREA=*-*,ACTIONS=(FNAME,FINT,FAUDT,FSPEC,FOPER,FREVK,FX
               GACC),MF=L
LLLEINTY EQU   *-LSTEINTY
IKJPARM  IKJPARM
USERPARM IKJPOSIT USERID,UPPERCASE,PROMPT='Required TSO/E userid ?'
         IKJENDP
COUTLINE EQU   * *
         DC    C'Userid: '
         DC    CL20' ' userid
         DC    C' Password Interval: '
         DC    CL2'NA' password interval
         DC    C' Attributes: '
         DC    C'None                                     '
CULEN    EQU   *-COUTLINE
REXXSERV EQU   * *
ECODE    DC    A(TSVEUPDT) Set value
NAMEPTR  DC    A(VARNAME)
NAMELEN  DC    F'5'
VALUELEN DC    A(*-*) Keep together
VALUEPTR DC    A(*-*)
TOKEN    DC    F'0'
VARNAME  DC    C'WHOIS' Rexx var name
RACFWMAP DSECT
SAVEAREA DC    18F'0'
WORK     DC    D'0'
CMDPECB  DC    A(K0)
CMDPANS  DC    A(K0)
PARSEPPL DC    (K28)AL1(K0) PARSE PPL
LXTEINTY DC    (LLLEINTY)AL1(K0)
RACFWORK DC    A(K96),(K92)AL1(K0)
LXTFNAME DC    (LLLFNAME)AL1(K0)
LXTFINT  DC    (LLLFINT)AL1(K0)
LXTFAUDT DC    (LLLFAUDT)AL1(K0)
LXTFSPEC DC    (LLLFSPEC)AL1(K0)
LXTFOPER DC    (LLLFOPER)AL1(K0)
LXTFREVK DC    (LLLFREVK)AL1(K0)
LXTFGACC DC    (LLLFGACC)AL1(K0)
USERID   DC    AL1(*-*),CL8' '
DOUTLINE EQU   * *
         DC    C'Userid: '
DUID     DC    CL20' ' userid
         DC    C' Password Interval: '
DPSWDINT DC    CL2' ' password interval
         DC    C' Attributes: '
DATTRIB  DC    C'None                                     '
DULEN    EQU   *-DOUTLINE
DWORDS   EQU   ((((*-SAVEAREA)+7)/8)*8)
         PRINT NOGEN
         IKJPPL
         IKJCPPL
         IKJTSVT
         CVT   DSECT=YES
         END
/*
//L.SYSLMOD DD DSN=SYS1.TECH.COMMON.LOADLIB(WHOIS),DISP=SHR,
// UNIT=,SPACE=
