//TECH110L JOB (5304010530000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=TECH110
//ASM EXEC ASMHCL,PARM.L='AC=1,LIST,XREF'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN DD *
MSGQ     TITLE 'MESSAGE QUEUEING SUBSYSTEM'
MSGQ     CSECT ,
         SYSSTATE ASCENV=P PRIMARY ASC MODE
MSGQ     AMODE 31
MSGQ     RMODE 24
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
AR0      EQU   0
AR1      EQU   1
AR2      EQU   2
AR3      EQU   3
AR4      EQU   4
AR5      EQU   5
AR6      EQU   6
AR7      EQU   7
AR8      EQU   8
AR9      EQU   9
ARA      EQU   10
ARB      EQU   11
ARC      EQU   12
ARD      EQU   13
ARE      EQU   14
ARF      EQU   15
         BAKR  RE,0 SAVE CALLERS STATUS ON LINKAGE STACK RETRG IS R14
         LAE   RA,0(,RF) SETUP ADDRESSING REGS GR/AR PRMY SPACE MODE
         USING MSGQ,RA,RB
         LAE   RB,4095(,RA) ...
         LAE   RB,1(,RB) ...
         SAC   0   SWITCH TO PRIMARY MODE
         LA    RD,SAVEAREA PTR TO MY SAVEAREA
         MVC   SAVEAREA+4(4),=C'F1SA' INDICATE REGS SAVED ON STACK
         USING PSA,R0
         L     R2,FLCCVT MAP CVT
         DROP  R0
         USING CVT,R2
         L     R2,CVTJESCT MAP JES SSCVT
         DROP  R2
         USING JESCT,R2
         L     R2,JESSSCT MAP SSCT CHAIN
         DROP  R2
         USING SSCT,R2
SCANSSCT CLC   =C'MSGQ',SSCTSNAM OUR SUBSYSTEM ?
         BE    MSGQSSYS FOUND
         ICM   R2,15,SSCTSCTA NEXT SSCVT
         BNZ   SCANSSCT LOOP THRU ALL SSCVTS
         B     NOSSCVT  NOT FOUND AT ALL
MSGQSSYS EQU   * * R2 PTR TO MSGQ SSCVT
         LA    R3,VTSPARML FORMAT SSVT CREATE PARM LIST
         USING VTSPL,R3
         XC    VTSPL(VTSSIZE),VTSPL CLEAR
         MVC   VTSID,=C'VTSP' ID
         MVC   VTSLEN,=AL2(VTSSIZE) CB SIZE
         MVI   VTSVER,VTSCVER VERSION CONTROL
         OI    VTSFLGS,VTSGLOAD LOAD SSSM TO GLOBAL
         OI    VTSREQ,VTSCREAT SSVT CREATE FUNCTION
         MVC   VTSNME,SSCTSNAM SUBSYS NAME
         MVC   VTSSVTD,=A(SSVTDATA) PTR SSVT DATA
         ST    R2,VTSSSCVT SSCVT PTR
         LA    R4,SSVTDATA FORMAT SSVT DATA PASSED TO CREATE SERV
         USING JSBVT,R4
         XC    JSBHDR(JSBVTLT),JSBHDR CLEAR
         MVC   JSBID,=C'JSBV' ID
         MVC   JSBLEN,=AL2(JSBVTLT) CB SIZE
         MVI   JSBVERS,JSBCVERS VERSION CONTROL
         MVC   JSBFUN,=AL2(1) SSSM FUNCTION ROUTINE COUNT
         MVI   JSBSPL,241 SUBPOOL FOR SSVT CREATE
         MVC   JSBMAXFR,JSBFUN SSSM FUNCTION ROUTINE COUNT MAX
         LA    R4,JSBVTLT(,R4) SKIP PAST FIXED HEADER
         DROP  R4
         USING JSBTBL,R4
         MVC   JSBLGTH,=AL2(JSBTBLGT+5*(JSBCDLGT)) LEN FUNC DATA/CODE
         MVC   JSBNME,=CL8'MSGQSSSM' SUBSYSTEM ROUTINES
         MVC   JSBNUM,=AL2(5) FUNCTION CODE COUNT
         LA    R4,JSBTBLGT(,R4) SKIP PAST FUNC ROUTINE DATA AREA
         DROP  R4
         USING JSBFCODG,R4
         MVC   JSBFCOD(10),=AL2(SSOBCONV,SSOBAGRP,SSOBOPEN,SSOBCLOS,SSOX
               BUNAL) C/I,GROUP ALLOC,OPEN,CLOSE,UNALLOC
         DROP  R4
         MODESET MODE=SUP,KEY=ZERO ENTER PROPER STATE
         XC    SSCTSSVT,SSCTSSVT COLD START SUBSYSTEM
         LA    R1,VTSPTR CALL SSVT CREATE
         LINK  EP=IEFJSVEC
         LTR   RF,RF OKAY ?
         BNZ   SSCVTF SSCVT CREATE FAILURE
         MVC   SSCTSSVT,VTSSVTAD ENABLE SUBSYS; CHAIN SSVT TO SSCVT
         WTO   'MSGQ SUBSYSTEM IS ENABLED'
EXITNORM SAC   0 SWITCH TO PRIMARY MODE
         SLR   RF,RF RC=0
         PR    , RESTORE CALLERS STATUS
NOSSCVT  SAC   0 SWITCH TO PRIMARY MODE
         LA    RF,24 TERM WITH SEVERE RC
         PR    , RESTORE CALLERS STATUS
SSCVTF   SAC   0 SWITCH TO PRIMARY MODE
         LA    RF,28 TERM WITH SEVERE RC
         PR    , RESTORE CALLERS STATUS
SAVEAREA DC    9D'0' LINKAGE
VTSPTR   DC    A(VTSPARML) PTR TO SSVT SERV ROUTINE PARM LIST
VTSPARML DC    (VTSSIZE)X'00' PARM LIST
SSVTDATA DC    0F'0',(JSBVTLT+JSBTBLGT+5*(JSBCDLGT))X'00'
         LTORG
         IHAPSA
         CVT   DSECT=YES
JESCT    IEFJESCT
         IEFJSCVT
         IEFVTSPL
         IEFJSBVT
         IEFJSSOB (CI,AL,DA,AG),CONTIG=NO
         IKJTCB
         IHARB
         END
/*
//L.SYSLMOD DD DSN=TECH110.MAINT.LOADLIB(MSGQ),DISP=SHR,UNIT=
//
