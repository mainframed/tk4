//JEFFKZ   JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=JEFFK,REGION=6M
//S EXEC ASMHCL
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
// DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN  DD *
DSABINUS CSECT
* SCAN TIOT AND SET ALL DSAB INUSE BITS ON CAUSING ALL DYNALLOC
* REQUESTS TO BE TREATED AS IF REGULAR JCL IN TERMS OF TSO/E
* FREE ALL COMMAND.
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
DSABINUS AMODE 24
DSABINUS RMODE 24
         SAVE  (14,12),,DSABINUS*&SYSDATE*&SYSTIME
         LR    RA,RF ADDRESSABILITY
         USING DSABINUS,RA,RB,RC
         LA    RB,4095(RA)
         LA    RB,1(RB)
         LA    RC,4095(RB)
         LA    RC,1(RC)
         LR    RF,RD SAVEAREA HOUSEKEEPING
         CNOP  0,4
         BAL   RD,*+76
         DC    18F'0'
         ST    RD,8(RF)
         ST    RF,4(RD)
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         USING PSA,R0
         L     R2,PSATOLD
         DROP  R0
         USING TCB,R2
         L     R2,TCBTIO
         DROP  R2
         USING TIOT1,R2
         LA    R3,TIOENTRY
         DROP  R2
         USING TIOENTRY,R3
         SLR   R4,R4 WORK
LOOPTIOT IC    R4,TIOELNGH
         LTR   R4,R4 TIOT END
         BZ    ENDTIOT
         CLI   TIOEDDNM,C' '
         BE    SCANTIOT
         CLI   TIOEDDNM,X'00'
         BE    SCANTIOT
         CLC   TIOEDDNM(4),=C'STEP'
         BE    SCANTIOT
         CLC   TIOEDDNM(3),=C'JOB'
         BE    SCANTIOT
         MVC   DRETDDNM+6(8),=CL8' '
         LA    RF,8
         LA    R1,TIOEDDNM+7
LOOPSIZE CLI   0(R1),C' '
         BNE   SIZEDDNM
         BCTR  RF,0
         BCT   R1,LOOPSIZE
SIZEDDNM STC   RF,DRETDDNM+5
         MVC   DRETDDNM+6(8),TIOEDDNM
         LA    R1,DSVC99P
         SVC   99
         TM    DRESP,X'40' INUSE ALREADY
         BO    SCANTIOT
         TPUT  DRETDDNM+6,8
         LA    R1,ASVC99P
         SVC   99
SCANTIOT LA    R3,0(R4,R3) NEXT TIOT ENTRY
         B     LOOPTIOT
ENDTIOT  L     RD,4(RD)
         RETURN (14,12),RC=0
ASVC99P  DC    0F'0',X'80',AL3(ARB)
ARB      DC    X'1406000000000000'
         DC    A(ATEXTP),8X'00'
ATEXTP   DC    X'80',AL3(DRETDDNM)
DSVC99P  DC    0F'0',X'80',AL3(DRB)
DRB      DC    X'1407000000000000'
         DC    A(DTEXTP),8X'00'
DTEXTP   DC    X'00',AL3(DRETATTR)
         DC    X'80',AL3(DRETDDNM)
DRETDDNM DC    X'000100010008',C'????????'
DRETATTR DC    X'000C00010001'
DRESP    DC    X'00' REQUEST RESP HERE
         LTORG
         PRINT NOGEN
         IHAPSA
         IKJTCB
         DSECT
         IEFTIOT1
         END
/*
//L.SYSLMOD DD DSN=IPO1.LINKLIB(DSABINUS),DISP=SHR,
// UNIT=,SPACE=
