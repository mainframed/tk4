/* CREATE JCL FOR BUILDING NEW USERIDS REXX EXEC */
ADDRESS TSO;
"ALLOC F(INPUT) DA('SRTY001.USER.CNTLLIB(REXINPUT)') SHR";
"EXECIO * DISKR INPUT (FINIS STEM INPUT.";
"FREE F(INPUT)";
"NEWSTACK";
QUEUE "//ADDUSERS JOB 5304010530000000,'T FINNER',NOTIFY=SRTY001,";
QUEUE "// CLASS=S,MSGCLASS=G,REGION=6M";
QUEUE "//UADS EXEC PGM=IKJEFT01";
QUEUE "//SYSTSPRT DD SYSOUT=*";
QUEUE "//SYSUADS DD DSN=SYS1.UADS,DISP=SHR";
QUEUE "//SYSLBC DD DSN=SYS1.BRODCAST,DISP=SHR";
QUEUE "//SYSTSIN DD *";
QUEUE "ACCOUNT";
DO I = 1 TO INPUT.0;
   PARSE VAR INPUT.I UID . ;
   QUEUE "ADD ("UID "PASSWORD ACCOUNT @IKJXPDF) SIZE(8192) +";
   QUEUE " UNIT(SYSDA) NOACCT JCL MAXSIZE(8192) NOMOUNT NOOPER";
END;
QUEUE "SYNC";
QUEUE "END";
DO I = 1 TO INPUT.0;
   PARSE VAR INPUT.I UID UNAME;
   UNAME = SPACE(UNAME);
   QUEUE "ADDUSER" UID "NAME('"UNAME"') PASSWORD("UID")";
   QUEUE "OWNER(SUPTEST) DFLTGRP(SUPTEST) GRPACC";
   QUEUE "ADDSD '"UID".*' GENERIC OWNER(SUPTEST) UACC(READ)";
END;
QUEUE "/*";
QUEUE "//DEFALIAS EXEC PGM=IDCAMS,COND=(0,LT)";
QUEUE "//SYSPRINT DD SYSOUT=*";
QUEUE "//SYSIN DD *";
DO I = 1 TO INPUT.0;
   PARSE VAR INPUT.I UID . ;
   QUEUE " DELETE" UID "ALIAS";
END;
QUEUE " SET MAXCC=0";
DO I = 1 TO INPUT.0;
   PARSE VAR INPUT.I UID . ;
   QUEUE " DEFINE ALIAS -";
   QUEUE " (NAME("UID") -";
   QUEUE " RELATE(SYS1.UCATTSOE.XA.CLUSTER)) -";
   QUEUE " CATALOG(CATALOG.MVSICFM.VMVSCAT)";
END;
QUEUE "/*";
QUEUE "//ALLOCPDS EXEC PGM=IEFBR14,COND=(0,LT)";
DO I = 1 TO INPUT.0;
   PARSE VAR INPUT.I UID . ;
   QUEUE "//DUMMY DD DSN="UID".USER.CNTLLIB,DISP=(NEW,CATLG),";
   QUEUE "// SPACE=(CYL,(5,,180)),";
   QUEUE "// UNIT=SYSDA,DCB=(RECFM=FB,LRECL=80,BLKSIZE=27920)";
END;
QUEUE "//";
QUEUE "";
"ALLOC F(OUTPUT) DA('SRTY001.USER.CNTLLIB(UTSOADD)') SHR";
"EXECIO * DISKW OUTPUT (FINIS";
"FREE F(OUTPUT)";
EXIT 0;
