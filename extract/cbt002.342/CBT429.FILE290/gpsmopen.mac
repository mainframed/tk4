GPSM     TITLE 'GPSMOPEN - PROBLEM STATE SUBSYSTEM OPEN RTN'
***************************************************************
* NAME = GPSMOPEN                                             *
* DESCRIPTIVE NAME = PROBLEM STATE OPEN EXECUTOR              *
* (C) COPYRIGHT 1982 YALE COMPUTER CENTER                     *
* STATUS = VERSION 1 MODIFICATION LEVEL 0                     *
* SYSTEM = MVS 3.8                                            *
* ATTRIBUTES = NONE                                           *
* LINKAGE = FROM GPSAM OPEN ROUTINE VIA SYNCH MACRO           *
* NORMAL RETURN =                                             *
*      GPSMAMEP SET TO GET/PUT ENTRY POINT OF ROUTINE         *
* ERROR RETURN =                                              *
*      ABEND U306 IF AUTHORIZATION FAILURE                    *
* ASSEMBLE: NEEDS HAL MACRO, AMODGEN, AND MAYBE PVTMACS       *
* LINKEDIT: MUST GO INTO AN APF LIBRARY. SHOULD NOT BE AC=1   *
*                                                             *
* AUTHOR = HOWARD GILBERT                                     *
*          YALE COMPUTER CENTER                               *
*          175 WHITNEY AVE.                                   *
*          P.O. BOX 2112 YALE STATION                         *
*          NEW HAVEN, CT 06520                                *
*          BITNET ADDRESS: GILBERT @ YALEVM                   *
*                                                             *
* THIS PROGRAM SEARCHES THE PARAMETERS PASSED TO THE          *
* SUBSYSTEM AS PART OF THE USER ALLOCATION:                   *
*                                                             *
*    //DDNAME DD SUBSYS=(GPSM,MODNAME,'...')                  *
*                                                             *
* THE FIRST PARAMETER MUST BE A MODULE NAME. THE NAMED        *
* MODULE IS LOAD FROM THE SYSTEM OR USER LIBRARY TO DO        *
* THE WORK. THE SECOND PARAMETER IS PASSED TO THE MODULE      *
* AS PART OF THE R1 LINKAGE.                                  *
*                                                             *
* NOTE: SINCE THE PARM FIELDS OF THE SUBSYS KEYWORD ARE       *
* NOT PART OF THE FORMAL PARAMETER INTERFACE, THIS MODULE     *
* MUST HUNT THEM UP. IT IS THEREFORE DEPENDENT ON THE         *
* FACT THAT THE JFCB WHICH IS PASSED AS A FORMAL PARAMETER    *
* THROUGH THE INTERFACE IS IN FACT IMBEDDED IN THE O/C/E      *
* WORK AREA MAPPED BY THE IECDSECT MACRO. THIS IS LIKELY      *
* TO REMAIN TRUE FOREVER, BUT IT IS NOT A DOCUMENTED ELEMENT  *
* OF THE SUBSYSTEM INTERFACE LINKAGE.                         *
***************************************************************
         PRINT OFF
         COPY  HAL
         PRINT ON
GPSMOPEN CSECT
         USING *,R15
         SAVE  (14,12),,*
         LR    R10,R15
         USING GPSMOPEN,R10
         DROP  R15
         USING GPSMPARM,R13
         ST    R13,SAVE2+4        ESTABLISH BACKCHAIN
         LA    R13,SAVE2
         USING SAVE2,R13
         L     R2,GPSMSSOB        GET SSOB POINTER
         USING SSOB,R2            NOW CHAIN DOWN TO ACB
DEBUG1   IF    (FLAGWORD,ON,DEBUG),THEN
*        ******************************************************
*        * IF ZAPPED ON, INVOKE THE YALE DEBUGGER             *
*        ******************************************************
         LOAD  EP=DBC
         LR    R3,R0
         XC    ESTAEL(24),ESTAEL
         ESTAE (R3),MF=(E,ESTAEL)
         #DIE
         BEND  DEBUG1
         TITLE 'GPSMOPEN - FIND DATASET CONTROL BLOCKS'
*        ******************************************************
*        * LOAD UP A BUNCH OF USEFUL REGISTERS                *
*        ******************************************************
         L     R3,SSOBINDV
         USING SSOBGN,R3
         L     R4,SSDADEBP
         USING DEBBASIC,R4
         L     R5,DEBDCBAD
         USING IFGACB,R5
         L     R3,SSDAJFCB
*        ******************************************************
*        * THE JFCB WHICH I AM PASSED CONTAINS NO USEFUL      *
*        * INFORMATION. HOWEVER, ELSEWHERE IN THE O/C/E       *
*        * WORKAREA THERE IS A POINTER TO THE DSAB AND        *
*        * FROM THERE I CAN FIND GOOD STUFF                   *
*        ******************************************************
         USING DXJBF,R3
         L     R6,DXDSAB
         ICM   R6,B'0111',DSABSSVA-DSAB(R6)
         USING INDMSIOT-16,R6
         MVC   GPSMDDN,SCTDDNAM   PICK UP DDNAME
         ICM   R6,B'0111',SIOTSSWA
         USING SSWA-16,R6         SSWA CONTAINS SUBSYS PARMS
GETPARMS BLOCK ,
         LH    R15,SSWAPRNO       GET NUMBER OF PARMS
         LEAVE GETPARMS,IF,(R15,LT,1) GOTTA HAVE MODULE NAME
         SR    R1,R1
         IC    R1,SSWAPLEN        LENGTH OF FIRST PARM
         LEAVE GETPARMS,IF,(R1,GT,8),OR,(R1,EQ,0)
         BCTR  R1,0               LESS ONE FOR MOVE
         MVI   MODNAME,C' '
         MVC   MODNAME+1(7),MODNAME
         EX    R1,MOVEMODN
DOPARM   IF    (R15,GT,1),THEN    IF MODULE GETS PARM STRING
         LA    R14,SSWAPVAL+1(R1) POINT PAST MODULE NAME
         ST    R14,GPSMMPRM       SAVE PARM POINTER
         BEND  DOPARM
         LEAVE GETPARMS
MOVEMODN MVC   MODNAME(*-*),SSWAPVAL  EXECUTED INSTRUCTION
         DROP  R6
         BEND  GETPARMS
         TITLE 'GPSMOPEN - PROCESS OPEN REQUEST'
OPENCLOS IF    (SSOBFUNC+1,EQ,SSOBOPEN,CLI),THEN
*        ******************************************************
*        * FUNCTION IS OPEN.                                  *
*        ******************************************************
AUTHCHEK BLOCK ,
*        ******************************************************
*        * WHEN WE DO THE LOAD WE WILL BE IN PROBLEM STATE    *
*        * AND PROBLEM PROTECT KEY. THE LOAD WILL THEREFORE   *
*        * BE PERMITTED TO FETCH MODULES FROM AN UNAUTHORIZED *
*        * USER STEPLIB AS LONG AS THE JOBSTEP IS NOT APF     *
*        * AUTHORIZED. TO AVOID MVS INTEGRITY PROBLEMS, WE    *
*        * ADOPT A SIMPLE RULE:                               *
*        *                                                    *
*        *  1) IF WE ARE APF AUTHORIZED, THE MODULE MUST      *
*        *     COME FROM AN APF LIBRARY (LOAD MACRO CHECKS    *
*        *     THIS AUTOMATICALLY) AND MUST BE IN A TABLE     *
*        *     PROVIDED BELOW.                                *
*        *                                                    *
*        *  2) IF WE ARE NOT AUTHORIZED, THE MODULE CAN COME  *
*        *     FROM ANY STEPLIB.                              *
*        *                                                    *
*        *  3) THE USE OF GPSM IS PROHIBITED IF THE PROGRAM   *
*        *     OPENING THE DATASET IS AUTHORIZED (IN          *
*        *     SUPERVISOR STATE OR SYSTEM KEY) WHILE THE      *
*        *     JOB STEP IS UNAUTHORIZED. THIS PREVENTS        *
*        *     EXPOSURES IF AN UNAUTHORIZED GPSM MODULE WERE  *
*        *     PASSED TO SYSUDUMP WHICH IS OPENED BY AN SVC.  *
*        ******************************************************
         L     R6,PSATOLD-PSA     -> TCB
         USING TCB,R6
         L     R7,TCBJSCB         APF AUTHORIZATION IS A BIT
         USING IEZJSCB,R7         IN THE JSCB
APF      IF    (JSCBOPTS,ON,JSCBAUTH),THEN  APF AUTHORIZED
         LA    R7,APFNAMES
         DO    WHILE,(0(R7),NE,0,CLI)
         LEAVE AUTHCHEK,IF,(MODNAME,EQ,0(R7),CLC)
         LA    R7,8(R7)           NEXT NAME
         BEND  ,
AUTHFAIL WTO   'GPSM001 - OPEN FAILED, AUTHORIZATION RESTRICTION'
         ABEND 306
         BEND  APF
         L     R7,TCBRBP          -> CURRENT RB (PRB FROM SYNCH)
         USING RBBASIC,R7
         L     R7,RBLINK          -> SVRB FROM OPEN TYPE=J
         L     R7,RBLINK          -> SVRB FROM OPEN
         L     R7,RBLINK          -> OPENER'S RB
         EXIT  AUTHFAIL,IF,       QUIT IF AUTHORIZED CALLER BUT        X
               (RBOPSWB2,NON,X'81') JOBSTEP IS NOT APF
         DROP  R7
         BEND  AUTHCHEK
         LOAD  EPLOC=MODNAME      LET IT 806 ABEND IF NAME BAD
         ST    R0,GPSMSAVA
         LA    R1,GPSMMPRM
         LR    R15,R0
         BALR  R14,R15
         TITLE 'GPSMOPEN - PROCESS THE CLOSE REQUEST'
         ELSE  ,
*        ******************************************************
*        * THE FUNCTION IS CLOSE                              *
*        ******************************************************
         L     R15,GPSMSAVA
         LA    R1,GPSMMPRM
         BALR  R14,R15
         DELETE EPLOC=MODNAME
         BEND  OPENCLOS
         TITLE 'GPSMOPEN - RETURN TO CALLER'
DEBUG2   IF    (FLAGWORD,ON,DEBUG),THEN
*        ******************************************************
*        * CANCEL THE YALE DEBUGGER                           *
*        ******************************************************
         ESTAE 0
         DELETE EP=DBC
         BEND  DEBUG2
RET      L     R13,4(R13)         BACK TO SAVE1
         RETURN (14,12),RC=0
         TITLE 'GPSMOPEN - CONSTANTS, VARIABLE, AND DSECTS'
FLAGWORD DC    AL1(0)
DEBUG    EQU   X'80'
         DC    3AL1(0)
APFNAMES DC    5XL8'00'           AUTHORIZED AM MODULES
         LTORG
         SPACE 5
GPSMPARM DSECT
SAVE1    DS    18A                R13 SAVE AREA
SAVE2    DS    18A                ANOTHER ONE FOR MY USE
MODNAME  DS    CL8                MODULE NAME BUILD AREA
GPSMMPRM DS    A                  POINTER TO MODULE PARM
GPSMSSOB DS    A                  POINT TO OPEN SSOB
GPSMAMEP DS    A                  RETURN ACB AM ENTRY POINT
GPSMSAVA DS    A                  WORD TO SAVE FOR CLOSE
GPSMDDN  DS    CL8                DDNAME
ESTAEL   DS    16A                ESTAE WORK AREA
         PRINT NOGEN
         EQUREGS
         IEFJSSWA
         EJECT
         IEFJSSOB DA
         EJECT
         IFGACB
         EJECT
         IEZDEB LIST=YES
         EJECT
         IKJTCB
         EJECT
         IHARB
         EJECT
         IEZJSCB
         EJECT
         IHAPSA
         EJECT
         IHADSAB
         EJECT
SIOT     DSECT
         IEFASIOT
         EJECT
FORCORE  DSECT
         IECDSECT
         END
