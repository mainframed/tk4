DDSS     TITLE 'DDSUBSYS - TSO COMMAND TO ALLOCATE SUBSYS FILE'
***************************************************************
* NAME = DDSUBSYS                                             *
* DESCRIPTIVE NAME = TSO COMMAND TO ALLOCATE SUBSYS FILE      *
* (C) COPYRIGHT 1982 YALE COMPUTER CENTER                     *
* STATUS = VERSION 1 MODIFICATION LEVEL 0                     *
* SYSTEM = MVS 3.8                                            *
* ATTRIBUTES = NONE                                           *
* DEPENDENCIES = NONE                                         *
*                                                             *
* AUTHOR = HOWARD GILBERT                                     *
*          YALE COMPUTER CENTER                               *
*          175 WHITNEY AVE.                                   *
*          P.O. BOX 2112 YALE STATION                         *
*          NEW HAVEN, CT 06520                                *
*          BITNET ADDRESS: GILBERT @ YALEVM                   *
***************************************************************
*        COPY  HAL
         PRINT OFF
         COPY  HAL
         PRINT ON
DDSUBSYS CSECT
         SAVE  (14,12),,*
         BALR  R12,0              STANDARD STARTUP STUFF
         USING *,R12
         ST    R1,DFCPPLP         SAVE FOR LATER
         ST    R13,STORAGE+4
         LA    R13,STORAGE
         USING CPPL,R1
         MVC   PPLUPT,CPPLUPT     INITIALIZE PARSE PARAMETERS
         MVC   PPLECT,CPPLECT
         MVC   PPLCBUF,CPPLCBUF
         DROP  R1
         XC    ECB,ECB
         XC    PPLUWA,PPLUWA
         LA    R1,PPL             POINT TO PARM BLOCK
PARSE    CALLTSSR EP=IKJPARS      PARSE COMMAND
         L     R9,ANS             PICK UP RESPONSE
         USING IKJPARMD,R9
PARSED   LA    R4,PDDN            FIND DDNAME
         LA    R5,DDNAME+TEXTLEN
DODDN    BAL   R14,MOVEDATA       MOVE DDNAME FROM PARSE TO TEXT
         LA    R4,PMODNAM         FIND MODULE NAME IN PARSE
         LA    R5,MODULE+TEXTLEN
DOMODN   BAL   R14,MOVEDATA       MOVE MODULE TO TEXT
         LH    R0,PPARM+4
         IF    (R0,NE,0),THEN
         LA    R4,PPARM           ADVANCE TO PARAMETER
DOPARM   BAL   R14,MOVEDATA       MOVE PARAMETER TO TEXT
         ELSE  ,
         MVC   MODULE+COUNT,ONE   ONLY MODULE, NO PARAMETER
         BEND  ,
         LH    R1,PRECFM
DORECFM  IF    (R1,EQ,1),THEN
         MVI   RECFM+TEXTDATA,RECFMF
         ELSEIF (R1,EQ,2),THEN
         MVI   RECFM+TEXTDATA,RECFMV
         ELSEIF (R1,EQ,3),THEN
         MVI   RECFM+TEXTDATA,RECFMU
         ELSE  ,
         MVC   RECFM(2),NOTEXT
         BEND  DORECFM
DOLRECL  L     R15,PLRECL
         IF    (R15,NE,0),THEN
         L     R15,0(R15)
         STH   R15,LRECL+TEXTDATA
         IF    (R1,EQ,2),THEN
         LA    R15,4(R15)
         BEND  ,
         STH   R15,BLKSIZE+TEXTDATA
         ELSE  ,
         MVC   LRECL(2),NOTEXT
         MVC   BLKSIZE(2),NOTEXT
         BEND  ,
ALLOCATE LA    R1,PARMLIST
         SVC   99                 ALLOCATE SUBSYS DATASET
         IF    (R15,NE,0),THEN
         IF    (ERRCODE,EQ,NOGEN,CLC),THEN
         TPUT  GENMSG,LGENMSG
         ELSEIF (ERRCODE,EQ,NORUN,CLC),THEN
         TPUT  RUNMSG,LRUNMSG
         ELSE  ,
         ST    R15,S99RETC
         LA    R1,DFDAPLP
         LINK  EP=IKJEFF18
         BEND  ,
         BEND  ,
RETURN   L     R13,4(R13)         CHAIN BACK TO OLD SAVE AREA
         RETURN (14,12),RC=(15)
MOVEDATA L     R2,0(R4)           GET USER RESPONSE DATA
         LH    R3,4(R4)           GET LENGTH
         STH   R3,0(R5)           SAVE LENGTH IN TEXT
         BCTR  R3,0               LESS 1 FOR MOVE
         EX    R3,MOVER           MOVE DATA
         AR    R5,R3              POINT AFTER END OF DATA
         LA    R5,3(R5)
         BR    R14
MOVER    MVC   2(*-*,R5),0(R2)
PARSPARM IKJPARM
PDDN     IKJIDENT 'DDNAME',OTHER=ALPHANUM,MAXLNTH=8,PROMPT='DDNAME'
PMODNAM  IKJIDENT 'MODULE',OTHER=ALPHANUM,MAXLNTH=8,PROMPT='MODULE'
PPARM    IKJPOSIT QSTRING,DEFAULT=''''''
FORMAT   IKJKEYWD
         IKJNAME 'RECFM',SUBFLD=RECFMS
BLOCK    IKJKEYWD
         IKJNAME 'LRECL',SUBFLD=LRECLS
RECFMS   IKJSUBF
PRECFM   IKJKEYWD
         IKJNAME 'F'
         IKJNAME 'V'
         IKJNAME 'U'
LRECLS   IKJSUBF
PLRECL   IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,INTEG
         IKJENDP
RUNMSG   DC    C'THE GPSM SUBSYSTEM HAS NOT BEEN STARTED'
LRUNMSG  EQU   *-RUNMSG
GENMSG   DC    C'THE GPSM SUBSYSTEM IS NOT DEFINED'
LGENMSG  EQU   *-GENMSG
         DS    0F
PARMLIST DC    AL1(X'80'),AL3(VERBBLK)
VERBBLK  DC    X'14010000'
ERRCODE  DC    H'0'
INFOCODE DC    H'0'
         DC    A(TEXTPTR)
         DC    2A(0)
TEXTPTR  DC    A(DDNAME)
         DC    A(SUBSYS)
         DC    A(MODULE)
         DC    A(PERM)
         DC    A(RECFM)
         DC    A(LRECL)
         DC    A(BLKSIZE)
         DC    A(BUFNO)
         DC    X'80000000'
DDNAME   DC    X'0001',H'1',H'6',CL8'SYSUT2'
SUBSYS   DC    X'005F',H'1',H'4',CL4'GPSM'
MODULE   DC    X'0060',H'2',H'8',CL8'TESTINGS'
         DC    H'128',CL128' '
PERM     DC    X'0052',H'0'
RECFM    DC    X'0049',H'1',H'1',H'0'
LRECL    DC    X'0042',H'1',H'2',H'0'
BLKSIZE  DC    X'0030',H'1',H'2',H'0'
BUFNO    DC    X'0034',H'1',H'1',X'0100'
COUNT    EQU   2
TEXTLEN  EQU   4
TEXTDATA EQU   6
RECFMF   EQU   X'80'
RECFMV   EQU   X'40'
RECFMU   EQU   X'C0'
NOTEXT   DC    H'0'               FLAG TO SKIP THIS TEXT ITEM
ONE      DC    H'1'
DFDAPLP  DC    A(VERBBLK)
DFRCP    DC    A(S99RETC)
DFJEFF02 DC    A(ZERO)
DFIDP    DC    A(DFFLAGS)
DFCPPLP  DC    A(*-*)
DFBUFP   DC    A(0)
S99RETC  DC    A(*-*)
ZERO     DC    A(0)
DFFLAGS  DC    X'0032'
NOGEN    DC    X'04B0'
NORUN    DC    X'04AC'
STORAGE  DS    18A
DSASECT  EQU   *
PPL      DS    0A
PPLUPT   DS    A              A(USER PROFILE TABLE)
PPLECT   DS    A              A(ENVIRONMENT CONTROL TABLE)
PPLECB   DC    A(ECB)         A(EVENT CONTROL BLOCK)
PPLPCL   DC    V(PARSPARM)    A(PARAMETER CONTROL LIST)
PPLANS   DC    A(ANS)         A(PARAMETER DESCRIPTOR LIST)
PPLCBUF  DS    A              A(COMMAND BUFFER)
PPLUWA   DS    A              A(WORKAREA)
ARGL     DS    A
ECB      DS    A
ANS      DS    A
         LTORG
         EQUREGS
         IKJCPPL
         CVT   DSECT=YES
         END
