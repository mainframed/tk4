LOWC     TITLE 'LOWCASAM - PROBLEM STATE SUBSYSTEM OPEN RTN'
***************************************************************
* NAME = LOWCASAM                                             *
* DESCRIPTIVE NAME = LOWER CASE TERMINAL INPUT  ACCESS METHOD *
* (C) COPYRIGHT 1982 YALE COMPUTER CENTER                     *
* STATUS = VERSION 1 MODIFICATION LEVEL 0                     *
* SYSTEM = MVS 3.8                                            *
* ATTRIBUTES = NONE                                           *
* LINKAGE = FROM GPSMOPEN AT OPEN AND CLOSE                   *
* NORMAL RETURN =                                             *
*      GPSMAMEP SET TO GET/PUT ENTRY POINT OF ROUTINE         *
*                                                             *
* AUTHOR = HOWARD GILBERT                                     *
*          YALE COMPUTER CENTER                               *
*          175 WHITNEY AVE.                                   *
*          P.O. BOX 2112 YALE STATION                         *
*          NEW HAVEN, CT 06520                                *
*          BITNET ADDRESS: GILBERT @ YALEVM                   *
***************************************************************
         PRINT OFF
         COPY  HAL
         PRINT ON
LOWCASAM CSECT
         USING *,R15
         SAVE  (14,12),,*
         LR    R10,R15
         USING LOWCASAM,R10
         DROP  R15
         LR    R2,R1
         USING AMPARM,R2
         L     R6,GPSMSSOB        GET SSOB POINTER
         USING SSOB,R6            NOW CHAIN DOWN TO ACB
DEBUG1   IF    (FLAGWORD,ON,DEBUG),THEN
*        ******************************************************
*        * IF ZAPPED ON, INVOKE THE YALE DEBUGGER             *
*        * NOTE: THIS IS HERE JUST TO SHOW HOW IT'S DONE.     *
*        * THERE IS NO REAL WORK DONE AT OPEN TIME IN THIS    *
*        * ROUTINE, BUT IF YOU USE IT AS A MODEL AND REALLY   *
*        * HAVE TO DO PROCESSING, THEN DEBUGGING DURING OPEN  *
*        * WOULD BE USEFUL                                    *
*        ******************************************************
         LOAD  EP=DBC
         LR    R3,R0
         ST    R3,DBCADDR         SAVE ADDRESS FOR PUT/GET
         ESTAE (R3)
         #DIE
         BEND  DEBUG1
         TITLE 'LOWCASAM - PROCESS OPEN/CLOSE REQUEST'
OPENCLOS IF    (SSOBFUNC+1,EQ,SSOBOPEN,CLI),THEN
*        ******************************************************
*        * FUNCTION IS OPEN.                                  *
*        ******************************************************
         LA    R1,PUTGET
         ST    R1,GPSMAMEP
         MVC   BADIODD,GPSMDDN
         L     R7,SSOBINDV
         L     R7,SSDAJFCB-SSOBGN(R7)
         USING JFCB,R7
         IF    (JFCRECFM,ON,JFCFIX),AND,(JFCRECFM,OFF,JFCVAR)
         OI    VARFLAG,RECFMF
         BEND  ,
         ELSE  ,
*        ******************************************************
*        * THE FUNCTION IS CLOSE                              *
*        ******************************************************
         IF    (FLAGWORD,ON,DEBUG),THEN
         DELETE EP=DBC
         BEND  ,
         BEND  OPENCLOS
         SPACE 5
DEBUG2   IF    (FLAGWORD,ON,DEBUG),THEN
*        ******************************************************
*        * CANCEL THE YALE DEBUGGER                           *
*        ******************************************************
         ESTAE 0
         BEND  DEBUG2
RET      L     R13,4(R13)         BACK TO SAVE1
         RETURN (14,12),RC=0
         DROP  ,
         TITLE 'LOWCASAM - PUT/GET ROUTINE ENTRY POINT'
***************************************************************
* ENTRY = PUTGET                                              *
* FUNCTION = HANDLE I/O REQUEST TO GPSAM FILE                 *
* LINKAGE = FROM GET/PUT/NOTE/POINT/ ETC. MACRO               *
*                                                             *
*        L     R1,RPL                                         *
*        LA    R0,FUNCTION                                    *
*        L     R15,24(R1)         POINT TO ACB                *
*        L     R15,8(R1)          POINT TO MY ENTRY POINT     *
*        BALR  R14,R15                                        *
*                                                             *
* FUNCTION CODES:                                             *
*        0     GET                READ RECORD FROM TERMINAL   *
*        1     PUT                NOT SUPPORTED               *
*       >1                        NOT SUPPORTED               *
***************************************************************
PUTGET   BLOCK ,
         SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         IF    (R0,EQ,0),THEN     IF ITS A GET
         LR    R2,R1              SAVE RPL ADDRESS
         USING IFGRPL,R2
         L     R1,RPLAREA         PICK UP BUFFER
         LR    R3,R1
         L     R0,RPLBUFL         GET BUFFER LENGTH
         TGET  (1),(0),EDIT       READ DATA
         IF    (VARFLAG,OFF,RECFMF),THEN
         ST    R1,RPLRLEN         READ DATA LENGTH
         ELSE  ,
         L     R5,RPLDACB         OR LRECL (FOR RECFM=F)
         LH    R5,ACBLRECL-IFGACB(R5)
         ST    R5,RPLRLEN
         BEND  ,
         IF    (0(2,R3),EQ,SLSHSTAR,CLC),THEN
         MVC   RPLFDBK,EOFCODE
         ELSE  ,
         XC    RPLFDBK,RPLFDBK
         BEND  ,
         ELSE  ,
         TPUT  BADIO,LBADIO
         MVC   RPLFDBK,ERRCODE
         BEND  ,
         LR    R1,R2
         L     R15,=V(GPSMEXIT)
         BR    R15
         BEND  PUTGET
         TITLE 'LOWCASAM - CONSTANTS, VARIABLE, AND DSECTS'
FLAGWORD DS    0F
ZAPFLAG  DC    AL1(0)             ZAPPED FLAGS
DEBUG    EQU   X'80'
VARFLAG  DC    AL1(0)             FLAGS SET DURING PROCESSING
RECFMF   EQU   X'80'              FIXED FORMAT PROCESSING
         DC    H'0'               RESERVED
DBCADDR  DC    A(*-*)
EOFCODE  DC    AL1(RPLLOGER,0,RPLEODER)
ERRCODE  DC    AL1(RPLLOGER,0,RPLINACC)
SLSHSTAR DC    C'/*'
BADIO    DC    C'ONLY READ/GET REQUESTS PERMITTED TO LOWCASE FILE '
BADIODD  DC    CL8' '
LBADIO   EQU   *-BADIO
         LTORG
         SPACE 5
AMPARM   DSECT
GPSMMPRM DS    A                  POINTER TO MODULE PARM
GPSMSSOB DS    A                  POINT TO OPEN SSOB
GPSMAMEP DS    A                  RETURN ACB AM ENTRY POINT
GPSMSAVA DS    A                  WORD TO SAVE FOR CLOSE
GPSMDDN  DS    CL8                DDNAME
         PRINT NOGEN
         EQUREGS
         IDARMRCD
         IFGRPL
         IFGACB
JFCB     DSECT
         IEFJFCBN
         IEFJSSOB DA
         END
