//GILBERTJ JOB (ACCT#),JOBRLSE,
// NOTIFY=&SYSUID,
//*RESTART=EXEC,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//*
//ASMH EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
*                       JOB RELEASE (JES2)                            *
*                                                                     *
***********************************************************************
*                                                                     *
*        THIS PROGRAM ALLOWS YOU TO ISSUE A $A COMMAND TO RELEASE     *
*        A JOB PREVIOUSLY SUBMITTED WITH "TYPRUN=HOLD".               *
*                                                                     *
*        TO PREVENT "MULTIPLE JOBS FOUND" PROBLEMS, THIS PROGRAM      *
*        USES THE SUB-SYSTEM INTERFACE TO INQUIRE ABOUT THE STATUS    *
*        OF HOMONYM JOBS.  IT THEN ISSUES A $A COMMAND WITH THE JOB   *
*        NUMBER (FOR EXAMPLE: $A JOB01234) OF THE FIRST JOB FOUND     *
*        IN THE INPUT QUEUE IN HELD STATUS.                           *
*                                                                     *
*        SAMPLE EXECUTION JCL:                                        *
*                                                                     *
*          //RLSENEXT EXEC PGM=JOBRLSE,PARM=JOBNAME                   *
*                                                                     *
*        WARNING !!! THIS PROGRAM MUST BE APF-AUTHORIZED.             *
*                                                                     *
***********************************************************************
JOBRLSE  CSECT
JOBRLSE  RMODE ANY
MAXJOBS  EQU   200                     MAX NUMBER OF HOMONYM JOBS
         SAVE  (14,12),,'GSF UTILITIES - JOBRLSE R108'
         BALR  R12,0
         USING *,R12
         ST    R13,SAVEAREA+4
         LA    R13,SAVEAREA
*
*        VALIDATE JCL PARM
*
         L     R2,0(,R1)               JCL PARM ADDRESS
         LH    R3,0(,R2)               PICK UP LENGTH
         LTR   R3,R3
         BNP   RETURN16                INVALID PARM LENGTH
         CH    R3,=H'8'
         BH    RETURN16                INVALID PARM LENGTH
*
*        ALLOCATE A SSOB AND BUILD IT.
*
         LA    R0,SSOBHSIZ+SSCSELSZ*MAXJOBS+256
         GETMAIN RU,LV=(0)
         ST    R1,SSOBPTR1
         OI    SSOBPTR1,X'80'
         LR    R11,R1
         USING SSOB,R11
         XC    SSOB(SSOBHSIZ),SSOB     BUILD THE SSOB
         LA    R0,SSOBHSIZ
         STH   R0,SSOBLEN
         MVC   SSOBID,=C'SSOB'
         LA    R0,SSOBSTAT
         STH   R0,SSOBFUNC             FUNCTION CODE: CANCEL/STATUS
         LA    R0,SSOBGN
         ST    R0,SSOBINDV
         LA    R0,SSCSIZE
         STH   R0,SSCSLEN
         MVI   SSCSFLGS,0
         MVI   SSCSULEN,0
         MVC   SSCSJOBN,SPACES         INITIALIZE THE FIELD
         BCTR  R3,0
         EX    R3,*+4
         MVC   SSCSJOBN(*-*),2(R2)     MOVE JOBNAME FROM PARM
         MVC   SSCSJOBI,SPACES         JOB ID
         LA    R0,SSCSELSZ*MAXJOBS
         STH   R0,SSCSDIMP
*
*        CALL JES2 VIA THE SUBSYSTEM INTERFACE
*
         MODESET KEY=ZERO,MODE=SUP
         LA    R1,SSOBPTR1
         IEFSSREQ  ,                   <==== CALL JES2
         LTR   R15,R15
         BZ    MODESET2                JUMP IF RC00
         ABEND 2001,DUMP               IEFSSREQ ERROR
MODESET2 MODESET KEY=NZERO,MODE=PROB
         ICM   R1,B'1111',SSOBRETN
         BZ    LOOKUP                  JUMP IF LENGTH GT ZERO
         ABEND 2002,DUMP               RETURNED LENGTH IS ZERO
*
*        LOOK UP JOB ID
*
LOOKUP   LA    R3,SSCSARBG             START OF ARRAY
         LA    R4,SSCSELSZ             ELEMENT SIZE
         LH    R5,SSCSDIMR             LENGTH OF ARRAY (IN BYTES)
         BCTR  R5,0
         ALR   R5,R3                   END OF ARRAY
         SLR   R3,R4                   1ST ENTRY
         USING SSCSARAY,R3
*LOOP
LOOP1    BXH   R3,R4,RETURN04          SCAN JOB NAMES
         TM    SSCSFLG1,SSCSEXCQ+SSCSHOLD
         BNO   LOOP1                   TRY NEXT JOB
*ENDLOOP
         MVC   CMDJOBI,SSCSARID        JOB NUMBER: 'JOB01234'
         MODESET KEY=ZERO,MODE=SUP
         SLR   R0,R0                   CONSOLE ID
         MGCR  CMD                     ISSUE 'RELEASE' COMMAND
         MODESET KEY=NZERO,MODE=PROB
         SLR   R2,R2                   RC=00
         B     FREEMAIN
*
*        CLEAN UP, RETURN
*
RETURN04 LA    R2,0004(0)              NO JOB FOUND: RC=04
*
FREEMAIN LA    R0,SSOBHSIZ+SSCSELSZ*MAXJOBS+256
         FREEMAIN RU,LV=(0),A=(R11)    FREE DYNAMIC AREA
         LR    R15,R2                  PASS RETURN CODE
         B     RETURN20
*
RETURN16 LA    R15,0016                PARM ERROR: RC16
RETURN20 L     R13,4(,R13)
         RETURN (14,12),RC=(15)        GOBACK TO SYSTEM
*----------------------------------------------------------------------
*
*        WORK AREA
*
*----------------------------------------------------------------------
SPACES   DC    CL8' '
CMD      DC    Y(CMDL,0),C'$A '
CMDJOBI  DS    CL8' '
CMDL     EQU   *-CMD
SSOBPTR1 DS    A
SAVEAREA DS    18A
*----------------------------------------------------------------------
*
*        CONTROL BLOCK DEFINITIONS
*
*----------------------------------------------------------------------
         IEFJSSOB CS
         CVT   DSECT=YES,PREFIX=NO
         IEFJESCT
         YREGS
         END
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,5)
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//SYSPRINT DD SYSOUT=*
//*
//LKED    EXEC PGM=LINKLLA,PARM='MAP,REUS,AC=1'
//SYSLIN   DD DISP=(OLD,DELETE),DSN=*.ASMH.SYSLIN
//SYSPRINT DD SYSOUT=*
//SYSLMOD  DD DISP=SHR,DSN=GILBERT.LOAD(JOBRLSE)    <== APF LOADLIB
//*
//*       TEST STEP
//*
//EXEC    EXEC PGM=JOBRLSE,PARM=IBMUSERH
//EXEC    EXEC PGM=JOBRLSE,PARM=IBMUSERH
//EXEC    EXEC PGM=JOBRLSE,PARM=IBMUSERH
//*TEPLIB  DD DISP=SHR,DSN=*.LKED.SYSLMOD
//SYSUDUMP DD SYSOUT=*
//
//GILBERTH JOB (ACCT#),GILBERT,TYPRUN=HOLD,       TARGET JOB
// CLASS=A,MSGCLASS=X                             TARGET JOB
//TGT EXEC PGM=IEFBR14                            TARGET JOB
