//GILBERTK JOB (ACCT#),COMPRESS,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMH EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MODULE NAME = COMPRESS                                              *
*                                                                     *
* DESCRIPTIVE NAME = COMPRESS a PDS with DISP=SHR                     *
*                                                                     *
* STATUS = R316                                                       *
*                                                                     *
* FUNCTION = This TSO/E command allows you to compress a partitionned *
*            data set with DISP=SHR using a protection scheme         *
*            compatible with ISPF and the linkage-editor.             *
*            It also allows you to compress several data sets with a  *
*            single command by specifying a generic data set name     *
*            such as 'SYS2.*'.  Together with the command itself,     *
*            there is an edit macro particularly handy when abend     *
*            SD37 strikes the PDS you're editing.                     *
*                                                                     *
* AUTHOR = Gilbert Saint-flour <gsf@pobox.com>                        *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = MVS/XA or MVS/ESA                                 *
*                   TSO/E 1.3                                         *
*                   STRING macro R400                                 *
*                                                                     *
*    AUTHORIZATION = NONE                                             *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
* MODULE TYPE = PROCEDURE, (TSO Command Processor)                    *
*                                                                     *
*    PROCESSOR = IBM OS/ASSEMBLER H VERSION 2 OR                      *
*                IBM HIGH LEVEL ASSEMBLER/MVS                         *
*                                                                     *
*    MODULE SIZE = 4K                                                 *
*                                                                     *
*    ATTRIBUTES = REENTERABLE, RMODE 24, AMODE 31,                    *
*                 PROBLEM STATE, KEY 8                                *
*                                                                     *
* SYNTAX = See Below                                                  *
*                                                                     *
*            COMPRESS 'data-set-name'                                 *
*                      VOLUME('volser')                               *
*                      CHECK/NOCHECK                                  *
*                      SHR/OLD                                        *
*                      CHANGED/NOCHANGED                              *
*                      SETMSG                                         *
*                                                                     *
*            'data-set-name' is the dsname of the data set or         *
*            a generic dsname like 'ISP.V2R3M0.*'.                    *
*            If you specify a generic name, "COMPRESS" retrieves      *
*            the full data sets names from the catalog.               *
*            If the dsname is not enclosed in apostrophes,            *
*            it is prefixed with your userid.                         *
*                                                                     *
*            'volser' is the volume serial number of the data set     *
*            when the data set is not cataloged or is cataloged on    *
*            another volume.                                          *
*                                                                     *
*            'OLD' specifies that the data-set(s) should be allo-     *
*            cated with DISP=OLD instead of SHR, which is the         *
*            default.  During a generic compress, 'OLD' allows you    *
*            to make sure that the data sets currently allocated      *
*            by other jobs are not compressed.                        *
*                                                                     *
*            CHECK/NOCHECK are key-words that only apply to           *
*            MVS systems with ASM2 installed.                         *
*            'CHECK' is the default and indicates that during a       *
*            generic compress operation, a data set should not        *
*            be compressed if its asm2id (ds1syscd+8 in the           *
*            f1-dscb) is identical to the job name; in other          *
*            words, if the PDS hasn't been updated since the last     *
*            time your job processed it, there's no need to           *
*            compress it again.                                       *
*            'NOCHECK' indicates that the the PDS should be           *
*            compressed regardless of what ASM2ID contains.           *
*                                                                     *
*            'CHANGED' indicates that during a generic compress       *
*            operation, a data set should not be compressed if its    *
*            DS1DSCHA bit is off, in other words, if the PDS          *
*            hasn't been updated since the last time it's been        *
*            backed up.  This is the default.                         *
*            'NOCHANGED' indicates that the the PDS should be         *
*            compressed regardless of the setting of the changed bit. *
*                                                                     *
*            'SETMSG' is an option used by the COMPRESS edit macro.   *
*                                                                     *
* OPERATION = See Below                                               *
*                                                                     *
*           1. You can install the "COMPRESS" command in any load     *
*              library since it requires no APF authorization.        *
*              If you put it in LPA, it takes less than 4K below      *
*              the 16Mb line.                                         *
*              To create the authorized environment required to       *
*              execute IEBCOPY, "COMPRESS" invokes IKJEFTSR (the      *
*              TSO/E service routine).  The default IKJEFTAP table    *
*              is set up for this purpose (and so is the IKJTSOxx     *
*              parmlib member if your system supports it).            *
*                                                                     *
*           2. The protection scheme used by "COMPRESS" is            *
*              similar to the one implemented by ISPF/PDF V2 and      *
*              provides against simultaneous updates by ISPF and      *
*              the linkage editor.  However, when you compress a      *
*              PDS with DISP=SHR you don't have any read              *
*              integrity.  This is particularly critical for          *
*              load libraries and compressing one while it is         *
*              used may cause S106 abends.                            *
*                                                                     *
*           3. If you specify a volser together with a generic        *
*              data set name, "COMPRESS" looks in the catalog for     *
*              the full data set names, skipping the data sets that   *
*              are cataloged on other disk packs.  "COMPRESS"         *
*              does not read the VTOC: if uncataloged data sets that  *
*              satisfy the generic name requirement exist on the      *
*              pack, they are not processed.                          *
*                                                                     *
*           4. If you intend to use the "COMPRESS" edit macro,        *
*              copy it to a clist library such as IPO1.CMDPROC        *
*              or ISR.V2R3M0.ISRCLIB.  That's all you need to be      *
*              able to invoke it as an edit primary commands like     *
*              'CANCEL' or 'RESET'.                                   *
*              At any moment while you're editing a member, you       *
*              may enter 'COMPRESS' to compress the PDS.  When        *
*              it is compressed, the edit macro returns an ISPF       *
*              message that indicates how full the PDS now is.        *
*                                                                     *
*           5. The TSO/E service routine creates a parallel TMP       *
*              to invoke IEBCOPY.                                     *
*              This is what the TCB structure looks like while        *
*              IEBCOPY is executing:                                  *
*                                                                     *
*                IKJEFT01           TMP INIT (JSTCB)                  *
*                  IKJEFT02         TMP MAINLINE                      *
*                    IKJEFT09       TMP SECOND LEVEL                  *
*                      COMPRESS     COMMAND PROCESSOR                 *
*                  IKJEFT02         TMP MAINLINE (PARALLEL TMP)       *
*                    IKJEFT09       TMP SECOND LEVEL (PARALLEL TMP)   *
*                      IEBCOPY      IEBCOPY                           *
*                                                                     *
*           6. The STTMPMD (set TMP mode) macro is used to disable    *
*              the attention and PA1 keys.  This prevents users       *
*              from destroying their PDS which can so easily happen   *
*              when they use ISPF compress services.                  *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*260 MODIFIED FOR TSO/E VERSION 2 (AMODE=31)                          *
*266 CHECK FOR PDSE'S AND DO NOT COMPRESS THEM                   @PDSE*
*267 REFRESH DISPLAY IF INVOKED FROM PDF 3.4 PANEL             @ZDLREF*
*310 Changed numbering scheme                                         *
*311 Goback with non-zero return-code after PARSE failure             *
*    Small changes to the REXX EXEC for ISPF 4.1                      *
*312 Add SPACE=(TRK,10) to SYSIN/SYSPRINT allocation and              *
*    invoke DAIRFAIL if it fails.                                     *
*313 Add SETMSG option which sets ZMSG000S and ZMSG000L and issues    *
*    SETMSG(ISPZ000) instead of the normal completion message;        *
*    the COMPRESS EDIT macro no longer needs to use OUTTRAP.          *
*314 Replace IKJTSMSG macros with STRING; issue statistics in long    *
*    message when SETMSG option is used.                              *
*315 Move STTMPMD macros around IEBCOPY invocation to make sure       *
*    that STTMPMD OFF is executed.                                    *
*316 Retrieve volser from ZDLVOL variable when invoked from PDF 3.4   *
*    Minor technical changes                                          *
&REL     SETC  'R316'                                                 *
***********************************************************************
COMPRESS CSECT
COMPRESS AMODE 31
         SAVE  (14,12),,'GSF UTILITIES - COMPRESS &REL'
         LR    R12,R15                 BASE REGISTER
         USING COMPRESS,R12
         LR    R11,R1                  POINT AT CPPL.
         USING CPPL,R11
***********************************************************************
*        ALLOCATE DYNAMIC STORAGE AREA                                *
***********************************************************************
         L     R0,=A(DYNAML)           SIZE OF DYNAMIC AREA
         GETMAIN R,LV=(0)              ACQUIRE DYNAMIC STORAGE
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING DYNAM,R13
         LA    R14,DYNAM+72            CLEAR DYNAMIC STORAGE AREA
         LA    R15,DYNAML-72           CLEAR DYNAMIC STORAGE AREA
         SLR   R1,R1                   CLEAR DYNAMIC STORAGE AREA
         MVCL  R14,R0                  CLEAR DYNAMIC STORAGE AREA
         MVI   MODE,C'S'               DEFAULT IS MODE=SINGLE
         EXTRACT ADDRTIOT,FIELDS=TIOT,MF=(E,EXTRACTL)
         EXTRACT ADDRCOMM,FIELDS=COMM,MF=(E,EXTRACTL)
***********************************************************************
*        BUILD PARAMETER LIST FOR IKJEFF02                            *
***********************************************************************
         ST    R11,MTCPPLP             STORE CPPL ADDRESS IN MTPL
         LA    R0,MTCSECTP             MESSAGE SECTION
         ST    R0,MTPLPTR              STORE ADDR IN MTPL
         LA    R0,DYNECB               EVENT CONTROL BLOCK
         ST    R0,MTECBP               STORE ECB IN MTPL
         L     R0,=A(MSGCSECT)         MESSAGE CSECT
         ST    R0,MTCSECTP             STORE ECT IN PPL
         OI    MTSW1,MTPUTLSW          PUTLINE IS NEEDED
         OI    MTSW2,MTFMT             31-BIT PARM LIST
***********************************************************************
*        BUILD PARSE PARM LIST, CALL IKJPARS                          *
***********************************************************************
PARSE000 LA    R1,DYNPPL               POINT TO PPL
         USING PPL,R1                  SET UP ADDRESSABILITY TO PPL
         L     R0,CPPLUPT              USER PROFILE TABLE
         ST    R0,PPLUPT               STORE UPT ADDRESS IN PPL
         L     R0,CPPLECT              ENVIRONMENT CONTROL TABLE
         ST    R0,PPLECT               STORE ECT IN PPL
         LA    R0,DYNECB               EVENT CONTROL BLOCK
         ST    R0,PPLECB               STORE ECB IN PPL
         MVC   PPLPCL,=A(PCLCSECT)     STORE PCL ADDR INTO PPL
         LA    R0,DYNANSWR             ANSWER AREA
         ST    R0,PPLANS               STORE ANSWER ADDRESS IN PPL
         L     R0,CPPLCBUF             COMMAND BUFFER
         ST    R0,PPLCBUF              STORE BUFFER ADDRESS IN PPL
         L     R15,CVTPTR              ADDR OF CVT
         L     R15,CVTPARS-CVTMAP(,R15) ADDR OF IKJPARS
         SYNCH (R15)                   CALL THE TSO PARSE ROUTINE
         ST    R15,MAXCC               SAVE RETURN CODE
         LTR   R15,R15                 CHECK FOR ZERO PARSE RETURN
         BNZ   FINISH                  GO AWAY UPSET
         DROP  R1                      WAS PPL
***********************************************************************
*        PROCESS PARSED PARAMETERS                                    *
***********************************************************************
         L     R8,DYNANSWR             LOAD PDL ADDRESS
         USING IKJPARMD,R8             SET UP ADDRESSABILITY TO PDL
PARSE100 LA    R14,DSNAME              44-BYTE AREA
         LA    R15,L'DSNAME            GET LENGTH
         L     R0,DSNPCE               GET ADDRESS
         LH    R1,DSNPCE+4             ACTUAL LENGTH OF DSN
         STH   R1,DSNLENGTH            SAVE ACTUAL LENGTH OF DSN
         ICM   R1,B'1000',=C' '        PADDING
         MVCL  R14,R0                  MOVE DATA SET NAME
PARSE200 MVI   CHECK,C'C'              DEFAULT IS 'CHECK'
         CLI   CHKPCE+1,2              CHECK FOR 2ND IKJPNAME
         BNE   PARSE220                JUMP IF NOT THE 2ND
         MVI   CHECK,C'N'              2ND IS 'NOCHECK'
PARSE220 MVI   CHANGED,C'C'            DEFAULT IS 'CHANGED'
         CLI   CHNGPCE+1,2             CHECK FOR 2ND IKJPNAME
         BNE   PARSE230                JUMP IF NOT THE 2ND
         MVI   CHANGED,C'N'            2ND IS 'NOCHANGED'
PARSE230 CLI   SETMPCE+1,1             SETMSG option specified?
         BNE   PARSE300                no, jump
         MVI   SETMSG,C'Y'             yes, remember it
PARSE300 LA    R0,=AL2(DALSTATS,1,1,X'0800') DISP=SHR KEY
         CLI   STATPCE+1,2             CHECK FOR 2ND IKJPNAME
         BNE   PARSE310                JUMP IF NOT THE 2ND
         LA    R0,=AL2(DALSTATS,1,1,X'0100') DISP=OLD KEY
PARSE310 ST    R0,STATUS               STORE ADDRESS OF STATUS FIELD
PARSE500 TM    VOLPCE2+6,X'80'         CHECK VOLSER SUBPARAMETER
         BZ    PARSE999                JUMP IF NO SUBPARAM SPECIFIED
         L     R1,VOLPCE2+00           GET SUBPARAM ADDRESS
         MVC   VOLSER,0(R1)            MOVE 6-BYTE VOLUME SERIAL
         MVI   VOLGIVEN,C'Y'           SHOW VOL HAS BEEN GIVEN L
PARSE999 IKJRLSA DYNANSWR              RELEASE PDL
***********************************************************************
*        EXTRACT DATA SET NAMES FROM CATALOG                          *
***********************************************************************
         LH    R1,DSNLENGTH            length of dsname
         LA    R15,DSNAME-1(R1)        point R15 at last character
         CLI   0(R15),C'*'             generic dsname?
         BNE   PROCESS2                NO, USE MODE=SINGLE
         BCTR  R1,0                    length(dsname)-1 for LISTCAT
         STC   R1,WK265                STORE LENGTH
         MVC   WK265+1(44),DSNAME      MOVE PARTIAL NAME
         LA    R0,WK265                ENTRY NAME
         ST    R0,CTGENT               ENTRY ADDRESS
         MVI   CTGOPTN1,CTGNAME        CTGENT POINTS TO ENTRY NAME
         OI    CTGOPTN1,CTGGENLD       GENERIC LOCATE REQUEST
         MVI   CTGOPTN3,CTGAM0         OS/VS2 CATALOG MGMT REQUEST
         OI    CTGOPTN3,CTGSUPLT       SUPER LOCATE REQUEST
         MVI   CTGTYPE,CTGTALIN        ENTRY TYPE IS NONVSAM
         OI    CTGOPTNS,CTGF2WKA       FORMAT-2 WORK AREA
         GETMAIN RC,LV=45*10000        WORK AREA FOR GENERIC LOCATE
         LTR   R15,R15                 CHECK COMPLETION
         BNZ   MSGGETM                 GETMAIN FAILED, EXIT
         ST    R1,CTGWKA               STORE ADDRESS INTO CTGPL
         ST    R0,0(,R1)               FORMAT WORK AREA HEADER
         XC    4(4,R1),4(R1)           FORMAT WORK AREA HEADER
         LOCATE CTGPL                  REQUEST CATALOG MGMT SERVICES
         LTR   R15,R15                 CHECK COMPLETION
         BNZ   FINISH                  GENERIC LOCATE FAILED, EXIT
         MVI   MODE,C'C'               MODE IS "CATALOG"
         L     R8,CTGWKA               SVC 26 WORK AREA ADDRESS
         LM    R0,R1,0(R8)             SVC 26 WORK AREA HEADER
         LA    R1,7(,R1)               USED LENGTH
         SRL   R1,3                     ROUNDED UP
         SLL   R1,3                      TO A MULTIPLE OF 8
         SR    R0,R1                   COMPUTE UNUSED LENGTH
         LA    R1,0(R1,R8)             ADDRESS OF UNUSED PORTION
         FREEMAIN RU,LV=(0),A=(1)      FREE UNUSED PORTION
         LNR   R0,R0                   GET NEGATIVE LENGTH
         A     R0,0(,R8)               ADD INITIAL LENGTH
         ST    R0,0(,R8)               STORE CURRENT LENGTH
         LA    R8,8(,R8)               FIRST DATA SET NAME
***********************************************************************
*        OBTAIN VOLUME AND DSCB INFORMATION                           *
***********************************************************************
PROCESS  CLI   0(R8),C'A'              NVSAM ENTRY?
         BNE   NEXTDSN4                NO, SKIP IT
         MVC   DSNAME,1(R8)            MOVE DATA SET NAME
PROCESS2 MVI   PRINTMSG,C'N'           RESET INDICATOR
         L     R0,=A(1000000)          ONE MEG
         GETMAIN RC,LV=(0),LOC=(BELOW,ANY) GET ONE MEG FOR IEBCOPY
         LTR   R15,R15                 CHECK COMPLETION
         BNZ   MSGGETM                 GETMAIN FAILED, EXIT
         FREEMAIN RU,LV=(0),A=(1)      FREE ONE MEG FOR IEBCOPY
         CLI   VOLGIVEN,C'Y'           CHECK IF VOLSER WAS SUPPLIED
         BE    OBTAIN00                JUMP IF IT WAS
***********************************************************************
*        retrieve DSLIST volser                                       *
***********************************************************************
         ICM   R15,B'1111',=A(ISPQRY)  ISPQRY AVAILABLE?
         BZ    ZDLVOL9                 NO, QUIT
         BALR  R14,R15                 CALL ISPQRY
         LTR   R15,R15                 ISPF SERVICES AVAILABLE?
         BNZ   ZDLVOL9                 NO, QUIT
         WXTRN ISPQRY,ISPLINK          THEY ARE OPTIONAL
         CLI   MODE,C'S'               MODE=SINGLE?
         BNE   ZDLVOL9                 NO, QUIT
*VCOPY (ZDLDSN,ZDLVOL)
         LA    R14,L'ZDLDSN            max length
         LA    R15,L'ZDLVOL            max length
         STM   R14,R15,DEVT1           length array
*******  ISPLINK ($VCOPY,=C'(ZENVIR ZAPPLID ZUSER ZSCREEN)',           X
               SIXWORDS,ZENVIR,$MOVE)
         LA    R14,=C'VCOPY '          FUNCTION
         LA    R15,=C'(ZDLDSN ZDLVOL)' VARIABLE NAME
         LA    R0,DEVT1                length array
         LA    R1,ZDLDSN               value array
         LA    R2,=C'MOVE '            mode
         STM   R14,R2,EFTPARM          STORE PARM LIST
         OI    EFTPARM+16,X'80'        MARK END OF LIST
         L     R15,=A(ISPLINK)         ISPF SERVICES
         LA    R1,EFTPARM              PARM LIST ADDRESS
         BALR  R14,R15                 ISSUE VCOPY
         LTR   R15,R15                 WENT OK?
*@SNAP ZDLDSN,ZDLVOL,DSNAME
         BNZ   ZDLVOL9                 NO, QUIT
         CLC   DSNAME,ZDLDSN           same dsname?
         BNE   ZDLVOL9                 NO, QUIT
         CLI   ZDLVOL,C'*'             ALIAS?
         BE    ZDLVOL9                 YES, QUIT
         MVC   VOLSER,ZDLVOL           yes, keep volser
         MVI   VOLGIVEN,C'Y'           VOLSER was supplied by DSLIST
         B     OBTAIN00                skip LOCATE
ZDLVOL9  EQU   *
*
*        Retrieve volser from the catalog and ignore migrated data sets
*
         L     R14,CAML26              CAMLST 1ST WORD
         LA    R15,DSNAME              DATA SET NAME
         SLR   R0,R0
         LA    R1,WK265                WORK AREA
         STM   R14,R1,EFTPARM          STORE RELOCATED CAMLST
         LOCATE EFTPARM                GET VOLSER AND DEVICE TYPE
         LTR   R15,R15                 CHECK FOR SUCCESSFUL COMPLETION
         BNZ   MSG26                   LOCATE FAILED, EXIT
         CLI   WK265+2+2,UCB3DACC      CHECK DEVICE CLASS
         BNE   MSG26B                  NOT DASD, EXIT
         MVC   VOLSER,WK265+2+4        KEEP VOLSER
         CLC   =C'MIGRAT',VOLSER       CHECK VOLSER
         BE    MSG26C                  MIGRATED BY HSM, EXIT
*
*        Read the F1-DSCB
*
OBTAIN00 L     R14,CAML28F1            CAMLST 1ST WORD
         LA    R15,DSNAME              DATA SET NAME
         LA    R0,VOLSER               VOL SERIAL
         LA    R1,DS1FMTID             FORMAT-1
         STM   R14,R1,EFTPARM          STORE RELOCATED CAMLST
         OBTAIN EFTPARM                READ F1-DSCB
         LTR   R15,R15                 CHECK FOR SUCCESSFUL COMPLETION
         BNZ   MSG28                   OBTAIN FAILED, EXIT
*
*        Skip data set if not a 100% pure PDS
*
         TM    DS1DSORG+0,DS1DSGPO     CHECK THAT DSORG=PO
         BNO   MSG28B                  EXIT IF DSORG IS NOT PO
         TM    DS1SMSFG,DS1PDSE        IS THIS A PDSE?            @PDSE
         BO    MSG28E                  YES, DO NOT PROCESS        @PDSE
         MVC   RECFM,DS1RECFM          KEEP RECFM FOR LATER USE
         CLI   MODE,C'S'               CHECK MODE
         BE    ALLOC000                JUMP IF MODE=SINGLE
         OC    DS1EXPDT,DS1EXPDT       CHECK NON-ZERO EXPDT
         BNZ   MSG28C                  NON ZERO EXPDT, JUMP
*
*        Skip data set if option "CHANGED" is specified
*        and the "data set changed" indicator is off
*
         CLI   CHANGED,C'N'            LOOK FOR "NOCHANGED" OPTION
         BE    OBTAIN45                BRANCH IF "NOCHANGED"
         TM    DS1DSIND,DS1DSCHA       CHECK FOR CHANGED BIT
         BNO   MSG28F                  JUMP IF CHANGED FLAG IS OFF
*
*        Skip data set if option "CHECK" is specified and the
*        job name stored by ASM2 is the same as my job's
*
OBTAIN45 CLI   CHECK,C'N'              LOOK FOR "NOCHECK" OPTION
         BE    ALLOC000                BRANCH IF "NOCHECK"
         CLI   DS1SYSCD+8,X'FF'        CHECK FOR ASM2 SIGNATURE
         BNE   ALLOC000                JUMP IF NO ASM2
         L     R14,ADDRTIOT            GET TIOT ADDRESS
         CLC   DS1SYSCD(8),0(R14)      CHECK JOB NAME
         BE    MSG28D                  JUMP IF SAME JOBNAME
***********************************************************************
*                                                                     *
*        ALLOCATE DATA SET TO BE COMPRESSED                           *
*                                                                     *
***********************************************************************
ALLOC000 BAL   R14,INIT99RB            INITIALIZE SVC99 RB
         USING S99RB,R2
         OI    S99FLG11,S99NOCNV       DO NOT USE EXISTING DD
         OI    S99FLG11,S99NOMNT       DO NOT MOUNT VOLUMES
         LA    R14,TU55DDN             DDNAME TEXT UNIT
         MVC   0(6,R14),=AL2(DALRTDDN,1,8)  RETURN DDNAME KEY
         LA    R15,TU02DSN             DSNAME TEXT UNIT
         MVC   0(6,R15),=AL2(DALDSNAM,1,44) DSNAME KEY
         L     R0,STATUS               ADDRESS OF STATUS KEY
         STM   R14,R0,S99RBEND+00      FIRST TO THIRD TEXT UNIT PTRS
         LA    R14,TU10VOL             VOLSER TEXT UNIT
         MVC   0(6,R14),=AL2(DALVLSER,1,6)  VOL SER KEY
         BAL   R15,ALLOC110
         DC    AL2(DALUNIT,1,L'ALLOC109)
ALLOC109 DC    C'SYSALLDA',0H'0'
ALLOC110 STM   R14,R15,S99RBEND+12     4TH, 5TH TEXT UNIT PTRS
         MVI   S99RBEND+16,X'80'       END OF LIST
         LA    R1,DFS99RBP             S99RBPTR
         DYNALLOC ,                    ISSUE SVC 99 (ALLOCATE)
         LTR   R15,R15
         BNZ   MSG99                   ALLOCATION FAILED, EXIT
         DROP  R2                      WAS S99RB
         MVC   DDNAME,TU55DDN+6        SAVE RETURNED DDNAME
         DEVTYPE DDNAME,DEVT1,DEVTAB   GET DEVICE CHARACTERISTICS
***********************************************************************
*                                                                     *
*        ALLOCATE SYSIN AND SYSPRINT DATA SETS.                       *
*                                                                     *
***********************************************************************
ALLOC200 CLI   TEMPALL,C'Y'            CHECK DDNAME ALREADY ALLOCATED
         BE    SYSINOK                 DD ALREADY ALLOCATED, OPEN IT
         MVI   TEMPALL,C'Y'            SHOW DDNAME ALREADY ALLOCATED
         BAL   R14,INIT99RB            INITIALIZE SVC99 RB
         USING S99RB,R2
         LA    R14,TU55DDN             DDNAME TEXT UNIT (ALREADY OK)
         LA    R15,=AL2(DALTRK,0)      SPACE=TRK
         LA    R0,ALLOC209             SPACE=(TRK,10)
         BAL   R1,ALLOC210
         DC    AL2(DALUNIT,1,8),CL8'SYSDA'
ALLOC209 DC    AL2(DALPRIME,1,3),AL3(10),0H'0'     SPACE=(TRK,10)
ALLOC210 STM   R14,R1,S99RBEND+00      STORE TEXT UNIT PTRS
         LA    R1,DFS99RBP             S99RBPTR
         DYNALLOC ,                    ISSUE SVC 99 (ALLOCATE SYSIN)
         LTR   R15,R15
         BNZ   ALLOC255                ALLOCATION FAILED, EXIT
         MVC   IEBDDIN,TU55DDN+6       MOVE SYSIN DDNAME
         LA    R1,DFS99RBP             S99RBPTR
         DYNALLOC ,                    ISSUE SVC 99 (ALLOCATE SYSPRINT)
         LTR   R15,R15
         BNZ   ALLOC255                ALLOCATION FAILED, EXIT
         MVC   IEBDDPRT,TU55DDN+6      MOVE SYSPRINT DDNAME
         B     SYSINOK                 ALLOCATION FAILED, EXIT
         DROP  R2                      WAS S99RB
ALLOC255 ST    R15,MAXCC               RC=8
         BAL   R14,DAIRFAIL            Issue dynalloc error message
         B     FINISH                  exit
***********************************************************************
*                                                                     *
*        WRITE SYSIN FILE AND BUILD PARAMETER LIST FOR IEBCOPY.       *
*                                                                     *
***********************************************************************
SYSINOK  LA    R2,WK265                DCB SLOT
         USING IHADCB,R2
         MVC   IHADCB(DCBLEN),MODELDCB MOVE MODEL DCB
         MVC   DCBDDNAM,IEBDDIN        MOVE SYSIN DDNAME
         MVI   DCBRECFM,DCBRECF        RECFM=F
         MVC   DCBBLKSI,=H'80'         BLKSIZE=80
         MVI   DCBBUFNO,01             BUFNO=1
         ST    R2,OPENLIST             BUILD OPEN LIST
         MVI   OPENLIST,X'8F'          OPEN MODE IS OUTPUT
         OPEN  MF=(E,OPENLIST)         OPEN IEBSYSIN
         LTR   R15,R15                 CHECK OPEN OK
         BNZ   FINISH                  OPEN FAILED, EXIT
         LA    R15,WK265+128           RMODE24 WORK AREA
         MVC   0(PUT24L,R15),PUT24     MOVE PUT MACRO TO 24-BIT STORAGE
         BASSM R14,R15                 DO PUT LOCATE
         STRING ' COPY I=',(DDNAME,,T),',O=',(DDNAME,,T),',LIST=NO',   X
               INTO=((R1),80)
         CLOSE MF=(E,OPENLIST)         CLOSE IEBSYSIN
         MVI   DCBBUFNO,00             PREVENT S30A-14
         FREEPOOL IHADCB               FREE BUFFERS
         DROP  R2                      WAS IHADCB
         MVI   IEBDD+1,IEBDDLEN        DDNAME LIST LENGTH
         LA    R0,=H'0'                IEBCOPY PARM
         LA    R1,IEBDD                DDN LIST
         STM   R0,R1,IEBPARM           BUILD PARAMETER LIST
         OI    IEBPARM+04,X'80'        MARK END OF LIST
***********************************************************************
*                                                                     *
*        BUILD PARM LIST FOR THE TSO/E SERVICE ROUTINE (IKJEFTSR)     *
*                                                                     *
***********************************************************************
         LA    R0,=X'0000,0102'        FLAGS
         ST    R0,EFTPARM+00
         LA    R0,=C'IEBCOPY  '        PGM NAME OR COMMAND BUFFER
         ST    R0,EFTPARM+04
         LA    R0,=F'8'                LENGTH OF COMMAND BUFFER
         ST    R0,EFTPARM+08
         LA    R0,RETCODE              RETURN CODE
         ST    R0,EFTPARM+12
         LA    R0,RSNCODE              REASON CODE
         ST    R0,EFTPARM+16
         LA    R0,ABNDCODE             ABEND CODE
         ST    R0,EFTPARM+20
         LA    R0,IEBPARM              IEBCOPY PARM
         ST    R0,EFTPARM+24
         OI    EFTPARM+24,X'80'        END OF LIST
***********************************************************************
*                                                                     *
*        SCAN TIOT TO GET UCB ADDRESS.                                *
*                                                                     *
***********************************************************************
ENQ      SLR   R0,R0                   WORK REGISTER
         L     R1,ADDRTIOT             GET TIOT ADDRESS
         USING TIOT1,R1
ENQ015   AR    R1,R0                   BUMP UP TO NEXT ENTRY
         IC    R0,TIOELNGH             LENGTH OF CURRENT ENTRY
         CLC   DDNAME,TIOEDDNM         SAME DDNAME?
         BNE   ENQ015                  LOOP THROUGH TIOT
         ICM   R0,B'0111',TIOEFSRT     GET 24-BIT UCB ADDRESS
         ST    R0,ADDRUCB              STORE UCB ADDRESS FOR RESERVE
         DROP  R1                      WAS TIOT1
***********************************************************************
*        ENQ/RESERVE WITH ISPF/HEWL CONVENTIONS                       *
***********************************************************************
         MVC   DYNENQL(16),ENQMODEL    MOVE PATTERN ENQ LIST
         TM    RECFM,DS1RECFU          CHECK RECFM
         BO    ENQLKED                 RECFM=U, ASSUME LOAD LIBRARY
         RESERVE (SPFEDIT,DSNAME,E,44,SYSTEMS),UCB=ADDRUCB,            X
               MF=(E,DYNENQL)
         B     ENQDONE                 ENQ DONE FOR RECFM=F/V
SPFEDIT  DC    C'SPFEDIT '             QNAME FOR ISPF V2 ENQUEUES
SYSLMOD  DC    C'SYSIEWLP'             QNAME FOR LINK-EDIT
ENQLKED  L     R1,ADDRUCB              LOAD UCB ADDRESS
         TM    UCBTBYT2-UCBOB(R1),UCBRR SHARED DASD?
         BO    ENQLKEDR                IF SHARED, DO A RESERVE
         ENQ   (SYSLMOD,DSNAME,E,44,SYSTEM),MF=(E,DYNENQL)
         B     ENQDONE
ENQLKEDR RESERVE (SYSLMOD,DSNAME,E,44,SYSTEMS),UCB=ADDRUCB,            X
               MF=(E,DYNENQL)
ENQDONE  EQU   *
***********************************************************************
*                                                                     *
*        CALL IEBCOPY VIA IKJEFTSR                                    *
*                                                                     *
***********************************************************************
         STTMPMD ON,KEYS=ALL           TRAP CLEAR AND PA1 KEYS
         LA    R1,EFTPARM              PARM FOR IKFEFTSR
         L     R15,CVTPTR              ADDR OF CVT
         L     R15,CVTTVT-CVTMAP(,R15) ADDR OF TSVT
         L     R15,TSVTASF-TSVT(,R15)  ADDR OF IKJEFTSR
         SYNCH (R15)                   CALL THE TSO SERVICE ROUTINE
         LTR   R15,R15                 CHECK RETURN CODE
         BZ    DEQUEUE                 NOT HIGHER, JUMP
         MVI   PRINTMSG,C'Y'           PRINT IEBCOPY MESSAGES
         CLC   MAXCC,RETCODE           HIGHEST SO FAR?
         BH    DEQUEUE                 MAXCC HIGHER, JUMP
         MVC   MAXCC,RETCODE           KEEP MAXCC
DEQUEUE  DEQ   MF=(E,DYNENQL)          DEQ/RELEASE
         STTMPMD OFF                   ALLOW CLEAR AND PA1 KEYS AGAIN
***********************************************************************
*        Free the PDS that has just been compressed                   *
***********************************************************************
         BAL   R14,INIT99RB            INITIALIZE SVC99 RB
         USING S99RB,R2
         MVI   S99VERB,S99VRBUN        VERB IS "UNALLOCATE"
         LA    R14,TU55DDN             DDNAME TEXT UNIT
         MVC   0(6,R14),=AL2(DUNDDNAM,1,8)  DDNAME KEY
         MVC   6(8,R14),DDNAME         DDNAME
         ST    R14,S99RBEND+00         1ST & ONLY TEXT UNIT PTR
         OI    S99RBEND+00,X'80'       END OF LIST
         LA    R1,DFS99RBP             S99RBPTR
         DYNALLOC ,                    ISSUE SVC 99 (FREE PDS)
         DROP  R2                      WAS S99RB
***********************************************************************
*        COMPUTE PERCENTAGE OF SPACE USED AFTER COMPRESSION           *
***********************************************************************
         OC    RETCODE,RETCODE         CHECK IEBCOPY RETURN CODE
         BNZ   MSG08                   JUMP IF RC=00
         SLR   R1,R1                   make it relative to zero
         ICM   R1,B'0011',DS1LSTAR     last track used (relative to 0)
         LA    R2,1(,R1)               number of tracks used (before)
USED00   L     R14,CAML28F1            CAMLST 1ST WORD
         LA    R15,DSNAME              DATA SET NAME
         LA    R0,VOLSER               VOL SERIAL
         LA    R1,DS1FMTID             FORMAT-1
         STM   R14,R1,EFTPARM          STORE RELOCATED CAMLST
         OBTAIN EFTPARM                READ F1-DSCB
         LTR   R15,R15                 CHECK FOR SUCCESSFUL COMPLETION
         BNZ   USED99                  OBTAIN FAILED, EXIT
         SLR   R1,R1                   make it relative to zero
         ICM   R1,B'0011',DS1LSTAR     last track used (relative to 0)
         LA    R1,1(,R1)               number of tracks used
         STM   R1,R2,TRKSUSED          tracks used (after,before)
         SLR   R3,R3                   PREPARE IC
         IC    R3,DS1NOEPV             NUMBER OF EXTENTS
         LA    R4,DS1EXT1              ADDR OF 1ST EXTENT DESCRIPTOR
         SLR   R5,R5                   TRACK COUNTER
USED20   LA    R0,DS1EXT3+L'DS1EXT3    ADDR OF 3RD EXTENT DESCRIPTOR
         CR    R4,R0
         BNE   USED40                  JUMP IF NOT END OF F1-DSCB
USED30   L     R14,CAML28F3            CAMLST 1ST WORD
         LA    R15,DS1PTRDS            PTR TO F3-DSCB
         LA    R0,VOLSER               VOL SERIAL
         LA    R1,IECSDSL3             FORMAT-3
         STM   R14,R1,EFTPARM          STORE RELOCATED CAMLST
         OBTAIN EFTPARM                READ F1-DSCB
         LTR   R15,R15                 CHECK FOR SUCCESSFUL COMPLETION
         BNZ   USED99                  OBTAIN FAILED, EXIT
         LA    R4,DS3EXTNT             FIRST 4 EXTENTS OF F3-DSCB
         B     USED50                  GO PROCESS IT
USED40   LA    R0,DS3FMTID             1ST EXTENT DESC IN F3-DSCB
         CR    R4,R0
         BNE   USED50                  JUMP IF FIRST 4 DESC PROC'D
         LA    R4,DS3ADEXT             9 ADDITIONAL EXTENTS OF F3-DSCB
USED50   LH    R0,2(,R4)               GET START CYL
         MH    R0,DEVT1+10             NUMBER OF TRKS PER CYL
         AH    R0,4(,R4)               ADD START TRACK
         LH    R1,6(,R4)               END CYL
         MH    R1,DEVT1+10             NUMBER OF TRKS PER CYL
         AH    R1,8(,R4)               END TRACK
         LA    R1,1(,R1)               END TRK +1
         SLR   R1,R0                   number of tracks in this extent
         LR    R0,R5                   save first track number
         ALR   R5,R1                   tally allocated tracks
         CL    R0,TRKSUSED             EOF IN THIS EXTENT?
         BNL   USED57                  NO, JUMP
         CL    R5,TRKSUSED             EOF IN THIS EXTENT?
         BL    USED57                  NO, JUMP
         LA    R0,1                    increment
         AH    R0,0(,R4)               current extent number
         STC   R0,XTNTUSED             SAVE NUMBER OF LAST EXTENT USED
USED57   LA    R4,L'DS1EXT1(,R4)       NEXT EXTENT DESC
         BCT   R3,USED20               LOOP UNTIL ALL EXTENTS PROCESSED
USED90   SLR   R0,R0                   PREPARE DR
         LA    R1,1                    PREPARE AH
         AH    R1,DS1LSTAR+0           LAST TT USED
         MH    R1,=H'100'              PREPARE PERCENTAGE CALC
         DR    R0,R5                   PCT=(USED*100)/TOTAL
         LTR   R1,R1                   0% ==> 1%
         BNZ   *+8                     0% ==> 1%
         LA    R1,1                    0% ==> 1%
         ST    R1,RETCODE              PERCENTAGE FOR MSG00
USED99   STRING 'MSG000 COMPRESS successful for ',(DSNAME,,T),         X
               ', now ',(RETCODE,F,L),'% full.',INTO=MESSAGE
***********************************************************************
*        check if we're running under ISPF in "single" mode           *
***********************************************************************
         ICM   R15,B'1111',=A(ISPQRY)  ISPQRY AVAILABLE?
         BZ    ISSUEMSG                NO, QUIT
         BALR  R14,R15                 CALL ISPQRY
         LTR   R15,R15                 ISPF SERVICES AVAILABLE?
         BNZ   ISSUEMSG                NO, QUIT
         CLI   MODE,C'S'               MODE=SINGLE?
         BNE   ISSUEMSG                NO, QUIT
***********************************************************************
*        REFRESH DISPLAY WHEN INVOKED FROM PDF 3.4 PANEL              *
***********************************************************************
*        ISPLINK VREPLACE ZDLREF Y
ZDLREF10 LA    R14,=C'VREPLACE'        SERVICE
         LA    R15,=C'ZDLREF  '        VARIABLE NAME
         LA    R0,=F'1'                VALUE LENGTH
         LA    R1,=C'Y'                ADDR OF VARIABLE VALUE
         STM   R14,R1,EFTPARM          STORE PARM LIST
         OI    EFTPARM+12,X'80'        MARK END OF LIST
         L     R15,=A(ISPLINK)         ISPF SERVICES
         LA    R1,EFTPARM              PARM LIST ADDRESS
         BALR  R14,R15                 ISSUE VREPLACE
         LTR   R15,R15                 WENT OK?
         BNZ   ZDLREF99                NO, QUIT
*        ISPLINK VPUT (ZDLREF)
ZDLREF20 LA    R14,=C'VPUT'            SERVICE
         LA    R15,=C'ZDLREF  '        VARIABLE NAME
         STM   R14,R15,EFTPARM         STORE PARM LIST
         OI    EFTPARM+04,X'80'        MARK END OF LIST
         L     R15,=A(ISPLINK)         ISPF SERVICES
         LA    R1,EFTPARM              PARM LIST ADDRESS
         BALR  R14,R15                 ISSUE VPUT
ZDLREF99 EQU   *
***********************************************************************
*        SETMSG function                                              *
***********************************************************************
SETMSG00 CLI   SETMSG,C'Y'             SETMSG option specified?
         BNE   SETMSG99                no, exit
*        ISPLINK VREPLACE ZMSG000S
SETMSG10 STRING 'Now ',(RETCODE,F,L),'% full',INTO=MESSAGE
         ST    R15,LENGTH              no, exit
         LA    R14,=C'VREPLACE'        SERVICE
         LA    R15,=C'ZMSG000S'        VARIABLE NAME
         LA    R0,LENGTH               VALUE LENGTH
         LA    R1,MESSAGE              VALUE
         STM   R14,R1,EFTPARM          STORE PARM LIST
         OI    EFTPARM+12,X'80'        MARK END OF LIST
         L     R15,=A(ISPLINK)         ISPF SERVICES
         LA    R1,EFTPARM              PARM LIST ADDRESS
         BALR  R14,R15                 ISSUE VREPLACE
         LTR   R15,R15                 WENT OK?
         BNZ   SETMSG99                NO, QUIT
*        ISPLINK VREPLACE ZMSG000S
SETMSG20 LM    R1,R2,TRKSUSED          tracks used (after,before)
         SLR   R2,R1                   R2=tracks recovered by IEBCOPY
         STRING (DSNAME,,T),' successfully compressed',                X
               ', tracks_recovered=',((R2),,L),'.',                    X
               ' Total_tracks=',((R5),,L),                             X
               ',Tracks_used=',(TRKSUSED,F,L),                         X
               ',Total_extents=',(DS1NOEPV,FL1,L),                     X
               ',Extents_used=',(XTNTUSED,FL1,L),                      X
               INTO=WK265
         ST    R15,LENGTH              no, exit
         LA    R14,=C'VREPLACE'        SERVICE
         LA    R15,=C'ZMSG000L'        VARIABLE NAME
         LA    R0,LENGTH               VALUE LENGTH
         LA    R1,WK265                VALUE
         STM   R14,R1,EFTPARM          STORE PARM LIST
         OI    EFTPARM+12,X'80'        MARK END OF LIST
         L     R15,=A(ISPLINK)         ISPF SERVICES
         LA    R1,EFTPARM              PARM LIST ADDRESS
         BALR  R14,R15                 ISSUE VREPLACE
         LTR   R15,R15                 WENT OK?
         BNZ   SETMSG99                NO, QUIT
*        ISPLINK SETMSG(ISPZ000)
SETMSG80 LA    R14,=C'SETMSG'          SERVICE
         LA    R15,=C'ISPZ000 '        VARIABLE NAME
         STM   R14,R1,EFTPARM          STORE PARM LIST
         OI    EFTPARM+04,X'80'        MARK END OF LIST
         L     R15,=A(ISPLINK)         ISPF SERVICES
         LA    R1,EFTPARM              PARM LIST ADDRESS
         BALR  R14,R15                 ISSUE VREPLACE
         LTR   R15,R15                 WENT OK?
         BZ    FINISH                  YES, EXIT
SETMSG99 EQU   *
         B     ISSUEMSG                VREPLACE/SETMSG failed
***********************************************************************
*        ISSUE MESSAGES                                               *
***********************************************************************
MSG08    STRING 'MSG008 COMPRESS failed for ',(DSNAME,,T),             X
               ', IEBCOPY return code is ',(RETCODE,F,L),'.',          X
               INTO=MESSAGE
         B     ISSUEMSG                ISSUE MESSAGE
MSG26    ST    R15,RETCODE             RETURN CODE
         STRING 'MSG26 LOCATE failed for ',(DSNAME,,T),                X
               ', return code is ',(RETCODE,F,L),'.',                  X
               INTO=MESSAGE
         B     CHECKSNG                ISSUE MESSAGE IF MODE=SINGLE
MSG26B   STRING 'MSG26B ',(DSNAME,,T),' is not a disk data set.',      X
               INTO=MESSAGE
         B     CHECKSNG                ISSUE MESSAGE IF MODE=SINGLE
MSG26C   STRING 'MSG26C ',(DSNAME,,T),' is a migrated data set.',      X
               INTO=MESSAGE
         B     CHECKSNG                ISSUE MESSAGE IF MODE=SINGLE
MSG28    ST    R15,RETCODE             RETURN CODE
         STRING 'MSG28 OBTAIN failed for ',(DSNAME,,T),                X
               ', return code is ',(RETCODE,F,L),'.',                  X
               INTO=MESSAGE
         B     CHECKSNG                ISSUE MESSAGE IF MODE=SINGLE
MSG28B  STRING 'MSG28B ',(DSNAME,,T),' is not a partitioned data set.',X
               INTO=MESSAGE
         B     CHECKSNG                ISSUE MESSAGE IF MODE=SINGLE
MSG28C   STRING 'MSG28C COMPRESS bypassed for ',(DSNAME,,T),           X
               ', EXPDT is non-zero.',INTO=MESSAGE
         B     ISSUEMSG                ISSUE MESSAGE
MSG28D   STRING 'MSG28D COMPRESS bypassed for ',(DSNAME,,T),           X
               ', same ASM2ID.',INTO=MESSAGE
         B     ISSUEMSG                ISSUE MESSAGE
MSG28E   STRING 'MSG28E ',(DSNAME,,T),' is a PDSE.',INTO=MESSAGE
         B     ISSUEMSG                ISSUE MESSAGE              @PDSE
MSG28F   STRING 'MSG28F COMPRESS bypassed for ',(DSNAME,,T),           X
               ', data set changed indicator off.',                    X
               INTO=MESSAGE
         B     ISSUEMSG                ISSUE MESSAGE
MSG99    BAL   R14,DAIRFAIL            issue dynalloc error message
****     STRING 'MSG99 DYNALLOC FAILED FOR ',(DSNAME,,T),              X
               ', return code is ',(RETCODE,F,L),'.',                  X
               INTO=MESSAGE
****     B     ISSUEMSG                ISSUE MESSAGE
         B     FINISH                  EXIT (DAIRFAIL issued message)
CHECKSNG CLI   MODE,C'S'               CHECK MODE
         BNE   PRINT000                JUMP IF MODE ^= SINGLE
         MVI   MAXCC+L'MAXCC-1,08      RETURN CODE = 08
ISSUEMSG BAL   R14,PUTLINE             write message
***********************************************************************
*        READ IEBCOPY SYSPRINT IF THERE IS AN ERROR                   *
***********************************************************************
PRINT000 CLI   PRINTMSG,C'Y'           CHECK IEBCOPY ERRORS
         BNE   NEXTDSN                 JUMP IF RETCODE IS ZERO
         LA    R2,WK265                DCB SLOT
         USING IHADCB,R2
         MVC   IHADCB(DCBLEN),MODELDCB MOVE MODEL DCB
         MVC   DCBDDNAM,IEBDDPRT       MOVE SYSPRINT DDNAME
         LA    R0,PRINT090             EODAD ROUTINE
         ST    R0,DCBEODAD             STORE IT INTO DCB
         ST    R2,OPENLIST             BUILD OPEN LIST
         MVI   OPENLIST,X'80'          OPEN MODE IS INPUT
         OPEN  MF=(E,OPENLIST)         OPEN SYSPRINT
         LTR   R15,R15                 CHECK OPEN OK
         BNZ   FINISH                  OPEN FAILED, EXIT
PRINT040 LA    R15,WK265+128           RMODE24 WORK AREA
         MVC   0(PUT24L,R15),PUT24     MOVE GET MACRO TO 24-BIT STORAGE
         BASSM R14,R15                 DO GET
         CLI   0(R1),C'1'              CHECK CONTROL CHARACTER
         BE    PRINT040                IGNORE TITLE LINE
         STRING 'MSG062 ',(1(R1),120),INTO=MESSAGE
         BAL   R14,PUTLINE             write message
         B     PRINT040                LOOP UNTIL EODAD IS REACHED
PRINT090 CLOSE MF=(E,OPENLIST)         CLOSE SYSPRINT
         FREEPOOL IHADCB               FREE BUFFERS
         DROP  R2                      WAS IHADCB
***********************************************************************
*        GET READY TO PROCESS NEXT DSNAME IN TABLE.                   *
***********************************************************************
NEXTDSN  CLI   MODE,C'S'               CHECK MODE
         BE    FINISH                  EXIT IF MODE=SINGLE
         CLI   ATTNFLAG,C'Y'           ATTENTION RECEIVED?
         BE    FINISH                  EXIT IF ATTENTION RECEIVED
*
         L     R2,ADDRCOMM             ECB/CIB POINTERS
         LM    R1,R2,0(R2)             PICK UP ECB/CIB ADDRESSES
         TM    0(R1),X'40'             COMM ECB POSTED?
         BNO   NEXTDSN4                NO, CONTINUE
         CLI   CIBVERB-CIB(R2),CIBSTOP STOP COMMAND ENTERED?
         BNE   NEXTDSN4                NO, CONTINUE
         STRING 'MSG60 COMPRESS terminated, STOP command received.',   X
               INTO=MESSAGE
         BAL   R14,PUTLINE             write message
         LA    R15,20                  RETURN CODE
         ST    R15,RETCODE             RETURN CODE
         B     FINISH                  EXIT IF STOP RECEIVED
*
NEXTDSN4 LA    R14,45                  LENGTH OF AN ENTRY
         L     R15,CTGWKA              WORK AREA ADDRESS
         A     R15,4(,R15)             POINT TO END OF WORK AREA
         BCTR  R15,0
         BXLE  R8,R14,PROCESS          PROCESS ANOTHER DATA SET
         B     FREEWKA                 CLEAN-UP, RETURN
ATTNEXIT MVI   ATTNFLAG,C'1'           SHOW ATTENTION KEY PRESSED
         BR    R14
***********************************************************************
*        CLEAN UP, RETURN.                                            *
***********************************************************************
MSGGETM  STRING 'MSG63 GETMAIN failed, increase region.',INTO=MESSAGE
         BAL   R14,PUTLINE             issue message
         MVI   MAXCC+L'MAXCC-1,20      RETURN CODE = 20
FREEWKA  ICM   R1,B'1111',CTGWKA       WORK AREA ADDRESS
         BZ    FINISH                  JUMP IF NOT ALREADY GOTTEN
         L     R0,0(,R1)               WORK AREA LENGTH
         FREEMAIN RU,LV=(0),A=(1)      FREE GENERIC LOCATE WORK AREA
FINISH   L     R0,=A(DYNAML)           GET LENGTH OF DYNAMIC AREA
         LR    R1,R13                  ADDRESS OF DYNAMIC AREA
         L     R2,MAXCC                HIGHEST IEBCOPY RETURN CODE
         L     R13,4(,R13)             CALLER'S SAVE AREA
         FREEMAIN RU,LV=(0),A=(1)      FREE DYNAMIC STORAGE AREA
         LR    R15,R2                  PASS RETURN CODE
         RETURN (14,12),RC=(15)        GOBACK TO CALLER
***********************************************************************
*        Write a message using IKJEFF02                               *
***********************************************************************
PUTLINE  LA    R0,L'MESSAGE            LENGTH OF INSERT
         ST    R0,MTLEN+00             LENGTH OF INSERT
         LA    R0,MESSAGE              RETURN CODE
         ST    R0,MTADDR+00            ADDRESS OF INSERT
         MVC   MTMSGID,=C'PRT '        MESSAGE ID
         L     R15,CVTPTR              ADDR OF CVT
         L     R15,CVTEFF02-CVTMAP(,R15) ADDR OF IKJEFF02
         LA    R1,MTPARML              POINT TO PPL
         BR    R15                     XCTL to IKJEFF02
***********************************************************************
*        INITIALIZE A DYNALLOC REQUEST BLOCK                          *
***********************************************************************
INIT99RB LA    R2,WK265                S99RB
         XC    0(S99RBEND-S99RB,R2),0(R2)  CLEAR DYNALLOC WORK SPACE
         USING S99RB,R2
         MVI   S99RBLN,S99RBEND-S99RB  RB LENGTH
         MVI   S99VERB,S99VRBAL        VERB IS "ALLOCATE"
         LA    R0,S99RBEND             END OF RB, START OF T.U. PTRS
         ST    R0,S99TXTPP             TEXT UNIT POINTERS
         ST    R2,DFS99RBP             S99RBPTR
         OI    DFS99RBP,X'80'          S99RBPND
         LA    R0,=AL1(0,DFSVC99)      PUTLINE ONLY, SVC99
         ST    R0,DFIDP                BUILD PARML FOR DAIRFAIL
         ST    R11,DFCPPLP             ADDR OF CPPL
         BR    R14
***********************************************************************
*        Invoke the DAIRFAIL routine
***********************************************************************
DAIRFAIL ST    R15,RETCODE             RETURN CODE
         LA    R0,RETCODE              RETURN CODE
         ST    R0,DFRCP                RETURN CODE
         LA    R0,=A(0)                NO ADDR FOR IKJEFF02
         ST    R0,DFJEFF02             RETURN CODE
         LOAD  EP=IKJEFF18             CALL DAIRFAIL
         LR    R15,R0                  pass EP addr
         LA    R1,DFPARMS              DAIRFAIL PARM LIST
         BR    R15                     CALL DAIRFAIL
***********************************************************************
*        PUT MACRO MOVED TO RMODE24 STORAGE
***********************************************************************
PUT24    ST    R14,0(,R13)             SAVE RETURN ADDRESS
         L     R1,OPENLIST             GET DCB ADDRESS
         PUT   (1)                     LOCATE RECORD
         L     R14,0(,R13)             LOAD RETURN ADDRESS
         BSM   0,R14                   GOBACK WITH AMODE24
PUT24L   EQU   *-PUT24
***********************************************************************
*        MODEL CONTROL BLOCKS, MOVED TO DYNAMIC STORAGE               *
***********************************************************************
CAML26   CAMLST NAME,*-*,,*-*          CAMLST FOR LOCATE
CAML28F1 CAMLST SEARCH,*-*,*-*,*-*     CAMLST FOR OBTAIN - F1-DSCB
CAML28F3 CAMLST SEEK,*-*,*-*,*-*       CAMLST FOR OBTAIN - F3-DSCB
ENQMODEL RESERVE (*-*,*-*,E,44,SYSTEMS),UCB=*-*,MF=L
MODELDCB DCB   DSORG=PS,MACRF=(GL,PL)
DCBLEN   EQU   *-MODELDCB
***********************************************************************
*        DEFINE MESSAGES TO BE ISSUED VIA IKJEFF02                    *
***********************************************************************
MSGCSECT IKJTSMSG (,),PRT
         IKJTSMSG
***********************************************************************
*        DEFINE INPUT PARAMETERS FOR IKJPARS                          *
***********************************************************************
PCLCSECT IKJPARM
DSNPCE   IKJPOSIT DSTHING,USID,PROMPT='DATA SET NAME'
CHKPCE   IKJKEYWD
         IKJNAME 'CHECK'
         IKJNAME 'NOCHECK'
CHNGPCE  IKJKEYWD
         IKJNAME 'CHANGED'
         IKJNAME 'NOCHANGED'
STATPCE  IKJKEYWD DEFAULT='SHR'
         IKJNAME 'SHR'
         IKJNAME 'OLD'
SETMPCE  IKJKEYWD
         IKJNAME 'SETMSG'
VOLPCE   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=VOLSUBF,ALIAS='VOLSER'
VOLSUBF  IKJSUBF
VOLPCE2  IKJIDENT 'VOLUME',MAXLNTH=6,FIRST=ALPHANUM,OTHER=ALPHANUM
         IKJENDP
***********************************************************************
*        DYNAMIC STORAGE AREA                                         *
***********************************************************************
DYNAM    DSECT
         DS    18F                     SAVE AREA
MAXCC    DS    F                       MAX RETURN CODE FROM IKJEFTSR
ATTNFLAG DS    C                       ATTENTION KEY PRESSED
RECFM    DS    C                       RECFM F/V/U
TEMPALL  DS    C                       TEMPORARY FILES ALLOCATED
MODE     DS    C                       SINGLE, CATALOG, VTOC
VOLGIVEN DS    C                       'Y' IF VOL PARAM SPECIFIED
CHECK    DS    C                       Y/N MEANS CHECK/NOCHECK
CHANGED  DS    C                       Y/N MEANS CHANGED/NOCHANGED
SETMSG   DS    C                       Y/N MEANS SETMSG
PRINTMSG DS    C                       'Y' TO PRINT IEBCOPY MESSAGES
RETCODE  DS    F                       RETURN CODE FROM IKJEFTSR
RSNCODE  DS    F                       REASON CODE FROM IKJEFTSR
ABNDCODE DS    F                       ABEND CODE FROM IKJEFTSR
EXCP     DS    F                       NUMBER OF EXCPS
MINUTES  DS    F                       NUMBER OF MINUTES
SECONDS  DS    F                       NUMBER OF SECONDS
IEBPARM  DS    A(0,IEBDD)              PARM LIST FOR IEBCOPY
IEBDD    DS    Y(IEBDDLEN)
         DS    C'SYSLIN  '           1 SYSLIN
         DS    XL8'00'               2 N/A
         DS    XL8'00'               3 N/A
         DS    C'SYSLIB  '           4 SYSLIB
IEBDDIN  DS    C'SYSIN   '           5 SYSIN
IEBDDPRT DS    C'SYSPRINT'           6 SYSPRINT
         DS    C'SYSPUNCH'           7 SYSPUNCH
IEBDDUT1 DS    C'SYSUT1  '           8 SYSUT1
IEBDDUT2 DS    C'SYSUT2  '           9 SYSUT2
         DS    C'SYSUT3  '          10 SYSUT3
         DS    C'SYSUT4  '          11 SYSUT4
IEBDDLEN EQU   *-(IEBDD+2)
DYNENQL  RESERVE (SYSLMOD,DSNAME,E,44,SYSTEMS),UCB=0,MF=L
DYNANSWR DS    F                       ANSWER AREA FOR PARSE
DYNUWA   DS    4F                      USER WORK AREA FOR PARSE
DYNPPL   DS    8F                      PPL FOR USE WITH PARSE
DYNECB   DS    F                       ECB FOR USE WITH PARSE
STATUS   DS    A                       STATUS KEY, SHR OR OLD
OPENLIST OPEN  IHADCB,MF=L             OPEN LIST
EXTRACTL EXTRACT ADDRTIOT,FIELDS=TIOT,MF=L
ADDRTIOT DS    A(TIOT)                 TIOT ADDRESS
ADDRCOMM DS    A                       COMM AREA ADDRESS
ADDRUCB  DS    A(UCBOB)                UCB ADDRESS FOR RESERVE
DEVT1    DS    5F                      DEVICE CHARACTERISTICS
EFTPARM  DS    8A                      PARM LIST FOR IKJEFTSR
DDNAME   DS    CL8                     DD NAME
TU02DSN  DS    AL2(DALDSNAM,1,44)   +0 DATA SET NAME KEY
DSNAME   DS    CL44                 +6 DATA SET NAME
TU10VOL  DS    AL2(DALVLSER,1,6)    +0 VOLSER KEY
VOLSER   DS    CL6                  +6 VOLUME SERIAL
TU55DDN  DS    AL2(DALRTDDN,1,8),CL8   RETURN DDNAME
LENGTH   DS    F                       message length
TRKSUSED DS    F,F                     tracks used (after,before)
XTNTUSED DS    FL1                     number of extents used
DSNLENGTH DS   H                       length of dsname
ZDLDSN   DS    CL44                    DSLIST dsname
ZDLVOL   DS    CL6                     DSLIST volser
         IKJEFFMT MTDSECT=NO,MTFORMAT=NEW
         IKJEFFDF DFDSECT=NO,DFDSEC2=NO
*@@      IEZCTGPL DSECT=NO             CATALOG PARAMETER LIST
CTGPL    DS    0F
CTGOPTN1 DS    B              FIRST OPTION BYTE:
CTGNAME  EQU   X'04' .... .1..  CTGENT CONTAINS DSNAME
CTGGENLD EQU   X'01' .... ...1  GENERIC LOCATE REQUEST
CTGOPTN2 DS    B              SECOND OPTION BYTE
CTGOPTN3 DS    B              THIRD OPTION BYTE
CTGSUPLT EQU   X'10' ...1 ....  SUPERLOCATE FUNCTION
CTGAM0   EQU   X'01' .... ...1  OS/VS2 CATALOG MANAGMENT REQUEST
CTGOPTN4 DS    B              FOURTH OPTION BYTE
CTGENT   DS    A              ADDRESS OF CATALOG RECORD IDENTIFIER
CTGCAT   DS    A              ADDRESS OF CATALOG DSNAME OR ACB
CTGWKA   DS    A              ADDRESS OF CALLER'S WORK AREA
CTGOPTNS DS    B              CATALOG MANAGMENT SERVICES REQUEST OPTION
CTGF2WKA EQU   X'04' 0000 01..  FORMAT-2 WORK AREA
         DS    B
CTGTYPE  DS    C              TYPE OF CATALOG RECORD:
CTGTALIN EQU   C'A'             NON-VSAM DATA SET
CTGNOFLD DS    FL1            NUMBER OF ENTRIES IN CTGFIELD
CTGFDBK  DS    XL2            FEEDBACK AREA
CTGFBFLG DS    B,B            FLAGS (SUPERLOCATE)
         DS    2A             UNUSED HERE
CTGPLLEN EQU   *-CTGPL
WK265    DS    XL265                   WORK AREA FOR LOCATE
MESSAGE  DS    CL100                   WORK AREA IKJEFF02
         ORG   DYNAM+(((*+15-DYNAM)/16)*16)
         IECSDSL1 (1,3)                FORMAT-1 AND FORMAT-3 DSCB MAPS
         AIF   (D'DS1SMSFG).SMS2                                  @PDSE
         ORG   IECSDSL1+X'4E'                                     @PDSE
DS1SMSFG DS    XL1                 SYSTEM MANAGED STORAGE INDICATORS
DS1SMSDS EQU   X'80'  1... ....    SYSTEM MANAGED DATA SET
DS1PDSE  EQU   X'08'  .... 1...    DATA SET IS A PDSE
         ORG   ,                                                  @PDSE
.SMS2    ANOP                                                     @PDSE
DYNAML   EQU   *-DYNAM                 LENGTH OF WORK AREA
*
*              MACROS FROM SYS1.MACLIB
*
         IKJPPL                        PARSE PARAMETER LIST
         IKJCPPL                       COMMAND PROCESSOR PARM LIST
         IEFZB4D0                      DYNALLOC REQUEST BLOCK
         IEFZB4D2                      DYNALLOC TEXT UNIT KEYS
         DCBD  DSORG=PS,DEVD=DA        DCB DSECT
         YREGS                         REGISTER EQUATES
*
*              MACROS FROM SYS1.MODGEN
*
TIOT     DSECT
         IEFTIOT1                      TIOT DSECT
*@@@     IEFUCBOB                      UCB DSECT
UCBOB    DSECT
UCBTBYT2 EQU   *+17,1                  UCB TYPE BYTE 2
UCBRR    EQU   X'20'                   RESERVE/RELEASE (SHARED DASD)
UCBTBYT3 EQU   *+18,1                  UCB TYPE BYTE 3
UCB3DACC EQU   X'20'                   DASD DEVICE CLASS
CIB      DSECT
         IEZCIB                        COMMAND INPUT BUFFER
         CVT   DSECT=YES,LIST=NO       CVT
         IKJTSVT                       TSO VECTOR TABLE
         STRING GENERATE
         END   COMPRESS
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//         DD DISP=SHR,DSN=GILBERT.FILE183.PDS(STRING)
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,5)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//LKED    EXEC PGM=HEWL,PARM=(LIST,MAP,RENT)
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,DELETE)
//         DD *
 INCLUDE ISPLOAD(ISPLINK)
//ISPLOAD  DD DISP=SHR,DSN=ISP.SISPLOAD
//SYSLMOD  DD DISP=SHR,DSN=GILBERT.LOAD(COMPRESS)
//SYSPRINT DD SYSOUT=*
//*
//GO      EXEC PGM=IKJEFT01,REGION=2M
//STEPLIB  DD DISP=SHR,DSN=*.LKED.SYSLMOD
//SYSTSIN  DD *
COMPRESS 'IBMUSER.EXEC' NOCHECK
/*
COMPRESS 'GILBERT.*' NOCHECK OLD
//SYSTSPRT DD SYSOUT=*
//SYSPRINT DD SYSOUT=*
//ABENDAID DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
//


/*
/*        THIS IS THE "COMPRESS" EDIT MACRO.   21JAN89
/*
ISREDIT MACRO (DSN BATCH)
IF &STR(&DSN) = &STR() OR &STR(&DSN) = BATCH THEN -
  DO
    SET BATCH = &STR(&DSN)                   /* SAVE POSSIBLE BATCH OPT
    ISREDIT (ID1) = DATAID
    ISPEXEC LMQUERY DATAID(&ID1) DATASET(DSN) VOLUME(VOL) -
                    PROJECT(PROJECT) GROUP1(GROUP1) TYPE(TYPE)
    SET DSN     = &DSN                       /* TRUNCATE TRAILING SPACES
    SET PROJECT = &PROJECT                   /* TRUNCATE TRAILING SPACES
    SET GROUP1  = &GROUP1                    /* TRUNCATE TRAILING SPACES
    IF &STR(&DSN) = &STR() THEN -
      SET DSN   = &PROJECT..&GROUP1..&TYPE
    ELSE -
      DO
        IF &SUBSTR(1:1,&DSN) NE &STR(') THEN -
          IF &SYSPREF NE &STR() THEN SET DSN = '&SYSPREF..&DSN'
        IF  &VOL ^= &STR() THEN SET &VOL = VOLUME(&VOL)
      END
  IF &BATCH ^= BATCH THEN -
    DO
      SET ZEDSMSG = COMPRESS IN PROGRESS
      ISPEXEC CONTROL DISPLAY LOCK
      ISPEXEC DISPLAY MSG(ISRZ001)
      SET &SYSOUTTRAP = 1
      COMPRESS &DSN &VOL              /* INVOKE THE "COMPRESS" COMMAND
      IF &LASTCC = 0 THEN -
        DO
          SET &ZEDLMSG = &SYSOUTLINE1
          SET &I = &SYSINDEX(NOW,&STR(&ZEDLMSG))
          SET ZEDSMSG = &SUBSTR(&I:&LENGTH(&ZEDLMSG)-1,&ZEDLMSG)
          ISPEXEC SETMSG MSG(ISRZ001)
        END
      ELSE -
        WRITE &SYSOUTLINE1            /* ERROR MESSAGE
    END
  END
IF &BATCH = BATCH THEN -
  DO
    SETVAR1                        /* RETRIEVE ACCT, NAME  */
    SET JOBNM = &SUBSTR(1:8,&SYSUID.CPR   )
    SUBMIT * END(ZZ)
    //&JOBNM JOB &ACCOUNT,'&PRGNM',
    // NOTIFY=&SYSUID,CLASS=Z,MSGCLASS=X,COND=(4,LT)
    &STR(//*) COMPRESS DSN=&DSN
    &STR(//*) SUBMITTED &SYSDATE &SYSTIME
    //COMPRESS EXEC PGM=IKJEFT01,REGION=2M
    //SYSTSPRT DD  SYSOUT=*
    //SYSTSIN  DD  *
    COMPRESS &DSN &VOL
    //COND99  EXEC PGM=CANMSGCL,PARM=1 PURGE THE JOB AFTER 1 HOUR
    //STEPLIB  DD  DISP=SHR,DSN=SYS2.LINKLIB    APF LIB
    ZZ
  END
ELSE -
  COMPRESS &DSN                     /* USER SPECIFIED A DATA SET NAME



   >>>>>>>>> The REXX EDIT macro is in the COMPRESS member <<<<<<<<<

