//GILBERTH JOB (ACCT#),SCANMODL,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMH EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MODULE NAME = SCANMODL                                              *
*                                                                     *
* DESCRIPTIVE NAME = RETRIEVE OFFSET & LENGTH OF A JES2 FIELD         *
*                                                                     *
* FUNCTION = THIS MODULE LOADS AN IPCS MODEL FROM SYS1.MIGLIB AND     *
*            RETURNS THE OFFSET & LENGTH OF A GIVEN FIELD.            *
*            IT IS USED TO WRITE PROGRAMS THAT ACCESS JES2 CONTROL    *
*            BLOCKS WITHOUT USING HASPSRC AND CONSEQUENTLY,           *
*            ARE RELEASE-INDEPENDENT.                                 *
*                                                                     *
* STATUS = R100                                                       *
*                                                                     *
* AUTHOR = GILBERT SAINT-FLOUR <GSF@POBOX.COM>                        *
*                                                                     *
* INVOCATION = VIA CALL OR LINK                                       *
*                                                                     *
*          LINK EP=SCANMODL,PARAM=(=C'HASMSDB ',=C'SDBRECCT')         *
*          LH   R2,0(,R1)                 OFFSET                      *
*          LH   R3,2(,R1)                 LENGTH                      *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = MVS/ESA V4.1 OR ABOVE                             *
*                                                                     *
*    AUTHORIZATION = NONE                                             *
*                                                                     *
* MODULE TYPE = PROCEDURE, (SUB-ROUTINE)                              *
*                                                                     *
*    PROCESSOR = IBM OS/ASSEMBLER H VERSION 2 OR                      *
*                IBM HIGH LEVEL ASSEMBLER/MVS                         *
*                                                                     *
*    MODULE SIZE = 4K                                                 *
*                                                                     *
*    ATTRIBUTES = REENTERABLE, RMODE ANY, AMODE 31,                   *
*                 PROBLEM STATE, TCB KEY                              *
*                                                                     *
* OPERATION = SEE DOCUMENTATION                                       *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*   @100 INITIAL VERSION                                              *
*                                                                     *
***********************************************************************
SCANMODL CSECT
SCANMODL RMODE ANY
         B     BEGIN-*(,R15)
         DC    C'GSF UTILITIES - SCANMODL R100 '
BEGIN    BAKR  R14,0
         LR    R11,R15
         USING SCANMODL,R11
         LM    R8,R9,0(R1)             R8=HASMSDB,R9=SDBRECCT
         L     R1,16                   CVTPTR
         L     R1,8(,R1)               CVTLINK
         LOAD  EPLOC=(R8),DCB=(R1),ERRET=RC12
         LR    R4,R0                   IM$SDB
         USING HEADER,R4
         SLR   R1,R1
         IC    R1,HDROFFSET            OFFSET TO 1ST FIELD DESC
         LA    R5,HEADER(R1)           ADDR OF 1ST FIELD DESC
         USING FLDDESC,R5
*LOOP
LOOP5    CLC   FLDNAME,0(R9)           REQUESTED FIELD?
         BE    LOOP5X                  YES, EXIT
         LA    R5,FLD_NEXT             NEXT FIELD DESCRIPTOR
         CLI   FLDDESC,0               END OF MODEL?
         BNE   LOOP5                   NO, PROCESS NEXT FIELD
*ENDLOOP
         LA    R15,8                   RC=8  (FIELD NOT FOUND)
         PR
*
*        RETURN OFFSET AND LENGTH IN SAVEAREA+8
*
LOOP5X   LA    R1,8(,R13)              4-BYTE WORK AREA CALLER'S AMODE
         MVC   0(4,R1),FLDOFFSET       MOVE OFFSET & LENGTH
         SLR   R15,R15                 RC=0
         PR
*
RC12     LA    R15,12                  RC=12 (LOAD FAILED)
         PR
*        BLSQCBM                   CONTROL BLOCK MODEL USED BY BLSQFORM
HEADER   DSECT
HDRACRO  DS    CL8                     EBCDIC CB ACRONYM
HDRUTHDR DS    CL8'$SDB'               OUTPUT HEADER
HDRMLVL  DS    CL8'HJE4430'            MAINTENANCE LEVEL
HDRCBLEN DS    FL2                     LENGTH OF CONTROL BLOCK
HDRAOFF  DS    FL2                     OFFSET TO ACRONYM
HDRALEN  DS    FL1                     LENGTH OF ACRONYM
HDRLLEN  DS    FL1                     LABEL LENGTH
HDRSCOL  DS    FL1                     START COLUMN OR LEFT MARGIN
HDRNCOL  DS    FL1                     NUMBER OF COLS BETWEEN LABELS
HDRFLAGS DS    XL1                     NUMBER OF COLS BETWEEN LABELS
HDROFFSET DS   FL1                     OFFSET TO 1ST FIELD DESC
HDRMODLVL DS   FL1                     MODEL LEVEL
          DS   X                       RESERVED
*
FLDDESC  DSECT
FLDNAME  DS    CL8                0    FIELD NAME
FLDFLAGS DS    X,X                8    FLAGS
FLDVIEW  DS    XL2               10    VIEW
FLDOFFSET DS   FL2               12    OFFSET
FLDLENGTH DS   FL2               14    LENGTH
FLD_NEXT EQU   *                 16
         YREGS
         END
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//LKED    EXEC PGM=IEWL,PARM=RENT
//SYSLMOD  DD DISP=SHR,DSN=GILBERT.LOADLIB(SCANMODL)
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,DELETE)
//SYSPRINT DD SYSOUT=*
//*
//ASMH2 EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
TEMPNAME CSECT
TEMPNAME CSECT
         BALR  R12,0
         USING *,R12
         LINK  EP=SCANMODL,PARAM=(HASMSDB,SDBRECCT)
         LR    R0,R15                  RETURN CODE
         L     R15,0(,R1)              LENGTH/OFFSET
         ABEND (R0),DUMP,REASON=(15)
HASMSDB  DC    C'HASMSDB '
SDBRECCT DC    C'SDBRECCT'
         YREGS
         END
//SYSLIB   DD DISP=SHR,DSN=SYS1.MACLIB
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//GO      EXEC PGM=LOADER,PARM=NOPRINT
//STEPLIB  DD DISP=SHR,DSN=GILBERT.LOADLIB
//SYSLIN   DD DSN=*.ASMH2.SYSLIN,DISP=(OLD,DELETE)
//SYSPRINT DD SYSOUT=*
//SYSDEBUG DD SYSOUT=*
//ABNLIGNR DD DUMMY
//SYSUDUMP DD SYSOUT=*
