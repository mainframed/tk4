//GILBERTI JOB (ACCT#),ISPCMDSJ,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//**********************************************************************
//*                                                                    *
//*    THIS JOB CREATES TCNAMS.TABLES(ISPCMDS) BY MERGING A SET        *
//*    OF IN-LINE COMMANDS WITH THE ORIGINAL ISPF COMMAND TABLE.       *
//*                                                                    *
//**********************************************************************
//ISPCMDS EXEC PGM=IKJEFT01
//*---------------------------------------------------------------------
//*
//*        UPDATE THESE DSNAMES FOR YOUR SITE
//*
//*---------------------------------------------------------------------
//ISPTLIB  DD DISP=SHR,DSN=ISP.SISPTENU              <== INPUT
//*
//ISPTABL  DD DISP=SHR,DSN=GILBERT.TABLES            <== OUTPUT
//*
//ISPMLIB  DD DISP=SHR,DSN=ISP.SISPMENU              <== MESSAGES
//*---------------------------------------------------------------------
//*
//*       THESE ARE THE NEW COMMANDS THAT WILL BE MERGED
//*       INTO THE NEW ISPCMDS TABLE.
//*
//*---------------------------------------------------------------------
//UPDATES DD *
PDF      0 SELECT PANEL(ISR@PRIM) OPT(&ZPARM) NEWAPPL(ISR)
           PDF MAIN MENU
ED       0 SELECT PGM(FASTPATH) PARM(EDIT,&ZPARM)
           FAST PATH COMMAND: EDIT
BR       0 SELECT PGM(FASTPATH) PARM(BROWSE,&ZPARM)
           FAST PATH COMMAND: BROWSE
VI       0 SELECT PGM(FASTPATH) PARM(VIEW,&ZPARM)
           FAST PATH COMMAND: VIEW
WP       0 SELECT PGM(FASTPATH) PARM(WP,&ZPARM)
           FAST PATH COMMAND: ISPF WORKPLACE
ICS      0 SELECT PGM(FASTPATH) PARM(ICS,&ZPARM)
           FAST PATH COMMAND: ICS
FSHELP   0 SELECT PGM(FASTPATH) PARM(FSHELP,&ZPARM)
           FULL-SCREEN HELP
MACLIB   3 SELECT PGM(FASTPATH) PARM(MACLIB,&ZPARM)
           FAST PATH COMMAND: VIEW MACLIB/MODGEN MACRO
SDSF     2 SELECT PGM(FASTPATH) PARM(SDSF,&ZPARM)
           FAST PATH COMMAND: SDSF
UTIL     2 SELECT PGM(FASTPATH) PARM(UTIL,&ZPARM)
           FAST PATH COMMAND: PDF UTILITIES
WHOAMI   3 SELECT CMD(%WHOAMI &ZPARM)
           DISPLAY SYSTEM ID AND USER ID IN LARGE LETTERS
RECEIVE  4 SELECT CMD(RECEIVE &ZPARM) MODE(FSCR)
           TSO INTERACTIVE TRANSMIT/RECEIVE FACILITY
IND$FILE 0 SELECT CMD(IND$FILE &ZPARM) MODE(FSCR)
           PC/3270
LISTALC  5 SELECT PGM(PGMEDBRO) PARM(L,AMSLISTA,AMSLISTA)
           FULL SCREEN TSO LISTALC
LISTBC   5 SELECT CMD(LISTBC &ZPARM) MODE(FSCR)
           FULL SCREEN TSO LISTBC
SEND     0 SELECT CMD(%AMSSEND &ZPARM) NEWAPPL(ISR)
           FULL SCREEN TSO SEND
XMIT     0 SELECT CMD(%AMSXMIT &ZPARM) NEWAPPL(ISR)
           FULL SCREEN TSO XMIT
MAIL     0 SELECT CMD(%NEWAPPL MAIL) NEWAPPL(MAIL)
           TSO/MAIL
HANDBOOK 5 SELECT PGM(FASTPATH) PARM(B,IBMUSER.HANDBOOK(&ZPARM))
           ON-LINE MACRO LISTING
VOLS     0 SELECT CMD(%AMSVOLS &ZPARM) NEWAPPL(ISR)
           VOLS COMMAND
PDS      0 SELECT CMD(%NEWAPPL PDS)
           PDS UTILITY
CZX      0 SELECT CMD(%NEWAPPL CORTEX &ZPARM) NEWAPPL(CZX)
           CORTEX PRIMARY MENU
RMFMON   3 SELECT CMD(RMFMON &ZPARM) MODE(FSCR)
           RMF
ENQ      0 SELECT CMD(ENQ) MODE(FSCR)
           ENQ COMMAND
USL      0 SELECT PGM(PGMEDBRO) PARM(E,USL,&ZPARM,STD)
           EDIT A USERLIB MEMBER
DOC      0 SELECT PGM(PGMEDBRO) PARM(E,DOC,&ZPARM,TEXT)
           EDIT A DOCLIB MEMBER
TXT      0 SELECT PGM(PGMEDBRO) PARM(E,TXT,&ZPARM,STD)
           EDIT A DTXTPRMT MEMBER
PROC     0 SELECT PGM(PGMEDBRO) PARM(E,PRC,&ZPARM,STD)
           EDIT A IBMUSER.SITE.MJCLPRC MEMBER
TEL      0 SELECT PGM(PGMEDBRO) PARM(B,USL,TEL)
           BROWSE THE TELEPHONE DIRECTORY
MEMOS    4 SELECT PGM(PGMEDBRO) PARM(E,MEMO,&ZPARM,TEXT)
           EDIT A MEMO
RDR      0 SELECT PGM(PGMEDBRO) PARM(E,RDR,&ZPARM,STD)
           EDIT A DJCLRDR MEMBER
IPT      0 SELECT PGM(PGMEDBRO) PARM(E,IPT,&ZPARM,STD)
           EDIT A DJCLIPT MEMBER
TIME     0 SELECT CMD(%AMSTIME)
           DISPLAY TIME AND DATE
CNTL     0 SELECT CMD(%AMSCNTL &ZPARM) NEWAPPL(ISR)
           EDIT ISPCTL0 DATA SET, EVEN IF VIO
LMCOPY   3 SELECT PANEL(LMCOPY) NEWAPPL(ISR)
           ISSUE LMCOPY COMMAND
CZXMSG   0 SELECT PGM(CZX2PMID) PARM(&ZPARM) NOFUNC
           DISPLAY MVS-MS MESSAGE
PGM      0 SELECT CMD(%XYZ2L053 &ZPARM) NEWAPPL(CZX)
           INVOKE PROGRAM MANAGEMENT PANEL OUTSIDE MVS-MS
IPCS     4 SELECT CMD(%NEWAPPL IPCS) NEWAPPL(BLSG)
           INVOKE IPCS COMMAND
ISMF     0 SELECT CMD(%NEWAPPL ISMF) NEWAPPL(DGT)
           INVOKE ISMF DIALOGS
RACF     0 SELECT CMD(%NEWAPPL RACF) NEWAPPL(RACF)
           INVOKE RACF DIALOGS
HSM      0 SELECT CMD(%NEWAPPL HSM) NEWAPPL(HSM)
           INVOKE HSM DIALOGS
SMPE     3 SELECT CMD(%NEWAPPL SMPE) NEWAPPL(GIM)
           INVOKE SMP/E DIALOGS
ICQ      0 SELECT CMD(%NEWAPPL ICQ) NEWAPPL(ICQ)
           INVOKE TSO/E DIALOGS
DITTO    3 SELECT CMD(%NEWAPPL DITTO) NEWAPPL(DUT)
           INVOKE DITTO/MVS DIALOGS
TLMS     0 SELECT CMD(%NEWAPPL TLMS) NEWAPPL(TLMS)
           INVOKE TLMS PANELS (NO SPLIT SCREEN)
LIBR     3 SELECT CMD(%NEWAPPL LIBR) NEWAPPL(LIB@)
           INVOKE LIBRARIAN /ELIPS PANELS
ELIPS    3 SELECT CMD(%NEWAPPL LIBR) NEWAPPL(LIB@)
           INVOKE LIBRARIAN /ELIPS PANELS
SHOWMVS  5 SELECT PGM(SHOWMVS) PARM(&ZPARM)
           INVOKE SHOWMVS PROGRAM
JCL      0 SELECT PGM(FASTPATH) PARM(E,&ZUSER..JCL(&ZPARM),STD)
           EDIT "USERID.JCL"
SCRIPT   0 SELECT PGM(ISRFPR) PARM((ISRFP09) 9) NEWPOOL
           ISSUE SCRIPT COMMAND
PLIB     0 SELECT PGM(FASTPATH) PARM(E,&ZUSER..PANELS(&ZPARM),STD)
           EDIT "USERID.PANELS"
SLIB     0 SELECT PGM(FASTPATH) PARM(E,&ZUSER..SKELS(&ZPARM),STD)
           EDIT "USERID.SKELS"
MLIB     0 SELECT PGM(FASTPATH) PARM(E,&ZUSER..MSGS(&ZPARM),STD)
           EDIT "USERID.MSGS"
EXEC     0 SELECT PGM(FASTPATH) PARM(E,&ZUSER..EXEC(&ZPARM),STD)
           EDIT "USERID.EXEC"
TEXT     0 SELECT PGM(FASTPATH) PARM(E,&ZUSER..TEXT(&ZPARM),STD)
           EDIT "USERID.TEXT"
CLIST    0 SELECT PGM(FASTPATH) PARM(E,&ZUSER..CLIST(&ZPARM),STD)
           EDIT "USERID.CLIST"
CA1      0 SELECT CMD(%NEWAPPL CA1) NEWAPPL(TMS)
           INVOKE CA-1 ISPF INTERFACE
EPIC     0 SELECT CMD(%NEWAPPL EPIC) NEWAPPL(EPIC)
           INVOKE EPIC/MVS PANELS
JOBTRAC  4 SELECT CMD(%NEWAPPL JOBTRAC) NEWAPPL(LJT)
           INVOKE JOBTRAC APPLICATION
SAR      0 SELECT CMD(%NEWAPPL SAR &ZPARM) NEWAPPL(SAR)
           INVOKE SYSOUT ARCHIVAL AND RETRIEVAL SYSTEM
OMEGAMON 4 SELECT CMD(%NEWAPPL OMEG)
           OMEGAMON
PCOMMAND 4 SELECT CMD(%NEWAPPL PCOMMAND)
           PREVAIL/XP (ALIAS SYSVIEW, PCOMMAND)
CCCA     0 SELECT CMD(%NEWAPPL CCCA) NEWAPPL(ABJ)
           COBOL CONVERSION
SELECT   0 SELECT &ZPARM
           GSF TEST
NEWAPPL  4 SELECT CMD(%NEWAPPL &ZPARM )
           GSF TEST
PDB      0 SELECT CMD(%APPLPDB SITE(&ZPARM) ) NEWAPPL(CZXG)
           CORTEX-PDB V5R2M1
/*
//*---------------------------------------------------------------------
//SYSTSPRT DD SYSOUT=*
//ISPLOG   DD SYSOUT=*,DCB=(RECFM=F,LRECL=121)
//BEFORE   DD SYSOUT=*
//AFTER    DD SYSOUT=*
//SYSPRINT DD SYSOUT=*
//ISPPROF  DD UNIT=SYSDA,SPACE=(1,(1,1,1)),DISP=(,PASS),
//            DCB=(RECFM=FB,LRECL=80,BLKSIZE=800)
//SYSPROC  DD DISP=(OLD,PASS),DSN=*.ISPPROF,VOL=REF=*.ISPPROF
//SYSUT2   DD DISP=(OLD,PASS),DSN=*.ISPPROF,VOL=REF=*.ISPPROF
//DUMMY    DD DUMMY
//ISPPLIB  DD DISP=(OLD,PASS),DSN=*.ISPPROF,VOL=REF=*.ISPPROF
//ISPSLIB  DD DISP=(OLD,PASS),DSN=*.ISPPROF,VOL=REF=*.ISPPROF
//SYSTSIN  DD  *
CALL 'SYS1.LINKLIB(IEBUPDTE)' 'NEW'
ISPSTART CMD(%ISPCMDS  -
             OLD(ISPCMDS)         /* OLD TABLE FROM DDN(ISPTLIB)   */+
             NEW(ISPCMDS)         /* NEW TABLE CREATED IN ISPTABL  */)
/*
//SYSIN    DD  *
./ ADD NAME=ISPCMDS
PROC 0 OLD() NEW()
CONTROL NOLIST NOFLUSH

ISPEXEC LMINIT DATAID(ISPTLIB) DDNAME(ISPTLIB) ENQ(SHR)
IF &LASTCC > 0 THEN GOTO SPFERROR

ISPEXEC LMINIT DATAID(ISPPROF) DDNAME(ISPPROF) ENQ(EXCLU)
IF &LASTCC > 0 THEN GOTO SPFERROR

ISPEXEC LMINIT DATAID(ISPTABL) DDNAME(ISPTABL) ENQ(EXCLU)
IF &LASTCC > 0 THEN GOTO SPFERROR

ISPEXEC LMCOPY FROMID(&ISPTLIB) FROMMEM(&OLD) -
               TODATAID(&ISPPROF) TOMEM(WORKCMDS) REPLACE
IF &LASTCC > 8 THEN GOTO SPFERROR

ISPEXEC TBOPEN WORKCMDS WRITE LIBRARY(ISPPROF)
IF &LASTCC > 0 THEN GOTO SPFERROR
                                    /**********************************/
OPENFILE BEFORE OUTPUT              /*                                */
                                    /*  DISPLAY TABLE BEFORE UPDATES  */
DO UNTIL (&RC > 0)                  /*                                */
  ISPEXEC TBSKIP WORKCMDS           /**********************************/
  SET RC = &LASTCC
  IF &RC = 0 THEN -
    DO
      ISPEXEC TBGET WORKCMDS ROWID(ROW)
      IF &LASTCC > 0 THEN GOTO SPFERROR
      SET BEFORE = &SUBSTR(1:9,&ZCTVERB          )
      SET BEFORE = &STR(&BEFORE &ZCTTRUNC &SYSNSUB(1,&ZCTACT) )
      PUTFILE BEFORE
      SET BEFORE = &STR(            &SYSNSUB(1,&ZCTDESC ) )
      PUTFILE BEFORE
    END
END
CLOSFILE BEFORE
 /*TROL SYMLIST CONLIST
                                    /**********************************/
ISPEXEC TBBOTTOM WORKCMDS           /*                                */
IF &LASTCC > 0 THEN GOTO SPFERROR   /*   READ UPDATES                 */
                                    /*   ADD THEM TO THE TABLE        */
                                    /*                                */
OPENFILE UPDATES                    /**********************************/
ERROR IF &LASTCC=400 THEN GOTO CLOSEUPD

DO UNTIL (&LASTCC=400)
  GETFILE  UPDATES                  /*       1ST LINE (VERB)          */
  SET ZCTVERB  = &SUBSTR(1:8,&SYSNSUB(1,&UPDATES) )
  SET ZCTVERB  = &ZCTVERB
  SET ZCTTRUNC = &SUBSTR(10:10,&SYSNSUB(1,&UPDATES) )
  SET ZCTACT   = &SUBSTR(12:72,&SYSNSUB(1,&UPDATES)                   )
  SET L = &LENGTH(&SYSNSUB(1,&ZCTACT))
  DO WHILE &SUBSTR(&L:&L,&SYSNSUB(1,&ZCTACT)) = &STR()
    SET L=&L-1
  END
  SET ZCTACT   = &SUBSTR(1:&L,&SYSNSUB(1,&ZCTACT))

  GETFILE  UPDATES                  /*       2ND LINE (DESC)          */
  SET ZCTDESC  = &SUBSTR(12:72,&SYSNSUB(1,&UPDATES)                   )
  SET L = &LENGTH(&SYSNSUB(1,&ZCTDESC))
  DO WHILE &SUBSTR(&L:&L,&SYSNSUB(1,&ZCTDESC)) = &STR()
    SET L=&L-1
  END
  SET ZCTDESC  = &SUBSTR(1:&L,&SYSNSUB(1,&ZCTDESC))
  ISPEXEC TBADD WORKCMDS
  IF &LASTCC > 0 THEN GOTO SPFERROR
END

CLOSEUPD: ERROR OFF                 /*     CANCEL ERROR TRAPPING     */
CLOSFILE UPDATES

ISPEXEC TBTOP WORKCMDS
IF &LASTCC > 0 THEN GOTO SPFERROR
                                    /**********************************/
OPENFILE AFTER OUTPUT               /*                                */
                                    /*  DISPLAY TABLE AFTER UPDATES   */
DO UNTIL (&RC > 0)                  /*                                */
  ISPEXEC TBSKIP WORKCMDS           /**********************************/
  SET RC = &LASTCC
  IF &RC = 0 THEN -
    DO
      ISPEXEC TBGET WORKCMDS ROWID(ROW)
      IF &LASTCC > 0 THEN GOTO SPFERROR
      SET AFTER = &SUBSTR(1:9,&ZCTVERB           )
      SET AFTER = &STR(&AFTER &ZCTTRUNC &SYSNSUB(1,&ZCTACT) )
      PUTFILE AFTER
      SET AFTER  = &STR(            &SYSNSUB(1,&ZCTDESC) )
      PUTFILE AFTER
    END
END
CLOSFILE AFTER

ISPEXEC TBCLOSE WORKCMDS LIBRARY(ISPPROF)
IF &LASTCC > 0 THEN GOTO SPFERROR

ISPEXEC LMCOPY FROMID(&ISPPROF) FROMMEM(WORKCMDS) -
               TODATAID(&ISPTABL) TOMEM(&NEW) REPLACE
IF &LASTCC > 8 THEN GOTO SPFERROR

EXIT CODE(0)

SPFERROR: +
  IF &ZERRMSG ^= &STR() THEN ISPEXEC LOG MSG(&ZERRMSG)
  EXIT CODE(12)
/*
