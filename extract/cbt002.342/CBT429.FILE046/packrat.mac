PACKRAT  CSECT
*                                                          AXC-CBT
*    ****** CBT CHANGED HOW MASS MUTUAL WAS CHECKING FOR   AXC-CBT
*           AN AUTHORIZED TSO USERS. THEY CHECKED THE      AXC-CBT
*           OPERATOR BIT FROM UADS. I LINK TO A PROGRAM    AXC-CBT
*           CALLED AON (IN FILE 227 OF THIS TAPE) TO       AXC-CBT
*           CHECK FOR AN AUTHORIZED (IE. TECH SUPPORT)     AXC-CBT
*           TSO USERS. YOU WILL PROBABLY WHAT TO DO        AXC-CBT
*           YOUR OWN CHECKING, REMOVE THE LINK TO AON      AXC-CBT
*           ANYWAYS BECAUSE YOU PROBABLY DON'T CALL YOUR   AXC-CBT
*           CHECKER BY THAT NAME. *******                  AXC-CBT
*                                                          AXC-CBT
*          A FEW CHANGES BY CBT. ALL ARE MARKED BY         AXC-CBT
*           1. CHECK FOR THE FOLOIWNG 1ST LEVELS:          AXC-CBT
*              SYS1. SYS2. CBT. PROD.                      AXC-CBT
*           2. CHANGED TITLE FROM MASS MUTUAL TO CBT       AXC-CBT
*                                                          AXC-CBT
*        THIS  IS THE MASS MUTUAL DISK PACK MAINTENANCE ROUTINE
*        IT'S  ONLY FUNCTION IS TO EXAMINE THE VTOC OF A
*        PARTICULAR VOLUME AND SCRATCH THE DATASETS SPECIFIED.
*        THIS MODULE IS THE TERMINAL INTERFACE MODULE
*        IT INVOKES A BACKEND MODULE TO DO THE ACTUAL SCRATCH
*        AND UNCATALOG.
*        THE TERMINAL INTERFACE WAS WRITTEN BY GERRY ROBERTS.
*        THE BACKEND WAS WRITTEN BY .......... ? IT WAS PART OF
*        THE MVT VERSION OF HYDRO.
*        THE UNCATALOG ONLY OCCURS IF THE DATA SET IS
*        CATALOGUED ON THE VOLUME CURRENTLY BEING EXAMINED.
*        IF IT IS A MULTI VOLUME DATA SET IT IS NOT SCRATCHED
*        OR UNCATALOGUED.
*        IF THE DSNAME BEGINS WITH SYS1 IT IS NOT SCRATCHED OR
*        UNCATALOGUED.
*        IN ADDITION TO THE DATA SET NAME, THE CREATION DATE
*        AND NUMBER OF TRACKS ARE DISPLAYED.
*        DATA SETS CREATED ON THE SCAN DATE ARE HIGHLIGHTED.
*        THE SCAN DATE CAN BE SPECIFIED ON THE INITIAL PROMPT
*        IF IT IS NOT SPECIFIED THEN THE CURRENT DATE IS USED.
*        THIS PROGRAM HAS BEEN USED ON 3330 AND 3350 DEVICES.
*        IT HAS BEEN RUN UNDER MVS 3.8 VANILLA.
*        IT ALSO HAS BEEN RUN UNDER MVS3.8 MVS/SP1.1 8106  AXC-CBT
*        IT LINK EDITED WITH AC=1
*        THE USER MUST HAVE OPERATOR AUTHORIZATION.
*        NO WARRANTIES ARE EXPRESSED OR IMPLIED IN  THIS CODE
*        USE IT AT YOUR OWN RISK.
*        THE PROMPT SCREEN ALLOWS YOU TO ENTER THE VOLSER YOU
*        WISH TO EXAMINE AND THE SCAN DATE .  ENTERING ANYTHING
*        IN THE ACTION FIELD WILL CASE THE PROGRAM TO END
*        UNLESS A VOLSER OR SCAN DATE HAS BEEN SPECIFIED IN WHICH CASE
*        THE ACTION FIELD IS IGNORED.
*        WHEN THE ENTIRE VTOC HAS BEEN SCANNED THE PROMPT IS RETURNED
*        GIVING YOU THE OPPORTUNITY TO SPECIFY ANOTHER VOLUME OR END.
*        A FULL SCREEN AF DATA SETS CAN BE SCRATCHED AT A TIME.
*        HITTING THE ENTER KEY PAGES YOU FORWARD THROUGH THE VTOC.
*        PLACING AN 'XX' IN FRONT OF THE DATA SET NAME CAUSES IT
*        TO BE SCRATCED AND UNCATALOGUED. ANYTHING OTHER THAN 'XX'
*        IS IGNORED .  MODIFYING THE VOLUME OR SCAN DATE ON OTHER
*        THAN THE INITIAL PROMPT WILL RESULT IN AN ERROR AND
*        TRANMISSION OF ANY INITIAL PROMPT .
         STM   RE,RC,12(RD)            SAVE REGS
         LR    R4,RF                   SET R4
         LA    R7,4095(R4)             SET R7 AS SECOND
         LA    R7,1(R7)                BASE REGISTER
         USING PACKRAT,R4,R7           ESATBLISH BASE R4 R7
         LR    R3,RD                   AND DO
         LA    RD,SAVEA                SAVE AREA
         ST    RD,8(,R3)               CHAINNING.
         ST    R3,4(,RD)
         USING CPPL,R1                 ADDRESSABILITY FOR CPPL
         L     R2,CPPLPSCB             POINT TO PSCB
         USING PSCB,R2
         LINK  EP=AON                                      AXC-CBT
         B     PUT1                                        AXC-CBT
         TM    PSCBATR1,PSCBCTRL       OPER AUTHORIZED.
         BNO   ENDUP                   NO THEN GO AWAY.
PUT1     DS    0H
         MVI   RETCD,X'00'
         NI    ACTFLAG,X'00'
         NI    VOLFLAG,X'00'
         XC    ERRMSG,ERRMSG           CLEAR OUT ERRMSG AREA
         XC    VOLSAVE,VOLSAVE         VOLSER
         XC    DATE,DATE               SCAN DATE
         XC    PARMAREA,PARMAREA
PUTERR   DS    0H
         LA    R5,PROMPT               POINT TO PROMPT AREA
         TPUT  (5),PRMPTLEN,FULLSCR    PUT IT OUT
READ1    DS    0H
         LA    R1,INPUT
         TGET  (1),1500,ASIS           READ THE SCREEN
         LTR   RF,RF                   CHECK RETCD
         BZ    READOK
         STC   R15,RETCD
         CLI   RETCD,X'08'             ATTENTION INTERRUPT
         BE    ENDUP                   YES THEN QUIT
*        MVC   ERRMSG,READERR          ELSE MOVE IN ERROR MSG
*        B     PUTERR
READOK   DS    0H
         BAL   RA,TRANSLAT       TRANSLATE TO UPPER CASE.
         LA    R8,3(R8)     BUMP PAST THE CURRENT CURSOR POSITION
         CLC   WORKSBA,0(R8)    IS THIS A WORK SCREEN
         BE    WORKPROC         GO PROCESS WORK SCREEN
         OI    VOLFLAG,PASSVOL  SET FLAG TO SIGNIFY PASS VOLSER
         CLC   VOLSBA,0(R8)     IS THIS THE VOLSER SBA
         BE    MOVEVOL          YES OK
         CLC   ENDSBA,0(R8)     ACTION SBA
         BE    ENDUP
         MVC   ERRMSG,VOLERR    NO THEN ERROR
         B     PUTERR           PUT IT OUT
MOVEVOL  DS    0H
         MVC   VOLSAVE,3(R8)    MOVE THE VOLSER
         LA    R8,9(R8)         BUMP PAST SBA AND VOLSER
         CLC   SCANSBA,0(R8)    SCAN DATE SPECIFIED
         BE    MOVEDATE         USE DATE SPECIFIED
         TIME  DEC              SET A DEFAULT DATE
         ST    R1,WORD
         UNPK  DATE,WORD+1(3)   UNPACK IT
         B     SETFLAG
MOVEDATE DS    0H
         MVC   DATE,3(R8)       MOVE IN SCAN DATE SPECIFIED
SETFLAG  DS    0H
         OI    ACTFLAG,GETDSNS  SET ACTION FLAG
INITWORK DS    0H
         LA    R6,ENTRY           POINT TO WORK SCREEN ENTRY
         USING WRKDSECT,R6
         LA    R5,15              LOOP CONTROL
RESET    DS    0H
         NI    ATTR,X'F7'         RESET HIGH INTENSITY FLAG
         XC    DATASETN,DATASETN  CLEAR DATASET NAME FIELD
         XC    CREATDAY,CREATDAY  CREATION DATE FIELD
         XC    TRACKS,TRACKS      TRACKS FIELD
         LA    R6,ENTRYLEN(R6)    BUMP POINTER
         BCT   R5,RESET           DO SOME MORE
CALLPROC DS    0H
         TM    ACTFLAG,GETDSNS  CALL BACKGROUND TO GET DSNAMES?
         BZ    CALLBACK         NO THEN CALL BACKGROUND
VOLMVE   DS    0H
         TM    VOLFLAG,PASSVOL  1ST TIME PASS VOLSER ?
         BZ    CLRPARM          NO
         MVC   PARMAREA(L'VOLPARM),VOLPARM YES... MOVE IN VOLSER.
         B     CALLBACK          GO CALL
CLRPARM  DS    0H
         XC    PARMAREA,PARMAREA CLEAR PARM AREA
CALLBACK DS    0H                CALL BACKGROUND
         NI    VOLFLAG,X'00'     RESET CALL FLAG
         LA    R1,PARM           POINT TO PARMS
         CALL  BACKEND           CALL
         CLC   ERRMASK,PARMAREA ANY ERRORS
         BNE   CHKEMPTY         NO... CHECK IF ANY RETURNED
         MVC   ERRMSG,PARMAREA  MOVE IN ERROR MSG
         B     PUTERR           PUT OUT THE ERROR MESSAGE
CHKEMPTY DS    0H
         CLC   EMPTY,PARMAREA   ANY DATA RETURNED.
         BNE   MOVENAME         YES... GO PROCESS IT
         OI    ACTFLAG,LASTTIME SET EOV FLAG
         B     PUTWORK          GO PUT OUT WORK SCREEN
MOVENAME DS    0H
         TM    ACTFLAG,GETDSNS  IS THIS SCRATCH PROCESSING
         BZ    MOVEANS          YES THEN GO MOVE RESULT OF SCRATCH
         LA    R6,ENTRY         POINT TO WORK SCREEN
         LA    R5,14            SET LOOP COUNTER
         USING WRKDSECT,R6      ESTABLISH ADDRESSABILITY
CHKFIELD DS    0H
         CLC   EMPTY,DATASETN   CHECK IF DATASET FIELD IS EMPTY
         BE    MOVEIT           YES GO MOVE IN NAME
         LA    R6,ENTRYLEN(R6)  BUMP TO NEXT ENTRY
         BCT   R5,CHKFIELD      ANY MORE
         MVC   DATASETN(L'PARMAREA),PARMAREA MOVE IN 15TH NAME.
         CLC   CREATDAY,DATE     CREATED ON SCAN DATE
         BNE   PUTWORK           NO ... PUT OUT SCREEN
         OI    ATTR,X'08'        YES SET HIGH INTENSITY
         B     PUTWORK           GO NOW PUT OUT SCREEN.
MOVEIT   DS    0H
         MVC   DATASETN(L'PARMAREA),PARMAREA MOVE IN DATASET NAME.
         CLC   CREATDAY,DATE     CREATE EQUAL TO SCAN DATE
         BNE   CALLPROC          NO GET SOME MORE NAMES.
         OI    ATTR,X'08'        SET HIGH INTENSITY ON
         B     CALLPROC          GO GET MORE NAMES.
PUTWORK  DS    0H
         NI    ACTFLAG,X'10'     RESET ACTION FLAG
         MVC   SCNDATE,DATE      MOVE IN DATE
         MVC   VOLUME,VOLSAVE    MOVE IN VOLSER
         LA    R5,WORKSCRN       POINT TO WORK SCREEN
         TPUT  (5),WORKLEN,FULLSCR  PUT IT OUT
         B     READ1            GO DO A READ
WORKPROC DS    0H
         LA    R5,7             CURRENT CURSOR POSS & WRKSCR SBA
         CR    R1,R5            WAS ANYTHING ENTERED
         BH    SCRPROC          YES GO PROCESS SCRATCHES
         OI    ACTFLAG,GETDSNS  SET FLAG TO GET DSNS
         TM    ACTFLAG,LASTTIME WAS THIS EOV
         BZ    INITWORK         NO ... THEN GET SOMEMORE
         B     PUT1             YES.. THEN GET NEXT VOLUME
SCRPROC  DS    0H
         LA    R8,4(R8)        BUMP  WORKSBA
         LR    R9,R1           SAVE LENGTH OF INPUT
         LA    R6,7            LENGTH OF CURSOR POSS AND WRKSBA
         SR    R9,R6           DECREMENT CURSOR & SBA
         LA    R6,ENTRY         POINT TO WORK SCREEN
         LA    R5,15            SET LOOP COUNTER
         USING WRKDSECT,R6      ESTABLISH ADDRESSABILITY
CKSCRSBA DS    0H
         NI    2(R8),X'FE'      DECREMENT THE SBA BY ONE
         CLC   SCRSBA,0(R8)     CHECK IF THIS IS DATA SET TO SCR
         BE    NAMEMOVE         YES GO MOVE NAME
         LA    R6,ENTRYLEN(R6)  BUMP TO NEXT ENTRY
         BCT   R5,CKSCRSBA      ANY MORE
         MVC   ERRMSG,READERR   NO ERROR
         B     PUTERR           PUT OUT PROMPT
NAMEMOVE DS    0H
         CLC   INDSCR,3(R8)      WAS PROPER INDICATOR SET?
         BNE   SKIPIT            NO THEN SKIP THIS GUY
         MVC   PARMAREA,DATASETN MOVE IN DATASET NAME TO SCRATCH
         NI    ACTFLAG,X'10'     RESET ACTION FLAG
         B     CALLPROC          GO GET HIM
MOVEANS  DS    0H
         MVC   DATASETN(L'PARMAREA),PARMAREA
SKIPIT   DS    0H
         LA    R8,5(R8)        BUMP TO NEXT SBA
         LA    RA,5            DECREMENT LENGTH OF INPUT
         SR    R9,RA           FROM SAVED LENTGH
         LTR   R9,R9           ANY LEFT
         BP    CKSCRSBA        YES.  GO PROCESS
         B     PUTWORK         GO GET THEM.
ENDUP    DS    0H
         LA    R5,CLEARSCR
         TPUT  (5),CLEARLEN,FULLSCR
         L     RD,SAVEA+4
         RETURN (14,12)
TRANSLAT DS    0H
         LA    R8,INPUT         POINT TO START OF DATA
         ST    R1,FULLENGH     SAVE FULL LENGTH
TRLOOP   ST    R1,SAVELEN       SAVE LEN OF DATA TO TRT
         LR    R12,R1           PUT DATA LENGTH IN R12
         C     R12,=F'256'      MORE THAN 256
         BNH   EXTRT            NO THEN DO TRT
         L     R12,=F'255'
         EX    R12,INSTR        TRANSLATE 255 WORTH
         L     R1,SAVELEN       RESTORE R1
         LA    R12,1(R12)       RESTORE R12 TO 256
         SR    R1,R12           SUBTRACT OUT TR DATA LEN OF 256
         LA    R8,0(R12,R8)     BUMP POINTER
         B     TRLOOP           GO AGAIN
EXTRT    BCTR  R12,0            MINUS 1 FOR EXEECUTE
         EX    R12,INSTR        TRANSLATE
         LA    R8,INPUT         RESTORE R8
         L     R1,FULLENGH     RESTOR1 R1
         BR    RA              RETURN TO CALLER
INSTR    TR    0(0,R8),TRANSTBL   TRANSLATE TO UPPER CASE !!!!
         LTORG
PROMPT   DC    X'27F5C3115D7E11404E1D6011404F'
*        DC    C'MASSACHUSETTS  MUTUAL  LIFE  INSURANCE  CO' AXC-CBT
         DC    C'    CONNECTICUT BANK AND TRUST COMPANY    ' AXC-CBT
         DC    X'11C1E9'
         DC    C'DISK  MANAGEMENT  ROUTINE'
         DC    X'11C37A'
         DC    C'VOLUME'
         DC    X'1DC811C4C1'
         DC    CL6' '
         DC    X'1DF011C4C7'
         DC    X'11C4E7'
         DC    C'SCAN DATE'
         DC    X'1DC8'
         DC    CL5' '
         DC    X'1D6011C54A'
         DC    C'ACTION'
         DC    X'1DC811C5D1'
         DC    CL3' '
         DC    X'1D60'
         DC    X'11C76A'
         DC    C'ENTER THE VOLUME YOU WISH TO EXAMINE'
         DC    C'  AND THE SCAN DATE.'
         DC    X'11C87A'
         DC    C'ANY DATASET CREATED ON THE SCAN DATE'
         DC    C' WILL BE HIGHLIGHTED.'
         DC    X'114A4A'
         DC    C'THE DEFAULT SCAN DATE IS TODAYS DATE. '
         DC    X'114D7A'
ERRMSG   DC    CL70' '
         DC    X'11C4C113'
PRMPTLEN EQU   *-PROMPT
WORKSCRN DC    X'27F5C3115D7E1140C41D7DC111404E1D6011404F'
*        DC    C'MASSACHUSETTS  MUTUAL  LIFE  INSURANCE  CO' AXC-CBT
         DC    C'    CONNECTICUT BANK AND TRUST COMPANY    ' AXC-CBT
         DC    X'11C1E9'
         DC    C'DISK  MANAGEMENT  ROUTINE'
         DC    X'11C37A'
         DC    C'VOLUME'
         DC    X'1DC811C4C1'
VOLUME   DC    CL6' '
         DC    X'1DF011C4C7'
         DC    X'11C4E7'
         DC    C'SCAN DATE'
         DC    X'1DC8'
SCNDATE  DC    CL5' '
         DC    X'1D6011C54A'
         DC    C'DATASET NAME'
         DC    X'11C5F8'
         DC    C'CREATION DATE   TRACKS'
ENTRY    DC    X'11C6D41DC8'              6 5
         DC    CL2' '
         DC    X'1D'
ATRIB1   DC    X'F0'
         DC    X'11C6D8'
         DC    C'  '
DSNAME1  DC    CL70' '
         DC    X'11C7E41DC8'        7 5
         DC    CL2' '
         DC    X'1D'
ATRIB2   DC    X'F0'
         DC    X'11C7E8'
         DC    C'  '
DSNAME2  DC    CL70' '
         DC    X'11C8F41DC8'        8 5
         DC    CL2' '
         DC    X'1D'
ATRIB3   DC    X'F0'
         DC    X'11C8F8'
         DC    C'  '
DSNAME3  DC    CL70' '
         DC    X'114AC41DC8'        9 5
         DC    CL2' '
         DC    X'1D'
ATRIB4   DC    X'F0'
         DC    X'114AC8'
         DC    C'  '
DSNAME4  DC    CL70' '
         DC    X'114BD41DC8'        10 5
         DC    CL2' '
         DC    X'1D'
ATRIB5   DC    X'F0'
         DC    X'114BD8'
         DC    C'  '
DSNAME5  DC    CL70' '
         DC    X'114CE41DC8'        11 5
         DC    CL2' '
         DC    X'1D'
ATRIB6   DC    X'F0'
         DC    X'114CE8'
         DC    C'  '
DSNAME6  DC    CL70' '
         DC    X'114DF41DC8'        12 5
         DC    CL2' '
         DC    X'1D'
ATRIB7   DC    X'F0'
         DC    X'114DF8'
         DC    C'  '
DSNAME7  DC    CL70' '
         DC    X'114FC41DC8'        13 5
         DC    CL2' '
         DC    X'1D'
ATRIB8   DC    X'F0'
         DC    X'114FC8'
         DC    C'  '
DSNAME8  DC    CL70' '
         DC    X'1150D41DC8'        14 5
         DC    CL2' '
         DC    X'1D'
ATRIB9   DC    X'F0'
         DC    X'1150D8'
         DC    C'  '
DSNAME9  DC    CL70' '
         DC    X'11D1E41DC8'        15 5
         DC    CL2' '
         DC    X'1D'
ATRIB10  DC    X'F0'
         DC    X'11D1E8'
         DC    C'  '
DSNAME10 DC    CL70' '
         DC    X'11D2F41DC8'        16 5
         DC    CL2' '
         DC    X'1D'
ATRIB11  DC    X'F0'
         DC    X'11D2F8'
         DC    C'  '
DSNAME11 DC    CL70' '
         DC    X'11D4C41DC8'        17 5
         DC    CL2' '
         DC    X'1D'
ATRIB12  DC    X'F0'
         DC    X'11D4C8'
         DC    C'  '
DSNAME12 DC    CL70' '
         DC    X'11D5D41DC8'        18 5
         DC    CL2' '
         DC    X'1D'
ATRIB13  DC    X'F0'
         DC    X'11D5D8'
         DC    C'  '
DSNAME13 DC    CL70' '
         DC    X'11D6E41DC8'        19 5
         DC    CL2' '
         DC    X'1D'
ATRIB14  DC    X'F0'
         DC    X'11D6E8'
         DC    C'  '
DSNAME14 DC    CL70' '
         DC    X'11D7F41DC8'        20 5
         DC    CL2' '
         DC    X'1D'
ATRIB15  DC    X'F0'
         DC    X'11D7F8'
         DC    C'  '
DSNAME15 DC    CL70' '
         DC    X'115A5E1D60'
         DC    C'INDICATE WHICH DATASET YOU WISH TO SCRATCH'
         DC    C' BY PLACING'
         DC    X'115B6F'
         DC    C'AN XX IN FRONT OF THE DATASET NAME'
         DC    X'115C7F'
         DC    C'THEN HIT ENTER TO PROCEED.'
         DC    X'11C6D513'
WORKLEN  EQU   *-WORKSCRN
CLEARSCR DC    X'27F5C3115D7E1140403C4040001140C113'
CLEARLEN EQU   *-CLEARSCR
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
PASSVOL  EQU   X'80'
GETDSNS  EQU   X'40'
SCRDSNS  EQU   X'20'
LASTTIME EQU   X'10'
SAVEA    DC    18F'0'
PARM     DC    A(PARMAREA)
PARMAREA DS    CL70
RETCD    DC    XL1'00'
VOLFLAG  DC    XL1'00'
ACTFLAG  DC    XL1'00'
ERRMASK  DC    CL4'ERROR'
INDSCR   DC    CL2'XX'
EMPTY    DC    F'0'
WORD     DC    XL4'0000000F'
VOLPARM  DS    0CL10
         DC    CL4'VOL '
VOLSAVE  DC    CL6' '
DATE     DC    CL5' '
SAVELEN  DC    F'0'
FULLENGH DC    F'0'
VOLSBA   DC    XL3'11C4C1'
SCANSBA  DC    XL3'11C4F1'
ENDSBA   DC    XL3'11C5D1'
WORKSBA  DC    XL3'1140C5'
READERR  DC    CL70'ERROR ON TGET TRY AGAIN'
VOLERR   DC    CL70'PLEASE SPECIFY A VOLUME OR ACTION.'
INPUT    DC    400F'0'
TRANSTBL DS    0H
*
*   IBM 3270 TRANSLATE TABLE TRANSLATES
*   EBCIDIC SMALL AND CAPITALS TO CAPITALS ONLY
*
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'000102030405060708090A0B0C0D0E0F' 0
         DC    X'101112131415161718191A1B1C1D1E1F' 1
         DC    X'202122232425262728292A2B2C2D2E2F' 2
         DC    X'303132333435363738393A3B3C3D3E3F' 3
         DC    X'404142434445464748494A4B4C4D4E4F' 4
         DC    X'505152535455565758595A5B5C5D5E5F' 5
         DC    X'606162636465666768696A6B6C6D6E6F' 6
         DC    X'707172737475767778797A7B7C7D7E7F' 7
         DC    X'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F' 8
         DC    X'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F' 9
         DC    X'A0E1E2E3E4E5E6E7E8E9AAABACADAEAF' A
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF' B
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF' C
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF' D
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF' E
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF' F
WRKDSECT DSECT
SCRSBA   DS   CL3       SBA
         DS   XL2       ATTR
SCRIND   DS   CL2       SCRATCH INDICATOR
SF       DS   XL1       START FIELD
ATTR     DS   XL1       ATTRIBUTE BYTE
         DS   CL5       SBA &LINE NUMBER
DATASETN DS   CL44
         DS   CL5
CREATDAY DS   CL5
         DS   CL6
TRACKS   DS   CL5
         DS   CL5
ENTRYLEN EQU  *-SCRSBA
         IKJPSCB
         IKJCPPL
         END
