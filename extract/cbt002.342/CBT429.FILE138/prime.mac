         MACRO
&NAME    PRIME &REG1,&REG2,&REG3,&LV=72,&AREA=,&GETMAIN=NO,            X
               &AMODE=24,&RMODE=24,&REGS=YES,&CSECT=YES
         GBLA  &SAVESIZE
         GBLB  &FREESAVE
         GBLC  &R
         LCLC  &REGX
         AIF   ('&CSECT' EQ 'YES').CSECTOK
         MNOTE 'CSECT=NO IS NO LONGER SUPPORTED'
.CSECTOK ANOP
         AIF   ('&REGS' EQ 'NO').NOREGS
         REGS
.NOREGS  ANOP
&NAME    CSECT
&NAME    AMODE &AMODE
&NAME    RMODE &RMODE
&REGX    SETC  '&REG1'
         AIF   (T'&REG1 EQ 'O').NOREG
         AIF   (T'&REG2 EQ 'O').ONEREG
         LUSE  &NAME,&REG1,&REG2          ESTABLISH ADDRESSABILITY
         AGO   .GOTUSE
.NOREG   ANOP
&REGX    SETC  '&R.12'
.ONEREG  LUSE  &NAME,&REGX                ESTABLISH ADDRESSABILITY
.GOTUSE  ANOP
         B     22(&R.15)
         DC    AL1(16)
         DC    CL8'&NAME'
         DC    CL8'&SYSDATE'
         DS    0H
         STM   &R.14,&R.12,12(&R.13)      SAVE REGISTERS
         LR    &REGX,&R.15
         AIF   (T'&REG2 EQ 'O').GOTBASE
         LA    &REG2,4095(&REGX)           INITIALIZE THE SECOND
         LA    &REG2,1(&REG2)               BASE REGISTER
.GOTBASE ANOP
         AIF   (T'&REG3 EQ 'O').NOPARM
         LR    &REG3,R1                   GET PARAMETER LIST ADDR
.NOPARM  ANOP
         AIF   ('&AREA' EQ '').NOAREA
         LA    1,&AREA
         AGO   .SETSAVE
.NOAREA  ANOP
&SAVESIZE SETA &LV
         AIF   ('&GETMAIN' EQ 'YES').GETSAVE
&FREESAVE SETB 0
         BAL   1,IX&SYSNDX
         DS    CL(&SAVESIZE)
IX&SYSNDX DS   0H
         AGO   .SETSAVE
.GETSAVE ANOP
         GETMAIN R,LV=&SAVESIZE
&FREESAVE SETB 1
.SETSAVE ANOP
         XC    0(72,&R.1),0(&R.1)        CLEAR SAVE AREA
         ST    &R.1,8(&R.13)             PUT ADDR IN PREVIOUS SAVE
         ST    13,4(&R.1)                CHAIN TO HIGHER SAVE AREA
         LM    0,1,20(&R.13)             RESTORE 0 AND 1
         L     &R.13,8(&R.13)            SET SAVE AREA POINTER
         MEND
