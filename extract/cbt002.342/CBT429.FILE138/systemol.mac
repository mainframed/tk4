 TITLE 'S Y S T E M   C O N F I G U R A T I O N   D I S P L A Y'
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
*                                                                     *
*  SYSMVS IS THE OS/MVS VERSION OF THE SYSTEM CONFIGURATION DISPLAY   *
*     COMMAND.  IT SHOWS A TSO USER THE OPERATING SYSTEM, RELEASE,    *
*     CPU MODEL, SERIAL NUMBER(S), AND MAIN STORAGE SIZE, AND DATE.   *
*                                                                     *
*        MODIFIED TO GIVE MODEL NUMBERS FOR 308X, 3090, AND 4381      *
*                                                                     *
*        MODIFIED BY ART TANSKY AT SUNGARD FOR 3090                A.T.
*        PROCESSORS.  IT MUST NOW BE AUTHORIZED AS                 A.T.
*        IT MAKES USE OF THE "STIDP" PRIVELEGED                    A.T.
*        INSTRUCTION                                               A.T.
*                                                                     *
*        COMES FROM CBT FILE#300                                      *
*                                                                     *
*        RE-MODIFIED TO LIST THE AMOUNT OF MEG ONLINE INSTEAD OF   A.T.
*        THE AMOUNT OF K.  SYSTEMS KEEP GETTING BIGGER.            A.T.
*                                                                     *
*        MODIFIED TO GIVE INFOMATION ABOUT THE VM SYSTEM MVS IS    A.T.
*        RUNNING UNDER.                                            A.T.
*                                                                     *
*        MODIFIED FORMAT OF OUTPUT LINE.                              *
*                                                                     *
*        MODIFIED TO SHOW AMOUNT OF ESTOR AVAILABLE.                  *
*                                                                     *
*        MODIFIED TO RUN AS A TSO COMMAND PROCESSOR.  USE PUTLINE  TSO*
*        INSTEAD OF TPUT.  CODE ADDED (CHANGED) IS MARKED "TSO".   TSO*
*                                                                     *
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*
         SPACE 2
         MACRO
&LBL     DIAG &R1,&R2,&CODE ISSUE DIAGNOSE MACHINE COMMAND
         DS    0H                  ASSURE HALFWORD ALIGNMENT
&LBL     DC    X'83'               OPCODE
         DC    AL.4(&R1,&R2)       OPERANDS 4 BITS EACH
         DC    Y(&CODE)            DIAGNOSE CODE
         MEND
         EJECT
SYSMVS   PRIME R12,AMODE=31,GETMAIN=NO,LV=144  AMODE REQ'D FOR RCE TSO
         LUSE  CPPL,R1                                             TSO
         LA    R10,MYIOPL                                          TSO
         LUSE  IOPL,R10                                            TSO
         L     R0,CPPLUPT                                          TSO
         ST    R0,IOPLUPT                                          TSO
         L     R0,CPPLECT                                          TSO
         ST    R0,IOPLECT                                          TSO
         LA    R0,MYECB                                            TSO
         ST    R0,IOPLECB                                          TSO
         LDROP R10                                                 TSO
         LDROP R1                                                  TSO
         EJECT
         L     R10,CVTPTR
         LA    R5,CVTMAP-CVTFIX    BACK TO PREFIX
         SR    R10,R5
         LUSE  CVTFIX,R10
         L     R3,CVTSYSAD
         MVC   BUFFER(BUFEND-BUFFER),MASK
         MVC   BPRODNAM,CVTPRODN
         MVC   BFMID,CVTPRODI
         IC    R14,CVTDCB
         N     R14,VSMASK
         SRL   R14,1
         L     R9,CVTEORM
         SRL   R9,20
         A     R9,=F'1'
         CVD   R9,DWD
         ED    BCORE,DWD+5
         L     R8,CVTRCEP          @ RCE
         LUSE  RCE,R8
         L     R8,RCEESPL          # FRAMES OF ESTOR AVAILABLE
         LDROP R8
         SRL   R8,8                CONVERT TO MEG
         CVD   R8,DWD
         MVO   DWD(4),DWD+5(3)     GET EXACTLY 4 DIGITS
         ED    BESTOR,DWD+1
         MVI   BESTORPL,C'+'       PUT THE PLUS SIGN IN THE OUTPUT LINE
         MVC   BIPLV,UCBVOLI-UCBOB(R3)
         ED    BDATE,CVTDATE+1
         UNPK  WORK(5),CVTMDL(3)
         MVC   BMODL,WORK                                          A.T.
         LUSE  PSA,R0
         L     R9,PSAPCCAV         @ PCCA
         LDROP R0
         LUSE  PCCA,R9
         MVC   BSNUM,PCCACPID+2
         MVC   DWD+2(3),PCCACPID   CONVERT PRINTABLE HEX TO HEX
         TR    DWD+2(3),PRHEXTBL
         PACK  DWD(2),DWD+2(3)
         CLI   DWD,X'FF'           ARE WE UNDER VM?
         BE    DOVM                  YES
         LDROP R9
TYPELKUP DS    0H                  LOOK UP CPU TYPE
         LA    R9,TBL3090          ASSUME A 3090
         CLC   =C'3090',BMODL      IS IT?
         BE    FINDTYPE
         LA    R9,TBL4381          ASSUME A 4381
         CLC   =C'4381',BMODL      IS IT?
         BE    FINDTYPE
         LA    R9,TBL308X          ASSUME A 308X
         CLC   =C'30',BMODL        IS IT?
         BNE   NOTYPE              NO, CAN'T DETERMINE MODEL TYPE
         CLC   =C'81',BMODL+2      3081?
         BE    FINDTYPE            YES
         CLC   =C'83',BMODL+2      3083?
         BE    FINDTYPE            YES
         CLC   =C'84',BMODL+2      3084?
         BE    FINDTYPE            YES
         B     NOTYPE              CAN'T DETERMINE TYPE
FINDTYPE DS    0D
*  FIND CPU TYPE BASED ON INTERNAL (SEMI-RANDOM) NUMBERS IN THE
*  FIRST BYTE OF VALUE GIVEN BY STIDP.  VALUES CAN BE FOUND IN THE
*  FUNCTIONAL CHARACTERISTICS MANUAL FOR EACH CPU MODEL.
         CLC   0(1,R9),DWD
         BE    MOVETYPE
         BH    NOTYPE              NO MATCH - GIVE UP
         LA    R9,6(R9)            BUMP TO NEXT ENTRY
         B     FINDTYPE            AND KEEP LOOKING
*  WE GOT THE TYPE - MOVE IT INTO THE PRINT LINE
MOVETYPE DS    0D
         MVC   BTYPE,1(R9)
NOTYPE   DS    0H
         EJECT
*        LA    R7,BUFEND-BUFFER                                    TSO
*        TPUT  BUFFER,(R7)                                         TSO
         PUTLINE PARM=PUTLPB,                                      TSO ?
               OUTPUT=(BUFRRDW,SINGLE,DATA),                       TSO ?
               ENTRY=LINKPUTL,                                     TSO ?
               MF=(E,MYIOPL)                                       TSO
         B     LEAVE               EXIT
DOVM     DS    0D                  GET INFO FROM VM
         TESTAUTH FCTN=1           ARE WE AUTHORIZED
         LTR   R15,R15
         BZ    AUTHOK                YES
         MVC   BTYPE,=C'SUBVM '
         B     NOTYPE
AUTHOK   DS    0D
         MODESET MODE=SUP,KEY=ZERO
         LRA   R1,VMBUFF
         LA    R2,VMBUFFLN
         DIAG  R1,R2,X'0000'       X'83120000' IF NO DIAG MACRO
         MODESET MODE=PROB,KEY=NZERO
         MVC   DWD(1),IPUVC        MOVE IN VERSION
         MVC   VMSYS,HPID
         MVC   VMID,USID
         XR    R7,R7
         IC    R7,VC
         CVD   R7,DWD2
         ED    VMVERS,DWD2+6
         IC    R7,LVL
         CVD   R7,DWD2
         ED    VMLVL,DWD2+6
         ICM   R7,B'0011',MCEL
         CVD   R7,DWD2
         ED    VMPUT,DWD2+5
*        LA    R7,VMLINEND-VMLINE                                  TSO
*        TPUT  VMLINE,(R7)                                         TSO
         PUTLINE PARM=PUTLPB,                                      TSO ?
               OUTPUT=(VMRDW,SINGLE,DATA),                         TSO ?
               ENTRY=LINKPUTL,                                     TSO ?
               MF=(E,MYIOPL)                                       TSO
         B     TYPELKUP            GO BACK AND PUT OUT 1ST LINE, SECOND
         SPACE
LEAVE    DS    0H
*
*     DEBUGGING STUFF:
*
*        MODESET MODE=SUP
*        STIDP DWD
*        MODESET MODE=PROB
*        LUSE  PSA,R0
*        L     R9,PSAPCCAV         @ PCCA
*        LDROP R0
*        LUSE  PCCA,R9
*        MVC   DWD+1(7),PCCACPID
*        LDROP R9
*        EX    15,*                ABEND 0C3
*
*     END OF DEBUGGING STUFF
*
         TERME RC=0
LINKPUTL SAVE  (14,12)
         LA    R15,72(,R13)
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         LINK  SF=(E,CALLPUTL),MF=(E,(1))
         L     R13,4(,R13)
         RETURN (14,12),T,RC=(15)
         EJECT
CALLPUTL LINK  EP=IKJPUTL,SF=L                                     TSO
MYECB    DC    F'0'                                                TSO
MYIOPL   DC    XL(IOPLLEN)'0',0F'0'                                TSO
PUTLPB   PUTLINE MF=L                                              TSO
VSMASK   DC    F'2'
F8       DC    F'8'
*VS R038.   S/370-3081 SUBVM  222285    48M  IPL'D FROM X220R2. TODAY
*P2.2.0  JBB2220  3081 SUBVM  222285    48M  IPL'D FROM X220R2. TODAY
*P2.2.0  JBB2220 3090 300E   222285    48M+ 128M SYSRES=X220R2. TODAY
MASK     DC    C'SP2.2.0 '         PRODUCT NAME
         DC    C' '
         DC    C'JBB2220'         FMID
         DC    C' '
         DC    C'3090'             CPU TYPE                        A.T.
         DC    C' '
         DC    C'     '            CPU MODEL (IF KNOWN)
         DC    C'  '                                               A.T.
         DC    C'123456'           SERIAL NUMBER                   A.T.
         DC    X'402020202120'     STORAGE AMOUNT                  A.T.
         DC    C'M'
         DC    X'4020202120'
         DC    C'M SYSRES='                                   A.T.
         DC    C'RESPAK'                                           A.T.
         DC    C'. TODAY IS YY.DDD'                                A.T.
         ORG   *-7                                                 A.T.
         DC    X'402120',C'.',X'202020'
*
*  NOTE: THESE TABLES MUST EACH BE SORTED ASCENDING
*        (WATCH OUT THAT A-F SHOULD SORT AFTER 0-9)
*
TBL3090  EQU   *                   MODEL NUMBERS FOR 3090'S
         DC    X'03',CL5'120E '
         DC    X'05',CL5'150  '
         DC    X'06',CL5'150E '
         DC    X'09',CL5'280E '
         DC    X'10',CL5'180  '
         DC    X'11',CL5'180E '
         DC    X'20',CL5'200  '
         DC    X'21',CL5'200E '
         DC    X'31',CL5'300E '
         DC    X'40',CL5'400  '
         DC    X'41',CL5'400E '
         DC    X'51',CL5'500E '
         DC    X'61',CL5'600E '
         DC    X'FF',CL5'SUBVM'
TBL308X  EQU   *                   MODEL NUMBERS FOR 308X'S
         DC    X'01',CL5'E    '
         DC    X'03',CL5'D    '
         DC    X'11',CL5'B    '
         DC    X'13',CL5'G    '
         DC    X'21',CL5'J    '
         DC    X'23',CL5'K    '
         DC    X'26',CL5'Q    '
         DC    X'53',CL5'G+   '
         DC    X'63',CL5'K+   '
         DC    X'66',CL5'Q+   '
         DC    X'81',CL5'EX   '
         DC    X'91',CL5'BX   '
         DC    X'93',CL5'GX   '
         DC    X'A1',CL5'JX   '
         DC    X'A3',CL5'KX   '
         DC    X'A6',CL5'QX   '
         DC    X'B1',CL5'CX   '
         DC    X'FF',CL5'SUBVM'
TBL4381  EQU   *                   MODEL NUMBERS FOR 4381'S
         DC    X'00',CL5'-2   '
         DC    X'01',CL5'-3   '
         DC    X'02',CL5'-1   '
         DC    X'03',CL5'-13  '
         DC    X'04',CL5'-14  '
         DC    X'06',CL5'-11  '
         DC    X'07',CL5'-23  '
         DC    X'08',CL5'-12  '
         DC    X'09',CL5'-21  '
         DC    X'0A',CL5'-22  '
         DC    X'FF',CL5'SUBVM'
VMBUFFLC DC    0F'0',AL2(VMBUFFLN),XL2'0'
NOTAUTH  DC    CL(BCORE-BMODL)'PGM NOT AUTH'
PRHEXTBL TABLE
         ORG   PRHEXTBL+C'A'
         DC    X'FAFBFCFDFEFF'     TRANSLATE HEX LETTERS TO "PACK-ABLE"
         ORG   PRHEXTBL+C'0'
         DC    C'0123456789'
         ORG   ,
         LTORG
         EJECT
BUFRRDW  DC    Y(BUFEND-BUFRRDW,0)
BUFFER   EQU   *
BMASK    EQU   *
BPRODNAM DC    C'SP2.2.0 '         PRODUCT NAME
         DC    C' '
BFMID    DC    C'JBB2220'         FMID
         DC    C' '
BMODL    DC    C'3090'             CPU TYPE                        A.T.
         DC    C' '
BTYPE    DC    C'     '            CPU MODEL (IF KNOWN)
         DC    C'  '                                               A.T.
BSNUM    DC    C'123456'           SERIAL NUMBER                   A.T.
BCORE    DC    X'402020202120'     STORAGE AMOUNT                  A.T.
         DC    C'M'
BESTORPL EQU   *,1                 PUT A PLUS HERE AFTER THE EDIT
BESTOR   DC    X'4020202120'
         DC    C'M SYSRES='                                        A.T.
BIPLV    DC    C'RESPAK'                                           A.T.
         DC    C'. TODAY IS YY.DDD'                                A.T.
         ORG   *-7                                                 A.T.
BDATE    DC    X'4021204B202020'   ' YY.DDD'                       A.T.
BUFEND   EQU   *
DWD      DS    D
DWD2     DS    D
VMBUFF   DS    0D
HPID     DS    CL8                 SYSTEM NAME
         DS    XL3                 RESERVED
IPUVC    DS    XL1                 CPU VERS. CODE (1ST BYTE COP STIDP)
MMCEL    DS    XL2                 370 GUESTS: MAX MCEL LENGTH
*                                  XA GUESTS: RESERVED
IPUAD    DS    XL2                 INSTR PROC UNIT
USID     DS    CL8                 VM USERID
PPFLG    DS    XL8                 PROG PROD BIT MAP
TIMEZONE DS    XL4                 TIME ZONE DIFFERENTIAL (IN SECONDS)
LVL      DS    XL1                 RELEASE NUMBER
VC       DS    XL1                 VERSION NUMBER
MCEL     DS    XL2                 PLC TAPE NUMBER (MVS PUT EQUIV.)
VMBUFFLN EQU   *-VMBUFF
WORK     DS    CL7                                                 A.T.
VMRDW    DC    Y(VMLINEND-VMRDW,0)
VMLINE   DC    C'IPLED UNDER '
VMSYS    DC    CL8'VM/XA/SP'
         DC    C' VERSION '
VMLVL    DC    X'40202120'
         DC    C'.'
VMVERS   DC    X'F0202120'
         DC    C' AT TAPE#'
VMPUT    DC    X'402020202120'
         DC    C'.  VM USERID='
VMID     DC    CL8'VMID'
VMLINEND EQU   *
         SPACE
         EJECT
         CVT   DSECT=YES,PREFIX=YES
         EJECT
         PRINT NOGEN
         IEFUCBOB
         IHAPCCA
         IHAPSA
         IARRCE
         SPACE 2
         IKJCPPL ,                                                 TSO
         SPACE 5                                                   TSO
         IKJIOPL ,                                                 TSO
IOPLLEN  EQU   *-IOPL                                              TSO
         END   SYSMVS
