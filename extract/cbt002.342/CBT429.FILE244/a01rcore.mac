REALCORE TITLE 'DISPLAY ALLOCATED PAGE FRAME COUNTS FOR SPECIFIED JOBS'
***********************************************************************
*                                                                     *
*   REALCORE                                                          *
*   ========                                                          *
*                                                                     *
*   DESCRIPTION :-  THIS PROGRAM WILL SCAN THROUGH THE ASCB CHAIN TO  *
*                   FIND SPECIFIED JOBNAMES, AND THEN PRINT OUT THE   *
*                   NUMBER OF FRAMES OF REAL CORE THEY ARE USING.     *
*                   THIS PROCESS IS REPEATED AT TEN SECOND INTERVALS, *
*                   FOR A SPECIFIED ELAPSED TIME.                     *
*                                                                     *
*                                                                     *
*   METHOD OF USE :-  //  EXEC PGM=REALCORE,PARM='NNNNXXXXXXXX...'    *
*                     //PRINTOUT DD SYSOUT=*                          *
*                                                                     *
*                 WHERE  NNNN = NUMBER OF MINUTES JOB IS TO RUN.      *
*                    XXXXXXXX = JOBNAME(S) TO REPORT ON.  MINIMUM 1,  *
*                               MAXIMUM 12, ALL EXACTLY 8 CHARACTERS. *
*                                                                     *
*                                                                     *
*   REGISTER USAGE :-                                                 *
*                                                                     *
*      R0   -                                                         *
*      R1   -                                                         *
*      R2   -                                                         *
*      R2   -                                                         *
*      R3   -                                                         *
*      R4   -                                                         *
*      R5   -                                                         *
*      R6   -                                                         *
*      R7   -  NUMBER TIMER INTERVALS LEFT                            *
*      R8   -  ASCBJBNI/ASCBJBNS/ASCBFMCT                             *
*      R9   -  POINTER TO ASBC                                        *
*      R10  -  POINTER TO CVT/ASVT                                    *
*      R11  -                                                         *
*      R12  -  BASE REGISTER                                          *
*      R13  -  POINTER TO SAVE AREA                                   *
*      R14  -                                                         *
*      R15  -  RETURN CODE                                            *
*                                                                     *
*   RETURN CODES  :-  0 = NORMAL COMPLETION                           *
*                    16 = INVALID PARM SPECIFIED                      *
*                                                                     *
***********************************************************************
         SPACE 3
REALCORE CSECT
         SAVE  (14,12),,*          SAVE REGISTERS
         LR    R12,R15             ESTABLISH -
         USING REALCORE,R12        BASE REGISTER
         LR    R10,R1              SAVE PARAMETER LIST
         GETMAIN R,LV=WORKLEN      GET SAVE/WORK AREA
         ST    R1,8(R13)           CHAIN SAVE AREAS FORWARD
         ST    R13,4(R1)           CHAIN SAVE AREAS BACKWARD
         LR    R13,R1              ADDRESS -
         USING WORKAREA,R13        SAVE AREA
         LR    R1,R10              RESTORE PARAMETER LIST
         EJECT
         L     R1,0(R1)            POINT TO JOB PARM
         LH    R3,0(R1)            PICK UP PARM LENGTH
         SH    R3,=H'4'            SUBTRACT LENGTH OF TIME
         SR    R2,R2               ZEROISE THE EVEN REGISTER
         D     R2,=F'8'            DIVIDE BY LENGTH OF JOBNAME
         LTR   R2,R2               CHECK FOR ZERO REMAINDER
         BNZ   ABORT               WRONG - ABORT
         LTR   R3,R3               CHECK FOR NO JOBNAMES
         BZ    ABORT               WRONG - ABORT
         SPACE 3
         PACK  PACKED,2(4,R1)      PICK UP ELAPSED TIME IN MINUTES
         MP    PACKED,=P'6'        CONVERT TO 10 SECOND INTERVALS
         CVB   R7,PACKED           STORE IN WORK REGISTER
         LA    R1,6(R1)            POINTER TO FIRST JOBNAME
         LA    R2,JOBNAMES         POINTER TO FIRST STORAGE ADDRESS
STOREJOB MVC   0(8,R2),0(R1)       STORE JOBNAME IN TABLE
         LA    R1,8(R1)            POINTER TO NEXT JOBNAME
         LA    R2,8(R2)            POINTER TO NEXT STORAGE ADDRESS
         BCT   R3,STOREJOB         DECREMENT COUNTER OF JOBNAMES
         MVI   0(R2),X'FF'         INDICATE END OF JOBNAMES
         SPACE 3
         OPEN  (PRINTOUT,OUTPUT)   OPEN PRINT FILE
         MVC   PRLINE(2),=CL2'1 '  INITIALISE HEADING LINE
         MVC   PRLINE+2(131),PRLINE+1
         TIME  ,                   GET CURRENT DATE
         ST    R1,PACKED           STORE DATE
         MVC   PRLINE+1(7),=X'4021204B202020' SET UP EDIT MASK
         ED    PRLINE+1(7),PACKED+1 EDIT DATE INTO HEADING LINE
         LA    R1,JOBNAMES         POINTER TO FIRST JOBNAME
         LA    R2,PRLINE+12        POINTER TO FIRST PRINT ADDRESS
PRINTJOB MVC   0(8,R2),0(R1)       MOVE JOBNAME TO PRINT LINE
         LA    R1,8(R1)            POINTER TO NEXT JOBNAME
         LA    R2,10(R2)           POINTER TO NEXT PRINT ADDRESS
         CLI   0(R1),X'FF'         TEST FOR END OF JOBNAMES
         BNE   PRINTJOB            NOT AT END - MOVE ANOTHER
         PUT   PRINTOUT,PRLINE     PRINT HEADING LINE
         MVI   PRLINE,C' '         INITIALISE DETAIL LINE
         MVC   PRLINE+1(132),PRLINE
         EJECT
REPEAT   TIME  DEC                 GET CURRENT TIME
         ST    R0,PACKED           STORE TIME
         MVC   PRLINE(9),=X'4021207A20207A2020' SET UP EDIT MASK
         ED    PRLINE(9),PACKED    EDIT TIME OF DAY
         OI    PRLINE+1,X'F0'      INSERT LEADING ZERO IF REQUIRED
         L     R10,X'10'           POINT TO CVT (FIXED)
         USING CVT,R10             ESTABLISH ADDRESSABILITY TO CVT
         L     R10,CVTASVT         POINT TO ASVT
         DROP  R10                 FINISHED WITH CVT
         USING ASVT,R10            ESTABLISH ADDRESSABILITY TO ASVT
         LA    R10,ASVTENTY        START OF ADDRESSES OF ASCBS
         DROP  R10                 FINISHED WITH START ADDRESS OF ASVT
         SPACE 3
TESTASID CLC   0(4,R10),=X'80000000' TEST FOR LAST ENTRY
         BE    PUTLINE             YES - GO AND PRINT DETAIL LINE
         TM    0(R10),ASVTAVAL     IS THIS ADDRESS SPACE ALLOCATED ?
         BO    NEXTASID            NO - GO AND TRY THE NEXT ONE
         L     R9,0(R10)           GET ASCB ADDRESS
         USING ASCB,R9             ESTABLISH ADDRESSABILITY TO ASCB
         L     R8,ASCBJBNI         PICK UP BATCH JOB NAME
         LTR   R8,R8               IS THIS A BATCH JOB ?
         BNZ   TESTJOB             YES - GO AND CHECK IT
         L     R8,ASCBJBNS         PICK UP TSO/STC NAME
TESTJOB  LA    R1,JOBNAMES         ADDRESS OF FIRST JOBNAME
         LA    R2,PRLINE+12        ADDRESS FIRST FRAME PRINT POSITION
NEXTJOB  CLC   0(8,R1),0(R8)       IS THIS THE ONE WE'RE LOOKING FOR ?
         BE    FOUNDJOB            YES - GET NUMBER OF PAGES
         LA    R1,8(R1)            POINT TO NEXT JOBNAME
         CLI   0(R1),X'FF'         END OF TABLE ?
         BE    NEXTASID            YES - TRY NEXT ADDRESS SPACE
         LA    R2,10(R2)           POINT TO NEXT FRAME PRINT POSITION
         B     NEXTJOB
         SPACE 3
NEXTASID LA    R10,4(R10)          POINT TO NEXT ASCB
         B     TESTASID            GO AND TRY THE NEXT ONE
         SPACE 3
FOUNDJOB LH    R8,ASCBFMCT         LOAD PAGE FRAME COUNT
         CVD   R8,PACKED           PACK PAGE FRAME COUNT
         MVC   0(6,R2),=X'402120202020' SET UP EDIT MASK
         ED    0(6,R2),PACKED+5    EDIT PAGE FRAME COUNT
         B     NEXTASID            GO AND TEST NEXT ONE
         SPACE 3
PUTLINE  PUT   PRINTOUT,PRLINE     PRINT DETAIL LINE
         BCT   R7,SLEEP            DECREMENT COUNTER
         B     CLOSE               END OF JOB
         SPACE 3
SLEEP    STIMER WAIT,DINTVL=WAITIME SLEEP FOR 10 SECONDS
         B     REPEAT
         SPACE 3
ABORT    LA    R15,16              SET ERROR RETURN CODE
         B     RETURN              AND ABORT THE RUN
         SPACE 3
CLOSE    CLOSE PRINTOUT
         LA    R15,0               SET RETURN CODE
RETURN   LR    R10,R15             SAVE RETURN CODE
         LR    R1,R13              POINT TO GETMAINED AREA
         L     R13,4(R13)          RESTORE HIGHER SAVE AREA
         FREEMAIN R,LV=WORKLEN,A=(1) FREE GETMAINED AREA
         LR    R15,R10             RESTORE SAVE AREA
         RETURN (14,12),RC=(15)    RETURN TO CALLER
         EJECT
         PRINT ON,NOGEN
PRINTOUT DCB   DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=3192,MACRF=PM,     X
               DDNAME=PRINTOUT
         DS    0D                  ALIGN TO A DOUBLEWORD BOUNDARY
WAITIME  DC    C'00001000'         TEN SECOND INTERVAL
*                HHMMSSTH
         LTORG
         YREGS
         CVT   DSECT=YES
         IHAASVT
         IHAASCB
         SPACE 3
WORKAREA DSECT
         DS    18F                 REGISTER SAVE AREA
PACKED   DS    D                   WORK AREA
JOBNAMES DS    12CL8               JOBS TO BE MONITORED
         DS    X                   IN CASE ALL 12 JOBNAMES SPECIFIED
PRLINE   DS    CL133               PRINT LINE
WORKLEN  EQU   *-WORKAREA          LENGTH OF WORK AREA
         END   REALCORE
