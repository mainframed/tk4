*
* MAKE CHANGES AS REQUIRED ON =====>  LINES
*
*
*
*
*
//YOUR JOBCARD                                    ======>
//*
//ASM     EXEC PGM=IEV90,
//             PARM=(NODECK,OBJECT)
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSIN    DD  *
SMFPROG  CSECT
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12),,*
         USING SMFPROG,R12
         LR    R12,R15
         LA    R14,SAVEAREA
         ST    R13,4(R14)
         ST    R14,8(R13)
         LR    R13,R14
*
*
         OPEN SMFIN
         OPEN (REPORT,(OUTPUT))
HEADING  PUT  REPORT,BLANK
         PUT  REPORT,HEAD1
         PUT REPORT,HEAD2
         PUT REPORT,HEAD3
         LA    R8,60
*
GETSMF   GET  SMFIN,FIELDA
*
         LA   R2,FIELDA
*        LR   R2,R1
         CLI  5(R2),17
         BNE  GETSMF
         USING SMF17HDR,R2
         MVC  OUTJBN(8),SMF17JBN
         MVC  OUTDSN(44),SMF17DSN
         CLC  SMF17DSN(10),=C'DWSF.TRKL1'                  =====>
*             LENGTH   **     **********    DATASET NAME
         BNE  GETSMF
         MVC  PARM2(4),SMF17DTE
         MVC  PARM3(4),SMF17TME
         MVC  PARM1A,=X'0011'
         MVC  PARM1B,=X'0047'
         MVC  PARM4,=X'0002'
         MVC  PARM6,=X'0034'
         CALL FWWCLOCK,(PARM1,PARM2,PARM3,PARM4,PARM5,PARM6,PARM7),VL
         MVC  OUTDTE(8),PARM5
         MVC  OUTTME(8),PARM7
         PUT  REPORT,JOBLINE
         BCTR R8,R0
         LTR  R8,R8
         BZ   HEADING
         B    GETSMF
ENDSMF   CLOSE SMFIN
         CLOSE REPORT
EXITR    L     R13,4(R13)
         RETURN (14,12),RC=(15)
         EJECT
         LTORG
SAVEAREA DS    18F
PARM1    DS    0XL4
PARM1A   DS    XL2
PARM1B   DS    XL2
PARM2    DS    PL4
PARM3    DS    BL4
PARM4    DS    XL2
PARM5    DS    CL8
PARM6    DS    XL2
PARM7    DS    CL8
SMFIN    DCB   DDNAME=SMFIN,DSORG=PS,MACRF=(GM),EODAD=ENDSMF,          X
               RECFM=VB
REPORT   DCB   DDNAME=REPORT,DSORG=PS,MACRF=(PM),LRECL=140
BLANK    DC    CL140'                    '
HEAD1    DS    0CL140
         DC    CL40'1JOBNAME     DATE      TIME    UPDATE/  '
         DC    CL40'SCRATCH/  DATASET NAME                  '
         DC    CL40'              CATALOG NAME              '
         DC    CL20'                    '                   '
HEAD2    DS    0CL140
         DC    CL40'                                DELETE  '
         DC    CL40'UNCATALOG                               '
         DC    CL40'                                        '
         DC    CL20'                    '                   '
HEAD3    DS    0CL140
         DC    CL40' =======     ====      ====    =======  '
         DC    CL40'========= ============                  '
         DC    CL40'              ============              '
         DC    CL20'                    '                   '
JOBLINE  DS    0CL140
         DC    CL1' '
OUTJBN   DS    CL8
         DC    CL2'  '
OUTDTE   DS    CL8
         DC    CL2'  '
OUTTME   DS    CL8
         DC    CL5'     '
         DC    CL10'          '
         DC    CL5'        '
OUTDSN   DS    CL44
         DC    CL44' '
FIELDA   DS    CL33000
         DSECT
         IFASMFR1 17
         END
/*
//SYSLIN   DD  DSN=&OBJ,SPACE=(3040,(40,40),,,ROUND),UNIT=VIO,
//             DISP=(MOD,PASS),
//             DCB=(BLKSIZE=3040,LRECL=80,RECFM=FBS,BUFNO=1)
//SYSUT1   DD  DSN=&SYSUT1,SPACE=(1024,(120,120),,,ROUND),UNIT=VIO,
//             DCB=BUFNO=1
//LKED    EXEC PGM=IEWL,COND=(5,LT,ASM),
//             PARM='LIST,LET,XREF,MAP'
//SYSLIB DD DSN=SYS1.LINKLIB,DISP=SHR
//       DD DSN=SYS3.UTILS.LOAD,DISP=SHR
//       DD DSN=SYS2.LINKLIB,DISP=SHR
//SYSPRINT DD  SYSOUT=*
//SYSLMOD  DD  SPACE=(CYL,(1,1,1)),DSN=&LOD(X),DISP=(,PASS),
//             UNIT=VIO,DCB=BUFNO=1
//SYSUT1   DD  DSN=&SYSUT1,SPACE=(1024,(120,120),,,ROUND),UNIT=VIO,
//             DCB=BUFNO=1
//SYSLIN   DD  DSN=&OBJ,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//GO      EXEC PGM=*.LKED.SYSLMOD,COND=((5,LT,LKED),(5,LT,ASM))
//SMFIN    DD DSN=GDG.FWW.SMFDATA(0),DISP=SHR,
*                 ******************       SMF DATASET NAME  =====>
//   DCB=(RECFM=VB,LRECL=32756,BLKSIZE=32760)
//REPORT   DD SYSOUT=X,DCB=(RECFM=FBA,LRECL=140,BLKSIZE=9380)
//SYSUDUMP DD SYSOUT=X
//*
