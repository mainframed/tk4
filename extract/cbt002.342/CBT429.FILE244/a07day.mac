WHATDAY  BEGIN R12
         REGEQS
         PRINT NOGEN
***********************************************************************
**                                                                   **
**  PURPOSE : TO INFORM THE USER WHAT DAY A PARTICULAR DATE WAS/IS/  **
**            WILL BE.                                               **
**  INPUT FORMAT : DD/MM/YYYY  NOPARM AND NO SYSIN DD FOR MASTER CONS**
**                                                                   **
**  REGISTER USAGE :-                                                **
**  R1  PASSED PARAMETER ( PARM INPUT ONLY)                          **
**  R2  WORK REGISTER                                                **
**  R3  WORK REGISTER                                                **
**  R4  ** NOT USED **                                               **
**  R5  WORK REGISTER                                                **
**  R6  ** NOT USED **                                               **
**  R7  WORK REGISTER                                                **
**  R8  WORK REGISTER                                                **
**  R9  WORK REGISTER                                                **
**  R10 MONTH TABLE POINTER                                          **
**  R11 ** NOT USED **                                               **
**  R12 BASE REGISTER                                                **
**  R13 STANDARD IBM LINKAGE                                         **
**  R14 STANDARD IBM LINKAGE                                         **
**  R15 STANDARD IBM LINKAGE                                         **
**                                                                   **
*********************************************************************
*                                                                   *
*                      D I S C L A I M E R                          *
*                                                                   *
*   This computer programming material has been developed by        *
*   Kevin E. Ferguson, Technical Support Manager, at Cleveland      *
*   County Council. Please address any inquiries to:-               *
*                                                                   *
*         Cleveland County Council,                                 *
*         P.O. Box 17,                                              *
*         Melrose House,                                            *
*         1 Melrose Street,                                         *
*         Middlesbrough,                                            *
*         Cleveland. UK                                             *
*         TS1 1LY                                                   *
*         (0642) 248155                                             *
*                                                                   *
*         ATTN:  K.E.Ferguson (Ext. 2290)                           *
*         (or:   Technical Support (Ext. 2363))                     *
*                                                                   *
*   This material has been used successfully by Cleveland County    *
*   Council, and to the best of our knowledge this material and any *
*   system(s) of which it is a part are operational as of the       *
*   service level or date stated in the body of this material (if   *
*   so stated). However, NO warranty is given or implied as to the  *
*   accuracy of this material or any related material or systems,   *
*   and no responsibility is assumed for any effect or modification *
*   directly or indirectly caused by the use of this material.      *
*   It is the responsibility of any user of this material to        *
*   evaluate its usefulness to the user's environment.              *
*                                                                   *
*   Cleveland County Council does not guarantee to keep this or any *
*   related material current, nor does it guarantee to provide      *
*   any corrections or extensions described by any users of this    *
*   material, or any corrections or extensions made in the future   *
*   by Cleveland County Council itself.                             *
*                                                                   *
*                       C O P Y R I G H T                           *
*                                                                   *
*     This computer programming material remains the exclusive      *
*     property of Cleveland County Council. Permission for its use  *
*     may be obtained by contacting the address above               *
*                                                                   *
*   Cleveland County Council hereby grants permission for this      *
*   programming material to be :-                                   *
*                                                                   *
*   a). Stored on your system and displayed on your terminals.      *
*                                                                   *
*   b). Modified to meet your sites specific needs.                 *
*                                                                   *
*   c). Printed (or Photocopied), but only the specific number of   *
*       copies as are necessary for your sites needs.               *
*                                                                   *
*   d). Circulated amongst bona fida members of the following IBM   *
*       user groups or their sub-groups :-                          *
*                                                                   *
*            i) G.U.I.D.E. (UK and US)                              *
*           ii) SHARE                                               *
*                                                                   *
*       with the provisor that the copyright is not infringed by    *
*       those members.                                              *
*                                                                   *
***********************************************************************
***********************************************************************
         L     R2,0(R1)                ADDRESS OF PASSED PARMLIST
         CLC   0(2,R2),=H'00'          PARM PRESENT
         BE    OPEN                    NO - OPEN FILES
         L     R3,0(R2)
         MVI   BTCHFLAG,C'P'
         MVC   INCARD(10),0(R3)        MOVE PARM TO INCARD
         L     R3,4(R2)                GET STORAGE TO USE
         ST    R3,FULLWORD             SAVE IT
         B     START
WTOR     EQU   *
         MVC   REPLYFLD,=X'40404040404040404040' CLEAR REPLY FIELD
         WTOR  'PLEASE ENTER A DATE IN THE FORMAT DD/MM/YYYY OR END',  X
               REPLYFLD,10,ECBFLD
         WAIT  ECB=ECBFLD              WAIT FOR OPERATORS REPLY
         NI    ECBFLD,X'00'            CLEAR ECBFLD AFTER REPLY
         LA    R2,REPLYFLD             R2 = ADDRESS OF REPLY
         OC    0(10,R2),=C'          '  CHANGE X'00'S TO X'40'
         CLC   0(3,R2),=C'END'         WAS END TYPED ?
         BNE   KEEPON                  NO - KEEP ON
         WTO   'END WAS TYPED ------ WHATDAY COMPLETE',ROUTCDE=(2)
         B     EXIT
KEEPON   MVI   BTCHFLAG,C'C'           SET BATCH FLAG OFF
         LA    R5,INCARD               MOVE REPLY TO INCARD
         MVC   0(10,R5),REPLYFLD       *
         B     START                   BRANCH TO START DATE PROCESSING
OPEN     OPEN  (INFILE,,OUTFILE,(OUTPUT))
         LTR   R15,R15                 OPEN OK?
         BNZ   WTOR                    NO - ASSUME MASTER CONSOLE
         MVI   BTCHFLAG,C'B'           SET BATCH FLAG
GETLOOP  GET   INFILE,INCARD           READ A RECORD
         LA    R2,OUTCARD              * CLEAR OUTPUT AREA
         MVI   0(R2),C' '              *
         MVC   1(132,R2),0(R2)         *
START    LA    R2,INCARD               ADDRESS OF INCARD IN R2
         CLC   MM,=C'12'               IS MONTH VALUE > 12 ?
         BH    ERROR                   YES - ERROR
         CLC   DD,=C'01'               IS DAY VALUE < 1 ?
         BL    ERROR                   YES - ERROR
         LA    R7,YY
         CLI   0(R7),C' '              CHECK YEAR FORMAT
         BL    ERROR
         CLI   1(R7),C' '
         BL    ERROR
         CLI   2(R7),C' '
         BL    ERROR
         CLI   3(R7),C' '
         BL    ERROR                   YES - ERROR
         MVC   WORK,MM                 MOVE MONTH TO WORK
         LA    R7,MONTAB               LOAD R7 WITH START OF MONTAB
         LA    R8,4                    SET INCR VALUE AT 4 BYTES
         LA    R9,ENDTAB               LOAD R9 WITH END OF SCAN
LOOP1    L     R10,0(R7)               L REG 10 WITH ADDRESS OF CARD
         CLC   0(2,R10),WORK           DOES THE MONTH MATCH ?
         BE    FOUND                   YES - PROCESS
         BXLE  R7,R8,LOOP1             LOOP BACK IF NOT END OF TABLE
ERROR    LA    R2,INCARD               LOAD DATE SUPPLIED INTO R2
         MVC   MESSD,0(R2)             MOVE DATE TO MESSAGE
         LA    R2,MESS                 LOAD MESSAGE INTO R2
         MVC   0(5,R2),=C'**** '       MOVE IN ASTERISKS
         MVC   16(24,R2),=C' IS AN INVALID DATE     '
         CLI   BTCHFLAG,C'B'           IS IT A BATCH RUN ?
         BE    MOVEMESS                YES - CARD OUTPUT
         CLI   BTCHFLAG,C'C'           IS IT A CONSOLE INPUT
         BE    MOVEREPL                YES - TERMINAL REPLY
         LA    R15,16                  NO - SET DUFF COND CODE
         B     EXIT
FOUND    EQU   *
         MVC   WORK,2(R10)             MOVE NUMBER OF DAYS INTO WORK
         CLC   DD,WORK                 IS DAYS SUPPLIED > DAYS IN MONTH
         BH    ERROR                   YES - ERROR
         MVC   YEARWORK,YY+2           MOVE YEAR GIVEN TO WORK AREA
         PACK  DOUBLE,YEARWORK         PACK YEARWORK INTO A DOUBLE WORD
         CVB   R3,DOUBLE               CONVERT YEAR WORK TO BINARY
         SR    R2,R2                   CLEAR R2 FOR REMAINDER
         LA    R5,4                    LOAD DIVISOR INTO R5
         DR    R2,R5                   DIVIDE BY 4
         LTR   R2,R2                   IS THERE A REMAINDER ?
         BZ    LEAPYEAR                NO - IT IS A LEAP YEAR
         CLC   MM,=C'02'               IS IT FEBRUARY ?
         BNE   FLAGOFF                 NO - SKIP NEXT TEST
         CLC   DD,=C'28'               IS THE DAYS SUPPLIED > 28 ?
         BH    ERROR                   YES - ERROR
FLAGOFF  MVI   LEAPFLAG,C'N'           SET LEAP YEAR FLAG OFF
         B     CONT                    CONTINUE
LEAPYEAR MVI   LEAPFLAG,C'Y'           SET LEAP YEAR FLAG
CONT     PACK  DOUBLE,YEARWORK         PACK YEARWORK INTO A DOUBLE WORD
         CVB   R3,DOUBLE               CONVERT YEAR WORK TO BINARY
         LA    R5,12                   LOAD DIVISOR INTO R5
         SR    R2,R2                   CLEAR REG 2
         DR    R2,R5                   DIVIDE BY 12
         LR    R9,R3                   PUT QUOTIENT INTO R9
         LR    R8,R2                   PUT REMAINDER INTO R8
         LR    R3,R2                   PUT REMAINDER INTO R3 FOR DIVIDE
         SR    R2,R2                   CLEAR REG 2
         LA    R5,4                    LOAD DIVISOR (4)
         DR    R2,R5                   DIVIDE BY 4
         AR    R3,R8              AND LAST TIMES REMAINDER TO QUOTIENT
         AR    R3,R9              NOW ADD LAST TIMES QUOTINET TO TOTAL
         LA    R5,7                    LOAD DIVISOR (7)
         SR    R2,R2                   CLEAR REG 2
         DR    R2,R5                   DIVIDE BY 7
         LR    R9,R2                   PUT REMAINDER INTO R9
         MVC   YEARWORK,4(R10)         MOVE WEIGHT INTO YEARWORK
         PACK  DOUBLE,YEARWORK         PACK IT
         CVB   R3,DOUBLE               AND CONVERT TO BINARY FOR ADD
         AR    R9,R3                   ADD WEIGHT
         LR    R3,R9                   GET READY FOR DIVIDE BY 7 (R5)
         SR    R2,R2                   CLEAR REG 2
         DR    R2,R5                   OK DIVIDE IT BY 7
         MVC   YEARWORK,DD             MOVE DAY INTO YEARWORK
         PACK  DOUBLE,YEARWORK         PACK IT
         CVB   R9,DOUBLE               AND CONVERT READY FOR EQUATE
         AR    R9,R2                   ADD DAY TO REMAINDER
         LR    R3,R9                   PUT ANSWER IN R3
         SR    R2,R2                   CLEAR REG 2
         DR    R2,R5                   DIVIDE BY 7 AGAIN
         CLI   LEAPFLAG,C'Y'           IS IT A LEAP YEAR ?
         BNE   GETIT                   NO - GO PUT CARD OUT
         CLC   MM,=C'02'               IS MONTH < OR = TO FEB (02) ?
         BH    GETIT                   NO - GO PUT CARD OUT
         LTR   R2,R2                   IS DAY POINTER ZERO /
         BNE   DAYOFF                  NO - SUBTRACT A DAY
         LA    R2,6                    FORCE IN LAST DAY OF WEEK
         B     GETIT                   NOW GO PUT CARD OUT
DAYOFF   BCTR  R2,0                    TAKE ONE DAY OFF
GETIT    SLL   R2,2                    MULTIPLY BY 4
         L     R8,DAYTAB(R2)           MOVE ADDRESS INTO REG 8
         LA    R3,INCARD               PUT ADDRESS OF INPUT INTO R3
         MVC   MESSD,0(R3)             MOVE DATE PROVIDED INTO MESSAGE
         MVC   MESS2,0(R8)             MOVE DAY INTO MESSAGE
         MVC   TMESS,=C' THE '         MOVE IN ' THE '
         MVC   CMESS,=C'          '    MOVE IN BLANKS
         MVC   IMESS,=C' IS A '        MOVE IN IS A
         CLI   BTCHFLAG,C'B'           IS IT A BATCH RUN ?
         BE    MOVEMESS                YES - GO TO MOVE MESSAGE
         CLI   BTCHFLAG,C'C'           IS IT A CONSOLE RUN ?
         BE    MOVEREPL                YES - GO TO MOVE MESSAGE
         L     13,PRGMSAVE+4           SAVE AERA IN R13
         LA    R2,MESS2                PUT ADDRESS OF MESSAGE INTO R1
         L     R3,FULLWORD             FIND WHERE TO PUT IT
         MVC   0(9,R3),0(R2)           MOVE IT IN
         B     EXIT                    GET OUT
MOVEREPL MVC   WTO+8(40),MESS          MOVE MESSAGE INTO WTO
WTO      WTO   '                                        ',             X
               ROUTCDE=(2),DESC=(2)
         B     WTOR                    NEXT
MOVEMESS MVC   OUTCARD,MESS            MOVE MESSAGE TO OUTPUT
         PUT   OUTFILE,OUTCARD         PRINT IT
         B     GETLOOP                 ** LOOP BACK FOR NEXT LINE  **
ENDOFIN  CLOSE (INFILE,,OUTFILE)       CLOSE FILES
SET0     LA    R15,0                   SET COND CODE 0
EXIT     EOJ                           END PROGRAM
***********************************************************************
**                AREAS TO BE USED AND DCBS
***********************************************************************
FULLWORD DS    F
OUTCARD  DS    CL133
MESS     DS    0CL40
TMESS    DS    C' THE '
MESSD    DS    CL10
IMESS    DS    C' IS A '
MESS2    DS    CL9
CMESS    DC    CL10' '
FILLER   DC    CL93' '
INCARD   DS    0CL80
DD       DS    CL2
         DS    CL1' '
MM       DS    CL2
         DS    CL1' '
YY       DS    CL4
         DS    CL70
WORK     DS    CL2
YEARWORK DS    CL2
DOUBLE   DS    D
BTCHFLAG DS    CL1
LEAPFLAG DS    CL1
DAYTAB   DS    0F                      SEVEN FULLWORDS
         DC    A(DAYCARD1)
         DC    A(DAYCARD2)
         DC    A(DAYCARD3)
         DC    A(DAYCARD4)
         DC    A(DAYCARD5)
         DC    A(DAYCARD6)
         DC    A(DAYCARD7)
DAYCARD1 DC    C'SATURDAY '
DAYCARD2 DC    C'SUNDAY   '
DAYCARD3 DC    C'MONDAY   '
DAYCARD4 DC    C'TUESDAY  '
DAYCARD5 DC    C'WEDNESDAY'
DAYCARD6 DC    C'THURSDAY '
DAYCARD7 DC    C'FRIDAY   '
*****                 TABLE OF DAYS IN MONTHS
MONTAB   DS    0F                      SEVEN FULLWORDS
         DC    A(DAYSJAN)
         DC    A(DAYSFEB)
         DC    A(DAYSMAR)
         DC    A(DAYSAPR)
         DC    A(DAYSMAY)
         DC    A(DAYSJUN)
         DC    A(DAYSJUL)
         DC    A(DAYSAUG)
         DC    A(DAYSSEP)
         DC    A(DAYSOCT)
         DC    A(DAYSNOV)
ENDTAB   DC    A(DAYSDEC)
DAYSJAN  DC    CL6'013101'
DAYSFEB  DC    CL6'022904'
DAYSMAR  DC    CL6'033104'
DAYSAPR  DC    CL6'043000'
DAYSMAY  DC    CL6'053102'
DAYSJUN  DC    CL6'063005'
DAYSJUL  DC    CL6'073100'
DAYSAUG  DC    CL6'083103'
DAYSSEP  DC    CL6'093006'
DAYSOCT  DC    CL6'103101'
DAYSNOV  DC    CL6'113004'
DAYSDEC  DC    CL6'123106'
INFILE   DCB   DDNAME=INPUT,EODAD=ENDOFIN,DSORG=PS,MACRF=(GM),         X
               LRECL=80,BLKSIZE=6400,RECFM=FB
OUTFILE  DCB   DDNAME=OUTPUT,DSORG=PS,MACRF=(PM),LRECL=133,            X
               BLKSIZE=133,RECFM=FBA
REPLYFLD DS    CL10
ECBFLD   DC    F'0'
         LTORG
         END
