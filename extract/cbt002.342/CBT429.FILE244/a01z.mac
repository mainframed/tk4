Z        CSECT
         SAVE  (14,12),,*          SAVE REGISTERS
         LR    12,15               ESTABLISH -
         USING Z,12                BASE REGISTER
         LR    10,1                SAVE PARAMETER LIST
         GETMAIN R,LV=WORKLEN      GET SAVE/WORK AREA
         ST    1,8(13)             CHAIN SAVE AREAS FORWARD
         ST    13,4(1)             CHAIN SAVE AREAS BACKWARD
         LR    13,1                ADDRESS -
         USING WORKAREA,13         SAVE AREA
         LR    1,10                RESTORE PARAMETER LIST
*
         LM    7,10,0(1)           PICK UP PARAMETERS
         LA    7,COMMAND           POINT TO NEW COMMAND LINE
         LINK  EP=LOGOFF,PARAM=((7),(8),(9),(10)),MF=(E,LINKLIST)
*
RETURN   LR    10,15               SAVE RETURN CODE
         LR    1,13                POINT TO GETMAINED AREA
         L     13,4(13)            RESTORE HIGHER SAVE AREA
         FREEMAIN R,LV=WORKLEN,A=(1) FREE GETMAINED AREA
         LR    15,10               RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN TO CALLER
*
         DS    0D
COMMAND  DC    H'10,6',CL6'LOGOFF' TSO COMMAND LINE
         LTORG
*
WORKAREA DSECT
         DS    18F                 REGISTER SAVE AREA
LINKLIST CALL  ,(,,,),MF=L         LIST FORM OF LINK MACRO ?
WORKLEN  EQU   *-WORKAREA          LENGTH OF WORK AREA
         END   Z
