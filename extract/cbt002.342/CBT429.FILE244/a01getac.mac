LGETACCT TITLE '              SET CLIST VARIABLE EQUAL TO ACCOUNT CODE'
***********************************************************************
*                                                                     *
*        THIS TSO COMMAND PROCESSOR SETS TSO CLIST VARIABLE           *
*        "LACCTVAR" EQUAL TO THE USER'S FIRST (OR ONLY) ACCOUNTING    *
*        FIELD.                                                       *
*                                                                     *
***********************************************************************
         SPACE 3
LGETACCT CSECT
         SAVE  (14,12),,'LGETACCT &SYSTIME &SYSDATE'
         LR    R12,R15             LOAD BASE REGISTER
         USING LGETACCT,R12        ESTABLISH ADDRESSABILITY
         LA    R11,SAVEAREA        POINT TO REGISTER SAVE AREA
         ST    R13,4(,R11)         SAVE PREVIOUS SAVE AREA
         ST    R11,8(,R13)         SAVE CURRENT SAVE AREA
         LR    R13,R11             POINT TO SAVE AREA
         SPACE 3
         LR    R10,R1              SAVE PARAMETER REGISTER
         EXTRACT FRS,FIELDS=(FRS)  GET POINTER TO FRS/TCB
         L     R1,FRS              POINT TO FRS
         LA    R1,32(,R1)          POINT TO TCB
         USING TCB,R1
         L     R1,TCBJSCB          POINT TO JSCB
         USING IEZJSCB,R1
         L     R1,JSCBJCT          POINT TO JCT
         LA    R1,16(,R1)          WHY ???????????????????
         USING INJMJCT,R1
         ICM   R1,B'0111',JCTACTAD POINT TO ACT
         LA    R1,16(,R1)          WHY ???????????????????
         USING IEFAACTB,R1
         MVC   V_VALL+3(1),ACTACCNT LENGTH OF DATA
         MVC   V_VAL,ACTACCNT+1    ACCOUNTING DATA
         L     R1,CVTPTR           POINT TO CVT
         USING CVT,R1
         L     R1,CVTTVT           POINT TO TSVT
         USING TSVT,R1
         L     R15,TSVTVACC        ADDRESS VARIABLE ACCESS SERVICE
         LR    R1,R10              RESTORE PARAMETER REGISTER
         CALL  (15),(V_CODE,V_NAMEP,V_NAMEL,V_VALP,V_VALL,V_TOKEN),VL
         SPACE 3
         L     R13,4(,R13)         POINT TO HIGHER SAVE AREA
         RETURN (14,12),RC=0       RETURN TO CALLER
         EJECT
SAVEAREA DC    18F'0'              REGISTER SAVE AREA
FRS      DS    A
V_CODE   DC    A(TSVEUPDT)         ENTRY CODE FOR SETTING VALUES
V_NAMEP  DC    A(V_NAME)           POINTER TO VARIABLE NAME
V_NAME   DC    CL8'LACCTVAR'       VARIABLE NAME
V_NAMEL  DC    A(8)                VARIABLE NAME LENGTH
V_VALP   DC    A(V_VAL)            POINTER TO VALUE
V_VAL    DC    CL15' '             VALUE
V_VALL   DC    F'0'                VALUE LENGTH
V_TOKEN  DC    F'0'                TOKEN
         PRINT NOGEN
         YREGS
         IKJTCB
         IEZJSCB
         IEFAJCTB
         CVT   DSECT=YES
         IKJTSVT
         END
