//SYSPIKJD JOB '9999,C4003,8700    ',
//    TSO,CLASS=H,PRTY=6,MSGCLASS=P,NOTIFY=TSOSYSJ
//*****************************************************************
//* TSO USER EXIT - MUST BE CHANGED IF PASSWORD CHANGED
//*                 ***********************************
//* STEP L2 COND CODE 8 ALLOWED WITH IKTINX2 MISSING
//*****************************************************************
//* THIS VERSION FOR NEW CBIPO XA 2.2
//*****************************************************************
//C        EXEC PGM=IEV90,PARM='OBJECT'
//SYSLIB   DD   DSN=DCC.MACLIB,DISP=SHR
//         DD   DSN=SYS1.MACLIB,DISP=SHR
//         DD   DSN=SYS1.AMODGEN,DISP=SHR
//SYSLIN   DD   UNIT=VIO,SPACE=(80,(400,50)),DISP=(,PASS)
//SYSPRINT DD   SYSOUT=Q
//SYSPUNCH DD   DUMMY
//SYSUT1   DD   UNIT=VIO,SPACE=(1700,(400,50))
//SYSIN    DD   *
IKJEFLD  TITLE  'TSO USER LOGON EXIT'
         REGEQS
*******************************************************************
*                                                                 *
*   REGISTER USAGE                                                *
*                                                                 *
*   R1  = PUTGET PARAMETER LIST                                   *
*   R2  = REGION SIZE / INPUT BUFFER                              *
*   R3  = ACCOUNT TABLE ENTRY (USERID)                            *
*   R4  = PROCEDURE NAME                                          *
*   R5  = USER ID                                                 *
*   R6  = PASSWORD                                                *
*   R7  = CONTROL SWITCHES                                        *
*   R8  = ECT                                                     *
*   R9  = UPT                                                     *
*   R10 = ACCOUNTING STRING                                       *
*   R11 = TSO PARAMETER LIST                                      *
*   R12 = BASE REGISTER                                           *
*   R13 = WORK/SAVE AREA                                          *
*                                                                 *
*******************************************************************
         IKJPGPB
         PRINT NOGEN
IKJEFLD  CSECT
         USING ACCTDS,R3
         USING ACCAREA,R10
         USING ADDRLIST,R11
         USING USERAREA,R13
         USING IKJEFLD,R12
         STM   R14,R12,12(R13)     SAVE REGS.
         LR    R12,R15             ADDRESS PROGRAM CODE
**  DO A GETMAIN NOW  - REG 13 WILL BE GOTTEN SAVE AND USER AREA
         GETMAIN R,LV=400
         ST    R13,4(,R1)         HIGH SAVE AREA
         ST    R1,LSA              LOW SAVE AREA
         LR    R13,R1              REG 13. IS NEW SAVE AREA
         L     R1,HSA              USE REG1 FOR OLD SAVE AREA - THEN
         LM    R0,R1,20(R1)        RESET REG0 AND REG1 FROM SAFE COPY
         LR    R11,R1              REG 11 WILL BE PARM AREA
*
         L     R2,IBFDESC          ADDRESS INPUT BUFFER DESC
         LH    R2,6(0,R2)          ADDRESS INPUT BUFFER LENGTH
         CH    R2,=H'8'            USERID IN INPUT BUFFER
         BH    BYPASS              YES - BYPASS
*
         MVI   TEKSW,X'00'         NOT TEKT.
         GTSIZE
         LTR   R0,R0               IS IT TEKT.
         BNZ   NOTTEK0             NO
         MVI   TEKSW,X'FF'         YES
NOTTEK0  DS    0H
         LOAD  EP=IKJGETL
         ST    R0,AGET
         LOAD  EP=IKJPUTL
         ST    R0,APUT
*
         L     R8,ECTDESC          ADDRESS ECT
         L     R8,0(0,R8)          ADDRESS ECT
         L     R9,UPTDESC          ADDRESS UPT
         L     R9,0(0,R9)          ADDRESS UPT
*
         L     R7,SWDESC           ADDRESS SWITCHES
         L     R7,0(0,R7)          ADDRESS SWITCHES
         OI    2(R7),X'C0'         MAIL AND NOTICES              CK
         TM    0(R7),X'80'         TEST USER-ID IN USE
         BO    INUSE
*
REUSE    DS    0H
         L     R5,USERDESC         ADDRESS USERID
         LA    R14,6(0,R5)         ADDRESS USERID LENGTH FIELD
         MVC   0(2,R14),=H'7'      USERID LENGTH FIELD ALWAYS 7
         L     R5,0(0,R5)          ADDRESS USERID
*
         L     R6,PASSDESC         ADDRESS PASSWORD
         LA    R14,6(0,R6)         ADDRESS PASSWD LENGTH FIELD
         MVC   0(2,R14),=H'8'      PASSWD LENGTH FIELD ALWAYS 8
         L     R6,0(0,R6)          ADDRESS PASSWORD
*
         L     R4,PROCDESC         ADDRESS PROCNAME
         LA    R14,6(0,R4)         ADDRESS PROCNM LENGTH FIELD
         MVC   0(2,R14),=H'8'      PROCNM LENGTH FIELD ALWAYS 8
         L     R4,0(0,R4)          ADDRESS PASSWORD
*
         L     R2,REGDESC          ADDRESS REGION SIZE
*
         L     R10,ACCDESC         ADDRESS ACCOUNT FIELD
         LA    R14,6(0,R10)        ADDRESS ACCOUNT LENGTH FIELD
         MVC   0(2,R14),=H'19'     ACCOUNT LENGTH FIELD ALWAYS 19
         L     R10,0(0,R10)        ADDRESS ACCOUNT FIELD
*
         MVI   ACCRECD,C' '        QUOTE FIELD
         MVC   ACCRECD+1(18),ACCRECD BLANK IT OUT
         MVI   ACCOMMA1,C'.'       COMMA FIELD
         MVI   ACCOMMA2,C'.'       COMMA FIELD
         MVC   ACCCODE,=C'9999'    ACCOUNT CODE ALWAYS 9999
         OI    0(R7),X'08'         SAY ACCOUNT CODE SUPPLIED
*
         STAX  ATTNEXIT,OBUF=(STAXMESS,46),IBUF=(STAXBUF,80),          *
               MF=(E,STAXLIST)
STAXCLR  DS    0H
         XC    STAXBUF,STAXBUF     CLEAR ATTN EXIT INPUT FIELD
         EJECT
**  FIND THE USERS COST CENTRE AND USER REFERENCE
*
DOUSER   DS    0H
*
         CLI   TEKSW,X'FF'
         BE    DOUSER1
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1H,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOUSER1  DS    0H
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS1,                                        *****
               OUTPUT=(LINE0A,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS1,                                        *****
               OUTPUT=(LINE0B,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS1,                                        *****
               OUTPUT=(LINE0C,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS1,                                        *****
               OUTPUT=(LINE1,TERM,SINGLE,DATA),                    *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   TEKSW,X'FF'
         BE    DOUSER2
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1L,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOUSER2  DS    0H
         L     R15,AGET
         GETLINE PARM=GETBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS2,                                        *****
               TERMGET=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS2)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   STAXBUF,0
         BNE   STAXUSED
*
         L     R1,INAREA
*        LA    R3,ACCTAB         DELETED BY APAR
         MVC   INUSER,BLANK
         OC    INUSER,4(R1)
         CLC   INUSER,BLANK
         BE    EXITOFF
         CLC   INUSER,LOGOFF
         BE    EXITOFF
REDOPASS DS    0H
         LA    R3,ACCTAB
USERLOOP DS    0H
         CLC   INUSER,USERID
         BE    DOPASS
         BL    DOUSER
         LA    R3,28(0,R3)
         B     USERLOOP
**
         EJECT
         PRINT NOGEN
DOPASS   DS    0H
*
         CLI   TEKSW,X'FF'
         BE    DOPASS1
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1H,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOPASS1  DS    0H
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE2,TERM,SINGLE,DATA),                    *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   TEKSW,X'FF'
         BE    DOPASS2
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1P,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOPASS2  DS    0H
         L     R15,AGET
         GETLINE PARM=GETBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS4,                                        *****
               TERMGET=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS2)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   STAXBUF,0
         BNE   STAXUSED
*
         L     R1,INAREA
         MVC   INPASS,BLANK
         OC    INPASS,4(R1)
PASSLOOP DS    0H
         CLC   INUSER,USERID
         BL    REDOPASS
         CLC   INPASS,PASSWORD
         BE    PASSOK
         LA    R3,28(0,R3)
         B     PASSLOOP
**
PASSOK   DS    0H
         MVC   0(7,R5),INUSER
         MVC   0(8,R6),INPASS
         MVC   0(8,R4),PROCNAME
         MVC   0(4,R2),F4096
*
         EJECT
DOCHGE   DS    0H
*
         CLI   TEKSW,X'FF'
         BE    DOCHGE1
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1H,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOCHGE1  DS    0H
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS1,                                        *****
               OUTPUT=(LINE3,TERM,SINGLE,DATA),                    *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   TEKSW,X'FF'
         BE    DOCHGE2
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1L,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOCHGE2  DS    0H
         L     R15,AGET
         GETLINE PARM=GETBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS2,                                        *****
               TERMGET=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS2)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   STAXBUF,0
         BNE   STAXUSED
*
         L     R1,INAREA
         OC    ACCCENTR,4(R1)
         CLC   ACCCENTR,BLANK
         BNE   DOUREF
         MVC   ACCCENTR,CHGECNTR
**
         EJECT
DOUREF   DS    0H
*
         CLI   TEKSW,X'FF'
         BE    DOUREF1
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1H,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOUREF1  DS    0H
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE4,TERM,SINGLE,DATA),                    *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   TEKSW,X'FF'
         BE    DOUREF2
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE1L,TERM,SINGLE,DATA),                   *****
               TERMPUT=(WAIT,CONTROL),                             *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
DOUREF2  DS    0H
         L     R15,AGET
         GETLINE PARM=GETBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS4,                                        *****
               TERMGET=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS2)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   STAXBUF,0
         BNE   STAXUSED
*
         L     R1,INAREA
         OC    ACCUSERR,4(R1)
         TR    ACCRECD,CHARTAB
         EJECT
EXIT     EQU   *
         STAX
BYPASS   EQU   *
**       EOJ
         LR    R1,R13
         L     R13,HSA
         FREEMAIN R,LV=400,A=(R1)
         L     R14,REG14
         LM    R0,R12,REG0
         LA    R15,0
         BR    R14
*
ATTNEXIT DS    0H
         LA    R15,0
         BR    R14
*
STAXUSED DS    0H
         CLI   STAXBUF,C'Y'
         BE    STAXCLR
         CLI   STAXBUF,X'A8'
         BE    STAXCLR
IOERROR  DS    0H
         OI    0(R7),X'10'
         B     EXIT
*
INUSE    DS    0H
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE5,TERM,SINGLE,DATA),                    *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         L     R15,APUT
         PUTLINE PARM=PUTBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS3,                                        *****
               OUTPUT=(LINE6,TERM,SINGLE,DATA),                    *****
               TERMPUT=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS1)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         L     R15,AGET
         GETLINE PARM=GETBLOCK,                                    *****
               UPT=(R9),                                           *****
               ECT=(R8),                                           *****
               ECB=ECBADS4,                                        *****
               TERMGET=(WAIT,EDIT),                                *****
               ENTRY=(15),                                         *****
               MF=(E,IOPLADS2)
*
         LTR   R15,R15
         BNZ   IOERROR
*
         CLI   STAXBUF,0
         BNE   STAXUSED
*
         L     R1,INAREA
         CLI   4(R1),C'N'
         BE    REUSE
         NI    0(R7),X'80'
         CLI   4(R1),C'Y'
         BE    EXIT
EXITOFF  DS    0H
         OI    0(R7),X'10'
         B     EXIT
*
STAXMESS DC    C'I/O INTERRUPT, REPLY ''Y'' TO RETRY, ''N'' TO QUIT'
*
LINE1H   DC    H'7'
         DC    H'0'
         DC    XL3'1DF815'           HIGH-INTENSITY PROT FIELD
LINE1L   DC    H'7'
         DC    H'0'
         DC    XL3'1D4015'           LOW-INTENSITY UNPROT FIELD
LINE1P   DC    H'7'
         DC    H'0'
         DC    XL3'1D4C15'           NON-DISPLAY CHARACTER
LINE0A   DC    H'29'
         DC    H'0'
         DC    CL25'THIS IS THE NEW TSO LOGON'
LINE0B   DC    H'37'
         DC    H'0'
         DC    CL33'DO NOT SWITCH OFF IN THIS DISPLAY'
LINE0C   DC    H'33'
         DC    H'0'
         DC    CL29'USE <PA1> OR LOGOFF TO ESCAPE'
LINE1    DC    H'16'
         DC    H'0'
         DC    CL12'ENTER USERID'
LINE2    DC    H'18'
         DC    H'0'
         DC    CL14'ENTER PASSWORD'
LINE3    DC    H'54'
         DC    H'0'
         DC    CL50'ENTER CHARGE CENTRE CODE, OR BLANK FOR DEFAULT'
LINE4    DC    H'54'
         DC    H'0'
         DC    CL50'ENTER USER REFERENCE, BLANK IS DEFAULT'
LINE5    DC    H'54'
         DC    H'0'
         DC    CL50'USER-ID IN USE, ''Y'' - ATTEMPT RECONNECT'
LINE6    DC    H'54'
         DC    H'0'
         DC    CL50'''N'' - TRY ANOTHER LOGON, ''Q'' - QUIT'
BLANK    DC    CL8' '
LOGOFF   DC    CL8'LOGOFF'
F4096    DC    F'4096'
*
         LTORG
*
CHARTAB  DC    256C'#'
         ORG   CHARTAB+C'.'
         DC    C'.'
         ORG   CHARTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   CHARTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   CHARTAB+C'S'
         DC    C'STUVWXYZ'
         ORG   CHARTAB+C'0'
         DC    C'0123456789'
         ORG   CHARTAB+256
         EJECT
ACCTAB   DS    0F
         DC    CL7'IBMUSER',CL8'IBMUSER ',CL5'C4003',CL8'IKJACCNT'
*
*                  *USER**      **PASS**      *ACC*      **PROC**
*                  *USER**      **PASS**      *ACC*      **PROC**
*                  *USER**      **PASS**      *ACC*      **PROC**
*                  *USER**      **PASS**      *ACC*      **PROC**
*
TABEND   DC    XL8'FFFFFFFFFFFFFFFF'
*                                                                     *
ACCTDS   DSECT
USERID   DS    CL7
PASSWORD DS    CL8
CHGECNTR DS    CL5
PROCNAME DS    CL8
*
ACCAREA  DSECT
ACCRECD  DS    0CL19
ACCCODE  DS    CL4
ACCOMMA1 DS    CL1
ACCCENTR DS    CL5
ACCOMMA2 DS    CL1
ACCUSERR DS    CL8
USERAREA DSECT
         DS    F
HSA      DS    F
LSA      DS    F
REG14    DS    F
REG15    DS    F
REG0     DS    13F
PUTBLOCK DS    F
         DS    F
         DS    F
         DS    F
GETBLOCK DS    F
INAREA   DS    F
         DS    F
         DS    F
IOPLADS1 DS    F
         DS    F
         DS    F
         DS    F
IOPLADS2 DS    F
         DS    F
         DS    F
         DS    F
ECBADS1  DS    F
ECBADS2  DS    F
ECBADS3  DS    F
ECBADS4  DS    F
AGET     DS    F
APUT     DS    F
TEKSW    DS    CL1
INUSER   DS    CL7
INPASS   DS    CL8
STAXLIST DS    6F
STAXBUF  DS    CL80
*
ADDRLIST DSECT
SWDESC   DS    F                   SWITCHES
IBFDESC  DS    F                   INPUT BUFFER
USERDESC DS    F                   USER-ID
PASSDESC DS    F                   PASSWORD
ACCDESC  DS    F                   ACCOUNT FIELD
PROCDESC DS    F                   PROCEDURE
REGDESC  DS    F                   REGION SIZE
JCLDESC  DS    F                   JCL
NEWPDESC DS    F                   NEW PASSWORD
SYSADESC DS    F                   SYSTEM ATTRIBUTE
USRADESC DS    F                   USER ATTRIBUTE
GENRDESC DS    F                   GENERIC UNIT
UPTDESC  DS    F                   UPT
ECTDESC  DS    F                   ECT
CECBDESC DS    F                   CANCEL ECB
LSCCDESC DS    F                   LAST STEP COMP CODE
PGRDESC  DS    F                   PERFORMANCE GROUP
SYSODESC DS    F                   DEFAULT SYSOUT DEST
GRPDESC  DS    F                   GROUP
         END
/*
//L        EXEC PGM=IEWL,PARM='XREF,LIST,RENT,REUS,NCAL',COND=(8,LE,C)
//SYSLIB   DD   DSN=DCC.SUBLIB,DISP=SHR
//SYSLIN   DD   DSN=*.C.SYSLIN,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSIN    DD   *
 NAME IKJEFLD(R)
//SYSLMOD  DD   DSN=ZZ.TESTLIB,DISP=SHR
//SYSPRINT DD   SYSOUT=P
//SYSUT1   DD   UNIT=VIO
/*
//L2       EXEC PGM=IEWL,PARM='XREF,LIST,RENT,REUS,LET'
//SYSPRINT DD   SYSOUT=P
//SYSUT1   DD   UNIT=VIO
//LPALIB   DD   DSN=SYS1.LPALIB,DISP=SHR
//SYSLMOD  DD   DSN=DCC.EXITLIB,DISP=SHR
//* SYSLMOD  DD   DSN=ZZ.TESTLIB,DISP=SHR
//SYSLIB   DD   DSN=DCC.SUBLIB,DISP=SHR
//TESTLIB  DD   DSN=ZZ.TESTLIB,DISP=SHR
//SYSLIN   DD   *
  INCLUDE TESTLIB(IKJEFLD)
  INCLUDE LPALIB(IKJEFLA)
 ORDER   IKJEFLA(P),IKJEFLIO
 ORDER   IKTLOGFF
 ORDER   IKJEFLP1,IKJEFTBL
 ORDER   IKTIIOM,IKTXINIT
 ORDER   IKTXLOG,IKTLOGR,IKTRPLXT
 ENTRY   IKJEFLA
  NAME IKJEFLA(R)
//******** GENERATE END OF JOB
