//SYSPCHUJ JOB '9999,C4003,8700','CHRIS',CLASS=H,PRTY=9,
//         MSGCLASS=P,NOTIFY=TSOSYSJ
// EXEC ASMFCL,MAC='DCC.MACLIB',MAC1='SYS1.MACLIB'
//*
//* TO UP THE LIMIT: CHANGE LOOP COUNT HERE AND IN IEFUJV
//*
//ASM.SYSIN DD *
IEFUJVRR TITLE 'PROGRAM TO MODIFY IEFUJV'
         REGEQS
         PRINT NOGEN
IEFUJVRR BEGIN 3
         LOAD  EP=IEFUJV
         LR    R4,R0
         LA    R4,100(0,R4)        START OF CHARGE CENTRES
         LA    R5,1000             1000 CHARGE CENTRES ALLOWED -WAS 512
OPEN     DS    0H
         OPEN  (MCDMAS)
         OPEN  (SYSPRINT,OUTPUT)
         MODESET KEY=ZERO
         MVC   KEYAPPL,=C'AAAAA'   START OF FILE
         GET   RPL=GETRPL          TO THE (VSAM) APPLICATION FILE
LOOP     DS    0H
         MVC   0(5,R4),APPLKEY     MOVE TO UJV
         LA    R4,5(0,R4)          NEXT POSITION
         AP    RECCNT,=PL3'1'      INCREMENT
         GET   RPL=GETRPL          NEXT CHARGE CENTRE
         BCT   R5,LOOP             KEEP LOOPING
         SPACE 2
         PUT   SYSPRINT,OVERLOAD   GIVE OVERLOAD WARNING MESSAGE
         SPACE 2
EODAD    DS    0H
         MVC   0(5,R4),ENDAPPL     MOVE TO UJV
         ED    OUTCNT,RECCNT
         PUT   SYSPRINT,OUTLINE    INFORM HOW MANY LOADED
         MODESET KEY=NZERO
         CLOSE (SYSPRINT) CLOSE THE FILES
         CLOSE (MCDMAS)
***      DC    F'0'                MAKE IT DUMP
         EOJ
         EJECT
*
*        CONTROL BLOCKS FOR FILES
*
MCDMAS   ACB   AM=VSAM,                                                *
               DDNAME=MCDMAS,                                          *
               EXLST=EXLST,                                            *
               MACRF=(KEY,SEQ,IN)
*
*
EXLST    EXLST AM=VSAM,                                                *
               EODAD=EODAD
*
GETRPL   RPL   ACB=MCDMAS,                                             *
               AREA=INAREA,                                            *
               AREALEN=1002,                                           *
               OPTCD=(KEY,SEQ,SYN,MVE),                                *
               ARG=KEYAPPL
*
*
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,MACRF=(PM),          *
               LRECL=133,BLKSIZE=1330
*
         EJECT
*
*        WORK AREAS AND CONSTANTS
*
KEYAPPL  DS    CL5                 APPLICATION CODE
ENDAPPL  DC    CL5'*****'          APPLICATION CODE
RECCNT   DC    PL3'0'              RECORD COUNT
*
OVERLOAD DC    CL133' TOO MANY MCDMAS FOR IEFUJV - LAST ONES DROPPED'
OUTLINE  DS    0CL133              OUTPUT LINE
         DC    CL17'  RECORD COUNT = '
OUTCNT   DC    XL6'402020202120'
         DC    CL110' '
*
*
         LTORG
INAREA   DS    0CL1002             IO AREA FOR APPLICATION FILE
APPLKEY  DS    CL5
         DS    CL997               APPLICATION IN TEXT FORM
*
         END
//LKED.SYSLIN  DD   DSN=SYS.SSI.IPLDATE,DISP=SHR
//             DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//             DD   DDNAME=SYSIN
//LKED.SYSLMOD DD   DSN=DCC.UTLIB,DISP=SHR
//* ED.SYSLMOD DD   DSN=ZZ.TESTLIB,DISP=SHR
//LKED.SYSIN   DD   *
 NAME IEFUJVRR(R)
//
