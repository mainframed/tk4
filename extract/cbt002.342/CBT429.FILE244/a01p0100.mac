LMMP0100 TITLE '                     L.E.B. MANUAL MANAGEMENT FACILITY'
***********************************************************************
*                                                                     *
*        PROGRAM: LMMP0100                                            *
*        AUTHOR:  D. THORBY, AUG 1989                                 *
*        DESCRIPTION:                                                 *
*              THIS PROGRAM IS THE LEB'S MANUAL MANAGEMENT FACILITY.  *
*                                                                     *
*        REGISTER USAGE:                                              *
*                                                                     *
*        R2    BASE REGISTER                                          *
*        R3    *SPARE*                                                *
*        R4    *SPARE*                                                *
*        R5    *SPARE*                                                *
*        R6    *SPARE*                                                *
*        R7    *SPARE*                                                *
*        R8    WORK REGISTER                                          *
*        R9    WORK REGISTER                                          *
*        R10   INTERNAL LINKAGE / FILE POINTER                        *
*        R11   BASE REGISTER                                          *
*        R12   BASE REGISTER                                          *
*        R13   SAVE AREA                                              *
*                                                                     *
***********************************************************************
         SPACE 3
LMMP0100 CSECT
         SAVE  (14,12),,'LMMP0100 &SYSDATE &SYSTIME'
         LR    R12,R15             LOAD BASE REGISTER
         LA    R11,4095(,R12)      LOAD SECOND -
         LA    R11,1(,R11)           BASE REGISTER
         LA    R2,4095(,R11)       LOAD THIRD -
         LA    R2,1(,R2)             BASE REGISTER
         USING LMMP0100,R12,R11,R2 ESTABLISH BASE REGISTERS
         LA    R10,SAVEAREA        POINT TO REGISTER SAVE AREA
         ST    R13,4(,R10)         CHAIN SAVE AREAS BACKWARD
         ST    R10,8(,R13)         CHAIN SAVE AREAS FORWARD
         LR    R13,R10             POINT TO REGISTER SAVE AREA
         LOAD  EP=ISPLINK          PICK UP ADDRESS OF INTERFACE ROUTINE
         ST    R0,ISPLINK          SAVE ADDRESS OF INTERFACE ROUTINE
         EJECT
***********************************************************************
*                                                                     *
*        DEFINE ISPF VARIABLES                                        *
*                                                                     *
***********************************************************************
         SPACE 3
         PRINT NOGEN               SUPPRESS MACRO EXPANSIONS
         MVC   VARNAME,=CL8'MANUALNO' MANUAL NUMBER
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,MANUALNO,CHAR,F12),VL,            X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'CATEGORY' MANUAL CATEGORY
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,CATEGORY,CHAR,F8),VL,             X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'KEYNO' ABBREVIATED MANUAL NUMBER
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,KEYNO,CHAR,F9),VL,MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'PRODUCT' PRODUCT NUMBER
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,PRODUCT,CHAR,F8),VL,              X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'TNLLEVEL' LATEST TECHNICAL NEWSLETTER
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,TNLLEVEL,CHAR,F9),VL,             X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'DESCRIPT' DESCRIPTION
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,DESCRIPT,CHAR,F70),VL,            X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'VERSION' MANUAL VERSION
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,VERSION,CHAR,F6),VL,              X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'CURRENCY' CURRENT VERSION INDICATOR
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,CURRENCY,CHAR,F1),VL,             X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'NOCOPIES' NUMBER OF COPIES
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,NOCOPIES,CHAR,F2),VL,             X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'COPYNO' COPY NUMBER
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,COPYNO,CHAR,F2),VL,               X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'GROUP' DISTRIBUTION INFORMATION
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,GROUP,CHAR,F12),VL,MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'LOCATION' DISTRIBUTION INFORMATION
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,LOCATION,CHAR,F12),VL,            X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'CONTACT' DISTRIBUTION INFORMATION
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,CONTACT,CHAR,F12),VL,             X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'STATUS' DISTRIBUTION INFORMATION
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,STATUS,CHAR,F1),VL,MF=(E,CALLLIST)
         SPACE 1
         MVC   VARNAME,=CL8'ZCMD'  ISPF COMMAND LINE
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,ZCMD,CHAR,F79),VL,MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'ROWS'  "ALL" OR "SCAN" ROWS FOR TBDISPL
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,ROWS,CHAR,F4),VL,MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'SEL'   TABLE SELECTION VARIABLE
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,SEL,CHAR,F1),VL,MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'UPDTAUTH' UPDATE AUTHORISATION INDICATOR
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,UPDTAUTH,CHAR,F1),VL,             X
               MF=(E,CALLLIST)
         MVC   VARNAME,=CL8'ZTDSELS' TABLE SELECTION COUNTER
         L     R15,ISPLINK         DEFINE ISPF VARIABLE
         CALL  (15),(VDEFINE,VARNAME,ZTDSELS,FIXED,F4),VL,             X
               MF=(E,CALLLIST)
         PRINT GEN                 PRINT MACRO EXPANSIONS
         SPACE 3
***********************************************************************
*                                                                     *
*        CREATE TEMPORARY ISPF TABLES                                 *
*                                                                     *
***********************************************************************
         SPACE 3
         L     R15,ISPLINK         CREATE TEMPORARY ISPF TABLE
         CALL  (15),(TBCREATE,TABNAME1,KEYLIST1,NAMLIST1,NOWRITE,      X
               REPLACE),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SORT TEMPORARY ISPF TABLE
         CALL  (15),(TBSORT,TABNAME1,SORTLST1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         CREATE TEMPORARY ISPF TABLE
         CALL  (15),(TBCREATE,TABNAME2,KEYLIST2,NAMLIST2,NOWRITE,      X
               REPLACE),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SORT TEMPORARY ISPF TABLE
         CALL  (15),(TBSORT,TABNAME2,SORTLST2),VL,MF=(E,CALLLIST)
         EJECT
***********************************************************************
*                                                                     *
*        READ DATA FILE INTO TABLES                                   *
*                                                                     *
***********************************************************************
         SPACE 3
         RDJFCB FILE               GET DATA SET NAME AND VOLUME SERIAL
         LA    R9,FILEJFCB         POINT TO JFCB
         USING INFMJFCB,R9         MAP THE JFCB
         MVC   DSNAME,JFCBDSNM     PICK UP DATA SET NAME
         MVC   VOLUME,JFCBVOLS     PICK UP VOLUME SERIAL
         DROP  R9                  DROP ADDRESSIBILITY TO JFCB
         RACHECK ENTITY=DSNAME,VOLSER=VOLUME,CLASS='DATASET',          X
               ATTR=UPDATE,APPL='LMMP0100'
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   OPENFILE            NOT AUTHORISED FOR UPDATE
         MVI   UPDTAUTH,C'A'       AUTHORISED FOR UPDATE
OPENFILE OPEN  (FILE,INPUT)        ACCESS THE DATA FILE
RE_OPEN  SR    R9,R9               SET UP DISTRIBUTION REGISTER
         SPACE 1
GETFILE  GET   FILE                READ A RECORD
         CLI   4(R1),0             IS THIS A FILE HEADER ?
         BE    SAVEHEAD            YES - SAVE DATE/TIME STAMP
         MVC   FILEFIX(FILEFIXL),4(R1) MOVE OVER FIXED PORTION
         BAL   R10,GETKEY          BUILD INTERNAL KEY
         IC    R9,#COPIES          PICK UP NUMBER OF COPIES
         CVD   R9,DBLWORD          CONVERT TO PACKED
         MVC   NCPYEDIT,CPY#MASK   MOVE IN EDIT MASK
         ED    NCPYEDIT,DBLWORD+6  NUMBER OF COPIES (DISPLAY)
         LA    R10,FILEFIXL+4(,R1) POINT TO START OF VARIABLE PORTION
         L     R15,ISPLINK         ADD BASE RECORD TO TABLE
         CALL  (15),(TBADD,TABNAME1,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ADDERROR            NON-ZERO - PUT OUT ERROR MESSAGE
         LTR   R9,R9               CHECK NUMBER OF COPIES
         BZ    GETFILE             NO COPIES EXIST !!!!!
         SR    R8,R8               INITIALISE COPY NUMBER
ADDVAR   MVC   FILEVAR(FILEVARL),0(R10) MOVE OVER VARIABLE PORTION
         LA    R8,1(R8)            NEXT COPY NUMBER
         CVD   R8,DBLWORD          STORE COPY NUMBER
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         ADD DISTRIBUTION RECORD TO TABLE
         CALL  (15),(TBADD,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   ADDERROR            NON-ZERO - PUT OUT ERROR MESSAGE
         LA    R10,FILEVARL(,R10)  POINT TO NEXT VARIABLE PORTION
         BCT   R9,ADDVAR           ADD ANOTHER ENTRY IF REQUIRED
         B     GETFILE             GET NEXT RECORD
         SPACE 2
SAVEHEAD MVC   DATETIME,24(R1)     SAVE DATE/TIME STAMP
         B     GETFILE             GET NEXT RECORD
         SPACE 3
EOF      CLOSE FILE                ALL DATA READ INTO TABLES
         L     R15,ISPLINK         POSITION TO TOP OF TABLE
         CALL  (15),(TBTOP,TABNAME1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         POSITION TO TOP OF TABLE
         CALL  (15),(TBTOP,TABNAME2),VL,MF=(E,CALLLIST)
         EJECT
***********************************************************************
*                                                                     *
*        DISPLAY MENU                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
MENU     MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         DISPLAY MENU PANEL
         CALL  (15),(DISPLAY,PNLMENU),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   REWRITE             END - UPDATE FILE
         MVC   FUNCTION,ZCMD       SAVE FUNCTION CODE
         CLI   ZCMD,C'A'           ADD COMMAND ?
         BE    A_ADD               YES - DO IT
         CLI   ZCMD,C'B'           BROWSE COMMAND ?
         BE    B_BROWSE            YES - DO IT
         CLI   ZCMD,C'D'           DELETE COMMAND ?
         BE    GET_KEY             YES - GET RECORD TO BE DELETED
         CLI   ZCMD,C'L'           LOCATE COMMAND ?
         BE    L_LOCATE            YES - DO IT
         CLI   ZCMD,C'S'           SEARCH COMMAND ?
         BE    S_SEARCH            YES - DO IT
         CLI   ZCMD,C'U'           UPDATE COMMAND ?
         BE    GET_KEY             YES - GET RECORD TO BE UPDATED
         L     R15,ISPLINK         UNKNOWN COMMAND
         CALL  (15),(SETMSG,MSGERR02),VL,MF=(E,CALLLIST)
         B     MENU                GET ANOTHER OPTION
         EJECT
***********************************************************************
*                                                                     *
*        ADD A MANUAL                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
A_ADD    BAL   R10,AUTH_CHK        CHECK AUTHORISATION
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME1),VL,MF=(E,CALLLIST)
A_NCLEAR MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         DISPLAY ADD PANEL # 1
         CALL  (15),(DISPLAY,PNLADD1),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   A_RETURN            END COMMAND - GET ANOTHER OPTION
         BAL   R10,GETKEY          BUILD INTERNAL KEY
         L     R15,ISPLINK         ADD BASE RECORD
         CALL  (15),(TBADD,TABNAME1,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   A_ERROR             NON-ZERO - PROBABLY A DUPLICATE
         CLI   NOCOPIES+1,C' '     SINGLE DIGIT NUMBER OF COPIES ?
         BNE   A_PACK              NO - PACK NUMBER OF COPIES
         MVC   NOCOPIES+1(1),NOCOPIES MOVE CHARACTER
         MVI   NOCOPIES,C'0'       ADD LEADING ZERO
A_PACK   PACK  DBLWORD,NOCOPIES    PACK NUMBER OF COPIES
         CVB   R9,DBLWORD          PICK UP NUMBER OF COPIES
         LTR   R9,R9               CHECK FOR ZERO
         BZ    A_EXIT              NO COPIES EXIST !!!!!
         MVC   FILEVAR(FILEVARL),FILEVARD SET UP DEFAULT DIST. INFO.
A_DETAIL CVD   R9,DBLWORD          STORE COPY NUMBER
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         ADD DISTRIBUTION RECORD
         CALL  (15),(TBADD,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         BCT   R9,A_DETAIL         WRITE OUT &NOCOPIES LINES
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME2),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSKIP,TABNAME1,F0),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSARG,TABNAME2),VL,MF=(E,CALLLIST)
A_DISPL  L     R15,ISPLINK         DISPLAY DISTRIBUTION INFORMATION
         CALL  (15),(TBDISPL,TABNAME2,PNLADD2),VL,MF=(E,CALLLIST)
         CH    R15,=H'4'           CHECK RETURN CODE
         BH    A_DELETE            END COMMAND - DELETE THE MANUAL
         BE    A_UPDATE            AT LEAST ONE MODIFIED LINE
         CLC   ZTDSELS,F0          ANY MODIFIED LINES ?
         BE    A_EXIT              NO - ADD IT AS IS
A_UPDATE OC    FILEVAR(FILEVARL),FILEVARB CONVERT TO UPPER CASE
         L     R15,ISPLINK         UPDATE THIS LINE
         CALL  (15),(TBPUT,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         CLC   ZTDSELS,F1          ANY LINES LEFT TO PROCESS ?
         BNH   A_DISPL             NO - REDISPLAY PANEL
         L     R15,ISPLINK         DISPLAY WITHOUT A PANEL
         CALL  (15),(TBDISPL,TABNAME2),VL,MF=(E,CALLLIST)
         B     A_UPDATE            GO AND CHECK WHAT THEY'VE DONE
         SPACE 3
A_DELETE L     R15,ISPLINK         DELETE BASE RECORD
         CALL  (15),(TBDELETE,TABNAME1),VL,MF=(E,CALLLIST)
         ICM   R9,B'0001',#COPIES  PICK UP NUMBER OF COPIES
         BZ    A_RETURN            NO COPIES EXIST !!!!!
A_DEL1   CVD   R9,DBLWORD          STORE COPY NUMBER
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         DELETE DISTRIBUTION RECORD
         CALL  (15),(TBDELETE,TABNAME2),VL,MF=(E,CALLLIST)
         BCT   R9,A_DEL1           DELETE &NOCOPIES LINES
         B     A_RETURN            GET NEXT COMMAND
         SPACE 3
A_ERROR  CH    R15,=H'8'           DUPLICATE ?
         BNE   A_ABORT             NO - ABORT
         L     R15,ISPLINK         DUPLICATE MANUAL NUMBER
         CALL  (15),(SETMSG,MSGADD02),VL,MF=(E,CALLLIST)
         MVC   ZTDSELS,F0          CANCEL TABLE DISPLAY IF ACTIVE
         B     A_RETURN            GET ANOTHER OPTION
         SPACE 3
A_ABORT  L     R15,ISPLINK         ERROR ADDING RECORD TO TABLE
         CALL  (15),(SETMSG,MSGERR01),VL,MF=(E,CALLLIST)
         B     ENDTABLE            CLOSE THE TABLES
         SPACE 3
A_EXIT   L     R15,ISPLINK         MANUAL ADDED
         CALL  (15),(SETMSG,MSGADD01),VL,MF=(E,CALLLIST)
         B     A_RETURN            RETURN TO CALLING ROUTINE
         SPACE 3
A_RETURN CLI   FUNCTION,C'A'       IS THIS ADD ?
         BE    MENU                GET ANOTHER OPTION
         CLI   FUNCTION,C'B'       IS THIS BROWSE ?
         BE    B_RETURN            RETURN TO BROWSE
         B     MENU                UNKNOWN FUNCTION
         EJECT
***********************************************************************
*                                                                     *
*        BROWSE - DISPLAY LIST OF MANUALS AND ACCEPT COMMANDS         *
*                                                                     *
***********************************************************************
         SPACE 3
B_BROWSE MVC   ROWS,=CL4'ALL'      DISPLAY ALL ROWS
         B     B_DISPL
         SPACE 3
B_RETRY  CLC   ZTDSELS,F1          DO WE HAVE AN OUTSTANDING SELECTION
         BH    B_REPEAT            YES - PROCESS IT
B_DISPL  MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         DISPLAY LIST OF MANUALS
         CALL  (15),(TBDISPL,TABNAME1,PNLBROW),VL,MF=(E,CALLLIST)
         CH    R15,=H'4'           CHECK RETURN CODE
         BH    MENU                END COMMAND - RETURN
         CLC   ZTDSELS,F0          ANY MODIFIED LINES ?
         BE    B_DISPL             NO - TRY AGAIN
         B     B_PROCSS            PROCESS A LINE
         SPACE 1
B_REPEAT L     R15,ISPLINK         DISPLAY WITHOUT A PANEL
         CALL  (15),(TBDISPL,TABNAME1),VL,MF=(E,CALLLIST)
B_PROCSS OI    SEL,C' '            ENSURE COMMAND IS IN UPPER CASE
         CLI   SEL,C'C'            COPY TO A NEW RECORD
         BE    B_ADD               SAVE DISPLAY THEN ADD RECORD
         CLI   SEL,C'D'            DELETE THIS RECORD
         BE    B_DELETE            SAVE DISPLAY THEN DELETE RECORD
         CLI   SEL,C'S'            SELECT - VIEW THIS RECORD
         BE    B_SELECT            SAVE DISPLAY THEN DISPLAY RECORD
         CLI   SEL,C'U'            UPDATE - UPDATE THIS RECORD
         BE    B_UPDATE            SAVE DISPLAY THEN UPDATE RECORD
         B     B_RETRY             CHECK FOR ANOTHER SELECTED LINE
         SPACE 3
B_ADD    BAL   R10,AUTH_CHK        CHECK AUTHORISATION
         L     R15,ISPLINK         SAVE CURRENT TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,SAVE),VL,MF=(E,CALLLIST)
         MVC   ZTDSAVE,ZTDSELS     SAVE # SELECTED LINES
         B     A_NCLEAR            ADD A SIMILAR RECORD
         SPACE 3
B_DELETE BAL   R10,AUTH_CHK        CHECK AUTHORISATION
         L     R15,ISPLINK         SAVE CURRENT TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,SAVE),VL,MF=(E,CALLLIST)
         MVC   ZTDSAVE,ZTDSELS     SAVE # SELECTED LINES
         B     D_DELETE            DELETE THIS RECORD
         SPACE 3
B_SELECT L     R15,ISPLINK         SAVE CURRENT TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,SAVE),VL,MF=(E,CALLLIST)
         MVC   ZTDSAVE,ZTDSELS     SAVE # SELECTED LINES
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME2),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSKIP,TABNAME1,F0),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSARG,TABNAME2),VL,MF=(E,CALLLIST)
B_SELRPT L     R15,ISPLINK         DISPLAY DISTRIBUTION INFORMATION
         CALL  (15),(TBDISPL,TABNAME2,PNLVIEW),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BZ    B_SELRPT            ZERO - REDISPLAY
         B     B_RETURN            RESTORE DISPLAY
         SPACE 3
B_UPDATE BAL   R10,AUTH_CHK        CHECK AUTHORISATION
         L     R15,ISPLINK         SAVE CURRENT TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,SAVE),VL,MF=(E,CALLLIST)
         MVC   ZTDSAVE,ZTDSELS     SAVE # SELECTED LINES
         B     U_UPDATE            UPDATE THIS RECORD
         SPACE 3
B_RETURN L     R15,ISPLINK         RESTORE TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,RESTORE),VL,MF=(E,CALLLIST)
         MVC   ZTDSELS,ZTDSAVE     RESTORE # SELECTED LINES
         B     B_RETRY             CHECK FOR ANOTHER SELECTED LINE
         EJECT
***********************************************************************
*                                                                     *
*        DELETE THE CURRENT RECORD                                    *
*                                                                     *
***********************************************************************
         SPACE 3
D_DELETE L     R15,ISPLINK         DISPLAY ARE-YOU-SURE PANEL
         CALL  (15),(DISPLAY,PNLDELT),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   D_RETURN            END COMMAND ENTERED
         L     R15,ISPLINK         DELETE BASE RECORD
         CALL  (15),(TBDELETE,TABNAME1),VL,MF=(E,CALLLIST)
         CLI   NOCOPIES+1,C' '     SINGLE DIGIT NUMBER OF COPIES ?
         BNE   D_PACK              NO - PACK NUMBER OF COPIES
         MVC   NOCOPIES+1(1),NOCOPIES MOVE CHARACTER
         MVI   NOCOPIES,C'0'       ADD LEADING ZERO
D_PACK   PACK  DBLWORD,NOCOPIES    PACK NUMBER OF COPIES
         CVB   R9,DBLWORD          PICK UP NUMBER OF COPIES
         LTR   R9,R9               CHECK FOR ZERO
         BZ    D_MESSGE            NO COPIES EXIST !!!!!
D_DETAIL CVD   R9,DBLWORD          STORE COPY NUMBER
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         DELETE DISTRIBUTION RECORD
         CALL  (15),(TBDELETE,TABNAME2),VL,MF=(E,CALLLIST)
         BCT   R9,D_DETAIL         DELETE &NOCOPIES LINES
D_MESSGE L     R15,ISPLINK         MANUAL DELETED
         CALL  (15),(SETMSG,MSGDEL01),VL,MF=(E,CALLLIST)
D_RETURN CLI   FUNCTION,C'B'       ARE WE IN BROWSE ?
         BE    B_RETURN            RETURN TO BROWSE
         B     MENU                GET ANOTHER OPTION
         EJECT
***********************************************************************
*                                                                     *
*        LOCATE - FIND DISTRIBUTED COPIES OF MANUALS                  *
*                                                                     *
***********************************************************************
         SPACE 3
L_LOCATE L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBTOP,TABNAME2),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME2),VL,MF=(E,CALLLIST)
L_RETRY  MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         GET SEARCH KEYS
         CALL  (15),(DISPLAY,PNLLOCAT),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   MENU                END COMMAND - GET ANOTHER OPTION
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSARG,TABNAME2),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   L_NOARGS            NO ARGUMENTS ESTABLISHED
         MVC   SAVEFIX,FILEFIX     SAVE SEARCH ARGUMENTS
         MVC   SAVEVAR,FILEVAR     SAVE SEARCH ARGUMENTS
         MVC   SAVEKEY,KEYNO       SAVE SEARCH ARGUMENT
         MVC   SAVECOPY,COPYNO     SAVE SEARCH ARGUMENT
         L     R15,ISPLINK         CHECK FOR MANUALS
         CALL  (15),(TBSCAN,TABNAME2),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   L_NOHITS            NO MANUALS FOUND
L_DISPL  MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         DISPLAY LIST OF MANUALS
         CALL  (15),(TBDISPL,TABNAME2,PNLLCTD),VL,MF=(E,CALLLIST)
         CH    R15,=H'4'           CHECK RETURN CODE
         BH    MENU                END COMMAND - RETURN
         CLC   ZTDSELS,F0          ANY MODIFIED LINES ?
         BE    L_DISPL             NO - TRY AGIAN
         B     L_PROCSS            PROCESS A LINE
         SPACE 1
L_REPEAT CLC   ZTDSELS,F1          ANY MORE MODIFIED LINES ?
         BNH   L_DISPL             NO - TRY AGIAN
         L     R15,ISPLINK         DISPLAY WITHOUT A PANEL
         CALL  (15),(TBDISPL,TABNAME2),VL,MF=(E,CALLLIST)
L_PROCSS OI    SEL,C' '            ENSURE COMMAND IS IN UPPER CASE
         CLI   SEL,C'D'            DELETE THIS RECORD
         BE    L_DELETE            SAVE DISPLAY THEN DELETE RECORD
         CLI   SEL,C'S'            SELECT - VIEW THIS RECORD
         BE    L_SELECT            SAVE DISPLAY THEN DISPLAY RECORD
         CLI   SEL,C'U'            UPDATE RECORD
         BE    L_UPDATE            SAVE DISPLAY THEN DISPLAY FOR UPDATE
         B     L_REPEAT            LOOK FOR ANOTHER LINE
         SPACE 3
L_DELETE BAL   R10,AUTH_CHK        CHECK AUTHORISATION
         L     R15,ISPLINK         GET MANUAL RECORD
         CALL  (15),(TBGET,TABNAME1),VL,MF=(E,CALLLIST)
         CLI   NOCOPIES+1,C' '     SINGLE DIGIT NUMBER OF COPIES ?
         BNE   L_PACK              NO - PACK NUMBER OF COPIES
         MVC   NOCOPIES+1(1),NOCOPIES MOVE CHARACTER
         MVI   NOCOPIES,C'0'       ADD LEADING ZERO
L_PACK   PACK  DBLWORD,NOCOPIES    PACK NUMBER OF COPIES
         CVB   R10,DBLWORD         PICK UP NUMBER OF COPIES
L_DELRPT L     R15,ISPLINK         DELETE DISTRIBUTION RECORD
         CALL  (15),(TBDELETE,TABNAME2),VL,MF=(E,CALLLIST)
         PACK  DBLWORD,COPYNO      PACK COPY NUMBER
         CVB   R9,DBLWORD          PICK UP COPY NUMBER
         CR    R9,R10              WAS THAT THE LAST COPY ?
         BE    L_DELEND            YES - REDUCE NUMBER OF COPIES
         LA    R9,1(,R9)           POINT TO NEXT COPY
         CVD   R9,DBLWORD          CONVERT COPY NUMBER TO DECIMAL
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         GET NEXT DISTRIBUTION RECORD
         CALL  (15),(TBGET,TABNAME2),VL,MF=(E,CALLLIST)
         BCTR  R9,0                DECREMENT COPY NUMBER
         CVD   R9,DBLWORD          CONVERT COPY NUMBER TO DECIMAL
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         RE-WRITE DISTRIBUTION RECORD
         CALL  (15),(TBADD,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         LA    R9,1(,R9)           POINT BACK TO NEXT COPY
         CVD   R9,DBLWORD          CONVERT COPY NUMBER TO DECIMAL
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         B     L_DELRPT            DELETE THIS COPY
         SPACE 2
L_DELEND BCTR  R10,0               DECREASE NUMBER OF COPIES
         CVD   R10,DBLWORD         CONVERT NUMBER OF COPIES TO DECIMAL
         MVC   NCPYEDIT,CPY#MASK   SET UP EDIT MASK
         ED    NCPYEDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         RE-WRITE MANUAL RECORD
         CALL  (15),(TBPUT,TABNAME1,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         B     L_DISPL             DISPLAY UPDATED TABLE
         SPACE 3
L_SELECT L     R15,ISPLINK         SAVE CURRENT TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,SAVE),VL,MF=(E,CALLLIST)
         MVC   ZTDSAVE,ZTDSELS     SAVE # SELECTED LINES
         L     R15,ISPLINK         GET MANUAL RECORD
         CALL  (15),(TBGET,TABNAME1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME2),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSKIP,TABNAME1,F0),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSARG,TABNAME2),VL,MF=(E,CALLLIST)
L_SELRPT L     R15,ISPLINK         DISPLAY DISTRIBUTION INFORMATION
         CALL  (15),(TBDISPL,TABNAME2,PNLVIEW),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BZ    L_SELRPT            ZERO - REDISPLAY
         B     L_RETURN            RESTORE DISPLAY
         SPACE 3
L_UPDATE BAL   R10,AUTH_CHK        CHECK AUTHORISATION
         L     R15,ISPLINK         SAVE CURRENT TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,SAVE),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         GET MANUAL RECORD
         CALL  (15),(TBGET,TABNAME1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         DISPLAY RECORD TO BE UPDATED
         CALL  (15),(DISPLAY,PNLLCTUP),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   L_UPEXIT            END COMMAND ENTERED - DO NOT UPDATE
         L     R15,ISPLINK         UPDATE DISTRIBUTION RECORD
         CALL  (15),(TBPUT,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
L_UPEXIT L     R15,ISPLINK         RESTORE TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,RESTORE),VL,MF=(E,CALLLIST)
         B     L_REPEAT            CHECK FOR ANY OTHER SELECTED ROWS
         SPACE 3
L_RETURN L     R15,ISPLINK         RESTORE TABLE DISPLAY
         CALL  (15),(CONTROL,DISPLAY,RESTORE),VL,MF=(E,CALLLIST)
         MVC   ZTDSELS,ZTDSAVE     RESTORE # SELECTED LINES
         CLC   ZTDSELS,F1          ANY MORE MODIFIED LINES ?
         BH    L_REPEAT            YES - GO AND PROCESS THEM
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME2),VL,MF=(E,CALLLIST)
         MVC   FILEFIX(FILEFIXL),SAVEFIX RESTORE SEARCH ARGUMENTS
         MVC   FILEVAR(FILEVARL),SAVEVAR RESTORE SEARCH ARGUMENTS
         MVC   KEYNO,SAVEKEY       RESTORE SEARCH ARGUMENT
         MVC   COPYNO,SAVECOPY     RESTORE SEARCH ARGUMENT
         L     R15,ISPLINK         RESTORE SCAN
         CALL  (15),(TBSARG,TABNAME2),VL,MF=(E,CALLLIST)
         B     L_DISPL             RE-DISPLAY TABLE
         SPACE 3
L_NOARGS L     R15,ISPLINK         NO ARGUMENTS ESTABLISHED
         CALL  (15),(SETMSG,MSGLCT01),VL,MF=(E,CALLLIST)
         B     L_RETRY             TRY AGAIN
         SPACE 3
L_NOHITS L     R15,ISPLINK         NO MANUALS FOUND
         CALL  (15),(SETMSG,MSGLCT02),VL,MF=(E,CALLLIST)
         B     L_RETRY             TRY AGAIN
         EJECT
***********************************************************************
*                                                                     *
*        SEARCH ON USER SUPPLIED KEYS                                 *
*                                                                     *
***********************************************************************
         SPACE 3
S_SEARCH L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBTOP,TABNAME1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME1),VL,MF=(E,CALLLIST)
S_RETRY  MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         GET SEARCH KEYS
         CALL  (15),(DISPLAY,PNLSRCH),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   MENU                END COMMAND - GET ANOTHER OPTION
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSARG,TABNAME1),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   S_NOARGS            NO ARGUMENTS ESTABLISHED
         L     R15,ISPLINK         CHECK FOR MANUALS
         CALL  (15),(TBSCAN,TABNAME1),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   S_NOHITS            NO MANUALS FOUND
         MVC   ROWS,=CL4'SCAN'     DISPLAY ONLY SELECTED ROWS
         MVI   FUNCTION,C'B'       CHANGE FUNCTION TO BROWSE
         B     B_DISPL             DISPLAY SELECTED RECORDS
         SPACE 3
S_NOARGS L     R15,ISPLINK         NO ARGUMENTS ESTABLISHED
         CALL  (15),(SETMSG,MSGSRH01),VL,MF=(E,CALLLIST)
         B     S_RETRY             TRY AGAIN
         SPACE 3
S_NOHITS L     R15,ISPLINK         NO MANUALS FOUND
         CALL  (15),(SETMSG,MSGSRH02),VL,MF=(E,CALLLIST)
         B     S_RETRY             TRY AGAIN
         EJECT
***********************************************************************
*                                                                     *
*        UPDATE CURRENT RECORD                                        *
*                                                                     *
***********************************************************************
         SPACE 3
U_UPDATE CLI   NOCOPIES+1,C' '     SINGLE DIGIT NUMBER OF COPIES ?
         BNE   U_PACK              NO - PACK NUMBER OF COPIES
         MVC   NOCOPIES+1(1),NOCOPIES MOVE CHARACTER
         MVI   NOCOPIES,C'0'       ADD LEADING ZERO
U_PACK   PACK  DBLWORD,NOCOPIES    PACK NUMBER OF COPIES
         CVB   R8,DBLWORD          PICK UP CURRENT NUMBER OF COPIES
         STC   R8,#COPIES          STORE NUMBER OF COPIES (BINARY)
         MVC   SAVEFIX,FILEFIX     SAVE MANUAL INFORMATION FOR COMPARE
         SPACE 1
U_DISPL1 L     R15,ISPLINK         DISPLAY BASE RECORD FOR UPDATE
         CALL  (15),(DISPLAY,PNLUPDT1),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   U_ABORT             END COMMAND - ABORT UPDATE
         CLI   NOCOPIES+1,C' '     SINGLE DIGIT NUMBER OF COPIES ?
         BNE   U_DISPAK            NO - PACK NUMBER OF COPIES
         MVC   NOCOPIES+1(1),NOCOPIES MOVE CHARACTER
         MVI   NOCOPIES,C'0'       ADD LEADING ZERO
U_DISPAK PACK  DBLWORD,NOCOPIES    PACK NUMBER OF COPIES
         CVB   R9,DBLWORD          PICK UP NEW NUMBER OF COPIES
         STC   R9,#COPIES          STORE NUMBER OF COPIES (BINARY)
         CLC   SAVEFIX,FILEFIX     ANY UPDATED FIELDS ?
         BE    U_PART2             NO - UPDATE DISTRIBUTION INFORMATION
         CR    R9,R8               HAS THE NUMBER OF COPIES CHANGED ?
         BE    U_UPDAT1            UNCHANGED - OTHER FIELDS UPDATED
         BH    U_ADDCPY            INCREASED - ADD EXTRA COPIES
         L     R15,ISPLINK         # COPIES REDUCED
         CALL  (15),(SETMSG,MSGUPD01),VL,MF=(E,CALLLIST)
         B     U_DISPL1            TRY AGAIN
         SPACE 3
U_ADDCPY MVC   FILEVAR(FILEVARL),FILEVARD SET UP DEFAULT DIST. INFO.
U_ADDONE CVD   R9,DBLWORD          STORE COPY NUMBER
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         ADD DISTRIBUTION RECORD
         CALL  (15),(TBADD,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         BCTR  R9,0                DECREASE COPY NUMBER
         CR    R9,R8               DOWN TO OLD COPY NUMBER ?
         BH    U_ADDONE            NO - WRITE ANOTHER RECORD
         SPACE 1
U_UPDAT1 CLC   CURRENCY,SAVEFIX+CURRENCY-FILEFIX IS CURRENCY CHANGED ?
         BE    U_RWRITE            NO - REWRITE MANUAL RECORD
         LTR   R9,R9               ARE THERE ANY COPIES TO BE UPDATED ?
         BZ    U_RWRITE            NO - REWRITE MANUAL RECORD
         IC    R8,CURRENCY         SAVE NEW CURRENCY
U_UPDATC CVD   R9,DBLWORD          STORE COPY NUMBER
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         RETRIEVE DISTRIBUTION RECORD
         CALL  (15),(TBGET,TABNAME2),VL,MF=(E,CALLLIST)
         STC   R8,CURRENCY         STORE NEW CURRENCY
         L     R15,ISPLINK         UPDATE DISTRIBUTION RECORD
         CALL  (15),(TBPUT,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         BCT   R9,U_UPDATC         UPDATE ALL COPIES
         SPACE 1
U_RWRITE L     R15,ISPLINK         UPDATE MANUAL RECORD
         CALL  (15),(TBPUT,TABNAME1,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         SPACE 1
U_PART2  CLI   #COPIES,0           ANY DISTRIBUTION INFORMATION ?
         BZ    U_RETURN            NO - RETURN TO CALLER
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME2),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         RE-READ CURRENT RECORD
         CALL  (15),(TBSKIP,TABNAME1,F0),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SET UP SCAN MASK
         CALL  (15),(TBSARG,TABNAME2),VL,MF=(E,CALLLIST)
U_DISPL2 MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         DISPLAY DISTRIBUTION INFORMATION
         CALL  (15),(TBDISPL,TABNAME2,PNLUPDT2),VL,MF=(E,CALLLIST)
         CH    R15,=H'4'           CHECK RETURN CODE
         BH    U_RETURN            END COMMAND - RETURN
         CLC   ZTDSELS,F0          ANY MODIFIED LINES ?
         BE    U_RETURN            NO - RETURN TO CALLER
         B     U_PROCSS            PROCESS A LINE
         SPACE 1
U_REPEAT CLC   ZTDSELS,F1          ANY MORE MODIFIED LINES ?
         BNH   U_DISPL2            NO - TRY AGIAN
         L     R15,ISPLINK         DISPLAY WITHOUT A PANEL
         CALL  (15),(TBDISPL,TABNAME2),VL,MF=(E,CALLLIST)
U_PROCSS OI    SEL,C' '            ENSURE COMMAND IS IN UPPER CASE
         CLI   SEL,C'D'            DELETE THIS RECORD
         BE    U_DELETE            DELETE DISTRIBUTION RECORD
         OC    FILEVAR(FILEVARL),FILEVARB CONVERT TO UPPER CASE
         L     R15,ISPLINK         UPDATE THIS LINE
         CALL  (15),(TBPUT,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         B     U_REPEAT            GET NEXT COMMAND
         SPACE 3
U_DELETE CLI   NOCOPIES+1,C' '     SINGLE DIGIT NUMBER OF COPIES ?
         BNE   U_PACK2             NO - PACK NUMBER OF COPIES
         MVC   NOCOPIES+1(1),NOCOPIES MOVE CHARACTER
         MVI   NOCOPIES,C'0'       ADD LEADING ZERO
U_PACK2  PACK  DBLWORD,NOCOPIES    PACK NUMBER OF COPIES
         CVB   R10,DBLWORD         PICK UP NUMBER OF COPIES
U_DELRPT L     R15,ISPLINK         DELETE DISTRIBUTION RECORD
         CALL  (15),(TBDELETE,TABNAME2),VL,MF=(E,CALLLIST)
         PACK  DBLWORD,COPYNO      PACK COPY NUMBER
         CVB   R9,DBLWORD          PICK UP COPY NUMBER
         CR    R9,R10              WAS THAT THE LAST COPY ?
         BE    U_DELEND            YES - REDUCE NUMBER OF COPIES
         LA    R9,1(,R9)           POINT TO NEXT COPY
         CVD   R9,DBLWORD          CONVERT COPY NUMBER TO DECIMAL
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         GET NEXT DISTRIBUTION RECORD
         CALL  (15),(TBGET,TABNAME2),VL,MF=(E,CALLLIST)
         BCTR  R9,0                DECREMENT COPY NUMBER
         CVD   R9,DBLWORD          CONVERT COPY NUMBER TO DECIMAL
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         RE-WRITE DISTRIBUTION RECORD
         CALL  (15),(TBADD,TABNAME2,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         LA    R9,1(,R9)           POINT BACK TO NEXT COPY
         CVD   R9,DBLWORD          CONVERT COPY NUMBER TO DECIMAL
         MVC   CPY#EDIT,CPY#MASK   SET UP EDIT MASK
         ED    CPY#EDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         B     U_DELRPT            DELETE THIS COPY
         SPACE 2
U_DELEND BCTR  R10,0               DECREASE NUMBER OF COPIES
         CVD   R10,DBLWORD         CONVERT NUMBER OF COPIES TO DECIMAL
         MVC   NCPYEDIT,CPY#MASK   SET UP EDIT MASK
         ED    NCPYEDIT,DBLWORD+6  CONVERT COPY NUMBER TO DISPLAY
         L     R15,ISPLINK         RE-WRITE MANUAL RECORD
         CALL  (15),(TBPUT,TABNAME1,SPFDUMMY,ORDER),VL,MF=(E,CALLLIST)
         B     U_DISPL2            RE-DISPLAY TABLE
         SPACE 3
U_RETURN L     R15,ISPLINK         RECORD UPDATED
         CALL  (15),(SETMSG,MSGUPD02),VL,MF=(E,CALLLIST)
U_ABORT  CLI   FUNCTION,C'B'       IS THIS BROWSE ?
         BE    B_RETURN            RETURN TO BROWSE
         B     MENU                GET ANOTHER OPTION
         EJECT
***********************************************************************
*                                                                     *
*        OUT OF LINE ROUTINES                                         *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        ERROR ADDING A RECORD TO THE TABLE                           *
*                                                                     *
***********************************************************************
         SPACE 3
ADDERROR L     R15,ISPLINK         ERROR ADDING RECORD TO TABLE
         CALL  (15),(SETMSG,MSGERR01),VL,MF=(E,CALLLIST)
         CLOSE FILE                ABORT PROGRAM
         B     ENDTABLE            CLOSE THE TABLES
         SPACE 3
***********************************************************************
*                                                                     *
*        GET A RECORD TO BE UPDATED/DELETED                           *
*                                                                     *
***********************************************************************
         SPACE 3
GET_KEY  BAL   R10,AUTH_CHK        CHECK AUTHORISATION
         L     R15,ISPLINK         CLEAR TABLE VARIABLES
         CALL  (15),(TBVCLEAR,TABNAME1),VL,MF=(E,CALLLIST)
GET_RPT  MVC   ZCMD,ZCMD-1         BLANK OUT COMMAND LINE
         L     R15,ISPLINK         DISPLAY PANEL
         CALL  (15),(DISPLAY,PNLGETK),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   MENU                END COMMAND - GET ANOTHER OPTION
         L     R15,ISPLINK         GET RECORD
         CALL  (15),(TBGET,TABNAME1),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   GETERROR            NON-ZERO - RECORD NOT FOUND
GETRETRN CLI   FUNCTION,C'D'       IS THE FUNCTION DELETE ?
         BE    D_DELETE            YES - DELETE THE RECORD
         CLI   FUNCTION,C'U'       IS THE FUNCTION UPDATE ?
         BE    U_UPDATE            YES - UPDATE THE RECORD
         B     MENU                INTERNAL ERROR - ABORT THE FUNCTION
         SPACE 3
GETERROR L     R15,ISPLINK         MANUAL NOT FOUND
         CALL  (15),(SETMSG,MSGERR03),VL,MF=(E,CALLLIST)
         B     GET_RPT             TRY AGAIN
         EJECT
***********************************************************************
*                                                                     *
*        CHECK USER'S AUTHORITY TO UPDATE FILE                        *
*                                                                     *
***********************************************************************
         SPACE 3
AUTH_CHK CLI   UPDTAUTH,C'U'       ALREADY UPDATING ?
         BER   R10                 YES - RETURN
         CLI   UPDTAUTH,C'N'       NOT AUTHORISED ?
         BE    NOT_AUTH            YES - PUT OUT MESSAGE
         ENQ   (ENQNAME,DSNAME,E,,SYSTEMS),RET=USE
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   IN_USE              NOT AVAILABLE
         MVI   UPDTAUTH,C'U'       INDICATE UPDATING
         OPEN  (FILE,INPUT)        RE-ACCESS THE FILE
         GET   FILE                READ FILE HAEDER
         CLC   DATETIME,24(R1)     HAS THE FILE BEEN UPDATED ?
         BE    AUTH_OK             NO - CONTINUE
         MVC   DATETIME,24(R1)     SAVE NEW DATE/TIME STAMP
         L     R15,ISPLINK         FILE CHANGED
         CALL  (15),(SETMSG,MSGERR07),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         DELETE TEMPORARY ISPF TABLE
         CALL  (15),(TBEND,TABNAME1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         DELETE TEMPORARY ISPF TABLE
         CALL  (15),(TBEND,TABNAME2),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         CREATE TEMPORARY ISPF TABLE
         CALL  (15),(TBCREATE,TABNAME1,KEYLIST1,NAMLIST1,NOWRITE,      X
               REPLACE),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SORT TEMPORARY ISPF TABLE
         CALL  (15),(TBSORT,TABNAME1,SORTLST1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         CREATE TEMPORARY ISPF TABLE
         CALL  (15),(TBCREATE,TABNAME2,KEYLIST2,NAMLIST2,NOWRITE,      X
               REPLACE),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         SORT TEMPORARY ISPF TABLE
         CALL  (15),(TBSORT,TABNAME2,SORTLST2),VL,MF=(E,CALLLIST)
         B     RE_OPEN             RE-LOAD FILE
         SPACE 3
AUTH_OK  CLOSE FILE                FINISHED WITH FILE
         BR    R10                 RETURN TO CALLER
         SPACE 3
IN_USE   L     R15,ISPLINK         FILE IN USE
         CALL  (15),(SETMSG,MSGERR08),VL,MF=(E,CALLLIST)
         B     AUTH_RET            RETURN TO CALLING ROUTINE
         SPACE 3
NOT_AUTH L     R15,ISPLINK         USER NOT AUTHORISED
         CALL  (15),(SETMSG,MSGERR09),VL,MF=(E,CALLLIST)
AUTH_RET CLI   FUNCTION,C'B'       ARE WE IN BROWSE/SEARCH ?
         BE    B_DISPL             YES - RETURN
         CLI   FUNCTION,C'L'       ARE WE IN LOCATE ?
         BE    L_DISPL             YES - RETURN
         B     MENU                GET ANOTHER COMMAND
         EJECT
***********************************************************************
*                                                                     *
*        END OF PROGRAM - UPDATE THE FILE IF AUTHORISED               *
*                                                                     *
***********************************************************************
         SPACE 3
REWRITE  CLI   UPDTAUTH,C'U'       HAS THE USER UPDATED THE FILE ?
         BNE   ENDTABLE            FILE NOT UPDATED - DO NOT SAVE
         L     R15,ISPLINK         POSITION TO TOP OF TABLE
         CALL  (15),(TBTOP,TABNAME1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         POSITION TO TOP OF TABLE
         CALL  (15),(TBTOP,TABNAME2),VL,MF=(E,CALLLIST)
OPENOUT  OPEN  (FILE,OUTPUT)       ACCESS THE DATA FILE
         TM    FILE+DCBOFLGS-IHADCB,DCBOFOPN  FILE OPEN SUCCESSFUL ?
         BZ    OPENFAIL            NO - TELL THE USER
         TIME  DEC                 GET CURRENT DATE AND TIME
         STM   R0,R1,DATETIME      STORE DATE AND TIME
         PUT   FILE                GET BUFFER AREA FOR HEADER RECORD
         MVC   0(4,R1),=H'32,0'    MOVE IN RDW
         MVC   4(20,R1),3(R1)      SET KEY FIELDS TO ZERO
         MVC   24(8,R1),DATETIME   MOVE IN DATE/TIME STAMP
         SPACE 1
GETENTRY L     R15,ISPLINK         GET NEXT ENTRY FROM TABLE
         CALL  (15),(TBSKIP,TABNAME1),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   CLOSFILE            END OF TABLE - CLOSE FILE
         PUT   FILE                GET BUFFER AREA FOR RECORD
         CLI   NOCOPIES+1,C' '     SINGLE DIGIT NUMBER OF COPIES ?
         BNE   Z_PACK              NO - PACK NUMBER OF COPIES
         MVC   NOCOPIES+1(1),NOCOPIES MOVE CHARACTER
         MVI   NOCOPIES,C'0'       ADD LEADING ZERO
Z_PACK   PACK  DBLWORD,NOCOPIES    PACK NUMBER OF COPIES
         CVB   R9,DBLWORD          PICK UP NUMBER OF COPIES
         STC   R9,#COPIES          STORE NUMBER OF COPIES
         LR    R10,R9              NUMBER OF COPIES
         MH    R10,=AL2(FILEVARL)  * LENGTH OF VARIABLE PORTION
         AH    R10,=AL2(FILEFIXL+4) + LENGTH OF FIXED PORTION
         STH   R10,0(,R1)          STORE RECORD LENGTH
         MVC   2(2,R1),=H'0'       COMPLETE RDW
         MVC   4(FILEFIXL,R1),FILEFIX MOVE OVER FIXED PORTION
         LA    R10,FILEFIXL+4(,R1)   POINT TO START OF VARIABLE PORTION
         LTR   R9,R9               CHECK NUMBER OF COPIES
         BZ    GETENTRY            NO COPIES EXIST !!!!!
GETVAR   L     R15,ISPLINK         ADD DISTRIBUTION RECORD TO TABLE
         CALL  (15),(TBSKIP,TABNAME2),VL,MF=(E,CALLLIST)
         MVC   0(FILEVARL,R10),FILEVAR MOVE OVER VARIABLE PORTION
         LA    R10,FILEVARL(,R10)  POINT TO NEXT VARIABLE PORTION
         BCT   R9,GETVAR           ADD ANOTHER ENTRY IF REQUIRED
         B     GETENTRY            GET NEXT ENTRY
         SPACE 3
CLOSFILE CLOSE FILE                ALL DATA WRITTEN TO FILE
         DEQ   (ENQNAME,DSNAME,,SYSTEMS)
         L     R15,ISPLINK         FILE UPDATED
         CALL  (15),(SETMSG,MSGEND01),VL,MF=(E,CALLLIST)
         SPACE 3
***********************************************************************
*                                                                     *
*        END OF PROGRAM - CLOSE THE TABLES                            *
*                                                                     *
***********************************************************************
         SPACE 3
ENDTABLE L     R15,ISPLINK         DELETE TEMPORARY ISPF TABLE
         CALL  (15),(TBEND,TABNAME1),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         DELETE TEMPORARY ISPF TABLE
         CALL  (15),(TBEND,TABNAME2),VL,MF=(E,CALLLIST)
         SPACE 3
***********************************************************************
*                                                                     *
*        END OF PROGRAM - RETURN TO CALLER                            *
*                                                                     *
***********************************************************************
         SPACE 3
RETURN   L     R13,4(,R13)         PICK UP PREVIOUS SAVE AREA
         RETURN (14,12),RC=0       RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        SUBROUTINES                                                  *
*                                                                     *
***********************************************************************
         SPACE 3
GETKEY   MVC   KEYNO(3),MANUALNO+1 DROP USAGE CODE
         MVC   KEYNO+3(4),MANUALNO+5 DROP HYPHEN
         MVC   KEYNO+7(2),MANUALNO+10 MOVE IN VERSION
         CLI   KEYNO+8,C' '        TWO DIGIT VERSION ?
         BNER  R10                 YES - RETURN
         MVC   KEYNO+8(1),KEYNO+7  MOVE VERSION
         MVI   KEYNO+7,C'0'        FILL IN HIGH ORDER DIGIT
         BR    R10                 RETURN
         EJECT
***********************************************************************
*                                                                     *
*        THE FILE CANNOT BE OPENED FOR OUTPUT !                       *
*                                                                     *
***********************************************************************
         SPACE 3
OPENFAIL L     R15,ISPLINK         DISPLAY FAILURE PANEL
         CALL  (15),(DISPLAY,PNLOPENF),VL,MF=(E,CALLLIST)
         LTR   R15,R15             CHECK RETURN CODE
         BZ    OPENOUT             ENTER PRESSED - RETRY THE OPEN
         B     ENDTABLE            ABORT UPDATE
         EJECT
***********************************************************************
*                                                                     *
*        SAVE AREA, VARIABLE POOL AND FILE LAYOUTS                    *
*                                                                     *
***********************************************************************
         SPACE 3
SAVEAREA DC    18F'0'              REGISTER SAVE AREA
ISPLINK  DS    A                   ADDRESS OF ISPF INTERFACE ROUTINE
CALLLIST CALL  ,(,,,,,),VL,MF=L    LIST FORM OF CALL MACRO
VARNAME  DS    CL8                 ISPF VARIABLE NAME
         SPACE 3
FILE     DCB   DDNAME=MMPFILE,EODAD=EOF,MACRF=(GL,PL),DSORG=PS,        X
               EXLST=FILEEXIT
FILEEXIT DS    0F                  FILE DCB EXIT LIST
         DC    X'87'               JFCB EXIT + LAST
         DC    AL3(FILEJFCB)       ADDRESS OF JFCB
FILEJFCB DC    XL176'00'           FILE JFCB
DSNAME   DS    CL44                DATA SET NAME
VOLUME   DS    CL6                 VOLUME SERIAL
ENQNAME  DC    CL8'LMMPENQ'        ENQ QNAME
DATETIME DC    2F'0'               DATE/TIME STAMP
         SPACE 1
FILEFIX  EQU   *                   FIXED PORTION OF RECORD
CATEGORY DC    CL8' '              CATEGORY
MANUALNO DC    CL12' '             MANUAL NUMBER
PRODUCT  DC    CL8' '              PRODUCT
TNLLEVEL DC    CL9' '              LATEST TECHNICAL NEWSLETTER
DESCRIPT DC    CL70' '             DESCRIPTION
VERSION  DC    CL6' '              MANUAL VERSION
CURRENCY DC    CL1' '              CURRENT VERSION INDICATOR
#COPIES  DC    AL1(0)              NUMBER OF COPIES (BINARY)
FILEFIXL EQU   *-FILEFIX           LENGTH OF FIXED PORTION OF RECORD
         SPACE 1
FILEVAR  EQU   *                   VARIABLE PORTION OF RECORD
GROUP    DC    CL12' '             DISTRIBUTION INFORMATION
CONTACT  DC    CL12' '             DISTRIBUTION INFORMATION
LOCATION DC    CL12' '             DISTRIBUTION INFORMATION
STATUS   DC    CL1' '              DISTRIBUTION INFORMATION
FILEVARL EQU   *-FILEVAR           LENGTH OF VARIABLE PORTION OF RECORD
FILEVARD DS    0CL(FILEVARL)       DEFAULTS FOR VARIABLE FIELDS
         DC    CL12'UNASSIGNED'    DEFAULT GROUP
         DC    CL12'LIBRARIAN'     DEFAULT CONTACT
         DC    CL12'LIBRARY'       DEFAULT LOCATION
         DC    CL1' '              DEFAULT STATUS
FILEVARB DC    CL(FILEVARL)' '     CONVERT DIST. INFO. TO UPPER CASE
         SPACE 1
SAVEFIX  DS    CL(FILEFIXL)        SAVED COPY OF FIXED INFORMATION
SAVEVAR  DS    CL(FILEVARL)        SAVED COPY OF VARIABLE INFORMATION
SAVEKEY  DS    CL(L'KEYNO)         SAVED COPY OF KEY
SAVECOPY DS    CL(L'COPYNO)        SAVED COPY OF COPY NUMBER
         SPACE 3
***********************************************************************
*                                                                     *
*        ISPF VARIABLES                                               *
*                                                                     *
***********************************************************************
         SPACE 1
         DC    C' '                FILL CHARACTER FOR COMMAND LINE
ZCMD     DC    CL79' '             COMMAND LINE
FUNCTION DC    CL1' '              FUNCTION CODE
KEYNO    DC    CL9' '              ABBREVIATED MANUAL NUMBER
NCPYEDIT DS    0CL4
         DC    CL2'  '             FILLER
NOCOPIES DC    CL2' 0'             NUMBER OF COPIES (DISPLAY)
CPY#EDIT DS    0CL4
         DC    CL2'  '             FILLER
COPYNO   DC    CL2'00'             COPY NUMBER
CPY#MASK DC    X'F0212020'         EDIT MASK FOR NO COPIES AND COPY NO
ROWS     DC    CL4'ALL '           ROWS PARAMETER FOR TBDISPL
SEL      DC    CL1' '              SELECTION FIELD FOR TABLE DISPLAYS
UPDTAUTH DC    C'N'                IS USER AUTHORISED TO UPDATE ?
*                                  N = NOT AUTHORISED TO UPDATE
*                                  A = AUTHORISED TO UPDATE
*                                  U = UPDATED AND ENQ OBTAINED
DBLWORD  DS    D                   WORK AREA FOR NUMERIC CONVERSION
ZTDSELS  DC    F'0'                COUNT OF SELECTED ROWS IN TABLE
ZTDSAVE  DS    F                   SAVED COUNT OF SELECTED ROWS
         SPACE 3
***********************************************************************
*                                                                     *
*        LITERALS                                                     *
*                                                                     *
***********************************************************************
         SPACE 1
SPFDUMMY DC    CL8' '              DUMMY PARAMETER FOR ISPF CALLS
CHAR     DC    CL8'CHAR'
CONTROL  DC    CL8'CONTROL'
DISPLAY  DC    CL8'DISPLAY'
FIXED    DC    CL8'FIXED'
NOWRITE  DC    CL8'NOWRITE'
ORDER    DC    CL8'ORDER'
REPLACE  DC    CL8'REPLACE'
RESTORE  DC    CL8'RESTORE'
SAVE     DC    CL8'SAVE'
SETMSG   DC    CL8'SETMSG'
TBADD    DC    CL8'TBADD'
TBCREATE DC    CL8'TBCREATE'
TBDELETE DC    CL8'TBDELETE'
TBDISPL  DC    CL8'TBDISPL'
TBEND    DC    CL8'TBEND'
TBGET    DC    CL8'TBGET'
TBPUT    DC    CL8'TBPUT'
TBSARG   DC    CL8'TBSARG'
TBSCAN   DC    CL8'TBSCAN'
TBSKIP   DC    CL8'TBSKIP'
TBSORT   DC    CL8'TBSORT'
TBTOP    DC    CL8'TBTOP'
TBVCLEAR DC    CL8'TBVCLEAR'
VDEFINE  DC    CL8'VDEFINE'
F0       DC    F'0'
F1       DC    F'1'
F2       DC    F'2'
F4       DC    F'4'
F6       DC    F'6'
F8       DC    F'8'
F9       DC    F'9'
F12      DC    F'12'
F70      DC    F'70'
F79      DC    F'79'
         SPACE 1
TABNAME1 DC    C'LMMPT100'
KEYLIST1 DC    C'(MANUALNO)'
NAMLIST1 DC    C'(CATEGORY KEYNO PRODUCT TNLLEVEL DESCRIPT VERSION CURRX
               ENCY NOCOPIES)'
SORTLST1 DC    C'(CATEGORY,C,A,KEYNO,C,A)'
TABNAME2 DC    C'LMMPT200'
KEYLIST2 DC    C'(MANUALNO COPYNO)'
NAMLIST2 DC    C'(CATEGORY KEYNO CURRENCY GROUP LOCATION CONTACT STATUSX
               )'
SORTLST2 DC    C'(CATEGORY,C,A,KEYNO,C,A,COPYNO,N,A)'
         SPACE 1
PNLMENU  DC    CL8'LMMPP010'       MENU PANEL
PNLADD1  DC    CL8'LMMPP020'       ADD MANUAL PANEL
PNLADD2  DC    CL8'LMMPP030'       ADD DISTRIBUTION PANEL
PNLBROW  DC    CL8'LMMPP040'       BROWSE MANUAL LIST PANEL
PNLVIEW  DC    CL8'LMMPP050'       BROWSE - VIEW PANEL
PNLDELT  DC    CL8'LMMPP060'       DELETE - ARE-YOU-SURE PANEL
PNLGETK  DC    CL8'LMMPP070'       GET KEY PANEL
PNLSRCH  DC    CL8'LMMPP080'       GET SEARCH ARGUMENTS PANEL
PNLLOCAT DC    CL8'LMMPP090'       GET LOCATE ARGUMENTS PANEL
PNLLCTD  DC    CL8'LMMPP100'       LOCATE DISPLAY PANEL
PNLLCTUP DC    CL8'LMMPP110'       LOCATE UPDATE PANEL
PNLUPDT1 DC    CL8'LMMPP120'       UPDATE MANUAL PANEL
PNLUPDT2 DC    CL8'LMMPP130'       UPDATE DISTRIBUTION PANEL
PNLOPENF DC    CL8'LMMPP140'       OPEN FAILURE PANEL
MSGEND01 DC    CL8'LMMPM000'       FILE UPDATED
MSGADD01 DC    CL8'LMMPM010'       MANUAL ADDED
MSGADD02 DC    CL8'LMMPM011'       DUPLICATE MANUAL NUMBER
MSGDEL01 DC    CL8'LMMPM020'       MANUAL DELETED
MSGSRH01 DC    CL8'LMMPM030'       NO SEARCH KEYS
MSGSRH02 DC    CL8'LMMPM031'       NO MANUALS FOUND
MSGLCT01 EQU   MSGSRH01            NO SEARCH KEYS
MSGLCT02 EQU   MSGSRH02            NO MANUALS FOUND
MSGUPD01 DC    CL8'LMMPM040'       # COPIES REDUCED
MSGUPD02 DC    CL8'LMMPM041'       RECORD UPDATED
MSGERR01 DC    CL8'LMMPM090'       ERROR ADDING RECORD
MSGERR02 DC    CL8'LMMPM091'       UNKNOWN COMMAND - MENU
MSGERR03 DC    CL8'LMMPM092'       MANUAL NOT FOUND - GET
MSGERR07 DC    CL8'LMMPM097'       FILE CHANGED - RELOAD
MSGERR08 DC    CL8'LMMPM098'       FILE IN USE - TRY LATER
MSGERR09 DC    CL8'LMMPM099'       USER NOT AUTHORISED
         SPACE 3
         LTORG
         YREGS
         DSECT
         IEFJFCBN                  MAP JFCB
         DCBD  DSORG=PS
         END   LMMP0100
