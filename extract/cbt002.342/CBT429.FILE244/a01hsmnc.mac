LHSMNCAT TITLE '                  FIND UNCATALOGUED MIGRATED DATA SETS'
***********************************************************************
*                                                                     *
*   REGISTER USAGE :-                                                 *
*                                                                     *
*      R2   -  VSAM RECORD POINTER                                    *
*      R12  -  BASE REGISTER                                          *
*      R13  -  SAVE AREA                                              *
*                                                                     *
***********************************************************************
         SPACE 3
LHSMNCAT CSECT
         SAVE  (14,12),,'LHSMNCAT &SYSDATE &SYSTIME'
         LR    R12,R15             ESTABLISH -
         USING LHSMNCAT,R12        BASE REGISTER
         LA    R10,SAVEAREA        ADDRESS REGISTER SAVE AREA
         ST    R10,8(R13)          CHAIN SAVE AREAS FORWARD
         ST    R13,4(R10)          CHAIN SAVE AREAS BACKWARD
         LR    R13,R10             ADDRESS SAVE AREA
         SPACE 3
         OPEN  (MCDS,,PRINT,OUTPUT,NOMIGRAT,OUTPUT) OPEN FILES
         TM    NOMIGRAT+DCBOFLGS-IHADCB,DCBOFOPN  OPEN SUCCESSFUL ?
         BO    GETMCDS             YES - CONTINUE
         OI    ZAP+1,X'F0'         BYPASS VOLUME CHECKING
         OI    EOF+1,X'F0'         BYPASS CLOSE STATEMENT
         SPACE 3
GETMCDS  GET   RPL=RPL             GET A RECORD
         L     R2,POINTER          ADDRESS RETRIEVED RECORD
         CLI   46(R2),0            CHECK FOR TYPE D RECORD
         BNE   GETMCDS             IGNORE ALL OTHER RECORD TYPES
         TM    70(R2),B'10000000'  CHECK FOR MIGRATION COPY
         BZ    GETMCDS             NO - IGNORE
         TM    70(R2),B'00010010'  CHECK FOR DELETED/RECALLED
         BNZ   GETMCDS             YES - IGNORE
         MVC   DSNAME,0(R2)        COPY DSNAME TO WORK AREA
         LOCATE CAMLST             IS THE DATA SET CATALOGUED ?
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   PUTPRINT            DATA SET NOT CATALOGUED
ZAP      NOP   GETMCDS             IF NOMIGRAT STATEMENT MISSING
         CLC   =C'MIGRAT',LOCAREA+6 IS THE VOLUME=MIGRAT
         BE    GETMCDS             YES - GET ANOTHER
         MVC   44(6,R2),LOCAREA+6  MOVE VOLSER AFTER DSNAME
         PUT   NOMIGRAT,(2)        PRINT DATA SET NAME AND VOLUME
         B     GETMCDS             GET ANOTHER RECORD
         SPACE 3
PUTPRINT PUT   PRINT,(2)           PRINT DATA SET NAME
         B     GETMCDS             GET ANOTHER RECORD
         EJECT
EOF      NOP   EOF2                IF NOMIGRAT STATEMENT MISSING
         CLOSE (NOMIGRAT)          END OF FILE
EOF2     CLOSE (MCDS,,PRINT)       END OF FILE
         SPACE 3
RETURN   L     R13,4(R13)          RESTORE HIGHER SAVE AREA
         RETURN (14,12),RC=0       RETURN TO CALLER
         EJECT
SAVEAREA DC    18F'0'              REGISTER SAVE AREA
ECB      DC    F'0'                VSAM ECB
POINTER  DC    F'0'                ADDRESS OF RETRIEVED RECORD
CAMLST   CAMLST NAME,DSNAME,,LOCAREA
DSNAME   DC    CL44' '
LOCAREA  DS    0D
         DS    265C
         PRINT NOGEN
PRINT    DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,                    X
               RECFM=FB,LRECL=44,BLKSIZE=3124
NOMIGRAT DCB   DDNAME=NOMIGRAT,MACRF=(PM),DSORG=PS,                    X
               RECFM=FB,LRECL=50,BLKSIZE=3150
MCDS     ACB   AM=VSAM,DDNAME=MCDS,MACRF=(KEY,SEQ,IN),EXLST=EXLST
EXLST    EXLST AM=VSAM,EODAD=EOF
RPL      RPL   ACB=MCDS,AM=VSAM,AREA=POINTER,AREALEN=4,ECB=ECB,        X
               OPTCD=(KEY,SEQ,FWD,SYN,LOC)
         LTORG
         YREGS
         DCBD  DSORG=PS
         END   LHSMNCAT
