         MACRO
&NAME    CLEAR &RC=0,&RE=NO,&PRINT=NOGEN,&LDSA=YES
         AIF   ('&PRINT' EQ 'GEN').TESTRE
         PUSH  PRINT
         PRINT NOGEN
.TESTRE  AIF   ('&RE' EQ 'NO').LOAD13
         AIF   ('&RE' EQ 'YES').RESET13
         MNOTE 4,'INVALID SPECIFICATION FOR ''RE'' ''NO'' ASSUMED'
         AGO   .LOAD13
.RESET13 ANOP
&NAME    LA    1,8
         LA    13,0(13)
         SR    13,1
         L     0,0(13)
         L     1,4(13)
         LA    1,0(1)
         L     13,12(13)
         AGO   .TESTRC
.LOAD13  AIF   ('&LDSA' EQ 'YES').SET13
         AIF   ('&LDSA' EQ 'NO').NOLDSA
         MNOTE 4,'INVALID LDSA SPECIFIED ''NO'' ASSUMED'
.NOLDSA  ANOP
&NAME    LA    13,0(13)
         AGO   .TESTRC
.SET13   ANOP
&NAME    L     13,4(13)
.TESTRC  AIF   ('&RC' EQ '(15)').STR15
         AIF   (T'&RC NE 'N').INVLCD
         AIF   (&RC GE 0 AND &RC LE 4095).SETRC
.INVLCD  ANOP
         MNOTE 8,'RETURN CODE SPECIFICATION INVALID - IGNORED'
         AGO   .FREE
.SETRC   LA    15,&RC
.STR15   ST    15,16(13)
.FREE    AIF   ('&RE' EQ 'NO').RELOAD
         FREEMAIN R,LV=(0),A=(1)
.RELOAD  LM    14,12,12(13)
         BR    14
         AIF   ('&PRINT' EQ 'GEN').MACEND
         POP   PRINT
.MACEND  MEND
