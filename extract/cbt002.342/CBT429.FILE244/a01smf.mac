LSMFSCAN TITLE '                            PRINT SELECTED SMF RECORDS'
***********************************************************************
*                                                                     *
*   REGISTER USAGE :-                                                 *
*                                                                     *
*      R1   -  SMF RECORD POINTER                                     *
*      R2   -  RECORD TYPE                                            *
*      R3   -  TYPE REQUIRED FLAG                                     *
*      R4   -  WORK REGISTER                                          *
*      R5   -  WORK REGISTER                                          *
*      R6   -  WORK REGISTER                                          *
*      R12  -  BASE REGISTER                                          *
*      R13  -  SAVE AREA                                              *
*                                                                     *
***********************************************************************
         SPACE 3
LSMFSCAN CSECT
         SAVE  (14,12),,'LSMFSCAN &SYSDATE &SYSTIME'
         LR    R12,R15             ESTABLISH -
         USING LSMFSCAN,R12        BASE REGISTER
         LA    R10,SAVEAREA        ADDRESS REGISTER SAVE AREA
         ST    R10,8(R13)          CHAIN SAVE AREAS FORWARD
         ST    R13,4(R10)          CHAIN SAVE AREAS BACKWARD
         LR    R13,R10             ADDRESS SAVE AREA
         L     R1,0(R1)            POINT TO PARM
         LH    R4,0(R1)            PICK UP PARM LENGTH
         LTR   R4,R4               IS THE LENGTH ZERO ?
         BZ    OPENFILE            NO PARM - USE DEFAULT RECORD TYPES
         MVI   TYPEFLAG,0          OVERWRITE TABLE -
         MVC   TYPEFLAG+1(255),TYPEFLAG OF RECORD TYPES
         LA    R5,2(R1)            POINT TO FIRST RECORD TYPE
PACKPARM PACK  PACKED,0(3,R5)      PACK RECORD TYPE
         CVB   R6,PACKED           GET IT INTO A REGISTER
         A     R6,=A(TYPEFLAG)     POINT TO OFFSET IN TABLE
         MVI   0(R6),1             FLAG THE RECORD TYPE AS REQUIRED
         LA    R5,4(R5)            POINT TO NEXT RECORD TYPE
         SH    R4,=H'4'            DECREMENT PARM LENGTH
         BP    PACKPARM            KEEP GOING UNTIL IT RUNS OUT
         SPACE 3
OPENFILE OPEN  (SMFIN,INPUT,SYSPRINT,OUTPUT)
         PUT   SYSPRINT,M_HEADER   PUT OUT PAGE HEADER
         SR    R2,R2               INITIALISE RECORD TYPE
         EJECT
READFILE GET   SMFIN               READ A RECORD
         USING SMFRCD0,R1          MAP STANDARD HEADER
         TM    SMF0SEG,X'02'       IS THIS THE START OF A RECORD ?
         BNZ   READFILE            NO - FORGET IT
         CLC   =H'18',SMF0LEN      IS THE HEADER ALL HERE
         BH    READFILE            NO - FORGET IT
         IC    R2,SMF0RTY          PICK UP RECORD TYPE
         LA    R3,TYPEFLAG         POINT TO TYPE TABLE
         AR    R3,R2               POINT TO RECORD TYPE
         CLI   0(R3),0             DO WE WANT THIS TYPE ?
         BZ    READFILE            NO - GET ANOTHER
         SPACE 1
         MVC   M_DETAIL+5(4),=X'F0202020'  RECORD TYPE MASK
         MVC   M_DETAIL+15(3),=X'F0202020' TIME EDIT MASK
         MVC   M_DETAIL+18(6),M_DETAIL+15  TIME EDIT MASK
         MVC   M_DETAIL+30(7),=X'F020204B202020'  DATE MASK
         CVD   R2,PACKED               STORE RECORD TYPE
         ED    M_DETAIL+5(4),PACKED+6  EDIT RECORD TYPE
         MVI   M_DETAIL+5,C'='     MOVE IN EQUALS SIGN
         SR    R4,R4               ZEROISE EVEN REGISTER
         ICM   R5,B'1111',SMF0TME  PICK UP TIME OF DAY
         D     R4,=F'100'          CONVERT TO SECONDS
         SR    R4,R4               ZEROISE EVEN REGISTER
         D     R4,=F'60'           CONVERT TO MINUTES + SECONDS
         CVD   R4,PACKED           STORE SECONDS
         MP    PACKED,=P'10'       GET IN A BYTE
         ED    M_DETAIL+21(3),PACKED+6  EDIT SECONDS
         MVI   M_DETAIL+21,C':'    MOVE IN COLON
         SR    R4,R4               ZEROISE EVEN REGISTER
         D     R4,=F'60'           CONVERT TO HOURS + MINUTES
         CVD   R4,PACKED           STORE MINUTES
         MP    PACKED,=P'10'       GET IN A BYTE
         ED    M_DETAIL+18(3),PACKED+6  EDIT MINUTES
         MVI   M_DETAIL+18,C':'    MOVE IN COLON
         CVD   R5,PACKED           STORE HOURS
         MP    PACKED,=P'10'       GET IN A BYTE
         ED    M_DETAIL+15(3),PACKED+6  EDIT HOURS
         MVI   M_DETAIL+15,C'='    MOVE IN EQUALS SIGN
         ED    M_DETAIL+30(7),SMF0DTE+1 EDIT DATE
         MVI   M_DETAIL+30,C'='    MOVE IN EQUALS SIGN
         MVC   M_DETAIL+43(4),SMF0SID MOVE IN SYSTEM IDENT
         MVC   M_DETAIL+48(84),M_DETAIL+47 BLANK OUT REMAINDER OF LINE
         IC    R2,SMF0RTY          PICK UP RECORD TYPE
         LA    R3,EXTRAS           POINT TO EXTRA INFORMATION TABLE
         AR    R3,R2               OFFSET BY RECORD TYPE
         CLI   0(R3),0             IS THERE ANYTHING EXTRA TO PRINT ?
         BZ    PUTPRINT            NO - PUT OUT WHAT WE HAVE
         SPACE 1
         CLI   SMF0RTY,0           IS THIS A TYPE 0 ?
         BE    TYPE0               YES - PERFORM SPECIAL PROCESSING
         CLI   SMF0RTY,5           IS THIS A TYPE 5 ?
         BE    TYPE5               YES - PERFORM SPECIAL PROCESSING
         CLI   SMF0RTY,6           IS THIS A TYPE 6 ?
         BE    TYPE6               YES - PERFORM SPECIAL PROCESSING
         CLI   SMF0RTY,17          IS THIS A TYPE 17 ?
         BE    TYPE17              YES - PERFORM SPECIAL PROCESSING
         CLI   SMF0RTY,18          IS THIS A TYPE 18 ?
         BE    TYPE18              YES - PERFORM SPECIAL PROCESSING
         CLI   SMF0RTY,21          IS THIS A TYPE 21 ?
         BE    TYPE21              YES - PERFORM SPECIAL PROCESSING
         CLI   SMF0RTY,35          IS THIS A TYPE 35 ?
         BE    TYPE35              YES - PERFORM SPECIAL PROCESSING
         CLI   SMF0RTY,80          IS THIS A TYPE 80 ?
         BE    TYPE80              YES - PERFORM SPECIAL PROCESSING
         B     PUTPRINT            NO EXTRA INFORMATION AFTER ALL
         DROP  R1                  DROP STANDARD HEADER
         EJECT
         USING SMFRCD0,R1          MAP RECORD TYPE 0
TYPE0    CLC   =H'34',SMF0LEN      IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(3),=C'JWT' MOVE IN DESCRIPTION
         MVC   M_DETAIL+53(4),=X'F0202020' TIME EDIT MASK
         ICM   R5,B'1111',SMF0JWT  PICK UP JOB WAIT TIME
         CVD   R5,PACKED           STORE JOB WAIT TIME
         ED    M_DETAIL+53(4),PACKED+6  EDIT JOB WAIT TIME
         MVI   M_DETAIL+53,C'='    MOVE IN EQUALS SIGN
         MVC   M_DETAIL+57(9),=C', VIRTUAL' MOVE IN DESCRIPTION
         MVC   M_DETAIL+66(8),=X'F020202020202020' VIRTUAL EDIT MASK
         ICM   R5,B'1111',SMF0VST  PICK UP VIRTUAL STORAGE SIZE
         CVD   R5,PACKED           STORE VIRTUAL STORAGE SIZE
         ED    M_DETAIL+66(8),PACKED+4  EDIT VIRTUAL STORAGE SIZE
         MVI   M_DETAIL+66,C'='    MOVE IN EQUALS SIGN
         MVI   M_DETAIL+74,C'K'    MOVE IN KILOBYTE SYMBOL
         MVC   M_DETAIL+75(6),=C', REAL' MOVE IN DESCRIPTION
         MVC   M_DETAIL+81(6),=X'F02020202020' REAL STORAGE EDIT MASK
         ICM   R5,B'1111',SMF0RST  PICK UP REAL STORAGE SIZE
         CVD   R5,PACKED           STORE REAL STORAGE SIZE
         ED    M_DETAIL+81(6),PACKED+5  EDIT REAL STORAGE SIZE
         MVI   M_DETAIL+81,C'='    MOVE IN EQUALS SIGN
         MVI   M_DETAIL+87,C'K'    MOVE IN KILOBYTE SYMBOL
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 0
         SPACE 3
         USING SMFRCD5,R1          MAP RECORD TYPE 5
TYPE5    CLC   =H'26',SMF5LEN      IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(4),=C'JOB=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+54(8),SMF5JBN  MOVE IN JOBNAME
         CLC   =H'66',SMF5LEN      IS THERE ENOUGH FOR JOB COMP. CODE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+62(5),=C', CC=' MOVE IN DESCRIPTION
         UNPK  M_DETAIL+67(5),SMF5JCC(3) UNPACK JOB COMPLETION CODE
         NC    M_DETAIL+67(5),=X'0F0F0F0F00' REMOVE ZONES
         TR    M_DETAIL+67(4),=C'0123456789ABCDEF' CONVERT TO CHARACTER
         TM    SMF5JBTI,B'00000010' SEE IF JOB ABENDED
         BZ    PUTPRINT            WRITE PRINT LINE
         MVC   M_DETAIL+72(7),=C'ABENDED' MOVE IN LITERAL
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 5
         SPACE 3
         USING SMFRCD6,R1          MAP RECORD TYPE 6
TYPE6    CLC   =H'26',SMF6LEN      IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(4),=C'JOB=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+54(8),SMF6JBN  MOVE IN JOBNAME
         CLC   =H'55',SMF6LEN      IS THERE ENOUGH FOR NUMBER OF LINES
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+62(7),=C', LINES' MOVE IN DESCRIPTION
         MVC   M_DETAIL+69(11),=X'2120202020202020202020'
         ICM   R5,B'1111',SMF6NLR  PICK UP # OF LOGICAL RECORDS
         CVD   R5,PACKED           STORE # OF LOGICAL RECORDS
         ED    M_DETAIL+68(12),PACKED+2  EDIT # OF LOGICAL RECORDS
         MVI   M_DETAIL+69,C'='    MOVE IN EQUALS SIGN
         CLC   =H'61',SMF6LEN      IS THERE ENOUGH FOR FORM NUMBER ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+80(7),=C', FORM=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+87(4),SMF6FMN MOVE IN FORM NUMBER
         CLC   =H'80',SMF6LEN      IS THERE ENOUGH FOR OUTPUT DEVICE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+91(9),=C', DEVICE=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+100(8),SMF6OUT MOVE IN OUTPUT DEVICE NAME
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 6
         SPACE 3
         USING SMF17HDR,R1         MAP RECORD TYPE 17
TYPE17   CLC   =H'26',SMF17LEN     IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(4),=C'JOB=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+54(8),SMF17JBN MOVE IN JOBNAME
         CLC   =H'88',SMF17LEN     IS THERE ENOUGH FOR DATA SET NAME ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+62(9),=C', DSNAME=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+71(44),SMF17DSN MOVE IN DATA SET NAME
         CLC   =H'100',SMF17LEN    IS THERE ENOUGH FOR VOLUME SERIAL ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+115(9),=C', VOLSER=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+124(6),SMF17FVL MOVE IN VOLUME SERIAL
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 17
         SPACE 3
         USING SMF18HDR,R1         MAP RECORD TYPE 18
TYPE18   CLC   =H'26',SMF18LEN     IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(4),=C'JOB=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+54(8),SMF18JBN MOVE IN JOBNAME
         CLC   =H'88',SMF18LEN     IS THERE ENOUGH FOR OLD DSNAME ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+62(9),=C', DSNAME=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+71(44),SMF18ODS MOVE IN OLD DATA SET NAME
         CLC   =H'132',SMF18LEN    IS THERE ENOUGH FOR NEW DSNAME ?
         BH    PUTPRINT            NO - FORGET IT
         PUT   SYSPRINT,M_DETAIL   WRITE A DETAIL LINE
         MVC   M_DETAIL+50(13),M_DETAIL+49 BLANK OUT JOBNAME
         MVC   M_DETAIL+63(8),=C'NEWNAME=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+71(44),SMF18NDS MOVE IN NEW DATA SET NAME
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 18
         SPACE 3
         USING SMF21HDR,R1         MAP RECORD TYPE 21
TYPE21   CLC   =H'26',SMF21LEN     IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(7),=C'VOLSER=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+57(6),SMF21VOL MOVE IN VOLUME SERIAL
         CLC   =H'28',SMF21LEN     IS THERE ENOUGH FOR CHANNEL ADDRESS
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+63(6),=C', CUU=' MOVE IN DESCRIPTION
         UNPK  M_DETAIL+69(5),SMF21CA(3) UNPACK CHANNEL ADDRESS
         NC    M_DETAIL+69(5),=X'0F0F0F0F00' REMOVE ZONES
         TR    M_DETAIL+69(4),=C'0123456789ABCDEF' CONVERT TO CHARACTER
         CLC   =H'34',SMF21LEN     IS THERE ENOUGH FOR TEMPORARY ERRORS
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+73(6),=C', TEMP' MOVE IN DESCRIPTION
         MVC   M_DETAIL+79(4),=X'F0202020'
         SRDL  R4,63               ZEROISE REG 5
         IC    R4,SMF21TR          PICK UP TEMPORARY READ ERRORS
         IC    R5,SMF21TW          PICK UP TEMPORARY WRITE ERRORS
         AR    R5,R4               TOTAL TEMPORARY ERRORS
         CVD   R5,PACKED           STORE TOTAL TEMPORARY ERRORS
         ED    M_DETAIL+79(4),PACKED+6 EDIT TOTAL TEMPORARY ERRORS
         MVI   M_DETAIL+79,C'='    MOVE IN EQUALS SIGN
         CLC   =H'36',SMF21LEN     IS THERE ENOUGH FOR NO OF START I/O
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+83(5),=C', SIO' MOVE IN DESCRIPTION
         MVC   M_DETAIL+88(6),=X'F02020202020'
         SR    R5,R5               ZEROISE REG 5
         ICM   R5,B'0011',SMF21SIO PICK UP NO OF START I/O
         CVD   R5,PACKED           STORE NO OF START I/O
         ED    M_DETAIL+88(6),PACKED+5 EDIT NO OF START I/O
         MVI   M_DETAIL+88,C'='    MOVE IN EQUALS SIGN
         CLC   =H'38',SMF21LEN     IS THERE ENOUGH FOR PERMANENT ERRORS
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+94(6),=C', PERM' MOVE IN DESCRIPTION
         MVC   M_DETAIL+100(4),=X'F0202020'
         SRDL  R4,63               ZEROISE REG 5
         IC    R4,SMF21PR          PICK UP PERMANENT READ ERRORS
         IC    R5,SMF21PW          PICK UP PERMANENT WRITE ERRORS
         AR    R5,R4               TOTAL PERMANENT ERRORS
         CVD   R5,PACKED           STORE TOTAL PARMANENT ERRORS
         ED    M_DETAIL+100(4),PACKED+6 EDIT TOTAL PERMANENT ERRORS
         MVI   M_DETAIL+100,C'='    MOVE IN EQUALS SIGN
         CLC   =H'44',SMF21LEN     IS THERE ENOUGH FOR TAPE DENSITY ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+104(6),=C', DEN=' MOVE IN DESCRIPTION
         UNPK  M_DETAIL+110(3),SMF21DEN(2) UNPACK TAPE DENSITY
         NC    M_DETAIL+110(3),=X'0F0F00' REMOVE ZONES
         TR    M_DETAIL+110(2),=C'0123456789ABCDEF' CONVERT TO CHAR.
         CLC   =H'46',SMF21LEN     IS THERE ENOUGH FOR BLOCK SIZE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+112(9),=C', BLKSIZE' MOVE IN DESCRIPTION
         MVC   M_DETAIL+121(6),=X'F02020202020'
         SR    R5,R5               ZEROISE REG 5
         ICM   R5,B'0011',SMF21BLS PICK UP BLOCK SIZE
         CVD   R5,PACKED           STORE BLOCK SIZE
         ED    M_DETAIL+121(6),PACKED+5 EDIT BLOCK SIZE
         MVI   M_DETAIL+121,C'='   MOVE IN EQUALS SIGN
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 21
         SPACE 3
         USING TLGSMF,R1           MAP RECORD TYPE 35
TYPE35   CLC   =H'26',TLGRLEN      IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(5),=C'USER=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+55(8),TLGUIF MOVE IN USER IDENTIFICATION
         CLC   =H'57',TLGRLEN      IS THERE ENOUGH FOR TERM. STATUS
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+63(5),=C', CC=' MOVE IN DESCRIPTION
         UNPK  M_DETAIL+68(5),TLGSTAT(3) UNPACK JOB COMPLETION CODE
         NC    M_DETAIL+68(5),=X'0F0F0F0F00' REMOVE ZONES
         TR    M_DETAIL+68(4),=C'0123456789ABCDEF' CONVERT TO CHARACTER
         CLC   =H'92',TLGRLEN      IS THERE ENOUGH FOR # TRANSACTIONS ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+72(7),=C', TRANS' MOVE IN DESCRIPTION
         MVC   M_DETAIL+79(11),=X'2120202020202020202020'
         ICM   R5,B'1111',TLGNTSN  PICK UP # TRANSACTIONS
         CVD   R5,PACKED           STORE # TRANSACTIONS
         ED    M_DETAIL+78(12),PACKED+2  EDIT # TRANSCATIONS
         MVI   M_DETAIL+79,C'='    MOVE IN EQUALS SIGN
         CLC   =H'94',TLGRLEN      IS THERE ENOUGH FOR PERF. GROUP ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+90(5),=C', PGN' MOVE IN DESCRIPTION
         MVC   M_DETAIL+95(4),=X'F0202020'
         SR    R5,R5               ZEROISE REG 5
         ICM   R5,B'0011',TLGPGNO  PICK UP PERFORMANCE GROUP #
         CVD   R5,PACKED           STORE PERFORMANCE GROUP #
         ED    M_DETAIL+95(4),PACKED+6  EDIT ERFORMANCE GROUP #
         MVI   M_DETAIL+95,C'='    MOVE IN EQUALS SIGN
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 35
         SPACE 3
         USING SMFRCD80,R1         MAP RECORD TYPE 80
TYPE80   CLC   =H'30',SMF80LEN     IS THERE ENOUGH OF THE RECORD HERE ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+50(7),=C'USERID=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+57(8),SMF80USR MOVE IN USER IDENTIFICATION
         CLC   =H'38',SMF80LEN     IS THERE ENOUGH FOR GROUP ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+65(8),=C', GROUP=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+73(8),SMF80GRP MOVE IN GROUP IDENT
         CLC   =H'54',SMF80LEN     IS THERE ENOUGH FOR TERMINAL ID. ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+81(11),=C', TERMINAL=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+92(8),SMF80TRM MOVE IN TERMINAL ID.
         CLC   =H'62',SMF80LEN     IS THERE ENOUGH FOR JOBNAME ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+100(6),=C', JOB=' MOVE IN JOBNAME
         MVC   M_DETAIL+106(8),SMF80JBN MOVE IN JOBNAME
         CLC   =H'78',SMF80LEN     IS THERE ENOUGH FOR USER IDENT. ?
         BH    PUTPRINT            NO - FORGET IT
         MVC   M_DETAIL+114(9),=C', USERID=' MOVE IN DESCRIPTION
         MVC   M_DETAIL+123(8),SMF80UID MOVE IN USER ID.
         B     PUTPRINT            WRITE PRINT LINE
         DROP  R1                  UN-MAP TYPE 80
         SPACE 3
PUTPRINT PUT   SYSPRINT,M_DETAIL   WRITE DETAIL LINE
         B     READFILE            GET ANOTHER RECORD
         EJECT
EOF      PUT   SYSPRINT,M_ENDEND   PUT OUT END OF FILE MESSAGE
         CLOSE (SMFIN,,SYSPRINT)   CLOSE FILES
         L     R13,4(R13)          RESTORE HIGHER SAVE AREA
         RETURN (14,12),RC=0       RETURN TO CALLER
         EJECT
SAVEAREA DC    18F'0'              REGISTER SAVE AREA
PACKED   DS    D                   WORK AREA
TYPEFLAG DC    256X'00'            TABLE OF RECORD TYPES
         ORG   TYPEFLAG            DEFAULT RECORD TYPES TO SELECT:-
         DC    X'01000101'         IPL, DUMP HEADER, DUMP TRAILER
         ORG
EXTRAS   DC    256X'00'            EXTRA INFORMATION AVAILABLE TABLE
         ORG   EXTRAS+0            IPL
         DC    X'FF'
         ORG   EXTRAS+5            JOB TERMINATION
         DC    X'FF'
         ORG   EXTRAS+6            WRITER
         DC    X'FF'
         ORG   EXTRAS+17           SCRATCH
         DC    X'FF'
         ORG   EXTRAS+18           RENAME
         DC    X'FF'
         ORG   EXTRAS+21           ERROR STATISTICS BY VOLUME
         DC    X'FF'
         ORG   EXTRAS+35           LOGOFF
         DC    X'FF'
         ORG   EXTRAS+80           RACF
         DC    X'FF'
         ORG
M_HEADER DC    CL133'1LSMFSCAN                     SYSTEM MANAGEMENT FAX
               CILITIES '
M_DETAIL DC    CL133'0TYPE=TTT, TIME=HH:MM:SS, DATE=YY.DDD, SID=SSSS '
M_ENDEND DC    CL133'-                             *****  END OF REPORTX
                 ****** '
         PRINT NOGEN
SMFIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SMFIN,EODAD=EOF,RECFM=VBS
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=133,  X
               BLKSIZE=3059
         LTORG
         YREGS
         EJECT
         PRINT GEN
SMFTYPES DSECT
         IFASMFR (0,5,6,17,18,21,35,80)
         END   LSMFSCAN
