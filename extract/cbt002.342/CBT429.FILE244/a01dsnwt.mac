LDSNWAIT TITLE '               WAIT FOR A DATA SET TO BECOME AVAILABLE'
***********************************************************************
*                                                                     *
*        THIS PROGRAM ISSUES A LOCATE FOR A DATA SET (THE NAME IS     *
*        PASSED AS A PARM).  IF THE DATA SET DOES NOT EXIST IT        *
*        WAITS FOR 1 MINUTE BEFORE RETRYING.  WHEN THE DATA SET IS    *
*        LOCATED, IT ISSUES AN ENQ FOR IT, TO ENSURE THAT IT IS       *
*        NOT IN USE BY ANY OTHER JOB.  THE USER CAN ALSO SPECIFY A    *
*        MAXIMUM NUMBER OF MINUTES THAT THE PROGRAM IS TO WAIT FOR    *
*        THE DATA SET TO BECOME AVAIALABLE.                           *
*                                                                     *
*        AUTHOR: DAVE THORBY   SEPTEMBER 1988                         *
*                                                                     *
*        REGISTER USAGE:                                              *
*        R1    SYS PARM                                               *
*        R9    LENGTH OF DATA SET NAME                                *
*        R10   DATA SET NAME                                          *
*        R11   INTERNAL LINKAGE                                       *
*        R12   BASE REGISTER                                          *
*        R13   SAVE AREA                                              *
*                                                                     *
***********************************************************************
         EJECT
LDSNWAIT CSECT
         SAVE  (14,12),,LDSNWAIT_&SYSDATE_&SYSTIME
         LR    R12,R15             ESTABLISH -
         USING LDSNWAIT,R12          BASE REGISTER
         LA    R10,SAVEAREA        POINT TO SAVE AREA
         ST    R10,8(R13)          CHAIN SAVE AREAS FORWARD
         ST    R13,4(R10)          CHAIN SAVE AREAS BACKWARD
         LR    R13,R10             ADDRESS SAVE AREA
         SPACE 3
         L     R1,0(R1)            POINT TO PARM
         CLI   1(R1),X'00'         CHECK FOR NULL PARM
         BE    ABORT1              NO PARM SPECIFIED
         LA    R10,2(R1)           POINT TO START OF PARM
         CLI   0(R10),C'0'         CHECK FOR NUMERIC PARM
         BL    NONUM               NO NUMBER SPECIFIED
NUMLOOP  LA    R10,1(R10)          POINT TO NEXT CHARACTER
         CLI   0(R10),C'0'         IS THIS A DIGIT ?
         BNL   NUMLOOP             YES - KEEP CHECKING
         LR    R9,R10              POINT TO START OF DSNAME
         SR    R9,R1               GET OFFSET TO DSNAME
         SH    R9,=H'3'            GET LENGTH FOR PACK
         EX    R9,PACKTIME         PACK NUMBER OF MINUTES
         AP    WAITTIME,MINUTES    ADD 1 FOR FIRST TRY
         CLI   0(R10),C'/'         IS THERE A SLASH NEXT ?
         BNE   NONUM               NO - CONTINUE
         LA    R10,1(R10)          POINT PAST THE SLASH
         SPACE 3
NONUM    LR    R9,R10              POINT TO START OF DSNAME
         SR    R9,R1               GET OFFSET OF DSNAME
         SH    R9,=H'2'            NUMBER OF NUMERIC CHARACTERS
         LH    R1,0(R1)            PICK UP PARM LENGTH
         SR    R1,R9               GET LENGTH OF DSNAME
         LR    R9,R1               SAVE LENGTH
         BCTR  R1,0                DECREMENT FOR MOVE
         EX    R1,MOVEDSN          SAVE DATA SET NAME
         SPACE 3
LOCATE   LOCATE DATASET            LOCATE DATA SET
         LTR   R15,R15             TEST RETURN CODE
         BZ    ENQ                 DATA SET EXISTS
         BAL   R11,WAIT            WAIT FOR A MINUTE
         B     LOCATE              TRY AGAIN
         EJECT
ENQ      ENQ   (MAJ,(10),S,(9),SYSTEM),RET=TEST
         LTR   R15,R15             TEST RETURN CODE
         BZ    RETURN              DATA SET IS AVAILABLE
         BAL   R11,WAIT            WAIT FOR A MINUTE
         B     ENQ                 TRY AGAIN
         EJECT
WAIT     SP    WAITTIME,=P'1'      CHECK HOW LONG WE'VE BEEN IN HERE
         BZ    ABORT2              TOO LONG - GIVE UP
         STIMER WAIT,DINTVL=ONEMIN WAIT FOR 1 MINUTE
         BR    R11
         EJECT
RETURN   SR    R15,R15             ZEROISE RETURN CODE
GOBACK   L     13,4(13)            RESTORE HIGHER SAVE AREA
         RETURN (14,12),RC=(15)    RETURN TO CALLER
         SPACE 3
ABORT1   WTO   'LEBDSNW01E  NO PARM SUPPLIED - PROGRAM ABORTED',       X
               ROUTCDE=(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15)
         LA    R15,12              SET RETURN CODE
         B     GOBACK              RETURN TO CALLER
         SPACE 3
ABORT2   BCTR  R9,0                SET UP LENGTH FOR MOVE
         EX    R9,WTO2MOVE         MOVE DSN INTO WTO MESSAGE
         MVC   WTO2+94(3),=X'202120' SET UP EDIT MASK
         ED    WTO2+93(4),MINUTES+6 PUT TIME INTO WTO MESSAGE
WTO2     WTO   'LEBDSNW02I  DATA SET                                   X
                          NOT AVAILABLE AFTER     MINUTES',            X
               ROUTCDE=(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15),DESC=11
         LA    R15,8               SET RETURN CODE
         B     GOBACK              RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        EXECUTED INSTRUCTIONS, DCBS AND LITERALS                     *
*                                                                     *
***********************************************************************
         SPACE 3
PACKTIME PACK  MINUTES,2(0,R1)     PACK NUMBER OF MINUTES
MOVEDSN  MVC   DSNAME(0),0(R10)    SAVE DATA SET NAME
WTO2MOVE MVC   WTO2+29(0),0(R10)   MOVE DSN INTO WTO MESSAGE
         SPACE 3
MINUTES  DC    PL8'-1'             NUMBER OF MINUTES LEFT
WAITTIME DC    PL8'1'              NUMBER OF MINUTES TO WAIT
ONEMIN   DS    0D
         DC    C'00010000'         WAIT FOR ONE MINUTE
         SPACE 1
MAJ      DC    CL8'SYSDSN'         MAJOR NAME FOR ENQ
         SPACE 1
DATASET  CAMLST NAME,DSNAME,,CL265
DSNAME   DC    CL44' '             DATA SET NAME
CL265    DS    0D
         DS    CL265               RETURN AREA FROM LOCATE
         SPACE 3
SAVEAREA DC    18F'0'              SAVE AREA
         SPACE 1
         LTORG
         YREGS
         END   LDSNWAIT
