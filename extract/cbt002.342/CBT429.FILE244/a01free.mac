LFREESPC CSECT
         SAVE  (14,12),,'LFREESPC &SYSDATE &SYSTIME'
         LR    R2,R15
         USING LFREESPC,R2
         ST    R13,SAVE+4
         LA    R13,SAVE
         OPEN  (SYSPRINT,OUTPUT)
         SPACE 3
GETUCB   L     R15,16              R15 -> COMMS VECTOR TABLE
         L     R15,1076(R15)       R15 -> UCB SCAN ROUTINE
         LA    R1,UCBPARM          R1 -> PARM LIST FOR UCBSCAN
         BASR  R14,R15             EXECUTE UCBSCAN ROUTINE
         LTR   R15,R15             IS THERE A UCB THERE ?
         BNZ   ENDUP               NO - TERMINATE
         L     R3,UCBADDR          YES - R3 -> CURRENT UCB
         TM    3(R3),X'80'         IS THE DEVICE ONLINE ?
         BZ    GETUCB              NO - GET NEXT UCB
         MVC   PCUU,13(R3)         YES - ESTABLISH DASD CUU
         MVC   PVOLSER,28(R3)      ESTABLISH DASD VOLSER
         LR    R0,R3               R0 -> CURRENT UCB
         LA    R1,RESULT           R1 -> SVC 78 RESULT FIELD
         SVC   78                  CALL SVC 78
         PUT   SYSPRINT,PCUU
         B     GETUCB              GET NEXT UCB
         SPACE 3
ENDUP    CLOSE (SYSPRINT)
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         EJECT
         PRINT NOGEN
SAVE     DC    18F'0'
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,                      X
               RECFM=FB,LRECL=41,BLKSIZE=2870
PCUU     DS    CL3
         DC    C' '
PVOLSER  DS    CL6
         DC    C' '
RESULT   DS    CL30
UCBPARM  DC    A(UCBWORK)
         DC    A(UCBTYPE)
         DC    X'80'
         DC    AL3(UCBADDR)
UCBWORK  DC    XL100'00'
UCBTYPE  DC    X'20'
UCBADDR  DS    F
UCBSCAN  DS    F
         LTORG
         YREGS
         END   LFREESPC
