UT01     SAVER EQU=N
         LR    5,1                      SAVE POINTER TO PARM
*
********
*
         OPEN  (PUNCH,(OUTPUT))
*
********
*
L01      DS    0H
         L     5,0(5)
         LH    2,0(5)                   LENGTH
         LA    5,2(5)                   ADDR. PARM
         MVC   RETCODE,=F'0'            RC=0
         MVI   DLIMITER,C';'            DEFAULT DELIMITER
         CLI   0(5),C'A'                TEST IF FIRST CHAR. LESS THAN A
         BNL   L10                      IF NOT DELEMITER DEFAULTS
         CLI   0(5),C' '                ALSO IF = BLANK
         BE    L10                      DELIMITER DEFAULTS
L05      DS    0H
         MVC   DLIMITER,0(5)            SET UP NEW DELIMITER
         BCTR  2,0                      PARM LENGTH - 1
         LA    5,1(5)                   POINT PAST DELIM. TO START TEXT
L10      DS    0H
         LR    3,5                      SAVE START
*
********
L15      DS    0H
         CLC   0(1,5),DLIMITER          IS THIS ONE = DLIM.
         BE    L20                      YES
         LA    5,1(5)                   NO-SEARCH NEXT
         BCT   2,L15                    DECREASE COUNT
         BAL   10,CREATE                PUT OUT RECORD
         B     THEEND
L20      DS    0H
         BAL   10,CREATE                PUT OUT RECORD
         BCT   2,L15                    DECREASE COUNT
*
********
*
THEEND   DS    0H
         CLOSE (PUNCH)
         L     13,4(13)
         L     15,RETCODE
         RETURN (14,12),RC=(15)
         PRINT NOGEN
PUNCH    DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,LRECL=80
         PRINT GEN
*
********
*
CREATE   DS    0H
         MVC   OUT(100),OUT-1           CLEAR AREA
         LR    4,3                      SAVE START
         SR    3,5                      CALC. LENGTH
         BZ    L25                      EMPTY CARD GENERATED
         LPR   3,3                      MAKE POSITIVE
         BCTR  3,0                      LENGTH CODE
         EX    3,MVC1
L25      DS    0H
         LA    5,1(5)                   NEXT CHAR.
         LR    3,5                      SAVE START NEXT PARM
         CLC   OUT+80(20),=CL20' '      IS OUTPUT AREA EXTENSION BLANK
         BE    L30                      YES
         MVC   RETCODE,=F'8'            ERROR
L30      DS    0H
         BAL   11,CARDOUT               OUTPUT CARD IMAGE
         BR    10
MVC1     MVC   OUT(0),0(4)              MOVE DATA TO OUTPUT AREA
*
********
*
CARDOUT  PUT   PUNCH,OUT
         BR    11
*
********
*
DLIMITER DS    CL1
         DC    CL1' '
OUT      DS    CL80
         DS    CL20                     EXTENSION
RETCODE  DS    F
         END
