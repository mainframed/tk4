INMRZ21R TITLE 'TSO/E RELEASE 4 - RECEIVE C.P. LOG ALLOCATION EXIT'
INMRZ21R CSECT
INMRZ21R AMODE 31                  SET ADDRESSING MODE
INMRZ21R RMODE ANY                 SET RESIDENCY MODE
         SPACE 3
***********************************************************************
*                                                                     *
* MODULE NAME - INMRZ21R                                              *
*                                                                     *
* CSECT  NAME - INMRZ21R                                              *
*                                                                     *
* DESCRIPTIVE NAME - TSO/E - RECEIVE C.P. LOG ALLOCATION EXIT         *
*                                                                     *
* FUNCTION -                                                          *
*                                                                     *
*  THIS ROUTINE IS INVOKED BEFORE THE RECEIVE COMMAND PROCESSOR       *
*  ALLOCATES ITS LOG DATA SET.                                        *
*  IT CHECKS TO SEE WHETHER THE LOG DATA SET HAS BEEN MIGRATED, AND   *
*  IF IT HAS IT SUPRESSES LOGGING.                                    *
*                                                                     *
* ERROR MESSAGES - LINMXZ21E                                          *
*                                                                     *
* ABEND CODES - NONE                                                  *
*                                                                     *
* ERROR CONDITIONS - DATA SET MIGRATED                                *
*                                                                     *
* LINKAGES: BRANCHED TO VIA BALR OR LINKED TO                         *
*                                                                     *
* MODULE(S) CALLED: NONE                                              *
*                                                                     *
* ENTRY POINT - INMRZ21R                                              *
*                                                                     *
* EXIT CONDITIONS                                                     *
*                                                                     *
*        EXIT IS ACCOMPLISHED VIA BSM                                 *
*                                                                     *
* REFERENCES                                                          *
*                                                                     *
*        ROUTINES   - NONE                                            *
*                                                                     *
*        DATA AREAS - NONE                                            *
*                                                                     *
*        TABLES     - NONE                                            *
*                                                                     *
* GLOBALS:                                                            *
*                                                                     *
*       NONE                                                          *
*                                                                     *
* MACRO USAGE: SAVE,                                                  *
*              GETMAIN,                                               *
*              FREEMAIN,                                              *
*              LOCATE,                                                *
*              CAMLST,                                                *
*              YREGS                                                  *
*                                                                     *
* MODULE ATTRIBUTES - REENTRANT,                                      *
*                     REFRESHABLE,                                    *
*                     REUSEABLE,                                      *
*                     AMODE(ANY),                                     *
*                     RMODE(31),                                      *
*                     MAY BE LINKED AC=1                              *
*                                                                     *
* INPUT - ON ENTRY TO IKJEXIT R1 MUST POINT TO A VARIABLE LENGTH      *
*         PARAMETER LIST THAT DESCRIBES A PARAMETER_ENTRY LIST.       *
*                                                                     *
*         THE HIGH ORDER BIT IN THE LAST PARAMETER                    *
*         OF THE PARAMETER LIST MUST BE ON                            *
*         X'80000000' TO INDICATE THE END OF THE LIST.                *
*                                                                     *
* CHANGE ACTIVITY:                                                    *
*                                                                     *
*      INTRODUCED IN TSO/E RELEASE 4 HTE1402                          *
*                                                                     *
***********************************************************************
         SPACE 3
         SAVE  (14,12),,'INMRZ21R &SYSDATE &SYSTIME'
         LR    R12,R15             LOAD BASE REGISTER
         USING INMRZ21R,R12        ESTABLISH BASE REGISTER
         LR    R2,R1               SAVE POINTER TO PARAMETER LIST
         GETMAIN RU,LV=WORKLEN,LOC=(BELOW,ANY)
         ST    R13,4(,R1)          SAVE PREVIOUS SAVE AREA
         ST    R1,8(,R13)          SAVE CURRENT SAVE AREA
         LR    R13,R1              LOAD ADDRESS OF SAVE AREA
         USING SAVEAREA,R13        ADDRESS SAVE AREA
         EJECT
         LR    R1,R2               ADDRESS OF PARAMETER LIST
         L     R2,40(R1)           ADDRESS OF ACTION FLAGS
         L     R3,44(R1)           ADDRESS OF MESSAGE TEXT
         L     R4,52(R1)           ADDRESS OF DATA SET NAME
         SR    R5,R5               SET DEFAULT RETURN CODE
         MVC   LOGDSET(CAMLSTL),CAMLST COPY CAMLST TO WORK AREA
         MVC   DSNAME,8(R4)        COPY DSNAME TO WORK AREA
         WTO   'INMRZ21R ENTERED',ROUTCDE=11
         LA    R15,DSNAME          ADDRESS OF DATA SET NAME
         ST    R15,LOGDSET+4       STORE IN CAMLST
         LA    R15,LOCAREA         ADDRESS OF RETURNED AREA
         ST    R15,LOGDSET+12      STORE IN CAMLST
         LOCATE LOGDSET            FIND OUT WHETHER THE LOG IS MIGRATED
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   RETURN              NEW DATA SET - CONTINUE
         CLC   LOCAREA+2+4(6),=C'MIGRAT' IS IT MIGRATED ?
         BNE   RETURN              NO - CONTINUE
         LA    R5,4                SET RETURN CODE
         OI    8(R2),B'11000000'   ISSUE MESSAGE INMX151I
         OI    3(R3),X'01'         INDICATE THERE IS A MESSAGE
         MVC   4(4,R3),=A(L'MESSAGE+44) MOVE IN MESSAGE TEXT LENGTH
         MVC   8(L'MESSAGE,R3),MESSAGE
         MVC   L'MESSAGE+8(44,R3),DSNAME
         EJECT
RETURN   LR    R1,R13              ADDRESS SAVE AREA
         L     R13,4(,R13)         ADDRESS PREVIOUS SAVE AREA
         FREEMAIN RU,LV=WORKLEN,A=(1)
         L     R14,12(,R13)        LOAD RETURN ADDRESS
         LR    R15,R5              PICK UP RETURN CODE
         LM    R0,R12,20(R13)      RESTORE REGISTERS
         BSM   0,14                RETURN TO CALLER
         EJECT
CAMLST   CAMLST NAME,0,,0
CAMLSTL  EQU   *-CAMLST
MESSAGE  DC    C'IDTF LOG DATA SET IS MIGRATED; LOGGING SUSPENDED.  DSNX
               AME='
         LTORG
SAVEAREA DSECT
         DS    18F                 REGISTER SAVE AREA
LOGDSET  CAMLST NAME,DSNAME,,LOCAREA
LOCAREA  DS    0D
         DS    265C
DSNAME   DS    CL44
WORKLEN  EQU   *-SAVEAREA
         YREGS
         END   INMRZ21R
