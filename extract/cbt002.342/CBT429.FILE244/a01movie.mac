LEBMOVIE CSECT
         SAVE  (14,12),,*          SAVE REGISTERS
         LR    R12,R15             ESTABLISH BASE REG
         USING LEBMOVIE,R12        ESTABLISH ADDRESSABILITY
         BAL   R1,START            GET WORKING STORAGE
         DC    18F'0'
         SPACE 1
START    ST    R1,8(R13)           CHAIN SAVE AREAS FORWARD
         ST    R13,4(R1)           CHAIN SAVE AREAS BACKWARD
         LR    R13,R1              ADDRESS SAVE AREA
         LOAD  EP=ISPLINK          LOAD ADDRESS OF INTERFACE ROUTINE
         ST    R0,ISPLINK          STORE ADDRESS OF INTERFACE ROUTINE
         L     R15,ISPLINK         ISPF SERVICE ROUTINE
         CALL  (15),(CONTROL,ERRORS,RETURN),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         ISPF SERVICE ROUTINE
         CALL  (15),(DISPLAY,PANELID),VL,MF=(E,CALLLIST)
         MVC   PANELID,=C'MOVIE000' INITIALISE PANEL NAME
         SPACE 1
LOOP     L     R15,ISPLINK         ISPF SERVICE ROUTINE
         CALL  (15),(CONTROL,DISPLAY,LOCK),VL,MF=(E,CALLLIST)
         PACK  P3,PANELID+5(3)     DISPLAY TO PACKED DECIMAL
         AP    P3,=P'1'            ADD 1 TO PANEL NUMBER
         UNPK  PANELID+5(3),P3     PACKED TO DISPLAY DECIMAL
         OI    PANELID+7,X'F0'     REMOVE SIGN
         L     R15,ISPLINK         ISPF SERVICE ROUTINE
         CALL  (15),(DISPLAY,PANELID),VL,MF=(E,CALLLIST)
         LTR   R15,R15             TEST RETURN CODE
         BZ    LOOP                GO AND DISPLAY NEXT PANEL
         PACK  P3,PANELID+5(3)     DISPLAY TO PACKED DECIMAL
         SP    P3,=P'1'            ADD 1 TO PANEL NUMBER
         UNPK  PANELID+5(3),P3     PACKED TO DISPLAY DECIMAL
         OI    PANELID+7,X'F0'     REMOVE SIGN
         L     R15,ISPLINK         ISPF SERVICE ROUTINE
         CALL  (15),(DISPLAY,PANELID),VL,MF=(E,CALLLIST)
         L     R15,ISPLINK         ISPF SERVICE ROUTINE
         CALL  (15),(DISPLAY,PANELID),VL,MF=(E,CALLLIST)
         SPACE 3
GOBACK   L     R13,4(R13)          RESTORE HIGHER SAVE AREA
         RETURN (14,12),RC=0       RETURN TO WHENCE WE CAME
         SPACE 3
CALLLIST CALL  ,(,,,),VL,MF=L
ISPLINK  DS    F
CONTROL  DC    CL8'CONTROL'
DISPLAY  DC    CL8'DISPLAY'
ERRORS   DC    CL8'ERRORS'
LOCK     DC    CL8'LOCK'
RETURN   DC    CL8'RETURN'
PANELID  DC    CL8'ISR@PRIM'
P3       DC    PL3'0'
         YREGS
         LTORG
         END   LEBMOVIE
