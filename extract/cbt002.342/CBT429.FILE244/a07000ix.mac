IOS000IX CSECT
IOS000IX AMODE 31
IOS000IX RMODE ANY
***********************************************************************
** PURPOSE :-  TO MAKE IOS000I A NON-DELETE MESSAGE FOR DISKS        **
**  ALSO IF THE DISK IS A SPOOLX VOLSER ISSUE A DRAIN FOR THE SPOOL  **
**                                                                   **
** THIS IS A MPF EXIT AND AS SUCH SHOULD BE LINKED AS RE-ENTERANT AND**
** RE-USABLE (RENT,REUS). THE EXIT RESIDES IN SYS1.LINKLIB AND IS    **
** NAMED IOS000IX . PLEASE REMEMBER TO REFRESH LLA AFTER LINKEDITING.**
**                                                                   **
** TO INVOKE THIS EXIT, CODE THE FOLLOWING IN SYS1.PARMLIB(MPFLST01) **
**                                                                   **
** IOS000I,SUP(NO),USEREXIT(IOS000IX)                                **
*********************************************************************
*                                                                   *
*                      D I S C L A I M E R                          *
*                                                                   *
*   This computer programming material has been developed by        *
*   Kevin E. Ferguson, Technical Support Manager, at Cleveland      *
*   County Council. Please address any inquiries to:-               *
*                                                                   *
*         Cleveland County Council,                                 *
*         P.O. Box 17,                                              *
*         Melrose House,                                            *
*         1 Melrose Street,                                         *
*         Middlesbrough,                                            *
*         Cleveland. UK                                             *
*         TS1 1LY                                                   *
*         (0642) 248155                                             *
*                                                                   *
*         ATTN:  K.E.Ferguson (Ext. 2290)                           *
*         (or:   Technical Support (Ext. 2363))                     *
*                                                                   *
*   This material has been used successfully by Cleveland County    *
*   Council, and to the best of our knowledge this material and any *
*   system(s) of which it is a part are operational as of the       *
*   service level or date stated in the body of this material (if   *
*   so stated). However, NO warranty is given or implied as to the  *
*   accuracy of this material or any related material or systems,   *
*   and no responsibility is assumed for any effect or modification *
*   directly or indirectly caused by the use of this material.      *
*   It is the responsibility of any user of this material to        *
*   evaluate its usefulness to the user's environment.              *
*                                                                   *
*   Cleveland County Council does not guarantee to keep this or any *
*   related material current, nor does it guarantee to provide      *
*   any corrections or extensions described by any users of this    *
*   material, or any corrections or extensions made in the future   *
*   by Cleveland County Council itself.                             *
*                                                                   *
*                       C O P Y R I G H T                           *
*                                                                   *
*     This computer programming material remains the exclusive      *
*     property of Cleveland County Council. Permission for its use  *
*     may be obtained by contacting the address above               *
*                                                                   *
*   Cleveland County Council hereby grants permission for this      *
*   programming material to be :-                                   *
*                                                                   *
*   a). Stored on your system and displayed on your terminals.      *
*                                                                   *
*   b). Modified to meet your sites specific needs.                 *
*                                                                   *
*   c). Printed (or Photocopied), but only the specific number of   *
*       copies as are necessary for your sites needs.               *
*                                                                   *
*   d). Circulated amongst bona fida members of the following IBM   *
*       user groups or their sub-groups :-                          *
*                                                                   *
*            i) G.U.I.D.E. (UK and US)                              *
*           ii) SHARE                                               *
*                                                                   *
*       with the provisor that the copyright is not infringed by    *
*       those members.                                              *
*                                                                   *
***********************************************************************
***********************************************************************
         REGEQS                <==== USER MACRO
         STM   R14,R12,12(R13)      SAVE CALLERS REGISTERS
         BALR  R12,0                ADDRESSABILITY
         USING *,R12                ESTABLISH ADDRESSABILTY
         L     R5,0(R1)             CTXT ADDRESS IN R5
         USING CTXT,R5              CTXT ADDRESSABILTY
***********************************************************************
** GETMAIN BELOW THE LINE BECAUSE SVC 34 REQUIRES 24 BIT ADDRESSING  **
***********************************************************************
         GETMAIN RU,LV=DATAEND,LOC=BELOW
         LR    R11,R1               SAVE PASSED ADDRESS
         USING DATA,R11             ADDRESSABILITY TO DATA AREA
         ST    R13,SAVEAREA+4       SET BACKWARD POINTER
         LA    R15,SAVEAREA         ADDRESS OF SAVE AREA
         ST    R15,8(R13)           SET FORWARD POINTER
         LR    R13,R15              R13 = SAVE AREA
         L     R2,CTXTTXPJ          GET ADRESSABILITY TO THE
         USING CTXTATTR,R2          MESSAGE ATTRIBUTES
         L     R3,CTXTDCP
         USING CTXTDESC,R3
         LA    R7,CTXTTMSG          ADDRESS OF THE TEXT AREA
         USING MSGTEXT,R7           ESTABLISH ADDRESSABILITY
**********************************************************************
** NOW CHECK DEVICE FOR A DISK DRIVE, IF IT IS SET THE MESSAGE TO   **
** NON-DELETE. IF IT IS NOT FOR A DISK LEAVE IT ALONE               **
**********************************************************************
         L     R4,CVTPTR           CVT ADDRESS
         USING CVTMAP,R4           DSECT FOR CVT
         XC    WORKAREA,WORKAREA   CLEAR UCB WORK AREA
         LA    R1,WORKAREA         PUT ADDRESS OF WORKAREA INTO R1
         ST    R1,UCBPARM1         SAVE IT
         LA    R1,DEVCLASS         ADDRESS OF DEV CLASS BYTE IN R1
         ST    R1,UCBPARM2         SAVE IT
         MVI   DEVCLASS,UCB3DACC   INDICATE ONLY DASD DEVICES
         LA    R1,ADDRESS          ADDRESS OF WHERE TO PUT IT IN R1
         ST    R1,UCBPARM3         SAVE IT
         OI    UCBPARM3,X'80'      FLAG END OF LIST
         LA    R1,UCBPARML         PUT ADDRESS OF UCBPARML IN R1
         ST    R1,SAVER1U          SAVE R1
         USING UCBOB,R6            ADDRESSABILITY TO UCB
NEWUCB   L     R1,SAVER1U          SCAN SERVICE ADDRESS
         L     R15,CVTUCBSC        SCAN SERVICE ADDRESS
         BALR  R14,R15             GO TO SCAN SERVICE
         LTR   R15,R15             HAS A UCB BEEN RETURNED ?
         BNZ   EXIT                NO - GO TO EXIT
         ST    R1,SAVER1U          SAVE R1
         L     R6,ADDRESS
         CLC   UCBNAME,DEVICE      IF UNIT NAMES MATCH
         BE    MATCH               THEN DO
         B     NEWUCB              BRANCH TO TOP OF LOOP
MATCH    EQU   *
         OI    CTXTRFB1,CTXTRCDC    SET TO CHANGE DESC CODE
         XC    CTXTDESC,CTXTDESC    CLEAR DESC FIELD
         OI    CTXTDC1,CTXTDC02     DESC=2
         CLC   VOLSER(5),=C'SPOOL'  IS IT A SPOOL VOLUME
         BNE   EXIT                NO - EXIT
         XC    MGCRPL(MGCRLTH),MGCRPL CLEAR PARM LIST
         MVC   REPLYV,VOLSER
         MVC   MGCRTEXT(L'REPLYT),REPLYT    MOVE IN REPLY BUFFER
         LA    R1,(MGCRTEXT-MGCRPL)+L'REPLYT    GET MGCRPL LENGTH
         STC   R1,MGCRLGTH          SAVE LENGTH
         SR    R0,R0                CLEAR R0
         MGCR  MGCRPL               ISSUE THE COMMAND
EXIT     L     R13,4(R13)           RESTORE R13
         FREEMAIN RU,LV=DATAEND,A=(R11) FREE THE STORAGE
         LM    R14,R12,12(R13)      RESTORE REGISTERS
         BR    R14
         LTORG
REPLYT   DC    CL14'$ZSPL,V=XXXXXX'  HALT SPOOL VOLUME
REPLYV   EQU   REPLYT+8,6
DATA     DSECT
         DS    0F
SAVEAREA DS    18F
         DS    0D
WORKAREA DS    CL100
UCBPARML DS 3F
UCBPARM1 EQU   UCBPARML+0,4
UCBPARM2 EQU   UCBPARML+4,4
UCBPARM3 EQU   UCBPARML+8,4
SAVER1U  DS    F
DEVCLASS DS    CL1
ADDRESS  DS    F
MGCR     IEZMGCR DSECT=NO
         ORG   MGCRTEXT
         ORG
DATAEND  EQU   *-DATA
MSGTEXT  DSECT
MSGID    DS    CL8
DEVICE   DS    CL3
         DS    CL1
PATH     DS    CL2
         DS    CL1
ERRTYPE  DS    CL3
         DS    CL1
CCW      DS    CL2
         DS    CL1
STATUS   DS    CL4
         DS    CL3
VOLSER   DS    CL6
         ORG   MSGTEXT
         CVT   DSECT=YES
         DSECT
         IEZVX100
         IEFUCBOB
         END
