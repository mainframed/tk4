         MACRO
&N       EOJ &C=0
         LCLC  &A,&B
         GBLC  &RENTGBL
* ******************************************************************* *
*                                                                     *
*                          E O J                                      *
*                                                                     *
*        GENERATE STANDARD RETURN LINKAGE AND RETURN CODE.            *
*                                                                     *
         AIF   ('&N' EQ '').GO
&N       DS    0H
.GO      AIF   ('&C'(1,1) EQ '(').REGCODE
         LA    15,&C                PUT CONDITION CODE INTO REG 15    *
         AGO   .RESTORE
.REGCODE ANOP
         AIF   ('&C'(3,1) EQ ')').MOVE1
         AIF   ('&C'(4,1) EQ ')').MOVE2
         AIF   ('&C'(5,1) EQ ')').MOVE3
.MERROR  ANOP
         MNOTE 16,'*** INVALID REGISTER PASSED AS RETURN CODE REGISTER'
         MEXIT
.MOVE3   ANOP
&A       SETC  '&C'(2,3)
         AGO   .CONT
.MOVE2   ANOP
&A       SETC  '&C'(2,2)
         AGO   .CONT
.MOVE1   ANOP
&A       SETC  '&C'(2,1)
.CONT    ANOP
         AIF   ('&A'(1,1) EQ 'R').RVALUE
         AIF   ('&A' GT '15').MERROR
         AGO   .LOADIT
.RVALUE  ANOP
&B       SETC  '&A'(2,2)
         AIF   ('&B' GT '15').MERROR
.LOADIT  ANOP
         LR    15,&A               LOAD REGISTER 15 WITH CODE        *
.RESTORE ANOP
         AIF   ('&RENTGBL' EQ 'Y').YESRENT
         L     13,PRGMSAVE+4       RELOAD HIS SAVE AREA ADDRESS       *
         L     14,12(13)           RELOAD REGISTER 14                 *
         LM    0,12,20(13)         RELOAD REGISTER 0 TO 12            *
         BR    14                  BRANCH ON REGISTER 14              *
         AGO   .EXIT
.YESRENT ANOP
         LR    1,13                PUT ADDRESS OF GOTTEN INTO REG 1   *
         L     13,4(1)             PUT HIS SAVE AREA ADDRESS IN REG 13*
         FREEMAIN R,LV=72,A=(1)    FREE STORAGE                       *
         L     14,12(13)           RELOAD REGISTER 14                 *
         LM    0,12,20(13)         RELOAD REGISTER 0 TO 12            *
         BR    14                  RETURN TO OS                       *
.EXIT    ANOP                                                         *
*                                                                K.F. *
* ******************************************************************* *
         MEND
