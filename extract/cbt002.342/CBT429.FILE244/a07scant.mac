SMFSCANT TITLE 'SMFSCAN - PAGE TITLE PRINTER'
SMFSCANT BEGIN R11
         REGEQS
*********************************************************************
*                                                                   *
*                      D I S C L A I M E R                          *
*                                                                   *
*   This computer programming material has been developed by        *
*   Kevin E. Ferguson, Technical Support Manager, at Cleveland      *
*   County Council. Please address any inquiries to:-               *
*                                                                   *
*         Cleveland County Council,                                 *
*         P.O. Box 17,                                              *
*         Melrose House,                                            *
*         1 Melrose Street,                                         *
*         Middlesbrough,                                            *
*         Cleveland. UK                                             *
*         TS1 1LY                                                   *
*         (0642) 248155                                             *
*                                                                   *
*         ATTN:  K.E.Ferguson (Ext. 2290)                           *
*         (or:   Technical Support (Ext. 2363))                     *
*                                                                   *
*   This material has been used successfully by Cleveland County    *
*   Council, and to the best of our knowledge this material and any *
*   system(s) of which it is a part are operational as of the       *
*   service level or date stated in the body of this material (if   *
*   so stated). However, NO warranty is given or implied as to the  *
*   accuracy of this material or any related material or systems,   *
*   and no responsibility is assumed for any effect or modification *
*   directly or indirectly caused by the use of this material.      *
*   It is the responsibility of any user of this material to        *
*   evaluate its usefulness to the user's environment.              *
*                                                                   *
*   Cleveland County Council does not guarantee to keep this or any *
*   related material current, nor does it guarantee to provide      *
*   any corrections or extensions described by any users of this    *
*   material, or any corrections or extensions made in the future   *
*   by Cleveland County Council itself.                             *
*                                                                   *
*                       C O P Y R I G H T                           *
*                                                                   *
*     This computer programming material remains the exclusive      *
*     property of Cleveland County Council. Permission for its use  *
*     may be obtained by contacting the address above               *
*                                                                   *
*   Cleveland County Council hereby grants permission for this      *
*   programming material to be :-                                   *
*                                                                   *
*   a). Stored on your system and displayed on your terminals.      *
*                                                                   *
*   b). Modified to meet your sites specific needs.                 *
*                                                                   *
*   c). Printed (or Photocopied), but only the specific number of   *
*       copies as are necessary for your sites needs.               *
*                                                                   *
*   d). Circulated amongst bona fida members of the following IBM   *
*       user groups or their sub-groups :-                          *
*                                                                   *
*            i) G.U.I.D.E. (UK and US)                              *
*           ii) SHARE                                               *
*                                                                   *
*       with the provisor that the copyright is not infringed by    *
*       those members.                                              *
*                                                                   *
***********************************************************************
PACKDIG  EQU   X'0F'
         L     R2,0(R1)        PRESERVE R1 IN R2
         CLC   0(4,R2),=F'0'   ANY PARMS ?
         BNE   KEEPON          YES - KEEP ON GOING
         WTO  'SMFSCAN - NO PARAMETERS PASSED TO SMFSCANT. ABEND U0001'
         ABEND 1
KEEPON   EQU   *
         MVC   SAVEADDR,0(R2)   SAVE ADDRESSES
         L     R3,APRINT       LOAD R3 WITH SYSPRINT ADDRESS
         L     R4,AFLAGS       GET FLAGS
         MVC   FLAGS,0(R4)     SAVE IT
         L     R4,APAGE        ADDRESS OF PAGE COUNT
         L     R4,0(R4)        PAGE COUNT
         TIME  DEC
         ST    R0,TIME               SAVE TIME
         ST    R1,DATE2              SAVE DATE IN PARM1
         LA    R1,DATE2
         ST    R1,PARM1
         LA    R1,OUTDATE            GET ADDRESS ON WHERE TO PUT IT
         ST    R1,PARM2              SAVE ADDRESS IN PARM2
         LINK  EP=DATE,PARAM=PARMS   GET TODAYS DATE
         MVC   PRINT(1),=C'1'        NEW PAGE
         MVC   PRINT+1(8),=C' DATE - ' MOVE IN TITLE
         MVC   PRINT+94(6),=C'TIME -' MOVE IN TITLE
         MVC   PRINT+9(30),OUTDATE+5 MOVE TODAYS DATE TO TITLE
         L     R1,TIME               GET TIME IN R2
         SRL   R1,4                  DROP OFF LAST BIT
         ST    R1,TIME               AND RE - SAVE TIME
         OI    TIME+3,PACKDIG        FORCE IN PACK DIGIT (F)
         UNPK  TEMPTIME,TIME(4)      CHANGE TO CHARACTER
         MVC   PRINT+101(2),TEMPTIME   MOVE IN HOURS
         MVC   PRINT+104(2),TEMPTIME+2 MOVE IN MINUTES
         MVC   PRINT+107(2),TEMPTIME+4 MOVE IN SECONDS
         MVC   PRINT+103(1),=C':'     MOVE IN SEPERATOR
         MVC   PRINT+106(1),=C':'     MOVE IN SEPERATOR
         BAL   R12,PRINTER           PRINT IT
         MVC   PRINT(1),=C' '        CLEAR CARRIAGE CONTROL BIT
         MVC   PRINT+2(1),=C'='      GET READY TO UNDERLINE
         MVC   PRINT+3(36),PRINT+2   UNDERLINE DATE
         MVC   PRINT+94(15),PRINT+2  UNDERLINE TIME
         BAL   R12,PRINTER           PRINT IT
         MVC   PRINT+59(15),=C' S M F S C A N ' PRETTY PROGRAM FLASH
         MVC   PRINT+2(5),=C'PAGE '  MOVE IN PAGE CONSTANT
         CVD   R4,PARMS              CONVERT R4 TO DECIMAL
         LA    R4,PARMS              LOAD ADDRESS OF PARMS INTO R4
         OI    7(R4),X'0F'           FORCE 'F' IN LAST BIT
         UNPK  UNPCKDTE,0(8,R4)      UNPACK PAGE COUNT
         LA    R4,UNPCKDTE           ADDRESS OF UNPCKDTE IN R4
         MVC   PRINT+7(4),12(R4)     MOVE IN PAGE COUNT
         BAL   R12,PRINTER            PRINT IT
         MVC   PRINT+60(1),=C'='    UNDERLINE
         MVC   PRINT+61(12),PRINT+60    UNDERLINE
         MVC   PRINT+2(9),PRINT+60      UNDERLINE
         BAL   R12,PRINTER            PRINT IT
         BAL   R12,PRINTER           PRINT BLANK LINE
         CLI   TYPEFLAG,C'S'         SYSIN PROCESSING ?
         BNE   TESTNEXT              NO - SKIP
         MVC   PRINT+39(25),=C'SYSIN CONTROL CARD REPORT'
         BAL   R12,PRINTER
         MVC   PRINT+39(1),=C'='      UNDER LINE IT
         MVC   PRINT+40(24),PRINT+39
         BAL   R12,PRINTER
         MVC   PRINT+2(27),=C'SYSIN CARD IMAGES FOLLOW :-'
         BAL   R12,PRINTER
         BAL   R12,PRINTER      BLANK LINE
         B     EOJ
TESTNEXT LA    R2,PRINT+2
         MVC   0(18,R2),=C'SMF TYPE 30 RECORD'
         LA    R2,19(R2)
         CLI   TYPEFLAG,C'F'       FULL REPORT ?
         BE    FULL                YES - PRINT IT
         MVC   0(7,R2),=C'SUMMARY'
         LA    R2,8(R2)
         B     MOVEREP                NOW MOVE IN REPORT
FULL     MVC   0(4,R2),=C'FULL'
         LA    R2,5(R2)
MOVEREP  MVC   0(6,R2),=C'REPORT'
         LA    R2,7(R2)
         BAL   R12,PRINTER            PRINT IT
         BAL   R12,PRINTER      BLANK LINE
         MVC   PRINT,SUMMTIT
         CLI   TYPEFLAG,C'F'       FULL REPORT
         BNE   A#PRINT1
         MVC   PRINT+97(8),=C' PROGRAM'
         MVC   PRINT+106(8),=C'  STEP  '
         MVC   PRINT+115(8),=C'  PROC  '
A#PRINT1 BAL   R12,PRINTER
         MVC   PRINT,SUMMTIT2
         CLI   TYPEFLAG,C'F'       FULL REPORT
         BNE   A#PRINT2
         MVC   PRINT+97(7),=C'  NAME  '
         MVC   PRINT+106(7),=C'  NAME  '
         MVC   PRINT+115(7),=C'  STEP  '
A#PRINT2 BAL   R12,PRINTER
         MVC   PRINT,SUMMTIU
         CLI   TYPEFLAG,C'F'       FULL REPORT
         BNE   A#PRINT3
         MVC   PRINT+96(28),PRINT+95
A#PRINT3 BAL   R12,PRINTER
         B     EOJ
FULLREP  EQU   *
EOJ      L     R5,APAGE        ADDRESS OF PAGE COUNT
         L     R4,0(R5)        PAGE COUNT
         LA    R4,1(R4)
         ST    R4,0(R5)        STORE IT
         EOJ
         DC    C'ADDRESS AREA ='
***********************************************************************
***  THE FOLLOWING IS THE PARAMETER LIST AS PASSED BY SMFSCAN       ***
***********************************************************************
ADDRESS  DS    11F
SAVEADDR EQU   ADDRESS+0,44
AFLAGS   EQU   ADDRESS+0,4
APRINT   EQU   ADDRESS+4,4
ACLASSES EQU   ADDRESS+8,4
APROGS   EQU   ADDRESS+12,4
AJOBS    EQU   ADDRESS+16,4
AATIME   EQU   ADDRESS+20,4
ABTIME   EQU   ADDRESS+24,4
ACTIME   EQU   ADDRESS+28,4
AETIME   EQU   ADDRESS+32,4
APSAVE   EQU   ADDRESS+36,4
APAGE    EQU   ADDRESS+40,4
***********************************************************************
*** PRINTER SUBROUTINE                                              ***
***********************************************************************
*** PURPOSE : TO PRINT LINE                                         ***
*** EXIT    : BR 12                                                 ***
*** CALLS   : NOTHING                                               ***
***********************************************************************
PRINTER  PUT   0(R3),PRINT         PUT OUT PRINT LINE
         MVI   PRINT,C' '             AND CLEAR
         MVC   PRINT+1(L'PRINT-1),PRINT      PRINT LINE OUT
         BR    R12         <========== SUBROUTINE RETURN
*** END OF PRINTER SUBROUTINE *****************************************
PARMS    DS    0D
PARM1    DS    F
PARM2    DS    F
TIME     DS    F
DATE2    DS    F
PAGECNT  DS    F
GOTTEN   DS    F
UNPCKDTE DS    CL16                UNPACK AREA FOR EDITING
TEMPTIME EQU   UNPCKDTE,6
OUTDATE  DS    CL50
PRINT    DC    CL133' '
SUMMTIT  DC    CL133'    JOB       START        END        CPU        EX
               LAPSED     EXCP  COMP  C PR RUN   RUN    JULIAN'
SUMMTIT2 DC    CL133'    NAME      TIME         TIME       TIME        X
               TIME       COUNT CODE  L TY DAY   DATE   DATE  '
SUMMTIU  DC    CL133'  ------------------------------------------------X
               -----------------------------------------------'
CLASSES  DS    CL8
JOBS     DS    4CL10
PROGS    DS    4CL10
ATIME    DS    CL5
BTIME    DS    CL5
CTIME    DS    CL6
ETIME    DS    CL6
PSAVE    DS    CL3
FLAGS    DS    CL14
CONTFLAG EQU   FLAGS+0,1
SUMFLAG  EQU   FLAGS+1,1
CLASFLAG EQU   FLAGS+2,1
NOTCFLAG EQU   FLAGS+3,1
JOBFLAG  EQU   FLAGS+4,1
PROGFLAG EQU   FLAGS+5,1
PRTYFLAG EQU   FLAGS+6,1
NOPRFLAG EQU   FLAGS+7,1
AFTRFLAG EQU   FLAGS+8,1
BFORFLAG EQU   FLAGS+9,1
TYPEFLAG EQU   FLAGS+10,1
ELAPFLAG EQU   FLAGS+11,1
CPUTFLAG EQU   FLAGS+12,1
FLUSFLAG EQU   FLAGS+13,1
         LTORG
         END
