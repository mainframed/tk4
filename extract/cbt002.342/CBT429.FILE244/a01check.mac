CHECKJOB CSECT
         SAVE  (14,12),,*          SAVE REGISTERS
         LR    R12,R15             ESTABLISH -
         USING CHECKJOB,R12        BASE REGISTER
         LR    R10,R1              SAVE PARAMETER LIST
         GETMAIN R,LV=WORKLEN      GET SAVE/WORK AREA
         ST    R1,8(R13)           CHAIN SAVE AREAS FORWARD
         ST    R13,4(R1)           CHAIN SAVE AREAS BACKWARD
         LR    R13,R1              ADDRESS -
         USING WORKAREA,R13        SAVE AREA
         LR    R1,R10              RESTORE PARAMETER LIST
*
         L     R1,0(R1)            POINT TO PROGRAM PARM
         LH    R10,0(R1)           GET LENGTH OF PROGRAM PARM
         LTR   R10,R10             PROGRAM PARM IS REQUIRED
         BZ    NOPARM              NO PROGRAM PARM SUPPLIED
         CH    R10,=H'8'           MAXIMUM OF 8 CHARACTERS
         BH    NOPARM              PROGRAM PARM TOO LONG
         MVI   JOBNAME,C' '        BLANK OUT THE JOBNAME
         MVC   JOBNAME+1(7),JOBNAME (LENGTH 8)
         BCTR  R10,0               DECREMENT FOR EXECUTE
         EX    R10,MOVEPARM        STORE PROGRAM PARM AS JOBNAME
*
RETRY    L     R10,X'10'           POINT TO CVT (FIXED)
         USING CVT,R10             ESTABLISH ADDRESSABILITY TO CVT
         L     R10,CVTASVT         POINT TO ASVT
         DROP  R10                 FINISHED WITH CVT
         USING ASVT,R10            ESTABLISH ADDRESSABILITY TO ASVT
         LA    R10,ASVTENTY        START OF ADDRESSES OF ASCBS
         DROP  R10                 FINISHED WITH ASVT
*
TESTASID CLC   0(4,R10),=X'80000000' TEST FOR LAST ENTRY
         BE    NOTFOUND            YES - RETURN
         TM    0(R10),X'80'        IS THIS ADDRESS SPACE ALLOCATED ?
         BO    NEXTASID            NO - GO AND TRY THE NEXT ONE
         L     R9,0(R10)           GET ASCB ADDRESS
         USING ASCB,R9             ESTABLISH ADDRESSABILITY TO ASCB
         L     R8,ASCBJBNI         PICK UP JOB/TSO JOBNAME
         CLC   JOBNAME,0(R8)       IS THIS THE ONE WE'RE LOOKING FOR ?
         BE    FOUNDJOB            YES - GO AND PUT OUT WTO
         L     R8,ASCBJBNS         PICK UP STC JOBNAME
         CLC   JOBNAME,0(R8)       IS THIS THE ONE WE'RE LOOKING FOR ?
         BE    FOUNDSTC            YES - GO AND PUT OUT WTO
         DROP  R9                  FINISHED WITH ASCB
NEXTASID LA    R10,4(R10)          POINT TO NEXT ASCB
         B     TESTASID            GO AND TRY THE NEXT ONE
*
NOTFOUND SR    R15,R15             SET RETURN CODE
         B     RETURN              GO BACK
*
FOUNDJOB MVC   WTOR(WTORL),WTORM   MOVE WTOR TO GETMAINED AREA
         B     PUTWTOR
*
FOUNDSTC MVC   WTOR(WTORL),WTORM   MOVE WTOR TO GETMAINED AREA
         MVC   WTOR+12(3),=C'STC'  REPLACE 'JOB' WITH 'STC'
PUTWTOR  MVC   WTOR+16(8),JOBNAME  REPLACE JOBNAME IN WTOR
         MVI   WTORECB,X'00'       ZEROISE ECB
         WTOR  ,REPLY,,WTORECB,MF=(E,WTOR)
         WAIT  ECB=WTORECB         WAIT FOR A REPLY
         CLI   REPLY,C'R'          (R)ETRY REQUESTED ?
         BE    RETRY               YES - DO ANOTHER SEARCH
         CLI   REPLY,C'C'          (C)ANCEL REQUESTED ?
         BE    CANCEL              YES - ABEND
         MVC   WTO(WTOL),WTOM      MOVE WTO TO GETMAINED AREA
         WTO   ,MF=(E,WTO)         "WHO'S A SILLY BOY, THEN?"
         B     PUTWTOR             TRY AGAIN
*
CANCEL   ABEND 16                  STOP THE JOB
*
NOPARM   WTL   'JOBNAME REQUIRED'
         LA    R15,16              SET ERROR RETURN CODE
         B     RETURN              RETURN TO CALLER
*
RETURN   LR    R10,R15             SAVE RETURN CODE
         LR    R1,R13              POINT TO GETMAINED AREA
         L     R13,4(R13)          RESTORE HIGHER SAVE AREA
         FREEMAIN R,LV=WORKLEN,A=(1) FREE GETMAINED AREA
         LR    R15,R10             RESTORE SAVE AREA
         RETURN (14,12),RC=(15)    RETURN TO CALLER
*
MOVEPARM MVC   JOBNAME(0),2(R1)    EXECUTED INSTRUCTION
*TORM    WTOR  'JOB XXXXXXXX    *** PLEASE IGNORE THIS MESSAGE ***',,1,
WTORM    WTOR  'JOB XXXXXXXX IS STILL ACTIVE - (R)ETRY OR (C)ANCEL',,1,*
               ,ROUTCDE=(1),MF=L   LIST FORM OF MACRO
WTORL    EQU   *-WTORM             LENGTH OF MACRO EXPANSION
WTOM     WTO   'INVALID REPLY - MUST BE "R" OR "C"',DESC=(5),MF=L,     X
               ROUTCDE=(2)         LIST FORM OF MACRO
WTOL     EQU   *-WTOM              LENGTH OF MACRO EXPANSION
*
         YREGS
         LTORG
*
WORKAREA DSECT
         DS    18F                 REGISTER SAVE AREA
JOBNAME  DS    CL8                 JOBNAME TO SEARCH FOR
WTOR     DS    0F                  LIST FORM OF MACRO
         ORG   WTOR+WTORL          LENGTH OF MACRO EXPANSION
WTORECB  DS    F                   ECB FOR WTOR
REPLY    DS    CL1                 REPLY AREA FOR WTOR
WTO      DS    0F                  LIST FORM OF MACRO
         ORG   WTO+WTOL            LENGTH OF MACRO EXPANSION
WORKLEN  EQU   *-WORKAREA          LENGTH OF WORK AREA
*
         PRINT ON,NOGEN
         CVT   DSECT=YES
         IHAASVT
         IHAASCB
*
         END   CHECKJOB
