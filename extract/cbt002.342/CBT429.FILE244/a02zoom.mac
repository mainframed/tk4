/**********************************************************************/
/*                                                                    */
/* Title:  ZOOM                                                       */
/* Author: Rob Mellor, Systems Programming Group, LOLA                */
/* Date:   12th October 1989                                          */
/*                                                                    */
/* Doc:    By placing the cursor on a JCL card containing an EXEC     */
/*         statement, and issueing the ZOOM command, this edit macro  */
/*         will 'zoom in' on (ie. display in browse) the JCL proc     */
/*         featured on the statement.                                 */
/*                                                                    */
/*         This command is most useful when defined to a PF key,      */
/*         thus leaving the cursor free to move to the line           */
/*         containing the EXEC statement.                             */
/*                                                                    */
/* NOTE==> You must customise this macro to reflect the Proclibs      */
/*         used at your shop. These should be obtained from your      */
/*         installation's JES2 procedure (make sure you use the       */
/*         right concatenation if more than one exists).              */
/*                                                                    */
/* Parms:  DEBUG  -  display clist statements during execution.       */
/*                                                                    */
/**********************************************************************/
  ISREDIT MACRO ( DEBUG )
/*------------------------------------------*/
  CONTROL NOFLUSH END(ENDO)
  IF &SYSCAPS(&DEBUG) = DEBUG THEN +
    CONTROL LIST CONLIST SYMLIST MSG PROMPT
  ELSE +
    CONTROL NOPROMPT NOMSG
  ISPEXEC CONTROL DISPLAY REFRESH
/*------------------------------------------*/

  ISREDIT (SYSDVAL) = LINE .ZCSR
  READDVAL DUMMY FUNCTION MEMBER

  IF &STR(&FUNCTION) = EXEC THEN +
    DO
    IF &LENGTH(&STR(&MEMBER)) >= 4 THEN +
      IF &SUBSTR(1:4,&STR(&MEMBER)) = &STR(PGM=) THEN +
        GOTO ERR
      ELSE +
        GOTO PROCLIB
    ENDO
  ELSE +
    DO
ERR:+
    SET &ZEDSMSG = &STR(Invalid card type.)
    SET &ZEDLMSG = &STR(Clist &SYSICMD only works on EXEC +
                   procedure cards.)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
    ENDO

/*--------------------------------------------------------------------*/
/* NOTE==> You must customise this list to reflect the Proclibs       */
/*         used at your shop. These should be obtained from your      */
/*         installation's JES2 procedure (make sure you use the       */
/*         right concatenation if more than one exists).              */
/*--------------------------------------------------------------------*/
PROCLIB:+
  SET PROC1  = SYS1.PROCLIB
  SET PROC2  = ESYS1.PROCLIB
  SET PROC3  = IPO1.PROCLIB
  SET PROC4  = IMS1.L130.PROCLIB
  SET PROC5  = IMS1.LV1.PROCLIB
  SET PROC6  = SG1.PROCLIB
  SET PROC7  = OPS1.PROCLIB
  SET PROC8  = IMS2.L130.PROCLIB
  SET PROC9  = IMS2.LV1.PROCLIB
  SET PROC10 = LOLA2.PROCLIB
  SET PROC11 = SG1.MKV50.PROCLIB
  SET PROC12 = E.PROCLIB
  SET PROC13 = STC1.PROCLIB
  SET PROC14 = STC2.PROCLIB
  SET PROC15 = TSO1.PROCLIB
  SET PROC16 = TSO2.PROCLIB

/*--------------------------------------------------------------------*/
/* NOTE==> Set MAXNUM to the total number of Proclibs listed above.   */
/*--------------------------------------------------------------------*/
  SET &MAXNUM = 16

SEARCH:+
  SET &FOUND = NO
  SET &PROC = &&PROC
  DO &I = 1 TO &MAXNUM UNTIL &FOUND = YES
     IF &SYSDSN('&STR(&PROC&I)(&MEMBER)') = OK THEN +
        SET &FOUND = YES
  ENDO

  IF &FOUND = YES THEN +
     DO
     SET &DSN = &STR(&PROC&I(&MEMBER))
     ISPEXEC BROWSE DATASET('&DSN')
     ENDO
  ELSE +
     DO
     SET &ZEDSMSG = &STR(Proc not found)
     SET &ZEDLMSG = &STR(Member &MEMBER could not be found in any +
                    JCL procedure library)
     ISPEXEC SETMSG MSG(ISRZ001)
     ENDO
  EXIT CODE(0)
