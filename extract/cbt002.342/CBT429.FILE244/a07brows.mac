         MACRO
&N       BEGIN &R,&R2,&RENT=N
* ******************************************************************* *
*                                                                     *
*                             B E G I N                               *
*                                                                     *
*        GENERATE A CSECT STATEMENT, SPECIFY AND LOAD THE BASE        *
*        REGISTERS, GENERATE STANDARD LINKAGE WITH SAVE AREA.         *
*                                                                     *
         LCLC  &REG,&LAB,&REG2,&REG3,&REG4,&BASE2
         GBLC  &RENTGBL
         AIF   (K'&N NE 0).LABOK
&LAB     SETC  'NONAME'
         MNOTE 4,'***** NO CSECT NAME SPECIFIED, NONAME USED *********'
         AGO   .LABCNT
.LABOK   ANOP
&LAB     SETC  '&N'
.LABCNT  ANOP
         AIF   (K'&R NE 0).REGOK
&REG     SETC  '12'
         MNOTE 4,'***** NO BASE REGISTER SPECIFIED, 12 USED **********'
         AGO   .REGCNT
.REGOK   ANOP
         AIF   ('&R'(1,1) NE 'R').NUMB
&REG     SETC  '&R'(2,2)
         AGO   .COMP
.NUMB    ANOP
&REG     SETC  '&R'
.COMP    ANOP
         AIF   ('&REG' LT '2').E1
         AIF   ('&REG' GT '12').E1
&REG     SETC  '&R'
.REGCNT  ANOP
&REG2    SETC  '&REG            USE &REG AS BASE REGISTER'
&BASE2   SETC  '1'
         AIF   (K'&R2 EQ 0).BASE1
&BASE2   SETC  '2'
         AIF   ('&R2'(1,1) NE 'R').NUMB2
&REG3    SETC  '&R2'(2,2)
         AGO   .COMP2
.NUMB2   ANOP
&REG3    SETC  '&R2'
.COMP2   ANOP
         AIF   ('&REG3' LT '2').E12
         AIF   ('&REG3' GT '12').E12
&REG3    SETC  '&R2'
         AIF   ('&REG3' EQ '&REG').E12
&REG4    SETC  '&REG3       USE &REG3 AS 2ND BASE REGISTER'
         MNOTE '***** THIS PROGRAM WILL HAVE 2 BASE REGISTERS ******'
.BASE1   ANOP
&RENTGBL SETC  'N'
         AIF   ('&RENT'(1,1) NE 'Y').NOTRENT
&RENTGBL SETC  'Y'
.NOTRENT ANOP
&LAB     CSECT                                                        *
         STM   14,12,12(13)     SAVE REGISTERS IN CALLERS AREA        *
         LR    &REG,15          *                                     *
         USING &LAB,&REG2                                             *
         AIF   ('&BASE2' EQ '1').AONE
         USING &LAB+4096,&REG4                                        *
         LA    &REG3,2048(&REG)                                       *
         LA    &REG3,2048(&REG3)                                      *
.AONE    ANOP
         AIF   ('&RENTGBL' EQ 'N').ANOTR
         GETMAIN R,LV=72        GET STORAGE FOR SAVE AREA             *
         ST    13,4(1)          PUT ADDRESS OF HIS SAVE AREA IN YOURS *
         ST    1,8(13)          PUT ADDRESS OF YOUR SAVE AREA IN HIS  *
         XR    13,1             *                                     *
         XR    1,13             *EXCHANGE REGISTERS 1 AND R13         *
         XR    13,1             *                                     *
         BAL   1,PRGMSAVE       BRANCH PASSED ASSEMBLY INFORMATION    *
*                                                                     *
         MNOTE '====> RE-ENTRANT VERSION OF EOJ WILL BE USED <==== *'
*                                                                     *
         AGO   .INFO
.ANOTR   ANOP
         LR    15,13                                                  *
         BAL   13,PRGMSAVE+118                                        *
PRGMSAVE DC    18F'0'                                                 *
.INFO    ANOP
*                                                                     *
*        ASSEMBLY INFORMATION - DATE, TIME AND CSECT NAME FOLLOW      *
*                                                                     *
         DC    CL8'&LAB'         *********  CSECT NAME  ***********   *
*                                                                     *
         DC    CL24'WRITTEN BY K E FERGUSON '                         *
*                                                                     *
         DC    CL8'&SYSDATE'     **** ASSEMBLY DATE (MM/DD/YY) ****   *
         DC    CL6' &SYSTIME'    ***** ASSEMBLY TIME (HH.MM) ******   *
*                                                                     *
         AIF   ('&RENTGBL' EQ 'N').ANOTR2
PRGMSAVE L     1,4(13)           RELOAD ADDRESS OF HIS SAVE AREA      *
         LM    0,1,20(1)         RESET REGISTERS 0 AND 1              *
         AGO   .EXIT
.ANOTR2  ANOP
         ST    13,8(15)                                               *
         ST    15,4(13)                                               *
.EXIT    ANOP
*                                                                K.F. *
* ******************************************************************* *
         SPACE 1
         MEXIT
.E12     MNOTE 8,' -- VALUE FOR SECOND BASE REGISTER INVALID -- '
.E1      MNOTE 8,'IMPROPER REGISTER SPECIFIED, NO STATEMENTS GENERATED'
         MEND
