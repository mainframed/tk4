LDELNVOL TITLE '       UNCATALOG ALL DASD DATA SETS ON OFFLINE VOLUMES'
***********************************************************************
*                                                                     *
*        LDELNVOL - UNCATALOG ALL DASD DATA SETS ON OFFLINE VOLUMES   *
*                                                                     *
*        AUTHOR: DAVE THORBY, MAY 1990                                *
*                                                                     *
*        THIS PROGRAM INVOKES UCBSCAN TO FIND ALL ONLINE DASD         *
*        VOLUMES.  IT THEN INVOKES IDCAMS, READS THE OUTPUT AND       *
*        CREATES  "DELETE  ...  NOSCRATCH"  CARDS FOR ALL DASD DATA   *
*        SETS ON OFFLINE VOLUMES.                                     *
*        THE INPUT IS ONE OR MORE  "LISTCAT  ...  VOLUME"  CARDS.     *
*                                                                     *
*        REGISTER USAGE:                                              *
*        R2    UCB / WORK                                             *
*        R3    CVT / WORK                                             *
*        R4    *SPARE*                                                *
*        R5    *SPARE*                                                *
*        R6    *SPARE*                                                *
*        R7    *SPARE*                                                *
*        R8    *SPARE*                                                *
*        R9    NUMBER OF VOLUMES FOUND                                *
*        R10   BASE REGISTER - SYSPRINT EXIT                          *
*        R11   BASE REGISTER - VOLUME TABLE                           *
*        R12   BASE REGISTER - MAIN PROGRAM                           *
*        R13   SAVE AREA                                              *
*                                                                     *
***********************************************************************
         SPACE 3
LDELNVOL CSECT
         SAVE  (14,12),,'LDELNVOL &SYSDATE &SYSTIME'
         LR    R12,R15             LOAD BASE REGISTER
         USING LDELNVOL,R12        ESTABLISH BASE REGISTER
         BAL   R11,BEGIN           POINT TO PROGRAM SAVE AREA
         DC    18F'0'              REGISTER SAVE AREA
BEGIN    ST    R11,8(R13)          CHAIN SAVE AREAS FORWARD
         ST    R13,4(R11)          CHAIN SAVE AREAS BACKWARD
         LR    R13,R11             POINT TO PROGRAM SAVE AREA
         LA    R10,PRNTEXIT        PICK UP ADDRESS OF EXIT ROUTINE
         USING PRNTEXIT,R10        ADDRESS REMAINDER OF PROGRAM
         SPACE 3
         LA    R11,VOL_NAME        POINT TO VOLUME NAME TABLE
         SR    R9,R9               ZEROISE COUNT OF VOLUMES
         L     R3,CVTPTR           POINT TO CVT
         USING CVTMAP,R3           ADDRESS CVT
         LA    R1,UCB_PARM         POINT TO UCBSCAN PARM
         USING UCBOB,R2            MAP UCB
UCB_SRCH L     R15,CVTUCBSC        PICK UP UCBSCAN ADDRESS
         BALR  R14,R15             INVOKE UCBSCAN
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   UCB_END             END OF UCB CHAIN
         L     R2,UCB_ADDR         PICK UP ADDRESS OF UCB
         TM    UCBSTAT,UCBONLI     IS THE DEVICE ONLINE ?
         BZ    UCB_SRCH            NO - GET NEXT UCB
         MVC   0(6,R11),UCBVOLI    SAVE VOLUME SERIAL
         LA    R9,1(,R9)           INCREMENT NUMBER OF VOLUMES
         LA    R11,6(,R11)         POINT TO NEXT ADDRESS
         B     UCB_SRCH            GET NEXT UCB
         DROP  R2,R3               FINISHED WITH MAPS
         SPACE 3
UCB_END  MVC   0(4,R11),=F'-1'     FLAG END OF VOLUME TABLE
         OPEN  (SYSPRINT,OUTPUT)   OPEN PRINT FILE
         CVD   R9,PARMLIST         NUMBER OF VOLUMES
         MVC   P_HEADER+27(4),=X'40202120'
         ED    P_HEADER+27(4),PARMLIST+6
         PUT   SYSPRINT,P_HEADER   PRINT HEADING LINE
         LA    R11,VOL_NAME        POINT TO VOLUME NAME TABLE
P_LINE   LA    R2,P_DETAIL+1       POINT TO OUTPUT LOCATION
         LA    R3,10               NUMBER OF VOLUMES PER LINE
P_MOVE   MVC   0(6,R2),0(R11)      MOVE VOLUME SERIAL TO PRINT LINE
         CLI   6(R11),X'FF'        END OF TABLE ?
         BE    P_CLOSE             YES - END OF PRINT
         LA    R11,6(,R11)         POINT TO NEXT VOLSER
         LA    R2,8(,R2)           POINT TO NEXT PRINT LOCATION
         BCT   R3,P_MOVE           MOVE NEXT VOLSER
         PUT   SYSPRINT,P_DETAIL   PRINT DETAIL LINE
         MVC   P_DETAIL+1(80),P_DETAIL BLANK OUT PRINT LINE
         B     P_LINE              SET UP NEXT LINE
         SPACE 2
P_CLOSE  PUT   SYSPRINT,P_DETAIL   PRINT LAST DETAIL LINE
         CLOSE (SYSPRINT)          CLOSE PRINT FILE
*??????????????????????????????????????????????????????????????????????
         OPEN  (CARDOUT,OUTPUT)    OPEN OUTPUT FILE
         LA    R8,1                HANDLE ONE DDNAME
         LA    R9,=CL10'DDSYSPRINT' ADDRESS OVERRIDE DDNAME
         LA    R11,SAVEAREA        ADDRESS OF 'USER DATA'
         STM   R8,R11,PARMLIST     STORE I/O LIST FOR IDCAMS
         LA    R6,=H'0'            NO PARM
         LA    R7,=H'0'            NO DDNAMES OVERRIDE LIST
         LA    R8,=H'0'            NO PAGE NUMBER LIST
         LA    R9,PARMLIST         POINT TO I/O LIST
         LINK  EP=IDCAMS,PARAM=((6),(7),(8),(9)),VL=1
         SPACE 3
         CLOSE (CARDOUT)           END OF OUTPUT FILE
         L     R13,4(R13)          PICK UP PREVIOUS SAVE AREA
         RETURN (14,12),RC=0       RETURN TO CALLER
         SPACE 3
SAVEAREA DC    18F'0'              REGISTER SAVE AREA
         EJECT
PRNTEXIT DS    0D
         DROP  R12,R10
         SAVE  (14,12),,'LDELNVOL - SYSPRINT EXIT'
         LR    R10,R15             LOAD BASE REGISTER
         USING PRNTEXIT,R10        ADDRESS REMAINDER OF PROGRAM
         LM    R7,R9,0(R1)         PICK UP PARAMETERS
         ST    R7,8(R13)           CHAIN SAVE AREAS FORWARD
         ST    R13,4(R7)           CHAIN SAVE AREAS BACKWARD
         LR    R13,R7              ADDRESS SAVE AREA
         SPACE 2
         CLI   0(R8),X'0C'         IS THIS A PUT OPERATION
         BNE   RETURN              NO - RETURN TO CALLER
         MVC   PRINTIN,PRINTIN-1   BLANK OUT PRINT LINE
         L     R7,0(R9)            PICK UP RECORD ADDRESS
         L     R8,4(R9)            PICK UP RECORD LENGTH
         BCTR  R8,0                REMOVE RDW AND 1 FOR EXECUTE
         EX    R8,MOVELINE         MOVE PRINT LINE TO STORAGE
         CLC   =C'CLUSTER',PRINTIN+1 CLUSTER NAME ?
         BE    CLUSTER             YES - WRITE OUTPUT FILE
         CLC   =C'NONVSAM',PRINTIN+1 NON-VSAM DATA SET ?
         BE    NONVSAM             YES - WRITE OUTPUT FILE
         CLC   =C'VOLSER',PRINTIN+8 VOLUME SERIAL NUMBER ?
         BE    VOLSER              YES - WRITE OUTPUT FILE
RETURN   L     R13,4(R13)          RESTORE HIGHER SAVE AREA
         RETURN (14,12),RC=0       RETURN TO CALLER
         SPACE 3
MOVELINE MVC   PRINTIN(0),0(R7)    EXECUTED INSTRUCTION
         EJECT
CLUSTER  CLC   =C'SYS1.VVDS.',PRINTIN+17
         BE    RETURN              DO NOT DELETE VVDS ENTRIES
         MVC   AMS_CARD+9(44),PRINTIN+17
         MVC   AMS_CARD+64(7),=C'CLUSTER'
         B     RETURN              GET NEXT LINE
         SPACE 3
NONVSAM  MVC   AMS_CARD+9(44),PRINTIN+17
         MVC   AMS_CARD+64(7),=C'NONVSAM'
         B     RETURN              GET NEXT LINE
         SPACE 3
VOLSER   CLI   AMS_CARD+64,C'?'    READY FOR VOLSER ?
         BE    RETURN              NO - FORGET IT
         CLC   =C'X''32008003',PRINTIN+50  TAPE DATA SET ?
         BE    NO_SRTCH            YES - FORGET IT
         CLC   =C'X''00068000',PRINTIN+50  ESOTERIC TAPE DATA SET ?
         BE    NO_SRTCH            YES - FORGET IT
         CLC   =C'X''34008003',PRINTIN+50  TAPE DATA SET ?
         BE    NO_SRTCH            YES - FORGET IT
         CLC   =C'X''00058000',PRINTIN+50  ESOTERIC TAPE DATA SET ?
         BE    NO_SRTCH            YES - FORGET IT
         CLC   =C'X''78008080',PRINTIN+50  CARTRIDGE DATA SET ?
         BE    NO_SRTCH            YES - FORGET IT
         CLC   =C'X''33008003',PRINTIN+50  COMPATIBLE MODE DATA SET ?
         BE    NO_SRTCH            YES - FORGET IT
         CLC   =C'X''00000000',PRINTIN+50  INDIRECT ADDRESSING ?
         BE    NO_SRTCH            YES - FORGET IT
         CLC   =C'MIGRAT',PRINTIN+26 IS IT MIGRATED ?
         BE    NO_SRTCH            YES - FORGET IT
         LA    R11,VOL_NAME        POINT TO TABLE OF VOLUME SERIALS
VOL_CHK  CLC   0(6,R11),PRINTIN+26 IS THIS AN ONLINE VOLUME ?
         BE    NO_SRTCH            YES - DO NOT DELETE
         LA    R11,6(,R11)         POINT TO NEXT VOLSER
         CLI   0(R11),X'FF'        END OF TABLE ?
         BNE   VOL_CHK             NO - TRY NEXT ENTRY
         SPACE 1
PUT_CARD MVC   AMS_CARD+74(6),PRINTIN+26 MOVE IN VOLUME SERIAL
         PUT   CARDOUT,AMS_CARD    WRITE DELETE CARD
NO_SRTCH MVI   AMS_CARD+64,C'?'    NOT READY FOR VOLSER
         B     RETURN              GET NEXT LINE
         EJECT
PARMLIST DS    4F                  WORK AREA
AMS_CARD DC    CL80' DELETE  ----+----1----+----2----+----3----+----4--*
               -- NOSCRATCH ??????? /*VOLSER'
P_HEADER DC    CL81'1LDELNVOL     THE FOLLOWING 000 DASD VOLUMES ARE CU*
               RRENTLY ONLINE:'
P_DETAIL DC    CL81' '
PRINTIN  DS    CL133
         SPACE 1
CARDOUT  DCB   DSORG=PS,RECFM=FB,LRECL=80,BLKSIZE=3120,DDNAME=CARDOUT, *
               MACRF=PM
SYSPRINT DCB   DSORG=PS,RECFM=FBA,LRECL=81,BLKSIZE=3159,DDNAME=SYSPRINT*
               ,MACRF=PM
         EJECT
UCB_PARM DC    A(UCB_WORK)
         DC    A(UCB_TYPE)
         DC    A(UCB_ADDR+X'80000000')
UCB_WORK DC    XL100'00'
UCB_ADDR DS    A
UCB_TYPE DC    AL1(UCB3DACC)
         SPACE 3
         LTORG
VOL_NAME DC    682CL6' '           LIST OF ONLINE VOLUMES (= 4K BYTES)
         DC    F'-1'               END OF VOLUME TABLE
         YREGS
         DSECT
         IEFUCBOB
         CVT   DSECT=YES
         END   LDELNVOL
