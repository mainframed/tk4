//SYSPEXIT JOB  '9999,C4003,CBIPO   ',
//        'KEVINS',CLASS=H,PRTY=6,MSGCLASS=P,NOTIFY=TSOSYSJ
//ASM      EXEC PGM=IEV90,
//            PARM='OBJECT,XREF(SHORT)'
//SYSLIB   DD   DSN=DCC.MACLIB,DISP=SHR
//         DD   DSN=SYS1.MACLIB,DISP=SHR UNIT=DISK,VOL=SER=IPL4A5
//         DD  DSN=SYS1.AMODGEN,DISP=SHR UNIT=DISK,VOL=SER=SYSCLG
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100)),
//             SEP=(SYSLIB)
//SYSUT2   DD   DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50)),
//             SEP=(SYSLIB,SYSUT1)
//SYSUT3   DD   DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD   SYSOUT=*,DCB=BLKSIZE=1089
//SYSPUNCH DD   SYSOUT=B
//SYSLIN   DD   DSN=&&OBJSET,UNIT=VIO,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS),DCB=(LRECL=80,BLKSIZE=80)
//SYSIN    DD   *
IEFUJV   CSECT
         REGEQS
         STM   R14,R12,12(R13)
         USING IEFUJV,R15
         L     R2,ANEXT                  LOCATE EXECUTABLE CODE
         BR    R2                        GO TO IT
ANEXT    DC    A(NEXT)                   ADDRESS EXECUTABLE CODE
* ******************************************************************* *
*                                                                     *
*                  DO NOT PLACE ANY CODE IN THIS MODULE               *
*                  WHICH WOULD CAUSE  TAB TO BE DISPLACED             *
*                  FROM LOC  DEC 100 HEX 64, SINCE UJV IS             *
*                  LOADED BY JOB A/C ROUTINES                         *
*                                                                     *
* ******************************************************************* *
*
         ORG   IEFUJV+100
TAB      DS    0H
*
* THIS TABLE IS NOW KEPT IN ALPHANUMERIC ORDER FOR EASE OF
*  DOCUMENTATION. ANY ADDITIONS SHOULD MAINTAIN THIS ORDER.
*  IT CAN BE MADE AS BIG AS YOU WANT, PROVIDED YOU "ORG" OVER IT.
*
         DC    CL5'ABCDE'
*        .........
*        .........
*        .........
*        .........
         DC    CL5'ZZYXW'
         DC    CL5'98765'
*
         DC    CL5'*****'       END OF TABLE MARKER
         EJECT
         ORG   IEFUJV+5100
NEXT     DS    0H
         USING *,R2
         LA    R4,TAB               TABLE ADDRESS IN REG 4
         L     R3,8(0,R1)          ADDRESS OF CARD TYPE INDICATOR
         CLI   0(R3),X'01'
         BNE   OUT                 NOT JOB CARD
         L     R3,4(0,R1)          ADDRESS OF JOB CARD IMAGE
         LR    R8,R3               SAVE CARD ADDRESS FOR LATER
         CLI   2(R3),X'40'         CONTINUED JOB CARD
         BE    OUT                 YES IGNORE IT
         LA    R7,80                CARD LIMIT
CLI03    DS    0H
         CLI   0(R3),C' '          LOOK FOR FIRST BLANK
         BE    CLC033
         LA    R3,1(0,R3)           INCREMENT 1 CARD COL.
         BCT   R7,CLI03
         B     NOT
CLC033   DS    0H
         CLC   0(3,R3),=C'JOB'      LOOK FOR JOB PARAMETER
         BE    LA333
         LA    R3,1(0,R3)           INCREMENT 1 CARD COL.
         BCT   R7,CLC033
         B     NOT
LA333    DS    0H
         LA    3,3(0,R3)            INCREMENT TO BLANK POSN. AFTER JOB
ACCA     DS    0H
         CLI   0(R3),C' '           LOOK FOR NEXT NON-BLANK
         BNE   GOT
         LA    R3,1(0,R3)           INCREMENT 1 CARD COL.
         BCT   R7,ACCA
         B     NOT
*
*       FOUND ACCOUNTING FIELD - IS IT O.K.
*
GOT      DS    0H
         LA    R7,1000              TABLE LIMIT - WAS 380
         CLC   =C'1,',0(R3)         IS IT XBATCH JOB?
         BE    OUT                 YES OK
         CLC   =C'8700',0(R3)         IS IT OLD SYSPROGS
         BE    OUT                 YES OK
         CLC   =C'8700',1(R3)         IS IT OLD SYSPROGS
         BE    OUT                 YES OK
         CLC   =C'ACCT',1(R3)         IS IT IBM
         BE    OUT                 YES OK
         CLC   =C'ACCT',2(R3)         IS IT IBM
         BE    OUT                 YES OK
         CLC   =C'''9999,',0(R3)    IS IT NEW STYLE JOB?
         BE    BUMP6               YES OK
         CLC   =C'''9999.',0(R3)       OR NEW STYLE TSO?
         BNE   NOT                 NO QUIT
BUMP6    DS    0H
         LA    R3,6(0,R3)           PASS 9999
TABCF    DS    0H
         CLC   0(5,R4),0(R3)         SEE IF IN TABLE
         BE    OUT                 IF THERE  O.K.
         CLC   0(5,R4),=C'*****'     IF END OF TABLE REACHED
         BE    NOT                 GO TO CANCEL JOB
         LA    R4,5(0,R4)             INCREMENT TO NEXT TABLE ENTRY
         BCT   R7,TABCF
NOT      DS    0H
         MVC   72(8,R8),=C'CODE=1  ' SIGNAL A/C CODE ERROR
RC4      DS    0H
         LM    R14,R12,12(R13)
         LA    R15,4
         BR    R14
OUT      DS    0H
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
         LTORG
         END
//LKED     EXEC PGM=IEWL,
//             REGION=512K,
//             PARM='XREF,LIST,RENT,REUS',
//             COND=(4,LT,ASM)
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=(SYSDA,SEP=(SYSLIN,SYSLMOD)),
//             SPACE=(1024,(50,20))
//SYSPRINT DD   SYSOUT=*
//SYSLIN   DD   DSN=SYS.SSI.IPLDATE,DISP=SHR
//         DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//* SLMOD  DD   DSN=DCC.EXITLIB,DISP=SHR
//SYSLMOD  DD   DSN=ZZ.TESTLIB,DISP=SHR
//SYSIN    DD   *
 NAME  IEFUJV(R)
//
