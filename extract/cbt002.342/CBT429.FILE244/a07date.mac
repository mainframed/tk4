DATE     BEGIN R12
***********************************************************************
** RETURNS TODAYS DATE OR REQUIRED DATE IN THE FORM :-               **
** '**** DAYNAME00 DDTH MONTHNAME YEAR ****DD/MM/YYYY '              **
**                                                                   **
** NOTE :- THIS PROGRAM HAS A LINK TO THE PROGRAM 'DAY'              **
*********************************************************************
*********************************************************************
*                                                                   *
*                      D I S C L A I M E R                          *
*                                                                   *
*   This computer programming material has been developed by        *
*   Kevin E. Ferguson, Technical Support Manager, at Cleveland      *
*   County Council. Please address any inquiries to:-               *
*                                                                   *
*         Cleveland County Council,                                 *
*         P.O. Box 17,                                              *
*         Melrose House,                                            *
*         1 Melrose Street,                                         *
*         Middlesbrough,                                            *
*         Cleveland. UK                                             *
*         TS1 1LY                                                   *
*         (0642) 248155                                             *
*                                                                   *
*         ATTN:  K.E.Ferguson (Ext. 2290)                           *
*         (or:   Technical Support (Ext. 2363))                     *
*                                                                   *
*   This material has been used successfully by Cleveland County    *
*   Council, and to the best of our knowledge this material and any *
*   system(s) of which it is a part are operational as of the       *
*   service level or date stated in the body of this material (if   *
*   so stated). However, NO warranty is given or implied as to the  *
*   accuracy of this material or any related material or systems,   *
*   and no responsibility is assumed for any effect or modification *
*   directly or indirectly caused by the use of this material.      *
*   It is the responsibility of any user of this material to        *
*   evaluate its usefulness to the user's environment.              *
*                                                                   *
*   Cleveland County Council does not guarantee to keep this or any *
*   related material current, nor does it guarantee to provide      *
*   any corrections or extensions described by any users of this    *
*   material, or any corrections or extensions made in the future   *
*   by Cleveland County Council itself.                             *
*                                                                   *
*                       C O P Y R I G H T                           *
*                                                                   *
*     This computer programming material remains the exclusive      *
*     property of Cleveland County Council. Permission for its use  *
*     may be obtained by contacting the address above               *
*                                                                   *
*   Cleveland County Council hereby grants permission for this      *
*   programming material to be :-                                   *
*                                                                   *
*   a). Stored on your system and displayed on your terminals.      *
*                                                                   *
*   b). Modified to meet your sites specific needs.                 *
*                                                                   *
*   c). Printed (or Photocopied), but only the specific number of   *
*       copies as are necessary for your sites needs.               *
*                                                                   *
*   d). Circulated amongst bona fida members of the following IBM   *
*       user groups or their sub-groups :-                          *
*                                                                   *
*            i) G.U.I.D.E. (UK and US)                              *
*           ii) SHARE                                               *
*                                                                   *
*       with the provisor that the copyright is not infringed by    *
*       those members.                                              *
*                                                                   *
***********************************************************************
         REGEQS
         EJECT
         L     R2,0(R1)                PRESERVE R1 IN R2
         CLC   0(4,R2),=F'0'           ANY PARMS ?
         BE    TICK                    NO - GET TODAYS DATE
         L     R3,0(R2)                LOAD R3 WITH DATE
         L     R3,0(R3)                LOAD R3 WITH DATE
         ST    R3,FULLWORK             MOVE DATE PASSED TO FULLWORD
         L     R3,4(R2)                ADDRESS OF GET MAIN AREA
         ST    R3,GETAREA              SAVE IT
         MVI   PARMFLAG,C'Y'           SET PARM PASSED FLAG ON
         B     LOAD                    NOW CONTINUE
TICK     TIME  DEC
         ST    R1,FULLWORK             STORE DATE PART IN FULLWORD
         MVI   PARMFLAG,C'N'           SET PARM PASSED FLAG OFF
         GETMAIN RC,LV=9,SP=1,LOC=BELOW ET 9 BYTES OF MAIN STORAGE
         ST    R1,GETAREA              SAVE ADDRESS OF GOTTEN AREA
LOAD     L     R3,FULLWORK             LOAD DATE INTO R3
         SRL   R3,16                   REMOVE SIGN AND DAY PART
         SLL   R3,4                    MAKE ROOM FOR SIGN
         ST    R3,FULLWORD             SAVE IT
         OI    FULLWORD+3,X'0F'        MOVE IN SIGN BIT
         CVB   R3,DECIMAL              CONVERT TO BINARY
         SR    R2,R2                   CLEAR R2
         ST    R2,DECIMAL              CLEAR DECIMAL
         LA    R5,4                    LOAD 4 INTO R5 (FOR LEAP YEAR)
         DR    R2,R5                   DIVIDE YEAR BY 4
         LTR   R2,R2                   IS THERE A REMAINDER ?
         BZ    LEAP                    NO - ITS A LEAP YEAR
         MVI   LEAPFLAG,C'N'           SET LEAPYEAR FLAG OFF
         B     KEEPON
LEAP     MVI   LEAPFLAG,C'Y'           SET LEAPYEAR FLAG ON
KEEPON   UNPK  UNPKDTE,FULLWORK        UNPACK FULLWORD
         MVC   YY,YEAR                 MOVE YEAR TO OUTDATE
         L     R3,FULLWORK             LOAD R3 WITH DATE
         SRL   R3,4                    REMOVE SIGN
         LR    R4,R3                   ALSO PUT IT IN R4
         SRL   R3,12                   REMOVE DAY PART FROM R3
         SLL   R3,12                   MAKE OF THE FORM YY000
         SR    R4,R3                   R4 SHOULD NOW = DDD
         SLL   R4,4                    MAKE ROOM FOR SIGN
         ST    R4,FULLWORD             SAVE R4 IN FULLWORD
         OI    FULLWORD+3,X'0F'        FORCE 'F' INTO LAST BYTE
         CVB   R4,DECIMAL              CONVERT TO BINARY
         SR    R2,R2                   CLEAR R2 FOR MONTH COUNT
         LA    R6,DAYS                 START OF TABLE OF DAYS IN R6
LOOP     LA    R2,1(R2)                UP R2 BY ONE
         C     R2,=F'12'               END OF THE YEAR ?
         BH    CONTINUE                YES - ABEND
         LR    R5,R4                   KEEP TAB OF R4
         S     R4,0(R6)                TAKE NUMBER OF DAYS OFF
         BNP   FOUND                   IF RESULT NOT + FOUND
         C     R2,=F'2'                IS IT FEB ?
         BNE   UPIT                    NO - KEEP GOING
         CLI   LEAPFLAG,C'Y'           IS IT A LEAP YEAR
         BNE   UPIT                    NO - KEEP GOING
         S     R4,=F'1'                TAKE OFF ONE MORE DAY
         BNP   FOUND                   IF RESULT NOT + FOUND
UPIT     LA    R6,4(R6)                UP TABLE TO NEXT ENTRY
         B     LOOP                    AND LOOP RETURN
CONTINUE ABEND 33,DUMP                 ABEND - TOO MANY MONTHS IN YEAR
FOUND    CVD   R5,DECIMAL              CONVERT R5 TO DECIMAL
         LA    R5,DECIMAL              LOAD ADDRESS OF DECIMAL INTO R5
         OI    7(R5),X'0F'             FORCE 'F' INTO LAST BYTE
         UNPK  UNPKDTE,FULLWORD        UNPACK FULLWORD
         MVC   DD,DAY+1                MOVE DAY INTO OUTDATE
         LR    R3,R2                   SAVE R2 IN R3 FOR PARMBACK
         CVD   R2,DECIMAL              CONVERT R5 TO DECIMAL
         LA    R2,DECIMAL              LOAD ADDRESS OF DECIMAL INTO R5
         OI    7(R2),X'0F'             FORCE 'F' INTO LAST BYTE
         UNPK  UNPKDTE,FULLWORD        UNPACK FULLWORD
         MVC   MM,DAY+1                MOVE MONTH INTO OUTDATE
         MVC   WYEAR,MM+3              MOVE YEAR INTO WTO
         MVC   WDAY,DD                 MOVE DAY INTO WTO
         MVC   SUFF,=C'TH'             MOVE IN SUFFIX
         CLI   WDAY+1,C'3'             IS IT X3 RD
         BNE   TRY2                    NO - TRY 2ND
         CLI   WDAY,C'1'               IS IT THE 13TH
         BE    SUPPZERO                YES - DOESN'T MATTER
         MVC   SUFF,=C'RD'             CHANGE SUFFIX TO RD
         BE    SUPPZERO                YES - DOESN'T MATTER
TRY2     CLI   WDAY+1,C'2'             IS IT X2 ND
         BNE   TRY1                    NO - TRY 1ST
         CLI   WDAY,C'1'               IS IT THE 12TH
         BE    SUPPZERO                YES - DOESN'T MATTER
         MVC   SUFF,=C'ND'             CHANGE SUFFIX TO ND
         BE    SUPPZERO                YES - DOESN'T MATTER
TRY1     CLI   WDAY+1,C'1'             IS IT X2 ND
         BNE   SUPPZERO                NO - SUPPRESS ANY ZERO
         CLI   WDAY,C'1'               IS IT THE 11TH
         BE    SUPPZERO                YES - DOESN'T MATTER
         MVC   SUFF,=C'ST'             CHANGE SUFFIX TO ND
SUPPZERO CLI   WDAY,C'0'               IS FIRST CHARACTER A ZERO
         BNE   MONPRO                  NO - PROCESS MONTH
         MVI   WDAY,C' '               CHANGE IT TO SPACE
MONPRO   LA    R4,DUMMY                POINT TO FIRST MON
MONLOOP  LA    R4,9(R4)                AND UP 9 BYTES
         BCT   R3,MONLOOP              ROUND UNTIL MONTH COUNT = 0
         MVC   WMONTH,0(R4)            AND MOVE IN MONTH
         LA    R1,OUTDATE              PUT ADDRESS OF OUTDATE INTO R1
         ST    R1,DECIMAL              STORE IT IN FIRST FULLWORD
         L     R1,GETAREA              PUT ADDRESS OF WHERE TO PUT IT
         ST    R1,FULLWORD             SAVE IT IN SECOND FULLWORD
         LR    R2,R1                   PUT IT R1
         LINK  EP=DAY,PARAM=DECIMAL
         MVC   DAYNAME,0(R2)           MOVE IN DAYNAME
         CLI   PARMFLAG,C'Y'           WAS THIS DATE PASSED TO IT
         BE    PARMBACK                YES SEND IT BACK THEN
         MVC   WTO1+10(40),MESS        MOVE IN MESSAGE
WTO1     WTO   '********************************************',         X
               ROUTCDE=(2,11),DESC=(2)
         L     R1,GETAREA
         FREEMAIN RC,LV=9,A=(R1),SP=1
         B     EXIT                    NOW GET OUT
PARMBACK L     R3,GETAREA              LOAD ADDRESS OF PASSED GET AREA
         MVC   0(50,R3),MESS           MOVE IN DATE DD/MM/YYYYXXXDAY
EXIT     EOJ   C=0                     EXIT
         LTORG
MESS     DS    0CL40
         DC    CL6'****  '
DAYNAME  DS    CL9
         DC    CL1' '
WDAY     DS    CL2
SUFF     DS    CL2
         DC    CL1' '
WMONTH   DS    CL9
         DC    CL1' '
WYEAR    DS    CL4
         DC    CL5' ****'
OUTDATE  DS    0CL10
DD       DS    CL2
         DC    CL1'/'
MM       DS    CL2
         DC    CL3'/19'
YY       DS    CL2
UNPKDTE  DS    0CL8
         DS    CL3
YEAR     DS    CL2
DAY      DS    CL3
PARMFLAG DS    CL1
LEAPFLAG DS    CL1
DECIMAL  DS    0D
         DS    F
FULLWORD DS    F
FULLWORK DS    F
GETAREA  DS    F
DAYS     DC    F'31'
         DC    F'28'
         DC    F'31'
         DC    F'30'
         DC    F'31'
         DC    F'30'
         DC    F'31'
         DC    F'31'
         DC    F'30'
         DC    F'31'
         DC    F'30'
         DC    F'31'
DUMMY    DC    CL9'DUMMY****'
         DC    CL9'JANUARY  '
         DC    CL9'FEBRUARY '
         DC    CL9'MARCH    '
         DC    CL9'APRIL    '
         DC    CL9'MAY      '
         DC    CL9'JUNE     '
         DC    CL9'JULY     '
         DC    CL9'AUGUST   '
         DC    CL9'SEPTEMBER'
         DC    CL9'OCTOBER  '
         DC    CL9'NOVEMBER '
         DC    CL9'DECEMBER '
         END
