         MACRO
&LABEL   LION  &COND,&GOTO=,&CALL=
.*       THIS MACRO ESTABLISHES A PSEUDO ON-UNIT FOR A PL/I
.*       CONDITION. THE ON-UNIT MAY EITHER DO A GOTO OR CALL AN
.*       INTERNAL ROUTINE.
         GBLC  &LICOND            PL/I CONDITION CODE VALUE
         GBLC  &LIONSCT           NAME OF ONCB CSECT
         GBLC  &LIDSA             NAME OF CURRENT DSA
         GBLA  &LION#             NAME OF ON UNITS SO FAR
         LCLC  &CD
         LICOND COND=&COND        LOOK UP CONDITION NAME
&CD      SETC  '&LICOND'(1,2)
&LABEL   MVI   LIONCELS+&LION#,X'&CD'  ESTABLISH ON UNIT
         LIREVERT &COND,UPTO=&LION#,CODE=&CD     CANCEL OTHERS
&LION#   SETA  &LION#+1           BUMP ON UNIT COUNTER
&LIONSCT CSECT ,                  SWITCH TO ONCB CSECT
         DC    X'&CD'             IDENTIFY CONDITION
         AIF   ('&GOTO' EQ '').NOGOTO
         DC    X'360000'          INDICATE GOTO UNIT
         DC    A(&GOTO-&LIDSA)    DSA OFFSET OF DEST. LABEL
         AGO   .JGOTO
.NOGOTO  DC    X'160000'          INDICATE CALL UNIT
         DC    A(&CALL)           ADDR OF INTERNAL ENTRY
.JGOTO   ANOP
&SYSECT  CSECT ,                  RESTORE ORIGINAL CSECT
         MEND
