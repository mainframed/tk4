         MACRO
         XWTOLKIN &L,&ID,&IN
.**********************************************************************
.*       THIS MACRO IS USED AS IN INNER MACRO BY XWTO TO DO A TABLE   *
.*       LOOKUP FOR A SYMBOL (THE LABEL OF A MESSAGE SEGMENT), AND    *
.*       TO RETURN THE INDEX NUMBER, AND THE CORRESPONDING ELEMENT OF *
.*       ANOTHER TABLE (THE TABLE OF PARAMETER LIST NAMES). IF THE    *
.*       LABEL IS NOT IN THE LIST, IT IS INSERTED, AND AN ALIAS NAME  *
.*       IS INSERTED IN THE CORRESPONDING ELEMENT OF THE OTHER TABLE. *
.*       SOME ENTRIES IN THE SECOND TABLE MAY BE REFERENCES TO OTHER  *
.*       TABLE ELEMENTS; IN THIS CASE, THE RESULT MAY BE GIVEN AS     *
.*       "NOT FOUND", OR AS THE INDIRECTLY REFERENCED ELEMENT,        *
.*       AS CHOSEN BY THE CALLER.                                     *
.**********************************************************************
         GBLC  &XWTOFLD(200)      THE TABLE OF SEGEMENT NAMES
         GBLC  &XWTOID(200)       THE TABLE OF PARM LIST NAMES
         GBLC  &XWTOAL            THE RETURNED RESULT
         GBLA  &XWTOIX            CURRENT TABLE SIZE
         GBLA  &XWTOLK            INDEX FOR THE LABEL (0 IF NOT FOUND)
.*
.*       SEE IF THE LABEL IS ALREADY IN THE LIST.
.*
         XWTOLKUP &L              GET LABEL INDEX NUMBER
         AIF   (&XWTOLK NE 0).IND LABEL WAS FOUND
&XWTOIX  SETA  &XWTOIX+1          INCREASE SIZE
&XWTOFLD(&XWTOIX) SETC '&L'       STORE NEW LABEL
&XWTOID(&XWTOIX) SETC '&ID'       STORE CORRESPONDING LIST
&XWTOAL  SETC  '&ID'              STORE IN RETURN VALUE
         AIF   ('&IN' NE 'I').MEND IF NOT INDIRECTING, RETURN LK=0
         AIF   ('&XWTOAL'(1,1) NE '0').INDN1
&XWTOLK  SETA  &XWTOAL
&XWTOAL  SETC  '&XWTOID(&XWTOLK)'
         AGO   .MEND
.INDN1   ANOP
&XWTOLK  SETA  &XWTOIX            ELSE RETURN NUMBER OF NEW ENTRY
         AGO   .MEND
.IND     AIF   ('&IN' EQ 'I').MEND NO MORE IF INDIRECTING
         AIF   ('&XWTOAL' EQ '&XWTOID(&XWTOLK)').MEND RESULT OK IF     *
                                  RESULT NOT INDIRECT
&XWTOLK  SETA  0                  ELSE SAY NOT FOUND
.MEND    MEND
