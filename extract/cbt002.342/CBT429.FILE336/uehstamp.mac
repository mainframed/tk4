         TITLE 'UEHSTAMP -- PRIMITIVE OWNERSHIP STAMPER FOR DATA SETS'
UEHSTAMP CSECT
         SPACE
***********************************************************************
*        THIS ROUTINE IS INVOKED BY A SVC 237 INSTRUCTION INSERTED    *
*        INTO EACH SVC  32 MODULE WHICH WRITES A NEW FORMAT 1 DSCB.   *
*        ITS FUNCTION IS TO STORE THE ACF2 LOGONID UNDER WHICH        *
*        THE DATASET WAS CREATED, IN ORDER TO ENHANCE DATA SET        *
*        ACCOUNTING. PARAMETERS TO THIS SVC ARE: THE ADDRESS OF THE   *
*        DSCB TO BE WRITTEN IN REG 1, AND THE UCB ADDRESS IN REG 0.   *
*        (IN THE ABSENCE OF ACF2, THIS PICKS UP THE SMF USERID        *
*        FIELD, WHICH YOU MAY HAVE ARRANGED TO ACTUALLY BE A USERID,  *
*        OR SOMETHING ELSE USEFUL.)                                   *
***********************************************************************
         SPACE
         DC    C'IGC0023G -- SVC 237'  FOR IDENTIFICATION
         REGISTER
         ENTRY IGC237             SVC ENTRY POINT
         USING TCB,R4             STANDARD REGS FOR SVC
         USING RBSECT,R5
         USING IGC237,R6          SET UP BY SLIH
         SPACE
IGC237   L     R7,RBLINK          LOOK AT CALLER'S RB
         TM    RBOPSW+1-RBSECT(R7),X'81'    KEY<8, SUPER STATE?
         BNZR  R14                NO, IGNORE CALL
         LR    R8,R0              COPY UCB ADDR FOR CONVENIENCE
         USING UCB,R8
         USING DSCBF1,R1
*        TM    UCBSTAB,UCBBSTR    STORAGE VOLUME?
*        BZR   R14                NO, DO NOTHING
         MVC   DS1LGNID(14),=CL14' '   CLEAR LOGONID IN ADVANCE
         MVC   DS1REFD,DS1CREDT   COPY CREATION DATE TO REF DATE
         MVI   DS1OFLGS,0         CLEAR FLAG BYTE
         L     R2,TCBTCT          FIND THE TCT
         LA    R2,0(,R2)
         LTR   R2,R2
         BZR   R14                LEAVE USERID BLANK IF NONE
         L     R2,TCTJMR-SMFTCT(R2)    FIND JOB MANAGEMENT RECORD
         LTR   R2,R2              ALIAS CPA
         BZR   R14                NO USERID IF NO CPA
         MVC   DS1LGNID,CPAUSRID-CPA(R2)    STORE LOGONID IN DSCB
         BR    R14                AND RETURN TO DADSM
         SPACE
         LTORG
         SPACE
DSCBF1   DSECT
         IECSDSL1 (1)             MAP FORMAT 1 DSCB
         ORG   DS1SYSCD-1         OVERLAY RSVD & SYSTEM CODE
DS1LGNID DS    CL7                CREATOR'S ACF2 LOGONID
         DS    CL7                RESERVED FOR ANOTHER LOGONID
         DS    CL3                DATE LAST REFERENCED (THANKS, IBM)
DS1OFLGS DS    BL1                OWNERSHIP FLAGS (TO BE DEFINED)
         EJECT
         PRINT NOGEN
         IKJTCB
         SPACE
         IKJRB
         SPACE
UCB      DSECT
         IEFUCBOB
         SPACE
         IEFTCT
         SPACE
         CPA
         SPACE
         END   IGC237
