         MACRO
&LABEL   LIREVERT &COND,&UPTO=LIONCELN,&CODE=
.*       THIS MACRO GENERATES CODE TO PERFORM A "REVERT" IN A PL/I
.*       OR PSEUDO-PL/I ENVIRONMENT. IT IS ALSO USED AS AN INNER
.*       MACRO BY LION TO CANCEL PREVIOUS ON-UNITS FOR A CONDITION.
         GBLC  &LICOND            PL/I CONDITION CODE
         LCLC  &CD
         LCLC  &S
&CD      SETC  '&CODE'            SEE IF CODE PASSED BY LION
         AIF   ('&CD' NE '').CODE
         LICOND COND=&COND        IF NOT, LOOK UP CONDITION
&CD      SETC  '&LICOND'(1,2)
.CODE    AIF   ('&LICOND'(3,2) EQ '00').OKCOND
         MNOTE 8,'***** INVALID CONDITION FOR LION/LIREVERT -- &COND ***
               ***'               ERROR IF NOT PURE CONDITION
.OKCOND  AIF   ('&UPTO' EQ '0').MEND   DON'T REVERT FOR 1ST ON
&LABEL   LA    R1,LIONCELS-1
         LA    R0,1
         LNR   R0,R0
         LA    R15,LIONCELS+&UPTO-1
&S       SETC  '&SYSNDX'
LIRV&S   CLI   0(R15),X'&CD'      THIS CELL FOR THIS CONDITION?
         BNE   LIRL&S             NO.
         MVI   0(R15),0           YES, CANCEL IT
LIRL&S   BXH   R15,R0,LIRV&SYSNDX
.MEND    MEND
