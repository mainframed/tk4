         TITLE 'UKJUGET -- EASY-TO-USE PUTGET INTERFACE ROUTINE'
UKJUGET  SAVEX LV=WKLEN,PARMR=R2
*
*        THIS ROUTINE IS A SIMPLE-TO-USE PUTGET INTERFACE, INVOKED
*        BY THE UGET MACRO.  ON ENTRY, R0 ADDRESSES A CPPL OR IOPL
*        (IF NEGATIVE) AND R1 ADDRESSES THE FOLLOWING PARM LIST:
*        +0    GETLINE CONTROL BITS
*        +1    PUTGET CONTROL BITS
*        +2    INPUT MESSAGE LENGTH
*        +4    TGET OPTION BITS
*        +5    INPUT BUFFER ADDRESS
*        +8    ADDR OF OUTPUT PARAMETERS (OR 0 IF INPUT ONLY)
*
*        THE OUTPUT PARM LIST IS MAPPED AS FOLLOWS:
*        +0    0
*        +1    PUTLINE CONTROL BITS
*        +2    MESSAGE LENGTH
*        +4    TPUT OPTION BITS
*        +5    MESSAGE ADDRESS
*        +8    SECOND-LEVEL MESSAGE LENGTH (IF PRESENT)
*        +12   SECOND-LEVEL MESSAGE ADDR (IF PRESENT)
*
         USING DSA,R13
         L     R3,4(,R13)
         L     R3,20(,R3)         GET CPPL FROM REG 0 SAVE SLOT
         LTR   R3,R3              IS IT AN IOPL INSTEAD?
         BP    BLDIOPL            NO, BUILD ONE
         MVC   IOPL(12),0(R3)     YES, COPY OVER ADDRESSES
         B     IOPLBLT
BLDIOPL  MVC   IOPL(4),4(R3)      PUT UPT ADDR
         MVC   IOPL+4(4),12(R3)   AND ECT ADDR INTO IOPL
         XC    DUMECB,DUMECB      CLEAR DUMMY ATTN ECB
         LA    R4,DUMECB
         ST    R4,IOPL+8          STORE ITS ADDR IN IOPL
IOPLBLT  L     R9,8(R2)           FIND OUTPUT PARM LIST
         XC    PGPB(16),PGPB
         LTR   R9,R9              OUTPUT MSG GIVEN?
         BZ    INPTONLY           YES, HANDLE SPECIAL CASE
         MVC   PGPB(1),1(R9)      COPY PUTLINE CONTROL FLAGS
         MVC   PGPB+1(1),1(R2)    COPY 2ND FLAG BYTE
         TM    1(R2),X'80'        IS THIS A MODE MSG?
         BNZ   MODEMSG            YES.
         OI    PGPB,X'01'         NO, SET PROMPT BIT
MODEMSG  MVC   PGPB+2(1),4(R9)    COPY TPUT OPTIONS
         LH    R4,2(,R9)          GET MESSAGE LENGTH
         L     R6,4(,R9)          LOCATE MESSAGE TEXT
         LA    R5,BUFPRE+4        BUFFER FOR OUTPUT MESSAGE
         BCTR  R4,0
         EX    R4,MOVETEXT        COPY OVER TEXT
         LA    R4,5(,R4)          GET LENGTH + 4
         SLL   R4,16              ADD A ZERO OFFSET
         ST    R4,BUFPRE          STORE FOR PUTGET
INFOMSG  MVC   SINGOLD,=A(1)      INDICATE 1 SEGMENT MSG
         TM    PGPB,X'04'         IS THIS MULTI-LEVEL?
         BNZ   TWOMSGS            YES.
         LA    R7,BUFPRE
         ST    R7,SINGOLD+4       STORE MSG PTR IN OLD
         LA    R7,SINGOLD
         ST    R7,PGPB+4          NO, SET UP FOR 1 LEVEL
         B     GETIT
         SPACE
TWOMSGS  LH    R4,10(,R9)         FIND LENGTH OF HELP
         L     R6,12(,R9)         FIND HELP MSG
         LA    R5,BUFPRE2+4
         BCTR  R4,0
         EX    R4,MOVETEXT        MOVE HELP INTO BUFFER
         LA    R4,5(,R4)          GET L'HELP+4
         SLL   R4,16
         ST    R4,BUFPRE2         SAVE FOR PUTLINE
         LA    R7,DUBLOLD
         ST    R7,PGPB+4
         LA    R7,OLD2
         ST    R7,DUBLOLD         LINK THE OLD'S TOGETHER
         XC    OLD2(8),OLD2
         MVI   OLD2+7,1           MAKE HELP MSG Q SEGMENT ALSO
         B     GETIT
         SPACE
INPTONLY MVI   PGPB+1,X'80'       INDICATE MODE MSG
         MVI   PGPB,X'10'
         SPACE
GETIT    MVC   PGPB+8(1),0(R2)    MOVE IN GETLINE FLAGS
         MVC   PGPB+10(1),4(R2)   AND TGET FLAGS
         SPACE
PUTIT    PUTGET PARM=PGPB,MF=(E,IOPL)  SEND THE MSG
         CH    R15,=H'12'         NOPAUSE RETURN CODE?
         BE    NOPAUSE            YES.
         BH    RETURN             NO, SOMETHING WORSE
         CH    R15,=H'8'          NO, WAS IT ATTN?
         BE    RETURN             YES, PASS IT BACK
         LH    R5,2(,R2)          NO, GET REQUESTED LENGTH
         L     R4,4(,R2)          AND BUFFER ADDRESS
         L     R6,PGPB+12         FIND RETURNED TEXT
         LH    R7,0(,R6)          GET LENGTH RETURNED
         SH    R7,=H'4'           ADJUST
         LA    R6,4(,R6)
         TM    4(R2),X'01'        TGET ASIS?
         BZ    TGETEDIT           NO.
         CR    R5,R7              YES, MSG LONGER THAN BUFFER?
         BNH   TGETEDIT           YES, THAT'S OK
         LR    R5,R7              NO, JUST COPY OVER ACTUAL MSG
TGETEDIT ICM   R7,8,=X'40'        PAD WITH BLANKS
         MVCL  R4,R6              COPY TEXT TO USER AREA
         LR    R8,R15             SAVE PUTGET RETURN CODE
         L     R1,PGPB+12         FREEMAIN THE MSG
         LH    R0,0(,R1)
         ICM   R0,8,=X'01'        FROM SUBPOOL 1
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R8             RESTORE THE RETURN CODE
         B     RETURN
         SPACE
NOPAUSE  TM    0(R2),X'10'        WAS THIS A PROMPT?
         BNZ   RETURN             YES, PASS 12 BACK TO CALLER
         L     R8,IOPL+4          NO, FIND THE ECT ADDR
         OI    8(R8),X'80'        SAY 2ND-LEVEL MSGS TO BE THROWN OUT
         B     PUTIT              AND TRY AGAIN
         SPACE
RETURN   RETURNX RC=(15)
         SPACE
MOVETEXT MVC   0(0,R5),0(R6)  MOVE MESSAGE TO BUFFER
         SPACE
DSA      DSECT ,                  SAVE & WORK AREA
         DS    18A
DUBLOLD  DC    A(OLD2-DSA)        FIRST OLD FOR MULTI-LEVEL
SINGOLD  DC    A(1)               OLD FOR SINGLE-LEVEL
         DC    A(BUFPRE-DSA)
BUFPRE   DC    A(0)
         DS    CL256              MESSAGE WILL BE INSERTED HERE
         SPACE
OLD2     DC    A(0)               OLD FOR LEVEL 2
         DC    A(1)
         DC    A(BUFPRE2-DSA)
BUFPRE2  DC    A(0)
         DS    CL256              HELP WILL BE INSERTED HERE
         SPACE
IOPL     DC    2A(0)
         DC    A(DUMECB-DSA)      DUMMY ATTN ECB ADDR
         DC    A(PGPB-DSA)
PGPB     DC    4A(0)              PUTGET PARM BLOCK
DUMECB   DC    A(0)               DUMMY ATTN ECB
         SPACE
WKLEN    EQU   *-DSA              LENGTH OF WORK AREA
         END
