         TITLE 'UKJUPUT -- EASY-TO-USE PUTLINE INTERFACE ROUTINE'
UKJUPUT  SAVEX LV=WKLEN,PARMR=R2
*
*        THIS ROUTINE IS A SIMPLE TO USE PUTLINE INTERFACE, INVOKED
*        BY THE UPUT MACRO.  ON ENTRY, R0 ADDRESSES A CPPL OR IOPL
*        (IF NEGATIVE) AND R1 ADDRESSES THE FOLLOWING PARM LIST:
*        +0    0
*        +1    PUTLINE CONTROL BITS
*        +2    MESSAGE LENGTH
*        +4    TPUT OPTION BITS
*        +5    MESSAGE ADDRESS
*        +8    SECOND-LEVEL MESSAGE LENGTH (IF PRESENT)
*        +12   SECOND-LEVEL MESSAGE ADDR (IF PRESENT)
*
         USING DSA,R13
         L     R3,4(,R13)
         L     R3,20(,R3)         GET CPPL/IOPL FROM REG 0 SAVE SLOT
         LTR   R3,R3              IS IT AN IOPL ADDR?
         BP    BLDIOPL            NO, BUILD FROM CPPL
         MVC   IOPL(12),0(R3)     YES, COPY TO OURS
         B     IOPLBLT
BLDIOPL  MVC   IOPL(4),4(R3)      PUT UPT ADDR
         MVC   IOPL+4(4),12(R3)   AND ECT ADDR INTO IOPL
         XC    DUMECB,DUMECB      CLEAR DUMMY ECB
         LA    R4,DUMECB
         ST    R4,IOPL+8          AND STORE IN IOPL
IOPLBLT  XC    PTPB(4),PTPB
         MVC   PTPB(1),1(R2)      COPY PUTLINE CONTROL FLAGS
         MVC   PTPB+2(1),4(R2)    COPY TPUT OPTIONS
         LH    R4,2(,R2)          GET MESSAGE LENGTH
         L     R6,4(,R2)          LOCATE MESSAGE TEXT
         LA    R5,BUFPRE+4        BUFFER FOR OUTPUT MESSAGE
         BCTR  R4,0
         EX    R4,MOVETEXT        COPY OVER TEXT
         LA    R4,5(,R4)          GET LENGTH + 4
         SLL   R4,16              ADD A ZERO OFFSET
         ST    R4,BUFPRE          STORE FOR PUTLINE
         TM    PTPB,X'20'         IS THIS PUTLINE DATA?
         BZ    INFOMSG            NO, HANDLE INFORMATIONAL MSG
         LA    R7,BUFPRE
         ST    R7,PTPB+4          STORE OUTPUT BUFFER ADDR
PUTIT    PUTLINE PARM=PTPB,MF=(E,IOPL) SEND THE MSG
         RETURNX RC=(15)
         SPACE
INFOMSG  MVC   SINGOLD,=A(1)      INDICATE 1 SEGMENT MSG
         TM    PTPB,X'04'         IS THIS MULTI-LEVEL?
         BNZ   TWOMSGS            YES.
         LA    R7,BUFPRE
         ST    R7,SINGOLD+4       STORE MSG PTR IN OLD
         LA    R7,SINGOLD
         ST    R7,PTPB+4          NO, SET UP FOR 1 LEVEL
         B     PUTIT
         SPACE
TWOMSGS  LH    R4,10(,R2)         FIND LENGTH OF HELP
         L     R6,12(,R2)         FIND HELP MSG
         LA    R5,BUFPRE2+4
         BCTR  R4,0
         EX    R4,MOVETEXT        MOVE HELP INTO BUFFER
         LA    R4,5(,R4)          GET L'HELP+4
         SLL   R4,16
         ST    R4,BUFPRE2         SAVE FOR PUTLINE
         LA    R7,DUBLOLD
         ST    R7,PTPB+4
         LA    R7,OLD2
         ST    R7,DUBLOLD         LINK THE OLD'S TOGETHER
         XC    OLD2(8),OLD2
         MVI   OLD2+7,1           MAKE HELP MSG 1 SEGMENT ALSO
         B     PUTIT              GO SEND MESSAGE
         SPACE
MOVETEXT MVC   0(0,R5),0(R6)  MOVE MESSAGE TO BUFFER
         SPACE
DSA      DSECT ,                  SAVE & WORK AREA
         DS    18A
DUBLOLD  DC    A(OLD2-DSA)        FIRST OLD FOR MULTI-LEVEL
SINGOLD  DC    A(1)               OLD FOR SINGLE-LEVEL
         DC    A(BUFPRE-DSA)
BUFPRE   DC    A(0)
         DS    CL256              MESSAGE WILL BE INSERTED HERE
         SPACE
OLD2     DC    A(0)               OLD FOR LEVEL 2
         DC    A(1)
         DC    A(BUFPRE2-DSA)
BUFPRE2  DC    A(0)
         DS    CL256              HELP WILL BE INSERTED HERE
         SPACE
IOPL     DC    2A(0)
         DC    A(DUMECB-DSA)      DUMMY ATTN ECB ADDR
         DC    A(PTPB-DSA)
PTPB     DC    2A(0)              PUTLINE PARM BLOCK
DUMECB   DC    A(0)               DUMMY ATTN ECB
         SPACE
WKLEN    EQU   *-DSA              LENGTH OF WORK AREA
         END
