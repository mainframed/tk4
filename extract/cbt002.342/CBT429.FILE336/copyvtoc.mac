COPYVTOC TITLE 'ICSA DISK MAPPER - VTOC COPY ROUTINE'
COPYVTOC SAVEX BANNER=YES
         EJECT
***********************************************************************
*                                                                     *
*        COPYVTOC: COPY VTOC OF VOLUME POINTED TO BY THE FIRST        *
*                  PARAMETER TO THE SPILL FILE.  PL/I CALLABLE        *
*                  WITH OPTIONS ASSEM AND RETCODE.  REQUIRES APF      *
*                  AUTHORIZATION TO ENQ (RESERVE) ON THE VTOC.        *
*                                                                     *
*        NOTE:     IF AN I/O ERROR IS ENCOUNTERED READING THE         *
*                  FORMAT 4 DSCB, THE PROGRAM ABENDS WITH U001.       *
*                  IF AN I/O ERROR IS ENCOUNTERED READING THE REST    *
*                  OF THE VTOC, A MESSAGE IS WRITTEN TO THE JOB LOG   *
*                  INDICATING THAT A TRACK OF THE VTOC WAS SKIPPED    *
*                  BECAUSE OF AN I/O ERROR AND A RETURN CODE OF 8     *
*                  IS PASSED BACK TO THE PL/I ROUTINE.                *
*                                                                     *
*        PARAMETERS:                                                  *
*                  1. ADDRESS OF PLI STRUCTURE AS FOLLOWS: (INPUT)    *
*                     1 VOLUME_ENTRY,                                 *
*                       2 DDNAME CHAR(8),                             *
*                       2 VOLSER CHAR(6),                             *
*                       2 UCB POINTER                                 *
*                  2. DSCBS_PER_TRACK FIXED BIN(15)        (OUTPUT)   *
*                  3. VTOC_START FIXED BIN(15)             (OUTPUT)   *
*                  4. TRACKS_PER_CYLINDER FIXED BIN(15)    (OUTPUT)   *
*                                                                     *
***********************************************************************
         SPACE 2
         XC    RETCODE,RETCODE        ZERO RETURN CODE
         LR    R11,R1                 SAVE PARM ADDRESS
         L     R2,0(,R11)
         L     R2,0(,R2)              R2 -> TABLE ENTRY
         MVC   VTOCDCB+40(8),0(R2)    MOVE IN DDNAME
         L     R2,14(,R2)             R2 -> UCB
         ST    R2,UCBPTR              R2 -> UCB
         OPEN  (VTOCDCB,INPUT)        OPEN THE VTOC
         L     R1,VTOCDCB+44          FIND THE DEB
         SH    R1,=H'15'              LOOK INTO THE PREFIX
         MVC   IOB+33(7),0(R1)        FIND VTOC SEEK ADDRESS
         XC    ECB,ECB                CLEAR THE ECB
         MVC   IOB+16(4),=A(READF4)   POINT TO CCW TO READ FORMAT 4
         MVI   READKYDA+4,X'00'       MAKE SURE NOT CHAINED
         EXCP  IOB                    READ THE FORMAT 4
         WAIT  ECB=ECB                WAIT FOR COMPLETION
         TM    ECB,X'7F'              DID THE READ GO ALRIGHT?
         BNO   ABEND                  IF NOT, ABEND U001
         LA    R3,DSCB                SET UP BASE REGISTER FOR
         USING FORMAT4,R3             MAPPING THE FORMAT 4
         LH    R4,DS4VTOCE+2          GET THE FIRST CYLINDER
         LH    R5,DS4DEVSZ+2          GET DEVICE SIZE (TRKS/CYL)
         L     R10,12(,R11)           SAVE AS
         STH   R5,0(,R10)               TRACKS_PER_CYLINDER
         BCTR  R5,0                   MAKE ZERO ORIGIN
         STH   R5,TRACKS              SAVE FOR LATER
         MH    R4,DS4DEVSZ+2          COMPUTE RELATIVE TRACK
         AH    R4,DS4VTOCE+4            ADDRESS OF BEGINNING OF
         L     R10,8(,R11)              THE VTOC AND SAVE AS
         STH   R4,0(,R10)             VTOC_START
         SR    R5,R5                  CLEAR FOR INSERT
         IC    R5,DS4DEVDT              OF NUMBER OF DSCBS/TRACK
         L     R10,4(,R11)            SAVE AS
         STH   R5,0(,R10)               DSCBS_PER_TRACK
         LR    R6,R5                  COPY FOR LATER
         MH    R5,=H'140'             COMPUTE
         STH   R5,SPILLDCB+62           BLOCKSIZE OF SPILL FILE
         STH   R5,SPILLDCB+82         SAVE AS LRECL, TOO
         OPEN  (SPILLDCB,OUTPUT)      OPEN SPILL FILE
         GETMAIN R,LV=(5)             GET BLOCK BUFFER
         LR    R10,R1                 SAVE BUFFER ADDRESS
         LR    R7,R6                  NEED CCW CHAIN OF DSCBS/TRACK
         LA    R7,3(,R7)                PLUS 3 MORE
         SLL   R7,3                   MULTIPLY BY 8 TO FIND LENGTH
         LR    R8,R10                 COPY BUFFER ADDRESS
         GETMAIN R,LV=(7)             GET SPACE FOR CCW CHAIN
         LR    R7,R1                  SAVE CCW CHAIN ADDRESS
         MVC   0(24,R7),READF4        MOVE IN PATTERN CCWS
         LA    R4,8(,R7)              POINT TO SECOND CCW
         STCM  R4,B'0111',17(R7)      FIX TIC ADDRESS FOR LOOP
         LA    R7,24(,R7)             POINT TO READ CHAIN
         MVI   READKYDA+4,X'40'       SET COMMAND CHAINING BIT
         LA    R9,CCWBLOOP            SET LOOP ADDRESS
CCWBLOOP MVC   0(8,R7),READKYDA       FIX UP THE CHAIN
         STCM  R10,B'0111',1(R7)      STORE ADDRESS FOR READ
         LA    R10,140(,R10)            AND BUMP TO NEXT DSCB SLOT
         LA    R7,8(,R7)              BUMP TO NEXT READ CCW
         BCTR  R6,R9                  AND BUILD ALL THE CCWS
         S     R7,=F'8'               TURN OFF COMMAND CHAINING
         MVI   4(R7),X'00'              IN THE LAST CCW
         ST    R1,IOB+16              SAVE CCW CHAIN POINTERS IN IOB
         L     R4,0(,R11)             POINT TO THE VOLSER
         L     R4,0(,R4)                ENTRY IN THE
         LA    R4,8(,R4)                FIRST PARAMETER STRUCTURE
         RESERVE (SYSVTOC,(4),E,6,SYSTEMS),UCB=UCBPTR  GRAB THE VTOC
         LA    R9,RWLOOP              SET LOOP ADDRESS
RWLOOP   XC    ECB,ECB                CLEAR THE ECB
         EXCP  IOB                    READ A TRACK OF DSCBS
         WAIT  ECB=ECB                WAIT FOR COMPLETION
         TM    ECB,X'7F'              CHECK FOR ALL OK
         BNO   IOERROR                IF NOT, SEND SKIPPED MESSAGE
         WRITE SPILL,SF,SPILLDCB,(8),'S'  WRITE A SPILL BLOCK
         CHECK SPILL                  CHECK FOR COMPLETION
NXTTRK   LH    R1,IOB+37              POINT TO TRACK ADDRESS
         CLC   IOB+35(4),DS4VTOCE+6   WAS THIS THE LAST TRACK?
         BE    QUITREAD               YES, QUIT READING
         CH    R1,TRACKS              END OF CYLINDER?
         BE    NEXTCYL                YES, GO TO NEXT CYLINDER
         LA    R1,1(,R1)              BUMP TO NEXT TRACK
         STH   R1,IOB+37                AND SAVE IN THE IOB
         BR    R9                     GO READ NEXT TRACK
         SPACE 1
NEXTCYL  XC    IOB+37(2),IOB+37       BUMP TO START OF NEXT CYL
         LH    R1,IOB+35              UPDATE
         LA    R1,1(,R1)                CYLINDER COUNTER
         STH   R1,IOB+35              SAVE IN THE IOB
         BR    R9                     GO READ NEXT TRACK
         SPACE 1
QUITREAD DEQ   (SYSVTOC,(4),6,SYSTEMS)  LET GO OF VTOC
         CLOSE (VTOCDCB,,SPILLDCB)    CLOSE BOTH FILES
         L     R15,RETCODE            GET RETURN CODE
         RETURNX RC=(15)                AND RETURN TO CALLER
         SPACE 1
ABEND    ABEND 1,DUMP,STEP            COULD NOT READ FORMAT 4
         SPACE 2
IOERROR  LH    R1,IOB+35              GET CYLINDER ADDRESS
         CVD   R1,DOUBLE              CONVERT TO DECIMAL
         MVC   CYLADDR,PATTERN        MOVE IN EDIT PATTERN
         ED    CYLADDR,DOUBLE+5         AND EDIT CYL ADDRESS FOR MSG
         LH    R1,IOB+37              GET TRACK ADDRESS
         CVD   R1,DOUBLE              CONVERT TO DECIMAL
         MVC   TRKADDR,PATTERN        MOVE IN EDIT PATTERN
         ED    TRKADDR,DOUBLE+5         AND EDIT TRK ADDRESS FOR MSG
         WTO   MF=(E,SKIPMSG)         SEND MESSAGE
         MVC   RETCODE,=F'8'          SET RETURN CODE TO 8
         B     NXTTRK                 GO BUMP TRACK POINTER
         EJECT
         LTORG
READF4   CCW   X'23',SECTOR0,X'40',1  SET SECTOR
         CCW   X'31',IOB+35,X'40',5   SEARCH ID EQUAL
         CCW   X'08',READF4+8,0,0     LOOP UNTIL PREV. CCW SUCCEEDS
READKYDA CCW   X'0E',DSCB,X'00',140     THEN READ A DSCB
SECTOR0  DC    X'0'                   USED FOR SET SECTOR
SYSVTOC  DC    CL8'SYSVTOC'           QNAME FOR VTOC
ECB      DS    A                      ECB FOR EXCP I/O
IOB      DC    X'42',XL3'0'           IOB
         DC    A(ECB)                   FOR
         DC    XL12'0'                    READING
         DC    A(VTOCDCB)                   THE
         DC    XL16'0'                        VTOC
DSCB     DS    CL140                  SPACE FOR THE FORMAT 4
UCBPTR   DS    A                      POINTER TO THE UCB
TRACKS   DS    H                      NUMBER OF LAST TRACK ON CYL
SKIPMSG  WTO   'WARNING: TRACK XXXXXX ON CYLINDER XXXXXX SKIPPED BECAUSX
               E OF I/O ERROR READING VTOC',                           X
               ROUTCDE=(11),DESC=(3,7),MF=L
TRKADDR  EQU   SKIPMSG+19,6           LOCATION FOR TRACK INSERT
CYLADDR  EQU   SKIPMSG+38,6           LOCATION FOR CYLINDER INSERT
PATTERN  DC    X'402020202120'        PATTERN FOR EDIT
DOUBLE   DS    D                      FOR CONVERT TO DECIMAL
RETCODE  DS    F                      RETURN CODE
         PUSH  PRINT
         PRINT NOGEN
SPILLDCB DCB   DDNAME=SPILL,RECFM=FS,MACRF=(WC),DSORG=PS  SPILL FILE
VTOCDCB  DCB   MACRF=(E),DSORG=PS,IOBAD=IOB,KEYLEN=44  VTOC TO COPY
         POP   PRINT
FORMAT4  DSECT
         DS    XL44                   KEY
         IECSDSL1 (4)                 FORMAT 4 DSCB EXPANSION
         END
