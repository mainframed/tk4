         MACRO
&LABEL   XWTORDM &L,&D,&TEXT
.**********************************************************************
.*       THIS MACRO IS CALLED AS AN INNER MACRO BY XWTO TO MODIFY     *
.*       ROUTE AND DESCRIPTOR CODE LISTS.                             *
.**********************************************************************
         LCLB  &B
         LCLA  &I,&J,&K,&N,&M(2)
         AIF   ('&L' EQ '').MEND
&I       SETA  1
.LOOP    AIF   (K'&L(&I) LE 2).OK
.ERR     MNOTE 4,'XWTO01 &L(&I) IS AN INVALID ROUTE OR DESCRIPTOR CODE,*
                IGNORED.'
         AGO   .NEXT
.OK      AIF   (K'&L(&I) EQ 2).L2
         AIF   ('&L(&I)' LE '0' OR '&L(&I)' GT '9').ERR
&B       SETB  ('&L(&I)' EQ '9')
         AGO   .GOOD
.L2      AIF   ('&L(&I)' LT '10' OR '&L(&I)' GT '16').ERR
&B       SETB  (1)
.GOOD    ANOP
&J       SETA  1
&K       SETA  &B+1
&N       SETA  8-&L(&I)+8*&B
         AIF   (&N LE 4).NO4
&J       SETA  &J*16
&N       SETA  &N-4
.NO4     AIF   (&N LE 2).NO2
&J       SETA  &J*4
&N       SETA  &N-2
.NO2     AIF   (&N LE 1).NO1
&J       SETA  &J*2
.NO1     ANOP
&M(&K)   SETA  &M(&K)+&J
.NEXT    ANOP
&I       SETA  &I+1
         AIF   (&I LE N'&L).LOOP
&LABEL   UHBMODF &D,ON=&M(1),COM=&TEXT..CODES,MF=M
         UHBMODF 1+&D,ON=&M(2),COM=&TEXT..CODES,MF=M
.MEND    MEND
