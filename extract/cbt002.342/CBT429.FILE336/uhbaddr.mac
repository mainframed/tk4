         MACRO
&LABEL   UHBADDR &REG,&ADDR,&LEN=4,&COM=,&MF=G
.*       THIS MACRO IS CALLED TO LOAD A VALUE (FULLWORD OR HALFWORD)
.*       INTO A REGISTER.
         LCLC  &S,&T,&R
&S       SETC  ' .      '         COMMENT SEPARATOR
         AIF   ('&ADDR'(1,1) EQ '(').RFORM
         AIF   (K'&ADDR LT 3).NORML
         AIF   ('&ADDR'(2,1) NE ':').NORML
         UHBCOLON &ADDR,&LEN,S,MF=&MF
&T       SETC  '&ADDR'(1,1)
         AIF   ('&T' EQ 'S').SFORM
         AIF   ('&T' EQ '*').STFORM
         AIF   ('&T' NE 'A').MEND
&LABEL   UHBILDUP L,'&REG,=A(',')',&COM
         MEXIT
.SFORM   ANOP
&LABEL   UHBILDUP LA,'&REG,','',&COM
         MEXIT
.STFORM  AIF   ('&LEN' EQ '2').STFORM2
&LABEL   UHBILDUP L,'&REG,','',&COM
         MEXIT
.STFORM2 ANOP
&LABEL   UHBILDUP LH,'&REG,','',&COM
         MEXIT
.NORML   ANOP
&LABEL   LA    &REG,&ADDR&S&COM
         MEXIT
.RFORM   ANOP
&R       SETC  '&ADDR'(2,K'&ADDR-2)
         AIF   ('&R' EQ '&REG').GLABEL
&LABEL   LR    &REG,&R&S&COM
         MEXIT
.GLABEL  AIF   ('&LABEL' EQ '').MEND
&LABEL   DS    0H
.MEND    MEND
