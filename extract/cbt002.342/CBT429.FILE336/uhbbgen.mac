         MACRO
&L       UHBBGEN &ALIGN=4,&COM=PARM.LIST,&MF=
.*       THIS MACRO GENERATES THE NECESSARY PRELUDE BEFORE THE
.*       GENERATION OF AN INLINE CONTROL BLOCK.
         GBLA  &UHBSF
         GBLB  &UHBG,&UHBM
         GBLB  &UHBIM,&UHBG1
         GBLB  &UHBSM(5)
         GBLC  &UHBGL,&UHBOGL,&UHBML
         GBLC  &UHBSL(5)
         LCLA  &MM
         LCLA  &I
         LCLC  &S
&S       SETC  ' .      '
&UHBIM   SETB  (0)
&UHBM    SETB  (0)
&UHBSF   SETA  0
&I       SETA  1
.ILOOP   ANOP
&UHBSL(&I) SETC ''
&UHBSM(&I) SETB 0
&I       SETA  &I+1
         AIF   (&I LE 5).ILOOP
         AIF   ('&MF(1)' EQ 'I').MFI
         AIF   ('&MF(1)' EQ 'L').MFL
         AIF   ('&MF(1)' EQ 'G').MFG
         AIF   ('&MF(1)' EQ 'M' OR '&MF(1)' EQ 'E').MFM
         MNOTE 12,'UHB003 INVALID MF=&MF SPECIFIED'
&UHBIM   SETB  (1)
         MEXIT
.MFI     AIF   ('&ALIGN' LT '4').NOIAL
         AIF   ('&ALIGN' NE '4').IAL8
         CNOP  0,4                ALIGN TO FULLWORD
         AGO   .NOIAL
.IAL8    CNOP  4,8                FORCE DOUBLEWORD ALIGNMENT
.NOIAL   ANOP
&UHBML   SETC  'UHB&SYSNDX.M'
&L       BAL   1,&UHBML&S.BRANCH.AROUND.&COM
&MM      SETA  2
&UHBGL   SETC  '&MF(2)'
         AGO   .JMFG
.MFL     AIF   ('&MF(2)' NE '').MFG
         AIF   ('&ALIGN' EQ '1').NOLAL
         AIF   ('&ALIGN' NE '2').LAL4
         DS    0H                 ALIGN TO HALFWORD
         AGO   .NOLAL
.LAL4    AIF   ('&ALIGN' NE '4').LAL8
         DS    0F                 ALIGN TO FULLWORD
         AGO   .NOLAL
.LAL8    DS    0D                 ALIGN TO DOUBLEWORD
.NOLAL   AIF   ('&L' NE '').LOK
         MNOTE 4,'UHB004 LABEL REQUIRED WHEN MF=L'
.LOK     ANOP
&UHBGL   SETC  '&L'
&MM      SETA  3
         AGO   .JMFG
.MFG     ANOP
&UHBML   SETC  '&L'
         AIF   ('&L' NE '').GLAB
&UHBML   SETC  'UHB&SYSNDX.M'
.GLAB    B     &UHBML&S.BRANCH.AROUND.&COM
         AIF   ('&ALIGN' LT '4').NOGAL
         AIF   ('&ALIGN' NE '4').GAL8
         CNOP  0,4                ALIGN TO FULLWORD
         AGO   .NOGAL
.GAL8    CNOP  0,8                ALIGN TO DOUBLEWORD
.NOGAL   ANOP
&UHBGL   SETC  'UHB&SYSNDX.G'
&MM      SETA  3
.JMFG    ANOP
&UHBOGL  SETC  '&UHBGL'
&UHBG    SETB  (1)
&UHBG1   SETB  (1)
         AGO   .JMF
.MFM     ANOP
&UHBG    SETB  (0)
&UHBM    SETB  (1)
&UHBML   SETC  '&L'
&MM      SETA  2
.JMF     AIF   (N'&MF LE &MM).MEND
         MNOTE 4,'UHB005 EXCESS MF PARAMETERS IGNORED'
.MEND    MEND
