         MACRO
&LABEL   UHBSTORE &TO,&FROM,&LEN,&REG=14,&COM=OPERAND,&MF=
.*       THIS MACRO IS CALLED WITH A SOURCE ADDRESS, A DESTINATION
.*       ADDRESS, A LENGTH AND A MACRO FORM TO GENERATE CODE TO STORE
.*       A VALUE INTO A PARAMETER LIST.
         LCLC  &R,&L,&T,&P,&S,&XCOM
&S       SETC  ' .      '
         AIF   ('&FROM' EQ '' OR '&FROM' EQ '*').EXIT
         AIF   (&LEN LE 2).NADDR
&XCOM    SETC  '.ADDRESS'
.NADDR   ANOP
         AIF   ('&FROM'(1,1) EQ '(').RFORM
         AIF   (K'&FROM LT 3).NORML
         AIF   ('&FROM'(2,1) NE ':').NORML
         UHBCOLON &FROM,&LEN,S,MF=&MF
&T       SETC  '&FROM'(1,1)
         AIF   ('&T' EQ 'S').SFORM
         AIF   ('&T' EQ '*').STFORM
         AIF   ('&T' NE 'A' OR ('&MF(1)' NE 'E' AND '&MF(1)' NE 'M')).M*
               END
&LABEL   UHBILDUP L,'&REG,=A(',')',LOAD.&COM&XCOM
         AGO   .NRFORM
.SFORM   ANOP
&LABEL   UHBILDUP LA,'&REG,','',LOAD.&COM&XCOM
         AGO   .NRFORM
.NORML   AIF   ('&MF(1)' NE 'M' AND '&MF(1)' NE 'E').EXIT
         AIF   ('&LEN' EQ '1').MVI
&LABEL   LA    &REG,&FROM&S.LOAD.&COM&XCOM
.NRFORM  ANOP
&L       SETC  ''
&R       SETC  '&REG'
         AGO   .STORE
.RFORM   ANOP
&R       SETC  '&FROM(1)'
&L       SETC  '&LABEL'
.STORE   AIF   ('&LEN' EQ '1').STC
         AIF   ('&LEN' EQ '2').STH
         AIF   ('&LEN' EQ '3').STCM
&L       ST    &R,&TO&S.STORE.&COM&XCOM
         MEXIT
.STCM    ANOP
&L       STCM  &R,B'0111',&TO&S.STORE.&COM&XCOM
         MEXIT
.STH     ANOP
&L       STH   &R,&TO&S.STORE.&COM
         MEXIT
.STC     ANOP
&L       STC   &R,&TO&S.STORE.&COM
         MEXIT
.MVI     ANOP
&LABEL   MVI   &TO,&FROM&S.INSERT.&COM
         MEXIT
.STFORM  ANOP
         AIF   (&LEN NE 3).OKLEN
&P       SETC  '1+'
.OKLEN   ANOP
&LABEL   UHBMOVE &TO,&P,&LEN,COPY.&COM&XCOM
         MEXIT
.EXIT    AIF   ('&LABEL' EQ '').MEND
&LABEL   DS    0H
.MEND    MEND
