XSPT     TITLE 'XSENDPUT -- TPUT ROUTINE FOR XSEND COMMAND'
***********************************************************************
*        THIS ROUTINE IS CALLED BY THE XSEND COMMAND TO SEND A MESSAGE*
*        TO ANOTHER USER. IT MAY BE CALLED AS A SUBROUTINE OR A       *
*        SUBTASK. ON ENTRY, R1 ADDRESSES THE TPUT R0 AND R1 VALUES,   *
*        R2 POINTS TO A HALFWORD TJID, AND R3 ADDRESSES A BYTE WHEREIN*
*        THE TPUT RETURN CODE IS TO BE STORED. THIS CODE IS ALSO      *
*        RETURNED IN R15. THE HIGH ORDER BYTE OF THE R0 VALUE         *
*        CONTAINS A FLAG BYTE WHICH INDICATES WHETHER THE BELL OR     *
*        FLASH OPERAND OF XSEND WAS SPECIFIED. IF NOT, TPUT IS USED;  *
*        OTHERWISE, THE XTPUT SVC (WHICH SENDS BELLS AT HIGH PRIORITY *
*        FOR FLASH) IS USED.                                          *
***********************************************************************
         SPACE
XSENDPUT SAVEX LV=72,PARMR=R4
         LM    R0,R1,0(R4)        LOAD TPUT REGS
         ICM   R0,12,0(R2)        GET ASID INTO R0
         CLI   0(R4),0            CHECK BELL FLAG
         BE    TPUT               IF NO BELL OPERAND, USE TPUT SVC
         SVC   238                ELSE USE XTPUT SVC
         B     TPUTDONE
TPUT     TPUT  (1),(0),R          SEND WITH TPUT IF NO BELLS
TPUTDONE STC   R15,0(,R3)         STORE TPUT RETURN CODE
         RETURNX RC=(15)          AND RETURN TO CALLER
         END
