         MACRO
&LABEL   UHBGEN &VAL,&LEN,&COM=,&RQR=,&NAME=,&SUB=,&SUBID=,&DEF=,&MF=
.*       THIS MACRO IS CALLED WITH A VALUE, A LENGTH, AND A MACRO FORM
.*       TO GENERATE A VALUE, OR SPACE FOR A VALUE, IN A PARAMETER LIST
         GBLA  &UHBSF
         GBLB  &UHBG1,&UHBIM,&UHBM
         GBLB  &UHBSM(5)
         GBLC  &UHBGL
         GBLC  &UHBSL(5)
         LCLC  &L
         LCLC  &S,&POST
&S       SETC  ' .      '
         AIF   (&UHBIM OR '&MF(1)' EQ 'E' OR '&MF(1)' EQ 'M').MEND
         AIF   (&LEN LE 2).NOPOST
&POST    SETC  '.ADDRESS'
.NOPOST  AIF   ('&RQR' NE 'Y').NORQR
         UHBRQR &VAL,&NAME,MF=&MF
.NORQR   ANOP
&L       SETC  '&LABEL'
         AIF   (NOT &UHBG1).L0
&UHBG1   SETB  (0)
         AIF   ('&L' NE '').L2
&L       SETC  '&UHBGL'
         AGO   .L0
.L2      ANOP
&UHBGL   EQU   *
.L0      AIF   (&UHBSF EQ 0).NOSUBF
         AIF   ('&UHBSL(&UHBSF)' EQ '').MEND
.NOSUBF  AIF   ('&VAL' EQ '').NOVAL
         AIF   ('&VAL' EQ '*').STARVAL
         AIF   ('&VAL'(1,1) EQ '(').RFORM
         AIF   (K'&VAL LT 3).NFORM
         AIF   ('&VAL'(2,1) EQ ':').CFORM
.NFORM   ANOP
&L       DC    AL&LEN.(&VAL)&S&COM&POST
         MEXIT
.RFORM   AIF   ('&MF(1)' NE 'L' OR '&MF(2)' NE '').MFORM
         MNOTE 8,'UHB001 REGISTER NOTATION ILLEGAL WITH MF=L'
         AGO   .MFORM
.CFORM   UHBCOLON &VAL,&LEN,G,MF=&MF
         AIF   ('&VAL'(1,1) NE 'A').MFORM
&L       UHBILDUP DC,'AL&LEN.(',')',&COM&POST
         MEXIT
.MFORM   AIF   (&UHBSF EQ 0).NOMSUB
&UHBSM(&UHBSF) SETB (1)
.NOMSUB  ANOP
&UHBM    SETB  (1)
&L       DC    AL&LEN.(*-*)&S.DUMMY.&COM&POST
         MEXIT
.STARVAL AIF   ('&SUB' EQ '').NOVAL
&UHBSL(&SUB) SETC '&SUBID'
         AIF   ('&SUBID' NE '').SUBIDOK
&UHBSL(&SUB) SETC 'UHB&SYSNDX.S'
.SUBIDOK AIF   ('&MF(1)' NE 'I' AND ('&MF(1)' NE 'L' OR '&MF(2)' NE '')*
               ).MFORM
&L       DC    AL&LEN.(&UHBSL(&SUB))&S&COM&POST
         MEXIT
.NOVAL   AIF   ('&DEF' NE '').DEF
&L       DC    AL&LEN.(0)
         MEXIT
.DEF     ANOP
&L       DC    AL&LEN.(&DEF)&S.DEFAULT.&COM&POST
.MEND    MEND
