         TITLE 'TAPESCUR -- TAPESCAN SECURITY EXIT FOR ACF2 (VOLUME PRO*
               TECTION ONLY)'
TAPESCUR CSECT
         SPACE
         REGISTER
         SPACE
         SAVE  (14,12),,*
         LR    R12,R15            COPY BASE REG
         USING TAPESCUR,R12
         LR    R11,R13            COPY SAVE AREA POINTER
         CNOP  0,4                GET FULLWORD ALIGNMENT
         BAL   R13,PASTSA         SET UP NEW SAVE AREA
SAVEAREA DC    18A(0)
PASTSA   ST    R11,4(,R13)        CHAIN SAVE AREAS
         ST    R13,8(,R11)
         LR    R11,R1             COPY PARM POINTER
         USING SCURPARM,R11
         XC    ACDSV(ACFSPRML),ACDSV   CLEAR ACF2 PARM LIST
         MVI   ACFSPREQ,ACFSPRVL  VALIDATE BY VOLSER
         MVI   ACFSPID1,ACFSPIUR  THIS IS A "USER CALL"
         MVI   ACFSPAC1,ACFSPAIN  ASSUME INPUT VALIDATION
         MVC   DDNAME,=CL8'INPUT'
         CLI   SECIOTYP,C'I'      IS IT?
         BE    INCHECK            YES.
         MVI   ACFSPAC1,ACFSPAIO  NO, VALIDATE FOR INOUT
         MVC   DDNAME,=CL8'OUTPUT'
INCHECK  MVC   ACFSPDDN,=A(DDNAME)     SET UP DDNAME PTR
         MVC   DSNAME(17),SECDSN  COPY DSNAME TO PARM LIST
         MVC   ACFSPDSN,=A(DSNAME)     EVEN THO WE WILL NOT USE IT
         LA    R2,SECVOL          STORE VOL PTR FOR ACF2
         ST    R2,ACFSPVOL
         L     R2,SECDCB          FIND THE DCB ADDRESS
         LH    R2,40(,R2)         FIND THE TIOT OFFSET
         L     R5,16              FIND OUR TCB
         L     R5,0(,R5)
         L     R5,4(,R5)
         L     R5,12(,R5)         FIND THE TIOT
         AR    R2,R5              FIND THE TIOT ENTRY
         MVC   ACFSPUCB,16(R2)    COPY OVER UCB ADDR FOR ACF2
         ACFSVC ACDSV,TYPE=S,NONE=ALLOW,CVT=FIND ASK ACF2
         LTR   R15,R15            IS IT OK?
         BZ    ALLOW              YES.
         LA    R15,4              SET NO RETURN CODE
         B     RETURN
ALLOW    DS    0H                 ACF2 SAYS OK OR IS NOT RUNNING
RETURN   L     R13,4(,R13)        RESTORE PREV SAVE AREA
         RETURN (14,12),RC=(15)   AND RETURN
         SPACE
         LTORG
         SPACE
DSNAME   DC    CL44' '            DSNAME BUFFER
DDNAME   DC    CL8' '             DDNAME BUFFER
         ACDSV DSECT=NO
         SPACE
SCURPARM DSECT ,                  TAPESCAN PARM LIST FORMAT
SECDCB   DS    A                  ADDR OF TAPE DCB
SECIOTYP DS    CL1                I=INPUT, O=OUTPUT
SECLABEL DS    CL1                A=AL,N=NL,S=SL
SECDSN   DS    CL17               DSN OF FIRST FILE ON TAPE
SECVOL   DS    CL6                TAPE VOLSER (FROM LABEL OR JCL)
         SPACE
         PRINT NOGEN
         ACCVT DSECT=YES
         END
