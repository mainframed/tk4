IDCAMSPF TITLE 'CALL IDCAMS FOR SPLIT SCREEN ISPF'
***********************************************************************
*    AUTHOR:       RANDALL SMITH          3/09/89                     *
*    ATTRIBUTES:   NONE                                               *
*    ALIAS:        NONE                                               *
*    SYSTEM:       MVS/SP1.3                                          *
***********************************************************************
*                                                                     *
*             MODULE NAME = IDCAMSPF                                  *
*                                                                     *
*             THIS MODULE CALLS IDCAMS FOR ISPF IN SPLIT SCREEN       *
*              MODE.  DD'S WILL BE TMPIN1 AND TMPPRIN1 OR TMPIN2      *
*              AND TMPPRIN2 DEPENDING ON THE SPLIT SCREEN BEING       *
*              USED (SEE &ZSCREEN VARIABLE IN ISPF).                  *
*                                                                     *
*              CALLER OF THIS PROGRAM (E.G. ISPF) MUST SUPPLY A 1 OR  *
*               2 IN THE EXECUTE PARM STATEMENT.                      *
*                                                                     *
*              MUST BE IN SAME LIBRARY AS IDCAMS FOR TSO PERFORMANCE  *
*               REASONS.                                              *
*                                                                     *
***********************************************************************
*        REGISTER USAGE FOLLOWS...                                    *
R0       EQU   0                   WORK REG                           *
R1       EQU   1                   PTR TO INPUT PARM                  *
R2       EQU   2                   WORK REG                           *
R4       EQU   4                   WORK REG                           *
R11      EQU   11                  PTR TO FIRST MESSAGE IF NOZERO PARM*
R12      EQU   12                  BASE REG                           *
R13      EQU   13                  PTR TO IPOSAVE                     *
R14      EQU   14                  LINKAGE REG                        *
R15      EQU   15                  LINKAGE REG                        *
ML       EQU   1                   MSG LENGTH                         *
***********************************************************************
IDCAMSPF CSECT                     BEGINING OF PROGRAM
         USING *,R12               SET UP BASE REG
         SAVE  (14,12)             SAVE REGESTERS
         LR    R12,R15             SET UP ADDRESSABILITY
         LR    R2,R13              SAVE ADDR OF PREVIOUS SAVEAREA
         LA    R13,IPOSAVE         POINT TO CURRENT SAVEAREA
         ST    R13,8(R2)           LINK PREVIOUS SAVEAREA TO IPOSAVE
         ST    R2,4(R13)           LINK IPOSAVE TO PREVIOUS SAVEAREA
         LR    R11,R1              PTR PARM ADDR - FIRST COMMAND
         L     R11,0(R11)          SAVE ADDR OF PARM IN R11
         B     IPOPARM             BRANCH OVER VIEWABLE MESSAGE
         DC    C'MUST BE IN SAME PDS AS IDCAMS FOR PERFORMANCE'
***                                ADD JOB'S NAME TO MESSAGE.
IPOPARM  LH    R2,0(R11)           LENGTH TO R2
RANDALL  LA    R2,ML               RESET PARM LENGTH TO MAX
IPOMOVE  MVC   IPOMSG,2(R11)       MOVE PARM
         MVC   SYSINX+5(1),IPOMSG  MOVE PARM TO DDNAME
         MVC   SYSPRINX+7(1),IPOMSG MOVE PARM TO DDNAME
         LOAD  EP=IDCAMS           LOAD IDCAMS
         LR    15,0                CAPTURE IDCAMS ADDRESS
         CALL  (15),(OPTIONS,DDNAMES),VL  GIVE IDCAMS A CALL
IPOEND   L     R13,IPOSAVE+4       RESTORE R13
         RETURN (14,12),T,RC=0     RETURN TO MVS
IPOSAVE  DS    18F                 IPOSAVE AREA
IPOAREA  DC    0F'0',AL2(ML+4,0)   LENGTH FOR OUTPUT IF RECFM=VB
IPOMSG   DC    CL(ML)' '           STORAGE FOR ONE PARM
OPTIONS  DC    H'0'                NO GNU OPTIONS
DDNAMES  DS    0F                  CHANGE DDAMES TO ISPF VERSIONS
         DC    H'48'               LENGTH IS 6 - 8 BYTE ENTRIES
         DC    XL8'00'             JUST ZEROS
         DC    XL8'00'             JUST ZEROS
         DC    XL8'00'             JUST ZEROS
         DC    XL8'00'             JUST ZEROS
SYSINX   DC    CL8'TMPINX'         SYSIN DD NAME
SYSPRINX DC    CL8'TMPPRINX'       SYSPRINT DD NAME
         LTORG                   , LTORG NEEDED FOR ADDRESSABILITY
         END                     , END OF PROGRAM
