//PTOPSAMP JOB  360T1130,SAMPLE,MSGLEVEL=1
//PTOPSTEP  EXEC  FORTGCLG,PARM.FORT=(NODECK,MAP,LOAD)
//FORT.SYSIN DD *
      EXTERNAL ASYN
      EXTERNAL ERRN
C ASYN=ASYNCHRONOUS ROUTINE
      DIMENSION TEMPRY(5),USARY(5),IPASS(2)
C THE PASSWORD IS READ FROM A CARD RECORD INTO IPASS.
 5    FORMAT(2A4)
      READ (5,5) IPASS
C GTNIT INITIALIZES COMMUNICATION WITH THE 1130 PROGRAM.
 10   CALL  GTNIT(9,20,20,ASYN,IPASS,IERR)
      IF(IERR)100,20,110
C GTRED IS CALLED TO RECEIVE THE DATA BEING TRANSMITTED
C FROM THE 1130 PROGRAM.
 20   CALL GTRED(9,TEMPRY,5,4)
C GTCLT IS CALLED TO TEST THE READ OPERATION
 30   CALL GTCLT(9,3,ITEST)
      GO TO (60,40,40,40,90,40,40,50),ITEST
C A BRANCH IS MADE DEPENDING UPON THE RETURN CODE PLACED IN ITEST. THE
C ERROR ROUTINE IS PASSED THE RETURN CODE INDICATING THE PROBLEM.
 40   CALL ERRN(ITEST)
C NO RETURN IS MADE FROM THE ERROR ROUTINE.
 50   PAUSE 'OPERATOR ASSISTANCE REQUIRED FOR TELEPROCESSING UNIT.'
      GO TO 20
C GDCTF CONVERTS THE DATA FROM 1130 FORMAT TO SYSTEM/360 FORMAT.
 60   CALL GDCTF(TEMPRY,USARY,5,1)
C THE DATA VARIABLES ARE DEFINED AND ASSIGNED VALUES FROM THE ARRAY.
      WB = USARY(1)
      CA = USARY(2)
      W  = USARY(3)
      C  = USARY(4)
      EL = USARY(5)
C WB=WHEELBASE. SHOULD BE WITHIN RANGE 110-200. MUST NOT BE 999.
C A VALUE OF 999 FOR WB INDICATES TO 1130 SAMPLE PROGRAM
C THAT IT HAS READ THE LAST DATA RECORD.
C CA=DISTANCE FROM BACK OF CAB TO CENTER OF REAR AXLE. SHOULD BE
C WITHIN THE RANGE 38-130.
C W=GROSS WEIGHT.  SHOULD BE WITHIN RANGE 5000-23,000.
C C=CLEARANCE BETWEEN BODY AND CAB. SHOULD BE 3.
C EL=BODY LENGTH. SHOULD BE WITHIN RANGE 90-188.
C THE WEIGHT DISTRIBUTION ON EACH AXLE IS COMPUTED.
C A=HORIZONTAL DISTANCE BETWEEN CENTER OF BODY AND CENTER OF REAR AXLE.
C FW=WEIGHT ON FRONT AXLE.
C RW=WEIGHT ON REAR AXLE.
      A = CA-C-EL/2.0
      FW = W*A/WB
      RW = W-FW
C RESULTS ARE PLACED IN THE USER'S ARRAY.
      USARY(4) = FW
      USARY(5) = RW
C GDCFF CONVERTS THE DATA TO THE 1130 FORMAT.
      CALL GDCFF(USARY(4),TEMPRY,2,1)
C GTWRT INITIATES DATA TRANSMISSION BACK TO THE 1130.
 70   CALL GTWRT(9,TEMPRY,2,4,1)
 80   CALL GTCLT(9,2,ITEST)
      GO TO (20,80,80,40,90,40,40,50),ITEST
C GTEND IS CALLED TO END COMMUNICATION WITH 1130 PROGRAM.
 90   CALL GTEND(9)
      GO TO 120
 100  WRITE (6,1)
 1    FORMAT (43H YOUR PASSWORD DOES NOT MATCH 1130 PASSWORD)
      GO TO 120
 110  WRITE (6,2)
 2    FORMAT (50H TRANSMISSION-LINE ERRORS PREVENTED INITIALIZATION)
 120  STOP
      END
      SUBROUTINE ERRN(ITEST)
C THE FOLLOWING IS A ROUTINE THAT GAINS CONTROL
C WHEN GTCLT DETERMINES THAT A READ OR WRITE
C OPERATION WAS UNSUCCESSFUL.
      IF (ITEST-7)100,200,200
 100  WRITE (6,111)
 111  FORMAT (48H TRANSMISSION-LINE ERRORS PREVENTED TRANSMISSION)
      GO TO 300
 200  WRITE (6,222)
 222  FORMAT (43H ELEMENT COUNTS OR DATA TYPES ARE INCORRECT)
 300  CALL GTEND(9)
      STOP
      END
      SUBROUTINE ASYN(IDSRF,ISTRT)
C THE FOLLOWING IS A DUMMY ASYNCHRONOUS ROUTINE
C FOR THE SAMPLE PROGRAM. IT IS NOT INVOKED BUT
C MUST BE PRESENT TO RESOLVE THE "ASYNROUTINE"
C ARGUMENT IN GTNIT.
      CALL GTEND(9)
      RETURN
      END
/*
//LKED.SYSLMOD DD SPACE=(1024,(50,20,1),RLSE)
/*
//GO.SYSABEND DD SYSOUT=A
//GO.FT09F001  DD  UNIT=012
//GO.SYSIN DD *
AXLEP1
/*
