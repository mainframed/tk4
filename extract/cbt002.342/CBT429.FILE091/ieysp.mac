//FORTRANG JOB   MSGLEVEL=1
//FTNGTEST EXEC  FORTGCLG,PARM.FORT='EBCDIC,LIST,MAP'
//FORT.SYSIN  DD  *
C   ********************************************************************
C   *                                                                  *
C   * THIS PROGRAM IS A TEST CASE DESIGNED TO VERIFY THAT THE FORTRAN  *
C   * IV (G) COMPILER HAS BEEN PROPERLY INSTALLED IN YOUR SYSTEM. THE  *
C   * REQUIRED JOB CONTROL LANGUAGE STATEMENTS AND THE DATA CARD       *
C   * ARE INCLUDED AS PART OF THE DECK.                                *
C   *                                                                  *
C   * THE PROGRAM GENERATES A TABLE OF BINOMIAL COEFFICIENTS WHICH IS  *
C   * THEN PRINTED ON THE SYSOUT DEVICE.  ALL DATA EXCEPT THE FIRST    *
C   * LINE OF OUTPUT IS PROGRAM GENERATED.                             *
C   *                                                                  *
C *                                                                    *
C *   THE OUTPUT SHOULD BE ---                                         *
C *                                                                    *
C *      I---------------------------------------------------------I   *
C *      I                           K                             I   *
C * I----I---------------------------------------------------------I   *
C * I  N I 1    2    3    4     5     6     7      8      9     10 I   *
C * I----I---------------------------------------------------------I   *
C * I  1 I  1    0    0    0     0     0     0      0      0     0 I   *
C * I  2 I  2    1    0    0     0     0     0      0      0     0 I   *
C * I  3 I  3    3    1    0     0     0     0      0      0     0 I   *
C * I  4 I  4    6    4    1     0     0     0      0      0     0 I   *
C * I  5 I  5   10   10    5     1     0     0      0      0     0 I   *
C * I  6 I  6   15   20   15     6     1     0      0      0     0 I   *
C * I  7 I  7   21   35   35    21     7     1      0      0     0 I   *
C * I  8 I  8   28   56   70    56    28     8      1      0     0 I   *
C * I  9 I  9   36   84  126   126    84    36      9      1     0 I   *
C * I 10 I 10   45  120  210   252   210   120     45     10     1 I   *
C * I 11 I 11   55  165  330   462   462   330    165     55    11 I   *
C * I 12 I 12   66  220  495   792   924   792    495    220    66 I   *
C * I 13 I 13   78  286  715  1287  1716  1716   1287    715   286 I   *
C * I 14 I 14   91  364 1001  2002  3003  3432   3003   2002  1001 I   *
C * I 15 I 15  105  455 1365  3003  5005  6435   6435   5005  3003 I   *
C * I 16 I 16 120  560 1820  4368  8008 11440  12870  11440   8008 I   *
C * I 17 I 17 136  680 2380  6188 12376 19448  24310  24310  19448 I   *
C * I 18 I 18 153  816 3060  8568 18564 31824  43758  48620  43758 I   *
C * I 19 I 19 171  969 3876 11624 27132 50388  75582  92378  92378 I   *
C * I 20 I 20 190 1140 4845 15504 38760 77520 125970 167960 184756 I   *
C * I----I---------------------------------------------------------I   *
C *                                                                    *
C   ********************************************************************
      DIMENSION NBYK(20,10),HEAD(20)
      DO 10 K=1,10
      NBYK(1,K) = K
   10 CONTINUE
      READ (5,1) HEAD
      WRITE (6,2) HEAD
      WRITE (6,3) (NBYK(1,K),K=1,10)
      DO 30 N=1,20
      DO 20 K=1,10
      NBYK(N,K) = KBINCO(N,K)
   20 CONTINUE
      WRITE (6,4) N,(NBYK(N,K),K=1,10)
   30 CONTINUE
      WRITE (6,5)
      STOP
    1 FORMAT (20A4)
    2 FORMAT ('1   FORTRAN G SAMPLE TEST CASE'///////1X,20A4/////)
    3 FORMAT (T7,'I',57('-'),'I'/T7,'I',T35,'K',T65,'I'/T2,'I----I',
     X        57('-'),'I'/T2,'I  N I',I2,I4,2I5,3I6,3I7,'  I'/T2,
     X        'I----I',57('-'),'I')
    4 FORMAT (' I',I3,' I',I3,I4,2I5,3I6,3I7,' I')
    5 FORMAT (' I----I',57('-'),'I')
      END
C
C   ************************************************
C   * KBINCO COMPUTES THE BINOMIAL COEFFICIENT,    *
C   * C(N,K) = (N*(N-1)***(N-K+1))/(K*(K-1)***1),  *
C   * WHERE N AND K ARE THE INTEGER ARGUEMENTS TO  *
C   * THE FUNCTION.  INTERMEDIATE CALCULATIONS ARE *
C   * PERFORMED IN REAL ARITHMETIC.  IN THE CASE   *
C   * WHERE K .GT. N, A VALUE OF ZERO IS RETURNED. *
C   * THE VALUES OF N AND K ARE LEFT UNCHANGED.    *
C   * THE FUNCTION HAS BEEN CHECKED FOR ALL COM-   *
C   * BINATIONS OF N=1,2,...,20 AND K=1,2,...,10.  *
C   ************************************************
C
      FUNCTION KBINCO (N,K)
CHECK FOR TRIVIAL CASES
      IF (K .GT. N) GO TO 50
      IF (K .EQ. 0  .OR.  N .EQ. K) GO TO 60
      IF (K .EQ. 1  .OR.  N-K .EQ. 1) GO TO 70
CONVERT TO REAL FOR INT. CALCULATIONS
      P = N
      Q = K
CHECK FOR LOWER 'DENOMINATOR'
      IF (P-Q .LT. Q) Q = P-Q
COMPUTE DENOMINATOR
      MAX = Q
      BOT = 1.0
      DO 30 I=2,MAX
      BOT = I*BOT
   30 CONTINUE
COMPUTE NUMERATOR
      MAX = P
      MIN = P - Q + 1.0
      TOP = 1.0
      DO 40 I=MIN,MAX
      TOP = I*TOP
   40 CONTINUE
CALCULATE AND ROUND BIN. COEFF.
      KBINCO = TOP/BOT + 0.5
      RETURN
   50 KBINCO = 0
      RETURN
   60 KBINCO = 1
      RETURN
   70 KBINCO = N
      RETURN
      END
/*
//GO.SYSIN DD *
          BINOMIAL COEFFICIENTS
/*
