*---------------------------------------------------------------------*
         TITLE 'EXTRACT TSO TERMINAL IDENTIFIER'
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION:                                                    *
*          EXTRACT TSO TERMINAL ID FROM ACEE AND PUT IN               *
*          CLIST VARIABLE &TERMID.                                    *
*                                                                     *
*        CLIST INVOCATION:                                            *
*          ISPEXEC VGET (TERMID) SHARED                               *
*          IF &TERMID = &STR() THEN -                                 *
*            DO                                                       *
*              CALL 'M204.CRAMLIB(TERMID)'                            *
*              ISPEXEC VPUT (TERMID) SHARED                           *
*            END                                                      *
*                                                                     *
*        USAGE:                                                       *
*          EDIT MODEL 204 USER LANGUAGE PROCEDURES UNDER ISPF.        *
*          %M204EDIT CLIST MUST FORM A UNIQUE //OUT.... DDNAME FOR    *
*          THE "USE OUT...." COMMAND TO AVOID ENQUEUE CONFLICTS WITH  *
*          OTHER %M204EDIT USERS.  THE LAST 4 POSITIONS OF ACEETRID   *
*          ARE USED IN %M204EDIT TO FORM THE UNIQUE NAME.             *
*                                                                     *
*        ATTRIBUTES:                                                  *
*          NON-AUTHORIZED                                             *
*                                                                     *
*        OPERATING SYSTEM:                                            *
*          MVS/ESA 5.2.2                                              *
*          TSO/E  2.04.0                                              *
*                                                                     *
*        MODIFICATIONS:                                               *
*                                                                     *
*         13 MAR 1997 - GET TERMINAL ID FROM ACEE INTEAD OF TSB       *
*                                                                     *
*          7 NOV 1989 - USE CLIST VARIABLE ACCESS SERVICE MODULE TO   *
*                       PASS BACK ACEE TERMINAL IDENTIFIER.           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
TERMID   CSECT
TERMID   AMODE 24
*        REGEQU                    SYMBOLIC REGISTERS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         USING TERMID,R12          ESTABLISH ENTRY PT AS BASE
         ST    R13,SAVEAREA+4      BACKWARD CHAIN
         LA    R5,SAVEAREA         R5  PTS @ SAVEAREA
         ST    R5,8(R13)           FORWARD  CHAIN
         LR    R13,R5              R13 PTS @ SAVEAREA
         ICM   R2,15,PSAAOLD       GET A(ASCB)
         USING ASCB,R2
         ICM   R3,15,ASCBASXB      GET A(ASXB)
         USING ASXB,R3
         ICM   R4,15,ASXBSENV      GET A(ACEE)
         USING ACEE,R4
         MVC   TRID(8),ACEETRID    TERMINAL ID
         LINK  EP=IKJCT441,        CLIST VARIABLE ACCESS SERVICE       *
               PARAM=(ECODE,       ENTRY CODE                          *
               NAMEPTR,            POINTER TO VARIABLE NAME            *
               NAMELEN,            LENGTH OF  VARIABLE NAME            *
               VALUEPTR,           POINTER TO VARIABLE VALUE           *
               VALUELEN,           LENGTH OF  VARIABLE VALUE           *
               TOKEN),             TOKEN TO VARIABLE ACCESS SERVICE    *
               VL=1                TURN HIGH BIT ON IN THE PARM LIST
EOJ      EQU   *
         L     R13,SAVEAREA+4      RESTORE CALLERS SA POINTER
         RETURN (14,12),RC=(15)    RESTORE CALLERS REGISTERS
         EJECT
         LTORG
         EJECT
PSAAOLD  EQU   548
NAME     DC    CL6'TERMID'         VARIABLE NAME
NAMELEN  DC    F'6'                LENGTH OF VARIABLE NAME
TRID     DC    CL8' '              TERMINAL IDENTIFIER FROM ACEE
VALUELEN DC    F'8'                LENGTH OF VARIABLE VALUE
NAMEPTR  DC    A(NAME)             POINTER TO VARIABLE NAME
VALUEPTR DC    A(TRID)             POINTER TO VARIABLE VALUE
TOKEN    DC    F'0'                TOKEN (UNUSED)
ECODE    DC    A(TSVEUPDT)         ENTRY CODE FOR SETTING VALUES
SAVEAREA DC    18F'0'
         EJECT
         IKJTSVT
         IHAASCB
         IHAASXB
         IHAACEE
         END
