 TITLE 'IHISAT, ATAN ROUTINE, SHORT PRECISION'
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION: 1. REDUCE THE CASE TO THE 1ST OCTANT BY USING    *
*                       ATAN(-X)=-ATAN(X), ATAN(1/X)=PI/2-ATAN(X)     *
*                    2. REDUCE FURTHER TO THE CASE /X/ LESS THAN      *
*                       TAN(PI/2) BY                                  *
*                       ATAN(X)=PI/6+ATAN((X*SQRT3-1)/(X+SQRT3)       *
*                    3. FOR THE BASIC RANGE (X LESS THAN TAN(PI/12)), *
*                       USE A FRACTIONAL APPROXIMATION.               *
*                                                                     *
*ENTRY POINT:                                                         *
*        IHISAT - ATAN FUNCTION, SHORT             LA  1,PARMLIST     *
*                                                  BALR 14,15         *
*                                                  DATA PASSED BY NAME*
*   THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE.           *
*                                                                     *
*INPUT: N/A                                                           *
*                                                                     *
*OUTPUT: N/A                                                          *
*                                                                     *
*EXTERNAL ROUTINES: N/A                                               *
*                                                                     *
*EXIT-NORMAL: RETURN BY MEANS OF   BR  14.  RESULT IN FPR0.           *
*                                                                     *
*EXIT-ERROR: N/A                                                      *
*                                                                     *
*TABLES/WORKAREAS: N/A                                                *
*                                                                     *
*ATTRIBUTES: THE MODULE IS SERIALLY REUSABLE.                         *
*                                                                     *
*NOTES: CHARACTER CODE DEPENDENCE                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL       *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
       SPACE
IHISATAN CSECT
       ENTRY   IHISAT                  ENTRY-POINT IDENTIFIER
       SPACE
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
FR6    EQU     6
         EJECT
       DC      CL7'IHISAT'             ENTRY POINT IDENTIFIER
       DC      X'06'
       USING   *,GRL
IHISAT STM     GRR,GRL,12(GRS)         SAVE REGISTERS
       L       GR1,0(GRA)
       LE      FR0,0(GR1)      OBTAIN ARGUMENT
       STE     FR0,SIGN          SAVE ARG FOR SIGN CONTROL
       LPER    FR0,FR0             AND SET SIGN POSITIVE
       SR      GR1,GR1         GR1 TO DENOTE THE SECTION TO WHICH
       CE      FR0,ONE           ANSWER BELONGS.   BREAK POINTS ARE
       BC      12,REDUC            TAN(PI/12), TAN(PI/4), TAN(5PI/12)
       LE      FR2,ONE         IF ARG GREATER THAN 1, TAKE INVERSE
       DER     FR2,FR0           AND CRANK GR1 BY 8
       LER     FR0,FR2
       LA      GR1,8
       SPACE
REDUC  CE      FR0,TAN15       IF ARG GREATER THAN TAN(PI/12), REDUCE
       BC      12,OK             THE ARG BY USING
       LER     FR2,FR0
       ME      FR0,RT3M1         ATAN(X) = PI/6+ATAN(Y)
       SE      FR0,ONE             WHERE Y = (X*SQRT3-1)/(X+SQRT3)
       AER     FR0,FR2
       AE      FR2,RT3           COMPUTE X*SQRT3-1 AS X(SQRT3-1)-1+X
       DER     FR0,FR2             TO PROTECT SIGNIFICANT DIGITS
       LA      GR1,4(GR1)        CRANK GR1 BY 4
       SPACE
OK     LER     FR4,FR0         NOW MAGNITUDE OF REDUCED ARG IS
       MER     FR0,FR0           LESS THAN TAN(PI/12)=0.26795
       LER     FR2,FR0
       ME      FR0,C           COMPUTE ANGLE BY
       LER     FR6,FR0
       AE      FR2,A             ATAN(X)/X = D-C*XSQ+B/(XSQ+A)
       LE      FR0,B
       DER     FR0,FR2
       SER     FR0,FR6
       AE      FR0,D
       MER     FR0,FR4
       SPACE
       C       GR1,EIGHT       DEPENDING ON THE SECTION TO WHICH
       BC      4,*+6             THE ANSWER BELONGS, ADD OR SUBTRACT
       LCER    FR0,FR0             REDUCED ANSWER FROM A BASE ANGLE
       AE      FR0,ZERO(GR1)
       TM      SIGN,X'80'      SIGN OF ANS SHOULD AGREE WITH SIGN
       BC      8,*+6             OF ARG
       LCER    FR0,FR0
       SPACE
       MVI     12(GRS),X'FF'   RETURN
       BCR     15,GRR
       SPACE
SIGN   DS      F
ONE    DC      X'41100000'
TAN15  DC      X'40449851'     TAN 15 DEGREES
RT3M1  DC      X'40BB67AF'     SQRT3-1
RT3    DC      X'411BB67B'     SQRT3
EIGHT  DC      F'8'
A      DC      X'41168A5E'     1.4087812
B      DC      X'408F239C'     0.55913709
C      DC      X'3FD35F49'     0.051604543
D      DC      X'409A6524'     0.60310579
ZERO   DC      F'0'
       DC      X'40860A92'     PI/6
       DC      X'411921FB'     PI/2
       DC      X'4110C152'     PI/3
       END
