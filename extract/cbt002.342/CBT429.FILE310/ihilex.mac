 TITLE 'IHILEX, EXPONENTIAL ROUTINE, LONG PRECISION'
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION: Y = X*LOG2(E) = 4A-B-C/16-D, WHERE A, B, AND C   *
*   ARE INTEGERS, B BETWEEN0 AND 3, C BETWEEN 0 AND 15. D IS A        *
*   FRACTION BETWEEN 0 AND 1/16. THEN                                 *
*   E**X = 2**Y = (16**A)(2**-B)N2**-C/16)(2**-D)                     *
*                                                                     *
*ENTRY POINT:                                                         *
*        IHILEX - EXP FUNCTION, LONG               LA  1,PARMLIST     *
*                                                  BALR 14,15         *
*                                                  DATA PASSED BY NAME*
*   THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE.           *
*                                                                     *
*INPUT: N/A                                                           *
*                                                                     *
*OUTPUT: N/A                                                          *
*                                                                     *
*EXTERNAL ROUTINES: N/A                                               *
*                                                                     *
*EXIT-NORMAL: RETURN BY MEANS OF   BR  14.  RESULT IN FPR0.           *
*                                                                     *
*EXIT-ERROR: IF ARGUMENT GREATER THAN 174673 GO TO ERROR ROUTINE VIA  *
*        B     FSAERR+24*4(13)                                        *
*                                                                     *
*TABLES/WORKAREAS: N/A                                                *
*                                                                     *
*ATTRIBUTES: THE MODULE IS SERIALLY REUSABLE.                         *
*                                                                     *
*NOTES: CHARACTER CODE DEPENDENCE                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL       *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
       SPACE
IHILEXPT CSECT
*C024600,024700                                                  A32993
       ENTRY   IHILEX
       SPACE
GR0    EQU     0               SCRATCH REGISTER
GR2    EQU     2
GR3    EQU     3
GRA    EQU     1               ARGUMENT POINTER
GR1    EQU     1
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTER
         EJECT
       DC      CL7'IHILEX'             ENTRY-POINT IDENTIFIER
       DC      X'06'
       USING   *,GRL
IHILEX STM     GRR,GR3,12(GRS)         SAVE REGISTERS
       L       GR1,0(GRA)      OBTAIN ARGUMENT
       LD      FR0,0(GR1)
       CE      FR0,MAX         MAX = 63*LOG16 = 174.67309
       BC      2,ERROR           IF ARG GREATER THAN THIS, ERROR
       CE      FR0,MIN         MIN = -65*LOG16 = -180.21867
       BC      12,SMALL          IF ARG LESS THAN THIS, GIVE ANS=0
       SPACE
       DD      FR0,LOGE2       Y = X*LOG2(E) BY ACCURATE DIVISION
       STE     FR0,SIGN        SAVE SIGN OF Y
       LER     FR2,FR0         DECOMPOSE Y = (-4A'-B'-C'/16)-D'
       AU      FR2,SCALER        BY FORCING CHARACTERISTIC OF X'45'
       STE     FR2,FIELDS      -4A'-B'-C'/16 IN FIELDS, UNNORMALIZED
       SDR     FR2,FR2
       AE      FR2,FIELDS      NORMALIZE THIS AND SUBTRACT IT
       SDR     FR0,FR2           FROM Y TO OBTAIN -D' IN FR0
       L       GR2,FIELDS
       SPACE
       TM      SIGN,X'80'      IF Y NEGATIVE, SKIP
       BC      1,READY         IF Y NON-NEGATIVE,
       SD      FR0,ONO16         -D = /D'/-1/16
       LA      GR2,1(GR2)        -4A-B-C/16 = -(-4A'-B'-(C'+1)/16)
       LCR     GR2,GR2         NOW IN ANY CASE, B, C, AND D ARE +
       SPACE
READY  SR      GR3,GR3
       SRDL    GR2,4           C IN HIGH GR3
       SRL     GR3,25
       SRDL    GR2,2           B IN HIGH GR3, C IN LOW GR3
       SLL     GR2,24
       LCR     GR0,GR2         A (IN SCALE B7) IN GR0, CHAR MODIFIER
       SR      GR2,GR2
       SLDL    GR2,2           B IN GR2, 8*C IN GR3
       SPACE
       LDR     FR2,FR0         COMPUTE 2**-D BY USE OF
       ME      FR0,C6            CHEBYSHEV INTERPOLATION
       AD      FR0,C5              POLYNOMIAL OF DEGREE 6
       MDR     FR0,FR2
       AD      FR0,C4
       MDR     FR0,FR2
         AD    FR0,C3
         MDR   FR0,FR2
         AD    FR0,C2
         MDR   FR0,FR2
       AD      FR0,C1
       MDR     FR0,FR2
       AD      FR0,C0A         ADD C0 = 1. IN 2 STEPS
       AD      FR0,C0A           TO PROTECT LAST DIGIT
       SPACE
       LTR     GR3,GR3         MULTIPLY 2**(-C/16)
       BC      8,SKIP2           IN DOING SO, AVOID
       CE      FR0,ONE             MULTIPLICATION BY 1.
       BC      4,SKIP1
       LD      FR0,MCONST-8(GR3)
       BC      15,SKIP2
SKIP1  MD      FR0,MCONST-8(GR3)
SKIP2  LTR     GR2,GR2         MULTIPLY 2**(-B)
       BC      8,SKIP3           BY HALVING B TIMES
       HDR     FR0,FR0
       BCT     GR2,*-2
SKIP3  STD     FR0,SIGN        ADD A TO CHARACTERISTIC
       A       GR0,SIGN
       ST      GR0,SIGN
       SDR     FR0,FR0         NORMALIZE THE ANSWER JUST IN CASE
       AD      FR0,SIGN
       SPACE
EXIT   LM      GR2,GR3,28(GRS) RETURN
       MVI     12(GRS),X'FF'
       BCR     15,GRR
       SPACE
SMALL  SDR     FR0,FR0         IF X IS VERY LARGE NEGATIVE,
       BC      15,EXIT           GIVE 0 AS ANSWER
       SPACE
ERROR    L     GRS,4(GRS)               RESTORE FSA ADDRESS      A32992
         B     FSAERR+24*4(GRS)         PARAM GREATER 174.673    A32992
FSAERR   EQU   X'1CC'
SIGN   DS      D
FIELDS EQU     SIGN+4
LOGE2  DC      X'40B17217F7D1CF79'       LOG 2 (BE) TRUNCATED
ONO16  DC      X'4010000000000000'
ONE    DC      X'41100000'
C6     DC      X'3D9E0F1E'               .1507368551403575E-3
C5     DC      X'3E575D42BB7276D4'       .1333073417706260E-2
C4     DC      X'3F276553A5F9BC94'       .9618117095313700E-2
C3     DC      X'3FE35846A61AEE7A'       .5550410840231345E-1
C2     DC      X'403D7F7BFF0289DE'       .2402265069563678
C1     DC      X'40B17217F7D1CC79'       .6931471805599346
C0A    DC      X'4080000000000000'       .5    C0/2
MCONST DC      X'40F5257D152486CC'           2**(-1/16)
       DC      X'40EAC0C6E7DD2439'           2**(-2/16)
       DC      X'40E0CCDEEC2A94E1'           2**(-3/16)
       DC      X'40D744FCCAD69D6B'           2**(-4/16)
       DC      X'40CE248C151F8481'           2**(-5/16)
       DC      X'40C5672A115506DB'           2**(-6/16)
       DC      X'40BD08A39F580C37'           2**(-7/16)
       DC      X'40B504F333F9DE65'           2**(-8/16)
       DC      X'40AD583EEA42A14B'           2**(-9/16)
       DC      X'40A5FED6A9B15139'           2**(-10/16)
       DC      X'409EF5326091A112'           2**(-11/16)
       DC      X'409837F0518DB8A9'           2**(-12/16)
       DC      X'4091C3D373AB11C3'           2**(-13/16)
       DC      X'408B95C1E3EA8BD7'           2**(-14/16)
       DC      X'4085AAC367CC487B'           2**(-15/16)
SCALER DC      X'45000000'
MAX    DC      X'42AEAC4E'         174.6731
MIN    DC      X'C2B437DF'         -180.2187
       END
