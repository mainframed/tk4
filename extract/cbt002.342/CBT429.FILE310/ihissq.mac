 TITLE 'IHISSQ, SQUARE ROOT ROUTINE, SHORT PRECISION'
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION: WRITE X = M*16**(2P-Q), M MANTISSA, Q=0 OR 1,    *
*   THEN SQRT(X) = SQRT(M*16**-Q)*16**P                               *
*                                                                     *
*ENTRY POINT:                                                         *
*        IHISSQ - SQRT FUNCTION, SHORT             LA  1,PARMLIST     *
*                                                  BALR 14,15         *
*                                                  DATA PASSED BY NAME*
*   THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE.           *
*                                                                     *
*INPUT: N/A                                                           *
*                                                                     *
*OUTPUT: N/A                                                          *
*                                                                     *
*EXTERNAL ROUTINES: N/A                                               *
*                                                                     *
*EXIT-NORMAL: RETURN BY MEANS OF   BR  14.  RESULT IN FPR0.           *
*                                                                     *
*EXIT-ERROR: IF ARGUMENT NEGATIVE GO TO ERROR ROUTINE VIA             *
*        B     FSAERR+23*4(13)                                        *
*                                                                     *
*TABLES/WORKAREAS: N/A                                                *
*                                                                     *
*ATTRIBUTES: THE MODULE IS SERIALLY REUSABLE.                         *
*                                                                     *
*NOTES: CHARACTER CODE DEPENDENCE                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL       *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
       SPACE
IHISSQRT CSECT
       ENTRY   IHISSQ
       SPACE
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GR2    EQU     14
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
       SPACE
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
         EJECT
       DC      CL7'IHISSQ'             ENTRY-POINT IOENTIFIER
       DC      X'06'
       USING   *,GRL
IHISSQ STM     GRR,GRL,12(GRS)         SAVE REGISTERS
       L       GR1,0(GRA)
       L       GR0,0(GR1)      OBTAIN ARGUMENT
       LE      FR4,0(GR1)
       LTR     GR0,GR0
       BC      8,ZRANS         IF ARG IS 0, ANSWER IS 0
       BC      4,ERROR         IF NEGATIVE ARG, ERROR
       AL      GR0,BIAS        ADD X'41' TO CHAR
       SRDL    GR0,25
       SLL     GR0,24          CHAR OF ANSWER IS READY
       LR      GR2,GR0           KEEP IT IN GR2
       LTR     GR1,GR1         SIGN BIT OF GR1 = 1-Q
       BC      4,*+8
       A       GR2,FOUR        IF Q=1, ADD 4 (B31) TO GR2 FOR INDEXING
       SRL     GR1,3           SCALE 1+M (Q=0) OR M (Q=1) TO B3
       A       GR1,C(GR2)      OBTAIN 1ST APPROXIMATION BY A HYPERBOLIC
       L       GR0,B(GR2)        FIT OF THE RESPECTIVE INTERVAL.
       DR      GR0,GR1           IF Q=1, INTERPRET M AS M/16 (B-1)
       A       GR1,A(GR2)
       AR      GR1,GR2         ADD ON CHAR TO COMPLETE 1ST APPROX
       ST      GR1,BUFF
       SPACE
       LER     FR0,FR4         GIVE 2 PASSES OF NEWTON-RAPHSON
       DE      FR0,BUFF          ITERATION
       AE      FR0,BUFF
       HER     FR0,FR0
       DER     FR4,FR0
       SER     FR4,FR0          Y2 =(Y1+X/Y1)/2 = (X/Y1-Y1)/2+Y1
       HER     FR4,FR4            USE THE LATTER TO PROTECT LAST DIGIT
       AER     FR0,FR4
FIN    L       GR2,12(GRS)
       MVI     12(GRS),X'FF'
       BCR     15,GRR          RETURN
       SPACE
ZRANS  SER     FR0,FR0
       BC      15,FIN
       SPACE
ERROR    SLL   GR0,1
         LTR   GR0,GR0            TEST IF NEGATIV ZERO
         BZ    ZRANS
         B     FSAERR+23*4(13)     NEGATIV PARAMETER
       SPACE
FSAERR   EQU   X'1CC'
BUFF   DS      F
BIAS   DC      X'41000000'
FOUR   DC      F'4'
A      DC      X'01CE9FE0'    1.8071270   A0 (B7)
       DC      X'006DC57C'    0.4287950   A1 (B7) MINUS 4 (B31)
B      DC      X'FFE6C37D'   -1.5772732   B0 (B11)
       DC      X'FFFA82EB'   -0.0214398   B1 (B7)
C      DC      X'FF44546F'    0.95418214  C0 (B3) MINUS 1 (B3)
       DC      X'0E0A7419'    0.0548470   C1 (B-1)
       END
