 TITLE 'IHISYS,SYSACT'
*
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION:CONSISTS OF 15 ROUTINES WITH DIFFERENT ACTIONS
*   ON DATA SET WHICH DEPENDING ON SECOND PARAMETER IN SYSACT
*
*ENTRY POINTS:
*        IHISYSCT-FROM GENERATED OBJECT MODULE    LA 1,PARMLIST
*   DATA PASSED BY NAME                           BALR 14,15
*
*INPUT:SYSACT4  READS TWO RECORDS AFTER REPOSITIONING
*
*OUTPUT: N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR -EVALUATE DATA SET NUMBER-OPEN DATA SET -CHANGE TO NEXT
*   RECORD-CLOSE DATA SET-CONVERT REAL TO INTEGER
*
*EXITS-NORMAL: RELOAD REGISTERS AND BR14
*     -ERROR :DATA SET NUMBER OUT OF RANGE             NO.0
*   INCOMPATIBLE ACTIONS ON SAME DATA SET              NO.2
* INPUT REQUEST BEYOND END OF DATA SET            NO.5
*   UNDEFINED FUNCTION NUMBER                          NO.9
*   DATA SET CLOSED                                    NO.10
*   DATA SET OPEN                                      NO.11
*   QUANTITY IN SYSACT PROCEDURE MUST BE A VARIABLE    NO.12
*   QUANTITY IN SYSACT PROCEDURE OUT OF RANGE          NO.13
*   BACKWARD REPOSITIONING NOT DEFINED                 NO.14
*   BRANCH TO IHIFSA      L 13,IHIFSA
*                         B FSAERR+XX*4(13)       XX  ERROR NO.
*
*TABLES/WORK AREAS: N/A
*
*ATTRIBUTES: SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
*
*
*
IHISYSCT CSECT
*2210039660-039730,041030-041090,049400,050410-050520,060020       0221
*2210060030-060035,105900,124000,125500-125750                     0221
*2210103400                                                        0222
*2210018600,022400,039800,040000,043000,043200,060100,060110       0226
*2210060290,060320,061600,061650,062760,062800,063320,063500       0226
*2210065200,065300,106000,107800,119200,119400,122200,122400       0226
*2210125000,125200,132600,132800,141200-142600                     0226
*CH091400                                                        A32998
*
*
*
ENTRY    EQU   15                       ADDRESS OF ROUTINE SYSACT
RETURN   EQU   14                       ADDRESS OF CALLING ROUTINE
SAVE     EQU   13                       ADDRESS OF SAVE AREA IN FSA
PARAM    EQU   1                        ADDRESS OF PARAMETER LIST
*
*        OTHER GENERAL REGISTER
*
DCB      EQU   8                        ADDRESS OF DATA CONTROL BLOCK
FSA      EQU   12                       ADDRESS OF FIXED STORAGE AREA
DSN      EQU   5                       ENTRY IN DSTAB
DSNR     EQU   6                        DATA SET NUMBER
BASE     EQU   7
GR0      EQU   0
GR1      EQU   1
GRA      EQU   2
GRB      EQU   3
QUAN     EQU   4
VARQ     EQU   9
NTE      EQU   10
GRNL     EQU   10
GR       EQU   11
*
*        FLOATING POINT REGISTER
*
FR0      EQU   0
*                                                                  0226
*  DISPLACEMENTS IN ADRLST IN IHIFSA                               0226
*                                                                  0226
CI       EQU   0            DISPLACEMENT FOR :  IHIIORCI           0226
CL       EQU   4                                IHIIORCL           0226
EV       EQU   8                                IHIIOREV           0226
NX       EQU   12                               IHIIORNX           0226
OP       EQU   16                               IHIIOROP           0226
OQ       EQU   20                               IHIIOROQ           0226
*
*
*   DATA SET FLAGS DSF IN DSTAB
*
DS0      EQU   X'80'
DS1      EQU   X'40'
DS2      EQU   X'20'
DS3      EQU   X'10'
DS4      EQU   X'08'
DS5      EQU   X'04'
DS6      EQU   X'02'
DS7      EQU   X'01'
DS8      EQU   X'80'
DS9      EQU   X'40'
DS10     EQU   X'20'
         SAVE  (14,12)
         LR    FSA,SAVE                 ADRESS OF FIXED STORAGE AREA   X
                                        TO REGISTER FSA
         LR    BASE,ENTRY               DEFINE AND LOAD BASEREGISTER
         USING IHISYSCT,BASE
         ST    SAVE,SAVEAR+4
         LA    SAVE,SAVEAR
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
*
*  EVALUATE DATA SET NUMBER IN REGISTER DSNR, ENTRY TO DSTAB IN
*  REGISTER DSN
*
         USING DSTABLE,DSN
*
*  EVALUATE SECOND PARAMETER FUNCTION
*
*
         L     GRA,4(0,PARAM)
         LTR   GRA,GRA
         BC    10,SYS1
         TM    OPTSW(FSA),X'20'         CONVERSION TO INTEGER TEST PRE.
         BO    SYS11                    SHORT
         LD    FR0,0(0,GRA)             LONG
         B     SYS11+4
SYS11    LE    FR0,0(0,GRA)
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,CI(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     *+8
SYS1     L     GR0,0(0,GRA)
         LR    GRA,GR0
         N     GR0,RANGEFUN
         BC    4,SYSERR9                FUNCTION OUT OF RANGE
         LTR   GRA,GRA
         BZ    SYSERR9                  FUNCTION OUT OF RANGE
         SLA   GRA,2
         L     GRB,SYS(GRA)
*
*  EVALUATE THIRD PARAMETER QUANTITY
*
*
         SR    VARQ,VARQ
         L     GRA,8(0,PARAM)
         SLL   GRA,1
         LTR   GRA,GRA
         BM    *+8
         LA    VARQ,1(0,VARQ)           QUANTITY IS A VARIABLE
         SRL   GRA,1
         N     GRA,=X'00FFFFFF'
         L     QUAN,0(0,GRA)
*
*  ADRESS TO ACTUAL SYSACT ROUTINE IN REGISTER GRB
*
*  ADRESS TO QUANTITY IN REGISTER GRA QUANTITY IN REG QUAN
*
*  REGISTER VARQ EQUAL ONE IF QUANTITY IS A VARIABLE ZERO ELSE
*
*
         BR    GRB
         SPACE
RETSYS   L     SAVE,SAVEAR+4
         RETURN (14,12)
         SPACE
SYSERR9  LR    13,FSA                   UNDEFINED FUNCTION NUMBER ERR9
         B     FSAERR+9*4(FSA)
*                                                                  0226
*        BRANCH TO NEXTREC IN IHIIOR                               0226
*                                                                  0226
NXREC    L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BR    ENTRY                                               0226
         EJECT                                                     0226
*
*  ROUTINE SYSACT1    QUANTITY.=R
*
*
         SPACE
         SPACE
         SPACE
SYSACT1  TM    DSF,DS0
         BO    SYS1T1
SYSCLOSD LR    13,FSA                   DATA SET IS CLOSED ERROR 10
         B     FSAERR+10*4(FSA)
SYS1T1   LTR   VARQ,VARQ
         BP    SYS1T2
SYSCONST LR    13,FSA                   QUANTITY SHOULD BE A VARIABLE
         B     FSAERR+12*4(FSA)         ERROR12
SYS1T2   L     QUAN,R
         S     QUAN,RE
         AH    QUAN,P
         LA    QUAN,1(0,QUAN)
         ST    QUAN,0(0,GRA)
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT2     R.=QUANTITY
*
*
SYSACT2  LTR   QUAN,QUAN
         BP    *+8
         B     SYS2T1
         CH    QUAN,P
         BNH   SYS2T1+6
SYS2T1   LR    13,FSA
         B     FSAERR+13*4(FSA)         QUANTITY OUT OF RANGE ERROR13
         SPACE
         SPACE
         TM    DSF,DS0
         BZ    SYSCLOSD                 DATA SET IS CLOSED
         LR    GRB,QUAN
         A     GRB,RE
         SH    GRB,P
         BCTR  GRB,0
         S     GRB,R
         BP    SYS2T2
*
*  NEW CHARACTER POINTER LESS OR EQUAL R
*
         TM    DSF,DS2
         BO    SYS2T3
         TM    DSF+1,DS10          IS D.S. OPENED BY SYSACT 12     0221
         BO    SYS2T30                                             0221
         TM    DSF,DS7             HAS EOD BEEN REACHED            0221
         BO    SYSEOD                                              0221
         BAL   RETURN,NXREC    LAST I/O PROCEDURE WAS INPUT        0226
         TM    DSF,DS7             IS END OF DATA REACHED          0213
         BZ    *+10                                                0213
         LR    GRB,QUAN            IS QUANTITY = 1                 0213
         BCT   GRB,SYSEOD          NO INPUT REQUEST BEYOND E.O.D.  0213
         A     QUAN,RE
         SH    QUAN,P              ASSIGN A NEW VALUE TO CHARACTER
         BCTR  QUAN,0
         ST    QUAN,R
         B     RETSYS
SYS2T30  LTR   DSNR,DSNR           IS DATA SET NR = 0              0221
         BZ    SYSINCOM                                            0213
         OI    DSF,DS2                                             0213
*
*  LAST I/O PROCEDURE WAS OUTPUT
*
SYS2T3   L     GRB,RE
         S     GRB,R
         BZ    SYS2T31
         LA    ENTRY,SYBLANK            FILL RECORD WITH BLANKS
         BALR  RETURN,ENTRY
SYS2T31  BAL   RETURN,NXREC                                        0226
         SPACE
         SPACE
         SPACE
         LR    GRB,QUAN
         BCTR  GRB,0
         LTR   GRB,GRB
         BZ    SYS2T32
         OI    DSF,DS3                                             0213
         LA    ENTRY,SYBLANK            FILL RECORD WITH BLANKS
         BALR  RETURN,ENTRY
SYS2T32  L     GR,R
         AR    QUAN,GR
         BCTR  QUAN,0                   CHARACTER POINTER
         ST    QUAN,R                   QUANTITY+R-1 ASSIGN TO R
         B     RETSYS
*
*  NEW CHARACTER POINTER GREATER THAN R
*
*
SYS2T2   TM    DSF,DS2
         BO    SYS2T20                                             0221
         TM    DSF+1,DS10          HAS DS BEEN OPENED BY SYSACT 12 0221
         BZ    SYS2T5                                              0213
         LTR   DSNR,DSNR           IS DATA SET NR = 0              0221
         BZ    SYSINCOM                                            0221
         OI    DSF,DS2                                             0221
SYS2T20  LR    GRB,QUAN                 LAST I/O WAS OUTPUT        0221
         A     GRB,RE
         S     GRB,R
         OI    DSF,DS3                                             0213
         SH    GRB,P
         BCTR  GRB,0
         LA    ENTRY,SYBLANK            FILL RECORD WITH BLANKS
         BALR  RETURN,ENTRY
         SPACE
         SPACE
SYS2T5   TM    DSF,DS7             HAS EOD BEEN REACHED            0221
         BO    SYSEOD                                              0221
SYS2T4   A     QUAN,RE                                             0221
         SH    QUAN,P              ASSIGN NEW VALUE TO CHAR POINTER
         BCTR  QUAN,0
         ST    QUAN,R
         B     RETSYS
         SPACE
         SPACE
         SPACE
         EJECT
*
*  ROUTINE SYSACT3        QUANTITY.=S
*
*
SYSACT3  TM    DSF,DS0
         BZ    SYSCLOSD                 DATA SET CLOSED ERROR10
         SPACE
         SPACE
         LTR   VARQ,VARQ
         BZ    SYSCONST                 QUANTITY IS NOT A VARIABLE     X
                                        ERROR12
         LH    QUAN,S
         ST    QUAN,0(0,GRA)
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT4  UNDEFINED IF DATA SET SPLIT INTO SECTION BY SYSACT8
*  OR IF THE DATA SET IS 0 OR 1                                    0206
*
*  ACTION AFTER INPUT:  S.=QUANTITY R.=1
*  ACTION AFTER OUTPUT: S.=QUANTITY R.=1  IF FORWARD SKIPPING FILL
*
*  SKIPPED RECORDS BY BLANKS
*
SYSACT4  L     DCB,ADCB                                            0206
         LTR   QUAN,QUAN           TEST QUANTITY                   0206
         BP    SYS4T01                                             0206
         LR    13,FSA                                              0206
         B     FSAERR+13*4(FSA)    QUANTITY OUT OF RANGE           0206
SYS4T01  TM    DSF+1,DS9           IS DATA SET SECTIONED           0206
         BO    SYSINCOM                                            0206
         LTR   GRA,DSNR            IS DATA SET NO = 1 OR 0         0206
         BZ    SYSINCOM                                            0206
         BCT   GRA,SYS4T0                                          0206
         B     SYSINCOM                                            0206
SYS4T0   TM    DSF,DS0             IS DATA SET OPEN                0206
         BZ    SYSCLOSD                                            0206
         CH    QUAN,S              IS QUANTITY  > S                0206
         BL    SYS4T2              NO LESS                         0206
         BE    SYS4T3              NO EQUAL                        0206
SYS4T1   TM    DSF,DS2             WAS LAST I/O OUTPUT             0206
         BO    SYS4T15             YES BRANCH TO SYS4T15           0206
         SPACE
*  LAST I/O INPUT AND QUANTITY > S                                 0206
         TM    DSF+1,DS10          HAS DS BEEN OPENED BY SYSACT 12 0221
         BZ    SYS4T12                                             0206
         OI    DSF,DS2             DATA SET OPEN FOR OUTPUT        0206
         B     SYS4T15                                             0206
SYS4T12  LA    ENTRY,SYSNOT        IS QUANTITY IN NOTTAB           0206
         BALR  RETURN,ENTRY                                        0206
         TM    DSF,DS7             HAS EOD BEEN REACHED            0221
         BO    SYSEOD                                              0221
         LTR   GRB,GRB                                             0206
         BP    SYS4T21             YES BRANCH TO SYS4T21           0206
SYS4T13  TM    DSF,DS7             HAS END OF DATA BEEM REACHED    0206
         BO    SYSEOD                                              0206
         BAL   RETURN,NXREC        GET NEXT RECORD                 0226
         CH    QUAN,S              IS QUANTITY = S                 0206
         BNE   SYS4T13                                             0206
         B     RETSYS                                              0206
         SPACE                                                     0206
* LAST I/O OUTPUT AND QUANTITY > S                                 0206
SYS4T15  L     GRB,RE              FILL RECORD WITH BLANKS         0206
         S     GRB,R                                               0206
         BZ    SYS4T14                                             0206
         LA    ENTRY,SYBLANK                                       0206
         BALR  RETURN,ENTRY                                        0206
SYS4T14  BAL   RETURN,NXREC                                        0226
         CH    QUAN,S              IS QUANTITY = S                 0206
         BNE   SYS4T15                                             0206
         B     RETSYS                                              0206
SYS4T2   LA    ENTRY,SYSNOT        IS QUANTITY IN NOTTAB           0206
         BALR  RETURN,ENTRY                                        0206
         LTR   GRB,GRB                                             0206
         BZ    SYSERR14            NO BACKWARD REP. NOT DEFINED    0206
         TM    DSF,DS2             WAS LAST I/O OUTPUT             0206
         BO    SYS4T24                                             0206
         SPACE                                                     0206
* LAST I/O INPUT AND QUANTITY < S                                  0206
         OI    DSF,DS5             DS5=1                           0206
         NI    DSF,X'FC'           DS6=DS7=0                       0206
SYS4T21  OI    DSF+1,DS8           DS8=1                           0206
         USING SYSD,DCB                                            0206
         CHECK SYDECB                                              0206
         NI    DSF+1,X'7F'         DS8=0                           0206
SYS4T22  LR    GR0,NTE                                             0206
         LR    1,DCB                                               0206
         POINT (1),(0)                                             0206
         L     GRA,NBB                                             0206
         READ  SYDECB,SF,(DCB),(GRA),MF=E                          0206
         L     GRA,BB                                              0206
         AH    GRA,BL                                              0206
         ST    GRA,RE                                              0206
         LR    GRA,QUAN                                            0206
         BCTR  GRA,0                                               0206
         STH   GRA,S               S = QUANTITY - 1                0206
         BAL   RETURN,NXREC                                        0226
         TM    DSF,DS1             IS DATA SET BLOCKED             0206
         BO    RETSYS                                              0206
         SR    GRA,GRA             YES EVALUATE NUMBER OF FIRST    0206
         L     GRB,BL              RECORD IN THE BLOCK=            0206
         SRL   GRB,16              ENTIER((QUANTITY-1)/(BLKLTH/    0206
         L     GR1,S               RECLNTH))*(BLKLNTH/RECLNTH)+1   0206
         SLL   GR1,16                                              0206
         SRL   GR1,16                                              0206
         DR    GRA,GR1                                             0206
         LR    GR1,GRB                                             0206
         SR    GRA,GRA                                             0206
         LR    GRB,QUAN                                            0206
         BCTR  GRB,0                                               0206
         DR    GRA,GR1                                             0206
         MR    GRA,GR1                                             0206
         LA    GRB,1(GRB)                                          0206
         STH   GRB,S                                               0206
SYS4T23  CH    QUAN,S              IS QUANTITY = S                 0206
         BE    RETSYS                                              0206
         BAL   RETURN,NXREC                                        0226
         B     SYS4T23                                             0206
         SPACE                                                     0206
* LAST I/O WAS OUTPUT AND QUANTITY < S                             0206
SYS4T24  TM    DSF,DS1             IS DATA SET BLOCKED             0206
         BO    SYS4T25                                             0206
         L     GRB,BB                                              0206
         AH    GRB,BL                                              0206
         ST    GRB,RE                                              0206
SYS4T25  L     GRB,RE              FILL BLOCK WITH BLANKS          0206
         S     GRB,R                                               0206
         BZ    SYS4T26                                             0206
         LA    ENTRY,SYBLANK                                       0206
         BALR  RETURN,ENTRY                                        0206
SYS4T26  BAL   RETURN,NXREC                                        0226
SYS4T27  CHECK SYDECB                                              0206
         CLOSE ((DCB),LEAVE),TYPE=T  SET END OF DATA MARK          0206
         OI    DSF,DS5                                             0206
         NI    DSF,X'DD'           DS2=DS6=0                       0206
         B     SYS4T22             WAS LAST I/O OUTPUT             0206
SYS4T3   TM    DSF,DS2                                             0206
         BZ    SYS4T31                                             0206
         SPACE                                                     0206
*  LAST I/O OUTPUT AND QUANTITY = S                                0206
         OI    DSF,DS4                                             0206
         L     GRB,BB                                              0206
         AH    GRB,BL                                              0206
         ST    GRB,RE                                              0206
         S     GRB,R               FILL BLOCK WITH BLANKS          0206
         BZ    SYS4T33                                             0206
         LA    ENTRY,SYBLANK                                       0206
         BALR  RETURN,ENTRY                                        0206
SYS4T33  BAL   RETURN,NXREC        WRITE BLOCK                     0226
         LA    ENTRY,SYSNOT        MAKE NOTTAB ENTRY               0206
         BALR  RETURN,ENTRY                                        0206
         B     SYS4T27                                             0206
         SPACE                                                     0206
* LAST I/O INPUT AND QUANTITY = S                                  0206
SYS4T31  L     GRA,RE                                              0206
         SH    GRA,P                                               0206
         ST    GRA,R               RESET CHARACTER POINTER         0206
         B     RETSYS                                              0206
SYSEOD   LR    13,FSA                                              0206
         B     FSAERR+5*4(FSA)                                     0206
*
*  SCAN NOTTAB IN ORDER TO FIND OUT IF AN ENTRY HAS BEEN MADE FOR
*  QUANTITY  IF YES GRB IS SET TO ONE,ADRESS OF NOTEADR IN REG NTE
*
SYSNOT   L     GRB,ANOTTAB(FSA)
         LR    NTE,GRB
SYSNOT1  C     NTE,0(0,GRB)
         BE    SYSNOT2
         LA    NTE,8(0,NTE)
         LR    GRA,DSNR
         CH    GRA,0(0,NTE)
         BNE   SYSNOT1
         CH    QUAN,2(0,NTE)
         BNE   SYSNOT1
         SR    GRB,GRB
         LA    GRB,1(0,GRB)
         LA    NTE,4(0,NTE)
         BR    RETURN
SYSNOT2  SR    GRB,GRB
         BR    RETURN
         SPACE
         SPACE
SYSERR14 LR    13,FSA                   BACKWARD REPOSITIONING NOT
         B     FSAERR+14*4(FSA)         DEFINED
         EJECT
*
*  ROUTINE SYSACT5   QUANTITY.=P
*
*
SYSACT5  TM    DSF,DS0
         BZ    SYSCLOSD                 DATA SET CLOSED ERROR10
         SPACE
         SPACE
         LTR   VARQ,VARQ
         BZ    SYSCONST                 QUANTITY IS NOT A VARIABLE     X
                                        ERROR12
         LH    QUAN,P
         ST    QUAN,0(0,GRA)
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT6    P.=QUANTITY  DATA SET HAS TO BE CLOSED
*
*
SYSACT6  TM    DSF,DS0
         BO    SYSOPEN
         LTR   QUAN,QUAN                TEST QUANTITY
         BC    12,SYSQOUTR              MINUS OR ZERO
         C     QUAN,=F'32760'
         BH    SYSQOUTR                 TOO GREAT
         STH   QUAN,P
         B     RETSYS
         SPACE
         SPACE
SYSOPEN  LR    13,FSA                   DATA SET IS OPEN ERROR 11
         B     FSAERR+11*4(FSA)
         EJECT
*
*  ROUTINE SYSACT7    QUANTITY.=Q
*
*
SYSACT7  LTR   VARQ,VARQ
         BZ    SYSCONST            QUANTITY IS NOT A VARIABLE ERROR12
         SPACE
         SR    QUAN,QUAN
         IC    QUAN,Q
         ST    QUAN,0(0,GRA)
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT8     Q.=QUANTITY DATA SET HAS TO BE CLOSED
*  ASSIGNING A VALUE TO Q
*
SYSACT8  TM    DSF,DS0
         BO    SYSOPEN                  DATA SET IS OPEN ERROR11
         LTR   DSNR,DSNR                IS IT DATA SET 0         A32998
         BZ    SYSINCOM            YES INCOMPATIBLE ACTION         0206
         LTR GRB,QUAN                   TEST QUANTITY
         BC    12,SYSQOUTR              MINUS OR ZERO
         S     GRB,TWO56
         BP    SYSQOUTR
         BZ    SYSQOUTR
         SPACE
         STC   QUAN,Q
         OI    DSF+1,DS9                RECORD CONTAIN CTLCHARACTER
         B     RETSYS
         SPACE
SYSDSN0  LR    13,FSA                   DATA SET NUMBER OUT OF RENGE
         B     FSAERR(FSA)
         SPACE
         SPACE
SYSQOUTR LR    13,FSA                   QUANTITY OUT OF RANGE ERROR 13
         B     FSAERR+13*4(FSA)
         SPACE
         SPACE
TWO56    DC    F'256'
         EJECT
*
*  ROUTINE  SYSACT9     QUANTITY.=K
*
*
SYSACT9  LTR   VARQ,VARQ
         BZ    SYSCONST            QUANTITY IS NOT A VARIABLE ERROR12
         SR    QUAN,QUAN
         IC    QUAN,K
         ST    QUAN,0(0,GRA)
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT10   K.=QUANTITY  I.E ASSIGN NUMBER OF BLANK DELIMTERS
*
*
SYSACT10 LTR   QUAN,QUAN                TEST QUANTITY
         BC    12,SYSQOUTR              MINUS OR ZERO
         C     QUAN,TWO56
         BNL   SYSQOUTR                 TOO GREAT
         STC   QUAN,K
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT11  ASSIGN VALUE TO QUANTITY FOR DEFINING IF
*  DATA SET OPEN OR CLOSED
*
SYSACT11 LTR   VARQ,VARQ
         BZ    SYSCONST            QUANTITY IS NOT A VARIABLE ERROR12
         SPACE
         SR    QUAN,QUAN
         TM    DSF,DS0
         BZ    SYS11T1             DATA SET IS CLOSED
         SPACE
         LA    QUAN,1(0,QUAN)
         TM    DSF,DS7
         BO    SYS11T2                 DS7 EQUAL 1                 0222
         TM    DSF,DS2                                             0222
         BO    SYS11T1                 OUTPUT                      0222
         TM    DSF,DS1                                             0222
         BO    SYS11T1                 UNBLOCKED                   0222
         L     GR,R                                                0222
         L     GR1,RE                                              0222
         SH     GR1,P                                              0222
         CR    GR1,GR                                              0222
         BNE   SYS11T1                 R NOT EQUAL RE MINUS P      0222
         L     GR1,BB                                              0222
         AH    GR1,BL                                              0222
         SR    GR1,GR                                              0222
SYS11T3  CLI   0(GR),C' '                                          0222
         BNE   SYS11T1                 CHARACTER NOT BLANK         0222
         LA    GR,1(GR)                                            0222
         BCT   GR1,SYS11T3                                         0222
         L     DCB,ADCB                                            0222
         CHECK SYDECB                                              0222
         TM    DSF,DS7                                             0222
         BZ    SYS11T1             NO END OF DATA
SYS11T2  LNR   QUAN,QUAN                                           0222
SYS11T1  ST    QUAN,0(0,GRA)
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT12  OPEN  OR CLOSE DATA SET
*
*
SYSACT12 LTR   QUAN,QUAN
         BZ    SYS12T1
         BCT   QUAN,SYSQOUTR            QUANTITY OUT OF RANGE
         TM    DSF,DS0             ROUTINE SHOULD PERFORM OPEN
         BO    SYS12T2                  DATA SET WAS OPEN ALREADY
         OI    DSF+1,DS10          DS10=1 DS OPENED BY SYSACT 12   0221
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OQ(ENTRY)     OPEN DATA SET                   0221
         BALR  RETURN,ENTRY
         B     RETSYS                                              0206
         SPACE
SYS12T1  TM    DSF,DS0             ROUTINE SHOULD PERFORM CLOSE
         BZ    SYS12T2                  DATA SET WAS CLOSED ALREADY
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,CL(ENTRY)                                     0226
         BALR  RETURN,ENTRY
SYS12T2  B     RETSYS
         EJECT
*
*  ROUTINE SYSACT 13  QUANTITY.=S  AND SETTING DS4 FLAG USED BY A LATER
*  SYSACT4 FOR AN ENTRY TO NOTTAB
*
SYSACT13 TM    DSF,DS0
         BZ    SYSCLOSD                 DATA SET IS CLOSED ERROR10
         TM    DSF+1,DS9           IS DATA SET SECTIONED           0206
         BO    SYSINCOM            YES INCOMPATIBLE ACTION         0206
         LTR   GRB,DSNR            IS DATA SET NO = 1 OR 0         0206
         BZ    SYSINCOM                                            0206
         BCT   GRB,SYS13T1                                         0206
         SPACE
         SPACE
SYSINCOM LR    13,FSA                   INCOMPATIBLE ACTION ON THE SAME
         B     FSAERR+2*4(FSA)     DATA SET                        0206
         SPACE
         SPACE
SYS13T1  LTR   VARQ,VARQ
         BZ    SYSCONST                 QUANTITY IS NOT A VARIABL      X
                                        ERROR12
         LH    QUAN,S
         ST    QUAN,0(0,GRA)
         SPACE
         SPACE
         OI    DSF,DS4                  SET FLAG DS4
         B     RETSYS
         EJECT
*
*  ROUTINE SYSACT14  SKIPS RECORDS OR FILLS THEM BY BLANKS,DEPENDING
*  ON THE LAST I/O PROCEDURE
*
SYSACT14 LTR   QUAN,QUAN
         BC    12,SYSQOUTR        QUANTITY OUT OF RANGE ERROR13
         TM    DSF,DS0
         BZ    SYSCLOSD
SYS14T1  AH    QUAN,S
         TM    Q,X'FF'
         BZ    SYS14T2
*  SECTIONED FORMAT ONLY OUTPUT POSSIBLE
*
         SR    GRA,GRA
         IC    GRA,Q
         CR    QUAN,GRA
         BNH   SYS14T2
         L     GRB,RE
         S     GRB,R               NUMBER OF BLANKS IN GRB
         BZ    SYS14T11
         LA    ENTRY,SYBLANK            FILL RECORD WITH BLANKS
         BALR  RETURN,ENTRY
         SPACE
         SPACE
SYS14T11 MVC   S+1(1),Q
         SPACE
         OI    DSF,DS2             OUTPUT                          0206
         SPACE
         SPACE
         BAL   RETURN,NXREC    SKIP TO BEGIN OF NEXT SECTION       0226
         B     RETSYS
*
*   NOT SECTIONED FORMAT OR QUANTITY LESS OR EQUAL Q
*
*
SYS14T2  TM    DSF,DS2
         BZ    SYS14T4                  LAST I/O WAS INPUT
SYS14T3  L     GRB,RE                                              0206
         S     GRB,R
         BZ    SYS14T5
         LA    ENTRY,SYBLANK            FILL RECORD WITH BLANKS
         BALR  RETURN,ENTRY
SYS14T5  BAL   RETURN,NXREC                                        0226
         CH    QUAN,S
         BNE   SYS14T3
         B     RETSYS
*
*  INPUT
*
SYS14T4  TM    DSF+1,DS10          HAS DS BEEN OPENED BY SYSACT 12 0221
         BO    SYS14T7                                             0213
*
*  ONLY INPUT READ THE FOLLWING UNTIL RECORD POINTER S EQUALS QUANTITY
*
SYS14T6  TM    DSF,DS7             HAS EOD BEEN REACHED            0221
         BO    SYSEOD                                              0221
         BAL   RETURN,NXREC                                        0221
         CH    QUAN,S
         BE    RETSYS                                              0213
         B     SYS14T6                                             0221
SYS14T7  LTR   DSNR,DSNR           IS DATA SET NR = 0              0221
         BZ    SYSINCOM                                            0213
         OI    DSF,DS2             OUTPUT                          0213
         B     SYS14T3                                             0213
         EJECT
*
*  ROUTINE SYSACT15 SKIP TO RECORD EQUAL QUANTITY IN NEXT SECTION
*  IF DATA IS NOT SECTIONED SYSACT14 IS INVOKED
*
*
SYSACT15 LTR   QUAN,QUAN
         BC    13,SYSQOUTR              QUANTITY OUT OF RANGE ERROR13
         TM    DSF,DS0
         BZ    SYSCLOSD
         SPACE
         TM    Q,X'FF'
         BZ    SYS14T1                  DATA IS NOT SECTIONED SYSACT14 X
                                        IS INVOKED
         SPACE
         SPACE
         SR    GRB,GRB
         IC    GRB,Q
         CR    QUAN,GRB
         BH    SYSQOUTR
         SPACE
         SPACE
         L     GRB,RE
         S     GRB,R                    FILL RECORD WITH BLANKS
         BZ    SYS15T0
         LA    ENTRY,SYBLANK            FILL RECORD WITH BLANKS
         BALR  RETURN,ENTRY
         SPACE
         SPACE
         SPACE
         SPACE
SYS15T0  MVC   S+1(1),Q
         OI    DSF,DS2             OUTPUT                          0206
         BAL   RETURN,NXREC                                        0226
         SPACE
         SPACE
         LR    GRB,QUAN
         BCT   GRB,SYS14T3
         SPACE
SYS15T1  B     RETSYS                   QUANTITY EQUALS ONE            X
                                        FIRST RECORD IN NEXT SECTION
         EJECT
*
*  SYBLANK FILL RECORD WITH BLANKS
*
*
SYBLANK  L     GR,R
SYBLANK1 MVI   0(GR),X'40'
         LA    GR,1(0,GR)
         BCT   GRB,SYBLANK1
         BR    RETURN
         EJECT
         CNOP  0,4
SAVEAR   DS    18F                      SAVEAREA
RANGEFUN DC    F'-16'                   SCOPE OUTSIDE RANGE OF FUNCTION
         LTORG
         SPACE
         SPACE
         SPACE
SYS      DS    A
         DC    A(SYSACT1)
         DC    A(SYSACT2)
         DC    A(SYSACT3)
         DC    A(SYSACT4)
         DC    A(SYSACT5)
         DC    A(SYSACT6)
         DC    A(SYSACT7)
         DC    A(SYSACT8)
         DC    A(SYSACT9)
         DC    A(SYSACT10)
         DC    A(SYSACT11)
         DC    A(SYSACT12)
         DC    A(SYSACT13)
         DC    A(SYSACT14)
         DC    A(SYSACT15)
*
*
*
*
DSTABLE  DSECT                          DUMMY SECTION FOR DSTAB
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
*
*
*
         SPACE
SYSD     DSECT                          DUMMY SECTION FOR ADRESSING
         DS    22F                      DECB
         READ  SYDECB,SF,MF=L
FAS      DSECT
         COPY  FSAREA
         END
