 TITLE 'IHIOBO,OUTBOOLEAN'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:TRANSFER BOOLEAN VALUE FROM SECOND ACTUAL PARAMETE{
*   TO AN OUTPUT BUFFER WITH CHARACTERSTRING  'TRUE'  OR  'FALSE'
*
*ENTRY POINTS:
*        IHIOBOOL-FROM GENERATED OBJECT MODULE    LA 1,PARMLIST
*   DATA PASSED BY NAME                           BALR  14,15
*
*        IHIOBOAR-FROM ARRAY MODULE IHIOBA       LA  2,DATA
*   DATA PASSED BY NAME                          BALR   14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER-OPEN DATA SET-CHANGE TO NEXT
*   RECORD
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*   -ERROR: TOO LONG RECORD                 NO.38
*   BRANCH TO IHIFSA         L 13,IHIFSA
*                            B FSAERR+XX*4(13)   XX ERROR NO.
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:LINKING TO IHIBOOAR DEVIATES FROM STANDARD
*   CHARACTER DEPENDENCE:
*   THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT
*   TO THE ONE USED AT ASSEMBLY TIME
*
         SPACE
IHIOBOOL CSECT
*0019015800,016800,018600,024600,028800,039800-041000              0226
         SPACE
         SPACE
*
ENTRY    EQU   15                  ADRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
FSA      EQU   12                  ADRESS OF FIXED STORAGE AREA
         SPACE
         SPACE
*
*  OTHER GENERAL REGISTERS
*
DSN      EQU   5                   ADRESS OF RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER
BASE     EQU   7                   BASE REGISTER
GRA      EQU   2
GRR      EQU   3
GRE      EQU   4
GRK      EQU   8
GRB      EQU   9
         SPACE
*
*  DATA SET FLAGS  (DSF AND DSF+1 IN DSTAB)
*
*
DS0      EQU   X'80'
DS1      EQU   X'40'
DS2      EQU   X'20'
DS3      EQU   X'10'
DS4      EQU   X'08'
DS5      EQU   X'04'
DS6      EQU   X'02'
DS7      EQU   X'01'
DS8      EQU   X'80'
DS9      EQU   X'40'
DS10     EQU   X'20'
*
*
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
*
*
         ENTRY IHIOBOAR
         SAVE  (14,12)
         LA    ENTRY,68(0,ENTRY)        BASE=ADRESS OF SAVE AREA   0226
         LR    BASE,ENTRY
         LR    FSA,SAVE                 ADRESS OF FIXED STORAGE AREA
         USING SAVAR,BASE               INTO REGISTER FSA
         ST    SAVE,SAVAR+4
         LA    SAVE,SAVAR
*
*  EVALUATE DATA SET NUMBER
*
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE
         SPACE
*
*  STORE SOURCE ADRESS
*
         L     PARAM,4(0,PARAM)
         ST    PARAM,ASOURCE
         B     SAVAR+72
         SPACE
         SPACE
IHIOBOAR STM   14,12,12(SAVE)           ENTRY POINT FOR OUTBARRY
         LA    BASE,26(0,ENTRY)        BASE=ADDRESS OF SAVE AREA
         USING SAVAR,BASE
         ST    GRA,ASOURCE
         ST    SAVE,SAVAR+4
         LA    SAVE,SAVAR
         B     SAVAR+72
SAVAR    DS    18F
         USING DSTABLE,DSN              DSTABLE DUMMY SECTION FOR ADRES
         OI    DSF,DS2                  SING IN DSTAB
         OI    DSF,DS3                  DS2 AND DS3 IS SET TO ONE
         NI    DSF,X'FE'                DS7 IS SET TO 0
         SPACE
         SPACE
         SPACE
         TM    DSF,DS0                  TEST IF DATA SET IS OPEN
         BO    OTBOOL1                  DATA SET IS OPEN
         OI    DSF,DS6                  DATA SET IS NOT OPEN
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         BALR  RETURN,ENTRY             CALL FOR ROUTINE OPEN
OTBOOL1  L     GRE,RE
         L     GRR,R
         LA    GRR,7(0,GRR)
         CR    GRE,GRR
         BNL   OTBOOL2                  SEVEN CHARACTER FREE IN RECORD
*
*  NOT PLACE ENOUGH TO STORE BOOLEAN VALUE IN RECORD  FILL CURRENT
*  RECORD BY BLANKS AND CALL FOR ROUTINE NEXTREC
*
          L    GRR,R
         L     GRE,RE
         SR    GRE,GRR
         BZ    OTBOOL7
         MVI   0(GRR),C' '
         BCTR  GRE,0
         BCTR  GRE,0
         LTR   GRE,GRE
         BM    OTBOOL7
         EX    GRE,BLANKS
OTBOOL7  L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         L     GRR,R
         LA    GRR,7(0,GRR)
         C     GRR,RE
         BH    OERROR                   TOO SHORT RECORD LENGTH
         OI    DSF,DS3
         B     OTBOOL2
         CNOP  0,4
BLANKS   MVC   1(0,GRR),0(GRR)
         SPACE
         SPACE
         SPACE
OTBOOL2  L     GRA,ASOURCE
         SR    GRB,GRB
         IC    GRB,0(0,GRA)
         L     GRR,R
         LTR   GRB,GRB
         BZ    OTBOOL3
         MVC   0(7,GRR),TRUE            BOOLEAN VALUE TRUE OR FALSE
         B     *+10                    TO RECORD WHICH ONE DEPENDING
OTBOOL3  MVC   0(7,GRR),FALSE           ON VALUE OF SOURCE
         LA    GRR,7(0,GRR)
         ST    GRR,R                    UPDATE CHARACTER POINTER
         SPACE
*
*     ROUTINE BLADEL
*
         SR    GRK,GRK
         IC    GRK,K                    NUMBER OF DELIMITERS
OTBOOL4  C     GRR,RE
         BE    OTBOOL5                  RECORD END REACHED
         MVI   0(GRR),C' '              FILL WITH BLANK
         LA    GRR,1(0,GRR)
         BCT   GRK,OTBOOL4
         ST    GRR,R                    UPDATE CHARACTER POINTER
         B     OTBOOL6
*
*       CALL NEXTREC
*
OTBOOL5  L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE
         SPACE
         SPACE
OTBOOL6  L     SAVE,SAVAR+4
         LM    14,12,12(SAVE)
         BR    RETURN
*
OERROR   LR    13,FSA
         B     FSAERR+38*4(FSA)
         CNOP  0,4
ASOURCE  DS    A
TRUE     DC    C'''TRUE'' '
FALSE    DC    C'''FALSE'''
         LTORG
         SPACE
         SPACE
         SPACE
*
*  PROVIDING A DUMMY SECTION FOR ADRESSING DSTAB
*
*
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
         SPACE 3                                                   0226
FAS      DSECT                                                     0226
         COPY  FSAREA                                              0226
         END
