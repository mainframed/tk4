 TITLE 'IHIIDE INREAL,ININTEGER'
*
*STATUS: CHANGE LEVEL 000
*
*FUNCTION/OPERATION:SCAN INPUT BUFFER UNTIL A VALID NUMBER IS FOUND
*   TRANSFER TO BINARY AND LOAD TO SECOND ACTUAL PARAMETER
*
*ENTRY POINTS:
*        IHIIDEIR - FROM -   GENERATED OBJECT MODULE;INREAL
*        IHIIDEII - FROM -   GENERATED OBJECT MODULE;ININTEGER
*   DATA PASSED BY NAME                              LA 1,PARMLIST
*                                                    BALR 14,15
*
*        IHIIDEAI - FROM -   ARRAY MODULE IHIIAR     L 7,A(DESTINATION)
*   DATA PASSED BY NAME                              L 10,FLAG
*                                                    BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR - EVALUATE DATA SET NUMBER - OPEN DATA SET -
*   CHANGE TO NEXT INPUT RECORD - CONVERT REAL TO INTEGER
*
*        IHIFSA - CNVIRD,INTEGER TO REAL
*
*        IHIPTT - POWER OF TEN TABLE
*TABLE/WORK AREAS: IPTAB-TO EVALUATE CHARACTER FROM INPUT BUFFER WHEN
*   SCANNING AND TO BRANCH TO CORRESPONDING SUBPROGRAM
*   MB-MANTISSA BUFFER FOR INTERMEDIATE STORING OF VALID DIGITS
*
*EXITS-NORMAL:RELOAD REGISTERS AND BR14
*    -ERROR : INPUT REQUEST BEYOND END OF DATA SET   NO.5
*   EXP PART OF INPUT NUMBER CONSISTS OF MORE THAN 2 DIGITS  NO.6
*   BRANCH TO FSA; LA 13,IHIFSA
*                  B  FSAERR+XX*4(13)    XX CORRESPONDING ERROR NUMBER
*
*ATTRIBUTES: SERIALLY REUSABLE
*
*NOTES: LINKING TO IHIIDEAI DEVIATE FROM STANDARD SEE ABOVE
*   CHARACTER DEPENDENCE;THE OPERATION OF THIS MODULE IS DEPENDENT
*   UPON A TRANSLATION OF EXTERNAL CHARACTERS BY MEANS OF THE
*   HEXADECIMAL TABLE IPTAB.
*   PACKED DECIMAL NUMBERS ARE SIGNED USING THE HEXADECIMAL BYTES
*   X'0F' AND X'0D'.
*   THE OPERATION OF THIS MODULE IS ALSO DEPENDENT UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT
*   TO THE ONE USED AT ASSEMBLY TIME
*
*
IHIIDECM CSECT
*2200027800                                                        0221
*2200024800,026800,027000,032400,059400,059600,085400              0226
*2200089800-090600                                                 0226
         SPACE
ENTRY    EQU   15                  ADDRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADDRES
SAVE     EQU   13                  ADDRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADDRESS OF PARAMETER LIST
         SPACE 2
*        REGISTER CONETNTS ON ENTRY POINT IHGIDEAI
         SPACE
*GRA     EQU   7                   DESTINATION ADDRESS
*SAVE    EQU   13                  ADDRESS OF SAVE AREA IN ARRINN
FSA      EQU   12                  ADDRESS OF FIXED STORAGE AREA
GRFKT    EQU   10                  FLAG BYTE
         SPACE 3
*        OTHER GENERAL REGISTERS
         SPACE
DSN      EQU   5                   RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER BINARY
BASE     EQU   13                  BASE REGISTER FOR THIS ROUTINE
GRA      EQU   7                   DESTINATION ADDRESS
GRR      EQU   4                   CHARACTER POINTER
FR0      EQU   0                   FLOATING-POINT NUMBER
GR0      EQU   0                   INTEGER NUMBER
GRMP     EQU   8                   MANTISSA BUFFER POINTER
GREP     EQU   9                   EXPONENT BUFFER POINTER
GR2      EQU   2                   USED TO EVALUATE A CHARACTER
GRCB     EQU   3                   BLANKCOUNTER
GRDP     EQU   10                  DECIMAL POINT POINTER
FR1      EQU   1                   FLOATING-POINT REGISTER FOR STORAGE
GRDP1    EQU   11                  EXPONENT LESS EIGHT
POWTEN   EQU   15                  ADDRESS OF POWERTENTABLE LESS EIGHT
         SPACE 3
*        DATA SET FLAGS
DS0      EQU   X'80'
DS2      EQU   X'20'                                               0206
DS6      EQU   X'02'                                               0206
DS7      EQU   X'01'
DS9      EQU   X'40'                                               0206
         SPACE 3
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
         SPACE 3                                                   0226
         ENTRY IHIIDEIR
         ENTRY IHIIDEII
         ENTRY IHIIDEAI
IHIIDEAI STM   14,12,12(SAVE)      REGISTERS STORED IN INARRAY SAVEAR
         USING IHIIDEAI,ENTRY
         ST    SAVE,SAVREG+4       SAVREG=SAVE AREA THIS ROUTINE
         LA    BASE,SAVREG         BASE=SAVE=ADDRESS OF SAVREG
         USING SAVREG,BASE
         STC   GRFKT,FKT           STORE FLAG BYTE
         B     LADDR+4
         SPACE 3
IHIIDEII STM   14,12,12(SAVE)      REGISTERS IN FIXED STORAGE AREA
         LR    FSA,SAVE
         DROP  BASE
         USING IHIIDEII,ENTRY
         ST    SAVE,SAVREG+4       SAVREG=SAVE AREA THIS ROUTINE
         LA    BASE,SAVREG         BASE=SAVE=ADDRESS OF SAVREG
         USING SAVREG,BASE
         MVI   FKT,X'04'           FLAG BYTE: ININTEGER
         B     SAVREG+72
         SPACE 3
IHIIDEIR STM   14,12,12(SAVE)      REGISTERS IN FIXED STORAGE AREA
         LR    FSA,SAVE
         DROP  BASE
         USING IHIIDEIR,ENTRY
         MVI   FKT,X'00'           FLAG BYTE: INREAL
         ST    SAVE,SAVREG+4       SAVREG=SAVE AREA THIS ROUTINE
         LA    BASE,SAVREG         BASE=SAVE=ADDRESS OF SAVREG
         USING SAVREG,BASE
         B     SAVREG+72
         SPACE 2
SAVREG   DS    18F
         SPACE 3
*        EVALUATE DATA SET NUMBER (EVDSN)
         SPACE
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 3
LADDR    L     GRA,4(0,PARAM)      DESTINATION ADDRESS
         USING DSTABLE,DSN
         NI    DSF+1,X'DF'         DS10=0                          0221
         TM    Q,X'FF'             IS DATA SET SECTIONED           0206
         BZ    *+8                                                 0206
         B     ERROR2              YES  INCOMPATIBLE ACTION        0206
         LR    GRR,DSNR                                            0206
         BCT   GRR,*+8             IS DATA SET NO=1                0206
         B     ERROR2              YES  INCOMPATIBLE ACTION        0206
         SPACE 3
*        TEST IF DATA SET IS OPEN
         SPACE
         TM    DSF,DS0
         BO    *+16                                                0226
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         B     SCAN-6              DCB IS CLOSED
         TM    DSF,DS2             WAS LAST I/O OUTPUT             0206
         BZ    *+8                                                 0206
         B     ERROR3              INPUT BEYOND LAST OUTPUT        0206
         TM    DSF,DS6                                             0206
         BZ    SCAN                                                0206
         TM    DSF,DS7                                             0206
         BO    ERROR5              END OF DATA REACHED             0206
         B     ERROR2                                              0221
         NI    DSF,X'FD'           DS6.=0
         BALR  RETURN,ENTRY
         SPACE 3
SCAN     L     GRR,R               CHARACTER POINTER
         LA    GRMP,MB+1           MANTISSA BUFFER POINTER
         LA    GREP,MB+19          EXPONENT BUFFER POINTER
         MVC   MB+1(20),MB         CLEAR BUFFER
         SPACE 2
SCAN1    BCTR  GRR,0               DECREASE CHARACTER POINTER      0206
         SPACE 2
SCAN2    MVI   SM,C'+'             MANTISSA SIGN INITIALLY PLUS
         MVI   F,X'00'             CLEAR FLAG BYTE F
         MVI   SE,C'+'             EXPONENT SIGN INITIALLY PLUS
         SPACE 2
SCAN3    LA    GRR,1(0,GRR)
         C     GRR,RE
         BNE   SCAN5               R IS NOT EQUAL RECORD END(RE)
         SPACE 2
SCAN4    TM    F,X'82'             RECORD END IS REACHED (R=RE)
         BC    5,DELIMIT           A VALID NUMBER HAS BEEN READ
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY        RECORD CHANGE IS PERFORMED
         L     GRR,R
         TM    DSF,DS7                                             0206
         BO    ERROR5              END OF DATA REACHED             0206
         B     SCAN1               NEXT RECORD IS TO BE SCANNED
         SPACE 2
SCAN5    SR    GR2,GR2
         TRT   0(256,GRR),IPTAB    EVALUATE NEXT CHARACTER
         SR    GRCB,GRCB           CLEAR BLANKCOUNTER (CB)
         B     BRANCH-4(2)
         SPACE 3
BRANCH   B     BLANK
         B     OTHERS
         B     DIGIT
         B     SIGN
         B     DECPT
         B     APOSTR
         SPACE 3
BLANK    LA    GRCB,1(0,GRCB)      INCREASE CB
         LA    GRR,1(0,GRR)        INCREASE R
         C     GRR,RE
         BE    SCAN4               RECORD END IS REACHED
         CLI   0(GRR),C' '
         BE    BLANK               NEXT CHARACTER ALSO BLANK
         SR    GR2,GR2
         IC    GR2,K
         CR    GRCB,GR2
         BL    SCAN5               LESS K BLANKS: EVAL.NEXT CHARACTER
         BCTR  GRR,0               K BLANKS READ: DECREASE R
         SPACE 2
OTHERS   TM    F,X'82'
         BZ    SCAN2               NO VALID NUMBER READ: NEW SCAN
         B     DELIMIT             A VALID NUMBER AND K BLANKS READ
         SPACE 2
DIGIT    TM    F,X'08'
         BO    DIGIT1              EXPONENT DIGIT
         TM    F,X'40'             MANTISSA DIGIT
         BZ    DIGIT2              FIRST MANTISSA DIGIT
         CR    GRMP,GREP
         BL    DIGIT3              LESS 19 MANTISSA DIGITS
         B     *+10
         SPACE
DIGIT3   MVC   0(1,GRMP),0(GRR)    DIGIT INTO MANTISSA BUFFER (MB)
         LA    GRMP,1(0,GRMP)      INCREASE MANTISSA POINTER
         TM    F,X'20'
         BZ    SCAN3               NO DEC POINT IS READ: NEXT CHARACTER
         OI    F,X'10'             F3.=1 DIGIT BEHIND DEC POINT READ
         B     SCAN3
         SPACE
DIGIT2   CLI   0(GRR),C'0'
         BE    *+12
         OI    F,X'C0'             F0.=F1.=1 MANTISSA DIGIT NOT 0
         B     DIGIT3
         OI    F,X'80'             F0.=1 LEADING ZERO
         TM    F,X'20'
         BZ    SCAN3
         BCTR  GRDP,0              DECREASE DECIMAL POINT POINTER
         OI    F,X'10'             F3.=1 DIGIT BEH. DEC POINT READ
         B     SCAN3
         SPACE
DIGIT1   TM    F,X'01'
         BZ    DIGIT4              NO EXP DIGIT NOT 0 WAS READ
         LA    GR2,MB+21           END OF EXPONENT BUFFER
         CR    GREP,GR2
         BNL   ERROR6              MORE THAN 2 EXP DIGITS WERE READ
         SPACE
DIGIT5   MVC   0(1,GREP),0(GRR)    DIGIT INTO EXPONENT BUFFER
         LA    GREP,1(0,GREP)      INCREASE EXPONENT POINTER
         B     SCAN3               EVALUATE NEXT CHARACTER
         SPACE
DIGIT4   OI    F,X'02'             F6.=1 ANY EXPONENT DIGIT READ
         CLI   0(GRR),C'0'
         BE    SCAN3               EVALUATE NEXT CHARACTER
         OI    F,X'01'             F7.=1 EXPONENT DIGIT NOT 0 READ
         B     DIGIT5
         SPACE 2
SIGN     TM    F,X'08'
         BO    SIGN1               SIGN FOLLOWED AN APOSTROPHE
         TM    F,X'80'
         BO    DELIMIT             SIGN FOLLOWED A MANTISSA DIGIT
         B     SIGN2
         SPACE
SIGN1    TM    F,X'06'
         BZ    SIGN2+10            EXPONENT SIGN
         TM    F,X'82'
         BC    5,DELIMIT           SIGN FOLLOWED A VALID NUMBER
         SPACE
SIGN2    MVC   SM(1),0(GRR)        MANTISSA SIGN
         B     SCAN2+4             NEW SCAN
         MVC   SE(1),0(GRR)        EXPONENT SIGN
         OI    F,X'04'             F5.=1 EXPONENT SIGN READ
         B     SCAN3               EVALUATE NEXT CHARACTER
         SPACE 2
DECPT    TM    F,X'28'
         BZ    DECPT1              NO DEC POINT OR APOSTROPHE BEFORE
         TM    F,X'82'
         BC    5,DELIMIT           DEC POINT FOLLOWED A VALID NUMBER
         TM    F,X'08'
         BO    *+12                DEC POINT FOLLOWED APOSTROPHE
         MVI   SM,C'+'             MANTISSA SIGN INITIALLY PLUS
         B     SCAN3               NEW SCAN
         MVC   SM(1),SE            EXP SIGN IS ASSIGNED TO MANTSIGN
         MVI   SE,C'+'             EXPONENT SIGN INITIALLY PLUS
         MVI   F,X'00'             CLEAR FLAG BYTE F
         SPACE
DECPT1   OI    F,X'20'             F2.=1 DEC POINT WAS READ
         LR    GRDP,GRMP           STATE OF MANT POINTER INTO GRDP
         ST    GRR,DPI             STATE OF CHAR POINTER INTO DPI
         B     SCAN3               NEXT CHARACTER
         SPACE 2
APOSTR   TM    F,X'08'
         BO    *+12                AN APOSTROPHE WAS ALREADY READ
         TM    F,X'30'
         BC    9,APOSTR1           A VALID NUMBER WAS READ
         TM    F,X'82'             APOSTROPHE FOLLOWED DEC POINT
         BC    5,DELIMIT           NUMBER BEFORE DEC POINT IS VALID
         MVC   SM(1),SE            EXP SIGN IS ASSIGNED TO MANT SIGN
         MVI   SE,C'+'             EXPONENT SIGN INITIALLY PLUS
         MVI   F,X'00'             CLEAR FLAG BYTE F
         SPACE
APOSTR1  OI    F,X'08'             APOSTROPHE WAS READ F4.=1
         ST    GRR,API             STATE OF CHAR POINTER INTO API
         B     SCAN3               EVALUATE NEXT CHARACTER
         SPACE 2
DELIMIT  TM    F,X'30'
         BC    9,DELIMIT1          MANTISSA PART IS VALID
         L     GRR,DPI             CHAR POINTER OF DEC POINT INTO R
         NI    F,X'DF'             F2.=0 NO DEC POINT WAS READ
         B     DELIMIT2
         SPACE
DELIMIT1 TM    F,X'0A'
         BC    9,DELIMIT2          EXPONENT PART IS VALID
         L     GRR,API             CHAR POINTER OF APOSTROPHE INTO R
         NI    F,X'F3'
         SPACE
DELIMIT2 C     GRR,RE
         BNE   *+18                                                0226
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY        RECORD CHANGE IS PERFORMED
         B     DELIMIT3
         LA    GRR,1(0,GRR)        INCREASE CHARACTER POINTER
         C     GRR,RE
         BE    DELIMIT2+8
         ST    GRR,R               STORE CHAR POINTER TO DSTABLE
DELIMIT3 TM    F,X'28'             TEST TYPE OF NUMBER
         BC    5,TRREAL            REAL TYPE NUMBER
         TM    F,X'40'
         BO    *+10                NUMBER IS NOT EQUAL ZERO
         SR    GR0,GR0
         B     TRINT1              NUMBER IS EQUAL ZERO
         LA    GR2,MB+11(0)
         CR    GRMP,GR2
         BH    TRREAL              MORE THAN 10 DIGITS READ: REAL
         BL    *+14                LESS THAN 10 DIGITS READ: INTEGER
         CLC   MB+1(10),DMINT      10 DIGITS WERE READ
         BNL   TRREAL              NUMBER GREATER (2**31)-1: REAL
         LA    GR2,MB+2
         SR    GRMP,GR2            NUMBER OF DIGITS DIMINSHED BY ONE
         EX    GRMP,PACK           PACK NUMBER
         CLI   SM,C'+'             TEST SIGN OF THE NUMBER
         BNE   *+12
         OI    MB+17,X'0F'
         B     *+12
         OI    MB+17,X'0D'
         NI    MB+17,X'FD'
         CVB   GR0,MB+10
         SPACE 3
TRINT1   LA    GR2,4
         CLI   FKT,X'04'
         BE    FIN3                CONVERSION NOT NECESSARY:FKT=INTEGER
         SPACE 3
*        CALL CONVERSION ROUTINE (LOADED IN FSA)
         SPACE
         STM   14,13,8(SAVE)       ALL REGISTERS INTO SAVREG
         LR    GR2,SAVE            ADDRESS OF SAVREG TO REG2
         LA    7,ACNVIRD(FSA)
         LR    14,GR0              INTEGER INTO REG.14
         LR    13,FSA              ADDRESS OF FSA TO REG.13
         BALR  8,7
         LM    14,13,8(GR2)        ALL REGISTERS RESET
         SR    GR2,GR2
         USING FSAAREA,FSA
         TM    OPTSW(FSA),X'20'    TEST IF LONG OR SHORT PRECISION
         BZ    FIN3                LONG PRECISION STATED
         LA    GR2,8
         B     FIN3
         SPACE 3
TRREAL   SWR   FR0,FR0
         TM    F,X'C0'
         BO    *+18
         BM    FIN                 MANTISSA IS ZERO
         SR    GRDP,GRDP
         LD    FR0,ONEFL           MANTISSA IS 1.0
         B     TRREAL1
         TM    F,X'20'
         BO    *+6                 A DECIMAL WAS READ
         LR    GRDP,GRMP           DECPT IS ACCEPTED BEH LAST DIGIT
         LA    GR2,MB+10
         SR    GRDP,GR2            POSITION OF DECPT: BEHIND 9. DIGIT
         SPACE 2
TRREAL2  PACK  MB+2(8),MB+1(9)     PACK UPPER 9 DIGITS
         OI    MB+9,X'0F'
         CVB   GR0,MB+2            CONVERT UPPER 9 DIGITS TO BINARY
         ST    GR0,MASK+4          MASK: FLOATING ZERO WITH EXPON 78
         AD    FR0,MASK            TRANSFORM NUMBER TO FLOATING TYPE
         CR    GRMP,GR2
         BNH   TRREAL1
         MD    FR0,TPNINE
         S     GRDP,NINE           SHIFT DECPT 9 POSITIONS TO RIGHT
         SR    GRMP,GRMP
         PACK  MB+2(8),MB+10(9)    PACK LOWER 9 DIGITS
         B     TRREAL2+6           REPEAT ACTION FOR LOWER 9 DIGITS
         SPACE 2
TRREAL1  CLI   SM,C'+'
         BE    *+6                 NUMBER IS POSITIV
         LCDR  FR0,FR0             INSERT MINUS
         SR    GR0,GR0
         TM    F,X'01'
         BZ    TRREAL3             SCALEFACTOR
         NI    MB+19,X'0F'         CLEAR ZONE OF ONE EXPONENT DIGIT
         IC    GR0,MB+19
         TM    MB+20,X'FF'         2. EXPONENT DIGIT IS TESTED
         BZ    TRREAL3-10          NO 2. EXPONENT
         MH    GR0,TEN             FIRST EXPONENT DIGIT MULTIPLIED 10
         LR    GR2,GR0
         NI    MB+20,X'0F'         CLEAR ZONE OF 2. EXPONENT DIGIT
         IC    GR0,MB+20
         AR    GR0,GR2             TOTAL PRELIMINARY EXPONENT
         CLI   SE,C'+'
         BE    *+6                 EXPONENT IS POSITIV
         LCR   GR0,GR0             INSERT MINUS
         SPACE 2
TRREAL3  AR    GRDP,GR0            CORR.EXP BY DEC POINT POSITION
         BZ    FIN                 CORRECTED EXPONENT IS ZERO
         L     POWTEN,APTTAB       ADDRESS OF POWERTENTABLE
         BP    *+8
         LA    POWTEN,128(POWTEN)  ADDRESS OF 10**(-1) DIMIN BY EIGHT
         LPR   GRDP,GRDP           ABS(CORRECTED EXPONENT)
         SR    GRDP1,GRDP1         CLEAR REGISTER
         SRDA  GRDP,3
         SLA   GRDP,3
         SPACE 2
TRREAL5  BZ    TRREAL4+4           CORRECTED EXPONENT LESS 8
         C     GRDP,SEVTWO
         BNH   TRREAL4             CORRECTED EXPONENT LESS 73
         MD    FR0,128(0,POWTEN)   MULTIPLY OR DIVIDE BY 10**72
         S     GRDP,SEVTWO         DECREASE CORRECTED EXP BY 72
         B     TRREAL5             REPEAT MULTIPLICATION OR DIVISION
         SPACE 2
TRREAL4  MD    FR0,56(GRDP,POWTEN)
         SRL   GRDP1,26
         LTR   GRDP1,GRDP1
         BZ    FIN
         MD    FR0,0(GRDP1,POWTEN) * OR / 10**(EXP-(ENTIER(EXP/8)*8))
         SPACE 3
FIN      SR    GR2,GR2             REG2=TYPE: REALLONG
         TM    OPTSW(FSA),X'20'    TEST IF LONG OR SHORT PRECISION
         BZ    FIN1                LONG PRECISION STATED
         STD   FR0,BUFF            SHORT PRECISION STATED
         MVC   ROUND(1),BUFF       EXPONENT TO ROUND
         AD    FR0,ROUND           LONG PRECISION ROUNDED TO SHORT
         LA    GR2,8               REG2=TYPE: REALSHORT
         SPACE 2
FIN1     CLI   FKT,X'00'
         BE    FIN3                CONVERSION NOT NECESSARY
         LA    GR2,4               REG2=TYPE: INTEGER
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,CI(ENTRY)                                     0226
         BALR  RETURN,ENTRY        NUMBER CONVERTED TO INTEGER
         SPACE 2
FIN3     EX    0,STORE(GR2)
         L     SAVE,SAVREG+4
         LM    14,12,12(SAVE)
         BR    RETURN
         SPACE 3
PACK     PACK  MB+10(8),MB+1(0)
         SPACE
STORE    STD   FR0,0(0,GRA)
         ST    GR0,0(0,GRA)
         STE   FR0,0(0,GRA)
         SPACE 3
ERROR2   LR    13,FSA                                              0206
         B     FSAERR+2*4(FSA)     INCOMP. ACTION ON DATA SET      0206
         SPACE 2                                                   0206
ERROR3   LR    13,FSA                                              0206
         B     FSAERR+3*4(FSA)     INPUT BEYOND LAST OUTPUT        0206
         SPACE 2                                                   0206
ERROR5   LR    13,FSA              INPUT REQUEST BEYOND END OF DATASET
         B     FSAERR+5*4(FSA)
         SPACE 2
ERROR6   LR    13,FSA              EXP PART CONSIST OF MORE TH.2DIGITS
         B     FSAERR+6*4(FSA)
         SPACE 3
*        EXTERNAL ADDRESSES
         SPACE
APTTAB   DC    V(IHIPTTAB)
ACNVIRD  EQU   X'120'
         SPACE 2
*        INTERNAL CONSTANTS AND STORAGE
         SPACE
NINE     DC    F'9'                FOR SHIFT OF DECIMAL POINT
SEVTWO   DC    F'72'               FOR EXPONENT TREATMENT
API      DS    F                   CHARACTER POINTER OF APOSTROPHE
DPI      DS    F                   CHARACTER POINTER OF DEC POINT
BUFF     DS    1D                  FOR CONVERSION OF NUMBER
ONEFL    DC    D'1.0'
TPNINE   DC    DE9'1'              10**9
MASK     DC    FL8S56'78'          FLOAT 0 WITH EXPONENT 78
ROUND    DC    X'0000000080000000' FOR CONVERSION OF NUMBER
TEN      DC    H'10'               FOR EXPONENT TREATMENT
FKT      DS    C                   FLAG BYTE
F        DC    X'00'               FLAG BYTE
SM       DS    C
SE       DS    C
MB       DC    X'00'               TO CLEAR MANTISSA BUFFER
         DS    20C                 MANTISSA BUFFER
DMINT    DC    C'2147483648'             2**31
         SPACE
IPTAB    DC    64X'08'             OTHERS
         DC    X'04'               BLANK
         DC    10X'08'             OTHERS
         DC    X'14'               DECIMAL POINT
         DC    2X'08'              OTHERS
         DC    X'10'               SIGN +
         DC    17X'08'             OTHERS
         DC    X'10'               SIGN -
         DC    28X'08'             OTHERS
         DC    X'18'               APOSTROPHE
         DC    114X'08'            OTHERS
         DC    10X'0C'             DIGITS 0 TO 9
         DC    6X'08'              OTHERS
         LTORG
         SPACE 3
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
         SPACE 3
FSAAREA  DSECT
         COPY  FSAREA
         END
