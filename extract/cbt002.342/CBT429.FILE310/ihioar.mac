    TITLE 'IHIOAR,OUTARRAY'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:TRANSFER NUMBERS FROM ARRAY INDICATED BY SECOND
*   ACTUAL PARAMETER TO OUTPUT BUFFER BY CALLING OUTREAL LONG OR
*   SHORT REPEATEDLY
*
*ENTRY POINT:IHIOARRY-FROM GENERATED OBJECT MODULE  LA 1,PARMLIST
*   DATA PASSED BY NAME                             BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER
*        IHISOR-OUTREAL SHORT
*        IHILOR-OUTREAL LONG
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*     -ERROR :N/A
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
IHIOARRY CSECT
*0019                                                              5608
*D016450-016600,A01645-016700                                    A39570
         SPACE 3
ENTRY    EQU   15                       ENTRY ADDRESS
RETURN   EQU   14                       RETURN ADDRESS
SAVE     EQU   13                       ADDRESS OF SAVEAREA IN FSA
PARAM    EQU   1                        ADDRESS OF PARAMETERLIST
         SPACE 3
*
*  OTHER GENERAL REGISTERS
*
DSN      EQU   5                        RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                        DATA SET NUMBER
BASE     EQU   8                        BASE REGISTER
CONV     EQU   3                        CONVERSION FLAG
GRA      EQU   7                        REGISTER FOR TRANSFER DESTADDR
GRB      EQU   2                        SCRATCH REGISTER
RLSH     EQU   4                        HANDLING LONG OR SHORT PRECS.
FSA      EQU   12                       ADDRESS OF FSA DURING EXECUTION
         SPACE 3
         SAVE  (14,12)
         LR    BASE,ENTRY               IN FSA
         USING IHIOARRY,BASE
         LR    FSA,SAVE                 FSA=ADDRESS OF FSA
         LA    SAVE,72(0,SAVE)          ADDRESS OF 2:D SAVEAREA IN FSA
*
         SR    CONV,CONV
*                                  EVALUATE DATA SET NUMBER
*
         L     ENTRY,AEVDSN
         BALR  RETURN,ENTRY
         SPACE 2
*
*                                  EVALUATE SOURCE ADDRESS
*
         L     PARAM,4(0,PARAM)
         LTR   PARAM,PARAM              TEST 2:D PARAMETER
         BC    10,OTARY1                BRANCH GREATER OR EQUAL 0
         O     CONV,=X'80000000'
         N     PARAM,=X'00FFFFFF'
OTARY1   L     GRB,12(0,PARAM)          ADDRESS OF DESTEND+1 TO GRB
         L     GRA,8(0,PARAM)           ADDRESS OF STARTDEST TO GRA
         AR    GRA,CONV
         AR    GRB,CONV
*  CALL ROUTINE OUREAL LONG OR SHORT
*
*
         LH    RLSH,=H'4'               SHORT
         TM    OPTSW(FSA),X'20'         TEST LONG OR SHORT PRECISION
         BO    *+8                      SHORT
         LH    RLSH,=H'8'               LONG
OTARY2   L     ENTRY,ARROUTR(RLSH)
         BALR  RETURN,ENTRY
         TM    OPTSW(FSA),X'20'        LONG OR SHORT             A39570
         BO    OTARY3                  SHORT                     A39570
         LTR   GRA,GRA                 LONG                      A39570
         BP    OTARY3                                            A39570
         SH    GRA,=H'4'                                         A39570
OTARY3   AR    GRA,RLSH                INCREASE DEST ADDR        A39570
         CR    GRA,GRB
         BL    OTARY2                   DESTEND NOT REACHED
         SPACE 2
*
         LR    SAVE,FSA
         RETURN (14,12)
         SPACE 3
*
*  EXTERNAL ADDRESSES
*
AEVDSN   DC    V(IHIIOREV)
ARROUTR  DS    A
         DC    V(IHISORAR)
         DC    V(IHILORAR)
         LTORG
FAS      DSECT
         COPY  FSAREA
         END
