 TITLE 'IHILSQ, SQUARE ROOT ROUTINE, LONG PRECISION'
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION: WRITE X = M*16**(2P-Q), M MANTISSA, Q=0 OR 1,    *
*   THEN SQRT(X) = SQRT(M)*(4**-Q)*(16**P)                            *
*                                                                     *
*ENTRY POINT:                                                         *
*        IHILSQ - SQRT FUNCTION, LONG              LA  1,PARMLIST     *
*                                                  BALR 14,15         *
*                                                  DATA PASSED BY NAME*
*   THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE.           *
*                                                                     *
*INPUT: N/A                                                           *
*                                                                     *
*OUTPUT: N/A                                                          *
*                                                                     *
*EXTERNAL ROUTINES: N/A                                               *
*                                                                     *
*EXIT-NORMAL: RETURN BY MEANS OF   BR  14.  RESULT IN FPR0.           *
*                                                                     *
*EXIT-ERROR: IF ARGUMENT NEGATIVE GO TO ERROR ROUTINE VIA             *
*        B     FSAERR+23*4(13)                                        *
*                                                                     *
*TABLES/WORKAREAS: N/A                                                *
*                                                                     *
*ATTRIBUTES: THE MODULE IS SERIALLY REUSABLE.                         *
*                                                                     *
*NOTES: CHARACTER CODE DEPENDENCE                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL       *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
       SPACE
IHILSQRT CSECT
       ENTRY   IHILSQ
       SPACE
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
         EJECT
       DC      CL7'IHILSQ'             ENTRY-POINT IDENTIFIER
       DC      X'06'
       USING   *,GRL
IHILSQ STM     GRR,GRL,12(GRS)         SAVE REGISTERS
       L       GR1,0(GRA)
       LD      FR0,0(GR1)      OBTAIN ARGUMENT
       LTDR    FR4,FR0
       BC      8,FIN           IF ARG=0, ANS=0.   RETURN
       BC      4,ERROR         IF NEGATIVE ARG, ERROR
       STE     FR0,BUFF        CONSTRUCT  Y = M*16**P AS FOLLOWS
       SR      GR0,GR0
       IC      GR0,BUFF          P+64 = INT PART OF (CHAR+65)/2
       AH      GR0,BIAS
       SRDL    GR0,1             P+64 IN GR0.   GR1 IS - IF EVEN CHAR
       STC     GR0,BUFF        Y IS READY IN BUFF
       STC     GR0,B           CONSTRUCT (2/9)*16**P IN B
       LE      FR0,BUFF        COMPUTE ((2/9+(8/9)*M)*16**P
       ME      FR0,A
       AE      FR0,B
       LTR     GR1,GR1
       BC      4,OK            IF EVEN CHAR, 1ST APPROX IS READY
       HER     FR0,FR0         IF ODD CHAR, DIVIDE BY 4 TO OBTAIN
       HER     FR0,FR0           1ST APPROXIMATION
       SPACE
OK     LER     FR2,FR4         NEWTON RAPHSON ITERATIONS
       DER     FR2,FR0            Y(N+1) = (Y(N)+ARG/Y(N))/2
       AER     FR0,FR2           2 PASSES IN SHORT FORM
       HER     FR0,FR0
       LER     FR2,FR4
       DER     FR2,FR0
       AER     FR0,FR2
       HER     FR0,FR0
       LDR     FR2,FR4           2 PASSES IN LONG FORM
       DDR     FR2,FR0
       HDR     FR2,FR2
       HDR     FR0,FR0
       ADR     FR0,FR2
       DDR     FR4,FR0
       SWR     FR4,FR0
       HDR     FR4,FR4
       ADR     FR0,FR4
FIN    MVI     12(GRS),X'FF'   RETURN
       BCR     15,GRR
ERROR    B     FSAERR+23*4(13)     NEGATIVE PARAMETER
       SPACE
BUFF   DS      F
FSAERR   EQU   X'1CC'
A      DC      X'40E38E39'
B      DC      X'4038E38E'
       DS      0H
BIAS   DC      X'0041'
       END
