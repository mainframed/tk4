 TITLE 'IHIIBO,INBOOLEAN'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:SCAN INPUT BUFFER UNTIL A BOOLEAN VALUE IS FOUND
*   TRANSFER IF 'TRUE' 1 IF 'FALSE' 0  TO BOOLEAN IDENTIFIER,SECOND
*   ACTUAL PARAMETER
*
*ENTRY POINTS:
*        IHIIBOOL-FROM GENERATED OBJECT MODULE    LA 1,PARMLIST
*   DATA PASSED BY NAME                           BALR 14,15
*
*        IHIIBOAR-FROM ARRAY MODULE IHIIBA        L 2, A(DESTINATION)
*   DATA PASSED BY NAME                           BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR -EVALUATE DATA SET NUMBER-OPEN DATA SET-
*   CHANGE TO NEXT INPUT RECORD
*
*EXITS-NORMAL:RELOAD REGISTERS AND BR14
*     -ERROR :INPUT REQUEST BEYOND END OF DATA SET NO.5
*   BRANCH TO IHIFSA         L 13,IHIFSA
*                  B  FSAERR+XX*4(13)    XX ERROR NO.
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*NOTES:LINKING TO IHIIBOAR DEVIATE FROM STANDARD
*   CHARACTER DEPENDENCE:
*   THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT
*   TO THE ONE USED AT ASSEMBLY TIME
         SPACE
         SPACE
IHIIBOOL CSECT
*2200023800,024000,048000-048400                                   0221
*2200016400,017000,018400,022800,027400,046600,050200-051400       0226
         SPACE
ENTRY    EQU   15                  ADRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADDRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA  IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
*
FSA      EQU   12                       ADRESS OF FIXED STORAGE AREA
         SPACE
         SPACE
*
*  OTHER GENERAL REGISTERS
*
DSN      EQU   5                   ADRESS OF RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER
BASE     EQU   7                   BASE REGISTER FOR THIS ROUTINE
GRA      EQU   2
GRE      EQU   3
GRK      EQU   4
GRR      EQU   8
CB       EQU   9
MP       EQU   10
EMB      EQU   11
KD       EQU   1
         SPACE
         SPACE
         SPACE
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
         SPACE
         SPACE
*
*
*  DATA SET FLAGS (DSF AND DSF+1 IN DSTAB)
DS0      EQU   X'80'
DS1      EQU   X'40'
DS2      EQU   X'20'
DS3      EQU   X'10'
DS4      EQU   X'08'
DS5      EQU   X'04'
DS6      EQU   X'02'
DS7      EQU   X'01'
DS8      EQU   X'80'
DS9      EQU   X'40'
DS10     EQU   X'20'
         ENTRY IHIIBOAR
         SAVE  (14,12)
         LA    BASE,56(0,ENTRY)                                    0226
         LR    FSA,SAVE                 ADRESS OF FIXED STORAGE AREA
         USING SAVAR,BASE              INTO REGISTER FSA
         ST    SAVE,SAVAR+4
         LA    SAVE,SAVAR
*  EVALUATE DATA SET NUMBER
*
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     INBOOL1
         SPACE
         SPACE
IHIIBOAR STM   14,12,12(SAVE)          ENTRY POINT FOR CALLING INBARRY
         LA    BASE,24(0,ENTRY)                                    0206
         USING SAVAR,BASE
         ST    SAVE,SAVAR+4
         LA    SAVE,SAVAR
         USING DSTABLE,DSN
         ST    GRA,ADEST                STORE DESTINATION ADRESS
         B     INBOOLOP
SAVAR    DS    18F                      SAVE REGISTER
         SPACE
INBOOL1  L     PARAM,4(0,PARAM)
         ST    PARAM,ADEST
INBOOLOP NI    DSF+1,X'DF'         DS10=0                          0221
         TM    Q,X'FF'                                             0221
         BZ    *+8                                                 0206
         B     ERROR2              INCOMPATIBLE ACTION ON DATA SET 0206
         LR    GRR,DSNR            IS DATA SET NR = 1              0206
         BCT   GRR,*+8                                             0206
         B     ERROR2              INCOMPATIBLE ACTION ON DATA SET 0206
         TM    DSF,DS0             IS DATA SET OPEN                0206
         BO    INBOOL2                  DATA SET IS OPEN
         NI    DSF,X'FD'                DS6 IS SET TO 0
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         BALR  RETURN,ENTRY             CALL FOR OPEN
         B     INBOOL3
INBOOL2  TM    DSF,DS2             WAS LAST I/O OUTPUT             0206
         BO    ERROR3              INPUT BEYOND LAST OUTPUT        0206
         TM    DSF,DS6           IS D.S. OPEN FOR OUTPUT OR E.O.D. 0206
         BZ    INBOOL3                                             0206
         TM    DSF,DS7             IS E.O.D. REACHED               0206
         BO    ERROR5                                              0206
         B     ERROR2                                              0221
INBOOL3  L     GRR,R                    CHARACTER POINTER IN REGISTER
         BCTR  GRR,0                    GRR
         ST    GRR,R
INBOOL4  MVI   F,X'00'                  CLEAR FLAGBYTE
INBOOL5  LA    MP,MB                    MANTISSA BUFFER POINTER
         SR    CB,CB                    BLANK COUNTER CLEARED
         TM    DSF,DS7
         BO    ERROR5                   INPUT REQUEST BEYOND END OF DAT
         SPACE
INBOOL6  L     GRE,RE
         BCTR  GRE,0
         L     GRR,R
         CR    GRR,GRE
         BNE   INBOOL7
         SPACE
         SPACE
         L     ENTRY,IORLST(FSA)      RECORD END IS REACHED        0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY             CALL FOR ROUTINE NEXTREC
         B     INBOOL3
         SPACE
         SPACE
INBOOL7  LA    GRR,1(0,GRR)             INCREASE R BY ONE
         ST    GRR,R
         CLI   0(GRR),C''''
         BE    INBOOL9                  CHARACTER JUST READ IS APOSTR
         SPACE
         SPACE
         TM    F,X'FF'
         BZ    INBOOL6                  NO APOSTR READ  NEXT CHARACTER
         SPACE
         SPACE
         SPACE
         CLI   0(GRR),C' '              APOSTR READ ALREADY
         BNE   INBOOL8                  CHARACTER IS NOT BLANK
         LA    CB,1(0,CB)               CHARACTER IS BLANK INCR COUNTER
         SR    GRK,GRK
         IC    GRK,K                    TEST IF BLANK DELIMITERS DENOTE
         CR    GRK,CB                   BY K IS ALREADY FOUND
         BE    INBOOL4                  NEW SCAN LOOP K DELIMT FOUND
         BNE   INBOOL6                  K DELIMITERS NOT FOUND
         SPACE
         SPACE
INBOOL8  SR    CB,CB               CLEAR BLANK COUNTER
         LA    EMB,MB+5            END OF BUFFER ADDRESS
         CR    MP,EMB
         BNL   INBOOL4                  NO VALID BOOLEAN VALUE FOUND   X
                                        NEW SCAN LOOP
         SPACE
         SPACE
         MVC   0(1,MP),0(GRR)           INSERT FOUND CHAR TO BUFFER
         LA    MP,1(0,MP)               INCREASE MP BY ONE
         B     INBOOL6                  EXAMINE NEXT CHARACTER
         SPACE
         SPACE
         SPACE
INBOOL9  SR    CB,CB
         TM    F,X'FF'
         BC    5,INBOOL10               APOSTR ALREADY FOUND
         OI    F,X'01'                  FIRST APOSTR  SET FLAG F
         B     INBOOL6
         SPACE
         SPACE
INBOOL10 LA    EMB,MB+4                 TEST IF FOUR CHARACTERCS READ
         CR    MP,EMB
         BL    INBOOL5
         BH    INBOOL11
         CLC   MB(4),ATRUE
         BNE   INBOOL5                  NO VALID BOOLEAN VALUE FOUND
         L     GRA,ADEST                BOOLEAN VALUE TRUE FOUND
         MVI   0(GRA),X'01'             CHARACTER TO DESTINATION
         B     INBOOL12
         SPACE
         SPACE
         SPACE
INBOOL11 CLC   MB(5),AFALSE
         BNE   INBOOL5                  NO VALID BOOLEAN VALUE FOUND
         L     GRA,ADEST                BOOLEAN VALUE FALSE FOUND
         MVI   0(GRA),X'00'             CHARACTER TO DESTINATION
         SPACE
         SPACE
         SPACE
*
*  RECOGNITION OF THE LAST DELIMITING CHARACTER
*
*
INBOOL12 SR    CB,CB
         SR    KD,KD
INBOOL13 LA    GRR,1(0,GRR)             INCREASE CHARACTER POINTER
         C     GRR,RE
         BNL   INBOOLNX                 NEXT RECORD
         CLI   0(GRR),C' '
         BNE   INBOOL14                 CHARACTER IS NOT BLANK
         LA    CB,1(0,CB)               CHARACTER IS BLANK  INCREASE CB
         SR    GRK,GRK                  TEST IF BLANK DELIMITERS DENOTE
         IC    GRK,K                    BY K IS ALREADY FOUND
         CR    GRK,CB
         BNE   INBOOL13                 NOT K BLANK DELIMITERS READ
         LA    KD,1(0,KD)               K BLANK DELIMITERS READ
         B     INBOOL13
         SPACE
         SPACE
INBOOL14 LTR   KD,KD                    CHARACTER NOT EQUAL BLANK FOUND
         BP    *+8                      K OR MORE BLANKS FOUND
         LA    GRR,1(0,GRR)             LESS THAN K BLANKS FOUND
         C     GRR,RE
         BE    INBOOLNX                 RECORD END REACHED
         ST    GRR,R                    STORE R TO DSTAB
INBOOLEX L     SAVE,SAVAR+4             RELOAD REGISTER
         LM    14,12,12(SAVE)
         BR    RETURN
         SPACE
         SPACE
INBOOLNX L     ENTRY,IORLST(FSA)      CALL FOR ROUTINE NEXTREC     0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     INBOOLEX
         SPACE
         SPACE
ERROR2   LR    13,FSA                                              0206
         B     FSAERR+2*4(FSA)     INCOMP. ACTION ON DATA SET      0206
         SPACE 2                                                   0206
ERROR3   LR    13,FSA                                              0206
         B     FSAERR+3*4(FSA)     INPUT BEYOND LAST OUTPUT        0206
         SPACE 2                                                   0206
ERROR5   LR    13,FSA              INPUT REQUEST BEYOND END OF
         B     FSAERR+5*4(FSA)     DATA SET
         SPACE
         SPACE
         SPACE
F        DS    C                        FLAG BYTE FOR  APOSTROPHE
MB       DS    6C                       BUFFER
ADEST    DS    A                        DESTINATION ADRESS
ATRUE    DC    C'TRUE'                  BOOLEAN VALUE TRUE
AFALSE   DC    C'FALSE'                 BOOLEAN VALUE FALSE
         LTORG
*
*
*
*
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
         SPACE 3                                                   0226
FAS      DSECT                                                     0226
         COPY  FSAREA                                              0226
         END
