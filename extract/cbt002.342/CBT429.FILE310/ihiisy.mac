 TITLE 'IHIISY,INSYMBOL'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:COMPARE ONE CHARACTER FROM AN INPUT BUFFER WITH
*   AN INTERNAL STRING AND ASSIGN WHEN CORRESPONDENCE POSITION NUMBER
*   IN STRING TO THIRD ACTUAL PARAMETER
*
*ENTRY POINT:
*        IHIISYMB - FROM GENERATED OBJECT MODULE  LA  1,PARMLIST
*   DATA PASSED BY NAME                           BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR - EVALUATE DATA SET NUMBER - OPEN DATA SET -
*   CHANGE TO NEXT INPUT RECORD
*
*EXITS-NORMAL:RELOAD REGISTERS AND BR14
*    -ERROR :INPUT REQUEST BEYOND END OF DATA SET NO.5
*   BRANCH TO FSA; LA 13,IHIFSA
*                  B FSAERR+XX*4(13)      XX ERROR NO.
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT
*   TO THE ONE USED AT ASSEMBLY TIME
*
*
*
IHIISYMB CSECT
*2200017400                                                        0221
*2200012000,014000,016400,016600,024000,025000-025800              0226
         SPACE
ENTRY    EQU   15                  ADRESS OF ROUTINE INSYMBOL
RETURN   EQU   14                  RETURN ADRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
         SPACE 2
*        OTHER GENERAL REGISTERS
         SPACE
DSN      EQU   5                   RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER BINARY
BASE     EQU   7                   BASE REGISTER FOR THIS ROUTINE
DEST     EQU   3                   DESTINATION ADRESS
GRAS     EQU   4                   ADRESS OF STRING SYMBOL
GRES     EQU   9                   ADRESS OF END OF STRING
GRR      EQU   10                  CHARACTER POINTER
GRINCR   EQU   8                   INCREMENT FOR LOOP
FSA      EQU   12                  ADRESS OF FIXED STORAGE AREA
         SPACE 3
*        DATA SET FLAGS
DS0      EQU   X'80'
DS6      EQU   X'02'                                               0206
DS2      EQU   X'20'                                               0206
DS7      EQU   X'01'
DS10     EQU   X'20'
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
         SPACE 3
         STM   14,12,12(SAVE)
         LR    BASE,ENTRY
         USING IHIISYMB,BASE
         ST    SAVE,SAVEISYM+4     SAVEISYM=SAVE AREA
         LR    FSA,SAVE            ADRESS OF FIXED STORAGE AREA TO FSA
         LA    SAVE,SAVEISYM
         SPACE 2
*        EVALUATE DATA SET NUMBER (EVDSN)
         SPACE
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 2
         L     GRAS,4(0,PARAM)     ADRESS OF STRING
         L     DEST,8(0,PARAM)     ADRESS OF DESTINATION
         LA    GRINCR,1            INCREMENT
         SPACE 3
*        TEST  IF DATA SET IS OPEN
         SPACE
         USING DSTABLE,DSN
         NI    DSF+1,X'DF'         DS10=0                          0221
         TM    Q,X'FF'             IS DATA SET SECTIONED           0206
         BZ    *+8                                                 0206
         B     ERROR2              INCOMPATIBLE ACTION             0206
         LR    GRR,DSNR            IS DATA SET NR = 1              0206
         BCT   GRR,*+8                                             0206
         B     ERROR2              INCOMPATIBLE ACTION             0206
         TM    DSF,DS0
         BO    *+16                                                0226
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         B     EVSYMB-6            DCB IS CLOSED
         TM    DSF,DS2             WAS LAST I/O OUTPUT             0206
         BZ    *+8                                                 0206
         B     ERROR3              INPUT BEYOND LAST OUTPUT        0206
         TM    DSF,DS6             D.S.OPEN FOR OUTPUT OR E.O.D.   0206
         BZ    EVSYMB                                              0206
         TM    DSF,DS7             HAS END OF DATA BEEN REACHED    0206
         BO    ERROR5              INPUT RQUEST BEYOND END OF DATA 0206
         B     ERROR2                                              0221
         NI    DSF,X'FD'           DS6.=0
         BALR  RETURN,ENTRY
         SPACE 3
EVSYMB   MVC   HALFW(2),0(GRAS)    LENGTH OF STRING
         LH    GRES,HALFW
         BCTR  GRES,0
         AR    GRES,GRAS           STRING-END LESS 1
         LA    GRAS,2(0,GRAS)      STARTING POINT FOR STRING-SYMBOL
         L     GRR,R               CHARACTERPOINTER
         SPACE 3
SYMBLOOP CLC   0(1,GRR),0(GRAS)
         BE    TERMIN-10
         BXLE  GRAS,GRINCR,SYMBLOOP
         SR    GRAS,GRAS           ZERO INSERTED GRAS
         B     TERMIN-4
         SPACE 3
         S     GRAS,4(0,PARAM)
         BCTR  GRAS,0
         ST    GRAS,0(DEST)        NUMBER OF SYMBOL INSERTED DESTNATION
         SPACE
TERMIN   LA    GRR,1(0,GRR)
         C     GRR,RE
         BNL   NEXTREC
         ST    GRR,R
         LR    SAVE,FSA
         LM    14,12,12(SAVE)
         BR    RETURN
         SPACE 3
NEXTREC  L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     NEXTREC-8
ERROR2   LR    13,FSA                                              0206
         B     FSAERR+2*4(FSA)     INCOMPATIBLE ACTION ON DATA SET 0206
ERROR3   LR    13,FSA                                              0206
         B     FSAERR+3*4(FSA)     INPUT BEYOND LAST OUTPUT        0206
ERROR5   LR    13,FSA                                              0206
         B     FSAERR+5*4(FSA)     INPUT REQUEST BEYOND END OF DATA0206
SAVEISYM DS 18F                    SAVE AREA
         SPACE 3
HALFW    DS    1H
         LTORG
         SPACE 3
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
         SPACE 3                                                   0226
FAS      DSECT                                                     0226
         COPY  FSAREA                                              0226
         END
