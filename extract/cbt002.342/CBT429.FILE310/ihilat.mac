 TITLE 'IHILAT, ATAN ROUTINE, LONG PRECISION'
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION: 1. REDUCE THE CASE TO THE 1ST OCTANT BY USING    *
*                       ATAN(-X)=-ATAN(X), ATAN(1/X)=PI/2-ATAN(X)     *
*                    2. REDUCE FURTHER TO THE CASE /X/ LESS THAN      *
*                       TAN(PI/2) BY                                  *
*                       ATAN(X)=PI/6+ATAN((X*SQRT3-1)/(X+SQRT3)       *
*                    3. FOR THE BASIC RANGE (X LESS THAN TAN(PI/12)), *
*                       USE A FRACTIONAL APPROXIMATION.               *
*                                                                     *
*ENTRY POINT:                                                         *
*        IHILAT - ATAN FUNCTION, LONG              LA  1,PARMLIST     *
*                                                  BALR 14,15         *
*                                                  DATA PASSED BY NAME*
*   THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE.           *
*                                                                     *
*INPUT: N/A                                                           *
*                                                                     *
*OUTPUT: N/A                                                          *
*                                                                     *
*EXTERNAL ROUTINES: N/A                                               *
*                                                                     *
*EXIT-NORMAL: RETURN BY MEANS OF   BR  14.  RESULT IN FPR0.           *
*                                                                     *
*EXIT-ERROR: N/A                                                      *
*                                                                     *
*TABLES/WORKAREAS: N/A                                                *
*                                                                     *
*ATTRIBUTES: THE MODULE IS SERIALLY REUSABLE.                         *
*                                                                     *
*NOTES: CHARACTER CODE DEPENDENCE                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL       *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
       SPACE
IHILATAN CSECT
       ENTRY   IHILAT
       SPACE
GR1    EQU     1
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GR0    EQU     0               SCRATCH REGISTERS
GRR    EQU     14              RETURN REGISTER
FR0    EQU     0               ANSWER REGISTER
FR2    EQU     2               SCRATCH REGISTERS
FR4    EQU     4
GRL    EQU     15              LINK REGISTER
FR6    EQU     6
         EJECT
       DC      CL7'IHILAT'             ENTRY-POINT IDENTIFIER
       DC      X'06'
       USING   *,GRL
IHILAT STM     GRR,GRL,12(GRS)         SAVE REGISTERS
       L       GR1,0(GRA)
       LD      FR0,0(GR1)      OBTAIN ARGUMENT
       STE     FR0,SIGN          SAVE ARG FOR SIGN CONTROL
       LPER    FR0,FR0             AND SET SIGN POSITIVE
       SR      GR1,GR1         GR1 FOR DISTINGUISHING CASES
       CE      FR0,ONE
       BC      4,SKIP1
       LD      FR2,ONE         IF X GREATER THAN 1, TAKE INVERSE
       DDR     FR2,FR0           AND INCREMEMENT GR1 BY 16
       LDR     FR0,FR2
       LA      GR1,16
       SPACE
SKIP1  CE      FR0,TAN15
       BC      12,SKIP2
       LDR     FR2,FR0         IF X GREATER THAN TAN(PI/12),
       MD      FR0,RT3M1         REDUCE X TO (X*SQRT3-1)/(X+SQRT3)
       SD      FR0,HALF
       SD      FR0,HALF          COMPUTE X*SQRT3-1 AS
       ADR     FR0,FR2             X*(SQRT3-1)-0.5-0.5+X
       AD      FR2,RT3               TO GAIN ACCURACY
       DDR     FR0,FR2
       LA      GR1,8(GR1)        INCREMENT GR1 BY 8
       SPACE
SKIP2  LDR     FR6,FR0         COMPUTE ATAN OF REDUCED ARGUMENT BY
       MDR     FR0,FR0
       LDR     FR2,FR0           ATAN(X) = X(1+F*XSQ)
       AD      FR2,BETA4
       LD      FR4,ALPHA4         F = A1/(B1+XSQ+A2/(B2+XSQ+A3
       DDR     FR4,FR2                      /(B3+XSQ+A4/(B4+XSQ))..)
       ADR     FR4,FR0
       AD      FR4,BETA3
       LD      FR2,ALPHA3
       DDR     FR2,FR4
       ADR     FR2,FR0
       AD      FR2,BETA2
       LD      FR4,ALPHA2
       DDR     FR4,FR2
       ADR     FR4,FR0
       AD      FR4,BETA1
       LD      FR2,ALPHA1
       DDR     FR2,FR4
       MDR     FR0,FR2
       MDR     FR0,FR6
       ADR     FR0,FR6
       SPACE
       C       GR1,SXTEN       DEPENDING ON THE CASE EITHER ADD
       BC      4,*+10            0 OR PI/6 OR SUBTRACT FROM PI/3 OR
       LCER    FR0,FR0             PI/2.   DO LATTER IN 2 STEPS
       AD      FR0,PO2M1-16(GR1)
       AD      FR0,ZERO(GR1)
       TM      SIGN,X'80'      IF SIGN WAS NEGATIVE,
       BC      8,*+6             ANSWER IS NEGATIVE
       LCER    FR0,FR0
       SPACE
       MVI     12(GRS),X'FF'   RETURN
       BCR     15,GRR
       SPACE
SIGN   DS      F
       DS      0D
RT3M1  DC      X'40BB67AE8584CAA8'      SQRT(3)-1
RT3    DC      X'411BB67AE8584CAB'      SQRT(3)
ALPHA1 DC      X'C0D5F788DF6CB457'       -0.8358083291502266
BETA1  DC      X'414D42F041242098'        4.828842405755528
ALPHA2 DC      X'C1DD6E91F2AD24DF'      -13.839494655565710
BETA2  DC      X'4168C2DCB9C0437F'        6.547573781576119
ALPHA3 DC      X'C1138256FCDD5CB6'       -1.219321239235610
BETA3  DC      X'41224D09A3EFF7AC'        2.143808021908152
ALPHA4 DC      X'C0145A9C5C07FB43'       -0.07950761076788829
BETA4  DC      X'4114451896975D03'        1.266869152304765
PO2M1  DC      X'40921FB54442D184'       90 DEGREES MINUS ONE
       DC      X'408C152382D73658'       60 DEGREES MINUS HALF
ZERO   DC      D'0'                     0
       DC      X'40860A91C16B9B2D'      30 DEGREES
ONE    DC      X'4110000000000000'
HALF   DC      X'4080000000000000'
TAN15  DC      X'40449851'
SXTEN  DC      F'16'
       END
