 TITLE 'IHIIOR'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:THIS MODULE CONTAINS A SET OF SERVICE ROUTINES
*   USED BY OTHER I/O MODULES AS SUBROUTINES. THEY PERFORM:
*   OPEN DATA SET - CHANGE TO NEXT RECORD - CLOSE DATA SET - CLOSE ALL
*   DATA SETS - CLEAR NOTTAB - ENTRY NOTTAB - EVALUATE DATA SET NUMBER
*   - END OF DATA HANDLING - SYNCHRONOUS ERROR HANDLING - CONVERT REAL
*   TO INTEGER .MORE DETAILED BEFORE EACH ROUTINE
*
*ENTRY POINTS:
*        IHIIOROP
*        IHIIOROQ
*        IHIIORNX
*        IHIIORCL
*        IHIIORCP
*        IHIIORCN
*        IHIIOREN
*        IHIIOREV
*        IHIIORCI
*        IHIIORED
*        IHIIORER
*   CALLED  BY BALR 14,15  DIFFERENCE EXPLAINED BEFORE EACH ROUTINE
*
*INPUT:SEE EACH ROUTINE
*
*OUTPUT:SEE EACH ROUTINE
*
*EXTERNAL ROUTINES:IHIGPR-CLOSE DATA SET FOR PUT/GET
*
*EXITS-NORMAL:ALL ROUTINES EXCEPT END OF DATA AND SYNAD RELOAD
*   REGISTERS AND BR14
*     -ERROR :DATA SET NUMBER OUT OF RANGE           NO.0
*   REAL NUMBER TO BE CONVERTED OUT OF INTEGER RANGE NO.1
*   INCOMPATIBLE ACTIONS ON SAME DATA SET            NO.2
*   INPUT BEYOND LAST OUTPUT                         NO.3
*   OVERFLOW OF NOTTAB                               NO.4
*   INPUT REQUEST BEYOND END OF DATA SET             NO.5
*   DATA SECTIONED AND NO CTRL-CHARACTER SPECIFIED   NO.7
*   UNRECOVERABLE I/O ERROR                          NO.32
*   BLOCKSIZE NOT A MULTIPLE OF RECORD LENGTH        NO.37
*   DD-CARD INCORRECT OR MISSING                     NO.41
*   ACTION BRANCH TO IHGFSA   LA 13,IHGFSA
*                              B  FSAERR+XX*4(13)  XX ERROR NO.
*
*
*TABLES/WORK AREAS:NOTTAB,FOR STORING OF RECORD IDENTIFICATION
*   USING WHEN REPOSITIONING,IS CREATED DYNAMICALLY WHEN OPEN A DATA
*   SET WITH UNBLOCKED RECORD FORMAT  SIZE 1024 BYTES
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
*
*
IHIIORTN CSECT
* RELEASE 19 CHANGES
*                                                                A22569
*
*
ENTRY    EQU   15             ADDRESS OF ROUTINE
RETURN   EQU   14             RETURN ADDRESS
SAVE     EQU   13             ADDRESS OF SAVE AREA
FSA      EQU   12             ADDRESS OF FSA
DSN      EQU   5              ADDRESS OF THE RELEVANT ENTRY IN DSTAB
DSNR     EQU   6              DATA SET NUMBER
*
*  OTHER GENERAL REGISTERS
*
BASE     EQU   7              PROGRAM BASE REGISTER
DCB      EQU   8              BASE REGISTER FOR DCB AND DECB'S
GR0      EQU   0
GR1      EQU   1
GRA      EQU   2
GRB      EQU   3
*
*  DATA SET FLAGS (DSF AND DSF+1 IN DSTAB)
DS0      EQU   X'80'
DS1      EQU   X'40'
DS6      EQU   X'02'
DS9      EQU   X'40'
DS10     EQU   X'20'
DS11     EQU   X'10'
DS14     EQU   X'02'                                               0221
DS15     EQU   X'01'                                               0219
DS3      EQU   X'10'
DS4      EQU   X'08'
DS5      EQU   X'04'
DS7      EQU   X'01'
DS2      EQU   X'20'
DS8      EQU   X'80'
* DISPLACEMENT IN FSA
*
ADSTAB   EQU   X'AC'
ANOTTAB  EQU   X'B0'
OPTSW    EQU   X'C2'
FSAERR   EQU   X'1CC'
         ENTRY IHIIOROP
         ENTRY IHIIOROQ
         EJECT
*
*
*
*   OPEN A DATA SET
*
*FUNCTION/OPERATION:RESERVE STORAGE FOR AND COMPLETE A DATA CONTROL
*   BLOCK AND FOR TWO  I/O BUFFERS COMPLETE DSTAB IN GENERATED OBJECT
*   MODULEDSTAB HAS AN ENTRY AND INDICATE STATUS FOR EVERY DATA SET
*   USED IN ONE JOB
*
*INPUT:IF DATA SET SHOULD PERFORM ONLY INPUT OR OUTPUT BLOCKED RECORD
*   FORMAT IS USED  ELSE  UNBLOCKED FORMAT ;ACCESS METHOD BSAM.
*   IN CASE OF INPUT TWO RECORDS OR BLOCKS    READ TO I/O BUFFERS
* BLOCKED RECORD FORMAT IS USED ONLY WHEN BLOCKING FACTOR > 1      0206
*
*OUTPUT:N/A
*
*NOTES:ADDRESS OF THE DATA CONTROL BLOCK IS LOADED IN REG8 AND KEEPT
*   TROUGH ALL I/O MODULES IN ORDER TO ADDRESS DCB
*
         SPACE 3
* THIS ROUTINE IHIIOROQ IS ENTERED FROM SYSACT 12                  0221
IHIIOROQ SAVE  (14,12)
         LR    4,ENTRY                                             0221
         USING IHIIOROQ,4                                          0221
         LA    BASE,IHIIOROP
         USING IHIIOROP,BASE
         ST    SAVE,SAVAR+4
         USING DSTABLE,DSN              DSN RELEVANT ENTRY IN DSTAB
         LA    SAVE,SAVAR
         CH    DSNR,=H'1'        IS DATA SET NUMBER 0 OR 1         0221
         BC    12,OPEN00         YES BRANCH TO IHIIOROP            0221
         NI    DSF,X'FD'                                           0221
         TM    DSF+1,DS14        HAS DATA SET BEEN OPEN BEFORE     0221
         BO    OPEN00             YES                              0221
* OPEN DATA SET FOR THE FIRST TIME.                                0221
* EXAMINE THE DISP PARAMETER IN JFCB                               0221
* IF NEW OPEN THE DATA SET FOR OUTIN OTHERWISE FOR INOUT           0221
         GETMAIN R,LV=288        GET AREA FOR DCB,DECB AND JFCB    0221
         ST    GR1,ADCB                                            0221
         LR    DCB,GR1                                             0221
         USING IHADCB,DCB
         MVC   0(104,DCB),DCBMODEL                                 0221
         CVD   DSNR,DBWORD                                         0221
         UNPK  DBWORD+2(2),DBWORD+6(2)                             0221
         L     GRA,DBWORD                                          0221
         STH   GRA,DCBDDNAM+6                                      0221
         OI    DCBDDNAM+7,X'F0'                                    0221
         MVC   DCBMACR(2),=X'2424'                                 0221
         LA    GRB,JFCB                                            0221
         ST    GRB,ADCBEXIT+4                                      0221
         MVI   ADCBEXIT+4,X'87'                                    0221
         RDJFCB ((DCB))                                            0221
         TM    JFCB+87,X'80'                                       0221
         BZ    OQEN1                                               0221
         OI    DSF,DS6                                             0221
OQEN1    LA    GR1,JFCB                                            0221
         FREEMAIN R,LV=176,A=(1)                                   0221
         B     OPEN20                                              0221
*
         SPACE 3
*
IHIIOROP SAVE  (14,12)
         LR    BASE,ENTRY
         ST    SAVE,SAVAR+4
         LA    SAVE,SAVAR
OPEN00   CH    DSNR,=H'1'                                          0219
         BNE   OPEN01                                              0219
* DATA SET NUMBER = 1                                              0219
* IF DATA SET HAS BEEN OPENED BEFORE (DS14=1)                      0219
* SET DS0=1 AND GO BACK . IF NOT OPEN THE DATA SET                 0219
         TM    DSF+1,DS14                                          0219
         BZ    OPEN01                                              0219
         OI    DSF,DS0                                             0219
         B     OPEN51                                              0219
OPEN01   GETMAIN R,LV=112        GET AREA FOR DCB AND DECB         0221
         ST    GR1,ADCB
         LR    DCB,GR1
         MVC   0(104,DCB),DCBMODEL
         CH    DSNR,=H'1'
         BL    DSIN
         BE    DSPRINT
         CVD   DSNR,DBWORD         DATA SET NUMBER TO
         L     GRA,DBWORD+4               DDNAME
         SRL   GRA,4
         STC   GRA,DCBDDNAM+7
         OI    DCBDDNAM+7,X'F0'
         SRL   GRA,4
DSLOW    STC   GRA,DCBDDNAM+6
         OI    DCBDDNAM+6,X'F0'
         B     OPEN2                                               0206
DSIN     MVC   DCBDDNAM(8),=C'SYSIN   '                            0206
         MVC   DCBMACR(2),=X'2400'                                 0206
         B     OPEN3                                               0206
DSPRINT  TM    DSF+1,DS11                                          0219
* IF DS11=0 OPEN DATA SET SYSPRINT                                 0219
* IF DS11=1 OPEN DATA SET ALGLDD01                                 0219
         BO    DSPR2                                               0219
         MVC   DCBDDNAM+6(2),=X'F0F1'                              0219
         OI    DSF+1,DS14                                          0219
         B     DSPR2+6                                             0219
DSPR2    MVC   DCBDDNAM(8),=C'SYSPRINT'                            0219
         MVC   DCBMACR(2),=X'0024'                                 0206
         B     OPEN3                                               0206
OPEN2    MVC   DCBMACR(2),=X'2424'      MACRF=(RP,WP)
OPEN20   EQU   *                                                   0221
         L     GRA,ANOTTAB(FSA)
         LTR   GRA,GRA
         BP    OPEN3
         GETMAIN R,LV=1024    GET AREA FOR NOTE TABLE
         ST    1,ANOTTAB(FSA)
         LR    GRA,GR1                 ANOTTAB TO GRA
         LR    GRB,GRA                 ANOTTAB TO GRB
         LA    GRB,8(0,GRB)
         ST    GRB,0(0,GRA)            STORE POINTER NXE IN NOTTAB
         LA    GRB,1016(0,GRB)
          ST    GRB,4(0,GRA)           STORE POINTER NEXEF IN NOTTAB
OPEN3    TM    DSF,DS6                  OUTPUT POSSIBLE            1728
         BO    OPEN30                   YES                        1728
         OPEN  ((DCB),(INOUT))                                     1728
OPEN300  EQU   *                        CONTINUE AFTER OPEN        1728
         TM    DCBOFLGS,X'10'          TEST DDCARD
         BO    OPEN355                 DD-CARD CORRECT
         NI    DSF+1,X'FD'              DS14=0                     0219
         LR    13,FSA                   DDCARD INCORRECT OR MISSING
         B     FSAERR+41*4(FSA)
OPEN30   OPEN  ((DCB),(OUTIN))                                     1728
         TM    DCBDEVT,X'40'            UNIT RECORD DEVICE         5875
         BZ    OPEN6                    NO                         5875
         MVC   DCBBLKSI(2),DCBLRECL     BLKSI=LRECL                5875
         MVC   BL(2),DCBLRECL                                      5875
OPEN6    B     OPEN300                  CONTINUE                   5875
OPEN355  TM    EXERFLAG,X'11'          TEST IF ERROR IN DCBEXIT
         BZ    OPEN301
*        CLOSE DATA SET AND FREEMAIN FOR DCB AND DECB
*
         CLOSE ((DCB),REREAD)
         FREEMAIN R,LV=112,A=ADCB                                  0221
         LR    13,FSA
         TM    EXERFLAG,X'01'           TEST IF ERROR 7 IN DCBEXIT
         MVI   EXERFLAG,X'00'           CLEAR FLAG
         BZ    OPEN350
         B     FSAERR+7*4(FSA)          ERROR7
OPEN350  B     FSAERR+37*4(FSA)        ERROR37
OPEN301  OI    DSF,DS0
         CH    DSNR,=H'1'        IS DATA SET NUMBER = 0 OR 1       0221
         BNH   *+8                                                 0221
         OI    BL+3,DS14                                           0221
         LH    GR0,BL
         SLA   GR0,1          DOUBLE BUFFER LENGTH
         GETMAIN R,LV=(0)     GET AREA FOR TWO BUFFERS
         ST    GR1,BB         BUFFER BEGIN
         LR    GRA,GR1
         AH    GR1,BL
         ST    GR1,NBB        ALTERNATE BUFFER BEGIN
         TM    DSF,DS6
         BO    OPEN4
OPEN31   OI    DSF+1,X'08'             SET MARK FOR END OF DATA    0206
         LA    GRE,DECB
         L     GRB,8(0,GRE)
         CR    GRB,DCB
         BNE   OPEN311
         CHECK DECB
OPEN311  READ  DECB,SF,(DCB),(GRA),MF=E      READ FIRST BLOCK
         CHECK DECB
         NI    DSF+1,X'F7'              RESET
         LH    1,DCBBLKSI                                          0050
         L     GRE,DCBIOBA                                         0050
         SH    1,22(GRE)                                           0050
         STH   1,BL                                                0050
         NOTE  (DCB)
         ST    GR1,NOTEADR
OPEN41   L     GRB,NBB
         READ   DECB,SF,(DCB),(GRB),MF=E     READ SECOND BLOCK
         B     OPEN5                                               0206
OPEN4    TM    DSF+1,DS9
         BZ    OPEN5
         MVC   0(1,GRA),=C'1'          INSERT FIRST CONTROL CHAR.
         LA    GRA,1(0,GRA)   PROVIDE SPACE FOR CONTROL CHARACTER
OPEN5    ST    GRA,R
         AH    GRA,P
         ST    GRA,RE
OPEN51   L     SAVE,SAVAR+4                                        0219
         RETURN (14,12)
         EJECT
*        ROUTINE DCBEXIT
*
LREC     EQU   4
*
IHIIORDX TM    DSF+1,DS11
         BZ    EXIT3                                               0206
*
*  EXIT ROUTINE FOR PRINTING ERROR MESSAGE
*
         MVC   Q(1),=X'32'              RECORD IS SECTIONED  Q=50
         MVC   P(2),=X'005A'            RECORD LENGTH P=90
         MVC   DCBRECFM(1),=X'94'       INSERT RECORD FORMAT=FBA
         OI    DSF+1,DS9               INSERT FLAG REC CONT CONTR CHAR
         LH    LREC,P
         LA    LREC,1(0,LREC)           P+1 TO LRECL
         STH   LREC,DCBLRECL
*
*  BLKSI EXAMINE
*
         LH    GRB,DCBBLKSI             ENTIER(BLKSI/LRECL) TO REG GRB
         SR    GRA,GRA
         DR    GRA,LREC
         LTR   GRB,GRB
         BZ    EXITA                    BLKSI LESS LRECL  BLKSI=0
         BCT   GRB,EXITB                                           0206
EXITA    STH   LREC,DCBBLKSI                                       0206
         OI    DSF,DS1                                             0206
EXITC    MVC   BL(2),DCBBLKSI                                      0206
         B     RETEX                                               0206
EXITB    LA    GRB,1(GRB)                                          0206
         MH    GRB,DCBLRECL                                        0206
         STH   GRB,DCBBLKSI                                        0206
         NI    DSF,X'BF'                                           0206
         B     EXITC                                               0206
*
* ALGOL USER'S EXIT ROUTINE
*
*
EXIT0    TM    DCBRECFM,X'FF'
         BZ    EXIT1                   RECFM=0
         TM    DCBRECFM,X'94'
         BO    EXIT4                                               0206
         TM    DCBRECFM,X'84'
         BO    EXIT4                                               0206
         TM    Q,X'FF'
         BZ    RETEX                                               0206
         OI    EXERFLAG,X'01'           DATA SET SPLIT INTO SECTIONS
         B     RETEX                    AND NO CTLCHARACTER  ERROR NO.7
EXIT1    OI    DCBRECFM,X'80'                                      0206
         TM    DSF,DS1                                             0206
         BO    EXIT12                                              0206
         OI    DCBRECFM,X'10'                                      0206
EXIT12   TM    DSF+1,DS9                                           0206
         BZ    RETEX                                               0206
         OI    DCBRECFM,X'04'                                      0206
         B     RETEX                                               0206
EXIT4    OI    DSF+1,DS9                                           0206
         B     RETEX                                               0206
*      LRECL  EXAMINE
EXIT3    OI    DSF,DS1                                             0206
         TM    Q,X'FF'                                             0206
         BZ    *+8                                                 0206
         OI    DSF+1,DS9                                           0206
         LH    LREC,DCBLRECL                                       0206
         LTR   LREC,LREC
         BZ    EXIT2
         TM    DSF+1,DS9
         BO    EXIT5               DS9=1  RECORDS CONTAIN CONTROL CHAR
         STH   LREC,P              LRECL  TO  P
         B     EXIT6
EXIT5    BCTR  LREC,0
         STH   LREC,P              LRECL-1 TO P
         LA    LREC,1(0,LREC)
         B     EXIT6
EXIT2    TM    DSF+1,DS9
         BO    EXIT7               DS9=1
         LH    LREC,P
         STH   LREC,DCBLRECL       P TO LRECL
         B     EXIT6
EXIT7    LH    LREC,P
         LA    LREC,1(0,LREC)      P+1 TO LRECL
         STH   LREC,DCBLRECL
*
*     BLKSI  EXAMINE
*
EXIT6    LH    GRA,DCBBLKSI
         LTR   GRA,GRA
         BZ    EXIT8               BLKSI=0
         CH    LREC,DCBBLKSI
         BE    EXIT61+4
         LH    GRB,DCBBLKSI
         SR    GRA,GRA
         DR    GRA,LREC
         LTR   GRA,GRA
         BZ    EXIT61
         OI    EXERFLAG,X'10'           BLOCKSIZE NOT A MULTIPLE OF
         B     RETEX                    LOGICAL RECORD LENGTH ERR NO.37
EXIT61   NI    DSF,X'BF'          0 TO DS1 UNBLOCKED FORMAT NESC.
         MVC   BL(2),DCBBLKSI      BLKSI TO BL
         B     EXIT0                                               0206
EXIT8    STH   LREC,DCBBLKSI
         STH   LREC,BL
         B     EXIT0                                               0206
RETEX    RETURN
         EJECT
*
*
*
*
*   NEXTREC CHANGE TO NEXT RECORD
*
*INPUT:IN CASE OF AN IN-MODULE CALLING NEXTREC,LAST I/O OPERATION IS
*   CHECKED FOR COMPLETION AND ONE BLOCK OR RECORD IS READ TO THE
*   OTHER I/O BUFFER
*
*OUTPUT:IN CASE OF AN OUT-MODULE CALLING NEXTREC,LAST I/O OPERATION IS
*   CHECKED FOR COMPLETION AND ONE BLOCK OR RECORD IS WRITTEN TO THE
*   DATA SET
*
*
*
*
*
*
*
*
*
GRE      EQU   4
         ENTRY IHIIORNX
IHIIORNX SAVE  (14,12)
         LR    BASE,ENTRY
         USING IHIIORNX,BASE
         ST    SAVE,SAVAR+4
         LA    SAVE,SAVAR
         SPACE
         L     DCB,ADCB
         SPACE
*
*        FLOW CHAR PROGRAM BEGIN
*
         TM    DSF,X'22'                                           0206
         BZ    NXIN1                                               0206
         BO    NXUT1                                               0206
*  DS6=0 DS2=1 CURRENT BLOCK WAS READ AND SHOULD BE WRITTEN BACK   0206
         CHECK DECB                                                0206
         MVC   BL(2),DCBBLKSI                                      0050
         LR    GR1,DCB                                             0206
         POINT (1),NOTEADR                                         0206
         OI    DSF,DS6                                             0206
* DS6=1  DS2=1   WRITE BLOCK IF LAST RECORD                        0206
NXUT1    TM    DSF,DS1                                             0206
         BZ    NXUT2                                               0206
*  CHANGE BUFFERS                                                  0206
NXUT3    L     GRA,BB                                              0206
         L     GRE,NBB                                             0206
         ST    GRE,BB                                              0206
         ST    GRA,NBB                                             0206
         ST    GRE,R                                               0206
         AH    GRE,P                                               0206
         ST    GRE,RE                                              0206
         NI    DSF,X'EF'                                           0206
NXUT4    L     GRE,DECB+8                                          0206
         CR    GRE,DCB                                             0206
         BNE   NXUT41                                              0206
         CHECK DECB                                                0206
NXUT41   WRITE DECB,SF,(DCB),(GRA),MF=E                            0206
*  CLEAR NOTTAB IF BACKWARD REPOSITIONING HAS OCCURED              0206
NXUT5    TM    DSF,DS5                                             0206
         BZ    NXUT6                                               0206
         L     ENTRY,ACLNOTB                                       0206
         BALR  RETURN,ENTRY                                        0206
         NI    DSF,X'FB'                                           0206
* INSERT NOTTAB ENTRY IF REQUESTED                                 0206
NXUT6    TM    DSF,DS4                                             0206
         BZ    NXUT7                                               0206
         CHECK DECB                                                0206
         NOTE  (DCB)                                               0206
         ST    GR1,NOTEADR                                         0206
         L     ENTRY,AENNOTB                                       0206
         BALR  RETURN,ENTRY                                        0206
         NI    DSF,X'F7'                                           0206
*  INSERT CONTROL CHARACTER IF SECTIONED                           0206
NXUT7    TM    DSF+1,DS9                                           0206
         BZ    NXRET                                               0206
         L     GRE,R                                               0206
         CLC   S+1(1),Q                                            0206
         BL    NXUT8                                               0206
         SR    GRA,GRA                                             0206
         STH   GRA,S                                               0206
         MVI   0(GRE),C'1'                                         0206
         B     NXUT9                                               0206
NXUT8    MVI   0(GRE),C' '                                         0206
NXUT9    LA    GRE,1(GRE)                                          0206
         ST    GRE,R                                               0206
         AH    GRE,P                                               0206
         ST    GRE,RE                                              0206
* INCREASE RECORD POINTER AND RETURN                               0206
NXRET    L     GRA,S                                               0206
         SPACE
         SRL   GRA,16                                              0206
         LA    GRA,1(GRA)                                          0206
         STH   GRA,S                                               0206
         L     SAVE,SAVAR+4                                        0206
         RETURN (14,12)                                            0206
*  BLOCKED FORMAT  CHECK IF LAST RECORD IND IF NOTTAB ENTRY REQ.   0206
NXUT2    L     GRA,BB                                              0206
         AH    GRA,BL                                              0206
         C     GRA,RE                                              0206
         BE    NXUT3                                               0206
         L     GRE,RE                                              0206
         ST    GRE,R                                               0206
         AH    GRE,P                                               0206
         ST    GRE,RE                                              0206
         TM    DSF,DS4                                             0206
         BZ    NXUT7                                               0206
         NI    DSF,X'FD'                                           0206
         L     GRA,BB                                              0206
         B     NXUT4                                               0206
*  DS6=0 DS2=0 CHECK IF NOTTAB ENTRY REQ. STORE ADDRESS OF LAST    0206
*  BLOCK IN NOTEADR AND READ NEXT BLOCK                            0206
NXIN1    TM    DSF,DS4                                             0206
         BZ    NXIN2                                               0206
         SPACE
         SPACE
         L     GR1,NOTEADR                                         0206
         L     ENTRY,AENNOTB                                       0206
         BALR  RETURN,ENTRY                                        0206
         NI    DSF,X'F7'                                           0206
NXIN2    TM    DSF,DS1                                             0206
         BZ    NXIN5                                               0206
NXIN3    CHECK DECB                                                0206
         TM    DSF,DS7                                             0222
         BZ    NXIN7                                               0222
         OI    DSF,DS6                                             0222
         MVC   BL(2),DCBBLKSI                                      0050
         B     NXIN6                                               0222
NXIN7    LH    1,DCBBLKSI                                          0050
         L     GRE,DCBIOBA                                         0050
         SH    1,22(GRE)                                           0050
         STH   1,BL                                                0050
         NOTE  (DCB)                                               0050
         ST    GR1,NOTEADR                                         0206
         L     GRA,BB                                              0206
         READ  DECB,SF,(DCB),(GRA),MF=E                            0206
*  CHANGE BUFFERS                                                  0206
NXIN6    L     GRA,BB                                              0206
         L     GRE,NBB                                             0206
         ST    GRE,BB                                              0206
         ST    GRE,R                                               0206
         AH    GRE,P                                               0206
         ST    GRE,RE                                              0206
         ST    GRA,NBB                                             0206
         B     NXRET                                               0206
*  BLOCKED FORMAT                                                  0206
NXIN5    L     GRA,BB                                              0206
         AH    GRA,BL                                              0206
         C     GRA,RE                                              0206
         BE    NXIN3                                               0206
         L     GRE,RE                                              0206
         ST    GRE,R                                               0206
         AH    GRE,P                                               0206
         ST    GRE,RE                                              0206
         B     NXRET                                               0206
         EJECT
*
*
*
*
*   CLOSE DATA SET
*
*FUNCTION/OPERATION:CLOSE A DATA SET,RELEASE STORAGE FOR I/O BUFFERS
*   AND DCB CALL FOR ROUTINE CLEAR NOTTAB
*
*OUTPUT:IN CASE OF OUTPUT WRITE LAST BLOCK TO DATA SET
*
*
*
*
         SPACE 2
GRNL     EQU   9
GRAA     EQU   10
         SPACE 2
*
         ENTRY IHIIORCL
IHIIORCL SAVE  (14,12)
         LR    BASE,ENTRY
         USING IHIIORCL,BASE
         ST    SAVE,SAVAR+4             SAVE REGISTER
         LA    SAVE,SAVAR
         L     DCB,ADCB
         CH    DSNR,=H'1'                                          0219
* DATA SET 1 IS TO BE CLOSED ONLY IF DS15=1.                       0219
* IF DS15=0 FILL CURRENT BLOCK WITH BLANKS AND BRANCH TO ROUTINE   0219
* IHIIORNX TO WRITE THE BLOCK AND RETURN                           0219
         BNE   CLOSE01                                             0219
         TM    DSF+1,DS15                                          0219
         BO    CLOSE01                                             0219
         OI    DSF,X'22'                                         A22569
         NI    DSF,X'7F'       DS0=0                               0219
         LH    GRA,BL                                              0219
         A     GRA,BB                                              0219
         L     GRB,R                                               0219
         ST    GRA,R                                               0219
         SR    GRA,GRB                                             0219
         BZ    CLOSE02                                             0219
CLOSE03  MVI   0(GRB),X'40'                                        0219
         LA    GRB,1(GRB)                                          0219
         BCT   GRA,CLOSE03                                         0219
CLOSE02  NI    S,X'00'                                             0219
         MVC   S+1(1),Q                                            0219
         L     BASE,ANXREC                                         0219
         B     14(BASE)                                            0219
CLOSE01  TM    DSF,DS7                                             0219
         BO    CLOSE1                  END OF DATA REACHED
         TM    DSF+1,X'04'             TEST IF I/O ERROR
         BO    CLOSE1                  CLOSE DCB
         SPACE
         LA    GRE,DECB                TEST IF READ OR WRITE BEFORE
         L     GRA,8(0,GRE)            PICK UP DCB ADDRESS
         CR    GRA,DCB
         BNE   CLOSE2
         OI    DSF+1,DS8               SET DS8=1 FOR END OF DATA
CLOSE0   CHECK DECB                    LAST I/O FINISHED
CLOSE2   TM    DSF,DS2             WAS LAST I/O OUTPUT             0213
         BZ    CLOSE1                                              0213
         TM    DSF,DS6             HAS BLOCK BEEN READ             0213
         BO    WRITE2                                              0213
         LR    GR1,DCB                  OUTPUT OCCUR WRITE BACK RECORD
         POINT (1),NOTEADR              NO BLANKS
         B     WRITE1                                              0213
WRITE2   TM    DSF,DS3             OUTPUT IN BLOCK                 0213
         BZ    CLOSE1              NO CLOSE                        0213
*
*        OUTPUT HAS OCCURED  FILL BUFFER WITH BLANKS AND WRITE
*
WRITE1   LH    GRA,BL
         L     GRB,R
         S     GRA,R
         A     GRA,BB
         BZ    CLOSE21
CLOSE22  MVI   0(GRB),X'40'
         LA    GRB,1(0,GRB)
         BCT   GRA,CLOSE22
*
*        WRITE BUFFER
*
CLOSE21  L     GRA,BB
CLOSE211 WRITE DECB,SF,(DCB),(GRA),MF=E
         CHECK DECB
*
*        CLOSE DATASET
*
CLOSE1   CLOSE ((DCB),REREAD)
*
         L     GR1,BB
         C     GR1,NBB
         BL    *+8                      THE LOWEST ADRESS TO GR1
         L     GR1,NBB
         LH    GR0,BL                   BUFFERLTH TO GR0
         SLA   GR0,1
*
*        FREEMAIN FOR RECORDBUFFERS
*
         FREEMAIN  R,LV=(0),A=(1)
*
*        FREEMAIN FOR DCB AND DECB
*
         FREEMAIN R,LV=112,A=ADCB                                  0221
*
*
         SR    GRA,GRA
         LA    GRA,1(0,GRA)
         STH   GRA,S
         LA    GRA,1(0,GRA)
         MVC   P(2),=X'0050'            P=80
         LTR   GRA,DSNR                                            0206
         BZ    CLOSE3                                              0206
         BCT   GRA,CLOSE4                                          0206
         B     CLOSE3                                              0206
CLOSE4   L     ENTRY,ACLNOTB                                       0206
         BALR  RETURN,ENTRY             CLEAR NOTTAB FOR DATA SET
CLOSE3   LH    GRA,BL+2                                            0206
         STH   GRA,DSF
         L     SAVE,SAVAR+4
         RETURN (14,12)
         EJECT
*
*
*
*
*   CLOSEPE CLOSE ALL DATA SETS
*
*FUNCTION/OPERATION:CALL ROUTINE CLOSE FOR ALL OPEN DATA SET
*   AND ROUTINE CLOSEGP IN  IHIGPR-MODULE
*
* THIS ROUTINE IS CALLED FROM IHIFSA AND IHIERR. IN BOTH CASES     0219
* REG DSN CONTAINS THE ADDRASS OF THE ENTRY IN DSTAB FOR DATA SET  0219
* ONE I.E. DSTAB+40                                                0219
*
*
*
LTH      EQU   36
         ENTRY IHIIORCP
         ENTRY IHIIORGP
IHIIORCP SAVE  (14,12)
         LR    BASE,ENTRY
         USING IHIIORCP,BASE            SAVE REGISTER
         ST    SAVE,SAVCLO+4
         LA    SAVE,SAVCLO
         SR    DSNR,DSNR
         SH    DSN,=H'40'               DSN TO DSTAB START         0219
         L     GRA,0(0,DSN)             TEST APGCF
         LTR   GRA,GRA
         BP    CLOSEPE4                 PUT/GET ENTRY IN DSTAB
         N     GRA,=X'00FFFFFF'
         LA    DSN,4(0,DSN)            FIRST ENTRY IN DSTAB
CLOSEPE2 CR    GRA,DSN
         BE    CLOSEPE3                 ALL DATA SETS ARE CLOSED
         NI    BL+3,X'FD'                                          0221
         CH    DSNR,=H'1'                                          0219
         BE    CLOSEPE5                                            0219
CLOSEPE7 TM    DSF,DS0                                             0219
         BZ    CLOSEPE1
*
*   CALL FOR ROUTINE CLOSE
CLOSEPE6 L     DCB,ADCB                                            0219
         L     ENTRY,ACLOSE
         BALR  RETURN,ENTRY
CLOSEPE1 LA    DSN,LTH(0,DSN)           NEXT ENTRY IN DSTAB
         LA    DSNR,1(0,DSNR)           INCREASE DATA SET NUMBER
         B     CLOSEPE2
CLOSEPE5 TM    DSF+1,DS14                                          0219
         BZ    CLOSEPE7                                            0219
         B     CLOSEPE6                                            0219
         SPACE 3
CLOSEPE4 TM    27(GRA),X'80'            TEST IF PUT/GET DATA SET OPEN
         BZ    CLOSEPE2-4               DATA SET CLOSED
         L     ENTRY,IHIIORGP
         BALR  RETURN,ENTRY             CLOSE PUT/GET DATA SET
         B     CLOSEPE2-4
*
*        ALL DATA SETS ARE CLOSED
*
*
*
CLOSEPE3 L     GR1,ANOTTAB(FSA)
         LTR   GR1,GR1
         BZ    RETCLOSP
         FREEMAIN R,LV=1024,A=(1)       NOTTAB FREE
RETCLOSP L     SAVE,SAVCLO+4
         RETURN (14,12)
*
*
SAVCLO   DS    18F
*
*        EXTERNAL ADRESS
*
*
ACLOSE   DC    A(IHIIORCL)
IHIIORGP DC    F'0'
         EJECT
*
*
*   CLEAR NOTTAB
*
*FUNCTION/OPERATION:ALL ENTRIES IN NOTTAB FOR RECORDS EQUAL OR GREATER
*   THAN ACTUAL RECORD COUNTERS ARE CLEARED BY INSERTING INVALID FLAG
*
*
*
NTE      EQU   4
NOT      EQU   9
         SPACE
         ENTRY IHIIORCN
IHIIORCN SAVE  (14,12)
         LR    BASE,ENTRY               DEFINE BASEREGISTER
         USING IHIIORCN,BASE
         L     NOT,ANOTTAB(FSA)
         LR    NTE,NOT
CLNOTB1  LA    NTE,8(0,NTE)             STARTENTRYADRESS TO NTE
         C     NTE,0(0,NOT)
         BE    RETCLEAR                 NOTTAB CLEARED
         LR    GRA,DSNR                 DATA SETNUMBER TO GRA
         CH    GRA,0(0,NTE)
         BNE   CLNOTB1                  CURRENT DSN NOT EQUAL
         CLC   S(2),2(NTE)
         BH    *+8
         MVI   0(NTE),X'80'        INSERT INVALID FLAG TO NOTTAB
         B     CLNOTB1
RETCLEAR RETURN (14,12)
         EJECT
*
*
*
*
*   ENTRY NOTTAB
*
*FUNCTION/OPERATION:AN ENTRY FOR RECORD JUST HANDLED IS MADE IN NOTTAB
*
*NOTES:ON ENTRY GR1=NOTEADR FROM DSTAB
*
*
*
         ENTRY IHIIOREN
IHIIOREN SAVE  (14,12)
         LR    BASE,ENTRY
         USING IHIIOREN,BASE
         L     NOT,ANOTTAB(FSA)
         LR    NTE,NOT
ENNOTB1  LA    NTE,8(0,NTE)            START ENTRY ADDRESS
         C     NTE,0(0,NOT)
         BNE   ENNOTB3
         L     GRA,0(0,NOT)             NXE TO GRA
         LA    GRA,8(0,GRA)             INCREASE NXE BY EIGHT
         ST    GRA,0(0,NOT)
         C     GRA,4(0,NOT)
         BE    ENNERR4                  OVERFLOW NOTTAB
ENNOTB2  LR    GRA,DSNR
         SLA   GRA,16
         ST    GRA,0(0,NTE)             NEW ENTRY  DSN AND S TO NOTTAB
         MVC   2(2,NTE),S
         ST    GR1,4(0,NTE)             NOTEADR
         RETURN (14,12)
ENNOTB3  TM    0(NTE),X'80'             TEST IF NOTTABENTRY IS INVALID
         BO    ENNOTB2
         B     ENNOTB1
ENNERR4  LR    13,FSA                   OVERFLOW OF RECORD IDENTIFICA-
         B     FSAERR+4*4(FSA)          TION AREA
         EJECT
*
*
*
*   EVALUATE DATA SET NUMBER
*
*FUNCTION/OPERATION:ADDRESS OF DSTAB IN GENERATED OBJECT MODUL
*   IS PICKED UP FROM FSA   L 4,ADSTAB(FSA),ACTUAL DATA SET NUMBER
*   LOADED IN BINARY FORM TO REG6,ADDRESS OF ACTUAL ENTRY IN DSTAB
*   TO REG5, THIS REGISTERS ARE KEEPT THROUGH ALL I/O MODULES IN ORDER
*   TO ADDRESS POINTERS AND FLAGS IN DSTAB
*
*ENTRY POINT:DATA IS PASSED VIA NAME   LA 1,PARMLIST
*                                      BALR 14,15
*
*
*
*
*
GRX      EQU   4
PARAM    EQU   1
FR0      EQU   0
         ENTRY IHIIOREV
IHIIOREV SAVE  (14,12)
         LR    BASE,ENTRY
         USING IHIIOREV,BASE
         ST    SAVE,SAVAR+4
         LA    SAVE,SAVAR
*
*        PARAMETER LIST ADRESS IN REGISTER PARAM
*
         L     DSN,0(0,PARAM)
         LTR   DSN,DSN
         BP    DSNINT
         TM    OPTSW(FSA),X'20'        TEST IF  LONG OR SHORT PRESC.
         BO    EVD1                    SHORT
         LD    FR0,0(0,DSN)            LONG
         B     EVD1+4
EVD1     LE    FR0,0(0,DSN)            SHORT
         L     ENTRY,ACNVRI
         BALR  RETURN,ENTRY
         B     *+8
DSNINT   L     GR0,0(0,DSN)             DATA SET NUMBER IN GR0
         LR    DSN,GR0
         N     GR0,RANGEDSN             DATA SET NUMBER OUT OF RANGE
         BM    EVDERR0
         LR    DSNR,DSN
         M     GRX,DSTABLTH
         L     GRX,ADSTAB(FSA)
*                                       ENTRY TO DSTAB IN REG DSN
         LA    DSN,4(DSN,GRX)
         SPACE
         L     SAVE,SAVAR+4
         LM    14,4,12(13)
         LM    7,12,48(13)              KEEP REGISTER DSN AND DSNR
         BR    RETURN
EVDERR0  LR    13,FSA                   DATA SET NUMBER OUT OF RANGE
         B     FSAERR(FSA)
*
*
*
DSTABLTH DC    F'36'                    LENGHT OF A DSTAB ENTRY
RANGEDSN DC    F'-16'                   SCOPE OUTSIDE RANGE OF DSNR
*
ACNVRI   DC    V(IHIIORCI)             ADDRESSS OF CONVERSION ROUTINE
         EJECT
*
*
*
*   END OF DATA
*
*FUNCTION/OPERATION:INVOKED VIA CHECK MACRO
*
*EXITS-NORMAL:CHECK FROM SYSACT4 OR CLOSE RELOAD REGISTERS AND RETURN
*   TO CALLING PROGRAM VIA BR14
*   CHECK FROM NEXTREC;BLOCKED FORMAT SET FLAG DS7 IN DSTAB AND BRANCH
*   TO END OF NEXTREC ;UNBLOCKED FORMAT OUTPUT RETURN TO NEXTREC TO
*   WRITE BACK CURRENT RECORD
*   ;UNBLOCKED FORMAT INPUT  SET FLAG DS7 IN DSTAB AND RETURN TO NEXT-
*   REC TO UPDATE POINTER IN DSTAB
*     -ERROR :CHECK FROM OPEN;INPUT REQUEST BEYOND END OF DATA SET
*
*
*
         ENTRY IHIIORED
IHIIORED SAVE  (14,12)
         BALR  BASE,0
         USING *,BASE
         ST    SAVE,SAVEOD+4
         LA    SAVE,SAVEOD
         L     DCB,ADCB
         TM    DSF+1,X'08'              TEST IF CHECK FROM OPEN
         BZ    END00                                               0206
         NI    DSF,X'CD'                SET FLAGS FOR A CORRECT CLOSE
         LR    13,FSA
         B     FSAERR+5*4(FSA)          INPUT REQUEST BEYOND END
END00    CLOSE ((DCB),LEAVE),TYPE=T
         TM    DSF+1,DS8                IS CHECK FROM CLOSE OR SYSACT4
         BZ    END02
END11    L     SAVE,SAVEOD+4
         LM    14,12,12(SAVE)
         BR    RETURN                   RETURN TO CALLING SYSACT4 OR   X
                                        CLOSE ROUTINE
         SPACE
END02    TM    DSF,DS2                                             0206
         BZ    END03                                               0206
         B     END11                                               0206
END03    OI    DSF,X'01'                                           0222
         MVI   DECB+8,X'FF'            FLAG DCB ADDRESS IN DECB
         L     SAVE,SAVEOD+4            RETURN TO CALLING NEXTREC
         LM    14,12,12(SAVE)                                      0222
         BR    RETURN                   RETURN TO CALLING NEXTREC
*
*        INTERNAL ADDRESSES
*
*
SAVEOD   DS    18F
         EJECT
*
*
*   CONVERSION TO INTEGER
*
*FUNCTION/OPERATION: CONVERT REAL LONG OR SHORT TO INTEGER
*
*
*NOTES: CALLED BY  BALR 14,15
*   DATA PASSED BY VALUE  IN FLOATING POINT REGISTER 0
*   RESULT IN GENERAL REGISTER 0
*
*
*
         SPACE 3
         ENTRY IHIIORCI
IHIIORCI TM    OPTSW(FSA),X'20'    TEST IF LONG OR SHORT PRECISION
         USING IHIIORCI,ENTRY
         BZ    LONG
         STE   FR0,BUFF3
         LD    FR0,BUFF3
         SPACE 3
LONG     AD    FR0,CONST2
         STD   FR0,BUFF4
         CE    FR0,CONST3
         BNL   ERROR1
         AW    FR0,CONST1
         BP    LABEL
         CLC   BUFF4(8),CONST4
         BH    ERROR1
LABEL    STD   FR0,BUFF2
         L     GR0,BUFF2+4
         X     GR0,CONST1+4
         SD    FR0,CONST1
         CD    FR0,BUFF4
         BCR   13,RETURN
         BCTR  GR0,RETURN
         SPACE 3
ERROR1   LR    13,FSA
         B     FSAERR+1*4(FSA)
         SPACE
         SPACE 2
*        INTERNAL CONSTANTS AND STORAGE
         SPACE
         CNOP  0,8
CONST1   DC    X'4E00000080000000'
CONST2   DC    X'4080000000000000'
BUFF2    DS    1D
BUFF3    DC    8X'0'
CONST4   DC    X'C880000000000000'
BUFF4    DS    1D
CONST3   DC    X'48800000'
         EJECT
*
*
*
*
*   SYNAD
*
*EXIT-NORMAL:BRANCH ERROR UNRECOVERABLE I/O ERROR
*
*
*
         ENTRY IHIIORER
IHIIORER LR    BASE,ENTRY
         USING IHIIORER,BASE
         OI    DSF+1,X'04'             SET MARK FOR CORRECT CLOSEPE
         LR    13,FSA
         B     FSAERR+32*4(FSA)         I/O ERROR
*
*
DCBMODEL DCB   DSORG=PS,MACRF=(RP,WP),DDNAME=ALGLDD,NCP=1,             X
               EODAD=IHIIORED,EXLST=ADCBEXIT,SYNAD=IHIIORER
ADCBEXIT DC    X'05'                                               0221
         DC    AL3(IHIIORDX)
         DC    X'87000000'                                         0221
DBWORD   DS    D              WORK AREA FOR DSN
SCOUNT   DC    F'0'
SAVAR    DS    18F
EXERFLAG DC    X'00'
NEXTERR2 NI    DSF,X'DD'               SET FLAGS FOR CLOSE;INPUT
         LR    13,FSA                   INCOMPATIBLE ACTIONS ON THE
         B     FSAERR+2*4(FSA)          SAME DATA SET
NEXTERR3 NI    DSF,X'CD'               SET FLAGS FOR ARIGHT CLOSEPE
         LR    13,FSA
         B     FSAERR+3*4(FSA)
         LTORG
*
*        INTERNAL ADDRESSES
*
ACLNOTB  DC    A(IHIIORCN)
AENNOTB  DC    A(IHIIOREN)
ANXREC   DC    A(IHIIORNX)                                         0219
*
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
*
*        PROVIDEING SYMBOLIC NAMES FOR DCB
*
         DCBD  DSORG=BS
*
*        PROVIDING ONE DECB
*
         READ DECB,SF,MF=L
JFCB     DS    22D                                                 0221
         EJECT
         END
