 TITLE 'IHILOR,OUTREAL LONG'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:CONVERT BINARY ARITHMETIC VALUE,FROM SECOND
*    ACTUAL PARAMETER,TO ZONED DECIMAL FORM AND TRANSFER TO AN OUTPUT
*    BUFFER
*
*ENTRY POINTS:
*        IHILOREL - FROM GENERATED OBJECT MODULE LA 1,PARMLIST
*   DATA PASSED BY NAME                          BALR 14,15
*
*        IHILORAR -FROM ARRAY MODULE IHIOAR      LA 7,DATA
*                                                BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR - EVALUATE DATA SET NUMBER -OPEN DATA SET-
*   CHANGE TO NEXT OUTPUT RECORD
*        IHIFSA - CNVIRD- CONVERT INTEGER TO REAL LONG
*        IHIPTT - POWER OF TEN TABLE LONG PREC
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*   -ERROR: TOO LONG RECORD                 NO.38
*   BRANCH TO IHIFSA         L 13,IHIFSA
*                            B FSAERR+XX*4(13)   XX ERROR NO.
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:LINKING TO IHILORAR DEVIATE FROM STANDARD
*   CHARACTER DEPENDENCE:
*   THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT
*   TO THE ONE USED AT ASSEMBLY TIME
*
IHILOREA CSECT
*0019013200,018800,024400,027000,048600,051600-052400,052800       0226
         SPACE
ENTRY    EQU   15                  ADDRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADDRESS
SAVE     EQU   13                  ADDRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADDRESS OF PARAMETER LIST
         SPACE 2
         SPACE
FSA      EQU   12                  ADDRESS OF FIXED STORAGE AREA
         SPACE 2
*        OTHER GENERAL REGISTERS
         SPACE
DSN      EQU   5                   RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER BINARY
BASE     EQU   13                  BASE REGISTER FOR THIS ROUTINE
GRA      EQU   7                   SOURCE ADDRESS
GRR      EQU   4                   CHARACTER POINTER
FR0      EQU   0                   FLOATINGPOINT NUMBER
GRK      EQU   8                   BLANKCOUNTER
GREXP    EQU   2                   EXPONENT GREATER EIGHT
GRE      EQU   3                   EXPONENT LESS EIGHT
GRDE     EQU   9                   DECIMAL EXPONENT
EXEC     EQU   8                   BLANKCOUNTER
POWTEN   EQU   15                  REGISTER FOR ADRESSING POWERTWNTABLE
         SPACE 3
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
*        DATA SET FLAGS
DS0      EQU   X'80'
DS6      EQU   X'02'
DS3      EQU   X'10'
         SPACE 3
         ENTRY IHILOREL
         ENTRY IHILORAR
IHILORAR STM   14,12,12(SAVE)      REGISTERS STORED IN OUTARRAY SAVEAR
         USING IHILORAR,ENTRY
         ST    SAVE,SAVREGOR+4     SAVEREGOR=SAVE AREA THIS ROUTINE
         LA    BASE,SAVREGOR       BASE=SAVE=ADDRESS OF SAVREGOR
         USING SAVREGOR,BASE
         B     SOUFLP+4
         SPACE 3
IHILOREL STM   14,12,12(SAVE)      REGISTERS IN FIXED STORAGE AREA
         LR    FSA,SAVE
         DROP  BASE
         USING IHILOREL,ENTRY
         ST    SAVE,SAVREGOR+4     SAVREGOR=SAVE AREA THIS ROUTINE
         LA    BASE,SAVREGOR       BASE=SAVE=ADDRESS OF SAVREGOR
         USING SAVREGOR,BASE
         B     SAVREGOR+72
         SPACE 2
SAVREGOR DS    18F
         SPACE 3
*        EVALUATE DATA SET NUMBER (EVDSN)
         SPACE
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 3
SOUFLP   L     GRA,4(0,PARAM)      ADDRESS OF SOURCE
         USING DSTABLE,DSN
         OI    DSF,X'30'           DS2.=DS3.=1
         NI    DSF,X'FE'           DS7.=0
         LTR   GRA,GRA
         BP    REAL1
         SPACE 3
*        CALL CONVERSION ROUTINE(LOADED IN FSA)
         SPACE
         STM   14,13,8(SAVE)       ALL REGISTERS INTO SAVREGOR
         LR    2,SAVE              ADDRESS OF SAVREGOR TO REG2
         L     14,0(0,GRA)         INTEGER INTO REG.14
         LA    7,ACNVIRD(FSA)
         LR    13,FSA              ADDRESS OF FSA TO REG. 13
         BALR  8,7
         LM    14,13,8(2)          ALL REGISTERS RESET
         B     *+8                 NUMBER IN FRO AFTER CONVERSION
         SPACE 3
REAL1    LD    FR0,0(0,GRA)        NUMBER IN FR0
         SPACE 2
*        TEST IF DATA SET IS OPEN
         SPACE
         TM    DSF,DS0
          BO    NOCLO              DCB IS OPEN
         OI    DSF,DS6             DS6.=1
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
          BALR RETURN,ENTRY
         SPACE 3
NOCLO    L     GRR,R               CHARACTER POINTER
         LA    EXEC,22(0,GRR)
         C     EXEC,RE
         BNH   NONEXREC            BUFFER CAN ACCEPT 22 DIGITS
         L     EXEC,RE
         SR    EXEC,GRR
         BC    12,*+16
BLANKS   MVI   0(GRR),C' '         BLANK IN BUFFER
         LA    GRR,1(0,GRR)        INCREASE R
         BCT   EXEC,BLANKS
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         L     GRR,R
         LA    EXEC,22(0,GRR)
         C     EXEC,RE
         BH    ORLERR                   TOO SHORT RECORD LENGTH
         OI    DSF,DS3
         SPACE 3
NONEXREC LA    GRDE,16
         LTDR  FR0,FR0
         BC    6,NOT0
         MVI   0(GRR),C' '         NUMBER IS ZERO
         MVC   1(21,GRR),0(GRR)
         MVI   1(GRR),C'0'
         B     TERMIN
         SPACE 3
NOT0     MVI   0(GRR),C'+'         ZONE IS INSERTED
         BP    *+10
         MVI   0(GRR),C'-'
         LCDR  FR0,FR0
         SPACE 3
EXPLOOP  STD   FR0,CHAR
         SR    GRE,GRE
         IC    GRE,CHAR
         MVI   SE,X'00'
         S     GRE,SEV8
         BP    *+14
         BZ    EXP0                EXPONENT.=78
         MVI   SE,X'80'
         LCR   GRE,GRE
         MH    GRE,LOG2
         AH    GRE,ROUND
         SRL   GRE,14
         C     GRE,SEV8
         BNH   *+8
         L     GRE,SEV8            EXPONENT.=78
         TM    SE,X'80'
         L     POWTEN,APTTAB       ADDRESS OF POWERTENTABLE
         BZ    *+10
         SR    GRDE,GRE
         B     *+10
         LA    POWTEN,128(0,POWTEN)
         AR    GRDE,GRE
         LR    GREXP,GRE
         SR    GRE,GRE             CLEAR REGISTER
         SRDA  GREXP,3
         SLA   GREXP,3
         SPACE 3
TESTEXP1 BZ    EXP1LS8
         C     GREXP,SEVTWO
         BL    EXP1LS8-4           EXPONENT NEGATIV
         MD    FR0,128(0,POWTEN)
         S     GREXP,SEVTWO
         B     TESTEXP1
         SPACE 3
         MD    FR0,56(GREXP,POWTEN)
EXP1LS8  SRL   GRE,26              EXPONENT MULTIPLIED EIGHT
         LTR   GRE,GRE
         BC    8,EXPLOOP
         MD    FR0,0(GRE,POWTEN)
         B     EXPLOOP
         SPACE 3
EXP0     CD    FR0,TENP16
         BNL   DIG17               NUMBER GE 10**16
         MVI   CHAR,X'00'
         LM    GREXP,GRE,CHAR
         D     GREXP,TENP9I
*   TRANSFORM NUMBER TO DECIMAL FORM AND EDIT OUTPUT NUMBER IN
*   I/O BUFFER
         CVD   GRE,BUFF            QUOTE CONVERTED
         UNPK  1(9,GRR),BUFF+3(5)
         OI    9(GRR),X'F0'
         CVD   GREXP,BUFF
         UNPK  10(9,GRR),BUFF+3(5)
         OI    18(GRR),X'F0'
          CLI  2(GRR),C'0'         LEADING ZERO
         BNE   *+12
         BCTR  GRDE,0
         MVC   2(16,GRR),3(GRR)
         MVC   1(1,GRR),2(GRR)
         MVI   2(GRR),C'.'         DECIMAL POINT INSERTED
         MVI   18(GRR),C''''       APOSTROPHE INSERTED
         SPACE 3
DECEXP   CVD   GRDE,BUFF
         UNPK  19(3,GRR),BUFF+6(2)
         LTR   GRDE,GRDE
         BC    10,*+12
         MVI   19(GRR),C'-'        EXPONENT SIGN IS NEGATIV
         B     *+8
         MVI   19(GRR),C'+'
         OI    21(GRR),X'F0'       ZONE INSERTED EXPONENT
         SPACE 3
*   TERMINATION ROUTINE INSERT BLANKS AND IF RECORD END CALL NEXTREC
TERMIN   LA    GRR,22(0,GRR)
         SR    GRK,GRK
         IC    GRK,K
         C     GRR,RE
         BE    RECEND
         MVI   0(GRR),C' '
         LA    GRR,1(0,GRR)
         BCT   GRK,TERMIN+10
         C     GRR,RE
         BE    RECEND
         ST    GRR,R
         L     SAVE,SAVREGOR+4
         LM    14,12,12(SAVE)
         BR    RETURN
         SPACE 3
RECEND   L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     RECEND-10
*   NUMBER GREATER OR EQUAL 10**16
         SPACE 3
DIG17    AW     FR0,FIVE
         STD   FR0,CHAR
         CD    FR0,TWOP56B
          BL   EXP0+8
          MVC  1(18,GRR),TWOP56    NUMBER GR 16**14
         B     DECEXP
*
ORLERR   LR    13,FSA
         B     FSAERR+38*4(FSA)
         SPACE 3
APTTAB   DC    V(IHIPTTAB)
ACNVIRD  EQU   X'120'
         SPACE 3
*        INTERNAL CONSTANTS AND STORAGE
         SPACE
SEVTWO   DC    F'72'
SEV8     DC    F'78'
LOG2     DC    H'19728'
ROUND    DC    H'8192'
TENP9I   DC    F'1000000000'
BUFF     DS    1D
CHAR     DS    1D
FIVE     DC    X'4E00000000000005'
TWOP56B  DC    X'4F10000000000000'
TENP16   DC    DE16'1'
SE       DS    1C
TWOP56   DC    C'7.205759403792794'''
         LTORG
         SPACE 3
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS     A
BL       DS    H
         SPACE 3                                                   0226
FAS      DSECT                                                     0226
         COPY  FSAREA                                              0226
         END
