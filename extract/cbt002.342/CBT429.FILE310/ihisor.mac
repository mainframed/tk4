 TITLE 'IHISOR,OUTREAL SHORT'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:CONVERT BINARY ARITHMETIC VALUE,FROM SECOND
*   ACTUAL PARAMETER,TO ZONED DECIMAL FORM AND TRANSFER TO AN OUTPUT
*   BUFFER
*
*ENTRY POINTS:
*        IHISOREL-FROM GENERATED OBJECT MODULE  LA 1,PARMLIST
*   DATA PASSED BY NAME                         BALR 14,15
*
*        IHISORAR-FROM ARRAY MODUL IHIOAR       LA 7,DATA
*   DATA PASSED BY NAME                         BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL -ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER - OPEN DATA SET -
*   CHANGE TO NEXT OUTPUT RECORD
*        IHIFSA-CNVIR-CONVERT INTEGER TO REAL SHORT
*
*EXITS-NORMAL:RELOAD REGISTERS AND BR14
*   -ERROR: TOO LONG RECORD                 NO.38
*   BRANCH TO IHIFSA         L 13,IHIFSA
*                            B FSAERR+XX*4(13)   XX ERROR NO.
*
*TABLES:PTTAB-POWER OF TEN TABLE,SHORT PREC
*
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES: LINKING TO IHISORAR DEVIATE FROM STANDARD
*   CHARACTER DEPENDENCE:
*   THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT
*   TO THE ONE USED AT ASSEMBLY TIME
*
         SPACE 3
IHISOREA CSECT
*0010013400,019200,024800,027400,047800,050400-051400              0226
         SPACE
ENTRY    EQU   15                  ADRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
         SPACE 2
         SPACE
FSA      EQU   12                  ADRESS OF FIXED STORAAGE AREA
         SPACE 2
*        OTHER GENERAL REGISTERS
         SPACE
DSN      EQU   5                   RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER BINARY
BASE     EQU   13                  BASE REGISTER FOR THIS ROUTINE
GRA      EQU   7                   SOURCE ADRESS
GRR      EQU   4                   CHARACTER POINTER
FR0      EQU   0                   FLOATING-POINT NUMBER
GRK      EQU   8                   BLANKCOUNTER
GREXP    EQU   2                   EXPONENT GREATER EIGHT
GRE      EQU   3                   EXPONENT LESS EIGHT
GRDE     EQU   9                   DECIMAL EXPONENT
EXEC     EQU   8                   BLANKCOUNTER
POWTEN   EQU   10                  REGISTER FOR ADRESSING POWERTENTABLE
*
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
         SPACE 3
*        DATA SET FLAGS
DS0      EQU   X'80'
DS6      EQU   X'02'
DS3      EQU   X'10'
         SPACE 3
         ENTRY IHISOREL
         ENTRY IHISORAR
IHISORAR STM   14,12,12(SAVE)      REGISTERS STORED IN OUTARRAY SAVEAR
         USING IHISORAR,ENTRY
         ST    SAVE,SAVREGOR+4     SAVREGOR=SAVE AREA THIS ROUTINE
         LA    BASE,SAVREGOR       BASE=SAVE=ADRESS OF SAVREGOR
         USING SAVREGOR,BASE
         B     SOUFLP+4
         SPACE 3
IHISOREL STM   14,12,12(SAVE)      REGISTERS IN FIXED STORAGE AREA
         LR    FSA,SAVE            ADRESS OF FIXED STORAGE AREA TO FSA
         DROP  BASE
         USING IHISOREL,ENTRY
         ST    SAVE,SAVREGOR+4     SAVREGOR=SAVE AREA THIS ROUTINE
         LA    BASE,SAVREGOR       BASE=SAVE=ADRESS OF SAVREGOR
         USING SAVREGOR,BASE
         B     SAVREGOR+72
         SPACE 2
SAVREGOR DS    18F
         SPACE 3
*        EVALUATE DATA SET NUMBER (EVDSN)
         SPACE
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 3
SOUFLP   L     GRA,4(0,PARAM)      ADRESS OF SOURCE
         USING DSTABLE,DSN
         OI    DSF,X'30'           DS2.=DS3.=1
         NI    DSF,X'FE'           DS7.=0
         LTR   GRA,GRA
         BP    REAL1
         SPACE 3
*        CALL CONVERSION ROUTINE (LOADED IN FSA)
         SPACE
         STM   14,13,8(SAVE)       ALL REGISTERS INTO SAVREGOR
         LR    2,SAVE              ADDRESS OF SAVREGOR TO REG2
         L     14,0(0,GRA)         INTEGER INTO REGISTER 14
         LA    7,ACNVIRD(FSA)
         LR    13,FSA              ADRESS OF FSA TO REG 13
         BALR  8,7
         LM    14,13,8(2)          ALL REGISTERS RESET
         B     *+8                 NUMBER IN FR0 AFTER CONVERSION
         SPACE 3
REAL1    LE    FR0,0(0,GRA)        NUMBER IN FR0
         SPACE 2
*        TEST IF DATA SET IS OPEN
         SPACE
         TM    DSF,DS0
         BO    NOCLO               DCB IS OPEN
         OI    DSF,DS6             DS6.=1
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 3
NOCLO    L     GRR,R               CHARACTER POINTER
         LA    EXEC,13(0,GRR)
         C     EXEC,RE
         BNH   NONEXREC            BUFFER CAN ACCEPT 13. DIGITS
         L     EXEC,RE
         SR    EXEC,GRR
         BC    12,*+16
BLANKS   MVI   0(GRR),C' '         BLANK IN BUFFER
         LA    GRR,1(0,GRR)        INCREASE R
         BCT   EXEC,BLANKS
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         L     GRR,R
         LA    EXEC,13(0,GRR)
         C     EXEC,RE
         BH    ORSERR
         OI    DSF,DS3
         SPACE 3
NONEXREC LA    GRDE,7
         LTER  FR0,FR0
         BC    6,NOT0
         MVI   0(GRR),C' '         NUMBER IS ZERO
         MVC   1(12,GRR),0(GRR)
         MVI   1(GRR),C'0'
         B     TERMIN
         SPACE 3
NOT0     MVI   0(GRR),C'+'         ZONE IS INSERTED
         BP    *+10
         MVI   0(GRR),C'-'
         LCER  FR0,FR0
         SPACE 3
EXPLOOP  STE   FR0,CHAR
         SR    GRE,GRE
         IC    GRE,CHAR
         MVI   SE,X'00'
         S     GRE,SEV0
         BP    *+14
         BZ    EXP0                EXPONENT.=70
         MVI   SE,X'80'
         LCR   GRE,GRE
         MH    GRE,LOG2
         AH    GRE,ROUND
         SRL   GRE,14
         C     GRE,SEV0
         BNH   *+8
         L     GRE,SEV0            EXPONENT.=70
         TM    SE,X'80'
         LA    POWTEN,PTTAB-4      ADRESS OF POWTEN
         BZ    *+10
         SR    GRDE,GRE
         B     *+10
         LA    POWTEN,64(0,POWTEN)
         AR    GRDE,GRE
         LR    GREXP,GRE
         SR    GRE,GRE             CLEAR REGISTER
         SRDA  GREXP,3
         SLA   GREXP,3
         SPACE 3
TESTEXP1 BZ    EXP1LS8
         C     GREXP,SEVTWO
         BL    EXP1LS8-8           EXPONENT NEGATIV
         ME    FR0,64(0,POWTEN)
         S     GREXP,SEVTWO
         B     TESTEXP1
         SPACE 3
         SRL   GREXP,1
         ME    FR0,28(GREXP,POWTEN)
EXP1LS8  SRL   GRE,27              EXPONENT MULTIPLIED FOUR
         LTR   GRE,GRE
         BC    8,EXPLOOP
         ME    FR0,0(GRE,POWTEN)
         B     EXPLOOP
         SPACE 3
EXP0     CE    FR0,TENP7
         BNL   DIG8                NUMBER GE 10**7
         MVI   CHAR,X'00'
         L     GRE,CHAR
         CVD   GRE,BUFF            INTEGER CONVERTED
         UNPK  1(9,GRR),BUFF+3(5)
         OI    9(GRR),X'F0'
         CLI   2(GRR),C'0'         LEADING ZERO
         BNE   *+12
         BCTR  GRDE,0
         MVC   2(7,GRR),3(GRR)
         MVC   1(1,GRR),2(GRR)
         MVI   2(GRR),C'.'         DECIMAL POINT INSERTED
         MVI   9(GRR),C''''        APOSTROPHE INSERTED
         SPACE 3
DECEXP   CVD   GRDE,BUFF
         UNPK  10(3,GRR),BUFF+6(2)
         LTR   GRDE,GRDE
         BC    10,*+12
         MVI   10(GRR),C'-'        EXPONENT SIGN IS NEGATIV
         B     *+8
         MVI   10(GRR),C'+'
         OI    12(GRR),X'F0'       ZONE INSERTED EXPONENT
         SPACE 3
TERMIN   LA    GRR,13(0,GRR)
         SR    GRK,GRK
         IC    GRK,K
         C     GRR,RE
         BE    RECEND
         MVI   0(GRR),C' '
         LA    GRR,1(0,GRR)
         BCT   GRK,TERMIN+10
         C     GRR,RE
         BE    RECEND
         ST    GRR,R
         L     SAVE,SAVREGOR+4
         LM    14,12,12(SAVE)
         BR    RETURN
         SPACE 3
RECEND   L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     RECEND-10
         SPACE 3
DIG8     AU    FR0,FIVE
         STE   FR0,CHAR
         CE    FR0,TWOP24B
         BL    EXP0+8
         MVC   1(9,GRR),TWOP24     NUMBER GE 10**7
         B     DECEXP
*
ORSERR   LR    13,FSA
         B     FSAERR+38*4(FSA)
ACNVIRD  EQU   X'120'
         SPACE 3
*        INTERNAL CONSTANTS AND STORAGE
         SPACE 2
SEVTWO   DC    F'72'
SEV0     DC    F'70'
LOG2     DC    H'19728'
ROUND    DC    H'8192'
TENP7    DC    X'46989680'
FIVE     DC    X'46000005'
TWOP24B  DC    X'47100000'
TWOP24   DC    C'1.677722'''
SE       DS    1C
CHAR     DS    1E
BUFF     DS    1D
         SPACE 3
PTTAB    DC    EE1'1'
         DC    EE2'1'
         DC    EE3'1'
         DC    EE4'1'
         DC    EE5'1'
         DC    EE6'1'
         DC    EE7'1'
         DC    EE8'1'
          DC   EE16'1'
         DC    EE24'1'
         DC    EE32'1'
         DC    EE40'1'
         DC    EE48'1'
         DC    EE56'1'
         DC    EE64'1'
         DC    EE72'1'
         DC    EE-1'1'
         DC    EE-2'1'
         DC    EE-3'1'
         DC    EE-4'1'
         DC    EE-5'1'
         DC    EE-6'1'
         DC    EE-7'1'
         DC    EE-8'1'
         DC    EE-16'1'
         DC    EE-24'1'
         DC    EE-32'1'
         DC    EE-40'1'
         DC    EE-48'1'
         DC    EE-56'1'
         DC    EE-64'1'
         DC    EE-72'1'
         LTORG
         SPACE 3
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
         SPACE 3                                                   0226
FAS      DSECT                                                     0226
         COPY  FSAREA                                              0226
         END
