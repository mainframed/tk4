 TITLE 'IHIOIN,OUTINTEGER'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:TRANSFER NUMBER FROM INTEGER INDICATED BY SECOND
*   ACTUAL PARAMETER TO OUTPUT BUFFER IN ZONED DECIMAL FORM
*
*ENTRY POINTS:
*        IHIOINTG-FROM GENERATED OBJECT MODULE  LA 1,PARMLIST
*   DATA PASSED BY NAME                         BALR 14,15
*
*        IHIOINAR-FROM ARRAY MODULE IHIOTA      LA 7,DATA
*   DATA PASSED BY NAME                         BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER-OPEN DATA SET-CHANGE TO NEXT
*   OUTPUT RECORD -CNVRI,CONVERT REAL TO INTEGER
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*   -ERROR: TOO LONG RECORD                 NO.38
*   BRANCH TO IHIFSA         L 13,IHIFSA
*                            B FSAERR+XX*4(13)   XX ERROR NO.
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:LINKING TO IHIOINAR DEVIATES FROM STANDARD
*   CHARACTER DEPENDENCE:
*   THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT
*   TO THE ONE USED AT ASSEMBLY TIME
*
IHIOINTE CSECT
*0019012600,017400,021200,023400,026600,036600,038200-039200       0226
         SPACE
ENTRY    EQU   15                  ADRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
         SPACE 2
FSA      EQU   12                  ADRESS OF FIXED STORAGE AREA
         SPACE 2
*        OTHER GENERAL REGISTERS
         SPACE
DSN      EQU   5                   RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER
BASE     EQU   13                  BASE REGISTER FOR THIS ROUTINE
GRA      EQU   7                   SOURCE ADRESS
GRR      EQU   2                   CHARACTER POINTER
GR0      EQU   0                   INTEGER NUMBER
FR0      EQU   0                   FLOATING POINT NUMBER FOR CONVERSION
GRRN     EQU   3
GRK      EQU   4
EXEC1    EQU   9
         SPACE 3
*        DATA SET FLAGS
DS0      EQU   X'80'
DS6      EQU   X'02'
DS3      EQU   X'10'
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
         SPACE 3
         ENTRY IHIOINTG
         ENTRY IHIOINAR
IHIOINAR STM   14,12,12(SAVE)      REGISTERS STORED IN OUTARRAY SAVEAR
         USING IHIOINAR,ENTRY
         ST    SAVE,SAVREGOI+4     SAVREGOI=SAVE AREA FOR THIS ROUTINE
         LA    BASE,SAVREGOI       BASE=SAVE=ADRESS OF SAVREGOI
         USING SAVREGOI,BASE
         B     SOUINT+4
         SPACE 3
IHIOINTG STM   14,12,12(SAVE)      REGISTERS IN FIXED STORAGE AREA
         LR    FSA,SAVE            ADRESS OF FIXED STORAGE AREA TO FSA
         DROP  BASE
         USING IHIOINTG,ENTRY
         ST    SAVE,SAVREGOI+4     SAVREGOI=SAVE AREA FOR THIS ROUTINE
         LA    BASE,SAVREGOI       BASE=SAVE=ADRESS OF SAVREGOI
         USING SAVREGOI,BASE
         B     SAVREGOI+72
         SPACE 2
SAVREGOI DS    18F
         SPACE 3
*        EVALUATE DATA SET NUMBER (EVDSN)
         SPACE
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 3
*        EVALUATE ADRESS OF SOURCE
*        TEST IF SOURCE NUMBER IS REAL
SOUINT   L     GRA,4(0,PARAM)      ADRESS OF SOURCE
         USING DSTABLE,DSN
         OI    DSF,X'30'           DS2.=DS3.=1
         NI    DSF,X'FE'           DS7.=0
         LTR   GRA,GRA
         BP    INT1
         SPACE 3
*        CALL CONVERSION ROUTINE
         SPACE
         LE    FR0,0(0,GRA)
         USING FSAAREA,FSA
         TM    OPTSW(FSA),X'20'    TEST IF LONG OR SHORT PRECISION
         BO    *+8                 SHORT PRECISION STATED
         LD    FR0,0(0,GRA)        LONG PRECISION STATED
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,CI(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     *+8
         SPACE 3
INT1     L     GR0,0(0,GRA)        NUMBER IN REG0
         SPACE 2
*        TEST IF DATA SET IS OPEN
         SPACE
         TM    DSF,DS0
         BO    NOCLO1              DCB IS OPEN
         OI    DSF,DS6             DS6.=1
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 3
*        TEST IF RECORD CAN ACCEPT 11 DIGITS,IF NOT FILL RECORD
*        WITH BLANKS AND CALL NEXTREC
*
NOCLO1   L     GRR,4(0,DSN)        CHARACTER POINTER
         LA    GRRN,11(0,GRR)
         C     GRRN,8(0,DSN)
         BNH   NONR1               BUFFER CAN ACCEPT 11. DIGITS
         L     EXEC1,RE
         SR    EXEC1,GRR
         BC    12,*+16
BLANKS   MVI   0(GRR),C' '         BLANK IN BUFFER
         LA    GRR,1(0,GRR)        INCREASE R
         BCT   EXEC1,BLANKS
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         L     GRR,4(0,DSN)
         LA    GRRN,11(0,GRR)
         C     GRRN,8(0,DSN)
         BH    OINERR                   TOO SHORT RECORD LENGTH
         OI    DSF,DS3
         SPACE 3
*
*        TEST SOURCE NUMBER AND CONVERT TO DECIMAL
*
NONR1    LTR   GR0,GR0
         BZ    OUTINT0             NUMBER IS ZERO
         CVD   GR0,BUFF
         UNPK  1(10,GRR),BUFF+2(6)
         OI    10(GRR),X'F0'       ZONE INSERTED
         SPACE 3
LEAD0    CLI   1(GRR),C'0'
         BNE   TERMIN0
         MVI   0(GRR),C' '         LEADING ZERO IS BLANKED
         LA    GRR,1(0,GRR)
         B     LEAD0
         SPACE 3
TERMIN0  LTR   GR0,GR0
         BP    *+12
         MVI   0(GRR),C'-'         SIGN INSERTED
         B     *+8
         MVI   0(GRR),C'+'
         SPACE 3
TERMIN1  SR    GRK,GRK
         IC    GRK,K               VALUE OF K IN REGISTER
         C     GRRN,8(0,DSN)
         BE    RECEND              RECORD END IS REACHED
         MVI   0(GRRN),C' '        FILL WITH BLANKS
         LA    GRRN,1(0,GRRN)
         BCT   GRK,TERMIN1+6
         C     GRRN,8(0,DSN)
         BE    RECEND
         ST    GRRN,4(0,DSN)
         L     SAVE,SAVREGOI+4      STORAGE ADRESS
         LM    14,12,12(SAVE)      RELOAD REGISTERS
         BR    RETURN
         SPACE 3
*        OUTINTEGER NUMBER IS 0
*
OUTINT0  MVI   0(GRR),C' '
         MVC   1(9,GRR),0(GRR)     BLANKS IN BUFFER
         MVI   10(GRR),C'0'        0. IN BUFFER
         B     TERMIN1
         SPACE 3
RECEND   L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     OUTINT0-10
*
*        RECORD LENGTH LESS THAN 11
OINERR   LR    13,FSA
         B     FSAERR+38*4(FSA)
         SPACE 3
BUFF     DS    1D
         LTORG
         SPACE 3
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
         SPACE 3
FSAAREA  DSECT
         COPY  FSAREA
         END
