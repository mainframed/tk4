 TITLE 'IHIIBA,INBARRAY'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:ASSIGN BOOLEAN VALUE TO ARRAY INDICATED BY SECOND
*   ACTUAL PARAMETER BY CALLING INBOOLEAN REPEATEDLY
*
*ENTRY POINT:
*        IHIIBARR-FROM GENERATED OBJECT MODULE    LA 1,PARMLIST
*   DATA PASSED BY NAME                           BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER
*        IHIIBO-INBOOLEAN
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*     -ERROR :N/A
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
         SPACE
         SPACE
IHIIBARR CSECT
         SPACE
ENTRY    EQU   15                  ADRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADDRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA  IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
FSA      EQU   12                       ADRESS OF FIXED STORAGE AREA
         SPACE
         SPACE
*
*  OTHER GENERAL REGISTERS
*
DSN      EQU   5                   ADRESS OF RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER
BASE     EQU   7                   BASE REGISTER FOR THIS ROUTINE
GRA      EQU   2
GRE      EQU   3
         SPACE
         SPACE
         SPACE
         SPACE
         SPACE
*
*
*  DATA SET FLAGS (DSF AND DSF+1 IN DSTAB)
DS0      EQU   X'80'
DS1      EQU   X'40'
DS2      EQU   X'20'
DS3      EQU   X'10'
DS4      EQU   X'08'
DS5      EQU   X'04'
DS6      EQU   X'02'
DS7      EQU   X'01'
DS8      EQU   X'80'
DS9      EQU   X'40'
DS10     EQU   X'20'
         EJECT
         SAVE  (14,12)
         LR    BASE,ENTRY
         USING IHIIBARR,BASE
         LR    FSA,SAVE                 ADRESS OF FIXED ST AREA TO FSA
         LA    SAVE,72(0,SAVE)
*  EVALUTE DATA SET NUMBER
*
         L     ENTRY,AEVDSNY
         BALR  RETURN,ENTRY
*
*  EVALUTE DESTINATION ADRESSES
*
         L     PARAM,4(0,PARAM)
         L     GRE,12(0,PARAM)          ADRESS OF DESTEND+1 TO GRE
         L     GRA,8(0,PARAM)           ADRESS OF START DEST TO GRA
*
*  CALL  FOR ROUTINE INBOOLEAN
INBARRY1 L     ENTRY,EARRINB
         BALR  RETURN,ENTRY
         SPACE
         SPACE
         SPACE
         LA    GRA,1(0,GRA)             INCREASE ADEST BY ONE
         CR    GRA,GRE
         BL    INBARRY1                 MORE VALUE TO BE READ
         LR    SAVE,FSA
         LM    14,12,12(SAVE)
         BR    RETURN
         SPACE
         SPACE
*
* EXTERNAL ADRESSES
*
*
AEVDSNY  DC    V(IHIIOREV)        ADRESS OF ROUT EVALUATE DATA SET NR
EARRINB  DC V(IHIIBOAR)           ADRESS OF ROUTIN INBOOLEAN
         END
