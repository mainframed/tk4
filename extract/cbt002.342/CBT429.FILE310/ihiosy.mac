 TITLE 'IHIOSY,OUTSYMBOL'
*
*STATUS: CHANGE LEVEL 000
*FUNCTION/OPERATION:COMPARE NUMBER    ASSIGNED TO THIRD ACTUAL
*   PARAMETER WITH AN INTERNAL STRING AND TRANSFER CHARACTER
*   IN CORRESPONDING POSITION TO AN OUTPUT BUFFER
*
*ENTRY POINT:
*        IHIOSYMB - FROM GENERATED OBJECT MODULE   LA 1,PARMLIST
*   DATA PASSED BY NAME
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR - EVALUATE DATA SET NUMBER - OPEN DATA SET -
*   CHANGE TO NEXT OUTPUT RECORD
*
*EXITS-NORMAL: RELOAD REGISTERS AND BR14
*    -ERROR : SOURCE DOES NOT MATCH STRING  NO.8
*   BRANCH TO FSA; LA 13,IHIFSA
*                  B  FSAERR+XX*4(13)    XX ERROR NO.
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE ASSUME THAT INTERNAL REPRESEN-
*   TATION OF THE EXTERNAL CHARACTER SET IS EQUIVALENT TO THE ONE
*   USED AT ALGOL COMPILE TIME
*
*
IHIOSYMB CSECT
*0010013400,015800,017800,023600,025400-026200                     0226
         SPACE
ENTRY    EQU   15                  ADRESS OF ROUTINE OUTSYMBOL
RETURN   EQU   14                  RETURN ADRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
         SPACE 2
*        OTHER GENERAL REGISTERS
         SPACE
GR0      EQU   0
FR0      EQU   0
DSN      EQU   5                   RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER BINARY
FSA      EQU   12                  ADRESS OF FIXED STORAGE AREA
SOURCE   EQU   3                   ADRESS OF SOURCE
GRAS     EQU   4                   ADRESS OF START OF STRING
BASE     EQU   7                   BASE REGISTER FOR THIS ROUTINE
GRINT    EQU   8                   INTEGER NUMBER FROM SOURCE
GRES     EQU   9                   LENGTH OF SOURCE STRING
GRR      EQU   10                  CHARACTER POINTER
         SPACE 3
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
*        DATA SET FLAGS
DS0      EQU   X'80'
         SPACE 3
         STM   14,12,12(SAVE)
         LR    BASE,ENTRY
         USING IHIOSYMB,BASE
         ST    SAVE,SAVEOSYM+4     SAVEOSYM=SAVE AREA
         LR    FSA,SAVE            ADRESS OF FIXED STORAGE AREA TO FSA
         LA    SAVE,SAVEOSYM
         SPACE 2
*        EVALUATE DATA SET NUMBER (EVDSN)
         SPACE
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         L     GRAS,4(0,PARAM)     ADRESS OF STRING
         SPACE 2
*        TEST IF DATA SET IS OPEN
         SPACE
         USING DSTABLE,DSN
         NI    DSF,X'FE'           DS7.=0
         OI    DSF,X'30'           DS2.=DS3.=1
         TM    DSF,DS0
         BO    SOURCE0
         OI    DSF,X'02'           DS6.=1
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         BALR  RETURN,ENTRY        DCB IS CLOSED
SOURCE0  L     SOURCE,8(0,PARAM)       ADRESS OF SOURCE
         LTR   SOURCE,SOURCE           TEST IF CONVERSION
         BP    EVSOURCE                 NO CONVERSION
         TM    OPTSW(FSA),X'20'         TEST IF LONG OR SHORT PREC.
         BO    SOURCE1                  SHORT
         LD    FR0,0(0,SOURCE)
         B     SOURCE1+4
SOURCE1  LE    FR0,0(0,SOURCE)
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,CI(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         LR    GRINT,GR0
         B     EVSOURCE+4
         SPACE 2
EVSOURCE L     GRINT,0(0,SOURCE)   SOURCE INTEGER LOADED
         LTR   GRINT,GRINT
         BM    ERR8                INTEGER NEGATIV
         L     GRR,R               CHARACTER POINTER
         BP    *+12                INTEGER POSITIV
         MVI   0(GRR),C' '         INTEGER EQUAL ZERO
         B     TERMIN
         MVC   HALFW(2),0(GRAS)    LENGTH OF STRING
         LH    GRES,HALFW
         LA    GRINT,1(0,GRINT)    INTEGER INCREASED BY ONE
         BCTR  GRES,0              LENGTH DIMINISHED BY ONE
         CR    GRINT,GRES
         BH    ERR8
         AR    GRINT,GRAS
         MVC   0(1,GRR),0(GRINT)
         SPACE 3
TERMIN   LA    GRR,1(0,GRR)
         C     GRR,RE
         BNL   NEXTREC
         ST    GRR,R
         LR    SAVE,FSA
         LM    14,12,12(SAVE)
         BR    RETURN
         SPACE 3
NEXTREC  L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         B     NEXTREC-8
         SPACE 3
ERR8     LR    13,FSA
         B     FSAERR+8*4(FSA)          SOURCE DOES NOT MATCH STRING
         SPACE 3
SAVEOSYM DS    18F                 SAVE AREA
         SPACE
HALFW    DS    1H
         LTORG
         SPACE 3
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
FSAAREA  DSECT
         COPY  FSAREA
         END
