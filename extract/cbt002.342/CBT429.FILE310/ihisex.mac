 TITLE 'IHISEX, EXPONENTIAL FUNCTION, SHORT PRECISION'
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION: Y = X*LOG2(E) = 4R-S-T, WHERE R AND S ARE        *
*   INTEGERS, T FRACTION AND BOTH S AND T ARE NON-NEGATIVE. THEN      *
*   E**X = 2**Y = (16**R)(2**-S)N2**-T)                               *
*                                                                     *
*ENTRY POINT:                                                         *
*        IHISEX - EXP FUNCTION, SHORT              LA  1,PARMLIST     *
*                                                  BALR 14,15         *
*                                                  DATA PASSED BY NAME*
*   THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE.           *
*                                                                     *
*INPUT: N/A                                                           *
*                                                                     *
*OUTPUT: N/A                                                          *
*                                                                     *
*EXTERNAL ROUTINES: N/A                                               *
*                                                                     *
*EXIT-NORMAL: RETURN BY MEANS OF   BR  14.  RESULT IN FPR0.           *
*                                                                     *
*EXIT-ERROR: IF ARGUMENT NOT POSITIVE GO TO ERROR OUTINE VIA          *
*        B     FSAERR+25*4(13)                                        *
*                                                                     *
*TABLES/WORKAREAS: N/A                                                *
*                                                                     *
*ATTRIBUTES: THE MODULE IS SERIALLY REUSABLE.                         *
*                                                                     *
*NOTES: CHARACTER CODE DEPENDENCE                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL       *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
       SPACE
IHISEXPT CSECT
*C023600,023700                                                  A39993
       ENTRY   IHISEX
       SPACE
GR0    EQU     0               SCRATCH REGISTERS
GR1    EQU     1
GR2    EQU     2
GR3    EQU     3
GR4    EQU     4
GR5    EQU     5
GR6    EQU     6
GRA    EQU     1               ARGUMENT POINTER
GRS    EQU     13              SAVE AREA POINTER
GRR    EQU     14              RETURN REGISTER
GRL    EQU     15              LINK REGISTER
FR0    EQU     0               ANSWER REGISTER
         EJECT
       DC      CL7'IHISEX'             ENTRY-POINT IDENTIFIER
       DC      X'06'
       USING   *,GRL
IHISEX STM     GRR,GR6,12(GRS)         SAVE REGISTERS
       L       GR1,0(GRA)
       LE      FR0,0(GR1)      OBTAIN ARGUMENT
       CE      FR0,MAX
       BC      2,ERROR         IF TOO BIG, ERROR
       CE      FR0,MIN
       BC      2,OK1
       SER     FR0,FR0         IF VERY SMALL, GIVE 0 AS ANSWER
       BC      15,EXIT
       SPACE
OK1    L       GR1,0(GR1)      PICK UP ARGUMENT AGAIN
       SLDL    GR0,8
       N       GR0,MASK        =X'0000007F' CHARACTERISTIC OF X
       CH      GR0,SMALL       =X'0039'
       BC      2,OK2
       LE      FR0,ONE         IF ABS. VALUE OF X LESS THAN 2**-28 GIVE
       BC      15,EXIT           1 AS ANSW.  THIS AVOIDS SHIFT TROUBLE
       SPACE
OK2    SRL     GR1,1           NORMAL CASE  MANTISSA OF X IN GR1, B0
       SLL     GR0,2
       LCR     GR2,GR0         -4*CHARACTERISTIC
       M       GR0,LOG2E       LOG E BASE 2 IN B1.  PRODUCT IN B2
       SRDL    GR0,287(GR2)    B33  /R/ IN GR0, /S+T/ IN GR1
       LTER    FR0,FR0
       BC      12,OK3          IF X NON-POSITIVE, JUMP
       X       GR0,ALLF        IF X POSITIVE, -R = -R(R'+1) IN GR0,
       X       GR1,ALLF          S+T = 4-(S'+T') IN GR1
       SPACE
OK3    SLL     GR0,24
       LR      GR2,GR0         SAVE -R IN GR2  B7.  CHAR MODIFIER
       SLDL    GR0,2           S IN GR0 LOW, T IN GR1 HIGH
       LR      GR3,GR0         SAVE S IN GR3, FOR SHIFT COUNT
       SRL     GR1,4           T                    (B3)
       LR      GR6,GR1         SAVE T       IN GR6  (B3)
       MR      GR0,GR1         T*T                  (B7)
       LR      GR5,GR0
       M       GR4,C           C*T*T        IN GR4  (B4)
       A       GR0,A
       LR      GR5,GR0         A+T*T        IN GR5  (B7)
       L       GR0,B
       DR      GR0,GR5         B/(A+T*T)    IN GR1  (B3)
       SR      GR1,GR6
       SRL     GR1,1           -T+B/(A+T*T) IN GR1  (B4)
       A       GR1,D
       AR      GR1,GR4         C*T*T+D-T+B/(A+T*T)  (B4)
       LR      GR4,GR6
       SRL     GR4,2           2*T                  (B6)
       DR      GR4,GR1         2*T/(C*T*T+D-T+B/(A+T*T))   (B1)
       A       GR5,FXONE       2**(-T) NOW READY IN  B1
       SRL     GR5,0(GR3)      (2**-S)(2**-T) READY (B1)
       A       GR5,FUDGE       ROUND AND
       C       GR5,FXONE         FLOAT THIS NUMBER
       BC      4,OK4
       L       GR5,ONE
       BC      15,JOIN
       SPACE
OK4    SRL     GR5,6
       O       GR5,FXONE       BASE CHARACTERISTIC
JOIN   SR      GR5,GR2         ADJUST CHARACTERISTIC WITH -R
       ST      GR5,BUFF
       LE      FR0,BUFF
       SPACE
EXIT   LM      GR2,GR6,28(GRS) RETURN
       MVI     12(GRS),X'FF'
       BCR     15,GRR
       SPACE
ERROR    L     GRS,4(GRS)               RESTORE FSA ADDRESS      A32992
         B     FSAERR+24*4(GRS)         PARAM GREATER 174.673    A32992
       SPACE
FSAERR   EQU   X'1CC'
MAX    DC      X'42AEAC4F'     174.673
MIN    DC      X'C2B437E0'    -180.218
ONE    DC      X'41100000'
MASK   DC      X'0000007F'
LOG2E  DC      X'5C551D95'     LOG E BASE 2    B1
ALLF   DC      X'FFFFFFFF'
A      DC      X'576AE119'     87.4174972      B7
B      DC      X'269F8E6B'     617.972269      B11
C      DC      X'B9059003'     -0.034657359    B-4
D      DC      X'B05CFCE3'     -9.95459578     B4
FXONE  DC      X'40000000'     1. B1  ALSO BASE CHARACTERISTIC
FUDGE  DC      X'00000020'
BUFF   DS      F
       DS      0H
SMALL  DC      X'0039'
       END
