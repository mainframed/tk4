    TITLE 'IHIOTA,OUTTARRAY'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:TRANSFER NUMBERS FROM ARRAY INDICATED BY SECOND
*   ACTUAL PARAMETER TO OUTPUT BUFFER BY CALLING OUTINTEGER REPEATEDLY
*
*ENTRY POINT:
*        IHIOTARR-FROM GENERATED OBJECT MODULE   LA 1,PARMLIST
*   DATA PASSED BY NAME                          BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER
*        IHIOIN-OUTINTEGER
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*     -ERROR :N/A
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:N/A
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
IHIOTARR CSECT
ENTRY    EQU   15                       ENTRY ADDRESS
RETURN   EQU   14                       RETURN ADDRESS
SAVE     EQU   13                       ADDRESS OF SAVEAREA IN FSA
PARAM    EQU   1                        ADDRESS OF PARAMETERLIST
         SPACE 3
*
* OTHER GENERAL REGISTERS
*
DSN      EQU   5                        RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                        DATA SET NUMBER
BASE     EQU   8                        BASE REGISTER
CONV     EQU   3                        CONVERSION FLAG
GRA      EQU   7                        REGISTER FOR TRANSFER DESTADDR
GRB      EQU   2                        SCRATCH REGISTER
FSA      EQU   12                       ADDRESS OF FSA DURING EXECUTION
         SAVE  (14,12)
         LR    BASE,ENTRY               IN FSA
         USING IHIOTARR,BASE
         LR    FSA,SAVE                 ADDRESS OF FSA
         LA    SAVE,72(0,SAVE)          ADDRESS OF 2:ND SAVEAREA IN FSA
*
         SR    CONV,CONV
*                                  EVALUATION DATA SET NUMBER
*
         L     ENTRY,AEVDSN
         BALR  RETURN,ENTRY
         SPACE 2
*
*                                  EVALUATE SOURCE ADDRESS
*
         L     PARAM,4(0,PARAM)
         LTR   PARAM,PARAM              TEST 2:ND PARAMETER
         BC    10,OUTT1                 BRANCH GREATER OR EQUAL 0
         O     CONV,=X'80000000'        INSERT FLAG BYTE
         N     PARAM,=X'00FFFFFF'
OUTT1    L     GRB,12(0,PARAM)          ADDRESS OF DESTEND+1 TO GRB
         L     GRA,8(0,PARAM)           ADDRESS OF STARTDEST TO GRA
         AR    GRA,CONV
         AR    GRB,CONV
*
*                                  CALL ROUTINE OUTINTEGER
*
OUTT2    L     ENTRY,ARROUTI
         BALR  RETURN,ENTRY
         AH    GRA,=H'4'
         BP    *+16
         TM    OPTSW(FSA),X'20'
         BO    *+8
         AH    GRA,=H'4'
         CR    GRA,GRB                  INCREASE DESTADDRESS
         BL    OUTT2                    DESTEND NOT REACHED
         SPACE 2
         LR    SAVE,FSA
         RETURN (14,12)
*
*
*
         SPACE 3
*  EXTERNAL ADDRESSES
*
AEVDSN   DC    V(IHIIOREV)
ARROUTI  DC    V(IHIOINAR)
         LTORG
         EJECT
FAS      DSECT
         COPY  FSAREA
         END
