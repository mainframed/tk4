 TITLE 'IHIOBA,OUTBARRAY'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:TRANSFER BOOLEAN VALUES FROM ARRAY DEFINED BY
*   SECOND ACTUAL PARAMETER TO AN OUTPUT BUFFER BY CALLING OUTBOOLEAN
*   REPEATEDLY
*
*ENTRY POINTS:
*        IHIOBARR-FROM GENERATED OBJECT MODULE   LA 1,PARMLIST
*   DATA PASSED BY NAME                          BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER
*        IHIOBO-INBOOLEAN
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*     -ERROR :N/A
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
         SPACE
         SPACE
IHIOBARR CSECT
         SPACE
*
*
ENTRY    EQU   15                  ADRESS OF ROUTINE
RETURN   EQU   14                  RETURN ADRESS
SAVE     EQU   13                  ADRESS OF SAVE AREA IN FSA
PARAM    EQU   1                   ADRESS OF PARAMETER LIST
*
*GRA     EQU   2                   ADRESS OF SOURCE
*SAVE    EQU   13                  ADRESS OF SAVE AREA IN OUTBARRY
FSA      EQU   12                  ADRESS OF FIXED STORAGE AREA
         SPACE
         SPACE
*
*  OTHER GENERAL REGISTERS
*
DSN      EQU   5                   ADRESS OF RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                   DATA SET NUMBER
BASY     EQU   7                   BASE REGISTER
GRA      EQU   2
GRE      EQU   4
         SPACE
*
*  DATA SET FLAGS  (DSF AND DSF+1 IN DSTAB)
*
*
DS0      EQU   X'80'
DS1      EQU   X'40'
DS2      EQU   X'20'
DS3      EQU   X'10'
DS4      EQU   X'08'
DS5      EQU   X'04'
DS6      EQU   X'02'
DS7      EQU   X'01'
DS8      EQU   X'80'
DS9      EQU   X'40'
DS10     EQU   X'20'
         EJECT
*
         SAVE  (14,12)
         LR    BASY,ENTRY
         USING IHIOBARR,BASY
         LR    FSA,SAVE                 ADRESS OF FIXED STORAGE AREA TO
         LA    SAVE,72(0,SAVE)          REGISTER FSA
*
* EVALUATE DATA SET NUMBER
*
         L     ENTRY,AEVDSNY
         BALR  RETURN,ENTRY
         SPACE
         SPACE
*
* STORE SOURCE ADRESS IN REGISTER GRA
*
         L     PARAM,4(0,PARAM)         ADRESS OF SOURCEEND+1 TO GRE
         L     GRE,12(0,PARAM)
         L     GRA,8(0,PARAM)           ADRESS OF SOURCE TO GRA
*
*  CALL FOR ROUTINE OUTBOOLEAN
*
OUTBY1   L     ENTRY,EARROUTB
         BALR  RETURN,ENTRY
         SPACE
         SPACE
         LA    GRA,1(0,GRA)             INCREASE ASOURCE BY ONE
         CR    GRA,GRE
         BL    OUTBY1                   MORE VALUE TO WRITE
         LR    SAVE,FSA
         LM    14,12,12(SAVE)           RELOAD REGISTER
         BR    RETURN
         SPACE
*
* EXTERNAL ADRESSES
*
*
AEVDSNY  DC    V(IHIIOREV)
EARROUTB DC    V(IHIOBOAR)
         END
