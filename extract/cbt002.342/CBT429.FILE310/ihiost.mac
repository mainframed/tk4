 TITLE 'IHIOST,OUTSTRING'
*
*STATUS:CHANGE LEVEL 000
*
*FUNCTION/OPERATION:TRANSFER STRING DEFINED BY SECOND ACTUAL PARAMETER
*   TO AN OUTPUT BUFFER
*
*ENTRY POINT:
*        IHIOSTRG-FROM GENERATED OBJECT MODULE    LA 1,PARMLIST
*   DATA PASSED BY NAME                           BALR 14,15
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR-EVALUATE DATA SET NUMBER-OPEN DATA SET-CHANGE TO NEXT
*   OUTPUT RECORD
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*     -ERROR :N/A
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
*
         SPACE 4
IHIOSTRG CSECT
*0019015200,018400,023400,026600,028600-029800                     0226
*
ENTRY    EQU   15                       ADDRESS OF ROUTINE
RETURN   EQU   14                       RETURN ADDRESS
SAVE     EQU   13                       ADDRESS OF SAVE AREA IN FSA
PARAM    EQU   1                        ADDRESS OF PARAMETER LIST
         SPACE 3
*
*  OTHER GENERAL REGISTERS
*
DSN      EQU   5                        ADDRESS OF RELEV ENTRY IN DSTAB
DSNR     EQU   6                       DATA SET NUMBER
BASE     EQU   8                        BASE REGISTER
GRA      EQU   2
GRB      EQU   3
GRN      EQU   4
GRR      EQU   9
GRX      EQU   7
FSA      EQU   12                       ADDRESS OF FSA
         SPACE 2
*
*  DATA SET  FLAGS  DSF AND DSF+1 IN DSTAB
*
*
DS0      EQU   X'80'
DS1      EQU   X'40'
DS2      EQU   X'20'
DS3      EQU   X'10'
DS4      EQU   X'08'
DS5      EQU   X'04'
DS6      EQU   X'02'
DS7      EQU   X'01'
DS8      EQU   X'80'
DS9      EQU   X'40'
DS10     EQU   X'20'
         SPACE 2                                                   0226
*   DISPLACEMENTS IN ADRLST IN IHIFSA                              0226
CI       EQU   0             DISPLACEMENT FOR: IHIIORCI            0226
CL       EQU   4                               IHIIORCL            0226
EV       EQU   8                               IHIIOREV            0226
NX       EQU   12                              IHIIORNX            0226
OP       EQU   16                              IHIIOROP            0226
OQ       EQU   20                              IHIIOROQ            0226
         SPACE 2                                                   0226
         SAVE  (14,12)
         LR    BASE,ENTRY
         USING IHIOSTRG,BASE
         ST    SAVE,SAVAR+4
         LR    FSA,SAVE
         LA    SAVE,SAVAR
*
*  EVALUATE DATA SET NUMBER
*
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,EV(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 3
*
*  STORE SOURCE ADDRESS
*
         L     PARAM,4(0,PARAM)
         ST    PARAM,ASTRING
         USING DSTABLE,DSN
         OI    DSF,DS2                  DS2=DS3=1  OUTPUT
         OI    DSF,DS3
         NI    DSF,X'FE'                DS7=0   NO END OF DATA
         SPACE 3
         TM    DSF,DS0                  TEST IF DATA SET IS OPEN
         BO    OUTSTR1                  DATA SET IS OPEN
         OI    DSF,DS6                  DATA SET IS NOT OPEN
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,OP(ENTRY)                                     0226
         BALR  RETURN,ENTRY             CALL FOR ROUTINE OPEN
         SPACE 2
OUTSTR1  L     GRA,ASTRING
         MVC   STRLTH(2),0(GRA)
         LH    GRN,STRLTH
         BCTR  GRN,0
         BCTR  GRN,0                    REAL STRING LENGTH TO REGISTER
         LTR   GRN,GRN
         BZ    OUTSTR3
         LA    GRA,2(0,GRA)
OUTSTR4  LR    GRB,GRN
         OI    DSF,DS3
         A     GRN,R
         C     GRN,RE
         BH    OUTSTR2                  OVERFLOW IN CURRENT RECORD
         SPACE 3
         BCTR  GRB,0
         L     GRR,R
         EX    GRB,OUTMOV               CONTENT OF STRING TO BUFFER
         LA    GRB,1(0,GRB)
         ST    GRN,R
         C     GRN,RE
         BNE   OUTSTR3
         SPACE 2
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY             CURRENT RECORD FILLED
         SPACE 2
OUTSTR3  L     SAVE,SAVAR+4
         RETURN (14,12)
         SPACE 3
OUTSTR2  L     GRX,RE               RE-R  AVALIABLE  PLACE IN
         S     GRX,R                CURRENT RECORD
         L     GRR,R
         BCTR  GRX,0
         EX    GRX,OUTMOV
         LA    GRX,1(0,GRX)
         LR    GRN,GRB
         SR    GRN,GRX
         AR    GRA,GRX
         SPACE 2
         L     ENTRY,IORLST(FSA)                                   0226
         L     ENTRY,NX(ENTRY)                                     0226
         BALR  RETURN,ENTRY
         SPACE 2
         B     OUTSTR4
         CNOP  0,8
OUTMOV   MVC   0(1,GRR),0(GRA)
         SPACE 4
SAVAR    DS    18F
ASTRING  DS    A
STRLTH   DS    H
         LTORG
*
DSTABLE  DSECT
ADCB     DS    A
R        DS    A
RE       DS    A
NBB      DS    A
BB       DS    A
S        DS    H
P        DS    H
K        DS    C
Q        DS    C
DSF      DS    H
NOTEADR  DS    A
BL       DS    H
         SPACE 3                                                   0226
FAS      DSECT                                                     0226
         COPY  FSAREA                                              0226
         END
