 TITLE 'IHISLO, LOGARITHMIC FUNCTION, SHORT PRECISION'
*STATUS: CHANGE LEVEL 000                                             *
*                                                                     *
*FUNCTION/OPERATION: WRITE X = M*16**P, M MANTISSA. PICK A BASE VALUE *
*   A DEPENDING ON SIZE OF M, WRITE Z = (M-A)/(M+A). THEN             *
*   LOG(X) = P*LOG(16) + LOG(A) + LOG((1+Z)/(1-Z))                    *
*                                                                     *
*ENTRY POINT:                                                         *
*        IHISLO - LOG FUNCTION, SHORT              LA  1,PARMLIST     *
*                                                  BALR 14,15         *
*                                                  DATA PASSED BY NAME*
*   THE MODULE IS ENTERED FROM THE GENERATED OBJECT MODULE.           *
*                                                                     *
*INPUT: N/A                                                           *
*                                                                     *
*OUTPUT: N/A                                                          *
*                                                                     *
*EXTERNAL ROUTINES: N/A                                               *
*                                                                     *
*EXIT-NORMAL: RETURN BY MEANS OF   BR  14.  RESULT IN FPR0.           *
*                                                                     *
*EXIT-ERROR: IF ARGUMENT NOT POSITIVE GO TO ERROR OUTINE VIA          *
*        B     FSAERR+25*4(13)                                        *
*                                                                     *
*TABLES/WORKAREAS: N/A                                                *
*                                                                     *
*ATTRIBUTES: THE MODULE IS SERIALLY REUSABLE.                         *
*                                                                     *
*NOTES: CHARACTER CODE DEPENDENCE                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL       *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
       SPACE
IHISLOGM CSECT
       ENTRY   IHISLO
       SPACE
GR0    EQU     0             SCRATCH REGISTERS
GR1    EQU     1
GRA    EQU     1             ARGUMENT POINTER
GR14   EQU     14
GRS    EQU     13            SAVE AREA POINTER
GRR    EQU     14            RETURN REGISTER
GRL    EQU     15            LINK REGISTER
FR0    EQU     0             ANSWER REGISTER
FR2    EQU     2             SCRATCH REGISTER
         EJECT
       DC      CL7'IHISLO'             ENTRY-POINT IDENTIFIER
       DC      X'06'
       USING   *,GRL
       SPACE
IHISLO STM     GRR,GRL,12(GRS)         NATURAL LOG ENTRY
       L       GR1,0(GRA)
       L       GR0,0(GR1)      OBTAIN ARGUMENT
       LTR     GR0,GR0
       BC      12,ERROR        IF ARG 0 OR NEGATIVE, ERROR
       SRDL    GR0,24
       SRL     GR1,8
       ST      GR1,ARG         STORE MANTISSA M ALONE
       OI      ARG,X'40'           AND FLOAT IT
       SLL     GR0,2           4*CHAR IN GR0
       STH     GR0,IPART+2       SAVE THIS IN FLOAT FORM
       SR      GR14,GR14               SET GR14 TO O,4 OR 8 AND DEFINE
       SRL     GR1,21            A TO BE 1, 1/4, OR 1/16, IF M IS IN
       IC      GR14,TABLE(GR1)      (1/2,1),(2/8,1/2),OR  IN (J/16,1/8)
       LE      FR0,ARG         OBTAIN  Z = (M-A)/(M+A)
       LER     FR2,FR0
       SE      FR0,ONE(GR14)
       AE      FR2,ONE(GR14)          POSSIBLY ONLY 21 SIGNIF BPTS HERE
       DER     FR0,FR2
       STE     FR0,ARG         Z READY.   STORE IT AT ARG
       MER     FR0,FR0         Z**2
       LE      FR2,C4          COMPUTE LOG((1+Z)/(1-Z)) USING
       MER     FR2,FR0           CHEBYSHEV INTERPOLATION POLYNOMIAL
       AE      FR2,C3
       MER     FR2,FR0
       AE      FR2,C2
       MER     FR2,FR0
       AE      FR2,C1
       MER     FR2,FR0
       LE      FR0,ARG
       MER     FR2,FR0
       AER     FR2,FR0
       AER     FR2,FR0
       LE      FR0,IPART       4*(P+64)
       SRL     GR14,1                  0,2 OR 4 = - LOG(A) BASE 2
       LA      GR14,256(GR14)          4*64-LOG2(A)
       STH     GR14,IPART+2            STORE THIS AND FLOAT IT
       SE      FR0,IPART       4*P+LOG2(A)
       ME      FR0,LOGE2
       ADR     FR0,FR2         NATURAL LOG READY
       SPACE
       L       GR14,12(GRS)    RETURN
       MVI     12(GRS),X'FF'
       BCR     15,GRR
       SPACE
       SPACE
ERROR    B     FSAERR+25*4(13)     PARAMETER ZERO OR NEGATIVE
FSAERR   EQU   X'1CC'
ARG    DS      F
IPART  DC      X'46000000'
TABLE  DC      X'0804040400000000'
ONE    DC      X'41100000'   1     FOLLOWING 3 CONSTANTS MUST
       DC      X'40400000'   1/4       BE CONSECUTIVE
       DC      X'40100000'   1/16
LOGE2  DC      X'40B17219'   LOG(2) BASE E + FUDGE 1
C4     DC      X'4048157B'   0.28157778
C3     DC      X'4047973F'   0.27965158
C2     DC      X'40667685'   0.40024595
C1     DC      X'40AAAA71'   0.66666322
       END
