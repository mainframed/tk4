 TITLE 'IHIIAR,INARRAY AND INTARRAY'
*
*STATUS:CHANGE LEVEL 000
**
*FUNCTION/OPERATION:ASSIGN NUMBERS TO ARRAY INDICATED BY SECOND
*   ACTUAL PARAMETER BY CALLING INREAL OR ININTEGER REPEATEDLY
*
*ENTRY POINTS:
*        IHIIARRY-FROM GENERATED OBJECT MODULE;INARRAY  LA 1,PARMLIST
*        IHIIARRT-FROM GENERATED OBJECT MODULE;INTARRAY BALR 14,15
*   DATA PASSED BY NAME
*
*INPUT:N/A
*
*OUTPUT:N/A
*
*EXTERNAL ROUTINES:
*        IHIIOR -EVALUATE DATA SET NUMBER
*        IHIIDE -INREAL OR ININTEGER
*
*EXIT-NORMAL:RELOAD REGISTERS AND BR14
*     -ERROR :N/A
*
*TABLES/WORK AREAS:N/A
*
*ATTRIBUTES:SERIALLY REUSABLE
*
*NOTES:THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A SPECIAL
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET
*
IHIIARTN CSECT
DSN      EQU   5                        RELEVANT ENTRY IN DSTAB
DSNR     EQU   6                        DATA SET NUMBER
GRA      EQU   7                        REGISTER FOR TRANSFER DESTADR
GRB      EQU   8                        SCRATCH REGISTER
BASE     EQU   9                        BASE REGISTER
FKTR     EQU   10                       REGISTER FOR TRANSFER FLAGBYTE
FIXSTA   EQU   12                       ADDRESS OF FSA DURING EXECUSION
         SPACE 4
ENTRY    EQU   15                       ENTRY ADDRESS
RETURN   EQU   14                       RETURN ADDRESS
SAVER    EQU   13                       ADDRESS OF 1:ST SAVEAREA IN FSA
PARAM    EQU   1                        ADDRESS OF PARAMETER LIST
         ENTRY IHIIARRT
         ENTRY IHIIARRY
IHIIARRT STM   14,12,12(SAVER)          SAVE REGISTERS IN FSA
         LR    FIXSTA,SAVER             ADDRESS OF FSA IN REGISTER 12
         LA    BASE,22(0,ENTRY)         BASE=ADDRESS OF IHGIARRY
         USING IHIIARRY,BASE
         LA    SAVER,72(0,SAVER)        ADDRESS OF 2:D SAVEAREA IN FSA
         LH    FKTR,FOUR                FLAGBYTE INTARRAY
         B     INAR1
IHIIARRY STM   14,12,12(SAVER)
         LR    FIXSTA,SAVER             ADDRESS OF FSA TO REGISTER 12
         LR    BASE,ENTRY
         LA    SAVER,72(0,SAVER)        ADDRESS OF 2:D SAVEAREA IN FSA
         SR    FKTR,FKTR                FLAGBYTE INARRAY
         SPACE 2
*        EVALUATE DATA SET NUMBER
         SPACE 2
INAR1    L     ENTRY,AEVDSNY
         BALR  RETURN,ENTRY
         SPACE 2
* EVALUATE DEST ADDRESSES
         SPACE 2
         L     PARAM,4(0,PARAM)
         L     GRB,12(0,PARAM)          ADDRESS OF DESTEND+1 TO GRB
         L     GRA,8(0,PARAM)           ADDRESS OF STARTDEST TO GRA
*
*  CALL ROUTINE INREAL-ININTEGER
*
*
INAR2    L     ENTRY,EARRINN
         BALR  RETURN,ENTRY
         SPACE
         SPACE
         LTR   FKTR,FKTR
         BP    INAR3
         TM    OPTSW(FIXSTA),X'20'      TEST LONG OR SHORT PRECS.
         BO    INAR3
         LA    GRA,8(0,GRA)             INCREASE  DESTADDRESS INARRAY
         B     *+8
INAR3    LA    GRA,4(0,GRA)             INCREASE  DESTADDRESS INTARRAY
         CR    GRA,GRB
         BL    INAR2
         LR    SAVER,FIXSTA
         RETURN (14,12)
         SPACE 4
FOUR     DC    H'4'
         SPACE
         SPACE
         SPACE
*  EXTERNAL ADDRESSES
         SPACE
AEVDSNY  DC    V(IHIIOREV)
EARRINN  DC    V(IHIIDEAI)
         EJECT
FAS      DSECT
         COPY  FSAREA
         END
