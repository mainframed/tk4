         MACRO
&L       ENDM  &DUMMY,&DISP=FREEMAIN,&PFX=SAVE,&NEXT=,&RC=,&RC1=
.*
.*             THIS MACRO WAS WRITTEN BY SHMUEL METZ
.*                                       COMPUTER CENTER
.*                                       TECHNION,
.*                                       ISRAEL INSTITUTE OF TECHNOLOGY
.*                                       TECHNION CITY, HAIFA
.*                                       ISRAEL
.*
.*             IT IS NOT TO BE DISTRIBUTED WITHOUT EXPLICIT PERMISSION.
.*
.*             PERMISSION IS GRANTED TO GERHARD POSTPISCHIL
.*                                      AMS COMPUTER CENTER
.*
.*                                      THOMAS A. LITTLE
.*                                      UNIVERSITY OF CHICAGO
.*
.*                                      UNIVERSITY OF WATERLOO
.*
.*                                      SHARE,
.*                                       ALL WORKING GROUPS AND
.*                                       COMITTEES THEREOF.
.*
.*             ANYONE MAKING ENHANCEMENTS IS REQUEST TO FORWARD THEM
.*             TO ME, IDENTIFIED BY "** XXX **' IN COLUMN 63,
.*             WHERE XXX IS THE SHARE INSTALLATION CODE.
.*
         LCLC  &LB,&SV
&LB      SETC  '&L'
&SV      SETC  '&PFX'
         AIF   ('&DISP' EQ 'RETAIN').RETAIN
&L       L     R0,&SV.SPLN
         LR    R1,R13
         L     R13,&SV.13
&LB      SETC  ''
.RETAIN  AIF   (T'&NEXT NE 'O').XCTL
         AIF   (T'&RC EQ 'O').T1RC1
         AIF   ('&RC'(1,1) EQ '(').SVRC
         AIF   (T'&RC EQ 'N').T1RC1
         MNOTE 12,'INVALID RETURN CODE &RC'
         MEXIT
.SVRC    ANOP  ,
&LB      ST    &RC(1),&SV.15
&LB      SETC  ''
.T1RC1   AIF   (T'&RC1 EQ 'O').EXIT
         AIF   ('&RC1'(1,1) EQ '(').SVRC1
         AIF   (T'&RC1 EQ 'N').EXIT
         MNOTE 12,'INVALID SECONDARY RETURN CODE &RC1'
         MEXIT
.SVRC1   ANOP  ,
&LB      ST    &RC1(1),&SV.1
&LB      SETC  ''
         AGO   .EXIT
.XCTL    AIF   ('&DISP' NE 'FREEMAIN').XCTL2
&LB   FREEMAIN R,LV=(0),A=(1)
&LB      SETC  ''
.XCTL2   ANOP  ,
&LB      LA    R15,X@&SYSNDX
         LM    R0,R12,&SV.0
         MVI   &SV.14,X'FF' .           FLAG AS LAST SAVE AREA
         XCTL  SF=(E,(15))
X@&SYSNDX XCTL EP=&NEXT,SF=L
         MEXIT
.EXIT    AIF   ('&DISP' NE 'FREEMAIN').NOFM
&LB   FREEMAIN R,LV=(0),A=(1)
&LB      SETC  ''
.NOFM    ANOP  ,
&LB      LM    R14,R12,&SV.14
         MVI   &SV.14,X'FF' .           FLAG AS LAST SAVE AREA
         AIF   (T'&RC NE 'N').T2RC1
         AIF   ('&RC'(1,1) EQ '(').T2RC1                          *TSM*
         AIF   ('&RC' EQ '0').EFES
         LA    R15,&RC
.T2RC1   AIF   (T'&RC1 NE 'N').BR
         AIF   ('&RC1' EQ '0').EFES1
         LA    R1,&RC1
.BR      BR    R14
         MEXIT
.EFES    SR    R15,R15
         AGO   .T2RC1
.EFES1   SR    R1,R1
         BR    R14
.END     MEND
         MEXIT
