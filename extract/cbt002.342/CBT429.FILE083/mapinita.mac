*                                                               20AUG92
**  Read a specific DSCB                                        20AUG92
**                                                              20AUG92
**     Now using CVAF macros to access the VTOC DSCBs           20AUG92
**                                                              20AUG92
**       Input                                                  20AUG92
**             R2  -  ptr to 5 byte seek address (CCHHR)        20AUG92
**             R3  -  ptr to 148 byte buffer                    20AUG92
**                    +0   -  44 byte DSN (DSCB key)            20AUG92
**                    +44  -  96 byte DSCB                      20AUG92
**                    +140 -  8 byte MBBCCHHR of DSCB           20AUG92
**             R14 -  return address                            20AUG92
**       Output                                                 20AUG92
**             R15 -  0  DSCB moved to buffer                   20AUG92
**                    4  EOF occured                            20AUG92
*                                                               20AUG92
RDSCBDIR DS    0H                                               20AUG92
         STM   R14,R12,12(R13)      Save registers              20AUG92
         BAL   R14,IMAP0228         Get new save area           20AUG92
         LA    R4,CV@BUFF           point to buffer             20AUG92
         USING BFLHDR,R4                                        20AUG92
         MVI   BFLHNOE,1            set number of entries       20AUG92
         OI    BFLHFL,BFLHDSCB      indicate this is DSCB       20AUG92
         LA    R4,BFLHLN(,R4)       point at first element      20AUG92
         USING BFLE,R4                                          20AUG92
         MVC   BFLEARG(5),0(R2)     set CCHHR                   20AUG92
         MVI   BFLEFL,BFLECHR       incideat arg is CCHHR       20AUG92
         MVI   BFLELTH,140          show big DSCB               20AUG92
         ST    R3,BFLEBUF                                       20AUG92
         DROP  R4                                               20AUG92
         LA    R4,CV@BUFF           point at buffer list        20AUG92
         L     R7,1136(,R11)        get UCB address             20AUG92
         CVAFDIR ACCESS=READ,                                   20AUG92X
               BUFLIST=(R4),                                    20AUG92X
               UCB=(R7),                                        20AUG92X
               DSN=(R11),                                       20AUG92X
               IOAREA=KEEP,                                     20AUG92X
               BRANCH=(YES,PGM),                                20AUG92X
               MF=(E,CV@CVPL)                                   20AUG92
         LTR   R15,R15              any error?                  20AUG92
         BNZ   CV$ERR               yes error                   20AUG92
         MVC   140(3,R3),=XL3'00'   set MBB                     20AUG92
         MVC   143(5,R3),BFLEARG-BFLE+BFLHLN(R4)  set CCHHR     20AUG92
         B     IMAP02C8             all done                    20AUG92
         EJECT                                                  20AUG92
*                                                               20AUG92
**  Read next DSCB                                              20AUG92
**                                                              20AUG92
**     Now using CVAF macros to access the VTOC DSCBs           20AUG92
**                                                              20AUG92
**       Input                                                  20AUG92
**             R3  -  ptr to 148 byte buffer                    20AUG92
**                    +0   -  44 byte DSN (DSCB key)            20AUG92
**                    +44  -  96 byte DSCB                      20AUG92
**                    +140 -  8 byte MBBCCHHR of DSCB           20AUG92
**             R14 -  return address                            20AUG92
**       Output                                                 20AUG92
**             R15 -  0  DSCB moved to buffer                   20AUG92
**                    4  EOF occured                            20AUG92
*                                                               20AUG92
RDSCBNXT DS    0H                                               20AUG92
         STM   R14,R12,12(R13)      Save registers              20AUG92
         BAL   R14,IMAP0228         Get new save area           20AUG92
*                                                               20AUG92
**       Is this the first time                                 20AUG92
*                                                               20AUG92
         TM    BFGFLAG,CVF@1ST      Has first pass occured      20AUG92
         BZ    CV$G1ST              No, so do initialization    20AUG92
*                                                               20AUG92
**       Any left in current list                               20AUG92
*                                                               20AUG92
         L     R4,CV@NEXT           Point at next entry         20AUG92
         OC    BFLEARG-BFLE(5,R4),BFLEARG-BFLE(R4) Present??    20AUG92
         BNZ   CV$BFDO              Yes, so go and move it      20AUG92
         B     CV$GMORE             if not then get next load   20AUG92
*                                                               20AUG92
**       First time through, build buffer list for 100 DSCBs    20AUG92
**       The 101st buffer entry is to stop the processing       20AUG92
*                                                               20AUG92
CV$G1ST  OI    BFGFLAG,CVF@1ST      flag that first occured     20AUG92
         MVC   CV@CVPL(CV$CVPLL),CV$CVPL  init parm list        20AUG92
         L     R0,=A(100*140)       get size of buffer area     20AUG92
         GETMAIN R,LV=(0)                                       20AUG92
         ST    R1,CV@BFAR           save address of buffer area 20AUG92
         LR    R7,R1                setup addressability        20AUG92
         L     R0,=A(BFLHLN+101*BFLELN) get size of storage     20AUG92
         GETMAIN R,LV=(0)                                       20AUG92
         ST    R1,CV@BFLST          save address of buffer list 20AUG92
         LR    R4,R1                setup addressability        20AUG92
         USING BFLHDR,R4                                        20AUG92
         LR    R0,R1                move address to correct reg 20AUG92
         L     R1,=A(BFLHLN+101*BFLELN) get length              20AUG92
         SR    R14,R14                                          20AUG92
         SR    R15,R15                                          20AUG92
         MVCL  R0,R14               clear out area              20AUG92
         MVI   BFLHNOE,100          set number of entries       20AUG92
         OI    BFLHFL,BFLHDSCB      indicate this is DSCB       20AUG92
         LA    R4,BFLHLN(,R4)       point to first entry        20AUG92
         USING BFLE,R4                                          20AUG92
         LA    R5,100               set count to do             20AUG92
CV$BFLP  DS    0H                                               20AUG92
         OI    BFLEFL,BFLECHR       indicate for cchhr          20AUG92
         MVI   BFLELTH,140          set length of buffer        20AUG92
         ST    R7,BFLEBUF           point at buffer             20AUG92
         LA    R7,140(,R7)                                      20AUG92
         LA    R4,BFLELN(,R4)       point to next entry         20AUG92
         BCT   R5,CV$BFLP           loop till done              20AUG92
         DROP  R4                                               20AUG92
*                                                               20AUG92
**       Issue the CVAF macro to get the data                   20AUG92
*                                                               20AUG92
         L     R4,CV@BFLST          point at buffer list        20AUG92
         L     R7,1136(,R11)        get UCB address             20AUG92
         CVAFSEQ ACCESS=GTEQ,                                   20AUG92X
               BUFLIST=(R4),                                    20AUG92X
               UCB=(R7),                                        20AUG92X
               IOAREA=KEEP,                                     20AUG92X
               BRANCH=(YES,PGM),                                20AUG92X
               MF=(E,CV@CVPL)                                   20AUG92
         LA    R4,BFLHLN(,R4)       point at first element      20AUG92
         ST    R4,CV@NEXT           save address of next elemnet20AUG92
         ST    R15,CV@CVRC          save return code for next timeAUG92
         CH    R15,=H'4'            see if real error           20AUG92
         BL    CV$BFDO              nope, just process          20AUG92
         BH    CV$ERR               go and process error        20AUG92
         CLI   CV@CVPL+CVSTAT-CVPL,X'20' did eof occur??        20AUG92
         BNE   CV$ERR               no, so process real error   20AUG92
         OI    BFGFLAG,CVFEOD       remember eod occured        07OCT92
         OC    BFLEARG-BFLE(5,R4),BFLEARG-BFLE(R4)              20AUG92
         BZ    CV$EOD               eod occured                 20AUG92
         B     CV$BFDO              go and give entry           20AUG92
*                                                               20AUG92
**       Have completed passing items in the list,              20AUG92
**       now see if there is any more data to read              20AUG92
*                                                               20AUG92
CV$GMORE DS    0H                                               20AUG92
         TM    BFGFLAG,CVFEOD       did eod occur??             07OCT92
         BO    CV$EOD               yes - process it            07OCT92
CV$GM1   L     R4,CV@BFLST          point at buffer list        20AUG92
ZZZ      EQU   BFLEARG-BFLE+BFLHLN+99*BFLELN                    20AUG92
         MVC   BFLEARG-BFLE+BFLHLN(5,R4),ZZZ(R4) set last CCHHR 20AUG92
         L     R7,1136(,R11)        get UCB address             20AUG92
         CVAFSEQ ACCESS=GT,                                     20AUG92X
               BUFLIST=(R4),                                    20AUG92X
               UCB=(R7),                                        20AUG92X
               DSN=0,                                           20AUG92X
               IOAREA=KEEP,                                     20AUG92X
               BRANCH=(YES,PGM),                                20AUG92X
               MF=(E,CV@CVPL)                                   20AUG92
         LA    R4,BFLHLN(,R4)       point at first element      20AUG92
         ST    R4,CV@NEXT           save address of next elemnet20AUG92
         ST    R15,CV@CVRC          save return code for next timeAUG92
         CH    R15,=H'4'            see if real error           20AUG92
         BL    CV$BFDO              nope, just process          20AUG92
         BH    CV$ERR               go and process error        20AUG92
         CLI   CV@CVPL+CVSTAT-CVPL,X'20' did eof occur??        20AUG92
         BNE   CV$ERR               no, so process real error   20AUG92
         OI    BFGFLAG,CVFEOD       remember eod occured        07OCT92
         OC    BFLEARG-BFLE(5,R4),BFLEARG-BFLE(R4)              20AUG92
         BZ    CV$EOD               eod occured on first DSCB   20AUG92
*                                                               20AUG92
**       R4 points to entry to move                             20AUG92
*                                                               20AUG92
CV$BFDO  DS    0H                                               20AUG92
         USING BFLE,R4                                          20AUG92
         L     R5,BFLEBUF           point to buffer             20AUG92
         MVC   0(140,R3),0(R5)      move in DSCB                20AUG92
         MVC   140(3,R3),=XL3'00'   set MBB                     20AUG92
         MVC   143(5,R3),BFLEARG    set CCHHR                   20AUG92
         LA    R4,BFLELN(,R4)       point to next buffer element20AUG92
         ST    R4,CV@NEXT           save it                     20AUG92
         SR    R15,R15              set good return code        20AUG92
         B     IMAP02E8             Now just return             20AUG92
*                                                               20AUG92
**       EOD detected from CVAF, return to proper place         20AUG92
*                                                               20AUG92
CV$EOD   DS    0H                                               20AUG92
         L     R0,=A(100*140)       get size of buffer area     20AUG92
         L     R1,CV@BFAR           get address of buffer area  20AUG92
         FREEMAIN R,LV=(0),A=(1)                                20AUG92
         L     R0,=A(BFLHLN+101*BFLELN) get size of storage     20AUG92
         L     R1,CV@BFLST          get address of buffer list  20AUG92
         FREEMAIN R,LV=(0),A=(1)                                20AUG92
         NI    BFGFLAG,255-CVF@1ST-CVFEOD  reset flags          07OCT92
         LA    R15,4                set EOD return code         20AUG92
         B     IMAP02E8                                         20AUG92
*                                                               20AUG92
**       Error occured, R15 has return code CVSTAT has status   20AUG92
*                                                               20AUG92
CV$ERR   DS    0H                                               20AUG92
         LR    R2,R15               save return code            20AUG92
         LA    R7,75*2              set message number (72)     20AUG92
         BAL   R14,IMAP04B0         format message              20AUG92
         LR    R0,R2                set value to convert        20AUG92
         BAL   R14,IMAP0728         convert to decimal          20AUG92
         MVC   1853+28(2,R11),2618(R11)  move in 2 digits       20AUG92
         LA    R2,CV@CVPL+CVSTAT-CVPL point to status field     20AUG92
         BAL   R14,IMAP09A8         convert to hex              20AUG92
         MVC   1853+37(2,R11),2632(R11)  move in data           20AUG92
         BAL   R14,IMAP0328         output message              20AUG92
         DC    H'0'                 **NO IDEA HOW TO ABORT**    20AUG92
*                                                               20AUG92
**       Constants                                              20AUG92
*                                                               20AUG92
         LTORG                                                  20AUG92
CV$CVPL  CVAFSEQ ACCESS=GT,                                     20AUG92X
               BUFLIST=1,                                       20AUG92X
               UCB=(XX),                                        20AUG92X
               IOAREA=KEEP,                                     20AUG92X
               BRANCH=(YES,PGM),                                20AUG92X
               MF=(L)                                           20AUG92
CV$CVPLL EQU   *-CV$CVPL                                        20AUG92
