         TITLE 'MAPDEV - TABLE OF DEVICE CONSTANTS'
***********************************************************************
*
*  FOLLOWING ZAPS INTEGRATED IN SOURCE:
*      ZAP3  -  3350 SUPPORT
*      ZAP4  -  3375 SUPPORT
*      ZAP5  -  3380 SUPPORT
*
***********************************************************************
*
*  FOLLOWING ENHANCEMENTS HAVE BEEN ADDED IN SOURCE:
*      1.  3390'S HAVE BEEN ADDED
*      2.  TABLE ORDERED IN DECREASING DEVICE CODE (NEWEST FIRST)
*      3.  CONSTANTS ARE NOT USED ANY MORE. MAPINIT GETS INFO FROM
*          THE SYSTEM DEVTYPE SERVICE.
*
*   18MAY92   ADD ANOTHER FIELD TO TABLE TO LIST NUMBER OF ALTERNATE
*             CYLINDERS, SINCE THIS IS INCLUDED IN THE COUNT RETURNED
*             BY DEVTYPE.
*   04AUG92   CHANGE FUNCTION. MAKE ACTIVE CSECT. CALL VIA R14, WITH
*             R1 CONTAINING TYPE CODE (ALL 4 BYTES OR JUST LAST BYTE).
*             WILL RETURN WITH POINTER TO CONSTANTS BLOCK IN R1.
*
***********************************************************************
*
* EACH ENTRY OF THE TABLE IN MAPDEV IS FORMATTED AS FOLLOWS:
*
*             FIELD NAME                      BYTES
*      DEVICE TYPE                              0
*      DEVICE DESCRIPTION                      1-15
*      SCALE FACTORS FOR CCHH OF DEVICE       16-23
*        FACTOR TO CONVERT REL-TRACK TO CCHH    16-17
*             (THIS IS USED FOR DEVICE THAT
*              HAVE HH AS A SINGLE NUMBER,
*              OTHERWISE SHOULD BE ZERO)
*             (THIS IS REALLY TRACKS PER CYL
*              FOR NORMAL DEVICES)
*        FACTOR TO CONVERT CC TO REL-TRACK      18-19
*             (THIS IS USED FOR DEVICES THAT
*              SPLIT THE FUNCTION OF HH INTO
*              H(1), AND H(2) WHICH THEN HAVE
*              DIFFERENT MEANINGS
*             (THIS IS REALLY TRACKS PER CYL
*              FOR NORMAL DEVICES)
*        FACTOR TO CONVERT H(1) TO REL-TRACK    20-21
*             (SHOULD BE ZERO IF H(1) NOT
*              USED)
*        FACTOR TO CONVERT H(2) TO REL-TRACK    22-23
*             (IN CONVERSIONFROM REL-TRACK TO
*              CCHH, THIS IS ASSUMED TO BE '1')
*      NUMBER OF TRACKS PER CYLINDER          24-25
*      MAX RELATIVE TRACK FOR DEVICE          26-27
*      NUMBER OF ALTERNATE CYLINDERS          28-29             18MAY92
*
***********************************************************************
MAPDEV   CSECT
         ENTRY MDVPATCH
         ENTRY MDEVEND
         STM   R14,R12,12(R13)         SAVE REGISTERS           04AUG92
         LR    R12,R15                 SET NEW BASE REGISTER    04AUG92
         USING MAPDEV,R12              SETUP ADDRESSABILITY     04AUG92
         LA    R2,MAPDEVT              POINT AT TABLE           04AUG92
MD1      EX    R1,MDCLI                MATCH DEVICETYPE+3?      04AUG92
         BE    MD2                     FOUND OK ENTRY           04AUG92
         CLI   0(R2),X'FF'             END OF TABLE?            04AUG92
         BE    MD2                     IS UNKNOWN ENTRY         04AUG92
         LA    R2,MDENTLN(,R2)         GO TO NEXT ENTRY         04AUG92
         B     MD1                     LOOP AGAIN               04AUG92
MDCLI    CLI   0(R2),*-*               **EXECUTED**             04AUG92
MD2      ST    R2,12+4+4+4(R13)        SAVE IN R1 SLOT          04AUG92
         LM    R14,R12,12(R13)         RESTORE REGISTERS        04AUG92
         BR    R14                                              04AUG92
         DROP  R12                                              04AUG92
*
MAPDEVT  DC    X'0F'
         DC    C'3390  DISK PACK'
         DC    H'15,15,1,1,15,16680,1'
MDENTLN  EQU   *-MAPDEVT                                        04AUG92
*
         DC    X'0E'
         DC    C'3380  DISK PACK'
         DC    H'15,15,1,1,15,13275,1'
*
         DC    X'0D'
         DC    C'3330-1 DISK    '
         DC    H'19,19,1,1,19,15352,7'
*
         DC    X'0C'
         DC    C'3375  DISK PACK'
         DC    H'12,12,1,1,12,11508,0'
*
         DC    X'0B'
         DC    C'3350  DISK PACK'
         DC    H'30,30,1,1,30,16650,5'
*
         DC    X'0A'
         DC    C'3340  DISK PACK'
         DC    H'12,12,1,1,12,4176,1'
*
         DC    X'09'
         DC    C'3330  DISK PACK'
         DC    H'19,19,1,1,19,7676,7'
*
         DC    X'08'
         DC    C'2314  DISK PACK'
         DC    H'0,20,1,1,20,4000,0'
*
         DC    X'07'
         DC    C'2305-2 DISK    '
         DC    H'0,8,0,1,8,768,0'
*
         DC    X'06'
         DC    C'2305-1 DISK    '
         DC    H'0,8,0,1,8,384,0'
*
         DC    X'05'
         DC    C'2321  DATA CELL'
         DC    H'0,46,0,1,46,11500,0'
*
         DC    X'04'
         DC    C'2302  DISK FILE'
         DC    H'0,46,0,1,46,11500,0'
*
         DC    X'03'
         DC    C'2303  DRUM     '
         DC    H'0,10,0,1,10,800,0'
*
         DC    X'02'
         DC    C'2301  DRUM     '
         DC    H'0,8,0,1,8,200,0'
*
         DC    X'01'
         DC    C'2311  DISK PACK'
         DC    H'0,10,0,1,10,2000,0'
MDEVEND  EQU   *
         DC    X'FF'                                            04AUG92
         DC    C'UNKNOWN DEVICE '         HOPEFULLY SUPPLIED    04AUG92
         DC    H'15,15,1,1,15,16680,1'           BY DEVTYP      04AUG92
MDVPATCH EQU   *
         DC    5XL(MDENTLN)'00'           SPACE FOR MORE        04AUG92
         REQU  ,                                                04AUG92
         END
