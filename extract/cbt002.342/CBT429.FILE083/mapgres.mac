MAPGRES  CSECT
***********************************************************************
*
*   CSECT DISASSEMBLED FROM IEHMAPIN TO ADD XA LEVEL CODE
*
*   PURPOSE OF THE ROUTINE IS APPARENTLY TO FIND THE IPL VOLUME
*   UCB ADDRESS
*
*   APPARENT EXIT CONDITIONS
*     R15=0   UCB FOUND  R1=UCB ADDRESS
*     R15=4   UCB NOT FOUND
*
*   GUY ALBERTELLI   5/3/86
*
*   FIXED FOR UCBSCAN LOOKUP TO FIND DYNAMIC UCBS.    S.GOLOB  8/28/95
*     ACTION DEPENDS ON THE LEVEL OF THE RUNNING SYSTEM.
*
***********************************************************************
         ENTRY GETSYSRS
         USING *,R12
GETSYSRS EQU   *
A000000  SAVE  (14,12),,'MAPGRES  &SYSDATE &SYSTIME'             GLA-XA
         BAL   R14,552(,R6)                PERFORM
         NOPR  R0
         LR    R12,R15
         L     R1,16                       CVT ADDRESS
CVTDCB   EQU   X'74'               CVT OPERATING SYSTEM TYPE     GLA-XA
         TM    CVTDCB(R1),X'93'    IS THIS XA                    GLA-XA
         BO    NEWXA               YES                           GLA-XA
         L     R2,40(R1)
         LA    R3,2
         L     R15,1068(,R11)
A000038  LH    R1,0(R2)
         LTR   R1,R1
         BZ    A000060
         NOPR  R0
         CL    R1,1552(,R11)
         BE    A000070
         NR    R1,R15
         NOPR  R0
         TM    18(R1),X'20'                IS THIS DASD UCB
         BZ    A000060
         TM    3(R1),X'02'                 IS THIS IPL VOLUME
         BO    A000068
A000060  ALR   R2,R3
         B     A000038
         NOPR  R0
A000068  SLR   R15,R15
         B     744(,R6)
         NOPR  R0
A000070  LA    R7,92
         BAL   R14,1168(,R6)               PERFORM
         LA    R15,4
         B     744(,R6)
*****************************************************************GLA-XA
*                                                                GLA-XA
*        XA CODE TO FIND UCB ADDRESS                             GLA-XA
*                                                                GLA-XA
*****************************************************************GLA-XA
CVTOSLV0 EQU   X'4F0'              FIRST OS LEVEL FLAG IN CVT    SG-ESA
NEWXA    DS    0H                                                GLA-XA
         L     R4,16                       CVT ADDRESS           SG-ESA
         TM    CVTDCB(R4),X'9B'    IS THIS ESA                   SG-ESA
         BNO   DOXARTNE            NO. DO THE XA ROUTINE         SG-ESA
         TM    CVTOSLV0(R4),X'08'  ARE UCB SERVICES AVAILABLE?   SG-ESA
         BO    NEWESA              USE UCB SCAN ROUTINE          SG-ESA
DOXARTNE DS    0H                  XA OR ESA RELEASE 3           SG-ESA
         GETMAIN R,LV=116          GET WORK AREA                 GLA-XA
         LR    R5,R1               SAVE ITS ADDRESS              GLA-XA
         L     R4,16               ADDRESS OF CVT                GLA-XA
CVTUCBSC EQU   X'434'              IOSLOOKUP RTN (XA-ONLY)       GLA-XA
         L     R4,CVTUCBSC(R4)     LOOKUP ROUTINE ADDRESS        GLA-XA
         XC    0(116,R5),0(R5)     CLEAR WORK AREA               GLA-XA
         ST    R5,100(R5)                                        GLA-XA
         LA    R1,=X'20'           SPECIFY ONLY DASD             GLA-XA
         ST    R1,104(R5)                                        GLA-XA
         LA    R1,112(R5)          WORD TO PUT ADDRESS OF UCB    GLA-XA
         ST    R1,108(R5)                                        GLA-XA
         OI    108(R5),X'80'       31 BIT PARM END SET           GLA-XA
UCBLOOP1 LA    R1,100(R5)          POINT AT PARM LIST            GLA-XA
         LR    R15,R4              GET MODULE ADDRESS            GLA-XA
         BALR  R14,R15             CALL MVS                      GLA-XA
         LTR   R15,R15             ANY ERROR                     GLA-XA
         BNZ   UCBNFND             IF ANY THEN NO IPL VOLUME ??? GLA-XA
         L     R1,112(R5)          GET ADDR OF UCB               GLA-XA
         TM    3(R1),X'02'         IPL VOLUME                    GLA-XA
         BZ    UCBLOOP1            NO SO SKIP TO NEXT            GLA-XA
UCBFND   LR    R2,R1               SAVEADDRESS                   GLA-XA
         FREEMAIN R,LV=116,A=(5)   FREE SPACE                    GLA-XA
         LR    R1,R2               RESTORE ADDRESS               GLA-XA
         B     A000068             GO AND EXIT NORMALLY          GLA-XA
UCBNFND  FREEMAIN R,LV=116,A=(5)   FREE SPACE                    GLA-XA
         B     A000070             IF ANY THEN NO IPL VOLUME ??? GLA-XA
*****************************************************************SG-ESA
*                                                                SG-ESA
*        ESA CODE TO FIND UCB ADDRESS                            SG-ESA
*      (COMMENTED CODE ASSEMBLES ON MVS/ESA 4.3.0 OR LATER)      SG-ESA
*      ( THIS IS INCLUDED SO YOU CAN SEE THE LOGIC....)          SG-ESA
*                                                                SG-ESA
*****************************************************************SG-ESA
*                                                                SG-ESA
*NEWESA   DS    0H                                               SG-ESA
*         GETMAIN R,LV=WKLEN                                     SG-ESA
*         LR    R5,R1                                            SG-ESA
*         USING WKAREA,R5                                        SG-ESA
*****************************************************************SG-ESA
**        SCAN UCB                                              *SG-ESA
*****************************************************************SG-ESA
*RESCANLP DS    0H                                               SG-ESA
*         IOCINFO IOCTOKEN=TOKEN,MF=(E,IOCINFP)                  SG-ESA
*         XC    SCANWORK,SCANWORK                                SG-ESA
*UCBLOOP  DS    0H                                               SG-ESA
****    PLACE CONTINUATION CHARACTER IN THE MACRO....            SG-ESA
*         UCBSCAN UCBAREA=UCBCOPY,WORKAREA=SCANWORK,DYNAMIC=YES, S
*               DEVCLASS=DASD,IOCTOKEN=TOKEN,MF=(E,UCBSLST)      SG-ESA
*         LTR   R15,R15           WAS A UCB RETURNED?            SG-ESA
*         BNZ   EUCBNFND          ERROR-THEN REPORT NO IPL VOLUMESG-ESA
*         LA    R1,UCBCOPY        ADDRESSABILITY TO UCBCOPY      SG-ESA
*         USING UCBCMSEG,R1       UCB COMMON SEGMENT DSECT       SG-ESA
*         TM    UCBSTAT,UCBSYSR   IPL VOLUME?                    SG-ESA
*         BZ    UCBLOOP           NO.  SKIP TO NEXT              SG-ESA
*EUCBFND  LR    R2,R1             SAVE ADDRESS                   SG-ESA
*         FREEMAIN R,LV=WKLEN,A=(5)                              SG-ESA
*         LR    R1,R2             RESTORE ADDRESS                SG-ESA
*         B     A000068           GO AND EXIT NORMALLY           SG-ESA
*EUCBNFND FREEMAIN R,LV=WKLEN,A=(5)                              SG-ESA
*         B     A000070           REPORT NO IPL VOLUME           SG-ESA
*WKAREA   DSECT                                                  SG-ESA
*WORKXX   DS    0F                                               SG-ESA
*UCBLIS   UCBSCAN MF=(L,UCBSLST)                                 SG-ESA
*IOCINF   IOCINFO MF=(L,IOCINFP)                                 SG-ESA
*TOKEN    DS    CL48                                             SG-ESA
*UCBCOPY  DS    CL48                                             SG-ESA
*SCANWORK DS    CL100                                            SG-ESA
*WKLEN    EQU   *-WORKXX                                         SG-ESA
* ------------------------------------------------------------- *SG-ESA
*     THE FOLLOWING ESA CODE ASSEMBLES ON LOWER LEVEL SYSTEMS.  *SG-ESA
* ------------------------------------------------------------- *SG-ESA
NEWESA   DS    0H                                                SG-ESA
         GETMAIN R,LV=352                                        SG-ESA
         LR    R5,R1            SAVE ADDRESS OF GETMAINED STG    SG-ESA
         USING WKAREA,R5                                         SG-ESA
RESCANLP DS    0H                                                SG-ESA
*     EXPANSION OF IOCINFO MF=(E,IOCINFP),IOCTOKEN=TOKEN         SG-ESA
M00M0010 DS    0H                                                SG-ESA
         LA    1,IOCINFP        LOCATE ARG PARMS                 SG-ESA
         XC    0(36,1),0(1)     INITIALIZE                       SG-ESA
         MVI   0(1),1           INPUT XVERSION XL1               SG-ESA
         LA    14,TOKEN         OUTPUT XIOCTOKEN A               SG-ESA
         ST    14,4(,1)         OUTPUT XIOCTOKEN A               SG-ESA
         OI    24(1),B'10000000'   OUTPUT BL1                    SG-ESA
         LA    0,7              SET IOCINFO FUNCTION CODE        SG-ESA
         L     14,16(0,0)       GET CVT ADDRESS                  SG-ESA
         L     14,772(14,0)     GET SFT ADDRESS                  SG-ESA
         L     14,312(14,0)     GET LX/EX FOR UCB SERVICES       SG-ESA
         PC    0(14)            PC TO UCB SERVICES               SG-ESA
*     END OF EXPANSION                                           SG-ESA
         XC    SCANWORK,SCANWORK     CLEAR WORK AREA             SG-ESA
UCBLOOP  DS    0H                                                SG-ESA
*     EXPANSION OF UCBSCAN MACRO                                 SG-ESA
M00M0011 DS    0H                                                SG-ESA
         LA    1,UCBSLST        LOCATE ARG PARMS                 SG-ESA
         XC    0(116,1),0(1)    INITIALIZE                       SG-ESA
         MVI   0(1),1           INPUT VERSION XL1                SG-ESA
         LA    14,UCBCOPY       INPUT XUCBAREA A                 SG-ESA
         ST    14,4(,1)         INPUT XUCBAREA A                 SG-ESA
         LA    14,SCANWORK      INOUT XWORKAREA A                SG-ESA
         ST    14,12(,1)        INOUT XWORKAREA A                SG-ESA
         MVC   66(2,1),=FL2'0'  INPUT XDCELEN XL2                SG-ESA
         LA    14,TOKEN         INOUT XIOCTOKEN A                SG-ESA
         ST    14,88(,1)        INOUT XIOCTOKEN A                SG-ESA
         MVI   96(1),32         INPUT XDEVCLASS AL1              SG-ESA
         OI    97(1),B'10100000'   INPUT BL1                     SG-ESA
         LA    0,2              SET UCBSCAN FUNCTION CODE        SG-ESA
         L     14,16(0,0)       GET CVT ADDRESS                  SG-ESA
         L     14,772(14,0)     GET SFT ADDRESS                  SG-ESA
         L     14,312(14,0)     GET LX/EX FOR UCB SERVICES       SG-ESA
         PC    0(14)            PC TO UCB SERVICES               SG-ESA
*     END OF EXPANSION                                           SG-ESA
         LTR   R15,R15          WAS A UCB RETURNED ?             SG-ESA
         BNZ   EUCBNFND         ERROR - REPORT NO IPL VOLUME     SG-ESA
         LA    R1,UCBCOPY       ADDRESSABILITY TO UCBCOPY        SG-ESA
         TM    3(R1),X'02'      IS THIS IPL VOLUME ?             SG-ESA
         BZ    UCBLOOP          NO.  SKIP TO NEXT                SG-ESA
EUCBFND  LR    R2,R1            SAVE ADDRESS                     SG-ESA
         FREEMAIN R,LV=352,A=(5)                                 SG-ESA
         LR    R1,R2            RESTORE ADDRESS                  SG-ESA
         B     A000068          GO AND EXIT NORMALLY             SG-ESA
EUCBNFND FREEMAIN R,LV=352,A=(5)                                 SG-ESA
         B     A000070          REPORT NO IPL VOLUME             SG-ESA
WKAREA   DSECT                                                   SG-ESA
WORKXX   DS    0F                                                SG-ESA
*     EXPANSION OF UCBSCAN - LIST VERSION                        SG-ESA
UCBLIS   DS    0H                                                SG-ESA
UCBSLST  DS    0D               UCBSCAN PARM LIST                SG-ESA
         DS    CL116                                             SG-ESA
UCBSLSTL EQU   *-UCBSLST        LENGTH OF PLIST                  SG-ESA
*     END OF EXPANSION                                           SG-ESA
*     EXPANSION OF IOCINFO - LIST VERSION                        SG-ESA
IOCINF   DS    0H                                                SG-ESA
IOCINFP  DS    0D               IOCINFO PARM LIST                SG-ESA
         DS    CL36                                              SG-ESA
IOCINFPL EQU   *-IOCINFP        LENGTH OF PLIST                  SG-ESA
*     END OF EXPANSION                                           SG-ESA
TOKEN    DS    CL48                                              SG-ESA
UCBCOPY  DS    CL48                                              SG-ESA
SCANWORK DS    CL100                                             SG-ESA
WKLEN    EQU   *-WORKXX                                          SG-ESA
R0       EQU   0                                                 GLA-XA
R1       EQU   1                                                 GLA-XA
R2       EQU   2                                                 GLA-XA
R3       EQU   3                                                 GLA-XA
R4       EQU   4                                                 GLA-XA
R5       EQU   5                                                 GLA-XA
R6       EQU   6                                                 GLA-XA
R7       EQU   7                                                 GLA-XA
R8       EQU   8                                                 GLA-XA
R9       EQU   9                                                 GLA-XA
R10      EQU   10                                                GLA-XA
R11      EQU   11                                                GLA-XA
R12      EQU   12                                                GLA-XA
R13      EQU   13                                                GLA-XA
R14      EQU   14                                                GLA-XA
R15      EQU   15                                                GLA-XA
         IEFUCBOB                                                SG-ESA
         END
