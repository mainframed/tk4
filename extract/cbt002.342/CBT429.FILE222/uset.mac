USET     TITLE 'USET - PRIV. CMD. TO RESET USERID FROM PREFIX.'
*%A PPLINK           AC(1)
         SPACE 1
USET     CSECT
*        REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)
         LR    R12,R15
         LR    R11,R1              PRESERVE CPPL POINTER.
         USING USET,R12
         LA    R2,SAVE
         ST    R2,8(,R13)
         ST    R13,SAVE+4
         LR    R13,R2
         SPACE 1
         L     R1,16               POINT TO CVT.
         L     R1,0(,R1)           POINT TO TCB/ASCB WORDS
         L     R10,12(,R1)         POINT TO ASCB.
         L     R9,4(,R1)           POINT TO TCB.
         L     R1,X'B4'(,R9)       POINT TO JSCB.
         L     R2,X'108'(,R1)      POINT TO PSCB.
         LA    R2,0(,R2)           CLEAR HIGH BYTE.
         LTR   R2,R2               ANY PSCB?
         BZ    EOJ8                B IF NOT.  NOT TSO.
         USING PSCB,R2
         TM    PSCBATR1,PSCBCTRL   OPER USER?
         BZ    EOJ4                B IF NOT.  NO MESSAGE.
*        BO    CONT0               B IF YES.
*        VTPUT 'YOU ARE NOT AUTHORIZED TO USE THIS COMMAND.'
*        B     EOJ8
CONT0    LTR   R11,R11             ANY CPPL?
         BZ    CONT1               NO.
         L     R1,0(,R11)          PT TO CMD BUFFER.
         LTR   R1,R1               ANY CMD BUFFER?
         BZ    CONT1               B IF NOT.
         LH    R3,0(,R1)           LOAD LENGTH.
         CH    R3,=H'4'            ANYTHING?
         BNH   CONT1               B IF NOT.
         LH    R3,2(,R1)           LOAD OFFSET.
         AR    R1,R3               POINT ...
         LA    R1,4(,R1)           ... TO CHAR.
         OI    0(R1),C' '          UPPERCASE IT.
         CLI   0(R1),C'A'          CHANGE ALL FIELDS?
         BNE   CONT1A              B IF NOT.
         OI    FLAG,ALLFLG         SET ALL FIELDS FLAG.
         B     CONT1               CONTINUE.
CONT1A   CLI   0(R1),C'N'          NOCONFIRM?
         BNE   CONT1               CONTINUE IF NOT.
         OI    FLAG,ALLFLG+NOCONFLG SET ALL-FIELDS AND NOCONFIRM.
         OI    1(R1),C' '          UPPERCASE NEXT CHAR.
         CLI   1(R1),C'N'          NO LISTB?
         BNE   CONT1               B IF NOT TO SUPPRESS LISTB.
         OI    FLAG,NOLBFG         TURN ON NO LISTB FLAG.
CONT1    L     R3,PSCBUPT          POINT TO UPT.
         USING UPT,R3
         CLI   UPTPREFL,X'00'      ANY PREFIX?
         BE    EOJ0                B IF NOT.  BYE.
         CLC   PSCBUSER(8),UPTPREFX   PREFIX ALREADY SAME AS USERID?
         BNE   KZ0                    B IF NOT.  CHANGE IT.
         TM    FLAG,ALLFLG         BUT ARE WE TO CHANGE ALL FIELDS?
         BZ    EOJ0                B IF NOT.  BYE.
         L     R1,12(,R9)          POINT TO TIOT.
         CLC   0(7,R1),UPTPREFX    JOBNAME AND PREFIX SAME?
         BE    EOJ0                B IF YES.  BYE.
KZ0      L     R1,16               PT TO CVT.
         L     R1,0(,R1)           PT TO TCB/ASCB WORDS.
         L     R1,12(,R1)          PT TO ASCB.
         L     R8,X'3C'(,R1)       PT TO TSB.
         LTR   R8,R8               ANY TSB?
         BNZ   KZ1                 B IF YES.  ONLINE USER.
         OI    FLAG,BATCH          ON BATCH FLAG.
         B     KZ                  CONTINUE.
KZ1      SR    R1,R1               CLEAR LENGTH REG.
         IC    R1,UPTPREFL         LOAD PREFIX LENGTH.
         SH    R1,=H'2'            SUBTRACT 2 FOR SS LEN 1 LESS.
         BM    UCHECK              B IF TOO SMALL.
         EX    R1,COMPPREF         NEW PREFIX SAME AS FIRST USER CHS?
         BE    KZ                  B IF YES.
         TM    FLAG,NOCONFLG       NO CONFIRM?
         BO    KZ                  B IF NO CONFIRM.
UCHECK   LA    R1,1(,R1)           CREATE FULL SS LEN.
         EX    R1,MOVEP3           MOVE PREFIX TO MESSAGE.
         TPUT  EMSG3,EMSG3L,ASIS   SAY: ARE YOU SURE?
         TGET  SAVE,4              GET ANSWER.
         OI    SAVE,C' '           UPPERCASE ANS.
         CLI   SAVE,C'Y'           ANS YES?
         BNE   EOJ0                B IF NOT.
KZ       MODESET KEY=ZERO
         TM    FLAG,BATCH          BATCH USER?
         BZ    NOTBATCH            B IF NOT.
         OI    PSCBATR1,PSCBVMNT   SET MOUNT FLAG IN PSCB.
         B     BATCHCNT            CONTINUE
NOTBATCH CLC   PSCBUSER(7),X'58'(R8)  RUNNING UNDER ACF?
         BNE   BATCHCNT            B IF NOT.
         MVC   X'58'(7,R8),UPTPREFX  MOVE PREFIX TO TSB PSWD FIELD ...
*                                  ... FOR ACF2.
BATCHCNT MVC   PSCBUSER(8),UPTPREFX  SET USERID TO PREFIX.
         MVC   PSCBUSRL,UPTPREFL   MOVE PREFIX LENGTH TO USERID LENGTH.
         TM    FLAG,NOLBFG         NO LISTB?
         BO    POSTLB              B IF NO LISTB.
         MVC   0(4,R11),=A(BPARM)  MOVE PARM POINTER TO CPPL.
         LR    R1,R11              POINT R1 TO CPPL.
         LINK  EP=LISTBC           DO LISTBC COMMAND.
POSTLB   TM    FLAG,ALLFLG         CHANGE ALL FIELDS?
         BZ    NZ                  B IF NOT.
         L     R1,12(,R9)          POINT TO TIOT.
         MVC   0(7,R1),UPTPREFX    SET TIOT JOBNAME TO PREFIX.
         MVI   7(R1),C' '          SET LAST CHAR TO BLANK.
         L     R1,X'B0'(,R10)      POINT TO ASCB JOBNAME.
         LTR   R1,R1               ANYTHING?
         BZ    NOAJ                B IF NOTHING.
         MVC   0(7,R1),UPTPREFX    SET ASCB JOBNAME TO PREFIX.
         MVI   7(R1),C' '          SET LAST CHAR TO BLANK.
NOAJ     L     R1,X'6C'(,R10)      POINT TO ASXB
         CLI   X'C0'(R1),X'00'     ANY JOBNAME?
         BZ    NZ                  B IF NOT.
         MVC   X'C0'(7,R1),UPTPREFX SET ASXB JOBNAME TO PREFIX.
NZ       MODESET KEY=NZERO
         TM    FLAG,BATCH          BATCH USER?
         BO    EOJ0                B IF YES.  NO MESSAGES.
         SR    R4,R4
         IC    R4,UPTPREFL         LOAD PREFIX LENGTH.
         BCTR  R4,*-*              CREATE SS LENGTH.
         TM    FLAG,ALLFLG         ALL FIELDS CHANGED?
         BO    ALLMSG              B IF YES
         EX    R4,MOVEPREF         MOVE PREFIX TO MESSAGE.
         TPUT  EMSG,EMSGL          OUTPUT BYE MESSAGE.
         B     EOJ0                B IF NOT.
ALLMSG   EX    R4,MOVEP2           MOVE PREFIX TO MSG.
         TPUT  EMSG2,EMSG2L        OUTPUT SECONDARY MSG.
EOJ4     LA    R15,4               SET CC = 4.
EOJ      L     R13,SAVE+4          BYE.
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
EOJ0     SR    R15,R15             SET CC = 0.
         B     EOJ                 CONT.
EOJ8     LA    R15,8               SET CC = 8.
         B     EOJ                 CONT
         SPACE 1
SAVE     DS    9D
COMPPREF CLC   UPTPREFX(*-*),PSCBUSER   EXECUTED.
MOVEPREF MVC   EMSGUSER(*-*),UPTPREFX   EXECUTED.
MOVEP2   MVC   EMSG2USR(*-*),UPTPREFX   EXECUTED.
MOVEP3   MVC   EMSG3USR(*-*),UPTPREFX   EXECUTED.
FLAG     DC    X'00'               FLAGS.
ALLFLG   EQU   B'10000000'         CHANGE ALL FIELDS.
BATCH    EQU   B'01000000'         BATCH USER.
NOCONFLG EQU   B'00100000'         NO CONFIRM FLAG.
NOLBFG   EQU   B'00010000'         NO LISTB FLAG.
         SPACE 1
BPARM    DC    AL2(BPARME-BPARM,0),C'NON'
BPARME   EQU   *
EMSG     DC    C'USERID reset to '
EMSGUSER DC    CL8' '
EMSGL    EQU   *-EMSG
EMSG2    DC    C'JOBNAME and USERID reset to '
EMSG2USR DC    CL8' '
EMSG2L   EQU   *-EMSG2
EMSG3    DC    C'Are you sure you want your USERID to be '
EMSG3USR DC    CL7' ',C'?'
EMSG3L   EQU   *-EMSG3
         IKJUPT
         IKJPSCB
         END
