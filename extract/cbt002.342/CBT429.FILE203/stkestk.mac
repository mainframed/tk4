*---------------------------------------------------------------------*
*                      S T K E S T K                                  *
*                                                                     *
*        THIS PROGRAM EDITS THE OUTPUT LISTING FROM STK AND PRODUCES  *
*        A FLAT FILE OF THE INFORMATION.  PLEASE NOTE WE ARE NOT      *
*        ALTERING THE RECORD COLUMNS OF THE VALUES.                   *
*                                                                     *
*        JIM MARSHALL, DEPT OF TREASURY/FMS                           *
*                                                                     *
*        //FORMSTK  EXEC PGM=STKESTK,PARM=PRINT                       *
*        //STEPLIB  DD - PRIVATE LOADLIB -                            *
*        //STKRPT   DD - INPUT STK VOLRPT REPORT -                    *
*        //SYSUT20  DD - FLAT FILE OF ALL ACS0 TAPES -                *
*        //SYSUT21  DD - FLAT FILE OF ALL ACS1 TAPES -                *
*        //SYSUT22  DD - FLAT FILE OF ALL ACS2 TAPES -           JDM1 *
*        //SYSUT23  DD - FLAT FILE OF ALL ACS3 TAPES -           JDM1 *
*        //SYSUT24  DD - FLAT FILE OF ALL ACS4 TAPES -           JDM1 *
*        //SYSUT3R  DD - OPTIONAL PRINTED REPORT OF FLAT FILES -      *
*                                                                     *
*        NOTES: ANY PARM WILL CAUSE BOTH FLAT FILES TO BE ALSO        *
*               PRINTED.                                              *
*               INPUT FILE COMES IN SORTED BY VOLSER.                 *
*                                                                     *
* CHNGE LOG: 13APR92 - JDM1 - ADDED SUPPORT FOR ACS2-4                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         MACRO
&NAME    MODE   &AMODE=31
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*  SET ADDRESSING MODE.  EITHER 'MODE AMODE=31' OR 'MODE AMODE=24'  *
.*                                                                   *
.* NOTES: EXAMPLE - '    MODE AMODE=31  SET ADDREESSING MODE TO 31   *
.*                  ' -MORE CODE -                                   *
.*                  '    MODE AMODE=24  SET ADDREESSING MODE TO 24   *
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
         LCLC   &I
         GBLA   &B
&I       SETC   '&SYSNDX'
         AIF    (&AMODE EQ 31).MOD31
&NAME    LA     15,RMOD&I
         BSM    0,15
RMOD&I   DS     0H
         AGO    .MEND
.MOD31   ANOP
&NAME    L      15,AMOD&I
         BSM    0,15
AMOD&I   DC     A(CON&I+X'80000000')
CON&I    DS     0H
.MEND    MEND
         SPACE
STKESTK  AMODE 31
STKESTK  RMODE 24
STKESTK  CSECT
         SAVE (14,12),,STKESTK_&SYSDATE._&SYSTIME
         LR   R12,R15
         USING STKESTK,R12
         LA    R9,SAVE
         ST    R13,SAVE+4
         ST    R9,8(R13)
         LR    R13,R9
         SPACE
         MVI   PRTSW,C'N'          INIT TO NO
         L     R2,0(,R1)
         LH    R1,0(,R2)           DID WE PASS A PARM
         LTR   R1,R1
         BZ    TALLYHO
         MVI   PRTSW,C'Y'          MIRROR THE IMAGE
         SPACE
*---------------------------------------------------------------------*
*        HAVE TMS REPORT NOW, SO READ IN LISTING EDIT OUT HEADINGS,   *
*        ETC, ETC
*---------------------------------------------------------------------*
         SPACE
TALLYHO  EQU   *
         OPEN  (STKRPT,INPUT,SYSUT20,(OUTPUT),SYSUT21,(OUTPUT),        X
               SYSUT22,(OUTPUT),SYSUT23,(OUTPUT),                JDM1  X
               SYSUT24,(OUTPUT),                                 JDM1  X
               SYSUT3R,(OUTPUT))
         LA    R9,0                KEEP A TALLY OF STK RECORDS
LOOP2    EQU   *
         MODE  AMODE=24
         GET   STKRPT,STKREC
         MODE  AMODE=31
         CLC   STKVOL(1),=C'1'     ARE WE IN BALL PARK ?
         BNE   LOOP2
         LA    R5,6                CHECK 6 NUMBERS (TAPES=5)
         LA    R6,STKVOL           GET VOLSER
LOOP3    EQU   *
         TM    0(R6),X'F0'         IS IT NUMBERIC
         BNO   LOOP2
         LA    R6,1(,R6)           BUMP POINTER
         BCT   R5,LOOP3            CONTINUE CHECKING
         SPACE
         MVC   VOLSER(6),STKVOL    MOVE IN VOLUME SERIAL
         MVC   ADDR(12),STKADDR    MOVE IN ADDR OF RECORD
         MVC   REST(93),STKREST    MOVE IN REST OF RECORD
         CLC   STKADDR(3),CAP0     DO WE USE 1ST CAP    ?
         BE    PUT20
         CLC   STKADDR(3),CAP1     DO WE USE 2ND CAP    ?
         BE    PUT21
         CLC   STKADDR(3),CAP2     DO WE USE 3RD CAP    ?        JDM1
         BE    PUT22                                             JDM1
         CLC   STKADDR(3),CAP3     DO WE USE 4TH CAP    ?        JDM1
         BE    PUT23                                             JDM1
         CLC   STKADDR(3),CAP4     DO WE USE 5TH CAP    ?        JDM1
         BE    PUT24                                             JDM1
         B     ABORT103
         SPACE
PUT20    EQU   *
         MODE  AMODE=24
         PUT   SYSUT20,STKOREC     WRITE THE EJECT RECORD
         MODE  AMODE=31
         B     CONT3
PUT21    EQU   *
         MODE  AMODE=24
         PUT   SYSUT21,STKOREC     WRITE THE EJECT RECORD
         MODE  AMODE=31
         B     CONT3
PUT22    EQU   *                                                 JDM1
         MODE  AMODE=24                                          JDM1
         PUT   SYSUT22,STKOREC     WRITE THE EJECT RECORD        JDM1
         MODE  AMODE=31                                          JDM1
         B     CONT3                                             JDM1
PUT23    EQU   *                                                 JDM1
         MODE  AMODE=24                                          JDM1
         PUT   SYSUT23,STKOREC     WRITE THE EJECT RECORD        JDM1
         MODE  AMODE=31                                          JDM1
         B     CONT3                                             JDM1
PUT24    EQU   *                                                 JDM1
         MODE  AMODE=24                                          JDM1
         PUT   SYSUT24,STKOREC     WRITE THE EJECT RECORD        JDM1
         MODE  AMODE=31                                          JDM1
         B     CONT3                                             JDM1
CONT3    EQU   *
         LA    R9,1(,R9)           KEEP TALLY
         CLI   PRTSW,C'N'          DO WE ALSO PRINT IT
         BE    LOOP2
         MODE  AMODE=24
         PUT   SYSUT3R,STKOREC     MIRROR THE RECORD
         MODE  AMODE=31
         B     LOOP2
EOJ2     CLOSE (STKRPT,,SYSUT20,,SYSUT21,,SYSUT3R,,              JDM1  X
               SYSUT22,,SYSUT23,,SYSUT24)
         LTR   R9,R9               DID WE GET ANY RECORDS ??
         BNZ   ENDIT
         LA    R15,12              BAD RETURN CODE
         ST    R15,RETCODE         SAVE IT
ENDIT    EQU   *
         L     R15,RETCODE         LOAD IT
         L     R13,SAVE+4
         ST    R15,16(,R13)        SET THE RET CODE
         LM    R14,R12,12(R13)
         BR    R14                 RETURN TO WHENCE WE CAME
         SPACE
STKRPT   DCB   DSORG=PS,MACRF=GM,DDNAME=STKRPT,EODAD=EOJ2
         SPACE
SYSUT20  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT20,RECFM=FBA,LRECL=133,   X
               BLKSIZE=3990
         SPACE
SYSUT21  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT21,RECFM=FBA,LRECL=133,   X
               BLKSIZE=3990
         SPACE
SYSUT22  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT22,RECFM=FBA,       JDM1  X
               LRECL=133,BLKSIZE=3990                            JDM1
         SPACE 1                                                 JDM1
SYSUT23  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT23,RECFM=FBA,       JDM1  X
               LRECL=133,BLKSIZE=3990                            JDM1
         SPACE 1                                                 JDM1
SYSUT24  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT24,RECFM=FBA,       JDM1  X
               LRECL=133,BLKSIZE=3990                            JDM1
         SPACE 1                                                 JDM1
SYSUT3R  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT3R,RECFM=FBA,LRECL=133,   X
               BLKSIZE=133
         SPACE
ABORT103 ABEND 103
ABORT104 ABEND 104
         SPACE
*---------------------------------------------------------------------*
*        DATA AREA FOLLOWS.                                           *
*---------------------------------------------------------------------*
         SPACE
         LTORG
SAVE     DC    18F'0'
RETCODE  DC    F'0'                RETURN CODE
PRTSW    DC    C'N'
CAP0     DC    CL3'000'
CAP1     DC    CL3'001'
CAP2     DC    CL3'002'                                          JDM1
CAP3     DC    CL3'003'                                          JDM1
CAP4     DC    CL3'004'                                          JDM1
BLANKS   DC    CL6' '
         SPACE
STKREC   DS    0CL121
         DC    CL02' '
STKVOL   DC    CL06'XXXXXX'        03 - VOLSER
         DC    CL08' '
STKADDR  DC    CL12'NNN:NN:NN:NN'       ADDRESS
STKREST  DC    CL93' '
         SPACE
STKOREC  DS    0CL121
         DC    CL02' '
VOLSER   DC    CL06'XXXXXX'        03 - VOLSER
         DC    CL08' '
ADDR     DC    CL12'NNN:NN:NN:NN'       ADDRESS
REST     DC    CL93' '
         SPACE
         END
