*---------------------------------------------------------------------*
*                      S T K E T M S                                  *
*                                                                     *
*                                                                     *
*        THIS PROGRAM EDITS THE OUTPUT LISTING FROM TMS AND PRODUCES  *
*        A FLAT FILE OF THE INFORMATION.  PLEASE NOTE WE ARE NOT      *
*        ALTERING THE RECORD COLUMNS OF THE VALUES.                   *
*                                                                     *
*                                                                     *
*        JIM MARSHALL, DEPT OF TREASURY/FMS                           *
*                                                                     *
*        //FORMTMS  EXEC PGM=STKETMS,PARM=PRINT                       *
*        //STEPLIB  DD - PRIVATE LOADLIB -                            *
*        //TMSRPT   DD - INPUT TMS VOLSER REPORT -                    *
*        //SYSUT2   DD - FLAT FILE OF SELECTED TMS TAPES -            *
*        //SYSUT3   DD - OPTIONAL PRINTED REPORT OF FLAT FILE -       *
*                                                                     *
*        NOTES: ANY PARM WILL CAUSE BOTH FLAT FILES TO BE ALSO        *
*               PRINTED.                                              *
*               INPUT FILE COMES IN SORTED BY VOLSER.                 *
*               IF OUTPUT FILE IS EMPTY, RETURN WITH RC=12            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         MACRO
&NAME    MODE   &AMODE=31
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*  SET ADDRESSING MODE.  EITHER 'MODE AMODE=31' OR 'MODE AMODE=24'  *
.*                                                                   *
.* NOTES: EXAMPLE - '    MODE AMODE=31  SET ADDREESSING MODE TO 31   *
.*                  ' -MORE CODE -                                   *
.*                  '    MODE AMODE=24  SET ADDREESSING MODE TO 24   *
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
         LCLC   &I
         GBLA   &B
&I       SETC   '&SYSNDX'
         AIF    (&AMODE EQ 31).MOD31
&NAME    LA     15,RMOD&I
         BSM    0,15
RMOD&I   DS     0H
         AGO    .MEND
.MOD31   ANOP
&NAME    L      15,AMOD&I
         BSM    0,15
AMOD&I   DC     A(CON&I+X'80000000')
CON&I    DS     0H
.MEND    MEND
         SPACE
STKETMS  AMODE 31
STKETMS  RMODE 24
STKETMS  CSECT
         SAVE (14,12),,STKETMS_&SYSDATE._&SYSTIME
         LR   R12,R15
         USING STKETMS,R12
         LA    R9,SAVE
         ST    R13,SAVE+4
         ST    R9,8(R13)
         LR    R13,R9
         SPACE
         MVI   PRTSW,C'N'          INIT TO NO
         L     R2,0(,R1)
         LH    R1,0(,R2)
         LTR   R1,R1               DID WE PASS A PARM
         BZ    TALLYHO
         MVI   PRTSW,C'Y'          MIRROR THE IMAGE
         SPACE
*---------------------------------------------------------------------*
*        HAVE TMS REPORT NOW, SO READ IN LISTING EDIT OUT HEADINGS,   *
*        ETC, ETC
*---------------------------------------------------------------------*
         SPACE
TALLYHO  EQU   *
         OPEN  (TMSRPT,INPUT,SYSUT2,(OUTPUT),SYSUT3,(OUTPUT))
         LA    R9,0                KEEP A TALLY OF STK RECORDS
LOOP2    EQU   *
         MODE  AMODE=24
         GET   TMSRPT,TMSREC
         MODE  AMODE=31
         LA    R5,6                CHECK 5 NUMBERS (CARTS=6)
         LA    R6,TMSVOL           GET VOLSER
LOOP3    EQU   *
         TM    0(R6),X'F0'         IS IT NUMBERIC
         BNO   LOOP2
         LA    R6,1(,R6)           BUMP POINTER
         BCT   R5,LOOP3            CONTINUE CHECKING
         SPACE
         CLC   TMSDEN(2),CARTDEN   DO FURTHER CHECK
         BE    CONT3A
         CLC   TMSDEN(2),CARTDEND  DO FURTHER CHECK
         BE    CONT3A
         B     LOOP2
         SPACE
CONT3A   EQU   *
         MVC   VOLSER(6),TMSVOL    MOVE IN VOLUME SERIAL
         MVC   CDATE(5),TMSCDATE   MOVE IN CDATE
         MVC   EXPDT(5),TMSEXPDT   MOVE IN EXPDT
         MVC   CJOBN(8),TMSCJOBN   MOVE IN CREATING JOBNAME
         MVC   VAULT(4),TMSVAULT   MOVE IN VAULT NAME
         MVC   DEN(2),TMSDEN       MOVE IN DENSITY
         MVC   DSN(44),TMSDSN      MOVE IN DSNAME
         MODE  AMODE=24
         PUT   SYSUT2,TMSOREC      WRITE THE EJECT RECORD
         CLI   PRTSW,C'N'          DO WE ALSO PRINT IT
         BE    CONT3
         PUT   SYSUT3,TMSOREC      MIRROR THE RECORD
CONT3    EQU   *
         MODE  AMODE=31
         LA    R9,1(,R9)           KEEP TALLY
         B     LOOP2
EOJ2     CLOSE (TMSRPT,,SYSUT2,,SYSUT3)
         LTR   R9,R9               DID WE GET ANY RECORDS ??
         BNZ   ENDIT
         LA    R15,12              GET A 12
         ST    R15,RETCODE         STORE IT IN RETURN CODE VARIABLE
ENDIT    EQU   *
         L     R15,RETCODE
         L     R13,SAVE+4
         ST    R15,16(,R13)        PUT IN RETURN CODE
         LM    R14,R12,12(R13)     RESTORE THE REGS
         BR    R14
         SPACE
TMSRPT   DCB   DSORG=PS,MACRF=GM,DDNAME=TMSRPT,EODAD=EOJ2
         SPACE
SYSUT2   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2,RECFM=FBA,LRECL=133,    X
               BLKSIZE=3990
         SPACE
SYSUT3   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT3,RECFM=FBA,LRECL=133,    X
               BLKSIZE=133
         SPACE
*---------------------------------------------------------------------*
*        DATA AREA FOLLOWS.                                           *
*---------------------------------------------------------------------*
         SPACE
         LTORG
         SPACE
SAVE     DC    18F'0'
RETCODE  DC    F'0'                RETURN CODE
PRTSW    DC    C'N'
CARTDEN  DC    CL02'E3'            DENSITY OF 3480S
CARTDEND DC    CL02'E7'            DENSITY OF 3480 WITH COMP
TAPEDEN  DC    CL02'D3'            DENSITY OF 3420S TESTING !!!
BLANKS   DC    CL6' '
         SPACE
TMSREC   DS    0CL133
         DC    CL03' '
TMSVOL   DC    CL06'XXXXXX'        04 - VOLSER
         DC    CL03' '
TMSCDATE DC    CL05'XXXXX'         13 - JULIAN CREATION DATE
         DC    CL03' '
TMSEXPDT DC    CL05'XXXXX'         20 - JULIAN EXPDT
         DC    CL03' '
TMSCJOBN DC    CL08'XXXXX'         29 - CREATING JOBNAME
         DC    CL03' '
TMSVAULT DC    CL04'XXXXX'         40 - VAULT NAME
         DC    CL06' '
TMSDEN   DC    CL02'XX'            50 - DENSITY CREATED
         DC    CL04' '
TMSDSN   DC    CL44'     '         56 - JULIAN CREATION DATE
         DC    CL55' '             FILL OUT RECORD
         SPACE
TMSOREC  DS    0CL133
         DC    CL03' '
VOLSER   DC    CL06'XXXXXX'        04 - VOLSER
         DC    CL03' '
CDATE    DC    CL05'XXXXX'         13 - JULIAN CREATION DATE
         DC    CL03' '
EXPDT    DC    CL05'XXXXX'         20 - JULIAN EXPDT
         DC    CL03' '
CJOBN    DC    CL08'XXXXX'         29 - CREATING JOBNAME
         DC    CL03' '
VAULT    DC    CL04'XXXXX'         40 - VAULT NAME
         DC    CL06' '
DEN      DC    CL02'XX'            50 - DENSITY CREATED
         DC    CL04' '
DSN      DC    CL44'     '         56 - DSN
         DC    CL55' '             FILL OUT RECORD
         SPACE
         END
