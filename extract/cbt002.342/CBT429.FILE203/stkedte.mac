*---------------------------------------------------------------------*
*                      S T K E D T E                                  *
*                                                                     *
*                                                                     *
*        THIS PROGRAM DETERMINES THE JULIAN DATES FOR EJECTION OF THE *
*        DISASTER DUMPS AND OUTPUTS A CA-1 TMSGRW PROGRAM TO BE       *
*        RUN LATER.                                                   *
*                                                                     *
*        JIM MARSHALL, DEPT OF TREASURY/FMS                           *
*                                                                     *
*        //EDATES   EXEC PGM=STKEDTE                                  *
*        //STEPLIB  DD - PRIVATE LOADLIB -                            *
*        //SYSUT2   DD - FILE CONTAINING TMSGRW PROGRAM -             *
*                                                                     *
* CHNGE LOG: 04APR92 - JDM1 - ADD YEAR ROLLOVER SUPPORT               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
STKEDTE AMODE 24
STKEDTE RMODE 24
STKEDTE CSECT
         SAVE (14,12),,STKEDTE_&SYSDATE._&SYSTIME
         LR   R12,R15
         USING STKEDTE,R12
         LA    R9,SAVE
         ST    R13,SAVE+4
         ST    R9,8(R13)
         LR    R13,R9
         SPACE
*---------------------------------------------------------------------*
*        ISSUE THE TIME MACRO TO GET THE DATE.                        *
*---------------------------------------------------------------------*
         SPACE
         TIME
         ST    R1,PWORK            R1 = 0CYYDDDF
         MVC   PACKDATE(3),PWORK+1
         SPACE 1                                                 JDM1
*----------------------------------------------------------------JDM1-*
*        NEED TO SEE IF YEAR ROLLOVER HAS OCCURRED. IF SO, THEN  JDM1 *
*        ADJUST THE DATES                                        JDM1 *
*----------------------------------------------------------------JDM1-*
         SPACE 1                                                 JDM1
         SP    PWORK,=P'7'         DECREASE BY SEVEN             JDM1
         CLC   PACKDATE(1),PWORK+1  YEARS THE SAME ?             JDM1
         BE    COMPUTE                                           JDM1
*----------------------------------------------------------------JDM1-*
*        ROLLOVER HAS HAPPENED, THEREFORE DO ALGORITHM OF        JDM1 *
*        MAXDATE = TODAY OR PACKDATE                             JDM1 *
*        MINDATE = MAXDATE - 1000 + (365) - 6                    JDM1 *
*----------------------------------------------------------------JDM1-*
         SPACE 1                                                 JDM1
         UNPK  MAXDATE,PACKDATE                                  JDM1
         OI    MAXDATE+4,X'F0'     MAKE A CHARACTER              JDM1
         ZAP   PWORK,PACKDATE      GET A COPY                    JDM1
         SP    PWORK,=P'1000'                                    JDM1
         AP    PWORK,=P'365'                                     JDM1
         SP    PWORK,=P'6'                                       JDM1
         UNPK  MINDATE,PWORK                                     JDM1
         OI    MINDATE+4,X'F0'     MAKE A CHARACTER              JDM1
         B     BUILD                                             JDM1
         SPACE
*---------------------------------------------------------------------*
*        NOW WE NEED TO TAKE OUR DATE DO A COMPUTATION. BUT REMEMBER  *
*        THE DATE IS IN CHARACTER FORMAT.                             *
*                                                                     *
*        MAXDATE  =  PACKDATE                                         *
*        MINDATE  =  MAXDATE  - 6                                     *
*---------------------------------------------------------------------*
         SPACE
COMPUTE  EQU   *
         UNPK  MAXDATE,PACKDATE
         OI    MAXDATE+4,X'F0'     MAKE A CHARACTER
         SPACE
         SP    PACKDATE,=P'6'      BACKUP TO COVER THE RANGE
         UNPK  MINDATE,PACKDATE
         OI    MINDATE+4,X'F0'     MAKE A CHARACTER
         SPACE
*---------------------------------------------------------------------*
*        NEED TO BUILD THE TMSGRW PROGRAM.                            *
*---------------------------------------------------------------------*
         SPACE
BUILD    EQU   *                                                 JDM1
         MVC   TMSHIGH+24(5),MAXDATE
         MVC   TMSLOW+24(5),MINDATE
         OPEN  (SYSUT2,(OUTPUT))
         LA    R11,TMSGRWP         GET ADDR OF PGM
LOOP1    EQU   *
         CLC   0(2,R11),=C'/*'     ARE WE DONE ?
         BE    EOJ1
         PUT   SYSUT2,(R11)
         LA    R11,80(,R11)        MOVE IT ALONG
         B     LOOP1
EOJ1     EQU   *
         CLOSE (SYSUT2)
         SPACE
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         SPACE
SYSUT2   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT2,RECFM=FB,LRECL=80,      X
               BLKSIZE=6160
         SPACE
*---------------------------------------------------------------------*
*        DATA AREA FOLLOWS.                                           *
*---------------------------------------------------------------------*
         SPACE
         LTORG
SAVE     DC    18F'0'
PWORK    DC    F'0'                WORK AREA
PACKDATE DC    PL3'0'
MAXDATE  DC    CL5'YYDDD'
MINDATE  DC    CL5'YYDDD'
TMSGRWP  DS    0CL80
         DC    CL80'CONTROL-SECTION                       '
         DC    CL80'   SORT VOLSER                        '
*                   0         1         2
*                   01234567890123456789012 345678 9
TMSHIGH  DC    CL80'   DEFINE MAXDATE   PD ''NNNNN''      '
TMSLOW   DC    CL80'   DEFINE MINDATE   PD ''NNNNN''      '
         DC    CL80'   DEFINE VAULT0    CH ''OFF ''       '
         DC    CL80'   DEFINE VAULT1    CH ''OFF1''       '
         DC    CL80'   DEFINE SCRT      HX ''04''         '
         DC    CL80'   DEFINE DELT      HX ''02''         '
         DC    CL80'PROCESS-SECTION                       '
         DC    CL80'  WHEN CRTDT   GT MAXDATE             '
         DC    CL80'       REJECT                         '
         DC    CL80'  WHEN CRTDT   LT MINDATE             '
         DC    CL80'       REJECT                         '
         DC    CL80'  WHEN FLAG1   ON SCRT                '
         DC    CL80'       REJECT                         '
         DC    CL80'  WHEN FLAG1   ON DELT                '
         DC    CL80'       REJECT                         '
         DC    CL80'  WHEN OUTCODE EQ VAULT0              '
         DC    CL80'       LIST                           '
         DC    CL80'  WHEN OUTCODE EQ VAULT1              '
         DC    CL80'       LIST                           '
         DC    CL80'REPORT-SECTION                        '
         DC    CL80'  PRINT VOLSER                        '
         DC    CL80'  PRINT CRTDT                         '
         DC    CL80'  PRINT EXPDT                         '
         DC    CL80'  PRINT CJOB                          '
         DC    CL80'  PRINT OUTCODE                       '
         DC    CL80'  PRINT DEN                           '
         DC    CL80'  PRINT DSN                           '
         DC    CL80'/*   '
         SPACE
         END
