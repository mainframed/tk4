*---------------------------------------------------------------------*
*                      S T K E J C T                                  *
*                                                                     *
*        THIS PROGRAM READS AN EDITTED TMS LIST OF ALL TAPES TO       *
*        GO OFFSITE AND AN EDITTED LIST OF STK CARTRIDGE LOCATIONS.   *
*        WHAT GETS CREATED IS A SERIES OF EJECT CARDS FOR A           *
*        PARTICULAR STK CAP.                                          *
*                                                                     *
*        JIM MARSHALL, DEPT OF TREASURY/FMS                           *
*                                                                     *
*                                                                     *
*        //GENERATE EXEC PGM=STKEJCT,PARM=PRINT                       *
*        //STEPLIB  DD - PRIVATE LOADLIB -                            *
*        //SYSUT1T  DD - TMS INPUT GENERATED BY STKETMS -             *
*        //SYSUT20  DD - STK INPUT GENERATED BY STKESTK ACS0          *
*        //SYSUT21  DD - STK INPUT GENERATED BY STKESTK ACS1          *
*        //SYSUT22  DD - STK INPUT GENERATED BY STKESTK ACS2     JDM1 *
*        //SYSUT23  DD - STK INPUT GENERATED BY STKESTK ACS3     JDM1 *
*        //SYSUT24  DD - STK INPUT GENERATED BY STKESTK ACS4     JDM1 *
*        //SYSUT3E  DD - FORMATTED STK EJECT CARDS -                  *
*        //SYSUT4R  DD SYSOUT=*                                       *
*                                                                     *
*        NOTE:  PASSING ANY PARM TO STKEJECT WILL CAUSE THE 'EJECT'   *
*               CARDS TO BE PRINTED TO SYSUT4R.                       *
*                                                                     *
*  CHNGE LOG: 13APR92 - JDM1 - ADDED ACS2 - ACS4 SUPPORT              *
*             13APR92 - JDM2 - LOGIC FOR EMPTY ACS FILE               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
         MACRO
&NAME    MODE   &AMODE=31
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*  SET ADDRESSING MODE.  EITHER 'MODE AMODE=31' OR 'MODE AMODE=24'  *
.*                                                                   *
.* NOTES: EXAMPLE - '    MODE AMODE=31  SET ADDREESSING MODE TO 31   *
.*                  ' -MORE CODE -                                   *
.*                  '    MODE AMODE=24  SET ADDREESSING MODE TO 24   *
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE
         LCLC   &I
         GBLA   &B
&I       SETC   '&SYSNDX'
         AIF    (&AMODE EQ 31).MOD31
&NAME    LA     15,RMOD&I
         BSM    0,15
RMOD&I   DS     0H
         AGO    .MEND
.MOD31   ANOP
&NAME    L      15,AMOD&I
         BSM    0,15
AMOD&I   DC     A(CON&I+X'80000000')
CON&I    DS     0H
.MEND    MEND
         SPACE
STKEJCT  AMODE 31
STKEJCT  RMODE 24
STKEJCT  CSECT
         SAVE (14,12),,STKEJCT_&SYSDATE._&SYSTIME
         LR   R12,R15
         USING STKEJCT,R12
         LA    R9,SAVE
         ST    R13,SAVE+4
         ST    R9,8(R13)
         LR    R13,R9
         MVI   PRTSW,C'N'          DEFAULT TO NO
         L     R2,0(,R1)
         LH    R1,0(,R2)           GET LENGTH
         LTR   R1,R1               DO WE HAVE A PARM
         BZ    TALLYHO
         MVI   PRTSW,C'Y'          MIRROR THE EJECT CARDS TO SYSUT4
         SPACE
TALLYHO  EQU   *
         OPEN  (SYSUT1T,INPUT,SYSUT20,INPUT,SYSUT21,INPUT,             X
               SYSUT22,INPUT,SYSUT23,INPUT,SYSUT24,INPUT,        JDM1  X
               SYSUT3E,(OUTPUT),SYSUT4R,(OUTPUT))
         SPACE
*---------------------------------------------------------------------*
*        HAVE TMS REPORT NOW, SO READ IN LISTING AND GET VOLUME       *
*        SERIAL NUMBERS TO BUILD THE EJECT CARDS FOR CAP(000).        *
*---------------------------------------------------------------------*
         SPACE
         LA     R9,SYSUT20         GET ADDR OF INPUT DCB
         BAL    R10,PROCESS        DO FIRST PASS FOR ACS0
         CLOSE (SYSUT1T)           CLOSE AFTER 1ST PASS
         SPACE
*---------------------------------------------------------------------*
*        HAVE TMS REPORT NOW, SO READ IN LISTING AND GET VOLUME       *
*        SERIAL NUMBERS TO BUILD THE EJECT CARDS FOR CAP(001).        *
*---------------------------------------------------------------------*
         SPACE
         OPEN  (SYSUT1T)           OPEN FILE FOR SECOND PASS
         LA    R9,SYSUT21         GET ADDR OF INPUT DCB
         BAL   R10,PROCESS         DO SECOND PASS FOR ACS1
         CLOSE (SYSUT1T)           CLOSE AFTER 2ND PASS          JDM1
         SPACE 1                                                 JDM1
*----------------------------------------------------------------JDM1-*
*        HAVE TMS REPORT NOW, SO READ IN LISTING AND GET VOLUME  JDM1 *
*        SERIAL NUMBERS TO BUILD THE EJECT CARDS FOR CAP(002).   JDM1 *
*----------------------------------------------------------------JDM1-*
         SPACE 1                                                 JDM1
         OPEN  (SYSUT1T)           OPEN FILE FOR THIRD  PASS     JDM1
         LA    R9,SYSUT22          GET ADDR OF INPUT DCB         JDM1
         BAL   R10,PROCESS         DO THIRD  PASS FOR ACS2       JDM1
         CLOSE (SYSUT1T)           CLOSE AFTER 3RD PASS          JDM1
         SPACE 1                                                 JDM1
*----------------------------------------------------------------JDM1-*
*        HAVE TMS REPORT NOW, SO READ IN LISTING AND GET VOLUME  JDM1 *
*        SERIAL NUMBERS TO BUILD THE EJECT CARDS FOR CAP(003).   JDM1 *
*----------------------------------------------------------------JDM1-*
         SPACE 1                                                 JDM1
         OPEN  (SYSUT1T)           OPEN FILE FOR FOURTH PASS     JDM1
         LA    R9,SYSUT23          GET ADDR OF INPUT DCB         JDM1
         BAL   R10,PROCESS         DO FOURTH PASS FOR ACS3       JDM1
         CLOSE (SYSUT1T)           CLOSE AFTER 4TH PASS          JDM1
         SPACE 1                                                 JDM1
*----------------------------------------------------------------JDM1-*
*        HAVE TMS REPORT NOW, SO READ IN LISTING AND GET VOLUME  JDM1 *
*        SERIAL NUMBERS TO BUILD THE EJECT CARDS FOR CAP(004).   JDM1 *
*----------------------------------------------------------------JDM1-*
         SPACE 1                                                 JDM1
         OPEN  (SYSUT1T)           OPEN FILE FOR FOURTH PASS     JDM1
         LA    R9,SYSUT24          GET ADDR OF INPUT DCB         JDM1
         BAL   R10,PROCESS         DO FIFTH  PASS FOR ACS4       JDM1
         CLOSE (SYSUT1T)           CLOSE AFTER 5TH PASS          JDM1
         SPACE 1                                                 JDM1
         CLOSE (SYSUT20,,SYSUT21,,SYSUT22,,SYSUT23,,             JDM1  X
               SYSUT3E,,SYSUT4R)                                 JDM1
ENDIT    EQU   *
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         SPACE
*---------------------------------------------------------------------*
*        PROCESS SUBROUTINE.                                          *
*---------------------------------------------------------------------*
         SPACE
PROCESS  EQU   *
         LA    R11,21              NUMBER OF ENTRIES PER TRANSACTION
LOOP0    EQU   *
         MODE  AMODE=24
         GET   SYSUT1T,TMSOREC
         SPACE
         GET   (R9),STKREC
         MODE  AMODE=31
         SPACE
LOOP01   EQU   *
         CLC   TMSVOL(6),STKVOL    COMPARE VOLSERS
         BE    LOOPWR
         CLC   TMSVOL(6),STKVOL    COMPARE VOLSERS
         BL    RDTMS
RDSTK    EQU   *
         MODE  AMODE=24
         GET   (R9),STKREC
         MODE  AMODE=31
         B     LOOP01
         SPACE
RDTMS    EQU   *
         MODE  AMODE=24
         GET   SYSUT1T,TMSOREC
         MODE  AMODE=31
         B     LOOP01
         SPACE
*---------------------------------------------------------------------*
*        THIS IS WHERE WE WRITE THE EJECT CARD.                       *
*---------------------------------------------------------------------*
         SPACE
LOOPWR   EQU   *
         C     R11,=F'21'          IS IT FIRST
         BE    WR1ST               WRITE THE FIRST
         C     R11,=F'1'           TIME TO CLOSE OUT ?
         BE    WRLAST
         SPACE
*---------------------------------------------------------------------*
*        THE WRITES THE MIDDLE SEGEMENT OF THE EJECT CARD.            *
*---------------------------------------------------------------------*
         SPACE
         MVC   STKVOLC(6),TMSVOL   MOVE IN VOLSER
         MODE  AMODE=24
         PUT   SYSUT3E,STKERECC    WRITE THE MIDDLE EJECT RECORD
         MODE  AMODE=31
         S     R11,=F'1'           SUB ONE FOR WRITE
         SPACE
         CLI   PRTSW,C'N'          DO WE PRINT THE RECORD ?
         BE    LOOP0
         MODE  AMODE=24
         PUT   SYSUT4R,STKERECC    MIRROR THE EJECT RECORD
         MODE  AMODE=31
         B     LOOP0
         SPACE
*---------------------------------------------------------------------*
*        THE WRITES THE FIRST  SEGEMENT OF THE EJECT CARD.            *
*---------------------------------------------------------------------*
         SPACE
WR1ST    EQU   *
         MVC   STKVOLF(6),TMSVOL   MOVE IN VOLSER
         MODE  AMODE=24
         PUT   SYSUT3E,STKERECF    MIRROR THE EJECT RECORD
         MODE  AMODE=31
         S     R11,=F'1'           SUB ONE FOR WRITE
         SPACE
         CLI   PRTSW,C'N'          DO WE PRINT THE RECORD ?
         BE    LOOP0
         MODE  AMODE=24
         PUT   SYSUT4R,STKERECF    MIRROR THE EJECT RECORD
         MODE  AMODE=31
         B     LOOP0
WRLAST   EQU   *
         MVC   STKVOLL(6),TMSVOL   MOVE IN VOLSER
         MVC   STKCAPL(3),STKADDR  SET TO PROCESS THIS CAP
         MODE  AMODE=24
         PUT   SYSUT3E,STKERECL    MIRROR THE EJECT RECORD
         MODE  AMODE=31
         LA    R11,21              SETUP FOR NEXT TIME
         SPACE
         CLI   PRTSW,C'N'          DO WE PRINT THE RECORD ?
         BE    LOOP0
         MODE  AMODE=24
         PUT   SYSUT4R,STKERECL    MIRROR THE EJECT RECORD
         MODE  AMODE=31
         B     LOOP0
         SPACE
EOJ0     EQU   *
         C     R11,=F'21'          DID WE DO ALL ?
         BE    LOOPEND
         MVC   STKVOLL(6),=C'IGNORE'  SET A BOGUS VOLSER
         MVC   STKCAPL(3),STKADDR  SET TO PROCESS THIS CAP
         MODE  AMODE=24
         PUT   SYSUT3E,STKERECL    MIRROR THE EJECT RECORD
         MODE  AMODE=31
         SPACE
         CLI   PRTSW,C'N'          DO WE PRINT THE RECORD ?
         BE    LOOPEND
         MODE  AMODE=24
         PUT   SYSUT4R,STKERECL    MIRROR THE EJECT RECORD
         MODE  AMODE=31
         SPACE
LOOPEND  EQU   *
         BR    R10                 RETURN
         SPACE
SYSUT1T  DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT1T,EODAD=EOJ0
         SPACE
SYSUT20  DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT20,EODAD=EOJ0
         SPACE
SYSUT21  DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT21,EODAD=EOJ0
         SPACE
SYSUT22  DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT22,EODAD=EOJ0       JDM1
         SPACE 1                                                 JDM1
SYSUT23  DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT23,EODAD=EOJ0       JDM1
         SPACE 1                                                 JDM1
SYSUT24  DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT24,EODAD=EOJ0       JDM1
         SPACE 1                                                 JDM1
SYSUT3E  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT3E,RECFM=FB,LRECL=80,     X
               BLKSIZE=80
         SPACE
SYSUT4R  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSUT4R,RECFM=FB,LRECL=80,     X
               BLKSIZE=80
         SPACE
*---------------------------------------------------------------------*
*        DATA AREA FOLLOWS.                                           *
*---------------------------------------------------------------------*
         SPACE
         LTORG
SAVE     DC    18F'0'
BLANKS   DC    CL6' '
PRTSW    DC    C' '                DO WE MIRROR EJECT CARDS?
         SPACE
* --------------------------------------------------------- *
*        FORMAT OF TMS EDITTED RECORD                       *
* --------------------------------------------------------- *
         SPACE
TMSOREC  DS    0CL133
         DC    CL03' '
TMSVOL   DC    CL06'XXXXXX'        04 - VOLSER
         DC    CL03' '
CDATE    DC    CL05'XXXXX'         13 - JULIAN CREATION DATE
         DC    CL03' '
EXPDT    DC    CL05'XXXXX'         20 - JULIAN EXPDT
         DC    CL03' '
CJOBN    DC    CL08'XXXXX'         29 - CREATING JOBNAME
         DC    CL03' '
VAULT    DC    CL04'XXXXX'         40 - VAULT NAME
         DC    CL06' '
DEN      DC    CL02'XX'            50 - DENSITY CREATED
         DC    CL04' '
DSN      DC    CL44'     '         56 - DSN
         DC    CL55' '             FILL OUT RECORD
         SPACE
* --------------------------------------------------------- *
*        FORMAT OF STK EDITTED RECORD                       *
* --------------------------------------------------------- *
         SPACE
STKREC   DS    0CL121
         DC    CL02' '
STKVOL   DC    CL06'XXXXXX'        03 - VOLSER
         DC    CL08' '
STKADDR  DC    CL12'NNN:NN:NN:NN'       ADDRESS
STKREST  DC    CL93' '
         SPACE
STKOREC  DS    0CL121
         DC    CL02' '
STKSER   DC    CL06'XXXXXX'        03 - VOLSER
         DC    CL08' '
ADDR     DC    CL12'NNN:NN:NN:NN'       ADDRESS
REST     DC    CL93' '
         SPACE
* --------------------------------------------------------- *
*        FORMAT OF 1ST EJECT CARD                           *
* --------------------------------------------------------- *
         SPACE
STKERECF DS    0CL80
         DC    CL14' EJECT VOLSER('
STKVOLF  DC    CL06'XXXXXX'
         DC    CL01','
         DC    CL59'       +'
         SPACE
* --------------------------------------------------------- *
*        FORMAT OF INTERMEDIATE EJECT CARD                  *
* --------------------------------------------------------- *
         SPACE
STKERECC DS    0CL80
         DC    CL14'              '
STKVOLC  DC    CL06'XXXXXX'
         DC    CL01','
         DC    CL59'       +'
         SPACE
* --------------------------------------------------------- *
*        FORMAT OF LAST EJECT CARD                          *
* --------------------------------------------------------- *
         SPACE
STKERECL DS    0CL80
         DC    CL14'              '
STKVOLL  DC    CL06'XXXXXX'
         DC    CL01')'
         DC    CL05' CAP('
STKCAPL  DC    CL03'NNN'
         DC    CL01')'
         DC    CL50' '
         SPACE
         LTORG
         SPACE
         DCBD  DSORG=(QS)
         SPACE
         END
