 TITLE 'IEX10 - OS/360 ALGOL COMPILER INITIALIZATION PHASE'
*STATUS: CHANGE LEVEL 0.
*
*FUNCTION/OPERATION:
*   THIS MODULE PERFORMS THE FOLLOWING INITIALIZATION ACTIONS:
*   1.  SAVE THE REGISTERS AND ESTABLISH THE LOWER PART OF THE COMMON
*   WORKAREA AS THE NEW SAVE AREA TO BE USED THROUGHOUT THE COMPILATION
*   2.  EXECUTE A SPIE MACRO.
*   3.  SCAN THE OPTION PARAMETER FIELD PROVIDED BY THE USER AND SET
*   THE SWITCHES IN HCOMPMOD ACCORDINGLY.
*   4.  IF PROVIDED BY THE USER, INSERT NEW DD NAMES INTO THE DCB'S AND
*   STORE THE INITIAL PAGECOUNT IN THE COMMON WORKAREA.
*   5.  ACCORDING TO THE VALUE OF THE SIZE OPTION PARAMETER, SELECT
*   A TABLE OF SIZES FOR CERTAIN STORAGE-SIZE DEPENDENT STORAGE AREAS
*   AND MOVE THE TABLE TO THE COMMON WORKAREA.
*   6.  OBTAIN STORAGE FOR THE ERROR POOL AND MOVE ANY ERROR PATTERNS
*   ALREADY STORED FROM THE PRELIMINARY ERROR POOL INTO THE NEW POOL.
*   7.  COMPLETE AND OPEN THE DCB'S FOR ALL DATA SETS TO BE USED.
*   8.  OBTAIN THE CALENDAR DATE BY MEANS OF A TIME MACRO AND STORE
*   IT IN EDITED FORM IN A HEADLINE, WHICH IS PRINTED AT THE TOP OF THE
*   FIRST PAGE.
*   9.  TRANSFER CONTROL TO THE SCANI/II PHASE (IEX11).
*
*   IF ERRORS ARE DETECTED IN THE STEPS DESCRIBED ABOVE (SUCH AS
*   INVALID OPTION PARAMETERS OR INCORRECT DD CARDS), SOME OF THE STEPS
*   MAY BE BYPASSED, AND AN ERROR EXIT MAY BE TAKEN.
*
*ENTRY POINTS:
*        IEX10000 - THE ONLY ENTRY POINT TO THIS MODULE. CONTROL IS
*   TRANSFERRED HERE FROM IEX00 BY THE MACRO 'LINK EP=IEX10'. REG. 0
*   THEN CONTAINS THE ADDRESS OF THE COMMON WORKAREA AND REG. 1 THE
*   ADDRESS OF THE PARAMETER LIST PROVIDED BY THE USER.
*
*INPUT: N/A.
*
*OUTPUT:  THE TEXT STORED AT FIRSTLIN IS OUTPUT TO SYSPRINT AS A
*   91-CHARACTER RECORD WITH MACHINE CONTROL CHARACTER.
*
*EXTERNAL ROUTINES: N/A.
*
*   IF NO TERMINATING ERRORS ARE FOUND, CONTROL IS TRANSFERRED TO
*   THE SCANI/II PHASE BY MEANS OF THE MACRO 'XCTL EP=IEX11'.
*
*EXITS - ERRORS:
*   IF A PROGRAM CHECK OR UNRECOVERABLE I/O ERROR OCCURS DURING ONE
*   OF THE INITIALIZATION STEPS, CONTROL IS TRANSFERRED (VIA AN ERROR
*   ROUTINE IN IEX00) TO THE ROUTINE GOTOTERM IN THIS MODULE. FROM
*   THERE AN EXIT IS MADE TO THE TERMINATING PHASE BY MEANS OF THE
*   MACRO 'XCTL EP=IEX51002'.
*
*   IF ANY OTHER TERMINATING ERROR IS DETECTED, CONTROL IS TRANSFERRED
*   TO THE ROUTINE GOTOEDIT IN THIS MODULE.  FROM THERE AN EXIT IS
*   MADE TO THE ERROR EDITING MODULE BY MEANS OF THE MACRO
*   'XCTL EP=IEX21000'.
*
*TABLES/WORK AREAS:
*   ARTAB - A TABLE OF SIZES OF WORK AREAS AND BUFFERS WHICH DEPEND
*   ON THE STORAGE SIZE.
*   STARTING AT PTRN200 - ERROR PATTERNS FOR ALL ERRORS WHICH MAY BE
*   DETECTED IN THIS MODULE.
*   FIRSTLIN - THE FIRST OUTPUT LINE TO BE PRINTED. CONTAINS PROGRAM
*   IDENTIFICATION AND LEVEL AND THE DATE OF THE RUN.
*   JANUARY - A TABLE OF THE MONTHS OF THE YEAR. USED WHEN EDITING
*   THE DATE FOR FIRSTLIN.
*   PARMLIST - A TABLE USED FOR DECODING THE OPTION PARAMETERS.
*
*ATTRIBUTES:  NOT REUSABLE.
*
*NOTES:
*   THIS MODULE IS ONLY INTENDED TO BE USED IN CONNECTION WITH THE
*   OTHER MODULES COMPRISING THE ALGOL COMPILER.
*
*   THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRESEN-
*   TATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO THE
*   ONE USED AT ASSEMBLY TIME.
         EJECT
IEX10000 START 0
         SPACE
* REGISTER ASSIGNMENTS
         SPACE
RA       EQU   4
RB       EQU   5
RC       EQU   6
RD       EQU   7
RE       EQU   8
RF       EQU   9
RG       EQU   10
RH       EQU   11
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
BASE     EQU   2                        BASE REGISTER
IDX      EQU   7                        INDEX USED BY EDITDATE ROUT.
         SPACE
REGA     EQU   4    RUNNING ADDR OF PARAMETER FIELD MINUS 2
REGB     EQU   5    ADDR OF BYTE FOLLOWING LAST BYTE OF FIELD MINUS 2
REGC     EQU   6    RUNNING ADDR OF PARAMETER LIST
REGD     EQU   7    ADDR OF LAST ENTRY OF PARAMETER LIST
REGE     EQU   8    LENGTH -1 OF PARAMETER
REGF     EQU   9    CURRENT FIELD IN ERROR POOL
REGG     EQU   10   NEXT FIELD IN ERROR POOL
REGH     EQU   11   BYTE COUNT OF PARAMETER IN ERROR
 EJECT
         SPACE 2
         SAVE  (14,12)
         BALR  BASE,0              GET BASE
         USING *,BASE
         ST    R0,8(R13)           ADDR OF LOWER LEVEL SAVAREA
         LR    R12,R13
         LR    R13,R0
         ST    R12,4(R13)          ADDR OF HIGHER LEVEL SAVEAREA
         USING WORKAREA,R13
         SPACE 2
* EXECUTE THE SPIE MACRO
         SPACE
         L     R3,PICAADD          ADDR OF PICA OF COMPILER
         SPIE  MF=(E,(3))
         ST    R1,OLDPICA          STORE ADDR OF OLD PICA IN WORKAREA
         SPACE 2
         MVC   ERET,=A(GOTOTERM)   ERROR ROUT FOR NEXT PART OF PHASE
* INTERROGATE THE CONTROL PARAMETERS AND SET SWITCHES IN WORKAREA
         SPACE
         L     R14,4(R13)
         L     R14,24(R14)
         LTR   R14,R14
         BC    8,OPEN              NO OPTIONS ARE SPECIFIED
         SPACE
         L     REGA,0(0,14)        ADDRESS OF PARMETERFIELD
         LA    REGA,0(0,REGA)           RESET HIGH ORDER BYTE
         LH    REGB,0(REGA)        FIELDLENGTH
         LTR   REGB,REGB           TEST FIELDLENGTH
         BZ    DDNAMES             NO CONTROL PARAMETERS SPECIFIED
         SPACE
         AR    REGB,REGA           END OF FIELD
         LA    REGD,LSTENTRY       LAST ENTRY OF LIST
         SR    REGE,REGE           ZERO REGISTER
FNDCOMMA CLI   2(REGA),C','
         BNE   FNDPAR              IF NOT COMMA
STEP1    LA    REGA,1(REGA)        STEP FIELD
COMP1    CR    REGA,REGB
         BL    FNDCOMMA            IF NOT END OF FIELD YET
         TM    HCOMPMOD+1,X'60'
         BC    14,*+8
         OI    HCOMPMOD,X'80'
         B     DDNAMES             END OF FIELD
         USING LENGTH,REGC
COMPINST CLC   2(0,REGA),PARAM
FNDPAR   CLC   2(5,REGA),=C'SIZE='
         BE    FNDSIZE
         LA    REGC,PARMLIST
NXTPAR   IC    REGE,LENGTH
         EX    REGE,COMPINST
         BNE   NOTFOUND
         LA    REGG,1(REGA,REGE)   ADDR OF BYTE FOLLOWING PARAMETER -2
         CLI   2(REGG),C','        IS IT COMMA
         BE    SETBIT
         CR    REGG,REGB           IS IT END OF PARAMETERFIELD
         BL    ERROR200            IF NO
SETBIT   LR    REGA,REGG
         EX    0,INSTR
         DROP  REGC
         B     COMP1
NOTFOUND CR    REGC,REGD
         LA    REGC,LENGTH-PARMLIST(REGC)  STEP LIST ADDR
         BL    NXTPAR              IF NOT END OF LIST YET
         SPACE
ERROR200 L     REGF,NEXTERR        ADDR OF NEXT FREE BYTE IN ERRORPOOL
         CLI   0(REGF),C'X'        TEST IF THE PRELIMINARY ERROR
         BE    FULLPOOL              POOL IS FILLED
         LA    REGG,16(REGF)
         ST    REGG,NEXTERR
         MVC   0(16,REGF),PTRN200  PATTERN WITH BLANKS TO ERROR POOL
         LA    REGH,12(0)
MOVE     MVC   4(1,REGF),2(REGA)
         LA    REGF,1(REGF)
MOVENOT  LA    REGA,1(REGA)
         CLI   2(REGA),C','
         BE    STEP1               IF END OF PARAMETER
         CLR   REGA,REGB
         BNL   DDNAMES             IF END OF PARAMETER LIST
         SH    REGH,=H'1'
         BP    MOVE
         B     MOVENOT
         SPACE
FNDSIZE  LA    REGG,5(REGA)        ADDR OF FIRST DIGIT -2
         SR    REGH,REGH           SET COUNT OF DIGITS TO ZERO
TSTDIGIT CLI   2(REGG),C'0'        TEST IF
         BL    ERROR200              THE CHARACTER
         CLI   2(REGG),C'9'          IS A DIGIT
         BH    ERROR200
         LA    REGG,1(REGG)
         CR    REGG,REGB           TEST IF END OF PARAMTER FIELD
         BE    PCKSIZE
         CLI   2(REGG),C','        TEST IF DIGIT IS FOLLOWED BY A COMMA
         BE    PCKSIZE
         LA    REGH,1(REGH)        ADD 1 TO DIGIT COUNT
         CH    REGH,=H'7'          TEST IF TOO MANY DIGITS
         BE    ERROR200
         B     TSTDIGIT
PCKINSTR PACK  FIELD1,7(0,REGA)
PCKSIZE  EX    REGH,PCKINSTR       L2 IS IN TEGH
         CVB   REGC,FIELD1
         LR    REGA,REGG
         C     REGC,=F'45056'      TEST IF SPECIFIED STORAGE SIZE
         BL    ERROR208              IS TOO SMALL
         ST    REGC,SIZE
         B     COMP1
         SPACE
ERROR208 L     REGF,NEXTERR
         MVC   0(4,REGF),PTRN208   PATTERN FOR MSG 208 TO ERROR POOL
         LA    REGF,4(REGF)        UPDATE POINTER TO NEXT
         CLI   0(REGF),C'X'        TEST IF THE PRELIMINARY ERROR
         BE    FULLPOOL              POOL IS FILLED
         ST    REGF,NEXTERR          FREE ENTRY
         B     COMP1
         SPACE
FULLPOOL MVC   0(4,REGF),PTRN206   PATTERN FOR MESSAGE 206
         LA    REGF,4(REGF)        UPDATE POINTER TO
         ST    REGF,NEXTERR          NEXT FREE ENTRY
         B     DDNAMES             NO PARAMERER TESTING ANY MORE
         SPACE 2
* FIND THE DD NAMES
         SPACE
DDNAMES  TM    0(14),X'80'
         BO    OPEN                IF NO DDNAMES AND HEADING INFO
         L     REGA,4(14)
         LH    REGB,0(REGA)
         LTR   REGB,REGB
         BZ    HEADINFO                 IF NO DDNAMES SPECIFIED
         CL    REGB,=F'80'         MORE THAN TEN
         BNH   FNDDCB-4              DD NAMES
         L     REGB,=F'80'             NOT ALLOWED
         L     REGF,NEXTERR
         MVC   0(4,REGF),PTRN207   PATTERN TO ERROR POOL
         LA    REGF,4(REGF)        UPDATE POINTER
         ST    REGF,NEXTERR          TO ERROR POOL
         LA    REGC,LINADD
FNDDCB   L     REGD,0(REGC)
         LTR   REGD,REGD
         BC    8,NXTDDNAM          IF THIS DATASET DOES NOT EXIST
         CLC   2(8,REGA),=XL8'00'
         BE    NXTDDNAM            IF THIS DDNAME WAS NOT SPECIFIED
         USING IHADCB,REGD
         MVC   DCBDDNAM,2(REGA)
         DROP  REGD
NXTDDNAM LA    REGA,8(REGA)
         LA    REGC,4(REGC)
         SH    REGB,=H'8'
         BP    FNDDCB
         SPACE 2
* HANDLE HEADING INFORMATION
         SPACE
HEADINFO TM    4(14),X'80'
         BO    OPEN                IF NO HEADING INFORMATION
         L     RA,8(14)            POINTER TO HEADING INFO
         ST    RA,HDING            STORE POINTER FOR USE IN LAST PHASE
         MVC   PAGECNT(4),2(RA)
         SPACE 2
* OPEN THE SYSPRINT DCB IF THE SIZE PARAMETER WAS INCORRECT
         SPACE
OPEN     L     R8,PRINTADD         ADDR OF SYSPRINT DCB
         USING IHADCB,R8
         MVC   DCBEXLST+1(3),=AL3(PRINTEX) ADDR OF EXLIST FOR PRINT DCB
         DROP  R8
         SPACE 2
* FIND THE CORRECT AREA SIZE TABLE AND MOVE IT TO THE COMMON WORKAREA
         SPACE
FNDARSIZ LA    RA,ARTAB-(ARTAB1-ARTAB) ADDR OF TABLES - LNGTH OF 1 TAB
         L     RB,SIZE             SIZE OF AVAILABLE STORAGE
FNDARTAB LA    RA,ARTAB1-ARTAB(RA) STEP TO NEXT TABLE
         C     RB,0(RA)
         BNL   FNDARTAB
         MVC   INBLKS(ARTAB1-ARTAB-4),4(RA) MOVE TABLE TO COM WORKAREA
         SPACE 2
* ACQUIRE THE ERROR POOL AND THE SOURCE PROG BUFFER 1
* STORE CONTENT OF PRELIMINARY ERROR POOL IN NEW ERROR POOL
         SPACE
         L     R0,POOLS            SIZE OF ERROR POOL
         LR    RA,R0
         A     R0,SRCE1S           SIZE OF SOURCE PROG BUFFER 1
         LR    RC,R0               SIZE OF TOTAL AREA
       GETMAIN R,LV=(0)
         XI    HCOMPMOD+1,NOBUF         COMMON BUFFER AREA PRESENT
         L     RB,NEXTERR          ADDR OF NEXT FREE BYTE IN PREL POOL
         LA    RD,PRELPOOL         ADDR OF BEGIN OF PREL POOL
         SR    RB,RD               NUMBER OF BYTES IN ERROR POOL
         EX    RB,MOVEPOOL
         B     *+10                BRANCH AROUND MVC INSTR
MOVEPOOL MVC   0(0,R1),PRELPOOL
         ST    R1,ERRPOOL          ADDR OF FIRST BYTE OF POOL
         AR    RB,R1               ADDR OF NEXT FREE BYTE
         ST    RB,NEXTERR
         AR    RA,R1               ADDR OF END OF POOL +1
         ST    RA,SRCE1ADD         ADDR OF SOURCE PROG BUFFER 1
         AR    RC,R1               END ADDR OF SOURCE PROG BUFFER 1
         ST    RC,SRCE1END
         SH    RA,=H'24'           ADDR OF END OF POOL -23
         ST    RA,ENDPOOL
         SPACE 2
* COMPLETE THE DATA CONTROL BLOCKS
         SPACE
         LM    R3,R12,LINADD       ADDRESSES OF THE DCBS
         USING IHADCB,R3           SYSLIN DCB
         MVC   DCBEXLST+1(3),=AL3(LINEX) ADDR OF EXLIST
         DROP  R3
         USING IHADCB,R7           SYSIN DCB
         MVC   DCBEXLST+1(3),=AL3(INEX) ADDR OF EXLIST
         DROP  R7
         USING IHADCB,R9           SYSPUNCH DCB
         MVC   DCBEXLST+1(3),=AL3(PCHEX) ADDR OF EXLIST
         DROP  R9
         USING IHADCB,R10          SYSUT1 DCB
         LH    R4,SRCE1S+2         SOURCE PROG BUFFER 1 SIZE
         STH   R4,DCBBLKSI         BLOCK SIZE
         DROP  R10
         USING IHADCB,R11          SYSUT2 DCB
         STH   R4,DCBBLKSI         BLOCK SIZE
         DROP  R11
         SPACE 2
* OPEN ALL DATA CONTROL BLOCKS
         SPACE
         MVC   ERET,=A(OPEXERR)    ERROR ROUT FOR OPEN EXIT ROUTINES
         OPEN  ((3),OUTPUT,(8),OUTPUT,(9),OUTPUT,(11),OUTIN,(12),OUTIN)X
                                   OPEN THE SYSLIN-, SYSPRINT-,        X
                                   SYSPUNCH-, SYSUT2- AND SYSUT3 DCBS
         OPEN  ((10),OUTIN)        OPEN THE SYSUT1 DCB
         OPEN  ((R7),)                  OPEN THE SYSIN DCB
         MVC   ERET,=A(GOTOTERM)
         SPACE 2
* TEST IF THE SYSPRINT DCB HAS BEEN OPENED
         SPACE
         USING IHADCB,8
TSTDCB   TM    DCBOFLGS,X'10'      TERMINATE THE COMPILATION IF THE
         DROP  8
         BO    EDITDATE
         OI    HCOMPMOD+2,PRTNO         SYSPRINT DCB NOT OPENED
         LR    R7,R8                    DCB ADDRESS
         L     R4,NEXTERR
         B     ERROR201
         SPACE 2
* GET DATE AND TRANSLATE IT TO MMM DD 19YY
         SPACE
EDITDATE EQU   *
         TIME  ,                   GET SYSTEM DATE
         ST    R1,FIELD2
         MVI   FIELD2,X'19'        TWENTIETH CENTURY
         UNPK  FIELD1(7),FIELD2(4)
         MVC   YEAR(4),FIELD1      YEAR IN UNPACKED FORM
         MVO   FIELD1(8),FIELD2+1(1) YEAR IN PACKED FORM
         CVB   RB,FIELD1
         SR    RA,RA
         D     RA,=F'4'
         LTR   RA,RA
         BC    7,NOLEAP            LEAP-YEAR IF NO REMAINDER
         MVI   FEBRUARY+1,X'1D'    FEBRUARY HAS 29 DAYS
NOLEAP   MVC   FIELD1+6(2),FIELD2+2 PACKED DAY OF THE YEAR
         CVB   RA,FIELD1           BINARY DAY OF THE YEAR
         LA    IDX,JANUARY-6       INITIALIZE FOR THE LOOP TO FIND
*                                    THE ACTUAL MONTH
FNDMONTH LA    IDX,6(IDX)          INCREASE INDEX FOR NEXT MONTH
         LR    RB,RA               SAVE DAY
         SH    RA,0(IDX)           THE MONTH IS FOUND IF
         BC    12,MONFOUND           THE DIFFERENCE IS ZERO OR NEG
         CLI   5(IDX),X'FF'
         BNE   FNDMONTH            IF NOT END OF YEAR
         MVC   MONTH-1(12),=C'IS INCORRECT'
         B     DATEND              THE SYSTEM DATE IS INCORRECT
MONFOUND MVC   MONTH(3),2(IDX)     3-LETTER MONTH NAME
         CVD   RB,FIELD1           PACKED DAY OF THE MONTH
         UNPK  DAY(2),FIELD1+6(2)  UNPACKED DAY OF THE MONTH
         MVZ   DAY+1(1),DAY        MOVE IN PROPER ZONE
DATEND   EQU   *
         SPACE 2
* WRITE FIRST RECORD ON SYSPRINT
         SPACE
         L     R1,PRINTADD
         PUT   (1)                      SKIP TO NEW PAGE
         MVC   0(2,1),SKIP                BY PRINTING A BLANK LINE
         MVC   2(89,1),1(1)
         L     R1,PRINTADD
         PUT   (1)
         MVC   0(91,1),FIRSTLIN         FIRST LINE ON FIRST PAGE
         SPACE 2
         MVC   ERET,=A(GOTOEDIT)   ERROR ROUT FOR REST OF PHASE
         SPACE 2
* TEST IF THE OTHER DCBS HAVE BEEN OPENED
         SPACE
         LM    R3,R12,LINADD
         L     R4,NEXTERR
         SPACE
         USING IHADCB,R3           TEST SYSLIN
         TM    HCOMPMOD+1,NLOAD
         BO    TSTPUNCH            IF NOLOAD SPECIFIED
         TM    DCBOFLGS,X'10'
         BO    TSTPUNCH            IF DCB HAS BEEN OPENED
         OI    HCOMPMOD+1,NLOAD    SET NOLOAD OPTION BIT ON
         MVC   0(4,R4),PTRN202     PATTERN FOR MSG 202
         LA    R4,4(R4)            POINTER TO NEXT FREE ENTRY
         DROP  R3
         SPACE
         USING IHADCB,R9           TEST SYSPUNCH
TSTPUNCH TM    HCOMPMOD+1,NDCK
         BO    TSTIN               IF NODECK SPECIFIED
         TM    DCBOFLGS,X'10'
         BO    TSTIN               IF DCB HAS BEEN OPENED
         OI    HCOMPMOD+1,NDCK     SWT NODECK OPTION BIT ON
         MVC   0(4,R4),PTRN203     PATTERN FOR MSG 203
         LA    R4,4(R4)            POINTER TO NEXT FREE ENTRY
         DROP  R9
         SPACE
TSTIN    BAL   R14,TSTDCBRT        DCB ADDR FOR SYSIN IS IN REG 7
         LR    R7,R10              TEST SYSUT1
         BAL   R14,TSTDCBRT
         LR    R7,R11              TEST SYSUT2
         BAL   R14,TSTDCBRT
         LR    R7,R12              TEST SYSUT3
ERROR201 EQU   *                        ENTRY IF SYSPRINT NOT OPENED
         LA    R14,STPOINTR        DO NOT RETURN
         USING IHADCB,R7
TSTDCBRT TM    DCBOFLGS,X'10'      TEST IF DCB OPENED
         BCR   1,R14               RETURN IF OK
         MVC   0(4,R4),PTRN201     PATTERN FOR MSG 201
         MVC   4(8,R4),DCBDDNAM    DDNAM
         LA    R4,12(R4)           UPDATE POINTER
         OI    HCOMPMOD+1,NOGO     SET NOGO SWITCH ON
         BR    R14                 RETURN
         DROP  R7
STPOINTR ST    R4,NEXTERR
         SPACE 2
* END OF INITIALIZATION OF THE COMPILER GO TO SCAN 1/2 OR TO PHASE 21
* FOR ERROR EDITING IF COMPILATION IS IMPOSSIBLE
         SPACE
         TM    HCOMPMOD+1,NOGO
         BO    GOTOEDIT            IF COMPILATION IMPOSSIBLE
         L     BASE,=A(IEX10001)
         BR    BASE                     TO INITIALIZE IEX11
         SPACE 2
* DCB EXIT ROUTINES
         SPACE
         USING IHADCB,R1
         SPACE
INEXRT   LH    RC,INBLKS           MAX BLOCKSIZE ALLOWED
         BAL   RE,COMEXRT          BRANCH TO ROUT COMMON TO ALL EXIT RT
         OI    HCOMPMOD+1,NOGO     ABNORMAL RETURN, SET NOGO SWITCH ON
         MVC   0(4,RF),PTRN204     ERROR PATTERN FOR MSG 204
         LA    RF,4(RF)            UPDATE POINTER TO
         ST    RF,NEXTERR            NEXT ENTRY
         BR    R14                 RETURN TO OPEN ROUT
         SPACE
LINEXRT  LH    RC,LINBLKS          MAX BLOCKSIZE ALLOWED
         BAL   RE,COMEXRT          BRANCH TO COMMON ROUTINE
         MVC   0(12,RF),PTRN205A   PATTERN FOR MSG 205 AND SYSLIN
         LA    RF,12(RF)           UPDATE POINTER TO
         ST    RF,NEXTERR            NEXT ENTRY
         BR    R14                 RETURN TO OPEN ROUT
         SPACE
PCHEXRT  LH    RC,PCHBLKS          MAX BLOCKSIZE ALLOWED
         BAL   RE,COMEXRT          BRANCH TO COMMON ROUTINE
         MVC   0(12,RF),PTRN205B   PATTERN FOR MSG 205 AND SYSPUNCH
         LA    RF,12(RF)           UPDATE POINTER TO
         ST    RF,NEXTERR            NEXT ENTRY
         BR    R14                 RETURN TO OPEN ROUTINE
         SPACE
PRTEXRT  LH    RC,PRTBLKS          MAX BLOCKSIZE ALLOWED
         BAL   RE,COMEXRT          BRANCH TO COMMON ROUTINE
         MVC   0(12,RF),PTRN205C   PATTERN FOR MSG 205 AND SYSPRINT
         LA    RF,12(RF)           UPDATE POINTER TO
         ST    RF,NEXTERR            NEXT ENTRY
         BR    R14                 RETURN TO OPEN ROUTINE
         SPACE
COMEXRT  LH    RB,DCBBLKSI         BLOCKSIZE FROM DCB
         LH    RD,DCBLRECL         RECORD LENGTH FROM DCB
         LTR   RB,RB               TEST IF BLKSIZE WAS SPECIFIED IN DD
         BC    7,DIVTEST           IF SPECIFIED.
         STH   RD,DCBBLKSI         SET BLKSIZE EQU LRECL
         BR    R14                 RETURN TO OPEN ROUTINE
DIVTEST  L     RF,NEXTERR          ADD OF NEXT ENTRY IN ERROR POOL
         SR    RA,RA               TEST IF BLKSIZE
         DR    RA,RD                 IS A MULTIPLE
         LTR   RA,RA                 OF LRECL
         BC    7,NOTOK             IF NOT MULTIPLE
         TM    HCOMPMOD+1,NOBUF    GO BACK TO OPEN ROUTINE IF THE
         BCR   1,R14                 SIZE PARAMETER WAS INCORRECT
         LH    RA,DCBBLKSI
         CLR   RA,RC               TEST IF BLOCKSIZE TOO LARGE
         BCR   12,R14              IF OK
NOTOK    STH   RD,DCBBLKSI         SET BLOCKSIZE EQU RECORD LENGTH
         BR    RE                  ERROR RETURN
         DROP  R1
         SPACE 2
* ERROR ROUTINES
         SPACE
*  ROUTINE FOR UNEXPECTED ERROR DURING OPEN. ENTERED VIA ERET.
         SPACE
OPEXERR  OI    HCOMPMOD+1,NOGO
         BR    R14                 GO BACK TO OPEN ROUTINE
         DROP  BASE
         SPACE
*  EXIT TO TERMINATING PHASE IF ERROR EDITING NOT POSSIBLE.
         SPACE
GOTOTERM BALR  BASE,0
         USING *,BASE
         BAL   RA,CLOSE
         XCTL  EP=IEX51002         GO TO TERMINATING PHASE
         DROP  BASE
         SPACE
*  TERMINATE VIA ERROR EDITING PHASE.
         SPACE
GOTOEDIT BALR  BASE,0
         USING *,BASE
         BAL   RA,CLOSE
         OI    HCOMPMOD,X'08'      INDICATE TERMINATING ERROR
         XCTL  EP=IEX21000         GO TO EDIT PHASE
         DROP  BASE
         SPACE
*  SUBROUTINE FOR CLOSING SYSIN AND SYSUT1
         SPACE
CLOSE    BALR  R3,0
         USING *,R3
         L     R6,INADD
         USING IHADCB,R6
         TM    DCBOFLGS,X'10'           TEST IF OPENED
         DROP  R6
         BO    *+8
         OI    SWITCH+1,X'F0'           IF SYSIN NOT OPENED
         L     R7,UT1ADD
         CLOSE ((6),,(7))               CLOSE SYSIN AND SYSUT1
SWITCH   NOPR  RA                       RETURN IF SYSIN NOT OPENED
         FREEPOOL (R6)                  FREE SYSIN BUFFER
         BR    RA                       RETURN
         DROP  R3
         SPACE 2
         LTORG
         EJECT
* DCB EXIT LIST FOR SYSIN, SYSLIN, SYSPUNCH AND SYSPRINT
         SPACE
         DS    0F
INEX     DC    X'85',AL3(INEXRT)
LINEX    DC    X'85',AL3(LINEXRT)
PCHEX    DC    X'85',AL3(PCHEXRT)
PRINTEX  DC    X'85',AL3(PRTEXRT)
         SPACE 2
* TABLE OF THE SIZES OF WORKAREAS WHICH VARY WITH THE AVAILABLE MAIN-
* STORAGE
         SPACE
* AVAILABLE SPACE IS BETWEEN 44K AND 50K
ARTAB    EQU   *
         DC    F'51200'  50K
         DC    H'400'    MAX BLKSIZE FOR SYSIN
         DC    H'455'    MAX BLKSIZE FOR SYSPRINT
         DC    H'400'    MAX BLKSIZE FOR SYSLIN
         DC    H'80'     MAX BLKSIZE FOR SYSPUNCH
         DC    F'600'    SIZE OF ERROR POOL
         DC    F'1024'   SOURCE PROG BUFFERS 1 AND 2
         DC    F'8184'   ITAB FOR PHASE 10
         DC    F'25000'  ITAB FOR PHASE 20
         DC    F'8800'   ITAB FOR PHASE 30
         DC    F'450'    CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
         DC    F'800'    LVTAB BUFFER FOR PHASE 30
         DC    F'224'    OPTAB BUFFERS 1 AND 2 FOR PHASES 40 AND 50
         DC    F'7200'   LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'14000'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'768'    OPERATOR OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 50K AND 58K
ARTAB1   EQU   *
         DC    F'59392'  58K
         DC    H'400'    MAX BLKSIZE FOR SYSIN
         DC    H'455'    MAX BLKSIZE FOR SYSPRINT
         DC    H'400'    MAX BLKSIZE FOR SYSLIN
         DC    H'400'    MAX BLKSIZE FOR SYSPUNCH
         DC    F'1000'   SIZE OF ERROR POOL
         DC    F'1536'   SOURCE PROGRAM BUFFERS 1 AND 2
         DC    F'11924'  ITAB FOR PHASE 10
         DC    F'29500'  ITAB FOR PHASE 20
         DC    F'11500'  ITAB FOR PHASE 30
         DC    F'450'    CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
         DC    F'800'    LVTAB BUFFER FOR PHASE 30
         DC    F'910'    OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'8000'   LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'15400'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'3072'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 58K AND 66K
         DC    F'67584'  66K
         DC    H'400'    MAX BLKSIZE FOR SYSIN
         DC    H'455'    MAX BLKSIZE FOR SYSPRINT
         DC    H'400'    MAX BLKSIZE FOR SYSLIN
         DC    H'400'    MAX BLKSIZE FOR SYSPUNCH
         DC    F'1304'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 2048 WHEN TRACK OVFL SUPPORTED
         DC    F'17754'  ITAB FOR PHASE 10
         DC    F'36500'  ITAB FOR PHASE 20
         DC    F'15000'  ITAB FOR PHASE 30
         DC    F'900'    CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
         DC    F'800'    LVTAB BUFFER FOR PHASE 30
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'9600'   LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'18200'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 66K AND 76K
         DC    F'77824'  76K
         DC    H'400'    MAX BLKSIZE FOR SYSIN
         DC    H'455'    MAX BLKSIZE FOR SYSPRINT
         DC    H'400'    MAX BLKSIZE FOR SYSLIN
         DC    H'400'    MAX BLKSIZE FOR SYSPUNCH
         DC    F'1600'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 4096 WHEN TRACK OVFL SUPPORTED
         DC    F'21054'  ITAB FOR PHASE 10
         DC    F'41000'  ITAB FOR PHASE 20
         DC    F'16000'  ITAB FOR PHASE 30
         DC    F'900'    CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
         DC    F'800'    LVTAB BUFFER FOR PHASE 30
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'11200'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'21000'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 76K AND 88K
         DC    F'90112'  88K
         DC    H'400'    MAX BLKSIZE FOR SYSIN
         DC    H'455'    MAX BLKSIZE FOR SYSPRINT
         DC    H'400'    MAX BLKSIZE FOR SYSLIN
         DC    H'400'    MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 6144 WHEN TRACK OVFL SUPPORTED
         DC    F'23584'  ITAB FOR PHASE 10
         DC    F'48000'  ITAB FOR PHASE 20
         DC    F'16000'  ITAB FOR PHASE 30
         DC    F'1350'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
         DC    F'800'    LVTAB BUFFER FOR PHASE 30
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'12800'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'23800'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 88K AND 102K
         DC    F'104448' 102K
         DC    H'1600'   MAX BLKSIZE FOR SYSIN
         DC    H'1820'   MAX BLKSIZE FOR SYSPRINT
         DC    H'3200'   MAX BLKSIZE FOR SYSLIN
         DC    H'1600'   MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 6144 WHEN TRACK OVFL SUPPORTED
         DC    F'23584'  ITAB FOR PHASE 10
         DC    F'50000'  ITAB FOR PHASE 20
         DC    F'16000'  ITAB FOR PHASE 30
         DC    F'1350'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
         DC    F'800'    LVTAB BUFFER FOR PHASE 30
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'13600'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'25200'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 102K AND 118K
         DC    F'120832' 118K
         DC    H'1600'   MAX BLKSIZE FOR SYSIN
         DC    H'1820'   MAX BLKSIZE FOR SYSPRINT
         DC    H'3200'   MAX BLKSIZE FOR SYSLIN
         DC    H'1600'   MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 8192 WHEN TRACK OVFL SUPPORTED
         DC    F'32736'  ITAB FOR PHASE 10
         DC    F'62000'  ITAB FOR PHASE 20
         DC    F'19000'  ITAB FOR PHASE 30
         DC    F'2250'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
*                          INCREASE TO  2800 WHEN TRACK OVFL SUPPORTED
         DC    F'1600'   LVTAB BUFFER FOR PHASE 30
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'16000'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'30800'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 118K AND 136K
         DC    F'139264' 136K
         DC    H'1600'   MAX BLKSIZE FOR SYSIN
         DC    H'1820'   MAX BLKSIZE FOR SYSPRINT
         DC    H'3200'   MAX BLKSIZE FOR SYSLIN
         DC    H'1600'   MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 8192 WHEN TRACK OVFL SUPPORTED
         DC    F'32736'  ITAB FOR PHASE 10
         DC    F'75000'  ITAB FOR PHASE 20
         DC    F'25000'  ITAB FOR PHASE 30
         DC    F'3600'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
*                          INCREASE TO  7000 WHEN TRACK OVFL SUPPORTED
         DC    F'1600'   LVTAB BUFFER FOR PHASE 30
*                          INCREASE TO 4800 WHEN TRACK OVFL SUPPORTED
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'19200'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'35000'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 136K AND 156K
         DC    F'159744' 156K
         DC    H'1600'   MAX BLKSIZE FOR SYSIN
         DC    H'1820'   MAX BLKSIZE FOR SYSPRINT
         DC    H'3200'   MAX BLKSIZE FOR SYSLIN
         DC    H'1600'   MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 8192 WHEN TRACK OVFL SUPPORTED
         DC    F'32736'  ITAB FOR PHASE 10
         DC    F'95000'  ITAB FOR PHASE 20
         DC    F'38000'  ITAB FOR PHASE 30
         DC    F'3600'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
*                          INCREASE TO  7000 WHEN TRACK OVFL SUPPORTED
         DC    F'1600'   LVTAB BUFFER FOR PHASE 30
*                          INCREASE TO 4800 WHEN TRACK OVFL SUPPORTED
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'24000'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'35000'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 156K AND 180K
         DC    F'184320' 180K
         DC    H'1600'   MAX BLKSIZE FOR SYSIN
         DC    H'1820'   MAX BLKSIZE FOR SYSPRINT
         DC    H'3200'   MAX BLKSIZE FOR SYSLIN
         DC    H'1600'   MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 8192 WHEN TRACK OVFL SUPPORTED
         DC    F'32736'  ITAB FOR PHASE 10
         DC    F'115000' ITAB FOR PHASE 20
         DC    F'58000'  ITAB FOR PHASE 30
         DC    F'3600'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
*                          INCREASE TO  7000 WHEN TRACK OVFL SUPPORTED
         DC    F'1600'   LVTAB BUFFER FOR PHASE 30
*                          INCREASE TO 6400 WHEN TRACK OVFL SUPPORTED
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'25600'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'56000'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS BETWEEN 180K AND 208K
         DC    F'212992' 208K
         DC    H'3200'   MAX BLKSIZE FOR SYSIN
         DC    H'3640'   MAX BLKSIZE FOR SYSPRINT
         DC    H'3200'   MAX BLKSIZE FOR SYSLIN
         DC    H'3200'   MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 8192 WHEN TRACK OVFL SUPPORTED
         DC    F'32736'  ITAB FOR PHASE 10
         DC    F'135000' ITAB FOR PHASE 20
         DC    F'58000'  ITAB FOR PHASE 30
         DC    F'3600'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
*                          INCREASED TO 7000 WHEN TRACK OVFL SUPPORTED
         DC    F'1600'   LVTAB BUFFER FOR PHASE 30
*                          INCREASE TO 6400 WHEN TRACK OVFL SUPPORTED
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'25600'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'56000'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE
* AVAILABLE SPACE IS 208K OR MORE
         DC    F'16777216'
         DC    H'3200'   MAX BLKSIZE FOR SYSIN
         DC    H'3640'   MAX BLKSIZE FOR SYSPRINT
         DC    H'3200'   MAX BLKSIZE FOR SYSLIN
         DC    H'3200'   MAX BLKSIZE FOR SYSPUNCH
         DC    F'2000'   SIZE OF ERROR POOL
         DC    F'2000'   SOURCE PROGRAM BUFFERS 1 AND 2
*                          INCREASE TO 8192 WHEN TRACK OVFL SUPPORTED
         DC    F'32736'  ITAB FOR PHASE 10
         DC    F'162000' ITAB FOR PHASE 20
         DC    F'58000'  ITAB FOR PHASE 30
         DC    F'3600'   CRIDTAB FOR PHASE 30
         DC    F'1400'   SUTAB BUFFER FOR PHASE 30
*                          INCREASE TO 11200 WHEN TRACK OVFL SUPPORTED
         DC    F'1600'   LVTAB BUFFER FOR PHASE 30
*                          INCREASE TO 6400 WHEN TRACK OVFL SUPPORTED
         DC    F'1792'   OPTAB BUFFERS FOR PHASE 40 AND 50
         DC    F'25600'  LVTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'56000'  SUTAB UNSORTED AND SORTED FOR PHASE 40
         DC    F'6144'   OPERATOR/OPERAND STACK FOR PHASE 50
         SPACE 2
* PATTERNS FOR THE ERROR MESSAGES OF THE INITIALIZATION PHASE
         SPACE
* W OPTION PARAMETER ... IS INVALID. THE PARAMETER IS DISREGARDED
PTRN200  DC    AL1(128+16,200),AL2(0),CL12' '
         SPACE
* T DD CARD FOR ... IS INCORRECT OR MISSING
PTRN201  DC    AL1(128+12,201),AL2(0)
         SPACE
* W DD CARD FOR SYSLIN IS INCORRECT OR MISSING. OPTION NOLOAD IS
*   ASSUMED
PTRN202  DC    AL1(128+4,202),AL2(0)
         SPACE
* W DD CARD FOR SYSPUNCH IS INCORRECT OF MISSING. OPTION NODECK IS
*   ASSUMED
PTRN203  DC    AL1(128+4,203),AL2(0)
         SPACE
* T BLOCKSIZE SPECIFIED FOR SYSIN IS INCORRECT
PTRN204  DC     AL1(128+4,204),AL2(0)
         SPACE
* W BLOCKSIZE SPECIFIED FOR ... IS INCORRECT. UNBLOCKED OUTPUT IS
*   GENERATED INSTEAD
PTRN205A DC    AL1(128+12,205),AL2(0),CL8'SYSLIN'
PTRN205B DC    AL1(128+12,205),AL2(0),CL8'SYSPUNCH'
PTRN205C DC    AL1(128+12,205),AL2(0),CL8'SYSPRINT'
         SPACE
* W INCORRECT PARAMETER FIELD. NO OPTION PARAMETERS ARE HANDLED ANY
*   MORE
PTRN206  DC    AL1(128+4,206),AL2(0)
         SPACE
* W POSSIBLE ERROR IN DD NAMES PARAMETER
PTRN207  DC    AL1(128+4,207),AL2(0)
         SPACE
* T SIZE PARAMERER IS INCORRECT. MINIMUM STORAGE SIZE FOR ALGOL F COM-
*   PILER IS 45056 BYTES
PTRN208   DC   AL1(128+4,208),AL2(0)
         SPACE 2
* FIRST LINE OF PRINTED OUTPUT
         SPACE
SKIP     DC    X'89',C' '               RECORD WITH SKIP CTL CHAR.
FIRSTLIN DC    X'11'                    CTL PRINT AND DOUBLE SPACE
LEVEL    DC    C'LEVEL 1JUL67',CL28' ',C'OS ALGOL F',CL23' ',C'DATE  '
MONTH    DC    CL4' '
DAY      DC    CL3' '
YEAR     DC    CL4' '
         SPACE 2
* TABLE OF THE MONTH NAMES AND THE NUMBER OF DAYS
         SPACE
JANUARY  DC    H'31'
         DC    C'JAN '
FEBRUARY DC    H'28'
         DC    C'FEB '
         DC    H'31'
         DC    C'MAR '
         DC    H'30'
         DC    C'APR '
         DC    H'31'
         DC    C'MAY '
         DC    H'30'
         DC    C'JUN '
         DC    H'31'
         DC    C'JUL '
         DC    H'31'
         DC    C'AUG '
         DC    H'30'
         DC    C'SEP '
         DC    H'31'
         DC    C'OCT '
         DC    H'30'
         DC    C'NOV '
         DC    H'31'
         DC    C'DEC'
         DC    X'FF'               END OF YEAR INDICATOR
         SPACE 2
* TABLE OF OPTION PARAMETERS
         SPACE
PARMLIST DC    HL1'6'
         DC    CL9'PROGRAM'
         NI    PARMFLD,PGR
LENGTH   DC    HL1'8'                   (LENGTH-1) OF PARAM
PARAM    DC    CL9'PROCEDURE'           PARAMETER
INSTR    OI    PARMFLD,PROC             SET SWITCH IN HCOMPMOD
         DC    HL1'4'
         DC    CL9'SHORT'
         NI    PARMFLD,SHRT
         DC    HL1'3'
         DC    CL9'LONG'
         OI    PARMFLD,LNG
         DC    HL1'5'
         DC    CL9'SOURCE'
         NI    PARMFLD+1,SRCE
         DC    HL1'7'
         DC    CL9'NOSOURCE'
         OI    PARMFLD+1,NSRCE
         DC    HL1'3'
         DC    CL9'LOAD'
         NI    PARMFLD+1,LOAD
         DC    HL1'5'
         DC    CL9'NOLOAD'
         OI    PARMFLD+1,NLOAD
         DC    HL1'3'
         DC    CL9'DECK'
         NI    PARMFLD+1,DCK
         DC    HL1'5'
         DC    CL9'NODECK'
         OI    PARMFLD+1,NDCK
         DC    HL1'5'
         DC    CL9'EBCDIC'
         NI    PARMFLD+1,EBCDIC
         DC    HL1'5'
         DC    CL9'NOTEST'
         OI    PARMFLD+2,NOTEST
         DC    HL1'3'
         DC    CL9'TEST'
         NI    PARMFLD+2,TEST
         DC    HL1'2'
         DC    CL9'ISO'
         OI    PARMFLD+1,ISO
         DC    HL1'1'
         DC    CL9'NS'
         OI    PARMFLD+1,NSRCE
         DC    HL1'1'
         DC    CL9'ND'
         OI    PARMFLD+1,NDCK
         DC    HL1'1'
         DC    CL9'NL'
         OI    PARMFLD+1,NLOAD
         DC    HL1'1'
         DC    CL9'EB'
         NI    PARMFLD+1,EBCDIC
         DC    HL1'1'
         DC    CL9'PG'
         NI    PARMFLD,PGR
         DC    HL1'1'
         DC    CL9'PC'
         OI    PARMFLD,PROC
         DC    HL1'1'
         DC    CL9'NT'
         OI    PARMFLD+2,NOTEST
         DC    HL1'1'
         DC    CL9'SP'
         NI    PARMFLD,SHRT
         DC    HL1'1'
         DC    CL9'LP'
         OI    PARMFLD,LNG
         DC    HL1'0'
         DC    CL9'D'
         NI    PARMFLD+1,DCK
         DC    HL1'0'
         DC    CL9'I'
         OI    PARMFLD+1,ISO
         DC    HL1'0'
         DC    CL9'L'
         NI    PARMFLD+1,LOAD
         DC    HL1'0'
         DC    CL9'S'
         NI    PARMFLD+1,SRCE
LSTENTRY DC    HL1'0'
         DC    CL9'T'
         NI    PARMFLD+2,TEST
         SPACE 2
* WORKSPACE FOR TRANSLATING THE SIZE PARAMTER AND THE ACTUAL DATE
         SPACE
FIELD1   DS    0D
         DC    PL8'0'
FIELD2   DS    F
         EJECT
IEX10001 CSECT
         USING IEX10001,BASE
         SPACE 2
*  (INSTRUCTIONS FOR INITIALIZING IEX11 CAN BE INSERTED HERE)
         SPACE 2
         XCTL  EP=IEX11000              EXIT TO NEXT PHASE
         EJECT
* DUMMY CONTROL SECTION TO PROVIDE ADRESSABILITY OF DCB
         SPACE
         DCBD  DSORG=(PS)
 EJECT
WORKAREA DSECT
         COPY  WORKAREA
         SPACE 2
*  CWA SYMBOL DEFINITIONS FOR IEX11 CAN BE INSERTED HERE
         SPACE 2
         END   IEX10000
