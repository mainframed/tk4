PROC 0 BATCH VOLUME(HB8116) DEBUG
CONTROL NOFLUSH NOLIST NOMSG
/**************************************************************/
/*                                                            */
/*    CLIST TO RUN IPCS IN EITHER INTERACTIVE OR BATCH MODE.  */
/*                                                            */
/*                                                            */
/*    YOU MUST BE OUTSIDE OF ISPF TO RUN THIS CLIST !!!!      */
/*                                                            */
/*                                                            */
/*    BATCH ==>  SPECIFY THIS OPERAND IF YOU WANT IPCS TO     */
/*               BE RUN IN BATCH (ISPF IS DEFAULT OTHERWISE)  */
/*                                                            */
/*                                                            */
/*   VOLUME ==>  SPECIFY VOLUME FOR %#IPCSDDR CLIST TO        */
/*               ALLOCATE IPCS VSAM DUMP DIRECTORY            */
/*                                                            */
/*                                                            */
/*    DEBUG ==>  SHOW DIAGNOSTICS OF CLIST IF PROBLEM         */
/*               SHOOTING                                     */
/*                                                            */
/**************************************************************/
IF &DEBUG EQ &STR(DEBUG) THEN +
   CONTROL CONLIST SYMLIST LIST MSG
IF &STR(&SYSISPF) EQ &STR(ACTIVE) THEN +
   DO
      WRITE #IPCSBGN CLIST CANNOT BE EXECUTED UNDER ISPF
      EXIT
   END
DEL '&SYSUID..DDIR.SVCDUMP' CLUSTER
DEL '&SYSUID..IPCSPRNT'
ALLOC F(IPCSPRNT) DA('&SYSUID..IPCSPRNT') +
   NEW SPA(5 1) CYL REL REUSE
IF &BATCH NE &STR(BATCH) THEN +
   DO
      WRITE #IPCSBGN CLIST ONLINE EXECUTION
      WRITE &SYSUID..DDIR.SVCDUMP DUMP DIRECTORY WILL BE CREATED
      WRITE DATASET &SYSUID..IPCSPRNT WILL BE CREATED
      %#IPCSDDR VOLUME(&VOLUME)
      IPCS NOPARM
      SETDEF ACTIVE LENGTH(200) FLAG(ERROR) +
             DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL) +
             NOCONFIRM NOVERIFY
      ISPF PGM(BLSGLIBD) PARM(PANEL(BLSPPRIM)) NEWAPPL(BLSG) PASSLIB
      EXIT
    END
IF &BATCH EQ &STR(BATCH) THEN +
    DO
      WRITE #IPCSBGN CLIST BATCH EXECUTION
      WRITE &SYSUID..DDIR.SVCDUMP DUMP DIRECTORY WILL BE CREATED
      WRITE DATASET &SYSUID..IPCSPRNT WILL BE CREATED
      %#IPCSDDR VOLUME(&VOLUME)
      IPCS NOPARM
      SETDEF ACTIVE LENGTH(200) FLAG(ERROR) +
             DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL) +
             NOCONFIRM NOVERIFY
      EXIT
    END
