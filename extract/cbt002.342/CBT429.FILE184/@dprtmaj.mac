/**************************** REXX **********************************/
/*                                                                  */
/*      This REXX EXEC will list each address space JOBNAME/STCNAME */
/*      and its addess space dispatching priority from              */
/*      highest ASCB to lowest ASCB.                                */
/*                                                                  */
/*      Keyword operands     none                                   */
/*                                                                  */
/********************************************************************/

/*******************************************************************/
/*    Check to see if we have a valid environment to run IPCS.     */
/*    Use REXX ADDRESS keyword to set a valid IPCS environment.    */
/*******************************************************************/

subcom ipcs
if rc \= 0 then
   do
     say '@DPRTMAJ does not have a valid IPCS environment'
     exit
   end
else NOP


ADDRESS IPCS


" NOTE '***************************************************' PAGE "
" NOTE '*   @DPRTMAJ: LIST EACH ADDRESS SPACE DISPATCHING *'      "
" NOTE '*             PRIORITY FROM HIGHEST ASCB TO       *'      "
" NOTE '*             LOWEST ASCB.                        *'      "
" NOTE '***************************************************'      "


/*******************************************************************/
/*    Perform IPCS EQUATES for our CVT and the HIGHEST ASCB        */
/*    (LOWEST ASID) in the system.                                 */
/*******************************************************************/

" EQUATE  PSA  0.  STRUCTURE(PSA)  "
  if rc > 0 then
     do
       say 'EQUATE for PSA return code is' rc
       exit
     end
  else NOP

" EQUATE  CVT  PSA+10?  REMARK('CVT ADDRESS')  "
  if rc > 0 then
     do
       say 'EQUATE for CVT return code is' rc
       exit
     end
  else NOP

" EQUATE  CVTASCBH  CVT+234? REMARK('HIGHEST ASCB ADDRESS') "
  if rc > 0 then
     do
       say 'EQUATE for CVTASCBH return code is' rc
       exit
     end
  else NOP


/*******************************************************************/
/*    Issue RUNCHAIN command to go from HIGHEST ASCB to LOWEST     */
/*    ASCB on ASCB QUEUE and execute ASCB JOBNAME/DISPATCHING      */
/*    PRIORITY LISTING called external subroutine rexx exec        */
/*    (@DPRTMIN) to perform the actual processing.                 */
/*                                                                 */
/*    ATTENTION: I TURN OFF TRACE DUE TO THE FACT THAT IPCS        */
/*               PERFORMS THE SCREEN DISPLAY BEFORE THE @DPRTMAJ   */
/*               RUNCHAIN HAS COMPLETED.  THIS WILL AVOID          */
/*               MANY NASTY REXX MESSAGES AND NEGATIVE RETURN      */
/*               CODES.                                            */
/*******************************************************************/

" DROPSYM ASCB:ASCC PURGE "

trace off

"  RUNCHAIN ADDRESS(CVTASCBH) NAME(ASCB) LINK(4) ",
"           CHAIN(999) LENGTH(416) EXEC((@DPRTMIN)) "
   if rc > 0 then
      do
        say 'user entered END (PF3) before display complete'
        say 'RUNCHAIN return code is' rc
        exit
      end
   else NOP
