PROC 0 BATCH DEBUG
CONTROL NOFLUSH NOLIST NOMSG
/**************************************************************/
/*                                                            */
/*    CLIST TO RUN IPCS IN EITHER INTERACTIVE OR BATCH        */
/*    MODE USING CURRENT IPCS DUMP DIRECTORY PREVIOUSLY       */
/*    CREATED.                                                */
/*                                                            */
/*                                                            */
/*    YOU MUST BE OUTSIDE OF ISPF TO RUN THIS CLIST !!!!      */
/*                                                            */
/*                                                            */
/*    BATCH ==>  SPECIFY THIS OPERAND IF YOU WANT IPCS TO     */
/*               BE RUN IN BATCH (ISPF IS DEFAULT OTHERWISE)  */
/*                                                            */
/*                                                            */
/*    DEBUG ==>  SHOW DIAGNOSTICS OF CLIST IF PROBLEM         */
/*               SHOOTING                                     */
/*                                                            */
/**************************************************************/
IF &DEBUG EQ &STR(DEBUG) THEN +
   CONTROL CONLIST SYMLIST LIST MSG
IF &STR(&SYSISPF) EQ &STR(ACTIVE) THEN +
   DO
      WRITE #IPCSCUR CLIST CANNOT BE EXECUTED UNDER ISPF
      EXIT
   END
DEL '&SYSUID..IPCSPRNT'
ALLOC F(IPCSPRNT) DA('&SYSUID..IPCSPRNT') +
   NEW SPA(5 1) CYL REL REUSE
SET DDIR EQ &SYSDSN('&SYSUID..DDIR.SVCDUMP')
IF &DDIR EQ &STR(DATASET NOT FOUND) THEN +
   DO
      WRITE &SYSUID..DDIR.SVCDUMP NOT FOUND
      WRITE
      WRITE USE #IPCSBGN CLIST FOR PROCESSING
      %#IPCSTRM
      EXIT
   END
ALLOC F(IPCSDDIR) DA('&SYSUID..DDIR.SVCDUMP') SHR REUSE
IF &BATCH NE &STR(BATCH) THEN +
   DO
      WRITE #IPCSCUR CLIST ONLINE EXECUTION
      WRITE EXISTING &SYSUID..DDIR.SVCDUMP VSAM DUMP DIRECTORY ALLOCATED
      WRITE DATASET &SYSUID..IPCSPRNT WILL BE CREATED
      IPCS NOPARM
      SETDEF LENGTH(200) FLAG(ERROR) LIST +
             DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL) +
             NOCONFIRM NOVERIFY TERMINAL NOPRINT
      ISPF PGM(BLSGLIBD) PARM(PANEL(BLSPPRIM)) NEWAPPL(BLSG) PASSLIB
      EXIT
    END
IF &BATCH EQ &STR(BATCH) THEN +
    DO
      WRITE #IPCSCUR CLIST BATCH EXECUTION
      WRITE EXISTING &SYSUID..DDIR.SVCDUMP VSAM DUMP DIRECTORY ALLOCATED
      WRITE DATASET &SYSUID..IPCSPRNT WILL BE CREATED
      IPCS NOPARM
      SETDEF LENGTH(200) FLAG(ERROR) LIST +
             DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL) +
             NOCONFIRM NOVERIFY TERMINAL NOPRINT
      EXIT
    END
