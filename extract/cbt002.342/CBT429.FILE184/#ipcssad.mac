PROC 1 SUFX BATCH DEBUG
CONTROL NOFLUSH NOLIST NOMSG
/**************************************************************/
/*                                                            */
/*    CLIST TO RUN IPCS IN EITHER INTERACTIVE OR BATCH        */
/*    MODE.                                                   */
/*                                                            */
/*                                                            */
/*    YOU MUST BE OUTSIDE OF ISPF TO RUN THIS CLIST !!!!      */
/*                                                            */
/*                                                            */
/*    SUFX  ==>  LAST INDEX NAME OF YOUR SEQUENTIAL DUMP      */
/*               DATASET FOR THE STAND-ALONE DUMP             */
/*                                                            */
/*                                                            */
/*    BATCH ==>  SPECIFY THIS OPERAND IF YOU WANT IPCS TO     */
/*               BE RUN IN BATCH (ISPF IS DEFAULT OTHERWISE)  */
/*                                                            */
/*                                                            */
/*    DEBUG ==>  SHOW DIAGNOSTICS OF CLIST IF PROBLEM         */
/*               SHOOTING                                     */
/*                                                            */
/**************************************************************/
IF &DEBUG EQ &STR(DEBUG) THEN +
   CONTROL CONLIST SYMLIST LIST MSG
IF &STR(&SYSISPF) EQ &STR(ACTIVE) THEN +
   DO
      WRITE #IPCSSAD CLIST CANNOT BE EXECUTED UNDER ISPF
      EXIT
   END
DEL '&SYSUID..IPCSPRNT'
ALLOC F(IPCSPRNT) DA('&SYSUID..IPCSPRNT') +
   NEW SPA(5 1) CYL REL REUSE
ALLOC F(IPCSDDIR) DA('&SYSUID..DDIR.SADUMP.&SUFX') SHR REUSE
IF &BATCH NE &STR(BATCH) THEN +
   DO
      WRITE #IPCSSAD CLIST ONLINE EXECUTION
      WRITE SEQUENTIAL DUMP DATASET IS &SYSUID..SADUMP.&SUFX
      WRITE DATASET &SYSUID..IPCSPRNT WILL BE CREATED
      IPCS NOPARM
      SETDEF DSNAME('&SYSUID..SADUMP.&SUFX') +
             LENGTH(200) FLAG(ERROR) LIST +
             DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL) +
             NOCONFIRM NOVERIFY TERMINAL NOPRINT
      ISPF PGM(BLSGLIBD) PARM(PANEL(BLSPPRIM)) NEWAPPL(BLSG) PASSLIB
      EXIT
    END
IF &BATCH EQ &STR(BATCH) THEN +
    DO
      WRITE #IPCSSAD CLIST BATCH EXECUTION
      WRITE SEQUENTIAL DUMP DATASET IS &SYSUID..SADUMP.&SUFX
      WRITE DATASET &SYSUID..IPCSPRNT WILL BE CREATED
      IPCS NOPARM
      SETDEF DSNAME('&SYSUID..SADUMP.&SUFX') +
             LENGTH(200) FLAG(ERROR) LIST +
             DISPLAY(MACHINE,REMARK,STORAGE,SYMBOL) +
             NOCONFIRM NOVERIFY TERMINAL NOPRINT
      EXIT
    END
