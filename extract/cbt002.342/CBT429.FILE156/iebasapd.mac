* ------------------------------------------------------------------ *
RTNCLN   SAVE  (14,12),,*              SAVE CALLERS REGS IN SAVEAREA #1
*
         LR    R12,R15                 LOAD ROUTINE ENTRY POINT ADDR
         USING RTNCLN,R12,R11          EST ROUTINE ADDRESSABILITY
         LR    R11,R12
         A     R11,RCF4096
*
         LA    R2,72(R13)              LOAD ADDR OF SAVEAREA #2
         ST    R13,4(R2)               SAVE ADDR OF SAVEAREA #1 IN
*                                      SAVEAREA #2
         ST    R2,8(R13)               SAVE ADDR OF SAVEAREA #2 IN
*                                      SAVEAREA #1
         LR    R13,R2                  LOAD ADDR OF SAVEAREA #2 IN R13
*
         USING DCBAREA,R9              R9 SHOULD POINT TO DCB AREA
         USING WORKAREA,R10            R10 SHOULD POINT TO WORK AREA
*
         B     RCROPN                  *
*
RCF4096  DC    F'4096'                 *
* ------------------------------------------------------------------ *
RCROPN   OPEN  (ASAPRPT,OUTPUT)        OPEN REPORT DATASET
         LA    R6,ASAPRPT              LOAD ADDR OF DCB
         USING IHADCB,R6               *
         TM    DCBOFLGS,X'10'          WAS DD STATEMENT MISSING?
         BNZ   RCAOPN                  NO,
*
RCROERR  EQU   *
         @ERROR RTNERR,RTNCLN0,COR,OR    /CLEANUP/OPEN/ASAPRPT//
*
         DROP  R6
* ------------------------------------------------------------------ *
RCAOPN   XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         OPEN  (ASAPACB)               OPEN ASAP DATABASE
         LTR   R15,R15                 WAS OPEN SUCCESSFUL?
         BZ    RCAMOD1                 YES, RETURN TO CALLER
*
RCAOERR  LR    R3,R15                  SAVE RETURN CODE
         SHOWCB ACB=ASAPACB,AREA=ERROR,LENGTH=4,FIELDS=(ERROR)
         LTR   R15,R15                 WAS SHOWACB SUCCESSFUL?
         BZ    RCAOF1                  YES,
*
         @ERROR RTNERR,RTNCLN1,CSA1,SA1  /CLEANUP/SHOWCB/ASAPDB/#1/
*
RCAOF1   L     R4,ERROR                SAVE ERROR CODE
*
         @ERROR RTNERR,RTNCLN1,COAR,OA   /CLEANUP/OPEN/ASAPDB/REGS/
* ------------------------------------------------------------------ *
RCAMOD1  MODCB RPL=ASAPRPL,OPTCD=(SEQ,UPD)  MODIFY ASAP RPL
         LTR   R15,R15                 WAS MODIFY SUCCESSFUL?
         BZ    RCAPNT                  YES,
*
         @ERROR RTNERR,RTNCLN2,CMA1,MA1  /CLEANUP/MODCB/ASAPRPL/#1/
* ------------------------------------------------------------------ *
RCAPNT   @FILL AREA=ASAPKEY,CHAR=40    BLANK RECORD KEY, PRIME REC
*
         XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         POINT RPL=ASAPRPL             POSITION TO PRIME RECORD
         LTR   R15,R15                 WAS OPEN SUCCESSFUL?
         BZ    RCGBL                   YES,
*
RCAPERR  LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=ASAPRPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWCB SUCCESSFUL?
         BZ    RCAPF1                  YES,
*
         @ERROR RTNERR,RTNCLN2,CSA,SA   /CLEANUP/SHOWCB/ASAPRPL//
*
RCAPF1   L     R4,ERROR                SAVE ERROR CODE
*
         @ERROR RTNERR,RTNCLN2,CPAR,PA  /CLEANUP/POINT/ASAPRPL/REGS/
* ------------------------------------------------------------------ *
RCGBL    @CALL RTNGBL                  GLOBAL ZONE/CSI ROUTINE
*
         C     R15,F0                  WAS RETURN CODE A 0?
         BE    RCHDR                   YES,
         C     R15,F4                  WAS RETURN CODE A 4?
         BE    RTNCLN2                 YES,
*
         ABEND 122,DUMP                IMPOSSIBLE ERROR, LARGE ROACH
* ------------------------------------------------------------------ *
RCHDR    ZAP   RPAGCTR,P0              ZERO REPORT PAGE COUNTER
         ZAP   RLINCTR,P0              ZERO REPORT LINE COUNTER
         ZAP   RECCTR,P0               ZERO RECORDS IN DB COUNTER
         ZAP   DELCTR,P0               ZERO DELETED RECORDS COUNTER
*
RCHDR3   @FILL AREA=HDR3DATA,CHAR=40   CLEAR HEADER WORK AREA
         LA    R6,HDR3FUNC             LOAD ADDR COMMAND DESCRIPTION
         MVC   0(8,R6),CLEANUP         INDICATE CLEANUP COMMAND
         LA    R6,8(,R6)               POINT PAST LITERAL
         CLI   CHECKRUN,C'Y'           IS THIS A CHECK RUN?
         BNE   RCHDR3A                 NO,
         MVC   0(6,R6),CHECK           INDICATE CHECK RUN
         LA    R6,6(,R6)               POINT PAST LITERAL
RCHDR3A  MVC   0(10,R6),PROCESS        COMPLETE HDR3
*
RCHDR1   MVC   HDR1MSG,GENHDR1         COMPLETE HDR1
*
RCHDR2   MVC   HDR2MSG,GENHDR2         COMPLETE HDR2
*
         @CALL RTNRHDR                 WRITE INITIAL REPORT HEADERS
* ------------------------------------------------------------------ *
RCAGET   @FILL AREA=ASAPREC,CHAR=40    CLEAR ASAP RECORD AREA
*
         XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         GET   RPL=ASAPRPL             READ RECORD FROM ASAP DATABASE
         LTR   R15,R15                 WAS GET SUCCESSFUL?
         BZ    RCCKPRIM                YES,
*
RCAGERR  LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=ASAPRPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWCB SUCCESSFUL?
         BZ    RCAGF1                  NO,
*
         @ERROR RTNERR,RTNCLN3,CSA2,SA2  /CLEANUP/SHOWCB/ASAPRPL/#2/
*
RCAGF1   L     R4,FEEDBACK             SAVE FEEDBACK CODE
         CLC   FEEDBACK,F4             WAS FEEDBACK A 4?
         BE    RTNCLN@                 YES, END OF FILE
*
         @ERROR RTNERR,RTNCLN3,CRAR,RA1  /CLEANUP/GET/ASAPRPL/REGS/
* ------------------------------------------------------------------ *
RCCKPRIM CLC   ASAPKEY,BLANKS          IS THIS THE PRIME RECORD?
         BE    RCAGET                  YES, GET NEXT RECORD
         AP    RECCTR,P1               ADD 1 TO RECORD COUNTER
* ------------------------------------------------------------------ *
RCGGET2  @FILL AREA=GBLREC,CHAR=00     CLEAR GLOBAL ZONE RECORD AREA
*
         MVC   GZONEID,=XL2'0000'      INSERT ZONEID
         MVC   GRTYP,=XL2'0300'        INSERT RECORD TYPE
         MVC   GKEYDATA,GLOBAL         INSERT RECORD NAME
         MVC   GRSUBTYP,=XL2'2030'     INSERT RECORD SUB-TYPE
         MVC   GSEQNO,=XL2'0000'       INSERT SEQUENCE NUMBER
         MVC   GDATA(7),ASAPFMID       INSERT FMID
         MVI   GDATA+7,C' '
*
         @CALL RTNRDG                  READ GLOBAL ZONE ROUTINE
*
         C     R15,F0                  RECORD FOUND?
         BE    RCAGET                  YES,
         C     R15,F4                  RECORD NOT FOUND?
         BE    RCRREC                  YES,
*
         @ERROR RTNERR,RTNCLN3,CRG,CFMD
* ------------------------------------------------------------------ *
RCRREC   EQU   *
         MVC   RPTAPAR,ASAPAPAR        GET APAR NUMBER
         MVC   RPTFMID,ASAPFMID        GET FMID
         MVC   RPTPTF,ASAPPTF          GET FIXING PTF NUMBER
         MVC   RPTPE,ASAPPE            GET PE PTF NUMBER
         MVC   RPTPID,ASAPPID          GET PRODUCT IDENTIFIER
         MVC   RPTDATE,ASAPDATE        GET DATE
         MVC   RPTHIP,ASAPHIP          SET HIPER STATUS
         MVC   RPTZE,ASAPZE            SET ZER APAR STATUS
RCRREC@  EQU   *                       *
* ------------------------------------------------------------------ *
RCADEL   CLI   CHECKRUN,C'Y'           IS THIS A CHECK RUN?
         BE    RCERASED                YES,
*
         XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         ERASE RPL=ASAPRPL             DELETE REC FROM ASAP DATABASE
         LTR   R15,R15                 WAS ERASE SUCCESSFUL?
         BZ    RCERASED                YES,
*
RCAEERR  LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=ASAPRPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWCB SUCCESSFUL?
         BZ    RCAEF1                  NO,
*
         @ERROR RTNERR,RTNCLN3,CSA3,SA3  /CLEANUP/SHOWCB/ASAPRPL/#3/
*
RCAEF1   L     R4,FEEDBACK             SAVE FEEDBACK CODE
*
         @ERROR RTNERR,RTNCLN3,CDAR,EA   /CLEANUP/ERASE/ASAPRPL/REGS/
* ------------------------------------------------------------------ *
RCERASED @MVC  RLINEMSG,MSG021         COMPLETE REPORT RECORD
         @CALL RTNRLIN                 WRITE RECORD TO ASAPRPT
         AP    DELCTR,P1               ADD 1 TO DELETED COUNTER
         B     RCAGET                  GO READ NEXT RECORD FROM DB
* ------------------------------------------------------------------ *
RTNCLN@  @FILL AREA=MSG000,CHAR=40     CLEAR OUTPUT RECORD
         @FILL AREA=RPTLINE,CHAR=40    CLEAR REPORT RECORD
         @CALL RTNRLIN                 WRITE RECORD TO ASAPRPT
*
         OI    RECCTR+2,X'0F'          FORCE TO NUMERIC
         @MVC  MSG000A,MSG101          GET MSG
         UNPK  MSG000A+9(5),RECCTR     UNPACK COUNTER
         PUT   ASAPRPT,MSG000          WRITE RECORD TO ASAPRPT
         @CALL RTNOLIN                 WRITE RECORD TO ASAPOUT
*
         OI    DELCTR+2,X'0F'          FORCE TO NUMERIC
         @MVC  MSG000A,MSG105          GET MSG
         UNPK  MSG000A+9(5),DELCTR     UNPACK COUNTER
         PUT   ASAPRPT,MSG000          WRITE RECORD TO ASAPRPT
         @CALL RTNOLIN                 WRITE RECORD TO ASAPOUT
*
         @FILL AREA=MSG000,CHAR=40     CLEAR OUTPUT RECORD
         @CALL RTNOLIN                 WRITE RECORD TO ASAPOUT
* ------------------------------------------------------------------ *
RTNCLN3  @CALL RTNGBL,ENTRY=4          GLOBAL ZONE/CSI ROUTINE
*
         C     R15,F0                  WAS RETURN CODE A 0?
         BE    RTNCLN2                 YES,
         C     R15,F4                  WAS RETURN CODE A 4?
         BE    RTNCLN2                 YES,
*
         ABEND 122,DUMP                IMPOSSIBLE ERROR, LARGE ROACH
* ------------------------------------------------------------------ *
RTNCLN2  CLOSE ASAPACB                 CLOSE ASAP DATABASE
* ------------------------------------------------------------------ *
RTNCLN1  CLOSE (ASAPRPT)               CLOSE ASAPRPT DATASET
* ------------------------------------------------------------------ *
RTNCLN0  L     R13,4(R13)              *
         RETURN (14,12),,RC=0          *
* ------------------------------------------------------------------ *
         DROP  R9                      *
         DROP  R10                     *
         DROP  R11                     *
         DROP  R12                     *
*
         LTORG                         IEBASAPD
*
LENCLN   EQU   *-RTNCLN
* ------------------------------------------------------------------ *
