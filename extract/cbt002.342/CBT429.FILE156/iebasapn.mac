* ------------------------------------------------------------------ *
* SUBROUTINE TO READ RECORDS FROM THE REQUESTED ZONE                 *
*                                                                    *
* RETURN CODES:                                                      *
*   0 - RECORD FOUND                                                 *
*   4 - RECORD NOT FOUND                                             *
*   8 - ERROR                                                        *
* ------------------------------------------------------------------ *
RTNRDZ   SAVE  (14,12),,*              SAVE CALLERS REGS IN SAVEAREA #1
*
         LR    R12,R15                 LOAD ROUTINE ENTRY POINT ADDR
         USING RTNRDZ,R12              EST ROUTINE ADDRESSABILITY
*
         LA    R2,RDZSAVE              LOAD ADDR OF OUR SAVEAREA
         ST    R13,4(R2)               SAVE ADDR OF CALLER SAVEAREA IN
*                                      OUR SAVEAREA
         ST    R2,8(R13)               SAVE ADDR OF OUR SAVEAREA IN
*                                      CALLER SAVEAREA
         LR    R13,R2                  LOAD ADDR OF OUR SAVEAREA IN R13
*
         USING DCBAREA,R9              R9 SHOULD POINT TO DCB AREA
         USING WORKAREA,R10            R10 SHOULD POINT TO WORK AREA
         B     RDZ                     *
*
RDZSAVE  DS    18F                     *
* ------------------------------------------------------------------ *
RDZ      MVI   REFRDZ,C'N'
* ------------------------------------------------------------------ *
RDZONE   XR    R3,R3                   *
         XR    R4,R4                   *
*
         GET   RPL=ZONERPL             READ RECORD FROM REQUESTED ZONE
         LTR   R15,R15                 WAS GET SUCCESSFUL?
         BZ    RTNRDZ0                 YES,
*
         LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=ZONERPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWRPL SUCCESSFUL?
         BZ    RDZFDBK                 YES,
*
         @ERROR RTNERR,RTNRDZ8,RSZ,0   /READZONE/SHOWCB/ZONERPL//
*
RDZFDBK  L     R4,FEEDBACK             SAVE FEEDBACK CODE
         CLC   FEEDBACK,F16            WAS FEEDBACK A 16?
         BE    RTNRDZ4                 YES, RECORD NOT FOUND
*
         @ERROR RTNERR,RTNRDZ8,RRZR,0  /READZONE/GET/ZONERPL/REGS/
* ------------------------------------------------------------------ *
RTNRDZ0  L     R13,4(R13)              *
         RETURN (14,12),,RC=0          *
* ------------------------------------------------------------------ *
RTNRDZ4  L     R13,4(R13)              *
         RETURN (14,12),,RC=4          *
* ------------------------------------------------------------------ *
RTNRDZ8  L     R13,4(R13)              *
         RETURN (14,12),,RC=8          *
* ------------------------------------------------------------------ *
         DROP  R9
         DROP  R10
         DROP  R12
*
         LTORG                         *
*
LENRDZ   EQU   *-RTNRDZ
* ------------------------------------------------------------------ *
