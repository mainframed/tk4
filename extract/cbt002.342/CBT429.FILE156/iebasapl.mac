* ------------------------------------------------------------------ *
* ENTRY CODE OF 0 INDICATES ALLOCATION                               *
*   - CHECK TO SEE IF REQUESTED ZONE RECORD IS IN GLOBAL ZONE        *
*   - ENQUEUE THE REQZONE CSI DATA SET                               *
*   - DYNAMICALLY ALLOCATE THE REQUESTED ZONE CSI DATA SET           *
*   - OPEN THE REQZONE CSI                                           *
*   - VERFIY THAT THE DATA SET CONTAINS SMP/E DATA                   *
*   - VERFIY THAT THE DATA SET CONTAINS THE REQUESTED ZONE           *
*   - MODIFY THE ACB FOR FULL-KEY, DIRECT ACCESS                     *
*                                                                    *
* ENTRY CODE OF 4 INDICATES DEALLOCATION                             *
*   - MODIFY THE ACB FOR GENERIC KEY, SEQUENTIAL ACCESS              *
*   - CLOSE THE REQZONE CSI                                          *
*   - DEQUEUE THE REQZONE CSI DATA SET                               *
*                                                                    *
* ANY ERROR CAUSES THE ROUTINE TO RETURN A NON-ZERO (4) RETURN CODE. *
* ------------------------------------------------------------------ *
RTNZONE  SAVE  (14,12),,*              SAVE CALLERS REGS IN SAVEAREA #1
*
         LR    R12,R15                 LOAD ROUTINE ENTRY POINT ADDR
         USING RTNZONE,R12             EST ROUTINE ADDRESSABILITY
*
         LA    R2,ZONESAVE             LOAD ADDR OF OUR SAVEAREA
         ST    R13,4(R2)               SAVE ADDR OF CALLER SAVEAREA IN
*                                      OUR SAVEAREA
         ST    R2,8(R13)               SAVE ADDR OF OUR SAVEAREA IN
*                                      CALLER SAVEAREA
         LR    R13,R2                  LOAD ADDR OF OUR SAVEAREA IN R13
*
         USING DCBAREA,R9              R9 SHOULD POINT TO DCB AREA
         USING WORKAREA,R10            R10 SHOULD POINT TO WORK AREA
         B     ZONE                    *
*
ZONESAVE DS    18F                     *
* ------------------------------------------------------------------ *
ZONE     MVI    REFZONE,C'N'           RESET ERROR FLAG
         C      R1,F0                  IS ENTRY FOR ALLOCATION?
         BZ     RZCHKZON               YES,
         C      R1,F4                  IS ENTRY FOR DEALLOCATION?
         BZ     RZMOD2                 YES,
*
         @ERROR RTNERR,RTNZONE@,ZK,0   /ZONEZONEE/ENTCODE///
* ------------------------------------------------------------------ *
RZCHKZON @FILL AREA=GBLREC,CHAR=00     ZERO GLOBAL ZONE RECORD AREA
*
         MVC   GZONEID,=XL2'0000'      INSERT ZONEID
         MVC   GRTYP,=XL2'0300'        INSERT RECORD TYPE
         MVC   GKEYDATA,GLOBAL         INSERT RECORD NAME
         MVC   GRSUBTYP,=XL2'2010'     INSERT RECORD SUSBTYPE
         MVC   GSEQNO,=XL2'0000'       INSERT SEQUENCE NUMBER
         MVC   GDATA(8),CNTLZONE       INSERT REQUESTED ZONENAME
*
         XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         GET   RPL=GBLRPL              READ RECORD FROM GLOBAL ZONE
         LTR   R15,R15                 WAS GET SUCCESSFUL?
         BZ    RZENQ                   YES,
*
RZGERR   LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=GBLRPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWCB SUCCESSFUL?
         BZ    RZGFDBK                 YES,
*
         @ERROR RTNERR,RTNZONE@,ZSG,0  /ZONE/SHOWCB/GBLRPL//
*
RZGFDBK  L     R4,FEEDBACK             SAVE FEEDBACK CODE
         CLC   FEEDBACK,F16            WAS FEEDBACK A 16?
         BE    RZQERR                  YES, REQZONE NOT PRESENT
*
         @ERROR RTNERR,RTNZONE@,ZRGR,0 /ZONE/GET/GBLRPL/REGS/
*
RZQERR   EQU   *
         @ERROR RTNERR,RTNZONE@,ZRZQ,0 /ZONE/GET/GBLRPL/NOREQZONE/
* ------------------------------------------------------------------ *
RZENQ    @FILL AREA=ZONEDATA,CHAR=00   CLEAR WORK AREA
*
         MVC   ZONEDATA,GDATA          GET DATA FROM GLOBAL ZONE RECORD
         TR    ZONEDATA,TRTAB1         TRANSLATE ALL X'00' TO X'40'
*
         MVC   QNAME,GIMSMP            SET QNAME
         MVC   RNAMEDSN,ZONEDSN        SET RNAME
*
         XR    R3,R3                   *
         XR    R4,R4                   *
         LA    R3,RNAMEDSN             LOAD ADDR OF DSN
RZENQDSN CLI   0(R3),X'40'             IS THIS POSTIION A BLANK?
         BE    RZENQLEN                YES,
         CLI   0(R3),X'00'             IS THIS POSITION A NULL?
         BE    RZENQLEN                YES,
         C     R4,F44                  HAVE WE CHECKED ALL POSITIONS?
         BE    RZEDERR                 YES,
         LA    R3,1(,R3)               POINT TO NEXT POSITION
         A     R4,F1                   ADD 1 TO COUNTER
         B     RZENQDSN                CHECK NEXT POSITION
RZENQLEN ST    R4,RNAMELEN             SAVE LENGTH OF DSN
*
         ENQ   (QNAME,RNAME,E,0,SYSTEMS),RET=USE
*
         LTR   R15,R15                 WAS ENQUEUE SUCESSFUL?
         BZ    RZDYNAM                 YES,
*
RZEERR   EQU   *
         CLI   3(R15),X'08'            ALREADY HAVE ENQUEUE?
         BE    RZDYNAM                 YES, MULTI ZONES IN CSI
         CLI   3(R15),X'14'            ALREADY HAVE ENQUEUE?
         BE    RZDYNAM                 YES, MULTI ZONES IN CSI
         @ERROR RTNERR,RTNZONE@,ZEZ,0  /ZONE/ENQUEUE/ZONEACB//
*
RZEDERR  EQU   *
         @ERROR RTNERR,RTNZONE@,ZEZD,0 /ZONE/ENQUEUE/ZONEACB/DSNAME/
* ------------------------------------------------------------------ *
RZDYNAM  MVC   P01DATA,ZONEDDN         INSERT DDNAME IN S99 PARMLIST
         MVC   P02DATA,ZONEDSN         INSERT DSNAME IN S99 PARMLIST
         MVC   P07DATA,BLANKS          CLEAR VOLSER RETURN AREA
         LA    R1,S99PTR               LOAD ADDR OF S99 PARMLIST
*
         XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         DYNALLOC                      CALL DYNAMIC ALLOCATION
         LTR   R15,R15                 WAS ALLOCATION SUCCESSFUL?
         BZ    RZOPN                   YES,
*
         @ERROR RTNERR,RZDEQ,ZAZR,0    /ZONE/DYNALLOC/ZONEACB/REGS/
* ------------------------------------------------------------------ *
RZOPN    MVC   ZONEACB+40(8),ZONEDDN   USE ZONENAME FOR DDNAME
*
         XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         OPEN  (ZONEACB)               OPEN ASAP DATABASE
         LTR   R15,R15                 WAS OPEN SUCCESSFUL?
         BZ    RZGET1                  YES,
*
RZOERR   LR    R3,R15                  SAVE RETURN CODE
         SHOWCB ACB=ZONEACB,AREA=ERROR,LENGTH=4,FIELDS=(ERROR)
         LTR   R15,R15                 WAS SHOWACB SUCCESSFUL?
         BZ    RZOFDBK                 YES,
*
         @ERROR RTNERR,RZDEQ,ZSZ1,0    /ZONE/SHOWCB/ZONEACB/#1/
*
RZOFDBK  L     R4,ERROR                SAVE ERROR CODE
*
         @ERROR RTNERR,RZDEQ,ZOZR,0    /ZONE/OPEN/ZONEACB/REGS/
* ------------------------------------------------------------------ *
RZGET1   XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         GET   RPL=ZONERPL             READ RECORD FROM GLOBAL ZONE
         LTR   R15,R15                 WAS GET SUCCESSFUL?
         BZ    RZGCKZR1                YES,
*
RZGERR1  LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=ZONERPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWRPL SUCCESSFUL?
         BZ    RZGFDBK1                YES,
*                                      *
         @ERROR RTNERR,RZCLOSE,ZSZ2,0  /ZONE/SHOWCB/ZONERPL/#2/
*
RZGFDBK1 L     R4,FEEDBACK             SAVE FEEDBACK CODE
*
         @ERROR RTNERR,RZCLOSE,ZRZR,1 /ZONE/GET/ZONERPL/REGS/
*
RZGCKZR1 CLC   ZKEYDATA,ZPOOL          IS THIS THE ZPOOL1 RECORD?
         BE    RZGET2                  YES,
*
         @ERROR RTNERR,RZCLOSE,ZRZC,0  /ZONE/GET/ZONERPL/NOTCSI/
* ------------------------------------------------------------------ *
RZGET2   XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         GET   RPL=ZONERPL             READ RECORD FROM ZONE
         LTR   R15,R15                 WAS GET SUCCESSFUL?
         BZ    RZGCKZR2                YES,
*
RZGERR2  LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=ZONERPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWRPL SUCCESSFUL?
         BZ    RZGFDBK2                YES,
*                                      *
         @ERROR RTNERR,RZCLOSE,ZSZ3,0  /ZONE/SHOWCB/ZONERPL/#3/
*
RZGFDBK2 L     R4,FEEDBACK             SAVE FEEDBACK CODE
*
         @ERROR RTNERR,RZCLOSE,ZRZR,1  /ZONE/GET/ZONERPL/REGS/
*
RZGCKZR2 CLC   =XL4'00000200',ZONEKEY  IS THIS A ZONE RECORD?
         BE    RZGCKZR3                YES,
         BH    RZGET2                  NOT YET,
*
         @ERROR RTNERR,RZCLOSE,ZRZZ,0  /ZONE/GET/ZONERPL/NOZONE/
*
RZGCKZR3 CLC   ZDATA(8),ZONEDDN        IS DDNAME'S RECORD PRESENT?
         BNE   RZGET2                  NO, TRY AGAIN
         MVC   ZONEID,ZONEREC+4        YES, GET ZONEID KEY
* ------------------------------------------------------------------ *
RZMOD1   MODCB RPL=ZONERPL,KEYLEN=24,OPTCD=(FKS,DIR)
         LTR   R15,R15                 WAS MODIFY SUCCESSFUL?
         BZ    RTNZONE0                YES,
*
         @ERROR RTNERR,RZCLOSE,ZMZ1,0  /ZONE/MODCB/ZONERPL/#1/
* ------------------------------------------------------------------ *
RZMOD2   MODCB RPL=ZONERPL,KEYLEN=14,OPTCD=(GEN,SEQ)
         LTR   R15,R15                 WAS MODIFY SUCCESSFUL?
         BZ    RZCLOSE                 YES,
*
         @ERROR RTNERR,RZCLOSE,XMZ2,0  /ZONE/MODCB/ZONERPL/#1/
* ------------------------------------------------------------------ *
RZCLOSE  XR    R3,R3                   CLEAR REG 3
         XR    R4,R4                   CLEAR REG 4
*
         CLOSE (ZONEACB)               OPEN ASAP DATABASE
         LTR  R15,R15                 WAS OPEN SUCCESSFUL?
         BZ    RZDEQ                   YES,
*
RZCERR   LR    R3,R15                  SAVE RETURN CODE
         SHOWCB ACB=ZONEACB,AREA=ERROR,LENGTH=4,FIELDS=(ERROR)
         LTR   R15,R15                 WAS SHOWACB SUCCESSFUL?
         BZ    RZCFDBK                 YES,
*
         @ERROR RTNERR,RZDEQ,ZSZ4,0    /ZONE/SHOWCB/ZONEACB/#4/
*
RZCFDBK  L     R4,ERROR                SAVE ERROR CODE
*
         @ERROR RTNERR,RZDEQ,ZCGR,0    /ZONE/OPEN/ZONERPL/REGS/
* ------------------------------------------------------------------ *
RZDEQ    MVC   QNAME,GIMSMP            SET QNAME
         MVC   RNAMEDSN,ZONEDSN        SET RNAME
*
         XR    R3,R3                   *
         XR    R4,R4                   *
         LA    R3,RNAMEDSN             LOAD ADDR OF DSN
RZDEQDSN CLI   0(R3),X'40'             IS THIS POSTIION A BLANK?
         BE    RZDEQLEN                YES,
         CLI   0(R3),X'00'             IS THIS POSITION A NULL?
         BE    RZDEQLEN                YES,
         C     R4,F44                  HAVE WE CHECKED ALL POSITIONS?
         BE    RZDDERR                 YES,
         LA    R3,1(,R3)               POINT TO NEXT POSITION
         A     R4,F1                   ADD 1 TO COUNTER
         B     RZDEQDSN                CHECK NEXT POSITION
RZDEQLEN ST    R4,RNAMELEN             SAVE LENGTH OF DSN
*
         DEQ   (QNAME,RNAME,0,SYSTEMS),RET=HAVE
*
         LTR   R15,R15                 WAS DEQUEUE SUCESSFUL?
         BZ    RTNZONE@                YES,
*
RZDERR   EQU   *
         @ERROR RTNERR,RTNZONE@,ZFZ,0  /XREF/DEQUEUE/ZONEACB//
*
RZDDERR  EQU   *
         @ERROR RTNERR,RTNZONE@,ZFZ,0  /XREF/DEQUEUE/ZONEACB/DSNAME/
* ------------------------------------------------------------------ *
RTNZONE@ CLI   REFZONE,C'Y'
         BE    RTNZONE4
* ------------------------------------------------------------------ *
RTNZONE0 L     R13,4(R13)              *
         RETURN (14,12),,RC=0          *
* ------------------------------------------------------------------ *
RTNZONE4 L     R13,4(R13)              *
         RETURN (14,12),,RC=4          *
* ------------------------------------------------------------------ *
         DROP  R9
         DROP  R10
         DROP  R12
*
         LTORG                         *
*
LENZONE  EQU   *-RTNZONE
* ------------------------------------------------------------------ *
