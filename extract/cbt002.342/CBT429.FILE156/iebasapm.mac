* ------------------------------------------------------------------ *
* SUBROUTINE TO READ RECORDS FROM THE GLOBAL ZONE                    *
*                                                                    *
* RETURN CODES:                                                      *
*   0 - RECORD FOUND                                                 *
*   4 - RECORD NOT FOUND                                             *
*   8 - ERROR                                                        *
* ------------------------------------------------------------------ *
RTNRDG   SAVE  (14,12),,*              SAVE CALLERS REGS IN SAVEAREA #1
*
         LR    R12,R15                 LOAD ROUTINE ENTRY POINT ADDR
         USING RTNRDG,R12              EST ROUTINE ADDRESSABILITY
*
         LA    R2,RDGSAVE              LOAD ADDR OF OUR SAVEAREA
         ST    R13,4(R2)               SAVE ADDR OF CALLER SAVEAREA IN
*                                      OUR SAVEAREA
         ST    R2,8(R13)               SAVE ADDR OF OUR SAVEAREA IN
*                                      CALLER SAVEAREA
         LR    R13,R2                  LOAD ADDR OF OUR SAVEAREA IN R13
*
         USING DCBAREA,R9              R9 SHOULD POINT TO DCB AREA
         USING WORKAREA,R10            R10 SHOULD POINT TO WORK AREA
         B     RDG                     *
*
RDGSAVE  DS    18F                     *
* ------------------------------------------------------------------ *
RDG      MVI   REFRDG,C'N'
* ------------------------------------------------------------------ *
RDGLOBAL XR    R3,R3                   *
         XR    R4,R4                   *
*
         GET   RPL=GBLRPL              READ RECORD FROM REQUESTED ZONE
         LTR   R15,R15                 WAS GET SUCCESSFUL?
         BZ    RTNRDG0                 YES,
*
         LR    R3,R15                  SAVE RETURN CODE
         SHOWCB RPL=GBLRPL,AREA=FEEDBACK,LENGTH=4,FIELDS=(FDBK)
         LTR   R15,R15                 WAS SHOWRPL SUCCESSFUL?
         BZ    RDGFDBK                 YES,
*
         @ERROR RTNERR,RTNRDG8,RSG,0   /READZONE/SHOWCB/GBLRPL//
*
RDGFDBK  L     R4,FEEDBACK             SAVE FEEDBACK CODE
         CLC   FEEDBACK,F16            WAS FEEDBACK A 16?
         BE    RTNRDG4                 YES, RECORD NOT FOUND
*
         @ERROR RTNERR,RTNRDG8,RRGR,0  /READZONE/GET/GBLRPL/REGS/
* ------------------------------------------------------------------ *
RTNRDG0  L     R13,4(R13)              *
         RETURN (14,12),,RC=0          *
* ------------------------------------------------------------------ *
RTNRDG4  L     R13,4(R13)              *
         RETURN (14,12),,RC=4          *
* ------------------------------------------------------------------ *
RTNRDG8  L     R13,4(R13)              *
         RETURN (14,12),,RC=8          *
* ------------------------------------------------------------------ *
         DROP  R9
         DROP  R10
         DROP  R12
*
         LTORG                         *
*
LENRDG   EQU   *-RTNRDG
* ------------------------------------------------------------------ *
