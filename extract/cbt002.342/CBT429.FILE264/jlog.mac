*          DATA SET SFT8MVBFN1 AT LEVEL 002 AS OF 02/17/92
*          DATA SET SFT8MVBFN  AT LEVEL 002 AS OF 12/05/89
         TITLE 'CURRENT JOB LOG PRINT PROGRAM'
***********************************************************************
*        THIS PROGRAM FULFILLS THE NEED TO HAVE JES2 JOBLOGS FROM     *
*        CERTAIN STARTED TASKS.  IT WILL ALSO ALLOW THE JOBLOG TO     *
*        BE ARCHIVED TO SAF IF DESIRED.                               *
*                                                                     *
*        IT MUST BE AUTHORIZED TO FUNCTION PROPERLY.                  *
*                                                                     *
*        INPUT CAN COME FROM EITHER THE EXEC PARM OR SYSIN.  IN BOTH  *
*        CASES THE FORMAT IS ONE OR MORE OF THE FOLLOWING KEYWORDS    *
*        SEPERATED BY BLANKS OR COMMAS:                               *
*                                                                     *
*                JLOG         REQUESTS THE JES JOB LOG                *
*                JCL          REQUESTS THE OS JCL LISTING             *
*                JMSG         REQUESTS THE OS ALLOCATION MSGS         *
*                DSID(#,#,...#)  ONE TO NINE JES2 DATASET IDS (LAST   *
*                                FIVE  CHARACTERS OF THE JES2 DSNAME: *
*                                103 IN 'JES2.JOB00451.SO000103'      *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                       AUTHOR: GUY ALBERTELLI   07/18/82             *
*                       LOCATION: SFT.LINKLIB                         *
*                                                                     *
*                       CHANGED 05/06/87 VAUGHN AUSTIN FOR IBM APAR   *
*                       OZ93770 (PTF UZ82296 - HJE2330) WHICH CHANGED *
*                       LENGTH OF DSID FOR PSO DATASETS TO 6 FROM 4.  *
*                                                                     *
*                       CHANGED 12/04/89 GUY ALBERTELLI FOR 370/ESA.  *
*                        JES2 CHANGED THE NAME OF INPUT DSN FROM      *
*                        '.SI000000' TO 'I0000000'                    *
*                                                                     *
*                       CHANGED 02/03/92 GUY ALBERTELLI FOR ESA 4.2.  *
*                        JES2 CHANGED THE NAME OF INPUT DSN FROM      *
*                        TO USER.JOBNAME.JOBID.DSID.? AND             *
*                        CHANGED SPECIFICATION OF SYSIN TO TEXTUNIT.  *
*                                                                     *
*                       CHANGED 02/17/92 GUY ALBERTELLI               *
*                        IN ESA 4.2 IF DSID HAS NEVER BEEN OPENED,    *
*                        JES WILL ALLOCATE IT BUT NOT OPEN IT. HANDLE *
*                        OPEN ERROR.                                  *
*                                                                     *
***********************************************************************
JOBLOG   CSECT
SFT8MVBF EQU   JOBLOG
         ENTRY SFT8MVBF
         STM   R14,R12,12(R13)         SAVE REGS
         BALR  R12,0                   LOAD BASE
         USING *,R12                   DECLARE BASE
         LR    R15,R13                 DO
         LA    R13,SAVE                   FORWARD AND
         ST    R13,8(R15)                 BACKWARD SAVEAREA
         ST    R15,4(R13)                 CHAINING;
         ST    R1,INPARM           SAVE PARM LIST
*
**       SETUP WORKAREAS FOR MESSAGE PROCESSING
*
*        $BFGMSG WORKAR=MSGWORK,MSGAREA=MSGAREA
         $BFGMSG WORKAR=MSGWORK,MSGAREA=MSGAREA
         SPACE
*        ACCESS THIS JOB ID
         SPACE
         USING PSA,R0
         L     R1,PSATOLD          POINT AT TCB
         L     R1,TCBJSCB-TCB(R1)  GET TO JSCB
         L     R1,JSCBACT-IEZJSCB(R1) GET TO ACTIVE JSCB
         L     R1,JSCBSSIB-IEZJSCB(R1) GET TO GOOD SSIB
         USING SSIB,R1
         MVC   MYJBID,SSIBJBID     MOVE IN NAME
         OC    MYJBID+3(5),=C'00000'
         DROP  R1
*
**       PARSE THE PARM STRING IF ANY
*
         L     R2,INPARM           GET PARM LIST POINTER
         L     R2,0(R2)            GET PARM STRING POINTER
         CLC   0(2,R2),=H'0'       ANY STRING
         BE    TRYSYSIN            NOPE
         MVI   FLAG,0              SET OFF ALL FLAGS
         XC    DSAREA,DSAREA       SET OFF ALL FLAGS
         KEYVAL (R2),KEYTAB
         B     RCTBL(R15)          GO AND PROCESS IN LOOP
TRYSYSIN DS    0H
*
**       OPEN SYSIN AND PROCESS EACH CARD
*
LOOPA    TM    SYSIN+48,X'10'      IS DCB OPEN
         BO    LOOPB               IF SO THEN READ CARD
         OPEN  (SYSIN,(INPUT))
         TM    SYSIN+48,X'10'      IS DCB OPEN
         BZ    NOSYSIN             IF NOT THEN QUIT
LOOPB    GET   SYSIN,CARD
         MVI   FLAG,0              SET OFF ALL FLAGS
         XC    DSAREA,DSAREA       SET OFF ALL FLAGS
         KEYVAL CRDHEAD,KEYTAB
         B     RCTBL(R15)
RCTBL    B     PROCESS             =0  CARD PARSED
         B     ERR1                =4  VALUE OF KEYWORD BAD
         B     ERR2                =8  KEYWORD INVALID
         B     ERR3                =12 NUMBR OF VALUES WRONG
         B     PROCESS             =16 CARD BLANK
         B     ERR4                =20 TYPE CHECK FAILED
         B     ERR5                =24 LENGTH CHECK FAILED
*
**       VALUE OF KEYWORD INVALID
*
ERR1     DS    0H '
         LR    R2,R0               SAVE POINTER
         $BFGMSG 9,(C4,(R2))
         B     LOOPA
*
**       KEYWORD INVALID
*
ERR2     DS    0H
         LR    R2,R0               SAVE POINTER
         $BFGMSG 5,(C8,(R2))
         B     LOOPA
*
**       NUMBER OF VALUES WRONG
*
ERR3     DS    0H
         LR    R2,R0               SAVE POINTER
         $BFGMSG 6,(C4,(R2))
         B     LOOPA
*
**       TYPE CHECK FAILED
*
ERR4     DS    0H
         LR    R2,R0               SAVE POINTER
         $BFGMSG 7,(C4,(R2))
         B     LOOPA
*
**       LENGTH CHECK FAILED
*
ERR5     DS    0H
         LR    R2,R0               SAVE POINTER
         LR    R3,R1               SAVE POINTER
         $BFGMSG 8,(C4,(R2)),(C8,(R3))
         B     LOOPA
*
**       IF REQUESTED THEN DUMP OUT THE JLOG
*
PROCESS  TM    FLAG,FJLOG          JLOG REQUESTED ?
         BZ    TRYJCL
         LA    R1,MYJBID           POINT TO MY JOBID
         LA    R0,2                THE DSID FOR JES LOG
         LA    R15,MOVEJES         GO AND MOVE IT
         BALR  R14,R15             GO AND MOVE IT
*
**       IF REQUESTED THEN DUMP OUT THE JCL
*
TRYJCL   TM    FLAG,FJCL           JCL REQUESTED ?
         BZ    TRYJMSG
         LA    R1,MYJBID           POINT TO MY JOBID
         LA    R0,3                THE DSID FOR JES JCL
         LA    R15,MOVEJES         GO AND MOVE IT
         BALR  R14,R15             GO AND MOVE IT
*
**       IF REQUESTED THEN DUMP OUT THE JMSG
*
TRYJMSG  TM    FLAG,FJMSG          JMSG REQUESTED ?
         BZ    TRYDSID
         LA    R1,MYJBID           POINT TO MY JOBID
         LA    R0,4                THE DSID FOR MVS ALLOCATION MSGS
         LA    R15,MOVEJES         GO AND MOVE IT
         BALR  R14,R15             GO AND MOVE IT
*
**       IF REQUESTED DO DSIDS
*
TRYDSID  TM    FLAG,FDSID          DSID REQUESTED ?
         BZ    LOOPA               EXIT IF NOT
         SR    R5,R5               CLEAR COUNT
         IC    R5,DSAREA           GET NUMBER TO DO
         LA    R4,DSAREA+1         POINT TO THE ONE TO DO
         LTR   R5,R5               ANY TO DO
         BZ    LOOPA               IF NOT THEN ALL DONE
LOOP1    SR    R2,R2               CLEAR FOR LENGTH
         IC    R2,0(R4)            GET LENGTH OF DATA
         BCTR  R2,0                GET MACHING LENGTH
         EX    R2,PACKIT
         LA    R4,2(R2,R4)
         CVB   R0,DWORK            GET ID
         LA    R1,MYJBID
         LA    R15,MOVEJES         GO AND MOVE IT
         BALR  R14,R15             GO AND MOVE IT
         BCT   R5,LOOP1            DO NEXT ID
         B     LOOPA               ALL DONE
PACKIT   PACK  DWORK,1(0,R4)
*
**       OUT OF SYSIN DATA
*
NOCARD   DS    0H
         CLOSE (SYSIN)
         B     EXITPGM             ALL DONE
*
**       IF NO SYSIN AVAILABLE AND THERE WAS NO PARM STRING
**       THEN JUST DUMP OUT THE JLOG
*
NOSYSIN  DS    0H
         L     R2,INPARM           GET PARM LIST POINTER
         L     R2,0(R2)            GET PARM STRING POINTER
         CLC   0(2,R2),=H'0'       ANY STRING
         BNE   EXITPGM             YES SO WE DID SOMETHING
         LA    R1,MYJBID           POINT TO MY JOBID
         LA    R0,2                THE DSID FOR JES LOG
         LA    R15,MOVEJES         GO AND MOVE IT
         BALR  R14,R15             GO AND MOVE IT
*
*  EXIT THE PROGRAM
*
EXITPGM  L     R13,4(R13)
         RETURN (14,12),RC=0
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*        THIS SUBROUTINE MOVES ONE JES FILE TO THE OUTPUT             *
*        DATASET.                                                     *
*                                                                     *
*        INPUT                                                        *
*              R1  -->   8 BYTE JOBID                                 *
*              R0  ===   DATASET NUMBER                               *
*                                                                     *
*        OUPUT                                                        *
*              R15 ===   0   DATASET MOVED                            *
*                        4   UNABLE TO ALLOCATE DATASET               *
*                        8   ERROR OCCURED DURING MOVE                *
*                                                                     *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         PUSH  USING
MOVEJES  PROLOGUE SAVE=INLINE
*                                                       ESA4.2 2/92 GLA
**       BUILD DSN                                      ESA4.2 2/92 GLA
*                                                       ESA4.2 2/92 GLA
         MVC   DSN,=CL44' '        CLEAR FIELD          ESA4.2 2/92 GLA
         L     R4,PSAAOLD          GET ASCB             ESA4.2 2/92 GLA
         USING ASCB,R4                                  ESA4.2 2/92 GLA
         ICM   R3,15,ASCBJBNI      GET JOB NAME         ESA4.2 2/92 GLA
         BNZ   *+8                                      ESA4.2 2/92 GLA
         L     R3,ASCBJBNS         GET STC NAME         ESA4.2 2/92 GLA
         L     R4,ASCBASXB         POINT TO ASXB        ESA4.2 2/92 GLA
         L     R4,ASXBSENV-ASXB(,R4) GET ACEE ADDR      ESA4.2 2/92 GLA
         USING ACEE,R4                                  ESA4.2 2/92 GLA
         SR    R5,R5               CLEAR REG            ESA4.2 2/92 GLA
         IC    R5,ACEEUSRL         GET LENGTH           ESA4.2 2/92 GLA
         MVC   DSN(8),ACEEUSRI     MOVE IN USERID       ESA4.2 2/92 GLA
         DROP  R4                                       ESA4.2 2/92 GLA
         LA    R5,DSN(R5)          POINT PAST USERID    ESA4.2 2/92 GLA
         MVI   0(R5),C'.'          SET DELIMITER        ESA4.2 2/92 GLA
         LA    R5,1(,R5)           BUMP POINTER         ESA4.2 2/92 GLA
         MVC   0(8,R5),0(R3)       MOVE IN JOB NAME     ESA4.2 2/92 GLA
         LA    R5,8(R5)            POINT PAST           ESA4.2 2/92 GLA
MJ1      CLI   0(R5),C' '          END OF JOB NAME      ESA4.2 2/92 GLA
         BNE   MJ2                 YES- FOUND IT        ESA4.2 2/92 GLA
         BCTR  R5,0                BACK UP              ESA4.2 2/92 GLA
         B     MJ1                 TRY AGAIN            ESA4.2 2/92 GLA
MJ2      MVI   1(R5),C'.'          SET DELIMITER        ESA4.2 2/92 GLA
         LA    R5,2(R5)            BUMP POINTER         ESA4.2 2/92 GLA
         MVC   0(8,R5),0(R1)       MOVE IN ID           ESA4.2 2/92 GLA
         MVC   8(2,R5),=C'.D'      SET NEXT DELIMITER AND HEADER/92 GLA
         LA    R5,10(R5)           BUMP POINTER         ESA4.2 2/92 GLA
         CVD   R0,DWORK            MAKE DECIMAL         ESA4.2 2/92 GLA
         UNPK  0(7,R5),DWORK       PUT IN NUMBER        ESA4.2 2/92 GLA
         OI    6(R5),X'F0'         CONVERT LAST DIGIT   ESA4.2 2/92 GLA
         MVC   7(2,R5),=C'.?'      SET LAST CHARS       ESA4.2 2/92 GLA
*                                                       ESA4.2 2/92 GLA
**       NOW ALLOCATE IT                                ESA4.2 2/92 GLA
*                                                       ESA4.2 2/92 GLA
         MVC   INFO(4),=F'0'       ZERO OUT THE INFO CODES
         LA    R1,DYNALLOC         POINT TO ALLOCATION
         LR    R5,R1               SAVE R1 IN CASE OF ABEND
         DYNALLOC ALLOCATE THEM
         LTR   R15,R15             CHECK ALLOCATION
         BZ    OPEN
         CLC   INFO(2),=X'0478'    IS DS UNACCESSIBLE
         BE    BD1                 YES SO DIFFERENT MESSAGE
         $BFGMSG 1,(H2,INFO),(H2,INFO+2)
         DC    H'0'
         LA    R3,4                SET ALLOCATION ERROR
         B     ENDJES1
BD1      DS    0H                  CANT ACCESS MESSAGE
         $BFGMSG 10,(C5,DSID+1)    CHANGED FOR APAR OZ93770       VGA
         DC    H'0'
         LA    R3,4                SET ALLOCATION ERROR
         B     ENDJES1
         SPACE 3
*
**       OPEN THE JOB LOG AND OUTPUT FILE
**       THEN COPY IT
*
OPEN     DS    0H
         USING IFGACB,R2           SETUP ADDRESS ABILITY
         LA    R2,MACB
         MVC   ACBDDNM,DDNAME      SET DDNAME
         OPEN  (MACB,(INPUT),OUT,(EXTEND))
         LTR   R15,R15             GOOD GET
         BZ    GDOPEN              IF OK
         $BFGMSG 2,(H1,ACBERFLG)
         B     EOF                                     ESA 4.2 02/17/92
         DROP  R2
GDOPEN   NOP   0
         SPACE 3
LOOP     GET   RPL=MRPL            GET A RECORD
         LTR   R15,R15             GOOD GET
         BZ    GDGET               IF OK
         USING IFGRPL,R2
         LA    R2,MRPL
*
**       IF PHYSICAL READ ERROR THEN ASSUME EOF
*
         CLI   RPLRTNCD,X'0C'      IS IT PHYSICAL ERROR
         BNE   BD2
         CLI   RPLERRCD,4          IS IT PHY READ ERROR
         BE    EOF                 YES SO MAKE EOF
BD2      DS    0H
         $BFGMSG 3,(H1,RPLRTNCD),(H1,RPLERRCD)
         DROP  R2
         LA    R3,8                SET RETURN CODE
         B     ENDJES2             AND EXIT
GDGET    DS    0H
         SHOWCB RPL=MRPL,AREA=LENGTH,LENGTH=4,FIELDS=(RECLEN)
         LTR   R15,R15             SEE IF OK
         BZ    GDSHOW              YES
         ST    R15,LENGTH
         $BFGMSG 4,(H1,LENGTH+3)
         LA    R3,8                SET RETURN CODE
         B     ENDJES2             AND EXIT
GDSHOW   DS    0H
         L     R1,LENGTH
         LA    R1,4(R1)            ADD FOR RDW
         STH   R1,VBA
         PUT   OUT,VBA             OUTPUT LINE
         B     LOOP
*
*  EXIT THE PROGRAM
*
EOF      EQU   *
         SR    R3,R3
ENDJES2  CLOSE (MACB,,OUT)
ENDJES1  L     R13,4(R13)
         LR    R15,R3
         RETURN (14,12),RC=(15)
         POP   USING
         EJECT
         PRINT NOGEN
         $BFGMSGD 1,'ALLOCATION FAILED ERRCD=Ö, INFO=Ö.',ROUTCDE=11
         $BFGMSGD 2,' OPEN FAILED FDBK=Ö.',ROUTCDE=11
         $BFGMSGD 3,' GET FAILED, RC=Ö, ERRCD=Ö.',ROUTCDE=11
         $BFGMSGD 4,' SHOWCB FAILED, RC=Ö.',ROUTCDE=11
         $BFGMSGD 5,' STRING STARTING WITH ''Ö'' IS INVALID.',         X
               ROUTCDE=11
         $BFGMSGD 6,' NUMBER OF VALUES IN ''Ö'' IS ILLEGAL.',          X
               ROUTCDE=11
         $BFGMSGD 7,' VALUE NOT NUMERIC FOR ''Ö''.',                   X
               ROUTCDE=11
         $BFGMSGD 8,' VALUE FOR ''Ö'' THAT STARTS ''Ö'' HAS WRONG LENGTH
               .',ROUTCDE=11
         $BFGMSGD 9,' VALUE INVALID FOR ''Ö''.',                       X
               ROUTCDE=11
         $BFGMSGD 10,' DATASET UNAVAILABLE PROBABLY SPUN DSID=Ö',      X
               ROUTCDE=11
         EJECT
*
**       INPUT CARD KEY WORDS
*
KEYTAB   KEYT  JLOG,,,,(0,0),                                          X
               FIELD1=(,O,FLAG,FJLOG)
         KEYT  JCL,,,,(0,0),                                           X
               FIELD1=(,O,FLAG,FJCL)
         KEYT  JMSG,,,,(0,0),                                          X
               FIELD1=(,O,FLAG,FJMSG)
         KEYT  DSID,,DSAREA,NUMERIC,(1,9),                             X
               FIELD1=(,O,FLAG,FDSID),                                 X
               LEN=(1,5),END=YES        CHANGED FOR APAR OZ93770    VGA
         PRINT GEN
         EJECT
*
**       WORKAREAS FOR MAIN PROGRAM
*
SAVE     DS    18F
MSGWORK  DS    18F
INPARM   DS    F                   PARM LIST VALUE
MSGAREA  DS    CL133
MYJBID   DS    CL8                 SPACE FOR THIS JOB JBID
FLAG     DC    X'00'               FLAGS
FJLOG    EQU   X'80'               DUMP JLOG ID=2
FJCL     EQU   X'40'               DUMP JCL ID=3
FJMSG    EQU   X'20'               DUMP JMSG ID=4
FDSID    EQU   X'10'               DUMP LIST OF DSIDS
DSAREA   DS    (1+(1+3)*9)C        AREA FOR MAX DATA
CRDHEAD  DC    H'72'
CARD     DS    CL80
SYSIN    DCB   DDNAME=SYSIN,                                           X
               LRECL=80,RECFM=FB,EODAD=NOCARD,                         X
               MACRF=(GM),DSORG=PS
         EJECT
*
**       WORKAREAS FOR DYNAMIC ALLOCATION
*
         DS    0D
DWORK    DS    D
DYNALLOC DC    X'80',AL3(*+3)
CODES    DC    X'1401000000000000',A(TEXT,0,0)
INFO     EQU   CODES+4,2
TEXT     DC    A(TEXT1,TEXT2,TEST2A,TEST2B,TEST2C)
         DC    X'80',AL3(TEXT3)
TEXT1    DC    X'005500010008'                DDNAME=
DDNAME   DC    CL8' '
TEST2A   DC    X'00520000'                    PERMANENT ALLOCATION
TEST2B   DC    X'001C0000'                    FREE AT CLOSE
TEST2C   DC    X'00620001000180'   DALSSAT(SYSIN REQ)   ESA4.2 2/92 GLA
TEXT2    DC    X'005C0000'                    SSREQ
TEXT3    DC    AL2(2,1,L'DSN)                 DSNAME
DSN      DC    CL44' '                     CHANGED FOR ESA4.2 2/92 GLA
*        DC    C'JES2.JOB08364.I0000002'   CHANGED FOR ESA 12/04/89 GLA
*        DC    C'JES2.JOB08364.SI000002'   CHANGED FOR APAR OZ93770 VGA
JBID     EQU   DSN+17,8
DSID     EQU   DSN+28,6                    CHANGED FOR APAR OZ93770 VGA
         EJECT
*
**       WORKAREAS FOR DATA MOVEMENT
*
LENGTH   DC    F'0'
EXLST    EXLST EODAD=(EOF)
MACB     ACB   DDNAME=A,                                               X
               MACRF=(ADR,SEQ,IN),                                     X
               EXLST=EXLST
MRPL     RPL   ACB=MACB,                                               X
               OPTCD=(ADR,SEQ,SYN,NUP,MVE),                            X
               AREA=LINE,                                              X
               AREALEN=133
OUT      DCB   DDNAME=SYSPRINT,                                        X
               LRECL=137,BLKSIZE=141,RECFM=VB,                         X
               MACRF=(PM),DSORG=PS
         DS    0F
VBA      DC    AL2(0,0)
LINE     DC    CL180' '
         REQUATE
         LTORG
         PRINT NOGEN
         IHAPSA
         IHAASCB ,                                      ESA4.2 2/92 GLA
         IHAASXB ,                                      ESA4.2 2/92 GLA
         IHAACEE ,                                      ESA4.2 2/92 GLA
         IKJTCB
         IEFJSSIB
         IEZJSCB
         IFGACB
         IFGRPL
         END
