//DUDASD       JOB  (U32020,17F),RUDDLE,CLASS=R,MSGCLASS=T
//*-----------------------------------------------------------------*
//*       INSTALL OF THE 'DUDASD' TSO CMD                           *
//*-----------------------------------------------------------------*
//DUDASD   EXEC ASMHCL,
//         MAC1='SYS1.AMODGEN',
//         PARM.LKED='LIST,LET,MAP,XREF,RENT'
//ASM.SYSLIB  DD   DCB=BLKSIZE=9440
//            DD
//            DD
//            DD   DSN=SFT.INSTALL.TSO.COMMANDS,DISP=SHR
 TITLE 'D U D A S D R  ---   D A S D   V S N   D I S P L A Y'
DUDASDR  CSECT
         SPACE
         USING    DSAWRK,R13
         USING    CVTMAP,R12
         USING    CPPL,R1
         USING    IOPL,R2
         USING    UCBOB,R10
         SPACE
         JVDENTER DSA=(DSAEND-DSAWRK),BASE=(R11),VERSION=&SYSPARM
         SPACE
         LA       R2,IOPLIST
         MVC      IOPLECT,CPPLECT
         MVC      IOPLUPT,CPPLUPT
         XC       ECB,ECB
         XC       PREFIX(4),PREFIX
         LA       R15,L'BUFFER+4
         STH      R15,PREFIX
         L        R12,CVTPTR
*                                                                XA-BFG
**  SUPPORT FOR MVS/XA UCB SEARCH ROUTINE                        XA-BFG
*                                                                XA-BFG
MYILK2   EQU   X'28'               CVTILK2 OFFSET IN NON-XA      XA-BFG
MYUCBSC  EQU   X'434'              CVTUCBSC OFFSET IN XA         XA-BFG
         TM    CVTDCB,X'93'        Q. IS THIS XA                 XA-BFG
         BO    XA1                 A. YES                        XA-BFG
         L     R7,MYILK2(,R12)     GET LOOKUP TABLE              XA-BFG
         B     AL1                                               XA-BFG
XA1      DS    0H                                                XA-BFG
         XC    IOSWK(100),IOSWK    CLEAR WORKAREA                XA-BFG
         LA    R1,IOSWK            GET WORK AREA                 XA-BFG
         ST    R1,IOSPLST          SAVE IT                       XA-BFG
         LA    R1,=X'20'           SET DASD TYPE                 XA-BFG
         ST    R1,IOSPLST+4        SAVE IT ALSO                  XA-BFG
         LA    R1,UCBADD           LOCATION TO PUT ADDRESS       XA-BFG
         ST    R1,IOSPLST+8        AND THIS ONE TOO              XA-BFG
         OI    IOSPLST+8,X'80'     SET END OF PARM LIST INDICATORXA-BFG
         L     R7,MYUCBSC(,R12)    GET ADDRESS OF ROUTINE        XA-BFG
AL1      DS    0H                                                XA-BFG
         XR       R10,R10
         EJECT
DULINE   EQU   *
         SPACE
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         LA    R5,BUFFER
         LA    R4,L'BUFFER/L'ELEMENT
         USING ELEMENT,R5
         SPACE
DUUNIT   EQU   *
         TM    CVTDCB,X'93'        Q. IS THIS XA                 XA-BFG
         BO    XA2                 A. YES                        XA-BFG
         CLI   0(R7),X'FF'
         BE    PUT
         LH    R15,0(R7)
         LA    R7,2(R7)
         CR    R15,R10
         BNH   DUUNIT
         LR    R10,R15
         CLI   UCBTYP+2,X'20'
         BNE   DUUNIT
         B     AL2                 SKIP XA CODE                  XA-BFG
XA2      DS    0H                                                XA-BFG
         LA    R1,IOSPLST          POINT AT PLIST                XA-BFG
         LR    R15,R7              GET ROUTINE ADDRESS           XA-BFG
         BALR  R14,R15             CALL ROUTINE                  XA-BFG
         LTR   R15,R15             Q. ANY ERRORS                 XA-BFG
         BNZ   PUT                 A. IF SO THEN ALL DONE        XA-BFG
         L     R15,UCBADD          GET UCB ADDRESS               XA-BFG
         CR    R15,R10             Q. SEE IF IN ASSCENDING SEQ   XA-BFG
         BNH   DUUNIT              A. IF NOT THEN IGNORE         XA-BFG
         LR    R10,R15             SET NEW ADDRESS               XA-BFG
AL2      DS    0H                                                XA-BFG
         CLC   LASTCUU,UCBNAME
         BE    DUUNIT
         MVC   ELEUNIT,UCBNAME
         MVC   LASTCUU,UCBNAME
         MVC   ELEVSN,SRTEVOLI
         TM    SRTESTAT,UCBONLI
         BO    ONLINE
         MVC   ELEVSN,=CL6'(OFF)'
         B     ULOOP
ONLINE   MVI   ELEDASH,C'-'
         MVI   ELESTAT,C'O'
         TM    SRTESTAT,UCBALOC
         BNO   NALLOC
         MVI   ELESTAT,C'A'
NALLOC   TM    SRTESTAT,UCBCHGS
         BNO   RESERV
         MVI   ELESTAT,C'P'
RESERV   CLI   UCBSQC,0
         BE    MOUNT
         MVI   ELERSRV,C'R'
MOUNT    TM    SRTESTAT,UCBRESV        IS IT RESERVED(MOUNT)
         BZ    RESIDENT                NO - CHECK RESIDENT
         MVC   ELEMSTAT(3),=C'RSV'     SET C'RSV' IN LINE
         B     ULOOP                   CONTINUE TO NEXT UCB
RESIDENT TM    SRTESTAT,UCBPRES        IS IT RESIDENT
         BZ    ULOOP                   NO - CONTINUE TO NEXT UCB
         MVC   ELEMSTAT(3),=C'RSD'     SET C'RSD' IN LINE
ULOOP    LA    R5,ELENEXT
         BCT   R4,DUUNIT
         SPACE
PUT      CLI   BUFFER,C' '
         BE    DONE
         SPACE
         PUTLINE                                                       +
               PARM=PUTLINE,MF=(E,IOPLIST),ECB=ECB,                    +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     +
               OUTPUT=(PREFIX,TERM,SINGLE,DATA)
         SPACE
         B     DULINE
         EJECT
DONE     JVDLEAVE
         EJECT
         LTORG
         SPACE
ELEWRK   DSECT
ELEMENT  DS    0CL20
ELEUNIT  DS    CL3
         DS    CL1
ELEVSN   DS    CL6
ELEDASH  DS    CL1
ELESTAT  DS    CL1
ELERSRV  DS    CL1
ELEMSTAT DS    CL3
         DS    CL2
ELENEXT  DS    0CL1
         SPACE
DSAWRK   DSECT
SSA      DS    18F
IOPLIST  DS    07F
ECB      DS    01F
PREFIX   DS    2H
BUFFER   DS    CL(L'ELEMENT*4)
LASTCUU  DS    CL3
PUTLINE  PUTLINE MF=L
UCBADD   DS    F                   ADDRESS OF FOUND UCB          XA-BFG
IOSPLST  DS    3F                  PARM LIST                     XA-BFG
IOSWK    DS    25F                 IOS WORK AREA                 XA-BFG
DSAEND   DS    0D
         EJECT
         IKJIOPL
         EJECT
         IKJCPPL
         EJECT
UCBWRK   DSECT
         IEFUCBOB
         SPACE
         JVDEQU
         EJECT
         CVT   DSECT=YES
         END   DUDASDR
//LKED.SYSLMOD DD DSN=ISPVS.TEST.LOAD,DISP=SHR,UNIT=,SPACE=
//SYSIN    DD *
  ENTRY DUDASDR
  NAME DUDASD(R)
/*
