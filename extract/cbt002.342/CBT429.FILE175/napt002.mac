++ USERMOD (NAPT002) .
++ VER (Z038) FMID(HTE21C2).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(IKJEFF10),DISP=SHR
//SYSIN  DD  SYS1.USERSRC(IKJEFF10),DISP=SHR
++SRC (IKJEFF10) DISTLIB(USERSRC).
*          DATA SET IKJEFF10   AT LEVEL 003 AS OF 12/09/85
IKJEFF10 CSECT ,                                                NAPT002
*** START --- NORTH AMERICAN PHILIPS LIGHTING MOD PROLOG ------ NAPT002
***                                                             NAPT002
*** FUNCTION.                                                   NAPT002
***       1. ALLOW UTILITY JOBS BY RECOGNIZING A SPECIAL USER   NAPT002
***          ID OF $$$$$$ WHICH WOULD BE CHANGED TO THE         NAPT002
***          SUBMITTERS ID WHEN IT IS FOUND IN THE JOB NAME     NAPT002
***          AND WHICH WILL BE SEARCHED FOR AND REPLACED        NAPT002
***          ANYWHERE ELSE IT OCCURS IN THE JCL (BUT NOT THE    NAPT002
***          DATA.                                              NAPT002
***                                                             NAPT002
***       2. DISALLOW SUBMISSION OF JOBS WITH A MESSAGE CLASS   NAPT002
***          OR SYOUT OF X OR Y  WHICH CANNOT BE LOOKED AT BY   NAPT002
***          THE SUBMITTER.  (FCH, FCI, CURRENT FIRST SHIFT     NAPT002
***          OPERATOR AND GENERIC OPERATOR SIGNONS CAN SUBMIT   NAPT002
***          JOBS WITH ANY JOB NAME, TEST JOBS WILL BE FORCED   NAPT002
***          TO 7 CHRS IF THEY ARE 6 CHRS AND NOT THE SUBMITTER NAPT002
***          LEGAL JOB NAMES ARE THE SUBMITTERS-ID OR THE       NAPT002
***          SUBMITTER'S ID PLUS 1 CHARACTER.                   NAPT002
***                                                             NAPT002
*** DEPENDENCIES.                                               NAPT002
***                                                             NAPT002
***         1. NORMAL LINKAGE CONVENTIONS ARE USED.             NAPT002
***         2. THE MODULE IS RE-ENTRANT AND DOES NOT CALL ANY   NAPT002
***            SUBROUTINES OR USE SYSTEM SERVICES OTHER THAN    NAPT002
***            GETMAIN/FREEMAIN                                 NAPT002
***                                                             NAPT002
*** REGISTER CONVENTIONS.                                       NAPT002
***                                                             NAPT002
***         R1  - AFFECTED BY TRT INSTRUCTION                   NAPT002
***         R2  - AFFECTED BY TRT INSTRUCTION                   NAPT002
***         R3  - POINTER TO SUBMITTER'S USER-ID                NAPT002
***         R4  - NOT USED                                      NAPT002
***         R5  - WORK REGISTER FOR SIZE OF FIELDS              NAPT002
***         R6  - POINTER TO THE CARD IMAGE                     NAPT002
***         R7  - BASE REGISTER FOR SUBMIT SECONDARY DSECT      NAPT002
***         R8  - POINTER TO THE MESSAGE AREA (TO/FROM USER)    NAPT002
***         R9  - BASE REGISTER FOR SUBMIT DSECT                NAPT002
***         R10 - BASE REGISTER FOR SUBROUTINE                  NAPT002
***         R11 - SIZE REGISTER FOR FIELD SCANS                 NAPT002
***         R12 - POINTER TO FIELD TO BE SCANNED                NAPT002
***                                                             NAPT002
*** CONTROL BLOCKS. (MAPPING MACROS FOLLOWED BY ATTRIBUTE)      NAPT002
***                                                             NAPT002
***         IKJEFFIE - TYPE=SUBMIT GENERATES                    NAPT002
***             IEDSECTD - GENERAL POINTERS AND FLAGS           NAPT002
***             IESUBCTD - SUBMIT POINTERS AND FLAGS            NAPT002
***                                                             NAPT002
***                                                             NAPT002
*** CHANGE ACTIVITY.                                            NAPT002
***                                                             NAPT002
***      CHANGES BY C F MORRIS, WESTINGHOUSE LAMP               NAPT002
***    1. REMOVE THE GETMAIN AND FREEMAIN FOR THE SAVE AREA     NAPT002
***       SINCE THEY ARE NOT NEEDED BECAUSE NOTHING IS          NAPT002
***       CALLED.                                               NAPT002
***    2. CHECK FOR 6 CHR USER-ID PLUS 1 CHR OR 6 CHR USER-ID   NAPT002
***       EQUAL TO THAT OF THE SUBMITTER OR 6 QUESTION MARKS    NAPT002
***       (??????) PLUS 1 CHR AS VALID JOB NAME.  IF 6          NAPT002
***       QUESTION MARKS REPLACE WITH USER-ID OF SUBMITTER AND  NAPT002
***       AND TURN ON SWITCHES TO SCAN FOR ?????? IN ALL JCL    NAPT002
***       REMOVE GENERAL DYNAMICS CODE.                         NAPT002
***    CHANGES 1-23 TO GET THE EXIT CODED RIGHT                 NAPT002
***    CHANGES 24-28 TO JUST REJECT JOBS WHICH GO TO MSGCLASS   NAPT002
***        X AND LACK THE SUBMITTERS ID IN THE JOB NAME AND     NAPT002
***        TO TREAT $$$$$$ THE SAME AS 6 QUESTION MARKS.        NAPT002
***    CHANGES 29-34 TO REJECT JOBS WHICH CONTAIN MSGCLASS=X    NAPT002
***        OR SYSOUT=X AND WHICH ARE NOT SUBMITTED BY           NAPT002
***        AUTHORIZED SUBMITTERS, ALLOW AUTHORIZED SUBMITTERS   NAPT002
***        TO SUBMIT IF TSO USER-ID FORMATTED JOB NAME, AND     NAPT002
***        VERIFY SUBMISSION DESIRED OF NON-TSO JOB BY          NAPT002
***        AUTHORIZED SUBMITTERS.                               NAPT002
***                                                             NAPT002
***    02/15/85-02/19/85 - FORCE A JCL ERROR ON THE CARD WITH   NAPT002
***        THE OFFENDING MESSAGE (SYSOUT) CLASS, IF CARD NOT    NAPT002
***        FLAGGED AS A JOB CARD, ALSO REPEAT THE SUBMIT        NAPT002
***        FAILURE MESSAGE.  I WOULD HAVE PURGED THE JOB BUT    NAPT002
***        THE DEVELOPERS OF JES3 IN THEIR INFINITE LACK OF     NAPT002
***        WISDOM AND REFUSAL OF JES2 COMPATIBILITY DO NOT      NAPT002
***        RECOGNIZE THE /*PURGE                                NAPT002
***                                                             NAPT002
***   03/07/85                                                  NAPT002
***        JES3 MODIFIED TO USE /*PURGE BY RECOGNITION IN       NAPT002
***        IATUX33 AND SETTING OPERATOR CANCEL NO PRINT BIT     NAPT002
***                                                             NAPT002
***   06/21/85                                                  NAPT002
***        REMOVE JCLERROR CONSTANT AND CHANGE SMP MOD ID TO    NAPT002
***        NAPT002                                              NAPT002
***                                                             NAPT002
***   09/24/85                                                  NAPT002
***        ALWAYS CHANGE $$$$$$ AND ?????? TO SUBMITTER USER-ID NAPT002
***                                                             NAPT002
*** AUTHOR(S).                                                  NAPT002
***                                                             NAPT002
***          THIS WAS WRITTEN USING THE IKJEFF10 SOURCE FROM    NAPT002
***          GENERAL DYNAMICS ON THE C. B. T. TAPE.             NAPT002
***          CLARK MORRIS, NORTH AMERICAN PHILIPS LIGHTING      NAPT002
***          SHARE CODE WEJ, 201-429-3607                       NAPT002
***                                                             NAPT002
*** END --- NORTH AMERICAN PHILIPS LIGHTING TSO MOD PROLOG ---- NAPT002
R0       EQU   0                                                NAPT002
R1       EQU   1                                                NAPT002
R2       EQU   2                                                NAPT002
R3       EQU   3                                                NAPT002
R4       EQU   4                                                NAPT002
R5       EQU   5                                                NAPT002
R6       EQU   6                                                NAPT002
R7       EQU   7                                                NAPT002
R8       EQU   8                                                NAPT002
R9       EQU   9                                                NAPT002
R10      EQU   10                                               NAPT002
R11      EQU   11                                               NAPT002
R12      EQU   12                                               NAPT002
R13      EQU   13                                               NAPT002
R14      EQU   14                                               NAPT002
R15      EQU   15                                               NAPT002
         SPACE 2                                                NAPT002
START    DS    0H                 START IT UP                   NAPT002
         STM   R14,R12,12(R13)    SAVE REGISTERS                NAPT002
         BALR  R10,0              LOAD R10                      NAPT002
         USING *,R10              USE R10 AS BASE REG           NAPT002
         LR    R9,R1              SAVE POINTER TO POINTER       NAPT002
         XC    8(4,R13),8(R13)    TELL OS NO CURRENT SAVE AREA  NAPT002
         SPACE 2                                                NAPT002
         L     R9,0(R9)           SAVE POINTER TO SUBMIT DSECT  NAPT002
         USING IEDSECTD,R9        SET R9 POINTER TO SUBMIT DATA NAPT002
         L     R7,IESUBCTP        LOAD R7 WITH ADDR SUB POINTER NAPT002
         USING IESUBCTD,R7        MAKE R7 POINTER TO 2ND DSECT  NAPT002
         L     R8,IEMSGP          LOAD R8 WITH MESSAGE TO REPLY NAPT002
         LTR   R8,R8              IS THERE A MESSAGE THERE?     NAPT002
         BZ    CHKJOB             KEEP LOOKING                  NAPT002
         TM    IEEXITWD,X'80'     IF REPEAT MESSAGE FLAG SET    NAPT002
         BNO   FREEMSG               UNSET FLAG AND REPEAT      NAPT002
         NI    IEEXITWD,X'7F'        THE MESSAGE                NAPT002
         B     MSGSEND                                          NAPT002
FREEMSG  DS    0H                                               NAPT002
         FREEMAIN R,LV=WORKLTH,A=(8) FREE MESSAGE AREA          NAPT002
         SLR   R8,R8              ZERO OUT MESSAGE POINTER      NAPT002
         ST    R8,IEMSGP                                        NAPT002
         L     R8,IEREPLYP        GET ADDR OF REPLY (IF ANY)    NAPT002
         LTR   R8,R8              IF ZERO GO TO ABORT CYCLE     NAPT002
         BZ    ABORT1                                           NAPT002
         CLI   1(R8),X'04'        IF REPLY SIZE LESS THAN 2     NAPT002
         BL    MORCHK               REPEAT THE MESSAGE          NAPT002
         CLI   1(R8),X'05'        IF REPLY SIZE NOT = 3 OR ANS  NAPT002
         BH    MORCHK               NOT YES AND NOT NO REPEAT   NAPT002
         BE    CHKYES               THE MESSAGE                 NAPT002
         CLC   =C'NO',2(R8)       IF ANSWER IS NO, GO TO ABORT  NAPT002
         BE    ABORT1               CYCLE                       NAPT002
         B     MORCHK                                           NAPT002
CHKYES   DS    0H                                               NAPT002
         CLC   =C'YES',2(R8)      IF ANSWER IS YES SUBMIT THE   NAPT002
         BNE   MORCHK                JOB                        NAPT002
         MVI   IEEXITWD,X'00'     SUPPRESS FURTHER CHECKING     NAPT002
         L     R6,IECARDP         POINT TO JCL CARD             NAPT002
         LTR   R6,R6               IF ZERO ADDRESS GO TO FINISH NAPT002
         BZ    ABORT2                OF THE ABORT CYCLE         NAPT002
         L     R3,IEUSRIDP       GET ADDR OF USER-ID IN R3      NAPT002
         B     CHKCARD                                          NAPT002
ABORT1   DS    0H                                               NAPT002
         L     R6,IECARDP         POINT TO JCL CARD             NAPT002
         LTR   R6,R6             IF CARD POINTER ZERO           NAPT002
         BZ    ABORT2              GO TO ACTUAL ABORT           NAPT002
         TM    IESTMTYP,IESJOB   IF JOB CARD AND NOT            NAPT002
         BZ    ABORT1A              CONTINUATION FORCE DO NOT   NAPT002
         TM    IESTMTYP,IESCONTN    FORCE JCL ERROR             NAPT002
         BZ    ABORT2                                           NAPT002
ABORT1A  DS    0H
         TM    IEEXITWD,X'40'    IF /* PURGE ALREADY SUBMITTED  NAPT002
         BO    ABORT2              GO TO ACTUAL ABORT           NAPT002
         OI    IEEXITWD,X'40'    SET EXIT WORD TO SHOW PURGE    NAPT002
         MVC   1(7,R6),=C'*PURGE '  CARD SUBMITTED, SUBMIT IT   NAPT002
         LA    R15,IERETURN         SET UP TO RETURN TO EXIT TO NAPT002
         B     RETURN                ABORT SUMBIT               NAPT002
CHKJOB   DS    0H                                               NAPT002
         L     R6,IECARDP         POINT TO JCL CARD             NAPT002
         LTR   R6,R6             IF CARD POINTER ZERO           NAPT002
         BZ    ABORT2              GO TO FORCE BAD JCL          NAPT002
         L     R3,IEUSRIDP       GET ADDR OF USER-ID IN R3      NAPT002
         TM    IESTMTYP,IESCONTN  IS THIS A CONTINUATION?       NAPT002
         BO    CHKCARD            ..YES, CHECK FOR MESSAGE CLAS NAPT002
         TM    IESTMTYP,IESJOB    IS THIS A JOB CARD?           NAPT002
         BO    CHKJOB1            ..YES, GO SEE IF VALID JOB NM NAPT002
CHKCARD  DS    0H                                               NAPT002
         LA    R11,62             GET INITIAL LENGTH FOR TRT    NAPT002
         LA    R12,3(,R6)         POINT TO STARTING CHR FOR TRT NAPT002
TRTLOOP  DS    0H                                               NAPT002
         EX    R11,TRTINS         EXECUTE TRT FOR QUESTION MARK NAPT002
         BZ    MORECARD           IF NO QUESTION MK OR $ EXIT   NAPT002
*                                    WITH CHECK FOR MORE CARD   NAPT002
         CLC   0(6,R1),=C'??????' IF SIX QUESTION MARKS OR 6    NAPT002
         BE    CNGUSER              DOLLAR SIGNS GO TO CHNG USR NAPT002
         CLC   0(6,R1),=C'$$$$$$'                               NAPT002
         BE    CNGUSER                                          NAPT002
         CLC   0(8,R1),=C'MSGCLASS' IF NOT MSGCLASS GO TO CHECK NAPT002
         BNE   CHKSYSOT               SYSOUT                    NAPT002
         CLI   9(R1),C'X'           IF MSGCLASS X OR Y GO TO    NAPT002
         BE    CHKVALID               CHECK IF SUBMITTER VALID  NAPT002
         CLI   9(R1),C'Y'                                       NAPT002
         BE    CHKVALID                                         NAPT002
BUMP11   DS    0H                                               NAPT002
         LA    R1,11(,R1)         INCREMENT PAST MSGCLASS       NAPT002
         B     CHKR1END                                         NAPT002
CHKSYSOT DS    0H                                               NAPT002
         CLC   0(7,R1),=C'SYSOUT=' IF NOT SYSOUT GO TO CHECK    NAPT002
         BNE   BUMP1                  NEXT CHAR                 NAPT002
         CLI   7(R1),C'X'          IF SYSOUT CLASS X OR Y GO TO NAPT002
         BE    CHKVALID               CHECK IF SUBMITTER VALID  NAPT002
         CLI   7(R1),C'Y'                                       NAPT002
         BE    CHKVALID                                         NAPT002
         CLI   8(R1),C'X'                                       NAPT002
         BE    CHKVALID                                         NAPT002
         CLI   8(R1),C'Y'                                       NAPT002
         BE    CHKVALID                                         NAPT002
BUMP8    DS    0H                                               NAPT002
         LA    R1,08(,R1)         INCREMENT PAST SYSOUT         NAPT002
         B     CHKR1END                                         NAPT002
CNGUSER  DS    0H                                               NAPT002
         MVC   0(6,R1),0(R3)      MOVE IN USER-ID               NAPT002
         LA    R1,6(,R1)          BUMP R1                       NAPT002
         B     CHKR1END                                         NAPT002
BUMP1    DS    0H                                               NAPT002
         LA    R1,1(,R1)                                        NAPT002
CHKR1END DS    0H                                               NAPT002
         LA    R11,65(,R6)        GET NEW SIZE FOR TRT AND IF   NAPT002
         SR    R11,R1                NEGATIVE CARD IS DONE      NAPT002
         BM    MORECARD                                         NAPT002
         LR    R12,R1                                           NAPT002
         B     TRTLOOP                                          NAPT002
TRTINS   TRT   0(1,R12),SPCCHTBL                                NAPT002
MORECARD DS    0H                                               NAPT002
         MVI   0(R7),IETJOB+IETEXEC+IETDD+IETJES+IETCOMNT       NAPT002
*               RETURN JOB, EXEC, DD, JES AND COMMENT CARDS     NAPT002
         LA    R15,IECONTIN       RETURN CODE OF ZERO           NAPT002
         B     RETURN             GET OUT OF HERE               NAPT002
CHKJOB1  DS    0H                                               NAPT002
         LA    R12,2(,R6)         POINT R12 TO JOBNAME          NAPT002
         LR    R1,R12             GUARANTEE R1 GOOD             NAPT002
         TRT   0(9,R12),SPACETBL  CHECK FOR LOC OF SPACE        NAPT002
         BZ    INVALID               IF NONE BY COL 11, BAD JOB NAPT002
*                                       NAME                    NAPT002
         LR    R5,R1              GET SIZE OF JOB NAME          NAPT002
         SR    R5,R12                                           NAPT002
         CLC   0(6,R12),=C'??????' IF JOB NAME = 6 QUESTION     NAPT002
         BE    SETUSER                MARKS OR $$$$$$ MOVE IN   NAPT002
         CLC   0(6,R12),=C'$$$$$$'    USER-ID                   NAPT002
         BNE   CHKJOB1A                                         NAPT002
SETUSER  DS    0H                                               NAPT002
         MVC   0(6,R12),0(R3)     MOVE IN USER-ID               NAPT002
CHKJOB1A DS    0H                                               NAPT002
         MVI   IEEXITWD,X'00'     SET EXITWORD TO ALLOW JOB     NAPT002
         CH    R5,=H'8'           IF JOB NAME IS 8 CHRS OR LESS NAPT002
         BE    CHKJOB2               THAN 6 CHRS CHECK IF OPER, NAPT002
         CH    R5,=H'6'              SYS ENG, OR TECH SERVICES  NAPT002
         BL    CHKJOB2               IS SUBMITTER               NAPT002
         CLC   0(6,R12),0(R3)     IF JOB-NAME IS NOT THE SAME   NAPT002
         BNE   CHKJOB2             AS THE USER-ID (OR THE 1ST   NAPT002
*                                  6 CHARACTERS CHANGED TO THE  NAPT002
*                                  USER ID) GO TO CHECK         NAPT002
*                                  SUBMITTER                    NAPT002
SETCHECK DS    0H                                               NAPT002
         LA    R12,8(R12)         POINT R12 TO COL 11           NAPT002
         LA    R11,55             SET SIZE FOR TRT              NAPT002
         B     TRTLOOP            GO TO THE TRANSLATE AND TEST  NAPT002
*                                   LOOP                        NAPT002
*                                                               NAPT002
CHKJOB2  DS    0H                                               NAPT002
         MVI   IEEXITWD,X'01'     SET EXITWORD TO CHK PRIV JOB  NAPT002
         CLC   0(2,R12),=C'FC'    IF TEST JOB AND SIZE IS 6 OR  NAPT002
         BNE   CHKJOB3               7 CHARACTERS SET EXITWORD  NAPT002
         CH    R5,=H'7'              BACK TO NO CHECKING.       NAPT002
         BH    CHKJOB3               IF THE SEVENTH CHARACTER   NAPT002
         CH    R5,=H'6'              IS BLANK, MOVE IN A $ TO   NAPT002
         BL    CHKJOB3               THE SEVENTH CHARACTER      NAPT002
         MVI   IEEXITWD,X'00'                                   NAPT002
         BH    CHKJOB3                                          NAPT002
         MVI   6(R12),C'$'                                      NAPT002
CHKJOB3  DS    0H                                               NAPT002
         CLC   0(6,R3),=C'FCBSH1' IF OPERATIONS 1ST SHIFT,      NAPT002
         BE    CHKLSTHD              OR ANY OPERATOR CHECK FOR  NAPT002
         CLC   0(5,R3),=C'FCBOP'     LISTHDR JOB AND            NAPT002
         BNE   CHKJOB4               AUTOMATICALLY ALLOW IT TO  NAPT002
CHKLSTHD DS    0H                    BE SUBMITTED               NAPT002
         CLC   =C'LISTHDR ',0(R12)                              NAPT002
         BNE   SETCHECK                                         NAPT002
         MVI   IEEXITWD,X'00'                                   NAPT002
         B     SETCHECK                                         NAPT002
CHKJOB4  DS    0H            IF SYSTEMS ENGINEERING OR TECHINAL NAPT002
         CLC   0(3,R3),=C'FCH'       SERVICES SET UP TO ALLOW   NAPT002
         BE    SETCHECK              SUBMISSION OF ANY JOB      NAPT002
         CLC   0(3,R3),=C'FCI'       EVEN IF IT HAS A MESSAGE   NAPT002
         BE    SETCHECK              CLASS OF X OR Y            NAPT002
         MVI   IEEXITWD,X'02'     SET EXITWORD TO CHK NON-PRIV  NAPT002
         B     SETCHECK           AND GO TO CHECK THE JOB CARD  NAPT002
CHKVALID DS    0H                                               NAPT002
         CLI   IEEXITWD,X'00'     IF EXIT WORD NOT SET FOR      NAPT002
         BE    BUMP8                 CHECKING GO CHK AFTER      NAPT002
*                                    MSGCLASS                   NAPT002
INVALID  DS    0H                                               NAPT002
         CLI   IEEXITWD,X'01'     IF PRIVILEGED SUBMITTER CHK   NAPT002
         BE    MORCHK                                           NAPT002
         GETMAIN R,LV=WORKLTH     GET SOME STORAGE              NAPT002
         LA    R8,0(,R1)          SET R8 TO POINT AT STORAGE    NAPT002
         USING WORKAREA,R8        WORKAREA IS DSECT             NAPT002
         MVC   WORKLGTH,=AL2(WORKLTH) STORE MESSAGE AREA LENGTH NAPT002
*                                    IN WORKAREA                NAPT002
         MVC   WORKMSG,MSGCONST   MOVE IN INVALID MSG           NAPT002
         ST    R8,IEMSGP          STORE POINTER TO MESSAGE AWAY NAPT002
         OI    IEEXITWD,X'80'     SET EXIT WORD TO SIGNAL       NAPT002
*                                    REPEAT OF MESSAGE          NAPT002
MSGSEND  DS    0H                                               NAPT002
         LA    R15,IEMSG          SET RETURN CODE               NAPT002
         B     RETURN             RETURN                        NAPT002
MORCHK   DS    0H                                               NAPT002
         GETMAIN R,LV=WORKLTH     GET SOME STORAGE              NAPT002
         LA    R8,0(,R1)          SET R8 TO POINT AT STORAGE    NAPT002
         USING WORKAREA,R8        WORKAREA IS DSECT             NAPT002
         MVC   WORKLGTH,=AL2(WORKLTH) STORE MESSAGE AREA LENGTH NAPT002
*                                    IN WORKAREA                NAPT002
         MVC   WORKMSG,MSGCONSR   MOVE IN REQUEST FOR REPLY     NAPT002
         DROP  R8                                               NAPT002
         ST    R8,IEMSGP          STORE POINTER TO MESSAGE AWAY NAPT002
         LA    R15,IEPROMPT       SET RETURN CODE TO REPLY      NAPT002
         B     RETURN             RETURN                        NAPT002
ABORT2   DS    0H                                               NAPT002
         SLR   R15,R15            ZERO OUT EXITWORD             NAPT002
         ST    R15,IEEXITWD                                     NAPT002
         LA    R15,IEABORT                                      NAPT002
         MVI   0(R7),IETJOB       RETURN ONLY JOB CARDS         NAPT002
         B     RETURN                                           NAPT002
RETURN   DS    0H                                               NAPT002
         ST    R15,16(,R13)       STORE RETURN CODE             NAPT002
         LM    R14,R12,12(R13)    RESTORE REGISTERS             NAPT002
         BR    R14                RETURN                        NAPT002
         EJECT                                                  NAPT002
         DS    0F                                               NAPT002
SPACETBL DS    0CL256             TABLE FOR TRT FOR SPACES      NAPT002
         DC    64X'00'                                          NAPT002
         DC    X'40'              SPACE POSITION                NAPT002
         DC    191X'00'                                         NAPT002
SPCCHTBL DS    0CL256             TABLE FOR TRT FOR QUESTION MK NAPT002
         DC    91X'00'                                          NAPT002
         DC    X'5B'              $ POSITION                    NAPT002
         DC    19X'00'                                          NAPT002
         DC    X'6F'              QUESTION MARK (?) POSITION    NAPT002
         DC    100X'00'                                         NAPT002
         DC    X'D4'              M POSITION                    NAPT002
         DC    13X'00'                                          NAPT002
         DC    X'E2'              S POSITION                    NAPT002
         DC    30X'00'                                          NAPT002
MSGCONST DC    C'*** JOB NOT SUBMITTED, IF MSGCLASS = X OR Y, JOBNAME M*
               UST BE 6 OR 7  BYTES WITH THE FIRST 6 BEING YOUR USER-ID*
                OR $$$$$$ '                                     NAPT002
MSGCONSR DC    C'+++ DO YOU WISH TO SUBMIT A JOB WITH MESSAGE CLASS OR *
               SYSOUT OF X OR Y THAT IS NOT A USER-ID PLUS 1 CH, REPLY *
               YES OR NO  '                                     NAPT002
         EJECT                                                  NAPT002
         LTORG                                                  NAPT002
         EJECT                                                  NAPT002
WORKAREA DSECT                                                  NAPT002
WORKLGTH DS    CL2                                              NAPT002
WORKMSG  DC    C'*** JOB NOT SUBMITTED, IF MSGCLASS = X OR Y, JOBNAME M*
               UST BE 6 OR 7  BYTES WITH THE FIRST 6 BEING YOUR USER-ID*
                OR $$$$$$ '                                     NAPT002
WORKLTH  EQU   *-WORKLGTH                                       NAPT002
         IKJEFFIE IETYPE=SUBMIT   SUBMIT DSECT                  NAPT002
         CSECT ,                                                NAPT002
         DC    C'NAPT002 10/19/85'                              NAPT002
         END   ,                                                NAPT002
