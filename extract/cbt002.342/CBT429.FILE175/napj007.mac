++ USERMOD (NAPJ007) .
++ VER (Z038) FMID(HJE2221).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT13),DISP=SHR
//SYSIN  DD  SYS1.JES2SRC(J2XIT13),DISP=SHR
//STEP2    EXEC PGM=IEWL,PARM='RENT'
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT13),DISP=SHR
//SYSLMOD  DD  DSN=SYS1.JES2LIB,DISP=SHR
//SYSLIN  DD  *
  INCLUDE SYSPUNCH(J2XIT13)
  NAME J2XIT13(R)
++SRC (J2XIT13) DISTLIB(JES2SRC).
*          DATA SET J2XIT13    AT LEVEL 012 AS OF 07/09/89
EXIT     TITLE 'PHILIPS LIGHTING NJE/TSO/E SYSOUT VALIDATION FOR  , CON*
               EMC2/TAO/PROFS INTERFACE'
         SPACE 5
         COPY  $HASPGBL
         PRINT GEN
J2XIT13  $MODULE TITLE='PHILIPS LIGHTING (WEJ) HASP EXIT ROUTINES',    X
               RPL,                GENERATE HASP RPL    DSECT          X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $HCT,               GENERATE HASP HCT DSECT             X
               $JCT,               GENERATE HASP JCT DSECT             X
               $PADDR,             GENERATE HASP JCT DSECT             X
               $PDDB,              GENERATE HASP PDDB DSECT            X
               $JQE,               GENERATE HASP JQE DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PCE,               GENERATE HASP PCE DSECT             X
               $NHD,               GENERATE HASP NDJ AND NJH DSECTS    X
               $HASPEQU
         TITLE 'JES2 EXIT 6 INTERNAL TEXT EXIT'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                      JES2 FOR SP 2.2.0 (XA)                         *
*                                                                     *
J2XIT13  AMODE 31
J2XIT13  RMODE 24
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             *
*                                                                     *
*        R0 - N/A                                                     *
*        R1       - ADDRESS OF 2-WORD PARAMETER LIST                  *
*           WORD 1 (+0)  ADDRESS OF THE NETWORK JOB HEADER            *
*           WORD 2 (+4)  ADDRESS OF THE NETWORK DATA SET HEADER       *
*           WORD 3 (+8)  ADDRESS OF THE PDDB                          *
*           WORD 4 (+12) ADDRESS OF THE SYSID OF LOGGED ON USER       *
*           WORD 5 (+16) ADDRESS OF THE 70 BYTE DELETE MESSAGE        *
*        R11      - ADDRESS OF THE HCT                                *
*        R13      - ADDRESS OF AN OS-STYLE SAVE AREA                  *
*        R14      - RETURN ADDRESS                                    *
*        R15      - ENTRY ADDRESS                                     *
*                                                                     *
***********************************************************************
         SPACE 4
***********************************************************************
*                                                                     *
*        REGISTER USAGE WITH THIS EXIT:                               *
*                                                                     *
*        R0,R1    - PARM/WORK REGISTERS                               *
*        R3       - ADDR OF THE NJH                                   *
*        R4       - ADDR OF THE NDH                                   *
*        R5       - ADDR OF THE PDDB                                  *
*        R6       - OPEN                                              *
*        R7       - OPEN                                              *
*        R8       - OPEN                                           WEJ*
*        R9       - OPEN                                           WEJ*
*        R10      - OPEN                                           WEJ*
*        R11      - ADDRESS OF THE HCT                                *
*        R12      - BASE REGISTER #1                                  *
*        R13      - WORKAREA POINTER                                  *
*        R14      - RETURN ADDRESS                                    *
*        R15      - RETURN CONDITION CODE                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        ACTUAL JES2 EXIT ENTRY POINT                                 *
*                                                                     *
***********************************************************************
J2XIT13E $ENTRY  BASE=R12
         $SAVE
         LR    R12,R15
         LM    R3,R5,0(R1)             GET POINTERS TO NJH,NDH,PDDB
         USING NJH,R3
         USING NDH,R4
         USING PDB,R5
         CLI   PDBCLASS,C'A'              IS SYSOUT CLASS=A
         BNE   EXIT                       NO, GET OUT
         TM    NDHGFLG2,NDHGF2PU          IS THIS PUNCH
         BZ    EXIT                       NO, GET OUT
         CLC   PDBDRMT,=H'0'              IS DEST REMOTE ZERO
         BNE   EXIT                       NO, GET OUT
         CLC   NDHGXWTR,=CL8' '           IS EXTERNAL WRITER FLD BLANK
         BE    EXIT                       YES, GET OUT
         CLC   NDHGXWTR,=XL8'00'          IS EXTERNAL WTR FLD LOW-VALUE
         BE    EXIT                       YES, GET OUT
         CLC   NDHGXWTR(2),=C'FC'         ARE 1ST 2 CHARS OF EXTERNAL
*                                           WRITER FIELD FC (TSO USER)
         BNE   CHGCLASS                   NO , USE IT
         CLC   NDHGXWTR+6(2),=C'  '       ARE LAST 2 CHARS OF EXTERNAL
*                                           WRITER FLD BLANK (CONFIRM
*                                           TSO USER ID)
         BE    EXIT                       YES, EXIT
CHGCLASS DS    0H
         MVI   PDBCLASS,C'N'              CHANGE CLASS TO N
EXIT     DS    0H
         $RETURN RC=0
         $MODEND
         END
