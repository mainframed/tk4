++ USERMOD (NAPJ006) .
++ VER (Z038) FMID(HJE2221).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT09),DISP=SHR
//SYSIN  DD  SYS1.JES2SRC(J2XIT09),DISP=SHR
//STEP2    EXEC PGM=IEWL,PARM='RENT'
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT09),DISP=SHR
//SYSLMOD  DD  DSN=SYS1.JES2LPA,DISP=SHR
//SYSLIN  DD  *
  INCLUDE SYSPUNCH(J2XIT09)
  NAME J2XIT09(R)
++SRC (J2XIT09) DISTLIB(JES2SRC).
*          DATA SET J2XIT09    AT LEVEL 009 AS OF 12/10/89
J2XIT09  TITLE 'WFB - EXIT 009 - JOB OUTPUT OVERFLOW'
***********************************************************************
*                                                                     *
* MODULE NAME: J2XIT09                                                *
*                                                                     *
* ENVIRONMENT: USER ADDRESS SPACE (MVS CONVENTIONS MUST BE USED)      *
*                                                                     *
* DESCRIPTIVE NAME: JES2 EXIT 009 - LINE EXCESSION OVERFLOW HANDLING  *
*           (ORIGINALLY USED FOR TSO/E TRANSMIT LINE LIMITATION)      *
* AUTHOR: TOM LITNEY                                                  *
*         WELLS FARGO BANK, N.A. (WFB)                                *
*         525 MARKET STREET, MAC 0103-141                             *
*         SAN FRANCISCO, CALIFORNIA  94163                            *
*         (415) 396-7883                                              *
* CHANGED BY : CLARK MORRIS                                           *
*         PHILIPS LIGHTING, DEPT 5069                                 *
*         PHILIPS SQUARE, CN6800                                      *
*         SOMERSET, NEW JERSEY 08875-6800                             *
*         (201) 563-3575                                              *
*                                                                     *
* FMID: HJE2226 (MVS/SP-JES2 2.2.0)                               TL1 *
*                                                                     *
* DATE WRITTEN: JANUARY 1985                                          *
*                                                                     *
* CHANGE HISTORY:                                                     *
*  MM/YY  WHO          WHAT                                       ID# *
*  05/86  TOM LITNEY   CHANGES FOR JES2 SP 2.1.5                  TL1 *
*  12/89  CLARK MORRIS CHANGED FUNCTION AND INSTALLED ON JES2 XA  CFM *
*                      2.2.0                                          *
*                                                                     *
* PURPOSE: THE LINES EXCESSION ROUTINE IS CHANGED SO THAT PRODUCTION  *
*          JOBS ARE CANCELLED IF THEY EXCEED 2.5 MILLION LINES OR THE *
*          LINE LIMIT IN THE JOBPARM CARD (PRODUCTION JOBS ARE THOSE  *
*          SUBMITTED BY UCC7 OR A SPECIAL SUBMISSION STARTED TASK     *
*          CALLED COMMAND).  ALL OTHER JOBS AND TSO SIGN-ONS WILL BE  *
*          CANCELLED IF THE /*JOBPARM LINE LIMIT IS EXCEEDED OR, IF   *
*          NO JOBPARM LINE LIMIT IS PRESENT, THE INITIALIZATION DECK  *
*          LINE LIMIT IS EXCEEDED.  STARTED TASKS WILL BE EXEMPT FROM *
*          THIS CHECKING AND THE HASP375 MESSAGE WILL BE SUPPRESSED   *
*          FOR THE STARTED TASK CADSDISP (DISPATCH, A REPORT          *
*          MANAGEMENT SYSTEM) UNTIL 2.5 MILLION LINES ARE EXCEEDED.   *
*          EXIT 10 HAS BEEN INSTALLED TO CHANGE THE DESCRIPTOR CODE   *
*          OF THE HASP375 MESSAGE TO EVENTUAL ACTION SO THAT IT IS    *
*          HIGHLIGHTED AND REMAINS ON THE SCREEN                      *
*                                                                     *
* ENTRY POINT: J2XIT09                                                *
*                                                                     *
* REGISTERS ON INPUT:                                                 *
*     R0: N/A                                                         *
*     R1: ADDRESS OF 7-WORD PARAMETER LIST WITH THE FOLLOWING LAYOUT: *
*         WORD 1 (+0)  JCTLINES OR JCTPUNCH VALUE                     *
*         WORD 2 (+4)  JCTPAGES VALUE                                 *
*         WORD 3 (+8)  JCTMXOUT VALUE                                 *
*         WORD 4 (+12) USERS INCREMENT FOR RECORDS                    *
*         WORD 5 (+16) USERS INCREMENT FOR PAGES                      *
*         WORD 6 (+20) USERS INCREMENT FOR BYTES                      *
*         WORD 7 (+24) OUTPUT OVERFLOW FLAG                           *
*              BIT 0   =0  CARDS HAVE NOT EXCEEDED THE LIMIT          *
*                      =1  CARDS HAVE EXCEEDED THE ESTIMATE           *
*              BIT 1   =0  LINES HAVE NOT EXCEEDED THE LIMIT          *
*                      =1  LINES HAVE EXCEEDED THE ESTIMATE           *
*              BIT 2   =0  PAGES HAVE NOT EXCEEDED THE LIMIT          *
*                      =1  PAGES HAVE EXCEEDED THE ESTIMATE           *
*              BIT 3   =0  BYTES HAVE NOT EXCEEDED THE LIMIT          *
*                      =1  BYTES HAVE EXCEEDED THE ESTIMATE           *
*              BIT 4-31    NOT APPLICABLE                             *
*     R2-R6: N/A                                                      *
*     R7 : ADDRESS OF THE JCT                                         *
*     R8-R10: N/A                                                     *
*     R11: ADDRESS OF THE SSVT                                        *
*     R12: N/A                                                        *
*     R13: OS-STYLE 18-WORD SAVE AREA                                 *
*     R14: RETURN ADDRESS                                             *
*     R15: ENTRY ADDRESS                                              *
*                                                                     *
* REGISTERS ON RETURN:                                                *
*     R0 : IF THE RETURN CODE IN R15 IS 8 THEN:                       *
*              BIT 0   =0 USE INSTALLATION CURRENT EXCESSIVE          *
*                         OUTPUT OPTION VALUE                         *
*                      =1 USE VALUE SPECIFIED IN BITS 24-31 AS        *
*                         EXCESSIVE OUTPUT VALUE                      *
*              BIT 1   =0 USE OUTPUT OVERFLOW INCREMENT AS SPECIFIED  *
*                         ON ESTLNCT, ESTPUN, ESTPAGE, OR ESTBYTE.    *
*                      =1 USE THE OUTPUT OVERFLOW INCREMENT AS        *
*                         SUPPLIED BY THE EXIT ROUTINE IN THE         *
*                         PARAMETER LIST                              *
*              BIT 2   =0 SEND DEFAULT ERROR MESSAGE ($HASP375) TO    *
*                         THE OPERATOR                                *
*                      =1 SUPPRESS DEFAULT MESSAGE                    *
*              BIT 3-23  N/A                                          *
*              BIT 24-31 =0 CONTINUE PROCESSING                       *
*                        =1 ABEND (722) WITHOUT A DUMP                *
*                        =2 ABEND (722) WITH A DUMP                   *
*     R1-R14: N/A                                                     *
*     R15 : A RETURN CODE                                             *
*           =0 PERFORM ANY ADDITIONAL EXITS ASSOCIATED WITH THIS EXIT.*
*              PERFORM STANDARD JOB OUTPUT OVERFLOW PROCESSING BASED  *
*              ON THE INT= AND OPT= KEYWORDS ON THE ESTLNCT, ESTPUN,  *
*              ESTPAGE, AND ESTBYTE INITIALIZATIN PARAMETERS          *
*           =4 DO NOT PERFORM ANY ADDITIONAL EXITS.                   *
*              PERFORM STANDARD JOB OUTPUT OVERFLOW PROCESSING BASED  *
*              ON THE INT= AND OPT= KEYWORDS ON THE ESTLNCT, ESTPUN,  *
*              ESTPAGE, AND ESTBYTE INITIALIZATIN PARAMETERS          *
*           =8 TAKE ACTION BASED ON RETURN PROCESSING FLAGS IN R0.    *
*              SEE R0 ABOVE.                                          *
*                                                                     *
***********************************************************************
         EJECT
         COPY $HASPGBL                 COPY HASP GLOBALS
         EJECT
J2XIT09  AMODE 31
J2XIT09  RMODE 24
J2XIT09  $MODULE                                                       C
               RPL,                    REQ'D BY $BUFFER                C
               $HASPEQU,               REQ'D FOR REG CONVENTIONS       C
               $MIT,                   REQ'D FOR EXIT MASK             C
               $BUFFER,                REQ'D FOR JCT                   C
               $JCT                    REQ'D TO GET JOBNAME, ETC.
         EJECT
J2XIT09E $ENTRY BASE=R12               EXIT ROUTINE ENTRY POINT
         SAVE  (14,12)                 SAVE CALLERS REGISTERS
         LR    R12,R15                 LOAD LOCAL BASE REGISTER
         SLR   R15,R15                 SET DEFAULT RETURN CODE
         EJECT
***********************************************************************
*  SET UP ADDRESSIBILITY AND TEST FOR TSO USER AND PUNCH OVERFLOW. IF *
*  NO RETURN.  IF YES, IT MUST BE TSO/E TRANSMIT UP PUNCH LIMIT, SET  *
*  FLAGS, AND RETURN.                                                 *
***********************************************************************
         USING JCT,R7                  ESTABLISH ADDRESSIBILITY     TL1
         TM    24(R1),B'01000000'      LINES EXCEED THE LIMIT??
         BZ    RETURN                  NO, RETURN
         CLI   JCTJOBID,C'S'           STARTED TASK???
         BNE   CHKJOB                  NO, CHECK FOR PRODUCTION JOB
         CLC   JCTJNAME,=C'CADSDISP'   IS THIS DISPATCH
         BNE   RETURN                  NO, RETURN
         CLC   JCTLINES,=F'2000000'    IS JCT LINES > 2 MILLION LNS.
         BH    RETURN                  YES, RETURN
         L     R0,=X'20000000'         SET FLAG BYTES IN R0
         B     RETURN8                 GO TO SET RETURN CODE TO 8
CHKJOB   DS    0H
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*  AT LIGHTING, PRODUCTION JOBS ARE SUBMITTED BY THE JOB SCHEDULER    *
*  UCC7 (NOW CA7 FROM COMPUTER ASSOCIATES).  EXIT 2 IS USED TO PLACE  *
*  THE ID OF THE SUBMITTER IN JCTINDEV WITH THE FORMAT THAT TSO       *
*  SUBMITTERS ARE THE TSO ID AND AN * IN POSITION 8, STARTED TASK     *
*  SUBMITTERS ARE THE STARTED TASK NAME WITH A PLUS SIGN IN POSITION  *
*  8, AND JOB SUBMITTERS ARE THE JOBNAME.                             *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
         CLC   JCTINDEV,=C'UCC7   +'   PRODUCTION JOB???
*        BE    CKLINES                 NO, GO TO KILL JOB
*        CLC   JCTINDEV,=C'COMMAND+'   PRODUCTION JOB???
         BNE   KILLJOB                 NO, GO TO KILL JOB
CKLINES  DS    0H
         CLC   JCTLINES,=F'2500000'    IS JCT LINES < 2.5 MILLION LNS.
         BL    RETURN                  YES, RETURN
KILLJOB  DS    0H
         L     R0,=X'80000002'         SET FLAG BYTES IN R0 FOR 722
*                                      WITH A DUMP
RETURN8  DS    0H                      RETURN CALLERS REG 14
         LA    R15,8                   SET RETURN CODE TO 8
         EJECT
RETURN   DS    0H                      RETURN CALLERS REG 14
         RETURN (14,12),RC=(15)        RETURN CALLERS REGISTERS
         DROP R12                      DROP ADDRESSABILITY
         EJECT
         LTORG
        $MODEND
         END
