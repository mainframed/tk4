++ USERMOD (NAPJ001) .
++ VER (Z038) FMID(HJE2221) /*
 MODULE J2XIT01     JES2 JOB HEADER EXIT                  */.
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT01),DISP=SHR
//SYSIN  DD  SYS1.JES2SRC(J2XIT01),DISP=SHR
//STEP2    EXEC PGM=IEWL,PARM='RENT'
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT01),DISP=SHR
//SYSLMOD  DD  DSN=SYS1.JES2LIB,DISP=SHR
//SYSLIN  DD  *
  INCLUDE SYSPUNCH(J2XIT01)
  NAME J2XIT01(R)
++SRC (J2XIT01) DISTLIB(JES2SRC).
         TITLE 'JES2 EXIT 1 PRINT/PUNCH SEPARATOR EXIT'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS XA 2.2.0                                   *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        CLARK F. MORRIS, DEPT.5069                   *
*                        PHILIPS LIGHTING COMPANY                     *
*                        PHILIPS SQUARE, CN6800                       *
*                        SOMERSET, NJ 08875-6800                      *
*                        (201) 563-3575  SHARE CODE WEJ               *
*        USING EXIT1 ON THE JES2 MODS TAPE FROM                       *
*                        JOHN V. HOOPER                               *
*                        FIRST COMPUTER SERVICES                      *
*                        420 BRUSHY MOUNTAIN ROAD                     *
*                        WILKESBORO, N.C. 28697                       *
*                        (919) 651-5414                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                          KOMSTAR                                    *
*                                                                     *
*        SUPPORT EXISTS IN THIS EXIT FOR ONLINE KOMSTAR (KODAK).      *
*        THE DEVICE NAMES OF PRINTER3 IS RESERVED FOR THIS            *
*        DEVICE.                                                      *
*                                                                     *
*        SYSOUT CLASSES               ACTION                          *
*                                                                     *
*        F,4         IF THE FORM BEGINS WITH 'F' AND THE DEVICE IS    *
*                    PRT3 - THE KOMSTAR, THE STANDARD HEADER AND      *
*                    TRAILER PAGES ARE SUPPRESSED AND NO SETUP IS     *
*                    DON FOR THE KOMSTAR.  IF THE FORM BEGINS WITH  . *
*                    OTHER THAN F NATIVE COMMANDS ARE ISSUED TO THE   *
*                    KOMSTAR TO GUARENTEE GOOD HEADERS.               *
*                                                                     *
*                                                                     *
***********************************************************************
*  CHANGES                                                            *
*   LVL28 SUPPRESS HEADERS FOR ALL CADSDISP STARTED TASK PRINTOUTS    *
*        DESTINED TO LOCAL PRINTERS                                   *
*   LVLS29-30 FIX READER START TIME PROBLEM BY USING DIFFERENT FIELD  *
*        AND CHECKING DATE FOR NUMERIC, ALSO ADDED ABILITY TO HANDLE  *
*        INVALID DATE                                                 *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*              M A C R O S   U S E D   I N   J E S E X I T 1          *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CAT         SYS1.HASPSRC                               *
*        03)  $DCT         SYS1.HASPSRC                               *
*        04)  $ENTRY       SYS1.HASPSRC                               *
*        05)  $FREEBUF     SYS1.HASPSRC                               *
*        06)  $GETBUF      SYS1.HASPSRC                               *
*        07)  $HASPEQU     SYS1.HASPSRC                               *
*        08)  $HASPGBL     SYS1.HASPSRC                               *
*        09)  $HCT         SYS1.HASPSRC                               *
*        10)  $JCT         SYS1.HASPSRC                               *
*        11)  $JOE         SYS1.HASPSRC                               *
*        12)  $JQE         SYS1.HASPSRC                               *
*        13)  $MIT         SYS1.HASPSRC                               *
*        14)  $MODEND      SYS1.HASPSRC                               *
*        15)  $MODULE      SYS1.HASPSRC                               *
*        16)  $PBLOCK      SYS1.HASPSRC                               *
*        17)  $PCE         SYS1.HASPSRC                               *
*        18)  $PDDB        SYS1.HASPSRC                               *
*        19)  $PPPWORK     SYS1.HASPSRC                               *
*        20)  $PRPUT       SYS1.HASPSRC                               *
*        21)  $RDT         SYS1.HASPSRC                               *
*        22)  $RETURN      SYS1.HASPSRC                               *
*        23)  $SAVE        SYS1.HASPSRC                               *
*        24)  $SEPPDIR     SYS1.HASPSRC                               *
*        25)  $SCAT        SYS1.HASPSRC                               *
*        26)  $SVT         SYS1.HASPSRC                               *
*        27)  $SYSPARM     SYS1.HASPSRC                               *
*        28)  $XECB        SYS1.HASPSRC                               *
*        29)  CVT          SYS1.AMODGEN                               *
*        30)  IFGRPL       SYS1.MACLIB                                *
*        31)  TIME         SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       4K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J2XIT01  $MODULE SYSP=(NOGEN,NOGEN,DATA,NOGEN,NOGEN),                  X
               ENVIRON=JES2,                                           X
               TITLE='JES2 EXIT1 PRINT/PUNCH SEPARATOR EXIT',          X
               CVT,                GENERATE MVS CVT DSECT              X
               RPL,                GENERATE MVS RPL DSECT              X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $MODMAP,            GENERATE HASP MODULE MAP            X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $HFAME,             GENERATE HASP HFAME DSECT           X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JOE,               GENERATE HASP JOE DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $RDT,               GENERATE HASP RDT DSECT             X
               $SVT,               GENERATE HASP SSVT DSECT            X
               $PCE,               GENERATE HASP PCE DSECT             X
               $CAT,               GENERATE HASP CAT DSECT             X
               $HCT,               GENERATE HASP HCT DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PDDB,              GENERATE HASP PDDB DSECT            X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $SCAT,              GENERATE HASP SCAT DSECT            X
               $XECB,              GENERATE HASP XECB DSECT            X
               $PPPWORK            GENERATE HASP PPPWORK DSECT
         TITLE 'JES2 EXIT 1 PRINT/PUNCH SEPARATOR EXIT'
***********************************************************************
*                                                                     *
*        WORK AREA USED BY THIS JES2 EXIT                             *
*                                                                     *
***********************************************************************
BUFFER1  DSECT
         DS    CL104               *****  RESERVED BY JES2 1.3.6 *****
HDRADDR  DS    A
WXEDHOUR DS    0CL3         HOURS FOR UNPACK
         DS    CL1
WX26TMDT DS    0CL16        FORMATTED TIME AND DATE             NAPJ047
WXEDTIME DS    0CL8         TIME FORMATTED                      NAPJ047
         DS    CL2          HOURS                               NAPJ047
WXEDMIN  DS    0CL3         MINUTES FOR UNPACK                  NAPJ047
WXEDPER1 DS    CL1                                              NAPJ047
         DS    CL2          MINUTES                             NAPJ047
WXEDSEC  DS    0CL3         SECONDS FOR UNPACK                  NAPJ047
WXEDPER2 DS    CL1          RDR PERIOD 2                        NAPJ047
         DS    CL2          SECONDS                             NAPJ047
WXEDBOTH DS    0CL12        DATE FORMATTED IN GREGORIAN/JULIAN  NAPJ047
WXEDDATE DS    0CL8         DATE FORMATTED IN GREGORIAN         NAPJ047
WXEDMON  DS    CL2          EDITED MONTH                        NAPJ047
WXEDDAY  DS    0CL3         EDITED DAY                          NAPJ047
WXEDSL1  DS    CL1          SLASH                               NAPJ047
         DS    CL2          EDITED DAY                          NAPJ047
WXEDSL2  DS    CL1          SLASH                               NAPJ047
WXEDJUL  DS    0CL6         EDITED JULIAN DATE
WXEDYEAR DS    CL2          EDITED YEAR                         NAPJ047
WXEDJULP DS    CL1          THE PERIOD
WXEDJULD DS    CL3          THE JULIAN DAY
WXDBLWRD DS    0D
WXWORK1  DS    0F           WORK WORD                           NAPJ047
         DS    CL1                                              NAPJ047
WXJULDT  DS    0PL3         TEMP WORK DATE IN JULIAN            NAPJ047
WXJULYR  DS    CL1                                              NAPJ047
WXJULDAY DS    0PL2         TEMP JULIAN DAYS                    NAPJ047
         DS    CL1                                              NAPJ047
WXJULSGN DS    CL1          SIGN POSITION                       NAPJ047
         DS    0F                                               NAPJ047
         DS    0PL4
         DS    CL2
WX2DIGIT DS    0PL2
         DS    CL1
WXDBLSGN DS    XL1
SAVETBL  DS    0CL52               AREA FOR COPY OF DATE TABLE
MOENT    DS    0CL4                DATE TABLE ENTRY FOR FEBRUARY
TBLDAY   DS    PL2
TBLMON   DS    CL2
FEBDAYS  DS    PL2
         DS    CL46
BYTEFND  DS    CL1
*
*
*
*
HDRDSECT DSECT                     HASP HEADER DSECT
         DS    CL104               *****  RESERVED BY JES2 1.3.6 *****
WXKOMLN  DS    0CL121                                           NAPJ047
         DS    CL4     000-003  X'014163FF'                     NAPJ047
         DS    0CL20   004-023                                  NAPJ047
         DS    CL2     004-005                                  NAPJ047
WXKOMJBN DS    CL8     006-013  KOM JOB NAME                    NAPJ047
WXKOMRST DS    0CL8    014-021  KOM TIME                        NAPJ047
WXKOMRSD DS    0CL8    014-021  KOM DATE                        NAPJ047
WXKOMACA DS    CL6     014-019  KOM JOB-ACCT 1ST 6 CHRS         NAPJ047
         DS    CL2     020-021                                  NAPJ047
         DS    CL2     022-023                                  NAPJ047
         DS    0CL20   024-043                                  NAPJ047
         DS    CL10    024-033                                  NAPJ047
WXKOMCLN DS    0CL8    034-041  KOM JOB CLASS                   NAPJ047
WXKOMACB DS    CL6     034-039  KOM JOB-ACCT LST 6 CHRS         NAPJ047
         DS    CL2     040-041                                  NAPJ047
         DS    CL2     042-043                                  NAPJ047
         DS    0CL20   044-063                                  NAPJ047
         DS    CL10    044-053                                  NAPJ047
WXKOMNMA DS    0CL9    054-062  KOM 1ST 9 CHRS OF PROGRAMMER-NM NAPJ047
WXKOMJID DS    CL8     054-061  KOM JOB NUMBER                  NAPJ047
         DS    CL1     062-062                                  NAPJ047
         DS    CL1     063-063                                  NAPJ047
         DS    0CL20   064-083                                  NAPJ047
         DS    CL10    064-073                                  NAPJ047
WXKOMNMB DS    0CL9    074-082  KOM 2ND 9 CHRS OF PROGRAMMER-NM NAPJ047
WXKOMDEV DS    CL8     074-081  KOM SUBMITTER                   NAPJ047
         DS    CL1     082-082                                  NAPJ047
         DS    CL1     083-083                                  NAPJ047
         DS    0CL20   084-103                                  NAPJ047
         DS    CL10    084-093                                  NAPJ047
WXKOMNJI DS    0CL8    094-101  KOM NJE NODE                    NAPJ047
WXKOMNMC DS    CL2     092-093  KOM LST 2 CHRS OF PROGRAMMER-NM NAPJ047
         DS    CL6     094-101                                  NAPJ047
         DS    CL2     102-103                                  NAPJ047
         DS    CL2     104-105                                  NAPJ047
         DS    CL15    106-120                                  NAPJ047
         ORG
J2XIT01  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
J2XIT01  AMODE 31
         PRINT OFF
         EJECT
         PRINT ON
***********************************************************************
*                                                                     *
*        EQUATED VARIABLES FOR SP 1.3.6 AND ABOVE                     *
*                                                                     *
***********************************************************************
         SPACE 3
DCTDSECT EQU   DCT
JCTDSECT EQU   JCT
JOEDSECT EQU   JOE
PCEDSECT EQU   PCE
RDTDSECT EQU   RDT
PPFLAG   EQU   PPPFLAG1,1
PPKEY    EQU   PPPKEY,6
PRLINECT EQU   PPPLNECT,4
PDEVTYPE EQU   PPPDEVTP,4
PWKJOE   EQU   PPPWKJOE,4
PCHJOE   EQU   PPPCHJOE,4
PCEJMTTR EQU   PPPLEBTR,4
PCEEJRCB EQU   PPPEBRCB,4
$SSVT    EQU   $SVT,4
$SVRDT   EQU   SVTRDT,4
         SPACE 3
         USING JCT,R10             SET ADDRESSABILITY TO JCT
.VEREND  ANOP
         PRINT ON
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
J2XIT01E $ENTRY BASE=R12
         SPACE 1
         $SAVE ,                   SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         SLR   R15,R15             ZERO REGISTER 15 FOR RETURN CODE
         LR    R2,R0               SAVE INPUT PARAMETER
         LR    R3,R1               SAVE INPUT DCT ADDRESS
         USING DCTDSECT,R3         SET ADDRESSABILITY TO DCT
         TM    DCTPPSW,DCTPPSWS    SEE IF SEPARATORS REQUESTED
         BO    EXITCHK             NO, GET OUT
         CLI   JCTJOBID,C'S'       IS THIS A STARTED TASK?
         BNE   CKKOM               NO, CHECK FOR KOMSTAR
         CLC   JCTJNAME,C#EMC05P   IS THIS AN EMC PROVIDED PRINTOUT
         BE    EXITCHK             YES, GO TO EXIT
         CLC   JCTJNAME,C#EMC05T   IS THIS AN EMC PROVIDED PRINTOUT
         BE    EXITCHK             YES, GO TO EXIT
         CLC   JCTJNAME,C#CADS     IS THIS A DISPATCH CONTROLLED PRINT
         BE    EXITCHK             YES, GO TO EXIT
CKKOM    DS    0H
         CLC   DCTDEVN,PRINTR3     IS THIS A KOMSTAR
         BE    HDRCHK              YES, CHECK HEADER
         CLC   DCTDEVN,PRT3        IS THIS A KOMSTAR
         BNE   EXITCHK             NO, GET OUT
HDRCHK   DS    0H
         CLI   DCTFORMS,C'F'       DOES THE FORM-ID BEGIN WITH 'F'
         BE    EXITCHK             YES, GO TO EXIT CHECK   E
         LTR   R0,R0               IS THIS A HEADER PAGE?
         BNZ   EXITCHK             NON-ZERO, NOT A HEADER, THUS EXIT
         ICM   R4,B'1111',PCEUSER1 CHECK IF BUFFERS ALREADY OBTAINED
         BNZ   GOTBUF
         USING BUFFER1,R4
         $GETBUF TYPE=HASP,FIX=YES,WAIT=YES GET A WORK BUFFER
         ST    R1,PCEUSER1         SAVE ADDRESS OF BUFFER
         LR    R4,R1
         $GETBUF TYPE=HASP,FIX=YES,WAIT=YES GET A WORK BUFFER
         ST    R1,HDRADDR
GOTBUF   DS    0H
         L     R8,HDRADDR          SAVE ADDRESS OF BUFFER
         USING HDRDSECT,R8         SET ADDRESSABILITY TO HEADER LINE
         EJECT
***********************************************************************
*                                                                     *
*                      K O M S T A R                                  *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        PRODUCE THE PROPER NATIVE MODE COMMANDS FOR THE KODAK        *
*        KOMSTAR UNIT IF REQUIRED FOR THE SYSOUT CLASS BEING          *
*        PROCESSED.                                                   *
*                                                                     *
***********************************************************************
***********************************************************************
*        WRITE KOMSTAR SETUP RECORD 1                                 *
***********************************************************************
         MVC   WXKOMLN,WXKOMC1     MOVE IN 1ST KOM LINE         NAPJ047
         MVC   WXKOMJBN,JCTJNAME   MOVE IN JOB NAME             NAPJ047
         LM    R0,R1,JCTRDRON      MOVE RDR START TIME AND DATE TO WRK
         BAL   R9,EDITDTTM         PERFORM DATE TIME EDIT
         MVC   WXKOMRSD,WXEDDATE   MOVE IN READER START DATE    NAPJ047
         MVC   WXKOMDEV,JCTINDEV   MOVE IN SUBMITTER ID         NAPJ047
         MVC   WXKOMNJI,JCTNONDE   MOVE IN ORIGINATING NODE ID  NAPJ047
         BAL   R9,PRTLINE          PERFORM $PRPUT FOR KOM      NAPJ047
         MVC   WXKOMLN,WXKOMC2     MOVE IN 2ND KOM LINE         NAPJ047
         MVC   WXKOMRST,WXEDTIME   MOVE IN EDITED RDR START TIME
         MVC   WXKOMCLN(L'JCTJCLAS),JCTJCLAS MOVE IN JOB CLASS  NAPJ047
         MVC   WXKOMNMA,JCTPNAME   MOVE 1ST 9 CHRS OF PGMR NM   NAPJ047
         MVC   WXKOMNMB,JCTPNAME+9 MOVE 2ND 9 CHRS OF PGMR NM   NAPJ047
         MVC   WXKOMNMC,JCTPNAME+18  MV LST 2 CHRS OF PGMR NM   NAPJ047
         BAL   R9,PRTLINE          PERFORM $PRPUT FOR KOM      NAPJ047
         MVC   WXKOMLN,WXKOMC3     MOVE IN 3RD KOM LINE         NAPJ047
*        MVC   WXKOMACA,  IF AVAILABLE MOVE IN 1ST PART OF ACCT
*        MVC   WXKOMACB,  IF AVAILABLE MOVE IN LST PART OF ACCT
         MVC   WXKOMJID,JCTJOBID   MOVE IN JCT JOB ID           NAPJ047
         BAL   R9,PRTLINE          PERFORM $PRPUT FOR KOM      NAPJ047
NEWFORM  DS    0H
         MVC   WXKOMLN,WXKOMC4     MOVE IN 4TH KOM LINE         NAPJ047
         BAL   R9,PRTLINE          PERFORM $PRPUT FOR KOM      NAPJ047
EXITCHK  DS    0H
         ICM   R4,B'1111',PCEUSER1 CHECK IF BUFFERS WERE OBTAINED
         BZ    RETCHK
         L     R8,HDRADDR          GET ADDRESS OF 2ND BUFFER
         $FREEBUF (R4),TYPE=HASP   FREE FIRST WORK BUFFER
         $FREEBUF (R8),TYPE=HASP   FREE SECOND WORK BUFFER
RETCHK   DS    0H
         SLR   R15,R15             ZERO OUT R15
         ST    R15,PCEUSER1        STORE IN PCEUSER1
         TM    DCTPPSW,DCTPPSWS    SEE IF SEPARATORS REQUESTED
         BO    JESRET              NO, FINAL EXIT
         CLI   JCTJOBID,C'S'       IS THIS A STARTED TASK
         BNE   CKKOM2              NO, CHECK KOM
         CLC   JCTJNAME,C#EMC05P   IS THIS AN EMC PROVIDED PRINTOUT
         BE    EXITSUP             YES, SUPPRESS JES HEADERS
         CLC   JCTJNAME,C#EMC05T   IS THIS AN EMC PROVIDED PRINTOUT
         BE    EXITSUP             YES, SUPPRESS JES HEADERS
         CLC   JCTJNAME,C#CADS     IS THIS A DISPATCH CONTROLLED PRINT
         BNE   CKKOM2              NO, CHECK FOR KOMSTAR
CKLOCAL  DS    0H
         CLC   =C'PR',DCTDEVN      IS THIS A LOCAL PRINTER
         BE    EXITSUP             YES, SUPPRESS JES HEADERS
CKKOM2   DS    0H
         CLC   DCTDEVN,PRINTR3     IS THIS A KOMSTAR
         BE    FORMCHK             YES, CHECK FORMS
         CLC   DCTDEVN,PRT3        IS THIS A KOMSTAR
         BNE   JESRET              NO, FINAL EXIT
FORMCHK  DS    0H
         CLI   DCTFORMS,C'F'       DOES THE FORM-ID BEGIN WITH 'F'
         BNE   JESRET              NO, DO NOT SUPPRESS HEADER
EXITSUP  DS    0H
         LA    R15,8               SET RETURN CODE TO SUPPRESS HEADER
JESRET   DS    0H
         $RETURN RC=(R15)          RETURN TO JES2
PRTLINE  DS    0H
         LA    R0,L'WXKOMLN        LOAD LINE LENGTH
         LA    R1,WXKOMLN          LOAD ADDRESS OF SETUP COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         BR    R9                  EXIT
*        MVC   ROOMNBR,JCTROOMN    MOVE ROOM NUMBER TO BUFFER
EDITDTTM DS    0H                  GET TIME AND DATE
         ST    R1,WXWORK1          SAVE DATE
         TRT   WXWORK1,TRTPNUM     IS THE DATE POSITIVE NUMERIC
         BNP   BADDATE             NO SIGN OR SIGN IN NUMERIC PART
         STC   R2,BYTEFND
         CLI   BYTEFND,X'FF'       IS THIS A POSITIVE SIGN BYTE?
         BNE   BADDATE             NO
         MVC   SAVETBL,YEARTBL     SAVE A COPY OF DATE TABLE FOR UPDATE
         LA    R6,SAVETBL-L'MOENT  LOAD ADDRESS OF DATE TABLE
         MVC   WXEDJUL,MASKJUL     EDIT THE JULIAN DATE
         ED    WXEDJUL,WXJULDT
         TM    WXJULYR,X'01'       IT THIS AN ODD YEAR
         BO    DAYLOOP             YES, CANNOT BE LEAP YEAR
         TM    WXJULYR,X'12'       MAGIC LEAP YEAR MASK
         BM    DAYLOOP             MIXED, CANNOT BE LEAP YEAR
         MVC   FEBDAYS,P#29        CHANGE FEB TO 29 DAYS
DAYLOOP  DS    0H                  SUBTRACT DAY/MONTH FROM TOTAL
         LA    R6,L'MOENT(,R6)    INCREMENT TO NEXT DATE ENTRY
         SP    WXJULDAY,0(L'TBLDAY,R6)  SUBTRACT DAYS IN THAT MONTH
         BP    DAYLOOP             SEE IF ONLY PARTIAL MONTH REMAINDING
         AP    WXJULDAY,0(L'TBLDAY,R6)  ADD BACK THE DAYS
         OI    WXJULSGN,X'0F'           FORCE THE F ZONE
         UNPK  WXEDDAY,WXJULDAY    UNPACK THE NUMBER OF DAYS
         MVI   WXEDSL1,C'/'        INSERT DELIMITER
         MVI   WXEDSL2,C'/'        INSERT DELIMITER
         MVC   WXEDMON,TBLMON-MOENT(R6) MOVE MONTH (MM)
         LR    R1,R0               SET UP TIME FOR CALCULATION
         SLR   R0,R0               ZERO OUT R0
         D     R0,F#100            DIVIDE BY 100 TO GET NO OF
         SLR   R0,R0                SECONDS
         D     R0,F#60             DIVIDE BY 60 TO GET NO. OF
         CVD   R0,WXDBLWRD           MINUTES WITH REMAINDER IN
         OI    WXDBLSGN,X'0F'        FORCE F ZONE
         UNPK  WXEDSEC,WX2DIGIT      SECONDS
         SLR   R0,R0
         D     R0,=F'60'           DIVIDE BY 60 TO GET NO. OF
         CVD   R0,WXDBLWRD           HOURS WITH REMAINDER IN
         OI    WXDBLSGN,X'0F'       FORCE F ZONE
         UNPK  WXEDMIN,WX2DIGIT      MINUTES
         CVD   R1,WXDBLWRD         GET HOURS
         OI    WXDBLSGN,X'0F'       FORCE F ZONE
         UNPK  WXEDHOUR,WX2DIGIT
         MVI   WXEDPER1,C'.'
         MVI   WXEDPER2,C'.'                                    NAPJ047
         BR    R9
BADDATE  DS    0H
         MVC   WX26TMDT,C#BADTMD
         BR    R9
TRTX     EQU   *-240
         DC    C'0123456789ABCDEF'
TRTPNUM  DS    0CL256
         DC    XL16'000000000000000000000A0BFF0D0EFF' 00-0F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 10-1F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 20-2F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 30-3F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 40-4F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 50-5F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 60-6F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 70-7F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 80-8F
         DC    XL16'000000000000000000000A0BFF0D0EFF' 90-9F
         DC    XL16'000000000000000000000A0BFF0D0EFF' A0-AF
         DC    XL16'000000000000000000000A0BFF0D0EFF' B0-BF
         DC    XL16'000000000000000000000A0BFF0D0EFF' C0-CF
         DC    XL16'000000000000000000000A0BFF0D0EFF' D0-DF
         DC    XL16'000000000000000000000A0BFF0D0EFF' E0-EF
         DC    XL16'000000000000000000000A0BFF0D0EFF' F0-FF
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS USED BY THIS JES2 EXIT                             *
*                                                                     *
***********************************************************************
F#60     DC    F'60'
F#100    DC    F'100'
C#CADS   DC    CL8'CADSDISP'
C#EMC05P DC    CL8'JLEMC05P'
C#EMC05T DC    CL8'JLEMC05T'
C#BADTMD DS    0CL16
C#BADTM  DC    CL8'BAD TIME'
C#BADDT  DC    CL8'BAD DATE'
P#29     DC    PL2'29'
PRINTR3  DC    CL8'PRINTR3'
PRT3     DC    CL8'PRT3   '
YEARTBL  DS    0CL52
         DC    PL2'31',CL2'01',PL2'28',CL2'02' JULIAN DATE
         DC    PL2'31',CL2'03',PL2'30',CL2'04'   TO
         DC    PL2'31',CL2'05',PL2'30',CL2'06' DAY AND MONTH
         DC    PL2'31',CL2'07',PL2'31',CL2'08' CONVERSION
         DC    PL2'30',CL2'09',PL2'31',CL2'10'  TABLE
         DC    PL2'30',CL2'11',PL2'31',CL2'12'
         DC    PL2'999',CL2'13'
         SPACE 3
MASK1    DC    X'402120202020'     EDIT MASK
MASK2    DC    X'402021204B20204B2020' EDIT MASK
MASK3    DC    X'402120'           EDIT MASK
MASK4    DC    X'21204B20204B2020' EDIT MASK
MASKJUL  DC    X'21204B202020'
WXKOMC1  DS    0CL121                                           NAPJ047
         DC    X'01'                                            NAPJ047
         DC    X'4163FF'                                        NAPJ047
         DC    C'W1'                                            NAPJ047
         DC    CL8' '   JOBNAME                                 NAPJ047
         DC    CL8' '   DATE                                    NAPJ047
         DC    CL2' '                                           NAPJ047
         DC    CL20'B108145T2 D$$$09'                           NAPJ047
         DC    CL20'B208145B2 D$$$01'                           NAPJ047
         DC    CL10'B308015T '                                  NAPJ047
         DC    CL8' '                   SUBMITTR                NAPJ047
         DC    CL2' '                                           NAPJ047
         DC    CL10'B408015B '          NJE NODE                NAPJ047
         DC    CL8' '                                           NAPJ047
         DC    CL2' '                                           NAPJ047
         DC    XL2'FC10'                                        NAPJ047
         DC    XL15'00'                                         NAPJ047
WXKOMC2  DS    0CL121                                           NAPJ047
         DC    X'01'                                            NAPJ047
         DC    X'4163FF'                                        NAPJ047
         DC    CL10'B508125T3'                                  NAPJ047
         DC    CL8' '          TIME                             NAPJ047
         DC    CL2' '                                           NAPJ047
         DC    CL10'B608125B3'                                  NAPJ047
         DC    CL8' '           JOBCLASS                        NAPJ047
         DC    CL2' '                                           NAPJ047
         DC    CL10'B709053L1'                                  NAPJ047
         DC    CL9' '    PGMR-ID 1-9                            NAPJ047
         DC    CL1' '                                           NAPJ047
         DC    CL10'B809083L1'                                  NAPJ047
         DC    CL9' '    PGMR-ID 10-18                          NAPJ047
         DC    CL1' '                                           NAPJ047
         DC    CL10'B902113L1'                                  NAPJ047
         DC    CL2' '    PGMR-ID 19-20                          NAPJ047
         DC    CL8' '                                           NAPJ047
         DC    XL2'FC10'                                        NAPJ047
         DC    XL15'00'                                         NAPJ047
WXKOMC3  DS    0CL121                                           NAPJ047
         DC    X'01'                                            NAPJ047
         DC    X'4163FF'                                        NAPJ047
         DC    CL10'BA06027T5'                                  NAPJ047
         DC    CL6' '      1ST 6 CHRS OF ACCT                   NAPJ047
         DC    CL4' '                                           NAPJ047
         DC    CL10'BB06037T4'                                  NAPJ047
         DC    CL6' '      LST 6 CHRS OF ACCT                   NAPJ047
         DC    CL4' '                                           NAPJ047
         DC    CL10'BC08025B4'                                  NAPJ047
         DC    CL8' '      DJC-NET-ID                           NAPJ047
         DC    CL2' '                                           NAPJ047
         DC    CL20'H2    064'                                  NAPJ047
         DC    CL20'BD03164T1 S$$$'                             NAPJ047
         DC    XL2'FC10'                                        NAPJ047
         DC    XL15'00'                                         NAPJ047
WXKOMC4  DS    0CL121                                           NAPJ047
         DC    X'01'                                            NAPJ047
         DC    X'4163FF'                                        NAPJ047
         DC    CL20'11012836162227140912'                       NAPJ047
         DC    CL20'20101'                                      NAPJ047
         DC    CL20'G164066'                                    NAPJ047
         DC    CL20'H1      S   R'                              NAPJ047
         DC    CL20'Q1   PMEJES3'                               NAPJ047
         DC    XL1'10'                                          NAPJ047
         DC    XL16'00'                                         NAPJ047
         EJECT
         LTORG
         EJECT
         $MODEND
         END
