*          DATA SET XIEF433DCM AT LEVEL 003 AS OF 03/29/90
XIEF433D TITLE 'LIGHTING MESSAGE HANDLING EXIT            '
*/* LIB:  N/A
*/* GDE:  GC28-1147 MVS/XA SPL USER EXITS
*/* DOC:  THIS EXIT IS TAKEN FROM THE CANCWAIT SAMPLE ON PAGE 51
*/*       ON THE JOB LOG WITH STEP TERMINATION STATUS.
***********************************************************************
*                                                                     *
*             MODULE NAME = XIEF433D                                  *
*                                                                     *
*             DESCRIPTIVE NAME =  COMMUNICATIONS TASK USER EXIT FOR   *
*                MESSAGE IEF433D                                      *
*                                                                     *
*             PHILIPS LIGHTING 1990, THIS IS IN THE PUBLIC DOMAIN AND *
*             IS IN USE AT PHILIPS LIGHTING.  PLEASE FEEL FREE TO USE *
*             AT YOUR OWN RISK.  NO WARRANTY EXPRESS OR IMPLIED IS    *
*             GIVEN                                                   *
*                                                                     *
*             FUNCTION =                                              *
*                AUTOMATICALLY ISSUE NOHOLD REPLY TO IEF433D          *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = NONE                                 *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 1  = WORK REGISTERS               *
*                    REGISTERS 2 TO 11 = SEE REGISTER EQUATES BELOW   *
*                    REGISTER  12      = ADDRESSABILITY TO XIEF433D   *
*                                        CSECT                        *
*                    REGISTER  13      = ADDRESSIBILITY TO DATA DSECT *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   BINARY ZEROES)                                    *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                ATTRIBUTES = KEY 0, REENTRANT,                       *
*                   SUPERVISOR STATE, ENABLED, NO LOCKS HELD          *
*                                                                     *
*             ENTRY POINTS = XIEF433D (ONLY ENTRY POINT)              *
*                                                                     *
*                LINKAGE =                                            *
*                                                                     *
*                                                                     *
*             INPUT = REG1 POINTS TO THE ADDRESS OF THE               *
*                     COMMUNICATIONS TASK EXIT PARAMETER LIST (CTXT)  *
*                     REGS 0 AND 2-12 IRRELEVANT
*                     REG 13, STANDARD SAVE AREA
*                     REG 14, RETURN ADDRESS
*                     REG 15, ENTRY POINT ADDRESS
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SYS1.MACLIB FREEMAIN, GETMAIN, IEZBITS,        *
*                          IEZMGCR, PANAEXIT, RETURN, SAVE            *
*                      SYS1.AMODGEN IEZVX100, MGCR                    *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
XIEF433D CSECT ,
XIEF433D AMODE 31
XIEF433D RMODE ANY
*
* CONSTANT ADDRESS EQAUTES
*
PSAAOLD  EQU    X'224',4    ADDRESS IN PSA OF CURRENT ASCB
ASCBJBNI EQU    X'AC',4     ADDRESS IN JOBNAME IN ASCB
CHSTEP   EQU    X'38',8     ADDRESS IN STEP NAME IN CSCB
CHPROCSN EQU    X'18',8     ADDRESS IN PROC STEP NAME IN CSCB
*
* CONSTANT EQUATES
*
K0       EQU   00                  NON-RELOCATABLE CONSTANT 00
K1       EQU   01                  NON-RELOCATABLE CONSTANT 01
K2       EQU   02                  NON-RELOCATABLE CONSTANT 02
K3       EQU   03                  NON-RELOCATABLE CONSTANT 03
K4       EQU   04                  NON-RELOCATABLE CONSTANT 04
K5       EQU   05                  NON-RELOCATABLE CONSTANT 05
K6       EQU   06                  NON-RELOCATABLE CONSTANT 06
K7       EQU   07                  NON-RELOCATABLE CONSTANT 07
K8       EQU   08                  NON-RELOCATABLE CONSTANT 08
K9       EQU   09                  NON-RELOCATABLE CONSTANT 09
KA       EQU   10                  NON-RELOCATABLE CONSTANT 10
KB       EQU   11                  NON-RELOCATABLE CONSTANT 11
KC       EQU   12                  NON-RELOCATABLE CONSTANT 12
KD       EQU   13                  NON-RELOCATABLE CONSTANT 13
KE       EQU   14                  NON-RELOCATABLE CONSTANT 14
KF       EQU   15                  NON-RELOCATABLE CONSTANT 15
*
* CONSTANT EQUATES
*
SPINPRVT EQU   230                 USE SUBPOOL 230 FOR GETMAIN
SUBPOOL  EQU   252                 USE SUBPOOL 252 FOR GETMAIN
CK       EQU   C'K'                CONSTANT CHAR 'K'
CASTR    EQU   C'*'                CONSTANT CHAR '*'
C0       EQU   C'0'                CONSTANT CHAR '0'
         IEZBITS
*
* REGISTER EQUATES
*
R0       EQU   00                  REG  0 - WORK REG
R1       EQU   01                  REG  1 - WORK/PARMETER REG
R2       EQU   02                  REG  2 - WORK REG
R3       EQU   03                  REG  3 - POINTER TO THE CTXT
R4       EQU   04                  REG  4 - PTR TO THE MSSG ATTRIBUTES
R5       EQU   05                  REG  5 - PTR TO THE MESSAGE AREA
R6       EQU   06                  REG  6 - POINTER TO THE REPLY
R7       EQU   07                  REG  7 - PTR TO MSSG ROUTING AREA
R8       EQU   08                  REG  8 - USED BY EXECUTED MOVE
R9       EQU   09                  REG  9 - UNUSED
R10      EQU   10                  REG 10 - UNUSED
R11      EQU   11                  REG 11 - UNUSED
R12      EQU   12                  REG 12 - CSECT BASE REG
R13      EQU   13                  REG 13 - DATA DSECT BASE REG
R14      EQU   14                  REG 14 - LINKAGE REGISTER
R15      EQU   15                  REG 15 - WORK REG
         EJECT
         SAVE  (14,12)
         USING XIEF433D,R12        SET UP CSECT ADDRESSABILITY
         LR    R12,R15             LOAD BASE REGISTER
         L     R3,0(,R1)           LOAD ADDRESS OF CTXT
         USING CTXT,R3
         L     R4,CTXTTXPJ         POINT TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R4
         LA    R5,CTXTTMSG         POINT TO THE MESSAGE AREA
         USING MSGTEXT,R5
         L     R7,CTXTRCP          POINT TO MESSAGE ROUTING AREA
         USING CTXTROUT,R7
         LA    R6,NOHLDMSG          YES, SET UP TO REPLY NOHOLD
***********************************************************************
*                                                                     *
*        DYNAMIC STORAGE FOR THIS MODULE IS BEING OBTAINED BELOW THE  *
*        16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR PARAMETER LIST  *
*        TO BE IN 24-BIT ADDRESSABLE STORAGE                          *
*                                                                     *
***********************************************************************
         GETMAIN RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW OBTAIN DYNAMIC
*                                                    STORAGE
         USING DATAAREA,R1
         ST    R13,SAVEPR13     SAVE THE SAVE AREA POINTER IN THE NEW
         ST    R1,8(,R13)           AREA AND R1 IN THE PRIOR ONE
         LR    R13,R1 POINT R13 TO THE DATA AREA
         DROP  R1
         USING DATAAREA,R13
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR OUT THE REPLY AREA
         XC    CTXTROUT(2),CTXTROUT    CLEAR OUT THE ROUT CODE
         SLR   R8,R8                   SET UP TO GET THE LENGTH OF
         IC    R8,0(,R6)                   THE REPLY TEXT
         EX    R8,MVREPLY              MOVE THE REPLY TEXT
         MVC   REPLY,CTXTRPID          MOVE IN THE REPLY ID
         LA    R8,MGCRTEXT-MGCRPL+1(,R8)   SET UP THE LENGTH OF THE
         STC   R8,MGCRLGTH                     REPLY
         SLR   R0,R0
         MGCR  MGCRPL                  ISSUE THE MESSAGE
         LR    R8,R13                  RESTORE THE SAVE AREA POINTER
         L     R13,SAVEPR13
         FREEMAIN RU,LV=DATAEND,A=(R8),SP=SPINPRVT  FREE THE DATA AREA
         B     CNGROUT
MVREPLY  MVC   MGCRTEXT(0),1(R6)       EXECUTED MOVE OF REPLY TEXT
         DROP  R13
CNGROUT  DS    0H
         OI    CTXTRFB1,CTXTRCRC       REQUEST TO CHANGE ROUT
EXIT     DS    0H
         RETURN (14,12)                EXIT, NOTE RETURN CODE NOT GIVEN
IEF433D  DC    CL7'IEF433D'
NOHLDMSG DS    0CL(L'NOHLLGTH+L'NOHLTXT)
NOHLLGTH DC    AL1(L'NOHLTXT-1)
NOHLTXT  DC    C'REPLY XX,NOHOLD'
DATAAREA DSECT
         DS    0D
SAVEAREA DS    0CL72
SAVEPL1  DS    F
SAVEPR13 DS    F
SAVENX13 DS    F
SAVE1412 DS    15F
         DS    0F
MGCR     IEZMGCR DSECT=NO
         ORG   MGCRTEXT
COMMAND  DS    CL6
REPLY    DS    CL2
REPLYMSG DS    CL50
         ORG
DATAEND  EQU   *-DATAAREA
MSGTEXT  DSECT
MSGID    DS    CL8
JOBDATA  DS    CL8
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
         DS    CL25
WAITVAL  DS    CL4
         IEZVX100
         END
