++ USERMOD (NAPJ003) .
++ VER (Z038) FMID(HJE2221).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT04),DISP=SHR
//SYSIN  DD  SYS1.JES2SRC(J2XIT04),DISP=SHR
//STEP2    EXEC PGM=IEWL,PARM='RENT'
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT04),DISP=SHR
//SYSLMOD  DD  DSN=SYS1.JES2LIB,DISP=SHR
//SYSLIN  DD  *
  INCLUDE SYSPUNCH(J2XIT04)
  NAME J2XIT04(R)
++SRC (J2XIT04) DISTLIB(JES2SRC).
CMDC     TITLE 'Lighting check for proper command submitter'
******************************************************************
*                                                                *
* Load module: J2XIT04.     Library: Linklist.                   *
*                           Environment: JES2 main task.         *
*                                                                *
*                                                                *
*      This module contains exit 4 routine to do JES2 command    *
*   if submitted by TSO, a job or a started task other than      *
*   one named COMMAND.                                           *
*                                                                *
******************************************************************
         COPY  $HASPGBL
J2XIT04  $MODULE $MIT,                                             WEJ C
               $BUFFER,                                                C
               $HASPEQU,                                               C
               $TAB,                                                   C
               RPL,                                                    C
               $PCE,                                                   C
               $JQE,                                                   C
               $JOE,                                                   C
               $JCT,                                                   C
               $IOT,                                                   C
               $DCT,                                                   C
               $HCT,                                                   C
               $MODMAP
J2XIT04  AMODE 31
J2XIT04  RMODE 24
         EJECT ,
         DROP  ,                     Discard USINGs done by $MODULE
J2XIT04E $ENTRY BASE=R12
***********************************************************************
*                                                                     *
*    Load module: J2XIT04.    Routine:  J2XIT02E                      *
*                             Environment:  JES2 main task (HASPRDR). *
*                                                                     *
*   I N S T A L L A T I O N      J C L / J E C L   S C A N            *
*                                                                     *
*      This exit routine provides installation control of JCL and     *
*    JECL.  It also passes commands.  At lighting it is used to       *
*    guarentee that JES2 commands are only submitted by a started     *
*    task named COMMAND by blanking out the $ if they are not.        *
*                                                                     *
*                                                                     *
*        Registers on entry are:                                      *
*                                                                     *
*        Reg. 0:    0 ==> JECL                                        *
*                   4 ==> JCL                                         *
*                                                                     *
*        Reg. 1:    --> A three word parameter list:                  *
*                   Word 1 - Address of the statement image.          *
*                                                                     *
*                   Word 2 - Address of RDWFLAGX in PCE.              *
*                      X'01' ==> JCL statement                        *
*                      X'02' ==> Control statement                    *
*                      X'04' ==> Job statement                        *
*                      X'08' ==> Continue statement                   *
*                      X'10' ==> Exit supplied next statement         *
*                      X'20' ==> Error msg. supplied by exit          *
*                                                                     *
*                   Word 3 - ADDRESS of JCTXWRK field in the JCT.     *
*                                                                     *
*        Reg. 2-9 --- N/A                                             *
*                                                                     *
*        Reg. 10----==> JCT, zero if this is a command                *
*                                                                     *
*        Reg. 11----==> HCT                                           *
*                                                                     *
*        Reg. 12----- N/A                                             *
*                                                                     *
*        Reg. 13----==> HASPRDR PCE                                   *
*                                                                     *
*        Reg. 14----- Contains return address                         *
*                                                                     *
*        Reg. 15----- Contains entry point address                    *
*                                                                     *
*  Return codes:                                                      *
*                                                                     *
*       0 - All ok, continue normally.                                *
*       4 - Same as 0, no more exit routines.                         *
*       8 - If JECL, change to //* and do not process, if JCL         *
*           do normal processing                                      *
*      12 - Cancel job, print the images thus far                     *
*      16 - Cancel job, queue to purge, no output.                    *
*                                                                     *
*                                                                     *
*  Routine description:                                               *
*                                                                     *
*     This routine will perform the following initialization steps:   *
*                                                                     *
*     1. If the input is JES2 JECL, and column 3 is $ (a command)     *
*        and the submitter is not a started task called COMMAND set   *
*        the return code to 12 and blank out the $                    *
*                                                                     *
*     Written by:                                                     *
*                        Clark Morris, Dept. 5069                     *
*                        Philips Lighting Company                     *
*                        Philips Square CN6800                        *
*                        Somerset, NJ 08875-6800                      *
*                        United States of America                     *
*                        201-563-3575 (WEJ)                           *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*          Maintenance   Log                                          *
*          =================                                          *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
         USING HCT,R11
         USING PCE,R13
        $SAVE                          Save registers.
         LR    R12,R15                 Load base register.
         SLR   R15,R15                 Set reg 15 to 0
         LR    R2,R1                   Preserve parm register.
         EJECT
***********************************************************************
*                                                                     *
*  If our device is an INTRDR, and it's owned by an STC or TSU,       *
*  default MSGCLASS to 'A'; naturally, any job-card MSGCLASS will     *
*  override this default.                                             *
*                                                                     *
***********************************************************************
         SPACE 1
         LTR   R0,R0                   JES2 control statement?
         BNZ   X4MSGD                  No. go to exit
         L     R1,0(,R2)               Point to the control statement
         CLI   2(R1),C'$'              Is this a command?
         BNZ   X4MSGD                  No. go to exit
         L     R9,PCEDCT               Point at DCT
         USING DCT,R9
         CLI   DCTDEVTP,DCTINR         Internal reader?
         BNE   X4MSGD                  No. Command will be handled
*                                      based on the init deck
         CLI   RIDJBID,C'J'            Owner of INTRDR is a JOB ?
         BE    X4MSGC                  Yes, Go to blank out $
         CLI   RIDJBID,C'S'            Owner of INTRDR is a STC ?
         BNE   X4MSGC                  No, go to blank out $
         CLC   RIDJNAM,=C'COMMAND '    Is task name COMMAND
         BE    X4MSGD                  Yes, go to exit
X4MSGC   DS    0H
         MVI   2(R1),C'?'              Change $ to ?
         LA    R15,12                  Set reg 15 to 12
         DROP  R9
         SPACE 1
***********************************************************************
*                                                                     *
*   Return to caller.                                                 *
*                                                                     *
***********************************************************************
         SPACE 1
X4MSGD   DS    0H
        $RETURN RC=(R15)
         EJECT ,
         LTORG ,
         $MODEND ,
         END
