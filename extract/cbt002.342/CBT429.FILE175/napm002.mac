++ USERMOD (NAPM002) .
++ VER (Z038) FMID(HBB2102).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(IEFUSI),DISP=SHR
//SYSIN  DD  SYS1.USERSRC(IEFUSI),DISP=SHR
//STEP2    EXEC PGM=IEWL,PARM='RENT'
//SYSPUNCH DD  DSN=&&PUNCH(IEFUSI),DISP=SHR
//SYSLMOD  DD  DSN=SYS1.LPALIB,DISP=SHR
//SYSLIN  DD  *
  INCLUDE SYSPUNCH(IEFUSI)
  NAME IEFUSI(R)
++SRC (IEFUSI) DISTLIB(USERSRC).
IEFUSI   TITLE 'SMF STEP INITIATION EXIT ROUTINE                '
*/* LIB: IPO1.SAMPLIB(SMFUSIX)
*/* GDE: CBIPO MVS CUSTOMIZATION
*/* DOC: THIS IS SAMPLE SMF EXIT ROUTINE TO SET REGION SIZE
*/*      AND REGION LIMIT (BELOW AND ABOVE 16MB) FOR ALL JOBS
***********************************************************************
**                                                                    *
*             MODULE NAME = IEFUSI                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  CUSTOM-BUILT  IPO SUPPLIED          *
*                SMF EXIT ROUTINE TO LIMIT GETMAIN FOR ALL JOBS       *
*                                                                     *
*             COPYRIGHT = 5750 AAM                                    *
*                         INTERNATIONAL BUSINESS MACHINES CORPORATION *
*                         1983                                        *
*                                                                     *
*             FUNCTION =                                              *
*                BASED ON THE REQUESTED REGION SIZE BELOW 16MB,       *
*                CALCULATES THE REGION LIMIT BELOW 16MB AS BEING      *
*                THE REGION SIZE PLUS N TIMES 64K.                    *
*                IT ALSO SETS A REGION SIZE AND LIMIT ABOVE 16MB.     *
*                                                                     *
*                                                                     *
*                OPERATION =                                          *
*                   INVOKED AT STEP INITIATION TIME AND ACTIVATED FOR *
*                   ALL JOBS.                                         *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = NONE                                 *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTER  0       = UNUSED                       *
*                    REGISTER  1       = ADDRESS OF INPUT PARAMETERS  *
*                    REGISTER  2       = UNUSED                       *
*                    REGISTER  3       = ADDRESS OF COMMON EXIT       *
*                                        PARAMETER AREA               *
*                    REGISTER  4       = ADDRESS OF JOB STEP NAME     *
*                    REGISTER  5       = ADDRESS OF PROGRAM NAME      *
*                    REGISTER  6       = ADDRESS OF ACCOUNTING INFO   *
*                    REGISTER  7       = ADDRESS OF VSM PARM LIST     *
*                    REGISTER  8       = ADDRESS OF SRM INFORMATION   *
*                    REGISTERS 9       = UNUSED                       *
*                    REGISTERS 10      = WORK REGISTER                *
*                    REGISTERS 11      = WORK REGISTER                *
*                    REGISTER  12      = ADDRESSABILITY TO IEFUSI     *
*                                        CSECT                        *
*                    REGISTER  13      = CALLERS SAVE AREA ADDRESS    *
*                    REGISTER  14      = WORK REGISTER                *
*                    REGISTER  15      = WORK REGISTER                *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   BINARY ZEROES)                                    *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASSEMBLER H VERSION 2                    *
*                                                                     *
*                MODULE SIZE =      BYTES                             *
*                                                                     *
*                ATTRIBUTES = KEY 0, REENTRANT,                       *
*                   SUPERVISOR STATE, ENABLED, NO LOCKS HELD          *
*                                                                     *
*             ENTRY POINTS = IEFUSI (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                                                                     *
*                                                                     *
*             INPUT = REG1 POINTS TO A LIST OF FOUR-BYTE ADRESSES AS  *
*                     FOLLOWS:                                        *
*                     1. ADDRESS OF COMMON EXIT PARAMETER AREA        *
*                     2. ADDRESS OF AN 8-BYTE AREA CONTAINING         *
*                        THE JOB STEP NAME FROM THE EXEC STATEMENT.   *
*                     3. ADDRESS OF AN 8-BYTE AREA CONTAINING         *
*                        THE PROGRAM NAME FROM THE EXEC STATEMENT.    *
*                     4. ADDRESS OF AN AREA CONTAINING THE ACCOUNTING *
*                        INFORMATION FROM THE EXEC STATEMENT.         *
*                     5. ADDRESS OF A SIX-WORD AREA CONTAINING REGION *
*                        LIMIT VALUES.                                *
*                     6. ADDRESS OF A WORD CONTAINING THE PERFORMANCE *
*                        GROUP NUMBER AND A FLAG INDICATING A V=R JOB.*
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BSM 0,14 TO          *
*                             RETURN IN CALLER'S MODE                 *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = NONE                                *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SAVE, RETURN                                   *
*                                                                     *
*             CHANGE ACTIVITY = THIS IS A NEW SMF EXIT WHICH IS       *
*                PROVIDED FOR MVS/XA IPO.                             *
*                IT WILL RUN IN31-BIT MODE ABOVE THE LINE             *
*                                                                     *
*             MESSAGES =                                              *
*                NONE                                                 *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
*  MODIFIED AT LIGHTING TO RESTRICT TSO USERS AND THE BATCH TMP TO    *
*  2 MEGABYTES ABOVE THE LINE                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
IEFUSI   CSECT ,                       CSECT NAME DECLARED
IEFUSI   AMODE 31
IEFUSI   RMODE ANY
*
*
*
* REGISTER EQUATES
*
R00      EQU   00                      REGISTER 0
R01      EQU   01                      REGISTER 1
R02      EQU   02                      REGISTER 2
R03      EQU   03                      REGISTER 3
R04      EQU   04                      REGISTER 4
R05      EQU   05                      REGISTER 5
R06      EQU   06                      REGISTER 6
R07      EQU   07                      REGISTER 7
R08      EQU   08                      REGISTER 8
R09      EQU   09                      REGISTER 9
R10      EQU   10                      REGISTER 10
R11      EQU   11                      REGISTER 11
R12      EQU   12                      REGISTER 12
R13      EQU   13                      REGISTER 13
R14      EQU   14                      REGISTER 14
R15      EQU   15                      REGISTER 15
         EJECT
         SAVE  (14,12),,IEFUSI_&SYSDATE._&SYSTIME
         USING IEFUSI,R12              SET UP BASE ADDRESSABILITY
*
* NOTE THAT NO SAVE_AREA WAS GETMAINED SINCE THIS ROUTINE
*      CALLS NO OTHER ROUTINE OR ISSUES ANY SVC'S
*
         LR    R12,R15                 LOAD BASE REG WITH ENTRY POINT
         LM    R03,R08,0(R01)          LOAD INPUT PARM LIST ADDRESSES
*
*    R03 = ADDRESS OF COMMON PARAMETER EXIT ARE
*    R04 = ADDRESS OF 8-BYTE AREA CONTAINING JOB STEP NAME
*    R05 = ADDRESS OF 8-BYTE AREA CONTAINING PROGRAM NAME
*    R06 = ADDRESS OF AREA CONTAINING ACCOUNTING INFORMATION
*    R07 = ADDRESS OF SIX-WORD AREA CONTAINING REGION LIMIT VALUES
*    R08 = ADDRESS OF WORD CONTAINING PERFORMANCE GROUP NUMBER
*
         USING REGION,R07              ADDRESSABILITY FOR REGION DSECT
         OI    REGFLAGS,X'80'          SET THAT IEFUSI CONTROLS REGIONS
         TM    0(R08),X'80'            V=R JOB?
         BO    EXIT                    YES USE DEFAULT VALUES
         L     R10,REGSZREQ            GET REQUESTED REGION SIZE
         LTR   R10,R10                 IS IT ZERO
         BZ    EXIT                    YES USE DEFAULT VALUES
         AL    R10,N64K                ADD N64K TO REGION SIZE BELOW
         ST    R10,REGLIMB             SET IT AS REGION LIMIT BELOW
         MVC   REGSIZB,REGSZREQ        SET REGION BELOW
         MVC   REGSIZA,REG32MB         SET REGION SIZE ABOVE TO 32MB
         MVC   REGLIMA,REG32MB         SET REGION LIMIT ABOVE TO 32MB
         CLC   =C'IKJEFT01',0(R05)    IS CALLER THE TERMINAL MONITOR
         BNE   EXIT                   NO, EXIT
         MVC   REGSIZA,REG2MB         SET REGION SIZE ABOVE TO 2MB
         MVC   REGLIMA,REG2MB         SET REGION LIMIT ABOVE TO 2MB
*
* RETURN TO CALLER
*
EXIT     DS    0H
         LM    14,12,12(13)            RESTORE REGISTERS
         LA    15,0                    SET RETURN CODE TO ZERO
         BSM   0,14                    RETURN TO CALLER IN ITS MODE
         EJECT
*
*  DATA AREA
*
REG32MB  DC    F'33554432'         REGION SIZE OF 32MB
REG2MB   DC    F'2097152'          REGION SIZE OF 2MB
N64K     DC    F'65536'            64K
*
*
* PATCH AREA
*
         DS    0F
MODLEN   EQU   *-IEFUSI           EQUATE FOR MODULE LENGTH(- PATCH)
PTCHLEN  EQU   ((MODLEN+7)/8+7)/8*8 PATCH AREA LENGTH EQUATE
PTCHLNTH DC    Y(PTCHLEN)          LENGTH OF PATCH AREA
PTCHBASE DC    S(PATCH)            BASE DISPLACED ADDRESS OF PATCH
PATCH    DC    XL(PTCHLEN)'00'     PATCH AREA
REGION   DSECT
REGFLAGS DS    F                   FLAG WORD
REGSZREQ DS    F                   REGION SIZE REQUESTED
REGLIMB  DS    F                   REGION LIMIT BELOW 16MB
REGSIZB  DS    F                   REGION SIZE BELOW 16MB
REGLIMA  DS    F                   REGION LIMIT ABOVE 16MB
REGSIZA  DS    F                   REGION SIZE ABOVE 16MB
         END   IEFUSI
