++USERMOD(NAPMM09) .
++ VER(Z038) FMID(HBB2102) .
++ MAC(PANAEXIT) DISTLIB(AMACLIB) SYSLIB(MACLIB) .
.**********************************************************************
.* MACRO DESCRIPTION  PANAEXIT-USERMOD NAPMM09                        *
.*                                                                    *
.*     THIS MACRO INSERTS A DATE TIME STAMP FOR ALL ASSEMBLER         *
.*     PROGRAMS SUBMITTED THROUGH PANVALET.  THE MACRO IS INSERTED    *
.*     BY THE PANVALET EXIT ROUTINE.                                  *
.*                                                                    *
.* INPUT                                                              *
.*     THE INPUT IS GENERATED FROM THE PANVALET BATCH SYSTEM.         *
.*                                                                    *
.* OUTPUT                                                             *
.*     THE OUTPUT OF THIS MACRO WILL PASS ONE OF TWO CONDITIONS:      *
.*            A. IF THE SOURCE CODE CONTAINS A CSECT CARD, THIS       *
.*               MACRO WILL GENERATE AT THE END OF THE PROGRAM.       *
.*               THE GENERATED DATA IS:                               *
.*                   LEVEL=???,         3 POSITION LEVEL.             *
.*                   DATE=MM/DD/YY,     DATE.                         *
.*                   NAME=PGM. NAME     (UP TO 10 CHARACTERS).        *
.*                   ASSEMBLED ON &SYSDATE AT &SYSTIME                *
.*                                                                    *
.*            B. IF THE SOURCE CODE DOES NOT CONTAIN A CSECT CARD     *
.*               (IE. CICS MAP) THIS MACRO WILL HAVE NO EFFECT.       *
.*                                                                    *
.*--------------------------------------------------------------------*
.*      RECORD OF CHANGES TO THIS MACRO  ----  PANAEXIT               *
.*                                                                    *
.*  ID         DATE        DESCRIPTION OF CHANGE                      *
.*  ---      --------      ---------------------                      *
.*  RAW      06/24/83      MACRO CREATION DATE                        *
.*  DWL      11/07/85      DOCUMENTATION CHANGES                      *
.*  CFM      02/19/89      MADE AN MVS MACRO BY USERMOD NAPMM09 SO    *
.*                         ALWAYS PRESENT FOR PANVALET ASSEMBLIES     *
.*                                                                    *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
.*
         MACRO
&SYMBOL  PANAEXIT &LEVEL=,&DATE=,&NAME=
         LCLC  &LCLBLNK
         LCLC  &LCLNAME
         LCLC  &LCLWORK
         AIF   (K'&SYSECT EQ 0).PANSKIP
&LCLBLNK SETC  '        '                ASSIGN BLANKS (8).
&LCLWORK SETC  '&NAME'.'&LCLBLNK'        PAD NAME WITH BLANKS.
&LCLNAME SETC  '&LCLWORK'(1,7)           TRUNCATE TO SEVEN CHARACTERS.
@&LCLNAME CSECT
         DC   CL80'LEVEL=&LEVEL.'
         DC   CL80'LAST ACCESSED=&DATE.'
         DC   CL80'NAME=&NAME.'
         DC   CL80'ASSEMBLED ON &SYSDATE. AT &SYSTIME. '
.PANSKIP ANOP
         MEND
