WHOIS    TITLE 'WHOIS -- TSO USERID COMMAND'
**********************************************************************
*                                                                    *
*  MODULE NAME -                                                     *
*                                                                    *
*     WHOIS  -- TSO COMMAND PROCESSOR TO DISPLAY THE "NAME"          *
*               FIELD OF A RACF USERID AT THE TERMINAL.              *
*                                                                    *
*  FUNCTION -                                                        *
*                                                                    *
*     SEE ABOVE...                                                   *
*                                                                    *
*  ATTRIBUTES -                                                      *
*                                                                    *
*     NONREENTRANT, NONREUSABLE, NONREFRESHABLE                      *
*                                                                    *
*  REQUIREMENTS -                                                    *
*                                                                    *
*     -  'WHOIS' MUST BE ENTERED IN THE RACF AUTHORIZED              *
*            CALLER TABLE (ICHAUTAB)                                 *
*                                                                    *
*     -  'WHOIS' SHOULD BE ENTERED INTO THE TSO/E AUTHORIZED         *
*            COMMAND TABLE (IKJEFTE2)                                *
*                                                                    *
*     -  LOAD MODULE MUST BE LINKED 'AC=1' AND PLACED INTO           *
*            AN APF AUTHORIZED LIBRARY                               *
*                                                                    *
*     -  THE RACF USERID(S) MUST BE (6) CHARACTERS IN LENGTH         *
*                                                                    *
*  CHANGE HISTORY -                                                  *
*                                                                    *
*     03/11/87, REMOVED THE CODE FOR 'SVC 236' THAT KARL TAI HAD     *
*               WRITTEN, WILL USE THE RACF AUTHORIZED CALLER TABLE   *
*               INSTEAD AS IT SHOULD BE...ELL                        *
*                                                                    *
**********************************************************************
WHOIS    CSECT
WHOIS    AMODE 24
WHOIS    RMODE 24
         STM   14,12,12(13)       SAVE CALLERS REGISTERS
         BALR  12,0               SET CURRENT ADDRESS
         USING *,12               ESTABLISH ADDRESSABILITY
         ST    13,SAVE+4          CHAIN SAVE AREA
         LR    11,13              POINT TO CALLERS SAVE AREA
         LA    13,SAVE            POINT TO OUR SAVE AREA
         ST    13,8(11)           CHAIN SAVE AREA
         SPACE
         USING CPPL,1             ADDRESS CPPL
         L     2,CPPLCBUF         POINT TO COMMAND BUFFER
         LA    3,4(2)             POINT TO START OF INPUT TEXT
         AH    3,2(2)             POINT TO USERID ENTERED
         AH    2,0(2)             POINT TO END OF COMMAND BUFFER
         SR    2,3                POINT TO PARAMETER ENTERED
         CH    2,=H'6'            WAS 6 CHARACTER PARAMTER ENTERED?
         BE    PARMOK             ..YES, CONTINUE
         TPUT  MSG1,L'MSG1        WRITE ERROR MESSGAE
         B     RETURN             RETURN TO CALLER
         EJECT
PARMOK   MVC   USERID(6),0(3)     MOVE PARAMETER TO RACF PARMLIST
         OC    USERID(6),=CL6' '  INSURE UPPER CASE
         L     3,16               POINT TO CVT
         USING CVT,3
         L     3,CVTTCBP          POINT TO TCB AND ASCB POINTERS
         L     3,4(3)             POINT TO CURRENT TCB
         EJECT
AUTHOK   DS    0H
         RACSTAT
         LTR   15,15              IS RACF OPERATING?
         BZ    RACFOK             ..YES, CONTINUE
         TPUT  MSG2,L'MSG2        WRITE ERROR MESSGAE
         B     RETURN             RETURN TO CALLER
         EJECT
RACFOK   ICHEINTY LOCATE,         LOCATE NEXT USERID                   X
               TYPE='USR',ENTRY=ENTRY,WKAREA=WKAREA,                   X
               ACTIONS=(ACTION1)
         LTR   15,15              WAS LOCATE SUCCESSFUL?
         BZ    FOUND              ..YES, CONTINUE
         C     15,=F'12'          WAS LOCATE UNSUCCESSFUL?
         BE    NOTFOUND           ..YES, CONTINUE
         CVD   15,WORKD           CONVERT RETURN CODE TO DECIMAL
         UNPK  ERRORCD,WORKD+6(2) MOVE TO ERROR MESSAGE
         OI    ERRORCD+1,X'F0'    MAKE LAST CHARACTER PRINTABLE
         TPUT  MSG5,L'MSG5        WRITE ERROR MESSAGE
         B     RETURN             RETURN TO CALLER
         EJECT
FOUND    MVC   MSG3,NAME          MOVE TO MESSAGE
         TPUT  MSG3,L'MSG3        WRITE MESSAGE
         B     RETURN             RETURN TO CALLER
NOTFOUND TPUT  MSG4,L'MSG4        WRITE NOFOUND MESSAGE
         B     RETURN             RETURN TO CALLER
         EJECT
RETURN   L     13,SAVE+4          POINT TO CALLERS SAVE AREA
         RETURN (14,12),RC=0      RETURN TO CALLER
         EJECT
SAVE     DS    18F                SAVE AREA
WORKD    DS    D                  DOUBLEWORD WORK AREA
ENTRY    DC    AL1(6)             LENGTH OF USERID FIELD
USERID   DC    XL8'00'            USERID
         DS    0F
WKAREA   DC    AL4(WKAREAL)       LENGTH OF WORKAREA
RBA      DS    CL6                RBA OF RACF RECORD
FLAGS    DS    X                  FLAGS
         DS    C                  RESERVED
DUPCOUNT DS    AL4                DUPLICATE RECORD COUNT
         DS    CL8                RESERVED
RETLEN   DS    AL4                LENGTH OF RETURNED DATA
NAMEL    DS    AL2
NAME     DS    CL20
WKAREAL  EQU   *-WKAREA           CALCULATE WORK AREA LENGTH
MSG1     DC    C'USERID NOT VALID - MUST BE SEVEN CHARACTERS'
MSG2     DC    C'RACF IS NOT OPERATING'
MSG3     DC    CL20' '
MSG4     DC    C'USERID NOT FOUND'
MSG5     DC    C'RACF ERROR, RETURN CODE FROM ICHEINTY MACRO = XX'
         ORG   MSG5+46
ERRORCD  DS    CL2                ERROR RETURN CODE
         ORG
         EJECT
ACTION1  ICHEACTN FIELD=PGMRNAME
         EJECT
         PRINT NOGEN
         IKJCPPL
         CVT DSECT=YES
         IKJTCB
         END
