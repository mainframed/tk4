IKJEFF10 CSECT
*
*  THIS EXIT INSERTS A CONTINUATION OF EACH JOB CARD SUBMITTED BY
*  A RACF DEFINED USER. THE CONTINUATION CARD CONTAINS THE USER ID
*  AND LOGON PASSWORD OF THE PERSION SUBMITTING THE JOB. IF THERE
*  IS NO ROOM TO INSERT A COMMA AND A BLANK THE JOB IS SUBMITTED
*  WITHOUT ADDING A CONTINNUATION CARD AND A MESSAGE IS SENT TO THE
*  USER INFORMING THEM OF THIS. IF THE USER IS NOT RACF DEFINED OR
*  EITHER 'USER' OR 'PASSWORD' KEY WORDS ARE FOUND THEN THE JOB IS
*  PASSED ON ASIS AND NO MESSAGE IS SENT. YOU CANNOT GET SOMEONE
*  ELSES PASSWORD BY USING THIS EXIT AS WRITTEN.
*
*  THIS EXIT IKJEFF10 REPLACES THE IBM VERSION OF IKJEFF10 WHICH IS
*  EFECTIVELY A BR14.  THIS EXIT WORKS WITH OR WITHOUT THE TSO/E OR
*  THE EARLYER TSO COMMAND PACKAGE AS THE DUMMY EXIT IS IN THE BASE
*  TSO CODE.  THIS EXIT WAS DEVELOPED AT THE GEORGIA DEPARTMENT OF
*  LABOR AND HAS BEEN IN USE FOR OVER ONE YEAR WITH NO KNOWN PROBLEMS.
*  WE WILL ATTEMPT TO FIX ERRORS AS LONG AS WE CONTINUE TO USE THIS
*  EXIT, BUT DO NOT PROMISE THAT WE WILL FIX BUGS OR PROVIDE ANY
*  SUPPORT IN THE FUTURE.
*
*  SEND COMMENTS AND ERROR REPORTS TO:
*        SYSTEMS SUPPORT UNIT
*        GEORGIA DEPARTMENT OF LABOR
*        ROOM 370 STATE LABOR BUILDING
*        ATLANTA, GA  30334
*
R0       EQU 0  OS LINKAGE
R1       EQU 1  OS LINKAGE - POINTER TO POINTER TO PARM LIST (IEEXITL)
R2       EQU 2  WORK REGISTER FOR GETMAIN, CVT, USERJCL, AND GENCD
R3       EQU 3  BASE REGISTER FOR ASCB
R4       EQU 4  BASE REGISTER FOR ASXB
R5       EQU 5  BASE REGISTER FOR ACEE
R6       EQU 6  BASE REGISTER FOR TSB
R7       EQU 7  BASE REGISTER FOR IEEXITL
R8       EQU 8  BASE REGISTER FOR IESUBCTD
R9       EQU 9  WORK REGISTER FOR CHANGING USERJCL (BASE FROM IECARDP)
R10      EQU 10 WORK REGISTER FOR USERJCL, MSGTEXT1, AND GENCD
R11      EQU 11 BASE REGISTER FOR GETMAIN AREA (STORED IN IEEXITWD)
R12      EQU 12 BASE REGISTER
R13      EQU 13 SAVE AREA
R14      EQU 14 OS LINKAGE
R15      EQU 15 OS LINKAGE - RETURN CODE FOR CALLING PROGRAM IKJEFF09
*
         USING *,R15
         SAVE  (14,12),,IKJEFF10-SUBMIT-USER-EXIT-&SYSDATE-&SYSTIME
         BALR  R12,R0
         USING *,R12
*
PBASE    EQU   *
         DROP  R15
*
*                                    REGISTERS NOT CHAINED UNTIL AFTER
*                                      GETMAIN
*
         L     R7,0(0,R1)            GET ADDRESS OF PARM LIST (IEEXITL)
         USING IEEXITL,R7
         L     R8,IESUBCTP           GET ADDRESS OF SUBMIT JCL INFO
         USING IESUBCTD,R8
*
         CLC   IEEXITWD,ZEROS        FIRST INVOCATION OF THIS EXIT?
         BE    GETMAIN               YES, INITIALIZE
*
         L     R11,IEEXITWD          NO, POINT TO SAME GETMAIN AREA
         B     CONTINUA              GO AROUND GETMAIN
*
GETMAIN  EQU   *
         L     R2,SIZDATD
         GETMAIN  R,LV=(2),SP=230
         LR    R11,R1                GET ADDRESS OF GETMAIN DSECT
         USING DATD,R11
         ST    R11,IEEXITWD          POINT TO GETMAIN AREA
         MVC   DATD(1),BLANKS
         MVC   DATD+1(ENDDATD-DATD-1),DATD       CLEAR GETMAIN AREA
*
         L     R2,CVTPTR             GET ADDRESS OF CVT (X'10')
         L     R2,0(R2)              GET ADDRESS OF LIST AF ADDRESSES
         L     R3,12(R2)             GET ADDRESS OF CURRENT ASCB
         USING ASCB,R3
         L     R4,ASCBASXB           GET ADDRESS OF CURRENT ASXB
         USING ASXB,R4
         L     R5,ASXBSENV           GET ADDRESS OF CURRENT ACEE
         USING ACEE,R5
         TM    ACEEFLG1,ACEERACF     IS USER RACF DEFINED?
         BZ    TAKEEXOF              NO, NO MORE JOB CARDS TO COME
*                                      TO THIS EXIT FOR THIS SUBMIT
         L     R6,ASCBTSB            GET ADDRESS OF TSB
         LTR   R6,R6                 IS THERE A TSB ADDRESS?
         BZ    TAKEEXOF              NO, CAN'T INSERT CARD
*
         USING TSB,R6
         MODESET KEY=ZERO
         MVC   SAVEPSWD,TSBPSWD      SAVE PASSWORD FROM TSB
         MODESET KEY=NZERO
         CLC   SAVEPSWD,ZEROS        IS THERE A LOGON PASSWORD?
         BE    TAKEEXOF              NO, CAN'T INSERT CARD
*
         MVC   SAVEUSER,ACEEUSER     SAVE PASSWORD FROM TSB
         B     CONTINUA
*
TAKEEXOF EQU   *
         MVI   IETAKEEX,ZEROHEX      TURN OFF TAKE EXIT SWITCH
         B     SETRC0
*
CONTINUA EQU   *
         LA    R1,SAVE
         ST    R13,4(,R1)             BACK CHAIN SAVE AREAS
         ST    R1,8(,R13)             FORWARD CHAIN SAVE AREAS
         LR    R13,R1                 SET R13 TO NEW SAVE AREA
         L     R1,4(R13)              SET R1 TO SAVE AREA AT ENTRY
         L     R1,24(,R1)             RESTORE R1 FROM SAVE AREA
*
         TM    IETAKEEX,IETJOB       TAKE EXIT FOR JOB CARD?
         BZ    SETRC0                SHOULD NOT BE IN THIS EXIT
*
         TM    IESTMTYP,IESJOB       IS STATEMENT JOB CARD?
         BZ    SETRC0                NO, DONT INSERT
*
         CLC   IEMSGP,ZEROS          RETURN FROM SENDING MESSAGE?
         BE    REENT                 NO,
*
         MVC   IEMSGP,ZEROS          YES, CLEAR POINTER TO MESSAGE
         B     SETRC0                THIS CARD ALREADY PROCESSED
*
REENT    EQU   *
         L     R9,IECARDP            GET ADDRESS OF CURRENT
         LTR   R9,R9                 IS EXIT BEING RE-ENTERED?
         BZ    GENCD                 YES, INSERT USER AND PASSWORD CARD
*
OPRAND   EQU   *
         CLI   IEOPRAND,ZEROHEX      OPERAND COLUMN?
         BZ    NOOPRAND              NO OPERANDS ON THIS CARD
*
         SR    R2,R2
         IC    R2,IEOPRAND
         AR    R9,R2                 REGISTER TO POINT TO OPERAND
         BCTR  R9,R0
*
         L     R10,COMMALMT          IN LOOP, LOOK AT 71 COLUMNS
         SR    R10,R2                LESS FIRST PART OF CARD
*
         SR    R2,R2                 CLEAR FOR QUOTE SEARCH
*
COMPQUOT EQU   *
         CLC   0(1,R9),QUOTE         IS IT A QUOTE MARK?
         BNE   CKQUOT                NO,
*
         LTR   R2,R2                 IS IT THE BEGINNING QUOTE?
         BZ    BEGQUOT               YES
*
         SR    R2,R2                 NO, END OF QUOTE, RESET SWITCH
         B     NEXTCOL
*
BEGQUOT  EQU   *
         LA    R2,1                  SET SWITCH ON FOR QUOTE
         B     NEXTCOL               AND GO TO NEXT COLUMN
*
CKQUOT   EQU   *
         LTR   R2,R2                 ARE WE IN A QUOTATION?
         BP    NEXTCOL               YES, DONT LOOK FOR PASSWORD=
*
COMPPSWD EQU   *
         CLC   0(9,R9),PSWDCON       IS IT PASSWORD=?
         BE    SETSW                 YES, DONT INSERT CARD
*
COMPUSER EQU   *
         CLC   0(5,R9),USRCON        IS IT USER=?
         BE    SETSW                 YES, DONT INSERT CARD
*
COMPBLK  EQU   *
         CLC   0(1,R9),BLANKS        END OF OPERANDS?
         BNE   NEXTCOL               NO,
*
         B     CONTOPER              YES
*
NEXTCOL  EQU   *
         LA    R9,1(R9)              TRY NEXT COLUMN
         BCT   R10,COMPQUOT          IF NOT AT END OF CARD, LOOP
*
         B     CONTOPER              YES, IS OPERAND CONTINUED?
*
SETSW    EQU   *
         MVI   SW,ONE                TURN SWITCH ON, DONT NEED CARD
*
CONTOPER EQU   *
         TM    IESTMTYP,IESOPCON     IS OPERAND TO BE CONTINUED?
         BO    SETRC0                YES
*
         CLI   SW,ONE                NEED TO INSERT CARD?
         BE    NOINSERT              NO, GO GET NEXT STATEMENT, IF ANY
*
         CLC   SAVEUSER,BLANKS       HAVE A GOOD USER VALUE?
         BE    SETRC0                NO,
*
         CLC   SAVEPSWD,BLANKS       HAVE A GOOD PSWD VALUE?
         BE    SETRC0                NO,
*
         CLC   0(2,R9),BLANKS        ROOM FOR COMMA AND BLANK?
         BE    MVCOMMA               YES, CAN INSERT CARD
*
         CLC   0(1,R9),BLANKS        ROOM FOR JUST COMMA?
         BE    LASTCOL               MAYBE OK
*
         B     WARNING               CANT INSERT CARD
*
LASTCOL  EQU   *
         C     R10,ZEROS             AT COLUMN 71?
         BE    MVCOMMA               YES, CAN PUT COMMA WITHOUT BLANK
*                                       FOLLOWING
WARNING  EQU   *
         MVC   MSG,MSG1              MOVE MSG TO MSG AREA
         LA    R10,MSG               POINT REGISTER TO MESSAGE AREA
         ST    R10,IEMSGP            GIVE ADDRESS TO CALLER
         LA    R15,IEMSG             RC=8, TELL CALLER TO SEND MESSAGE
         B     RETURN
*
MVCOMMA  EQU   *
         MVI   0(R9),COMMA
*
         MVC   SAVETYP,IESTMTYP      SAVE SWITCHES
         OI    IESTMTYP,IESOPCON     NOW THERE IS A COMMA,
         OI    IESTMTYP,IESSCON        SO SET THE FLAGS
         LA    R15,IERETURN          RC=4, TELL CALLER TO RETURN FOR
         B     RETURN                    INSERTED CARDS
*
NOOPRAND EQU   *
         CLI   SW,ONE                USER OR PASSWORD ALREADY FOUND
         BNE   SETRC0                OR NOT FOUND AND GENERATED
*
NOINSERT EQU   *
         MVC   SW,BLANKS             TURN SWITCH OFF FOR NEXT JOB CARD
*
SETRC0   EQU   *
         LA    R15,IECONTIN          RC=0, TELL CALLER TO COMPLETE
*
RETURN   EQU   *
         L     R13,4(R13)            RESTORE SAVE AREA
         RETURN (14,12),RC=(15)
*
GENCD    EQU   *
         LA    R10,CD               POINT WORK REGISTER TO GETMAIN AREA
         ST    R10,IECARDP          POINT TO INSERTED CARD FOR CALLER
         MVC   CD(1),BLANKS
         MVC   CD+1(ENDCD-CD-1),CD  CLEAR CARD AREA
         MVC   0(19,R10),CDUSER     MOVE USER CONSTANT
         LA    R10,19(R10)
         MVC   0(8,R10),SAVEUSRI    MOVE USER TO CARD
         SR    R2,R2                CLEAR REG FOR LENGTH
         IC    R2,SAVEUSRL
         AR    R10,R2               BUMP PAST USER
         MVC   0(10,R10),CDPSWD     MOVE PASSWORD CONSTANT TO CARD
         LA    R10,10(R10)
         MVC   0(8,R10),SAVEPSWD    MOVE PASSWORD TO CARD
         LA    R10,8(R10)
         MVC   0(26,R10),CDMSG      MOVE COMMENT CONSTANT
*
         MVI   IEOPRAND,FIFTEEN     MOVE OPERAND COLUMN NUMBER
*
         MVC   IESTMTYP,SAVETYP     RESTORE SWITCH
         TM    IESTMTYP,IESSCON     IS THIS LAST CARD?
         BZ    SETCONTN             YES
*
         LR    R10,R11              SET UP WORK REGISTER
         LA    R10,71(R10)          TO COLUMN 72
         MVI   0(R10),NONBLANK      AND MOVE X TO COLUMN 72
*
SETCONTN EQU   *
         OI    IESTMTYP,IESCONTN    SET CONTINUATION FLAG
         B     SETRC0               AND LEAVE
         EJECT
*                        CONSTANTS IN PROGRAM
ZEROS    DC    D'0'
*
COMMALMT DC    F'71'
*
SIZDATD  DC    AL1(0)
         DC    AL3(ENDDATD-DATD)
*
USRCON   DC    CL5'USER='
PSWDCON  DC    CL9'PASSWORD='
BLANKS   DC    CL9'         '
QUOTE    DC    XL1'7D'
*
CDUSER   DC    CL19'//            USER='
CDPSWD   DC    CL10',PASSWORD='
CDMSG    DC    CL26'          GENERATED BY GDL'
*
MSG1     EQU   *
MSGL1    DC    AL2(L'MSGTEXT1+2)
MSGTEXT1 DC    C'NO SPACE FOR COMMA - CAN NOT INSERT USER/PASSWORD'
*
MSGAREAL EQU   L'MSGTEXT1+2          LENGTH OF AREA IN GETMAIN AREA
*
COMMA    EQU   C','
ONE      EQU   C'1'
NONBLANK EQU   C'X'
ZEROHEX  EQU   X'00'
FIFTEEN  EQU   X'0F'
*
         LTORG
*                        CONSTANTS IN GETMAIN AREA
DATD     DSECT
*                        CONSTANTS IN GETMAIN AREA
CD       DS    CL80                       CARD TO BE INSERTED IN JCL
ENDCD    EQU   *
*
SAVE     DS    CL72                       REGISTER SAVE AREA
*
MSG      DS    CL(MSGAREAL)               WARNING MESSAGE TO TSO USER
*
SW       DS    CL1                 SWITCH ON(=1) IF USER OR PASSWORD
*                                     ALREADY ON THE JOB STATEMENT
SAVEUSER DS    0CL9
SAVEUSRL DS    AL1
SAVEUSRI DS    CL8
*
SAVEPSWD DS    CL8
*
SAVETYP  DS    CL1
*
ENDDATD  EQU   *
         EJECT
         CVT      DSECT=YES
         EJECT
         IHAACEE
         EJECT
         IHAASCB
         EJECT
         IHAASXB
         EJECT
         IKJEFFIE IETYPE=SUBMIT
         EJECT
         IKJTSB   LIST=YES
         END   IKJEFF10
