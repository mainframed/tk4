*          DATA SET CBT1428    AT LEVEL 001 AS OF 03/27/83
*    PROGRAM STARTS ABOUT 155 LINES DOWN
         MACRO
&NAME    SAVEALL &RENT,&REG=3,&EQU=YES,&ID=NO
         GBLB  &SAVRENT
         GBLB  &REGSW
         GBLB  &TIME
         GBLC  &SAVNDX
&SAVNDX  SETC  '&SYSNDX'
&NAME    CSECT
         AIF   ('&SAVRENT' EQ '1').NOTFREE
         AIF   ('&REGSW' EQ '1').NOEQU
         AIF   ('&EQU' NE 'YES').NOEQU
&REGSW   SETB  1
         SPACE
***********************************************************************
********           DEFINITION OF GENERAL REGISTERS             ********
***********************************************************************
R0       EQU   0    GENERAL REGISTER ZERO
R1       EQU   1    GENERAL REGISTER ONE
R2       EQU   2    GENERAL REGISTER TWO
R3       EQU   3    GENERAL REGISTER THREE
R4       EQU   4    GENERAL REGISTER FOUR
R5       EQU   5    GENERAL REGISTER FIVE
R6       EQU   6    GENERAL REGISTER SIX
R7       EQU   7    GENERAL REGISTER SEVEN
R8       EQU   8    GENERAL REGISTER EIGHT
R9       EQU   9    GENERAL REGISTER NINE
R10      EQU   10   GENERAL REGISTER TEN
R11      EQU   11   GENERAL REGISTER ELEVEN
R12      EQU   12   GENERAL REGISTER TWELVE
R13      EQU   13   GENERAL REGISTER THIRTEEN
R14      EQU   14   GENERAL REGISTER FOURTEEN
R15      EQU   15   GENERAL REGISTER FIFTEEN
         SPACE
RA       EQU   10   GENERAL REGISTER TEN
RB       EQU   11   GENERAL REGISTER ELEVEN
RC       EQU   12   GENERAL REGISTER TWELVE
RD       EQU   13   GENERAL REGISTER THIRTEEN
RE       EQU   14   GENERAL REGISTER FOURTEEN
RF       EQU   15   GENERAL REGISTER FIFTEEN
***********************************************************************
********             END OF REGISTER DEFINITIONS               ********
***********************************************************************
         SPACE
.NOEQU   ANOP
         SAVE  (14,12)
         AIF   ('&REG' EQ '').MESS
         BALR  &REG(1),0
         AIF   (N'&REG EQ 1).A
         AIF   (N'&REG EQ 2).B
         AIF   (N'&REG EQ 3).C
         AIF   (N'&REG EQ 4).D
         AIF   (N'&REG EQ 5).E
         MNOTE *****,'EXCESSIVE BASE REGISTERS -- 5 ASSUMED *****'
         AGO   .E
.MESS    MNOTE 12,'***** NO BASE REGISTER SPECIFIED *****'
         MEXIT
.NOTFREE MNOTE 12,'***** ENDALL NOT ISSUED PRIOR TO NEW SAVEALL *****'
         MEXIT
.E       USING *,&REG(1),&REG(2),&REG(3),&REG(4),&REG(5)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         L     &REG(5),=A(Q&SYSNDX+16384)
         AGO   .END
.D       USING *,&REG(1),&REG(2),&REG(3),&REG(4)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         AGO   .END
.C       USING *,&REG(1),&REG(2),&REG(3)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         AGO   .END
.B       USING *,&REG(1),&REG(2)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTER
         AGO   .END
.A       USING *,&REG(1)
.END     ANOP
         AIF   ('&RENT' NE 'RENT').NORENT
&SAVRENT SETB  1
         GETMAIN R,LV=4096         GET STORAGE FOR SAVE AREA
         ST    13,4(1)             SET UP FORWARD CHAIN
         ST    1,8(13)             SET UP BACKWARD CHAIN
         L     0,20(0,13)          RESET REGISTER 0
         L     1,24(0,13)          RESET REGISTER 1
         L     13,8(0,13)          LOAD SAVE AREA ADDRESS
         B     J&SYSNDX           BRANCH AROUND LTORG
         AGO   .RENT
.NORENT  ANOP
&TIME    SETB  1
         ST    13,SAVE&SYSNDX+4    SET UP FORWARD CHAIN
         LA    1,SAVE&SYSNDX
         ST    1,8(0,13)           SET UP BACKWARD CHAIN
         L     1,24(0,13)          RESET REGISTER 1
         LA    13,SAVE&SYSNDX
         B     J&SYSNDX           BRANCH AROUND BASE REGISTER CONST
SAVE&SYSNDX DC 18F'0'
.RENT    ANOP
         LTORG
         AIF   ('&ID' NE 'YES').NOID
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
.NOID    ANOP
J&SYSNDX DS    0H
         MEND
         MACRO
&NAME    ENDALL &CODE=
         GBLB  &TIME
         GBLB  &SAVRENT
         GBLC  &SAVNDX
         LCLA  &LEN
         LCLC  &REG
         AIF   ('&CODE' GT '4095').INVRET
&NAME    DS    0H
         AIF   (K'&CODE LE 0).CHKRENT
         AIF   ('&CODE'(1,1) NE '(').CHKRENT
         AIF   ('&CODE' EQ '(13)' OR '&CODE' EQ '(14)').INVREG
         AIF   ('&CODE' EQ '(0)' OR '&CODE' EQ '(1)').INVREG
         AIF   ('&CODE' NE '(15)').CHKRENT
         LR    2,15               SAVE REG 15 RETURN CODE
.CHKRENT AIF   (NOT &SAVRENT).NOTRENT
&SAVRENT SETB  0
         LR    1,13               LOAD SAVE AREA ADDRESS
         L     13,4(0,13)         RE-LOAD REGISTER 13
         FREEMAIN R,LV=4096,A=(1) FREE SAVE AREA STORAGE
         AGO   .RENT
.NOTRENT ANOP
         AIF   (NOT &TIME).NOSAVE
&TIME    SETB  0
         L     13,SAVE&SAVNDX+4   RE-LOAD REGISTER 13
.RENT    ANOP
         AIF   ('&CODE' EQ '').COND1
         AIF   ('&CODE' NE '(15)').CHKREG
         LR    15,2               GET RETURN CODE BACK FROM R2
         AGO   .ENDIT
.CHKREG  AIF   ('&CODE'(1,1) NE '(').NOTREG
&LEN     SETA  K'&CODE-2
&REG     SETC  '&CODE'(2,&LEN)
         LR    15,&REG            GET RETURN CODE
         AGO   .ENDIT
.NOTREG  ANOP
         LA    15,&CODE           LOAD CODE TO REG 15
         AGO   .ENDIT
.INVRET  MNOTE 12,'***** RETURN CODE PASSED GREATER THAN 4095 *****'
         MEXIT
.NOSAVE  MNOTE 12,'***** SAVEALL NOT ISSUED PRIOR TO ENDALL *****'
         MEXIT
.INVREG  MNOTE 12,'***** INVALID REGISTER USED FOR RETURN CODE "&CODE"'
         MEXIT
.COND1   LA    15,0               LOAD ZERO (NO CODE) TO REG 15
.ENDIT   ANOP
         RETURN     (14,12),T,RC=(15)
         MEND
*          DATA SET DEDIOS     AT LEVEL 001 AS OF 03/24/83
*********************************************************************
*********************************************************************
**                                                                 **
**       THIS PROGRAM HAS TO RUN APF AUTHORIZED.                   **
**                                                                 **
**       CONDITIONALLY FIX DSCB VOLSER IN DSCB, OR                 **
**       TURN RACF BITS ON OR OFF.                                 **
**                                                                 **
**       VALID PARMS ARE:                                          **
**                                                                 **
**       PARM='CUA,VOLSER' FIX VOLSER ALL VOLSERS IF WRONG         **
**       PARM='CUA,RACFON' TURN ALL RACF BITS ON                   **
**       PARM='CUA,NORACF' TURN ALL RACF BITS OFF                  **
**                                                                 **
**       * NOTE * WITH PARMS RACFON AND NORACF VSAM WILL NOT       **
**                BE TOUCHED.                                      **
**                                                                 **
**       WHERE CUA = DEVICE ADDRESS OF VOLUME TO BE CHANGED        **
**                                                                 **
**       SAMPLE JCL.                                               **
**       //TEC107   EXEC PGM=TEC107,PARM='100,NORACF'              **
**       //STEPLIB  DD   DSN=AUTHORIZED.LIBRARY,DISP=SHR           **
**       //SYSPRINT DD   SYSOUT=1                                  **
**                                                                 **
**-----------------------------------------------------------------**
** CHANGES:  ENABLED PROGRAM TO RUN IN AN MVS/XA ENVIRONMENT       **
**              09/03/86                                           **
**                                                                 **
**                                                                 **
**                                                                 **
**                                                                 **
*********************************************************************
*********************************************************************
TEC107   SAVEALL REG=(12)
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       CHECK FOR VALID PARM.                                     **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         L     R2,0(R1)                ADDRESS OF PARM FIELD
         LH    R9,0(R2)                LENGTH
         LTR   R9,R9                   IS THERE A PARM?
         BZ    NOPARM                      NO.. GET OUT
         CLC   0(2,R2),=XL2'000A'     LENGTH 10?
         BNE   BADPARM                    NO - BAD PARM
         CLC   6(6,R2),=C'VOLSER'      FIX VOLSER IN DSCB?
         BE    PARM1                      YES.. CONTINUE
         CLC   6(6,R2),=C'RACFON'      TURN ON RACF BIT IN DSCB?
         BE    PARM2                      YES.. CONTINUE
         CLC   6(6,R2),=C'NORACF'      TURN OFF RACF NIT IN DSCB?
         BE    PARM3                      YES.. CONTINUE
         B     BADPARM                    NO... PARM INVALID
         SPACE
PARM1    MVI   FLAG,C'1'               SET FIX DSCB VOLSER
         B     START
PARM2    MVI   FLAG,C'2'               SET RACF BIT ON
         B     START
PARM3    MVI   FLAG,C'3'               SET RACF OFF
         B     START
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       GET DEVICE ADDRESS AND COVERT IT TO HEX FOR IOSLOOK       **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
START    OPEN  (SYSPRINT,(OUTPUT))     OPEN PRINT FILE
         MVC   ADDRWRK(3),2(R2)        GET DEVICE ADDRESS
         TR    ADDRWRK,HEX             CONVERT EACH BYTE TO HEX
         IC    R4,ADDRWRK+1            GET UNIT ADDRESS
         SLL   R4,4                    SHIFT LEFT 4 BITS
         STC   R4,ADDRWRK+1            AND RESTORE
         OC    ADDRWRK+2(1),ADDRWRK+1  'OR' OVER DEVICE ADDRESS
         MVC   ADDRWRK+1(1),ADDRWRK    MOVE CHANNEL ADDRESS RIGHT
         MVI   ADDRWRK,X'00'           ZERO OUT HIGH ORDER BYTE
         SR    R4,R4                   RESET R4
         ICM   R4,7,ADDRWRK            GET DEVICE ADDRESS (HEX)
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       GET INTO SUPERVISOR MODE KEY 0 AND DO AN IOSLOOK TO       **
**       FIND THE UCB ADDRESS FOR CVAF.                            **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
SETSUP   MODESET MODE=SUP,KEY=ZERO     GO TO SUPERVISOR STATE AND KEY 0
         SPACE
IOSLOOK  ST    R13,SAVE13              SAVE MY REG 13
         LA    R13,IOSSAVE             POINT TO IOS SAVE AREA
         IOSLOOK DEV=(4),UCB=(5)       RETURN UCB ADDRESS IN R5
         SPACE
         L     R13,SAVE13              RESTORE REG 13
         MODESET MODE=PROB,KEY=NZERO   RESET TO PROBLEM STATE AND KEY
         LTR   R15,R15                 VALID DEVICE?
         BNZ   BADUCB                     NO...END
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       HAVE A VALID UCB IS IT A BLANK VOLSER?                    **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         USING UCBOB,R5
GOTUCB   CLC   UCBVOLI,=XL6'00'        BLANK VOLSER?
         BNE   UNITTST                   NO...CONTINUE
         WTO   'BLANK UCB',ROUTCDE=(11)  YES..
         L     R15,8                   SET BAD RETURN CODE
         B     EOF                            AND RETURN
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       CHECK TO SEE IF INDEXED OR NON-INDEXED VTOC               **
**       AND BRANCH TO APPROPRIATE ROUTINE.                        **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
UNITTST  CLC   UCBNAME(3),2(R2)        RIGHT UCB?
         BE    CVAFTST
         ABEND 1,DUMP
         SPACE
CVAFTST  MVC   PVOL,UCBVOLI            PUT VOLSER IN PRINT LINE
         MVC   PUNIT(3),2(R2)          PUT UNIT IN PRINT LINE
         MVC   PPARM(6),6(R2)          PUT PARM IN PRINT LINE
         MVC   VOLUME,UCBVOLI          GET VOLSER FOR ENQ/DEQ
         CVAFTST UCB=(R5)              INDEXED VTOC?
         SPACE
         B     TSTRTN(R15)             BRANCH TO APPROPRIATE ROUTINE
TSTRTN   DS    0H
         B     NOINDEX                 RC 0 - NO DF/DS UNINDEXED
         B     NOINDEX                 RC 4 - NO INDEXED VTOC
         B     INDEXED                 RC 8 - INDEXED VTOC
         B     NOMOUNT                 RC 12- VOL NOT MOUNTED
         B     NODASD                  RC 16- NOT DASD
         ABEND 16                      JUST IN CASE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       SET UP BUFFER TO READ FIRST DSCB                          **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
INDEXED  MVI   IXVTOC,C'1'             SET THIS IS INDEXED VTOC
         SPACE
NOINDEX  BAL   R10,NEWPAGE             AND START A NEW PAGE
         XC    BUFLIST(BFLHLN),BUFLIST CLEAR BUFFER LIST
         XC    BFLE(BFLELN),BFLE       CLEAR BUFFER ENTRY
         MVI   BFLHNOE,X'1'            NUMBER OF DSCBS TO BE READ = 1
         OI    BFLHFL,BFLHDSCB         SET DSCBS AS ARGUMENT
         OI    BFLEFL,BFLECHR          ARGUMENT IS CCHHR
         MVI   BFLELTH,DSCBLTH         LENTGH OF DSCB
         XC    BFLEARG,BFLEARG         SET TO READ 1ST DSCB
         LA    R3,DS1DSNAM             ADDR OF DSCB BUFFER
         ST    R3,BFLEBUF              PLC ADDR OF DSCB BUFFER IN BFLE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       READ THE DSCBS AND CHECK RETURN CODE.                     **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
SEQLOOP  CVAFSEQ BRANCH=YES,MF=(E,CVPL),UCB=(R5)  GET A DSCB
         SPACE
         LTR   R15,R15                 ANY ERROR?
         BZ    CKFMT1                     NO....CHECK FOR FORMAT 1
         C     R15,=F'4'               RETURN CODE 4?
         SPACE
         BH    ABEND12                   NO...ABEND (RC=R15 AND CVSTAT)
         CLI   CVSTAT,X'20'            END OF VTOC?
         SPACE
         BNE   ABEND12                   NO...ABEND (RC=R15 AND CVSTAT)
         SR    R15,R15                   GIVE ZERO RETURN CODE
         B     EOF                       YES..EOJ
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       IS IT A FORMAT 1 DSCB?                                    **
**       INITIALIZE PRINT LINE.                                    **
**       GO READ NEXT DSCB IF NOT FORMAT 1.                        **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
CKFMT1   CLC   DS1FMTID(1),FORMAT1     FORMAT 1 DSCB?
         BNE   SEQLOOP                    NO....GET ANOTHER DSCB
         MVC   DSNAME(44),DS1DSNAM     MOVE DSN TO SYSPRINT
         MVC   VOLSER(6),DS1DSSN       MOVE VOLSER TO SYSPRINT
         MVC   STAT(2),=C'  '          SET DEFAULT NOT CHANGED
         MVC   VCHG(11),=C'NOT CHANGED' SET DEFAULT VOLSER NOT CHANGED
         MVC   RCHG(11),=C'NOT CHANGED' SET DEFAULT RACF   NOT CHANGED
         CLI   FLAG,C'1'               DOING VOLSERS ONLY?
         BE    CKDSCBV                    YES... GO CHECK DSCBS
         CLI   FLAG,C'2'               DOING RACF ON ONLY?
         BE    RACON                       YES... GO CHECK IT OUT
         CLI   FLAG,C'3'               DOING RACF OFF ONLY?
         BE    RACOFF                     YES... GO CHECK IT OUT
         ABEND 8                       JUST IN CASE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       HERE WE ARE TURNING THE RACF INDICATOR OFF IF IT IS ON    **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
RACOFF   TM    DS1DSORG+1,X'08'        VSAM?
         BO    PRINT                      YES.. DONT TOUCH
         TM    DS1DSIND,X'40'          IS RACF BIT ON?
         BNO   PRINT                      NO... JUST PRINT
         NI    DS1DSIND,X'BF'   TURN RACF BIT OFF, PRESERVE OTHER INFO
         MVC   RCHG(11),=C'CHANGED    ' SAY WE CHANGED THE VOLSER
         MVC   STAT(2),=C'**'          INDICATE CHANGED
         B     PRINTW                  BRANCH TO PRINT AND REWRITE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       HERE WE ARE TURNING THE RACF INDICATOR ON IF IT IS OFF    **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
RACON    TM    DS1DSORG+1,X'08'        VSAM?
         BO    PRINT                      YES.. DONT TOUCH
         TM    DS1DSIND,X'40'          IS RACF BIT ON?
         BO    PRINT                      YES.. JUST PRINT
         OI    DS1DSIND,X'40'          TURN RACF BIT ON
         MVC   RCHG(11),=C'CHANGED    ' SAY WE CHANGED THE VOLSER
         MVC   STAT(2),=C'**'          INDICATE CHANGED
         B     PRINTW                  BRANCH TO PRINT AND REWRITE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       HERE WE FIXING THE DSCB VOLSER IF IT IS INCORRECT         **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
CKDSCBV  CLC   UCBVOLI,DS1DSSN         VOLSER MATCH?
         BNE   CHGV                       NO... BAD VOLSER, GO FIX IT
         B     PRINT                   PRINT AND GET ANOTHER DSCB
CHGV     MVC   DS1DSSN(6),UCBVOLI      MOVE IN CORRECT VOLSER
         MVC   VCHG(11),=C'CHANGED    ' SAY WE CHANGED THE VOLSER
         MVC   STAT(2),=C'**'          INDICATE VOLSER WAS CHANGED
         B     PRINTW                  GO PRINT AND REWRITE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       REWRITE THE DSCB IF WE HAVE CHANGED SOMETHING             **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
REWRITE  MVI   BADSW,C'1'              WE HAVE CHANGED SOMETHING
         USING CVPL,R1
         XR    R9,R9                   ZERO OUT REGISTER 9
         LA    R9,DS1DSNAM             LOADING THE DSN OF BAD DSCB
         XR    R8,R8                   ZERO OUT REGISTER 8
         LA    R8,BUFLIST              LOADING THE ADDRESS OF BUFLIST
         EJECT
         ENQ   (QNAME,VOLUME,E,6,SYSTEM)  ENQ THE VTOC
         SPACE 3
*        RESERVE (CNBVOL,RVOLUME,E,0,SYSTEMS),UCB=(R5) RESERVE IT
         EJECT
         CVAFDIR ACCESS=WRITE,DSN=(R9),BUFLIST=(R8),                   X
               BRANCH=NO,UCB=(R5),IOAREA=NOKEEP,MAPRCDS=NO
         EJECT
         DEQ   (QNAME,VOLUME,6,SYSTEM)    DEQ THE VTOC
         SPACE 3
*        DEQ   (CNBVOL,VOLUME,6,SYSTEMS),UCB=(R5) RELEASE THE VTOC
         SPACE
         LTR   R15,R15
         BNZ   CHECKDIR
         B     SEQLOOP
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       PRINT LOOP IF REWRITING THE DSCB                          **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
PRINTW   CP    LINECTR,=PL2'53'        53 LINES?
         BNE   CONT1                       NO...CONTINUE
         BAL   R10,NEWPAGE                 YES..GO DO NEW PAGE
         SPACE
CONT1    AP    LINECTR,ONE             ADD ONE TO LINE COUNTER
         PUT   SYSPRINT,PRINTLNE       PRINT THE LINE
         B     REWRITE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       PRINT LOOP IF NOT REWRITING THE DSCB                      **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
PRINT    CP    LINECTR,=PL2'53'        FULL PAGE?
         BNE   CONT2                      NO... CONTINUE
         BAL   R10,NEWPAGE                YES.. NEW PAGE
         SPACE
CONT2    AP    LINECTR,ONE             ADD ONE TO LINE COUNTER
         PUT   SYSPRINT,PRINTLNE       PRINT THE LINE
         B     SEQLOOP                 GO GET NEXT DSCB
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       HAVE AND INVALID UCB.                                     **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
BADUCB   WTO   'INVALID UCB',ROUTCDE=(11) NO...INDICATE INVALID UCB
         L     R15,12                  SET BAD RETURN CODE
         B     EOF                             AND RETURN
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       NO PARM SPECIFIED                                         **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
NOPARM   WTO   'NO PARM SPECIFIED',ROUTCDE=(11)     YES..
         L     R15,4                   SET BAD RETURN CODE
         B     EOJ                            AND RETURN
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**      BAD PARM SPECIFIED                                         **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
BADPARM  WTO   'INVALID PARM SPECIFIED',ROUTCDE=(11)     YES..
         L     R15,4                   SET BAD RETURN CODE
         B     EOJ                            AND RETURN
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       ERROR VOLUME NOT MOUNTED.                                 **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
NOMOUNT  WTO   'VOLUME NOT MOUNTED',ROUTCDE=(11)
         L     R15,8                              SET BAD RETURN CODE
         B     EOF                                AND GO TO EOJ
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       ERROR FROM DSCB WRITE (CVAFDIR)                           **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
CHECKDIR DS    0H
         WTO   'ERROR FROM CVAFDIR',ROUTCDE=(11)
         ABEND 10,DUMP                            ABEND
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       ERROR VOLUME NOT DASD.                                    **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
NODASD   WTO   'VOLUME NOT DASD',ROUTCDE=(11)
         L     R15,8                              SET BAD RETURN CODE
         B     EOF                                AND GO TO EOJ
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       EOJ PROCESSING.                                           **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
EOF      CLI   BADSW,C'1'              ANY BAD DSCB'S?
         BE    ENDIT                      YES..EOJ
         BAL   R10,NEWPAGE                NO...GO DO NEW PAGE
         PUT   SYSPRINT,BLANKS                 PRINT BLANK LINE
         PUT   SYSPRINT,NOBAD                  WRITE...NO UPDATES
ENDIT    CLOSE SYSPRINT                CLOSE PRINT FILE
EOJ      ENDALL CODE=(15)              RETURN TO MVS WITH RETURN CODE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       NEW PAGE ROUTINE.                                         **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
NEWPAGE  AP    PAGECTR,ONE             ADD ONE TO PAGE COUNTER
         MVC   PAGE,PATTERN            MOVE IN PAGE NUMBER PATTERN
         ED    PAGE,PAGECTR            EDIT THE PAGE NUMBER IN
         PUT   SYSPRINT,HEADER         PRINT HEADER
         PUT   SYSPRINT,BLANKS         PRINT BLANK LINE
         PUT   SYSPRINT,HEADER2        PRINT HEADER2
         PUT   SYSPRINT,UNDERL         UNDERLINE IT
         PUT   SYSPRINT,BLANKS         PRINT BLANK LINE
         SP    LINECTR,LINECTR         CLEAR LINE COUNTER
         BR    R10
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       SYSPRINT DCB                                              **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=133,MACRF=PM,RECFM=FBA
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       NO ERRORS FOUND LINE.                                     **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
NOBAD    DS   0CL133
         DC   C' '
         DC   CL132'NO DSCB''S CHANGED'
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       HEADER LINE.                                              **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
HEADER   DS   0CL133
         DC   C'1'
         DC   CL15' '
         DC   C'STATUS OF DSCB CHANGES FOR VOLUME '
PVOL     DS   CL6
         DC   C' ON UNIT '
PUNIT    DS   CL3
         DC   C' '
         DC   C'USING PARM VALUE - '
PPARM    DS   CL6
         DC   CL15' '
         DC   C'PAGE '
PAGE     DS   CL2
         DC   CL20' '
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       HEADER LINE 2                                             **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
HEADER2  DS   0CL133
         DC   C' '
         DC   CL2' '
         DC   C' '
         DC   CL44'DATASET NAME'
         DC   CL2' '
         DC   CL6'VOLSER'
         DC   CL4' '
         DC   C'VOLSER CHANGE'
         DC   CL6' '
         DC   C'RACF CHANGE'
         DC   CL80' '
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       UNDER LINE HEADER 2                                       **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
UNDERL   DS   0CL133
         DC   C'+'
         DC   CL2' '
         DC   C' '
         DC   44C'_'
         DC   CL2' '
         DC   6C'_'
         DC   CL4' '
         DC   13C'_'
         DC   CL6' '
         DC   11C'_'
         DC   CL80' '
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       PRINT  LINE.                                              **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
PRINTLNE DS   0CL133
CC       DC   C' '
STAT     DS   CL2
         DC   C' '
DSNAME   DS   CL44
         DC   CL2' '
VOLSER   DS   CL6
         DC   CL4' '
VCHG     DS   CL11
         DC   CL8' '
RCHG     DS   CL11
         DC   CL80' '
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       STORAGE AND CONSTANTS.                                    **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         DS  0F
QNAME    DC   CL8'SYSVTOC '
         DS  0F
CNBVOL   DC   CL8'CNBVOL  '
FLAG     DS   CL1
         DS  0F
RVOLUME  DS  0CL7
         DC   X'06'
VOLUME   DS   CL6
PAGECTR  DC   PL1'0'
LINECTR  DC   PL2'0'
ONE      DC   PL1'1'
PATTERN  DC   XL2'4021'
         EJECT
ABEND12  ABEND 12,DUMP
LISTSTRT DC    F'0'                    ADDR O FCCHHR TO START SEARCH
LISTPRMS EQU   *
LISTBUF  DC    F'0'
LISTCHR  DS    0F
LISTLAST DC    X'00'
LASTBIT  EQU   X'80'
         DC    AL3(0)
LISTNEXT EQU   *
         EJECT
BADSW    DS    CL1
IXVTOC   DS    CL1
BLANKS   DC    CL133' '
FORMAT1  DC    C'1'                    FORMAT 1 DSCB
SAVE13   DC    F'0'                    IOSLOOK SAVE AREA
IOSSAVE  DC    16F'0'                  IOSLOOK SAVE AREA
ADDRWRK  DC    XL3'000000'             DEVICE ADDRESS WORK AREA
HEX      DC    CL255' '                HEX TRANSLATE TABLE
         ORG   HEX+C'0'
         DC    X'00010203040506070809' DIGITS 0-9
         ORG   HEX+C'A'
         DC    X'0A0B0C0D0E0F'         CHARS A-F
         ORG
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       LITERALS                                                  **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         LTORG
         SPACE
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       CVAF MAPPING MACRO                                        **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         LTORG
BUFLIST  ICVAFBFL DSECT=NO
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       FORMAT 1 DSCB MAPPING MACRO                               **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         IECSDSL1  (1)
DSCBLTH  EQU   *-IECSDSL1              LENGTH OF DSCB
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       CVAFSEQ (NON-INDEXED VTOC)                                **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
CVPL     CVAFSEQ ACCESS=GT,BRANCH=(YES,PGM),BUFLIST=BUFLIST,MF=L
         ORG   CVPL
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       CVPL MAPPING MACRO                                        **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
CVPLMAP  ICVAFPL  DSECT=NO
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       CVT  MAPPING MACRO                                        **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         CVT   DSECT=YES
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**    IOSLOOK MAPPING MACRO                                        **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         IECDIOCM
         EJECT
*********************************************************************
*********************************************************************
**                                                                 **
**       UCB  MAPPING MACRO                                        **
**                                                                 **
*********************************************************************
*********************************************************************
         SPACE
         IEFUCBOB LIST=YES
         END
