 /*  LISTRP -  L I S T  R A C F  P R O T E C T I O N                  */
 /*********************************************************************/
 /*                                                                   */
 /* PROGRAM NAME: LISTRP   (LIST RACF PROTECTION)                     */
 /*               UTILITY PROGRAM TO COMPARE OUTPUT FROM A LISTCAT AND*/
 /*               THE OUTPUT FROM A RACF SEARCH COMMAND TO DETERMINE  */
 /*               WHICH RACF PROFILES PROTECT WHICH CATALOGED DATASETS*/
 /*               (NONVSAM, CLUSTERS, AND DATA & INDEX COMPONENTS).   */
 /*               THIS PROGRAM CONVERTED (ALMOST) LINE FOR LINE FROM  */
 /*               THE CLIST LISTRP IN 'SYS1.SAMPLIB(RACINSTL)'.       */
 /*                                                                   */
 /* AUTHOR:       STUART SABEL, BLUE CROSS OF WASHINGTON & ALASKA 4/85*/
 /*                                                                   */
 /* INPUT DATA:   DDNAME - RACFIN, DATASET - &SYSUID.EXEC.RACF.CLIST  */
 /*               THIS DATASET CONTAINS THE OUTPUT FROM THE COMMAND   */
 /*                  SEARCH MASK(&PREFIX) CLIST NOLIST                */
 /*                                                                   */
 /*               DDNAME - LISTCAT, DCB=(LRECL=125,RECFM=VBA)         */
 /*               THIS DATASET CONTAINS THE OUTPUT FROM THE FOLLOWING */
 /*               LISTCAT COMMAND:                                    */
 /*                  LISTCAT LEVEL(&PREFIX) OFILE(LISTCAT)            */
 /*                                                                   */
 /*                                                                   */
 /* OUTPUT DATA:  THE OUTPUT FROM THIS PROGRAM IS A REPORT OF WHICH   */
 /*               RACF PROFILES PROTECT WHICH CATALOGED DATASETS.     */
 /*               DDNAME - SYSPRINT                                   */
 /*                                                                   */
 /*                                                                   */
 /* NOTES:        ARRAYS FOR RACF RECORDS AND LISTCAT RECORDS ARE     */
 /*               SET UP FOR A MAXIMUM OF 1000 RECORDS EACH.          */
 /*                                                                   */
 /*               THIS PROGRAM MAY BE EXECUTED FROM EITHER A CLIST,   */
 /*               OR IN A BATCH JOB. THE PROGRAM EXECUTES QUICKLY;    */
 /*               THE PROBLEM WITH EXECUTING IT FROM A CLIST IS THAT  */
 /*               THE SEARCH COMMAND, OR LISTC COMMAND TO GET THE     */
 /*               NECESSARY DATA MAY TAKE A LONG TIME...              */
 /*                                                                   */
 /*********************************************************************/
 LISTRP: PROC OPTIONS(MAIN);                   /* MAIN PROCEDURE START*/
 DCL RACFIN   FILE;                            /* SEARCH CMD OUTPUT.  */
 DCL LISTCAT  FILE;                            /* LISTCAT OUTPUT.     */
 DCL SYSPRINT FILE;                            /* SYSOUT MESSAGES.    */
 DCL INDEX    BUILTIN;                         /* INDEX FUNCTION.     */
 DCL LENGTH   BUILTIN;                         /* LENGTH FUNCTION.    */
 DCL STRING   BUILTIN;                         /* STRING FUNCTION.    */
 DCL SUBSTR   BUILTIN;                         /* SUBSTRING FUNCTION. */
 DCL RCOUNT1  FIXED       BIN(15)     INIT(0); /* RACFIN READ COUNT.  */
 DCL RCOUNT2  FIXED       BIN(15)     INIT(0); /* LISTCAT READ COUNT. */
 DCL LEN      FIXED       BIN(31)     INIT(0); /* STRING LENGTH VAR.  */
 DCL RACIN    CHAR(255)   VARYING     INIT((255) ' '); /* RACF REC.   */
 DCL CATIN    CHAR(125)   VARYING     INIT((125) ' '); /* LISTC REC.  */
 DCL STRPOS   FIXED       BIN(31)     INIT(0); /* CHAR. POSITION VAR. */
 DCL RTOT     FIXED       BIN(31)     INIT(1); /* TOT # OF RACF DSNS  */
 DCL CTOT     FIXED       BIN(31)     INIT(1); /* TOT # OF LISTC DSNS */
 DCL RACREC(1000) CHAR(53) VARYING    INIT((1000) (53) ' '); /* ARRAY */
 DCL CATREC(1000) CHAR(61) VARYING    INIT((1000) (61) ' '); /* ARRAY */
 DCL PREFIX   CHAR(08)    VARYING     INIT((8)' '); /* GRAB HLQ.      */
 DCL PROFILE  CHAR(44)    VARYING     INIT((44)' '); /* MATCH PROFILE.*/
 DCL DATASET  CHAR(44)    VARYING     INIT((44)' '); /* MATCH DSNAME. */
 DCL DSN_COL  FIXED       BIN(31)     INIT(0); /* COLUMN POINTER.     */
 DCL PROF_COL FIXED       BIN(31)     INIT(0); /* COLUMN POINTER.     */
 DCL PSEUDO   CHAR(44)    VARYING     INIT((44)' '); /* PSEUDO DSNAME */
 DCL RUNNER   FIXED       BIN(31)     INIT(0); /* ARRAY COUNTER. RACF */
 DCL RUNNER2  FIXED       BIN(31)     INIT(0); /* ARRAY COUNTER. CATLG*/
 DCL COUNT    FIXED       BIN(31)     INIT(0); /* COLUMN COUNTER.     */
 DCL COUNT2   FIXED       BIN(31)     INIT(0); /* COLUMN COUNTER.     */
 DCL MATCHDS  CHAR(03)    VARYING     INIT((03)' '); /* MATCH FLAG.   */
 DCL PROF_LEN FIXED       BIN(31)     INIT(0); /* PROFILE LENGTH CNTR.*/
 DCL DSN_LEN  FIXED       BIN(31)     INIT(0); /* DSNAME LENGTH CNTR. */
 DCL START    FIXED       BIN(31)     INIT(0); /* COL. SCAN START COL.*/
 DCL DCR      FIXED       BIN(31)     INIT(0); /* DSN COLUMN POINTER. */
 DCL PLEN     FIXED       BIN(31)     INIT(0); /* PSEUDO NAME LENGTH. */
 DCL FTIME    CHAR(03)    VARYING     INIT((03)' '); /* PROCESS FLAG. */
 DCL DATS     CHAR(44)    VARYING     INIT((44)' '); /* DS SCAN VAR.  */
 /*********************************************************************/
 /* SET UP VARIABLES WITH CURRENT DATE AND TIME.                      */
 /*********************************************************************/
 DCL DATE     BUILTIN;
 DCL TIME     BUILTIN;
 DCL SL            CHAR(01)   INIT('/');
 DCL CO            CHAR(01)   INIT(':');
 DCL CURRDATE      CHAR(08)   INIT((08)' ');
 DCL CURRTIME      CHAR(08)   INIT((08)' ');
 DCL 1 DATEIN,
       5 YEAR      CHAR(02)   INIT('  '),
       5 MONTH     CHAR(02)   INIT('  '),
       5 DAY       CHAR(02)   INIT('  ');
 DCL 1 TIMEIN,
       5 HOURS     CHAR(02)   INIT('  '),
       5 MINUTES   CHAR(02)   INIT('  '),
       5 SECONDS   CHAR(02)   INIT('  '),
       5 MILLIES   CHAR(03)   INIT('  ');
 STRING(DATEIN) = DATE;                        /* PULL IN CURR DATE. */
 STRING(TIMEIN) = TIME;                        /* PULL IN CURR TIME. */
 CURRDATE = MONTH ×× SL ×× DAY ×× SL ×× YEAR ;       /* SET UP DATE. */
 CURRTIME = HOURS ×× CO ×× MINUTES ×× CO ×× SECONDS ;/* SET UP TIME. */
 /*********************************************************************/
 /* WHAT TO DO WITH END OF FILE CONDITIONS...                         */
 /*********************************************************************/
 ON ENDFILE(RACFIN) GOTO READ2;                /* AT EOF, READ NEXT.  */
 ON ENDFILE(LISTCAT) GOTO GETPREF;             /* AT EOF, PROCESS EM. */

 /*********************************************************************/
 /* SET UP REPORT HEADINGS.                                           */
 /*********************************************************************/
 PUT SKIP EDIT ('BLUE CROSS OF WA/AK   -   LIST RACF PROTECTIO',
                'N UTILITY ') (A,A);           /* SET UP HEADINGS     */
 PUT SKIP EDIT (CURRDATE,'                                      ',
                CURRTIME) (A,A,A);             /* WRITE DATE AND TIME */
 PUT SKIP EDIT                                 /* WRITE TOTAL LINE.   */
       ('------------------------------------------------------')
            (A);                               /*                     */
 PUT SKIP EDIT (' ') (A);                      /* WRITE BLANK LINE.   */
                                               /*                     */
 /*********************************************************************/
 /* READ RACF SEARCH COMMAND DATA INTO THE RACIN ARRAY.               */
 /*********************************************************************/
 READ1: READ FILE(RACFIN) INTO(RACIN);         /* READ 'RACFIN'.      */
    RCOUNT1 = RCOUNT1 + 1;                     /* COUNT THE READ.     */
    LEN = LENGTH(RACIN);                       /* FIND LENGTH.        */
    RACREC(RTOT) = SUBSTR(RACIN,10,LEN-10);    /* GRAB DSN, NO QUOTES */
    RTOT = RTOT + 1;                           /* INCREMENT COUNTER.  */
    GOTO READ1;                                /* READ THE NEXT ONE.  */
 /*********************************************************************/
 /* READ LISTCAT COMMAND DATA INTO THE CATIN ARRAY.                   */
 /*********************************************************************/
 READ2: READ FILE(LISTCAT) INTO(CATIN);        /* READ 'LISTCAT'      */
    IF SUBSTR(CATIN,02,07) = 'NONVSAM'         /* TAKE AS VALID -     */
         THEN GOTO GOTIT;                      /* NONVSAM DATASETS    */
    IF SUBSTR(CATIN,02,07) = 'CLUSTER'         /* TAKE AS VALID -     */
         THEN GOTO GOTIT;                      /* VSAM CLUSTERS.      */
    IF SUBSTR(CATIN,02,05) = 'INDEX'           /* TAKE AS VALID -     */
         THEN GOTO GOTIT;                      /* VSAM INDEX COMPONENT*/
    IF SUBSTR(CATIN,02,04) = 'DATA'            /* TAKE AS VALID -     */
         THEN GOTO GOTIT;                      /* VSAM DATA COMPONENTS*/
    GOTO READ2;                                /* IF NOT, READ AGAIN. */
    GOTIT:
    LEN = LENGTH(CATIN);                       /* FIND LENGTH.        */
    CATREC(CTOT) = SUBSTR(CATIN,18,LEN-17);    /* GRAB DSNAME ONLY.   */
    RCOUNT2 = RCOUNT2 + 1;                     /* COUNT THE READ.     */
    CTOT = CTOT + 1;                           /* INCREMENT COUNTER.  */
    GOTO READ2;                                /* READ THE NEXT ONE.  */

 /*********************************************************************/
 /* FIRST, GET THE PREFIX OF THE USER/GROUP TO BE PROCESSED.          */
 /*********************************************************************/
 GETPREF:
      STRPOS = INDEX(RACREC(1),'.');           /* LOOK FOR FIRST DOT. */
      PREFIX = SUBSTR(RACREC(1),01,STRPOS-1);  /* GRAB PREFIX.        */
 /*********************************************************************/
 /* CLIST DATA STARTS HERE.                                           */
 /*********************************************************************/
  COUNT = RTOT;                                /* SET COUNT TO RTOT.  */
 COUNT2 = CTOT;                                /* SET COUNT2 TO CTOT. */
 START = LENGTH(PREFIX) + 2;                   /* GET STARTING COLUMN.*/
 RUNNER = 1 ;                                  /* SET ARRAY COUNTER.  */
 DO WHILE (RUNNER <= COUNT);                   /* PROCESS ALL PROFILES*/
   MATCHDS = 'NO';                             /* MATCH INDICATOR = NO*/
   PROFILE = RACREC(RUNNER);                   /* SET UP PROFILE.     */
   PUT SKIP EDIT (' ') (A);                    /* WRITE A BLANK LINE. */
   PUT SKIP EDIT ('     PROFILE ===> ',PROFILE) (A,A);
   PUT SKIP EDIT
        ('  PROTECTS THE FOLLOWING CATALOGED DATA SET(S)') (A);
   PROF_LEN = LENGTH(PROFILE);                 /* GET PROFILE LENGTH. */
   RUNNER2 = 1;                                /* SET ARRAY COUNTER.  */
  DO WHILE (RUNNER2 <= COUNT2);                /* PROCESS ALL DATASETS*/
   DATASET = CATREC(RUNNER2);                  /* SET UP DSNAME.      */
   IF DATASET = '9' THEN GOTO BYPASSDS;        /* BYPASSED PROCESSED. */
   DSN_LEN = LENGTH(DATASET);                  /* SET SCAN LIMIT.     */
   DSN_COL = START;                            /* DSN COL SCAN VALUE. */
   PROF_COL = START;                           /* PROF COL SCAN VALUE.*/
   PSEUDO = PREFIX ×× '.' ;                    /* SET UP SCAN PROFILE.*/
   DO WHILE (PROF_COL <= PROF_LEN);            /* IF WHOLE PROF^SCANND*/
 /*********************************************************************/
 /* IF A PERCENT '%' SIGN IS FOUND, PROCESS IT.                       */
 /*********************************************************************/
    IF SUBSTR(PROFILE,PROF_COL,1) = '%' THEN DO; /* IF PERCENT, THEN. */
      IF DSN_COL <= DSN_LEN THEN               /* GET NEXT CHARACTER. */
         PSEUDO = PSEUDO ×× SUBSTR(DATASET,DSN_COL,1);
      PROF_COL = PROF_COL + 1;                 /* INCR. COL CNTR BY 1.*/
      DSN_COL = DSN_COL + 1;                   /* INCR. COL CNTR BY 1.*/
      GOTO ENDMAIN;                            /*                     */
     END;
 /*********************************************************************/
 /* IF AN ASTERISK IS FOUND, AND THE PROFILE IS THE SAME LENGTH AS    */
 /* THE DATASET NAME, THEN PROCESS IT HERE.                           */
 /*********************************************************************/
    IF SUBSTR(PROFILE,PROF_COL,1) = '*' &      /* LOOK FOR GENERIC    */
         PROF_COL = PROF_LEN THEN DO;          /* CHARS. IF SO THEN...*/
      IF DSN_COL <= DSN_LEN THEN               /* IF SO, GRAB INFO.   */
           PSEUDO = PSEUDO ×× SUBSTR(DATASET,DSN_COL,DSN_LEN-DSN_COL+1);
      DSN_COL = DSN_LEN;                       /* RESET DSN_COL.      */
      PROF_COL = PROF_COL + 1;                 /* ADD ONE TO PROF_COL.*/
      GOTO ENDMAIN;                            /*                     */
     END;
 /*********************************************************************/
 /* IF AN ASTERISK IS FOUND, AND THE PROFILE IS SHORTER IN LENGTH     */
 /* THAN THE DATASET NAME, PROCESS IT HERE.                           */
 /*********************************************************************/
    IF SUBSTR(PROFILE,PROF_COL,1) = '*' & PROF_COL < PROF_LEN  THEN DO;
      IF (DSN_COL <= DSN_LEN) THEN DO;         /*                     */
          DCR = DSN_COL;                       /* CURR DS RANGE TO DSN*/
          DO WHILE (SUBSTR(DATASET,DCR,1) ^= '.' & DCR < DSN_LEN);
            DCR = DCR + 1;
          END;
          PSEUDO = PSEUDO ×× SUBSTR(DATASET,DSN_COL,DCR-DSN_COL);
        END;
      PROF_COL = PROF_COL + 1;                 /* INCR COLUMN COUNTER.*/
      DSN_COL = DCR;                           /* RESET DSN POINTER.  */
      GOTO ENDMAIN;                            /*                     */
     END;                                      /*                     */
    PSEUDO = PSEUDO ×× SUBSTR(PROFILE,PROF_COL,1);
    PROF_COL = PROF_COL + 1;                   /* INCR COLUMN COUNTER.*/
    DSN_COL = DSN_COL + 1;                     /* INCR COLUMN COUNTER.*/
 /*********************************************************************/
 /* CHECK TO SEE IF THE PSEUDO NAME AND THE DATASET NAME MATCH. IF    */
 /* NOT, THEN TRY AGAIN.                                              */
 /*********************************************************************/
   ENDMAIN:
    PLEN = LENGTH(PSEUDO);                     /* SAVE LEN. OF PSEUDO.*/
    IF PLEN > DSN_LEN THEN PLEN = DSN_LEN;     /* RESET PLEN IF >     */
    IF PSEUDO ^= SUBSTR(DATASET,1,PLEN) THEN   /* IF NO MATCH, RESET  */
         PROF_COL = PROF_LEN + 1;              /* PROF_COL.           */
   END;
 /*********************************************************************/
 /* IF THE PSEUDO AND THE DATASET NAME MATCH, WRITE IT OUT HERE.      */
 /*********************************************************************/
   IF PSEUDO = DATASET THEN DO;                /* IF A MATCH, THEN....*/
        PUT SKIP EDIT                          /* WRITE OUT THE DATA- */
             ('DATASET ===> ',DATASET) (A,A);  /* SET NAME.           */
        CATREC(RUNNER2) = '9';                 /* MARK DSN AS MATCHED.*/
        MATCHDS = 'YES';                       /* MATCHED FLAG=YES.   */
    END;
 /*********************************************************************/
 /* THIS LABEL IS FOR BYPASSING INVALID DATASET NAME, AND ADVANCING   */
 /* THE ARRAY COUNTER FOR DATASETS.                                   */
 /*********************************************************************/
   BYPASSDS:
    RUNNER2 = RUNNER2 + 1;                     /* ADVANCE TO NEXT DSN.*/
   END;
 /*********************************************************************/
 /* IF NO MATCHING DATASET NAMES ARE FOUND, WRITE OUT A MESSAGE HERE. */
 /*********************************************************************/
 IF MATCHDS = 'NO' THEN DO;                    /* IF NO MATCH TO PROF,*/
    PUT SKIP EDIT                              /* WRITE NONE FOUND.   */
         ('NO MATCHING DATA SET NAMES FOUND') (A);
    PUT SKIP EDIT (' ') (A);
  END;
 /*********************************************************************/
 /* ADVANCE THE PROFILE ARRAY COUNTER BY ONE.                         */
 /*********************************************************************/
 NEXTPROF:
  RUNNER = RUNNER + 1;                         /* GET NEXT PROFILE.   */
  IF SUBSTR(RACREC(RUNNER),1,1) = ' ' THEN     /* DO NOT PROCESS ANY  */
       GOTO NEXTPROF;                          /* BLANK PROFILE NAMES.*/
 END;
 /*********************************************************************/
 /* WHEN ALL DATASETS AND PROFILES HAVE BEEN MATCHED, SCAN THE        */
 /* DATASETS TO DETERMINE WHICH (IF ANY) ARE UNPROTECTED.             */
 /*********************************************************************/
   RUNNER = 1;                                 /* RESET COUNTER.      */
   FTIME = 'YES';                              /* FIRST TIME THROUGH. */
   DO WHILE (RUNNER <= COUNT2);                /* PROCESS ALL DSNS.   */
   DATS = CATREC(RUNNER);                      /* SET UP DSNAME.      */
   IF SUBSTR(DATS,1,1) = ' ' THEN GOTO REALEND;/* IF EOF, QUIT IT.    */
   IF DATS ^= '9' THEN DO;                     /* IF NOT PROCESSED....*/
      IF FTIME = 'YES' THEN DO;                /* AND FIRST TIME THRU,*/
        PUT SKIP EDIT (' ') (A);               /* WRITE A BLANK..     */
        PUT SKIP EDIT (' ') (A);               /* WRITE A BLANK..     */
        PUT SKIP EDIT                          /* WRITE OUT UNPROTECT */
             ('THE FOLLOWING DATASET(S) ARE NOT RACF PROTECTED') (A);
        PUT SKIP EDIT (' ') (A);
       END;
      PUT SKIP EDIT ('UNPROTECTED DATASET ===> ',DATS) (A,A);
      FTIME = 'NO';                            /* SET FTIME=NO        */
     END;
   RUNNER = RUNNER + 1;                        /* INCREMENT ARRAY CNTR*/
   END;
 /*********************************************************************/
 /* DISPLAY THE COUNTS HERE.                                          */
 /*********************************************************************/
 REALEND:
       PUT SKIP EDIT (' ') (A);                /* WRITE BLANK LINE.   */
       PUT SKIP EDIT                           /* WRITE TOTAL LINE.   */
       ('-------------------------------------------------------')
            (A);                               /*                     */
       PUT SKIP EDIT (' ') (A);                /* WRITE BLANK LINE.   */
       PUT SKIP EDIT (RCOUNT1,' RACF PROFILES.') (A,A);
       PUT SKIP EDIT (RCOUNT2,' CATALOGED DATASETS.') (A,A);
 /*********************************************************************/
 /* CLOSE THE INPUT FILES AND GET OUT.                                */
 /*********************************************************************/
 ALLDONE:
    CLOSE FILE(RACFIN);                        /* CLOSE RACF FILE.    */
    CLOSE FILE(LISTCAT);                       /* CLOSE LISTCAT FILE. */
    RETURN(0);                                 /* TERMINATE BLOCK.    */
    END LISTRP;                                /* THAT'S ALL FOLKS!   */
