         TITLE 'JESEXIT2 - ENFORCE JOB NAME STANDARDS'
**********************************************************************
*                                                                    *
* MODULE NAME: JESEXIT2                                              *
*                                                                    *
* FUNCTION:    THIS EXIT (JES #2) WILL ENFORCE  JOB CARD STAN-       *
*              DARDS. EXCEPTIONS TO THE STANDARDS WILL BE ALLOWED    *
*              IF THE SUBMITTOR HAVE RACF READ ACCESS TO THE RACF    *
*              PROFILE IN THE JES2 CLASS THAT MATCHES THE KEYWORD(S) *
*              THAT ARE EXCEPTIONS. IF A JOB DOES NOT PASS THE       *
*              CRITERIA FOR JOB CARD STANDARDS, THE JOB WILL END     *
*              WITH A JCL ERROR.                                     *
*                                                                    *
* STANDARDS:   JOB NAME - MUST USER-ID + 1 CHARACTER                 *
*              CLASSES  - INPUT CLASSES ARE ALWAYS CHECKED VIA RACF  *
*              ACCT NUM - ACCOUNT NUMBERS ARE CHECKED IN JES EXIT #3 *
*              TIME     - TIME= PARAMETERS NORMALLY NOT ALLOWED      *
*              PRTY     - PRTY= PARAMETERS NORMALLY NOT ALLOWED      *
*              ADDRSPC  - ADDRSPC= PARAMETERS NORMALLY NOT ALLLOWED  *
*              PERFORM  - PERFORM= PARAMETERS NORMALLY NOT ALLLOWED  *
*              RD       - RD= PARAMETERS NORMALLY NOT ALLLOWED       *
*                                                                    *
* RACF:        ALL OF THE ABOVE STANDARDS (EXCEPT ACCT NUM) ARE      *
*              VALIDATED BY THIS EXIT. THE RACF CLASS 'JES2' HAS     *
*              BEEN SET UP AS A USER DEFINED CLASS. WITHIN THE JES2  *
*              CLASS, THERE ARE PROFILES THAT ARE CHECKED BY THIS    *
*              EXIT TO SEE IF A USER IS ALLOWED TO USE THE VARIOUS   *
*              JOB CARD PARAMETERS AVAILABLE. THIS EXIT ISSUES A     *
*              'THIRD PARTY RACHECK' WHICH IS A FEATURE OF RACF      *
*              RELEASE 1.8. THE PROFILES THAT ARE USED ARE:          *
*                                                                    *
*              JOBNAME  - IF YOU HAVE ACCESS, YOU CAN HAVE A NON-    *
*                         STANDARD JOB NAME                          *
*              CLASS%   - WHERE '%' IS AN INPUT CLASS, OR * FOR A    *
*                         CATCH-ALL GENERIC PROFILE                  *
*              TIME     - ACCESS TO THIS PROFILE ALLOWS USE OF TIME= *
*              PRTY     - ACCESS TO THIS PROFILE ALLOWS USE OF PRTY= *
*              ADDRSPC  - ACCESS TO THIS PROFILE ALLOWS USE OF THE   *
*                         ADDRSPC= PARAMETER                         *
*              PERFORM  - ACCESS TO THIS PROFILE ALLOWS USE OF THE   *
*                         PERFORM= PARAMETER                         *
*              RD       - ACCESS TO THIS PROFILE ALLOWS USE OF RD=   *
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
*                                                                    *
* EXIT FLAGS:  JCTUSER0 - FLAG FOR EXIT #2 COMPLETE                  *
*              JCTUSER1 - FLAGS FOR TIME, PRTY, CLASS IN THE EXIT    *
*              JCTUSER2 - FLAGS FOR ADDRSPC, PERFORM, RD IN THE EXIT *
*              JCTUSER3 - NOT USED                                   *
*              JCTUSER4 - FLAG TO HAVE EXIT 20 CANCEL THE JOB        *
*              JCTUSER5 - USED BY THIS EXIT FOR JOB CARD SCANNING    *
*                                                                    *
* RETURN CODE: AT EXIT, REGISTER 15 WILL HAVE ONE OF THE FOLLOWING   *
*              0  - CONTINUE WITH ADDITIONAL EXITS/HASPRDR PROCESSING*
*              4  - CONTINUE WITH NORMAL HASPRDR PROCESSING (EXIT 20 *
*                   WILL CANCEL THIS JOB                             *
*                                                                    *
* LINK EDIT:   NAME(JESEXIT2),RENT,REUS,AC(1),RMODE=24,AMODE=31      *
*                                                                    *
* CHANGES:     10/18/88   WRITTEN                                    *
*                                                                    *
**********************************************************************
         EJECT
         COPY  $HASPGBL                 COPY HASP GLOBALS
JESEXIT2 $MODULE $BUFFER,                                              +
               $HASPEQU,                REQUIRED FOR REG CONVENTIONS   +
               $HCT,                    REQ'D BY $SAVE,$RETURN,ETC.    +
               $JCT,                    REQ'D FOR MAPPING              +
               $JOE,                    REQ'D BY $COMWORK              +
               $MIT,                    REQ'D BY $MODULE               +
               $NHD,                    REQ'D BY RACF                  +
               $PCE,                    HASP PROCESSOR CONTROL ELEMENT +
               $RDRWORK,                HASP INPUT SERVICE PCE WORK    +
               RPL                      REQ'D BY $BUFFER
         TITLE 'JESEXIT2 - ENFORCE JOB NAME STANDARDS'
         PRINT NOGEN
         EJECT
***********************************************************************
*        JES2 - INITIALIZATION AND ADDRESSABILITY                     *
***********************************************************************
JSEXIT2  $ENTRY BASE=R12                EXIT ROUTINE ENTRY POINT
         $SAVE
         LR    R12,R15                  LOAD LOCAL BASE REG
         SLR   R15,R15                  CLEAR REG 15
         LR    R5,R1                    SAVE PARMLIST ADDRESS
         USING JCT,R10                  ADDRESSABILITY TO JCT
         USING HCT,R11                  ADDRESSABILITY TO HCT
         LA    R8,JCTNJHDR              FIND ADDRESS OF NJH
         USING NJH,R8                   ADDRESSABILITY TO NJH
*
         CLC   JOB,JCTJOBID             THIS A BATCH JOB?
         BNE   RETURN                   NO, RETURN
         TM    JCTUSER0+2,EXIT002       EXIT 02 PROCESSING COMPLETED?
         BO    RETURN                   YES, WE'RE DONE
*
         C     R0,F0                    INITIAL JOB STATEMENT?
         BNE   PARMS000                 NO, DON'T INITIALIZE JCTUSER'S
*
         MVC   JCTXWRK(80),SPACES       BLANK OUT WORK FIELD FIRST
         EJECT
***********************************************************************
* SCAN THROUGH THE CARD FOR BLANKS, QUOTES, AND COMMAS                *
***********************************************************************
PARMS000 DS    0H
         SLR   R15,R15                  CLEAR REG 15
         L     R2,0(R5)                 POINT TO JOB STMT IMAGE BUFFER
         LA    R3,70                    MAX LENGTH OF JOB STMT BUFFER
*
         C     R0,F0                    INITIAL JOB STATEMENT?
         BE    PARMS010                 YES, SCAN CARD
*
         CLC   ERROR,0(R2)              SHOULD THIS JOB BE CANCELED?
         BE    RETURN                   YES, FLAG SET SO JUST RETURN
         CLC   COMMENT,0(R2)            IS THIS A COMMENT?
         BE    RETURN                   YES, RETURN
*
         CLC   CONTINUE,0(R2)           IS THIS A CONTINUED JOBCARD
         BNE   RETURN                   NO, EXIT NOW.
         LA    R2,3(R2)                 THIS IS A CONTINUED JOB CARD
BLANKER  DS    0H
         CLC   BLANK,0(R2)              A LEADING BLANK?
         BNE   PARMS010                 NO, PROCESS STATEMENT
         LA    R2,1(R2)                 BUMP UP R2 BY 1
         BCT   R3,BLANKER               CONTINUE TO SCAN FOR BLANKS
*
PARMS010 DS    0H
         CLC   QUOTE,0(R2)              IS THIS A SINGLE QUOTE?
         BNE   CONT1                    IF NOT, SEE WHAT IT IS
QUOTES   DS    0H
         LA    R2,1(R2)                 BUMP POINTER UP BY 1
         CLC   QUOTE,0(R2)              IS THIS A SINGLE QUOTE?
         BE    CONT2                    IF SO, BYPASS PROGRAMMER NAME
         BCT   R3,QUOTES                IF NOT, CONTINUE TO SCAN
         EJECT
***********************************************************************
* CHECK TO SEE IF WE HAVE ANY OF THESE PARAMETERS.                    *
***********************************************************************
CONT1    DS    0H
         CLC   TIME1,0(R2)              IS THIS A TIME= PARAMETER?
         BE    PARMS030                 YES, SET FLAGS
         CLC   PRTY1,0(R2)              IS THIS A PRTY= PARAMETER?
         BE    PARMS035                 YES, SET FLAGS
         CLC   CLASS1,0(R2)             IS THIS A MSGCLASS= PARAMETER
         BE    PARMS038                 YES, BUMP PAST IT
         CLC   CLASS2,0(R2)             IS THIS A CLASS= PARAMETER?
         BE    PARMS040                 YES, SET FLAGS
         CLC   SCANJOB,0(R2)            IS THIS ' JOB ' STATEMENT
         BE    PARMS045                 YES, SET FOUND JOBCARD FLAGS
         CLC   ADDRSPC,0(R2)            IS THIS AN ADDRSPC= PARAMETER?
         BE    PARMS060                 YES, SET FLAGS
         CLC   PERFORM,0(R2)            IS THIS A PERFORM= PARAMETER?
         BE    PARMS065                 YES, SET FLAGS
         CLC   RD,0(R2)                 IS THIS A RD= PARAMETER?
         BE    PARMS070                 YES, SET FLAGS
         CLC   TRAILCOM,0(R2)           IS THIS ', ' (TRAILING COMMA)?
         BE    RETURN                   END OF STMT, CONTINUATION NEXT
         CLC   BLANK,0(R2)              IS THIS A ' ' BLANK
         BNE   CONT2                    NO, KEEP GOING
*
         C     R0,F0                    INITIAL JOB STATEMENT?
         BNE   RACF000                  NO,  DONE
*
         TM    JCTUSER5+1,SCANFLG1      FLAG1 SET?
         BNO   CONT2                    NO, CONTINUE
*
         TM    JCTUSER5+1,SCANFLG2      FLAG2 SET?
         BO    RACF000                  YES, DONE
*
BLANKET  DS    0H
         LA    R2,1(R2)                 INCREMENT REG2
         CLC   BLANK,0(R2)              ANOTHER BLANK
         BNE   PARMS050                 NO, SET FLAG2
         BCT   R3,BLANKET               CONTINUE TO SCAN
         B     RETURN                   NO, EXIT NOW
*
CONT2    DS    0H
         LA    R2,1(R2)                 INCREMENT REG2
         BCT   R3,PARMS010              CONTINUE TO SCAN
         B     RACF000                  PERFORM POST JOBCARD PROCESSING
         EJECT
***********************************************************************
*        TIME=                                                        *
***********************************************************************
PARMS030 DS    0H
         OI    JCTUSER1+1,TIMEUSER      SET TIME FLAG
         LA    R2,5(R2)                 INCREMENT REG2
         B     CONT2                    CONTINUE
***********************************************************************
*        PRTY=                                                        *
***********************************************************************
PARMS035 DS    0H
         OI    JCTUSER1+1,PRTYUSER      SET PRTY FLAG
         LA    R2,5(R2)                 INCREMENT REG2
         B     CONT2                    CONTINUE
***********************************************************************
*        MSGCLASS=                                                    *
***********************************************************************
PARMS038 DS    0H
         LA    R2,9(R2)                 INCREMENT REG2 TO BYPASS THIS
         B     CONT2                    CONTINUE
***********************************************************************
*        CLASS=                                                       *
***********************************************************************
PARMS040 DS    0H
         OI    JCTUSER1+1,JOBCLASS      SET CLASS FLAG
         LA    R2,6(R2)                 INCREMENT REG2
         MVC   JCTXWRK(1),0(R2)         SAVE JOBCLASS
         B     CONT2                    CONTINUE
***********************************************************************
*        SCAN FLAG1                                                   *
***********************************************************************
PARMS045 DS    0H
         OI    JCTUSER5+1,SCANFLG1      SET SCAN FLAG
         LA    R2,4(R2)                 INCREMENT REG2
         B     CONT2                    CONTINUE
***********************************************************************
*        SCAN FLAG2                                                   *
***********************************************************************
PARMS050 DS    0H
         OI    JCTUSER5+1,SCANFLG2      SET SCAN FLAG
         B     PARMS010                 SCAN NON BLANK
***********************************************************************
*        ADDRSPC=                                                     *
***********************************************************************
PARMS060 DS    0H
         OI    JCTUSER2+1,ADDRUSER      SET ADDRSPC FLAG
         LA    R2,6(R2)                 INCREMENT REG2
         B     CONT2                    CONTINUE
***********************************************************************
*        PERFORM=                                                     *
***********************************************************************
PARMS065 DS    0H
         OI    JCTUSER2+1,PERFUSER      SET PERFORM FLAG
         LA    R2,6(R2)                 INCREMENT REG2
         B     CONT2                    CONTINUE
***********************************************************************
*        RD=                                                          *
***********************************************************************
PARMS070 DS    0H
         OI    JCTUSER2+1,RDUSER        SET RD FLAG
         LA    R2,1(R2)                 INCREMENT REG2
         B     CONT2                    CONTINUE
         EJECT
***********************************************************************
*  GETMAIN RACF WORK AREA, FIND USER-ID, AND INITIALIZE FOR RACF WORK *
***********************************************************************
RACF000  DS    0H
         LA    R0,WORKLEN               LENGTH OF GETMAIN AREA
         $GETMAIN RC,LV=(R0),SP=230,LOC=BELOW    GETTMAIN STG FOR RACF
         LR    R9,R1                    R9 - ADDRESS OF GETMAINED AREA
         USING RACWORK,R9               ESTABLISH ADDRESSABILITY TO
         MVC   RACROUT1(RACRTLEN),ROUTDATA   MOVE STATIC INTO DYNAMIC
         LA    R7,PROFILE               COPY ADDR OF PROFILE
         MVC   USERID(8),SPACES         INITIALIZE FIELD
         MVC   JBCLASS(1),JCTXWRK       INITIALIZE FIELD
         CLC   =X'00',NJH2USR           IF ZEROS, IT IS PROPAGATED
         BNE   NOPROP                   IF NOT GET IT FROM NJH2USR
         MVC   USERID(8),NJH2SUSR       SAVE USER-ID OF SUBMITTOR
         B     CHECKJOB                 SEE IF IT MATCHES JOBNAME
NOPROP   DS    0H
         MVC   USERID(8),NJH2USR        SAVE USER-ID OF SUBMITTOR
         EJECT
***********************************************************************
*  CHECK TO SEE IF WE HAVE A VALID JOB NAME. PROFILE=JOBNAME          *
***********************************************************************
CHECKJOB DS    0H
         LA    R6,USERID                GRAB ADDR OF MESSAGE
         CLC   USERID(6),JCTJNAME       IS THE JOB NAME VALID?
         BE    RACF100                  YES, GO CHECK FOR TIME
         MVC   PROFILE(25),SPACES       INTITALIZE FIELD
         MVC   PROFILE(7),=C'JOBNAME'   CHECKING FOR JOBNAME AUTH
         RACROUTE REQUEST=AUTH,         WE WANT A RACHECK              X
               CLASS=RACLASS,           CLASS IN THE RACF CDT (JES2)   X
               ENTITY=((R7)),           PROFILE NAME TO CHECK          X
               USERID=((R6)),           THIRD PARTY RACHECK USER-ID    X
               ATTR=READ,               SEE IF THEY CAN READ IT        X
               LOG=ASIS,                LOG IF PROFILE SET TO LOG      X
               RELEASE=1.8,             WE ARE USING RACF V1R8         X
               WORKA=RACWK,             512 BYTE RACROUTE WORK AREA    X
               MF=(E,RACROUT1)          EXECUTE FORM OF RACROUTE
         C     R15,=F'8'                CHECK RETURN CODE
         BL    RACF100                  IF NOT 8 OR ABOVE, ALLOW IT
         MVC   JCTXWRK(80),ERR4             BAD JOBNAME
         OI    RDWFLAGX,RDWXXSNC        SET JES2 FLAG
         B     PARMS080                 ABEND
         EJECT
***********************************************************************
* IF THE USER WANTS TO USE TIME=, CHECK RACF PROFILE 'TIME'.          *
***********************************************************************
RACF100  DS    0H
         TM    JCTUSER1+1,TIMEUSER      TIME= ON THE JOB CARD?
         BNO   RACF200                  NO, DON'T CHECK WITH RACF
         MVC   PROFILE(25),SPACES       INTITALIZE PROFILE TO CHECK
         MVC   PROFILE(4),=C'TIME'      CHECKING FOR TIME= AUTH
         RACROUTE REQUEST=AUTH,         WE WANT A RACHECK              X
               CLASS=RACLASS,           CLASS IN THE RACF CDT (JES2)   X
               ENTITY=((R7)),           PROFILE NAME TO CHECK          X
               USERID=((R6)),           THIRD PARTY RACHECK USER-ID    X
               ATTR=READ,               SEE IF THEY CAN READ IT        X
               LOG=ASIS,                LOG IF PROFILE SET TO LOG      X
               RELEASE=1.8,             WE ARE USING RACF V1R8         X
               WORKA=RACWK,             512 BYTE RACROUTE WORK AREA    X
               MF=(E,RACROUT1)          EXECUTE FORM OF RACROUTE
         C     R15,=F'8'                CHECK RETURN CODE
         BL    RACF200                  IF LESS THAN 8, ALLOW IT.
         MVC   JCTXWRK(80),ERR1         MOVE IN NOT ALLOWED MESSAGE
         OI    RDWFLAGX,RDWXXSNC        SET JES2 FLAG
         B     PARMS080                 EXIT WITH A JCL ERROR
         EJECT
***********************************************************************
* IF THE USER WANTS TO USE PRTY=, CHECK RACF PROFILE 'PRTY'.          *
***********************************************************************
RACF200  DS    0H
         TM    JCTUSER1+1,PRTYUSER      PRTY= ON THE JOB CARD?
         BNO   RACF300                  NO, BYPASS RACF CHECK
         MVC   PROFILE(25),SPACES       INTITALIZE PROFILE FIELD
         MVC   PROFILE(4),=C'PRTY'      CHECKING FOR JOBNAME AUTH
         RACROUTE REQUEST=AUTH,         WE WANT A RACHECK              X
               CLASS=RACLASS,           CLASS IN THE RACF CDT (JES2)   X
               ENTITY=((R7)),           PROFILE NAME TO CHECK          X
               USERID=((R6)),           THIRD PARTY RACHECK USER-ID    X
               ATTR=READ,               SEE IF THEY CAN READ IT        X
               LOG=ASIS,                LOG IF PROFILE SET TO LOG      X
               RELEASE=1.8,             WE ARE USING RACF V1R8         X
               WORKA=RACWK,             512 BYTE RACROUTE WORK AREA    X
               MF=(E,RACROUT1)          EXECUTE FORM OF RACROUTE
         C     R15,=F'8'                CHECK RETURN CODE
         BL    RACF300                  IF LESS THAN 8, ALLOW IT
         MVC   JCTXWRK(80),ERR2         MOVE IN NOT ALLOWED MESSAGE
         OI    RDWFLAGX,RDWXXSNC        SET JES2 FLAG
         B     PARMS080                 EXIT WITH A JCL ERROR
         EJECT
***********************************************************************
* WE SAVED THE JOB CLASS FROM THE CLASS= PARAMETER. WE NOW CREATE A   *
* LITERAL OF 'CLASS%' WHERE '%' IS THE CLASS THE USER WANTS. WE USE   *
* THIS LITERAL AS THE PROFILE NAME TO DO A RACF CHECK ON.             *
***********************************************************************
RACF300  DS    0H
         TM    JCTUSER1+1,JOBCLASS      CLASS= ON THE JOB CARD?
         BNO   RACF400                  NO, BYPASS THIS RACF CHECK
         MVC   PROFILE(25),SPACES       INTITALIZE PROFILE TO PASS
         MVC   PROFILE(5),=C'CLASS'     PUT IN CLASS
         MVC   PROFILE+5(1),JBCLASS     MAKE IT A REAL JOB CLASS
         RACROUTE REQUEST=AUTH,         WE WANT A RACHECK              X
               CLASS=RACLASS,           CLASS IN THE RACF CDT (JES2)   X
               ENTITY=((R7)),           PROFILE NAME TO CHECK          X
               USERID=((R6)),           THIRD PARTY RACHECK USER-ID    X
               ATTR=READ,               SEE IF THEY CAN READ IT        X
               LOG=ASIS,                LOG IF PROFILE SET TO LOG      X
               RELEASE=1.8,             WE ARE USING RACF V1R8         X
               WORKA=RACWK,             512 BYTE RACROUTE WORK AREA    X
               MF=(E,RACROUT1)          EXECUTE FORM OF RACROUTE
         C     R15,=F'8'                CHECK RETURN CODE
         BL    RACF400                  IF LESS THAN 8, ALLOW IT
         MVC   JCTXWRK(80),ERR3         MOVE IN ERROR MESSAGE
         OI    RDWFLAGX,RDWXXSNC        SET JES2 FLAG
         B     PARMS080                 GO ISSUE A JCL ERROR
         EJECT
***********************************************************************
* IF THE USER WANTS TO USE ADDRSPC=, CHECK RACF PROFILE 'ADDRSPC'.    *
***********************************************************************
RACF400  DS    0H
         TM    JCTUSER2+1,ADDRUSER      ADDRSPC= ON THE JOB CARD
         BNO   RACF500                  NO, BYPASS THIS RACF CHECK
         MVC   PROFILE(25),SPACES       INTITALIZE PROFILE TO PASS
         MVC   PROFILE(7),=C'ADDRSPC'   MOVE IN PROFILE NAME TO CHECK
         RACROUTE REQUEST=AUTH,         WE WANT A RACHECK              X
               CLASS=RACLASS,           CLASS IN THE RACF CDT (JES2)   X
               ENTITY=((R7)),           PROFILE NAME TO CHECK          X
               USERID=((R6)),           THIRD PARTY RACHECK USER-ID    X
               ATTR=READ,               SEE IF THEY CAN READ IT        X
               LOG=ASIS,                LOG IF PROFILE SET TO LOG      X
               RELEASE=1.8,             WE ARE USING RACF V1R8         X
               WORKA=RACWK,             512 BYTE RACROUTE WORK AREA    X
               MF=(E,RACROUT1)          EXECUTE FORM OF RACROUTE
         C     R15,=F'8'                CHECK RETURN CODE
         BL    RACF500                  IF LESS THAN 8, ALLOW IT
         MVC   JCTXWRK(80),ERR5         MOVE IN ERROR MESSAGE
         OI    RDWFLAGX,RDWXXSNC        SET JES2 FLAG
         B     PARMS080                 EXIT WITH A JCL ERROR
         EJECT
***********************************************************************
* IF THE USER WANTS TO USE ADDRSPC=, CHECK RACF PROFILE 'ADDRSPC'.    *
***********************************************************************
RACF500  DS    0H
         TM    JCTUSER2+1,PERFUSER      PERFORM= ON JOB CARD?
         BNO   RACF600                  NO, BYPASS THIS RACF CHECK
         MVC   PROFILE(25),SPACES       INTITALIZE PROFILE TO PASS
         MVC   PROFILE(7),=C'PERFORM'   MOVE IN PROFILE NAME TO CHECK
         RACROUTE REQUEST=AUTH,         WE WANT A RACHECK              X
               CLASS=RACLASS,           CLASS IN THE RACF CDT (JES2)   X
               ENTITY=((R7)),           PROFILE NAME TO CHECK          X
               USERID=((R6)),           THIRD PARTY RACHECK USER-ID    X
               ATTR=READ,               SEE IF THEY CAN READ IT        X
               LOG=ASIS,                LOG IF PROFILE SET TO LOG      X
               RELEASE=1.8,             WE ARE USING RACF V1R8         X
               WORKA=RACWK,             512 BYTE RACROUTE WORK AREA    X
               MF=(E,RACROUT1)          EXECUTE FORM OF RACROUTE
         C     R15,=F'8'                CHECK RETURN CODE
         BL    RACF600                  IF LESS THAN 8, ALLOW IT
         MVC   JCTXWRK(80),ERR6         MOVE IN ERROR MESSAGE
         OI    RDWFLAGX,RDWXXSNC        SET JES2 FLAG
         B     PARMS080                 EXIT WITH A JCL ERROR
         EJECT
***********************************************************************
* IF THE USER WANTS TO USE RD=, CHECK RACF PROFILE 'RD'.              *
***********************************************************************
RACF600  DS    0H
         TM    JCTUSER2+1,RDUSER        RD= ON THE JOB CARD
         BNO   RETURN0                  NO, EXIT. A GOOD JOB CARD
         MVC   PROFILE(25),SPACES       INTITALIZE PROFILE TO PASS
         MVC   PROFILE(2),=C'RD'        MOVE IN PROFILE NAME
         RACROUTE REQUEST=AUTH,         WE WANT A RACHECK              X
               CLASS=RACLASS,           CLASS IN THE RACF CDT (JES2)   X
               ENTITY=((R7)),           PROFILE NAME TO CHECK          X
               USERID=((R6)),           THIRD PARTY RACHECK USER-ID    X
               ATTR=READ,               SEE IF THEY CAN READ IT        X
               LOG=ASIS,                LOG IF PROFILE SET TO LOG      X
               RELEASE=1.8,             WE ARE USING RACF V1R8         X
               WORKA=RACWK,             512 BYTE RACROUTE WORK AREA    X
               MF=(E,RACROUT1)          EXECUTE FORM OF RACROUTE
         C     R15,=F'8'                CHECK RETURN CODE
         BL    RETURN0                  IF LESS THAN 8, ALLOW IT
         MVC   JCTXWRK(80),ERR7         MOVE IN ERROR MESSAGE
         OI    RDWFLAGX,RDWXXSNC        SET JES2 FLAG
*                                       FALL THROUGH FOR A JCL ERROR
         EJECT
***********************************************************************
* FLAG THIS JOB AS 'SHOULD BE CANCELLED'                              *
***********************************************************************
PARMS080 DS    0H
         OI    RDWFLAGX,RDWXXSNC        WE'VE SUPPLIED NEXT STMT IMAGE
         OI    JCTUSER4,BYTEFF          JOB WILL BE CANCELED IN EXIT020
         OI    JCTUSER0+2,EXIT002       EXIT 02 PROCESSING COMPLETE
         LA    R0,WORKLEN               LENGTH OF GETMAIN AREA
         LA    R1,RACWORK               ADDRESS OF GETMAIN AREA
         $FREMAIN R,LV=(R0),A=(R1),SP=230    FREEMAIN STORAGE
         LA    R15,4                    GIVE EM A RETURN CODE OF 4
         B     RETURN                   WE'RE DONE
***********************************************************************
* EXIT WITH RETURN CODE 0                                             *
***********************************************************************
RETURN0  DS    0H
         LA    R0,WORKLEN               LENGTH OF GETMAIN AREA
         LA    R1,RACWORK               ADDRESS OF GETMAIN AREA
         $FREMAIN R,LV=(R0),A=(R1),SP=230    FREEMAIN STORAGE
         OI    JCTUSER0+2,EXIT002       EXIT 02 PROCESSING COMPLETE
         SLR   R15,R15                  EXIT WITH RETURN CODE 0
         B     RETURN                   WE'RE DONE
***********************************************************************
* RESTORE REGISTERS AND RETURN                                        *
***********************************************************************
RETURN   DS    0H
         $RETURN RC=(R15)               RETURN TO CALLER
         EJECT
***********************************************************************
* FLAGS USED BY THIS EXIT                                             *
***********************************************************************
*
*        JCTUSER0
*
EXIT002  EQU   X'01'                    EXIT 02 PROCESSING COMPLETE
*
*        JCTUSER1
*
TIMEUSER EQU   X'10'                    USER SPECIFIED 'TIME' PARAMETER
PRTYUSER EQU   X'20'                    USER SPECIFIED 'PRTY'
JOBCLASS EQU   X'40'                    USER SPECIFIED 'CLASS'
*
*        JCTUSER2
*
ADDRUSER EQU   X'10'                    USER SPECIFIED 'ADDRSPC'
PERFUSER EQU   X'20'                    USER SPECIFIED 'PERFORM'
RDUSER   EQU   X'40'                    USER SPECIFIED 'RD'
*
*        JCTUSER4
*
BYTEFF   EQU   X'FF'                    BYTE CONSTANT, VALUE X'FF'
*
*        JCTUSER5
*
SCANFLG1 EQU   X'40'                    JOBCARD SPACES
SCANFLG2 EQU   X'80'                    JOBCARD SPACES DONE
         EJECT
***********************************************************************
* PROGRAM CONSTANTS                                                   *
***********************************************************************
F0       DC    F'0'                     FULLWORD CONSTANT, VALUE X'00'
F8       DC    F'8'                     FULLWORD CONSTANT, VALUE X'08'
RACLASS  DS    0F                       ADDRESS OF RACF CLASS NAME
         DC    AL1(4)                   LENGTH OF CLASS NAME
RACLASSN DC    CL8'JES2'                RACF CLASS TO CHECK IN
TIME1    DC    C'TIME='                 TIME PARAMETER
CLASS1   DC    C'MSGCLASS='             MSGCLASS PARAMETER
CLASS2   DC    C'CLASS='                CLASS PARAMETER
PRTY1    DC    C'PRTY='                 PRTY PARAMETER
ADDRSPC  DC    C'ADDRSPC'               ADDRSPC
PERFORM  DC    C'PERFORM'               PERFORM
RD       DC    C'RD'                    RD
QUOTE    DC    C''''                    QUOTE
TRAILCOM DC    C', '                    TRAILING COMMA
SPACES   DC    CL80' '                  BLANKS
JOB      DC    C'JOB'                   JCTJOBID FOR BATCH JOB
SCANJOB  DC    C' JOB '                 JOBCARD SCAN
COMMENT  DC    C'//*'                   USED TO CHECK FOR COMMENT
CONTINUE DC    C'// '                   USED TO CHECK FOR CONTINUE
BLANK    DC    C' '                     USED TO SKIP LEADING BLANKS
ERROR    DC    C'//**ERROR**'           USED TO CHECK FOR ERROR CARD
ERR1     DC    CL80'//**ERROR** BAD JOBCARD, NOT AUTHORIZED FOR TIME='
ERR2     DC    CL80'//**ERROR** BAD JOBCARD, NOT AUTHORIZED FOR PRTY='
ERR3     DC    CL80'//**ERROR** BAD JOBCARD, NOT AUTHORIZED FOR JOB CLAX
               SS'
ERR4     DC    CL80'//**ERROR** BAD JOBCARD, JOBNAME MUST BE YOUR USERIX
               D PLUS 1 CHARACTER'
ERR5     DC    CL80'//**ERROR** BAD JOBCARD, NOT AUTHORIZED FOR ADDRSPCX
               ='
ERR6     DC    CL80'//**ERROR** BAD JOBCARD, NOT AUTHORIZED FOR PERFORMX
               ='
ERR7     DC    CL80'//**ERROR** BAD JOBCARD, NOT AUTHORIZED FOR RD='
***********************************************************************
* LIST FORM OF RACROUTE MACRO                                         *
***********************************************************************
         DS    0F
ROUTDATA RACROUTE REQUEST=AUTH,MF=L,WORKA=*-*,RELEASE=1.8
RACRTLEN EQU   *-ROUTDATA
         LTORG
         EJECT
***********************************************************************
* DYNAMIC AREA FOR GETMAINED VARIABLES AND RACF WORK AREAS            *
***********************************************************************
RACWORK  DSECT
USERID   DS    CL8                      USER-ID TO RACHECK AGAINST
PROFILE  DS    CL25                     PROFILE TO RACHECK AGAINST
JCLASS   DS    0CL6                     JOBCLASS TO RACHECK AGAINST
CLASWORD DS    CL5                      LITERAL 'CLASS'
JBCLASS  DS    CL1                      ACTUAL JOB CLASS FROM JCL
RACWK    DS    CL512                    RACROUTE WORK AREA
         DS    0D                       ALIGNMENT ON DOUBLEWORD BDY
DOUBLE   DC    D'0'
RACROUT1 RACROUTE REQUEST=AUTH,MF=L,WORKA=*-*,RELEASE=1.8,             X
               CLASS=RACLASS,LOG=ASIS,ENTITY=PROFILE,USERID=USERID,    X
               ATTR=READ
WORKLEN  EQU   *-RACWORK
JESEXIT2 CSECT
         $MODEND
         END  ,
