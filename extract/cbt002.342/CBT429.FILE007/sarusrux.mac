         TITLE 'SARUSRUX - USERID OBTAIN USER EXIT FOR ROSCOE'
SARUSRUX CSECT
***********************************************************************
*                                                                     *
* SARUSRUX -                                                          *
*        USER EXIT TO OBTAIN USERID FOR ROSCOE USER.                  *
*                                                                     *
* INPUTS -                                                            *
*        REG 1 - ADDRESS OF PARAMETER LIST (MAPPED BY SARUPL).        *
*        REG13 - ADDRESS OF REGISTER SAVE AREA                        *
*        REG14 - RETURN ADDRESS                                       *
*        REG15 - ENTRY POINT ADDRESS.                                 *
*                                                                     *
* RETURN CODES -                                                      *
*        0 - USER IS ALLOWED ACCESS TO SAR.                           *
*        4 - DENY ACCESS TO SAR.                                      *
*        8 - USER IS TO BE PROMPTED FOR HIS USERID AND PASSWORD.      *
*            THE EXIT WILL BE CALLED AGAIN AFTER PROMPTING SO THAT    *
*            THE USERID AND PASSWORD ENTERED BY THE USER CAN BE       *
*            VERIFIED.                                                *
*                                                                     *
* ATTRIBUTES -                                                        *
*        RENT, REUS.                                                  *
*        MUST BE LINKED AS AUTHORIZED, BUT EXECUTES IN UNAUTHORIZED   *
*        STATE.                                                       *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12),,SARUSRUX.&SYSDATE
         LR    R12,R15             LOAD BASE REGISTER
         USING SARUSRUX,R12
         LR    R10,R1              LOAD PLIST ADDRESS
         USING UPL,R10             ADDRESSABILITY FOR PLIST
         LA    R11,UPLUWORK        LOAD WORK AREA ADDRESS
         USING RACF,R11            BASE FOR RACF DSECT
         SPACE
         L     R3,UPLUSERA         LOAD USERID ADDRESS
         CLI   0(R3),C' '          ANY USERID SUPPLIED?
         BE    RC8                 NO, GO PROMPT FOR ONE
         SPACE
         L     R3,UPLPSWDA         LOAD PASSWRD ADDRESS
         CLI   0(R3),C' '          ANY PASSWRD SUPPLIED?
         BNE   DORACF              NO, CHECK RACF
         SPACE
         B     RC8
         SPACE
DORACF   XC    RACFKEY,RACFKEY
         XC    RACFPSW,RACFPSW
         SPACE
         LA    R6,8
         LA    R5,0
         LA    R4,RACFKEY
         L     R3,UPLUSERA
USERLUP  CLI   0(R3),X'40'
         BE    USEREND
         MVC   0(1,R4),0(R3)
         LA    R3,1(R3)
         LA    R4,1(R4)
         LA    R5,1(R5)
         BCT   R6,USERLUP
         B     RC4
         SPACE
USEREND  STCM  R5,R1,RACFKEYL
         SPACE
         LA    R6,7
         LA    R5,0
         LA    R4,RACFPSW
         L     R3,UPLPSWDA
PASSLUP  CLI   0(R3),X'40'
         BE    PASSEND
         MVC   0(1,R4),0(R3)
         LA    R3,1(R3)
         LA    R4,1(R4)
         LA    R5,1(R5)
         BCT   R6,PASSLUP
         B     RC4
         SPACE
PASSEND  STCM  R5,R1,RACFPSWL
         SPACE
         XC    ACFB,ACFB
         SR    R7,R7
         SR    R5,R5
         SR    R6,R6
         SPACE
         LA    R4,RACFEXEC
         MVC   0(RACFILEN,R4),RACFINT
         SPACE
         RACINIT ENVIR=CREATE,                                         X
               USERID=RACFKEYL,                                        X
               PASSWRD=RACFPSWL,                                       X
               NEWPASS=(7),                                            X
               GROUP=(6),                                              X
               TERMID=(5),                                             X
               ACEE=ACFB,                                              X
               MF=(E,(4))
         SPACE
         ST    R15,RACFRESP
         SPACE
         LA    R4,RACFEXEC
         MVC   0(RACFILEN,R4),RACFINT
         SPACE
         RACINIT ENVIR=DELETE,                                         X
               ACEE=ACFB,                                              X
               MF=(E,(4))
         SPACE
         L     R15,RACFRESP
         LTR   R15,R15
         BZ    RC0
         B     RC4
         SPACE
RC0      RETURN (14,12),RC=0       RETURN TO ALLOW FULL ACCESS
RC4      RETURN (14,12),RC=4       RETURN TO PREVENT ACCESS
RC8      RETURN (14,12),RC=8       RETURN TO PROMPT FOR USERID
         SPACE
RACFINT  DS    0F
         RACINIT MF=L
RACFILEN EQU   *-RACFINT
         SPACE
RACFM    DS    0F
         SPACE
RACF     DSECT
RACFEXEC DS    CL(RACFILEN)
ACFB     DS    A
RACFRESP DS    F
RACFKEYL DS    X
RACFKEY  DS    CL8
RACFPSWL DS    X
RACFPSW  DS    CL8
         SPACE
         SARUPL
         SPACE
         LTORG
         END
