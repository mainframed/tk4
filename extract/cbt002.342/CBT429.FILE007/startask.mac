***********************************************************************
*                                                                     *
* STARTASK MACRO:                                                     *
*                                                                     *
*     THIS MACRO IS A SHORT CUT DEFINITION FOR AN ENTRY IN THE RACF   *
*     STARTED TASK NAME TABLE. VALID UP TO RACFV1R7                   *
*                                                                     *
*     OPERANDS:                                                       *
*          &NAME   -  STARTED TASK JOBNAME                            *
*          &USER   -  STARTED TASK RACF USERID.                       *
*                     IF USER=* IS SPECIFIED OR DEFAULTED TO, THEN    *
*                     THE USERID IS ASSUMED TO BE THE SAME AS THE     *
*                     JOBNAME (&NAME)                                 *
*          &GROUP  -  RACF CONNECT GROUP ASSOCIATED WITH THE          *
*                     USERID'S (&USER'S) LOGON. DEFAULT=STARTASK.     *
*                                                                     *
*     NOTE:                                                           *
*          THIS MACRO DOES NOT INCLUDE USE OF THE PRIVILEGED FLAGS.   *
*          IF THEY ARE NEEDED IN THE FUTURE, THIS MACRO SHOULD BE     *
*          UPGRADED ACCORDINGLY TO SET THESE BITS.                    *
*                                                                     *
***********************************************************************
         MACRO
&LABEL   STARTASK &NAME,&USER=*,&GROUP=STARTASK
         LCLC  &C1
&C1      SETC  '&NAME'.'        '
&C1      SETC  '&C1'(1,8)
         DC    CL8'&C1'                STARTED TASK NAME
         AIF   ('&USER' EQ '*').UIDEF
&C1      SETC  '&USER'.'        '
&C1      SETC  '&C1'(1,8)
.UIDEF   ANOP
         DC    CL8'&C1'                STARTED TASK USERID
&C1      SETC  '&GROUP'.'        '
&C1      SETC  '&C1'(1,8)
         DC    CL8'&C1'                STARTED TASK GROUPID
         DC    XL8'0000000000000000'   PRIVILIGE FLAG & RESERVED BYTES
*
         MEXIT ,
         MEND  ,
***********************************************************************
* IN-LINE MACRO ENDS HERE.                                            *
***********************************************************************
* THE RACF STARTED TASK TABLE ICHRIN03 BEGINS HERE                    *
***********************************************************************
ICHRIN03 CSECT
NUMENTS  EQU   ((ENDTABLE-BGNTABLE)/ENTLEN)    CALCULATE # OF ENTRIES
         DC    AL2(NUMENTS+X'8000')
*
BGNTABLE EQU   *
         STARTASK ADMPRINT             PRINT GDDM STUFF
*
ENTLEN   EQU   *-BGNTABLE
*
         STARTASK ARTSINIT             UCC11 INIT
         STARTASK CICSA                TEST CICS 'A'
         STARTASK CICSB                PROD CICS 'B'
         STARTASK CLRDMP               DUMP DATASET CLEARING TASK
         STARTASK CMFXA                CMF MONITORING
         STARTASK DBRCPROD             IMS 1.3 PRODUCTION DBRC
         STARTASK DBRCTEST             IMS 1.3 TEST DBRC
         STARTASK DLIPROD              IMS 1.3 DLI SAS
         STARTASK DLITEST              IMS 1.3 DLI SAS
         STARTASK DSPRINT              TSO PRINT FACILITY
         STARTASK EREP                 IBM CE TASK
         STARTASK EREPTSS,USER=EREP    IBM CE TASK
         STARTASK FOCUS                FOCUS SU UPDATES
         STARTASK HDRCART,USER=HEADER  DUMP CARTRIDGE TAPE HEADER
         STARTASK HDR1600,USER=HEADER  DUMP 1600 BPI TAPE HEADER
         STARTASK HDR3480,USER=HEADER  DUMP 3480 TAPE HEADER
         STARTASK HDR6250,USER=HEADER  DUMP 6250 BPI TAPE HEADER
         STARTASK H015                 HYPERCHANNEL
         STARTASK ICOM                 UCC SMF WRITER
         STARTASK IMFSS                PROD IMS MONITOR
         STARTASK IMFSST               TEST IMS MONITOR
         STARTASK IMSRDR               IMS INTERNAL READER JOB
         STARTASK IMS80000             PROD IMS CONTROL REGION
         STARTASK INIT                 INITIATORS
         STARTASK IRLMPROC             DB2 IRLM ADDRESS SPACE
         STARTASK JESA,USER=JES2       JES2 ALTERNATE "A"
         STARTASK JESB,USER=JES2       JES2 ALTERNATE "B"
         STARTASK JES2                 JES2
         STARTASK LLA                  LINKLIST LOOKASIDE
         STARTASK NCCF                 NETWORK STUFF
         STARTASK NET                  VTAM, ETC.
         STARTASK NPM                  NETWORK PERFORMANCE MONITOR
         STARTASK OLTEP,USER=EREP      IBM CE TASK
         STARTASK OLTEPU,USER=EREP     IBM CE TASK
         STARTASK ROSBTAM,USER=ROSCOE  PRODUCTION ROSCOE BTAM
         STARTASK ROSCOE,USER=ROSCOE   PRODUCTION ROSCOE VTAM
         STARTASK SMFDUMP              DUMP THE SMF DATASETS
         STARTASK STOPFOC,USER=FOCUS   STOP PROD FOCUS
         STARTASK STOPTFOC,USER=FOCUS  STOP TEST FOCUS
         STARTASK TESTFOC              TEST FOCUS SU UPDATES
         STARTASK TESTIMS              TEST IMS CONTROL REGION
         STARTASK TESTROS              TEST ROSCOE
         STARTASK TMSINIT              UCC1 INIT
         STARTASK TSO                  TSO
         STARTASK TSSO                 STAND ALONE TSO SUBSYSTEM
         STARTASK UCC7ONLN             UCC7 SCHEDULING SYSTEM
         STARTASK VTAM                 VTAM
         STARTASK X                    X
         STARTASK XWTR                 EXTERNAL WRITER
*
ENDTABLE EQU   *
         END
