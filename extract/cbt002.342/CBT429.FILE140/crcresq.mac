PROC 0 DEBUG
/* REF: ABL.CMDPROC(ARCRESQ)
/* DOC: CLIST FOR SUBMITTING BATCH DF/DSS DASD RESTORE JOBSTREAM
/*      TO RECOVER AN ARCHIVED DATASET FROM A QUARTERLY ARCHIVE TAPE.
/*      THIS CLIST INVOKED VIA SPF 3.A.ARC.RESQ (DATA ARCHIVE SYSTEM)
CONTROL MSG NOLIST
IF &DEBUG = DEBUG THEN CONTROL MSG LIST SYMLIST CONLIST
ISPEXEC DISPLAY PANEL(ARCRESQ)
IF &LASTCC EQ 8 THEN +
 DO
  ISPEXEC SETMSG MSG(ARC012A)
  EXIT CODE(0)
 END
ISPEXEC VGET (ZTEMPF ARCDSN ARCVOL ARCQTR ARCYEAR)
IF &SYSDSN('&ARCDSN') = OK THEN +
 DO
  /* PROCEED NO FURTHER .... ALREADY HAVE A CATALOG ENTRY FOR THE
  /* SPECIFIED DATASET TO BE RESTORED.
  ISPEXEC SETMSG MSG(ARC012B)
  EXIT CODE(0)
 END
/* DEFINE SKELETON'S TAPE DSN "DISK" VOLUME
/* DEFINE SKELETON'S TAPE FILE SEQUENCE NUMBER
IF &SUBSTR(4,&ARCVOL) = 1 THEN +
 DO
   SET &ARCVOL = USER01
   SET &ARCSEQ# = 1
 END
IF &SUBSTR(4,&ARCVOL) = 2 THEN +
 DO
   SET &ARCVOL = USER02
   SET &ARCSEQ# = 2
 END
IF &SUBSTR(4,&ARCVOL) = 3 THEN +
 DO
   SET &ARCVOL = USER03
   SET &ARCSEQ# = 3
 END
ISPEXEC FTOPEN TEMP
ISPEXEC FTINCL ARCRESQ
ISPEXEC FTCLOSE
SUBMIT '&ZTEMPF'
SET &WHOFOR = &SUBSTR(1:7,&ARCDSN)
SE 'RESTORE OF DATASET &ARCDSN BEGAN AT &SYSTIME',USER(&WHOFOR),LOGON
ISPEXEC SETMSG MSG(ARC012C)
SE '#ABLUTIL: RESTORE OF DATASET &ARCDSN REQUESTED',CN(01)
SE '#ABLUTIL: USING ARCHIVE TAPE &ARCVOL0&ARCQTR&ARCYEAR',CN(01)
%OUTLIST
EXIT CODE (0)
