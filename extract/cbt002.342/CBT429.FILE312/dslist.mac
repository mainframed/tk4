/* ---------------------  rexx procedure  ---------------------- *
 * Name:      dslist                                             *
 *                                                               *
 * Function:  Invoke ISPF 3.4 with the provided high level index *
 *                                                               *
 * Syntax:    %dslist hlq                                        *
 *                                                               *
 * Installation:                                                 *
 *                                                               *
 *            To install this application do the following:      *
 *                                                               *
 *            1.  Copy this exec into your production clist      *
 *                or exec library                                *
 *                                                               *
 *            2.  Update your ISPCMDS table to add the following *
 *                entry by:                                      *
 *                a. copy your production ISPCMDS into member    *
 *                   TCMDS                                       *
 *                b. use ISPF 3.9 to update applid 'T' and       *
 *                   add the entry:                              *
 *                   Verb:   DSLIST                              *
 *                   T:      3                                   *
 *                   Action: Select CMD(%DSLIST &ZPARM)          *
 *                   Desc:    Data set list (via LMDDISP)        *
 *                c. Rename ISPCMDS to OSPCMDS and TCMDS to      *
 *                   ISPCMDS                                     *
 *                d. Exit ISPF and re-enter ISPF                 *
 *                                                               *
 *            3.  Try it out (e.g. Command ===> DSL SYS1)        *
 *                                                               *
 * Author:    Lionel B. Dyck                                     *
 *                                                               *
 * History:                                                      *
 *            01/10/95 - Strip quotes and add support for        *
 *                       2nd pass with users prefix              *
 *            05/20/94 - additional cleanup                      *
 *            05/20/94 - cleanup and additional doc              *
 *            05/19/94 - created by Lionel Dyck                  *
 *                                                               *
 * ------------------------------------------------------------- */

arg hlq

orig_hlq = ""
first    = 0

hlq = strip(hlq,'b',"'")    /* remove any quotes */

Begin_Here:
Address ISPExec

if length(hlq) = 0 then do
   zedsmsg = "Error"
   zedlmsg = "Invalid syntax for command"
   "Setmsg Msg(isrz000)"
   exit 0
   end

dsl = translate(hlq," ",".")
if words(dsl) > 1 then
   if right(hlq,1) <> "*" then
      if length(word(dsl,words(dsl))) < 8 then hlq = hlq"*"

"Control Errors Return"

"LMDINIT LISTID(LISTID) LEVEL("hlq")"
l_rc = rc
if l_rc > 0 then do
   zedsmsg = ""
   Select
     When l_rc = 8 then zedlmsg = "Listid not created for" hlq
     When l_rc = 12 then
          zedlmsg = "Invalid high level qualifier:" hlq
     When l_rc = 16 then
          zedlmsg = "Truncation or translation error in dialog",
                 "variables."
     When l_rc = 20 then
          zedlmsg = "Severe Error in ISPF Processing"
     otherwise
          zedlmsg = "Unknown error in LMDINIT"
   end
   zedsmsg = ""
   "Setmsg Msg(isrz000)"
   exit 4
   end

"LMDDISP LISTID("ListId") Confirm(Yes)",
        "View(Volume)"
l_rc = rc
if l_rc > 0 then do
   if first = 0 then do
      first = 1
      hlq = sysvar('syspref')"."hlq
      signal begin_here
      end
      else hlq = orig_hlq
   end
 if l_rc > 0 then do
    zedsmsg = "Nothing"
    zedlmsg = "No data sets found for index:" hlq
   "Setmsg Msg(isrz000)"
    exit 4
    end

"LMDFREE LISTID("ListId")"
Exit 0
