***********************************************************************
**   REGISTER USAGE                                                  **
***********************************************************************
R0       EQU   0                       RESERVED
R1       EQU   1                       PARAMETER LIST
R2       EQU   2                       CVT
R3       EQU   3                       UCB
R4       EQU   4                       SVC 99 RB
R5       EQU   5                       WORK REGISTER
R6       EQU   6                       UNUSED
R7       EQU   7                       UNUSED
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      BASE ADDRESS
R13      EQU   13                      SAVE AREA POINTER
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      ENTRY POINT/RETURN CODE
***********************************************************************
VTOCSCAN CSECT
VTOCSCAN AMODE 24
VTOCSCAN RMODE 24
         SAVE  (14,12),,'VTOCSCAN &SYSDATE'
         LR    R12,R15                 ENTRY POINT ADDRESS
         USING VTOCSCAN,R12            ESTABLISH ADDRESSABILITY
         LA    R0,WORKLEN              LOAD SIZE OF WORK AREA
         GETMAIN R,LV=(0)              GET WORK/SAVE AREA STORAGE
         ST    R13,4(R1)               ADDRESS OF CALLER'S SAVE AREA
         ST    R1,8(R13)               ADDRESS OF OUR SAVE AREA
         L     R1,24(R13)              RESTORE PARM POINTER
         L     R13,8(R13)              GET BACK OUR SAVE AREA ADDRESS
         USING WORKAREA,R13            MAP IT
***********************************************************************
         OPEN  (SYSPRDCB,OUTPUT)
         L     R2,CVTPTR               CVT POINTER
         USING CVT,R2                  MAP THE CVT
         USING UCBBGN,R3               MAP THE UCB
         LA    R4,RB                   SVC 99 REQUEST BLOCK
         USING S99RB,R4                MAP THE RB
         ST    R4,RBP                  STORE RB ADDRESS IN POINTER LIST
         OI    RBP,X'80'               FLAG LAST POINT
*
         XC    UCB$WORK,UCB$WORK       CLEAR UCBSCAN WORK AREA
         MVI   UCB$TYPE,UCB3DACC       SEARCH FOR DASD UCBS
NEXTUCB  L     R15,CVTUCBSC            ADDRESS OF UCBSCAN ROUTINE
         CALL  (15),(UCB$WORK,UCB$TYPE,UCB$ADDR)
         LTR   R15,R15                 END OF LIST?
         BNZ   DONE                    YES -> FINISH UP
         L     R3,UCB$ADDR             GET UCB ADDRESS
         LTR   R3,R3                   REALLY A UCB?
         BZ    NEXTUCB                 NO -> GET THE NEXT ONE
         TM    UCBSTAT,UCBONLI         DEVICE ONLINE?
         BZ    NEXTUCB                 NO -> NEXT UCB
         MVC   VOLSER,UCBVOLI          SAVE THE VOLSER
         MVC   PRTVOL+1(6),UCBVOLI
         MVC   PRTVOL+9(3),UCBNAME
         PUT   SYSPRDCB,PRTVOL
*
         XC    S99RB(S99RBLEN),S99RB   CLEAR RB
         MVI   S99RBLN,S99RBLEN        STORE LENGTH OF RB
         MVI   S99VERB,S99VRBAL        INDICATE ALLOCATE
         LA    R5,TUPLAL               TEXT UNIT POINTER LIST ADDRESS
         ST    R5,S99TXTPP             STORE IT IN RB
         LA    R1,RBP
         DYNALLOC
         LTR   R15,R15                 DYNAMIC ALLOCATION FAILED?
         BNZ   NEXTUCB                 GET NEXT UCB
*
         DEVTYPE DDNAME,DEVINFO,DEVTAB
         SLR   R8,R8                   CLEAR R8 FOR DIVIDE
         LH    R9,UCBVTOC              VTOC RELATIVE TRACK ADDRESS
         LH    R10,NUMTRKS             TRACKS PER CYLINDER
         DR    R8,R10                  DIVIDE RTA TO GET CYL/TRK
         STH   R9,CC                   CYLINDER ADDRESS
         STH   R8,HH                   TRACK ADDRESS
         MVC   R(1),UCBVTOC+2          RECORD NUMBER
         OBTAIN CAMDSCB4               GET THE FORMAT 4 DSCB
*
NEXTDSCB SLR   R15,R15                 CLEAR R15 FOR IC
         IC    R15,R                   GET RECORD NUMBER
         LA    R15,1(R15)              NEXT RECORD
         STC   R15,R                   SAVE RECORD NUMBER
         CLC   R(1),DS4DEVDT           TOO HIGH FOR THIS TRACK?
         BNH   GETDSCB                 NO -> GET NEXT DSCB
         MVI   R,X'01'                 RECORD 1 OF NEXT TRACK
         LH    R15,HH                  GET TRACK NUMBER
         LA    R15,1(R15)              NEXT TRACK
         STH   R15,HH                  SAVE TRACK NUMBER
         CH    R15,NUMTRKS             TOO HIGH FOR THIS CYLINDER?
         BL    GETDSCB                 NO -> GET NEXT DSCB
         XC    HH(2),HH                TRACK 0 OF NEXT CYLINDER
         LH    R15,CC                  GET CYLINDER NUMBER
         LA    R15,1(R15)              NEXT CYLINDER
         STH   R15,CC                  SAVE CYLINDER NUMBER
GETDSCB  OBTAIN CAMDSCB1               GET THE NEXT DSCB
         CLI   DS1FMTID,C'1'           FORMAT 1 DSCB?
         BNE   CHKLAST                 NO -> ARE WE DONE YET?
         MVC   PRTDSN+4(44),DS1DSNAM
         ICM   R15,B'1111',DS1SMSFG    LOAD PREVIOUSLY RESERVED BYTES
         LTR   R15,R15                 ANYTHING SET?
         BZ    CHKLAST                 NO -> DON'T PRINT THIS DATASET
         SLR   R15,R15
         LA    R11,HEXCHAR
         IC    R15,DS1SMSFG
         SRL   R15,4
         AR    R15,R11
         MVC   PRTDSN+60(1),0(R15)
         SLR   R15,R15
         IC    R15,DS1SMSFG
         N     R15,LOW4
         AR    R15,R11
         MVC   PRTDSN+61(1),0(R15)
         SLR   R15,R15
         IC    R15,DS1SCXTF
         SRL   R15,4
         AR    R15,R11
         MVC   PRTDSN+74(1),0(R15)
         SLR   R15,R15
         IC    R15,DS1SCXTF
         N     R15,LOW4
         AR    R15,R11
         MVC   PRTDSN+75(1),0(R15)
         SLR   R15,R15
         IC    R15,DS1SCXTV
         SRL   R15,4
         AR    R15,R11
         MVC   PRTDSN+88(1),0(R15)
         SLR   R15,R15
         IC    R15,DS1SCXTV
         N     R15,LOW4
         AR    R15,R11
         MVC   PRTDSN+89(1),0(R15)
         SLR   R15,R15
         IC    R15,DS1SCXTV+1
         SRL   R15,4
         AR    R15,R11
         MVC   PRTDSN+90(1),0(R15)
         SLR   R15,R15
         IC    R15,DS1SCXTV+1
         N     R15,LOW4
         AR    R15,R11
         MVC   PRTDSN+91(1),0(R15)
         PUT   SYSPRDCB,PRTDSN
*
CHKLAST  CLC   CCHHR(5),DS4HPCHR
         BL    NEXTDSCB
*
         XC    S99RB(S99RBLEN),S99RB   CLEAR RB
         MVI   S99RBLN,S99RBLEN        STORE LENGTH OF RB
         MVI   S99VERB,S99VRBUN        INDICATE UNALLOCATE
         LA    R5,TUPLUN               TEXT UNIT POINTER LIST ADDRESS
         ST    R5,S99TXTPP             STORE IT IN RB
         LA    R1,RBP
         DYNALLOC
         B     NEXTUCB
*
DONE     CLOSE SYSPRDCB
***********************************************************************
EXIT     LR    R1,R13                  SAVE ADDRESS OF OUR SAVE AREA
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         FREEMAIN R,LV=WORKLEN,A=(1)   FREEMAIN OUR SAVE AREA
         XR    R15,R15                 RC = 0
         RETURN (14,12),RC=(15)        RETURN
***********************************************************************
         DS    0D
UCB$WORK DS    XL100                   UCBSCAN WORK AREA
UCB$TYPE DS    X                       DEVICE CLASS
UCB$ADDR DS    F                       UCB ADDRESS
*
CAMDSCB1 CAMLST SEEK,CCHHR,VOLSER,IECSDSF1
CAMDSCB4 CAMLST SEEK,CCHHR,VOLSER,DS4DSNAM
*
         IECSDSL1 1                    FORMAT 1 DSCB
DS4DSNAM DS    CL44
         IECSDSL1 4                    FORMAT 1 DSCB
*
CC       DS    1H
HH       DS    1H
R        DS    X
CCHHR    EQU   CC,5
*
         DS    0F
DEVINFO  EQU   *,20                    FILLED IN BY DEVTYPE MACRO
DEVTYPE  DS    1F                      UCB DEVICE TYPE
MAXBLK   DS    1F                      MAXIMUM BLKSIZE FOR DEVICE
NUMCYLS  DS    1H                      NUMBER OF CYLINDERS ON DEVICE
NUMTRKS  DS    1H                      NUMBER OF TRACKS PER CYLINDER
TRKLEN   DS    1H                      MAXIMUM TRACK LENGTH
         DS    8H                      THE REST OF THE STORY
*
LOW4     DC    F'15'
ZEROS    DC    CL4'0000'
HEXCHAR  DC    CL16'0123456789ABCDEF'
*
PRTVOL   DC    CL133'-###### (###)'
PRTDSN   DC    CL133'    ############################################  X
               DS1SMSFG: XX  DS1SCXTF: XX  DS1SCXTV: XXXX'
***********************************************************************
RBP      DS    1F
RB       DS    0F
         ORG   *+S99RBLEN
S99RBLEN EQU   S99RBEND-S99RB
*
TUPLAL   DC    AL4(TUDDNAL)
         DC    AL4(TUUNTAL)
         DC    AL4(TUVOLAL)
         DC    X'80',AL3(TUSHRAL)
*
TUDDNAL  DC    AL2(DALDDNAM)
         DC    AL2(1)
         DC    AL2(8)
DDNAME   DC    CL8'VTOCVTOC'
*
TUUNTAL  DC    AL2(DALUNIT)
         DC    AL2(1)
         DC    AL2(8)
         DC    CL8'SYSALLDA'
*
TUVOLAL  DC    AL2(DALVLSER)
         DC    AL2(1)
         DC    AL2(6)
VOLSER   DS    CL6
*
TUSHRAL  DC    AL2(DALSTATS)
         DC    AL2(1)
         DC    AL2(1)
         DC    X'08'
***********************************************************************
TUPLUN   DC    AL4(TUDDNUN)            DEALLOCATE DDNAME
         DC    X'80',AL3(TUUNAUN)      UNALLOCATE ANYWAY
*
TUDDNUN  DC    AL2(DUNDDNAM)           DDNAME TEXT UNIT
         DC    AL2(1)                  PARMS = 1
         DC    AL2(8)                  LENGTH = 8
         DC    CL8'VTOCVTOC'           DDNAME
*
TUUNAUN  DC    AL2(DUNUNALC)           UNALLOCATE
         DC    AL2(0)                  PARMS = 0
***********************************************************************
SYSPRDCB DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,LRECL=133,           X
               BLKSIZE=23408,MACRF=(PM)
***********************************************************************
         LTORG
***********************************************************************
         DS    0F
WORKAREA DSECT
SAVEAREA DS    18F
WORKLEN  EQU   *-WORKAREA
***********************************************************************
         DSECT
         IEFUCBOB
         CVT    DSECT=YES
         IEFZB4D0
         IEFZB4D2
***********************************************************************
         END   VTOCSCAN
/*
