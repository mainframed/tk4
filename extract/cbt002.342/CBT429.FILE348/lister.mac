LISTER   CSECT
         STM   R14,R12,12(R13)
         LR    12,15
         USING LISTER,R12
         ST    R13,OLD13
         LA    R13,SAVEAREA
         ST    R13,NEW13
*
SS0102   DS    0H                 OPEN INFILE AND OUTFILE
         OPEN  (INFILE,(INPUT))      INPUT PDS
         OPEN  (OUTFILE,(OUTPUT))    OUTPUT SEQUENTIAL FILE
         EJECT
*
**********************************************************************
*
*   SS02   (READ BLOCK)
*
**********************************************************************
*
SS0201   DS    0H                 ISSUE READ AND CHECK
         READ  DIRDECB,SF,INFILE,DIRBLK,264
         CHECK DIRDECB
*
SS0202   DS    0H                 SET END OF BLOCK ADDRESS
         SR    R1,R1
         ICM   R1,3,DIRBLK+8
         A     R1,=AL4(DIRBLK)
         LA    R1,8(R1)
         ST    R1,ENDIRBLK
*
SS0203   DS    0H                 POINT TO 1ST ENTRY
         LA    R3,DIRBLK+10
         EJECT
*
**********************************************************************
*
*   SS03   (PROCESS ENTRY)
*
**********************************************************************
*
SS0301   DS    0H                 IS MEM NAME X'FF'
*                                 (Y) SS0501 (READ COMPLETE)
*                                 (N) SS0302
         CLC   0(8,R3),=8X'FF'
         BE    SS0501             YES
*        B     SS0302             NO
*
SS0302   DS    0H                 WRITE NAME
         MVC   OUTAREA(80),=80C' '
         MVC   OUTAREA(8),0(R3)
         PUT   OUTFILE,OUTAREA
         EJECT
*
*********************************************************************
*
*   SS04   (EOP FOR AN ENTRY)
*
*********************************************************************
*
SS0401   DS    0H                 POINT TO NEXT DIR ENTRY
         IC    R1,11(R3)
         SLL   R1,27
         SRL   R1,26
         AR    R3,R1
         LA    R3,12(R3)
*
SS0402   DS    0H                 PAST BLOCK END ?
*                                 (Y) SS0201 (READ BLOCK)
*                                 (N) SS0403
         C     R3,ENDIRBLK
         BNL   SS0201             YES
*        B     SS0403             NO
*
SS0403   DS    0H                 IS MEM NAME X'FF' ?
*                                 (Y) SS0501 (READ COMPLETE)
*                                 (N) SS0301 (PROCESS ENTRY)
         CLC   0(8,R3),=8X'FF'
         BNE   SS0301             NO
*        B     SS0501             YES
         EJECT
*
***********************************************************************
*
*   SS05   (READ COMPLETE)
*
***********************************************************************
*
SS0501   DS    0H                 CLOSE FILES
         CLOSE (INFILE)
         CLOSE (OUTFILE)
*
SS0502   DS    0H                 RETURN TO CALLER
         L     R13,OLD13
         L     R14,R12(R13)
         LM    R0,R12,20(R13)
         BR    14
*
INFILE   DCB   DDNAME=INFILE,MACRF=R,DSORG=PO,LRECL=256,BLKSIZE=256,   X
               KEYLEN=8,EODAD=SS0501
OUTFILE  DCB   DDNAME=OUTFILE,MACRF=PM,DSORG=PS,LRECL=80
*
         DS    0F
ENDIRBLK DS    XL4                A(DIRECTORY BLOCK END)
SAVEAREA DS    F
OLD13    DS    F
NEW13    DS    F
         DS    15F
OUTAREA  DS    CL80
DIRBLK   DS    CL264              DIRECTORY BLOCK READ HERE
         LTORG
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
