SP2RUS99 TITLE 'ERBRUS99 -- RMF USER REPORT INTERACTIVE ONLY MODULE'
***********************************************************************
**                                                                   **
**     THIS MODULE MUST BE LINK EDITED INTO LINKLIB OR INTO A TSO    **
**     USER'S STEPLIB AS "ERBRUS99".  MODULE "ERBGUS99" IS NOT       **
**     TO BE MODIFIED FOR THIS ROUTINE. THIS MODULE MUST BE "RENT"   **
**     AND ALL EXTERNAL REFERENCES MUST BE RESOLVED.  THIS ASSEMBLY  **
**     REQUIRES THE "AAPVT" COMPONENT FROM THE SYSTEM OPTIONAL       **
**     MACHINE READABLE MATERIAL AS A MACLIB.                        **
**                                                                   **
**  USED IN SP2.1.X                                                  **
**                                                                   **
***********************************************************************
**                                                                   **
**     TO USE THIS MODULE, ENTER ON TSO:                             **
**          RMFMON                                                   **
**          USER HELP      TO DISPLAY THE AVAILABLE OPTIONS          **
**          USER OPTION    FOR THE PARTICULAR OPTION                 **
**                                                                   **
***********************************************************************
ERBRUS99 CSECT
ERBRUS99 AMODE 31
ERBRUS99 RMODE ANY
         USING *,15
         B     BYMODULE
         DC    AL1(23)
         DC    CL8'ERBRUS99'
         DC    CL7' &SYSTIME'
         DC    CL8'&SYSDATE'
         DC    CL8'SP2RUS99'
FW2      DC    F'2'
         PRINT NOGEN
BYMODULE SAVE  (14,12)
         LR    R3,R1
         USING PARMFLD,R3
         L     R1,PARMCODE
         CLC   FW2,0(R1)      IS THE ERBFMENU SET UP CORRECTLY?
         BE    MENUOK
         LA    R15,8           NO, RETURN CODE IMMEDIATELY
         L     R13,4(,R13)
         B     EXITQ
MENUOK   L     R4,PARMSUBP    LOAD SUBPOOL NUMBER FOR GETMAIN
         SR    R6,R6
         IC    R6,0(,R4)
         L     R5,GETMAINL    LENGTH OF THE GETMAINED AREA
         GETMAIN R,LV=(R5),SP=(R6)
         ST    R1,8(,R13)     SAVE FORWARD POINTER
         ST    R13,4(,R1)     SAVE BACKWARD POINTER
         LR    R13,R1         SET UP REGISTER FOR SAVE AREA
         USING GETMAINA,R13
         ST    R3,PARMFLDA    SAVE ADDRESS OF RMF PARAMETERS
         ST    R13,GOTMAINA
         ST    R6,GOTMAINS
         DROP  R15
         BALR  R12,0
         USING *,R12
         MVC   EYECATCH(L'EYELIT),EYELIT
         MVC   PUTLPLCB,PARMPLCB MOVE IN PUTLINE CONTROL BLOCK ADDRESS
         OI    PUTLPLCB,X'80'
         L     R5,PARMPUTL    MOVE IN PUTLINE ENTRY ADDRESS
         MVC   PUTLINEA,0(R5)
         LA    R5,L'PRTLINE   SET LENGTH OF DISPLAY
         STH   R5,PRTDTL
         LA    R5,DSPLHI      SAVE ADDRESSES FOR THE OPTIONS
         ST    R5,DSPLHIA
         LA    R5,DSPLLOW
         ST    R5,DSPLLOWA
         LA    R5,HDRLHD
         ST    R5,HDRLHDA
         LA    R5,HDRLDT
         ST    R5,HDRLDTA
         MVC   PINDHOLD,=CL4' '
         LA    R5,16          POINTER TO THE CVT
         L     R5,0(,R5)
         USING CVT,R5
         ST    R5,CVTA        SAVE THE CVT ADDRESS
         L     R11,CVTOPCTP   POINTER TO THE RMCT
         DROP  R5
         USING RMCT,R11
         CLC   =CL4'RMCT',RMCTNAME IS THE POINTER CORRECT?
         BE    *+8             YES, CONTINUE
         SR    R4,R4           NO, BLOW UP
         DR    R8,R4
         ST    R11,RMCTA      SAVE ADDRESS OF THE RMCT
         SPACE 3
         LA    R8,VALIDOPT    POINTER TO VALID OPTIONS
         L     R10,PARMOPER   DECIDE THE REPORT TO PRESENT
         LH    R9,0(,R10)     LOAD LENGTH OF THE REPORT OPTION
         CH    R9,=H'4'       CHECK FOR THE "TEST" OPTION
         BNE   OPTLOOP
         SPACE 1
***********************************************************************
**                                                                   **
**  THIS SHORT SECTION ALLOWS DEVELOPING A NEW SUBROUTINE WITHOUT    **
**  EXCESSIVE IMPACT ON THE OTHER "USER XXX" ROUTINES. CODE THE      **
**  NEW ROUTINE WITH A CSECT NAME AND LOAD MODULE NAME OF "ERBRUSTS" **
**  AND LINK THAT ROUTINE INTO YOUR TSO USERID STEPLIB. ENTER        **
**  "USER TEST" TO TEST THE NEW SUBROUTINE.  WHEN THE NEW SUBROUTINE **
**  IS CORRECT, CHANGE THE CSECT NAME TO WHATEVER YOU LIKE AND       **
**  INSERT THE CSECT NAME INTO THE "VALIDOPT" TABLE BELOW.  THEN     **
**  LINK THE NEW SUBROUTINE WITH THIS CSECT AS LOAD MODULE ERBRUS99. **
**                                                                   **
***********************************************************************
         SPACE 1
         CLC   =C'TEST',2(R10)
         BNE   OPTLOOP
         LINK  EP=ERBRUSTS,DCB=0
         LTR   R15,R15        TEST FOR BAD RETURN CODE
         BNZ   EXITF
         MVC   PUTLHDRL,HDRLDTA
         MVC   PUTLDSPL,DSPLLOWA
         LA    R1,HEADE99
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         B     EXIT
         SPACE 1
OPTLOOP  LTR   R9,R9
         BNP   OPTLOOP9
         BCTR  R9,0
         CH    R9,=H'2'       USE A MAXIMUM OF THREE CHAR FOR COMPARE
         BNH   *+8
         LA    R9,2
         LA    R8,L'VALIDOPT(,R8)
         L     R7,VALIDLEN    NUMBER OF ENTRIES
         BCTR  R7,0
OPTLOOP1 EX    R9,OPTCOMPR    TEST FOR A MATCH
         BE    OPTLOOP9
         LA    R8,L'VALIDOPT(,R8)
         BCT   R7,OPTLOOP1
         B     HELPRPT
OPTLOOP9 L     R15,4(R8)      LOAD ADDRESS OF THE CORRECT MODULE
         CALL  (15),ID=73
         B     EXITF
         SPACE 1
OPTCOMPR CLC   2(0,R10),0(R8)
         SPACE 3
VALIDHD1 DC    H'18',CL18'VALID OPTIONS ARE:'
         SPACE 1
VALIDOPT DS    0D             VALID OPTIONS FOR THE "USER" COMMAND
VALIDESC DC    CL4'HELP',V(ERBUSMPL) DEFAULT OPTION--MUST BE FIRST
         DC    CL4'MPLC',V(ERBUSMPL) MPL ADJUSTMENT CONSTANTS
VALIDEND DS    0C
         SPACE 1
VALIDLEN DC    A((VALIDEND-VALIDOPT)/L'VALIDOPT)
         SPACE 3
HELPRPT  MVC   PUTLHDRL,HDRLDTA HANDLE INVALID OPTION ON "USER"
         MVC   PUTLDSPL,DSPLHIA
         LA    R1,VALIDHD1
         ST    R1,PUTLRECD
         BAL   R4,PUTLINE
         LA    R4,PRTDTL
         ST    R4,PUTLRECD
         LA    R7,VALIDOPT    START OF TABLE
         L     R3,VALIDLEN    LENGTH OF TABLE
VALIDLOP LA    R5,PRTLINE+1   START OF PRINT LINE
         LA    R6,1           LENGTH OF THE CURRENT PRINT LINE
         MVI   PRTLINE,C' '
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE
VALIDLP1 LA    R6,L'VALIDESC+1(,R6) LENGTH AFTER THIS IS ADDED
         CH    R6,PRTDTL      WILL THIS OPTION FIT ON THE LINE?
         BH    VALIDLP2         NO, PRINT THIS AND GO TO NEXT
         MVC   0(L'VALIDESC,R5),0(R7) MOVE IN THE VALID OPTION
         LA    R7,L'VALIDOPT(,R7)
         LA    R5,L'VALIDESC+1(,R5)
         BCT   R3,VALIDLP1
VALIDLP2 BAL   R4,PUTLINE
         LTR   R3,R3
         BNZ   VALIDLOP
         SPACE 3
EXIT     SR    R15,R15        NORMAL RETURN
EXITF    LR    R2,R15         SAVE RETURN CODE
         L     R6,GOTMAINA
         L     R5,GETMAINL
         L     R4,GOTMAINS    LOAD SUBPOOL FOR FREEMAIN
         L     R13,4(,R13)    RESTORE REGISTER 13
         FREEMAIN R,SP=(R4),LV=(R5),A=(R6)
         LR    R15,R2         RESTORE RETURN CODE
EXITQ    L     R14,12(R13,0)
         LM    R0,R12,20(R13)
         MVI   12(R13),X'FF'
         BR    R14
         SPACE 1
PUTLINE  LA    R1,PUTLRECD    LOAD ADDRESS OF PARAMETERS
         L     R15,PUTLINEA   ADDRESS OF PUTLINE ROUTINE
         BALR  R14,R15        GO DISPLAY THE REQUEST
         LTR   R15,R15        WAS THE PUTLINE SUCCESSFUL?
         BZR   R4              YES, CONTINUE THE PROCESSING
         LA    R15,12          NO, RETURN TO RMF CONTROL
         B     EXITF
         SPACE 3
GETMAINL DC    A(GETMAINE-GETMAINA) LENGTH OF THE GETMAINED AREA
DSPLHI   DC    X'80'          HIGH INTENSITY DISPLAY
DSPLLOW  DC    X'00'          LOW INTENSITY DISPLAY
HDRLHD   DC    C'HD'          RECORD IS A HEADER LINE
HDRLDT   DC    C'DT'          RECORD IS A DETAIL LINE
EYELIT   DC    CL16' ERBRUS99 DATA'
         SPACE 1
HEADE99  DC    H'79',CL80'1234567810123456782012345678301234567840123456
               6785012345678601234567870123456789'
         SPACE 1
         LTORG
         SPACE 3
*  IGNORE ANY ASSEMBLY ERRORS IN THIS MACRO
         PRINT GEN
         IRARMCT
         SPACE 3
         ERBUSDTA
         SPACE 3
         PRINT NOGEN
         REGS
         CVT   DSECT=YES
         END
