         PRINT GEN
         LCLC  &MYNAME                  LOCAL CONSTANT
*
&MYNAME  SETC  'GTEUA50'                CSECT NAME
RBASE    EQU   12                       BASE REGISTER FOR CSECT
RBAL     EQU   10                       BAL REGISTER
*
         TITLE '&MYNAME'                LISTING TITLE
**********************************************************************
***                                                                ***
***                   GTEUA50                                      ***
***                                                                ***
***  DESCRIPTION:                                                  ***
***    The function of this program is to convert some of the      ***
***    ISPF statistics from a directory entry into comments for    ***
***    a Panvalet library member.                                  ***
***                                                                ***
***  This program is a callable subroutine which uses two parms    ***
***    for input:  a pointer to a 42-byte directory entry with     ***
***    ISPF statistics, and a pointer to a 52-byte output area     ***
***    for the resulting Panvalet comment text.                    ***
***                                                                ***
***  Sample code for calling this program is:                      ***
***                                                                ***
*** *------------------------------------------------------------- ***
***          LA    R15,PANCOM               ADDR OF PAN COMMENT    ***
***          ST    R15,PARM50+00              INTO PARM LIST       ***
***          LA    R15,DIRENTRY             ADDR OF DIR ENTRY      ***
***          ST    R15,PARM50+04              INTO PARM LIST       ***
***          OI    PARM50+04,X'80'          MARK END OF LIST       ***
***          LA    R1,PARM50                POINT TO PARM LIST     ***
***          L     R15,=V(GTEUA50)          BASE ADDR OF SUBPGM    ***
***          BALR  R14,R15                  CALL THE SUBPROGRAM    ***
*** *------------------------------------------------------------- ***
*** WORKD    DSECT                                                 ***
*** *                                                              ***
*** DIRENTRY GTEUM01 DSECT=NO               PDS DIRECTORY ENTRY    ***
*** PANCOM   GTEUM03 DSECT=NO               PANVALET COMMENT AREA  ***
*** PARM50   DS    2F                       PARMS FOR SUBPROGRAM   ***
*** *------------------------------------------------------------- ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  FUNCTION:                                                     ***
***                                                                ***
***  1. Initialize the Panvalet comment field to blanks.           ***
***  2. Convert the following statistics fields to comments:       ***
***     a.  Version and Modification                               ***
***     b.  Date created                                           ***
***     c.  Date modified                                          ***
***     d.  Time modified                                          ***
***     e.  Initial size                                           ***
***     f.  Logonid                                                ***
***                                                                ***
***  NOTES:                                                        ***
***                                                                ***
***  1. The program will check for valid input Julian dates.       ***
***  2. The program does not check for other input data to be      ***
***     valid.                                                     ***
***                                                                ***
***  RETURN CODES:                                                 ***
***     00 - OKAY                                                  ***
***     16 - Input date out of range (0-365/366).                  ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  REGISTER USAGE:                                               ***
***     Describe register usage here, such as:                     ***
***                                                                ***
***     R0-R3   - Work registers for macros, linkage conventions   ***
***                 and open code.                                 ***
***     R4      - (not used)                                       ***
***     R5      - (not used)                                       ***
***     R6      - (not used)                                       ***
***     R7      - (not used)                                       ***
***     R8      - Base address of Panvalet comment area.           ***
***     R9      - Base address of directory entry area.            ***
***     R10     - Linkage register used with BAL.                  ***
***     R11     - (not used)                                       ***
***     R12     - Base register of program code.                   ***
***     R13     - Linkage conventions, base of WORKD DSECT.        ***
***     R14-R15 - Linkage conventions and work registers.          ***
***                                                                ***
***                                                                ***
***  NON-IBM MACROS USED:                                          ***
***                                                                ***
***     GTEUM01  - PDS directory entry in STOW format.             ***
***     GTEUM03  - Panvalet comment area.                          ***
***                                                                ***
***  ASSEMBLER OPTIONS:       RENT                                 ***
***  LINKAGE EDITOR OPTIONS:  RENT, REUS, REFR                     ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  CULPRIT:                                                      ***
***                                                                ***
***     Chuck Hoffman, IBM Systems Programming                     ***
***     GTE Laboratories, Technical Computation Center             ***
***     40 Sylvan Road                                             ***
***     Waltham, Massachusetts  02254                              ***
***                                                                ***
***                     617/466-2131    V/N 679-2131               ***
***                                                                ***
**********************************************************************
         SPACE 3
**********************************************************************
***                                                                ***
***  MAINTENANCE HISTORY:                                          ***
***  1.  A.  Initial version.                                      ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
**********************************************************************
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS ENTERING PROGRAM                       ***
***                                                                 ***
***********************************************************************
&MYNAME  CSECT ,
         STM   R14,R12,12(R13)          SAVE REGS TO CALLER S.A.
         B     (BEGIN-&MYNAME)(R15)     BRANCH AROUND EYECATCHER
         DC    A(L'NAME)                LENGTH OF CSECT NAME
NAME     DC    C'&MYNAME'               CSECT NAME
         DC    C' &SYSDATE &SYSTIME '   ASSEMBLY DATE/TIME STAMP
BEGIN    LR    RBASE,R15                LOAD BASE REGISTER
         USING &MYNAME,RBASE              ADDRESSABILITY
         PRINT NOGEN
         GETMAIN R,LV=WORKDLEN          GET SAVE/WORK AREA
         ST    R1,8(0,R13)              MY S.A. ADDR INTO CALLER S.A.
         ST    R13,4(0,R1)              CALLER S.A. ADDR INTO MY S.A.
         LR    R13,R1                   R13 POINTS TO MY S.A.
         USING WORKD,R13                ADDRESSABILITY OF SAVE AREA
         L     R1,4(0,R13)              R1 POINTS TO CALLER S.A.
         LM    R15,R1,16(R1)            R15 R0 AND R1 ARE RESTORED
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAINLINE ROUTINE                                           ***
***                                                                 ***
***********************************************************************
MAIN     EQU   *                        BEGIN MAINLINE ROUTINE
         ST    R1,R1SAVE                SAVE INITIAL R1
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE
         MVI   SWITCH0,B'00000000'      INITIALIZE SWITCH AREA
*
         LM    R8,R9,0(R1)              R8 POINTS TO PAN COMMENT AREA
*                                       R9 POINTS TO DIRECTORY ENTRY
         USING PANCOM,R8                ADDRESSABILITY
         USING DIRENTRY,R9              ADDRESSABILITY
*
         MVC   PANCOM(PANCOMLN),BLANKS  INITIALIZE DATA AREA
         BAL   RBAL,PANCMMNT            BUILD COMMENT
         CLC   RETCDE,=F'0'             IF NOT CLEAN
         BNE   END16                      GO TO HIGH ENDING
*
END00    LA    R15,0                    SET COMPLETION CODE 00
         ST    R15,COMPCODE               INTO STORAGE
         B     ENDING                   GO TO ENDING
*
END16    LA    R15,16                   SET COMPLETION CODE 16
         ST    R15,COMPCODE               INTO STORAGE
         B     ENDING                   GO TO ENDING
*
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS EXITING PROGRAM                        ***
***                                                                 ***
***********************************************************************
ENDING   L     R14,COMPCODE             R14 SAVES COMP CODE
         LR    R1,R13                   R1 SAVES ADDR OF MY S.A.
         L     R13,4(0,R1)              R13 RESTORED, PTR CALLER S.A.
         FREEMAIN R,LV=WORKDLEN,A=(R1)  FREE MY SAVE/WORK AREA
         LR    R15,R14                  R15 SET TO COMP CODE
         LM    R0,R12,20(R13)           R0-R12 RESTORED
         L     R14,12(0,R13)            R14 RESTORED
         MVI   12(R13),X'FF'            SET COMPLETION SIGNAL
         BR    R14                      RETURN TO CALLER
*
         EJECT
**********************************************************************
***                                                                ***
***  CREATE THE PANVALET COMMENT FROM ISPF STATS                   ***
***                                                                ***
**********************************************************************
PANCMMNT ST    RBAL,PARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   PANCOM(PANCOMLN),BLANKS  INITIALIZE WITH BLANKS
         MVC   GU03ID,GU01ID            MOVE USER ID INTO COMMENT
*
         MVC   WKYYDDDF(4),GU01DATM     MOVE DATE MODIFIED TO WK AREA
         BAL   RBAL,DATE                CONVERT THE DATE
         CLC   RETCDE(4),=F'0'          IF NOT GOOD RETURN
         BNE   PARET04                    GO TO BAD ENDING
         MVC   GU03DATM(8),MDYAREA+2    MOVE CONVERTED DATE TO COMMENT
*
         MVC   WKYYDDDF(4),GU01DATC     MOVE DATE CREATED TO WK AREA
         BAL   RBAL,DATE                CONVERT THE DATE
         CLC   RETCDE(4),=F'0'          IF NOT GOOD RETURN
         BNE   PARET04                    GO TO BAD ENDING
         MVC   GU03DATC(8),MDYAREA+2    MOVE CONVERTED DATE TO COMMENT
*
         XR    R3,R3                    R3 CLEAR FOR INSERT
         ICM   R3,3,GU01TIMM            R3 HAS 0000HHMM
         SLL   R3,4                     R3 HAS 000HHMM0
         O     R3,=F'15'                R3 HAS 000HHMMF
         ST    R3,PACKHHMM              SAVE FOR EDIT
         MVC   HHMMAREA(7),HHMMMASK     INITIALIZE WITH EDIT MASK
         ED    HHMMAREA(7),PACKHHMM+1   CONVERT TO BBHH:MM
         MVC   GU03TIMM(5),HHMMAREA+2   MOVE TO COMMENT AREA
         MVI   GU03TIMM-1,C'-'          PUT HYPHEN ONTO LINE
*
         XR    R3,R3                    R3 CLEAR FOR INSERT
         IC    R3,GU01VER               R3 HAS BINARY VERSION NUMBER
         ZAP   PACKDUBL(8),PACK0        CLEAR WORK DOUBLEWORD
         CVD   R3,PACKDUBL              PACKDUBL HAS 00000000 0000VVVC
         UNPK  VMAREA(3),PACKDUBL+6(2)  CONVERT TO 999
         MVC   GU03VER(3),VMAREA        SAVE IN COMMENT AREA
         OI    GU03VER+2,X'F0'          KILL THE SIGN
*
         XR    R3,R3                    R3 CLEAR FOR INSERT
         IC    R3,GU01MOD               R3 HAS BINARY MOD NUMBER
         ZAP   PACKDUBL(8),PACK0        CLEAR WORK DOUBLEWORD
         CVD   R3,PACKDUBL              PACKDUBL HAS 00000000 00000MMC
         UNPK  VMAREA(2),PACKDUBL+6(2)  CONVERT TO 99
         MVC   GU03MOD(2),VMAREA        SAVE IN COMMENT AREA
         OI    GU03MOD+1,X'F0'          KILL THE SIGN
         MVI   GU03MOD-1,C'.'           PUT PERIOD INTO COMMENT AREA
*
         XR    R3,R3                    R3 CLEAR FOR INSERT
         ICM   R3,3,GU01INIT            R3 HAS BINARY INITIAL LINES
         ZAP   PACKDUBL(8),PACK0        CLEAR WORK DOUBLEWORD
         CVD   R3,PACKDUBL              PACKDUBL HAS 00000000 00NNNNNC
         UNPK  VMAREA(5),PACKDUBL+5(3)  CONVERT TO 99999
         MVC   GU03INIT(5),VMAREA       SAVE IN COMMENT AREA
         OI    GU03INIT+4,X'F0'         KILL THE SIGN
*
         B     PAEND
*
PARET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     PAEND                    BRANCH TO ENDING
*
PAEND    L     RBAL,PARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  CONVERT PACKED 00YYDDDF IN WKYYDDDF TO EDITED DISPLAY IN      ***
***  MDYAREA.                                                      ***
***                                                                ***
**********************************************************************
DATE     ST    RBAL,DARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
DALEAPX  NI    SWITCH0,X'FF'-X'08'      TURN LEAPYEAR SWITCH OFF
         ICM   R3,15,WKYYDDDF           R3 HAS 00YYDDDF
         SRA   R3,16                    R3 HAS 000000YY
         SLA   R3,4                     R3 HAS 00000YY0
         O     R3,=F'15'                R3 HAS 00000YYF
         ST    R3,PACKYY                SAVE IT
         ZAP   PACKDUBL(8),PACKYY       PUT YYF INTO DUBLWRD WK AREA
         CVB   R3,PACKDUBL              R3 HAS BINARY YEAR
         XR    R2,R2                    CLEAR R2 FOR DIVIDE
         D     R2,=F'4'                 DIVIDE YEAR BY 4
         LTR   R2,R2                    IF A LEAPYEAR
         BNZ   *+8                        THEN
         OI    SWITCH0,X'08'                TURN ON LEAPYEAR SWITCH
*
DARANGE  XC    PACKDUBL,PACKDUBL        CLEAR WORK DOUBLEWORD
         MVC   PACKDUBL+6(2),WKYYDDDF+2 PACKDUBL IS 00000000 0000DDDF
         CVB   R3,PACKDUBL              R3 HAS BINARY DDD
         C     R3,=F'1'                 IF LESS THAN ONE
         BL    DARET04                    GO TO BAD ENDING
         C     R3,=F'366'               IF GREATER THAN 366
         BH    DARET04                    GO TO BAD ENDING
         TM    SWITCH0,X'08'            IF NOT LEAPYEAR
         BO    *+12                       AND
         C     R3,=F'365'                 IF GREATER THAN 365
         BH    DARET04                      GO TO BAD ENDING
*
DALEAPP1 TM    SWITCH0,X'08'            IF NOT LEAPYEAR
         BO    *+16                       AND
         C     R3,=F'60'                  IF MARCH 1 OR LATER
         BL    *+8                          THEN
         LA    R3,1(0,R3)                     ADD 1 TO SKIP 2/29
*
DATABL   BCTR  R3,0                     R3 -1 TO CONVERT TO OFFSET
         SLL   R3,1                     R3 IS NOW OFFSET INTO TABLE
         LA    R3,DADTABLE(R3)          R3 POINTS TO TABLE ENTRY
         LH    R3,0(0,R3)               R3 HAS 0000MMDD
         SLA   R3,12                    R3 HAS 0MMDD000
         O     R3,PACKYY                R3 HAS 0MMDDYYF
         ST    R3,MMDDYYF               MOVE TO STORAGE FOR EDIT
         MVC   MDYAREA(10),MDYMASK      INITIALIZE WITH EDIT MASK
         ED    MDYAREA(10),MMDDYYF      CONVERT TO BBMM/DD/YY
*
         B     DAEND
*
DARET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     DAEND                    BRANCH TO ENDING
*
DAEND    L     RBAL,DARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
***********************************************************************
***                                                                 ***
***      FIXED DATA AREA                                            ***
***                                                                 ***
***********************************************************************
F0       DC    F'0'                     CONSTANT
BLANKS   DC    CL80' '                  LITERAL
X00S     DC    XL40'00'                 LITERAL
PACK0    DC    PL4'+0'                  CONSTANT
*
*                    F Z M M / D D / Y Y
MDYMASK  DC    XL10'40212020612020612020'  EDIT MASK FOR MM/DD/YY
*
*                   F Z H H : M M
HHMMMASK DC    XL7'402120207A2020'      EDIT MASK FOR HH:MM
*
         LTORG
         EJECT
DADTABLE DS    0D        366-ELEMENT MMDD TABLE
JAN  DC    XL2'0101,0102,0103,0104,0105,0106,0107,0108,0109,0110'
     DC    XL2'0111,0112,0113,0114,0115,0116,0117,0118,0119,0120'
     DC    XL2'0121,0122,0123,0124,0125,0126,0127,0128,0129,0130,0131'
FEB  DC    XL2'0201,0202,0203,0204,0205,0206,0207,0208,0209,0210'
     DC    XL2'0211,0212,0213,0214,0215,0216,0217,0218,0219,0220'
     DC    XL2'0221,0222,0223,0224,0225,0226,0227,0228'
LEAP DC    XL2'0229'
MAR  DC    XL2'0301,0302,0303,0304,0305,0306,0307,0308,0309,0310'
     DC    XL2'0311,0312,0313,0314,0315,0316,0317,0318,0319,0320'
     DC    XL2'0321,0322,0323,0324,0325,0326,0327,0328,0329,0330,0331'
APR  DC    XL2'0401,0402,0403,0404,0405,0406,0407,0408,0409,0410'
     DC    XL2'0411,0412,0413,0414,0415,0416,0417,0418,0419,0420'
     DC    XL2'0421,0422,0423,0424,0425,0426,0427,0428,0429,0430'
MAY  DC    XL2'0501,0502,0503,0504,0505,0506,0507,0508,0509,0510'
     DC    XL2'0511,0512,0513,0514,0515,0516,0517,0518,0519,0520'
     DC    XL2'0521,0522,0523,0524,0525,0526,0527,0528,0529,0530,0531'
JUN  DC    XL2'0601,0602,0603,0604,0605,0606,0607,0608,0609,0610'
     DC    XL2'0611,0612,0613,0614,0615,0616,0617,0618,0619,0620'
     DC    XL2'0621,0622,0623,0624,0625,0626,0627,0628,0629,0630'
JUL  DC    XL2'0701,0702,0703,0704,0705,0706,0707,0708,0709,0710'
     DC    XL2'0711,0712,0713,0714,0715,0716,0717,0718,0719,0720'
     DC    XL2'0721,0722,0723,0724,0725,0726,0727,0728,0729,0730,0731'
AUG  DC    XL2'0801,0802,0803,0804,0805,0806,0807,0808,0809,0810'
     DC    XL2'0811,0812,0813,0814,0815,0816,0817,0818,0819,0820'
     DC    XL2'0821,0822,0823,0824,0825,0826,0827,0828,0829,0830,0831'
SEP  DC    XL2'0901,0902,0903,0904,0905,0906,0907,0908,0909,0910'
     DC    XL2'0911,0912,0913,0914,0915,0916,0917,0918,0919,0920'
     DC    XL2'0921,0922,0923,0924,0925,0926,0927,0928,0929,0930'
OCT  DC    XL2'1001,1002,1003,1004,1005,1006,1007,1008,1009,1010'
     DC    XL2'1011,1012,1013,1014,1015,1016,1017,1018,1019,1020'
     DC    XL2'1021,1022,1023,1024,1025,1026,1027,1028,1029,1030,1031'
NOV  DC    XL2'1101,1102,1103,1104,1105,1106,1107,1108,1109,1110'
     DC    XL2'1111,1112,1113,1114,1115,1116,1117,1118,1119,1120'
     DC    XL2'1121,1122,1123,1124,1125,1126,1127,1128,1129,1130'
DEC  DC    XL2'1201,1202,1203,1204,1205,1206,1207,1208,1209,1210'
     DC    XL2'1211,1212,1213,1214,1215,1216,1217,1218,1219,1220'
     DC    XL2'1221,1222,1223,1224,1225,1226,1227,1228,1229,1230,1231'
*
         EJECT
***********************************************************************
***                                                                 ***
***      DSECT FOR MY SAVE AREA AND VARIABLES.                      ***
***                                                                 ***
***********************************************************************
WORKD    DSECT
MYSAVE   DS    18F                      MY REGISTER SAVE AREA
COMPCODE DS    F                        PROGRAM COMPLETION CODE
RETCDE   DS    F                        INTERNAL RETURN CODE
R1SAVE   DS    F                        INITIAL VALUE IN R1
*
PARBALSV DS    F                        RETURN ADDRESS SAVE AREA
DARBALSV DS    F                        RETURN ADDRESS SAVE AREA
*
SWITCH0  DS    B'00000000'              SWITCH AREA 0
*                ....1...               X'08' - LEAPYEAR
*                1111.111               (UNUSED)
*
PACKYY   DS    0F                       00000YYF
         DS    PL4                        REFORMAT
*
PACKHHMM DS    0F                       000HHMMF
         DS    PL4                        REFORMAT
*
VMAREA   DS    CL5                      WORK AREA FOR VERS,MOD,INIT
*
PACKDUBL DS    0D                       DOUBLEWORD FOR CVB/CVD
         DS    PL8                        REFORMAT
*
MDYAREA  DS    CL10                     BBMM/DD/YY
WKYYDDDF DS    F                        WORK AREA FOR DATE
MMDDYYF  DS    F                        0MMDDYYF
HHMMAREA DS    CL7                      BBHH:MM
*
         DS    0D
WORKDLEN EQU   *-WORKD
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAPPING DSECTS                                             ***
***                                                                 ***
***********************************************************************
PANCOM   GTEUM03 DSECT=YES              PANVALET COMMENT AREA
PANCOMLN EQU   *-PANCOM
*
DIRENTRY GTEUM01 DSECT=YES              ISPF STATS DIRECTORY ENTRY
*
         EJECT
***********************************************************************
***                                                                 ***
***      REGISTER EQUATES                                           ***
***                                                                 ***
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         END
