         PRINT GEN
         LCLC  &MYNAME                  LOCAL CONSTANT
*
&MYNAME  SETC  'GTEUA3T'                CSECT NAME
RBASE    EQU   12                       BASE REGISTER FOR CSECT
RBAL     EQU   10                       BAL REGISTER
*
         TITLE '&MYNAME'                LISTING TITLE
***********************************************************************
***                                                                 ***
***   CODE TO TEST GTEUA30                                          ***
***                                                                 ***
***********************************************************************
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS ENTERING PROGRAM                       ***
***                                                                 ***
***********************************************************************
&MYNAME  CSECT ,
         STM   R14,R12,12(R13)          SAVE REGS TO CALLER S.A.
         B     (BEGIN-&MYNAME)(R15)     BRANCH AROUND EYECATCHER
         DC    A(L'NAME)                LENGTH OF CSECT NAME
NAME     DC    C'&MYNAME'               CSECT NAME
         DC    C' &SYSDATE &SYSTIME '   ASSEMBLY DATE/TIME STAMP
BEGIN    LR    RBASE,R15                LOAD BASE REGISTER
         USING &MYNAME,RBASE              ADDRESSABILITY
         PRINT NOGEN
         GETMAIN R,LV=WORKDLEN          GET SAVE/WORK AREA
         ST    R1,8(0,R13)              MY S.A. ADDR INTO CALLER S.A.
         ST    R13,4(0,R1)              CALLER S.A. ADDR INTO MY S.A.
         LR    R13,R1                   R13 POINTS TO MY S.A.
         USING WORKD,R13                ADDRESSABILITY OF SAVE AREA
         L     R1,4(0,R13)              R1 POINTS TO CALLER S.A.
         LM    R15,R1,16(R1)            R15 R0 AND R1 ARE RESTORED
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAINLINE ROUTINE                                           ***
***                                                                 ***
***********************************************************************
MAIN     EQU   *                        BEGIN MAINLINE ROUTINE
         ST    R1,R1SAVE                SAVE INITIAL R1
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE
*
         L     R2,R1SAVE                R2 POINTS TO PARM LIST
         L     R2,0(0,R2)               R2 POINTS TO PARM
         LH    R3,0(0,R2)               R3 HAS LENGTH OF PARM
         LTR   R3,R3                    IF ZERO LENGTH
         BZ    ENDING                     GO TO ENDING
         MVC   PARMIN(8),BLANKS         CLEAR PARMIN FIELD
         BCTR  R3,0                     LENGTH -1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   PARMIN(0),2(R2)            MOVE PARM CHARACTERS
         EX    R3,*-6                   EXECUTE THE MOVE
*
         PRINT GEN
*
         MVC   PDSDCB(PDSDCBL),PDSDCBD     INITIALIZE DCB
*
         MVC   PDOPENL(PDOPENLN),PDOPEND INITIALIZE OPEN LIST
         OPEN  (PDSDCB,(INPUT)),MF=(E,PDOPENL) OPEN IT
*
         MVC   GU01NAM(8),PARMIN        MOVE NAME INTO DIR ENTRY
*
         LA    R15,PDSDCB               ADDR OF DCB
         ST    R15,PARM30+00              INTO PARM LIST
         LA    R15,DIRENTRY             ADDR OF DIRECTORY ENTRY
         ST    R15,PARM30+04              INTO PARM LIST
         OI    PARM30+04,X'80'          MARK END OF LIST
         LA    R1,PARM30                ADDRESS OF PARM LIST
         L     R15,=V(GTEUA30)          ADDRESS OF SUBROUTINE
         BALR  R14,R15                  PERFORM THE SUBROUTINE
*
         MVC   PDCLOSL(PDCLOSLN),PDCLOSD INITIALIZE CLOSE LIST
         CLOSE (PDSDCB),MF=(E,PDCLOSL)  CLOSE IT
*
         PRINT NOGEN
*
END00    LA    R15,0                    SET COMPLETION CODE 00
         ST    R15,COMPCODE               INTO STORAGE
         B     ENDING                   GO TO ENDING
*
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS EXITING PROGRAM                        ***
***                                                                 ***
***********************************************************************
ENDING   L     R14,COMPCODE             R14 SAVES COMP CODE
         LR    R1,R13                   R1 SAVES ADDR OF MY S.A.
         L     R13,4(0,R1)              R13 RESTORED, PTR CALLER S.A.
         FREEMAIN R,LV=WORKDLEN,A=(R1)  FREE MY SAVE/WORK AREA
         LR    R15,R14                  R15 SET TO COMP CODE
         LM    R0,R12,20(R13)           R0-R12 RESTORED
         L     R14,12(0,R13)            R14 RESTORED
         MVI   12(R13),X'FF'            SET COMPLETION SIGNAL
         BR    R14                      RETURN TO CALLER
*
         EJECT
***********************************************************************
***                                                                 ***
***      FIXED DATA AREA                                            ***
***                                                                 ***
***********************************************************************
F0       DC    F'0'                     CONSTANT
BLANKS   DC    CL80' '                  LITERAL
X00S     DC    XL40'00'                 LITERAL
         PRINT GEN
PDOPEND  OPEN  (,),MF=L
PDCLOSD  CLOSE (),MF=L
*
PDSDCBD  DCB   DDNAME=PDS,DSORG=PO,MACRF=R
*
         LTORG
*
         EJECT
***********************************************************************
***                                                                 ***
***      DSECT FOR MY SAVE AREA AND VARIABLES.                      ***
***                                                                 ***
***********************************************************************
WORKD    DSECT
MYSAVE   DS    18F                      MY REGISTER SAVE AREA
COMPCODE DS    F                        PROGRAM COMPLETION CODE
RETCDE   DS    F                        INTERNAL RETURN CODE
R1SAVE   DS    F                        INITIAL VALUE IN R1
PARMIN   DS    CL8                      INPUT PARM
DIRENTRY GTEUM01 DSECT=NO               DIRECTORY ENTRY
PARM30   DS    2F                       PARM LIST FOR GTEUA30
*
         PRINT GEN
PDOPENL  OPEN  (,),MF=L
PDOPENLN EQU *-PDOPENL
PDCLOSL  CLOSE (),MF=L
PDCLOSLN EQU *-PDCLOSL
         PRINT NOGEN
PDSDCB   DCB   DDNAME=PDS,DSORG=PO,MACRF=R
PDSDCBL  EQU   *-PDSDCB
*
         DS    0D
WORKDLEN EQU   *-WORKD
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAPPING DSECTS                                             ***
***                                                                 ***
***********************************************************************
         IHADCB DSORG=PO,DEVD=DA
*
         EJECT
***********************************************************************
***                                                                 ***
***      REGISTER EQUATES                                           ***
***                                                                 ***
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         END
