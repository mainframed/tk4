         PRINT GEN
         LCLC  &MYNAME                  LOCAL CONSTANT
*
&MYNAME  SETC  'GTEUA5T'                CSECT NAME
RBASE    EQU   12                       BASE REGISTER FOR CSECT
RBAL     EQU   10                       BAL REGISTER
*
         TITLE '&MYNAME'                LISTING TITLE
***********************************************************************
***                                                                 ***
***      CODE TO TEST GTEUA50.                                      ***
***                                                                 ***
***********************************************************************
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS ENTERING PROGRAM                       ***
***                                                                 ***
***********************************************************************
&MYNAME  CSECT ,
         STM   R14,R12,12(R13)          SAVE REGS TO CALLER S.A.
         B     (BEGIN-&MYNAME)(R15)     BRANCH AROUND EYECATCHER
         DC    A(L'NAME)                LENGTH OF CSECT NAME
NAME     DC    C'&MYNAME'               CSECT NAME
         DC    C' &SYSDATE &SYSTIME '   ASSEMBLY DATE/TIME STAMP
BEGIN    LR    RBASE,R15                LOAD BASE REGISTER
         USING &MYNAME,RBASE              ADDRESSABILITY
         PRINT NOGEN
         GETMAIN R,LV=WORKDLEN          GET SAVE/WORK AREA
         ST    R1,8(0,R13)              MY S.A. ADDR INTO CALLER S.A.
         ST    R13,4(0,R1)              CALLER S.A. ADDR INTO MY S.A.
         LR    R13,R1                   R13 POINTS TO MY S.A.
         USING WORKD,R13                ADDRESSABILITY OF SAVE AREA
         L     R1,4(0,R13)              R1 POINTS TO CALLER S.A.
         LM    R15,R1,16(R1)            R15 R0 AND R1 ARE RESTORED
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAINLINE ROUTINE                                           ***
***                                                                 ***
***********************************************************************
MAIN     EQU   *                        BEGIN MAINLINE ROUTINE
         ST    R1,R1SAVE                SAVE INITIAL R1
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE
         MVI   SWITCH0,B'00000000'      INITIALIZE SWITCH AREA
*
         MVC   DIRENTRY(DIRLEN),DUMYDIR INITIALIZE ENTRY
         LA    R15,PANCOM               ADDR OF PANVALET COMMENT
         ST    R15,PARM50+00              INTO PARM LIST
         LA    R15,DIRENTRY             ADDR OF DIRECTORY ENTRY
         ST    R15,PARM50+04              INTO PARM LIST
         OI    PARM50+04,X'80'          MARK END OF LIST
         LA    R1,PARM50                POINT TO PARM LIST
         L     R15,=V(GTEUA50)          BASE ADDR OF SUBPROGRAM
         BALR  R14,R15                  CALL THE SUBPROGRAM
*
END00    LA    R15,0                    SET COMPLETION CODE 00
         ST    R15,COMPCODE               INTO STORAGE
         B     ENDING                   GO TO ENDING
*
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS EXITING PROGRAM                        ***
***                                                                 ***
***********************************************************************
ENDING   L     R14,COMPCODE             R14 SAVES COMP CODE
         LR    R1,R13                   R1 SAVES ADDR OF MY S.A.
         L     R13,4(0,R1)              R13 RESTORED, PTR CALLER S.A.
         FREEMAIN R,LV=WORKDLEN,A=(R1)  FREE MY SAVE/WORK AREA
         LR    R15,R14                  R15 SET TO COMP CODE
         LM    R0,R12,20(R13)           R0-R12 RESTORED
         L     R14,12(0,R13)            R14 RESTORED
         MVI   12(R13),X'FF'            SET COMPLETION SIGNAL
         BR    R14                      RETURN TO CALLER
*
         EJECT
***********************************************************************
***                                                                 ***
***      FIXED DATA AREA                                            ***
***                                                                 ***
***********************************************************************
F0       DC    F'0'                     CONSTANT
BLANKS   DC    CL80' '                  LITERAL
X00S     DC    XL40'00'                 LITERAL
*
DUMYDIR  DS    0D                     , ISPF STATS PDS DIRECTORY ENTRY
DUMYNAM  DC    CL8'TESTNAME'            MEMBER NAME
DUMYTTR  DC    XL3'000000'              PDS VALUE 'TTR'
DUMYC    DC    X'0F'                    PDS VALUE 'C'
DUMYVER  DC    X'03'         003        ISPF VERSION NUMBER (BIN)
DUMYMOD  DC    X'59'         89         ISPF MOD NUMBER (BIN)
         DC    XL2'0000'                (UNUSED, X'0000')
DUMYDATC DC    XL4'0084100F' 04/09/84   ISPF DATE CREATED (PACK)
DUMYDATM DC    XL4'0085100F' 04/10/85   ISPF DATE MODIFIED (PACK)
DUMYTIMM DC    XL2'1357'     13:57      ISPF TIME MODIFIED (PK NOSIGN)
DUMYSIZE DC    XL2'22'       34         ISPF SIZE (BIN)
DUMYINIT DC    XL2'3039'     12345      ISPF INITIAL SIZE (BIN)
DUMYMODL DC    XL2'11'       17         ISPF COUNT OF MOD LINES (BIN)
DUMYID   DC    CL7'CHUCKIE'             ISPF USERID
         DC    CL3'   '                 (UNUSED X'404040')
*
         LTORG
*
         EJECT
***********************************************************************
***                                                                 ***
***      DSECT FOR MY SAVE AREA AND VARIABLES.                      ***
***                                                                 ***
***********************************************************************
WORKD    DSECT
MYSAVE   DS    18F                      MY REGISTER SAVE AREA
COMPCODE DS    F                        PROGRAM COMPLETION CODE
RETCDE   DS    F                        INTERNAL RETURN CODE
R1SAVE   DS    F                        INITIAL VALUE IN R1
*
SWITCH0  DS    B'00000000'              SWITCH AREA
*                1.......               X'80' - (NOT USED)
*                .1111111               (NOT USED)
DIRENTRY GTEUM01 DSECT=NO               PDS DIRECTORY ENTRY
DIRLEN   EQU   *-DIRENTRY
*
PANCOM   GTEUM03 DSECT=NO               PANVALET COMMENT AREA
*
PARM50   DS    2F                       PARMS FOR SUBPROGRAM
*
         DS    0D
WORKDLEN EQU   *-WORKD
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAPPING DSECTS                                             ***
***                                                                 ***
***********************************************************************
*
         EJECT
***********************************************************************
***                                                                 ***
***      REGISTER EQUATES                                           ***
***                                                                 ***
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         END
