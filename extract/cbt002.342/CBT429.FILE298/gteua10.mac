         PRINT GEN
         LCLC  &MYNAME                  LOCAL CONSTANT
*
&MYNAME  SETC  'GTEUA10'                CSECT NAME
RBASE    EQU   12                       BASE REGISTER FOR CSECT
RBAL     EQU   11                       BAL REGISTER
*
         TITLE '&MYNAME'                LISTING TITLE
**********************************************************************
***                                                                ***
***                   GTEUA10                                      ***
***                                                                ***
***  DESCRIPTION:                                                  ***
***                                                             GU00301
***   RESTORE and UPDATE functions -                            GU00301
***                                                             GU00301
***    The function of this program is to accept a character       ***
***    string, such as 'NAME', then find information from the      ***
***    Panvalet library directory for the highest name in the      ***
***    pattern 'NAMEnn', where 'nn' is '01' to '99'.  This         ***
***    program will return comment and directory information,      ***
***    and will also return the next name in the sequence.         ***
***                                                                ***
***    If the input name is ARGLE, and the Panvalet library        ***
***    has members ARGLE01, ARGLE02, and ARGLE03, this program     ***
***    will return directory and comment information about         ***
***    ARGLE03, and will return the name ARGLE04 as the next name  ***
***    in the sequence.                                            ***
***                                                                ***
***    The first parm is a 12-character field with the function    ***
***    name (e.g. UPDATE, RESTORE) in it.  The sequence, above,    ***
***    is used when the function is UPDATE, returning information  ***
***    about the current entry, and computing the name of the      ***
***    next entry.  If RESTORE is selected, the assumption is that ***
***    the input name is not a pattern, but is the precise name of ***
***    the member the caller is interested in.  Panvalet directory ***
***    and comment information is returned about that member, and  ***
***    the next name in the sequence is not computed.              ***
***                                                             GU00301
***   STATS function -                                          GU00301
***                                                             GU00301
***    The STATS function will display statistics from the      GU00301
***    Panvalet library and the PDS.  If the name provided is   GU00301
***    ARGLE, and if ARGLE01, ARGLE02, and ARGLE03 exist in the GU00301
***    Panvalet library, information about all of these members GU00301
***    will be displayed on the SYSPRINT file.  This is the     GU00301
***    only time SYSPRINT and PDS are opened, and the only time GU00301
***    there is a call to GTEUA50 to convert ISPF statistics    GU00301
***    into Panvalet comment statistics.                        GU00301
***                                                                ***
***  The following sample code would invoke this program:          ***
***                                                                ***
*** *------------------------------------------------------------  ***
***          LA    R15,FUNCTION             ADDR OF PARM           ***
***          ST    R15,PARM10+00              INTO PARM LIST       ***
***          LA    R15,PANDIR               ADDR OF PARM           ***
***          ST    R15,PARM10+04              INTO PARM LIST       ***
***          LA    R15,PANCOM               ADDR OF PARM           ***
***          ST    R15,PARM10+08              INTO PARM LIST       ***
***          LA    R15,NEXTNAME             ADDR OF PARM           ***
***          ST    R15,PARM10+12              INTO PARM LIST       ***
***          LA    R15,DDNAME               ADDR OF PARM           ***
***          ST    R15,PARM10+16              INTO PARM LIST       ***
***          OI    PARM10+16,X'80'          MARK END OF LIST       ***
***          LA    R1,PARM10                R1 POINTS TO LIST      ***
***          L     R15,=V(GTEUA10)          R15 POINTS TO SUBPROG  ***
***          BALR  R14,R15                  CALL THE SUBPROGRAM    ***
***          ST    R15,COMPCODE             SAVE THE COMP CODE     ***
*** *------------------------------------------------------------  ***
*** WORKD    DSECT                                                 ***
*** *                                                              ***
*** COMPCODE DS    F                        PROGRAM COMPL CODE     ***
*** PARM10   DS    5A                       PARM LIST FOR SUBPROG  ***
*** *                                                              ***
*** PANDIR   GTEUM04 DSECT=NO               PANVALET DIR ENTRY     ***
*** PANCOM   GTEUM03 DSECT=NO               PANVALET COMMNT ENTRY  ***
*** FUNCTION DS    CL12                    'RESTORE' 'UPDATE' ETC. ***
*** NEXTNAME DS    CL10                     NEXT PANVALET NAME     ***
*** DDNAME   DS    CL8                      DDNAME FOR PAN FILE    ***
*** *------------------------------------------------------------  ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  FUNCTION:                                                     ***
***                                                                ***
***  1.  LOAD the PAM routines into storage, and compute the       ***
***      entry point addresses for POPEN, PSRCH, PREAD and PCLOSE. ***
***  2.  Open the Panvalet library.                                ***
***  3.  Search the directory for the last name in the sequence,   ***
***      retrieving the directory information and the comments.    ***
***  4.  Compute the next name in the sequence.                    ***
***  5.  Close the Panvalet library.                               ***
***  6.  DELETE the PAM routines.                                  ***
***                                                                ***
***                                                                ***
***  NOTES:                                                        ***
***                                                                ***
***  1. The PAM routines are dynamically loaded as a single module ***
***     named PAM.  POPEN, PSRCH, PREAD, and PCLOSE are alternate  ***
***     entry points.  The PAM routines are reusable, but not      ***
***     reenterable.  Since this program is reenterable, the PAM   ***
***     routines should not be hard linked into the program.       ***
***                                                                ***
***  RETURN CODES:                                                 ***
***                                                                ***
***     00 - Directory/comment for current entry is returned, and  ***
***          next name has been computed.  Note, next name is not  ***
***          computed for RESTORE function.                        ***
***                                                                ***
***     04 - There was no current entry, and next name has been    ***
***          computed (e.g. NAME01).  Note, next name is not       ***
***          computed for RESTORE function.                        ***
***                                                                ***
***     12 - Directory/comment for current entry is returned,      ***
***          but next name could not be computed because the       ***
***          current name is too high (e.g. current: NAME99).      ***
***                                                                ***
***     16 - Failure to open or close the Panvalet library.        ***
***          Any returned information is unreliable.               ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  REGISTER USAGE:                                               ***
***                                                                ***
***     R0-R3   - Linkage conventions, work, macros.               ***
***     R4-R5   - Work registers, not used to pass information     ***
***               between routines.                                ***
***     R6      - (unused)                                         ***
***     R7      - (unused)                                         ***
***     R8      - Base address of PANDIR, Panvalet directory info. ***
***     R9      - Base address of PANCOM, Panvalet comment info.   ***
***     R10     - Base address of NEXTNAME, returned computed name.***
***     R11     - Linkage register used with BAL.                  ***
***     R12     - Base register of program code.                   ***
***     R13     - Linkage conventions, base of WORKD area.         ***
***     R14-R15 - Linkage conventions, work, macros.               ***
***                                                                ***
***                                                                ***
***  NON-IBM MACROS USED:                                          ***
***                                                                ***
***     GTEUM01  - Map BLDL entry list                          GU00301
***     GTEUM02  - Map STOW directory entry                     GU00301
***     GTEUM03  - Map the Panvalet comment area.                  ***
***     GTEUM04  - Map the Panvalet directory entry                ***
***                                                                ***
***  ASSEMBLER OPTIONS:       RENT                                 ***
***  LINKAGE EDITOR OPTIONS:  RENT, REUS, REFR                     ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  CULPRIT:                                                      ***
***                                                                ***
***     Chuck Hoffman, IBM Systems Programming                     ***
***     GTE Laboratories, Technical Computation Center             ***
***     40 Sylvan Road                                             ***
***     Waltham, Massachusetts  02254                              ***
***                                                                ***
***                     617/466-2131    V/N 679-2131               ***
***                                                                ***
**********************************************************************
         SPACE 3
**********************************************************************
***                                                                ***
***  MAINTENANCE HISTORY:                                          ***
***                                                                ***
***  1.  A.  Initial installation.                                 ***
***                                                                ***
***  2.  A.  Add the STATS function.                            GU00301
***                                                                ***
***  3.  A.  Fix loop caused by not checking return code from   GU00302
***          PSRCH after a search for an invalid Panvalet name. GU00302
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
**********************************************************************
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS ENTERING PROGRAM                       ***
***                                                                 ***
***********************************************************************
&MYNAME  CSECT ,
         STM   R14,R12,12(R13)          SAVE REGS TO CALLER S.A.
         B     (BEGIN-&MYNAME)(R15)     BRANCH AROUND EYECATCHER
         DC    A(L'NAME)                LENGTH OF CSECT NAME
NAME     DC    C'&MYNAME'               CSECT NAME
         DC    C' &SYSDATE &SYSTIME '   ASSEMBLY DATE/TIME STAMP
BEGIN    LR    RBASE,R15                LOAD BASE REGISTER
         USING &MYNAME,RBASE              ADDRESSABILITY
         PRINT NOGEN
         GETMAIN R,LV=WORKDLEN          GET SAVE/WORK AREA
         ST    R1,8(0,R13)              MY S.A. ADDR INTO CALLER S.A.
         ST    R13,4(0,R1)              CALLER S.A. ADDR INTO MY S.A.
         LR    R13,R1                   R13 POINTS TO MY S.A.
         USING WORKD,R13                ADDRESSABILITY OF SAVE AREA
         L     R1,4(0,R13)              R1 POINTS TO CALLER S.A.
         LM    R15,R1,16(R1)            R15 R0 AND R1 ARE RESTORED
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAINLINE ROUTINE                                           ***
***                                                                 ***
***********************************************************************
MAIN     EQU   *                        BEGIN MAINLINE ROUTINE
         ST    R1,R1SAVE                SAVE INITIAL R1
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE
         MVI   SWITCH0,B'00000000'      INITIALIZE SWITCH AREA
*
MAPARM   L     R2,0(0,R1)               R2 POINTS TO FUNCTION PARM
         MVC   MAFUNCT,0(R2)              MOVE TO STORAGE
         LM    R8,R10,4(R1)             NEXT 3 PARM ADDRESSES TO REGS
         USING PANDIR,R8                ADDRESSABILITY
         USING PANCOM,R9                ADDRESSABILITY
         USING NEXTNAME,R10             ADDRESSABILITY
         L     R2,16(0,R1)              R2 POINTS TO PARM #5
         MVC   DDNAME(8),0(R2)          COPY THE DDNAME IN
*                                                               GU00301
MASOPEN  CLC   MAFUNCT(12),MASTATS      IF FUNCTION IS 'STATS'  GU00301
         BNE   *+8                        THEN                  GU00301
         BAL   RBAL,SOPEN                 OPEN SYSPRINT, PANDD1 GU00301
*
MALOAD   BAL   RBAL,LOAD                LOAD THE PANVALET ROUTINES
*
MAOPEN   BAL   RBAL,OPEN                OPEN THE PANVALET LIBRARY
         CLC   RETCDE,F0                IF RETURN BAD
         BNE   END16                      GO TO HIGH RETURN
*
MASEARCH BAL   RBAL,SEARCH              FIND THE PANVALET INFORMATION
         CLC   RETCDE,F0                IF MODULE NOT FOUND
         BE    MANEXT                     THEN
         MVC   COMPCODE,F4                  SET COMPLETION CODE TO 4
*
MANEXT   BAL   RBAL,NEXT                CREATE THE NEXT NAME
         CLC   RETCDE,F0                IF NEXTNAME COULDNT BE COMPUTED
         BNE   END12                      GO TO HIGH RETURN
*
MACLOSE  BAL   RBAL,CLOSE               CLOSE THE PANVALET LIBRARY
         CLC   RETCDE,F0                IF RETURN BAD
         BNE   END16                      GO TO HIGH RETURN
*
MADELETE BAL   RBAL,DELETE              DELETE THE PANVALET ROUTINES
*
         B     END00                    GO TO ENDING
*
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS EXITING PROGRAM                        ***
***                                                                 ***
***********************************************************************
END00    LA    R15,0                    IF COMPLETION CODE
         C     R15,COMPCODE               NOT GREATER THAN 00
         BNH   *+8                          THEN
         ST    R15,COMPCODE                   RESET IT
         B     ENDING                   GO TO ENDING
*
END04    LA    R15,4                    IF COMPLETION CODE
         C     R15,COMPCODE               NOT GREATER THAN 04
         BNH   *+8                          THEN
         ST    R15,COMPCODE                   RESET IT
         B     ENDING                   GO TO ENDING
*
END12    LA    R15,12                   IF COMPLETION CODE
         C     R15,COMPCODE               NOT GREATER THAN 12
         BNH   *+8                          THEN
         ST    R15,COMPCODE                   RESET IT
         B     ENDING                   GO TO ENDING
*
END16    LA    R15,16                   IF COMPLETION CODE
         C     R15,COMPCODE               NOT GREATER THAN 16
         BNH   *+8                          THEN
         ST    R15,COMPCODE                   RESET IT
         B     ENDING                   GO TO ENDING
*
ENDING   CLC   MAFUNCT(12),MASTATS      IF FUNCTION IS 'STATS'  GU00301
         BNE   *+8                        THEN                  GU00301
         BAL   RBAL,SCLOSE                CLOSE SYSPRINT PANDD1 GU00301
*                                                               GU00301
         L     R14,COMPCODE             R14 SAVES COMP CODE     GU00301
         LR    R1,R13                   R1 SAVES ADDR OF MY S.A.
         L     R13,4(0,R1)              R13 RESTORED, PTR CALLER S.A.
         FREEMAIN R,LV=WORKDLEN,A=(R1)  FREE MY SAVE/WORK AREA
         LR    R15,R14                  R15 SET TO COMP CODE
         LM    R0,R12,20(R13)           R0-R12 RESTORED
         L     R14,12(0,R13)            R14 RESTORED
         MVI   12(R13),X'FF'            SET COMPLETION SIGNAL
         BR    R14                      RETURN TO CALLER
*                                                               GU00301
         EJECT                                                  GU00301
*************************************************************** GU00301
***                                                         *** GU00301
***   OPEN THE SYSPRINT AND PDS FILE USED BY STATS FUNCTION *** GU00301
***                                                         *** GU00301
*************************************************************** GU00301
SOPEN    ST    RBAL,SORBALSV            SAVE RETURN ADDRESS     GU00301
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO  GU00301
*                                                               GU00301
         MVC   SYSDCB(SYSDCBLN),SYSDCBD  INITIAL SYSPRINT DCB   GU00301
         MVC   PDSDCB(PDSDCBLN),PDSDCBD  INITIAL PDS DCB        GU00301
*                                                               GU00301
         MVC   OPENSL(OPENSLLN),OPENSD   INITIAL OPEN PARM LIST GU00301
*                                                               GU00301
         OPEN  (SYSDCB,(OUTPUT),PDSDCB,(INPUT)),MF=(E,OPENSL)   GU00301
*                                                               GU00301
         B     SOEND                                            GU00301
*                                                               GU00301
SORET04  LA    R15,X'04'                RETURN CODE TO R15      GU00301
         ST    R15,RETCDE               SAVE IT                 GU00301
         B     SOEND                    BRANCH TO ENDING        GU00301
*                                                               GU00301
SOEND    L     RBAL,SORBALSV            RESTORE RETURN ADDRESS  GU00301
         BR    RBAL                     RETURN                  GU00301
*                                                               GU00301
         SPACE 3                                                GU00301
*************************************************************** GU00301
***                                                         *** GU00301
***  CLOSE THE SYSPRINT AND PDS FILE USED BY STATS FUNCTION *** GU00301
***                                                         *** GU00301
*************************************************************** GU00301
SCLOSE   ST    RBAL,SCRBALSV            SAVE RETURN ADDRESS     GU00301
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO  GU00301
*                                                               GU00301
         MVC   CLOSSL(CLOSSLLN),CLOSSD   INITIAL CLOSE PARM LST GU00301
*                                                               GU00301
         CLOSE (SYSDCB,,PDSDCB),MF=(E,CLOSSL)  CLOSE            GU00301
*                                                               GU00301
         B     SCEND                                            GU00301
*                                                               GU00301
SCRET04  LA    R15,X'04'                RETURN CODE TO R15      GU00301
         ST    R15,RETCDE               SAVE IT                 GU00301
         B     SCEND                    BRANCH TO ENDING        GU00301
*                                                               GU00301
SCEND    L     RBAL,SCRBALSV            RESTORE RETURN ADDRESS  GU00301
         BR    RBAL                     RETURN                  GU00301
*
         EJECT
**********************************************************************
***                                                                ***
***   THE OFFSETS FOR COMPUTING THE ADDRESSES OF POPEN,            ***
***   PSRCH, PREAD AND PCLOSE, BELOW, ARE TAKEN FROM A MAP         ***
***   OF THE PAM MODULE AFTER INSTALLATION.  THE DYNAMIC           ***
***   LOAD, AND THE SUBSEQUENT COMPUTATIONS ARE PERFORMED          ***
***   BECAUSE THE PAM MODULE IS NOT REENTERABLE, BUT I WANT        ***
***   THIS MODULE TO BE REENTERABLE.  CHUCK.                       ***
***                                                                ***
***  P.S.--                                                        ***
***   YOU MAY HAVE TO CHANGE THIS CODE WHEN NEW RELEASES OF        ***
***   THE PANVALET PRODUCT ARE INSTALLED.  SORRY.  CHUCK.          ***
***                                                                ***
**********************************************************************
LOAD     ST    RBAL,LORBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LOAD  EP=PAM                   R0 POINTS TO PAM (POPEN)
         LR    R3,R0                    R3 GETS PTR, BECAUSE OF LA
         LA    R2,X'0000'(0,R3)         R2 POINTS TO POPEN
         ST    R2,VPOPEN                  SAVE THE ADDRESS
         LA    R2,X'00C8'(0,R3)         R2 POINTS TO PSRCH
         ST    R2,VPSRCH                  SAVE THE ADDRESS
         LA    R2,X'00F0'(0,R3)         R2 POINTS TO PREAD
         ST    R2,VPREAD                  SAVE THE ADDRESS
         LA    R2,X'0118'(0,R3)         R2 POINTS TO PCLOSE
         ST    R2,VPCLOSE                 SAVE THE ADDRESS
*
         B     LOEND
*
LORET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     LOEND                    BRANCH TO ENDING
*
LOEND    L     RBAL,LORBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  DELETE THE PANVALET LOAD MODULE                               ***
***                                                                ***
**********************************************************************
DELETE   ST    RBAL,DERBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         DELETE  EP=PAM                 DELETE THE LOAD MODULE
         XC    VPOPEN,VPOPEN            CLEAR THE POINTER
         XC    VPSRCH,VPSRCH            CLEAR THE POINTER
         XC    VPREAD,VPREAD            CLEAR THE POINTER
         XC    VPCLOSE,VPCLOSE          CLEAR THE POINTER
*
         B     DEEND
*
DERET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     DEEND                    BRANCH TO ENDING
*
DEEND    L     RBAL,DERBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  OPEN THE PANVALET FILE                                        ***
***                                                                ***
**********************************************************************
OPEN     ST    RBAL,OPRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
OPINIT   XC    ACTION,ACTION            CLEAR THE ACTION FIELD
         MVC   BACKUP(8),NOENTRY        SET THE BACKUP FIELD
*
OPPARMS  LA    R15,ACTION
         ST    R15,OPPL+00
         LA    R15,DDNAME
         ST    R15,OPPL+04
         LA    R15,BACKUP
         ST    R15,OPPL+08
         OI    OPPL+08,X'80'            MARK END OF LIST
*
OPCALL   L     R15,VPOPEN               LOAD ADDRESS OF POPEN
         LA    R1,OPPL                  POINT TO PARMS
         BALR  R14,R15                  CALL POPEN
         LTR   R15,R15                  IF RETURN BAD
         BNZ   OPRET04                    GO TO HIGH RETURN
*
         B     OPEND
*
OPRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     OPEND                    BRANCH TO ENDING
*
OPEND    L     RBAL,OPRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  CLOSE THE PANVALET FILE                                       ***
***                                                                ***
**********************************************************************
CLOSE    ST    RBAL,CLRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    ACTION,ACTION            CLEAR THE ACTION FIELD
         LA    R15,ACTION               ADDR OF PARM
         ST    R15,CLPL+00                INTO PARM LIST
         OI    CLPL+00,X'80'            MARK THE END OF THE LIST
         LA    R1,CLPL                  R1 POINTS TO PARM LIST
         L     R15,VPCLOSE              R15 POINTS TO CLOSE ROUTINE
         BALR  R14,R15                  CALL THE ROUTINE
         LTR   R15,R15                  IF BAD RETURN
         BNZ   CLRET04                    GO TO HIGH RETURN CODE
*
         B     CLEND
*
CLRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     CLEND                    BRANCH TO ENDING
*
CLEND    L     RBAL,CLRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  SEARCH FOR THE NEXT PANVALET NAME                             ***
***                                                                ***
**********************************************************************
SEARCH   ST    RBAL,SERBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
SEINIT   MVC   NAME1(22),BLANKS         CLEAR NAME1 FIELD
         MVC   NAME2(11),BLANKS         CLEAR NAME2 FIELD
         MVC   STNAME(10),GU04NAML      SAVE NAME FOR STATS     GU00301
*
SEINIT1  CLC   MAFUNCT,MARESTOR         IF THIS IS A RESTORE
         BNE   SEINIT2                    THEN
         MVC   NAME1(10),GU04NAML           USE THE INPUT NAME AS IS
         MVC   NAME2(10),GU04NAML           DITTO
         B     SENAMES                      BYPASS NAME GENERATOR CODE
*
SEINIT2  LA    R2,8                     SET COUNTER TO EIGHT
         LA    R3,GU04NAML              R3 POINTS TO INPUT NAME
         BCTR  R3,0                     R3 POINTS JUST BEFORE NAME
         LA    R4,NAME1                 R4 POINTS TO BEGIN NAME
         LA    R5,NAME2                 R5 POINTS TO END NAME
*
SELOOP1  LA    R3,1(0,R3)               R3 POINTS TO NEXT CHARACTER
         CLI   0(R3),X'40'              IF BLANK CHARACTER
         BE    SESUFFIX                   GO OUT OF LOOP
         MVC   0(1,R4),0(R3)            MOVE CHARACTER TO NAME1
         MVC   0(1,R5),0(R3)            MOVE CHARACTER TO NAME2
         LA    R4,1(0,R4)               INCR NAME1 POINTER
         LA    R5,1(0,R5)               INCR NAME2 POINTER
         BCT   R2,SELOOP1               LOOP BACK UP
*
SESUFFIX LA    R1,8                     ORIGINAL LENGTH OF SEARCH
         SR    R1,R2                    R1 AS OFFSET OF SUFFIX
         ST    R1,SEOFFSET              SAVE IT
         MVC   0(2,R4),=C'00'           MOVE SUFFIX '00' TO NAME1
         MVC   0(2,R5),=C'99'           MOVE SUFFIX '99' TO NAME2
*
SENAMES  MVC   LASTNAME(10),NAME1       SAVE THE FIRST NAME
         MVC   NEXTNAME(10),BLANKS      CLEAR THE NEXT NAME
*
SECPARMS MVC   PANDIR(PANDIRLN),BLANKS  CLEAR DIRECTORY ENTRY
         MVC   PANCOM(PANCOMLN),BLANKS  CLEAR COMMENT
*
SEPARMS  XC    ACTION,ACTION            CLEAR ACTION FIELD
         MVC   MYDIR(PANDIRLN),BLANKS   CLEAR DIRECTORY ENTRY
         MVC   MYCOM(PANCOMLN),BLANKS   CLEAR COMMENT
         MVC   SUBSET(27),NOENTRY       INITIALIZE SUBSET PARM
*
SELIST   LA    R15,ACTION               ADDR OF PARM
         ST    R15,SEPL+00                INTO LIST
         LA    R15,MYDIR                ADDR OF PARM
         ST    R15,SEPL+04                INTO LIST
         LA    R15,NAME1                ADDR OF PARM
         ST    R15,SEPL+08                INTO LIST
         LA    R15,NAME2                ADDR OF PARM
         ST    R15,SEPL+12                INTO LIST
         LA    R15,MYCOM                ADDR OF PARM
         ST    R15,SEPL+16                INTO LIST
         LA    R15,SUBSET               ADDR OF PARM
         ST    R15,SEPL+20                INTO LIST
         OI    SEPL+20,X'80'            MARK END OF LIST
*
SECALL   NI    SWITCH0,X'FF'-X'04'      TURN OFF MODULE FOUND SWITCH
         NI    SWITCH2,X'FF'-X'80'-X'40'  TURN OFF STATS SW'S   GU00301
SELOOP2  LA    R1,SEPL                  R1 POINTS TO PARM LIST
         L     R15,VPSRCH               R15 POINTS TO PAN ROUTINE
         BALR  R14,R15                  CALL PAN ROUTINE
         LTR   R15,R15                  IF BAD RETURN           GU00302
         BNZ   SECALLX                    EXIT THE LOOP         GU00302
         CLC   MYDIR(2),=C'$*'          IF END OF SEARCH
         BE    SECALLX                    BRANCH OUT
         OI    SWITCH0,X'04'            TURN ON MODULE FOUND SWITCH
         MVC   LASTNAME(10),MYDIR+(GU04NAML-PANDIR)  SAVE LAST NAME
         MVC   PANDIR(PANDIRLN),MYDIR   SAVE THE DIRECTORY ENTRY
         MVC   PANCOM(PANCOMLN),MYCOM   SAVE THE COMMENT
         CLC   MAFUNCT,MASTATS          IF THIS IS STATS FUNCT. GU00301
         BNE   *+8                        THEN                  GU00301
         BAL   RBAL,STATS                   PERFORM STATS PROC  GU00301
         B     SELOOP2                  LOOP BACK UP
*                                                               GU00301
SECALLX  CLC   MAFUNCT,MASTATS          IF THIS IS STATS FUNCT. GU00301
         BNE   *+12                       THEN                  GU00301
         OI    SWITCH2,X'40'                ON PDS STATS SWITCH GU00301
         BAL   RBAL,STATS                   PERFORM STATS PROC  GU00301
         TM    SWITCH0,X'04'            IF MODULE WAS NOT FOUND GU00301
         BNO   SERET04                    GO TO HIGH ENDING
*
SERESTL  CLC   MAFUNCT,MARESTOR         IF THIS IS NOT RESTORE
         BNE   SEEND                      GO TO ENDING
*
         LA    R1,7                     R1 IS COUNTER FOR LOOP
SELOOP3  LA    R2,NAME2+2(R1)           R2 POINTS TO TRAILING CHAR
         CLI   0(R2),X'40'              IF NONBLANK
         BNE   *+8                        GET OUT OF LOOP
         BCT   R1,SELOOP3               ELSE LOOP BACK UP
         LA    R2,NAME2+2(R1)           R2 POINTS TO TRAILING CHAR
         BCTR  R2,0                     R2 POINTS TO SUFFIX
         MVC   0(2,R2),=C'99'           SET ENDING SUFFIX TO '99'
*
SELOOP4  LA    R1,SEPL                  R1 POINTS TO PARM LIST
         L     R15,VPSRCH               R15 POINTS TO PAN ROUTINE
         BALR  R14,R15                  CALL PAN ROUTINE
         CLC   MYDIR(2),=C'$*'          IF END OF SEARCH
         BE    SELOOP4X                   BRANCH OUT
         MVC   LASTNAME(10),MYDIR+(GU04NAML-PANDIR)  SAVE LAST NAME
         B     SELOOP4                  LOOP BACK UP
SELOOP4X NOPR  0                        LOOP EXIT
*
         B     SEEND
*
SERET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     SEEND                    BRANCH TO ENDING
*
SEEND    L     RBAL,SERBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*                                                               GU00301
         EJECT                                                  GU00301
*************************************************************** GU00301
***                                                         *** GU00301
***  DISPLAY PANVALET STATISTICS                            *** GU00301
***                                                         *** GU00301
*************************************************************** GU00301
STATS    ST    RBAL,STRBALSV            SAVE RETURN ADDRESS     GU00301
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO  GU00301
*                                                               GU00301
         TM    SWITCH2,X'40'            IF GET PDS STATS SW ON  GU00301
         BO    STPDS                      GO TO THAT CODE INSTE GU00301
*                                                               GU00301
STHEAD   TM    SWITCH2,X'80'            IF HEADING NOT PRINTED  GU00301
         BO    STOUT                      THEN                  GU00301
         MVC   STATLINE(133),BLANKS         CLEAR THE LINE      GU00301
         MVI   STATLINE,C'1'                FORM EJECT TO LINE  GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG3         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG1         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG2         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         OI    SWITCH2,X'80'                TURN ON HEADING PRI GU00301
*                                                               GU00301
STOUT    MVC   STATLINE(133),BLANKS     CLEAR THE OUTPUT LINE   GU00301
         MVC   STNAML(10),GU04NAML      PAN NAME TO LINE        GU00301
         MVC   STSTMT(5),GU04STMT+3     STATEMENT COUNT TO LINE GU00301
         CLI   GU03VER+L'GU03VER,C'.'   IF THERE IS NO DOT FOLL GU00301
         BE    STOUT2                     THEN ASSUME NO ISPF S GU00301
         MVC   STVERMOD(50),GU03VER         JUST PUT COMMENT TO GU00301
         B     STOUT3                       AND SKIP THE REST   GU00301
STOUT2   MVC   STVERMOD(6),GU03VER      VER.MD TO LINE          GU00301
         MVC   STDATC(8),GU03DATC       DATE CREATED TO LINE    GU00301
         MVC   STDATIMM(14),GU03DATM    DATE-TIME MODIFIED TO L GU00301
         MVC   STINIT(5),GU03INIT       INITIAL SIZE TO LINE    GU00301
         MVC   STID(7),GU03ID           LOGONID/JOBNAME TO LINE GU00301
STOUT3   BAL   R2,STPRINT               PRINT THE LINE          GU00301
*                                                               GU00301
         B     STEND                                            GU00301
*                                                               GU00301
STPDS    TM    SWITCH2,X'80'            IF PANVALET STUFF FOUND GU00301
         BO    STPDS2                     GO PRINT PDS STUFF    GU00301
         MVC   STATLINE(133),BLANKS     ELSE CLEAR THE LINE     GU00301
         MVI   STATLINE,C'1'                FORM EJECT TO LINE  GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG3     ELSE HEADINGS TO LINE   GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG1         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG2         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),BLANKS         BLANKS TO LINE      GU00301
         MVC   STATLINE(13),=C' (None found)'  'NOT FOUND' MSG  GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
*                                                               GU00301
STPDS2   MVC   STATLINE(133),BLANKS         BLANKS TO LINE      GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG4         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG1         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
         MVC   STATLINE(133),STHDG2         MOVE HEADINGS TO LI GU00301
         BAL   R2,STPRINT                   PRINT THE LINE      GU00301
*                                                               GU00301
         XC    PDSBLDL(PDSBLDLL),PDSBLDL    CLEAR BLDL LIST     GU00301
         MVC   GU02FF(2),=X'0001'           SET COUNT TO 1 ENTR GU00301
         LA    R2,PDSBLDLL-4(0,0)           LENGTH OF ISPF ENTR GU00301
         STH   R2,GU02LL                      INTO BLDL AREA    GU00301
         MVC   GU02NAM(8),STNAME            MOVE NAME TO BLDL L GU00301
         BLDL  PDSDCB,PDSBLDL               GET A BLDL ENTRY    GU00301
*                                                               GU00301
STX      LTR   R15,R15                      IF MODULE NOT FOUND GU00301
         BZ    STALIAS                        THEN              GU00301
         MVC   STATLINE(133),BLANKS             CLEAR THE LINE  GU00301
         MVC   STNAML(8),STNAME                 NAME TO LINE    GU00301
         MVC   STATLINE+13(11),=C'(Not found)'  TEXT TO LINE    GU00301
         BAL   R2,STPRINT                       PRINT THE LINE  GU00301
         MVC   STATLINE(133),BLANKS             CLEAR THE LINE  GU00301
         BAL   R2,STPRINT                       PRINT THE LINE  GU00301
         B     STRET04                          GO TO HIGH ENDI GU00301
*                                                               GU00301
STALIAS  TM    GU02C,X'80'                  IF ENTRY IS AN ALIA GU00301
         BNO   STLENGTH                       THEN              GU00301
         MVC   STATLINE(133),BLANKS             CLEAR THE LINE  GU00301
         MVC   STNAML(8),STNAME                 NAME TO LINE    GU00301
         MVC   STATLINE+13(7),=C'(Alias)'       TEXT TO LINE    GU00301
         BAL   R2,STPRINT                       PRINT THE LINE  GU00301
         MVC   STATLINE(133),BLANKS             CLEAR THE LINE  GU00301
         BAL   R2,STPRINT                       PRINT THE LINE  GU00301
         B     STRET04                          GO TO HIGH ENDI GU00301
*                                                               GU00301
STLENGTH CLI   GU02C,X'0F'                  IF 'C' DOESNT LOOK  GU00301
         BE    STSTATS                        THEN              GU00301
         MVC   STATLINE(133),BLANKS             CLEAR THE LINE  GU00301
         MVC   STNAML(8),STNAME                 NAME TO LINE    GU00301
         MVC   STATLINE+13(10),=C'(No stats)'   TEXT TO LINE    GU00301
         BAL   R2,STPRINT                       PRINT THE LINE  GU00301
         MVC   STATLINE(133),BLANKS             CLEAR THE LINE  GU00301
         BAL   R2,STPRINT                       PRINT THE LINE  GU00301
         B     STRET04                          GO TO HIGH ENDI GU00301
*                                                               GU00301
STSTATS  MVC   GU01NAM(11),GU02NAM         NAME, TTR            GU00301
         MVC   GU01C(28),GU02C             C THRU ID            GU00301
         MVC   GU01ID+L'GU01ID(3),BLANKS   ENDING BLANKS        GU00301
         MVC   PANCOM(PANCOMLN),BLANKS  CLEAR PANVALET COMMENT  GU00301
         LA    R15,PANCOM               ADDR OF PAN COMMENT ARE GU00301
         ST    R15,A50PARM                INTO PARM LIST        GU00301
         LA    R15,GU01NAM              ADDR OF PDS ENTRY       GU00301
         ST    R15,A50PARM+04             INTO PARM LIST        GU00301
         OI    A50PARM+04,X'80'         MARK END OF LIST        GU00301
         LA    R1,A50PARM               POINT TO PARM LIST      GU00301
         L     R15,=V(GTEUA50)          ADDR OF SUBROUTINE      GU00301
         BALR  R14,R15                  CALL THE SUBROUTINE     GU00301
         LTR   R15,R15                  IF RETURN BAD           GU00301
         BNZ   STRET04                    GO TO HIGH ENDING     GU00301
*                                                               GU00301
STSTATP  MVC   STATLINE(133),BLANKS     CLEAR THE LINE          GU00301
         MVC   STNAML(8),GU01NAM        NAME TO LINE            GU00301
         MVC   STVERMOD(6),GU03VER      VER AND MOD             GU00301
         MVC   STDATC(8),GU03DATC       DATE CREATED            GU00301
         MVC   STDATIMM(14),GU03DATM    DATE-TIME MODIFIED      GU00301
         MVC   STINIT(5),GU03INIT       INITIAL SIZE            GU00301
         MVC   STID(7),GU03ID           ID                      GU00301
         XR    R2,R2                    R2 CLEAR FOR INSERT     GU00301
         ICM   R2,3,GU01SIZE            R2 HAS SIZE IN BINARY   GU00301
         CVD   R2,PACKDUBL              PACKDUBL HAS SIZE, PACK GU00301
         OI    PACKDUBL+7,X'0F'           KILL THE SIGN         GU00301
         UNPK  STSTMT(5),PACKDUBL+5(3)  ZONE DECIMAL TO LINE    GU00301
         BAL   R2,STPRINT               PRINT THE LINE          GU00301
         MVC   STATLINE(133),BLANKS     CLEAR THE LINE          GU00301
         BAL   R2,STPRINT               PRINT THE LINE          GU00301
*                                                               GU00301
STPDSX   B     STEND                                            GU00301
*                                                               GU00301
STRET04  LA    R15,X'04'                RETURN CODE TO R15      GU00301
         ST    R15,RETCDE               SAVE IT                 GU00301
         B     STEND                    BRANCH TO ENDING        GU00301
*                                                               GU00301
STEND    L     RBAL,STRBALSV            RESTORE RETURN ADDRESS  GU00301
         BR    RBAL                     RETURN                  GU00301
*                                                               GU00301
STPRINT  PUT   SYSDCB,STATLINE                                  GU00301
         BR    R2                                               GU00301
*
         EJECT
**********************************************************************
***                                                                ***
***  COMPUTE THE NEXT PANVALET NAME                                ***
***                                                                ***
**********************************************************************
NEXT     ST    RBAL,NERBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
NEREST   CLC   MAFUNCT(12),MARESTOR     IF FUNCTION IS RESTORE
         BE    NENEXT2                    THEN FIND SUFFIX BY WAY #2
         B     NENEXT                   ELSE FIND SUFFIX BY WAY #1
*
NENEXT   MVC   NEXTNAME(10),LASTNAME    INITIALIZE NEXT NAME FIELD
         LA    R3,NEXTNAME              R3 POINTS TO BEGINNING
         A     R3,SEOFFSET              R3 POINTS TO SUFFIX
         B     NEADD1                   GO COMPUTE NEW SUFFIX
*
NENEXT2  MVC   NEXTNAME(10),LASTNAME    INITIALIZE NEXT NAME FIELD
         LA    R1,10                    R1 IS COUNTER FOR LOOP
         LA    R2,NEXTNAME              R2 POINTS TO NAME
         BCTR  R2,0                     R2 POINTS JUST BEFORE NAME
NELOOP1  LA    R2,1(0,R2)               R2 POINTS TO NEXT CHARACTER
         CLI   0(R2),X'40'              IF CHARACTER IS BLANK
         BE    NELOOP1X                   GET OUT OF LOOP
         BCT   R1,NELOOP1               ELSE LOOP BACK UP
         LA    R2,1(0,R2)               FALL THRU, R2 POINTS BEYOND SUF
NELOOP1X LR    R3,R2                    R3 POINTS JUST BEYOND SUFFIX
         S     R3,F2                    R3 POINTS TO SUFFIX
         B     NEADD1                   GO COMPUTE NEW SUFFIX
*
NEADD1   ZAP   PACKWORK(4),PACK0(4)     INITIALIZE PACKED FIELD
         PACK  PACKWORK(4),0(2,R3)      SUFFIX TO PACKED NUMBER
         AP    PACKWORK(4),PACK1(4)     ADD ONE
         CP    PACKWORK(4),PACK100      IF 100 OR GREATER
         BNL   NERET04                    GO TO ERROR
         UNPK  0(2,R3),PACKWORK(4)      UNPACK THE NEW NUMBER
         OI    1(R3),X'F0'              KILL THE SIGN
*
         B     NEEND
*
NERET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     NEEND                    BRANCH TO ENDING
*
NEEND    L     RBAL,NERBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
***********************************************************************
***                                                                 ***
***      FIXED DATA AREA                                            ***
***                                                                 ***
***********************************************************************
F0       DC    F'0'                     CONSTANT
F2       DC    F'2'                     CONSTANT
F4       DC    F'4'                     CONSTANT
BLANKS   DC    CL133' '                 LITERAL                 GU00301
X00S     DC    XL40'00'                 LITERAL
NOENTRY  DC    CL32'NO-ENTRY'           LITERAL
PACK0    DC    PL4'+0'                  CONSTANT
PACK1    DC    PL4'+1'                  CONSTANT
PACK100  DC    PL4'+100'                CONSTANT
MARESTOR DC    CL12'RESTORE'            LITERAL
MASTATS  DC    CL12'STATS'              LITERAL                 GU00301
STHDG1   DS    0CL133                   HEADING FOR OUTPUT PRIN GU00301
         DC    CL01' '                                          GU00301
         DC    CL12'Name'                                       GU00301
         DC    CL07'Size'                                       GU00301
         DC    CL08'Ver.Md'                                     GU00301
         DC    CL10'Created'                                    GU00301
         DC    CL16'   Modified'                                GU00301
         DC    CL07'Init'                                       GU00301
         DC    CL08'ID'                                         GU00301
         DC    CL64' '                                          GU00301
*                                                               GU00301
STHDG2   DS    0CL133                   HEADING FOR OUTPUT PRIN GU00301
         DC    CL01' '                                          GU00301
         DC    CL12'----------'                                 GU00301
         DC    CL07'-----'                                      GU00301
         DC    CL08'------'                                     GU00301
         DC    CL10'--------'                                   GU00301
         DC    CL16'--------------'                             GU00301
         DC    CL07'-----'                                      GU00301
         DC    CL08'-------'                                    GU00301
         DC    CL64' '                                          GU00301
*                                                               GU00301
STHDG3   DC    CL24' '                                          GU00301
         DC    CL21'PANVALET STATISTICS'                        GU00301
         DC    CL88' '                                          GU00301
*                                                               GU00301
STHDG4   DC    CL27' '                                          GU00301
         DC    CL16'PDS STATISTICS'                             GU00301
         DC    CL90' '                                          GU00301
*                                                               GU00301
OPENSD   OPEN  (,,,),MF=L               OPEN PARM LIST          GU00301
CLOSSD   CLOSE (,,),MF=L                CLOSE PARM LIST         GU00301
*                                                               GU00301
SYSDCBD  DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=133,BLKSIZE=1330, GU00301X
               RECFM=FBA,MACRF=(PM)                             GU00301
*                                                               GU00301
PDSDCBD  DCB   DDNAME=PDS,DSORG=PO,MACRF=(R)                    GU00301
*
         LTORG
*
         EJECT
***********************************************************************
***                                                                 ***
***      DSECT FOR MY SAVE AREA AND VARIABLES.                      ***
***                                                                 ***
***********************************************************************
WORKD    DSECT
MYSAVE   DS    18F                      MY REGISTER SAVE AREA
COMPCODE DS    F                        PROGRAM COMPLETION CODE
RETCDE   DS    F                        INTERNAL RETURN CODE
R1SAVE   DS    F                        INITIAL VALUE IN R1
*
LORBALSV DS    F                        RETURN ADDRESS SAVE AREA
DERBALSV DS    F                        RETURN ADDRESS SAVE AREA
OPRBALSV DS    F                        RETURN ADDRESS SAVE AREA
CLRBALSV DS    F                        RETURN ADDRESS SAVE AREA
SERBALSV DS    F                        RETURN ADDRESS SAVE AREA
NERBALSV DS    F                        RETURN ADDRESS SAVE AREA
STRBALSV DS    F                        RETURN ADDRESS SAVE ARE GU00301
SORBALSV DS    F                        RETURN ADDRESS SAVE ARE GU00301
SCRBALSV DS    F                        RETURN ADDRESS SAVE ARE GU00301
*
SWITCH0  DS    B'00000000'              SWITCH AREA
*                .....1..               X'40' - PANVALET ENTRY FOUND
*                11111.11               (NOT USED)
*                                                               GU00301
SWITCH2  DS    B'00000000'              SWITCH AREA             GU00301
*                1.......               X'80' - HEADINGS PRINTE GU00301
*                .1......               X'40' - GET PDS STATS   GU00301
*                ..111111               (NOT USED)              GU00301
*
OPPL     DS    3A                       PARM LIST FOR POPEN
SEPL     DS    6A                       PARM LIST FOR PSEARCH
CLPL     DS    1A                       PARM LIST FOR PCLOSE
*
VPOPEN   DS    A                        ADDRESS OF POPEN ENTRY POINT
VPSRCH   DS    A                        ADDRESS OF PSRCH ENTRY POINT
VPREAD   DS    A                        ADDRESS OF PREAD ENTRY POINT
VPCLOSE  DS    A                        ADDRESS OF PCLOSE ENTRY POINT
*
MAFUNCT  DS    CL12                     FUNCTION PARM
*
ACTION   DS    F                        PANVALET ACTION PARM
DDNAME   DS    CL8                      PANVALET LIBRARY DDNAME
BACKUP   DS    CL8                      PANVALET BACKUP PARM
NAME1    DS    CL22                     PANVALET NAME1 PARM
NAME2    DS    CL11                     PANVALET NAME2 PARM
SUBSET   DS    CL27                     PANVALET SUBSET PARM
*
LASTNAME DS    CL10                     LAST PANVALET NAME USED
SEOFFSET DS    F                        OFFSET OF SUFFIX IN NAME
*
PACKWORK DS    PL4                      WORK AREA
*
MYDIR    DS    CL80                     WORK AREA FOR PAN DIR ENTRY
MYCOM    DS    CL52                     WORK AREA FOR PAN COMMENT
*                                                               GU00301
STNAME   DS    CL10                     SAVED PDS NAME FOR STAT GU00301
*                                                               GU00301
STATLINE DS    0CL133                   STATISTICS OUTPUT LINE  GU00301
         DS    CL1                                              GU00301
STNAML   DS    CL10                       PAN MEMBER NAME       GU00301
         DS    CL2                                              GU00301
STSTMT   DS    CL5                        NUMBER OF STATEMENTS  GU00301
         DS    CL2                                              GU00301
STVERMOD DS    CL6                        COMMENT VER.MD        GU00301
         DS    CL2                                              GU00301
STDATC   DS    CL8                        COMMENT DATE CREATED  GU00301
         DS    CL2                                              GU00301
STDATIMM DS    CL14                       COMMENT DATE-TIME MOD GU00301
         DS    CL2                                              GU00301
STINIT   DS    CL5                        COMMENT INITIAL SIZE  GU00301
         DS    CL2                                              GU00301
STID     DS    CL7                        COMMENT LOGONID/JOBNA GU00301
         DS    CL1                                              GU00301
         DS    CL64                                             GU00301
*                                                               GU00301
PACKDUBL DS    D                        DOUBLE WORD FOR CVD     GU00301
*                                                               GU00301
A50PARM  DS    2F                       PARMLIST FOR SUBROUTINE GU00301
*                                                               GU00301
OPENSL   OPEN  (,,,),MF=L               OPEN PARM LIST          GU00301
OPENSLLN EQU   *-OPENSL                                         GU00301
CLOSSL   CLOSE (,,),MF=L                CLOSE PARM LIST         GU00301
CLOSSLLN EQU   *-CLOSSL                                         GU00301
*                                                               GU00301
SYSDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=133,BLKSIZE=1330, GU00301X
               RECFM=FBA,MACRF=(PM)                             GU00301
SYSDCBLN EQU   *-SYSDCB                                         GU00301
*                                                               GU00301
PDSDCB   DCB   DDNAME=PDS,DSORG=PO,MACRF=(R)                    GU00301
PDSDCBLN EQU   *-PDSDCB                                         GU00301
*                                                               GU00301
PDSSTOW  GTEUM01 DSECT=NO               DIRECTORY ENTRY (FOR ST GU00301
PDSSTOWL EQU   *-PDSSTOW                                        GU00301
*                                                               GU00301
PDSBLDL  GTEUM02 DSECT=NO               DIRECTORY ENTRY (FROM B GU00301
PDSBLDLL EQU   *-PDSBLDL                                        GU00301
*                                                               GU00301
         DS    0D
WORKDLEN EQU   *-WORKD
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAPPING DSECTS                                             ***
***                                                                 ***
***********************************************************************
PANDIR   GTEUM04  DSECT=YES             PANVALET DIRECTORY ENTRY
PANDIRLN EQU   *-PANDIR
PANCOM   GTEUM03  DSECT=YES             PANVALET COMMENT AREA
PANCOMLN EQU   *-PANCOM
NEXTNAME DSECT                          NAME OF NEXT PANVALET NAME
         DS    CL10
*
         EJECT
***********************************************************************
***                                                                 ***
***      REGISTER EQUATES                                           ***
***                                                                 ***
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         END
