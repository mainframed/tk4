//D061001X JOB (00000000,06009,10),RANGE,MSGCLASS=Q,NOTIFY=D061001
//*
//* REFER: D061001.SYSLOG(RANGE)
//* DOC  : COMPILE,LINK, AND TEST THE SYSLOG RANGE DETERMINATION PGM
//*
//ASSEMBLE EXEC PGM=IFOX00,PARM='OBJ,TERM'
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1700,(600,100))
//SYSUT2   DD DSN=&&SYSUT2,UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSUT3   DD DSN=&&SYSUT3,UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSTERM  DD SYSOUT=*
//SYSPUNCH DD DUMMY
//SYSGO    DD DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSPRINT DD SYSOUT=*
//SYSIN    DD *
*
*   REFER: D061001.SYSLOG(RANGE)
*
*    - THIS SUBSET PROGRAM WAS CREATED FOR THE SPECIFIC PURPOSE
*      OF SCANNING A GIVEN SYSLOG TO DETERMINE THE DATE/TIME
*      RANGE ENCOMPASSED.  IT IS USED IN THE ISPF SYSLOG APPLICATION
*      AS OPTION 3 UNDER THE 'SEARCH CURRENT' MENU. SEE ISPF TUTORIAL.
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
RANGE    CSECT
         STM   14,12,12(13)
         BALR  9,0
         USING *,9
         ST    13,SAVEAREA+4
         LA    12,SAVEAREA
         ST    12,8(13)
         LR    13,12
         OPEN  (SYSUT1,,SYSUT2,(OUTPUT))
* FOLLOWING (UP TO 'READ' LABEL) IS  ONE-TIME CODE
GETFIRST GET   SYSUT1
         LA    R1,4(R1)
         CLC   25(7,R1),=C'IEE136I'  IS THIS A 'D T' DATE STAMP?
         BNE   GETFIRST
         MVC   LOWDATE,62(R1)
         MVC   LOWHOUR,45(R1)
         MVC   HIDATE,62(R1)
         MVC   HIHOUR,45(R1)
         B     DATECHEK
READ     GET   SYSUT1             READ SYSLOG
         LA    R1,4(R1)           INCREMENT R1 BY 4 TO BYPASS RDW
         CLC   25(7,R1),=C'IEE136I'  IS THIS A 'D T' DATE STAMP?
         BNE   READ
DATECHEK CLC   62(3,R1),LOWDATE   THIS DATE LOWER THAN LOWEST SO FAR?
         BL    NEWLOW             YES.  WE HAVE A NEW LOW FOLKS
         BE    LOHRCHEK
         CLC   62(3,R1),HIDATE    NO. LET'S SEE IF WE HAVE A NEW HIGH
         BL    READ               NOPE.  KEEP READING.
         BE    HIHRCHEK           SAME DAY. SEE IF HOUR IS HIGHER
         MVC   HIDATE,62(R1)      WE HAVE A NEW HIGH DATE
         MVC   HIHOUR,45(R1)      ALSO SAVE NEW HIGH DATE'S HOUR
HIHRCHEK CLC   45(2,R1),HIHOUR    DO WE HAVE A NEW HIGH HOUR ALSO?
         BNH   READ               NOPE.  GO READ SOME MORE
         MVC   HIHOUR,45(R1)      MOVE NEW HIGH HOUR INTO IT'S NICHE.
         B     READ
NEWLOW   MVC   LOWDATE,62(R1)     SAVE NEW LOW DATE
         MVC   LOWHOUR,45(R1)     ALSO SAVE NEW LOW DATE'S HOUR
LOHRCHEK CLC   45(2,R1),LOWHOUR   DO WE HAVE A NEW LOW HOUR ?
         BNL   READ               NOPE.
         MVC   LOWHOUR,45(R1)     YES, HAUL IT ON IN HERE BELINDA.
         B     READ
EOF      MVC   WTO3+28(3),LOWDATE   MOVE LOWEST DATE TO WTO
         MVC   WTO3+37(2),LOWHOUR   MOVE LOWEST HOUR TO WTO
         MVC   WTO3+53(3),HIDATE    MOVE HIGHEST DATE TO WTO
         MVC   WTO3+62(2),HIHOUR    MOVE HIGHEST HOUR TO WTO
         WTO   ' ',ROUTCDE=11
         WTO   'THE SYSLOG DATA RANGE THAT WAS SEARCHED IS:',ROUTCDE=11
WTO3     WTO   '       - LOW   DATE=XXX TIME=XX  HIGH   DATE=XXX TIME=XX
               X',ROUTCDE=11
CLOSE    CLOSE (SYSUT1,,SYSUT2)   CLOSE FILES
         LA    R15,0              SET RETURN CODE TO ZERO
         L     13,SAVEAREA+4
         LM    0,12,20(13)
         L     14,12(13)
         BR    14
         EJECT
         LTORG
LOWDATE  DS    CL3
LOWHOUR  DS    CL2
HIDATE   DS    CL3
HIHOUR   DS    CL2
SAVEAREA DC    18F'0'
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=EOF,              X
               BLKSIZE=32760,LRECL=133,EROPT=SKP
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,BLKSIZE=137,RECFM=VBA,  X
               LRECL=133
         END
/*
//LINKEDT EXEC PGM=IEWL,COND=(4,LT),
//         PARM='LIST,XREF,LET',REGION=650K
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=SYSDA,SPACE=(1700,(400,50))
//SYSLMOD  DD  DISP=SHR,DSN=D061001.LOAD
//OBJSET   DD  DSN=&&OBJSET,DISP=(OLD,DELETE,DELETE)
//SYSLIN   DD  *
 INCLUDE OBJSET
 NAME RANGE(R)
//*
//SYSLOG   EXEC PGM=RANGE,REGION=900K,TIME=5
//STEPLIB DD DSN=D061001.LOAD,DISP=SHR
//SYSUT1 DD DSN=D061001.LOG387,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSUT2 DD SYSOUT=*
//SYSUDUMP  DD SYSOUT=*
