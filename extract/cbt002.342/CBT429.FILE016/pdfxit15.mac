//SYSRMSIP JOB  ,MSGLEVEL=(1,1),NOTIFY=SYSRMS,
//    MSGCLASS=T,CLASS=A,REGION=9700K
//ISPMDR   EXEC ASMHCL,
//           PARM.C='OBJECT',
//           PARM.L='MAP,XREF,LIST,RENT,REUS'
//C.SYSLIB DD DCB=BLKSIZE=12800
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//         DD DSN=SYSRMS.MACLIB,DISP=SHR
//         DD DSN=ISP.V2R3M0.ISPMACS,DISP=SHR,UNIT=3380,VOL=SER=MVS220
ISPDFLTS   ISPMTAIL   TROWDEF=1,CMDSTKSZ=512,EXITS=YES
//L.SYSLMOD DD DSN=SYS1.LPALIB,VOL=SER=MVS220,UNIT=3380,DISP=SHR
//L.SYSIN   DD *
    INCLUDE SYSLIB(ISPMAIN)
    ENTRY   ISPMDR
    NAME    ISPMAIN(R)
//L.SYSLIB  DD DSN=ISP.V2R3M0.ISPLOAD,DISP=SHR,UNIT=3380,VOL=SER=MVS220
//ISPXIT15 EXEC ASMHCL,
//           PARM.C='OBJECT',
//           PARM.L='MAP,XREF,LIST,RENT,REUS'
//C.SYSLIB DD DCB=BLKSIZE=12800
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//         DD DSN=SYSRMS.MACLIB,DISP=SHR
//         DD DSN=ISP.V2R3M0.ISPMACS,DISP=SHR,UNIT=3380,VOL=SER=MVS220
         PRINT NOGEN
ISPXIT15 ENTER1 RENT=YES,BASE=R12,CSECT=YES,WKBYTES=4096               X
               TITLE='ISPF/PDF EXIT 15 SCREEN DISPLAY &SYSDATE'
         @     ISPXIT15
         LA    R2,13(,R13)           GET TO LOCAL WORK AREA
         USING WORK,R2               AND MAKE IT
*******************************************************************
*                                                                 *
*                                                                 *
*    NAME:    ISPXIT15                                            *
*    DATE:    MARCH 1988                                          *
*    VERSION: 01.00                                               *
*    AUTHOR:  ROBERT M. SIRKIS                                    *
*             SCOTT AND WHITE MEMORIAL HOSPITAL                   *
*             2401 SOUTH 31ST STREET                              *
*             TEMPLE, TEXAS 76508                                 *
*             (817) 774 2800                                      *
*                                                                 *
*    FUNCTION: DETERMINE THE PRIMARY ISPF/PDF PRIMARY OPTION      *
*              MENU BASED UPON THE FIRST 3 CHARACTERS OF THE      *
*              TSO USERID.                                        *
*                                                                 *
*    LOGIC:    1.) SAVE INPUT PARMATERS FROM ISPF/PDF             *
*              2.) CHECK TO SEE IF PANEL IS ISR@PRIM, AND IF NOT  *
*                  EXIT WITH RETURN CODE OF 0.                    *
*              3.) CHECK LOGIN ID TABLE FOR MATCH ON              *
*                  FIRST 3 CHARACETERS OF JOBNAME.                *
*              4.) IF FOUND ALTER THE PANEL NAME IN               *
*                  PASSED LIST, AND SET RETURN CODE TO 4.         *
*              5.) ELSE SET DEFAULT TO USR@PRIM, A VERY LIMITED   *
*                  ISPF/PDF PRIMARY OPTION PANEL.                 *
*                                                                 *
*    NOTES:    THIS EXIT MUST BE ASSEMBLED AND LINKED AS          *
*              AMODE 31, AND RMODE ANY FOR MVS/XA.                *
*                                                                 *
*$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$*
*                                                                 *
*        R1     USED TO ADDRESS PARAMTERS FROM ISPF/PDF           *
*        R2     USED TO ADDRESS WORK AREA                         *
*        R3     USED TO ADDRESS LOGON ID TABLE                    *
*        R4     LOOP CONTROL/WORK REG.                            *
*        R12    BASE REGISTER                                     *
*                                                                 *
*******************************************************************
         EJECT
*
*        SAVE PARMLIST
*        AND MOVE PARAMETERS TO WORK AREA
*
         MVC   PARMLIST(40),0(R1)    SAVE PARMLIST
         ST    R1,@PRMLIST           SAVE PARMLIST ADDRESS
         L     R1,PARMLIST           GET EXIT ID
         MVC   ISPXITID,0(R1)        AND SAVE IT
         L     R1,PARMLIST+4         GET LOGON ID
         MVC   ISPUSRID,0(R1)        AND SAVE IT
         L     R1,PARMLIST+8         GET LOGICAL SCREEN ID
         MVC   ISPSCRID,0(R1)        AND SAVE IT
         L     R1,PARMLIST+12        GET ZENVIR DATA
         MVC   ISPZNVIR,0(R1)        AND SAVE IT
         L     R1,PARMLIST+16        GET LENGTH OF USER DATA
         MVC   ISPDATAL,0(R1)        AND SAVE IT
         L     R1,PARMLIST+20        GET ADDRESS OF USER DATA
         MVC   ISPDATAP,0(R1)        AND SAVE IT
         L     R1,PARMLIST+24        GET CURRENT PANEL ID
         MVC   ISPXPANL,0(R1)        AND SAVE IT
         L     R1,PARMLIST+28        GET MESSAGE PANEL ID
         MVC   ISPXMESG,0(R1)        AND SAVE IT
         L     R1,PARMLIST+32        GET CURSOR POSITION ON SCREEN
         MVC   ISPXCURS,0(R1)        AND SAVE IT
         L     R1,PARMLIST+36        GET CURSOR OFFSET
         MVC   ISPXCURO,0(R1)        AND SAVE IT
         L     R1,PARMLIST+40        GET CURRENT TABLE NAME
         MVC   ISPXTABL,0(R1)        AND SAVE IT
         L     R1,PARMLIST+44        GET FLAG BYTES
         MVC   ISPXFLAG(4),0(R1)     AND SAVE THEM
*
*        IS PANEL ISR@PRIM ?
*
         L     R1,PARMLIST+24        GET ADDRESS OF PANEL ID
         CLC   =C'ISR@PRIM',0(R1)    IF IT IS ISR@PRIM
         BNE   RETURN0               CONTINUE, WE WANT @PRIM ONLY
*
*        DETERMINE USERID
*
         L     R4,PANLTAB#           GET NUMBER OF TABLE ENTRIES
         LA    R3,PANLTAB            GET TABLE ADDRESS
PANLLOOP DS    0H
         CLC   0(3,R3),ISPUSRID      IS LOGONID MATCH  ?
         BE    PANLMOVE              YES
         LA    R3,11(,R3)            POP THE TABLE ADDRESS
         BCT   R4,PANLLOOP           AND TRY AGAIN
         LA    R3,=C'DMYUSR@PRIM'    GET ADDRESS OF USR PRIM
PANLMOVE DS    0H
         L     R1,PARMLIST+24        GET ADDRESS OF PANEL ID
         MVC   0(8,R1),3(R3)         MOVE TO PANELID
         B     RETURN4               INDICATE PANEL CHANGE
RETURN0  DS    0H
         SLR   R15,R15               SET NO PANEL CHANGE IN R15
         B     LEAVE                 AND EXIT
RETURN4  DS    0H
         LA    R15,4                 SET PANEL  CHANGE IN R15
LEAVE    DS    0H
         LEAVE RC=(R15)
PANLTAB  DS    0F
         DC    C'SYS',C'SYS@PRIM'        DEFAULT SYSTEMS PRIMARY PANEL
         DC    C'USR',C'USR@PRIM'        DEFAULT NO D.P. PRIMARY PANEL
         DC    C'RES',C'PGM@PRIM'        RES TEAM
         DC    C'CIS',C'PGM@PRIM'        CIS TEAM
         DC    C'PBS',C'PGM@PRIM'        PBS TEAM
         DC    C'PDC',C'PDC@PRIM'        PDC TEAM
         DC    C'OPR',C'OPR@PRIM'        OPR TEAM
         DC    C'ADM',C'ADM@PRIM'        ADM TEAM
         DC    C'QAG',C'PGM@PRIM'        QAG TEAM
         DC    C'DBA',C'SYS@PRIM'        DBA TEAM
         DC    C'HIS',C'PGM@PRIM'        HIS TEAM
         DC    C'FIS',C'PGM@PRIM'        FIS TEAM
         DC    C'IIS',C'PGM@PRIM'        IIS TEAM
         DC    C'USX',C'PGM@PRIM'        USX TEAM
PANLTAB# DC    A(((*-PANLTAB)/11))    NUMBER OF ENTRIES IN TABLE
         LTORG ,
         $REGS
WORK     DSECT
PARMLIST DS    12A
@PRMLIST DS    F
ISPXITID DS    A                     BINARY EXIT ID
ISPUSRID DS    CL8                   TSO LOGON ID
ISPSCRID DS    CL8                   LOGICAL SCREEN ID
ISPZNVIR DS    CL32                  32 BYTE ZENVIR FIELD
ISPDATAL DS    A                     LENGTH OF USER FIELD
ISPDATAP DS    A                     ADDRESS OF USER FIELD
ISPXPANL DS    CL8                   PANEL ID FOR PANEL TO BE DISPLAYED
ISPXMESG DS    CL8                   MESSAGE ID
ISPXCURS DS    CL8                   FIELD NAME TO POS. CURSOR
ISPXCURO DS    A                     OFFSET WITH ISPXCURS TO POS CURSOR
ISPXTABL DS    CL8                   TABLE NAME (CANT BE CHANGED)
ISPXFLAG DS    0XL4                  ISPF FLAGS
ISPXFLG1 DS    X                     FLAGE BYTE 1
ISPXNDSP EQU   X'80'                 DISPLAY
ISPXCMND EQU   X'40'                 COMMAND OPTION SET
         DS    XL3                   LAST THREE FLAG BYTES
         END
//L.SYSLMOD DD DSN=SYS1.LPALIB,VOL=SER=MVS220,UNIT=3380,DISP=SHR
//L.SYSIN   DD *
    ORDER   ISPXIT15
    ORDER   ISPXDT
    INCLUDE SYSLIB(ISPXDT)
    ENTRY   ISPXDT
    NAME    ISPEXITS(R)
//L.SYSLIB  DD DSN=SYSRMS.LOAD,DISP=SHR
//
//ISPXDT   EXEC ASMHCL,
//           PARM.C='OBJECT',
//           PARM.L='MAP,XREF,LIST'
//C.SYSLIB DD DCB=BLKSIZE=12800
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//         DD DSN=SYSRMS.MACLIB,DISP=SHR
//         DD DSN=ISP.V2R3M0.ISPMACS,DISP=SHR,UNIT=3380,VOL=SER=MVS220
ISPXDT     ISPMXED    START
           ISPMXLST   (15)
           ISPMXDEF   15
           ISPMEPT    ISPXIT15,ISPXPARM
           ISPMXEND
           ISPMXED    END
           ISPMXDD    START
           ISPMDAD    ISPXPARM,2048
           ISPMXDD    END
//L.SYSLMOD DD DSN=SYSRMS.LOAD,DISP=SHR
//L.SYSIN   DD *
    NAME ISPXDT(R)
//L.SYSLIB  DD DSN=SYSRMS.LOAD,DISP=SHR
