          MACRO
&NAME  CONVCODE &IN=0(3),&LEN=0(2),&OUT=0(1)
.***************************************************************
.* THIS MACRO IS USED TO CONVERT THE HEX DATA SPECIFIED IN THE
.* 'IN' PARM TO DISPLAYABLE EXTENDED DECIMAL FORMAT. 'LEN' IS
.* THE LENGTH OF THE DATA REFERENCED BY 'IN'. IT IS PLACED IN
.* THE FIELD REFERENCED BY 'OUT' FOR WHATEVER LENGTH IS NECESSARY.
.*****
.* EXAMPLE:
.* TO DISPLAY THE 2 BYTES OF HEX DATA IN FIELD A AND PLACE IT INB:
.* CONVCODE IN=A,LEN=L'A,OUT=B
.* A  DC X'0102' WILL PLACE C'12' IN FIELD B
.* B  DC C'12'
.*****
.* TO DISPLAY THE 4 BYTES OF HEX DATA AT THE ADDRESS IN R3 AND
.* PLACE IT AT THE ADDRESS REFERENCED BY R4:
.* CONVCODE IN=0(R3),LEN=4,OUT=0(R4)
.***************************************************************
&NAME.CONV DS  0H                  CONVERT TO DISPLAY
         STM   1,3,&NAME.SAVE
         LA    1,&OUT            ADDR TO MOVE TO
         LA    3,&IN             FIELD TO CONVERT
         LA    2,&LEN
&NAME.GO  DS   0H
         IC    0,0(3)            LOAD CHAR
         SRL   0,4                 SHIFT OVER 4 BITS
         STC   0,&NAME.CHAR        STORE IT
         NI  &NAME.CHAR,X'0F'     ZERO OUT HI 4 BITS
         TR   &NAME.CHAR,&NAME.TAB TRANSLATE TO DEC
         MVC   0(1,1),&NAME.CHAR  MOVE TO OUTPUT
         MVC  &NAME.CHAR,0(3)     MOVE FIELD
         NI   &NAME.CHAR,X'0F'    ZERO OUT HI 4 BITS
         TR  &NAME.CHAR,&NAME.TAB
         MVC   1(1,1),&NAME.CHAR  MOVE TO TD ...MSGRET
         LA    3,1(3)
         LA    1,2(1)
         BCT   2,&NAME.GO         NEXT BYTE IF ANY
          LM   1,3,&NAME.SAVE
           B    &NAME.END
&NAME.SAVE DS  3F
&NAME.TAB DC   X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
&NAME.CHAR DS  C
&NAME.END  DS   0H
          MEND
