         TITLE 'J2XIT09 - SP1.3.6 OUTPUT EXCESSION FOR TEST JOBS'
***********************************************************************
*                                                                     *
*  INTENT:  THIS EXIT WILL ENFORCE OUTPUT EXCESSION FOR ALL TEST      *
*           JOBS.  TEST JOBS ARE DEFINED AN ANY JOB THAT DOES NOT     *
*           HAVE A 'P' IN THE FIRST POSTION OF THE JOB NAME.          *
*           STARTED TASKS ARE EXEMPT.                                 *
*           TSO USERS ARE NOT EXEMPT.                                 *
*                                                                     *
*  NOTE :   THIS EXIT *MUST* BE IN LPA (FROM A CLPA/MLPA) - NO STEPLIB*
***********************************************************************
         PRINT NOGEN
         COPY  $HASPGBL ,      REQUIRED FOR ALL EXITS
J2XIT09  $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=USER,                                           X
               $HASPEQU,       REQUIRED FOR ALL EXITS                  *
               $BUFFER,        REQUIRED FOR ALL EXITS                  *
               $MIT,           REQUIRED FOR ALL EXITS                  *
               $JCT,           REQUIRED FOR ALL EXITS                  *
               $SCAT,          REQUIRED FOR EXIT 09                    *
               $XECB,          REQUIRED FOR EXIT 09                    *
               $SVT            REQUIRED FOR EXIT 09
*              $PCE,           REQUIRED FOR ALL EXITS                 *
*              $JQE,           REQUIRED FOR ALL EXITS                 *
*              $CAT,           REQUIRED FOR ALL EXITS                 *
*              $HCT,           REQUIRED FOR ALL EXITS                 *
         SPACE 2
***********************************************************************
*             SETUP THE EXIT                                          *
         SPACE 1
J2XIT09E $ENTRY CSECT=YES,BASE=R12
         SPACE 1
         SAVE  (14,12)             SAVE THE REGS
         LR    R12,R15             LOAD BASE REGISTER
         SPACE 2
***********************************************************************
*             TEST JOB TYPE AND OUPTPUT EXCESSION                     *
         SPACE 1
         NOP   0                   WTO   'J2XIT09 ENTER'
         LR    R10,R7              SET PROPER REG FOR JCT
         USING JCT,R10             NEED TO LOOK AT THE JCT
         SPACE 1
         CLI   JCTJOBID,C'J'       Q. IS THIS A JOB
         BNE   EXITOK              A. NO - MUST BE A STC OR TSO TASK
         SPACE 1
         CLI   JCTJNAME,C'P'       Q. IS IT A PRODUCTION JOB
         BE    EXITOK              A. YES - LET IT LIVE
         SPACE 1
         CLI   JCTJNAME,C'Q'       Q. IS IT A USER PRODUCTION JOB
         BE    EXITOK              A. YES - LET IT LIVE
         SPACE 1
         DROP  R10
         SPACE 2
***********************************************************************
*             EXIT ROUTINES                                           *
         SPACE 1
KILLJOB  NOP   0                   WTO   'J2XIT09 KILLJOB'
         LA    R15,8               SET RETURN CODE 8 (KILL JOB)
         L     R0,STOPWORD         KILL THIS SUCKER
         B     EXITALL             COMMON EXIT
         SPACE 1
EXITOK   NOP   0                   WTO   'J2XIT09 EXITOK'
         SR    R15,R15             SET GOOD RETURN
         L     R0,CONTWORD         JUST CONTINUE
         SPACE 1
EXITALL  ST     R0,20(R13)         SET RETURN WORD
         RETURN (14,12),RC=(15)    RETURN TO JES2
         SPACE 2
***********************************************************************
*             CONSTANTS AND OTHER STUFF                               *
         SPACE 1
         DS    0F              FOR ALIGNMENT
STOPWORD DC    X'80000001'     TELL JES2 TO CANCEL THIS JOB
CONTWORD DC    X'80000000'     TELL JES2 TO CONTINUE
         LTORG
         $MODEND
         END
