         MACRO
&NAME    COMEIN &BASE=3,&SAVE=MYSAVE,&REGS=R,&RC=CC,&LINK=14,&BASE2=,  X
               &BASE3=
         GBLC  &COMEIN
&COMEIN  SETC  '&RC'
&NAME    START 0
         STM   &REGS.14,&REGS.12,12(&REGS.13)  SAVE RETURN REGS
         AIF   ('&BASE3' EQ '').TWOBASE
         USING &NAME,&REGS&BASE,&REGS&BASE2,&REGS&BASE3
         LR    &REGS&BASE,&REGS.15   GET THE LOAD POINT
         LA    &REGS&BASE2,4095(&REGS&BASE) PRIME BASE NUMBER 2
         LA    &REGS&BASE2,1(&REGS&BASE2)  NOW NUMBER 2 IS SET
         LA    &REGS&BASE3,4095(&REGS&BASE2) PRIME BASE NUMBER 3
         LA    &REGS&BASE3,1(&REGS&BASE3)  NOW NUMBER 3 IS SET
         AGO   .BASESET
.TWOBASE AIF   ('&BASE2' EQ '').ONEBASE
         USING &NAME,&REGS&BASE,&REGS&BASE2  EST ADDRESSABILITY
         LR    &REGS&BASE,&REGS.15   GET THE LOAD POINT
         LA    &REGS&BASE2,4095(&REGS&BASE) PRIME BASE NUMBER 2
         LA    &REGS&BASE2,1(&REGS&BASE2)  NOW NUMBER 2 IS SET
         AGO   .BASESET
.ONEBASE USING &NAME,&REGS&BASE    ESTABLISH ADDRESSABILITY
         LR    &REGS&BASE,&REGS.15
.BASESET LR    &REGS.14,&REGS.13   CHAIN SAVE AREAS
         BAL   &REGS.13,&SAVE.+92  SET 13 TO CURRENT SAVE AREA
&SAVE    DC    18F'0'              CURRENT SAVE AREA
&RC      DC    F'0'                RETURN CODE HOLD AREA
         L     &REGS.13,&SAVE.+4    CODE TO RETURN TO CALLING PGM
         MVC   16(4,&REGS.13),&RC   SET UP RETURN CODE
         LM    &REGS.14,&REGS.12,12(&REGS.13)
         BR    &REGS.14             RETURN
         ST    &REGS.14,&SAVE.+4   ABOVE BAL COMES HERE
         ST    &REGS.13,8(&REGS.14)
         SPACE 1
&REGS.0  EQU   0                  REG EQUATES
&REGS.1  EQU   1
&REGS.2  EQU   2
&REGS.3  EQU   3
&REGS.4  EQU   4
&REGS.5  EQU   5
&REGS.6  EQU   6
&REGS.7  EQU   7
&REGS.8  EQU   8
&REGS.9  EQU   9
&REGS.10 EQU   10
&REGS.11 EQU   11
&REGS.12 EQU   12
&REGS.13 EQU   13
&REGS.14 EQU   14
&REGS.15 EQU   15
&REGS.A  EQU   10
&REGS.B  EQU   11
&REGS.C  EQU   12
&REGS.D  EQU   13
&REGS.E  EQU   14
&REGS.F  EQU   15
&REGS.LINK EQU &LINK
         MEND
