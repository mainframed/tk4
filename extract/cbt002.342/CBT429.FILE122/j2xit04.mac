         TITLE 'JES2 SP1.3 EXIT 04 - REMOVE JOB/STEPCAT CARDS'
***********************************************************************
*                                                                     *
*  INTENT:  THIS EXIT WILL COMMENT OUT JOB AND STEPCAT DD CARDS WHEN  *
*           THEY ARE INCLUDED OUTSIDE OF PROCS.  A SEPERATE JES2      *
*           EXIT MUST BE USED TO CATCH STEPCATS INSIDE OF PROCEDURES. *
*                                                                     *
* 6-17-85   MODIFIED TO COMMENT OUT CONCATENATED DD CARDS.            *
* 6-27-85   MODIFIED TO ADD DEFAULT //NJE OUTPUT CARDS.               *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         COPY  $HASPGBL
J2XIT04  $MODULE                                                       *
               $HASPEQU,                                               *
               $BUFFER,                                                *
               $JQE,                                                   *
               $MIT,                                                   *
               $PCE,                                                   *
               $JCT,                                                   *
               $LRC,                                                   *
               $CAT,                                                   *
               $HCT,                                                   *
               $DCT,                                                   *
               $RDRWORK
         SPACE 2
***********************************************************************
*             START MAINLINE                                          *
J2XIT04E $ENTRY CSECT=YES
         $SAVE
         LR    R8,R15              ESTABLISH BASE REG
         SPACE 1
         LTR   R0,R0               Q. IS THIS A JCL CARD
         BZ    EXITOK              A. NO - IT IS A JECL CARD - EXIT
         SPACE 1
         CLC   8(4,R1),=F'0'       Q. DO I HAVE A JCT YET
         BE    EXITOK              A, NO - I DON'T WANT A 0C4 - EXIT
         SPACE 1
         USING JCT,R10
         SPACE 1
         L     R2,4(R1)            POINT TO RXITFLAG
         TM    0(R2),X'02'         Q. JECL CARD
         BO    EXITOK              A. YES - JUST EXIT
         SPACE 1
         L     R12,0(R1)           POINT TO JCL CARD
         CLC   0(2,R12),=C'//'     Q. IS THIS A JCL CARD
         BNE   EXITOK              A. NO - BYPASS IT
         SPACE 1
         CLC   =C'//SYSIN',0(R12)  Q. IS THIS A SYSIN STATEMENT
         BE    EXITOK              A. YES - THERE MAY BE NO STEPS
         SPACE 1
         CLC   JCTJNAME,JCTUSER0   Q. FIRST TIME IN
         BE    VMLINK2             A. NO - TEST FOR JCL INSERTS
         SPACE 2
***********************************************************************
*             THIS IS THE FIRST TIME IN - DO MORE CHECKING            *
         SPACE 1
         CLC   0(7,R12),=C'//SYSIN' IF THE USER HAS SUBMITTED A JOB
         BE    EXITOK               WITHOUT A VALID JCL CARD AS THE
*                                   FIRST CARD, THEN IGNORE IT
         SPACE 1
         MVC   JCTUSER0(8),JCTJNAME SET JOB NAME IN USER AREA
         MVC   JCTUSER2,=F'0'       CLEAR SWITCH AREA
         SPACE 1
         USING PCE,R3              SETUP TO USE THE PCE
         L     R3,PCEDCT           GET ADDR OF DCT FROM PCE
         DROP  R3                  NOW DROP THE PCE
         SPACE 1
         USING DCT,R3              NOW USE THE DCT
VMLINK   CLC   =C'STC',DCTDEVN     Q. IS THIS A STARTED TASK
         BE    NOT1ST              A. YES - NO OUTPUT CARDS REQUIRED
         SPACE 1
         CLI   DCTDEVN,C'L'        Q. IS THIS A NJE XMITTER
         BNE   VMLINK1             A. NO
         SPACE 1
         CLI   DCTDEVN+1,C'0'      Q. IS THIS A NUMBER
         BL    VMLINK1             A. NO - IT IS NOT A NJE XMITTER
         OI    JCTUSER2,X'80'      SHOW THIS IS A NJE XMITTER
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*
*  THIS ROUTINE WILL DO THE FOLLOWING:
*   1) MOVE THE CURRENT JCL CARD TO THE JCT WORK AREA
*   2) MOVE AN OUTPUT JCL CARD TO THE JES2 JCL BUFFER
*   3) SET THE PROPER FLAGS TO TELL JES2 THAT THE JCL CARD IN THE
*      JCT WORK AREA IS THE NEXT CARD.
*
         SPACE 1
VMLINK1  L     R12,0(R1)           POINT TO JCL CARD
         MVC   JCTXWRK(80),0(R12)  MOVE JCL CARD TO JCT WORK AREA
         MVC   0(80,R12),$LOCAL    MOVE $LOCAL CARD TO JCL AREA
         L     R2,4(R1)            GET ADDR OF RXITFLG BYTE
         OI    0(R2),X'10'         TELL JES2 THIS IS THE NEXT CARD
         B     EXITOK              NOW LET JES2 PROCESS THE CARD
         SPACE 1
VMLINK2  TM    JCTUSER2,X'80'      Q. IS THIS A NJE XMITTER
         BNO   NOT1ST              A. NO - NO MORE INSERTS REQUIRED
         SPACE 1
         TM    JCTUSER2,X'40'      Q. DO WE NEED A $JESDS CARD
         BO    VMLINK3             A. NO - TRY FOR $DEFAULT
         SPACE 1
         L     R12,0(R1)           POINT TO JCL CARD
         MVC   JCTXWRK(80),0(R12)  MOVE JCL CARD TO JCT WORK AREA
         MVC   0(80,R12),$JESDS    MOVE $JESDS CARD TO JCL AREA
         L     R2,4(R1)            GET ADDR OF RXITFLG BYTE
         OI    JCTUSER2,X'40'      SHOW THAT WE HAVE A $JESDS CARD
         OI    0(R2),X'10'         TELL JES2 THIS IS THE NEXT CARD
         B     EXITOK              NOW LET JES2 PROCESS THE CARD
         SPACE 1
VMLINK3  TM    JCTUSER2,X'20'      Q. DO WE NEED A $DEFAULT CARD
         BO    NOT1ST              A. NO - WE ARE DONE
         SPACE 1
         L     R12,0(R1)           POINT TO JCL CARD
         MVC   JCTXWRK(80),0(R12)  MOVE JCL CARD TO JCT WORK AREA
         MVC   0(80,R12),$DEFAULT  MOVE $DEFAULT CARD TO JCL AREA
         L     R2,4(R1)            GET ADDR OF RXITFLG BYTE
         OI    JCTUSER2,X'20'      SHOW THAT WE HAVE A $JESDS CARD
         OI    0(R2),X'10'         TELL JES2 THIS IS THE NEXT CARD
         B     EXITOK              NOW LET JES2 PROCESS THE CARD
         SPACE 1
***********************************************************************
*             NOW IT IS TIME TO CHECK FOR JOB/STEPCAT CARDS           *
NOT1ST   CLI   JCTJCLAS,C'0'       Q. IS THIS TECH SUPPORT
         BNL   EXITOK              A. YES - JUST EXIT WITHOUT CHECKING
         SPACE 1
         L     R12,0(R1)           POINT TO JCL CARD
         SPACE 1
         CLC   =C'JOBCAT',2(R12)   Q. IS THIS A JOBCAT DD CARD
         BE    CATCARD             A. YES
         SPACE 1
         CLC   =C'STEPCAT',2(R12)  Q. IS THIS A STEPCAT DD CARD
         BE    CATCARD             A. YES
         SPACE 1
         TM    JCTUSER2+1,X'80'    Q. IS THERE A JOB/STEPCAT
         BNO   EXITOK              A. NO - JUST EXIT
         SPACE 1
         CLI   2(R12),C' '         Q. CONCATINATION
         BE    CATCARD             A. YES - FIX IT
         SPACE 1
         MVI   JCTUSER2+1,X'00'    CLEAR THE SWITCH
         B     EXITOK              JUST EXIT FOR GOOD GUY
         SPACE 1
CATCARD  MVI   2(R12),C'*'
         OI    JCTUSER2+1,X'80'    FOUND A JOB/STEPCAT CARD
         SPACE 2
***********************************************************************
*            WE HAVE DONE WHAT WE CAME HERE TO DO - NOW LEAVE         *
EXITOK   SR    R15,R15           SET GOOD RETURN
         $RETURN RC=(R15)        THEN RETURN TO JES2
         SPACE 1
$DEFAULT DC    CL80'//$DEFAULT OUTPUT DEST=ALRGMVS1,DEFAULT=YES'
$JESDS   DC    CL80'//$JESDS   OUTPUT DEST=ALRGMVS1,JESDS=ALL'
$LOCAL   DC    CL80'//$LOCAL   OUTPUT DEST=ALRGMVS1'
         SPACE 1
         LTORG
         $MODEND
         END
