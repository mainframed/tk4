          TITLE 'JES2-EXIT2  ADD /*ROUTE TO NJE JOBS   '
***********************************************************************
*                                                                     *
*     MODIFICATIONS :                                                 *
*     BETO - ADDED CODE TO CHECK FOR STC'S FIRST.  IF TRUE THEN       *
*            INSERT ACCT NO. INTO STC GENERATED JOBCARD (FOR JARS).   *
*                                                                     *
*     BETO - ADDED CODE TO CHECK FOR CICS RUNNING AS AN STC.  IF SO,  *
*            INSERT CICS ACCT NO., MSGCLASS & MSGLEVEL.               *
*                                                                     *
*                                                                     *
*     THE ORIGINAL RULES :                                            *
*        STC JOBS ARE EXEMPT                                          *
*        TSU JOBS ARE EXEMPT                                          *
*                                                                     *
*      ALL JOBS ENTERING THE SYSTEM VIA THE NJE LINK WILL HAVE A      *
*      "/*ROUTE PRINT ALRGMVS1"                                       *
*      ADDED TO THE JOB STREAM.                                       *
*                                                                     *
*      THIS CARD IS REQUIRED TO FORCE ALL OUTPUT TO SPOOL TO THE      *
*      VM PRINTERS, AND EVENTUALLY BACK TO THE CMS USER.              *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*           COMMON SETUP FOR EXIT2                                    *
         COPY  $HASPGBL
J2XIT02  $MODULE                                                       X
               $HASPEQU,                                               X
               $BUFFER,                                                X
               $JCT,                                                   X
               $JQE,                                                   X
               $PCE,                                                   X
               $CAT,                                                   X
               $HCT,                                                   X
               $MIT,                                                   X
               $LRC,                                                   X
               $DCT,                                                   X
               $RDRWORK
         SPACE 1
***********************************************************************
*               THE MAIN AND ONLY LOGIC                               *
         SPACE 1
J2XIT02E $ENTRY
         $SAVE  (R14,R12)
         LR    R8,R15                  SET BASE REG
         SPACE 1
         USING PCE,R13                 THE PCE IS CONNECTED TO
         L     R9,PCEDCT               THE DCT
         DROP  R13
*
         USING JCT,R10                 MAKE IT ACCESSABLE         BJM
         CLI   JCTJOBID,C'S'           Q. IS THIS AN STC ?        BJM
         BE    STCACCT                 A. YES - GO INSERT ACTTNO. BJM
*
         USING DCT,R9                  USE THE DCT
         CLI   DCTDEVN,C'L'            Q. CAN THIS BE A NJE LINK
         BNE   EXITOK                  A. NO
         SPACE 1
         CLI   DCTDEVN+1,C'0'          Q. IS THIS A NUMBER
         BL    EXITOK                  A. NO - IT IS NOT A NJE LINK
         DROP  R9
         SPACE 1
NOTNJE   L     R3,0(R1)                POINT TO JOB CARD
         SPACE 1
         CLI   71(R3),C' '             Q. ANYTHING IN COL 72
         BNE   EXITOK                  A. YES - JUST EXIT
         SPACE 1
         LA    R3,2(R3)                JUMP OVER //
         LA    R4,69                   SET SCAN LIMIT
         CLI   0(R3),C' '              Q. IS THIS THE FIRST CARD
         BE    CONTCARD                A. NO - GO TO CONTINATION CARD
         SPACE 1
         SPACE 1
JOB1     CLC   0(5,R3),=C' JOB '       Q. IS THIS THE JOB IDENTIFIER
         BE    JOB2                    A. YES
         LA    R3,1(R3)                BUMP THE INDEX
         BCT   R4,JOB1                 LOOP UNTIL ' JOB ' FOUND
         B     EXITOK                  IT IS BAD - LET JES2 TAKE IT
         SPACE 1
JOB2     LA    R3,5(R3)                JUMP OVER 'JOB '
         S     R4,=F'5'                DECRIMENT THE REMAINING LENGTH
         SPACE 1
CONTCARD CLI   0(R3),C' '              Q. IS THIS POSITION BLANK
         BNE   SCAN1                   A. NO - START THE SCAN
         LA    R3,1(R3)                BUMP THE INDEX
         BCT   R4,CONTCARD             CONTINUE THE SCAN
         B     EXITOK                  SOMETHING IS WRONG
         SPACE 1
SCAN1    CLC   0(2,R3),=C', '          Q. IS THIS CARD CONTINUED
         BE    EXITOK                  A. YES - JUST EXIT
         SPACE 1
         CLI   0(R3),C''''             Q. IS THIS A QUOTE
         BNE   SCAN3                   A. NO - THEN CONDINUE
         SPACE 1
         LA    R3,1(R3)                BUMP THE INDEX
         BCTR  R4,0                    DECRIMENT THE LENGTH
         SPACE 1
SCAN2    CLI   0(R3),C''''             Q. ENDING QUOTE
         BE    SCAN3                   A. YES - THATS GOOD
         LA    R3,1(R3)                BUMP THE INDEX
         BCT   R4,SCAN2                CONTINUE TO LOOK FOR NEXT QUOTE
         B     EXITOK                  NOT FOUND - LET JES2 TAKE IT
         SPACE 1
SCAN3    CLI   0(R3),C' '              Q. IS THIS THE END
         BE    ISEND                   A. YES - THATS GOOD
         SPACE 1
         LA    R3,1(R3)                BUMP THE INDEX AGAIN
         BCT   R4,SCAN1                AND START ALL OVER AGAIN
         SPACE 1
         CLI   0(R3),C','              IS THE LAST POSITION A COMMA
         BE    EXITOK                  A. YES - THEN EXIT
         SPACE 1
ISEND    MVC   JCTXWRK,ROUTPRT         MOVE THE ROUTE CARD TO JCT WORK
         L     R2,4(R1)                POINT TO THE RXITFLAG
         OI    0(R2),X'10'             TELL JES2 ABOUT ROUTE CARD
         B     EXITOK
         SPACE 1
***********************************************************************
STCACCT  L     R3,0(R1)                GET ADDR OF JOB CARD        BJM
         CLC   JCTJNAME(4),=C'CICS'    IS THIS A CICS REGION ?     BJM
         BE    CICSNUM                 YES, INSERT CICS JCARD PARM BJM
         MVC   14(L'ACCTNO,R3),ACCTNO  OTHERWISE, INSERT STC PARMS BJM
         B     EXITOK                  TIME TO LEAVE               BJM
CICSNUM  MVC   14(L'CICSACCT,R3),CICSACCT  INSERT CICS JCARD PARMS BJM
         B     EXITOK                  TIME TO LEAVE               BJM
***********************************************************************
*               EXIT ROUTINE                                          *
EXITOK   LA    R15,0                SET GOOD RETURN CODE
         SPACE 1
EXIT     $RETURN RC=(R15)           COMMON EXIT ROUTINE
         SPACE 1
***********************************************************************
*                  CONSTANTS ETC                                      *
ROUTPRT  DC    CL80'/*ROUTE PRINT ALRGMVS1'
ACCTNO   DC    CL20' 55MVSOVD,MSGLEVEL=1'
CICSACCT DC    CL33' 55CICS,MSGLEVEL=(1,1),MSGCLASS=J'
         SPACE 1
         LTORG
         $MODEND
         END
