         TITLE 'JES2 SP1.3 EXIT 03 - VERIFY ACCOUNT NUMBER ON JOBCARD'
***********************************************************************
*                                                                     *
*  INTENT:  THIS EXIT WILL LOAD A VALID-JOB-ACCOUNT-NUMBER TABLE INTO *
*           STORAGE (AT JES2 STARTUP); IT WILL VERIFY ALL BATCH JOBS  *
*           TO MAKE SURE THEY USE VALID ACCT. NOS.; IT WILL RECOGNIZE *
*           A "TRIGGER" ACCT. NO. THAT WILL CAUSE THE EXIT TO REFRESH *
*           ITS COPY OF THE VALID-JOB-ACCOUNT-NUMBER TABLE; IT WILL   *
*           ALSO RECOGNIZE A "BYPASS" ACCOUNT NUMBER THAT WILL DISABLE*
*           THIS EXIT.                                                *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         COPY  $HASPGBL
J2XIT03  $MODULE                                                       *
               $HASPEQU,                                               *
               $BUFFER,                                                *
               $MIT,                                                   *
               $PCE,                                                   *
               $JCT,                                                   *
               $HCT,                                                   *
               $RDRWORK
         SPACE 2
***********************************************************************
*             START MAINLINE                                          *
J2XIT03E $ENTRY CSECT=YES
         $SAVE
         LR    R12,R15              ESTABLISH BASE REG
         USING J2XIT03E,R12         ADDRESSABILITY TO THIS CSECT
         USING JCT,R10              ADDRESSABILITY TO JCT
         SPACE 1
         LR    R2,R0                SAVE LENGTH-OF-ACCT-NUM-FIELD VALUE
         LM    R3,R5,0(R1)          R3 = ADDR OF JCTWORK (ACCT FIELD)
*                                   R4 = ADDR OF RDWFLAGX BYTE IN PCE
*                                   R5 = ADDR OF JCTXWRK FIELD IN JCT
         SPACE 1
         CLC   TABLADDR,=F'0'      Q. HAS TABLE BEEN LOADED (EVER) ?
         BNE   CHKJOB              A. YES - GO CHECK JOB TYPE
         LOAD  EP=J2TBL03          LOAD TABLE AND SEARCH ROUTINE
         ST    R0,TABLADDR         SAVE TABLE-SEARCH ENTRY POINT
         SPACE 1
CHKJOB   CLI   JCTJOBID,C'J'       Q. IS THIS A BATCH JOB ?
         BNE   EXITRC08            A. NO - TSO OR STC SO ALLOW/IGNORE
         SPACE 1
         CLC   0(8,R3),=C'99REFRSH' Q. TIME TO REFRESH ACCT NUM TABLE?
         BNE   CHKBYPAS            A. NO - CHECK "BYPASS" EXIT SWITCH
         SPACE 1
         DELETE EP=J2TBL03          GET RID OF PREVIOUS COPY OF MODULE
         LOAD  EP=J2TBL03          LOAD TABLE AND SEARCH ROUTINE
         ST    R0,TABLADDR         SAVE TABLE-SEARCH ENTRY POINT
         B     EXITRC12            NOW CANCEL "REFRESH" JOB AND LEAVE
         SPACE 1
CHKBYPAS CLC   TABLADDR,=X'FFFFFFFF' Q. HAS BYPASS SWITCH BEEN SET ON?
         BE    EXITRC08            A. YES - ALLOW ALL JOBS AND LEAVE
         SPACE 1
         LTR   R2,R2               Q. DOES JOBCARD HAVE ACCT NUM ?
         BZ    EXITRC12            A. NO - CANCEL JOB AND LEAVE
         SPACE 1
         CLC   0(8,R3),=C'99BYPASS' Q. WANT TO BYPASS EXIT ?
         BNE   CHKJOB2             A. NO - CONTINUE VERIFYING ACCT NO.
         MVC   TABLADDR,=X'FFFFFFFF'      SET BYPASS SWITCH ON
         B     EXITRC12            LEAVE AND CANCEL "BYPASS" JOB
         SPACE 1
***********************************************************************
*            CALL SUBROUTINE TO VERIFY ACCOUNT INFORMATION            *
CHKJOB2  L     R15,TABLADDR        LOAD R15 WITH SUBROUTINE ADDR
         CALL  (15)                INVOKE ACCT NO. VERIFY SUBROUTINE
         SPACE 1
         B     *+4(R15)            BRANCH USING R15 AS A VECTOR(0 OR 4)
         B     EXITRC08            MEANS ACCT. NO. WAS VALID
         B     EXITRC12            MEANS ACCT. NO. WAS NOT VALID
***********************************************************************
*            ALLOW JOB TO RUN                                         *
EXITRC08 LA    R15,8             SET GOOD RETURN CODE (ACCT IS VALID)
         $RETURN RC=(R15)        THEN RETURN TO JES2
         SPACE 1
***********************************************************************
*            CANCEL JOB                                               *
EXITRC12 LA    R15,12            SET BAD RETURN CODE (CANCEL JOB)
         $RETURN RC=(R15)        THEN RETURN TO JES2
         SPACE 1
TABLADDR DC    F'0'
         SPACE 1
         LTORG
         $MODEND
         END
