J2XIT01  TITLE 'JES2 SP1.3.6 EXIT 01 - HEADER/TRAILER PAGE HANDLING'
***********************************************************************
*                                                                     *
*        HEADER PAGE EXIT POINT                                       *
*                                                                     *
*        AT THIS POINT THE USER HAS THE CHANCE TO CREATE AND/OR       *
*        ADD TO THE STANDARD HEADER PAGE.                             *
*                                                                     *
*        ON CALL TO THE INSTALLATION EXIT THE FOLLOWING               *
*        REGISTERS WILL BE SETUP:                                     *
*                                                                     *
*        REG             VALUE                                        *
*                                                                     *
*        R0              0 - INDICATION START OF PRINT                *
*                        4 - INDICATION CONTINUATION OF PRINT         *
*                        8 - INDICATION TRAILER PAGE                  *
*        R1              PRINTER DCT ADDR.                            *
*        R2-R9           N/A                                          *
*        R10             ADDR. JCT                                    *
*        R11             ADDR. HCT                                    *
*        R12             N/A                                          *
*        R13             ADDR. PCE                                    *
*        R14             RETURN POINT                                 *
*        R15             ENTRY POINT                                  *
*                                                                     *
*        ON RETURN FROM THE USER EXIT THE REGISTERS SHOULD BE         *
*        THE CAME AS ENTRY WITH THE EXECPTION OF R15 WHICH            *
*        WILL HAVE THE RETURN CODE AS SET BY THE USER EXIT.           *
*                                                                     *
*        RETURN CODE                                                  *
*                                                                     *
*        0 AND 4 - CREATE SEPERATOR PAGE BASED ON DCTPPSWS            *
*        8       - CREATE USER SEPERATOR PAGE                         *
*        12      - UNCONDITIONAL CREATE SEPERATOR PAGE                *
***********************************************************************
*                                                                     *
*  THIS IS THE ORIGINAL ALLERGAN JES2 EXIT 01.  IT HAS BEEN MODIFIED  *
*  TO ISSUE SVC 217 TO CLOSE VIRTUAL PRINTERS BECAUSE DIAGNOSE        *
*  WILL NOT WORK UNDER PMA.                                           *
*                                                                     *
*  BACK TO USING THE DIAG COMMAND.  VM GETS A SVC002 (VM NOT MVS)     *
*  WHEN A SIO IS ISSUED FOR THE VIRT PRINTERS.  3-16-86  PLB          *
*                                                                     *
*  USE REAL ADDRESS WHEN USING DIAG 8           3-16-86  PLB          *
*                                                                     *
*  MOVE WORKAREA FROM JCT TO THIS EXIT          3-17-86  PLB          *
*                                                                     *
*  PGFIX THIS PGM TO FORCE WORKARE BELOW 16MB   3-17-86  PLB          *
*                                                                     *
*  MODIFY TO CALL J2SRB01 IN COMMON.  J2SRB01   7-01-86  PLB          *
*   WILL SCHEDULE A SRB TO DO THE DIAGNOSE TO                         *
*   SPOOL THE VIRTUAL PRINT FILES TO VM.                              *
*                                                                     *
*  MODIFIED TO CREATE USER SEPERATOR PAGE      12/14/87  PLB          *
*   FOR VIRTUAL PRINTERS.  ROUTER WILL SCAN                           *
*   THE SEPERATOR PAGE AND ROUTE THE OUTPUT.                          *
*                                                                     *
*  ADD 2 REAL PRINTERS (11 & 12)               01/25/88  PLB          *
*                                              PAUL BANKS             *
***********************************************************************
         SPACE 2
***********************************************************************
*              DEFINE JES2 STUFF                                      *
         SPACE 2
         COPY  $HASPGBL
J2XIT01  $MODULE RPL,                                                  *
               $HASPEQU,                                               *
               $BUFFER,                                                *
               $DCT,                                                   *
               $JQE,                                                   *
               $JOE,                                                   *
               $MIT,                                                   *
               $PCE,                                                   *
               $JCT,                                                   *
               $CAT,                                                   *
               $PPPWORK,                                               *
               $HCT
         PRINT NOGEN
UCB      DSECT
         IEFUCBOB PREFIX=NO
         DCBD
         IEZDEB
         PRINT GEN
         SPACE 2
J2XIT01E $ENTRY CSECT=YES          SETUP ENTRY POINT FOR JES2
         $SAVE
         LR    R8,R15              ESTABLISH BASE REG
         SPACE 1
***********************************************************************
*              FIRST DETERMINE IF THIS IS A VIRTUAL PRINTER           *
         SPACE 2
FIXED    LR    R6,R1               SAVE DCT ADDRESS
         USING DCT,R6              DCT ADDRESSABILITY
         SPACE 1
         CLI   DCTDEVTP,DCTPRT     Q. IS THIS A LOCAL PRINTER
         BNE   J1RETN0             A. NO - JUST LEAVE
         SPACE 1
         CLC   DCTDEVN,=C'PRT8    ' Q. IS THIS A REAL PRINTER
         BE    J1RETN0              A. YES - JUST LEAVE
         SPACE 1
         CLC   DCTDEVN,=C'PRT9    ' Q. IS THIS A REAL PRINTER
         BE    J1RETN0              A. YES - JUST LEAVE
         SPACE 1
         CLC   DCTDEVN,=C'PRT10   ' Q. IS THIS A REAL PRINTER
         BE    J1RETN0              A. YES - JUST LEAVE
         SPACE 1
         CLC   DCTDEVN,=C'PRT11   ' Q. IS THIS A REAL PRINTER
         BE    J1RETN0              A. YES - JUST LEAVE
         SPACE 1
         CLC   DCTDEVN,=C'PRT12   ' Q. IS THIS A REAL PRINTER
         BE    J1RETN0              A. YES - JUST LEAVE
         SPACE 1
***********************************************************************
*              FIND UCB NAME FOR VIRTUAL PRINTER                      *
         SPACE 1
         L     R4,DCTDCB           LOAD DCB PTR
         USING IHADCB,R4           EST. DCB ADDRESSABILITY
         L     R4,DCBDEBAD         LOAD DEB PTR
         DROP  R4
         SPACE 1
         USING DEBBASIC,R4         EST. DEB ADDRESSABILITY
         ICM   R4,7,DEBSUCBB       LOAD UCB PTR
         DROP  R4
         SPACE 1
         USING JCT,R10             SETUP TO USE JCT USER FIELDS
         USING UCB,R4              EST UCB ADDRESSABILITY
         MVC   JCTUSER0,UCBNAME    SAVE THE UCB NAME
         DROP  R4
         SPACE 2
         C     R0,=F'8'            Q. TRAILER PAGE INVOCATION?
         BE    J1TRAIL             A. TAG AND CLOSE THE PRINT FILE
         SPACE 2
***********************************************************************
*        BUILD A HEADER PAGE FOR VIRTUAL PRINTERS                     *
         USING BFPDSECT,R3
         $GETBUF TYPE=HASP,WAIT=YES,FIX=YES     BUFFER FOR HEADER
         LR    R3,R1                 SAVE BUFFER ADDRESS
         $SEPPDIR (R3)
         MVC   MODID,JCTJOBID        MOVE JOB NUMBER
         MVC   MODJOB,JCTJNAME       MOVE JOB NAME
         MVC   MODPRT,DCTDEVN        SET PRINTER NUMBER
         USING JOE,R7
         L     R7,PPPWKJOE
         MVC   MODCLS,JOECURCL
         MVC   BUFSTART(L'MODEL),MODEL   MOVE MODEL LINE TO BUFFER
         LA    R0,L'MODEL            LENGTH OF MODEL
         LA    R1,BUFSTART           START OF BUFFER DATA AREA
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES
         $FREEBUF (R3),TYPE=HASP
         B     J1RETN8               PRINT THIS SEPERATOR PAGE
         DROP  R3,R7
         SPACE 2
***********************************************************************
*        BUILD THE CP CLOSE COMMAND IN THE JCTWORK AREA               *
         SPACE 1
J1TRAIL  MVC   JCTWORK(L'MSG),MSG      BUILD THE MESSAGE IN THE JCT
         MVC   JCTWORK+06(3),JCTUSER0  MOVE UCB NAME TO VM COMMAND
         MVC   JCTWORK+15(8),JCTJNAME  MOVE JOBNAME TO VM COMMAND
         MVC   JCTWORK+29(8),JCTJNAME  MOVE JOBNAME TO VM COMMAND AGAIN
         SPACE 2
***********************************************************************
*  MOVE CLOSE COMMAND TO UNIQUE WORKAREA DEPENDING ON PRT NBR         *
*              THEN CALL J2SRB01 TO CLOSE THE PRINTER                 *
         SPACE 1
         CLC   DCTDEVN,=C'PRT1    ' Q. IS THIS PRINTER 1
         BNE   TRYPRT2              A. NO - TRY NEXT PRINTER
         B     DODIAG                  AND TELL VM TO DO IT TO IT
         SPACE 1
TRYPRT2  CLC   DCTDEVN,=C'PRT2    ' Q. IS THIS PRINTER 2
         BNE   TRYPRT3              A. NO - TRY NEXT PRINTER
         B     DODIAG                  AND TELL VM TO DO IT TO IT
         SPACE 1
TRYPRT3  CLC   DCTDEVN,=C'PRT3    ' Q. IS THIS PRINTER 3
         BNE   TRYPRT4              A. NO - TRY NEXT PRINTER
         B     DODIAG                  AND TELL VM TO DO IT TO IT
         SPACE 1
TRYPRT4  CLC   DCTDEVN,=C'PRT4    ' Q. IS THIS PRINTER 4
         BNE   TRYPRT5              A. NO - TRY NEXT PRINTER
         B     DODIAG                  AND TELL VM TO DO IT TO IT
         SPACE 1
TRYPRT5  CLC   DCTDEVN,=C'PRT5    ' Q. IS THIS PRINTER 5
         BNE   TRYPRT6              A. NO - TRY NEXT PRINTER
         B     DODIAG                  AND TELL VM TO DO IT TO IT
         SPACE 1
TRYPRT6  CLC   DCTDEVN,=C'PRT6    ' Q. IS THIS PRINTER 6
         BNE   TRYPRT7              A. NO - TRY NEXT PRINTER
         B     DODIAG                  AND TELL VM TO DO IT TO IT
         SPACE 1
TRYPRT7  CLC   DCTDEVN,=C'PRT7    ' Q. IS THIS PRINTER 7
         BNE   OOPS                 A. NO - OOPS
         B     DODIAG                  AND TELL VM TO DO IT TO IT
         SPACE 1
DODIAG   NOP   DODIAG1                 A NOP 1ST TIME
         MVI   *-3,X'F0'               MAKE A BRANCH
         L     R2,16                   START AT THE CVT
         USING CVT,R2
         L     R3,CVTJESCT             ADDR OF SUB-SYS CNTL TBL
         DROP  R2                      DROP THE CVT
         SPACE 1
         USING JESCTID,R3
         L     R2,JESSSCT              ADDR OF SUBSYS COMM TBL
         DROP  R3                      DROP THE CVT
         SPACE 1
         USING SSCT,R2
RMSGTLU  CLC   SSCTSNAM,=C'RMSG'       Q. IS THIS THE RMSG SUB-SYS
         BE    GOTRMSG                 A. YES - SAVE THE ADDR
         SPACE 1
         L     R2,SSCTSCTA             POINT TO NEXT SSCVT
         B     RMSGTLU
         SPACE 1
GOTRMSG  ST    R2,RMSGSSVT             SAVE ADDR OF RMSG SSCVT
         SPACE 1
DODIAG1  L     R2,RMSGSSVT             POINT TO SSCVT FOR RMSG
         L     R3,SSCTSSVT             POINT TO SSVT FOR RMSG
         DROP  R2
         SPACE 1
         USING SSVT,R3                 USE THE SSVT
         LTR   R3,R3                   Q. IS RMSG ACTIVE
         BZ    NORMSG                  A, NO - TIME TO STOP
         SPACE 1
         LA    R2,SSVTFRTN             POINT TO FUNCITON WORDS
         LA    R2,256*4(R2)            POINT TO SRB VECTOR TABLE
         L     R3,0(R2)                GET SRB VECTOR POINTER
         SPACE 1
         LTR   R3,R3                   Q. IS SRB AVAILABLE
         BZ    NOSRB                   A. NO - TIME TO QUIT
         DROP  R3
         SPACE 1
         MODESET KEY=ZERO              MATCH THE KEY IN COMMON
         USING J2SRBD,R3
         LA    R2,L'MSG                THE LENGTH OF CP CMD FOR DIAG 8
         ST    R2,SRBDLEN              SAVE IT
         L     R2,SRBDMSG              ADDR OF MSG HOLD AREA
         MVC   SRBDMSG,JCTWORK         MOVE MSG TO SRB WORK AREA
         L     R15,SRBDENT             GET ENTRY TO SRB ROUTINE
         BALR  R14,R15                 LET THE RB DO THE DIAGNOSE
         MODESET KEY=NZERO             BACK TO THE JES2 KEY
         B     J1RETN8              RETURN TO JES2
         DROP  R3
NORMSG   WTO   'J2XIT01 - RMSG IS NOT AVAILABLE.  DRAIN ALL PRINTERS', X
               ROUTCDE=(1),DESC=(2)
         WTO   'J2XIT01 - CONTACT TECH SUPPORT',                       X
               ROUTCDE=(1),DESC=(2)
         B     J1RETN0
NOSRB    WTO   'J2XIT01 - J2SRB01 NOT AVAILABLE.  DRAIN ALL PRINTERS', X
               ROUTCDE=(1),DESC=(2)
         WTO   'J2XIT01 - CONTACT TECH SUPPORT',                       X
               ROUTCDE=(1),DESC=(2)
         B     J1RETN8
         SPACE 1
OOPS     MVC   OOPSWTO+19(L'MSG),JCTWORK
OOPSWTO  WTO   'JXT1 ERROR-.........1.........2.........3.........4....X
               ....',                                                  X
               ROUTCDE=(1),DESC=(2)
         DROP  R10                 DONE WITH JCT
         SPACE 2
***********************************************************************
*               COMMON EXIT ROUTINES                                  *
J1RETN0  LA    R15,0               0 = PRODUCE SEPARATOR PAGE    PLB
         B     J1RETURN            ALL DONE
         SPACE 1
         SPACE 1
J1RETN8  LA    R15,8               PRINT MY HEADER PAGE
         SPACE 1
J1RETURN $RETURN RC=(R15)          RETURN TO JES WITH RC IN REG 15
         SPACE 2
***********************************************************************
*               CONSTANTS AND OTHER STUFF                             *
MODEL    DC    CL80' JOB NAME=XXXXXXXX PRINTER=XXXXXXXX JOB ID=XXXXXXXXC
                PRTCLASS=X'
MODJOB   EQU   MODEL+10,8
MODPRT   EQU   MODEL+27,8
MODID    EQU   MODEL+43,8
MODCLS   EQU   MODEL+61,1
*
RMSGSSVT DC    A(0)                SAVE ADDR OF RMSG SSCVT
MSG      DC    CL48'CLOSE CUU DIST 12345678 NAME 12345678 LISTING '
FIXSTART EQU   *         START OF PGFIX AREA
P1DIAG   DC    CL48' '    ONE
P2DIAG   DC    CL48' '       SLOT
P3DIAG   DC    CL48' '           FOR
P4DIAG   DC    CL48' '              EACH
P5DIAG   DC    CL48' '                  JES2
P6DIAG   DC    CL48' '                      VIRTUAL
P7DIAG   DC    CL48' '                             PRINTER
FIXEND   EQU   *         END OF PGFIX AREA
         SPACE  2
         LTORG
         $MODEND
***********************************************************************
*                 NON-JES2 DSECTS                                     *
J2SRBD   DSECT    J2SRB DSECT FOR SRB VECTOR TABLE
SRBDENT  DS    A                ENTRY POINT ADDRESS
SRBDLEN  DS    F                LENGTH OF CLOSE MESSAGE
SRBDMSG  DS    CL80             THE ACTUAL CLOSE MSG
         CVT   DSECT=YES
         IEFJESCT
         IEFJSCVT
         IEFJSSVT
*
         END
/*
