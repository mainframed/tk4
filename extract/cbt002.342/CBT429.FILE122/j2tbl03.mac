         TITLE 'VALIDATE ACCOUNT NUMBER FOR J2XIT03'
***********************************************************************
*              SUB-ROUTINE INITIALIZATION                             *
J2TBL03  COMEIN BASE=12
         MVC   CC,=F'0'           ASSUME VALID RETURN
         SPACE 2
***********************************************************************
*              ARE WE RUNNING IN CMS OR MVS                           *
MVSCMS   NOP   MVS
         CLC   =8X'FF',16(R1)     IF CMS THEN THIS IS TRUE
         BE    CMS
         MVI   MVSCMS+1,X'F0'     SHOW MVS SYSTEM
         B     MVS
         SPACE 1
CMS      LA    R3,8(R1)           POINT TO THE ACCOUNT NUMBER
         LA    R2,8               WITH THIS LENGTH
         SPACE 2
***********************************************************************
*              SEARCH THE TABLE INDEX                                 *
MVS      MVC   AWORK,0(R3)        MOVE UNMODIFIED ACCT NBR TO HOLD
         CLI   0(R3),C'('         Q. IS IT IN PARENS
         BE    STRIP              A. YES - STRIP IT DOWN
         CLI   0(R3),X'7D'        Q. IS IT IN QUOTES
         BNE   FORMATOK           A. NO. THE FORMAT IS OK
         SPACE 1
STRIP    BCTR  R2,0               SKIP OVER PAREN/QUOTE
         BCTR  R2,0               BACK DOWN 1 FOR EXECUTE
         C     R2,=F'7'           Q. IS ACCT INFO > 8 CHARS ?   BJM
         BNH   MOVEIT             A. NO - OK TO MOVE IN         BJM
         LA    R2,7               ELSE SET LENGTH TO MAX.       BJM
MOVEIT   MVC   AWORK,AWORK-1      CLEAR THE WORKAREA
         EX    R2,FROMJES         MOVE ACCT TO WORK AREA
         SPACE 1
         LA    R4,AWORK           POINT TO THE WORKAREA
         LA    R5,1               LENGTH COUNTER
         SPACE 1
STRIP1   CLI   0(R4),C' '         Q. IS IT A BLANK
         BE    STRIP2             A. YES - THATS ALL
         SPACE 1
         CLI   0(R4),C','         Q. IS IT A COMMA
         BE    STRIP2             A. YES - THATS ALL
         SPACE 1
         CLI   0(R4),C')'         Q. IS IT A PAREN
         BE    STRIP2             A. YES - THATS ALL
         SPACE 1
         CLI   0(R4),X'7D'        Q. IS IT A QUOTE
         BE    STRIP2             A. YES - THATS ALL
         LA    R4,1(R4)           BUMP TO NEXT POSITION
         LA    R5,1(R5)           BUMP THE LENGTH
         C     R5,=F'9'           Q. END OF ACCOUNT FIELD
         BNE   STRIP1             A. NOT YET
         SPACE 1
STRIP2   BCTR  R5,0               BACK DOWN TO REAL LENGTH
         BCTR  R4,0               BACK DOWN TO REAL END
         LR    R2,R5              RESET ACCT NBR LENGTH
         BCTR  R5,0               BACK DOWN FOR EXECUTE
*        EX    R5,TOJES
         SPACE 1
FORMATOK LR    R1,R3              SAVE R3
         LA    R3,AWORK           POINT TO ACCT NUMBER
         CLC   0(2,R3),=C'00'     Q. DOES IT START WITH A NBR
         BL    CC4                A. NO - ITS INVALID
         SPACE 1
         USING INDEXD,R4
         LA    R4,INDEX           POINT TO TABLE INDEX
TLU1     CLC   0(2,R3),IDXKEY     Q. VALID KEY
         BE    KEYOK              A. YES
*        BH    CC4                A. ACCT NBR NOT VALID
         LA    R4,L'IDXALL(R4)    POINT TO NEXT ENTRY
         CLI   IDXALL,X'FE'       Q. END OF TABLE
         BNE   TLU1               A. NO - CONTINUE
         B     CC4                ACCT NBR IS INVALID
FROMJES  MVC   AWORK(0),1(R3)     MOVE ACCOUNT NUMBER FROM JES AREA
*TOJES    MVC   0(0,R3),AWORK      MOVE ACCOUNT NUMBER TO JES AREA
***********************************************************************
*              SEARCH THE TABLE FOR FULL ACCOUNT NUMBER               *
KEYOK    LM    R5,R6,IDX1ST       LOAD THE SEARCH REGS
         BCTR  R2,0               SETUP FOR EXECUTE
         SPACE 1
TLU2     EX    R2,CLC             Q. EQUAL ENTRY
         BE    CC0                A. YES
         LA    R5,L'DIV01(R5)     BUMP TO NEXT ENTRY
         BCT   R6,TLU2            AND LOOP
         B     CC4
CLC      CLC   0(0,R3),0(R5)      Q. SAME ACCOUNT NBR (EXECUTED)
***********************************************************************
*              RETURN TO CALLER WITH CC 4 OR CC 0                     *
CC4      MVC   CC,=F'4'           SET RETURN CODE TO 4
         B     EXIT
         SPACE 1
CC0      EQU   *
EXIT     GETOUT ,                 INITIAL CC IS ZERO
***********************************************************************
*              INDEX TABLE FOR MAIN ACCOUNT NUMBER TABLE              *
         DC    C' '               USE TO CLEAR AWORK
AWORK    DC    CL8' '
INDEX    DS    0F                 START ON A FULL WORD
         DC    CL2'01',H'0',A(DIV01),A((DIV02-DIV01)/L'DIV01)
         DC    CL2'02',H'0',A(DIV02),A((DIV03-DIV02)/L'DIV01)
         DC    CL2'03',H'0',A(DIV03),A((DIV04-DIV03)/L'DIV01)
         DC    CL2'04',H'0',A(DIV04),A((DIV05-DIV04)/L'DIV01)
         DC    CL2'05',H'0',A(DIV05),A((DIV06-DIV05)/L'DIV01)
         DC    CL2'06',H'0',A(DIV06),A((DIV07-DIV06)/L'DIV01)
         DC    CL2'07',H'0',A(DIV07),A((DIV08-DIV07)/L'DIV01)
         DC    CL2'08',H'0',A(DIV08),A((DIV09-DIV08)/L'DIV01)
         DC    CL2'09',H'0',A(DIV09),A((DIV10-DIV09)/L'DIV01)
         DC    CL2'10',H'0',A(DIV10),A((DIV11-DIV10)/L'DIV01)
         DC    CL2'11',H'0',A(DIV11),A((DIV12-DIV11)/L'DIV01)
         DC    CL2'12',H'0',A(DIV12),A((DIV14-DIV12)/L'DIV01)
         DC    CL2'14',H'0',A(DIV14),A((DIV15-DIV14)/L'DIV01)
         DC    CL2'15',H'0',A(DIV15),A((DIV16-DIV15)/L'DIV01)
         DC    CL2'16',H'0',A(DIV16),A((DIV17-DIV16)/L'DIV01)
         DC    CL2'17',H'0',A(DIV17),A((DIV18-DIV17)/L'DIV01)
         DC    CL2'18',H'0',A(DIV18),A((DIV55-DIV18)/L'DIV01)
         DC    CL2'55',H'0',A(DIV55),A((DIVXX-DIV55)/L'DIV01)
         DC    12X'FE'
         SPACE 1
***********************************************************************
*              TABLE OF VALID ACCOUNT NUMBERS                         *
DIV01    DC    CL8'01ACCORP'   CORPORATE ACCOUNTING
         DC    CL8'01ACTREC'   ACCOUNTS RECEIVABLE
         DC    CL8'01CPA   '           ADDED 01/21/88   PLB
         DC    CL8'01CRPFIN'   CORPORATE FINANCE
         DC    CL8'01FIXAST'   FIXED ASSETS
         DC    CL8'01GENLED'   GENERAL LEDGER
         DC    CL8'01PAYABL'   ACCOUNTS PAYABLE
         DC    CL8'01PAYROL'   PAYROLL
         SPACE 1
DIV02    DC    CL8'02GNLADM'   GENERAL ADMINISTRATIVE
         DC    CL8'02PRESGA'   PRESIDENTIAL ADMINISTRATION
         DC    CL8'02LEGAL'            ADD 03/21/88  PLB
         SPACE 1
DIV03    DC    CL8'03HRBFIN'   HERBERT LABS FINANCE
         DC    CL8'03HRBGA '   HERBERT LABS ADMINISTRATION
         DC    CL8'03HRBMKT'   HERBERT LABS MARKETING
         DC    CL8'03ORDENT'           ADDED 01/21/88   PLB
         DC    CL8'03SDADM '   HERBERT LABS SALES ADMINISTRATION
         DC    CL8'03SDDTL '   HERBERT LABS SALES DETAILING
         SPACE 1
DIV04    DC    CL8'04COMBEN'   COMPENSATION AND BENEFITS
         DC    CL8'04HRDEV '   HUMAN RESOURCES DEVELOPMENT
         DC    CL8'04HUMRES'   HUMAN RESOURCES ADMINISTRATION
         DC    CL8'04MANPOW'   MANPOWER PLANNING
         DC    CL8'04OHS   '   OCCUPATIONAL HEALTH/SAFETY
         SPACE 1
DIV05    DC    CL8'05DPADMN'   IS ADMINISTRATION
         DC    CL8'05DPDBSE'   IS DATA BASE
         DC    CL8'05DPINTL'   IS INTERNATIONAL
         DC    CL8'05DPMINI'   IS MINI/MICRO TECH SUPPORT
         DC    CL8'05DPOP  '   IS OPERATIONS
         DC    CL8'05DPPLAN'   IS PLANNING
         DC    CL8'05DPPROG'   IS SYSTEMS DEVELOPMENT
         DC    CL8'05DPTECH'   IS TECH SUPPORT
         DC    CL8'05DPTELE'   TELEPHONES
         DC    CL8'05OFFAUT'   OFFICE AUTOMATION
         SPACE 1
DIV06    DC    CL8'06EXPSER'   EXPORT SERVICES
         DC    CL8'06FININT'   INTERNATIONAL FINANCE
         DC    CL8'06HRINTL'   INTERNATIONAL HUMAN RESOURCES
         DC    CL8'06IACCTG'   INTERNATIONAL ACCOUNTING
         DC    CL8'06IRVTRN'   INTERNATIONAL SALES TRAINING/SUPPORT
         DC    CL8'06LATAMR'   LATIN AMERICA ADMINISTRATION
         DC    CL8'06LOGIST'   LOGISTICS
         DC    CL8'06MASTER'   MASTER FILE
         DC    CL8'06MATRLS'   DEMAND FORECAST           6-19
         DC    CL8'06MGTRPT'   MANAGEMENT REPORTING
         DC    CL8'06MKTPLN'   INTERNATIONAL MARKETING AND PLANNING
         DC    CL8'06PII   '   PROFITS IN INVENTORY
         DC    CL8'06PMF   '   PACKAGING MASTER FORM
         DC    CL8'06PROPLN'   PROMOTION PLANNING
         DC    CL8'06SOERPE'   SOUTHERN EUROPE ADMINISTRATION
         SPACE 1
DIV07    DC    CL8'07BID   '   BID REIMBURSEMENT
         DC    CL8'07BRC   '   BUSINESS REPLY CARDS
         DC    CL8'07CACCT '   COST ACCOUNTING
         DC    CL8'07HRAP  '   PHARM HUMAN RESOURCES
         DC    CL8'07INVOIC'   INVOICING
         DC    CL8'07LISTS '   LISTS AND LABELS
         DC    CL8'07ORDENT'   ORDER ENTRY
         DC    CL8'07PHMDTL'   PHARM SALES DETAILING
         DC    CL8'07PHMFIN'   PHARM FINANCE
         DC    CL8'07PHMGNA'   PHARM ADMINISTRATION
         DC    CL8'07PHMISA'   PHARM SALES ADMINISTRATION
         DC    CL8'07PHMMKT'   PHARM MARKETING
         DC    CL8'07PHMSAD'   PHARM SADS
         DC    CL8'07PHMTPR'   PHARM SALES TRADE & PROF RELATIONS
         DC    CL8'07PHYINV'   PHYSICAL INVENTORY
         DC    CL8'07PPO   '   PPO
         DC    CL8'07PROFIL'   PROFILING
         DC    CL8'07PRSNEL'   PERSONNEL
         DC    CL8'07QTRLTR'   QUARTERLY LETTERS
         DC    CL8'07SALRPT'   SALES REPORTING
         DC    CL8'07SLSAUT'   SALESMAN AUTOMATICS
         DC    CL8'07WARNTY'   WARRANTY CARDS
         DC    CL8'07WPG   '   WPG
         SPACE 1
DIV08    DC    CL8'08MEDASB'   MEDICAL ASSEMBLY
         DC    CL8'08PADMIN'   PLASTICS ADMINISTRATION
         DC    CL8'08PMLDNG'   PLASTICS MOLDING
         DC    CL8'08PWHSE '   PLASTICS WAREHOUSE
         SPACE 1
DIV09    DC    CL8'09DISTRB'           ADDED 01/21/88   PLB
         DC    CL8'09INVCON'   INVENTORY CONTROL
         DC    CL8'09MATADM'   MATERIALS ADMINISTRATION
         DC    CL8'09MRPS  '   MATERIALS PLANNING
         DC    CL8'09OPHPKG'   OPHTHALMIC PACKAGING
         DC    CL8'09OPRTNS'   OPERATIONS
         DC    CL8'09PHYINV'   PHYSICAL INVENTORY
         DC    CL8'09PKGDEV'   PACKAGE DEVELOPMENT
         DC    CL8'09PLANNG'   PLANNING
         DC    CL8'09PRCHNG'   PURCHASING
         DC    CL8'09TRANS '   TRANSMISSION TO SERIES I
         DC    CL8'09WHEREC'   WAREHOUSE AND RECEIVING
         SPACE 1
DIV10    DC    CL8'10HEATER'   HEATERS
         DC    CL8'10QCDOCU'   QC DOCUMENTATION
         DC    CL8'10QCADMN'   QC ADMINISTRATION
         DC    CL8'10QCCHEM'   QC CHEMISTRY
         DC    CL8'10RECALL'   PRODUCT RECALL
         SPACE 1
DIV11    DC    CL8'11ANYDEV'   ANALYTICAL CHEMISTRY
         DC    CL8'11BOSTAT'   BIOSTATISTICS
         DC    CL8'11CICEN '   CORPORATE INFORMATION CENTER     6-30
         DC    CL8'11CISTMS'   INFORMATION SYSTEMS AND SERVICES
         DC    CL8'11CRSA  '   MEDICAL OPERATIONS
         DC    CL8'11DRBMET'   DRUG METABOLISM                  6-30
         DC    CL8'11IPL   '   INVESTIGATIVE PRODUCTS LAB
         DC    CL8'11MBSADM'   DISCOVERY RESEARCH
         DC    CL8'11MCBLGY'   MICROBIOLOGY
         DC    CL8'11MEDOP '   MEDICAL OPERATIONS               6-30
         DC    CL8'11MRD   '   RESEARCH AND DEVELOPMENT ADMINISTRATION
         DC    CL8'11MSI   '   COMMUNICATION SERVICES
         DC    CL8'11PHARM '   PHARMACEUTICAL RESEARCH          6-30
         DC    CL8'11PKINET'   PHARMACOKINETICS                 6-30
         DC    CL8'11QCPARC'   QC PRODUCT ASSURANCE
         DC    CL8'11RDADM '   R&D FINANCE/ADMINISTRATION
         DC    CL8'11RDSIS '   R&D INFOR SYSTEMS AND SERVICES
         DC    CL8'11SPECT '   SPECTROSCOPY                     6-30
         DC    CL8'11STSADM'   SCIENTIFIC & TECH SERVICES ADMIN 6-30
         DC    CL8'11TOXLGY'   TOXICOLOGY
         SPACE 1
DIV12    DC    CL8'12CLCADM'   CONTACT LENS CLINICAL R&D ADMIN
         DC    CL8'12HRAO  '   OPTICAL HUMAN RESOURCES
         DC    CL8'12OPTDTL'   OPTICAL SALES DETAILING
         DC    CL8'12OPTFIN'   OPTICAL FINANCE
         DC    CL8'12OPTGNA'   OPTICAL ADMINISTRATION
         DC    CL8'12OPTISA'   OPTICAL SALES ADMINISTRATION
         DC    CL8'12OPTMKT'   OPTICAL MARKETING
         DC    CL8'12ORDENT'           ADDED 01/21/88   PLB
         SPACE 1
DIV14    DC    CL8'14CRPCOM'   CORPORATE COMMUNICATIONS
         DC    CL8'14CRPPLN'   CORPORATE PLANNING
         DC    CL8'14GMCADM'   GROUP MARKETING
         DC    CL8'14MKTINF'   MARKETING INFORMATION
         DC    CL8'14PROFPR'   PROFESSIONAL EDUCATION/PUBLIC RELATIONS
         SPACE 1
DIV15    DC    CL8'15CONV  '   AMO CONVERSION            6-19
         DC    CL8'15ORDERS'   AMO ORDER ENTRY           6-19
         DC    CL8'15SALES '   AMO SALES REPORTING       6-19
         DC    CL8'15M01200'   AMO SALES                 6-30
         DC    CL8'15M05200'   AMO FINANCE               6-30
         DC    CL8'15M05310'   AMO ACCOUNTS RECEIVABLE   6-30
         DC    CL8'15M05330'   AMO ACCOUNTS PAYABLE      6-30
         DC    CL8'15M06100'   AMO HUMAN RESOURCES       6-30
         SPACE 1
DIV16    DC    CL8'16ARCI  '   ALLERGAN CLINICAL RESEARCH INST  6-30
         DC    CL8'16OPHCR '   OPHTHALMIC CLINICAL RESEARCH
         DC    CL8'16PHMDEV'   PHARM DEVELOPMENT
         DC    CL8'16PHMRAD'   PHARM R&D ADMINISTRATION
         DC    CL8'16PHMREG'   PHARM REGULATORY AFFAIRS
         SPACE 1
DIV17    DC    CL8'17CLANY '   CONTACT LENS ANALYTICAL
         DC    CL8'17CLCD  '   CONTACT LENS CLINICAL DEVELOPMENT
         DC    CL8'17CLDEV '   CONTACT LENS ADMINISTRATION
         DC    CL8'17CLDRES'   CONTACT LENS DEVICE RESEARCH 6-30
         DC    CL8'17CLFORM'   CONTACT LENS FORMULATION
         DC    CL8'17CLINIC'   ALLERGAN RESEARCH CLINIC
         DC    CL8'17CLPISV'   CONTACT LENS PRODUCT INFO SERVICES 6-30
         DC    CL8'17CLREG '   CONTACT LENS REGULATORY AFFAIRS
         DC    CL8'17CLRES '   CONTACT LENS RESEARCH        6-30
         DC    CL8'17OPTRAD'   OPTICAL R&D ADMINISTRATION   6-30
         SPACE 1
DIV18    DC    CL8'18HRBCL '   HERBERT LABS CLINICAL R&D
         DC    CL8'18HRBPCL'   HERBERT LABS PRECLINICAL R&D
         DC    CL8'18HRBRA '   HERBERT LABS REGULATORY AFFAIRS    6-30
         DC    CL8'18HRBRD '   HERBERT LABS R&D ADMINISTRATION
         SPACE 1
DIV55    DC    CL8'55CICS  '   CICS
         DC    CL8'55MVSOVD'   MVS OVERHEAD
         DC    CL8'55PRDTIS'   PRODUCTION TIS
         DC    CL8'55TSTTIS'   TEST TIS
         DC    CL8'55VMOVHD'   VM
         DC    CL8'55VMOPER'   VM OPERATIONS
         DC    8X'FE'
         SPACE 1
DIVXX    EQU   *
***********************************************************************
*              LITERAL POOL                                           *
         LTORG
***********************************************************************
*              DSECT FOR TABLE INDEX                                  *
INDEXD   DSECT
IDXALL   DS    0CL12
IDXKEY   DS    CL2                1ST PART OF ACCOUNT NUMBER
         DS    H                  RESERVED
IDX1ST   DS    A(0)               START OF LIST
IDXNBR   DS    F'0'               NUMBER OF ENTRIES
         END
