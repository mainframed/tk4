//RTPLBMSG JOB (05DPTECH,TECH),'P BANKS',
//             MSGCLASS=R,CLASS=2,
// USER=UUUUUUUU,PASSWORD=PPPPPPPP
//*XXXXXXX EXEC PGM=IFOX00,PARM='DECK,NOLOAD'
//XXXXXXXX EXEC PGM=IEV90,PARM='DECK,NOLOAD'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS2.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSUT2   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSUT3   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSPUNCH DD  DSN=BANKS.SOURCE.OBJ(RMSG),DISP=SHR
//SYSPRINT DD  SYSOUT=*
//SYSIN DD *
         TITLE 'SUB-SYSTEM TO TRAP WTOR MSG FOR OPERATORS'
***********************************************************************
*                                                                     *
*  10-25-87 PLB  MODIFIED TO RUN ON MULTIPLE SYSTEMS                  *
*                CHANGED LOGIC TO ISSUES COMMANDS USING RMSGCMD PROC  *
*                RESTRUCTURED SEARCH LOGIC                            *
*                RESTRUCTURED CMD START LOGIC                         *
*                                                                     *
*                                                                     *
*  THIS IS THE RMSG SUBSYSTEM.  IT IS LOADED BY THE RMSGLOAD PROGRAM. *
*                                                                     *
*  THIS PROGRAM IS LOADED INTO COMMON STORAGE (SP 241)                *
*                                                                     *
*  THE COMMUNICATIONS CHARACTER IS +                                  *
*   SEE COMMENTS AROUND THE LABEL HELP FOR MORE INFORMATION.          *
*                                                                     *
*  THIS SUB-SYSTEM SEES ALL WTO WTOR AND OPERATOR COMMANDS.           *
*                                                                     *
*  RMSG WILL RESPOND TO BOTH WTOR AND WTO MESSAGES                    *
*  I CAUTION THE USER AGAINST USING THIS PROGRAM WITHOUT FULLY        *
*   UNDERSTANDING THE REPLIES AND ACTIONS TAKEN FOR EACH MESSAGE      *
*   LISTED IN THE SOURCE CODE.                                        *
***********************************************************************
         SPACE 1
***********************************************************************
*                SUB-SYSTEM INITALIZATION                             *
RMSG     CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING RMSG,R15
RMSGINIT SAVE  (14,12)            SAVE THE REGS
         LA    R2,SSVTRSV1        ADDR OF THE SSVT FOR RMSG
         ST    R2,0(R1)           HOOK THE SSVT TO THE SSCVT
         WTO   'RMSG1 RMSG CONNECTED TO SSVT'
         RETURN (14,12)           RETURN TO RMSGLOAD
         DROP  R15
***********************************************************************
*                THE REAL SSVT                                        *
         DS    0F
SSVTRSV1 DC    H'0'                 RESERVED
SSVTFNUM DC    H'2'                 NUMBER OF FUNCTIONS SUPPORTED
SSVTFCOD DC    256X'00'             FUNCTION MATRIX
         ORG   SSVTFCOD+8           ORG TO FUNCTION 9
         DC    X'01'                LOOK AT WTO MSGS
         ORG   SSVTFCOD+9           ORG TO FUNCTION 10
         DC    X'01'                LOOK AT COMMANDS
         ORG   ,
SSVTFRTN DC    256A(*-*)            256 WORD MATRIX TABLE
         ORG   SSVTFRTN             ORG BACK TO 1ST WORD
         DC    A(RMSGENT)           ENTRY TO RMSG FOR MESSAGES
         ORG   ,
         DC    A(*-*)       POINTER TO SRB VECTOR TBL FOR J2XIT01
*
         SPACE 1
***********************************************************************
*                NORMAL SUB-SYSTEM ENTRY                              *
*                                                                     *
*       R0 POINTS TO MY SSCVT                                         *
*       R1 POINTS TO CURRENT SSOB                                     *
*       R13 POINT TO A STANDARD SAVE AREA                             *
*                                                                     *
         SPACE 1
RMSGENT  SAVE  (14,12)            SAVE THE REGS
         LR    R3,R15             SET THE SUB-SYSTEM BASE
         USING RMSGENT,R3
         SPACE 1
***********************************************************************
*          SUBSYSTEM SELECTION ROUTINE                                *
         SPACE 1
         LR    R4,R1              SET SSOB BASE
         NOP   PASTWTO
         MVI   *-3,X'F0'
******************************************************************BJM**
*          SUBSYSTEM SELECTION ROUTINE                            BJM *
         USING PSA,R0                                             BJM
         L     R2,CVTPTR               CVT POINTER                BJM
         USING CVT,R2                                             BJM
         L     R5,CVTSMCA              POINT TO SMF CONTROL AREA  BJM
         USING SMCABASE,R5                                        BJM
         MVC   SID,SMCASID             SAVE SYSTEM ID  FOR LATER  BJM
         SPACE 1
***********************************************************************
*         SETUP TO PROCESS MESSAGES                                   *
PASTWTO  EQU   *
         USING SSOB,R4
         SPACE 1
         CLC   SSOBFUNC,=H'9'     Q. IS THIS A WTO/R REQUEST
         BE    TRYWTOR            A. YES - GO TO WTOR SCAN ROUTINE
         SPACE 1
         CLC   SSOBFUNC,=H'10'    Q. IS THIS A COMMAND
         BE    TRYRMSG            A. YES - GO TO COMMAND SECTION
         WTO   'RMSG MISSED FUNCTION'
         B     QUICKXIT
         SPACE 1
TRYWTOR  TM    FUNCTION,$NOMSG    Q. PROCESS MESSAGES
         BO    EXIT0              A. NO - JUST EXIT
         SPACE 1
         L     R5,SSOBINDV        POINT TO SSOB EXTENSION FOR WTO
         USING SSWTBGN,R5
         SPACE 1
         L     R7,SSWTWQE         GET ADDRESS OF WQE FOR MESSAGE
         USING WQE,R7
         SPACE 1
         LTR   R7,R7              Q. IS THERE A WQE
         BZ    EXIT0              A. NO - SOMETHING IS WRONG - LEAVE
         SPACE 1
         L     R6,SSWTORE         GET REPLY ELEMENT ADDRESS
         USING OREF,R6
         SPACE 1
         LTR   R6,R6              Q. IS THERE A REPLY ELEMENT
         BZ    WTOMSG             A. NO - MUST BE A WTO MSG
         SPACE 1
***********************************************************************
*         ROUTINE TO PROCESS WTOR MESSAGES                            *
         SPACE 1                                                   BJM
         CLC   =C'MSX048D',WQETXT+4 Q. MSX STARTUP MESSAGE         BJM
         BE    REPLYY               A. YES - REPLY 'Y' TO IT       BJM
         SPACE 1
         CLC   =C'ICK508A',WQETXT+4 Q. ICKDSF VTOC CONVERSION
         BE    REPLYU               A. YES - REPLY 'U' TO IT
         SPACE 1
         CLC   =C'IEC804A',WQETXT+4 Q. CU/DEVICE NOT OPERATIONAL
         BE    IEC804A              A. YES - CHECK DEVICE ETC
         SPACE 1
         CLC   =C'IEF433D',WQETXT+4 Q. TAPE WAIT MSG               PLB
         BE    IEF433D              A. YES - REPLY NOHOLD TO IT    PLB
         SPACE 1
         CLC   =C'IKT010D',WQETXT+4 Q. TSO SHUTDOWN W/USERS
         BE    IKT010D              A. NO - TRY NEXT MSG
         SPACE 1
         CLC   =C'IKT012D',WQETXT+4 Q. TSO SHUTDOWN W/USERS
         BE    REPLYU               A. YES - REPLY 'U' TO IT
         B     EXIT0                MSG NOT WANTED - EXIT
         SPACE 1
*---  VERIFY REPLY IS FOR A TERMINAL AND NOT A  CONTROLLER
IEC804A  LA    R12,WQETXT+13      START SCAN FOR DEVICE
         LA    R11,10             LIMIT THE SCAN
         LA    R1,POSTMSG         ASSUME WE WILL REPLY
         SPACE 1
SCANDEV  CLC   =C'DEVICE',0(R12)  Q. IS THIS A DEVICE MSG
         BE    POSTIT             A. YES - PREPARE TO POST THE MSG
         LA    R12,1(R12)         BUMP THE INDEX
         BCT   R11,SCANDEV        CONTINUE THE SCAN
         B     EXIT0              NOT FOUND - JUST EXIT
*---  SETUP TO REPLY 'FSTOP' FOR TSO MSG
IKT010D  LA    R1,FSTOPMSG        POINT TO R ##,FSTOP MSG
         B     POSTIT             SEND THE REPLY
         SPACE 1
*---  SETUP TO REPLY 'NOHOLD' FOR TAPE MSG
IEF433D  LA    R1,NOHOLD          POINT TO R ##,NOHOLD MSG
         B     POSTIT             SEND THE REPLY
         SPACE 1
*---  SETUP TO REPLY 'U' TO WTOR
REPLYU   LA    R1,UMSG
         SPACE 1
*---  SETUP TO REPLY TO THE MESSAGE
POSTIT   MVC   4+2(2,R1),OREID    SET THE REPLY NUMBER
DOCMD    SR    R0,R0              SETUP TO SEND A COMMAND
         SVC   34
         B     EXIT0              LEAVE
         SPACE 1
*---  SETUP TO REPLY 'Y' TO WTOR                                   BJM
REPLYY   LA    R1,YMSG                                             BJM
         B     POSTIT                                              BJM
         SPACE 1                                                   BJM
*---  ISSUE A SERIES OF COMMANDS
CMDLOOP  CLI   0(R2),X'FE'        Q. END OF TABLE                  PLB
         BE    EXIT0              A. YES - WE ARE DONE             PLB
         L     R1,0(R2)           LOAD COMMAND POINTER             PLB
         SR    R0,R0              CLEAR R0                         PLB
         SVC   34                 ISSUE COMMAND                    PLB
         LA    R2,4(R2)           NEXT ENTRY                       PLB
         B     CMDLOOP            ISSUE NEXT COMMAND               PLB
         SPACE 1
***********************************************************************
*         ROUTINE TO PROCESS WTO MESSAGES                             *
WTOMSG   EQU   *
*---  QUICK INDEX TO MSG LOOKUP ROUTINES
         CLI   WQETXT+1,C'$'         Q. IS IT A HASP MSG           PLB
         BE    HASPMSG               A. YES - PROCESS IT           PLB
         SPACE 1
         CLC   =C'EC',WQETXT+1       Q. IS IT AN EPILOG MSG        PLB
         BE    ECMSG                 A. YES - PROCESS IT           PLB
         SPACE 1
         CLC   =C'IEC',WQETXT+1      Q. IS IT A TAPE MSG           PLB
         BE    IECMSG                A. YES - PROCESS IT           PLB
         SPACE 1
         CLC   =C'IEE',WQETXT+1      Q. IS IT AN IEE.. MSG         PLB
         BE    IEEMSG                A. YES - PROCESS IT           PLB
         SPACE 1
         CLC   =C'IEF',WQETXT+1      Q. IS IT AN IEF.. MSG         PLB
         BE    IEFMSG                A. YES - PROCESS IT           PLB
         SPACE 1
         CLC   =C'IST',WQETXT+1      Q. IS IT A TSO MSG            PLB
         BE    ISTMSG                A. YES - PROCESS IT           PLB
         SPACE 1
         CLC   =C'+RCMD',WQETXT+1    Q. IS IT A RMSG REQUEST       PLB
         BE    RCMD                  A. YES - PROCESS IT           PLB
         SPACE 1
         CLC   =C'SAR',WQETXT+1      Q. IS IT A SAR MSG            PLB
         BE    SARMSG                A. YES - PROCESS IT           PLB
         B     EXIT0                WE DON'T WANT IT - EXIT        PLB
         SPACE 1
*---  $HASP MESSAGE LOOKUP ROUTINE
HASPMSG  CLC   =C'$HASP085',WQETXT+1 Q. IS JES SHUT DOWN
         BE    HASP085               A. YES - ISSUE ZEOD
         SPACE 1
         CLC   =C'$HASP099',WQETXT+1 Q. IS JES IDLE
         BE    HASP099               A. YES - TEST FOR SHUTDOWN
         SPACE 1
         CLC   =C'$HASP190',WQETXT+1 Q. IS THIS A JES SETUP MSG
         BE    HASP190               A. YES - SCAN FOR DEVICE TYPE
         SPACE 1
         CLC   =C'$HASP373 PODOPEP',WQETXT+1 Q. START OF EPIUTIL ? BJM
         BE    HASP373              A. YES - WHICH SYSTEM          PLB
         B     EXIT0                WE DON'T WANT IT - EXIT        PLB
         SPACE 1
*---  EPILOG MESSAGE LOOKUP ROUTINE
ECMSG    CLC   =C'EC899:',WQETXT+1 Q. IS THE EPILOG DATBASE FULL?  BJM
         BE    EC899               A. YES - FIX IT                 PLB
         B     EXIT0                WE DON'T WANT IT - EXIT        PLB
         SPACE 1
*---  IEC.. MESSAGE LOOKUP ROUTINE
IECMSG   CLC   =C'IECTMS3',WQETXT+1 Q. TAPE DENSITY CHANGE         PLB
         BE    IECTMS3              A. YES - CHECK CODE            PLB
         B     EXIT0                WE DON'T WANT IT - EXIT        PLB
         SPACE 1
*---  IEE.. MESSAGE LOOKUP ROUTINE
IEEMSG   CLC   =C'IEE043I',WQETXT+1 Q. SYSLOG SWITCH               PLB
         BE    IEE043               A. YES - START A TASK          PLB
         SPACE 1
*        CLC   =C'IEE362A',WQETXT+1 Q. SMF SWITCH                  PLB
*        BE    IEE362               A. YES - START A TASK          PLB
         B     EXIT0                WE DON'T WANT IT - EXIT        PLB
         SPACE 1
*---  RCMD MESSAGE LOOKUP ROUTINE
RCMD     EQU   *
         CLC   =C'+RCMD',WQETXT+1 Q. IS THIS A RMSGCMD REQUEST  PLB
         BNE   EXIT0                A. NO - LEAVE                  PLB
         SPACE 1
*        CLI   WQETXT+6,X'08'       Q. GOOD MSG                    PLB
*        BNE   EXIT0                A. NO - JUST EXIT              PLB
         SPACE 1
RCMDOK   MVC   RCMDCMD,WQETXT+7     MOVE COMMAND TO WTO
         LA    R1,RCMDWTO
         B     DOCMD                DO IT AND LEAVE
         SPACE 1
*---  IEF.. MESSAGE LOOKUP ROUTINE
IEFMSG   EQU   *
         CLC   =C'IEF257I',WQETXT+1 Q. REQUESTED SPACE NOT AVAIL. ?BJM
         BE    IEF257I              A. YES - CHECK FOR PROD JOB    PLB
         SPACE 1
         CLC   =C'IEF452I',WQETXT+1 Q. JCL ERROR?                  PLB
         BE    IEF452I              A. YES - CHECK FOR PROD JOB    PLB
         SPACE 1
         CLC   =C'IEF453I',WQETXT+1 Q. JCL ERROR?                  PLB
         BE    IEF453I              A. YES - CHECK FOR PROD JOB    PLB
         B     EXIT0                WE DON'T WANT IT - EXIT        PLB
         SPACE 1
*---  TSO MESSAGE LOOKUP ROUTINE
ISTMSG   CLC   =C'IST020I',WQETXT+1 Q. IS THIS A VTAM INIT COMPLETE ?
         BE    IST020I              A. YES - START OTHER STUFF     PLB
         B     EXIT0                WE DON'T WANT IT - EXIT        PLB
         SPACE 1
*---  SAR MESSAGE LOOKUP ROUTINE
SARMSG   CLC   =C'SARSTC99',WQETXT+1 Q. SAR ERROR MESSAGE?         PLB
         BNE   EXIT0                 A. NO - LEAVE                 PLB
         B     DOLOLITE              SET ROLL DELETE & INFO ONLY   PLB
         SPACE 1
*---  JES2 TERMINATED - ISSUE 'Z EOD' IF QUIT MVS COMMAND USED
HASP085  CLI   QUITSW,C'Y'          Q. WAS QUIT MVS CMD ISSUED
         BNE   EXIT0                A. NO - JUST LEAVE
         SPACE 1
         MVI   QUITSW,C'N'          RESET IT
         LA    R1,ZEOD              PREPARE TO DO 'Z EOD'
         B     DOCMD                AND DO IT
         SPACE 1
*---  JES2 IS IDLE - ISSUE '$PJES2' IF QUIT MVS COMMAND USED
HASP099  CLI   QUITSW,C'Y'          Q. WAS QUIT MVS CMD ISSUED
         BNE   EXIT0                A. NO - JUST LEAVE
         SPACE 1
         LA    R1,PJES2             PREPARE TO STOP JES2
         B     DOCMD                AND DO IT
         SPACE 1
*---  SCAN FOR DEVICE FOR SETUP MESSAGE
HASP190  LA    R12,VPRINT           POINT TO TABLE OF VIRTUAL PRINTERS
VPRTLOOP CLI   0(R12),X'FE'         Q. END OF TABLE
         BE    EXIT0                A. YES - JUST EXIT
         SPACE 1
         CLC   WQETXT+27(8),0(R12)  Q. IS THIS A VIRTUAL PRINTER
         BE    VPRTOK               A. YES - SET IT UP
         LA    R12,8(R12)           INDEX TO NEXT ENTRY
         B     VPRTLOOP             AND LOOP
         SPACE 1
VPRTOK   MVC   $SPRTID,WQETXT+27    MODIFY START PRINTER COMMAND
         LA    R1,$SPRT             POINT TO IT
         B     DOCMD                AND ISSUE THE COMMAND
         SPACE 1
*---  CHECK FOR PRODUCTION JOB - IF IT IS, HIGHLIGHT THE MSG
IEF452I  EQU   *
IEF453I  EQU   *
IEF257I  EQU   *
IEF722I  EQU   *
         CLI   WQETXT+9,C'P'        Q. IS JOBNM (IN MSG) FOR PROD? BJM
         BNE   EXIT0                A. NO - DON'T WANT THIS JOB    PLB
         SPACE 1
         CLI   WQETXT+16,C' '       Q. IS JOB NAME 8 CHARACTERS    PLB
         BE    EXIT0                A. NO - NOT A VALID PROD JOB   PLB
         SPACE 1
         CLC   =C'PEDERSEN',WQETXT+9 Q. IS THIS VICKI              PLB
         BE    EXIT0                 A. YES - IT'S NOT PRODUCTION  PLB
         SPACE 1
         B     DOHILITE
         SPACE 1
DOHILITE MVI   WQEDC1,WQEDCB        SET DESC CODE TO HI-LITE MSG   BJM
         B     EXIT0                TIME TO LEAVE                  BJM
         SPACE 1
DOLOLITE MVI   WQEDC1,WQEDCF        SET DESC CODE TO JOB STATUS    PLB
         MVI   WQEROUT1,WQEROUTB    SET ROUTE CODE TO INFORMATION  PLB
         B     EXIT0                TIME TO LEAVE                  PLB
         SPACE 1
*---  VTAM IS STARTED - NOW START OTHER TASKS
IST020I  LA    R2,VTAMMVS1        ASSUME MVS1 SYSTEM               PLB
         CLC   SID,=C'MVS1'       Q. ARE WE RUNNING ON MVS NATIVE? BJM
         BE    CMDLOOP            A. YES - ISSUE COMMANDS          BJM
         SPACE 1
         CLC   SID,=C'MINI'       Q. ARE WE RUNNING ON MVS TEST    PLB
         BE    CMDLOOP            A. YES - ISSUE COMMANDS          PLB
         SPACE 1
         LA    R2,VTAMMXAT        ASSUME MVS/XA TEST SYSTEM        PLB
         CLC   SID,=C'MXAT'       Q. ARE WE RUNNING ON XA TEST     PLB
         BE    CMDLOOP            A. YES - ISSUE COMMANDS          PLB
         SPACE 1
         LA    R2,VTAMMVS2        ASSUME MVS2 SYSTEM               PLB
         CLC   SID,=C'MVS2'       Q. ARE WE RUNNING ON MVS/VM      PLB
         BE    CMDLOOP            A. YES - ISSUE COMMANDS          PLB
SIDBAD   WTO   'RMSG MVS SYSTEM ID NOT KNOWN',DESC=1,ROUTCDE=1     PLB
         B     EXIT0
         SPACE 1
*---  EPILOG IF FULL UNLOAD IT AND RESTART IT
EC899    LA    R2,EPMVS1          ASSUME MVS NATIVE                PLB
         CLC   SID,=C'MVS1'       Q. ARE WE RUNNING ON MVS NATIVE? BJM
         BE    CMDLOOP            A. YES - ISSUE THE COMMANDS      PLB
         LA    R2,EPMVS2          NOW TRY MVS/VM                   PLB
         CLC   SID,=C'MVS2'       Q. ARE WE RUNNING ON MVS/VM      PLB
         BE    CMDLOOP            A. YES - ISSUE THE COMMANDS      PLB
         B     SIDBAD             OOPS - UNKNOWN SYSTEM            PLB
         SPACE 1
*---  TMS TAPE MESSAGE                                             PLB
IECTMS3  CLC   =C'(88)',WQETXT+35 Q. DENSITY CHANGE                PLB
         BNE   EXIT0              A. NO - JUST LEAVE               PLB
         SPACE 1
         MVC   TMSMSG1V,WQETXT+10
         WTO   'RMSG - TAPE DENSITY CHANGE NOT ALLOWED',           PLB X
               ROUTCDE=(1),DESC=(2)
TMSMSG1  WTO   'RMSG - THE SCRATCH TAPE ON XXX IS THE WRONG DENSITY',  X
               ROUTCDE=(1),DESC=(2)
TMSMSG1V EQU   TMSMSG1+35,3
         WTO   'RMSG - MOUNT A SCRATCH TAPE WITH THE PROPER DENISTY',  X
               ROUTCDE=(1),DESC=(2)
         B     EXIT0
         SPACE 1
*---  SYSLOG SWITCHED - SETUP A CONSTANT FOR SAR
IEE043   LA    R1,SYSLOGN         ASSUME MVS NATIVE                PLB
         CLC   SID,=C'MVS1'       Q. ARE WE RUNNING ON MVS NATIVE? BJM
         BE    DOCMD              A. YES - ISSUE THE COMMANDS      PLB
         SPACE 1
         LA    R1,SYSLOGV         NOW TRY MVS/VM                   PLB
         CLC   SID,=C'MVS2'       Q. ARE WE RUNNING ON MVS/VM      PLB
         BE    DOCMD              A. YES - ISSUE THE COMMANDS      PLB
         SPACE 1
         LA    R1,SYSLOGM         NOW TRY MVS TEST SYSTEM          PLB
         CLC   SID,=C'MINI'       Q. ARE WE RUNNING ON MVS TEST    PLB
         BE    DOCMD              A. YES - ISSUE COMMANDS          PLB
         SPACE 1
         LA    R1,SYSLOGX         NOW TRY XA TEST SYSTEM          PLB
         CLC   SID,=C'MXAT'       Q. ARE WE RUNNING ON XA TEST     PLB
         BE    DOCMD              A. YES - ISSUE COMMANDS          PLB
         SPACE 1
         B     SIDBAD             OOPS - UNKNOWN SYSTEM            PLB
         SPACE 1
*---  SMF SWITCHED - START A TASK TO DUMP IT
IEE362   LA    R1,SMFDUMP         POINT TO PARM FIELD IN COMMAND   PLB
         MVC   SMFID,WQETXT+36    MOVE SMF ID TO START COMMAND     PLB
         B     DOCMD              ISSUE SMFDUMP,X                  PLB
         SPACE 1
*---  EPILOG IS RESTARTING - WHICH ONE IS IT?
HASP373  LA    R1,SEPICMDN        ASSUME MVS NATIVE                BJM
         CLC   =C'$HASP373 PODOPEPN',WQETXT+1 Q. START OF EPIUTILN?BJM
         BE    DOCMD                A. YES - GO START EPILOGN STC  BJM
         SPACE 1                                                   BJM
         LA    R1,SEPICMDV        NOW TRY MVS/VM                   BJM
         CLC   =C'$HASP373 PODOPEPV',WQETXT+1 Q. START OF EPIUTILV?BJM
         BE    DOCMD                A. YES - GO START EPILOGV STC  BJM
         B     EXIT0                ITS NOT EPILOG - EXIT          PLB
         SPACE 1
         DROP  R5                 THE SSOB EXTENTION
         DROP  R6                 THE ORE
         DROP  R7                 AND FINALLY THE WQE
         SPACE 1
***********************************************************************
*        TRY FOR RMSG SUBSYSTEM COMMANDS                              *
         SPACE 1
TRYRMSG  EQU   *
         SPACE 1
         L     R5,SSOBINDV        POINT TO SSOB EXTENSION FOR OP CMDS
         USING SSCMBGN,R5
         SPACE 1
         L     R6,SSCMBUFF        POINT TO THE COMMAND BUFFER
         USING CBF,R6
         SPACE 1
         LA    R12,CBFTXT         FIND FIRST NON-BLANK IN CMD LINE
         LH    R11,CBFCNT         LENGTH OF COMMAND
NONBLANK CLI   0(R12),C' '        Q. IS THIS POSITION BLANK
         BNE   GOT1ST             A. NO - THATS GOOD
         LA    R12,1(R12)         BUMP INDEX
         BCT   R11,NONBLANK       LOOP FOR AWHILE
         B     QUICKXIT           NOTHING FOUND
         SPACE 1
GOT1ST   CLI   0(R12),C'+'        Q. IS THIS MY COMMAND
         BNE   TRYCMDS            A. NO - TRY FOR OPER COMMAND
         SPACE 1
         WTO   'RMSG IS ALIVE AND WELL'
         BAL   R14,PARSE          PARSE THE COMMAND
         SPACE 1
         CLC   WORD2,START        Q. DO THEY WANT TO START SOMETHING?
         BE    CMSTART            A. YES - SEE WHAT IT IS
         SPACE 1
         CLC   WORD2,STOP         Q. DO THEY WANT TO STOP SOMETHING?
         BE    CMSTOP             A. YES - SEE WHAT IT IS
         SPACE 1
         CLC   WORD2,HELP         Q. DO THEY WANT SOME HELP?
         BE    CMHELP             A. YES - GIVE IT TO THEM
         SPACE 1
         CLC   WORD2,STATUS       Q. DO THEY WANT THE STATUS
         BE    CMSTAT             A. YES - SHOW IT TO THEM
         SPACE 1
         CLC   WORD2,ABORT        Q. AM I ABOUT READY TO DIE?
         BE    CMABORT            A. YES
         B     CMHUH              THEY DON'T KNOW WHAT THEY ARE DOING
         SPACE 1
CMSTART  CLI   WORD3,C'C'         Q. TURN ON COMMAND PROCESSING
         BNE   CMSTART1           A. NO - TRY FOR MESSAGES
         NI    FUNCTION,255-$NOCMD TURN ON COMMAND PROCESSING
         B     CMSTAT             AND SHOW NEW STATUS
         SPACE 1
CMSTART1 CLI   WORD3,C'M'         Q. TURN ON MESSAGE PROCESSING
         BNE   CMHUH              A. INVALID FUNCTION
         NI    FUNCTION,255-$NOMSG  TURN ON MESSAGE PROCESSING
         B     CMSTAT             AND SHOW NEW STATUS
         SPACE 1
CMSTOP   CLI   WORD3,C'C'         Q. TURN OFF COMMAND PROCESSING
         BNE   CMSTOP1            A. NO - TRY FOR MESSAGES
         OI    FUNCTION,$NOCMD    TURN OFF COMMAND PROCESSING
         B     CMSTAT             AND SHOW NEW STATUS
         SPACE 1
CMSTOP1  CLI   WORD3,C'M'         Q. TURN OFF MESSAGE PROCESSING
         BNE   CMHUH              A. INVALID FUNCTION
         OI    FUNCTION,$NOMSG    TURN OFF MESSAGE PROCESSING
         B     CMSTAT             AND SHOW NEW STATUS
         SPACE 1
CMHUH    WTO   'RMSG - COMMAND SYNTAX ERROR OR UNKNOWN COMMAND'
         WTO   'RMSG - ENTER + HELP FOR PROPER COMMAND SYNTAX '
         B     CMDONE             COMMAND EXIT
         SPACE 1
CMABORT  WTO   'RMSG - AND AWAY I GO.'
         L     R8,20(R13)         GET POINTER TO MY SSCVT
         USING SSCT,R8
         CLC   SSCTSNAM,=C'RMSG'  Q. IS THIS ME
         BE    CMBYE              A. YES - THEN GO AWAY
         WTO   'RMSG - LOGIC ERROR. ALL FUNCTIONS NOW INACTIVE'
         MVI   FUNCTION,$ALLOFF   TURNOFF EVERYTHING
         B     QUICKXIT           AND LEAVE
         SPACE 1
CMBYE    MVC   SSCTSSVT,=F'0'     CLEAR MY SSVT POINTER
         B     CMDONE             JUST LEAVE
         SPACE 1
CMSTAT   TM    FUNCTION,$NOMSG    Q. IS MESSAGES PROCESSING ACTIVE
         BO    CMSTAT1            A. NO
         WTO   'RMSG - MESSAGE PROCESSING IS ACTIVE'
         B     CMSTAT2
         SPACE 1
CMSTAT1  WTO   'RMSG - MESSAGE PROCESSING IS NOT ACTIVE'
         SPACE 1
CMSTAT2  TM    FUNCTION,$NOCMD     Q. IS COMMAND PROCESSING ACTIVE
         BO    CMSTAT3             A. NO
         WTO   'RMSG - COMMAND PROCESSING IS ACTIVE'
         B     CMSTAT4
         SPACE 1
CMSTAT3  WTO   'RMSG - COMMAND PROCESSING IS NOT ACTIVE'
CMSTAT4  B     CMDONE
         SPACE 1
CMHELP   WTO   'RMSG - THE VALID RMSG COMMANDS ARE:'
         WTO   '     + START MESSAGES  - TURN ON MESSAGE PROCESSING'
         WTO   '     + STOP MESSAGES   - TURN OFF MESSAGE PROCESSING'
         WTO   '     + START COMMAND   - TURN ON COMMAND PROCESSING'
         WTO   '     + STOP COMMAND    - TURN OFF COMMAND PROCESSING'
         WTO   '     + ABORT           - REMOVE RMSG FROM SYSTEM'
         B     CMDONE
         SPACE 1
***********************************************************************
*            COMMAND PROCESSOR                                        *
TRYCMDS  TM    FUNCTION,$NOCMD      Q. IS COMMAND PROCESSING ACTIVE
         BO    QUICKXIT             A. NO - LEAVE
         SPACE 1
         CLC   =C'QUIT',0(R12)      Q. IS THIS MY COMMAND
         BNE   QUICKXIT             A. NO - LEAVE
         SPACE 1
         BAL   R14,PARSE            PARSE THE COMMAND
         CLC   =C'MVS',WORD2        Q. STOPPING MVS
         BNE   QUICKXIT             A. NO - LEAVE
         SPACE 1
         LA    R2,QUITMVS           POINT TO COMMAND LIST
         MVI   QUITSW,C'Y'          SHOW QUIT MVS CMD ISSUED
QUITLOOP CLI   0(R2),X'FE'          Q. END OF LIST
         BE    CMDONE               A. YES - LEAVE
         L     R1,0(R2)             POINT TO THE COMMAND
         SR    R0,R0                CLEAR R0
         SVC   34                   ISSUE THE COMMAND
         LA    R2,4(R2)             GET NEXT COMMAND POINTER
         B     QUITLOOP             AND LOOP
         SPACE 1
***********************************************************************
*              COMMON PARSE ROUTINE                                   *
PARSE    MVC   PLIST,PLIST-1       CLEAR THE PLIST
         LA    R10,WORD1            POINT TO 1ST WORD
         BAL   R15,PARSEWD          PARSE THIS WORD
         OC    WORD1,BLANKS         MAKE IT UPPER CASE
         SPACE 1
         LA    R10,WORD2            POINT TO 2ND WORD
         BAL   R15,PARSEBLK         FIND NEXT NON-BLANK
         OC    WORD2,BLANKS         MAKE IT UPPER CASE
         SPACE 1
         LA    R10,WORD3            POINT TO 3RD WORD
         BAL   R15,PARSEBLK         FIND NEXT NON-BLANK
         OC    WORD3,BLANKS         MAKE IT UPPER CASE
         SPACE 1
         LA    R10,WORD4            POINT TO 4TH WORD
         BAL   R15,PARSEBLK         FIND NEXT NON-BLANK
         OC    WORD4,BLANKS         MAKE IT UPPER CASE
         SPACE 1
         LA    R10,WORD5            POINT TO 5TH WORD
         BAL   R15,PARSEBLK         FIND NEXT NON-BLANK
         OC    WORD5,BLANKS         MAKE IT UPPER CASE
         SPACE 1
         LA    R10,WORD6            POINT TO 6TH WORD
         BAL   R15,PARSEBLK         FIND NEXT NON-BLANK
         OC    WORD6,BLANKS         MAKE IT UPPER CASE
         SPACE 1
         LA    R10,WORD7            POINT TO 7TH WORD
         BAL   R15,PARSEBLK         FIND NEXT NON-BLANK
         OC    WORD7,BLANKS         MAKE IT UPPER CASE
         SPACE 1
         LA    R10,WORD8            POINT TO 8TH WORD
         BAL   R15,PARSEBLK         FIND NEXT NON-BLANK
         OC    WORD8,BLANKS         MAKE IT UPPER CASE
         SPACE 1
PARSEX   BR    R14                 OUT OF DATA OR TO MANY WORDS
         SPACE 1
* - - SCAN FOR NEXT NON-BLANK
         SPACE 1
PARSEBLK CLI   0(R12),C' '         Q. IS THIS POSITION BLANK
         BNE   PARSEWD             A. NO - DO NORMAL PARSE
         BCT   R11,PARSEBL1        CONTINUE IF MORE DATA
         SPACE 1
         B     PARSEX              OUT IF DATA
PARSEBL1 LA    R12,1(R12)          BUMP TO NEXT WORD
         B     PARSEBLK            AND LOOP
         SPACE 1
* - - MOVE A WORD
PARSEWD  MVC   0(1,R10),0(R12)     MOVE 1 LETTER
         LA    R10,1(R10)          MAKE ROOM FOR NEXT LETTER
         BCT   R11,PARSEWD1        CONTINUE IF MORE
         B     PARSEX              OUT OF DATA
         SPACE 1
PARSEWD1 LA    R12,1(R12)          BUMP TO NEXT LETTER
         CLI   0(R12),C' '         Q. IS THERE A NEXT LETTER
         BNE   PARSEWD             A. YES - MOVE IT
         BR    R15                 RETURN TO PARSE ROUTINE
         SPACE 1
***********************************************************************
*                EXIT ROUTINES                                        *
CMDONE   LA    R15,SSCMSUBC        I HAVE PROCESSED THIS COMMAND
         ST    R15,SSOBRETN        TELL OTHER USERS
         B     QUICKXIT            RETURN
         SPACE 1
QUICKXIT NOP   0                   BETTER THAN AN EQU
         SPACE 1
EXIT0    SR    R15,R15             SET GOOD RETURN CODE
EXIT     RETURN (14,12),RC=(15)
         SPACE 1
***********************************************************************
*                MISC CONSTANTS AND OTHER STUFF                       *
CM       EQU   10                  OPERATOR COMMAND FLAG
WT       EQU   9                   WTO WTOR FLAG
OK       DC    F'0'                SHOW WE PROCESSED THE COMMAND
ZERO     DC    C'0000'             WORK FIELD FOR ICH408I        PLB
         SPACE 1
* - - SUBSYSTEM FUNCTION STATUS INDICATORS
FUNCTION DC    X'00'               FUNCTION SWITCH (ALL ENABLED)
$INIT    EQU   X'80'                INITIALIZATION COMPLETE
$NOMSG   EQU   X'40'                NO MESSAGE PROCESSING
$NOCMD   EQU   X'20'                NO COMMAND PROCESSING
$ALLOFF  EQU   X'FF'                ALL FUNCTIONS DISABLED
QUITSW   DC    C'N'                 N = QUIT MVS NOT ISSUED
*                                   Y = QUIT MVS ISSUED
         SPACE 1
* - - SUBSYSTEM KEY WORDS.  MUST BE SAME LENGTH AS WORDS IN PLIST
ABORT    DC    CL8'ABORT'
STOP     DC    CL8'STOP'
START    DC    CL8'START'
HELP     DC    CL8'HELP'
STATUS   DC    CL8'STATUS'
BLANKS   DC    CL16' '             JUST SOME CONVIENT BLANKS
         DC    C' '                USED TO CLEAR PLIST
SID      DC    CL4' '              SAVE AREA FOR CURRENT SID
PLIST    DS    0CL40               ROOM FOR 8 WORDS
WORD1    DC    CL8' '
WORD2    DC    CL8' '
WORD3    DC    CL8' '
WORD4    DC    CL8' '
WORD5    DC    CL8' '
WORD6    DC    CL8' '
WORD7    DC    CL8' '
WORD8    DC    CL8' '
         SPACE 1
HALFWRD  DC    CL5' '              DISPLAY HALF WORD
FULLWRD  DC    CL9' '              DISPLAY FULL WORD
HEXTBL   ORG   *-256
         ORG   HEXTBL+X'F0'
         DC    C'0123456789ABCDEF'
* - - - RESPONSES TO WTO AND WTOR COMMANDS
         SPACE 1
RMSGPARM DS    0A                  HOLD AREA FOR PARM POINTERS
RMSGR0   DC    A(0)                 R0
RMSGR1   DC    A(0)                 R1
         PRINT NOGEN
POSTMSG  WTO   'R XX,POST',MF=L
FSTOPMSG WTO   'R XX,FSTOP',MF=L
NOHOLD   WTO   'R XX,NOHOLD',MF=L                               PLB
UMSG     WTO   'R XX,U',MF=L
YMSG     WTO   'R XX,Y',MF=L                                       BJM
STSOCMD  WTO   'S TSO',MF=L
SNCCFCMD WTO   'S NCCF',MF=L
SCLSESS  WTO   'S CLSESS',MF=L
SCLMENU  WTO   'S CLMENU',MF=L
PAG17990 WTO   'PA NONVIO=PAGE.VSSD000.LOCAL',MF=L
PAG27990 WTO   'PA NONVIO=PAGE.VSSD001.LOCAL',MF=L
PAG37990 WTO   'PA NONVIO=PAGE.VSSD002.LOCAL',MF=L
PAG47990 WTO   'PA NONVIO=PAGE.VSSD003.LOCAL',MF=L
SUTLCMDN WTO   'S JCLSUB,MEM=EPIUTILN',MF=L
SUTLCMDV WTO   'S JCLSUB,MEM=EPIUTILV',MF=L
SEPICMDN WTO   'S EPILOGN',MF=L
SEPICMDV WTO   'S EPILOGV',MF=L
PEPICMDN WTO   'P EPILOGN',MF=L
PEPICMDV WTO   'P EPILOGV',MF=L
PAGEMOVE WTO   'S PAGEMOVE',MF=L
SYSLOGN  WTO   'S SYSLOGV.SYSLOGN',MF=L  SAR SYSLOG ID
SYSLOGV  WTO   'S SYSLOGV.SYSLOGV',MF=L
SYSLOGM  WTO   'S SYSLOGV.SYSLOGM',MF=L
SYSLOGX  WTO   'S SYSLOGV.SYSLOGX',MF=L
         PRINT GEN
RCMDWTO  WTO   '                            ',MF=L
RCMDCMD  EQU   RCMDWTO+4,30
         PRINT GEN
SMFDUMP  WTO   'S SMFMVS,N=X',MF=L
SMFID    EQU   SMFDUMP+15,1
         PRINT NOGEN
PJES2    WTO   '$PJES2',MF=L
ZEOD     WTO   'Z EOD',MF=L
         SPACE 1
* - - - RESPONSES TO QUIT MVS COMMAND
QMVS1    WTO   'S RMSGCMD,MEM=QUITMVS',MF=L  SUBMIT CMDS FROM PARMLIB
QMVS2    WTO   '$PI',MF=L                DRAIN ALL INITS
QUITMVS  DC    A(QMVS1)
         DC    A(QMVS2)
         DC    X'FEFE'                   END OF THE LIST
         PRINT GEN
         SPACE 1
* - - - VIRTUAL PRINTER NAMES AND COMMAND TO START THEM
$SPRT    WTO   '$SXXXXXXXX ',MF=L
$SPRTID  EQU   $SPRT+6,8
VPRINT   DC    CL8'PRT1   '
         DC    CL8'PRT2   '
         DC    CL8'PRT3   '
         DC    CL8'PRT4   '
         DC    CL8'PRT5   '
         DC    CL8'PRT6   '
         DC    CL8'PRT7   '
         DC    8X'FE'             END OF VIRTUAL PRINTER NAME TABLE
         SPACE 1
*--- TABLE OF COMMANDS TO BE ISSUED FOR EPILOG
EPMVS1   DC    A(PEPICMDN)        POINT TO STOP EPILOGN COMMAND    PLB
         DC    A(SUTLCMDN)        POINT TO START EPIUTILN COMMAND  PLB
         DC    X'FEFEFEFE'        END OF TABLE
         SPACE 1
EPMVS2   DC    A(PEPICMDV)        POINT TO STOP EPILOGV COMMAND    PLB
         DC    A(SUTLCMDV)        POINT TO START EPIUTILV COMMAND  PLB
         DC    X'FEFEFEFE'        END OF TABLE
         SPACE 1
*--- TABLE OF COMMANDS TO BE ISSUED WHEN VTAM ON MVS1 IS STARTED
VTAMMVS1 DC    A(PAG17990)        POINT TO PAGEADD OF FIRST 7990   PLB
         DC    A(PAG27990)        POINT TO PAGEADD OF  2ND  7990   PLB
         DC    A(PAG37990)        POINT TO PAGEADD OF THIRD 7990   PLB
         DC    A(PAG47990)        POINT TO PAGEADD OF  4TH  7990   PLB
         DC    A(STSOCMD)         POINT TO START TSO COMMAND       PLB
         DC    A(SNCCFCMD)        POINT TO START NCCF COMMAND      PLB
         DC    A(SCLMENU)         POINT TO START CLMENU COMMAND    PLB
         DC    A(SCLSESS)         POINT TO START CLSESS COMMAND    PLB
         DC    A(PAGEMOVE)        POINT TO START PAGEMOVE CMD      PLB
         DC    X'FEFEFEFE'        END OF TABLE                     PLB
         SPACE 1
*--- TABLE OF COMMANDS TO BE ISSUED WHEN VTAM ON MVSXA TEST STARTS
VTAMMXAT DC    A(STSOCMD)         POINT TO START TSO COMMAND       PLB
*        DC    A(SNCCFCMD)        POINT TO START NCCF COMMAND      PLB
*        DC    A(SCLMENU)         POINT TO START CLMENU COMMAND    PLB
*        DC    A(SCLSESS)         POINT TO START CLSESS COMMAND    PLB
         DC    X'FEFEFEFE'        END OF TABLE                     PLB
         SPACE 1
*--- TABLE OF COMMANDS TO BE ISSUED WHEN VTAM ON MVS2 IS STARTED
VTAMMVS2 DS    0F                 TABLE IS EMPTY FOR NOW           PLB
         DC    A(STSOCMD)         POINT TO START TSO COMMAND       PLB
         DC    X'FEFEFEFE'        END OF TABLE                     PLB
         SPACE 1
***********************************************************************
*                THE LITERAL POOL                                     *
         LTORG
         SPACE 1
***********************************************************************
*                DSECTS FOR THE WHOLE WORLD                           *
         PRINT NOGEN
CBF      DSECT               COMMAND BUFFER
CBFCNT   DS    H              LENGTH OF TEXT
         DS    H              RESERVED (?)
CBFTXT   DS    CL140          COMMAND TEXT (AS ENTERED)
         SPACE 1
         CVT   DSECT=YES
         IHAPSA
         IEESMCA
         IEFJSCVT
         IHAORE
         IHAWQE
         IEFJSSOB (WT,CM)
         END
//LKED      EXEC  PGM=IEWL,PARM='XREF,LIST,LET'
//SYSPRINT  DD SYSOUT=*
//SYSLIN    DD DDNAME=SYSIN
//OBJLIB    DD DSN=BANKS.SOURCE.OBJ,DISP=SHR
//SYSLIB    DD DSN=BANKS.SOURCE.LOAD,DISP=SHR
//SYSLMOD   DD DSN=SYS6.LINKLIB,DISP=SHR
//*YSLMOD   DD DSN=SYS6.PRODXA.LINKLIB,DISP=SHR
//SYSUT1    DD SPACE=(1024,(50,20)),UNIT=SYSDA
//SYSIN DD *
 INCLUDE OBJLIB(RMSG)
 NAME RMSG(R)
