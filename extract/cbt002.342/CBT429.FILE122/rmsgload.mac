//RTPLBLD1 JOB (05DPTECH,TECH),'P BANKS',CLASS=A,MSGCLASS=R,
// USER=UUUUUUUU,PASSWORD=PPPPPPPP
/*JOBPARM L=9999
//RMSGLOAD    EXEC PGM=IFOX00,PARM='DECK,NOLOAD,TEST'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,DCB=BLKSIZE=12960
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS2.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSUT2   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSUT3   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSPUNCH DD  DSN=BANKS.SOURCE.OBJ(RMSGLOAD),DISP=OLD
//SYSPRINT DD  SYSOUT=*
//SYSIN DD *
         TITLE 'LOAD AND BUILD CONTROL BLOCKS FOR RMSG'
***********************************************************************
*                                                                     *
*  THIS IS THE PROGRAM TO LOAD AND ACTIVATE THE RMSG SUB-SYSTEM.      *
*  THIS PROGRAM AND THE SUB-SYSTEM MUST BE AUTHORIZED.                *
*                                                                     *
*  3 PARAMETERS ARE PERMITTED.                                        *
*    FORCE - FORCE A NEW COPY OF RMSG INTO THE SYSTEM.                *
*    RUN   - THIS IS THE SAME AS NO PARM INFORMATION.                 *
*                                                                     *
*  IF THE PARAMETER INFORMATION IS NOT SPECIFIED THEN IT IS ASSUMED   *
*   THAT A COPY OF RMSG IS NOT ACTIVE.  IF IT IS, YOU WILL GET        *
*   A MESSAGE AND A CLOUD OF PAPER.                                   *
*                                                                     *
*  LOGIC FLOW.                                                        *
*                                                                     *
*  1) CHECK THE PARM INFORMATION.                                     *
*                                                                     *
*  2) LOCATE THE RMSG SSCT.                                           *
*                                                                     *
*  3) LOAD THE RMSG SUB-SYSTEM                                        *
*                                                                     *
*  4) CALL THE SUB-SYSTEM TO INITALIZE ITS SELF                       *
*                                                                     *
*  5) LOAD THE SRB ROUTINE FOR J2XIT01                                *
*                                                                     *
*  6) CALL THE SRB ROUTINE TO INITALIZE ITS SELF                      *
*                                                                     *
*  7) BUMP THE UCMBRDCS COUNT BY ONE SO WE CAN SEE WTOR MESSAGES      *
*                                                                     *
*  8) SUBMIT START COMMANDS VIA THE INTRDR                            *
*                                                                     *
*  9) LEAVE - THE SUB-SYSTEM IS ACTIVE                                *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                PROGRAM INITIALIZATION                               *
         SPACE 1                                                      *
         PRINT NOGEN
RMSGLOAD COMEIN
         SPACE 1
         L     R1,0(R1)           GET POINTER TO PARM AREA
         CLC   0(2,R1),=H'0'      Q. ANY PARMS
         BE    PARMOK             A. NO
         SPACE 1
PARM1    CLC   2(3,R1),=C'RUN'    Q. IS THIS THE 'RUN' PARM
         BE    PARMOK             A. YES - JUST LIKE NO PARM
         SPACE 1
PARM2    CLC   2(5,R1),=C'FORCE'  Q. IS THIS THE 'FORCE' PARM
         BNE   EXIT16             A. NO - QUIT
         OI    PARMSW,FORCE       TURN ON FORCE SWITCH
         B     PARMOK             ONLY PARM ALLOWED
         PRINT GEN
         SPACE 1
***********************************************************************
*        SEARCH FOR MY SUB-SYSTEM CONTROL TABLE (SSCT)                *
         SPACE 1
PARMOK   TESTAUTH ,
         LTR   R15,R15            Q. AM I AUTHORIZED
         BNZ   EXIT20             A. NO - LEAVE
         SPACE 1
         L     R12,16             START AT THE CVT. WHERE ELSE?
         USING CVT,R12
         SPACE 1
         L     R11,CVTJESCT       ADDR OF 1ST SUB-SYS CONTROL TABLE
         USING JESCT,R11
         SPACE 1
         CLC   JESCTID,=C'JEST'   Q. IS THIS REALLY THE JESCT
         BNE   EXIT4              A. NO - RETURN IN ERROR
         SPACE 1
         L     R10,JESSSCT        POINT TO SUBSYS COMMUNICATION TBL
         USING SSCT,R10
         SPACE 1
CKSSCT   CLC   SSCTID,=C'SSCT'    Q. IS THIS STILL THE SSCT
         BNE   EXIT8              A. NO - RETURN IN ERROR
         SPACE 1
         CLC   SSCTSNAM,=C'RMSG'  Q. IS THIS ME
         BE    ITSME              A. YES - BUILD MY SSVT
         SPACE 1
         L     R10,SSCTSCTA       POINT TO NEXT SSCT
         LTR   R10,R10            Q. MORE TO GO
         BNZ   CKSSCT             A. YES - THEN DO IT
         B     EXIT8              RETURN IN ERROR
         SPACE 1
         DROP  R12                DONE WITH THE CVT
         DROP  R11                DONE WITH THE JESCT
         SPACE 1
***********************************************************************
*                 GET SIZE OF RMSG AND SRB ROUTINE                    *
ITSME    LOAD  EP=RMSG            LOAD TO GET MODULE SIZE
         SLL   R1,3               MULT BY 8 FOR SIZE IN BYTES
         ST    R1,RMSGSIZE        SAVE THE SIZE
         MVC   RMSGSIZE(1),=AL1(245)  SET SUB-POOL ID
         DELETE EP=RMSG           NOW DELETE IT
         SPACE 1
         LOAD  EP=J2SRB01         LOAD TO GET MODULE SIZE
         SLL   R1,3               MULT BY 8 FOR SIZE IN BYTES
         ST    R1,SRBSIZE         SAVE SRB SIZE
         MVC   SRBSIZE(1),=AL1(245)  SET SUB-POOL ID
         DELETE EP=J2SRB01        NOW DELETE IT
         SPACE 1
***********************************************************************
*                 BUILD MY SUB-SYSTEM VECTOR TABLE (SSVT)             *
         MODESET KEY=ZERO,MODE=SUP   GOTTA BE HEAVY FOR AWHILE
         L     R9,SSCTSSVT         POINT TO RMSG SSVT
         USING SSVT,R9
         SPACE 1
         LTR   R9,R9              Q. DO I HAVE A SSVT
         BZ    ITSMEOK            A. NO - I AM NOT ACTIVE
         SPACE 1
         TM    PARMSW,FORCE       Q. AM I GOING TO FORCE A NEW SUBSYS
         BNO   EXIT12             A. NO - EXIT IN ERROR
         SPACE 1
         XC    SSCTSSVT,SSCTSSVT  DEACTIVATE RMSG
         SPACE 1
ITSMEOK  OPEN  (LOADLIB,(INPUT))
         L     R0,RMSGSIZE        GET SIZE AND SUB-POOL
         GETMAIN R,LV=(0)
         ST    R1,RMSGREAL        SAVE LOAD ADDRESS
         LTR   R15,R15            Q. WAS GETMAIN SUCSESSFUL
         BNZ   EXIT24             A. NO
         SPACE 1
LOADRMSG BLDL  LOADLIB,RMSGBLD
         LTR   R15,R15            Q. WAS BLDL SUCESSFUL
         BNZ   EXIT28             A. NO
         SPACE 1
LOADROK  L     R2,RMSGREAL
         LOAD  EP=RMSG,ADDR=(2),DCB=LOADLIB
         ST    R0,RMSGREAL        TRACKS
         SPACE 1
         LA    R1,SSCTSSVT        WHERE TO PUT ADDR OF NEW SSVT
         L     R15,RMSGREAL       PREPARE TO CALL RMSG
         BALR  R14,R15            RMSG INITALIZATION
         WTO   'RMSGLOAD - RMSG SUB-SYSTEM CONNECTED'
         SPACE 1
         L     R0,SRBSIZE        GET SIZE AND SUB-POOL
         GETMAIN R,LV=(0)
         ST    R1,SRBREAL         SAVE ENTRY POINT
         LTR   R15,R15            Q. WAS GETMAIN SUCSESSFUL
         BNZ   EXIT32             A. NO
         SPACE 1
LOADSRB  BLDL  LOADLIB,SRBBLD
         LTR   R15,R15            Q. WAS BLDL SUCESSFUL
         BNZ   EXIT36             A. NO
         SPACE 1
LOADSOK  L     R2,SRBREAL
         LOAD EP=J2SRB01,ADDR=(2),DCB=LOADLIB
         ST   R0,SRBREAL
         SPACE 1
         L     R1,SSCTSSVT      ADDR OF SSVT FOR RMSG
         LA    R1,4(R1)         THE FIRST 2 HALF WORDS IN THE SSVT
         LA    R1,256(R1)       THE MATRIX TABLE IN THE SSVT
         LA    R1,256*4(R1)     THE FUNCTION POINTER WORDS IN THE SSVT
         L     R15,SRBREAL        PREPARE TO CALL THE SRB ROUTINE
         BALR  R14,R15            INITALIZE THE SRB ROUTINE
         WTO   'RMSGLOAD - J2SRB01 CONNECTED'
         SPACE 1
         L     R12,16             GET THE CVT POINTER AGAIN
         USING CVT,R12
         SPACE 1
         L     R11,CVTCUCB        POINT TO THE UCM
         USING UCM,R11
         SPACE 1
         LH    R2,UCMBRDST        GET BROADCAST COUNT
         LA    R2,1(R2)           BUMP IT SO I CAN SEE WTOR MSGS
         STH   R2,UCMBRDST        AND PUT IT BACK
         SPACE 1
         DROP  R12                DROP THE CVT (IT WON'T BREAK)
         DROP  R11                DROP THE UCM
         MODESET KEY=NZERO,MODE=PROB LIGHTEN UP
         B     SUBMIT
         SPACE 1
***********************************************************************
*                   SUBMIT STARTED TASKS ETC                          *
SUBMIT   TM    PARMSW,FORCE             Q. IS THIS A REPLACEMENT
         BO    EXIT0                    A. YES - JUST EXIT
         SPACE 1
         OPEN  (INTRDR,(OUTPUT))
         OPEN  (CMDFILE,(INPUT))
NEXTCMD  GET   CMDFILE,INAREA
         CLI   INAREA,C'*'              Q. COMMENT
         BE    NEXTCMD                  A. YES
         PUT   INTRDR,INAREA
         B     NEXTCMD
         SPACE 1
LASTCMD  CLOSE CMDFILE
         CLOSE INTRDR
*        CLOSE LOADLIB
         B     EXIT0
         SPACE 1
***********************************************************************
*                   EXIT AND ABEND ROUTINES                           *
         SPACE 1
EXIT0    LA    R15,0              RETURN IN STYLE
         GETOUT
         SPACE 1
EXIT4    WTO   'RMSGLOAD - MISSED JESCT'
         ABEND 4,DUMP             BAIL OUT IN A CLOUD OF PAPER
         SPACE 1
EXIT8    WTO   'RMSGLOAD - ABNORMAL END OF SSCT'
         ABEND 8,DUMP             BAIL OUT IN A CLOUD OF PAPER
         SPACE 1
EXIT12   WTO   'RMSGLOAD - RMSG IS ALREADY ACTIVE'
         ABEND 18,DUMP            BAIL OUT IN A CLOUD OF PAPER
         SPACE 1
EXIT16   WTO   'RMSGLOAD - INVALID PARAMETER'
         ABEND 16,DUMP            BAIL OUT IN A CLOUD OF PAPER
         SPACE 1
EXIT20   WTO   'RMSGLOAD - NOT AUTHORIZED'
         ABEND 20                 BAIL OUT WITHOUT A CLOUD OF PAPER
         SPACE 1
EXIT24   WTO   'RMSGLOAD - GETMAIN FOR RMSG FAILED'
         ABEND 24,DUMP
         SPACE 1
EXIT28   WTO   'RMSGLOAD - BLDL FOR RMSG FAILED'
         ABEND 28,DUMP
         SPACE 1
EXIT32   WTO   'RMSGLOAD - GETMAIN FOR J2SRB01 FAILED'
         ABEND 24,DUMP
         SPACE 1
EXIT36   WTO   'RMSGLOAD - BLDL FOR J2SRB01 FAILED'
         ABEND 36,DUMP
         SPACE 1
***********************************************************************
*                MISC CONSTANTS AND OTHER STUFF                       *
TEMPSAVE DC    5F'0'              SAVE REGS 14 - 2
ANSWER   DC    C' '
WTOECB   DC    F'0'
RMSGREAL DC    A(0)               ADDR OF RMSG IN COMMON
SRBREAL  DC    A(0)               ADDR OF J2SRB01 IN COMMON
RMSGSIZE DC    F'0'               SIZE OF RMSG LOAD MODULE
SRBSIZE  DC    F'0'               SIZE OF SRB LOAD MODULE
         SPACE 1
PARMSW   DC    X'00'              PARAMETER SWITCH
FORCE    EQU   X'80'               FORCE NEW SUBSYSTEM
         SPACE 1
INAREA   DC    CL80' '            COMMAND INPUT AND OUTPUT AREA
         DS    0F                  FORCE ALIGNMENT
RMSGBLD  DS   0CL80' '            BLDL LIST FOR RMSG
         DC    H'1'                NUMBER OF ENTRIES
         DC    H'80'               LENGTH OF 1 ENTRY
         DC    CL8'RMSG'           NAME OF MODULE
         DC    CL68' '             FILLED IN BY BLDL
SRBBLD   DS   0CL80' '            BLDL LIST FOR J2SRB01
         DC    H'1'                NUMBER OF ENTRIES
         DC    H'80'               LENGTH OF 1 ENTRY
         DC    CL8'J2SRB01'        NAME OF MODULE
         DC    CL68' '             FILLED IN BY BLDL
         SPACE 1
***********************************************************************
*                THE LITERAL POOL                                     *
         LTORG
         SPACE 1
***********************************************************************
*                DCB'S FOR INTRDR AND CMDFILE                         *
INTRDR   DCB   DDNAME=INTRDR,DSORG=PS,MACRF=PM
CMDFILE  DCB   DDNAME=CMDFILE,DSORG=PS,MACRF=GM,EODAD=LASTCMD
LOADLIB  DCB   DDNAME=LOADLIB,DSORG=PO,MACRF=R
         SPACE 1
***********************************************************************
*                DSECTS FOR THE WHOLE WORLD                           *
         PRINT NOGEN
         CVT   DSECT=YES
         IEFJESCT
         IEFJSCVT
         IEFJSSVT
UCM      DSECT
         IEECUCM
         SPACE 1
         END
//LKED      EXEC  PGM=IEWL,PARM='XREF,LIST,LET,AC=1'
//SYSPRINT  DD SYSOUT=*
//SYSLIN    DD DDNAME=SYSIN
//OBJLIB    DD DSN=BANKS.SOURCE.OBJ(RMSGLOAD),DISP=SHR
//*YSLMOD   DD DSN=BANKS.SOURCE.LOAD,DISP=SHR
//SYSLMOD   DD DSN=SYS6.LINKLIB,DISP=SHR
//*            VOL=SER=MVSRS1,UNIT=3380
//SYSUT1    DD SPACE=(1024,(50,20)),UNIT=SYSDA
//SYSIN DD *
 INCLUDE OBJLIB
 NAME RMSGLOAD(R)
