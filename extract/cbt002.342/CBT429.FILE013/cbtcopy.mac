*          DATA SET CBT1708    AT LEVEL 001 AS OF 11/16/86
 TITLE '              COPY CBT FILE 1 TO A VB DATASET, MINIMIZING SIZE'
CBTCOPY  CSECT
*
*  RON TANSKY
*
*    THIS PROGRAM SQUEEZES THE CBT INDEX.  IT IS DESIGNED FOR THOSE
*  WHO WISH TO PUT IT INTO A DISK DATSET AND USE THE MINIMUM AMOUNT
*  OF SPACE.  IT GETS RID OF THE LEADING '//* ', IT CHOPS OFF THE
*  TRAILING '*' AND SEQUENCE NUMBER FIELDS, AND IT TURNS THE FILE
*  INTO A VB FORMAT.  THE SPACE DIFFERENCE IS SIGNIFICANT.
*
*  REGISTER USAGE
*
*        3     BASE
*        4     HOLDS A CONSTANT 5 (LENGTH FIELD + 1)
*        5     POINTER TO INPUT FILE
*        6     POINTER TO OUTPUT FILE
*        7     POINTER INTO RECORD BEING PROCESSED
*        8     COUNTER TO SHOW LENGTH OF RECORD
*
         USING *,3
         SAVE  (14,12)
         LR    R3,R15
         ST    R13,SAVE+4
         LA    R15,SAVE
         ST    R15,8(R13)
         LR    R13,R15
         LA    R5,IN
         LA    R6,OUT
         LA    R4,4
         OPEN  ((R5),(INPUT),(R6),(OUTPUT))
MAINLOOP GET   (R5)
         MVC   DATA,4(R1)      SKIP THE FIRST 4 BYTES OF INPUT
         LA    R8,ENDDATA-DATA
         LA    R7,ENDDATA
*
* SQUEEZE OUT TRAILING BLANKS
*
SQUEEZE  CLI   0(R7),C' '
         BNE   SQDONE
         BCTR  R7,0
         BCT   R8,SQUEEZE
SQDONE   AR    R8,R4            ADD 4 TO LENGTH FIELD
         STH   R8,OUTLEN
         CLC   DATA(6),=C'&&$%&&$%'  STOP AT ADDITIONS SECTION
         BE    EOF
         PUT   (R6),OUTREC
         B     MAINLOOP
         SPACE
EOF      CLOSE ((R5),,(R6))
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE
OUTREC   DS    0CL70
OUTLEN   DC    H'0'
         DC    H'0'
DATA     DS    CL66
ENDDATA  EQU   *-1
         SPACE
SAVE     DS    9D
         SPACE
IN       DCB   DSORG=PS,MACRF=GL,EODAD=EOF,DDNAME=SYSUT1
OUT      DCB   DSORG=PS,MACRF=PM,RECFM=VB,LRECL=80,DDNAME=SYSUT2
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
#PAN$AUD CSECT
$PAN#AUD DC    CL21'001CBT1708   11/16/86'
         END   CBTCOPY
