*----------------------------------------------------------------------
*        GLOBAL FACILITIES FOR SYSTEMS PROGRAMMERS USE
*----------------------------------------------------------------------
         PRINT NOGEN
PXP0010  #SLC R12,R11
*----------------------------------------------------------------------
*        DEFINE VARIABLES
*----------------------------------------------------------------------
         SPFVDEF 1,(ZCMD,SPLOADER,SPSOURCE,SPBLDL,SPTABTYP,SPPDSC,     +
               SPSECURE)
         SPFVDEF 2,(SPRELEAS)
         SPFVDEF 5,(SPENTRES)
         SPFVDEF 8,(TARGET1,TARGET2,TARGET3,EXIT1,EXIT2,EXIT3)
         SPFVDEF 56,(SPPARM)
         SPFVDEF 71,(SPJOBC1,SPJOBC2,SPJOBC3)
         SPFVCOPY (MSG,8),DC=NO
         SPFVCOPY (ZUSER,8,ZTEMPF,44)
         SPFVGET (SPJOBC1,SPJOBC2,SPJOBC3),PROFILE
         LA    R1,SPPARM+48   CALCULATE ENDPARM ADDR TO PREVENT OVFLOW
         ST    R1,ENDPARM
*        DISPLAY SYSTEMS PROGRAMMER PANEL
ENTRY010 SPFSPLAY 'PXP0010',MSG,,,EOJ
         MVC   SPPARM,BLANKS    CLEAR
         MVC   MSG,=CL8'PXP000'   CLEAR
         OI    ZCMD,C' '        UPPER CASE
         CLI   ZCMD,C'A'        ACTIVATE ALL?
         BNE   AAA010           NO
         MVC   SPPARM(5),ACTALL    YES
         B     FTSKEL
AAA010   CLI   ZCMD,C'D'        DEACTIV?
         BNE   BBB010           NO
         MVC   SPPARM(5),DEACTALL  YES
         B     FTSKEL
BBB010   CLI   ZCMD,C'R'        REPORT?
         BNE   ENTRY100         NO
         MVC   SPPARM(5),REPRTALL  YES
         B     FTSKEL
ENTRY100 EQU   *
         LA    R5,SPPARM        USE R5 AS BASE FOR PARM
         SR    R1,R1               CLEAR
         MVC   SECEXITD,=CL2';Y'      RESET
         CLI   SPLOADER,C' '    PANEXEC LOADER?
         BE    ENTRY110         NO
         MVC   0(2,R5),=C'1;'  YES - CODE FOR PANEXEC LOADER
         MVC   2(1,R5),SPLOADER A/D (ACTIVATE/DEACTIVATE)
         LA    R5,3(R5)
         CLI   SPLOADER,C'A'    ACTIVATE?
         BNE   EXIT100          NO
         MVI   0(R5),C';'
         MVC   1(1,R5),SPSOURCE YES- SOURCE
         MVI   2(R5),C';'
         LA    R5,3(R5)
         MVC   0(2,R5),SPRELEAS      RELEASE
         TRT   0(3,R5),BLNKTAB  POINT BEYOND RELEASE FIELD ENTERED
         LR    R5,R1
EXIT100  MVC   0(3,R5),=C';X;'  EXECUTE
         LA    R5,3(R5)
ENTRY110 EQU   *
         CLI   SPBLDL,C' '    DYNAMIC BLDL?
         BE    ENTRY120         NO
         MVC   0(2,R5),=C'2;'  YES - CODE FOR DYNAMIC BLDL
         MVC   2(1,R5),SPBLDL   A/F/D (ACTIVATE/REFRESH/DEACTIVATE)
         LA    R5,3(R5)
         CLI   SPBLDL,C'A'    ACTIVATE?
         BNE   EXIT110          NO
         MVI   0(R5),C';'       YES
         MVC   1(1,R5),SPTABTYP  BLDL TABLE TYPE
         LA    R5,2(R5)
         CLI   SPTABTYP,C'D'         DYNAMIC
         BE    AAA110                YES
         CLI   SPTABTYP,C'P'         NO - PANEOPT DEFAULT
         BNE   EXIT110                    NO
AAA110   MVI   0(R5),C';'                 YES
         LA    R5,1(R5)
         MVC   0(5,R5),SPENTRES     NUMBER OF ENTRIES FOR DYNAMIC
         TRT   0(6,R5),BLNKTAB    GET LENGTH OF RELEASE FIELD ENTERED
         CR    R1,R5
         BE    ZZZ110             0 LENGTH ENTERED
         LR    R5,R1
         B     EXIT110
ZZZ110   BCTR  R5,0               OVERLAY LAST ;
EXIT110  MVC   0(3,R5),=C';X;'  EXECUTE
         LA    R5,3(R5)
ENTRY120 EQU   *
         CLI   SPPDSC,C' '      PDS COMPAT?
         BE    ENTRY130         NO
         MVC   0(2,R5),=C'3;'  YES - CODE FOR PDS COMPAT
         MVC   2(1,R5),SPPDSC   A/D (ACTIVATE/DEACTIVATE)
         MVI   3(R5),C';'
         LA    R5,4(R5)
         CLC   TARGET1(2),=C'A '   ALL TARGETS ENTERED
         BNE   AAA120           NO
         MVI   0(R5),C'A'       YES
         LA    R5,1(R5)
         B     EXIT120
AAA120   MVC   0(8,R5),TARGET1  TARGET1
         TRT   0(9,R5),BLNKTAB  POINT BEYOND TARGET FIELD ENTERED
         LR    R5,R1
         MVI   0(R5),C';'
         LA    R5,1(R5)
         CLI   TARGET2,C' '     TARGET2 ENTERED?
         BE    ZZZ120           NO
         MVC   0(8,R5),TARGET2
         TRT   0(9,R5),BLNKTAB    POINT BEYOND TARGET FIELD ENTERED
         LR    R5,R1
         MVI   0(R5),C';'
         LA    R5,1(R5)
         CLI   TARGET3,C' '     TARGET3 ENTERED?
         BE    ZZZ120           NO
         MVC   0(8,R5),TARGET3
         TRT   0(9,R5),BLNKTAB    POINT BEYOND TARGET FIELD ENTERED
         LR    R5,R1
         MVI   0(R5),C';'
         LA    R5,1(R5)
ZZZ120   MVI   0(R5),C'E'
         LA    R5,1(R5)
EXIT120  MVC   0(3,R5),=C';X;'  EXECUTE
         LA    R5,3(R5)
ENTRY130 EQU   *
         CLI   SPSECURE,C' '    SECURITY EXITS
         BE    ENTRY140         NO
         MVC   0(2,R5),=C'4;'  YES - CODE FOR SECURITY EXITS
         MVC   2(1,R5),SPSECURE A/D (ACTIVATE/DEACTIVATE)
         MVI   3(R5),C';'
         LA    R5,4(R5)
         C     R5,ENDPARM       BEYOND THE ALLOWABLE PARM?
         BH    TOOLONG          YES
         LA    R4,2             SET R4 FOR LENGTH OF ;Y RESPONSE
         CLI   SPSECURE,C'A'    IS THIS ACTIVATION
         BNE   AAA130           NO
         MVC   SECEXITD,BLANKS  YES- NO NEED FOR DEACT VERIF
         LA    R4,0             SET R4=0: NO ;Y RESPONSE NEEDED
AAA130   CLC   EXIT1(2),=C'A '  ALL EXITS ENTERED
         BNE   BBB130           NO
         MVI   0(R5),C'A'       YES
         MVC   1(2,R5),SECEXITD
         LA    R5,1(R4,R5)
         C     R5,ENDPARM       BEYOND THE ALLOWABLE PARM?
         BH    TOOLONG          YES
         B     EXIT130
BBB130   MVC   0(8,R5),EXIT1    NO - EXIT1
         TRT   0(9,R5),BLNKTAB    POINT BEYOND EXIT NAME ENTERED
         LR    R5,R1
         MVI   0(R5),C';'
         LA    R5,1(R5)
         CLI   EXIT2,C' '       EXIT2 ENTERED?
         BE    ZZZ130           NO
         C     R5,ENDPARM   YES-BEYOND THE ALLOWABLE PARM?
         BH    TOOLONG          YES
         MVC   0(8,R5),EXIT2    NO
         TRT   0(9,R5),BLNKTAB    POINT BEYOND EXIT NAME ENTERED
         LR    R5,R1
         MVI   0(R5),C';'
         LA    R5,1(R5)
         CLI   EXIT3,C' '       EXIT3 ENTERED?
         BE    ZZZ130           NO
         C     R5,ENDPARM   YES-BEYOND THE ALLOWABLE PARM?
         BH    TOOLONG          YES
         MVC   0(8,R5),EXIT3
         TRT   0(9,R5),BLNKTAB   POINT BEYOND EXIT NAME ENTERED
         LR    R5,R1
         MVI   0(R5),C';'
         LA    R5,1(R5)
ZZZ130   C     R5,ENDPARM   YES-BEYOND THE ALLOWABLE PARM?
         BH    TOOLONG          YES
         MVI   0(R5),C'E'
         MVC   1(2,R5),SECEXITD    ';Y' SECURITY EXIT DEACT VERIFICATN
         LA    R5,1(R4,R5)
EXIT130  MVC   0(3,R5),=C';X;'  EXECUTE
         LA    R5,3(R5)
ENTRY140 EQU   *
         MVI   0(R5),C'Q'       QUIT
*        TPUT  SPPARM,71
FTSKEL   CALL  ISPLINK,(FTOPEN,TEMP),VL
         CALL  ISPLINK,(FTINCL,SKELETON),VL
         CALL  ISPLINK,(FTCLOSE),VL
         SPFSUB  ZTEMPF
         SPFJOBNM SPJOBC1
         B     ENTRY010
EOJ      EQU   *
         SPFVDEL  (ZCMD)
         LA    R15,0
         #RTN
TOOLONG  MVC   MSG,=CL8'PXP027'
         B     ENTRY010
*----------------------------------------------------------------------
*        STORAGE AREAS
*----------------------------------------------------------------------
ENDPARM  DS    A                 ADDR OF THE END OF THE PARM
SKELETON DC    CL8'PXP0010S'
ACTALL   DC    CL5'A;X;Q'        ACTIVATE ALL
DEACTALL DC    CL5'D;X;Q'      DEACTIVATE ALL
REPRTALL DC    CL5'R;X;Q'        REPORT ALL
SECEXITD DC    CL2';Y'       SECURITY EXIT DEACTIVATION VERIFICATION
BLNKTAB  DC    X'FF',255X'00'         X'00' AND SPACES
         ORG   BLNKTAB+C' '
         DC    C' '
         ORG   BLNKTAB+256
         SPACE 1
PXP0010  CSECT
SPFMACS  SPFCMDS FILES=YES
         END
