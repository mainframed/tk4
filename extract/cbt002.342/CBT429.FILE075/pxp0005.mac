*        COMPILE AND LINKEDIT
*----------------------------------------------------------------------
*        DEFINE/COPY VARIABLES
*----------------------------------------------------------------------
         PRINT NOGEN
PXP0005  #SLC  R12,R11,R10        STANDARD LINKAGE
         SPFVDEF 1,(SRCIND,NOLK,LKCTL,PXPMACRO,PXPPANV)
         SPFVDEF 2,(PXPSEL,PXPPVCLS,DSORG)
         SPFVDEF 4,(PXPLANG)
         SPFVDEF 6,(NOPVOL,PXPNOLK,LKVOL)
         SPFVDEF 7,(PXPLKCTL)
         SPFVDEF 8,(NOPVMBR,PXPLKMBR)
         SPFVDEF 10,(PVMBR)
         SPFVDEF 30,(ERRMSG)
         SPFVDEF 44,(PVDSN,NOPVDSN,SPVDSN,SNOPVDSN,SPXPDSN,            +
               PXPMACL1,PXPMACL2,PXPMACL3,PXPMACL4,PXPMACL5,           +
               SPXPMAC1,SPXPMAC2,SPXPMAC3,SPXPMAC4,SPXPMAC5,           +
               PXPLKDSN,SPXPLKDS,CHKDSN)
         SPFVDEF 71,(PXPOPT,PXPLKOPT,PXPJOBC1,PXPJOBC2,PXPJOBC3,       +
               PXPLKC1,PXPLKC2,PXPLKC3,PXPLKC4,PXPLKC5,PXPLKC6,PXPLKC7,+
               PXPLKC8,PXPLKC9,PXPLKC10)
         SPFVCOPY (PXPDSN,44,ZTEMPF,44,PXPGRP,8,PXPELT,8,ZUSER,8,      +
               ZPREFIX,8,PXPVOL,6)
         SPFVCOPY (MSG,8),DC=NO
         SPFVGET (PXPOPT,PXPLKOPT,PXPJOBC1,PXPJOBC2,PXPJOBC3,          +
               PXPMACL1,PXPMACL2,PXPMACL3,PXPMACL4,PXPMACL5,PXPMACRO,  +
               NOLK,LKCTL,PVDSN,NOPVDSN,PXPLANG,PXPPANV),PROFILE
         MVC   CURSOR,BLANKS             CLEAR
*----------------------------------------------------------------------
*        DISPLAY COMPILE/LKED PANEL
*----------------------------------------------------------------------
ENTRY010 SPFSPLAY 'PXP0005',MSG,CURSOR,,EOJ
         MVC   MSG,=CL8'PXP000'          CLEAR
         MVC   CURSOR,BLANKS             CLEAR
         MVC   WORKDSN,PXPDSN            CONVERT DSN FOR SKELETON USE
         BAL   R14,ENTRY928
         MVC   SPXPDSN,WORKDSN2
         MVC   WORKDSN,PVDSN
         BAL   R14,ENTRY928
         MVC   SPVDSN,WORKDSN2
         MVC   WORKDSN,NOPVDSN
         BAL   R14,ENTRY928
         MVC   SNOPVDSN,WORKDSN2
         CLI   PVMBR,C' '             PANVALET MEMBER SPECIFIED?
         BNE   BBB010                 YES
         MVC   CHKDSN,SNOPVDSN        NO-VALIDATE DSN/MBR FOR DSORG
         MVC   CHKMBR,NOPVMBR
         MVC   INVOLSER,=C'ABSENT'
         CLI   NOPVOL,C' '            VOLSER SPECIFIED
         BE    AAA010                 NO
         MVC   INVOLSER,NOPVOL
AAA010   BAL   R14,ENTRY930
         MVC   CURSOR,=CL8'NOPVMBR'   POSITION CURSOR TO FLD IN ERROR
         ICM   R15,15,SPFRC           DSN/MBR OK?
         BNZ   ENTRY010                  NO
         MVC   CURSOR,BLANKS       YES-CLEAR
BBB010   CLI   PXPMACRO,C'Y'          ADDITIONAL MACRO LIBS REQUIRED
         BNE   CCC010                            NO
         BAL   R14,ENTRY950                      YES
         MVC   PANEL,=CL8'PXP0008'    MACRO/COPY LIB PANEL
         BAL   R14,ENTRY920           DISPLAY PANEL
         MVC   WORKDSN,PXPMACL1          CONVERT DSN FOR SKELETON USE
         BAL   R14,ENTRY928
         MVC   SPXPMAC1,WORKDSN2
         MVC   WORKDSN,PXPMACL2
         BAL   R14,ENTRY928
         MVC   SPXPMAC2,WORKDSN2
         MVC   WORKDSN,PXPMACL3
         BAL   R14,ENTRY928
         MVC   SPXPMAC3,WORKDSN2
         MVC   WORKDSN,PXPMACL4
         BAL   R14,ENTRY928
         MVC   SPXPMAC4,WORKDSN2
         MVC   WORKDSN,PXPMACL5
         BAL   R14,ENTRY928
         MVC   SPXPMAC5,WORKDSN2
         BAL   R14,ENTRY951
CCC010   CLC   PXPNOLK,=CL6'NOLKED'
         BE    DDD010
         CLC   PXPLKCTL,=CL7'PANELIN'
         BNE   DDD010
         BAL   R14,ENTRY950
         MVC   PANEL,=CL8'PXP0006'    LKED CNTL STATEMENTS PANEL
         BAL   R14,ENTRY920           DISPLAY PANEL
         MVC   WORKDSN,PXPLKDSN          CONVERT DSN FOR SKELETON USE
         BAL   R14,ENTRY928
         MVC   SPXPLKDS,WORKDSN2
         CLI   PXPLKDSN,C' '          LKED DSN SPECIFIED?
         BE    CCCEXIT                NO
         MVC   CHKDSN,SPXPLKDS    YES-VALIDATE DSN/MBR FOR DSORG
         MVC   CHKMBR,PXPLKMBR
         MVC   INVOLSER,=C'ABSENT'
         CLI   LKVOL,C' '            VOLSER SPECIFIED
         BE    CCC011                 NO
         MVC   INVOLSER,LKVOL
CCC011   BAL   R14,ENTRY930           CHK DSN/MBR RTN
         ICM   R15,15,SPFRC           DSN/MBR OK
         BNZ   CCC010                  NO
CCCEXIT  BAL   R14,ENTRY951
DDD010   CALL  ISPLINK,(FTOPEN,TEMP),VL
         CALL  ISPLINK,(FTINCL,SKELETON),VL
         CALL  ISPLINK,(FTCLOSE),VL
         SPFSUB  ZTEMPF
         SPFJOBNM PXPJOBC1
         B     ENTRY010
*----------------------------------------------------------------------
*        END OF JOB
*----------------------------------------------------------------------
EOJ      LA    R15,0
         #RTN
*----------------------------------------------------------------------
*        DISPLAY PANEL
*----------------------------------------------------------------------
ENTRY920 DS    0H
         ST    R14,ST920               SAVE R14
         SPFSPLAY PANEL,MSG,CURSOR    ,,ENTRY010
         LTR   R15,R15                .
         BZ    EXIT920                .
         MVC   MSG,=CL8'PXP000'          CLEAR
         BAL   R14,ENTRY951           RESTORE ENVIRONMENT
         B     ENTRY010               RETURN TO PRIOR SCREEN
EXIT920  L     R14,ST920
         BR    R14
ST920    DC    F'-1'
*----------------------------------------------------------------------
*        FIX DSN
*----------------------------------------------------------------------
ENTRY928 DS    0H
         ST    R14,ST928          SAVE R14
         CLI   WORKDSN,X'7D'           NAME IN QUOTES ?
         BE    AAA928                  YES - REMOVE QUOTES
         MVC   WORKDSN2,BLANKS       NO-CLEAR WORK FIELD
         MVC   WORKDSN2(8),ZPREFIX     MOVE USER PROFIX
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET TO BLANK
         TRT   WORKDSN2,SPFTRT         FIND END OF PREFIX
         MVI   0(R1),C'.'              ADD A DOT
         MVC   1(35,R1),WORKDSN        MOVE REST OF DSN
         B     BBB928
AAA928   MVC   WORKDSN2(43),WORKDSN+1  SKIP QUOTE
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET FOR BLANK
         MVI   SPFTRT+X'7D',X'FF'      SET FOR QUOTE
         TRT   WORKDSN2,SPFTRT         FIND END OF DSN
         BZ    BBB928                  FULL 44 BYTE NAME
         MVI   0(R1),C' '              REMOVE QUOTE
BBB928   L     R14,ST928          RESTORE
         BR    R14
ST928    DC    F'-1'
*----------------------------------------------------------------------
*        CHECK DSORG
*----------------------------------------------------------------------
ENTRY930 DS    0H
         ST    R14,ST930               SAVE R14
         MVC   SPFRC,=F'0'
         LINK  EP=FILEATTR,PARAM=(CHKDSN,VALSWTCH,ERRMSG,OVOL,LRECL,   +
               BLKSIZE,DSORG,RECFM,DEVC,INVOLSER),VL=1
         CLI   VALSWTCH,C'Y'     VALID RETURN?
         BNE   ZZZ930            NO - PUT OUT ERROR MSG
         CLC   DSORG,=C'PO'      YES- IS DATASET A PDS?
         BNE   XXX930            NO
         CLI   CHKMBR,C' '       YES- MAKE SURE MEMBER NOT BLANK
         BNE   EXIT930                OK
         B     YYY930                 NG
XXX930   CLI   CHKMBR,C' '       DSORG=PS, MAKE SURE NO MBR
         BE    EXIT930           OK
YYY930   MVC   MSG,=CL8'PXP004'       NG
         MVC   SPFRC,=F'4'
         B     EXIT930
ZZZ930   MVC   MSG,=CL8'PXP029'
         MVC   SPFRC,=F'4'
EXIT930  L     R14,ST930
         BR    R14
ST930    DC    F'-1'
*----------------------------------------------------------------------
*        SELECT CONTROL SAVE
*----------------------------------------------------------------------
ENTRY950 DS    0H
         ST    R14,ST950               SAVE R14
         CALL  ISPLINK,(CONTROL,DISPLAY,SAVE),VL
         L     R14,ST950
         BR    R14
ST950    DC    F'-1'
*----------------------------------------------------------------------
*        SELECT CONTROL RESTORE
*----------------------------------------------------------------------
ENTRY951 DS    0H
         ST    R14,ST951               SAVE R14
         CALL  ISPLINK,(CONTROL,DISPLAY,RESTORE),VL
         L     R14,ST951
         BR    R14
ST951    DC    F'-1'
*----------------------------------------------------------------------
*        STORAGE AREAS
*----------------------------------------------------------------------
WORKDSN  DC    CL44' '
WORKDSN2 DC    CL44' '
SKELETON DC    CL8'PXP0005S'
         SPACE
CHKMBR   DS    CL8
VALSWTCH DS    CL3
OVOL     DS    CL6
LRECL    DS    CL5
BLKSIZE  DS    CL5
RECFM    DS    CL1
DEVC     DS    CL1
INVOLSER DS    CL6
PXP0005  CSECT
SPFMACS  SPFCMDS FILES=YES
         END
