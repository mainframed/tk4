*----------------------------------------------------------------------
*        ZAP THE MODULE, DISPLAY LOAD MODULE XREF, MAP AND STATISTICS
*----------------------------------------------------------------------
         PRINT NOGEN
PXP0009  #SLC R12,R11
         LR   R3,R1      SAVE PARM -FULLY QUALIFIED PANX NAME
*----------------------------------------------------------------------
*        DEFINE VARIABLES
*----------------------------------------------------------------------
         SPFVDEF 1,(PXPZPST,PXPZPMOD)
         SPFVDEF 6,(PXPZPOPT)
         SPFVDEF 8,(PXPZPGRP,PXPZPELT)
         SPFVDEF 72,(PXPZAP1,PXPZAP2,PXPZAP3,PXPZAP4,PXPZAP5,PXPZAP6,  +
               PXPZAP7,PXPZAP8,PXPZAP9,PXPZAP10,PXPZAP11,PXPZAP12,     +
               PXPZAP13)
         SPFVCOPY (MSG,8),DC=NO
         SPFVGET (PXPZPGRP,PXPZPST,PXPZPMOD,PXPZPOPT),PROFILE
*        DISPLAY ZAP PANEL
         SPFSPLAY 'PXP0009',MSG,,,EOJ
         OPEN  (PANEIN,(OUTPUT))       ALOCATED BY PXP0000
         MVC   PANEINCC(12),=C'%ZAP        '
         CLI   PXPZPOPT,C' '           ARE OPTIONS SPECIFIED
         BE    NOOPTS                  NO
         MVI   PANEINCC+4,C','         YES
         MVC   PANEINCC+5(6),PXPZPOPT  ZAP OPTIONS
NOOPTS   MVC   PANEINCC+12(34),0(R3)    FULLY QUALIFIED PANX NAME
         CLI   PXPZPELT,C' '           IS OUTPUT ELT SPECFIED
         BE    SAMEELT                 NO
         MVI   PANEINCC+46,C','        YES
         MVC   PANEINCC+47(8),PXPZPGRP   OUTPUT ZAPPED MODULE
         MVI   PANEINCC+55,C'.'
         MVC   PANEINCC+56(8),PXPZPELT
         MVI   PANEINCC+64,C';'
         MVC   PANEINCC+65(1),PXPZPST
         MVC   PANEINCC+66(1),PXPZPMOD
SAMEELT  BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP1
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP2
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP3
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP4
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP5
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP6
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP7
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP8
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP9
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP10
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP11
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP12
         BAL   R4,ENTRY020
         MVC   PANEINCC(72),PXPZAP13
         BAL   R4,ENTRY020
         CLOSE  (PANEIN)         CLOSE FILES
         FREEPOOL PANEIN          FREE THE BUFFERS
         LINK  EP=PANEXEC,PARAM=(CHPXPARM),VL=1
EOJ      EQU   *
         SPFALLOC U,DD='PANEIN',DISP='DELETE' UNALLOC & DELETE
         LA    R15,0
         #RTN
*----------------------------------------------------------------------
*        PUT THE PANEIN REC
*----------------------------------------------------------------------
ENTRY020 CLI   PANEINCC,C' '     ANYTHING ON CONTROL CARD
         BER   R4                    NO
         PUT   PANEIN,PANEINCC       YES - WRITE IT
         BR    R4
*----------------------------------------------------------------------
*        STORAGE AREAS
*----------------------------------------------------------------------
PANEINCC DC    CL80' '
CHPXPARM DC    H'20'
         DC    CL20'%INPUT,S PANEIN*'
SMSG     DS    CL24
LMSG     DS    CL72
*----------------------------------------------------------------------
PANEIN   DCB   DDNAME=PANEIN,DSORG=PS,MACRF=PM,BLKSIZE=80
SPFMACS  SPFCMDS DSCB1=NO,ENQDEQ=NO,TABLES=NO
         END
