*        LIBRARY MAINTENANCE
*----------------------------------------------------------------------
*        DEFINE/COPY VARIABLES
*----------------------------------------------------------------------
         PRINT NOGEN
PXP0007  #SLC  R12,R11,R10        STANDARD LINKAGE
         SPFVDEF 1,(LIBRREP,ZCMD)
         SPFVDEF 2,(REPL)
         SPFVDEF 4,(LIBRETPD,LIBFUPDT,LIBGDG,LIBGDGAB)
         SPFVDEF 6,(PDSVOL,LIBCVL,LIBCYL,VOLSER)
         SPFVDEF 7,(LIBMXELT)
         SPFVDEF 8,(LIBGRP,LIBELT)
         SPFVDEF 44,(LIBPDS,SLIBPDS,SPXPDSN)
         SPFVDEF 71,(LIBJOBC1,LIBJOBC2,LIBJOBC3)
         SPFVCOPY (PXPDSN,44,ZTEMPF,44,PXPGRP,8,PXPELT,8,ZUSER,8,      +
               ZPREFIX,8,PXPVOL,6,PXPTYPE,4,PXPSTAT,1,PXPMODE,1)
         SPFVCOPY (MSG,8),DC=NO
         SPFVGET (LIBJOBC1,LIBJOBC2,LIBJOBC3,LIBGDG),PROFILE
*----------------------------------------------------------------------
*        DISPLAY LIBRARY MAINTENANCE PANEL
*----------------------------------------------------------------------
ENTRY010 SPFSPLAY 'PXP0007',MSG,,,EOJ
         MVC   WORKDSN,PXPDSN            CONVERT DSN FOR SKELETON USE
         BAL   R14,ENTRY928
         MVC   SPXPDSN,WORKDSN2
         MVC   WORKDSN,LIBPDS           CONVERT DSN FOR SKELETON USE
         BAL   R14,ENTRY928
         MVC   SLIBPDS,WORKDSN2
         OI    ZCMD,C' '   UPPER CASE
*--------------------
*        FILEOPT
*--------------------
         CLI   ZCMD,C'M'   IS THIS MODIFY ATTR (FILEOPT) OPT
         BNE   ENTRY020                 NO
         MVC   PANXPARM+11(4),LIBFUPDT
         MVC   PANXPARM+24(8),PXPGRP
         LINK  EP=PANEXEC,PARAM=(PANXPARM),VL=1  EXEC PANEXEC WITH PARM
         B     ENTRY010
*--------------------
*        SKELETONS FOR ALL OTHER OPTIONS
*--------------------
ENTRY020 SPFVPUT (SPXPDSN,LIBGDG,LIBGDGAB),SHARED SHARED POOL FOR CLIST
         CLI   ZCMD,C'D'         IS THIS THE DELETE OPTION
         BNE   AAA020            NO
         MVC   CLIST,=C'PXPVOLD '  USE CLIST FOR DELETE DSN FOR /*SETUP
         B     BBB020
AAA020   CLI   ZCMD,C'R'         RESTORE?
         BNE   CCC020
         MVC   CLIST,=C'PXPVOLR '  USE CLIST FOR RESTORE DSN FOR /*SETP
BBB020   CALL  ISPLINK,(SELECT,CMDLEN,CMD),VL
         SPFVGET (VOLSER),SHARED
CCC020   CALL  ISPLINK,(FTOPEN,TEMP),VL
         CALL  ISPLINK,(FTINCL,SKELETON),VL
         CALL  ISPLINK,(FTCLOSE),VL
         SPFSUB  ZTEMPF
         SPFJOBNM LIBJOBC1
         B     ENTRY010
EOJ      SPFVDEL (ZCMD)
         LA    R15,0
         #RTN
*----------------------------------------------------------------------
*        FIX DSN
*----------------------------------------------------------------------
ENTRY928 DS    0H
         ST    R14,ST928          SAVE R14
         CLI   WORKDSN,X'7D'           NAME IN QUOTES ?
         BE    AAA928                  YES - REMOVE QUOTES
         MVC   WORKDSN2,BLANKS       NO-CLEAR WORK FIELD
         MVC   WORKDSN2(8),ZPREFIX     MOVE USER PROFIX
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET TO BLANK
         TRT   WORKDSN2,SPFTRT         FIND END OF PREFIX
         MVI   0(R1),C'.'              ADD A DOT
         MVC   1(35,R1),WORKDSN        MOVE REST OF DSN
         B     BBB928
AAA928   MVC   WORKDSN2(43),WORKDSN+1  SKIP QUOTE
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET FOR BLANK
         MVI   SPFTRT+X'7D',X'FF'      SET FOR QUOTE
         TRT   WORKDSN2,SPFTRT         FIND END OF DSN
         BZ    BBB928                  FULL 44 BYTE NAME
         MVI   0(R1),C' '              REMOVE QUOTE
BBB928   L     R14,ST928          RESTORE
         BR    R14
ST928    DC    F'-1'
*----------------------------------------------------------------------
*        STORAGE AREAS
*----------------------------------------------------------------------
PANXPARM DC    H'100'
         DC    CL100'%FILEOPT,     PANELIB*        .;TA' SUPPLY OPTS,GP
WORKDSN  DC    CL44' '
WORKDSN2 DC    CL44' '
SKELETON DC    CL8'PXP0007S'
         SPACE 1
CMDLEN   DC    F'13'           LENGTH OF THE FOLLOWING CMD
CMD      DC    C'CMD('
CLIST    DS    CL8             CLIST NAME TO EXECUTE
         DC    C')'
PXP0007  CSECT
SPFMACS  SPFCMDS FILES=YES
         END
