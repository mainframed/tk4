*        PANEXEC MASTER DIALOG MANAGER
         PRINT  NOGEN
PXP0000  SPFINIT  R12,R11,R10,R9,R8,R7,USER=Y,TIME=Y,TERM=Y,TABLES=Y
*----------------------------------------------------------------------
*        DEFINE VARIABLES
*----------------------------------------------------------------------
ENTRY010 DS    0H
         SPFVDEF 2,(YZA),DC=NO
         SPFVDEF 2,(YZB),DC=NO
         SPFVDEF 2,(YZC),DC=NO
         SPFVDEF 2,(YZCH),DC=NO
         SPFVDEF 2,(YZCL),DC=NO
         SPFVDEF 2,(YZD),DC=NO
         SPFVDEF 2,(YZE),DC=NO
         SPFVDEF 2,(YZF),DC=NO
         SPFVDEF 2,(YZG),DC=NO
         SPFVDEF 2,(YZI),DC=NO
         SPFVDEF 2,(YZIN),DC=NO
         SPFVDEF 2,(YZL),DC=NO
         SPFVDEF 2,(YZM),DC=NO
         SPFVDEF 2,(YZP),DC=NO
         SPFVDEF 2,(YZPM),DC=NO
         SPFVDEF 2,(YZR),DC=NO
         SPFVDEF 2,(YZS),DC=NO
         SPFVDEF 2,(YZT),DC=NO
         SPFVDEF 2,(YZX),DC=NO
         SPFVDEF 2,(YZZ),DC=NO
         SPFVDEF 8,(MSG),DC=NO
*
         SPFVDEF 1,(PXPSTAT,PXPMODE,PXPNEWMD,PXPSSOUT,PXPREPL,PXPEXPND,+
               PXPSTAT2,PXPMODE2,PXPLOG,PXPDIAG,PXPSVIEW,PXPCONF)
         SPFVDEF 3,(DISP)
         SPFVDEF 4,(PXPTYPE)
         SPFVDEF 6,(PXPVOL,VOL2)
         SPFVDEF 8,(PXPGRP,PXPELT,PXPNEWNM,PXPNEWGP,MEMB,              +
               PXPGRP2,PXPELT2,SCRHORZ)
         SPFVDEF 10,(PXPFUNC)
         SPFVDEF 44,(PXPDSN,PXPDSN2,DSN2)
         SPFVDEF 100,(PXPARMS)
         SPFVGET (PXPDSN,PXPGRP,PXPELT,PXPSTAT,PXPMODE,PXPSSOUT,PXPLOG,+
               DSN2,PXPREPL,PXPEXPND,ZUSD,PXPDIAG,PXPTYPE,             +
               PXPCONF),PROFILE
         MVC   SCRHORZ,BLANKS
         BAL   R14,ENTRY910
         SPFSPLAY 'PXPLOGO',,,,GOBACK    DISPLAY LOGO
         MVC   MSG,=CL8'PXP000'  INIT BLANK MESSAGE
         MVC   PANEL,=CL8'PXP0000'    INIT PANEL
*----------------------------------------------------------------------
*        DISPLAY PRIMARY PANEL
*----------------------------------------------------------------------
ENTRY060 EQU   *
         CLC   MSG+4(2),=C'00'   IS THERE A MSG TO BE PUT OUT?
         BNE   AAA060            YES
         MVC   MSG,=CL8'PXP000'      NO-INIT BLANK MESSAGE
         MVC   PXPFUNC,BLANKS        NO-INIT BLANK FUNC
AAA060   MVC   PANXPARM+2(100),BLANKS
         MVC   PANEL,=CL8'PXP0000'    INIT PANEL
         CLI   MOREFLAG,C'1'    MORE CMDS FROM PXP0100?
         BNE   DISP060          NO
         CLI   ERRFLAG,C'1' YES-WAS THERE AN ERROR ON 'S' OPT SELECTN
         BE    FFF190     YES- DONT SETMSG (ALLOWS NEXT MSG TO DISPL)
         CALL  ISPLINK,(SETMSG,MSG),VL   NO- FORCE MSG DISPLAY
         B     FFF190           RETURN TO 190 RTN TO GET NEXT CMD
DISP060  SPFSPLAY PANEL,MSG,'ZCMD',,GOBACK
         MVC   WORKDSN,PXPDSN
         BAL   R14,ENTRY928            PROCESS DSN
*----------------------------------------------------------------------
*        CHECK PRIMARY COMMAND - ZCMD
*----------------------------------------------------------------------
BBB060   MVC   PANEL,=CL8'PXP0000'      REINIT TO INIT PANEL
         MVC   MSG,=CL8'PXP000'        BLANK MESSAGE
         MVC   PXPFUNC,BLANKS          INIT BLANK FUNC
         USING SELDSECT,R1
         OC    ZCMD,BLANKS           UPPER CASE
         MVC   SAVSEL,ZCMD             SAVE SELECTION OPTION
         LA    R1,SELTABLE             TABLE OF SELECT OPTIONS
LOOP070  CLC   SAVSEL,SELOPT           FIND OPTION
         BE    CCC070                  FOUND IT
         LA    R1,SELLL(R1)            BUMP
         CLI   0(R1),X'FF'             END OF TABLE ?
         BNE   LOOP070                 NO - OK
ENTRYERR MVC   ZCMD,BLANKS           YES - CLEAR SEL OPTION
         MVC   PANEL,BLANKS       NO- REDISPLAY PANEL WITH ERROR MSG
         MVC   MSG,=CL8'PXP001'      INVALID SELECTION CODE
         B     ENTRY060                DISPLAY
CCC070   ICM   R14,15,SELADDR          PROCESSING ROUTINE ADDRESS
         BR    R14                     GO TO PROCESSING ROUTINE FOR
*----------------------------------------------------------------------
*        BROWSE/EDIT REQUEST
*----------------------------------------------------------------------
ENTRY100 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
A100     BAL   R14,ENTRY920       ALLOC FILES
         CLI   SAVSEL,C'E'        EDIT?
         BNE   AA100              NO
         BAL   R14,ENTRY960       YES-ENQ THE ELEMENT
         MVC   LRECL,=H'80'       SET PANPRINT LRECL=80
AA100    MVC   DDNAME,=CL8'PANPRINT'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANPRINT)
         MVC   LRECL,=H'0'        RESET LRECL
         MVC   PANXPARM+2(9),=CL9'%PRINT,D '
         CLI   TYPE,C'C'     IS TYPE=CNTL  (PXPTYPE WAS MOVED TO TYPE)
         BNE   AAA100             NO
         MVC   PANXPARM+8(2),=C'  ' YS-BLANK OUT ,D
AAA100   MVC   PANXPARM+11(34),PANECC2
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         LTR   R15,R15            WAS PRINT CMD SUCCESSFUL?
         BNZ   KKK100             NO
         CLI   SAVSEL,C'E'    YES-WAS EDIT REQUESTED
         BE    FFF100               YES
         MVC   IMAC,=CL8'PXPIMACB'  NO-INVOKE THE EDIT INIT MACRO & END
         BAL   R14,ENTRY924       EDIT THE TEMP DS  FOR BROWSING
         BAL   R14,ENTRY930       BROWSE TEMP DS USING CUSTOMIZE PANEL
         LTR   R15,R15                 DID WE BROWSE IT ?
         BZ    EEE100                  YES - OK
BBB100   MVC   MSG,=CL8'PXP002'    NO-BROWSE/EDIT FAILED
         MVC   PANEL,BLANKS
EEE100   EQU   *
         BAL   R14,ENTRY970       DEQ THE ELEMENT
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060                DISPLAY
FFF100   EQU   *             EDIT RTN
         MVC   PANEL,=CL8'PXPEDDE'   USE CUSTOMIZED EDIT PANEL
         MVC   IMAC,=CL8'PXPIMACE'     INVOKE THE EDIT INITIAL MACRO
         BAL   R14,ENTRY924       EDIT THE TEMP DS
         C     R15,=F'4'    TEMP DS EDITED ?  (0=SAVED OR 4=CANCELLED)
         BE    EEE100       CANCELLED - DONT SAVE, JUST GET OUT
         BH    BBB100                      NO
         BAL   R14,ENTRY920    RE-ALLOC PANELIB & UNALLOC PANPRINT
         MVC   PANXPARM+2(8),=C'%ADD,R  '  ADD IT BACK TO PANX LIB
         MVC   PANXPARM+45(7),=C',SEQIN*'
         MVC   DSN2DD,=CL8'SEQIN'         ALLOC SEQU FILE
         MVC   DDNAME,DSN2DD              (FOR LATER DELETION)
         MVC   SAVDSN2,SAVDSN     SAVE THE DSN FOR 'S' OPT
         MVC   SAVDSN,TEMPOUT
         MVC   DISP,=CL3'OLD'             SET DS DISP
         MVC   FREE,=CL5'END'  DONT FREE UNTIL UNALLOC/DEL IN EEE100
         BAL   R14,ENTRY923
         MVC   SAVDSN,SAVDSN2   RESET THE DSN IF EDIT/BROWSE, COPY/XFER
*                         INPUT, FETCH AND ADD REINVOKED ON 'S' OPTION
         BAL   R14,ENTRY922               REALLOCATE PANPRINT
         MVC   PXPFUNC,=CL10'SAVED'   ELT SAVED
         BAL   R14,ENTRY929               INVOKE PANEXEC
         B     EEE100             DELETE AND UNALLOC TEMP DS
*        CREATE NEW ELEMENT FOR EDIT
KKK100   EQU   *                  %PRINT FAILED - ELEMENT NOT FOUND
         CLI   SAVSEL,C'E'        IS IT EDIT
         BNE   BBB100             NO -ERROR
         BAL   R14,ENTRY970       YES-DEQ THE ELEMENT
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS  -PANPRINT
         MVC   DDNAME,=CL8'PANEIN'   CREATE BLANK ELEMENT
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANEIN)
         OPEN  (PANEIN,(OUTPUT))
         MVC   PANECCE1+7(34),PANECC2
         PUT   PANEIN,PANECCE1          %ADD,D ELTNAME
         PUT   PANEIN,BLANKS            BLANKS
         BAL   R14,ENTRY926        CLOSE AND FREEPOOL
         MVC   PANXPARM+2(80),INPUTCC   %INPUT,S PANEIN*
         BAL   R14,ENTRY920    RE-ALLOC PANELIB & UNALLOC PANPRINT
         BAL   R14,ENTRY922    RE-ALLOC PANPRINT TO TERMINAL
         BAL   R14,ENTRY929             CALL PANEXEC
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- PANEIN
         B     A100
*----------------------------------------------------------------------
*        DELETE ELEMENT
*----------------------------------------------------------------------
ENTRY110 DS    0H
         BAL   R14,ENTRY950       CONTROL SAVE
         CLC   SAVDSN,FPANESRL    TRYING TO DELETE FROM PANESRL
         BNE   AAA110              NO-OK
         MVC   MSG,=CL8'PXP003'   YES-ERROR
         B     ZZZ110
*----------------------------------------------------------------------
*        DELETE - DISPLAY CONFIRMATION PANEL
*----------------------------------------------------------------------
AAA110   CLI   PXPCONF,C'Y'       SHOW CONFIRM DELETE?
         BNE   AAA111             NO
         SPFSPLAY 'PXP0002',,,,ZZZ110    YES- RC=0 ENTER; RC=8 END
AAA111   BAL   R14,ENTRY960       ENQ THE ELT
         BAL   R14,ENTRY920       ALLOC STANDARD FILES
         BAL   R14,ENTRY922       ALLOC PRINT FILE
         MVC   DDNAME,=CL8'PANEIN'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANEIN)
         SPFALLOC A,RETDD=PRTECTDD,DSN=SAVDSN,DISP='(NEW,DELETE)',     +
               SPACE='(TRK,1)',UNIT='SYSDA',VOL='HRWKA1',FREE='CLOSE'
         OPEN  (PANEIN,(OUTPUT))
         PUT   PANEIN,PANECC1           %REMOVE,FL PANELIB*,PROTECT*
         PUT   PANEIN,PANECC2+9         ELEMENT G.E/T;SM
         BAL   R14,ENTRY926        CLOSE AND FREEPOOL
         MVC   PANXPARM+2(80),INPUTCC   %INPUT,S PANEIN*
         MVC   PXPFUNC,=CL10'DELETED' ELT DELETED
         BAL   R14,ENTRY929             CALL PANEXEC
         ST    R15,SPFRC                SAVE THE RC
         BAL   R14,ENTRY970       DEQ THE ELT
         ICM   R15,15,SPFRC       RETRIEVE THE PANEXEC RC
         BNZ   EXIT110            NOT SUCCESSFUL DELETE
         CLI   MOREFLAG,C'1'  YES-DELETE FROM ELEMENT LIST?
         BNE   EXIT110            NO
         CALL  ISPLINK,(TBDELETE,PXPTABLE),VL  DELETE SUCCESS -TBL UPDT
EXIT110  BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060                DISPLAY
*----------------------------------------------------------------------
*        USER CHANGED THEIR MIND
*----------------------------------------------------------------------
ZZZ110   BAL   R14,ENTRY951       CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        TRANSFER ELEMENT
*----------------------------------------------------------------------
ENTRY130 DS    0H
         BAL   R14,ENTRY950       CONTROL SAVE
         BAL   R14,ENTRY920       ALLOC STANDARD FILES
         BAL   R14,ENTRY922       ALLOC PRINT FILE
         MVC   PXPFUNC,=CL10'TRANSFERED'  ELEMENT TRANSFERED
         B     AAA140          GET ELT1 AND ELT2 NAMES FROM COPY RTN
         SPACE 1
AAA130   MVC   DDNAME,=CL8'PANEIN'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANEIN)
         OPEN  (PANEIN,(OUTPUT))
         CLI   PXPREPL,C'Y'             REPLACE CURRENT ELEMENT
         BNE   BBB130
         MVI   PANECCT1+12,C'R'
BBB130   PUT   PANEIN,PANECCT1         %TRANSFER,FL  PANELIB*,PANELIBO*
         MVC   PANECCT2(69),PANXPARM+10 <===ELT1,ELT2
         PUT   PANEIN,PANECCT2
         BAL   R14,ENTRY926        CLOSE AND FREEPOOL
         MVC   PANXPARM+2(80),INPUTCC   %INPUT,S PANEIN*
         BAL   R14,ENTRY929             CALL PANEXEC
         BAL   R14,ENTRY970       DEQ THE ELT
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060                DISPLAY
*----------------------------------------------------------------------
*        COPY ELEMENT
*----------------------------------------------------------------------
ENTRY140 DS    0H
         BAL   R14,ENTRY950       CONTROL SAVE
         BAL   R14,ENTRY920       ALLOC FILES AND SETUP PANX QUAL ELTNM
         BAL   R14,ENTRY922       ALLOC PRINT FILE
         MVC   PXPFUNC,=CL10'COPIED'  ELEMENT COPIED
AAA140   MVC   PANEL,=CL8'PXP0001'      FETCH/ADD/INPUT/COPY/XFER PANEL
         MVC   CURSOR,=CL8'PXPDSN2'
         BAL   R14,ENTRY927             DISPLAY PANEL
         BAL   R14,ENTRY960       ENQ THE ELT
         MVC   COPYDSN,SAVDSN      SAVE THE 'FROM' DSN
         MVC   WORKDSN,PXPDSN2     PROCESS PANEXEC DSN
         BAL   R14,ENTRY928             CHECK DSN
         MVC   PANXPARM+2(5),=C'%COPY'
         CLI   PXPREPL,C'Y'             REPLACE CURRENT ELEMENT
         BNE   BBB140
         MVC   PANXPARM+7(3),=C',R '
BBB140   MVC   PANXPARM+10(34),PANECC2
         MVI   PANXPARM+44,C','
         MVC   GRP,PXPGRP2         SET UP QNAM
         MVC   ELT,PXPELT2
         MVC   STAT,PXPSTAT2
         MVC   MODE,PXPMODE2
         MVI   COPYSAMF,C'1'    SET FLAG -INDICATE COPY TO SAME FILE
*IF THE SECOND DSN IS NOT THE SAME AS THE FIRST DSN ALLOC 2ND PANX FILE
         CLC   COPYDSN,SAVDSN
         BE    CCC140           COPY DEFAULTS TO INPUT F*
         MVC   DSN2DD,=CL8'PANELIBO'
         MVC   PANXPARM+45(9),=C'PANELIBO*'
         BAL   R14,ENTRY923       ALLOC PANELIBO FILE
         MVI   COPYSAMF,C'2'    INDICATE COPY TO DIFFERENT FILE
         MVC   SAVDSN,COPYDSN   RESET THE DSN IF EDIT/BROWSE, COPY/XFER
*                         INPUT, FETCH AND ADD REINVOKED ON 'S' OPTION
CCC140   MVC   PANXPARM+54(25),PANECC2+9
         CLI   SAVSEL,C'T'        RETURN TO TRANSFER RTN
         BE    AAA130
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         BAL   R14,ENTRY980       DEQ THE ELT;UPDATE TBL FOR DIRECTORY
         BAL   R14,ENTRY951       CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        MODIFY MODE
*----------------------------------------------------------------------
ENTRY150 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         CLI   MOREFLAG,C'1'      MODIFY MODE FROM DIRECTORY LIST?
         BNE   AAA150                  NO
         MVC   PANEL,=CL8'PXP0000'    INIT PANEL
         MVC   CURSOR,=CL8'PXPNEWMD'  SET CURSOR
         BAL   R14,ENTRY927        YES-DISPALY PRIMARY MENU
AAA150   BAL   R14,ENTRY960       ENQ THE ELT
         BAL   R14,ENTRY920            ALLOC FILES
         BAL   R14,ENTRY922            ALLOC PRINT FILE
         MVC   PANXPARM+2(6),=C'%MODE,'
         MVC   PANXPARM+8(1),PXPNEWMD
         MVI   PANXPARM+9,C' '
         MVC   PANXPARM+10(34),PANECC2
         MVC   PXPFUNC,=CL10'MODE MODIF' ELT MODE MODIFIED
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         MVC   MODE,PXPNEWMD      FOR OPT='S', TO ADD NEW TABLE ENTRY
         BAL   R14,ENTRY980       DELETE OLD TBL ENTRY AND ADD NEW ONE
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        PROMOTE TO PRODUCTION STATUS
*----------------------------------------------------------------------
ENTRY160 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         BAL   R14,ENTRY960       ENQ THE ELT
         BAL   R14,ENTRY920            ALLOC FILES
         BAL   R14,ENTRY922            ALLOC PRINT FILE
         MVC   PANXPARM+2(8),=C'%STATUS '
         MVC   PANXPARM+10(34),PANECC2
         MVC   PXPFUNC,=CL10'PROMOTED' ELT PROMOTED
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         MVI   STAT,C'P'          FOR OPT='S', TO ADD NEW TABLE ENTRY
         BAL   R14,ENTRY980       DELETE OLD TBL ENTRY AND ADD NEW ONE
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        INPUT A PANEXEC PROCEDURE
*----------------------------------------------------------------------
ENTRY170 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         BAL   R14,ENTRY920            ALLOC FILES
         MVC   SAVDSN2,SAVDSN    SAVE SAVDSN
         MVC   PANEL,=CL8'PXP0001'      FETCH/ADD/INPUT PANEL
         MVC   CURSOR,=CL8'PXPDSN2'
         BAL   R14,ENTRY927             DISPLAY PANEL
         MVC   SAVDIAG,PXPDIAG         SAVE DIAGNOSE INDICATOR
         MVI   PXPDIAG,C'Y'            TURN ON DIAG FLAG
         BAL   R14,ENTRY922            ALLOC PRINT FILE
         MVC   PANXPARM+2(7),=C'%INPUT '
         CLI   PXPELT2,C' '            IS PROC FROM PANEXEC LIB
         BE    CCC170                  NO- GET IT FROM SEQ/PDS FILE
         MVC   WORKDSN,PXPDSN2     PROCESS PANEXEC DSN
         BAL   R14,ENTRY928             CHECK DSN
         CLC   SAVDSN,FPANESRL
         BE    AAA170      USE PANESRL DEFAULT
         MVC   DSN2DD,=CL8'PANELIBI'
         MVC   PANXPARM+11(9),=C'PANELIBI*'
         BAL   R14,ENTRY923       ALLOC PANELIBI FILE TO GET INPUT PROC
AAA170   MVC   GRP,PXPGRP2
         MVC   ELT,PXPELT2
         MVC   STAT,PXPSTAT2
         MVC   MODE,PXPMODE2
         MVC   TYPE,=CL4'CNTL'      TYPE MUST BE CNTL
         MVC   PANXPARM+20(25),PANECC2+9      INPUT PROC NAME
         CLC   ELT,=CL8'REPORTS'             IS PROC='REPORTS'
         BNE   ZZZ170                              NO
         MVC   DDNAME,=CL8'FILEA'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR FILEA)
         MVC   TEMPQUAL,=CL5'TEMP2'
         MVC   DDNAME,=CL8'EZTIN'                  YES
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR EZTIN)
         MVC   TEMPQUAL,=CL5'TEMP1'
         B     ZZZ170
CCC170   MVC   WORKDSN,DSN2             PROCESS SEQ/PDS FILE NAME
         BAL   R14,ENTRY928             CHECK DSN
         MVC   DSN2DD,=CL8'SEQIN'
         MVC   PANXPARM+8(3),=C',S '  INDICATE SEQUENTIAL INPUT
         MVC   PANXPARM+11(6),=C'SEQIN*'
         CLI   MEMB,C' '
         BE    DDD170
         MVC   DSN2DD,=CL8'PDSIN'
         MVC   PANXPARM+8(3),=C',P '  PDS INPUT
         MVC   PANXPARM+11(7),=C'PDSIN*.'
         MVC   PANXPARM+18(8),MEMB
DDD170   BAL   R14,ENTRY923       ALLOC PDSIN/SEQIN FILE
ZZZ170   MVI   PANXPARM+45,C','
         MVC   PANXPARM+46(54),PXPARMS
         MVC   SAVDSN,SAVDSN2   RESET THE DSN IF EDIT/BROWSE, COPY/XFER
*                         INPUT, FETCH AND ADD REINVOKED ON 'S' OPTION
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         MVC   DDNAME,=CL8'EZTIN'
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- EZTIN
         MVC   DDNAME,=CL8'FILEA'
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- FILEA
         BAL   R14,ENTRY951            CONTROL RESTORE
         MVC   PXPDIAG,SAVDIAG     RESTORE DIAGNOSE INDICATOR
         B     ENTRY060
         SPACE 1
*----------------------------------------------------------------------
*        CHANGE DIRECTORY INFO (FALL THRU TO ENTRY190)
*----------------------------------------------------------------------
ENTRY180 DS    0H
         MVC   FREE,=CL5'END'     DONT FREE PANELIB IMMEDIATELY
*----------------------------------------------------------------------
*        DISPLAY AN PANEXEC ELEMENT LIST FOR PROCESSING
*----------------------------------------------------------------------
ENTRY190 DS    0H
         BAL   R14,ENTRY950       CONTROL SAVE
         BAL   R14,ENTRY920       ALLOC STANDARD FILES
         MVC   DDNAME,=CL8'PANEIN'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANEIN)
         CLI   SAVSEL,C'S'             IS THIS I OR CH OPTION
         BNE   AAA190                        YES
         CALL  ISPLINK,(CONTROL,DISPLAY,LOCK),VL    NO
         SPFSPLAY ,'PXP030'            DISPLAY 'BUILDING DIRECTORY'
         MVC   SPACE,SPCFILEA          GET LOTS OF SPACE FOR FILEA
         CLI   PXPSVIEW,C'L'      LONG VIEW (PANVALET SOURCE STATS)?
         BNE   AAA190                     NO
         MVI   PANECCD1+10,C'X'     YES-EXTRACT XIR RECORDS
AAA190   BAL   R14,ENTRY922       ALLOC PRINT FILE
         MVC   DDNAME,=CL8'FILEA'
         MVC   BLKSIZE,=H'23476'
         MVI   BUFNO,X'1E'            BUFNO=30
         MVC   TEMPQUAL,=CL5'TEMP2'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR FILEA)
         MVC   SPACE,SPACETMP       RESET SPACE, BLKSIZE, BUFNO
         MVC   BLKSIZE,=H'21280'
         MVI   BUFNO,X'05'
         MVC   TEMPQUAL,=CL5'TEMP1'
         OPEN  (PANEIN,(OUTPUT))
         PUT   PANEIN,PANECCD1          %DIRSTAT,F  PANELIB*,FILEA*
         MVI   PANECCD1+10,C' '         RESET
         MVC   SAVELT,BLANKS            CLEAR SAVELT
         XC    SPFTRT,SPFTRT            CLEAR TRT TABLE
         MVI   SPFTRT+C'*',X'FF'
         TRT   ELT,SPFTRT               GENERIC ELT REQUESTED
         BZ    CCC190               NO- DO DIRSTAT FOR THIS ELTNM ONLY
         MVC   SAVELT,ELT           YES-SAVE FOR GENERIC ELT SELECTION
         MVC   ELT,BLANKS               BLANK OUT ELT FOR WHOLE GRP
CCC190   MVC   SPFTRT,BLANKS   CAUSE ANY NON-BLANK/NON-DELIMIT TO STOP
         MVI   SPFTRT+C' ',X'00'        BLANK
         MVI   SPFTRT+C'.',X'00'        PERIOD
         MVI   SPFTRT+C'/',X'00'        SLASH
         MVI   SPFTRT+C';',X'00'        SEMI-COLON
         TRT   PANECC2+9(71),SPFTRT   ANY RESTRICTIVE CRITERIA ENTERED
         BNZ   DDD190                   YES - OK
         MVI   PANECC2+9,C'*'             NO- DO GLOBAL DIRSTAT
         MVC   PANECC2+10(70),BLANKS     BLANK OUT REST OF CARD
DDD190   PUT   PANEIN,PANECC2+9         ELEMENT G.E/T;SM  OR *
         CLI   PANECC2+9,C'*'           WAS IT A GLOBAL DIRSTAT
         BNE   DDD191                   NO
         MVC   QNAM-1(24),NAMESKEL YES-RESTORE DELIMITERS
DDD191   CLI   SAVSEL,C'I'             IS THIS I OPTION
         BE    KKK190                        YES
EEE190   BAL   R14,ENTRY926        CLOSE AND FREEPOOL
         MVC   PANXPARM+2(80),INPUTCC   %INPUT,S PANEIN*
         BAL   R14,ENTRY929             CALL PANEXEC
         CLI   SAVSEL,C'C'             IS THIS CH OPTION
         BE    QQQ190                        YES
         MVC   DDNAME,=CL8'PANEIN'
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- PANEIN
         MVC   DDNAME,=CL8'EZTIN'
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- EZTIN
         CLI   SAVSEL,C'I'             IS THIS I OPTION
         BNE   FFF190                        NO
         MVC   DDNAME,=CL8'FILEA'            YES
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- FILEA
         MVC   IMAC,=CL8'PXPIMACB'  NO-INVOKE THE EDIT INIT MACRO & END
         BAL   R14,ENTRY924       EDIT THE TEMP DS  FOR BROWSING
         BAL   R14,ENTRY930       BROWSE TEMP DS USING CUSTOMIZE PANEL
         MVC   DDNAME,=CL8'PANPRINT'
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- PANPRINT
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060                      RETURN
FFF190   MVC   SCRHORZ,=CL8'PASSTHRU'  NO- TURN ON HORZ SCROLLING
         BAL   R14,ENTRY910
         SPFVREPL (MSG,8)      UPDATE MSG IN FUNCTION POOL FOR PXP0100
         LA    R1,SAVELT       NO-  PASS GENERIC ELT NAME
         LA    R2,EPA0100           ELT PROCESSOR
         LA    R3,EPA0100+4
         BAL   R14,ENTRY900
         ST    R15,ST900       SAVE R15 TILL AFTER RESET SCROLLING
         MVC   SCRHORZ,BLANKS
         BAL   R14,ENTRY910
         MVI   MOREFLAG,C'0'      TURN OFF MORE CMDS FLAG
         L     R15,ST900       RC FROM ELT PROCESSOR (PXP0100)
         C     R15,=F'8'       CHECK PXP0100 RC:0=LAST (ONLY) LINE CMD
*                                 4=ADDITIONAL LINE CMDS,8=END
         BL    GGG190          GO TURN ON MORE FLAG, DO FUNCTION
*        SPFVCOPY (ZTDSELS,4)   (TO PROCESS LINES AFTER END KEY)
*        CLC   ZTDSELS,=F'0'
*        BH    GGG190              MUST STILL PROCESS SOME LINE CMDS
***      MVC   MSG,=CL8'PXP000'        BLANK MESSAGE
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060                  RETURN
         SPACE 1
GGG190   MVI   MOREFLAG,C'1'   TURN ON MORE CMDS FLAG
         MVC   PANXPARM+2(100),BLANKS
         SPFVCOPY (PXPSEL,2,PXPTABLE,8,PXPQNAM,25)
         CLI   PXPSEL,C'S'
         BE    ENTRYERR           DISALLOW NESTED TABLE DISPLAYS
         MVC   ZCMD(2),PXPSEL      PLACE SEL CODE IN ZMD
         MVC   QNAM(25),PXPQNAM   PLACE QUALIF NAME IN PANECC2
         MVC   PXPGRP,GRP       PLACE GRP IN DIALOG VARIABLE
         MVC   PXPELT,ELT       PLACE ELT IN DIALOG VARIABLE
         MVC   PXPTYPE,TYPE     PLACE TYPE IN DIALOG VARIABLE
         MVC   PXPSTAT,STAT     PLACE STAT IN DIALOG VARIABLE
         MVC   PXPMODE,MODE     PLACE MODE IN DIALOG VARIABLE
         CALL  ISPLINK,(CONTROL,NONDISPL,ENTER),VL
         BAL   R14,ENTRY950     SAVE DIRECTORY ENVIRONMENT
*        MVC   PANEL,=CL8'PXP0000'    MAIN PANEL ID
         MVC   CURSOR,BLANKS           BLANK CURSOR
         MVC   MSG,=CL8'PXP000'        BLANK OUT MSG
         MVI   ERRFLAG,C'1'       PRESET ERR FLAG
         BAL   R14,ENTRY927 SIMULATE DISPLAY OF MAIN PANEL FOR ERR PROC
         MVI   ERRFLAG,C'0'  IF RETURNS HERE THEN NO ERROR.
         BAL   R14,ENTRY951 RESTORE DIRECTORY ENVIRONMENT
         B     BBB060               GO PROCESS
*----------------------------------------------------------------------
*        LIBRARY INFORMATION
*----------------------------------------------------------------------
KKK190   EQU   *
         MVC   TEMPQUAL,=CL5'TEMP3'
         MVC   DDNAME,=CL8'EZTIN'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR EZTIN)
         MVC   DDNAME,=CL8'PANPRINT'
         MVC   TEMPQUAL,=CL5'TEMP4'
         MVC   SPFDISP(4),=CL4'(MOD'  MUST MOD ON TO PANPRINT
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANPRINT)
         MVC   SPFDISP(4),=CL4'(NEW'   RESET
         MVC   TEMPQUAL,=CL5'TEMP1'    RESET
         PUT   PANEIN,PANECCI3    PUT PGM PE112T IN  EZTIN (LIB INFO)
         PUT   PANEIN,PANECCI4    EXEC EASYTRIEVE PGM PANETREV
         B     EEE190
*----------------------------------------------------------------------
*        CHANGE DIRECTORY INFO
*----------------------------------------------------------------------
QQQ190   EQU   *
         MVC   PXPFUNC,=CL10'CHANGED'
         LA    R1,PANECC2      PASS FULLY QUALIFIED NAME AS PARM
         LA    R2,EPA0004      CHANGE PROCESSOR
         LA    R3,EPA0004+4
         BAL   R14,ENTRY900
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        RENAME AN ELEMENT
*----------------------------------------------------------------------
ENTRY200 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         CLI   MOREFLAG,C'1'           RENAME FROM DIRECTORY LIST?
         BNE   AAA200                  NO
         MVC   PANEL,=CL8'PXP0000'    INIT PANEL
         MVC   CURSOR,=CL8'PXPNEWNM'  SET CURSOR
         BAL   R14,ENTRY927            DISPALY PRIMARY MENU
AAA200   BAL   R14,ENTRY960       ENQ THE ELT
         BAL   R14,ENTRY920            ALLOC FILES
         BAL   R14,ENTRY922            ALLOC PRINT FILE
         MVC   PANXPARM+2(8),=C'%RENAME '
         MVC   PANXPARM+10(34),PANECC2
         MVI   PANXPARM+44,C','
         MVC   PANXPARM+45(8),PXPNEWGP
         MVI   PANXPARM+53,C'.'
         MVC   PANXPARM+54(8),PXPNEWNM
         MVC   PXPFUNC,=CL10'RENAMED' ELT RENAME
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         CLC   PXPNEWGP,BLANKS         WAS A NEW GRP SPECIFIED?
         BE    WWW200                             NO
         MVC   GRP,PXPNEWGP                      YES
WWW200   CLC   PXPNEWNM,BLANKS         WAS A NEW NAME SPECIFIED?
         BE    XXX200                             NO
         MVC   ELT,PXPNEWNM                      YES
XXX200   BAL   R14,ENTRY980   DELETE OLD TBL ENTRY & ADD NEW ONE
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        FETCH AN ELEMENT
*----------------------------------------------------------------------
ENTRY210 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         BAL   R14,ENTRY920            ALLOC FILES - MOVE IN PANX DSN
         BAL   R14,ENTRY922            ALLOC PRINT FILE
         MVC   PXPFUNC,=CL10'FETCHED'   ELT FETCHED
         MVC   SAVDSN2,SAVDSN       SAVE SAVDSN
         MVC   PANEL,=CL8'PXP0001'      FETCH/ADD/INPUT PANEL
         MVC   CURSOR,=CL8'DSN2'
         BAL   R14,ENTRY927             DISPLAY PANEL
         BAL   R14,ENTRY960             ENQ THE ELT
         MVC   WORKDSN,DSN2             PROCESS SEQ/PDS FILE NAME
         BAL   R14,ENTRY928             CHECK DSN
         MVC   PANXPARM+2(6),=C'%FETCH'
         CLI   PXPEXPND,C'Y'             EXPAND EMBEDDED INCLUDES
         BNE   AAA210
         MVC   PANXPARM+8(3),=C',E '
AAA210   MVC   PANXPARM+11(34),PANECC2
         MVI   PANXPARM+45,C','
         MVC   DSN2DD,=CL8'SEQOUT'
         MVC   PANXPARM+46(7),=C'SEQOUT*'
         CLI   MEMB,C' '                NO MEMBER NAME - SEQOUT DD
         BE    BBB210
         MVC   DSN2DD,=CL8'PDSOUT'
         MVC   PANXPARM+46(8),=C'PDSOUT*.'
         MVC   PANXPARM+54(8),MEMB
BBB210   BAL   R14,ENTRY923       ALLOC PDSOUT/SEQOUT FILE
         MVC   SAVDSN,SAVDSN2   RESET THE DSN IF EDIT/BROWSE, COPY/XFER
*                         INPUT, FETCH AND ADD REINVOKED ON 'S' OPTION
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         BAL   R14,ENTRY970             DEQ THE ELT
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        ADD ELEMENT
*----------------------------------------------------------------------
ENTRY220 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         BAL   R14,ENTRY920            ALLOC FILES - MOVE IN PANX DSN
         BAL   R14,ENTRY922            ALLOC PRINT FILE
         MVC   PXPFUNC,=CL10'ADDED'   ELT ADDED
         MVC   PANEL,=CL8'PXP0001'      FETCH/ADD/INPUT PANEL
         MVC   CURSOR,=CL8'DSN2'
         BAL   R14,ENTRY927             DISPLAY PANEL
         MVC   WORKDSN,DSN2             PROCESS SEQ/PDS FILE NAME
         BAL   R14,ENTRY928             CHECK DSN
         MVC   PANXPARM+2(4),=C'%ADD'
         CLI   PXPREPL,C'Y'             REPLACE CURRENT ELEMENT
         BNE   AAA220
         MVC   PANXPARM+6(3),=C',R '
AAA220   MVC   PANXPARM+9(34),PANECC2
         MVI   PANXPARM+43,C','
         MVC   DSN2DD,=CL8'SEQIN'
         MVC   PANXPARM+44(6),=C'SEQIN*'
         CLI   MEMB,C' '                NO MEMBER NAME - SEQIN DD
         BE    BBB220
         MVC   DSN2DD,=CL8'PDSIN'
         MVC   PANXPARM+44(7),=C'PDSIN*.'
         MVC   PANXPARM+51(8),MEMB
BBB220   BAL   R14,ENTRY923       ALLOC PDSIN/SEQIN FILE
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        PRINT
*----------------------------------------------------------------------
ENTRY270 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         BAL   R14,ENTRY920            ALLOC FILES
         SPFALLOC A,DD=PANPRINT,SYSOUT=PXPSSOUT,FREE='CLOSE'
         MVC   PANXPARM+2(9),=C'%PRINT,D '
         CLI   TYPE,C'C'     IS TYPE=CNTL  (PXPTYPE WAS MOVED TO TYPE)
         BNE   AAA270             NO
         MVC   PANXPARM+8(2),=C'  ' YS-BLANK OUT ,D
AAA270   MVC   PANXPARM+11(34),PANECC2
         MVC   PXPFUNC,=CL10'PRINTED' ELT PRINTED
         BAL   R14,ENTRY929       LINK TO PANEXEC USING PARM
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        COMPILE/LINK PROCEDURE
*----------------------------------------------------------------------
ENTRY290 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         LA    R2,EPA0005           CHANGE ELT
         LA    R3,EPA0005+4
         BAL   R14,ENTRY900
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        LIBRARY MAINTENANCE
*----------------------------------------------------------------------
ENTRY300 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         BAL   R14,ENTRY920        ALLOC FILES - FOR CREATE & FILEOPT
         BAL   R14,ENTRY922        ALLOC PRINT FILE
         CLI   ZUSER,C'C'          IS THIS TECH SERV OR OPERATIONS
         BNE   ZZZ300              NO
         LA    R2,EPA0007       YES-LIBRARY MAINTENANCE
         LA    R3,EPA0007+4
         BAL   R14,ENTRY900
EXIT300  BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
ZZZ300   MVC   MSG,=CL8'PXP007'        UNAUTHORIZED USER
         B     EXIT300
*----------------------------------------------------------------------
*        ZAP
*----------------------------------------------------------------------
ENTRY310 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         BAL   R14,ENTRY920            ALLOC FILES - MOVE IN PANX DSN
         BAL   R14,ENTRY922            ALLOC PRINT FILE
         MVC   DDNAME,=CL8'PANEIN'
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANEIN)
         MVC   DDNAME,=CL8'PANPRINT'
         MVC   TEMPQUAL,=CL5'TEMP3' (DONT USE TEMP2-IN USE BY DIRECTRY)
         BAL   R14,ENTRY921       ALLOC A TEMPORARY FILE (FOR PANPRINT)
         LA    R1,PANECC2      PASS FULLY QUALIFIED NAME AS PARM
         LA    R2,EPA0009           ZAP PROCESSING
         LA    R3,EPA0009+4
         BAL   R14,ENTRY900
         BAL   R14,ENTRY930       BROWSE TEMP DS USING CUSTOMIZE PANEL
         MVC   DDNAME,=CL8'PANPRINT'
         BAL   R14,ENTRY925       DELETE AND UNALLOC TEMP DS- PANPRINT
         MVC   TEMPQUAL,=CL5'TEMP1'
         BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
*----------------------------------------------------------------------
*        CLEAR PANEL FIELDS - 'X' OPTION
*----------------------------------------------------------------------
ENTRY320 DS    0H
         MVC   PXPGRP,BLANKS
         MVC   PXPELT,BLANKS
         MVC   PXPTYPE,BLANKS
         MVC   PXPSTAT,BLANKS
         MVC   PXPMODE,BLANKS
         MVI   ZCMD,C'S'          MOVE IN 'S' OPTION
         B     ENTRY060
*----------------------------------------------------------------------
*        GLOBAL FACILITIES
*----------------------------------------------------------------------
ENTRY330 DS    0H
         BAL   R14,ENTRY950            CONTROL SAVE
         CLI   ZUSER,C'C'          IS THIS TECH SERV OR OPERATIONS
         BNE   ZZZ330              NO
         LA    R2,EPA0010       YES-LIBRARY MAINTENANCE
         LA    R3,EPA0010+4
         BAL   R14,ENTRY900
EXIT330  BAL   R14,ENTRY951            CONTROL RESTORE
         B     ENTRY060
ZZZ330   MVC   MSG,=CL8'PXP007'        UNAUTHORIZED USER
         B     EXIT330
*----------------------------------------------------------------------
*        END OF JOB
*----------------------------------------------------------------------
GOBACK   LA    R3,LOADLIST             SUB-ROUTINE LIST
DELLOOP  ICM   R15,15,0(R3)            DID WE LOAD THIS ONE ?
         BZ    GOAAA                   NO - OK
         LA    R1,4(R3)                YES - POINT TO NAME
         DELETE  EPLOC=(R1)            DELETE IT
GOAAA    EQU   *
         LA    R3,12(R3)               BUMP TO NEXT SUB-ROUTINE
         CLI   0(R3),X'FF'             END OF SUBS ?
         BNE   DELLOOP                 NO - CHECK ALL
         LA    R15,0                   SET ZERO
         SPFEXIT ,                     SO LONG
*----------------------------------------------------------------------
*        INVOKE PROCESSING PROGRAM
*----------------------------------------------------------------------
ENTRY900 DS    0H
         ST    R14,ST900               SAVE R14
         ST    R1,ST1                  SAVE R1
         ICM   R15,15,0(R2)            PICK UP EPA FOR THIS SUB-ROUTINE
         BNZ   AAA900                  WE GOT IT
         LOAD  EPLOC=(R3)              LOAD IT, FIRST TIME WE USED IT
         LR    R15,R0                  COPY EPA TO R15
         ST    R15,0(R2)               SAVE EPA FOR NEXT TIME
AAA900   EQU   *
         L     R1,ST1                  PICK UP R1
         BALR  R14,R15                 BRANCH TO SUB-ROUTINE
         L     R14,ST900               PICK UP R14
         BR    R14                     RETURN TO WHERE WE CAME FROM
ST900    DC    F'-1'
ST1      DC    F'0'
*----------------------------------------------------------------------
*        SET PF KEYS FOR SCROLLING
*----------------------------------------------------------------------
ENTRY910 DS    0H
         ST    R14,ST910          SAVE R14
         SPFVPUT (SCRHORZ),SHARED
         L     R14,ST910               PICK UP R14
         BR    R14                     RETURN TO WHERE WE CAME FROM
ST910    DC    F'-1'
*----------------------------------------------------------------------
*        ALLOC COMMON FILES - PANESRL, PANELIB AND PANPRINT AND
*                MOVE IN PANEXEC ELEMENT NAME
*----------------------------------------------------------------------
ENTRY920 DS    0H
         ST    R14,ST920          SAVE R14
         SPFALLOC A,DD='PANESRL',DSN=FPANESRL,DISP='SHR'
         MVC   PANELIB,=CL8'PANELIB'  INIT
         MVC   PANELIB0,PANELIB       SET DDNAMES FOR DEFAULT PANX LIB
         MVC   PANELIB1,PANELIB       SET DDNAMES FOR DELETE
         MVC   PANELIB2,PANELIB                       TRANSFER
         MVC   PANELIB3,PANELIB                       DIRSTAT
         CLC   SAVDSN,FPANESRL    IS INPUT PANEXEC LIB=HRA.PANESRL
         BNE   AAA920                 NO
         MVC   PANELIB,=CL8'PANESRL'  YES
         MVC   PANELIB0,PANELIB       SET DDNAMES FOR DEFAULT PANX LIB
         MVC   PANELIB1,PANELIB       SET DDNAMES FOR DELETE
         MVC   PANELIB2,PANELIB                       TRANSFER
         MVC   PANELIB3,PANELIB                       DIRSTAT
         B     BBB920                 SKIP THE ALLOCATION
AAA920   SPFALLOC U,DD=PANELIB
         SPFALLOC A,DD=PANELIB,DSN=SAVDSN,DISP='SHR',VOL=PXPVOL,       +
               FREE=FREE
         MVC   FREE,=CL5'CLOSE'
BBB920   SPFALLOC U,DD=PANPRINT          UNALLOC PANPRINT -SEE ENTRY922
         CLI   MOREFLAG,C'1'     ARE WE IN MIDST OF ELT SEL;OPT='S'
         BE    EXIT920           YES- WE'LL GET NAME FROM PXPQNAM
         MVC   GRP,PXPGRP
         MVC   ELT,PXPELT
         MVC   TYPE,PXPTYPE
         MVC   STAT,PXPSTAT
         MVC   MODE,PXPMODE
EXIT920  L     R14,ST920          RESTORE
         BR    R14
ST920    DC    F'-1'
*----------------------------------------------------------------------
*        ALLOC A TEMP FILE
*----------------------------------------------------------------------
ENTRY921 DS    0H
         ST    R14,ST920          SAVE R14
         SPFTEMP   ,TEMPOUT,TEMPQUAL
         SPFALLOC U,DD=DDNAME
         SPFALLOC A,DD=DDNAME,DSN=TEMPOUT,DISP=SPFDISP,UNIT='3380',    +
               SPACE=SPACE,LRECL=LRECL,BLKSIZE=BLKSIZE,BUFNO=BUFNO  <==
         L     R14,ST920          RESTORE
         BR    R14
*----------------------------------------------------------------------
*        ALLOC A PRINT FILE
*----------------------------------------------------------------------
ENTRY922 DS    0H
         ST    R14,ST920          SAVE R14
         MVC   DUMMY,YES
         MVC   TERM,NO         DONT DISPLAY PANEXEC MSGS-ONLY SPF MSGS
         CLI   PXPDIAG,C'Y'       ARE DIAGNOSTICS REQUESTED
         BNE   AAA922                    NO
         MVC   DUMMY,NO                  YES
         MVC   TERM,=CL2'TS'      DISPLAY PANEXEC MSGS
AAA922   SPFALLOC A,DD=PANPRINT,TERM=TERM,FREE='CLOSE',DUMMY=DUMMY
         L     R14,ST920          RESTORE
         BR    R14
DUMMY    DC    CL3'NO'     TURN OFF DUMMY  ('YES' TO TURN ON DUMMY)
TERM     DC    CL2'TS'     TERM=TS         ('NO' TO TURN OFF TERM=TS)
*----------------------------------------------------------------------
*        ALLOC SEQIN/SEQOUT/PDSIN/PDSOUT FILES FOR FETCH/ADD/INPUT
*        ALLOC PANELIBO FILE FOR COPY/TRANSFER AND FORM 'TO' PANX NAME
*----------------------------------------------------------------------
ENTRY923 DS    0H
         ST    R14,ST920          SAVE R14
        SPFALLOC A,DD=DSN2DD,DSN=SAVDSN,DISP=DISP,VOL=VOL2,FREE=FREE
         MVC   FREE,=CL5'CLOSE'
         L     R14,ST920          RESTORE
         BR    R14
FREE     DC    CL5'CLOSE'
*----------------------------------------------------------------------
*        EDIT THE TEMP DS
*----------------------------------------------------------------------
ENTRY924 DS    0H
         ST    R14,ST920          SAVE R14
         SPFEDIT TEMPOUT,,,PANEL,IMAC
         L     R14,ST920          RESTORE
         BR    R14
*----------------------------------------------------------------------
*        DELETE AND UNALLOC DS
*----------------------------------------------------------------------
ENTRY925 DS    0H
         ST    R14,ST920          SAVE R14
         SPFALLOC U,DD=DDNAME,DISP='DELETE'  DEL & UNALLOC TEMP DS
         L     R14,ST920          RESTORE
         BR    R14
*----------------------------------------------------------------------
*        CLOSE  AND FREEPOOL PANEIN
*----------------------------------------------------------------------
ENTRY926 DS    0H
         ST    R14,ST920          SAVE R14
         CLOSE (PANEIN)
         FREEPOOL PANEIN
         L     R14,ST920          RESTORE
         BR    R14
*----------------------------------------------------------------------
*        DISPLAY PANEL
*----------------------------------------------------------------------
ENTRY927 DS    0H
         ST    R14,ST920          SAVE R14
         SPFSPLAY PANEL,MSG,CURSOR           ,,ENTRY060
         LTR   R15,R15                       .
         BZ    AAA927                        .
         BAL   R14,ENTRY951         RESTORE ENVIRON .
         B     ENTRY060                      .
AAA927   MVC   CURSOR,BLANKS
         L     R14,ST920          RESTORE
         BR    R14
*----------------------------------------------------------------------
*        PROCESS DSN
*----------------------------------------------------------------------
ENTRY928 DS    0H
         ST    R14,ST920          SAVE R14
         CLI   WORKDSN,X'7D'           NAME IN QUOTES ?
         BE    AAA928                  YES - REMOVE QUOTES
         MVC   SAVDSN,BLANKS         NO-CLEAR WORK FIELD
         MVC   SAVDSN(8),ZPREFIX       MOVE USER PROFIX
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET TO BLANK
         TRT   SAVDSN,SPFTRT           FIND END OF PREFIX
         MVI   0(R1),C'.'              ADD A DOT
         MVC   1(35,R1),WORKDSN        MOVE REST OF DSN
         B     BBB928
AAA928   MVC   SAVDSN(43),WORKDSN+1    SKIP QUOTE
         XC    SPFTRT,SPFTRT           CLEAR
         MVI   SPFTRT+C' ',X'FF'       SET FOR BLANK
         MVI   SPFTRT+X'7D',X'FF'      SET FOR QUOTE
         TRT   SAVDSN,SPFTRT           FIND END OF DSN
         BZ    BBB928                  FULL 44 BYTE NAME
         MVI   0(R1),C' '              REMOVE QUOTE
BBB928   L     R14,ST920          RESTORE
         BR    R14
*----------------------------------------------------------------------
*        LINK TO PANEXEC
*----------------------------------------------------------------------
ENTRY929 DS    0H
         ST    R14,ST929          SAVE R14
         LA    R1,PARMPTR      ADDR OF PANEXEC PARM
         LA    R2,EPA0999           PANEXEC PROCESSOR
         LA    R3,EPA0999+4
         BAL   R14,ENTRY900
*        LINK  EP=PANEXEC,PARAM=(PANXPARM),VL=1  EXEC PANEXEC WITH PARM
         CLI   PXPFUNC,C' '       IS THERE A MSG TO WRITE
         BE    EXIT929            NO
         LTR   R15,R15            RC FROM PANEXEC
         BNZ   ZZZ929            NG
         MVC   MSG,=CL8'PXP008'   FUNCTION SUCCESSFUL
         CLI   PXPLOG,C'Y'        LOGGING REQUESTED
         BNE   EXIT929            NO
         CALL  ISPLINK,(LOG,MSG),VL  YES
EXIT929  L     R14,ST929          RESTORE
         BR    R14
ZZZ929   MVC   MSG,=CL8'PXP009' FUNCTION NOT SUCCESSFUL
         B     EXIT929
ST929    DC    F'-1'
PARMPTR  DC    X'80',AL3(PANXPARM)
*----------------------------------------------------------------------
*      BROWSE THE TEMP DS
*----------------------------------------------------------------------
ENTRY930 DS    0H
         ST    R14,ST920          SAVE R14
         MVC   PANEL,=CL8'PXPBROBF'   USE CUSTOMIZED BROWSE PANEL
         SPFBRWSE  TEMPOUT,,,PANEL     BROWSE TEMP DS USING CUST PANEL
         L     R14,ST920          RESTORE
         BR    R14
*----------------------------------------------------------------------
*        SELECT CONTROL SAVE
*----------------------------------------------------------------------
ENTRY950 DS    0H
         ST    R14,ST950               SAVE R14
         CALL  ISPLINK,(CONTROL,DISPLAY,SAVE),VL
         L     R14,ST950
         BR    R14
ST950    DC    F'-1'
*----------------------------------------------------------------------
*        SELECT CONTROL RESTORE
*----------------------------------------------------------------------
ENTRY951 DS    0H
         ST    R14,ST951               SAVE R14
         CALL  ISPLINK,(CONTROL,DISPLAY,RESTORE),VL
         L     R14,ST951
         BR    R14
ST951    DC    F'-1'
*----------------------------------------------------------------------
*        ENQ ON THE ELEMENT
*----------------------------------------------------------------------
ENTRY960 DS    0H
         ST    R14,ST960
         MVC   ENQDSN,SAVDSN
         MVC   ENQQNAM,QNAM
         SPFENQ SPFPANX,,ENQDSN,ENQQNAM  (DSN,MBR)
         LTR   R15,R15
         BZ    EXIT960
         MVC   MSG,=CL8'PXP006'      ELEMENT IN USE
         MVC   PANEL,BLANKS
         B     ENTRY060    (TO DISPLAY MSG)
EXIT960  L     R14,ST960
         BR    R14
ST960    DC    F'-1'
*----------------------------------------------------------------------
*        DEQ ON THE ELEMENT
*----------------------------------------------------------------------
ENTRY970 DS    0H
         ST    R14,ST970
         SPFDEQ SPFPANX,,ENQDSN,ENQQNAM     DSN MBR
         L     R14,ST970
         BR    R14
ST970    DC    F'-1'
*----------------------------------------------------------------------
*        DELETE OLD TBL ENTRY & ADD NEW ONE (FOR MODE, PROMOTE, RENAME)
*----------------------------------------------------------------------
ENTRY980 DS    0H
         ST    R14,ST980
         ST    R15,SPFRC                SAVE THE RC
         BAL   R14,ENTRY970       DEQ THE ELT
         ICM   R15,15,SPFRC       RETRIEVE THE PANEXEC RC
         BNZ   EXIT980            NOT SUCCESSFUL (MODE,RENAME,PROMOTE)
         CLI   MOREFLAG,C'1'  YES-DELETE FROM ELEMENT LIST?
         BNE   EXIT980            NO
         CLI   COPYSAMF,C'1'  YES-COPY FUNCTION TO SAME FILE?
         BE    ZZZ980             YES - SKIP DELETE
         BH    EXIT980            COPY TO DIFFERENT FILE (FLAG=2)
         CALL  ISPLINK,(TBDELETE,PXPTABLE),VL  YES-CH SUCCES-DELETE ELT
ZZZ980   MVC   PXPQNAM,QNAM            YES - INSERT NEW TBL ENTRY
         MVC   PXPSEL,BLANKS
         SPFVREPL (PXPQNAM,25,PXPSEL,2)
         CALL  ISPLINK,(TBADD,PXPTABLE,,ORDER),VL
EXIT980  MVI   COPYSAMF,C'0'           RESET FLAG
         L     R14,ST980
         BR    R14
ST980    DC    F'-1'
*----------------------------------------------------------------------
*        STORAGE AREAS
*----------------------------------------------------------------------
RC       DC    F'0'
MINUS1   DC    F'-1'
SAVDIAG  DS    CL1
MOREFLAG DC    C'0'        MORE COMMANDS FLAG- SET BY RC OF PXP0100
COPYSAMF DC    C'0'  FLAG TO UPDATE TBL ON COPY FUNCTION
*  0=NOT COPY FUNCTION, 1=COPY TO SAME FILE, 2=COPY TO DIFFERENT FILE
OLDDSN   DC    CL44' '
TEMPOUT  DC    CL44' '          TEMPORARY DSN
PANECC1  DS    0CL80
         DC    CL13'%REMOVE,FL   '  FIRST REMOVE CONTROL STAT
PANELIB1 DS    CL8         PANELIB DDNAME (OR PANESRL FOR HRA.PANESRL)
         DC    CL2'*,'
PRTECTDD DS    CL8              PROTECT DDNAME FOR REMOVE
         DC    CL49'* '
         SPACE 1
NAMESKEL DC    C'*',CL8' ',C'.',CL8' ',C'/',CL4' ',C';'
PANECC2  DS    0CL80              SUBCOMMAND CONTROL CARD
PANELIB0 DC    CL8' '       DEFAULT PANELIB
         DC    C'*'
QNAM     DS   0CL25
GRP      DC    CL8' '
         DC    C'.'
ELT      DC    CL8' '
         DC    C'/'
TYPE     DC    CL4' '
         DC    C';'
STAT     DC    CL1' '
MODE     DC    CL1' '
         DC    CL55' '    EXTRA 9 BLANKS FOR   PUT PANEIN,PANECC2+9
         SPACE 1
PANECCT1 DC    CL14'%TRANSFER,FL  '
PANELIB2 DS    CL8         PANELIB DDNAME (OR PANESRL FOR HRA.PANESRL)
         DC    CL58'*,PANELIBO*'
         SPACE 1
PANECCT2 DC    CL80' '
PANECCD1 DC    CL13'%DIRSTAT,F   '
PANELIB3 DS    CL8         PANELIB DDNAME (OR PANESRL FOR HRA.PANESRL)
         DC    CL59'*,FILEA*'
         SPACE 1
INPUTCC  DC    CL80'%INPUT,S PANEIN*'
         SPACE 1
PANXPARM DC    H'100'               PANEIN PARM
         DC    CL100' '
         SPACE 1
PANECCI3 DC    CL80'%FETCH PANESRL*$EZTLIB.PE112T/CNTL;PA,EZTIN*'
PANECCI4 DC    CL80'%EXEC  PANESRL*$OS.PANETREV/;P'
         SPACE 1
PANECCE1 DC    CL80'%ADD,D '
         SPACE 1
PANEIN   DCB   DDNAME=PANEIN,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80
*              BLKSIZE=800
DDNAME   DS    CL8        USED TO ALLOC TEMP FILES (PANEIN,PANPRINT)
DSN2DD   DS    CL8          PDSOUT/SEQOUT/PDSIN/SEQIN/PANELIBO DNAME
PANELIB  DC    CL8'PANELIB'    INPUT PANEXEC LIBRARY
PANPRINT DC    CL8'PANPRINT'
IMAC     DC    CL8' '            EDIT INITIAL MACRO
TEMPQUAL DC    CL5'TEMP1'
SAVSEL   DC    CL2' '
SAVELT   DS    CL8
SPFDISP  DC    CL20'(NEW,CATLG,DELETE)'
SPACE    DC    CL20'(TRK,(02,03),RLSE)'
SPACETMP DC    CL20'(TRK,(02,03),RLSE)'
SPCFILEA DC    CL20'(CYL,(5,15),RLSE)'
LRECL    DC    H'0'         LRECL SET TO 80 FOR EDIT
BLKSIZE  DC    H'21280'
BUFNO    DC    FL1'5'
FPANESRL DC    CL44'HRA.PANESRL'
SAVDSN   DC    CL44' '
SAVDSN2  DC    CL44' '   SAVDSN FOR REINVOKING EDIT/BROWSE
WORKDSN  DC    CL44' '
COPYDSN  DS    CL44     FOR %COPY, IF FROM AND TO PANX LIB ARE DIFFER
ENQDSN   DS    CL44     FOR DEQ (TO USE SAME DSN AS ENQ)
ENQQNAM  DS    CL25     FOR DEQ (TO USE SAME QNAM AS ENQ)
SPFPANX  DC    CL8'SPFPANX'  QNAME FOR ENQ/DEQ
ERRFLAG  DC    CL1'0'   ERROR FLAG-CHKED WHEN MULTIPLE CMDS ON 'S' OPT
*              0=NO ERRORS ON NONDISPL PRIMARY MENU,1=ERRORS
*OREAD   DC    CL8'NOREAD'   FOR TBBOTTOM AND TBSKIP SERVICES
         DC    X'FF'
*----------------------------------------------------------------------
LOADLIST DS    0F
EPA0004  DC    F'0',CL8'PXP0004'       CHANGE
EPA0005  DC    F'0',CL8'PXP0005'       COMPILE/LINK
EPA0007  DC    F'0',CL8'PXP0007'       LIBRARY MAINTENANCE
EPA0009  DC    F'0',CL8'PXP0009'       ZAP
EPA0010  DC    F'0',CL8'PXP0010'       GLOBAL FACILITIES
EPA0100  DC    F'0',CL8'PXP0100'       ELT PROCESSOR
EPA0999  DC    F'0',CL8'PANEXEC'       PANEXEC PROCESSOR
         DC    F'-1'                   END FLG
*----------------------------------------------------------------------
*        SELECT TABLE CODE
SELTABLE DS    0F
YZA      DC    C'  ',H'0',A(ENTRY220) ADD
YZB      DC    C'  ',H'0',A(ENTRY100) BROWSE
YZC      DC    C'  ',H'0',A(ENTRY140) COPY
YZCH     DC    C'  ',H'0',A(ENTRY180) CHANGE
YZCL     DC    C'  ',H'0',A(ENTRY290) COMPILE/LINK
YZD      DC    C'  ',H'0',A(ENTRY110) DELETE
YZE      DC    C'  ',H'0',A(ENTRY100) EDIT
YZF      DC    C'  ',H'0',A(ENTRY210) FETCH
YZG      DC    C'  ',H'0',A(ENTRY330) PANEXEC GLOBAL FACILITIES
YZI      DC    C'  ',H'0',A(ENTRY190) LIBRARY INFO
YZIN     DC    C'  ',H'0',A(ENTRY170) INPUT PROC
YZL      DC    C'  ',H'0',A(ENTRY300) LIBRARY MAINTENAC
YZM      DC    C'  ',H'0',A(ENTRY150) MODIFY MODE
YZP      DC    C'  ',H'0',A(ENTRY270) PRINT
YZPM     DC    C'  ',H'0',A(ENTRY160) PROMOTE
YZR      DC    C'  ',H'0',A(ENTRY200) RENAME
YZS      DC    C'  ',H'0',A(ENTRY190) DISPLAY DIRECTORY
YZT      DC    C'  ',H'0',A(ENTRY130) TRANSFER
YZX      DC    C'  ',H'0',A(ENTRY320) CLEAR PANEL FIELDS
YZZ      DC    C'  ',H'0',A(ENTRY310) ZAP
         DC    F'-1'
*----------------------------------------------------------------------
SELDSECT DSECT
SELOPT   DS    CL2
         DS    H
SELADDR  DS    F
SELEND   EQU   *
*----------------------------------------------------------------------
PXP0000  CSECT
SELLL    EQU   SELEND-SELOPT
SPFMACS  SPFCMDS TABLES=YES,DSCB1=NO
         END
