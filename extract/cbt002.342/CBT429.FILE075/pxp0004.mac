*----------------------------------------------------------------------
*        DISPLAY AND CHANGE THE DIRECTORY OF AN ELEMENT
*----------------------------------------------------------------------
         PRINT NOGEN
PXP0004  #SLC R12,R11
         LR   R3,R1      SAVE PARM -FULLY QUALIFIED PANX NAME
*----------------------------------------------------------------------
*        DEFINE VARIABLES
*----------------------------------------------------------------------
         SPFVDEF 2,(PXPSRCCH)
         SPFVDEF 3,(PXPVLTCH,PXPSRLCH)
         SPFVDEF 4,(PXPUSECH,PXPCORCH,PXPCOPCH)
         SPFVDEF 8,(PXPACTCH,PXPDSTCH,PXPSYSCH,PXPSRDCH,PXPSSICH,      +
               PXPALSCH)
         SPFVDEF 10,(PXPSRNCH)
         SPFVDEF 20,(PXPOWNCH)
         SPFVDEF 44,(PXPDSCCH)
         SPFVCOPY (PXPLOG,1)
         SPFVCOPY (MSG,8),DC=NO
         USING DIRENTRY,R5     SET UP R5 AS BASE FOR DIRECTORY RECORD
         OPEN  (FILEA)              OPEN FILEA  (ALLOCATED BY PXP0000)
*----------------------------------------------------------------------
*        READ FILEA FOR CHANGE STATS
*----------------------------------------------------------------------
*NTRY040 GET   FILEA,DIRENTRY GET A DIRECTORY REC
ENTRY040 GET   FILEA          GET A DIRECTORY REC USING DIRENTRY DSECT
         LR    R5,R1
         CLI   CIRTYPE,C'E'       IS THIS AN EIR RECORD
         BNE   ENTRY040
         MVC   PXPSRCCH,SOURCECL
         MVC   PXPVLTCH,EDIT4
         ED    PXPVLTCH,VIOLATED
         OI    PXPVLTCH+2,X'F0'
         MVC   PXPSRLCH,EDIT4
         ED    PXPSRLCH,SOURCELV
         OI    PXPSRLCH+2,X'F0'
         MVC   PXPUSECH,USER
         MVC   PXPCORCH,CORE
         MVC   PXPCOPCH,EDIT4
         ED    PXPCOPCH,COPIES+1
         OI    PXPCOPCH+3,X'F0'
         MVC   PXPACTCH,ACCOUNT
         MVC   PXPDSTCH,DESTID
         MVC   PXPSYSCH,SYSTEM
         MVC   PXPSRDCH,SOURCEDT
         UNPK  HEXWRK,SSI(5)      CONVERT BINARY FIELD-ADD PAD BYTE
         MVC   PXPSSICH,HEXWRK
         TR    PXPSSICH,HEXTAB
         MVC   PXPALSCH,ALIAS
         MVC   PXPSRNCH,SOURCENM
         MVC   PXPOWNCH,OWNER
         MVC   PXPDSCCH,DESCRIPT
ZZZ040   EQU   *
         CLOSE  (FILEA)          CLOSE FILES
         FREEPOOL FILEA           FREE THE BUFFERS
         SPFALLOC U,DD='FILEA',DISP='DELETE' UNALLOC & DELETE FILEA
*        DISPLAY CHANGE PANEL
         SPFSPLAY 'PXP0004',MSG,,,EOJ
         OPEN  (PANEIN,(OUTPUT))       ALOCATED BY PXP0000
         MVC   PANEINCC(8),=C'%CHANGE '
         MVC   PANEINCC+8(34),0(R3)    FULLY QUALIFIED PANX NAME
         BAL   R4,ENTRY020
         MVC   PANEINCC(44),BLANKS
         MVC   PANEINCC(11),CHCARD10
         MVC   PANEINCC+11(2),PXPSRCCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD1
         MVC   PANEINCC+11(3),PXPVLTCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD11
         MVC   PANEINCC+11(3),PXPSRLCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD3
         MVC   PANEINCC+11(4),PXPUSECH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD7
         MVC   PANEINCC+11(4),PXPCORCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD13
         MVC   PANEINCC+11(4),PXPCOPCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD4
         MVC   PANEINCC+11(8),PXPACTCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD6
         MVC   PANEINCC+11(8),PXPDSTCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD8
         MVC   PANEINCC+11(8),PXPSYSCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD9
         MVC   PANEINCC+11(10),PXPSRNCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD12
         MVC   PANEINCC+11(8),PXPSRDCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD14
         MVC   PANEINCC+11(8),PXPSSICH
         BAL   R4,ENTRY020
         CLC   PXPALSCH,BLANKS
         BE    SKPALIAS
         MVC   PANEINCC(11),CHCARD15
         MVC   PANEINCC+11(8),PXPALSCH
         BAL   R4,ENTRY020
SKPALIAS MVC   PANEINCC(11),CHCARD2
         MVC   PANEINCC+11(20),PXPOWNCH
         BAL   R4,ENTRY020
         MVC   PANEINCC(11),CHCARD5
         MVC   PANEINCC+11(44),PXPDSCCH
         BAL   R4,ENTRY020
         CLOSE  (PANEIN)         CLOSE FILES
         FREEPOOL PANEIN          FREE THE BUFFERS
         LINK  EP=PANEXEC,PARAM=(CHPXPARM),VL=1
         LTR   R15,R15            SUCCESSFUL
         BNZ   CHNGFAIL
         MVC   MSG,=CL8'PXP008'
         CLI   PXPLOG,C'Y'        LOGGING REQUESTED
         BNE   EOJ                NO
         CALL  ISPLINK,(LOG,MSG),VL   YES-LOG THE CHANGE
         B     EOJ
CHNGFAIL MVC   MSG,=CL8'PXP009'
EOJ      EQU   *
         SPFVREPL (MSG,8)
         SPFALLOC U,DD='PANEIN',DISP='DELETE' UNALLOC & DELETE
         #RTN
*----------------------------------------------------------------------
*        PUT THE PANEIN REC
*----------------------------------------------------------------------
ENTRY020 PUT   PANEIN,PANEINCC
         BR    R4
*----------------------------------------------------------------------
*        STORAGE AREAS
*----------------------------------------------------------------------
PANEINCC DC    CL80' '
CHCARD1  DC    CL11'VIOLATED,'
CHCARD2  DC    CL11'OWNER,'
CHCARD3  DC    CL11'USER,'
CHCARD4  DC    CL11'ACCOUNT,'
CHCARD5  DC    CL11'DESCRIPTIO,'
CHCARD6  DC    CL11'DESTID,'
CHCARD7  DC    CL11'CORE,'
CHCARD8  DC    CL11'SYSTEM,'
CHCARD9  DC    CL11'SOURCENAME,'
CHCARD10 DC    CL11'SOURCECLSS,'
CHCARD11 DC    CL11'SOURCELVL,'
CHCARD12 DC    CL11'SOURCEDATE,'
CHCARD13 DC    CL11'COPIES,'
CHCARD14 DC    CL11'SETSSI,'
CHCARD15 DC    CL11'ALIAS,'
CHPXPARM DC    H'20'
         DC    CL20'%INPUT,S PANEIN*'
EDIT4    DC    X'40202021'
HEXTAB   DC    240X'00',C'0123456789ABCDEF'
HEXWRK   DS    CL9
SMSG     DS    CL24
LMSG     DS    CL72
*----------------------------------------------------------------------
FILEA    DCB   DDNAME=FILEA,DSORG=PS,MACRF=GL,EODAD=ZZZ040,RECFM=VB
PANEIN   DCB   DDNAME=PANEIN,DSORG=PS,MACRF=PM
SPFMACS  SPFCMDS DSCB1=NO,ENQDEQ=NO,TABLES=NO
         DSECT
         PXPIRFLD                , DIRSTAT DIRECTORY RECORD
         END
