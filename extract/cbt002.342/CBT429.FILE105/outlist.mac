PROC 0 DEBUG
/* REFER: ABL.CMDPROC(OUTLIST) 9/88    MARK DIEHL-HERCULES ABL
/* DOC  : REPLACEMENT FOR ISPF/PDF'S (V2.2) 3.8 OUTLIST UTILITY
/* NOTE : MSG MUST BE TURNED ON IN CONTROL STATEMENT FOR STATUS TO WORK
/* NOTE : USING DEBUG MODE WILL OFFSET SYSOUTTRAP COUNTERS BY PLUS ONE
CONTROL MAIN MSG NOLIST
IF &DEBUG = DEBUG THEN +
   CONTROL MAIN MSG LIST SYMLIST CONLIST PROMPT
ISPEXEC CONTROL ERRORS RETURN
ATTN DO
  WRITE TERMINATING PER YOUR REQUEST
  EXIT CODE(0)
 END
/*                                                                    */
GETJOBS: +
SET SYSOUTTRAP = 100       /*  SET MAX TRAPPABLE JOB LIMIT            */
STATUS
SET UPPER = &SYSOUTLINE       /* SAVE # LINES FOR TABLE BUILD LOOP    */
SET SYSOUTTRAP = 0            /* TURN OFF TRAPPING MECHANISM          */
SET CTR = 1
/*                                                                    */
IF &DEBUG = DEBUG THEN SET CTR = 2   /* DEBUG MODE ADDS AN EXTRA LINE */
/* (CONTAINING LITERALLY "STATUS") TO THE SYSOUTTRAP. THE ABOVE LINE  */
/* OF CODE SKIPS OVER THIS TRAP LINE IF DEBUG MODE IS INVOKED.        */
/* NEXT, SEE IF STATUS SAYS "NO JOBS FOUND"                           */
/*                                                                    */
SET EVALLINE = &&SYSOUTLINE&CTR  /* PREPARE TO EVALUATE LINE          */
IF &SYSINDEX(NO JOBS FOUND,&STR(&EVALLINE)) = 0 THEN GOTO GOTJOBS
 ISPEXEC SETMSG MSG(JOBQ013) /*DON'T SHOW REFRESH MSG IF NO JOBS FOUND*/
 WRITE
 WRITE
 WRITE     NO JOBS FOUND FOR USER &SYSUID
 WRITE
 EXIT CODE(0)
/*********************************************************************/
/* USER HAS JOBS ON SYSTEM ... CREATE AN IN-CORE ISPF JOB TABLE      */
/*                                                                   */
GOTJOBS: +
ISPEXEC TBCREATE JOBTABLE NAMES(JOBID) NOWRITE SHARE
DO WHILE &CTR LE &UPPER                /* BUILD TABLE ENTRIES (ROWS) */
 SET JOBXX = &&SYSOUTLINE&CTR
 SET MAXCHAR = &LENGTH(&JOBXX) /* ALLOW FOR VARIABLE LENGTH RECORDS, */
                               /* JOBS MAY BE IN DIFFERENT JES STATES*/
 /*                                                                  */
 /* FIND WHERE "JOB" BEGINS THEN BUMP BY 4 TO SKIP "JOB_" TO POSITION*/
 /* TO ACTUAL JOB ID (JOBNAME(JOB#) -TO BE USED IN TABLE DISPLAY).   */
 /*                                                                  */
 SET JOBINDEX = 4 + (&SYSINDEX(JOB,&STR(&JOBXX)))
 /*                                                                  */
 SET JOBID = &SUBSTR(&JOBINDEX:&MAXCHAR,&JOBXX)
 SET CMDID = &SUBSTR(&JOBINDEX:32,&JOBXX) /*STRIP FOR TSO COMMAND USE*/
 ISPEXEC TBADD JOBTABLE
 SET &CTR = &CTR + 1
END
/*    END OF ISPF TABLE BUILD LOOP ... GO DISPLAY TABLE TO USER      */
/*********************************************************************/
SET &RC = 0
 ISPEXEC TBTOP JOBTABLE
DO WHILE &RC LT 8         /* LOOP TILL USER ENTERS END (PF3) COMMAND */
 ISPEXEC TBDISPL JOBTABLE PANEL(JOBQMAIN) AUTOSEL(NO)
 SET &RC = &LASTCC
 IF &RC EQ 20 THEN DO    /* SEVERE ERROR DURING TABLE PROCESSING     */
  WRITE
  WRITE ERROR DURING PROCESSING ... CONTACT DATA PROCESSING FOR HELP
  WRITE
  /* SEND A MESSAGE TO OPER CONSOLE FOR TRACKING PURPOSES            */
  SE '#ABLUTIL: OUTLIST (3.8) TABLE PROCESSING ERROR',CN(01)
  GOTO EXIT
 END
 SET &PROCFLAG = ON
 DO WHILE &PROCFLAG = ON
  IF &ZCMD = CANCEL THEN GOTO EXIT
  IF &ZCMD = END    THEN GOTO EXIT
  IF &ZCMD = REF    THEN GOTO REFRESH
  IF &ZCMD = ST     THEN GOTO REFRESH
  /*                                                                 */
  /*      PROCESS JOBS SELECTED BY USER                              */
  /*                                                                 */
  SET &CMDID = &SUBSTR(1:18,&JOBID) /* SAVE JOBID FOR TSO COMMAND USE*/
  SET OUTID = &SUBSTR(10:17,&JOBID) /*SAVE FOR TSO OUTLIST BROWSE DSN*/
  CONTROL NOMSG      /* DON'T POLLUTE SCREEN WITH TSO CMD OUTPUT     */
  /*                                                                 */
  IF &OPTION = O THEN DO  /* OPTION O NOT AVAILABLE AS PUBLIC OPTION */
   FREE FI(OUT)    /* PRE-ALLOC OUT DSN TO FORCE ASA PRESERVATION    */
   ALLOC FI(OUT) DA('&SYSUID..&OUTID..OUTLIST') NEW CATALOG TR +
     SP(10,5) BLKSIZE(23275) LRECL(133) RECFM(F B A) VO(SYS001)
   IF &LASTCC NE 0 THEN DO
    WRITE
    WRITE ALLOCATION OF ONLINE OUTLIST DATASET FAILED ...
    WRITE CALL MARK DIEHL ON X5140 FOR ASSISTANCE
    WRITE
   END
   OUTPUT &CMDID PRINT(&OUTID)              /* STORE JOB ONLINE      */
   FREE DA(&OUTID..OUTLIST)
   SET ACTION = SEE &OUTID..OUTLIST
   ISPEXEC TBPUT JOBTABLE SAVE(ACTION)
   ISPEXEC TBTOP JOBTABLE
  END
  /*                                                                 */
  IF &OPTION = D THEN DO
   /* PRESERVE JOB OUTPUT IF JOB IS CURRENTLY EXECUTING              */
   /*                                                                */
   /* TRUNCATE JOBID TO THE GENERAL RANGE WHERE "EXECUTING" APPEARS, */
   /* ELSE &SYSINDEX IN NEXT STATEMENT WILL ABEND IKJ79002I  BECAUSE */
   /* OF THE COMMA PRESENT WHEN A JOB IS IN HOLD STATUS. IF ANYONE   */
   /* KNOWS OF A BETTER WAY TO HANDLE THIS, PLEASE CALL ME.          */
   /*                                                                */
   SET TRUNC = &SUBSTR(19:27,&JOBID)
   IF &SYSINDEX(EXECUTING,&TRUNC,19) NE 0 THEN DO
    CANCEL &CMDID     /* CANCEL EXECUTING JOB WITHOUT PURGE OPERAND  */
    SET ACTION = CANCELLED. ON OUTPUT Q
    GOTO TBPUT
   END
   CANCEL &CMDID PURGE     /*   DELETE JOB'S OUTPUT FROM SYSTEM      */
   SET ACTION = DELETED    /* TELL USER OF "ACTION" JUST PERFORMED   */
   TBPUT: +
    ISPEXEC TBPUT JOBTABLE SAVE(ACTION)
    ISPEXEC TBTOP JOBTABLE
  END
  /*                                                                 */
  IF &OPTION = P THEN DO
   OUTPUT &CMDID NEWCLASS(A) CLASS(Q)  /* RE-Q JOB TO PRINT CLASS A  */
   SET ACTION = PRINT SCHEDULED
   ISPEXEC TBPUT JOBTABLE SAVE(ACTION)
   ISPEXEC TBTOP JOBTABLE
  END
  /*                                                                 */
  IF &OPTION = S THEN DO
   DELETE '&SYSUID..&OUTID..OUTLIST'
   FREE FI(OUT)
   ALLOC FI(OUT) DA('&SYSUID..&OUTID..OUTLIST') NEW CATALOG TR +
     SP(10,5) BLKSIZE(23275) LRECL(133) RECFM(F B A) VO(SYS001)
   IF &LASTCC NE 0 THEN DO
    SET &RC = 8             /* FORCE EXIT FROM TABLE LOOP */
    SET PROCFLAG = OFF      /* FORCE EXIT FROM TABLE LOOP */
    WRITE
    WRITE ALLOCATION OF ONLINE OUTLIST DATASET FAILED ...
    WRITE CALL MARK DIEHL ON X5140 FOR ASSISTANCE
    WRITE
    ISPEXEC TBEND JOBTABLE  /* CLOSE TABLE, EXIT OUTLIST PROCESSOR */
    EXIT CODE (0)
   END
   SET ACTION = VIEWED     /* TELL USER OF "ACTION" JUST PERFORMED */
   OUTPUT &CMDID PRINT('&SYSUID..&OUTID..OUTLIST') HOLD KEEP
   ISPEXEC CONTROL DISPLAY SAVE     /* SAVE ISPF ENVIRONMENT       */
    ISPEXEC BROWSE DATASET('&SYSUID..&OUTID..OUTLIST')
    IF &LASTCC NE 0 THEN +
     SET ACTION = NOT A VIEWABLE JOB  /* JOB NOT IS MSGCLASS=Q     */
    ISPEXEC CONTROL DISPLAY RESTORE
    ISPEXEC TBPUT JOBTABLE SAVE(ACTION)
    ISPEXEC TBTOP JOBTABLE
    DELETE '&SYSUID..&OUTID..OUTLIST'
  END
  IF &ZTDSELS GT 1 THEN +
    ISPEXEC TBDISPL JOBTABLE  /* GET NEXT ROW WITH COMMAND PENDING  */
   ELSE SET &PROCFLAG = OFF
 END
 ISPEXEC TBSKIP JOBTABLE NUMBER(&ZTDTOP) /* RESHOW ORIGINAL TABLE   */
END                    /* END OF LOOP:   "DO WHILE &PROCFLAG = ON"  */
IF &ZCMD = CANCEL THEN GOTO EXIT
IF &ZCMD = END    THEN GOTO EXIT
IF &ZCMD = ST     THEN GOTO REFRESH
END                     /* END OF LOOP:  "DO WHILE RC LT 8"         */
/*                                                                  */
EXIT: +
 ISPEXEC TBEND JOBTABLE
 EXIT CODE(0)
/*  USER HAS ENTERED "ST" ON COMMAND LINE, REFRESH JOB DISPLAY      */
REFRESH: +
 ISPEXEC SETMSG MSG(JOBQ012)
 ISPEXEC TBEND JOBTABLE
 CONTROL MSG
 GOTO GETJOBS
