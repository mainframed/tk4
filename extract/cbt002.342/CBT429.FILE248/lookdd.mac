********************************************************************
*        SCAN TSO TIOT TABLE FOR MEMBER IN GIVEN DD CONCATENTATION *
*        AND LIST DATASETS WHICH MEMBER WAS FOUND IN FOR THAT DD   *
*        BOYSEN/AMDAHL   JAN 94                                    *
*        SYNTAX:  LISTDD 'DDNAME' 'MEMBERNAME'                     *
*        EX:      LISTDD SYSPROC AAABBB                            *
*                 SEARCH DD SYSPROC FOR MEMBER AAABBB              *
********************************************************************
LOOKDD   CSECT
R15      EQU   15
R14      EQU   14
R13      EQU   13
R12      EQU   12                       BASE REGISTER
R11      EQU   11
R10      EQU   10
R9       EQU   9
R8       EQU   8
R7       EQU   7
R6       EQU   6
R5       EQU   5
R4       EQU   4
R3       EQU   3
R2       EQU   2
R1       EQU   1
R0       EQU   0
LOOKDD   CSECT
         STM   R14,R12,12(R13)          SAVE CALLERS REGS
         BASR  R12,0                    ESTABLISH ADDRESSABILITY
         USING *,12                     BASE OF EXECUTING PROGRAM
         ST    R13,SAVEAREA+4           CALLERS SAVE AREA
         LA    R15,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
         ST    R15,8(,R13)              EXECUTING PROGRAMS SAVEAREA
         LA    R13,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
         LR    R3,R1                    SAVE CPPL ADDRESS
         ST    R3,CPPLADDR              SAVE CPPL ADDRESS FOR SVC99
         LA    R1,ERASE                 POINT TO ERASE CNTL CHARS
         LA    R0,L'ERASE               GET THE LENGTH
         TPUT  (1),(0),FULLSCR          ERASE THE 3270 SCREEN
         USING CPPL,R3
         GETMAIN R,LV=WORKLEN           GET WORK AREA
         LR    R10,R1                   SAVE AREA ADDR IN R10
         USING WORK,R10
         LA    R5,PARSPARM
         USING PPL,R5
         XC    PARSE(PARSELEN),PARSE    INITIALIZE PPL
         MVC   PPLUPT,CPPLUPT           UPT
         MVC   PPLECT,CPPLECT           ECT
         MVC   PPLCBUF,CPPLCBUF         CIB
         DROP  R3
         LA    R1,PARSECB               ECB
         ST    R1,PPLECB
         L     R1,=A(PCL)               PCL
         ST    R1,PPLPCL
         LA    R1,PARSANS               PDL
         ST    R1,PPLANS
         CALLTSSR EP=IKJPARS,MF=(E,(R5))    CALL PARSE
         DROP  R5
         L     R10,PARSANS              OK ?
         USING PDL,R10
         LTR   R10,R10
         BP    CHCKPARM                 YES, CONTINUE
         TPUT  BADPARSE,17              PUT OUT BAD PARSE MSG
         B     GOBACK
*
CHCKPARM DS    0H
         GTEDASET ABC,CPPLPTR=CPPLADDR  SET POINTERS
         L     R3,MEMBPTR               LOAD PTR TO PASSED MEMBER NAME
         LA    R2,MEMBNAME
         LH    R1,MEMBLEN               LOAD LENGTH OF PARM
         BCTR  R1,0
         EX    R1,IKJMVC                MOVE THE PARM
         L     R3,DDNPTR                LOAD PTR TO PASSED DDN
         LA    R2,DDNAME
         LH    R1,DDNLEN                LOAD LENGTH OF PARM
         BCTR  R1,0
         EX    R1,IKJMVC                MOVE THE PARM
         USING TIOENTRY,R10
         USING INFMJFCB,R5
         L     R10,PSATOLD-PSA          CURRENT TCB ADDR
         L     R10,TCBTIO-TCB(,R10)     OUR TIOT ADDR
         LA    R10,TIOENTRY-TIOT(,R10)  1ST DD SEGMENT IN TIOT
LOOP1    CLC   DDNAME,TIOEDDNM          IS IT THE ONE WE WANT
         BNE   GETNEXT                  GET NEXT DD IN TIOT
GETDSN   ICM   R5,7,TIOEJFCB            JFCB POINTER
         LA    R5,16(R5)                FIX IT
         MVC   ABCDSNAM(44),JFCBDSNM    MOVE DSN NAME FOR SVC99
         GTEDAALC ABC,VERB=AL           ALLOCATE INDIVIDUAL DD DSN(S)
         MVI   DBLWORD,X'80'            SET OPEN OPTION INPUT
         OPEN  (LOOKDD01),MF=(E,DBLWORD)     OPEN PDS FOR BLDL
         SR    R6,R6                    CLEAR R3
         LA    R6,BLDLAREA              LOAD ADDR OF BLDL AREA
         USING PDS2,R6                  ADDRESSABILITY TO DSECT
         MVC   PDS2NAME,MEMBNAME        MOVE PASSED MEMBER NAME
         BLDL  LOOKDD01,BLDL_LIST       LOOK FOR MEMBER
         B     *+4(R15)                 CHECK AND PROCESS RC'S
         B     TPUT_DSN                 MEMBER FOUND  RC=00
         B     NEXTDSN                  MEMBER NOT FOUND   RC=04
         B     NEXTDSN                  RC=08
         B     GOBACK                   SEVERE ERROR
NEXTDSN  BAL   R14,NEXTDD               R10->NEXT DD ENTRY
         CLI   0(R10),X'00'             END OF TIOT?
         BE    NOTFOUND                 NO->MORE DDNAMES IN TABLE
         CLC   TIOEDDNM,=8C' '          PART OF OUR CONCAT ???
         BNE   NOTFOUND                 NO, GET OUT
         CLOSE (LOOKDD01)               CLOSE THE DSN
         B     GETDSN                   GET NEXT DSN IN CONCAT
GETNEXT  BAL   R14,NEXTDD               R10->NEXT DD ENTRY
         CLI   0(R10),X'00'             END OF TIOT?
         BE    NOTALLOC                 NO->MORE DDNAMES IN TABLE
         B     LOOP1                    NO, KEEP ON GOING
TPUT_DSN CLI   PRNTFLAG,PRINTED         HAVE WE PRINTED HEADER ?
         BE    TPUT_DS2                 YES
         MVC   DDMSG3,DDNAME            MOVE DDNAME TO MSG
         MVC   MEMMSG3,MEMBNAME         MOVE MEMBER NAME TO MSG
         TPUT  MSG3,L'MSG3              TELL USER MEMBER FOUND
         TPUT  =C' ',1                  TPUT A BLANK LINE
         MVI   PRNTFLAG,PRINTED         INDICATE HEADER PRINTED
TPUT_DS2 TPUT  JFCBDSNM,L'JFCBDSNM      PRINT OUT DSN
         B     NEXTDSN                  NEXT ENTRY
NOTALLOC MVC   DDMSG1,DDNAME            MOVE DDNAME TO MSG
         TPUT  MSG1,L'MSG1              TELL USER DDNAME NOT ALLOC
         B     GOBACK                   GO HOME
NOTFOUND CLI   PRNTFLAG,PRINTED         HAVE WE PRINTED HEADER ?
         BE    GOBACK                   YES, MUST HAVE FOUND IT
         MVC   DDMSG2,DDNAME            MOVE DDNAME TO MSG
         MVC   MEMMSG2,MEMBNAME         MOVE MEMBER NAME TO MSG
         TPUT  MSG2,L'MSG2              TELL USER MEMBER NOT FOUND
GOBACK   DS    0H                       RETURN ROUTINE
         L     R13,4(,R13)              CALLERS SAVE AREA
         L     R14,12(,R13)             RESTORE R14
         LM    R0,R12,20(R13)           RESTORE REMAINING REGS
         BR    R14                      RETURN TO CALLER, REG 15
*
*        SUBRTN TO MOVE R10 TO THE NEXT DD ENTRY IN THE TIOT
*
NEXTDD   SLR   R8,R8
         IC    R8,0(,R10)            R8=LENGTH OF CURRENT DD ENTRY
         LA    R10,0(R8,R10)         R10->NEXT DD ENTRY IN TIOT
         BR    R14                   RETURN
*
*        STORAGE AREA
*
SAVEAREA DS    18F
IKJMVC   MVC   0(*-*,R2),0(R3)          MOVE FOR EX
MSG1     DS    0CL29
         DC    CL7'DDNAME '
DDMSG1   DC    CL8' '
         DC    CL14' NOT ALLOCATED'
MSG2     DS    0CL51
         DC    CL7'MEMBER '
MEMMSG2  DC    CL8' '
         DC    CL14' NOT FOUND IN '
DDMSG2   DC    CL8' '
         DC    CL14' CONCATENATION'
MSG3     DS    0CL58
         DC    CL7'MEMBER '
MEMMSG3  DC    CL8' '
         DC    CL10' FOUND IN '
DDMSG3   DC    CL8' '
         DC    CL14' CONCATENATION'
         DC    CL11' DATASET(S)'
CPPLADDR DS    F
MEMBNAME DC    CL8' '
DDNAME   DC    CL8' '
PRNTFLAG DC    X'00'
PRINTED  EQU   X'80'
BADPARSE DC    CL17'BAD PARSE OF PARM'
ERASE    DC    X'27F5401D40'            USED TO CLEAR 3270 TERMINAL
ABC      GTEDADAT DSNAM=XX.YYY,DDNAM=LOOKDD01,                         X
               CLOSE=YES,STATS=SHR,CNVRT=YES
LOOKDD01 DCB   DDNAME=LOOKDD01,                                        X
               DSORG=PO,                                               X
               MACRF=R,                                                X
               EODAD=GOBACK,                                           X
               RECFM=FB,                                               X
               LRECL=0,                                                X
               BLKSIZE=0
DBLWORD  DS    D
                  DS  0H
BLDL_LIST         DC  H'1'
BLDL_LENGTH       DC  H'76'
BLDLAREA          DS  1CL76
PCL      IKJPARM DSECT=PDL
IKJDDN   IKJIDENT 'DDNAME SEARCH',UPPERCASE,FIRST=ALPHA,               X
               MAXLNTH=8,OTHER=ALPHANUM,PROMPT='DD SEARCH NAME',       X
               HELP='VALID DDNAME'
MEMB     IKJIDENT 'MEMBER SEARCH NAME',UPPERCASE,FIRST=ALPHA,          X
               MAXLNTH=8,OTHER=ALPHANUM,PROMPT='MEMBER SEARCH NAME',   X
               HELP='VALID MEMBER NAME'
         IKJENDP
         TITLE 'COMMAND PROCESSOR PARAMETER LIST'
         IKJCPPL
         TITLE 'GETMAIN WORK AREA'
WORK     DSECT
PARSE    EQU   *
PARSPARM DS    7A                       PPL
PARSECB  DS    F                        ECB
PARSANS  DS    A                        PDL
PARSELEN EQU   *-PARSE
WORKLEN  EQU   *-WORK
         TITLE 'PARSE PARAMETER LIST'
         IKJPPL
PDL      DSECT
         ORG   MEMB
MEMBPTR  DS    A                       ADDRESS OF SEARCH MEMBER
MEMBLEN  DS    H                       LENGTH OF MEMBER
MEMBFLAG DS    X                       FLAGS
MEMFOUND EQU   X'80'                   MEMBER PRESENT
         ORG   IKJDDN
DDNPTR   DS    A                       ADDRESS OF DDN
DDNLEN   DS    H                       LENGTH OF DDN
DDNFLAG  DS    X                       FLAGS
DDNFOUND EQU   X'80'                   DDN PRESENT
*
         TITLE 'IKJEFF18 CONTROL BLOCKS'
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES
         TITLE 'CVT MAPPING'
         CVT   DSECT=YES
         TITLE 'PSA MAPPING'
         IHAPSA
         TITLE 'TCB MAPPING'
         IKJTCB
         TITLE 'TIOT MAPPING'
TIOT     DSECT
         IEFTIOT1
         TITLE 'JFCB MAPPING'
JFCB     DSECT
         IEFJFCBN
         TITLE 'PARTITIONED DATA SET ENTRY'
         IHAPDS
         TITLE 'SVC-99 CONTROL BLOCKS'
         IEFZB4D0
         END
