         TITLE 'LISTV COMMAND'
**********************************************************************
*  NAME        -   LISTV                                             *
*                                                                    *
*  FUNCTION    -   LIST VOLUME SPACE STATISTICS                      *
*                                                                    *
*  SYNTAX      -   LISTV 'SEARCH INDEX'  OR LISTV                    *
*                                                                    *
*  VERSION     -   1.0 FOR MVS 3.8 AND MVS/SE2                       *
*                  VER XA.2 FOR MVS/SP 2.2                           *
*                  VER XA.3 WITH OUTPUT MODS AND 3390 SUPPORT        *
*                                                                    *
*  DATE        -   6/1/80                                            *
*                  2/21/90 VER XA.2                                  *
*                  4/20/92 VER XA.3
*                                                                    *
*  INPUT       -   R1->CPPL                                          *
*                                                                    *
*  OUTPUT      -                                                     *
*       VOLUME DEV USE <----- FREE ------> LARGEST    DEV            *
*        NAME  ADR CNT CYL  TRK  NUM  DSCB CYL  TRK   TYPE  STATUS   *
*       TONE01 14B 014 0040 0309 0149 0812 0007 0004 3380-K PRIVATE  *
*                                                                    *
*  REGS        -   R12 - BASE                                        *
*                  R13 - SAVE AREA                                   *
*                  R4  - WORK AREA                                   *
*                  R10 - PDL                                         *
*                                                                    *
*  LOGIC       -                                                     *
*                  1. ENTRANCE CONVENTIONS                           *
*                  2. GETMAIN WORK AREA                              *
*                  3. BUILD PARSE PARAMETER LIST                     *
*                  4. INVOKE IKJPARS TO PARSE OPERAND                *
*                  5. SCAN UCBS FOR SEARCH INDEX MATCH (DEFAULT=ALL) *
*                  6. EXTRACT INFORMATION FROM UCB                   *
*                  7. GET SPACE INFORMATION VIA LSPACE SVC           *
*                  8. INVOKE RESIDENT CONVERT ROUTINE                *
*                  9. PRINT DETAIL LINE TO TERMINAL                  *
*                 10. IF MORE UCBS, GO TO 6                          *
*                 11. EXIT CONVENTIONS                               *
*                                                                    *
*  MACROS      -   REGS      -   REGISTER EQUATES                    *
*                  SAVE      -   ENTRANCE CONVENTIONS                *
*                  GETMAIN   -   ACQUIRE VIRTUAL MEMORY              *
*                  CALLTSSR  -   INVOKE TSO SERVICE ROUTINE          *
*                  PUTLINE   -   TERMINAL OUTPUT                     *
*                  IKJRLSA   -   RELEASE PARSE WORK AREA             *
*                  FREEMAIN  -   FREE VIRTUAL MEMORY                 *
*                  RETURN    -   EXIT CONVENTIONS                    *
*                  IKJPARM   -   SETUP PARSE PCL                     *
*                  IKJIDENT  -   SPECIFY POSITION PARAMETER          *
*                  IKJENDP   -   END PARSE PCL                       *
*                  LSPACE    -   GET SPACE INFO FROM UCB             *
*                                                                    *
*  DSECTS      -   CB OR AREA    DSECT NAME                          *
*                  --            ----- ----                          *
*                  CPPL          IKJCPPL                             *
*                  CVT           CVT DSECT=YES,PREFIX=NO             *
*                  UCB           IEFUCBOB                            *
*                  PPL           IKJPPL                              *
*                  PDL           PDL                                 *
*                  WORK AREA     WORK                                *
*                  MESSAGE AREA  LSPACE                              *
*                  DCE           DCE                                 *
*                                                                    *
*  ERRORS      -   PARSE FAILURE                                     *
*                  LSPACE FAILURE                                    *
*                                                                    *
*  PRIVILEGES  -   PROBLEM PROGRAM                                   *
*                                                                    *
*  DEPENDENCIES-   INTERFACE TO RESIDENT CONVERT ROUTINE             *
*                  LSPACE PARAMETERS                                 *
*                  VARIOUS MVS CONTROL BLOCKS                        *
*                                                                    *
*  INSTALL     -   ASSEMBLE LISTV                                    *
*                  LINK LISTV                                        *
*                  EXECUTE FORM LINKLST OR STEPLIB                   *
*                                                                    *
*  MODIFICATION -  ADDED DEVICE TYPE AND STATUS TO OUTPUT            *
*                  REMOVE WAAMENTR MACRO CALL                        *
*                  JIM BOYSEN/AMDAHL  JULY - 1992                    *
*                                                                    *
*                  ADDED 3390 DEVICE SUPPORT                         *
*                  BOYSEN  OCTOBER - 1992                            *
*                  ADDED 3390 MOD 2 AND 3 DISTINCTION                *
*                  BOYSEN  AUGUST - 1993                             *
*                  ADDED CLEAR 3270 SCREEN CODE BEFORE DISPLAY       *
*                  BOYSEN FEB - 1994                                 *
*                                                                    *
**********************************************************************
         TITLE 'LISTV - INITIALIZATION AND OPERAND PARSE'
LISTV    CSECT
R15      EQU   15
R14      EQU   14
R13      EQU   13
R12      EQU   12
R11      EQU   11
R10      EQU   10
R9       EQU   9
R8       EQU   8
R7       EQU   7
R6       EQU   6
R5       EQU   5
R4       EQU   4
R3       EQU   3
R2       EQU   2
R1       EQU   1
R0       EQU   0
LISTV    CSECT
         STM   R14,R12,12(R13)          SAVE CALLERS REGS
         BALR  R12,0                    ESTABLISH ADDRESSABILITY
         USING *,12                     BASE OF EXECUTING PROGRAM
         ST    R13,SAVEAREA+4           CALLERS SAVE AREA
         LA    R15,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
         ST    R15,8(,R13)              EXECUTING PROGRAMS SAVEAREA
         LA    R13,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
*
         LR    R3,R1                        SAVE CPPL ADDRESS
         USING CPPL,R3
         LA    R7,IOPLPARM                  ADDRESS IOPL FOR PUTLINE
         USING IOPL,R7
         L     R5,CPPLUPT                   LOAD AND
         ST    R5,IOPLUPT                   SAVE UPT ADDR
         L     R5,CPPLECT                   LOAD AND
         ST    R5,IOPLECT                   SAVE ECT ADDR
         LA    R5,ECB                       LOAD AND
         ST    R5,IOPLECB                   SAVE ECB ADDRESS
         DROP  R7
*
         GETMAIN R,LV=WORKLEN               GET WORK AREA
         LR    R4,R1                        SAVE WORK AREA ADDRESS
         USING WORK,R4
         MVI   PRTFLAG,X'00'                CLEAR PRINT FLAG
         LA    R5,PARSPARM                  GET PPL ADDRESS
         USING PPL,R5
         XC    PARSE(PARSELEN),PARSE        INITIALIZE PPL
         MVC   PPLUPT,CPPLUPT               UPT
         MVC   PPLECT,CPPLECT               ECT
         MVC   PPLCBUF,CPPLCBUF             CIB
         DROP  R3
         LA    R1,PARSECB                   ECB
         ST    R1,PPLECB
         L     R1,=A(PCL)                   PCL
         ST    R1,PPLPCL
         LA    R1,PARSANS                   PDL
         ST    R1,PPLANS
         CALLTSSR EP=IKJPARS,MF=(E,(R5))    CALL PARSE
         DROP  R5
         L     R10,PARSANS                  OK?
         USING PDL,R10
         LTR   R10,R10
         BP    GOODPARS                     YES, CONTINUE
         PUTLINE PARM=PUTLINE,                                         X
               OUTPUT=(PARSERR,TERM,SINGLE,DATA),                      X
               MF=(E,IOPLPARM)
         B     RETURN
GOODPARS EQU   *
         GTSIZE
         LTR   R0,R0               IS THIS A 3270 TYPE DEVICE
         BZ    NOTTSO              NO - CANNOT BE MCS CONSOLE
         LA    R1,ERASE            POINT TO ERASE CNTL CHARS
         LA    R0,L'ERASE          GET THE LENGTH
         TPUT  (1),(0),FULLSCR     ERASE THE 3270 SCREEN
NOTTSO   PUTLINE PARM=PUTLINE,                                         X
               OUTPUT=(HEADER1,TERM,MULTLIN,DATA),                     X
               MF=(E,IOPLPARM)
         TITLE 'EXTRACT UCB INFORMATION'
UCBPARM  DS    0H
         GETMAIN R,LV=PARMLEN
         LR    R3,R1                        SAVE WORKAREA ADDR
         USING PARMWORK,R3
         XC    WORKAREA,WORKAREA            CLEAR WORK AREA
         LA    R5,WORKAREA                  LOAD AND
         ST    R5,PARMWA                    STORE ADDR OF WORKAREA
         LA    R5,DEVTYPE                   LOAD AND
         ST    R5,PARMDEVT                  STORE ADDR OF DEV TYPE
         MVI   DEVTYPE,UCB3DACC             SET DEV TYPE TO DASD
         LA    R5,ADDRUCB                   LOAD AND
         ST    R5,PARMUCB                   STORE ADDR OF UCB
         OI    PARMUCB,X'80'                MARK END OF PARM
SEARCH   DS    0H
         L     R1,CVTPTR                    CVT ADDRESS
         USING CVT,R1
         L     R15,CVTUCBSC                 ENTRY TO IOSVSUCB
         LA    R1,PARMLIST                  LOAD ADDR OF PARM
         BALR  R14,R15                      LINK FOR UCB
         LTR   R15,R15                      ANY UCB ?
         BNZ   EXIT                         NO UCB OR DONE
         L     R8,ADDRUCB                   GET RETURNED UCB ADDR
         USING UCBOB,R8
GOTUCB   DS    0H
         TM    UCBTBYT3,UCB3DACC            DIRECT ACCESS?
         BZ    SEARCH                       NO, OMIT
         TM    UCBSTAT,UCBONLI              ONLINE?
         BNO   SEARCH                       NO, IGNORE DEVICE
         CLC   UCBTBYT3(2),=X'2005'         DATA CELL?
         BE    SEARCH                       IF SO SKIP IT
         TM    UCBVOLI,X'FF'                ANY VOLUME MOUNTED?
         BZ    SEARCH                       NO, IGNORE IT
         TM    VOLFLAG,VOLFOUND             SEARCH INDEX SPECIFIED?
         BZ    USEUCB                       NO, USE UCB
         L     R2,VOLPTR                    GET INDEX ADDRESS
         LH    R1,VOLLEN                    GET INDEX LENGTH
         BCTR  R1,0
         EX    R1,COMPVOL                   INDEX SATISFIED?
         BNE   SEARCH
         BE    USEUCB                       YES, USE UCB
COMPVOL  CLC   0(*-*,R2),UCBVOLI
*
USEUCB   MVC   VOLNAME,UCBVOLI              PRINT VOLUME NAME
         MVC   UNITADDR,UCBNAME             GET UNIT ADDRESS
         SR    R1,R1                        CLEAR A REG
         LH    R1,UCBUSER                   GET USE COUNT
         CVD   R1,DBLW                      CONVERT TO PRINT
         UNPK  USECNT,DBLW+6(2)             PLACE IT ON THE LINE
         OI    USECNT+2,X'F0'               FIX SIGN BITS
         LA    R15,STCYLS                   GET DEVICE TABLE
DEVLOC   DS    0H
         CLC   UCBUNTYP,(STCYLS-STCYLS)(R15)
         BE    DEVGOT
         CLI   (STCYLS-STCYLS)(R15),X'FF'
         BE    DEVGOT
         LA    R15,(STCYLS1-STCYLS)(R15)  NEXT
         B     DEVLOC                  LOOP
DEVGOT   DS    0H
         ST    R15,SAVE15
         CLC   UCBUNTYP,=X'0E00'            IS IT A 3380 ?
         BE    X3380                        YES
         CLC   UCBUNTYP,=X'0F00'            IS IT A 3390 ?
         BE    X3390                        YES
         B     CHKSTAT                      CHECK DEVICE STATUS
X3380    L     R6,UCBEXTPT                  LOAD UCB EXTENSION
         USING UCBCMEXT,R6
         L     R6,UCBCLEXT                  LOAD ADDRESS OF DCE
         DROP  R6
         USING DCE,R6
         TM    DCEFLAG1,DCEVALFS            DEVICE FIELD VALID ?
         BZ    MOVETYPE                     NO - MOVE 3380 TYPE
         LH    R7,DCEUDCYL                  MAX NUMBER OF CYLINDERS
         CH    R7,=H'1771'                  IS IT A 3380E ?
         BNE   CHECKK
         MVC   DEVKIND,=C'3380-E'
         B     CHKSTAT
CHECKK   CH    R7,=H'2656'                  IS IT A 3380K
         BNE   MOVETYPE
         MVC   DEVKIND,=C'3380-K'
         B     CHKSTAT
X3390    L     R6,UCBEXTPT                  LOAD UCB EXTENSION
         USING UCBCMEXT,R6
         L     R6,UCBCLEXT                  LOAD ADDRESS OF DCE
         DROP  R6
         USING DCE,R6
         TM    DCEFLAG1,DCEVALFS            DEVICE FIELD VALID ?
         BZ    MOVETYPE                     NO - MOVE 3390 TYPE
         LH    R7,DCEUDCYL                  MAX NUMBER OF CYLINDERS
         CH    R7,=H'2227'                  IS IT A 3390-2 ?
         BNE   CHECK_M3
         MVC   DEVKIND,=C'3390-2'
         B     CHKSTAT
CHECK_M3 CH    R7,=H'3340'                  IS IT A 3390-3 ?
         BNE   MOVETYPE
         MVC   DEVKIND,=C'3390-3'
         B     CHKSTAT
MOVETYPE L     R15,SAVE15
         MVC   DEVKIND,(STCYLNAM-STCYLS)(R15)     DEV. NAME
CHKSTAT  TM    UCBSTAB,UCBBPRV              PRIVATE ?
         BO    MOVEPVT                      YES , MOVE OUTPUT
         TM    UCBSTAB,UCBBPUB              PUBLIC  ?
         BO    MOVEPUB                      YES,  MOVE PUBLIC
         TM    UCBSTAB,UCBBSTR              STORAGE ?
         BO    MOVESTR
         B     SPACE_INFO
MOVEPVT  MVC   DEVSTAT,=C'PRIVATE'          MOVE PRIVATE LITERAL
         B     SPACE_INFO
MOVEPUB  MVC   DEVSTAT,=C'PUBLIC '          MOVE PUBLIC LITERAL
         B     SPACE_INFO
MOVESTR  MVC   DEVSTAT,=C'STORAGE'          MOVE STORAGE LITERAL
         TITLE 'EXTRACT SPACE INFORMATION USING LSPACE SVC'
*
SPACE_INFO    LA    R1,PARMFREE
         LSPACE MF=(E,(1)),UCB=(R8),MSG=DBLW
         LTR   R15,R15                      DID IT WORK?
         BZ    SPCOK                        YES
         PUTLINE PARM=PUTLINE,                                         X
               OUTPUT=(VTOCERR,TERM,SINGLE,DATA),                      X
               MF=(E,IOPLPARM)
         B     RETURN
PUTIT    PUTLINE PARM=PUTLINE,                                         X
               OUTPUT=(DETAIL,TERM,SINGLE,DATA),                       X
               MF=(E,IOPLPARM)
         MVI   PRTFLAG,X'FF'                SAY WE HAVE DONE SOME
         B     SEARCH                       NOW GO ON
*
*  LSPACE RETURNS ITS INFORMATION IN THE FORM:
*        SPACE=CCCC,TTTT,AAAA/EEEE,KKKK
*        1...5....1....5....2....5....3
*  WHERE          0         0         0
*        CCCC = TOTAL FREE CYLINDERS
*        TTTT = TOTAL FREE TRACKS
*        AAAA = TOTAL FREE AREAS
*        EEEE = NUMBER OF CYLINDERS IN LARGEST FREE EXTENT
*        KKKK = NUMBER OF TRACKS IN LARGEST FREE EXTENT
*
SPCOK    LA    R1,DBLW                      R1->MONITOR SPACE MSG AREA
         USING LMSG,R1
         MVC   TOTCYL,LSPMTEXT+6            CYL
         MVI   TOTCYL+4,C'+'
         MVC   TOTTRK,LSPMTEXT+11           TRK
         MVC   TOTAREA,LSPMTEXT+16          EXTENTS
         MVC   LARGECYL,LSPMTEXT+21         CYL (LARGEST)
         MVI   LARGECYL+4,C'+'
         MVC   LARGETRK,LSPMTEXT+26         TRK (LARGEST)
         DROP  R1
*
         LA    R1,PARMF0S
         LSPACE MF=(E,(1)),UCB=(R8),DATA=F0SCNT
         LA    R2,F0SCNT
         USING LDATA,R2
         L     R1,LSPDF0S
         CVD   R1,DBLW                      CONVERT IT
         UNPK  FMT0S,DBLW+5(3)              AND MOVE TO THE LINE
         MVI   FMT0S,C' '                   DON'T NEED HIGH BYTE
         OI    FMT0S+4,X'F0'                FIX SIGN
         MVI   DIRFFLG,C' '                 CLEAR DIRF FLAG INDICATOR
         B     PUTIT                        NOW GO ON
         TITLE 'EXIT CONVENTIONS'
EXIT     TM    PRTFLAG,X'FF'                ANY VOLUMES SHOWN
         BO    RETURN
         PUTLINE PARM=PUTLINE,                                         X
               OUTPUT=(NOINDEX,TERM,SINGLE,DATA),                      X
               MF=(E,IOPLPARM)
RETURN   IKJRLSA PARSANS                    FREE PDL
         FREEMAIN R,LV=WORKLEN,A=(R4)       FREE PARSE WORK AREA
         FREEMAIN R,LV=PARMLEN,A=(R3)       FREE UCB SCAN WORK AREA
         DROP  R4,R10,R3
ENDIT    DS    0H
         L     R13,4(,R13)              CALLERS SAVE AREA
         L     R14,12(,R13)             RESTORE R14
         LM    R0,R12,20(R13)           RESTORE REMAINING REGS
         BR    R14                      RETURN TO CALLER, REG 15
*  SAVE AREA
SAVEAREA DS    18F
SAVE15   DS    F
ERASE    DC    X'27F5401D40'       USED TO CLEAR 3270 TERMINAL BUFFER
*  DEVICE CHARACTERISTICS TABLE
STCYLS   DS    0H                  TABLE OF DEVICE TYPE TO MAX. CYLS
         DC    X'0900'             DEVICE TYPE
STCYLMAX EQU   *
         DC    H'404'              MAX CYLS
STCYLNAM EQU   *
         DC    CL5'3330 '          EBCDIC NAME
STCYLTRK EQU   *
         DC    H'19'               TRACKS PER CYLINDER
STCYLTTK EQU   *
         DC    AL2(404*19)         TRACKS PER VOL
STCYLS1  DS    0H                  FOR SIZE OF ENTRY CALC
         DC    X'0D00',H'0808',CL6' 3331 ',H'19',AL2(0808*19)
         DC    X'0B00',H'0555',CL6' 3350 ',H'30',AL2(0555*30)
         DC    X'0800',H'0200',CL6' 2314 ',H'20',AL2(0200*20)
         DC    X'0A00',H'0696',CL6' 3340 ',H'12',AL2(0696*12)
         DC    X'0C00',H'0959',CL6' 3375 ',H'12',AL2(0959*12)
         DC    X'0E00',H'0885',CL6' 3380 ',H'15',AL2(0885*15)
         DC    X'0F00',H'2226',CL6' 3390 ',H'15',AL2(2226*15)
         DC    X'FFFF',H'9999',CL6' UNKN ',H'19',AL2(99*99)
HEXTR    DC    C'0123456789ABCDEF'
*  PRINT LINES
HEADER1  DS    0F
         DC    A(HEADER2)
         DC    Y(4+58,0)
         DC    CL45'VOLUME DEV USE <----- FREE ------> LARGEST  '
         DC    CL13' DEV         '
HEADER2  DS    0F
         DC    A(0)
         DC    Y(4+59,0)
         DC    CL45' NAME  ADR CNT CYL  TRK  NUM  DSCB CYL  TRK '
         DC    CL14' TYPE   STATUS'
PARSERR  DS    0H
         DC    Y(4+30,0)
         DC    CL30'LISTV TERMINATED - PARSE ERROR'
VTOCERR  DS    0H
         DC    Y(4+25,0)
         DC    CL25'ERROR IN VTOC'
NOINDEX  DS    0H
         DC    Y(4+40,0)
         DC    CL40'NO VOLUMES FOUND FOR INDEX GIVEN'
DETAIL   DS    0H
         DC    Y(4+60,0)
VOLNAME  DS    CL6                           VOLUME NAME
DIRFFLG  DS    C                             DIRF FLAG
UNITADDR DS    CL3                           UNIT ADDRESS
         DS    C
USECNT   DS    CL3                           # OF CURRENT ALLOCATIONS
         DS    C
TOTCYL   DS    CL4                           TOTAL FREE CYLINDERS
         DS    C
TOTTRK   DS    CL4                           TOTAL FREE TRACKS
         DS    C
TOTAREA  DS    CL4                           TOTAL FREE AREAS
FMT0S    DS    CL5                           # FREE DSCBS
         DS    C
LARGECYL DS    CL4                           # CYLS IN LARGEST FREE EXT
         DS    C
LARGETRK DS    CL4                           # TRKS IN LARGEST FREE EXT
         DS    C
DEVKIND  DS    CL6                           # DEVICE TYPE
         DS    C
         DS    C
DEVSTAT  DS    CL7                           * DEVICE STATUS
*
PUTLINE  PUTLINE MF=L
*
F0SCNT   DS    XL36
         EJECT
* PARM LISTS
         DS    0F
PARMFREE DS    0CL24
         DC    CL4'LSPA'
         DC    AL2(PARMFREE-FREEEND)
         DC    AL1(20)
         DC    XL1'00'
         DS    0F
         DC    XL1'00'
         DC    XL1'81'
         DC    XL1'00'
         DC    XL1'00'
         DC    A(0)
FREERTRN DS    F
         DC    A(0)
FREEEND  EQU   *
PARMF0S  DS    0CL24
         DC    CL4'LSPA'
         DC    AL2(PARMF0S-F0SEND)
         DC    AL1(00)
         DC    XL1'00'
         DS    0F
         DC    XL1'00'
         DC    XL1'82'
         DC    XL1'00'
         DC    XL1'00'
         DC    A(0)
F0SRTRN  DS    F
         DC    A(0)
F0SEND   EQU   *
* PUTLINE WORKAREAS
ECB      DS    A
IOPLPARM DC    4F'0'
         TITLE 'LISTV - LITERALS'
         LTORG
         TITLE 'LISTV - PARSE PARAMETER CONTROL LIST'
PCL      IKJPARM DSECT=PDL
VOL      IKJIDENT 'VOLUME SEARCH INDEX',OTHER=ANY,MAXLNTH=6            $
               FIRST=ANY
         IKJENDP
         TITLE 'I/O PARAMETER LIST'
         IKJIOPL
         TITLE 'COMMUNICATION VECTOR TABLE'
         CVT   DSECT=YES,PREFIX=NO
         TITLE 'UNIT CONTROL BLOCK'
         PRINT NOGEN
         IEFUCBOB
         PRINT GEN
         TITLE 'COMMAND PROCESSOR PARAMETER LIST'
         IKJCPPL
         TITLE 'GETMAIN WORKAREA DSECT'
WORK     DSECT
PARSE    EQU   *
PARSPARM DS    7A                           PPL
PARSECB  DS    F                            ECB
PARSANS  DS    A                            PDL
PARSELEN EQU   *-PARSE
PRTFLAG  DS    X                            PRINTING FLAG
DBLW     DS    4D                           LSPACE RETURN AREA
WORKLEN  EQU   *-WORK                       LENGTH OF WORK AREA
         TITLE 'PARSE PARAMETER LIST'
         IKJPPL
         TITLE 'IKJIDENT PDE'
PDL      DSECT
         ORG   VOL
VOLPTR   DS    A                            ADDRESS OF SEARCH INDEX
VOLLEN   DS    H                            LENGTH OF SEARCH INDEX
VOLFLAG  DS    X                            FLAGS
VOLFOUND EQU   X'80'                        INDEX PRESENT
         TITLE 'MONITOR SPACE MESSAGE AREA'
LMSG     LSPACE MF=(D,MSG)
LDATA    LSPACE MF=(D,DATA)
PARMWORK DSECT
PARMLIST DS   0F
PARMWA   DS   F
PARMDEVT DS   F
PARMUCB  DS   F
*
DEVTYPE  DS   XL1
ADDRUCB  DS   F
         DS   0D
WORKAREA DS   CL100
PARMLEN  EQU  *-PARMLIST
         TITLE 'UCB DASD EXTENSION MAPPING MACRO'
         IECDDCE
         END
