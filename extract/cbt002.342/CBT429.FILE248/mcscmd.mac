MCSCMD   CSECT
*****************************************************************
*   THIS PROGRAM WILL RUN AS A STARTED TASK AND USES THE MVS    *
*   MODIFY COMMAND TO COMMUNICATE WITH THE TASK.  IT ENABLES    *
*   OPERATORS TO ENTER A SYSTEM COMMAND AS IF IT CAME FROM THE  *
*   MASTER CONSOLE, I.E.   CF COMMANDS FROM A MCS CONSOLE       *
*   ENTER   F MCSCMD,END  TO TERMINATE THE TASK                 *
*   BOYSEN/AMDAHL    07/23/93                                   *
*****************************************************************
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12)
         BALR  R11,0
         USING *,R11
         ST    R13,SAVE+4
         LA    R6,SAVE
         ST    R6,8(R13)
         LA    R13,SAVE
******** INITIALIZE POINTERS *********
         L     R5,CVTPTR               GET ADDR OF CVT
         USING CVTMAP,R5
         L     R5,CVTCUCB              GET CONSOLE UCM BASE ADDR
         DROP  R5
         LA    R5,0(,R5)               INSURANCE
         S     R5,=F'4'                BACK UP FOUR BYTES
         L     R5,0(,R5)               GET ADDR OF UCM PREFIX
         USING UCMPRFX,R5
         L     R5,UCMMCENT             GET ADDR OF MASTER CONSOLE UCM
         DROP  R5
         USING UCMLIST,R5
         MVC   CONSOLE,UCMID           GET CONSOLE UCMID
FINDCIB  DS    0H
         EXTRACT COMMADDR,FIELDS=COMM
         L     R1,COMMADDR             ANSWER AREA
         L     R2,4(,R1)               ADDRESS OF CIB
         LA    R3,4(,R1)               ADDRESS OF ADDRESS OF CIB
         LTR   R2,R2                   IS THERE A CIB ?
         BZ    NOSTART
         USING CIB,R2
         CLI   CIBVERB,CIBSTART        IS IT A START
         BNE   NOSTART
         DROP  R2
         QEDIT ORIGIN=(3),BLOCK=(2)
NOSTART  QEDIT ORIGIN=(3),CIBCTR=2
         LA    R15,0
CIBWAIT  MVC   REPLY(30),BLANKS        CLEAR OUT REPLY
         L     R1,COMMADDR             POINT TO COMM AREA
         L     R1,0(,R1)               POINT TO COMM ECB
         WAIT  ECB=(1)                 WAIT FOR MODIFY
         L     R1,COMMADDR             POINT TO COMMADDR
         L     R2,4(,R1)               POINT TO CIB
         USING CIB,R2
         LA    R3,4(,R1)
         CLI   CIBVERB,CIBSTOP         STOP ISSUED ????
         BE    RETURN                  GET OUT, STOP ISSUED
         LH    R1,CIBDATLN             GET LENGTH OF COMMAND
         BCTR  R1,0                    LENGTH MINUS 1
         EX    R1,MVCHAR               MOVE OPERAND TO REPLY
         B     TESTCMD                 SEE WHAT WE HAVE
MVCHAR   MVC   REPLY(0),CIBDATA        MOVE FOR EXECUTE
TESTCMD  DS    0H
         QEDIT ORIGIN=(3),BLOCK=(2)    FREE THE CIB
         OC    REPLY,BLANKS            UPPERCASE THE ANSWER
         CLC   REPLY(3),=C'END'        IS THE REPLY 'END'
         BE    RETURN                  NOPE....YES, GET OUT
ISSUECMD DS    0H                      ISSUE OPERATOR COMMAND
         MODESET KEY=ZERO,MODE=SUP
         LA    R1,SVC34CMD             PUT COMMAND ADDR IN R1
         SR    R0,R0                   ZERO FOR INSERT
         IC    R0,CONSOLE              INSERT MASTER CONSOLE ID
         SVC   34                      SCHEDULE CSCB
         XC    MCSFLAGS,MCSFLAGS       ZERO MCS FLAGS
         MODESET KEY=NZERO,MODE=PROB
         B     CIBWAIT                  GO WAIT FOR ANOTHER REPLY
         SPACE
RETURN   L     R13,SAVE+4               GET OLD SAVE AREA
         RETURN (14,12),RC=4095         AND SPLIT......
         EJECT
SVC34CMD DS    0CL34
         DC    AL2(SVC34LEN-*)
MCSFLAGS DC    H'0'
REPLY    DC    CL30' '                  OPERATOR COMMAND
SVC34LEN EQU   *
SAVE     DC    18F'0'
         DC    H'0'
BLANKS   DC    CL30' '
CONSOLE  DC    XL1'00'                  CONSOLE ID FROM UCMID
COMMADDR DC    A(0)
ECBADDR  DC    A(0)
CIBADDR  DC    A(0)
         SPACE 2
******** DSECTS ********
         CVT   DSECT=YES,PREFIX=YES,LIST=YES
         SPACE 2
         IEECUCM FORMAT=NEW
         SPACE 2
CIB      DSECT
         IEZCIB
         END
