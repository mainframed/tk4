**********************************************************************
*  THIS PROGRAM WILL GET THE SMF CPUID FROM THE SMCA AND SET A       *
*  CLIST VARIABLE WITH THE SYSTEM ID    BOYSEN/AMDAHL   11/06/92     *
**********************************************************************
SMFCPUID CSECT
         PRINT NOGEN
         CVT   DSECT=YES
         IEESMCA
         PRINT ON
SMFCPUID CSECT
R15      EQU   15
R14      EQU   14
R13      EQU   13
R12      EQU   12
R11      EQU   11
R10      EQU   10
R9       EQU   9
R8       EQU   8
R7       EQU   7
R6       EQU   6
R5       EQU   5
R4       EQU   4
R3       EQU   3
R2       EQU   2
R1       EQU   1
R0       EQU   0
         IKJTSVT
SMFCPUID CSECT
         STM   R14,R12,12(R13)          SAVE CALLERS REGS
         BALR  R12,0                    ESTABLISH ADDRESSABILITY
         USING *,12                     BASE OF EXECUTING PROGRAM
         ST    R13,SAVEAREA+4           CALLERS SAVE AREA
         LA    R15,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
         ST    R15,8(,R13)              EXECUTING PROGRAMS SAVEAREA
         LA    R13,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
*
         L     R4,CVTPTR                GET ADDRESS OF CVT
         L     R5,CVTSMCA-CVT(,R4)      LOAD THE SMCA ADDRESS
         MVC   VALUE,SMCASID-SMCABASE(R5)    MOVE THE SYSTEM ID
*
         L     R15,CVTPTR               ACCESS THE CVT
         L     R15,CVTTVT-CVT(,R15)     ACCESS THE TSVT
         L     R15,TSVTVACC-TSVT(,15)   ACCESS THE VARIABLE ACCESS RTN
*
*        INVOKE THE VARIABLE ACCESS SERVICE
*
         LTR   R15,R15                  VERIFY TSVT ADDRESS
         BNZ   CALL441                  IF PRESENT, CALL IKJCT441
LINK441  LINK  EP=IKJCT441,                                            X
               PARAM=(ECODE,NAMEPTR,NAMELEN,VALUEPTR,VALUELEN,TOKEN),  X
               VL=1
         B     RET441
CALL441  CALL  (15),(ECODE,NAMEPTR,NAMELEN,VALUEPTR,VALUELEN,TOKEN),VL
*
RET441   DS    0H
         LTR   R15,R15                  CHECK RETURN CODE
         BNZ   ERRORRTN
*
*
ERRORRTN DS    0H
         L     R13,4(,R13)              CALLERS SAVE AREA
         L     R14,12(,R13)             RESTORE R14
         LM    R0,R12,20(R13)           RESTORE REMAINING REGS
         BR    R14                      RETURN TO CALLER, REG 15
*                                       CONTAINS RETURN FROM IKJCT441
*
*        STORAGE AREA
*
NAME     DC    CL8'SMFCPUID'            NAME OF THE VARIABLE
NAMELEN  DC    F'8'                     LENGTH OF VARIABLE NAME
VALUE    DC    CL4' '                   VALUE OF VARIABLE
VALUELEN DC    F'4'                     LENGTH OF VALUE
NAMEPTR  DC    A(NAME)                  POINTER TO VARIABLE NAME
VALUEPTR DC    A(VALUE)                 POINTER TO VARIABLE VALUE
TOKEN    DC    F'0'                     UNUSED, SET TO 0
ECODE    DC    A(TSVEUPDT)              ENTRY CODE FOR SETTING VALUES
SAVEAREA DS    18F
         END
