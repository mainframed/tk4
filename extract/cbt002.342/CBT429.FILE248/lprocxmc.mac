//COM     EXEC PGM=ASMA90,REGION=512K,
//    PARM='LOAD,XREF(SHORT),LINECNT=55,NODECK,RENT'
//*
//SYSLIB   DD  DISP=SHR,DSN=SYS1.MACLIB
//         DD  DISP=SHR,DSN=SYS1.MODGEN
//         DD  DISP=SHR,DSN=YOUR.SOURCE
//SYSLIN   DD  UNIT=SYSDA,DSN=&LOADSET,DISP=(MOD,PASS),
//             SPACE=(CYL,(2,2)),DCB=BLKSIZE=1680
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(10,2))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(CYL,(12,2))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(CYL,(20,2))
//SYSPUNCH DD  DUMMY =*,DCB=FUNC=I
//SYSPRINT DD  SYSOUT=*
//SYSUDUMP DD  SYSOUT=*
//SYSIN    DD  *
*********************************************************************
*  FINDS TIOT IN SECONDARY ADDRESS SPACE (JES2) AND RETURNS A       *
*  TABLE OF DSN'S TO THE CALLING PROGRAM FOR DDNAMES STARTING       *
*  WITH "PROC".   CALLED BY LPROC COMMAND                           *
*  BOYSEN/AMDAHL   02/94                                            *
*                                                                   *
*  MODIFIED TO RETURN THE DDNAME FOR DISPLAY  BOYSEN 08/94          *
*                                                                   *
*  MODIFIED TO CHECK FOR EXISTENCE OF CA-ENF PRODUCT  BOYSEN '95    *
*********************************************************************
LPROCXM  CSECT
R15      EQU   15
R14      EQU   14
R13      EQU   13
R12      EQU   12                       BASE REGISTER
R11      EQU   11
R10      EQU   10
R9       EQU   9
R8       EQU   8
R7       EQU   7
R6       EQU   6
R5       EQU   5
R4       EQU   4
R3       EQU   3
R2       EQU   2
R1       EQU   1
R0       EQU   0
LPROCXM  CSECT
LPROCXM  AMODE 31
LPROCXM  RMODE ANY
         SAVE  (14,12)                  SAVE CALLERS REGS
         BASR  R12,0                    ESTABLISH ADDRESSABILITY
         USING *,12                     BASE OF EXECUTING PROGRAM
         LR    R3,R1                    SAVE PARM ADDRESS
         USING XMPARMS,R3               MAP PARMS
*
GETTIOT  DS    0H
         L     R6,XMASCB                GET 2ND ASCB FROM PARM
         L     R7,XMDSNS                ADDRESS OF TABLE OF DSNS
         SAC   256                      GET INTO SECONDARY ADDR MODE
         L     R6,ASCBASXB-ASCB(,R6)    ADDR OF ASXB IN SECONDARY ADDR
         L     R6,ASXBFTCB-ASXB(,R6)    ADDR OF FIRST TCB
         USING TCB,R6                   MAP TCB
TCBLOOP  DS    0H
         USING CDENTRY,R8               MAP CDE
         ICM   R8,15,TCBJPQ             ADDRESS OF JOB-PACK-AREA QUEUE
         BZ    NEXTTCB                  =0, NO MODULES FOR THIS TCB
         CLC   CDNAME(4),CA_NAME        CHECK FOR CA PRODUCT NAME
         BE    NEXTTCB                  GET NEXT TCB IF CA
         L     R10,TCBTIO-TCB(,R6)      SECONDARY TIOT ADDR
         LA    R10,TIOENTRY-TIOT(,R10)  1ST DD SEGMENT IN TIOT
         USING TIOENTRY,R10             ADDRESSABILTY TO TIOT
         USING JFCB,R2                  ADDRESSABILTY TO JFCB
LOOP1    CLC   PROCS(4),TIOEDDNM        IS IT ONE OF THE PROCXX DD'S
         BNE   GETNEXT                  GET NEXT DD IN TIOT
         LA    R5,2                     MOVE LENGTH   8/94
         MVCP  0(R5,R7),SLASHES,R4      MOVE DDNAME
         LA    R7,2(,R7)                BUMP 1 FOR DDNAME
         LA    R5,8                     MOVE LENGTH   8/94
         MVCP  0(R5,R7),TIOEDDNM,R4     MOVE DDNAME
         LA    R7,L'JFCBDSNM-2(,R7)     NEXT ENTRY IN DSN TABLE
GETDSN   ICM   R2,7,TIOEJFCB            JFCB POINTER
         LA    R2,16(R2)                FIX IT
         LA    R5,L'JFCBDSNM            LOAD LENGTH OF MVCP
         MVCP  0(R5,R7),JFCBDSNM,R4     MOVE DSNAME(S)
         LA    R7,L'JFCBDSNM(,R7)       NEXT ENTRY IN DSN TABLE
NEXTDSN  BAL   R14,NEXTDD               R10->NEXT DD ENTRY
         CLI   0(R10),X'00'             END OF TIOT?
         BE    EXIT00                   NO->MORE DDNAMES IN TABLE
         CLC   TIOEDDNM,=8C' '          PART OF OUR CONCAT ???
         BNE   LOOP1                    NO, GET OUT
         B     GETDSN                   GET NEXT DSN IN CONCAT
GETNEXT  BAL   R14,NEXTDD               R10->NEXT DD ENTRY
         CLI   0(R10),X'00'             END OF TIOT?
         BE    EXIT00                   NO->MORE DDNAMES IN TABLE
         B     LOOP1                    NO, KEEP ON GOING
         B     EXIT00                   WE HAVE IT, LETS GO
NEXTTCB  DS    0H
         ICM   R6,15,TCBTCB             ADDR OF NEXT TCB ON TCB QUEUE
         BNZ   TCBLOOP                  LET'S TRY THIS TCB
EXIT00   DS    0H
         SAC   0                        GET INTO PRIMARY ADDR MODE
         LA    R15,X'00'                SET RETURN CODE 00
GOBACK   DS    0H                       RETURN ROUTINE
         RETURN (14,12)                 RESTORE REGS AND RETURN
*
*        SUBRTN TO MOVE R10 TO THE NEXT DD ENTRY IN THE TIOT
*
NEXTDD   SLR   R8,R8
         IC    R8,0(,R10)            R8=LENGTH OF CURRENT DD ENTRY
         LA    R10,0(R8,R10)         R10->NEXT DD ENTRY IN TIOT
         BR    R14                   RETURN
*
*        STORAGE AREA
*
PROCS    DC    CL4'PROC'
SLASHES  DC    CL2'//'
CA_NAME  DC    CL4'#CA-'
*----------------------------------- PARM MAPPING FOR CROESS MEM
XMPARMS  DSECT
XMASCB   DS    F                     ASCB
XMDSNS   DS    A                     ADDRESS OF DSN NAMES
XMPARMSL EQU   *-XMPARMS             LENGTH
*----------------------------------- END OF PARM AREA
         PRINT NOGEN
         TITLE 'TCB MAPPING'
         IKJTCB
         TITLE 'TIOT MAPPING'
TIOT     DSECT
         IEFTIOT1
         TITLE 'JFCB MAPPING'
JFCB     DSECT
         IEFJFCBN
         TITLE 'ASCB MAPPING'
         IHAASCB
         TITLE 'ASCB EXTENSION MAPPING'
         IHAASXB
         TITLE 'CONTENTS DIRECTORY ENTRY'
         IHACDE
         END
/*
//LKED    EXEC PGM=IEWL,REGION=1024K,COND=(4,LT),
//        PARM='XREF,LET,LIST,RENT,REUS'
//SYSPRINT     DD   SYSOUT=*
//SYSUT1       DD   DSN=&SYSUT1,UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLMOD   DD DISP=SHR,DSN=SYS1.DMCR.LINKLIB
//SYSLIN       DD   UNIT=SYSDA,DSN=&LOADSET,DISP=(OLD,DELETE)
//  DD  DDNAME=SYSIN
//SYSIN  DD *
 NAME LPROCXM(R)
/*
