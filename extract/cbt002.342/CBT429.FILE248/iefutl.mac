IEFUTL   CSECT
***********************************************************************
*                                                                     *
*        MODULE NAME =IEFUTL    PROGRAMMER - JIM BOYSEN/AMDAHL        *
*                                                                     *
*        FUNCTION:                                                    *
*           ALLOW TSO SESSIONS TO BE DISCONNECTED AND THEN 622        *
*           CANCEL ONCE DISCONNECT LIMIT HAS BEEN EXCEEDED.           *
*           EXTEND JOB TIME FOR 20 MINUTE INCREMENTS AND ISSUE        *
*           MESSAGE TO NOTIFY USER/OPERATOR OF THIS                   *
*                                                                     *
*        OPERATION: *                                                 *
*                                                                     *
*           ASSUME A 20 MINUTE DEFAULT WAIT TIME IN JWT PARMATER      *
*               OF SMFPRM00.                                          *
*                                                                     *
*        ANALYZE SMF COMMON EXIT PARAMETER AREA.                      *
*        DO A VTAM V NET COMMAND TO DISCONNECT THE TSO                *
*        USER SO THEY CAN RECONNECT BASED ON THE VALUE IN             *
*        TSOKEYXX IN SYS1.PARMLIB                                     *
*                                                                     *
*           ATTRIBUTES = SCHEDULER KEY 0, REENTRANT, SUPERVISOR       *
*               STATE, ENABLED                                        *
*                                                                     *
*           LINKAGE = FROM INITIATOR VIA MODUL IEATLEXT               *
*                                                                     *
*           INPUT = REG 1 POINTS TO A LIST OF FULL WORDS THE FIRST    *
*               OF WHICH POINTS TO THE SMF COMMON EXIT PARAMETER      *
*               AREA DESCRIPPED IN SMF SPL                            *
*                                                                     *
*           OUTPUT = REG 15 - RETURN CODE                             *
*                    REG 1 - TIME LIMIT EXTENSION                     *
*                    USER COMMUNICATION FIELD - EXTENSION COUNT, AND  *
*                    FIRST TIME X'FF' FLAG                            *
*                                                                     *
*                                                                     *
*   MODIFICATIONS:                                                    *
*                                                                     *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         LR    R11,R1              HOLD POINTER TO ADDRESS LIST FOR SMF
         LR    R5,0                 SAVE REG 0
         LA    R0,LENCON1           LENGTH OF GETMAIN REQUIRED FOR
         GETMAIN  R,LV=(0)          WTOR AND WAIT ECB AND SAVE AREA.
         LR    R7,R1                 USE 7 FOR DSECT BASE
         USING WORK,R7              SET BASE FOR DSECT
         LA    R8,SAVE
         ST    R13,4(R8)
         ST    R8,8(R13)
         LR    R9,R13                HOLD OLD SAVE ADDRESS
         LR    R13,R8
         L     R3,0(R11)             ADDR OF JOBNAME
         CH    R5,=H'8'             IS THIS WAIT TIME?
         BNE   GOOD                NO - MUST BE CPU, LETS GET OUT
         TM    29(R3),R3             IS IT FOREGROUND?
         BZ    BATCH               MUST BE BACKGROUND, LET DO BATCH
TSODISC  DS    0H
         MVC   24(4,R9),TSOTIME      EXTEND FOR 20 MINUTES
         MVC   16(4,R9),=XL4'8'      RESET FOR DISCONNECT
         ST    R5,SAVER5
         ST    R8,SAVER8
         MVC   USERID(8),BLANKS      BLANK OUT USER ID
         MVC   USERID(7),0(R3)       MOVE JOBNAME TO USER ID
         LOCJOB JOBNAME=USERID,NOFIND=LOGOFF    GET ASCB OF USER
         LR    R5,R1                 LOAD ADDRESS OF ASCB
         USING ASCB,R5               ADDRESSABILITY
         L     R8,ASCBTSB            GET ADDRESS OF TSB
         DROP  R5
         USING TSB,R8                ADDRESSABILITY
         L     R5,TSBEXTNT           GET THE TSBX ADDRESS
         DROP  R8
         USING TSBX,R5               ADDRESSABILITY
         L     R8,TSBXTVWA           GET TVWA ADDRESS
         USING TVWA,R8               ADDRESSABILITY
         MVC   CMDD,CMDC             MOVE TO DSECT AREA
         MVC   LUNAME(7),TVWAPPL+1   MOVE LU NAME TO COMMAND
         LA    R1,CMDD               LOAD PARM FOR SVC34
         SR    R0,R0
         SVC   34
         L     R5,SAVER5
         L     R8,SAVER8
         DROP  R5,R8
         B     GOOD
LOGOFF   DS    0H
         MVC   16(4,R9),=XL4'0'      LOG THEM OFF
         B     GOOD
BATCH    DS    0H                    BATCH PROCESSING
         MVC   WTOWORK,MESSAGE       MOVE WTO MESSAGE
         MVC   JOBN(8),0(R3)         MOVE THE JOB NAME
         LA    R1,WTOWORK            MOVE WTO MESSAGE
         SVC   35                    ISSUE WTO SVC
         MVC   24(4,R9),BCHTIME      EXTEND FOR 20 MINUTES
         MVC   16(4,R9),=XL4'8'      EXTEND IN SECONDS
GOOD     LR    R1,R7
         LA    R0,LENCON1
         L     R13,SAVE+4
         FREEMAIN R,LV=(0),A=(1)
         RETURN (14,12)
         TITLE 'CONSTANTS'
BLANKS   DC    CL8'        '
BCHTIME  DC    F'1200'               TIME IN SECONDS
TSOTIME  DC    F'1200'               TIME IN SECONDS
         DS    0F
CMDC     DS    0CL64
         DC    AL2(CMDENDC-*),H'0'
         DC    C'V NET,TERM,NOTIFY=NO,TYPE='
         DC    C'COND,LU1=        '
         DC    CL17' '
CMDENDC  EQU   *
MESSAGE  DS    0F
LENCON   DC    AL2(MESSLN-*)
         DC    XL2'0'              FLAGS
         DC    CL45'SMF0001 JOB XXXXXXXX  TIME EXPIRED - EXTENDED'
         DC    CL15' FOR 20 MINUTES'
MESSLN   EQU  *
MESSLEN  EQU (*-MESSAGE)
EYE      DC    AL1(L'CATCHER)
CATCHER  DC    C'IEFUTL_V1.0_&SYSDATE._&SYSTIME.'
         DC    C'JIM BOYSEN.AMDAHL'
         LTORG
         TITLE 'SAVE AREA AND DSECTS'
WORK     DSECT
SAVE     DS    18F
USERID   DS    CL8
SAVER5   DS    F
SAVER8   DS    F
CMDD     DS    0CL64
         DS    CL39
LUNAME   DS    CL8
         DS    CL17
WTOWORK  DS    0CL64
         DS    CL16
JOBN     DS    CL8
         DS    CL40
WTOLEN   EQU   *-WTOWORK
         DS    0D                  DOUBLE WORD ALINE FOR GETMAIN
LENCON1  EQU   *-WORK         LENGTH OF GETMAIN
         PRINT NOGEN
         TITLE 'MAPPING MACROS FOR TSO/VTAM AREAS'
         IHAASCB              ASCB MAPPING MACRO
         IKJTSB EXT=YES       TSB AND EXTENSION MAPPING MACRO
         IKTTVWA              TSO/VTAM WORK AREA
         END
