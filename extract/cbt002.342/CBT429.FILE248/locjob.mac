         MACRO
&NAME    LOCJOB &JOBNAME=,&ASID=,&ASCB=,&NOFIND=
         LCLB  &SW1
         LCLC  &A,&A1,&REG
&A       SETC  'IHB0'.'&SYSNDX'
&A1      SETC  'IHB1'.'&SYSNDX'
         AIF   ('&NOFIND' EQ '').ERROR1
         AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     ANOP
         AIF   ('&ASID' EQ '').TRYJN
         AIF   ('&ASID'(1,1) EQ '(').REG1
         LH    R1,&ASID      GET ASID FOR LOCATE
         AGO   .CONT1
.REG1    ANOP
&REG     SETC  '&ASID'(2,K'&ASID-2)
         LR    R1,&REG               GET ASID FOR LOCATE
.CONT1   ANOP
         LR    R0,R1         SAVE ASID FOR RETURN
         SLL   R1,2          TIMES 4 FOR LOOKUP
         L     R15,16        CVT ADDRESS (CVTPTR)
         L     R15,556(,R15) ASVT POINTER (CVTASVT)
         C     R0,516(,R15)  IS THIS ASID WITHIN MAXIMUM ??
         BH    &NOFIND       NO, MUST BE BAD
         LA    R1,528-4(R1,R15) GET ASCB SLOT ADDRESS (ASVTENTY-4)
         TM    0(R1),X'80'   IS ASCB ASSIGNED ??
         BO    &NOFIND       NO, THIS IS IN ERROR
         L     R1,0(,R1)     LOAD ASCB ADDRESS FOR RETURN
         AIF   (&SW1).END
.XXNAM   ANOP
         ICM   R2,B'1111',172(R1) JOBNAME FOR INITIATED TASKS
         BNZ   *+4+4+4+4+4+8   GOT IT HERE
         ICM   R2,B'1111',176(R1) JOBNAME FOR STRT/MNT/LOGON
         BNZ   *+4+4+4+8       THIS IS IT
         LA    R2,*+4+4        SET DEFAULT NAME
         B     *+4+8           BRANCH AROUND CONSTANTS
         DC    CL8'STARTING'
.END     ANOP
         MEXIT
.TRYJN   ANOP
         AIF   ('&JOBNAME' EQ '').TRYAS
         AIF   ('&JOBNAME'(1,1) EQ '(').REG2
         LA    R2,&JOBNAME   ADDRESS OF JOBNAME
         AGO   .CONT2
.REG2    ANOP
&REG     SETC  '&JOBNAME'(2,K'&JOBNAME-2)
         LR    R2,&REG         GET JOBNAME FOR LOCATE
.CONT2   ANOP
         L     R15,16          POINT TO CVT
         L     R15,556(,R15)   ADDRESS OF ASVT
         L     R0,516(,R15)    NUMBER OF ASCB'S
         LA    R15,528(,R15)   FIRST ASCB SLOT ADDRESS
&A       DS    0H
         TM    0(R15),X'80'    IS ASCB ASSIGNED ??
         BO    &A1             NO, GET NEXT SLOT
         L     R1,0(,R15)      GET ASCB ADDRESS
         ICM   R14,B'1111',172(R1) JOBNAME FOR INITIATED TASKS
         BNZ   *+4+4+4+4+4+8   GOT IT HERE
         ICM   R14,B'1111',176(R1) JOBNAME FOR STRT/MNT/LOGON
         BNZ   *+4+4+4+8       THIS IS IT
         LA    R14,*+4+4       SET DEFAULT NAME
         B     *+4+8           BRANCH AROUND CONSTANTS
         DC    CL8'STARTING'
         CLC   0(8,R14),0(R2)  IS THIS OUR JOBNAME ??
         BE    *+4+4+4+4       YES, GET ASID AND END....
&A1      DS    0H
         LA    R15,4(,R15)     NEXT ASCB SLOT
         BCT   R0,&A           LOOP THROUGH ALL ASCB'S
         B     &NOFIND         OUR NAME DOESN'T EXIST
         LH    R0,36(,R1)      ASID OF OUR JOB
         MEXIT
.TRYAS   ANOP
         AIF   ('&ASCB' EQ '').ERROR2
         AIF   ('&ASCB'(1,1) EQ '(').REG3
         L     R1,&ASCB      GET ASCB ADDRESS
         AGO   .CONT3
.REG3    ANOP
&REG     SETC  '&ASCB'(2,K'&ASCB-2)
         LR    R1,&REG         FET ASCB FOR LOCATE
.CONT3   ANOP
         CLC   0(4,R1),*+6+4+4 CHECK FOR ASCB ACRONYM
         BE    *+4+4+4       OK, ASSUME VALID ASCB ADDRESS
         B     &NOFIND       NO POSSIBILITY THIS IS AN ASCB
         DC    C'ASCB'       FOR ASCB CHECK
         LH    R0,36(,R1)    ASID FROM ASCB
         AGO   .XXNAM
         MEXIT
.ERROR1  MNOTE 4,'*** ERROR, NOFIND MUST BE SPECIFIED'
         MEXIT
.ERROR2  MNOTE 4,'*** ERROR, NO VALID OPERANDS SPECIFIED'
         MEND
