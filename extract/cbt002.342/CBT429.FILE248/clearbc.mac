//COM     EXEC PGM=ASMA90,REGION=512K,
//    PARM='XREF(SHORT),NODECK'
//*
//SYSLIB   DD  DISP=SHR,DSN=SYS1.MACLIB
//         DD  DISP=SHR,DSN=SYS1.MODGEN
//SYSLIN   DD  UNIT=SYSDA,DSN=&LOADSET,DISP=(MOD,PASS),
//             SPACE=(CYL,(2,2)),DCB=BLKSIZE=1680
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(10,2))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(CYL,(12,2))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(CYL,(20,2))
//SYSPUNCH DD  DUMMY =*,DCB=FUNC=I
//SYSPRINT DD  SYSOUT=*
//SYSUDUMP DD  SYSOUT=*
//SYSIN    DD  *
*********************************************************************
*                                                                   *
*  THIS PROGRAM CHANGES THE USERID IN THE PSCB TO THE SPECIFIED     *
*  USERID FOR THE PURPOSE OF CLEARING UP BUILT-UP MESSAGES IN THE   *
*  SYS1.BRODCAST DATA SET BY ISSUING THE TSO  "LISTBC" COMMAND      *
*  UNDER THE OTHER USER'S USERID.  CLEARBC MUST BE IN IKJTSOXX AS   *
*  AN AUTHORIZED COMMAND                                            *
*                                                                   *
*  BOYSEN/AMDAHL   JULY 1995                                        *
*                                                                   *
*********************************************************************
*                                                                   *
*  FIXED SMALL INTEGRITY PROBLEM WITH PLACEMENT OF MODESET. S-GOLOB *
*                                                                   *
*********************************************************************
CLEARBC  CSECT
R15      EQU   15
R14      EQU   14
R13      EQU   13                       SAVEAREA
R12      EQU   12                       BASE REGISTER
R11      EQU   11
R10      EQU   10
R9       EQU   9
R8       EQU   8
R7       EQU   7                        CPPL ADDRESS
R6       EQU   6
R5       EQU   5
R4       EQU   4
R3       EQU   3
R2       EQU   2
R1       EQU   1
R0       EQU   0
CLEARBC  CSECT
         STM   R14,R12,12(R13)          SAVE CALLERS REGS
         BASR  R12,0                    ESTABLISH ADDRESSABILITY
         USING *,12                     BASE OF EXECUTING PROGRAM
         ST    R13,SAVEAREA+4           CALLERS SAVE AREA
         LA    R15,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
         ST    R15,8(,R13)              EXECUTING PROGRAMS SAVEAREA
         LA    R13,SAVEAREA             EXECUTING PROGRAMS SAVEAREA
         LR    R7,R1                    SAVE CPPL ADDRESS
         ST    R7,CPPLADDR              SAVE CPPL ADDRESS FOR SVC99
         LA    R1,ERASE                 POINT TO ERASE CNTL CHARS
         LA    R0,L'ERASE               GET THE LENGTH
         TPUT  (1),(0),FULLSCR          ERASE THE 3270 SCREEN
*
         USING CPPL,R7
         GETMAIN R,LV=WORKLEN           GET WORK AREA
         LR    R10,R1                   SAVE AREA ADDR IN R10
         USING WORK,R10
         LA    R5,PARSPARM
         USING PPL,R5
         XC    PARSE(PARSELEN),PARSE    INITIALIZE PPL
         MVC   PPLUPT,CPPLUPT           UPT
         MVC   PPLECT,CPPLECT           ECT
         MVC   PPLCBUF,CPPLCBUF         CIB
         LA    R1,PARSECB               ECB
         ST    R1,PPLECB
         L     R1,=A(PCL)               PCL
         ST    R1,PPLPCL
         LA    R1,PARSANS               PDL
         ST    R1,PPLANS
         CALLTSSR EP=IKJPARS,MF=(E,(R5))    CALL PARSE
         DROP  R5
         L     R10,PARSANS              OK ?
         USING PDL,R10
         LTR   R10,R10
         BP    MOVEUSER                 YES, CONTINUE
         TPUT  BADPARSE,17              PUT OUT BAD PARSE MSG
         B     GOBACK
*
MOVEUSER DS    0H
         L     R3,USERPTR               LOAD PTR TO PASSED USERER NAME
         LA    R2,USERID
         LH    R1,USERLEN               LOAD LENGTH OF PARM
         BCTR  R1,0
         EX    R1,MVCPARM               MOVE THE PARM
CHG_USER DS    0H                       CHANGE USERID IN PSCB
         L     R4,PSATOLD-PSA           CURRENT TCB ADDR
         USING TCB,R4                   MAP CALLER'S TCB
         L     R2,TCBJSCB               LOAD JSCB ADDRESS
         USING IEZJSCB,R2               JSCB ADDRESSABILITY
         L     R3,JSCBPSCB              LOAD PSCB ADDRESS
         USING PSCB,R3                  PSCB ADDRESSABILITY
         TM    PSCBATR1,PSCBCTRL        ARE THEY OPER ?
         BZ    GOBACK                   NO THEY AREN'T
         MODESET KEY=ZERO,MODE=SUP      SUPERVISOR STATE/KEY ZERO
         MVC   OUSERID,PSCBUSER         SAVE OLD USER ID
         MVC   PSCBUSER,USERID          MOVE NEW USER ID
CALL_LISTBC    DS  0H
         L     R15,CVTPTR               ESTABLIST
         L     R15,CVTTVT-CVTMAP(,R15)  ADDRESSABILITY TO
         L     R15,TSVTASF-TSVT(,R15)   TSO SERVICE FACILITY
         CALL  (15),(FLAGS,CMDBUF,BUFLEN,RETCODE,RSNCODE,ABNDCODE),VL
         LTR   R15,R15                  CHECK RETURN CODE
         BNZ   BCERROR                  ERROR, GET OUT
         B     RESTORE                  RESTORE ORIGINAL ENVIRONMENT
*
BCERROR  DS    0H
         TPUT  ERRORMSG,L'ERRORMSG
RESTORE  DS    0H
         MVC   PSCBUSER,OUSERID         RESTORE OLD USED ID
         MODESET KEY=NZERO,MODE=PROB    SET PROTECT KEY TO NONZERO
GOBACK   DS    0H                       RETURN TO CALLER
         L     R13,4(,R13)              CALLERS SAVE AREA
         L     R14,12(,R13)             RESTORE R14
         LM    R0,R12,20(R13)           RESTORE REMAINING REGS
         BR    R14                      RETURN TO CALLER, REG 15
*
*        STORAGE AREA
*
SAVEAREA DS    18F
ERRORMSG DC    CL29'COMMAND TERMINATED ABNORMALLY'
EYE      DC    AL1(L'CATCHER)
CATCHER  DC    C'CLEARBC_V1.0_&SYSDATE._&SYSTIME.'
         DC    C'JIM.BOYSEN.AMDAHL'
PARMLIST IKJIFRIF MF=L
MVCPARM  MVC   0(*-*,R2),0(R3)          MOVE USERER NAME
CPPLADDR DS    F
BADPARSE DC    CL17'BAD PARSE OF PARM'
ERASE    DC    X'27F5401D40'            USED TO CLEAR 3270 TERMINAL
*
PARMADDR DS    0CL12
USERID   DC    CL7' '                   NEW USER ID
OUSERID  DC    CL7' '                   OLD USER ID
*
*        DATA AREAS FOR LISTBC COMMAND
ZERO     DC    F'0'                     ZERO CONSTANT
FLAGS    DS    0F                       MAPS FIRST PARM TO IKJEFTSR
RESFLAGS DC    H'0'                     FLAG WORD
ABFLAGS  DC    X'01'                    DUMP IF ABEND OCCURS
FNCFLAGS DC    X'01'                    TELL TSR TO EXECUTE THE COMMAND
CMDBUF   DC    C'LISTBC'                NAME OF COMMAND TO EXECUTE
BUFLEN   DC    F'6'                     LENGTH OF COMMAND
RETCODE  DS    F                        RETURN CODE FROM COMMAND
RSNCODE  DS    F                        REASON CODE
ABNDCODE DS    F                        ABEND CODE
*
PCL      IKJPARM DSECT=PDL
USER     IKJIDENT 'VALID USERID',UPPERCASE,FIRST=ALPHA,                X
               PROMPT='VALID USERID',                                  X
               MAXLNTH=7,OTHER=ALPHANUM,HELP='VALID USERID'
         IKJENDP
         TITLE 'COMMAND PROCESSOR PARAMETER LIST'
         IKJCPPL
         TITLE 'GETMAIN WORK AREA'
WORK     DSECT
PARSE    EQU   *
PARSPARM DS    7A                       PPL
PARSECB  DS    F                        ECB
PARSANS  DS    A                        PDL
PARSELEN EQU   *-PARSE
WORKLEN  EQU   *-WORK
         TITLE 'PARSE PARAMETER LIST'
         IKJPPL
PDL      DSECT
         ORG   USER
USERPTR  DS    A                       ADDRESS OF USERID
USERLEN  DS    H                       LENGTH OF USERID
USERFLAG DS    X                       FLAGS
USERFND  EQU   X'80'                   USERID PRESENT
*
         PRINT OFF
         TITLE '*** CVT ***'
         CVT  DSECT=YES
         TITLE '*** TSVT ***'
         IKJTSVT
         TITLE '*** PSA ***'
         IHAPSA
         TITLE '*** PSCB ***'
         IKJPSCB
         TITLE '*** JSCB ***'
         IEZJSCB
         TITLE '*** TCB ***'
         IKJTCB
         END
/*
//LKED    EXEC PGM=IEWL,REGION=1024K,COND=(4,LT),
//        PARM='XREF,LET,LIST'
//SYSPRINT     DD   SYSOUT=*
//SYSUT1       DD   DSN=&SYSUT1,UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLMOD   DD DISP=SHR,DSN=YOUR.LINKLIB
//SYSLIN       DD   UNIT=SYSDA,DSN=&LOADSET,DISP=(OLD,DELETE)
//  DD  DDNAME=SYSIN
//SYSIN  DD *
 NAME CLEARBC(R)
/*
