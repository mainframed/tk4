/*REXX*/ 
/*****************************************************************/
/*LISTTAPE: REXX program to display all online TAPE devices.     */
/*    Note: Developed and tested on an MVS/SP 2.2.3 system.      */
/*****************************************************************/
/* MVS DATA AREAS:                                               */
/*                CVT - Communications Vector Table (@'10'x)     */
/*                UCB - Device Unit Control Block                */
/*                                                               */
/*                                                               */
/*                                                               */
/*****************************************************************/
@cvt = STORAGE(10,4)                  /* get CVT address from PSAAOLD */
@cvt = D2X(C2D(@cvt))                    /* binary to dec to hex      */
@cvtucba  = STORAGE(D2X(X2D(@cvt)+1252),4)   /* get CVTUCBA address   */
@cvtucba  = D2X(C2D(@cvtucba))                 /* binary to dec to hex*/
Address TSO ; "CLEAR"
Say 'CVT: '@cvt' CVTUCBA: '@cvtucba' ===> TAPE UCB scan started: 'TIME()
Say ' '
@ucbchn = @cvtucba
i = 0 ; endofucb = 0 ; j = 0
Do While endofucb = 0 & i < 10000
   i = i + 1
   ucb     = STORAGE(@ucbchn,24)    /* get 24 byte ucb common segment */
   ucbid   = SUBSTR(ucb,3,1)                 /* get the 'FF'x id byte */
   ucbstat = SUBSTR(ucb,4,1)              /* status byte '80'x online */
   ucbchan = SUBSTR(ucb,5,2)                /* channel address in hex */
   ucbnxucb= SUBSTR(ucb,9,4)                   /* pointer to next ucb */
   ucbtbyt3= SUBSTR(ucb,19,1)                 /* device '80'x is TAPE */
   ucbtbyt4= SUBSTR(ucb,20,1)           /* contains device type codes */
   If (BITAND(ucbstat,'80'x)='80'x & BITAND(ucbtbyt3,'80'x)='80'x) Then Do
      j = j + 1; device = 'Unknown'
      ioaddr = C2X(ucbchan)
      If BITAND(ucbtbyt4,'80'x)='80'x Then device = '3480   '
      If BITAND(ucbtbyt4,'01'x)='01'x Then device = '2400   '
      If BITAND(ucbtbyt4,'03'x)='03'x Then device = '3420   '
      If BITAND(ucbtbyt4,'81'x)='81'x Then device = '3490E  '
      @ucbchn=D2X(X2D(@ucbchn)+24)            /* bump to TAPE segment */
      ucbtape = STORAGE(@ucbchn,24)       /* get 24 byte TAPE segment */
      ucbfser = SUBSTR(ucbtape,5,6)              /* volser if mounted */
      If C2X(ucbfser) = '000000000000' Then ucbfser = '-idle-'
      Say RIGHT(j,3,' ') RIGHT(ioaddr,3) STRIP(device) ucbfser
   End
   @ucbchn = D2X(C2D(ucbnxucb))
   If @ucbchn = 0 Then endofucb = 1    /* zero address ends chain */
End
Say ' '
Say 'Last UCB after 'i' blocks scanned.  Completed: 'TIME()
Exit
