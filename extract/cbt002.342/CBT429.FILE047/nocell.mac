./       ADD   NAME=$$$INDEX
THE MEMBERS DISTRIBUTED IN THIS PDS ARE:
(THOSE MARKED '*' ARE COPIED INTO THE MAINLINE BY THE ASSEMBLY.
 THOSE MARKED 'E' ARE DISTRIBUTED EMPTY.)

  $$DOC     * - HEADER COMMENTS. COPIED INTO THE ASSEMBLY.
  $AMODE      - MACRO TO ALTER MVS/XA ADDRESSING MODE.
  $CALL       - MACRO FOR INTERNAL SUBROUTINE CALL.
  $CHANGES  * - HISTORY OF CHANGES MADE TO NOCELL.
  $COPY       - IEBCOPY CONTROL CARDS USED TO INSTALL.
  $DOC        - USER'S GUIDE. (IN DCF FORMAT).
  $EPILOG     - EXIT LINKAGE MACRO.
  $INSTALL    - JCL TO INSTALL NOCELL.
  $JCL        - SAMPLE JCL TO EXECUTE NOCELL.
  $OBJ        - AN OBJECT DECK (IF YOU DON'T WANT TO ASSEMBLE IT).
  $PROLOG     - ENTRY LINKAGE MACRO.
  $REGS       - REGISTER EQUATES.
  $TEST       - MACRO TO APPLY A TABLE BASED SELECTION TEST.
  $TRACE      - MACRO TO CREATE A TRACE TABLE ENTRY.
  $TRCTABL    - MACRO TO GENERATE THE TRACE TABLE CSECT.
  ALLOC       - MACRO TO CALL DYNAMIC ALLOCATION.
  ARCHCAT   * - ROUTINE TO PASS THE ASM2 ARCHIVE CATALOG.
  CLIST       - A CLIST TO INVOKE THE NOCELL ISPF DIALOG.
  DYNSPACE    - MACRO TO GENERATE A WORKAREA FOR DYNAMIC ALLOCATION.
  EDIT        - MACRO TO GENERATE ED INSTRUCTIONS.
  FREE        - MACRO TO DYNAMICALLY UNALLOCATE THINGS.
  HEX         - MACRO TO HEX TRANSLATE DATA FOR PRINTING.
  HEXVMF    * - ROUTINE TO HEX PRINT TLMSII VMF RECORDS.
  MESGRTN   * - ROUTINE TO PRINT DIAGNOSTIC MESSAGES.
  NCHELP      - ISPF HELP PANEL.
  NCH0        - ISPF HELP PANEL.
  NCH0A       - ISPF HELP PANEL.
  NCH1        - ISPF HELP PANEL.
  NCH10       - ISPF HELP PANEL.
  NCH11       - ISPF HELP PANEL.
  NCH12       - ISPF HELP PANEL.
  NCH13       - ISPF HELP PANEL.
  NCH14       - ISPF HELP PANEL.
  NCH15       - ISPF HELP PANEL.
  NCH16       - ISPF HELP PANEL.
  NCH17       - ISPF HELP PANEL.
  NCH18       - ISPF HELP PANEL.
  NCH19       - ISPF HELP PANEL.
  NCH2        - ISPF HELP PANEL.
  NCH20       - ISPF HELP PANEL.
  NCH21       - ISPF HELP PANEL.
  NCH22       - ISPF HELP PANEL.
  NCH23       - ISPF HELP PANEL.
  NCH24       - ISPF HELP PANEL.
  NCH3        - ISPF HELP PANEL.
  NCH4        - ISPF HELP PANEL.
  NCH5        - ISPF HELP PANEL.
  NCH6        - ISPF HELP PANEL.
  NCH7        - ISPF HELP PANEL.
  NCH8        - ISPF HELP PANEL.
  NCH9        - ISPF HELP PANEL.
  NCISPF      - ISPF INVOCATION PANEL FOR NOCELL DIALOG.
  NOCELL      - MAINLINE ROUTINE.
  NOCELL1   * - REPORT1 ROUTINE.
  NOCELL4   * - REPORT4 ROUTINE.
  NOCELL99    - ISPF PANEL SAYING NO DATASETS SELECTED.
  OPTIONS   * - SUBROUTINE TO LIST OPTIONS IN EFFECT.
  PARMS     * - SUBROUTINE TO READ SYSIN PARAMETERS, SET FLAGS ETC.
  PASSVMF   * - SUBROUTINE TO READ THE TLMSII VOLUME MASTER FILE.
  PRINT     * - PRINT SUBROUTINE.
  QKSRT1      - MACRO TO GENERATE IN-CORE SORT ROUTINES.
  RCPDDN      - INNER MACRO TO ALLOC/FREE.
  RCPDINC     - INNER MACRO TO ALLOC/FREE.
  RCPDISP     - INNER MACRO TO ALLOC/FREE.
  RCPFDDN     - INNER MACRO TO ALLOC/FREE.
  RCPSR2      - INNER MACRO TO ALLOC/FREE.
  RCPSYSOU    - INNER MACRO TO ALLOC/FREE.
  RCPUNALC    - INNER MACRO TO ALLOC/FREE.
  RCPUNIT     - INNER MACRO TO ALLOC/FREE.
  RCPVCHAR    - INNER MACRO TO ALLOC/FREE.
  RETDSNVL    - OBJECT DECK FOR A PROGRAM NEEDED BY THE ISPF DIALOG.
  STUB        - SAMPLE OF A STUB TO REPLACE ARCHCAT OR PASSVMF.
  UNITNAME    - SUBROUTINE TO TRANSLATE AN ESOTERIC UNITNAME INTO THE
                VOLSERS IT CORRESPONDS TO.
  VMFBASE  E* - TLMSII RECORD LAYOUT.
  VMFMDS   E* - TLMSII RECORD LAYOUT.
  VTOC      * - SUBROUTINE TO ALLOCATE AND READ VTOC'S.
  XPRCLOSE    - PRINT MACRO: CLOSE A PRINT FILE
  XPRDCB      - PRINT MACRO: GENERATE A DCB ETC.
  XPREJECT    - PRINT MACRO: SKIP TO A NEW PAGE.
  XPRHEAD     - PRINT MACRO: DEFINE A REPORT HEADING.
  XPRINNRA    - PRINT MACRO: INNER.
  XPRLDEF     - PRINT MACRO: DEFINE A PRINT LINE LAYOUT.
  XPRLIST     - PRINT MACRO: PRINT SEVERAL LINES.
  XPRMOD      - PRINT MACRO: CHANGE PRINTER OPTIONS IN MID-STREAM
  XPRNTLIN    - PRINT MACRO: PRINT A SINGLE LINE.
  XPROPEN     - PRINT MACRO: OPEN A PRINT FILE.
  XPRSPACE    - PRINT MACRO: SKIP LINES.
./       ADD   NAME=$$DOC
         TITLE 'DISCLAIMER - INSTALLATION INSTRUCTIONS'
***********************************************************************
*                                                                     *
*     THIS PROGRAM, DEVELOPED AT CONTINENTAL BANK OF CANADA,          *
*     MANUFACTURER'S LIFE INSURANCE AND SUNCOR OR SUPPLIED BY OTHER   *
*     USERS ON A NON-RESTRICTIVE BASIS, IS OF GENERAL INTEREST AND IS *
*     SUBMITTED FOR UNRESTRICTED DISTRIBUTION.                        *
*                                                                     *
*                                                                     *
*                                                                     *
*     THE AUTHOR IS CURRENTLY WITH THE TORONTO HYDRO ELECTRIC SYSTEM. *
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO THES.  HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*                                                                     *
*                                                                     *
*              JIM LANE                                               *
*              TECHNICAL SUPPORT GROUP                                *
*              TORONTO HYDRO ELECTRIC SYSTEM                          *
*              14 CARLTON ST                                          *
*              TORONTO, ONTARIO                                       *
*                                                                     *
*                PHONE: (416)-599-0400                                *
*                                                                     *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY THES WITHOUT CHARGE OR        *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
*                                                                     *
***********************************************************************
 EJECT ,
***********************************************************************
*                                                                     *
* NAME         NOCELL                                                 *
*                                                                     *
*                                                                     *
*  INTRODUCTION                                                       *
*  ____________                                                       *
*                                                                     *
*  NOCELL IS A UTILITY THE PURPOSE OF WHICH IS TO ANALYZE ALLOCATED   *
*  DATASETS.  THE  PROGRAM  CAN  PROCESS ALL DATASETS OR SELECT A     *
*  SUBSET, AND PRODUCE DETAIL LISTINGS OR SUMMARY REPORTS.            *
*                                                                     *
*                                                                     *
*                                                                     *
*  JCL REQUIREMENTS.                                                  *
*  _________________                                                  *
*                                                                     *
*  NOCELL CAN BE EXECUTED USING THE FOLLOWING JCL:                    *
*                                                                     *
*    //STEP     EXEC PGM=NOCELL,REGION=4096K                          *
*    //STEPLIB  DD DSN=<YOUR.LOADLIB>,DISP=SHR                        *
*    //SYSUT1   DD DSN=<HSM.MCDS>,DISP=SHR                            *
*    //SYSUT2   DD DSN=<HSM.BCDS>,DISP=SHR                            *
*    //VMF      DD DSN=<TLMSII.VMF>,DISP=SHR                          *
*    //TMC      DD DSN=<CA1.TMC>,DISP=SHR                          @TEC
*    //SYSUDUMP DD SYSOUT=*                                           *
*    //SYSIN    DD *                                                  *
*    /*                                                               *
*                                                                     *
* INSTALLATION  JCL TO INSTALL NOCELL IS CONTAINED IN MEMBER $INSTALL *
*               WHICH ASSEMBLES AND LINKS THE CODE AND COPIES THE     *
*               ELEMENTS OF THE ISPF DIALOG TO THE PROPER LIBRARIES   *
*                                                                     *
* DOCUMENTATION A USER'S GUIDE COMPLETE WITH JCL EXAMPLES IS IN       *
*               MEMBER $DOC. YOU WILL NEED IBM'S DCF TO PRINT THIS    *
*               MEMBER.                                               *
*                                                                     *
*                                                                     *
***********************************************************************
 EJECT ,
***********************************************************************
* ENVIRONMENT  MVS/SP 3.1.3                                           *
*              DF/HSM 2.5.0                                           *
*              TLMSII 5.3                                             *
*              CA-1 4.9                                            @TEC
*                                                                     *
* AUTHOR       THE PORTIONS OF THIS PROGRAM HAVING TO DO WITH         *
*              MANIPULATING THE DSCB'S AND MASSAGING THE HSM AND TLMS *
*              DATASETS WERE WRITTEN BY J. LANE AT CBOC.  A LOT OF    *
*              THE REST OF THE CODE HAS BEEN BORROWED FROM VARIOUS    *
*              SOURCES, MOSTLY OFF THE CBT TAPE. IF SOMETHING IN      *
*              HERE LOOKS FAMILIAR, IT'S PROBABLY BECAUSE YOU WROTE   *
*              IT.  THE CODE FOR TRANSLATING ESOTERIC UNITNAMES TO    *
*              VOLUME SERIALS WAS BORROWED FROM A PROGRAM CALLED      *
*              TSDYNLXA FROM FILE127 OF THE CBT TAPE.                 *
*                                                                     *
*                                                                     *
*                                                                     *
* REGISTER USAGE                                                      *
*                                                                     *
*              R0  - WORK REGISTER                                    *
*              R1  - BASE ADDRESS FOR HSM RECORDS.                    *
*                  - CURRENT TRACE TABLE ENTRY.                       *
*              R2  - ADDRESS OF TEXT TO BE INSERTED INTO A MESSAGE.   *
*                  - INCREMENT FOR LOOPING OVER TRACE TABLE.          *
*                  - CVT ADDRESS IN MAINLINE.                         *
*              R3  - CURRENT ADDRESS FOR LOOPING OVER SAVEFMT         *
*                  - UCB ADDRESS IN MAINLINE.                         *
*                  - HIGHEST ENTRY IN TRACE TABLE.                    *
*              R4  - ADDRESS OF CURRENT ENTRY IN SAVEFMT TABLE        *
*                  - INCREMENT VALUE FOR LOOPING OVER SAVEFMT         *
*              R5  - DSCB BASE ADDRESS.                               *
*                  - STOP ADDRESS FOR LOOPING OVER SAVEFMT.           *
*                  - HSM RECORD BASE ADDRESS.                         *
*              R6  - BASE ADDRESS FOR VMF RECORDS.                    *
*              R7  - WORK REGISTER                                    *
*              R8  - WORK REGISTER                                    *
*              R9  - WORK REGISTER                                    *
*              R10 - INTERNAL SUBROUTINE LINKAGE.                     *
*              R11 - BASE ADDRESS OF DATASECT                         *
*              R12 - PROGRAM BASE REGISTER 1                          *
*              R13 - SAVE AREA ADDRESS                                *
*              R14 - LINKAGE                                          *
*              R15 - LINKAGE                                          *
*                                                                     *
***********************************************************************
 EJECT ,
***********************************************************************
*                                                                     *
*      NOCELL CONSISTS OF THE FOLLOWING CSECTS:                       *
*                                                                     *
*   NOCELL    MAINLINE CODE. OPEN FILES, ALLOCATE THE DATASET TABLE,  *
*             CALL OTHER ROUTINES, AND PROCESS ONLINE VTOCS           *
*   ISPFINIT  DETERMINE IF NOCELL IS RUNNING UNDER ISPF AND, IF SO,   *
*             INITIALIZE FOR RUNNING AS A DIALOG                      *
*   ISPFPARM  DISPLAY PANELS TO DETERMINE OPTIONS TO BE USED IN       *
*             SELECTING DATASET. VALIDATE ENTERED OPTIONS AND SET     *
*             FLAGS. ISPF EQUIVALENT TO CSECT PARMS FOR BATCH         *
*   ISPFLIST  BUILD AND DISPLAY AN ISPF TABLE CONTAINING A 1-UP       *
*             LISTING OF SELECTED DATASET. ISPF EQUIVALENT TO         *
*             THE BATCH CSECT LISTOUT.                                *
*   LISTOUT   PRODUCE DETAILE LISTING OF SELECTED DATASETS.           *
*   NOCELL1   PRODUCE REPORT1.                                        *
*   NOCELL2   PRODUCE REPORT2.                                        *
*   NOCELL4   PRODUCE REPORT4.                                        *
*   PASSVMF   READ THE TLMSII VOLUME MASTER FILE AND UPDATE THE       *
*             DATASET TABLE WITH INFO ON TAPE DATASETS.               *
*   PASSMCDS  READ THE HSM MIGRATION CONTROL DATASET AND UPDATE THE   *
*             DATASET TABLE WITH INFO ON MIGRATED DATASETS.           *
*   HEXVMF    PRINTS THE CONTENTS OF TLMSII VMF RECORDS IN HEX DUMP   *
*             FORMAT.                                                 *
*   UNITNAME  TRANSLATES ESOTERIC UNITNAMES INTO THE VOLSERS OF THE   *
*             DASD DEVICES THEY CORRESPOND TO.                        *
*   PARMS     OPEN SYSIN, READ AND SYNTAX CHECK CONTROL CARDS         *
*             BUILDING TABLES AND SETTING FLAGS ACCORDINGLY.          *
*   OPTIONS   PRINTS MESSAGES LISTING THE OPTIONS IN EFFECT.          *
*   MESGRTN   PRINT DIAGNOSTIC MESSAGES.                              *
*   MESSAGES  CONTAINS MESSAGE TEXT.                                  *
*   SORT1     SORT THE DATASET TABLE IN ORDER BY DSNAME.              *
*   SORT2     SORT THE DATASET TABLE IN ORDER BY SIZE.                *
*   SORT3     SORT THE DATASET TABLE BY DSNAME WITHIN VOLSER.         *
*   XVTCREAD  OPEN, READ AND CLOSE A VTOC. ORIGINALLY PRODUCED BY THE *
*             MIT INSTRUMENTATION LAB.                                *
*   XPRNTSUB  REPORT PRINTING SUBROUTINE. HANLDLES PAGE EJECTS,       *
*             TITLES, NUMBERING ETC.                                  *
*   XDATEDIT  FORMAT A DATE.                                          *
*   DATASECT  DATA AREAS SHARED BETWEEN CSECTS.                       *
*   TRACTABL  INTERNAL TRACE TABLE.                                   *
***********************************************************************
 EJECT ,
***********************************************************************
*                                                                     *
* THE FOLLOWING NON-IBM MACROS ARE USED IN NOCELL:                    *
*                                                                     *
*     $AMODE,$CALL,$EPILOG,$OPT1,$OPT2,$OPT3,                         *
*     $PROLOG,$REGS,$TEST,$TRCTABL,ALLOC,DYNSPACE,EDIT,FREE,          *
*     HEX,MESG,QKSRT1,XPRCLOSE,XPRDCB,XPREJECT,XPRHEAD,XPRLDEF,       *
*     XPRNTLIN,XPROPEN                                                *
*                                                                     *
*                                                                     *
* THE FOLLOWING IBM MACROS ARE USED IN NOCELL:                        *
*                                                                     *
*     ABEND,ACB,CAMLST,CLOSE,CVT,DCB,DEVTYPE,                         *
*     ESTAE,EXCP,FREEMAIN,FREEPOOL,GET,GETMAIN,                       *
*     IECSDSL1,IEFJESCT,IEFUCBOB,IEFZB4D0,IEFZB4D2,IHASDWA,           *
*     IKJTCB,LOCATE,OBTAIN,OPEN,PUT,RDJFCB,RETURN,                    *
*     RPL,SAVE,SETRP,SYNADAF,SYNADRLS,TIME,WAIT,WTO                   *
*                                                                     *
***********************************************************************
 EJECT ,
***********************************************************************
*                                                                     *
* NOTES                                                               *
*                                                                     *
*        THE DISTRIBUTED PDS CONTAINS ALL THE SOURCE NECESSARY TO     *
*        ASSEMBLE NOCELL WITH THE FOLLOWING EXCEPTIONS:               *
*                                                                     *
*         VMFDSECT - DSECTS FOR TLMSII RECORD FORMATS.                *
*                                                                     *
*        I "OBTAINED" THE TLMS DSECTS FROM THE TLMSII DSECTLIB.  I    *
*        DID NOT INCLUDE THEM HERE ON THE ASSUMPTION THAT THEY ARE    *
*        PROPRIETARY. YOU CAN GET YOURS THE WAY I GOT MINE (IF YOU    *
*        NEED THE FUNCTION) OR REPLACE EITHER OR BOTH OF THE          *
*        DISTRIBUTED MEMBER PASSVMF WITH A BRANCH-BACK STUB.          *
*        OTHERWISE SEE MEMBER $OBJ FOR AN OBJECT DECK.                *
*                                                                     *
*                                                                     *
***********************************************************************
./       ADD   NAME=$$TEST
//JIMLANE  JOB (,MDOM00-0000),LANE,NOTIFY=JIMLANE,MSGCLASS=A,
//         CLASS=X
/*JOBPARM T=1,L=99,R=N0GA
//NOCELL EXEC NOCELL,REGION=4096K
//STEPLIB  DD DSN=JIMLANE.SYSTEMS.LOAD,DISP=SHR
//SYSPRINT DD SYSOUT=*
//ABNLDUMP DD DUMMY
//SYSUDUMP DD SYSOUT=*
//SYSIN    DD *
LIST.
UNIT(TEST).
LEVEL(JIMLANE).
ENDREQ.
LIST.
VOLUME(TSO).
LEVEL(VARGA).
./       ADD   NAME=$AMODE
         MACRO
&LABEL  $AMODE &MODE,&REG=R2,&SAVE=,&RESET=
&SP      SETC  'SP'.'&SYSNDX'
         AIF   (T'&MODE EQ 'O').MODEOK
         AIF   ('&MODE' EQ '24' OR '&MODE' EQ '31').MODEOK
         MNOTE 12,'*** INVALID AMODE SPECIFIED.'
         MEXIT
.MODEOK  ANOP
         AIF   (T'&LABEL EQ 'O').NOLABEL
&LABEL   DS    0H
.NOLABEL ANOP
         L     &REG,16
         TM    CVTDCB-CVTMAP(&REG),CVTMVSE
         BZ    &SP
         AIF   (T'&SAVE EQ 'O').NOSAVE
         SR    &REG,&REG
         BSM   &REG,0
         ST    &REG,&SAVE
.NOSAVE  ANOP
         AIF   (T'&RESET EQ 'O').SET
         L     &REG,&RESET
         O     &REG,=AL4(*+6)
         BSM   0,&REG
         AGO   .AMODE99
.SET     ANOP
         AIF   ('&MODE' EQ '31').TO31
         L     &REG,=AL4(*+6)
         BSM   0,&REG
         AGO   .AMODE99
.TO31    ANOP
         L     &REG,=AL4(*+6+X'80000000')
         BSM   0,&REG
.AMODE99 ANOP
&SP      DS    0H
         MEND
./       ADD   NAME=$ASM
//TSGJCL  JOB T50000,LANE,
//         NOTIFY=TSGJCL,MSGCLASS=X,CLASS=X
//ASSEMBLE PROC DISTR='TSGJCL.SYSTEMS.NOCELL',
//             LOAD='TSGJCL.SYSTEMS.LOAD'
//*
//*
//* ASSEMBLE NOCELL
//*
//ASM      EXEC  PGM=IEV90,
//         PARM='OBJECT,BATCH,XREF(SHORT)',REGION=2048K
//SYSLIB   DD  DISP=SHR,DSN=&DISTR,DCB=BLKSIZE=32720
//         DD  DISP=SHR,DSN=SYS1.AMODGEN
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(9,1))
//SYSPUNCH DD  DUMMY
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  UNIT=SYSDA,SPACE=(TRK,(15,15,1),RLSE),DISP=(,PASS)
//SYSIN    DD  DISP=SHR,DSN=&DISTR(NOCELL)
//*
//* LINKEDIT NOCELL
//*
//LINK     EXEC PGM=LINKEDIT,PARM='LET,LIST,MAP,XREF,NCAL'
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=VIO,SPACE=(CYL,1)
//SYSLIN   DD  DSN=*.ASM.SYSLIN,DISP=(SHR,PASS)
//SYSLMOD  DD  DISP=SHR,DSN=&LOAD(NOCELL)
//*
//ENDPROC  PEND
//*
//ASSEMBLE EXEC ASSEMBLE
//
./       ADD   NAME=$CALL
         MACRO
&LABEL   $CALL &ROUTINE
         L     R15,=V(&ROUTINE)
         BALR  R14,R15
         MEND
./       ADD   NAME=$CHANGES
 TITLE 'CHANGE ACTIVITY LOG '                                      @164
*
* CHANGE ACTIVITY SUMMARY:
*
*    @PKT - FIX A BUG IN REPORT4 WHEREBY THE LAST DATASET OF EACH
*           LEVEL WAS BEING SKIPPED. AS REPORTED BY P.KAMAT OF
*           MANULIFE
*    @339 - ADD DEVICE CONSTANTS FOR 3390'S.
*    @TEC - ADD SUPPORT FOR CA-1 TAPE MANAGEMENT SYSTEM
*         - EXTERNALIZE LINES PER PAGE. OPTION PAGELEN(NN).
*    @170 - FIX ERRORS DISCOVERED AFTER AUTHOR'S MOVE TO SUNCOR:
*         . TAPES CAN APPARENTLY BE IN-SERVICE,NON-SCRATCH WITHOUT A
*           VALID DATASET NAME OR ANYTHING. ELIMINATE BY CHECKING FOR
*           A FILE SEQUENCE NUMBER OF ZERO.
*         . IN REPORT4 LOGIC, DSN=AA. FOLLOWED BY DSN=AAB. WAS NOT
*           BEING RECOGNIZED AS A CHANGE IN HIGH-LEVEL QUALIFIER.
*         . FIX A FORMATTING ERROR WITH THE TOTAL LINES IN REPORT2.
*         . DEVICE TYPE FIELD IN THE SAVEFMT TABLE WAS NOT BEING FILLED
*           IN FOR HSM MIGRATED DATASETS. THIS WAS CAUSING MIGRATED
*           AMOUNTS NOT TO BE SHOWN IN REPORT1 LOGIC.
*         . FIX A FORMATTING ERROR WITH THE TOTAL LINES IN LIST OUTPUT.
*           HIGH ORDER DIGITS WERE BEING DROPPED WHEN BREAKLEVEL IS OFF
*         . CHANGE LOGIC IN PASSMCDS TO READ MCD RECORDS DIRECTLY.
*           PREVIOUSLY THE SEQUENTIAL READ WAS SCANNING FOR MIGRATED
*           COPY RECORDS AND USING THE ASSOCIATED "REAL" NAME FOR THE
*           KEY OF A SECOND DIRECT READ OF THE MCD RECORD. THIS WAS
*           BECAUSE NOCELL ORIGINALLY HANDLED HSM BY IDENTIFYING
*           MIGRATED DATASETS IN LEVEL 1 VTOCS BY NAME AND USING THAT
*           AS THE MCDS LOOK UP ARGUMENT.
*           ASIDE FROM ALL THE EXTRA I/O THIS IS A BUG BECAUSE IT WAS
*           CAUSING DATASETS MIGRATED INTO AN SDSP TO BE SKIPPED
*           THEY DON'T HAVE MIGRATED COPY RECORDS.
*         . DATASETS WHICH HAVE BEEN MIGRATED AND THEN RECALLED CAN
*           STILL HAVE MIGRATED COPIES AROUND AND, THEREFORE, SHOW UP
*           IN THE MCDS. ADDED A TEST FOR A FLAG BYTES TO DETECT THIS
*           AND SKIP THE DATASET.
*    $NTC - FIX BUG REPORTED BY NORTHERN TRUST COMPANY. THE UNITNAME
*           LOGIC BUILDS A LIST OF VOLSERS IN THE SAME TABLE USED BY
*           A VOLUME REQUEST. AN ESOTERIC WITH A LOT OF UNITS WAS
*           OVERFLOWING THE VOLUME TABLE.
*         - MADE ROOM FOR THE ABOVE BY TAKING STUFF OUT OF DATASECT
*    $EDT - UPGRADE UNITNAME LOGIC TO WORK ON DFP/XA 2.3.0
*    @166 - FIX BUGS IN REBLOCKING RECOMMENDATION REPORT.
*         - DON'T LIST DATASETS IN REPORT2 IF NOTHING WOULD CHANGE.
*         - LOGIC FOR UNUSED WAS TREATING NEVER USED DATASETS
*           INCORRECTLY. THE CREATE DATE SHOULD BE USED AS REF DATE.
*         - EXCLUDE RECFM=U DATASETS FROM REPORT2.
*         - DON'T RECOMMEND REBLOCKING IF THERE WOULD BE NO SPACE SAVED
*           AND MORE BLOCKS.
*         - FIX BUG REPORTED BY STANLEY WARD (TUFTS UNIVERSITY):
*           NON-XA GETMAIN WAS DESTROYING HARDCODED INCREMENT VALUE
*           IN THE WORD FOLLOWING "TABSTART". SYMPTOM WAS THAT ONE, AND
*           ONLY ONE, DATASET APPEARED ON LIST OUTPUT. OOPS!
*         - EXPAND TOTAL FIELDS FOR TRACKS ALLOCATED AND USED PER LEVEL
*           TO 6 DIGITS. WITH 3380K'S 100000 TRACKS ISN'T WHAT IT USED
*           TO BE.
*         - FIX ABEND0C4 DURING LOOKUP OF INTERNAL DSORG TABLE.
*           LOGIC IN PARMS WAS NOT SETTING END OF TABLE.
*    @165 - ADD SUPPORT FOR SELECTION OF MIGRATED DATASETS BASED ON
*           LEVEL 1 VS LEVEL 2. OPTION HSM CAN NOW ALSO BE SPECIFIED
*           AS HSM(ML1). OR HSM(ML2). THE OLD FORM MEANS BOTH.
*         - FIX BUG REPORTED BY TIM HOLLAND (STATE OF OKLAHOMA):
*           SPECIFYING A NON-EXISTING ESOTERIC UNIT WAS CUASING AN 0C4
*         - ADD SELECTION FOR CHANGED DATASETS ON DISK.
*         - ADD SELECTION BY DSORG.
*         - FIX SEVERAL MISCELLANEOUS BUGS:
*         . SPECIFY 24 DATA BUFFERS ON ACBMCDS TO SPEED UP THE DIALOG
*         . ADD EXTRA CHECKING CODE TO AVOID 0C9 ABENDS IN NOCELL2.
*         . ADD LOGIC TO HANDLE THE CASE WHERE ENDREQ IS THE LAST
*           THING IN THE INPUT STREAM. PREVIOUSLY AN EXTRA LOGICAL
*           REQUEST WAS BEING DONE WITH ALL DEFAULTS IN EFFECT. NOW
*           A FINAL ENDREQ IS TREATED AS END OF JOB.
*         . ZERO REPORT FLAGS AFTER ENDREQ. OOPS!
*         . ZERO SUBTOTAL FIELDS AT START OF LIST PROCESSING IF
*           BREAKLEVEL ISN'T IN EFFECT. TOTALS FOR EACH LOGICAL REQUEST
*           WERE COMING OUT CUMULATIVE.
*         . ADD LOGIC TO LISTOUT TO TEST FOR MULTIPLE LOGICAL REQUESTS
*           AND NOT FREE DDNAME SYSPRINT IF THERE COULD STILL BE MORE
*           LIST OUTPUT TO FOLLOW. LIST OUTPUTS WERE BEING PRINTED
*           INDIVIDUALLY BETWEEN DIFFERENT SEPARATOR PAGES.
*         . OPTION REPORT4 WAS NOT BEING LISTED ON THE "OPTIONS IN
*           EFFECT" LIST.
*         . IN REPORT4 PROCESSING THE LAST (OR ONLY, WHICH IS HOW IT
*           BECAME OBVIOUS) HIGH-LEVEL QUALIFIER WAS BEING SKIPPED.
*         . BREAKLEVEL PROCESSING WAS PRODUCING AN EXTRA BLANK PAGE AT
*           THAT START DURING THE 2ND OR SUBSEQUENT LOGICAL REQUEST.
*           FLAG "FIRSTQ" WASN'T BEING RESET AFTER ENDREQ.
*    @164 - REWORK HSM LOGIC IN SUPPORT OF DF/HSM 2.1.0
*           INSTEAD OF CHECKING EVERY DASD DATASET TO SEE IF IT IS AN
*           HSM MIGRATED COPY, WE NOW PASS THE ENTIRE MCDS IF THE HSM
*           OPTION IS REQUESTED AND SELECT ANY MIGRATED DATASETS THAT
*           SEEM TO APPLY. THIS MEANS THAT ML1 DATASETS IN A
*           SMALLDATASETPACKING DATASET CAN NOW BE SELECTED. ALSO
*           "HSM,NODASD" IS NOW A VALID COMBINATION, LISTING ONLY
*           MIGRATED DATASETS.
*         - IDENTIFY UNMOVEABLE DATASETS AS SUCH ON LIST OUTPUT.
*    @163 - ADD REPORT2 THE "REBLOCKING RECOMMENDATION" REPORT FOR
*           ALL SELECTED DSORG=PS DASD DATASETS.
*    @162 - ADD CONTROL CARD "ENDREQ." TO DELIMIT A SERIES OF LOGICAL
*           REQUESTS IN ONE INPUT STREAM.
*         - ADD OPTION "UNIT(...,....)." TO ALLOW SELECTION BASED ON
*           ESOTERIC UNIT NAME.
*         - ADD OPTION "CREATEDBY(...)." AND "READBY(...)." TO ALLOW
*           SELECTION OF TAPE DATASETS BASED ON THE JOB THAT CREATED OR
*           MOST RECENTLY READ THE TAPE.
*         - CODED MACRO $TEST TO GENERATE LOGIC TO APPLY SELECTION
*           TESTS BASED ON A TABLE OF POSSIBILITIES.
*         - MOVE CODE FOR PRODUCING LIST OUTPUT INTO A NEW CSECT CALLED
*           LISTOUT IN ORDER TO PRESERVE ADDRESSABILITY IN THE MAINLINE
*         - ADD MACRO $AMODE TO CHANGE FROM 24 TO 31 BIT IN XA OR DO
*           NOTHING OTHERWISE.
*         - ADD SUPPORT FOR PRINTING OUT A COMPLETE LIST OF OPTIONS IN
*           EFFECT.
*         - ADD A POOR MAN'S INTERNAL TRACE TABLE FUNCTION. MACRO
*           $TRACE ADDS AN ENTRY. CHANGE INTERNAL CALLS TO MACRO $CALL.
*    @161 - ADD OPTIONS "SORTBYVOLUME" TO CAUSE SELECTED DATASETS TO
*           BE SORTED BY DATASET WITHIN VOLSER. THIS MEANT RE-ARRANGING
*           THE ORDER OF FIELDS IN THE SAVEFMT TABLE.
*         - THE ABOVE REVELAED A BUG IN REPORT4 LOGIC WHEREBY IT WAS
*           DEPENDING ON DSNAME BEING THE 1ST FIELD IN A SAVEFMT ENTRY.
*         - CHANGE LOGIC IN VTOC ROUTINE TO ALLOCATE THE VTOC USING
*           UNIT=SYSALLDA TO REMOVE DEVICE DEPENDENCY.
*    @160 - ADD SUPPORT FOR PRINTING TLMSII VOLUME MASTER FILE RECORDS
*           IN HEX. USEFUL SINCE THE CONTENTS ARE NOT THAT WELL
*           DOCUMENTED. OPTION "DUMPVMF(XXX....)."
*         - USING THE ABOVE, FIX A BUG WHEREBY TAPE DATASETS WERE
*           BEING LISTED TWICE. SOMETIMES TLMSII KEEPS "D" TYPE
*           RECORDS AROUND WITH ZERO IN THE FILE SEQUENCE NUMBER FIELD.
*           THESE WERE DUPLICATES.
*         - ADD OPTION "DATEPROTECTED." TO SELECT ONLY DATASETS WITH
*           A NON-ZERO EXPIRY DATE.
*         - FIX ERROR DECODING A 3-DIGIT UNUSED DATE.
*         - ERROR IN PARMS. NOLEVEL WAS TURNING OFF OTHER FLAGS IN
*           BYTE FLAG1.
*         - A MATCH ON NOLEVEL WAS CAUSING THE REST OF THE CURRENT
*           VOLUME TO BE SKIPPED. OOPS!
*         - ADD CONTROL CARD "WITHIN" TO SELECT RECENTLY REFERENCED
*           ARCHIVED DATASETS.
*         - ARCHCAT WAS SELECTED WEIRD DATASETS. FIRST CHARACTER OF
*           CATALOG RECORD EQUAL 'E' ISN'T A DATASET.
*         - EXPAND "BREAKLEVEL" COUNT FIELDS TO 6 DIGITS.
*         - BUG: MEMOREX 3350 UCBTYP IS X'3070200B'. UGH!
*         - ADD OPTION "DENSITY(X)" TO SELECT BY TAPE DENSITY.
*         - DON'T SELECT VSAM DATASETS AS EMPTY.
*         - NOLEVEL FLAG WAS BEING IMPROPERLY SET. OOPS!
*         - DON'T SELECT VSAM NAMES AND OS TEMPS AS UNCATALOGED.
*    @152 - FOR TAPE DATASETS, PRINT FILE SEQUENCE NUMBER ON LIST
*           OUTPUT. USE THE FIELD THAT IS EXTENTS FOR A DISK DATASET.
*         - CONVERT TO MVS/XA. USE LOC=(ANY,ANY) TO GETMAIN THE
*           DATASET INFO TABLE. CODE NOW RUNS RMODE=24. AMODE IS 31
*           EXCEPT FOR THOSE PLACES THAT MUST BE 24.
*         - INCREASE MAXIMUM DATASET TABLE CAPACITY.
*         - CORRECT FORMATTING ERROR WITH MESSAGE ABOUT TABLE CAPACITY.
*         - ADD OPTION "ONEFILE" TO SELECT ONLY TAPES WITH A SINGLE
*           DATASET.
*    @151 - REMOVE CBOC INSTALLATION SPECIFIC CODE.
*         - ADD SUPPORT FOR SELECTION BY LENGTH OF TIME UNUSED.
*           BASED ON OPTION UNUSED. AND SU60 DATE.
*         - ADD SUPPORT FOR SELECTION OF EMPTY DATASETS.
*         - SET RETURN CODE 8 AND SKIP REPORT PRINT IF NOTHING
*           SELECTED. USED TO REPORT LAST DATASET SCANNED. OOPS!
*         - BUG: NOT IDENTIFYING EMPTY DATASETS CORRECTLY.
*         - ADD SUPPORT FOR FOR SELECTION AND EXCLUSION OF DATASETS
*           BY TRAILING AND IMBEDDED CHARACTER STRINGS. ENABLED BY
*           SUBLIST OPTIONS ENDING/NOTENDING,CONTAINING/NOTCONTAINING
*         - ADD SUPPORT FOR SORTING THE DETAIL PRINTOUT IN DESCENDING
*           ORDER BY TRACKS ALLOCATED. OPTION BYSIZE.
*    @150 - ADD SUPPORT FOR ASM2 ARCHIVED DATASETS.
*           OPTION ASM2./NOASM2. PASS THE ARCHIVE CATALOG TO GET THE
*           INFO.
*         - CONVERT UCB TABLE LOOKUP LOGIC TO CALL UCB LOOKUP SERVICE
*           ROUTINE IN ANTICIPATION OF MVS/XA. NB: THIS MEANS NOCELL
*           WILL NO LONGER WORK ON ANYTHING PRIOR TO MVS/SP 1.3
*         - REMOVE REPORT5 LOGIC, WHICH WAS NEVER DOCUMENTED ANYWAY.
*         - CHANGE REPORT FILE ALLOCATION TO USE ANY DDNAME THAT MAY
*           HAVE BEEN SUPPLIED IN THE JCL.
*         - PRINT MESSAGE DOCUMENTING DSNAME TABLE CAPACITY OBTAINED.
*         - ADD SUPPORT FOR SETTING A RETURN CODE IN CASE OF TROUBLE.
*    @141 - FIX BUGS FOUND AFTER AUTHOR'S MOVE TO MANULIFE:
*         . XVTCREAD ASSUMED A 3375 DEVICE TYPE. ADD 3350 AND 3380.
*         . MAKE NOHSM THE DEFAULT. NO HSM HERE.
*         . CHANGE REPORT1 TO USE WHICHEVER VOLSERS WE FIND.
*         . SKIP SYSUT1 OPEN WHEN NOHSM.
*         . INCREASE DATASET TABLE CAPACITY TO 64K, I HOPE.
*         . BREAKLEVEL CODE ASSUMED A 7-CHAR HI-LEVEL QUALIFIER
*         . EXTENT DESCRIPTORS FROM A FORMAT3 WERE BEING SETUP WRONG.
*         . DESCRIBE A ZERO EXTENT DATASET AS HAVING ZERO TRACKS USED.
*    @140 - ADD OPTION "BREAKLEVEL" TO CAUSE LIST OUTPUT TO PAGE
*           EJECT ON A NEW HIGH-LEVEL QUALIFIER.
*         - WHEN "BREAKLEVEL" IS IN EFFECT, PRINT TOTALS AT END OF HIGH
*           LEVEL QUALIFIER.
*    @133 - ENHANCED DIAGNOSTIC MESSAGE SUPPORT.
*         - GENERAL CODE CLEANUP. ADDED COMMENTS AND JUSTIFIED BY
*           COLUMNS.
*    @132 - ADD FULL SUPPORT FOR VSAM OBJECTS WITH MORE THAN 16 EXTENTS
*           AS INTRODUCED BY DF/EF.
*    @131 - ADD TREASURY ACRONYM TO TABLE IN NOCELL2.
*           OTHER CORRECTIONS TO ACRONYM TABLE.
*    @130 - SELECT DATASETS BY CATALOG STATUS. ENABLED BY OPTION:
*           CAT/UNCAT/MISCAT.
*         - DIAGNOSTIC MESSAGE SUPPORT ADDED.
*    @122 - BLDTAB ROUTINE IN CSECT PARMS IS SPACING TABLE ENTRIES
*           9 BYTES APART INSTEAD OF USING THE LENGTH PASSED IN R8.
*           THE RESULT IS THAT ALL VOLUME VALUES IN A LIST AFTER
*           THE FIRST ONE ARE IGNORED.
*    @121 - FIX PROBLEM WITH HEADINGS MISSING ON LIST.
*         - REMOVE HARD-CODED BLKSIZE FROM VMF.
*         - ADD BUFNO OPTION TO VMF DCB.
*         - FOR VMF TYPE 'D' RECORDS, EXAMINE THE CHAIN FIELDS
*           MDRELPRE AND MDRELNXT. IF THESE ARE BOTH ZERO THEN THE
*           RECORD MUST BE INVALID, I THINK?
*    @120 - PROCESS HSM BACKUP VERSIONS AS TAPE DATASETS.
*           ENABLED BY OPTION CARD HSMTAPE/NOHSMTAPE .
*    @111 - ADD PUB705,PUB706,PRV711 AND PRV712.
*         - CHECK LOGICAL EXPIRED DATE ON VMF TYPE 'D' RECORDS.
*           A NON-ZERO DATE MEANS THE TAPE IS SCRATCH, I THINK?
*
./       ADD   NAME=$COPY
 COPY OUTDD=PANELS,INDD=((PDS,R))
          S M=NCHELP ,NCHELP))
          S M=NCH0 ,NCH0))
          S M=NCH0A ,NCH0A))
          S M=NCH1 ,NCH1))
          S M=NCH10 ,NCH10))
          S M=NCH11 ,NCH11))
          S M=NCH12 ,NCH12))
          S M=NCH13 ,NCH13))
          S M=NCH14 ,NCH14))
          S M=NCH15 ,NCH15))
          S M=NCH16 ,NCH16))
          S M=NCH17 ,NCH17))
          S M=NCH18 ,NCH18))
          S M=NCH19 ,NCH19))
          S M=NCH2 ,NCH2))
          S M=NCH20 ,NCH20))
          S M=NCH21 ,NCH21))
          S M=NCH22 ,NCH22))
          S M=NCH23 ,NCH23))
          S M=NCH24 ,NCH24))
          S M=NCH25 ,NCH25))
          S M=NCH26 ,NCH26))
          S M=NCH27 ,NCH27))
          S M=NCH3 ,NCH3))
          S M=NCH4 ,NCH4))
          S M=NCH5 ,NCH5))
          S M=NCH6 ,NCH6))
          S M=NCH7 ,NCH7))
          S M=NCH8 ,NCH8))
          S M=NCH9 ,NCH9))
          S M=NCISPF ,NCISPF))
          S M=NCISPF1 ,NCISPF1))
          S M=NCISPF2 ,NCISPF2))
          S M=NOCELL99
          S M=((PRINTOFP,PRINTOFF,R))
 COPY OUTDD=MLIB,INDD=((PDS,R))
 S M=NC00
 COPY OUTDD=PDS,INDD=((TLMSII,R))
 S M=(VMFBASE,VMFMDS)
./       ADD   NAME=$DOC
.rc 1 ×
.du add NOCELL
.du add backup
.du add BACKUP
.du add TLMS
.du add TLMSII
.du add DD
.du add CPU
.du add dsname
.du add ddname
.du add TSO
.du add COBOL
.du add VMF
.du add DUMPVMF
.du add WITHIN
.du add UNIT
.du add LIST
.du add NOLIST
.du add SORT
.du add NOSORT
.du add DASD
.du add NODASD
.du add TAPE
.du add NOTAPE
.du add HSM
.du add NOHSM
.du add HSMTAPE
.du add NOHSMTAPE
.du add ENDREQ
.du add SYSOUT
.du add JCL
.du add VOLUME
.du add NOVOLUME
.du add LEVEL
.du add NOLEVEL
.du add BREAKLEVEL
.du add SORTBYVOLUME
.du add ASM2
.du add CONTAINING
.du add NOTCONTAINING
.du add ENDING
.du add NOTENDING
.du add BYSIZE
.du add ONEFILE
.du add DUMPVMF
.du add DATEPROTECTED
.du add WITHIN
.du add DENSITY
.dl ecan
:gdoc sec='NOCELL User''s guide.'
:titlep.
:title NOCELL User's Guide.
:docnum 1.7.0
:date
:author Jim Lane
:etitlep.
:toc.
:body.
:h1 Summary of amendments.
:h2 Release 1.6.5 update.
:p.Support is added for selection of disk datasets by DSORG or by
change status and for HSM migrated datasets by migration level 1 or 2.
:h2 Release 1.6.4 update.
:p.Logic supporting the HSM options is re-worked in support of DFHSM
2.1.0. If HSM is in effect, the entire MCDS dataset is passed
sequentially to select any applicable migrated datasets. It is now
possible to select only migrated datasets by specifying HSM and NODASD.
:p.Unmoveable datasets are now shown as such on LIST output.
:h2 Release 1.6.3 update.
:p.Add a new detail report recommending "optimal" block sizes for
sequential DASD datasets. This option is controlled by the option
REPORT2.
:h2 Release 1.6.2 update.
:p.Add support for selection of DASD datasets based on esoteric unit
name.
:p.Add support for multiple logical requests within one run of
NOCELL.
:h2 Release 1.6.1 update.
:p.Add option SORTBYVOLUME to cause LIST output to be sorted by
dsname within volume serial number.
:h2 Release 1.6.0 update.
:p.The following new options have been added:
:ul.
:li.CONTAINING.
:li.NOTCONTAINING.
:li.ENDING.
:li.NOTENDING.
:li.BYSIZE.
:li.ONEFILE.
:li.DUMPVMF.
:li.DATEPROTECTED.
:li.WITHIN.
:li.DENSITY.
:eul.
:h2 Release 1.5.0 update.
:p.Support is added to process ASM2 archived datasets. A new option,
ASM2, controls this selection.
:p.Preallocated DDNAMES can now be used for the report files. For
example if a ddname of SYSLIST is provided the detail listing will
be written to it and dynamic allocation will not be used.
:h2 Release 1.4.0 update.
:p.A new option, BREAKLEVEL, is provided to cause LIST output to
print totals and skip a page at every change in high level qualifier
of the dataset name.
:h2 Release 1.3.0 update.
:p.Dataset selection processing is updated to allow selection by
catalog status. Options provided allow selection of catalogued,
uncatalogued and miscatalogued datasets.
:p.An additional report will now be produced containing diagnostic
messages as to the results of NOCELL processing.
:h2 Release 1.2.0 update.
:p.Support is added under tape processing for listing HSM controlled
backup versions. These are listed under the names they originally had
on DASD.
:h1 Introduction
:p.NOCELL is a utility the purpose of which is to analyze allocated
datasets. The program can process all datasets or select a subset, and
produce detail listings or summary reports.
:p.Datasets types which can be selected include:
:ul.
:li.Online DASD datasets.
:li.Datasets migrated by DFHSM.
:li.Datasets archived by ASM2.
:li.Backup copies of DASD datasets taken by DFHSM.
:li.TAPE datasets recorded by TLMSII.
:eul.
:p.NOCELL provides a set of options, activated by control cards, the
effect of which are to limit the number of datasets selected. The
logic of NOCELL is such as to select all datasets unless the options
in effect provide a reason to do otherwise. A diagnostic report is
produced showing all the options used (defaults as well as those
explicitly requested).
:h1 JCL requirements.
:p.NOCELL can be executed using the following JCL:
:xmp.
   //jobname  JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   <control cards>
   /*
:exmp.
:p.The NOCELL catalogued procedure consists of the following JCL:
:xmp.
//IEFPROC  EXEC PGM=NOCELL,REGION=4096K
//SYSUT1   DD DSN=HSM.MCDS,DISP=SHR
//SYSUT2   DD DUMMY
//VMF      DD DSN=SYS3.TLMS.VMF,DISP=SHR
//SYSUDUMP DD SYSOUT=*
:exmp.
:p.The SYSUT1 DD statement is the HSM migration control dataset, which
is required to determine information about HSM migrated datasets.  The
SYSUT2 DD statement is the HSM backup control dataset, which is required
to determine information about HSM backup copies.  The VMF DD statement
is the TLMSII volume master file, which is required to determine
information about tape resident datasets.  The CATALOG DD statement is
the ASM2 archive catalog, which is required to determine information
about archived datasets.  All of these DD statements are optional in the
sense that if the control cards supplied are such as to not invoke the
function involved they will not be OPENed.
:p.All other datasets required by NOCELL are dynamically allocated.
:h1.Control Cards.
:h2.Syntax.
:p.Control statements can be coded anywhere from columns 1 to 72.
Syntax is generally free form. Control statements begin with one of
the verbs listed below and end with a period. Anything not forming
part of a valid control statement is processed as a comment.  Any
number of blanks may be used wherever one blank is valid.  Control
statements may be specified in any order and are processed and noted
in the order given with the effects of the last relevant control
statement specified being those used.
:p.No effort is made to detect and reject logically absurd combinations.
In cases of binary pairs the last value specified is the one used.
:p.If LEVEL and NOLEVEL are specified, LEVEL is used and NOLEVEL is
ignored.  If VOLUME and NOVOLUME are specified, VOLUME is used and
NOVOLUME is ignored.
:p.Many control cards specify tests the effect of which is to limit
the number of datasets selected. In general, all such tests are
logically ANDED, that is to say all criteria must apply if a dataset
is to be selected. As soon as a test fails NOCELL continues with the
next dataset.
:p.The following control cards, with the meanings indicated, may be
supplied to NOCELL:
:h2.DASD./NODASD.
:h6.Meaning:
Include, or do not include, DASD resident datasets.
:h6.Default:
DASD.
:h2.ASM2./NOASM2.
:h6.Meaning:
Include, or do not include, ASM2 archived datasets. If NOASM2 is in
effect, the CATALOG DD statement need not be supplied.
:h6.Default:
NOASM2.
:h2.HSM./HSM(ML1)./HSM(ML2)./NOHSM.
:h6.Meaning:
Include, or do not include, HSM migrated datasets. If NOHSM is in
effect, the SYSUT1 DD statement need not be supplied. HSM(ML1) limits
the selection to only datasets on HSM level 1 migration volumes and
HSM(ML2) to only datasets on HSM level 2.
:h6.Default:
NOHSM.
:h2.TAPE./NOTAPE.
:h6.Meaning:
Include, or do not include, TAPE resident datasets. If NOTAPE is in
effect, the VMF DD statement need not be supplied.
:h6.Default:
NOTAPE.
:h2.HSMTAPE./NOHSMTAPE.
:h6.Meaning:
Include, or do not include, tape datasets which are HSM incremental
backup versions. These datasets will be listed on the detail listing
under the names which they have (or had) on DASD. If NOHSMTAPE is in
effect, the SYSUT2 DD statement need not be supplied. Backup versions
are listed as residing on volume serial BACKUP.
:p.HSMTAPE, if in effect, implies TAPE. NOHSMTAPE, if in effect, implies
NOTAPE.
:h6.Default.
NOHSMTAPE.
:h2.LIST./NOLIST.
:h6.Meaning:
Produce, or do not produce, a detail report (one line per dataset)
on the status of all datasets selected for analysis.
:h6.Default:
NOLIST.
:h2.SORT./NOSORT.
:h6.Meaning:
Sort, or do not sort, the in core table of datasets in order by
dataset name. If no "by DSNAME" reports are wanted, specifying NOSORT
can decrease execution time of NOCELL.
:h6.Default:
SORT.
:h2.BYSIZE.
:h6.Meaning:
Specifies, if SORT is in effect, that datasets are to be sorted in
descending order by allocated size. If this option is in effect,
BREAKLEVEL and REPORT4 cannot be specified as they assume a table sorted
by name.
:h6.Default:
SORT by dataset name.
:h2.DUMPVMF(xxxxxx,xxxxxx,.....).
:h6.Meaning:
Specifies, if TAPE is in effect, that the TLMSII volume master file
records for the specified volumes are to be printed in hex.
:h6.Default:
None.
:h2.DATEPROTECTED.
:h6.Meaning:
Specifies that only datasets with a non zero retention date are to
be selected.
:h6.Default:
Select all datasets with and without retention dates.
:h2.WITHIN(xxxx).
:h6.Meaning:
Specifies that only datasets which have been used within fewer than
"xxxx" days are to be selected.
:h6.Default:
Select all datasets without regard to when, or if, they have been
used.
:h2.DENSITY(x).
:h6.Meaning:
Specifies, if TAPE is in effect, that only volumes recorded at the
given density are to be selected. Only the first dataset on each
volume will be LISTed.
:h6.Default:
Select datasets without regard to tape density.
used.
:h2.SYSOUT(x).
:h6.Meaning:
Specifies the class to which reports are to be printed.
:h6.Default:
SYSOUT(A).
:h2.CAT.
:h6.Meaning:
Datasets catalogued to the volumes on which they are found
will be selected.
:h6.Default:
None. Only those catalog options which are specified will have any
effect.
:h2.UNCAT.
:h6.Meaning:
Datasets not catalogued to any volume will be selected.
:h6.Default:
None. Only those catalog options which are specified will have any
effect.
:h2.MISCAT.
:h6.Meaning:
Datasets catalogued, but not to the volume on which they are found,
will be selected. Only the catalog structure accessible to the system
on which NOCELL runs will be searched. No account can be taken of
differences between master catalogs in a multi CPU configuration. This
limitation can lead to datasets being selected as miscatalogued if
two versions with the same name exist, each catalogued to a different
volume in a different catalog.
:h6.Default:
None. Only those catalog options which are specified will have any
effect.
:h2.LEVEL(x).
:h6.Meaning:
Specifies a list of high level qualifiers (or leading parts thereof)
of datasets for NOCELL to select. All datasets not matching will be
ignored.
:h6.Default:
Select all datasets.
:h2.NOLEVEL(x).
:h6.Meaning:
Specifies a list of high level qualifiers (or leading parts thereof)
of datasets for NOCELL to exclude. All datasets not matching will be
selected.
:h6.Default:
Select all datasets.
:h2.BREAKLEVEL.
:h6.Meaning:
Specifies, when LEVEL selection is in effect, that LIST output is to
print totals and skip a page at every change in dataset high level
qualifier.
:h6.Default:
LIST all selected datasets in one group without page ejecting.
:h2.VOLUME(x).
:h6.Meaning:
Specifies a list of volume serials (or leading parts thereof) of
volumes for NOCELL to process. All datasets not residing on such
volumes will be ignored. Volume selection is effective only for DASD
datasets; tape datasets, if selected will be listed for all volumes.
:h6.Default:
Process datasets on all DASD volumes.
:h2.NOVOLUME(x).
:h6.Meaning:
Specifies a list of volume serials (or leading parts thereof) of
volumes for NOCELL to ignore. All datasets residing on such
volumes will be ignored.
:h6.Default:
Process datasets on all volumes.
:h2.UNUSED(x).
:h6.Meaning:
Specifies that only datasets that have not been referenced for the
specified number of days or longer will be selected. This selection
applies only to DASD resident datasets.
:h6.Default:
Select datasets without regard to when or if they have been
referenced.
:h2.EMPTY.
:h6.Meaning:
Specifies that only datasets with no space used are to be selected.
This selection applies to DASD resident datasets only.
:h6.Default:
Select empty and non empty datasets.
:h2.CONTAINING(x).
:h6.Meaning:
Specifies a list of strings for which NOCELL is to search within the
dataset names. If the dataset name contains any of these strings the
dataset is selected, otherwise it is not.
:h6.Default:
Select all datasets.
:h2.NOTCONTAINING(x).
:h6.Meaning:
Specifies a list of strings for which NOCELL is to search within the
dataset names. If the dataset name does not contain any of these
strings the dataset is selected, otherwise it is not.
:h6.Default:
Select all datasets.
:h2.ENDING(x).
:h6.Meaning:
Specifies a list of strings against which NOCELL is to compare the
trailing portions of the dataset names. If the name ends with any of
these strings the dataset is selected, otherwise it is not.
:h6.Default:
Select all datasets.
:h2.NOTENDING(x).
:h6.Meaning:
Specifies a list of strings against which NOCELL is to compare the
trailing portions of the dataset names. If the name does not end with
any of these strings the dataset is selected, otherwise it is not.
:h6.Default:
Select all datasets.
:h2.LARGER(x).
:h6.Meaning:
Specifies that NOCELL is to select only datasets larger than x tracks
or longer than x blocks (on tape).
:h2.SMALLER(x).
:h6.Meaning:
Specifies that NOCELL is to select only datasets smaller than x
tracks or shorter than x blocks (on tape).
:h6.Default:
Select all datasets.
:h2.DSORG(xx,xx).
:h6.Meaning:
When DASD is in effect, this option further limits selection to only
those datasets with the specified dataset organizations. Any of the
following values may be entered:
:ul.
:li.PS  - physical sequential.
:li.PSU - physical sequential unmoveable.
:li.PO  - partitioned.
:li.POU - partitioned unmoveable.
:li.DA  - direct access.
:li.DAU - direct access unmoveable.
:li.IS  - indexed sequential.
:li.VS  - VSAM.
:eul.
:h2.ONEFILE.
:h6.Meaning:
Specifies that, if NOCELL is to select tape datasets, only those
with a single dataset per volume are to be selected. This option has
no meaning if NOTAPE is in effect.
:h6.Default:
Select all tapes.
:h2.SORTBYVOLUME.
:h6.Meaning:
Specifies, when LIST is in effect, that LIST output is to be sorted
by dataset name within volume serial number.
:h6.Default:
None.
:h2.ENDREQ.
:h6.Meaning:
Indicates the end of a "logical request" within the set of NOCELL
control cards. When an ENDREQ control card is scanned, NOCELL processes
based on the options found since either the beginning of the SYSIN input
or the previous ENDREQ, whichever was the most recent. After the
"logical request" is completed, NOCELL resets all options to the
defaults and resumes scanning
of control cards to formulate the next set
of options.
:h6.Default:
None. If no ENDREQ statements are used, all options form one request.
:h2.UNIT(xxxxxx,xxxxx).
:h6.Meaning:
Specifies a list of one or more esoteric unit names, or leading portions
thereof, for NOCELL to use in selecting DASD resident datasets. NOCELL
handles a UNIT request by internally converting it to its equivalent as
a VOLUME request. When UNIT is in effect only datasets residing on the
DASD volumes corresponding to the requested esoteric groups will be
selected. UNIT has no effect on the selection of tape resident datasets.
:h6.Default:
Select all datasets.
:h2.REPORTx.
:h6.Meaning:
Produce defined report "x", where "x" is the number of one of the
reports listed in the section "Reports".
:h6.Default:
Produce only those reports requested explicitly.
:h1.Reports.
:p.In addition to the detail listing requested by the LIST control
card, the following summary or extract reports can be requested:
:h2.Report1.
:p.Report 1 is a summary of DASD datasets by volume. It includes
counts of datasets and tracks allocated and is broken down by datasets
currently online, HSM migrated datasets and the total of the two.
:p.This report is based on whatever datasets have been selected for
analysis. If volume selection, level selection, NODASD or NOHSM are
in effect the report will cover something less than the total situation.
:h2.Report2.
:p.Report 2 is a detail listing of all selected, sequential DASD
datasets showing a calculated "optimal" block size for the file and the
savings, if any, in size and I/O time that could be expected. The logic
for this report makes two assumptions. Firstly all tracks of the
dataset including the last are assumed to be completely full. In the
case of variable blocks (DCB=RECFM=VB), the blocks are assumed all to
be equal in length and equal to the DCB block size.  These assumptions
are made because NOCELL does not actually count the number of blocks in
the dataset or average their length (this would require reading the
entire dataset).
:p.The block size calculated for Report 2 is the largest possible
size smaller than half the track length of the device. In some cases
where very long blocks (or very long LRECLs) are alredy in use, the
recommendations on this report will appear worse than the present
situation.
:h2.Report4.
:p.Report 4 is a summary of allocated datasets by high level qualifier.
For each qualifier the count of datasets and tracks is produced for
online migrated and total DASD datasets. Also the average percent
used for online datasets is calculated. For tape datasets, the count
of datasets, total tape volumes, total tape blocks and average blocks
per volume is reported.
:h1.Performance
:p.Maximum run time for NOCELL (DASD, TAPE, HSM, LIST and all reports
specified) is approximately 20 Sec CPU time. A region of at
least 4 Meg is required to process all datasets.
:p.Options causing NOCELL to select less than the sum total of all
datasets can lead to greatly reduced execution time.
:h1.JCL Examples.
:h2.List TSO user's datasets. DASD only.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   LEVEL(JIMLANE).
   LIST.
   DASD.
   NOTAPE.
   /*
:exmp.
:h2.List datasets on TSO volumes.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   VOLUME(TSO).
   LIST.
   DASD.
   NOTAPE.
   /*
:exmp.
:h2.List datasets for an application - TAPE and DASD.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   LEVEL(AG,TAG,PAG).
   LIST.
   DASD.
   TAPE.
   ASM2.
   /*
:exmp.
:h2.List uncatalogued production datasets.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   UNCAT.
   LIST.
   LEVEL(P).
   /*
:exmp.
:h2.List tape datasets with fewer than 20 blocks.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   TAPE.
   NODASD.
   LIST.
   SMALLER(20).
   /*
:exmp.
:h2.List 45 day old TSO datasets not created by COBOL compiles.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   LIST.
   VOLUME(TSO).
   NOTENDING(.SYM,.ERR,.LIST)
   UNUSED(45).
   /*
:exmp.
:h2.List all non stacked, short tapes that aren't ASM2 created.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   LIST.
   TAPE.
   NODASD.
   SMALLER(20).
   NOTCONTAINING(ARCH).
   /*
:exmp.
:h2.List all non production datasets on production DASD.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   LIST.
   DASD.
   NOLEVEL(P).
   UNIT(PROD).
   /*
:exmp.
:h2.Determine optimal block sizes for a production application.
:xmp.
   //jobname JOB ......
   //stepname EXEC NOCELL
   //SYSIN    DD   *
   NOLIST.
   DASD.
   UNIT(PROD).
   LEVEL(PPS).
   REPORT2.
   /*
:exmp.
:egdoc.
./       ADD   NAME=$EPILOG
         MACRO
&LABEL   $EPILOG &RC
&LABEL   LR    R1,R13              GET SAVEAREA ADDRESS
         L     R13,4(R13)          GET BACK CHAIN POINTER
         L     R0,16(R13)          GET SAVEAREA LENGTH
         ST    R15,16(R13)         SAVE REGISTER 15 (RETCODE)
         FREEMAIN R,LV=(0),A=(1)   FREE SAVEAREA
         LM    R14,R12,12(R13)     RESTORE CALLERS REGS
         AIF   (T'&RC EQ 'O').SPEC
         LA    R15,&RC             SET RETURN CODE
.SPEC    ANOP
         BR    R14                 RETURN TO CALLER
         MEND
./       ADD   NAME=$EXEC
//TSGJCL   JOB T50000,LANE,NOTIFY=TSGJCL,MSGCLASS=X
//NOCELL EXEC PGM=NOCELL,REGION=4096K
//SYSLIST  DD SYSOUT=*
//SYSPRINT DD SYSOUT=*
//STEPLIB  DD DSN=TSGJCL.SYSTEMS.LOAD,DISP=SHR
//VMF      DD DSN=SYS3.TLMS.VMF,DISP=SHR
//SYSUT1   DD DUMMY
//SYSIN    DD *
SYSOUT(X). REPORT4. DASD. NOLIST. ENDREQ.
./       ADD   NAME=$INSTALL
//JIMLANE  JOB (,MDOA00-0000),LANE,NOTIFY=JIMLANE,MSGCLASS=A,
//             CLASS=X
/*JOBPARM T=1,L=1,R=N0GA
//INSTALL PROC DISTR='NOCELL.SOURCE.PDS',
//             PANELS='ISPF.PANEL.LIBRARY',
//             MLIB='ISPF.MESSAGE.LIBRARY',
//             TLMSII='TLMSII.DSECTLIB',
//             ISPLINK='SYS1.LPALIB',
//             LOAD='NOCELL.LOAD.LIBRARY'
//*
//COPY      EXEC PGM=IEBCOPY
//*
//* COPY MESSAGES, PANELS TO ISPF LIBRARIES
//* COPY PROPRIETARY SOURCE INTO DISTRIBUTION PDS
//* COPY OBJECT DECKS TO FB 80,3200 LIBRARY.
//*
//SYSPRINT  DD SYSOUT=*
//SYSUT3    DD UNIT=SYSDA,
//             SPACE=(CYL,1)
//SYSUT4    DD UNIT=SYSDA,
//             SPACE=(CYL,1)
//PDS       DD DSN=&DISTR,DISP=SHR
//PANELS    DD DSN=&PANELS,DISP=SHR
//MLIB      DD DSN=&MLIB,DISP=SHR
//TLMSII    DD DSN=&TLMSII,DISP=SHR
//OBJ       DD DSN=&&DECK,UNIT=SYSDA,SPACE=(TRK,(5,5,1),RLSE),
//             DISP=(,PASS),
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=3200)
//SYSIN     DD DSN=&DISTR($COPY),DISP=SHR
//*
//* ASSEMBLE NOCELL
//*
//ASM       EXEC  PGM=IEV90,PARM='OBJECT,BATCH',REGION=2048K
//SYSLIB    DD DISP=SHR,DSN=&DISTR,DCB=BLKSIZE=32720
//          DD DISP=SHR,DSN=SYS1.AMODGEN
//          DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1    DD UNIT=SYSDA,SPACE=(CYL,(9,1))
//SYSPUNCH  DD DUMMY
//SYSPRINT  DD SYSOUT=*
//SYSLIN    DD UNIT=SYSDA,SPACE=(TRK,(15,15,1),RLSE),DISP=(,PASS)
//SYSIN     DD DISP=SHR,DSN=&DISTR(NOCELL)
//*
//* LINKEDIT NOCELL
//*
//LINK      EXEC PGM=LINKEDIT,PARM='LET,LIST,MAP,XREF,NCAL'
//SYSPRINT  DD SYSOUT=*
//SYSUT1    DD UNIT=VIO,SPACE=(CYL,1)
//SYSLIN    DD DSN=*.ASM.SYSLIN,DISP=(SHR,PASS)
//SYSLMOD   DD DISP=SHR,DSN=&LOAD(NOCELL)
//*
//ENDPROC  PEND
//*
//INSTALL   EXEC INSTALL
//
./       ADD   NAME=$JCL
//JIMLANE  JOB (,MDOA00-0000),LANE,NOTIFY=JIMLANE,MSGCLASS=A,
//         CLASS=X,TIME=2
/*JOBPARM T=1,L=99,R=N0GA
//NOCELL EXEC NOCELL,REGION=4096K
//STEPLIB  DD DSN=JIMLANE.SYSTEMS.LOAD,DISP=SHR
//SYSPRINT DD SYSOUT=*
//DUMPVMF  DD SYSOUT=*
//ABNLDUMP DD DUMMY
//SYSUDUMP DD SYSOUT=*
//SYSIN    DD *
LIST.
NODASD.
NOTAPE.
ASM2.
WITHIN(180).
./       ADD   NAME=$OBJ
 MODE AMODE(31),RMODE(24)
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
ESD      
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
TXT 
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
RLD      
END                            1566896201 020191322
 IDENTIFY DATASECT('@166 11/18/91 11.21')
 IDENTIFY DOUCB('@V2  11/18/91 11.21')
 IDENTIFY GENCARDS('@165 11/18/91 11.21')
 IDENTIFY HEXVMF('@160 11/18/91 11.21')
 IDENTIFY ISPFINIT('@V2  11/18/91 11.21')
 IDENTIFY ISPFLIST('@V2  11/18/91 11.21')
 IDENTIFY ISPFPARM('@V2  11/18/91 11.21')
 IDENTIFY LISTOUT('@170 11/18/91 11.21')
 IDENTIFY MESGRTN('@162 11/18/91 11.21')
 IDENTIFY MESSAGES('@162 11/18/91 11.21')
 IDENTIFY NOCELL('@170 11/18/91 11.21')
 IDENTIFY NOCELL1('@165 11/18/91 11.21')
 IDENTIFY NOCELL2('@170 11/18/91 11.21')
 IDENTIFY NOCELL4('@170 11/18/91 11.21')
 IDENTIFY OPTIONS('@165 11/18/91 11.21')
 IDENTIFY PARMS('@166 11/18/91 11.21')
 IDENTIFY PASSMCDS('@170 11/18/91 11.21')
 IDENTIFY PASSTMC('@TEC 11/18/91 11.21')
 IDENTIFY PASSVMF('@170 11/18/91 11.21')
 IDENTIFY SETUP('@V2  11/18/91 11.21')
 IDENTIFY SORT1('@161 11/18/91 11.21')
 IDENTIFY SORT2('@161 11/18/91 11.21')
 IDENTIFY SORT3('@161 11/18/91 11.21')
 IDENTIFY UNITNAME('@165 11/18/91 11.21')
 IDENTIFY XDATEDIT('BASE 11/18/91 11.21')
 IDENTIFY XPRNTSUB('@162 11/18/91 11.21')
 IDENTIFY XVTCREAD('@161 11/18/91 11.21')
 NAME NOCELL2(R)
./       ADD   NAME=$PROLOG
         MACRO
&LABEL   $PROLOG &LV=0
.**********************************************************************
.*
.*       THIS MACRO WILL PROVIDE ENTRY LINKAGE AND OPTIONALLY
.*       MULTIPLE BASE REGISTERS.  ALSO, VIA THE 'LV=' KEYWORD
.*       PROVIDE ADDITIONAL USER STORAGE (APPENDED TO THE
.*       SAVE AREA) ADDRESSABLE FROM REG 13.  IF NO OPERANDS
.*       ARE CODED, REG 12 IS ASSUMED THE BASE. EXAMPLE:
.*              SECTNAME $PROLOG          = STANDARD REG 12 BASE
.*              SECTNAME $PROLOG 5        = STANDARD, REG 5 BASE
.*              SECTNAME $PROLOG 10,LV=20 = ADD 20 BYTES TO SAVE AREA
.*                                             REG 10 IS BASE
.*              SECTNAME $PROLOG R10,R11  = REGS 10 AND 11 ARE BASES
.*
.**********************************************************************
         LCLA  &AA,&AB,&AC
         GBLB  &PRORG
&AC      SETA  4096
&LABEL   CSECT
         B     32(R15)             BRANCH AROUND
         DC    AL1(26)
         DC    CL8'&LABEL'         CSECT NAME
         DC    C'-'
         DC    CL8'&SYSDATE'       COMPILE DATE
         DC    C'-'
         DC    CL8'&SYSTIME'       COMPILE TIME
         CNOP  0,4                 ALIGNMENT
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             LOAD BASE REG
         USING &LABEL,R12          INFORM ASSEMBLER
         AIF   (&LV GT 4023).MERR
         LA    R0,&LV+72           LOAD REG 0 WITH LENGTH VARIABLE
         GETMAIN R,LV=(0)          GET CORE FOR SAVEAREA AND USER
         AIF   (&LV+72 LE 256).XC2
         AIF   (&LV+72 LE 512).XC1
         MVI   0(R1),X'00'         MOVE X'00' TO FIRST BYTE
         LR    R2,R1               SAVE POINTER IN EVEN REG
         LA    R4,1(R1)            SET RECEIVING POINTER
         LR    R5,R0               SET RECEIVING LENGTH
         BCTR  R5,R0               DECREMENT LENGTH
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE
         LA    R3,1                SET SENDING LENGTH
         MVCL  R4,R2               INSTRUCTION PADS WITH X'00'
         AGO   .STORE
.XC1     ANOP
         XC    256(&LV-184,R1),256(R1)  CLEAR SAVE AREA
         XC    0(256,R1),0(R1)          CLEAR SAVE AREA
         AGO   .STORE
.XC2     ANOP
         XC    0(&LV+72,R1),0(R1)       CLEAR SAVE AREA
.STORE   ANOP
         ST    R13,4(R1)           SAVE BACK CHAIN
         ST    R1,8(R13)           SET FORWARD CHAIN
         LR    R11,R1              SAVE NEW SAVEAREA ADDRESS
         L     R15,16(R13)         RESTORE REG 15
         ST    R0,16(R13)          SAVE SAVEAREA LENGTH
         LM    R0,R1,20(R13)       RESTORE REGS USED IN GETMAIN
         LR    R13,R11             SET SAVEAREA POINTER
         AIF   (N'&SYSLIST EQ 0).MEND
         AIF   ('&SYSLIST(1)' EQ 'R12').SKIPIT
         AIF   ('&SYSLIST(1)' EQ '12').SKIPIT
         LA    &SYSLIST(1),&LABEL  LOAD REQUESTED BASE REG
         DROP  R12                 DROP ASSUMED BASE REG
         USING &LABEL,&SYSLIST(1)  INFORM ASSEMBLER
.SKIPIT  ANOP
&AA      SETA  2
.LOOP    ANOP
         AIF   (&AA GT N'&SYSLIST).MEXIT
&AB      SETA  &AA-1
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AB))  LOAD NEXT BASE REG
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AA))  LOAD NEXT BASE REG
         USING &LABEL+&AC,&SYSLIST(&AA) INFORM ASSEMBLER
&AC      SETA  &AC+4096
&AA      SETA  &AA+1
         AGO   .LOOP
.MEXIT   ANOP
         AIF   (&PRORG).MEX2
         SPACE
         $REGS
         SPACE
.MEX2    ANOP
&AA      SETA  &LV+72
         MNOTE *,'TOTAL STORAGE AREA RECEIVED = &AA'
         MEXIT
.MEND    ANOP
         MNOTE *,'NO REGISTER SPECIFIED - R12 ASSUMED'
         AGO   .MEXIT
.MERR    ANOP
         MNOTE 12,'LV > 4023 - REQUEST IGNORED'
         AGO   .MEXIT
         MEND
./       ADD   NAME=$REGS
         MACRO
         $REGS
         GBLB  &PRORG
         AIF   (&PRORG).MEX2
&PRORG   SETB  1
 SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
 SPACE
REG0     EQU   0
REG1     EQU   1
REG2     EQU   2
REG3     EQU   3
REG4     EQU   4
REG5     EQU   5
REG6     EQU   6
REG7     EQU   7
REG8     EQU   8
REG9     EQU   9
REG10    EQU   10
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
 SPACE
.MEX2    ANOP
       MEND
./       ADD   NAME=$TEST
         MACRO
         $TEST   &FLAG,&MASK,&TABLE,&COMPRND,&LENGTH,                  X
               &FOUND,&NOTFND,&REGS
&R1      SETC  'R1'
&R2      SETC  'R14'
&R3      SETC  'R7'
&R4      SETC  'R8'
&R5      SETC  'R9'
         AIF   (T'&REGS EQ 'O').NOREGS
         AIF   (N'&REGS EQ 5).SETREGS
         MNOTE 12,'5 REGISTERS MUST BE SPECIFIED.'
         MEXIT
.SETREGS ANOP
&R1      SETC  '&REGS(1)'
&R2      SETC  '&REGS(2)'
&R3      SETC  '&REGS(3)'
&R4      SETC  '&REGS(4)'
&R5      SETC  '&REGS(5)'
.NOREGS  ANOP
&EXIT    SETC  'DEC'.'&SYSNDX'
&AGAIN   SETC  'REP'.'&SYSNDX'
&SCAN    SETC  'SCN'.'&SYSNDX'
         TM    &FLAG,&MASK
         BZ    &EXIT
         AIF   (N'&COMPRND GT 1).NOTFIRST
         LA    &R1,&TABLE
&AGAIN   SR    &R2,&R2
         IC    &R2,0(0,&R1)
         BCTR  &R2,0
         B     *+10
         CLC   1(0,&R1),&COMPRND
         EX    &R2,*-6
         AIF   ('&FOUND'(1,1) EQ '(').OUTREG
         BE    &FOUND
         AGO   .OUTEND
.OUTREG  ANOP
&ER      SETC  '&FOUND(1)'
         BER   &ER
.OUTEND  ANOP
         LA    &R1,&LENGTH.(0,&R1)
         CLI   0(&R1),255
         BNE   &AGAIN
         AIF   ('&NOTFND'(1,1) EQ '(').NOTREG
         B     &NOTFND
         AGO   .NOTEND
.NOTREG  ANOP
&NR      SETC  '&NOTFND(1)'
         BR    &NR
.NOTEND  ANOP
         AGO   .ENDTEST
.NOTFIRST ANOP
         AIF   (T'&COMPRND(2) EQ 'O').LAST
         LA    &R1,&TABLE
&AGAIN   LA    &R3,&COMPRND(1)
         LA    &R4,1
         LA    &R5,&COMPRND(1)
         LA    &R5,&COMPRND(3).(0,&R5)
         SR    &R2,&R2
         IC    &R2,0(0,&R1)
         SR    &R5,&R2
         BCTR  &R2,0
         B     *+10
         CLC   1(0,&R1),0(&R3)
&SCAN    EX    &R2,*-6
         AIF   ('&FOUND'(1,1) EQ '(').FOUTREG
         BE    &FOUND
         AGO   .FOUTEND
.FOUTREG ANOP
&ER      SETC  '&FOUND(1)'
         BER   &ER
.FOUTEND ANOP
         BXLE  &R3,&R4,&SCAN
         LA    &R1,&LENGTH.(0,&R1)
         CLI   0(&R1),255
         BNE   &AGAIN
         AIF   ('&NOTFND'(1,1) EQ '(').FNOTREG
         B     &NOTFND
         AGO   .FNOTEND
.FNOTREG ANOP
&NR      SETC  '&NOTFND(1)'
         BR    &NR
.FNOTEND ANOP
         AGO   .ENDTEST
.LAST    ANOP
         LA    &R1,&TABLE
&AGAIN   LA    &R3,&COMPRND(1)
         LA    &R4,1
         LA    &R5,&COMPRND(1)
         LA    &R5,&COMPRND(3).(0,&R5)
         CLI   0(&R3),X'41'
         BL    *+8
         BXLE  &R3,&R4,*-8
         SR    &R2,&R2
         IC    &R2,0(,&R1)
         SR    &R3,&R2
         BCTR  &R2,0
         B     *+10
         CLC   1(0,&R1),0(&R3)
&SCAN    EX    &R2,*-6
         AIF   ('&FOUND'(1,1) EQ '(').LOUTREG
         BE    &FOUND
         AGO   .LOUTEND
.LOUTREG ANOP
&ER      SETC  '&FOUND(1)'
         BER   &ER
.LOUTEND ANOP
         LA    &R1,9(,&R1)
         CLI   0(&R1),255
         BNE   &AGAIN
         AIF   ('&NOTFND'(1,1) EQ '(').LNOTREG
         B     &NOTFND
         AGO   .LNOTEND
.LNOTREG ANOP
&NR      SETC  '&NOTFND(1)'
         BR    &NR
.LNOTEND ANOP
.ENDTEST ANOP
&EXIT    DS    0H
         MEND
./       ADD   NAME=$TRACE
         MACRO
&LABEL   $TRACE &NAME=,&DATA=,&TEXT=,&REGS=(R1,R2,R3)
&R1      SETC  '&REGS(1)'
&R2      SETC  '&REGS(2)'
&R3      SETC  '&REGS(3)'
         L     R7,=V(TRACTABL)
         USING TRACTABL,R7
         STM   &R1,&R3,TRACREGS
         LM    &R1,&R3,TRACURNT
         BXLE  &R1,&R2,*+8
         L     &R1,TRACFRST
         USING TRACFRMT,&R1
         ICM   &R2,15,TRAC####
         LA    &R2,1(0,&R2)
         STCM  &R2,15,TRACSEQ#
         STCM  &R2,15,TRAC####
         MVC   TRACNAME,=C&NAME
         AIF   (T'&DATA EQ 'O').NODATA
         AIF   (N'&DATA NE 2).OKDATA
         AIF   ('&DATA(1)'(1,1) EQ '(').DATA1REG
         MVC   TRACWRD1,&DATA(1)
         AGO   .DATA2
.DATA1REG ANOP
&L       SETA  K'&DATA(1)-2
&R       SETC  '&DATA(1)'(2,&R)
         STCM  &R,15,TRACWRD1
.DATA2   ANOP
         AIF   ('&DATA(2)'(1,1) EQ '(').DATA2REG
         MVC   TRACWRD2,&DATA(2)
         AGO   .OKDATA
.DATA2REG ANOP
&L       SETA  K'&DATA(2)-2
&R       SETC  '&DATA(2)'(2,&R)
         STCM  &R,15,TRACWRD2
         AGO   .OKDATA
.NODATA  ANOP
         STCM  R14,15,TRACWRD1
         STCM  R15,15,TRACWRD2
.OKDATA  ANOP
         AIF   (T'&TEXT EQ 'O').NOTEXT
         MVC   TRACTEXT,=CL16&TEXT
.NOTEXT  ANOP
         ST    &R1,TRACURNT
         LM    &R1,&R3,TRACREGS
         DROP  &R1
         MEND
./       ADD   NAME=ALLOC
         MACRO
&NAME    ALLOC &DUMMY,&PERM,&DSN=,&DDN=,&DDNRET=,&MEMBER=,&DISP=,      X
               &VOL=,&UNIT=,&SYSOUT=,&FREE=,&COPIES=,&LABEL=,          X
               &BLKSIZE=,&DEN=,&DSORG=,&KEYLEN=,&LRECL=,&RECFM=,       X
               &PASWORD=,&DSNRET=,&MF=AUTO,&PREFIX=,&ERROR=,           X
               &SPACE=,&F=,&FILE=,&DA=,&QNAME=,&DSORGRT=,              X
               &VOLRET=,&DCBDSN=,&DCBDDN=,&SPECIAL=,&DDNTO=,           X
               &FORMS=,&DEST=,&SSREQ=,&FORUSER=,&TU=,&DSNPDE=
.**********************************************************************
.*                                                                    *
.*    THIS MACRO PROVIDES A DYNAMIC ALLOCATION FUNCTION BY BUILDING   *
.*    A DYNAMIC ALLOCATION PARAMETER LIST AND INVOKING SVC 99.        *
.*    IT FIRST SETS UP A WORKAREA ENVIRONMENT FOR THE PARAMETER LIST  *
.*    AND THEN TESTS THE KEYWORDS SUPPLIED AND INVOKES INNER MACROS   *
.*    TO BUILD THE TEXT UNITS. THE INNER MACROS THEMSELVES USE INNER  *
.*    MACROS TO UPDATE GLOBAL VARIABLES, STORE TEXT UNIT POINTERS ETC *
.*    THERE ARE THREE WAYS OF SPECIFYING THE WORK AREA ADDRESS.       *
.*    A) MF=AUTO, MF=G, MF=(E,ADDRESS,LNTHSYMB).                      *
.*    IN THE FIRST FORM, AN INNER MACRO DYNSPACE IS CALLED TO NAME    *
.*    A WORK AREA, THE NAME BEING RETURNED IN THE GLOBAL SETC         *
.*    VARIABLE &DYNSP. A DSECT IS CREATED TO MAP THIS AREA.           *
.*    THE GLOBAL VARIABLES &DTUO (TEXT UNIT OFFSET COUNTER) AND       *
.*    &DTUPO (TEXT UNIT POINTER OFFSET ACCUMULATOR) ARE SET TO ZERO.  *
.*    THESE ACCUMULATORS ARE UPDATED AS EACH TEXT UNIT PROCESSOR      *
.*    AQUIRES STORAGE. AFTER ALL TEXT UNITS HAVE BEEN BUILT, THE      *
.*    AMOUNT OF SPACE USED IS CALCULATED, AND THE DYNSPACE MACRO IS   *
.*    THEN CALLED AGAIN TO LOG THE AMOUNT NEEDED. DYNSPACE SETS A     *
.*    GLOBAL VARIABLE &DYNSPQ TO THE HIGHEST AMOUNT ANY ALLOC OR      *
.*    FREE MACRO REQUESTED, AND WHEN CALLED WITH THE EXPAND OPTION,   *
.*    (NO OPERANDS OR NAME FIELD SUPPLIED), EXPANDS INTO A DS FOR     *
.*    THAT QUANTITY. (SEE DYNSPACE)                                   *
.*    MF=G SPECIFIES THAT THE ALLOC MACRO ENTER THE BEGIN MACRO       *
.*    WORKAREA TO ACQUIRE THE STORAGE NECESSARY. IT DOES THIS VIA     *
.*    THE RCPDS MACRO. (SEE RCPDS). HOWEVER, IF THE ALLOC MACRO IS    *
.*    CALLED SEVERAL TIMES WITH THIS OPTION, A LOT OF STORAGE WILL BE *
.*    USED UP, AS THE STORAGE WILL NOT BE SHARED. THUS, THIS FORM     *
.*    SHOULD ONLY BE USED IF THE ALLOC/FREE MACRO IS ONLY TO BE USED  *
.*    ONCE OR TWICE DURING AN ASSEMBLY.                               *
.*    MF=E CAUSES THE MACRO TO USE A USER SPECIFIED WORK AREA. THE    *
.*    SECOND PARAMETER GIVES THE NAME OF THE WORKAREA, AND AN         *
.*    OPTIONAL THIRD PARAMETER IS THE NAME OF A SYMBOL TO BE EQUATED  *
.*    TO THE LENGTH OF THE REQUIRED WORK AREA.                        *
.*                                                                    *
.*    DYNAMIC ALLOCATION FUNCTIONS ARE SIMILAR TO THOSE AVAILABLE    *
.*    WITH JCL, USING THE SAME KEYWORDS. HOWEVER, CERTAIN FORMATS    *
.*    ARE SLIGHTLY DIFFERENT. FOR INSTANCE, CERTAIN KEYWORDS CAN     *
.*    HAVE VARYING PARAMETERS, EG DATASET NAME, DDNAME, VOLSER ETC.  *
.*    PROVISION IS MADE FOR BOTH VARIABLE SPECIFICATION.             *
.*    IN THE ABSOLUTE FORM, THE PARAMETER IS ENTERED IN QUOTES,      *
.*    E.G.   ALLOC DSN='SYS1.LINKLIB',DISP=SHR                       *
.*    HOWEVER, THIS NAME REMAINS FIXED FOR THE ASSEMBLY.             *
.*    IN THE VARIABLE FORMAT, THE ADDRESS OF A LOCATOR IS SPECIFIED, *
.*    WHERE THE LOCATOR CONSISTS OF A SIX BYTE FIELD, THE FIRST 4    *
.*    BYTES OF WHICH POINT TO THE PARAMETER, WHILE THE NEXT TWO      *
.*    CONTAIN THE LENGTH.                                            *
.*    EG          ALLOC DSN=LOCATOR                                  *
.*       LOCATOR  DC    A(DSN),Y(12)                                 *
.*       DSN      DC    C'SYS1.LINKLIB'                              *
.*                                                                   *
.*       NUMERIC QUANTITIES E.G. COPIES= FOR SYSOUT, SHOULD EITHER   *
.*       SPECIFY A NUMERIC VALUE, COPIES=3,                          *
.*       A VALUE IN A REGISTER, COPIES=(R3),                         *
.*       OR THE NAME OFF A FULLWORD CONTAINING THE VALUE,            *
.*          COPIES=NUMCOPYS, WHERE NUMCOPYS IS THE NAME OF A         *
.*       FULLWORD FIELD.                                             *
.*                                                                   *
.*       OTHER KEYWORDS SUCH AS DISP= CAN ONLY HAVE THE ABSOLUTE     *
.*       FORM, AND VALUES SHOULD NOT BE ENTERED WITHIN QUOTES.       *
.*       ADDITIONAL FACILITIES NOT AVAILABLE WITH JCL ARE THE        *
.*       RETURN BY THE SYSTEM OF INFORMATION ON THE DATASET, EG      *
.*       DSORG. THIS IS DONE BY SPECIFYING DSORGRT=SYMBOL, WHERE     *
.*       SYMBOL IS A SYMBOL WHICH WILL BE EQUATED TO A TWO BYTE      *
.*       FIELD CONTAINING THE DSORG TYPE (SEE JOB MANAGEMENT,        *
.*       SUPERVISOR AND TSO).                                        *
.*       THE SYSTEM CAN ALSO GENERATE AND RETURN A DDNAME. THIS IS   *
.*       CARRIED OUT BY ENTERING DDNTO=(ADDR1,ADDR2,,...)            *
.*       WHERE ADDR1,ADDR2 ETC ARE THE NAMES OF 8 BYTE FIELDS WHICH  *
.*       ARE TO RECEIVE THE DDNAME.                                  *
.*       FOR FURTHER INFORMATION ON DYNAMIC ALLOCATION, SEE          *
.*       JOB MANAGEMENT, SUPERVISOR AND TSO.                         *
.*                                                                   *
.**********************************************************************
         GBLA  &RCPDYN            COUNTER FOR NO ENTRIES TO MACRO
         GBLA  &DTUO              OFFSET TO TEXT UNITS
         GBLA  &DTUPO             OFFSET TO TEXT UNIT POINTERS
         GBLB  &RCPS99(2)         TELL RCPDSECT NEED DSECTS
         GBLC  &DYNP              PREFIX FOR LABELS FOR THIS CALL
         GBLC  &DYNSP         NAME FOR AUTOMATIC STORAGE ALLOC
         LCLA  &DDNRTO,&DSNRTO         FOR EQUATES FOR RETURNED FLDS
         LCLA  &VOLRTO,&DSRGRTO        FOR EQUATES FOR RETURNED FIELDS
         LCLA  &I                 COUNTER
         LCLB  &DSECT             DSECT NEEDED FOR STORAGE, MF=E
         LCLC  &C,&T,&PAR
.*
.*   THE ALLOC MACRO PROVIDES A DYNAMIC ALLOCATION FUNCTION,
&RCPS99(1)     SETB           1
&RCPDYN  SETA  &RCPDYN+1          INCEREMENT COUNTER
&DYNP    SETC  'DYN&RCPDYN' SET DEFAULT PREFIX
&NAME    DS    0H
         AIF   ('&PREFIX' EQ '').TMF
         AIF   (K'&PREFIX LT 4).POK
         MNOTE 4,'PREFIX TOO LONG, 1ST 4 CHARS USED'
&DYNP    SETC  '&PREFIX'(1,4)
         AGO   .TMF
.POK     ANOP
&DYNP    SETC  '&PREFIX'
.TMF     AIF   ('&MF(1)' EQ 'G').GEN
         AIF   ('&MF' NE 'AUTO').TMFE
NAME     DYNSPACE             GET NAME FOR SPACE
         LA    R1,&DYNSP               LOAD ADDRESS OF PARAM LIST
         USING &DYNP.DS,R1             USE GENERATED DSECT
&T       SETC  'A'
&PAR     SETC  '&DYNSP+4'
&DSECT   SETB  1
         AGO   .START
.TMFE    AIF   ('&MF(2)' NE '').E2OK
         MNOTE 4,'PLIST ADDRESS OMITTED, MF=G USED'
         AGO   .GEN
.E2OK    ANOP
&DSECT   SETB  1
         AIF   ('&MF(2)' EQ '(').RMFE
         LA    R1,&MF(2)               LOAD PARAM LIST ADDRESS
         USING &DYNP.DS,R1             USE GENERATED DSECT
         AGO   .START
.RMFE    AIF   ('&MF(2)' EQ '(R1)' OR '&MF(2)' EQ '(1)').START
         LR    R1,&PAR                 LOAD S99 PARAM LIST ADDRESS
         AGO   .START
.GEN     LA    R1,&DYNP.RBP            LOAD ADDRESS OF S99 RBP
.START   LA    R15,&DYNP.RB            LOAD ADDRESS OF S99 RB
         USING S99RB,R15
         ST    R15,0(R1)               AND STORE IN RB POINTER
         XC    4(&DYNP.LEN-4,R1),4(R1) ZERO PARAMETER LIST
         MVI   S99RBLN,20              MOVE IN LIST LENGTH
         MVI   S99VERB,S99VRBAL        MOVE IN VERB CODE
         LA    R14,&DYNP.TUP           LOAD ADDRESS OF TU POINTERS
         ST    R14,S99TXTPP            STORE ADDRESS IN S99 RB
         LA    R15,&DYNP.TU            POINT TO SPACE FOR TEXT UNITS
         USING S99TUNIT,R15
&DTUO    SETA  0
&DTUPO   SETA  0
         AIF   ('&SSREQ' EQ 'YES').SSREQ
.TDSN    AIF   ('&DSN&DA' NE '').DSN
         AIF   ('&DSNPDE' NE '').DSNPDE
         AIF   ('&DSNRET' NE '').DSNRT
         AIF   ('&SYSOUT' NE '').SYSOUT
         AIF   ('&DUMMY' NE '').DUMMY
         AIF   ('&QNAME' NE '').QNAME
.TDDN    AIF   ('&DDN&FILE&F' NE '').DDN
         AIF   ('&DDNRET&DDNTO' NE '').DDNRT
.TUNIT   AIF   ('&UNIT&VOL' NE '').UNIT
.TVOLRET AIF   ('&VOLRET' NE '').VOLRET
.TDSRGO  AIF   ('&DSORGRT' NE '').DSORGRT
.TLABEL  AIF   ('&LABEL' NE '').LABEL
.TPSWD   AIF   ('&PASWORD' NE '').PASWORD
.TFORUSE AIF   ('&FORUSER' NE '').FORUSER
.TTU     AIF   ('&TU' NE '').TU
.TDISP   AIF   ('&DISP' NE '').DISP
.TSPACE  AIF   ('&SPACE' NE '').SPACE
.TLRECL  AIF   ('&LRECL' NE '').DCB
         AIF   ('&DEN' NE '').DCB
         AIF   ('&RECFM' NE '').DCB
         AIF   ('&BLKSIZE' NE '').DCB
         AIF   ('&DSORG' NE '').DCB
         AIF   ('&KEYLEN' NE '').DCB
.TDCBDSN AIF   ('&DCBDSN' NE '').DCBDSN
.TDCBDDN AIF   ('&DCBDDN' NE '').DCBDDN
.TFREE   AIF   ('&FREE' EQ 'CLOSE').FREE                         TE7343
.TPERM   AIF   ('&PERM' EQ 'PERM' OR '&PERM' EQ 'PERMANENT').PERM
         AIF   ('&DUMMY' EQ 'PERM' OR '&DUMMY' EQ 'PERMANENT').PERM
.TSPECI  AIF   ('&SPECIAL' NE '').SPECIAL
         AGO   .SVC99
.SSREQ   RCPSSREQ
         AGO   .TDSN
.DSN     RCPDSN &DSN&DA,&MEMBER
         AGO   .TDDN
.DSNPDE  RCPDSNPD &DSNPDE
         AGO   .TDDN
.DSNRT   RCPDSNRT &DSNRET
&DSNRTO  SETA  &DTUO-46
         AGO   .TDDN
.SYSOUT  RCPSYSOU &SYSOUT,COPIES=&COPIES,FREE=&FREE,DEST=&DEST,        X
               FORMS=&FORMS
         AGO   .TDDN
.DUMMY   RCPDUMMY &DUMMY
         AGO   .TDDN
.QNAME   RCPQNAME &QNAME
         AGO   .TDDN
.DDN     RCPDDN &DDN&F&FILE
         AGO   .TUNIT
.DDNRT   RCPDDNRT &DDNRET
&DDNRTO  SETA  &DTUO-10
         AGO   .TUNIT
.UNIT   RCPUNIT &UNIT,&VOL
         AGO   .TVOLRET
.VOLRET  RCPVOLRT &VOLRET
&VOLRTO  SETA  &DTUO-8
         AGO   .TDSRGO
.DSORGRT RCPDSRGR
&DSRGRTO SETA  &DTUO-2
         AGO   .TLABEL
.LABEL   RCPLABEL &LABEL
         AGO   .TPSWD
.PASWORD RCPPSWD &PASWORD
         AGO   .TFORUSE
.FORUSER RCPFORUS &FORUSER
         AGO   .TTU
.TU      RCPTU &TU
         AGO   .TDISP
.DISP    RCPDISP &DISP
         AGO   .TSPACE
.SPACE   RCPSPACE &SPACE
         AGO   .TLRECL
.DCB     RCPDDCB LRECL=&LRECL,DEN=&DEN,RECFM=&RECFM,BLKSIZE=&BLKSIZE,  X
               DSORG=&DSORG,KEYLEN=&KEYLEN
         AGO .TDCBDSN
.DCBDSN  RCPDCBDS &DCBDSN
         AGO .TDCBDDN
.DCBDDN  RCPDCBDD &DCBDDN
         AGO .TFREE                                              TE7343
.FREE    RCPFREE  &FREE                                          TE7343
         AGO   .TPERM
.PERM    RCPPERM
         AGO   .TSPECI
.SPECIAL RCPSPEC &SPECIAL
.SVC99   ANOP
&DTUPO   SETA  &DTUPO-4
         SPACE
         MVI   &DYNP.TUP+&DTUPO,X'80'  SET HIGH ORDER BIT ON TEXT PTRS
         MVI   &DYNP.RBP,X'80'         SET HIGH ORDER BIT ON RB PTR
         RCPSR2 UNSAVE
&DTUPO   SETA  &DTUPO+4
         AIF   (NOT &DSECT).DYNA
         DROP  R1,R15                  DEACTIVATE ADDRESSABILITY
         LA    R14,4(R1)               POINT TO REQUEST BLOCK
.DYNA    DYNALLOC
         AIF   (NOT &DSECT).LTR
         USING &DYNP.RB,R14            SET UP ADDRESSABILITY
**       NOTE  R14 HAS RB ADDRESS, R15 HAS SVC 99 RETURN CODE        **
.LTR     AIF   ('&ERROR' EQ '').TDDTO
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   &ERROR                  BRANCH IF NON ZERO
.TDDTO   AIF   ('&DDNTO' EQ '').RESERVE
&I       SETA  0
.DDNTOL  ANOP
&I       SETA  &I+1
         AIF   ('&DDNTO(&I)' EQ '').RESERVE
         AIF   ('&DDNTO(&I)'(1,1) EQ '(').DDNTOR
         MVC   &DDNTO(&I).(8),&DYNP.TU+&DDNRTO+2
         AGO   .DDNTOL
.DDNTOR  ANOP
&C       SETC  '&DDNTO(&I)'(2,K'&DDNTO(&I)-2)
         MVC   0(8,&C),&DYNP.TU+&DDNRTO+2
         AGO   .DDNTOL
.RESERVE AIF   (&DSECT).RESDS
         SPACE 1
***********************************************************************
**       RESERVE SPACE FOR DYNALLOC PARAMETER LIST                   **
***********************************************************************
         RCPDS
.SSP     ANOP
&DYNP.RBP DS   F                       SVC 99 REQ BLOCK POINTER
&DYNP.RB  DS   5F                      SVC 99 REQUEST BLOCK
&DYNP.TUP DS   CL&DTUPO                SPACE FOR TEXT POINTERS
         AIF   (&DTUO EQ 0).DTU21
&DYNP.TU  DS   CL&DTUO                 SPACE FOR TEXT UNITS
         AIF   (&DSNRTO EQ 0).TDDNRTO
&DSNRET  EQU   &DYNP.TU+&DSNRTO        OFFSET TO RETURNED DSN
.TDDNRTO AIF   ('&DDNRET' EQ '').DTU11
&DDNRET  EQU   &DYNP.TU+&DDNRTO        OFFSET TO RETURNED DDNAME
.DTU11   AIF   (&VOLRTO EQ 0).DTU12
&VOLRET  EQU   &DYNP.TU+&VOLRTO        OFFSET TO RETURNED VOLSER
.DTU12   AIF   (&DSRGRTO EQ 0).DTU10
&DSORGRT EQU   &DYNP.TU+&DSRGRTO       OFFSET TO RETURNED DSORG
         AGO   .DTU10
.DTU21   ANOP
&DYNP.TU  DS   0C                      NO SPACE NEEDED FOR TEXT UNITS
.DTU10   ANOP
&DYNP.LEN EQU  *-&DYNP.RBP             LENGTH OF SPACE USED
         AIF   (&DSECT).DSP
         RCPDS
         SPACE 3
         AGO   .EXIT
.RESDS   ANOP
         AIF   ('&DYNSP' EQ '').SP3
         DYNSPACE ADD
.SP3     SPACE
&DYNP.DS DSECT                         DSECT TO MAP SVC 99 DATA
         AGO   .SSP
.DSP     AIF   ('&MF(3)' EQ '').END1
&MF(3)   EQU   &DYNP.LEN               LENGTH OF AREA
.END1    ANOP
&SYSECT  CSECT
         SPACE 3
.EXIT    MEND
./       ADD   NAME=DATASECT
         TITLE 'DATA CSECT.'
DATASECT CSECT
         DS    0D
DTWORK   DC    D'0'                WORK FIELD FOR DATE CONVERSION
DTPARM   DC    AL4(0)
DTL4     EQU   DTWORK+4,4          4 BYTES FOR EDIT MACRO          @152
DTL3     EQU   DTWORK+5,3          3 BYTES FOR EDIT MACRO
DTL2     EQU   DTWORK+6,2          2 BYTES FOR EDIT MACRO
PLIST    DS    0D
DDNAME   DC    CL8'VOLUME  '       DDNAME USED IN ALLOCATING A VTOC.
VOLSER   DC    CL6' '              VOLSER OF VOLUME BEING PROCESSED
DEVTYP   DC    XL4'00'             DEVICE TYPE OF ABOVE.           @141
F3CAMLST CAMLST  SEEK,CCHHR3,                                          X
               VOLSER,F3WORK       CAMLST TO READ A FORMAT3 DSCB.
CCHHR3   DC    XL5'00'             POINTER TO FORMAT 3 DSCB
F3WORK   DC    XL148'00'           FORMAT 3 DSCB WORKAREA.
VTOCCHHR DC    XL5'00'             CCHHR OF CURRENT DSCB.          @160
TABBXLE  DS    0F
TABSTART DC    AL4(0)              START OF DSCB TABLE.
         DC    AL4(SAVELEN)        LENGTH OF DATA SAVED FOR A DATASET.
TABEND   DC    AL4(0)              END OF DSCB TABLE.
TABLEN   DC    AL4(0)              LENGTH OF DSCB TABLE.
DSCBS    DC    AL4(0)              COUNT OF DSCB'S READ.
TABOFF   DC    AL4(0)              CURRENT OFFSET W/IN DSCB TABLE.
TABMIN   DC    A(SAVELEN*100000)   MIN # OF SLOTS TO GETMAIN
TABMAX   DC    A(SAVELEN*6400000)  MAX # OF SLOTS TO GETMAIN       @150
TABAD    DC    AL4(0)              START OF GOTMAINED AREA
TABGOT   DC    AL4(0)              LENGTH OF GOTMAINED AREA.
MAXDSCBS DC    AL4(0)              MAX # OF DSCBS I CAN HANDLE
         SPACE ,
MAXMSG   DC    CL8' '              PRINT AREA FOR TABLE CAPACITY.  @150
         SPACE ,
RETCODE  DC    F'0'                RETURN CODE AT EOJ.             @150
         SPACE ,
DSCBLEN  EQU   140                 LENGTH OF A DSCB
PAGELEN  DC    F'60'               LINES PER PAGE                  @TEC
         SPACE ,
ISPLINK  DC    AL4(0)              ENTRY ADDRESS OF ISPLINK
         SPACE ,
COMPDATE DC    PL4'0'              DATE TO COMPARE FOR AGE SELECT. @151
OLDDATE  DC    PL2'0'              AGE TO SELECT ON.               @151
DSCBOLD  DC    XL3'00'             THRESH DATE IN DSCB FORMAT.     @151
SAVLSTAR DC    XL3'00'             SAVE DS1LSTAR FOR EMPTY CHECK.  @151
         SPACE ,
         DS    0F                                                  @165
WICOMP   DC    PL4'0'              DATE TO COMPARE FOR RECENTNESS. @160
WIDATE   DC    PL2'0'              RECENTNESS TO SELECT.           @160
DSCBWI   DC    XL3'00'             THRESH DATE IN DSCB FORMAT.     @160
         SPACE ,
         DS    0F                                                  @165
CRCOMP   DC    PL4'0'              DATE TO COMPARE FOR CREATION    @165
CRDATE   DC    PL2'0'              NUMBER OF DAYS AGO.             @165
DSCBCR   DC    XL3'00'             CREATE DATE IN DSCB FORMAT.     @165
         SPACE ,
DENSITY  DC    CL1' '              TAPE DENSITY TO SELECT.         @160
         SPACE ,
TRKVALUE DC    F'0'                SIZE THRESHOLD VALUE            @151
TRKPACK  DC    D'0'                PACK WORKAREA.                  @151
         SPACE ,
SAVEQUAL DC    CL9' '              CURRENT HI-LEVEL QUALIFIER.     @140
QUALLEN  DC    H'9'                LENGTH OF QUALIFIER             @141
FIRSTQ   DC    XL1'FF'             DON'T PRINT A TOTAL PAGE 1ST.   @141
         SPACE ,
UDSCOUNT DC    F'0'                COUNT OF DATASETS, THIS USER.   @140
UTRALLOC DC    F'0'                TRACKS ALLOCATED, THIS USER.    @140
UTRUSED  DC    F'0'                TRACKS USED, THIS USER.         @140
         SPACE ,
         DS    0F
BUFPTR   DC    AL4(0)
ACBMCDS  ACB   AM=VSAM,DDNAME=SYSUT1,BUFNI=16,STRNO=2,             @164X
               BUFND=24                                            @165
MCDSOPEN DC    X'00'               SET TO FF AFTER MCDS HAS BEEN OPENED
         SPACE ,
NUMBERS  DC    C'0123456789ABCDEF'
TRHEX    EQU   *-256
         EJECT
*
* FLAGS1
TAPE     EQU   B'10000000'         SELECT TAPE DATASETS
SORT     EQU   B'01000000'         SORT THE DATASETS BY DSNAME
DASD     EQU   B'00100000'         SELECT DASD DATASETS
LIST     EQU   B'00010000'         1-UP LIST SELECTED DATASETS
HSM      EQU   B'00001000'         SELECT MIGRATED DATASETS
LEVEL    EQU   B'00000100'         SELECT BY LEFTMOST OF DSNAME
NOLEVEL  EQU   B'00000010'         EXCLUDE BY LEFTMOST OF DSNAME
*
* FLAGS2
VOLUME   EQU   B'10000000'         SELECT BY VOLUME
NOVOLUME EQU   B'01000000'         EXCLUDE BY VOLUME
HSMTAPE  EQU   B'00100000'         (OBSOLETE)                      @120
*
CTLGSEL  EQU   B'00011100'         SELECT BY CATALOG STATUS        @130
*
CAT      EQU   B'00010000'         SELECT IF CATALOGUED            @130
*
UNCAT    EQU   B'00001000'         SELECT IF NOT CATALOGUED        @130
*
MISCAT   EQU   B'00000100'         SELECT IF CATALOGUED WRONG      @130
*
BRKLVL   EQU   B'00000010'         PAGE BREAK ON NEW HIQUAL        @140
*
ASM2     EQU   B'00000001'         SELECTED ASM2 ARCHIVED          @150
*
*
* FLAGS3
UNUSED   EQU   B'10000000'                                         @151
EMPTY    EQU   B'01000000'                                         @151
CONTAIN  EQU   B'00100000'                                         @151
ENDING   EQU   B'00010000'                                         @151
NOTCONT  EQU   B'00001000'                                         @151
NOTEND   EQU   B'00000100'                                         @151
BYSIZE   EQU   B'00000010'                                         @151
*
* FLAGS4
LARGER   EQU   B'10000000'                                         @151
SMALLER  EQU   B'01000000'                                         @151
SIZESEL  EQU   SMALLER+LARGER                                      @151
ONEFILE  EQU   B'00100000'                                         @152
DUMPVMF  EQU   B'00010000'                                         @160
DATEPROT EQU   B'00001000'                                         @160
WITHIN   EQU   B'00000100'                                         @160
DNSTY    EQU   B'00000010'                                         @160
VOLSORT  EQU   B'00000001'                                         @161
*
* FLAGS5
CREATEBY EQU   B'10000000'
READBY   EQU   B'01000000'
CLEANUP  EQU   B'00100000'                                         @165
HSML1    EQU   B'00010000'                                         @165
HSML2    EQU   B'00001000'                                         @165
CHANGED  EQU   B'00000100'                                         @165
DSORG    EQU   B'00000010'                                         @165
CREATED  EQU   B'00000001'                                         @165
*
* FLAGS6
T3420    EQU   B'10000000'                                         @166
T3480    EQU   B'01000000'                                         @166
PGL      EQU   B'00100000'                                         @TEC
*
NOTAPE   EQU   255-TAPE
NOSORT   EQU   255-SORT
NODASD   EQU   255-DASD
NOLIST   EQU   255-LIST
NOHSM    EQU   255-HSM
NOHSMTAP EQU   255-HSMTAPE                                         @120
NOCAT    EQU   255-CAT                                             @130
NOUNCAT  EQU   255-UNCAT                                           @130
NOMISCAT EQU   255-MISCAT                                          @130
NOASM2   EQU   255-ASM2                                            @150
NOBRKLVL EQU   255-BRKLVL                                          @151
*
* DEFAULTS: NOTAPE,SORT,DASD,NOLIST,NOHSM,NOHSMTAPE                @141
*
         DS    0F
FLAGS    DC    AL1(SORT+DASD),AL1(0),AL1(0),AL1(0),AL1(0),AL1(0)   @166
FLAGS1   EQU   FLAGS,1
FLAGS2   EQU   FLAGS+1,1
FLAGS3   EQU   FLAGS+2,1
FLAGS4   EQU   FLAGS+3,1
FLAGS5   EQU   FLAGS+4,1                                           @162
FLAGS6   EQU   FLAGS+4,1                                           @166
         SPACE ,
DFLTFLAG DC    AL1(SORT+DASD),AL1(0),AL1(0),AL1(0),AL1(0),AL1(0)   @166
INTFLAG  DC    AL1(0)              INTERNAL FLAGS.                 @162
EODPARMS EQU   B'10000000'         EOF REACHED ON SYSIN.           @162
UNITPARM EQU   B'01000000'         UNIT LIST ENTERED.              @162
UNITOK   EQU   B'00100000'         UNIT LIST WAS VALID.            @165
EOJPARMS EQU   B'00010000'         ENDREQ WAS LAST PARAMETER.      @165
SYSPOPEN EQU   B'00001000'         SYSPRINT FILE ALREADY OPEN.     @165
UNDERSPF EQU   B'00000100'         NOCELL RUNNING AS AN ISPF DIALOG@SPF
         SPACE ,
*
* DEFAULTS: NO REPORTS
*
REPORTS  DC    AL1(0)
REPORT1  EQU   B'10000000'
REPORT2  EQU   B'01000000'
REPORT3  EQU   B'00100000'
REPORT4  EQU   B'00010000'
REPORT5  EQU   B'00001000'
REPORT6  EQU   B'00000100'
REPORT7  EQU   B'00000010'
REPORT8  EQU   B'00000001'
NREPORT1 EQU   255-REPORT1
NREPORT2 EQU   255-REPORT2
NREPORT3 EQU   255-REPORT3
NREPORT4 EQU   255-REPORT4
NREPORT5 EQU   255-REPORT5
NREPORT6 EQU   255-REPORT6
NREPORT7 EQU   255-REPORT7
NREPORT8 EQU   255-REPORT8
ETAB     DS    0H                                                  @151
         DC    AL1(255),CL8' '                                     @151
         DC    16X'000000000000000000'                             @151
         DC    X'FF'                                               @151
CTAB     DS    0H                                                  @151
         DC    AL1(255),CL8' '                                     @151
         DC    16X'000000000000000000'                             @151
         DC    X'FF'                                               @151
NETAB    DS    0H                                                  @151
         DC    AL1(255),CL8' '                                     @151
         DC    16X'000000000000000000'                             @151
         DC    X'FF'                                               @151
NCTAB    DS    0H                                                  @151
         DC    AL1(255),CL8' '                                     @151
         DC    16X'000000000000000000'                             @151
         DC    X'FF'                                               @151
LTAB     DS    0H
         DC    AL1(255),CL8' '
         DC    16X'000000000000000000'
         DC    X'FF'
NLTAB    DS    0H
         DC    AL1(255),CL8' '
         DC    16X'000000000000000000'
         DC    X'FF'
         SPACE ,
DVTAB    DS    0H                                                  @160
         DC    AL1(255),CL6' '                                     @160
         DC    16X'00000000000000'                                 @160
         DC    X'FF'                                               @160
         SPACE ,
WITAB    DS    0H                                                  @160
         DC    AL1(255),CL3' '                                     @160
         DC    X'FF'                                               @160
         SPACE ,
UNITS    DS    0H                                                  @162
         DC    AL1(255),CL8' '                                     @162
         DC    4X'000000000000000000'                              @162
         DC    X'FF'                                               @162
         SPACE ,
PLPACK   DC    D'0'                                                @TEC
PLTAB    DS    0H                                                  @TEC
         DC    AL1(255),CL2'60'                                    @TEC
         DC    X'FF'                                               @TEC
         SPACE ,
UTAB     DS    0H                                                  @151
         DC    AL1(255),CL3' '                                     @151
         DC    X'FF'                                               @151
         SPACE ,
STAB     DS    0H                                                  @151
         DC    AL1(255),CL4' '                                     @151
         DC    X'FF'                                               @151
         DS    0F
CREATJOB DC    AL1(255),CL8' '                                     @162
         DC    8XL9'00'                                            @162
         DC    X'FF'                                               @162
READJOB  DC    AL1(255),CL8' '                                     @162
         DC    8XL9'00'                                            @162
         DC    X'FF'                                               @162
         SPACE ,
DOTAB    DC    AL1(255),CL2' '                                     @165
         DC    4XL3'00'                                            @165
         DC    X'FF'                                               @165
         SPACE ,
         SPACE ,
DOTAB$   DS    0H                                                  @165
         DC    AL1(255),CL3' '                                     @165
         DC    04X'00000000'                                       @165
         DC    X'FF'                                               @165
         SPACE ,
CRTAB    DS    0H                                                  @165
         DC    AL1(255),CL3' '                                     @165
         DC    X'FF'                                               @165
         SPACE ,
         DS    0F
CLASS    DC    AL4(*+4)
*        DC    CL1'A'
         DC    CL1'X'                                              @170
         DYNSPACE  ,
         LTORG ,
NVTAB    DS    0H
         DC    AL1(255),CL6' '
         DC    16X'00000000000000'
         DC    X'FF'
VTAB     DS    0H
         DC    AL1(255),CL6' '
         DC    64X'00000000000000'  $NTC
         DC    X'FF'
SCANTAB  DS    0D
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'00000000000000000000000000000000'  0
         DC    X'00000000000000000000000000000000'  1
         DC    X'00000000000000000000000000000000'  2
         DC    X'00000000000000000000000000000000'  3
         DC    X'00000000000000000000000000000000'  4
         DC    X'00000000000000000000000000020000'  5
         DC    X'00000000000000000000000100000000'  6
         DC    X'00000000000000000000000000000000'  7
         DC    X'00000000000000000000000000000000'  8
         DC    X'00000000000000000000000000000000'  9
         DC    X'00000000000000000000000000000000'  A
         DC    X'00000000000000000000000000000000'  B
         DC    X'00000000000000000000000000000000'  C
         DC    X'00000000000000000000000000000000'  D
         DC    X'00000000000000000000000000000000'  E
         DC    X'00000000000000000000000000000000'  F
./       ADD   NAME=DOUCB
         TITLE 'PROCESS A DASD VOLUME.'
DOUCB   $PROLOG R12
         USING UCBOB,R3           ADDR TO UCB DSECT                @150
         L     R11,=V(DATASECT)    ADDRESS OF DATA CSECT
         USING DATASECT,R11        SET ADDRESSIBILITY.
         LA    R10,DOUCB99
         MVC   VOLSER,UCBVOLI      SETUP VOLSER IN PLIST.
         MVC   DEVTYP,UCBTYP       SETUP DEVICE TYPE.              @141
         TM    FLAGS2,VOLUME+NOVOLUME  ALL VOLUMES WANTED?
         BZ    DOUCBALL            DO THEM ALL
        $TEST  FLAGS2,VOLUME,VTAB,VOLSER,7,DOUCBALL,(R10)
        $TEST  FLAGS2,NOVOLUME,NVTAB,VOLSER,7,(R10),DOUCBALL
DOUCBALL LA    R0,1                CALL MODE 1 FOR OPEN
         LA    R1,PLIST            OPEN PARAMETER LIST
        $CALL  XVTCREAD
         LTR   R15,R15             DID IT OPEN?
         BZ    DOUCB1              YES,
         ABEND 99,DUMP             NO, DIE YOU SUCKER.
DOUCB1   SR    R0,R0               CALL MODE 0 FOR READ.
         LA    R1,VTOCCHHR         FEEDBACK FIELD FOR DSCB ADDR.   @160
        $CALL  XVTCREAD
         CH    R15,=H'4'           END OF VTOC?
         BE    DOUCB9              THAT'S ALL, FOLKS
         LR    R5,R1               ADDR OF DSCB JUST READ
         LA    R5,8(,R5)           SKIP OVER COUNT FIELD.
         USING IECSDSL1,R5         ADDRESS FORMAT1 DSCB DSECT
         CLI   DS1FMTID,C'1'       IS IT A FORMAT 1 DSCB?
         BNE   DOUCB1              NO, SKIP IT.
         TM    FLAGS1,LEVEL+NOLEVEL  DATASET SELECTION WANTED?
         BZ    DOUCB1A             IF NOT, PROCESS IT.
        $TEST  FLAGS1,LEVEL,LTAB,DS1DSNAM,9,DOUCB1A,DOUCB1
        $TEST  FLAGS1,NOLEVEL,NLTAB,DS1DSNAM,9,DOUCB1,DOUCB1A
DOUCB1A  TM    FLAGS2,CTLGSEL      CATALOG SELECTION?              @130
         BZ    DOUCB1B             NO, LETS DO IT.                 @130
         MVC   CAMDSN,DS1DSNAM     SETUP DSN FOR LOCATE.           @130
         LOCATE  CAMLOC            LOOK UP DSN IN CATALOG.         @130
         C     R15,16              RC GT 16?                       @130
         BNH   DOUCB1OK            NO, PROBABLY OK.                @130
         ABEND 130,DUMP            INVALID RETURN CODE.            @130
DOUCB1OK B     DOUCB1BR(R15)       ACTION DEPENDS ON RETURNCODE    @130
DOUCB1BR B     DOUCB1CT            00=CATALOGUED.                  @130
         B     DOUCB1UN            04=NO CATALOGUE                 @130
         B     DOUCB1UN            08=NOT CATALOGUED.              @130
         B     DOUCB1UN            12=NOT CATALOGUED.              @130
         B     DOUCB1UN            16=NOT CATALOGUED.              @130
DOUCB1CT CLC   LOCAREA+6(6),=C'MIGRAT'  CTLGED TO MIGRAT?          @130
         BE    DOUCB1B             IF SO, DON'T CALL IT UNCAT      @165
         CLC   LOCAREA+6(6),VOLSER  CATALOGUED TO THIS VOLUME?     @130
         BNE   DOUCB1MS            NO, SCREWUPS WANTED?            @130
         TM    FLAGS2,CAT          CATALOGUED WANTED?              @130
         BO    DOUCB1B             YES. SO DO IT.                  @130
         B     DOUCB1              OR NOT AS THE CASE MAY BE.      @130
DOUCB1MS TM    FLAGS2,MISCAT       MIS-CATALOGUED OK?              @130
         BO    DOUCB1B             YES. SO DO IT.                  @130
         B     DOUCB1              OR NOT AS THE CASE MAY BE.      @130
DOUCB1UN TM    FLAGS2,UNCAT        UNCATALOGUED TO BE SELECTED?    @130
         BZ    DOUCB1              NO. SKIP IT.                    @160
         TM    FLAGS5,CLEANUP      CLEANUP CARDS WANTED.           @165
         BZ    DOUCB2UN            NO                              @165
         LA    R1,1                SAY WHICH ERROR.                @165
        $CALL  GENCARDS            CREATE A DEFNVSAM COMMAND.      @165
DOUCB2UN DS    0H                                                  @165
         CLC   =C'VSAMDSET',DS1DSNAM FUNNY VSAM NAME?              @160
         BE    DOUCB1              YES. DON'T SAY UNCAT.           @160
         CLC   =C'VSAMDSPC',DS1DSNAM FUNNY VSAM NAME?              @160
         BE    DOUCB1              YES. DON'T SAY UNCAT.           @160
         CLC   =C'Z999',DS1DSNAM   OTHER VSAM NAME?                @160
         BE    DOUCB1              YES. DON'T SAY UNCAT.           @160
         CLC   =C'CATINDEX',DS1DSNAM ICF CATALOG COMPONENT?        @160
         BE    DOUCB1              YES. DON'T SAY UNCAT.           @160
         CLC   =C'SYS',DS1DSNAM    LOOK LIKE A TEMPORARY?          @160
         BNE   DOUCB1B             NO. SELECT IT.                  @160
         CLC   =C'.T',DS1DSNAM+8   STILL LOOK LIKE A TEMPORARY?    @160
         BNE   DOUCB1B             NO. SELECT IT.                  @160
         B     DOUCB1              SKIP IT.
DOUCB1B  DS    0H
        $TEST  FLAGS3,CONTAIN,CTAB,(DS1DSNAM,1,44),9,DOUCBNC,DOUCB1
DOUCBNC  DS    0H
        $TEST  FLAGS3,NOTCONT,NCTAB,(DS1DSNAM,1,44),9,DOUCB1,DOUCBNNC
DOUCBNNC DS    0H
        $TEST  FLAGS3,ENDING,ETAB,(DS1DSNAM,,44),9,DOUCBNE,DOUCB1
DOUCBNE  DS    0H
        $TEST  FLAGS3,NOTEND,NETAB,(DS1DSNAM,,44),9,DOUCB1,DOUCBNNE
DOUCBNNE DS    0H
         TM    FLAGS5,DSORG        SELECT BY DSORG?                @166
         BZ    DOUCBDO             NO.                             @166
         CLC   DS1DSORG,=X'0000'   DSORG INVALID?                  @166
         BNE   DOUCBDO2            NO.                             @166
         LA    R1,DOTAB                                            @166
DOUCBDO1 SR    R14,R14                                             @166
         IC    R14,0(0,R1)                                         @166
         BCTR  R14,0                                               @166
         B     *+10                                                @166
         CLC   1(0,R1),=X'FEFE'    INVALID DSORG?                  @166
         EX    R14,*-6                                             @166
         BE    DOUCBDO                                             @166
         LA    R1,3(0,R1)                                          @166
         CLI   0(R1),255                                           @166
         BNE   DOUCBDO1                                            @166
         B     DOUCB1                                              @166
DOUCBDO2 DS    0H                                                  @166
        $TEST  FLAGS5,DSORG,DOTAB,DS1DSORG,3,DOUCBDO,DOUCB1
DOUCBDO  DS    0H                                                  @165
         TM    FLAGS3,UNUSED       UNUSED DATASETS ONLY?           @151
         BZ    DOUCB1C             NO                              @151
         CLC   DS1REFD,=XL3'00'    IS IT NEWLY CREATED?            @151
         BNE   *+18                NO.                             @166
         CLC   DS1CREDT,DSCBOLD    USE CREATION DATE.              @166
         BL    DOUCB1C             SELECT IF OLD ENOUGH.           @166
         B     DOUCB1              OTHERWISE, SKIP IT.             @166
         CLC   DS1REFD,DSCBOLD     HOW LONG HAS IT BEEN?           @151
         BL    DOUCB1C             LONG ENOUGH.                    @151
         B     DOUCB1              OTHERWISE, SKIP IT.             @151
         SPACE ,
DOUCB1C  TM    FLAGS4,DATEPROT     SELECT DATE PROTECTED ONLY?     @160
         BZ    DOUCB1D             NO. DON'T TEST EXPDT.           @160
         CLC   DS1EXPDT,=X'0B006F' 11/111 IS "PERMANENT" DSN.      @160
         BE    DOUCB1              DON'T COUNT AS PROTECTED.       @160
         CLC   DS1EXPDT,=XL3'00'   IS THERE AN EXPIRY DATE.        @160
         BE    DOUCB1              IF NOT, SKIP IT.                @160
DOUCB1D  DS    0H
         SPACE ,
         TM    FLAGS4,WITHIN       RECENTLY USED ONLY.             @162
         BZ    DOUCB1E             NO.                             @162
         SR    R0,R0               CLEAR 3 BYTES.                  @162
         IC    R0,DS1REFD          GET YEAR                        @162
         MH    R0,=H'1000'         TIME 1000.                      @162
         MVC   DTWORK(2),DS1REFD+1  MOVE DAY                       @162
         AH    R0,DTWORK           ADD BINARY DAY                  @162
         CVD   R0,DTWORK           PACKED DECIMAL DATE.            @162
         CP    DTWORK,WICOMP       IS IT RECENT ENOUGH?            @162
         BL    DOUCB1              NO. SKIP THIS ONE.              @162
         SPACE ,
DOUCB1E  DS    0H
         TM    FLAGS5,CREATED      RECENTLY CREATED ONLY.          @165
         BZ    DOUCB1G             NO.                             @165
         SR    R0,R0               CLEAR 3 BYTES.                  @165
         IC    R0,DS1CREDT         GET YEAR                        @165
         MH    R0,=H'1000'         TIME 1000.                      @165
         MVC   DTWORK(2),DS1CREDT+1 MOVE DAY                       @165
         AH    R0,DTWORK           ADD BINARY DAY                  @165
         CVD   R0,DTWORK           PACKED DECIMAL DATE.            @165
         CP    DTWORK,CRCOMP       IS IT RECENT ENOUGH?            @165
         BL    DOUCB1              NO. SKIP THIS ONE.              @165
DOUCB1G  DS    0H
         SPACE ,
         L     R4,TABOFF           CURRENT TABLE ENTRY
         XC    0(SAVELEN,R4),0(R4)  ZERO IT
         USING SAVEFMT,R4          ADDRESS DATASET INFO TABLE.
         SR    R1,R1               CLEAR 3 BYTES.
         IC    R1,UCBTBYT4         GET 4TH BYTE OF UCBTYPE FIELD
         IC    R1,DTAB(R1)         AS INDEX INTO DEVICE TABLE.
         STC   R1,SV1TKCYL         TRACKS PER CYLINDER
         MVI   SV1DEVC,C'D'        CALL IT A DISK
         MVC   SV1DSNAM,DS1DSNAM   DSNAME
         MVC   SV1DSSN,VOLSER      VOLUME SERIAL NUMBER
         MVC   SV1CREDT,DS1CREDT   CREATION DATE
         MVC   SV1EXPDT,DS1EXPDT   EXPIRATION DATE
         MVC   SV1NOEPV,DS1NOEPV   NUMBER OF EXTENTS
         MVC   SV1REFD,DS1REFD     LAST DATE REFERENCED - SU 60
         MVC   SV1DSORG,DS1DSORG   DATA SET ORGANIZATION
         MVC   SV1PTRDS,DS1PTRDS   ADDR OF FORMAT 3.               @160
         SPACE ,
         MVC   SV1RECFM,=CL3' '    BLANK OUT RECFM.                @160
         SPACE ,
         MVC   SV1DEVT,UCBTYP      SAVE DEVICE TYPE.               @163
         SPACE ,
         TM    DS1RECFM,X'C0'      CHECK RECFM=U
         BNO   LIST2A              NO
         MVI   SV1RECFM,C'U'       SET RECFM VALUE IN PRINTLINE
         B     LIST2Z              TRY POSSIBLE 2ND BYTE.
LIST2A   TM    DS1RECFM,X'40'      CHECK RECFM=V
         BZ    LIST2B              NO
         MVI   SV1RECFM,C'V'       SET RECFM VALUE IN PRINTLINE
         B     LIST2Z              TRY POSSIBLE 2ND BYTE.
LIST2B   TM    DS1RECFM,X'80'      CHECK RECFM=F
         BZ    LIST2C              NO
         MVI   SV1RECFM,C'F'       SET RECFM VALUE IN PRINTLINE
         B     LIST2Z              TRY POSSIBLE 2ND BYTE.
LIST2C   MVI   SV1RECFM,C'?'       BEATS ME.
         B     LIST3Z              THAT'S ALL FOR RECFM
LIST2Z   TM    DS1RECFM,X'20'      TRACK OVERFLOW
         BZ    LIST3A              NO
         MVI   SV1RECFM+1,C'T'     SET RECFM VALUE IN PRINTLINE
         B     LIST3Z              THAT'S ALL FOR RECFM.
LIST3A   TM    DS1RECFM,X'08'      STANDARD BLOCKS
         BZ    LIST3B              NO
         MVI   SV1RECFM+1,C'S'     SET RECFM VALUE IN PRINTLINE
         B     LIST3Z              THAT'S ALL FOR RECFM.
LIST3B   TM    DS1RECFM,X'10'      BLOCKED
         BZ    LIST3Z              NO
         MVI   SV1RECFM+1,C'B'     SET RECFM VALUE IN PRINTLINE
LIST3Z   MVC   SV1BLKL,DS1BLKL     BLOCKSIZE
         MVC   SV1LRECL,DS1LRECL   RECORD LENGTH
         MVC   SV1LSTAR,DS1LSTAR   DS1LSTAR                        @151
         MVC   SV1EXT1,DS1EXT1     FIRST EXTENT DESCRIPTION
         MVC   SV1EXT2,DS1EXT2     SECOND EXTENT DESCRIPTION
         MVC   SV1EXT3,DS1EXT3     THIRD EXTENT DESCRIPTION
         SPACE ,
         TM    FLAGS5,CHANGED      CHANGED ONLY WANTED?            @165
         BZ    DOUCB1F             NO                              @165
         TM    DS1DSIND,DS1DSCHA   CHANGE FLAG ON?                 @165
         BO    DOUCB1F             YES. SELECT IT.                 @165
         B     DOUCB1              ELSE, SKIP IT                   @165
DOUCB1F  DS    0H                                                  @165
         SPACE ,
         CLC   DS1PTRDS,=5X'00'    IS THERE A FORMAT 3?
         BE    DOUCB2A             NO, SKIP IT.
         MVC   CCHHR3,DS1PTRDS     POINTER TO FORMAT 3 DSCB.
         LA    R7,SV3EXT4          ADDR TO STORE 1ST 4 EXTENTS     @141
         LA    R8,SV3EXT8          ADDR TO STORE THE REST.         @141
         SR    R1,R1               CLEAR HI-ORDER 3 BYTES.         @132
         IC    R1,SV1NOEPV         GET NUMBER OF EXTENTS.          @132
         SH    R1,=H'3'            MINUS THOSE IN FORMAT1          @132
NEXTF3   OBTAIN  F3CAMLST          READ IN A FORMAT 3 DSCB.
         LTR   R15,R15             DID WE GET IT?
         BNZ   DOUCB2A             OH WELL
         DROP  R5                  THAT'S ALL FOR THE FORMAT 1
         USING FORMAT3,R5          ADDRESS FORMAT 3 DSCB.
         LA    R5,F3WORK           ADDR OF FORMAT 3 DSCB
         MVC   0(40,R7),DS3EXTNT   EXTENTS 4,5,6 AND 7             @141
         MVC   0(90,R8),DS3ADEXT  EXTENTS 8 TO 16.                 @141
         SH    R1,=H'12'           12 EXTENTS PER F3 AT MOST.      @132
         LTR   R1,R1               ANY MORE EXTENTS                @132
         BNP   DOUCB2A             THAT'S ALL.                     @132
         AH    R7,=H'110'          IF MULTI FORMAT 3'S THEN ONLY   @132
         AH    R8,=H'110'          11 EXTENTS WERE VALID.          @132
         MVC   CCHHR3,DS3PTRDS     POINTER TO NEXT FORMAT3 DSCB.   @132
         B     NEXTF3              GO OBTAIN IT.                   @132
         DROP  R5                  THAT'S ALL FOR THE FORMAT 3.
DOUCB2A  SR    R15,R15             ZERO LOOP REGISTER
         SR    R7,R7               ZERO INDEX REGISTER
         SR    R8,R8               ZERO COUNT OF TRACKS ALLOCATED.
         IC    R15,SV1NOEPV        GET NUMBER OF EXTENTS
         LTR   R15,R15             ARE THERE ANY?
         BZ    LIST5A              SKIP IT IF ZERO EXTENT
LIST5B   EX    R0,EXTLIST(R7)      GET AN EXTENT ADDR IN R9.
         CLI   0(R9),X'00'         IS THIS AN ACTIVE EXTENT?
         BE    LIST5C              IF NOT, SKIP IT.
         SR    R0,R0               CLEAR 3 BYTES.
         ICM   R0,3,2(R9)          STARTING CC
         SR    R14,R14             CLEAR 3 BYTES.
         ICM   R14,1,SV1TKCYL      TRACKS PER CYLINDER
         SRDA  R0,32               SETUP TO MULTIPLY.
         MR    R0,R14              R1 = TRACKS OF CC IN STARTING CCHH
         ICM   R14,3,4(R9)         STARTING TT
         AR    R1,R14              STARTING RELATIVE TRACK.
         ST    R1,STARTTRK         SAVE IT.
         SR    R0,R0               CLEAR 3 BYTES.
         ICM   R0,3,6(R9)          ENDING CC
         SR    R14,R14             CLEAR 3 BYTES.
         ICM   R14,1,SV1TKCYL      TRACKS PER CYLINDER
         SRDA  R0,32               SETUP TO MULTIPLY.
         MR    R0,R14              R1 = TRACKS OF CC IN ENDING CCHH
         ICM   R14,3,8(R9)         ENDING TT
         AR    R1,R14              STARTING RELATIVE TRACK.
         S     R1,STARTTRK         TRACKS IN THIS EXTENT IN R1
         LA    R1,1(,R1)           KEEP IT HONEST
         AR    R8,R1               COUNT IT
         LA    R7,4(,R7)           INDEX THROUGH ALL EXTENTS.
LIST5C   BCT   R15,LIST5B          COUNTING THEM DOWN AS WE GO.
LIST5A   STCM  R8,15,SV1EXT1       TRACKS ALLOCATED
         SPACE ,
DOUCB2   TM    FLAGS4,SIZESEL      SIZE SELECTION?                 @151
         BZ    DOUCB2C             NO                              @151
         TM    FLAGS4,LARGER       WANT LARGEST ONLY?              @151
         BZ    DOUCB2D             NO. MUST BE SMALLEST.           @151
         CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BH    DOUCB2C             LARGE ENOUGH.                   @151
         B     DOUCB1              THROW IT BACK.                  @151
DOUCB2D  CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BL    DOUCB2C             SMALL ENOUGH                    @151
         B     DOUCB1              THROW IT BACK.                  @151
         SPACE ,
DOUCB2C  TM    FLAGS3,EMPTY        EMPTY DATASETS ONLY.            @151
         BZ    DOUCB2B             NO                              @151
         TM    SV1DSORG+1,X'08'    IS IT VSAM?                     @160
         BO    DOUCB1              YES. THEY ALWAYS LOOK EMPTY.    @160
         CLC   SV1LSTAR,=XL3'00'   ZERO TRACKS USED?               @151
         BNE   DOUCB1              NO, THEN SKIP IT.               @151
         TM    FLAGS5,CLEANUP      CLEANUP CARDS WANTED?           @165
         BZ    DOUCB2B             NO                              @165
         LA    R1,2                SAY WHICH ERROR TYPE.           @165
        $CALL  GENCARDS            CREATE A DELETE COMMAND.        @165
DOUCB2B  LA    R4,SAVELEN(,R4)     POINT TO NEXT ENTRY.
         ST    R4,TABOFF           AND REMEMBER WHERE WE ARE.
         L     R1,DSCBS            GET COUNT OF DSCBS READ.
         LA    R1,1(,R1)           INCREMENT BY 1
         ST    R1,DSCBS            AND SAVE IT.
         B     DOUCB1              KEEP READING.
DOUCB9   LA    R0,2                MODE 2 FOR CLOSE.
        $CALL  XVTCREAD
DOUCB99  DS    0H
        $EPILOG ,
DOUCBCLC CLC   1(0,R1),VOLSER
DOLEVCLC CLC   1(0,R1),0(R5)       DS1DSNAM
DOCONCLC CLC   1(0,R1),0(R7)       DS1DSNAM                        @151
DOENDCLC CLC   1(0,R1),0(R7)                                       @151
         DS    0F
STARTTRK DC    F'0'                ABS TRACK VALUE OF START OF EXTENT
CAMLOC   CAMLST  NAME,                                                 X
               CAMDSN,,LOCAREA     CAMLST TO CHECK IF DS IS CTLGED @130
CAMDSN   DC    CL44' '                                             @130
LOCAREA  DS    0D,CL265                                            @130
         LTORG ,
         DROP  R4
         DROP  R3
DTAB     DC    YL1(0)              00
         DC    AL1(0)              2311                     01
         DC    AL1(0)              2301                     02
         DC    AL1(0)              2303                     03
         DC    AL1(46)             2302                     04
         DC    AL1(0)              2321                     05
         DC    AL1(8)              2305-1                   06
         DC    AL1(8)              2305-2                   07
         DC    AL1(0)              2314                     08
         DC    AL1(19)             3330                     09
         DC    AL1(00)             UNKNOWN AS OF 4/75       0A
         DC    AL1(30)             3350                     0B
         DC    AL1(12)             3375                     0C   JCL
         DC    AL1(19)             3330-1                   0D
         DC    AL1(15)             3380                     0E   CST
         DC    AL1(15)             3390                     0F     @339
         PRINT OFF
         USING SAVEFMT,R4                                          @151
EXTLIST  LA    R9,SV1EXT1
         LA    R9,SV1EXT2
         LA    R9,SV1EXT3
         LA    R9,SV3EXT4
         LA    R9,SV3EXT5
         LA    R9,SV3EXT6
         LA    R9,SV3EXT7
         LA    R9,SV3EXT8
         LA    R9,SV3EXT9
         LA    R9,SV3EXT10
         LA    R9,SV3EXT11
         LA    R9,SV3EXT12
         LA    R9,SV3EXT13
         LA    R9,SV3EXT14
         LA    R9,SV3EXT15
         LA    R9,SV3EXT16
         LA    R9,SV3EXT17                                         @132
         LA    R9,SV3EXT18                                         @132
         LA    R9,SV3EXT19                                         @132
         LA    R9,SV3EXT20                                         @132
         LA    R9,SV3EXT21                                         @132
         LA    R9,SV3EXT22                                         @132
         LA    R9,SV3EXT23                                         @132
         LA    R9,SV3EXT24                                         @132
         LA    R9,SV3EXT25                                         @132
         LA    R9,SV3EXT26                                         @132
         LA    R9,SV3EXT27                                         @132
         LA    R9,SV3EXT28                                         @132
         LA    R9,SV3EXT29                                         @132
         LA    R9,SV3EXT30                                         @132
         LA    R9,SV3EXT31                                         @132
         LA    R9,SV3EXT32                                         @132
         LA    R9,SV3EXT33                                         @132
         LA    R9,SV3EXT34                                         @132
         LA    R9,SV3EXT35                                         @132
         LA    R9,SV3EXT36                                         @132
         LA    R9,SV3EXT37                                         @132
         LA    R9,SV3EXT38                                         @132
         LA    R9,SV3EXT39                                         @132
         LA    R9,SV3EXT40                                         @132
         LA    R9,SV3EXT41                                         @132
         LA    R9,SV3EXT42                                         @132
         LA    R9,SV3EXT43                                         @132
         LA    R9,SV3EXT44                                         @132
         LA    R9,SV3EXT45                                         @132
         LA    R9,SV3EXT46                                         @132
         LA    R9,SV3EXT47                                         @132
         LA    R9,SV3EXT48                                         @132
         LA    R9,SV3EXT49                                         @132
         LA    R9,SV3EXT50                                         @132
         LA    R9,SV3EXT51                                         @132
         LA    R9,SV3EXT52                                         @132
         LA    R9,SV3EXT53                                         @132
         LA    R9,SV3EXT54                                         @132
         LA    R9,SV3EXT55                                         @132
         LA    R9,SV3EXT56                                         @132
         LA    R9,SV3EXT57                                         @132
         LA    R9,SV3EXT58                                         @132
         LA    R9,SV3EXT59                                         @132
         LA    R9,SV3EXT60                                         @132
         LA    R9,SV3EXT61                                         @132
         LA    R9,SV3EXT62                                         @132
         LA    R9,SV3EXT63                                         @132
         LA    R9,SV3EXT64                                         @132
         LA    R9,SV3EXT65                                         @132
         LA    R9,SV3EXT66                                         @132
         LA    R9,SV3EXT67                                         @132
         LA    R9,SV3EXT68                                         @132
         LA    R9,SV3EXT69                                         @132
         LA    R9,SV3EXT70                                         @132
         LA    R9,SV3EXT71                                         @132
         LA    R9,SV3EXT72                                         @132
         LA    R9,SV3EXT73                                         @132
         LA    R9,SV3EXT74                                         @132
         LA    R9,SV3EXT75                                         @132
         LA    R9,SV3EXT76                                         @132
         LA    R9,SV3EXT77                                         @132
         LA    R9,SV3EXT78                                         @132
         LA    R9,SV3EXT79                                         @132
         LA    R9,SV3EXT80                                         @132
         LA    R9,SV3EXT81                                         @132
         LA    R9,SV3EXT82                                         @132
         LA    R9,SV3EXT83                                         @132
         LA    R9,SV3EXT84                                         @132
         LA    R9,SV3EXT85                                         @132
         LA    R9,SV3EXT86                                         @132
         LA    R9,SV3EXT87                                         @132
         LA    R9,SV3EXT88                                         @132
         LA    R9,SV3EXT89                                         @132
         LA    R9,SV3EXT90                                         @132
         LA    R9,SV3EXT91                                         @132
         LA    R9,SV3EXT92                                         @132
         LA    R9,SV3EXT93                                         @132
         LA    R9,SV3EXT94                                         @132
         LA    R9,SV3EXT95                                         @132
         LA    R9,SV3EXT96                                         @132
         LA    R9,SV3EXT97                                         @132
         LA    R9,SV3EXT98                                         @132
         LA    R9,SV3EXT99                                         @132
         LA    R9,SV3EXTA0                                         @132
         LA    R9,SV3EXTA1                                         @132
         LA    R9,SV3EXTA2                                         @132
         LA    R9,SV3EXTA3                                         @132
         LA    R9,SV3EXTA4                                         @132
         LA    R9,SV3EXTA5                                         @132
         LA    R9,SV3EXTA6                                         @132
         LA    R9,SV3EXTA7                                         @132
         LA    R9,SV3EXTA8                                         @132
         LA    R9,SV3EXTA9                                         @132
         LA    R9,SV3EXTB0                                         @132
         LA    R9,SV3EXTB1                                         @132
         LA    R9,SV3EXTB2                                         @132
         LA    R9,SV3EXTB3                                         @132
         LA    R9,SV3EXTB4                                         @132
         LA    R9,SV3EXTB5                                         @132
         LA    R9,SV3EXTB6                                         @132
         LA    R9,SV3EXTB7                                         @132
         LA    R9,SV3EXTB8                                         @132
         LA    R9,SV3EXTB9                                         @132
         LA    R9,SV3EXTC0                                         @132
         LA    R9,SV3EXTC1                                         @132
         LA    R9,SV3EXTC2                                         @132
         LA    R9,SV3EXTC3                                         @132
         DROP  R4                                                  @151
         PRINT ON
./       ADD   NAME=DYNSPACE
         MACRO
&NAME    DYNSPACE &TYPE
.*
.*    THIS IS AN INNER MACRO TO ALLOC/FREE.
.*    IT IS CALLED TO   A) NAME AN AREA FOR THE PARMLIST
.*                      B) LOG THE VARIOUS AMOUNTS NEEDED BY
.*                         EACH, REMEMBERING THE LARGEST.
.*                      C) GENERATING A DS FOR THE LARGEST AMOUNT.
.*    THE FIRST TWO FUNCTIONS ARE INVOKED BY ALLOC/FREE MACROS ONLY,
.*    AND THE THIRD IS USED BY THE PROGRAMMER, EITHER EXPLICITLY,
.*    OR BY BEGINWKA, IF THE LATTER IS USED.
.*
.*     TO INVOKE THE NAMING FUNCTION, ALLOC/FREE GENERATE
.*     NAME DYNSPACE
.*     NOTE. THE NAMING OPERATION ONLY GENERATES A NAME ON THE
.*     FIRST CALL IN THE ASSEMBLY. THE NAME REMAINS THE SAME UNTIL
.*     DYNSPACE IS CALLED TO EXPAND INTO A DS.
.*
.*     THE SECOND FUNCTION IS INVOKED BY THE MACRO CALL
.*          DYNSPACE ADD
.*     (NO NAME FIELD AND ONE OPERAND)
.*     IT USES THE GLOBAL VARIABLES &DTUO AND &DTUPO TO CALCULATE
.*     THE SPACE FOR THIS REQUEST, AND UPDATES &DYNSPQ ONLY IF THE
.*     CURRENT REQUEST IS FOR A GREATER AMOUNT
.*
.*     THE THIRD FUNCTION IS INVOKED BY CALLING DYNSPACE WITH NO
.*     NAME OR OPERAND FIELD.
.*     THIS EXPANDS INTO A DEFINE STORAGE, CLEARS THE DYNSPACE NAME
.*     GLOBAL SETC, AND THE &DYNSPQ GLOBAL SETA.
.*     THUS, THE MACRO IS SERIALLY REUSABLE IN ALL FUNCTIONS.
.*
         GBLA  &DYNSPQ,&DTUO,&DTUPO,&RCPDYN
         GBLC  &DYNP,&DYNSP
         LCLA  &I
         AIF   ('&NAME' NE '').NAME
         AIF   ('&TYPE' EQ '').ALLOC
.*   THE ACCUMULATE FUNCTION IS REQUIRED
&I       SETA  24+&DTUO+&DTUPO         GET AMOUNT FOR THIS REQUEST
         AIF   (&I LE &DYNSPQ).EXIT    IF CURRENT < MAX, EXIT
&DYNSPQ  SETA  &I                      ELSE UPDATE CURRENT MAXIMUM
         MEXIT
.NAME    AIF   ('&DYNSP' NE '').EXIT   IF NAME ALREADY EXISTS, EXIT
&DYNSP   SETC  'DYNSP&RCPDYN'           ELSE GENERATE A NAME
.EXIT    MEXIT
.ALLOC   AIF   ('&DYNSP' EQ '').EXIT
*
**     RESERVE SPACE FOR ALLOC/FREE MACRO WORK AREA
*
&DYNSP   DS    0F,CL&DYNSPQ            RESERVE SPACE
&DYNSP   SETC  ''                      SET MAX QUANTITY TO 0
&DYNSPQ  SETA 0
         MEND
./       ADD   NAME=EDIT
         MACRO
&NAME    EDIT  &PR,&NUM,&ED
         LCLA  &A1,&A2,&A3,&A4
         LCLB  &B1,&B2
         LCLC  &C1,&C2,&C3
         ACTR  200
         AIF   (N'&SYSLIST EQ 2 AND K'&ED EQ 0).ANOP1
         AIF   (N'&SYSLIST NE 3).MNOTE1
         AIF   ('&ED'(1,1) EQ '-' OR '&ED'(1,1) EQ '$').EDMK
.ANOP1   ANOP
&A1      SETA  &A1+1
         AIF   (K'&ED LT &A1).CONT
&A2      SETA  &A2+1
&A4      SETA  &A4+1
         AIF   ('&ED'(&A1,1) NE 'Z' AND '&ED'(&A1,1) NE '&C2').NINE
.ANOP2   ANOP
&C1      SETC  '&C1.20'
         AGO   .ANOP1
.NINE    AIF   ('&ED'(&A1,1) NE '9').PER
         AIF   (&B1).ANOP2
&B1      SETB  1
         AIF   (&A1 EQ 1).SIGNIF
         AIF   (&A1 EQ 2 AND &B2).SIGNIF
&C3      SETC  '&C1'(&A4*2-3,2)
         AIF   ('&C3' NE '20').NINE2
&C1      SETC  '&C1'(1,&A4*2-4).'2120'
         AGO   .ANOP1
.NINE2   ANOP
&C1      SETC  '&C1'(1,&A4*2-6).'21&C3.20'
         AGO   .ANOP1
.SIGNIF  ANOP
&C1      SETC  '21'
         AGO   .ANOP1
.PER     AIF   ('&ED'(&A1,1) NE '.').COMMA
&C1      SETC  '&C1.4B'
&A2      SETA  &A2-1
         AGO   .ANOP1
.COMMA   AIF   ('&ED'(&A1,1) NE 'V').SLASH
&C1      SETC  '&C1.6B'
&A2      SETA  &A2-1
         AGO   .ANOP1
.SLASH   AIF   ('&ED'(&A1,1) NE '/').DBCR
&C1      SETC  '&C1.61'
&A2      SETA  &A2-1
         AGO   .ANOP1
.DBCR    AIF   (&A1+1 NE K'&ED).MNOTE2
         AIF   ('&ED'(&A1,2) NE 'CR').DB
&C1      SETC  '&C1.C3D9'
&A2      SETA  &A2-1
         AGO   .CONT
.DB      AIF   ('&ED'(&A1,2) NE 'DB').MNOTE2
&C1      SETC  '&C1.C4C2'
&A2      SETA  &A2-1
.CONT    ANOP
&C3      SETC  '&PR'
         AIF   (L'&NUM*2-1 NE &A2).EDSIZE
.ENDCHEC ANOP
&A2      SETA  0
         AIF   (K'&C1/2+1 NE L'&PR).AREASIZ
.LAST    AIF   (&B2).EDMKEND
&NAME    MVC   &C3,=X'40&C1'
         ED    &C3,&NUM
         MEXIT
.EDSIZE  AIF   (L'&NUM*2-1 LT &A2).LONG
.SHORT   ANOP
&C1      SETC  '20&C1'
&A3      SETA  &A3+1
         AIF   (L'&NUM*2-1 GT &A2+&A3).SHORT
         AGO   .ENDCHEC
.LONG    ANOP
&C1      SETC  '&C1'(3,K'&C1-2)
&A3      SETA  &A3+1
         AIF   (L'&NUM*2-1 LT &A2-&A3).LONG
         AGO   .ENDCHEC
.AREASIZ AIF   (K'&C1/2+1 LT L'&PR).LESS
&A2      SETA  K'&C1/2+1-L'&PR
&A3      SETA  K'&C1/2+1
&C3      SETC  '&C3-&A2.(&A3)'
&B1      SETB  1
         AGO   .LAST
.LESS    ANOP
&B1      SETB  0
&A3      SETA  K'&C1/2+1
         AIF   (K'&C1/2+5 GE L'&PR).PLUSET
&C3      SETC  '&C3.(&A3)'
         AGO   .LAST
.PLUSET  ANOP
&A2      SETA  L'&PR-K'&C1/2-1
&C3      SETC  '&C3+&A2.(&A3)'
         AGO   .LAST
.EDMK    AIF   ('&ED'(1,1) EQ '-').NEG
&C2      SETC  '$'
         AGO   .C2SET
.NEG     ANOP
&C2      SETC  '-'
.C2SET   ANOP
&A1      SETA  1
&B2      SETB  1
         AGO   .ANOP1
.EDMKEND ANOP
&A1      SETA  -1
.EDMKNOP ANOP
&A1      SETA  &A1+2
         AIF   (K'&C1 LT &A1).SETA
         AIF   ('&C1'(&A1,2) EQ '21').SETAA
         AIF   ('&C1'(&A1,2) NE '4B').EDMKNOP
&A1      SETA  (&A1+1)/2
         AGO   .LA
.SETA    ANOP
&A1      SETA  (&A1+1)/2-1
         AGO   .LA
.SETAA   ANOP
&A1      SETA  (&A1+1)/2+1
.LA      ANOP
         AIF   (&B1).MINUS
&A1      SETA  &A1+&A2
         AGO   .MVC
.MINUS   ANOP
&A1      SETA  &A1-&A2
.MVC     ANOP
&NAME    MVC   &C3,=X'40&C1'
         LA    1,&PR+&A1
         EDMK  &C3,&NUM
         AIF   ('&C2' EQ '$').DOLLAR
         BNM   EDIT&SYSNDX
         BCTR  1,0
         MVI   0(1),C'-'
&C3      SETC  '&SYSNDX'
EDIT&C3  EQU   *
         MEXIT
.DOLLAR  BCTR  1,0
         MVI   0(1),C'$'
         MEXIT
.MNOTE1  MNOTE 8,'THREE OPERANDS MUST BE SPECIFIED'
         MEXIT
.MNOTE2  MNOTE 8,'INVALID EDIT PATTERN'
         MEND
./       ADD   NAME=FREE
         MACRO
&NAME    FREE  &UNALC,&DSN=,&DDN=,&MEMBER=,&DISP=,&SYSOUT=,            X
               &ERROR=,&MF=AUTO,&PREFIX=,&FILE=,&F=,&DA=,&HOLD=
         GBLA  &RCPDYN            COUNTER FOR NO ENTRIES TO MACRO
         GBLA  &DTUO              OFFSET TO TEXT UNITS
         GBLA  &DTUPO             OFFSET TO TEXT UNIT POINTERS
         GBLB  &RCPS99(2)         TELL RCPDSECT NEED DSECTS
         GBLC  &DYNP              PREFIX FOR LABELS FOR THIS CALL
         GBLC  &DYNSP         NAME FOR AUTOMATIC STORAGE ALLOC
         LCLB  &DSECT             DSECT NEEDED FOR STORAGE, MF=E
         LCLC  &C,&T,&PAR
&RCPS99(1)     SETB           1
&RCPDYN  SETA  &RCPDYN+1          INCEREMENT COUNTER
&DYNP    SETC  'DYN&RCPDYN' SET DEFAULT PREFIX
&NAME    DS    0H
         AIF   ('&PREFIX' EQ '').TMF
         AIF   (K'&PREFIX LT 4).POK
         MNOTE 4,'PREFIX TOO LONG, 1ST 4 CHARS USED'
&DYNP    SETC  '&PREFIX'(1,4)
         AGO   .TMF
.POK     ANOP
&DYNP    SETC  '&PREFIX'
.TMF     AIF   ('&MF(1)' EQ 'G').GEN
         AIF   ('&MF' NE 'AUTO').TMFE
NAME     DYNSPACE             GET NAME FOR SPACE
         LA    R1,&DYNSP               LOAD ADDRESS OF PARAM LIST
         USING &DYNP.DS,R1             USE GENERATED DSECT
&T       SETC  'A'
&PAR     SETC  '&DYNSP+4'
&DSECT   SETB  1
         AGO   .START
.TMFE    AIF   ('&MF(2)' NE '').E2OK
         MNOTE 4,'PLIST ADDRESS OMITTED, MF=G USED'
         AGO   .GEN
.E2OK    ANOP
&DSECT   SETB  1
         AIF   ('&MF(2)' EQ '(').RMFE
         LA    R1,&MF(2)               LOAD PARAM LIST ADDRESS
&T       SETC  'A'
&PAR     SETC  '&MF(2)+4'
         USING &DYNP.DS,R1             USE GENERATED DSECT
         AGO   .START
.RMFE    AIF   ('&MF(2)' EQ '(R1)' OR '&MF(2)' EQ '(1)').START
&PAR     SETC  '&MF(2)'(2,K'&MF(2)-2)
&T       SETC  'R'
         LR    R1,&PAR                 LOAD S99 PARAM LIST ADDRESS
&PAR     SETC  '4&MF(2)'
         USING &DYNP.DS,R1             USE GENERATED DSECT
         AGO   .START
.GEN     LA    R1,&DYNP.RBP            LOAD ADDRESS OF S99 RBP
&T       SETC  'A'
&PAR     SETC  '&DYNP.RB'
.START   LA    R15,&DYNP.RB            LOAD ADDRESS OF S99 RB
         USING S99RB,R15
         ST    R15,0(R1)               AND STORE IN RB POINTER
         XC    4(&DYNP.LEN-4,R1),4(R1) ZERO PARAMETER LIST
         MVI   S99RBLN,20              MOVE IN LIST LENGTH
         MVI   S99VERB,S99VRBUN        MOVE IN VERB CODE
         LA    R14,&DYNP.TUP           LOAD ADDRESS OF TU POINTERS
         ST    R14,S99TXTPP            STORE ADDRESS IN S99 RB
         LA    R15,&DYNP.TU            POINT TO SPACE FOR TEXT UNITS
         USING S99TUNIT,R15
&DTUO    SETA  0
&DTUPO   SETA  0
         AIF   ('&DSN&DA' NE '').DSN
         AIF   ('&SYSOUT' NE '').SYSOUT
.TDDN    AIF   ('&DDN&FILE&F' NE '').DDN
.TDISP   AIF   ('&DISP' NE '').DISP
.TUNALC  AIF   ('&UNALC' NE '').PERM
.THOLD   AIF   ('&HOLD' NE '').HOLD
         AGO   .SVC99
.DSN     RCPFDSN &DSN&DA,&MEMBER
         AGO   .TDDN
.SYSOUT  RCPFSYS &SYSOUT
         AGO   .TDDN
.DDN     RCPFDDN &DDN&F&FILE
         AGO   .TDISP
.DISP RCPFDISP &DISP
         AGO   .TUNALC
.PERM    RCPUNALC
         AGO   .THOLD
.HOLD    RCPFHOLD &HOLD
.SVC99   ANOP
&DTUPO   SETA  &DTUPO-4
         SPACE
         MVI   &DYNP.TUP+&DTUPO,X'80'  SET HIGH ORDER BIT ON TEXT PTRS
         MVI   &DYNP.RBP,X'80'         SET HIGH ORDER BIT ON RB PTR
         RCPSR2 UNSAVE
&DTUPO   SETA  &DTUPO+4
         AIF   (NOT &DSECT).DYNA
         DROP  R1,R15                  DEACTIVATE ADDRESSABILITY
.DYNA    DYNALLOC
         AIF   ('&ERROR' EQ '').RESERVE
         AIF   ('&PAR' EQ '').LTR
         L&T   R14,&PAR                 LOAD REG 14 WITH ADDRESS OF RB
         AIF   (NOT &DSECT).LTR
         USING &DYNP.RB,R14            SET UP ADDRESSABILITY
.LTR     LTR   R15,R15                 TEST RETURN CODE
         BNZ   &ERROR                  BRANCH IF NON ZERO
**       NOTE.  R14 POINTS TO REQUEST BLOCK, R15 HAS RETURN CODE     **
.RESERVE AIF   (&DSECT).RESDS
         SPACE
***********************************************************************
**       RESERVE SPACE FOR DYNALLOC DATA                             **
***********************************************************************
         RCPDS
.SSP     ANOP
&DYNP.RBP DS   F                       SVC 99 REQ BLOCK POINTER
&DYNP.RB  DS   5F                      SVC 99 REQUEST BLOCK
&DYNP.TUP DS   CL&DTUPO                SPACE FOR TEXT POINTERS
         AIF   (&DTUO EQ 0).DTU11
&DYNP.TU  DS   CL&DTUO                 SPACE FOR TEXT UNITS
         AGO   .DTU10
.DTU11   ANOP
&DYNP.TU  DS   0C                      NO SPACE NEEDED FOR TEXT UNITS
.DTU10   ANOP
&DYNP.LEN EQU  *-&DYNP.RBP             LENGTH OF SPACE USED
         AIF   (&DSECT).DSP
         RCPDS
         SPACE 3
         AGO   .EXIT
.RESDS   ANOP
         AIF   ('&DYNSP' EQ '').SP3
         DYNSPACE ADD
.SP3     SPACE
&DYNP.DS DSECT                         DSECT TO MAP SVC 99 DATA
         AGO   .SSP
.DSP     AIF   ('&MF(3)' EQ '').END1
&MF(3)   EQU   &DYNP.LEN               LENGTH OF AREA
.END1    ANOP
&SYSECT  CSECT
         SPACE 3
.EXIT    MEND
./       ADD   NAME=GENCARDS
         TITLE 'GENERATE COMMAND RECORDS TO CLEANUP PROBLEMS.'
GENCARDS $PROLOG R12
         USING SAVEFMT,R4          ADDRESS DATASET INFO TABLE.
         USING IECSDSL1,R5         ADDRESS FORMAT 1 DSCB.
         USING UCBOB,R3            ADDRESS UCB.
         LR    R2,R1               SAVE CALLER'S PARAMETER.
         L     R11,=V(DATASECT)    ADDRESS OF DATA CSECT
         USING DATASECT,R11        SET ADDRESSIBILITY.
         CLI   GENDCBFL,255        IS SYSPUNCH OPEN?
         BE    GENCRD01            YES
         OPEN  (SYSPUNCH,OUTPUT)
         TM    SYSPUNCH+48,X'10'   DID SYSPUNCH OPEN?
         BO    GENCRD02            YES.
         ABEND 165
GENCRD02 DS    0H
         MVI   GENDCBFL,255        SAY SYSPUNCH OPEN.
GENCRD01 DS    0H
         C     R2,=F'1'            FC=01. UNCAT DATASET.
         BNE   GENCRD10            TRY SOMETHING ELSE.
         MVI   GENDEFDS,C' '
         MVC   GENDEFDS+1(43),GENDEFDS BLANK OUT DATASET NAME.
         MVC   GENDEFDS,DS1DSNAM   SET UP DATASET NAME.
         TRT   GENDEFDS(45),GENTAB  FIND BLANK OR NULL
         MVC   0(3,R1),=C')) '     CLOSE OFF DSNAME
         MVC   GENDEFVO,VOLSER     MOVE IN VOLSER
         SR    R1,R1               CLEAR 3 BYTES.
         IC    R1,UCBTYP+3         GET 4TH BYTE OF UCBTYPE FIELD
         SLL   R1,3                TIMES 8
         LA    R1,DTAB2(R1)        GET ADDR OF DEVT
         MVC   GENDEFDV,8(R1)      MOVE IN DEVICE TYPE.
        $AMODE 24,SAVE=GENAMODE    AMODE24 FOR QSAM.
         MVC   PUNCHCRD,GENDEFM1   MOVE IN DEFINE COMMAND.
         PUT   SYSPUNCH,PUNCHCRD   CREATE DEFINE COMMAND.
         MVC   PUNCHCRD,GENDEFM2   MOVE IN DEFINE COMMAND.
         PUT   SYSPUNCH,PUNCHCRD   CREATE DEFINE COMMAND.
        $AMODE RESET=GENAMODE      RESTORE CALLER'S AMODE.
         B     GENCRD99            GET OUT.
GENCRD10 DS    0H
         C     R2,=F'2'            FC=02. EMPTY DATASET.
         BNE   GENCRD99            ??
         MVI   GENDELDS,C' '
         MVC   GENDELDS+1(43),GENDELDS BLANK OUT DATASET NAME.
         MVC   GENDELDS,SV1DSNAM   SET UP DATASET NAME.
         MVC   PUNCHCRD,GENDELMD   MOVE IN DELETE COMMAND.
        $AMODE 24,SAVE=GENAMODE    AMODE24 FOR QSAM.
         PUT   SYSPUNCH,PUNCHCRD   CREATE DELETE COMMAND.
        $AMODE RESET=GENAMODE      RESTORE CALLER'S AMODE.
GENCRD99 $EPILOG ,
         DROP  R3
         DROP  R4
         DROP  R5
         LTORG ,
GENAMODE DC    F'0'
GENDCBFL DC    XL1'00'
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM
PUNCHCRD DC    CL80' '
GENDELMD DC    CL80' '
         ORG   GENDELMD
         DC    C' DELETE '
GENDELDS DC    CL44' '
         DC    C' PURGE SCRATCH '
         ORG   GENDELMD+80
GENDEFM1 DC    CL80' '
         ORG   GENDEFM1
         DC    C' DEFINE NONVSAM(VOLUMES('
GENDEFVO DC    CL6' '
         DC    C') DEVT('
GENDEFDV DC    CL8' '
         DC    C') '
         ORG   GENDEFM1+80
GENDEFM2 DC    CL80' '
         ORG   GENDEFM2
         DC    C' NAME(  '
GENDEFDS DC    CL44' '
         ORG   GENDEFM2+80
         DS    0H
DTAB2    DC    CL8' '              00
         DC    CL8'2311'                                    01
         DC    CL8'2301'                                    02
         DC    CL8'2303'                                    03
         DC    CL8'2302'                                    04
         DC    CL8'2321'                                    05
         DC    CL8'2305-1'                                  06
         DC    CL8'2305-2'                                  07
         DC    CL8'2314'                                    08
         DC    CL8'3330'                                    09
         DC    CL8'  '                                      0A
         DC    CL8'3350'                                    0B
         DC    CL8'3375'                                    0C   JCL
         DC    CL8'3330-1'                                  0D
         DC    CL8'3380'                                    0E   CST
         DC    CL8'  '                                      0F
GENTAB   DS    0D
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'01000000000000000000000000000000'  0
         DC    X'00000000000000000000000000000000'  1
         DC    X'00000000000000000000000000000000'  2
         DC    X'00000000000000000000000000000000'  3
         DC    X'01000000000000000000000000000000'  4
         DC    X'00000000000000000000000000000000'  5
         DC    X'00000000000000000000000000000000'  6
         DC    X'00000000000000000000000000000000'  7
         DC    X'00000000000000000000000000000000'  8
         DC    X'00000000000000000000000000000000'  9
         DC    X'00000000000000000000000000000000'  A
         DC    X'00000000000000000000000000000000'  B
         DC    X'00000000000000000000000000000000'  C
         DC    X'00000000000000000000000000000000'  D
         DC    X'00000000000000000000000000000000'  E
         DC    X'00000000000000000000000000000000'  F
./       ADD   NAME=HEX
         MACRO
&NFS     HEX   &TO,&FROM,&L,&LEN=,&HEXTAB=,&BYTE=C' ',&DIGITS=
         GBLC  &HEXTAB#
         LCLA  &LT                      UNPK "TO" LENGTH
         LCLA  &LF                      UNPK "FROM" LENGTH
         LCLA  &LTR                     TR "TO" LENGTH
         LCLA  &LL                      &LEN ONE WAY OR ANOTHER
         LCLC  &F1,&F2,&T1,&T2,&LX
&F1      SETC  '&FROM(1)'               FOR SHORTER STRING LATER
&F2      SETC  '&FROM(2)'               FOR SHORTER STRING LATER
&T1      SETC  '&TO(1)'                 FOR SHORTER STRING LATER
&T2      SETC  '&TO(2)'                 FOR SHORTER STRING LATER
&LX      SETC  '&L&LEN'                 GET LENGTH USING EITHER METHOD
         AIF   ('&LX' EQ '').DEFLEN     LENGTH WILL DEFAULT TO 4
&LL      SETA  &LX                      GET IT
         AGO   .OKLEN
.DEFLEN  ANOP
&LL      SETA  4                        SET THE DEFAULT LENGTH
.OKLEN   AIF   ('&HEXTAB' EQ '').OKHEX1
&HEXTAB# SETC  '&HEXTAB'
.OKHEX1  AIF   ('&HEXTAB#' NE '').OKHEX2
&HEXTAB# SETC  'HEXTAB'
.OKHEX2  ANOP
&LT      SETA  &LL*2
         AIF   ('&DIGITS' EQ '').OKDIGIT
&LT      SETA  &DIGITS
.OKDIGIT AIF   (N'&TO NE 2).TO1
         AIF   (N'&FROM NE 2).T2F1
&NFS     UNPK  &T1.(&LT+1,&T2),&F1.(&LL+1,&F2)
         TR    &T1.(&LT,&T2),&HEXTAB#
         MVI   &T1+&LT.(&T2),&BYTE
         AGO   .DONE
.T2F1    AIF   (N'&FROM NE 1).ERRF
&NFS     UNPK  &T1.(&LT+1,&T2),&FROM.(&LL+1)
         TR    &T1.(&LT,&T2),&HEXTAB#
         MVI   &T1+&LT.(&T2),&BYTE
         AGO   .DONE
.TO1     AIF   (N'&TO NE 1).ERRT
         AIF   (N'&FROM NE 2).T1F1
&NFS     UNPK  &TO.(&LT+1),&F1.(&LL+1,&F2)
         TR    &TO.(&LT),&HEXTAB#
         MVI   &TO+&LT,&BYTE
         AGO   .DONE
.T1F1    AIF   (N'&FROM NE 1).ERRF
&NFS     UNPK  &TO.(&LT+1),&FROM.(&LL+1)
         TR    &TO.(&LT),&HEXTAB#
         MVI   &TO+&LT,&BYTE
         AGO   .DONE
.ERRF    MNOTE 8,'ERROR IN "FROM" PARAMETER, MACRO TERMINATED'
         MEXIT
.ERRT    MNOTE 8,'ERROR IN "TO" PARAMETER, MACRO TERMINATED'
         MEXIT
.DONE    ANOP
         MEND
./       ADD   NAME=HEXVMF
         TITLE 'PRINT A VMF RECORD IN HEX, IF REQUESTED.'
* THIS LOGIC IS CALLED OUT OF PASSVMF WHEN THE OPTION "DUMPVMF" HAS
* BEEN SPECIFIED BY THE USER. R6 HOLDS THE ADDRESS OF THE VMF RECORD
* WE ARE PRESENTLY WORKING ON. HERE WE LOOK UP IN TABLE "DVTAB" TO SEE
* IF THIS IS A RECORD FOR A VOLSER WE WANT AND IF SO WE HEXPRINT IT.
* ALL THIS LOGIC WAS ADDED FOR 1.6.0 SO NO UPDATE FLAGS ARE USED.
HEXVMF   $PROLOG R12
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         LA    R1,DVTAB            ADDR OF VMFDUMP VOLSER TABLE.
HEXVMFV  SR    R14,R14             ZERO HI-ORDER 3 BYTES.
         IC    R14,0(,R1)          LENGTH TO COMPARE
         BCTR  R14,0               SETUP FOR EX.
         EX    R14,HEXVMFCO        COMPARE VOLSERS.
         BE    HEXVMF1             VOLUME SELECTED!
         LA    R1,7(,R1)           R1 -> ADDR OF NEXT VOLSER.
         CLI   0(R1),255           END OF LIST?
         BNE   HEXVMFV             COMPARE TO NEXT VOLSER IN TABLE.
HEXVMF99 DS    0H
         $EPILOG ,
         USING VMFBASE,R6
HEXVMF1  CLI   1(R6),C'B'          VMF BASE RECORD?
         BNE   HEXVMF2             MUST BE AN EXTENTION.
         BAL   R9,HEXALLOC         MAKE SURE WE HAVE A PRINT FILE.
         MVC   HVLINE1+1(1),BASRVSCR
         MVC   HVLINE1+3(1),BATYPE
         MVC   HVLINE1+5(6),BAVSN
         HEX   HVLINE1+11,BAVOLSQ,                                     X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+16,BAVOLCT,                                     X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+21,BAFILECT,                                    X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+26(6),BAVSN1
         HEX   HVLINE1+33,BAVSNL,                                      X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+42,BAMDS1,                                      X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+51,BAMDSL,                                      X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+60,BAUNIQUE,                                    X
               4,HEXTAB=TRHEX
         MVC   HVLINE1+62(1),BADEN
         MVC   HVLINE1+64(1),BATRTCH
         MVC   HVLINE1+66(1),BALABEL
         HEX   HVLINE1+68,BAACTIVE,                                    X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+73,BACDSSEQ,                                    X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+78(2),BALOC
         MVC   HVLINE1+81(4),BACCSS
         HEX   HVLINE1+87,BAVMOVED,                                    X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+94,BAVKEEPD,                                    X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+101,BAVEXPDT,                                   X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+108,BAVSCRDT,                                   X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+115,BATLENTH,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+120,BAPURCH,                                    X
               3,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=2,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         MVC   HVLINE1+1(2),BAMANU
         MVC   HVLINE1+4(2),BATAPTYP
         MVC   HVLINE1+7(1),BALOST
         MVC   HVLINE1+9(1),BADAMAGE
         MVC   HVLINE1+11(1),BADEST
         HEX   HVLINE1+013,BADESTDT,                                   X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+020,BACLUSES,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+025,BACEUSES,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+030,BATOUSES,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+035,BACURERG,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+040,BAMAXERG,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+045,BATMPRER,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+050,BACLNCT,                                    X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+055,BACERTCT,                                   X
               2,HEXTAB=TRHEX
         HEX   HVLINE1+060,BACLNDT,                                    X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+067,BACERTDT,                                   X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+074,BAMVLCNT,                                   X
               2,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         MVC   HVLINE1+001(6),BAMVLVSN+00
         HEX   HVLINE1+008,BAMVLSEQ+00,                                X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+013(6),BAMVLVSN+08
         HEX   HVLINE1+020,BAMVLSEQ+08,                                X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+025(6),BAMVLVSN+16
         HEX   HVLINE1+032,BAMVLSEQ+16,                                X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+037(6),BAMVLVSN+24
         HEX   HVLINE1+044,BAMVLSEQ+24,                                X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+049(6),BAMVLVSN+32
         HEX   HVLINE1+056,BAMVLSEQ+32,                                X
               2,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         HEX   HVLINE1+001,BAFILESQ,                                   X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+006(1),BAABEND
         MVC   HVLINE1+008(1),BACDSFLG
         MVC   HVLINE1+010(44),BADSN
         HEX   HVLINE1+056,BACTIME,                                    X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+065,BACDATE,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+072(03),BACDRIVE
         MVC   HVLINE1+076(08),BACJOB
         MVC   HVLINE1+085(08),BACSTEP
         HEX   HVLINE1+094,BAIDATE,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+103(03),BAIDRIVE
         MVC   HVLINE1+107(08),BAIJOB
         HEX   HVLINE1+116,BABLKCT,                                    X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+125,BALRECL,                                    X
               2,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         HEX   HVLINE1+001,BABLKSI,                                    X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+006(03),BARECFM
         MVC   HVLINE1+010(15),BAJOBACC
         HEX   HVLINE1+026,BADKEEPD,                                   X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+033,BADEXPDT,                                   X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+040(01),BACPUID
         MVC   HVLINE1+042(01),BASPAN
         HEX   HVLINE1+044,BADXPIRD,                                   X
               3,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         B     HEXVMF99            ALL DONE.
         USING VMFMDS,R6
HEXVMF2  DS    0H
         BAL   R9,HEXALLOC         MAKE SURE WE HAVE A PRINT FILE.
         MVC   HVLINE1+1(01),MDTYPE
         MVC   HVLINE1+3(06),MDVSN
         HEX   HVLINE1+010,MDRELREC,                                   X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+019,MDRELPRE,                                   X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+028,MDRELNXT,                                   X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+037,MDUNIQUE,                                   X
               4,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=2,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         HEX   HVLINE1+001,MDFILESQ,                                   X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+006(1),MDDSNABN
         MVC   HVLINE1+008(1),MDDSNCTL
         MVC   HVLINE1+010(44),MDDSN
         HEX   HVLINE1+055,MDCTIME,                                    X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+064,MDCDATE,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+071(03),MDCDRIVE
         MVC   HVLINE1+075(08),MDCJOB
         MVC   HVLINE1+084(08),MDCSTEP
         HEX   HVLINE1+093,MDIDATE,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+100(03),MDIDRIVE
         MVC   HVLINE1+104(08),MDIJOB
         HEX   HVLINE1+113,MDBLKCT,                                    X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+122,MDLRECL,                                    X
               2,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         HEX   HVLINE1+001,MDBLKSI,                                    X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+006(03),MDRECFM
         MVC   HVLINE1+010(15),MDJACCT
         HEX   HVLINE1+026,MDKEEPDT,                                   X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+033,MDEXPDT,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+040(01),MDCPU
         MVC   HVLINE1+042(01),MDSPAN
         HEX   HVLINE1+044,MDEXPIRE,                                   X
               3,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         LA    R6,132(0,R6)        POINT TO 2ND ENTRY.
         HEX   HVLINE1+001,MDFILESQ,                                   X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+006(1),MDDSNABN
         MVC   HVLINE1+008(1),MDDSNCTL
         MVC   HVLINE1+010(44),MDDSN
         HEX   HVLINE1+055,MDCTIME,                                    X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+064,MDCDATE,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+071(03),MDCDRIVE
         MVC   HVLINE1+075(08),MDCJOB
         MVC   HVLINE1+084(08),MDCSTEP
         HEX   HVLINE1+093,MDIDATE,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+100(03),MDIDRIVE
         MVC   HVLINE1+104(08),MDIJOB
         HEX   HVLINE1+113,MDBLKCT,                                    X
               4,HEXTAB=TRHEX
         HEX   HVLINE1+122,MDLRECL,                                    X
               2,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         HEX   HVLINE1+001,MDBLKSI,                                    X
               2,HEXTAB=TRHEX
         MVC   HVLINE1+006(03),MDRECFM
         MVC   HVLINE1+010(15),MDJACCT
         HEX   HVLINE1+026,MDKEEPDT,                                   X
               3,HEXTAB=TRHEX
         HEX   HVLINE1+033,MDEXPDT,                                    X
               3,HEXTAB=TRHEX
         MVC   HVLINE1+040(01),MDCPU
         MVC   HVLINE1+042(01),MDSPAN
         HEX   HVLINE1+044,MDEXPIRE,                                   X
               3,HEXTAB=TRHEX
         XPRNTLIN  PWH,                                                X
               TEXT=HVLINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         MVI   HVLINE1,C' '
         MVC   HVLINE1+1(L'HVLINE1-1),HVLINE1
         S     R6,=F'132'          BACK UP FOR PASSVMF.
         B     HEXVMF99            ALL DONE.
         DROP  R6
         TITLE ' - ALLOCATE A PRINT FILE FOR DUMPVMF OUTPUT.'
HEXALLOC CLI   HVALLOC,255         SYSOUT ALREADY ALLOCATED.
         BER   R9                  YES. GET OUT.
         MVI   HVALLOC,255         1ST TIME ONLY.
         ALLOC SYSOUT=CLASS,                                           X
               DDN=LOCREPH,                                            X
               ERROR=HVLS98        ALLOCATE A PRINT FILE.
         B     HVLS0               DYNALLOC WORKED.
HVLS98   LA    R2,DDNREPH          ADDR OF DDNAME FOR MESSAGE.
         LM    R15,R1,=A(MESGRTN,0,21)
         BALR  R14,R15             SAY, DYNALLOC FAILED.
         LM    R15,R1,=A(MESGRTN,0,22)
         BALR  R14,R15             SAY, TRYING PLAN B
HVLS0    L     R2,PAGELEN          PICK UP PAGE LENGTH             @TEC
         XPROPEN PWH,                                                  X
               PAGELEN=(2),                                        @TECX
               DDNAME=DUMPVMF      AND OPEN IT.
         LTR   R15,R15             DID DUMPVMF OPEN?
         BNZ   HVLS99              IF NOT, NO REPORT.
         XPREJECT  PWH             SKIP TO TOP OF FORM.
         XPRHEAD PWH,                                                  X
               LIST=(HVHEAD1)      DEFINE THE HEADING LINES.
HVLS99   BR    R9
         DROP  R11
         TITLE ' - LOCAL DATA AREA.'
         DS    0F
LOCREPH  DC    AL4(DDNREPH)
         DC    Y(L'DDNREPH)
DDNREPH  DC    CL8'DUMPVMF'        DDNAME FOR REPORT LISTING.
PWH      XPRDCB  DDNAME=DUMPVMF
HVHEAD1  XPRLDEF TEXT=HTEXT1,LENGTH=L'HTEXT1,SPB=(0,ATHOF),SPA=2
NVHEAD1  DC    C'&NAME &VERSION -- TLMSII VOLUME MASTER FILE CONTENTS '
         LTORG ,
HVLINE1  DS    0CL133
         DC    CL1' '
         DC    CL132' '
         LTORG ,
HEXVMFCO CLC   2(0,R6),1(R1)       COMPARE VOLSERS
HVALLOC  DC    X'00'               SET TO X'FF' IF SYSOUT FILE ALLOC.
./       ADD   NAME=ISPCALL
         MACRO
.*                                                      RON MURA, TPD
&LBL     ISPCALL &LIST,        CALL PARAMETERS                         X
               &EP=ISPLINK,                                            X
               &LISTLBL=,      LABEL FOR PARAMETER LIST (NON-REENT)    X
               &WKAREA=,       TO BUILD CALL LIST (REENTERANT CODE)    X
               &WKREG=,        WORK REGISTER (DEFAULT 1)               X
               &TRACE=         'OFF' OR BIT FOR TRACE MODE
.*  ONCE WKAREA, WKREG, AND TRACE ARE SPECIFIED, THEY ARE USED IN ALL
.*  SUBSEQUENT OCCURRENCES OF THIS MACRO, UNLESS THE PARAMETER IS
.*  EXPLICITLY CODED.
.*  NOTE: FIRST 4 BYTES OF WKAREA ARE USED TO STORE NUMERIC VALUES,
.*  WHICH ARE PRIMARILY LENGTH FIELDS.
         LCLA  &S,&T,&U,&TRCEMSK
         LCLB  &NUMSW
         LCLC  &NDX,&WKAR,&WKRG
         GBLC  &ISPRREG,&ISPRAR,&ISPTRC1
         GBLA  &ISPTRC2
&NDX     SETC  '&SYSNDX'
&S       SETA  1               SUBSCRIPT FOR GOING THRU &LIST
***********************************************************************
         AIF   (T'&LBL NE 'O').L1
*                  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
         AGO   .L2
.L1      ANOP
&LBL     DS    0H  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
.L2      ANOP
         AIF   (T'&LIST NE 'O').A1
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE PROVIDED ***'
         AGO   .EXIT
.A1      AIF   ('&LIST'(1,1) EQ '(').CHKTRCE
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE ENCLOSED IN PARENTHESX
               ES ***'
         AGO   .EXIT
.****************** ANALYZE "TRACE" PARAMETER *************************
.CHKTRCE AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' NE '').TRCE4
         AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' EQ '').TRCEOFF
         AIF   ('&TRACE'(1,1) EQ '(').TRCEON
         AIF   ('&TRACE' EQ 'OFF').TRCEOFF
         MNOTE 12,'*** INVALID TRACE PARAMETERS ***'
         AGO   .EXIT
.TRCEON  AIF   (T'&TRACE(2) EQ 'N').TRCE2
.TRCE8   MNOTE 12,'*** TRACE BIT (2ND PARAMETER) MUST BE 0 - 7 ***'
         AGO   .EXIT
.TRCE2   AIF   (&TRACE(2) GE 0 OR &TRACE(2) LE 7).TRCE3
         AGO   .TRCE8
.TRCE3   ANOP
&ISPTRC1 SETC  '&TRACE(1)'     SET GLOBAL VARIABLE
&ISPTRC2 SETA  &TRACE(2)       SET GLOBAL VARIABLE
.TRCE4   ANOP
&U       SETA  0
&TRCEMSK SETA  255
&T       SETA  128
.TRCE5   AIF   (&U GT 7).CHKREG
         AIF   (&ISPTRC2 NE &U).TRCE7
.TRCE6   ANOP
&U       SETA  &U+1
&T       SETA  &T/2
         AGO   .TRCE5
.TRCE7   ANOP
&TRCEMSK SETA  &TRCEMSK-&T
         AGO   .TRCE6
.TRCEOFF ANOP
&ISPTRC1 SETC  ''              SET GLOBAL VARIABLE
.****************** ANALYZE "WKREG" PARAMETER *************************
.CHKREG  AIF   (T'&WKREG EQ 'O').A2
&ISPRREG SETC  '&WKREG'        SET GLOBAL VARIABLE
.A2      AIF   ('&ISPRREG' EQ '').A3
&WKRG    SETC  '&ISPRREG'
         AGO   .CHKAREA
.A3      ANOP
&ISPRREG SETC  '1'             SET GLOBAL VARIABLE
         AGO   .A2
.****************** SEE IF USING PROVIDED WORKAREA ********************
.CHKAREA AIF   (T'&WKAREA NE 'O' OR '&ISPRAR' NE '').RENT
         AIF   (T'&LISTLBL EQ 'O').B1
&WKAR    SETC  '&LISTLBL'
         AGO   .B2
.B1      ANOP
&WKAR    SETC  'ISPC&NDX'      LABEL FOR MACRO WORKAREA
.B2      ANOP
&T       SETA  4*(N'&LIST+1)   LENGTH OF MACRO WORKAREA
         B     &WKAR.+&T             BRANCH AROUND DATA         ISPCALL
&T       SETA  N'&LIST+1
&WKAR    DC    &T.F'0'               PARAMETER LIST             ISPCALL
.******************** BUILD PARAMETER LIST ****************************
.LOOP    AIF   (&S GT N'&LIST).ENDLOOP
         AIF   ('&LIST(&S)'(1,1) EQ '''').C1 TEST FOR LITERAL
         AIF   (T'&LIST(&S) EQ 'N').NUM      TEST FOR NUMERIC
         AIF   ('&LIST(&S)'(1,1) EQ '(').REG TEST FOR REG NOTATION
         LA    &WKRG,&LIST(&S)       LOAD DATA ADDR             ISPCALL
.ST      ANOP
&T       SETA  4*&S
         ST    &WKRG,&WKAR.+&T       STORE IN PARAM LIST        ISPCALL
         AIF   ('&ISPTRC1' EQ '' OR &S GT 1).C3           TRACE
         TM    &ISPTRC1,&TRCEMSK     IS TRACE BIT ON?     TRACE ISPCALL
         BZ    ISPT&NDX                                   TRACE ISPCALL
         AIF   ('&WKRG' EQ '1' OR '&WKRG' EQ 'R1').C4     TRACE
         LR    1,&WKRG               LOAD SRVC NAME ADDR  TRACE ISPCALL
.C4      AIF   ('&LIST(&S)'(1,1) EQ '''').C5 LITERAL?     TRACE
         LA    0,8                   LOAD LENGTH          TRACE ISPCALL
         AGO   .C6                                        TRACE
.C5      ANOP                                             TRACE
&U       SETA  K'&LIST(&S)-2   LENGTH OF SERVICE NAME     TRACE
         LA    0,&U                  LOAD LENGTH          TRACE ISPCALL
.C6      ANOP                                             TRACE
         SVC   93                    ISSUE TPUT SVC       TRACE ISPCALL
ISPT&NDX EQU   *                                          TRACE ISPCALL
.C3      ANOP
&S       SETA  &S+1
         AGO   .LOOP
.C1      AIF   (&S EQ 1).C2    SPF SERVICE NAME - LENGTH IS OK
&T       SETA  K'&LIST(&S)-2
         AIF   (&T GT 7).C2    LENGTH 8 OR GREATER IS OK
&T       SETA  &T+1            ADD FOR ONE BLANK
         LA    &WKRG,=CL&T.&LIST(&S) LOAD DATA ADDR             ISPCALL
         AGO   .ST
.C2      ANOP
         LA    &WKRG,=C&LIST(&S)     LOAD DATA ADDR             ISPCALL
         AGO   .ST
.NUM     AIF   (&NUMSW).NUM2
.*  NOTE: SPF WILL MODIFY LENGTH FIELDS, SO THEY CANNOT BE IN
.*  REENTERANT CODE.
         MVC   &WKAR.(4),=F'&LIST(&S)'                                 X
                                     MOVE NUMERIC VALUE         ISPCALL
&NUMSW   SETB  1
         AGO   .LA
.NUM2    MNOTE 12,'*** ONLY ONE NUMERIC ALLOWED PER ISPCALL MACRO - LISX
               T ITEM &S --&LIST(&S)-- IS SECOND OCCURRENCE ***'
         AGO   .EXIT
.LA      ANOP
         LA    &WKRG,&WKAR           LOAD DATA ADDR             ISPCALL
         AGO   .ST
.REG     ANOP
&T       SETA  4*&S
         ST    &LIST(&S),&WKAR.+&T                              ISPCALL
         AGO   .C3
.******************** USING PROVIDED WORKAREA *************************
.RENT    AIF   (T'&WKAREA EQ 'O').RENT2
&ISPRAR  SETC  '&WKAREA'       SET GLOBAL VARIABLE
.RENT2   ANOP
&WKAR    SETC  '&ISPRAR'
         AGO   .LOOP
.****************** GENERATE STATEMENTS FOR CALL **********************
.ENDLOOP ANOP
         OI    &WKAR.+&T,X'80'       TURN ON VL BIT             ISPCALL
         LA    1,&WKAR.+4                                       ISPCALL
         L     15,&EP
         BALR  14,15                                            ISPCALL
.EXIT    ANOP
***********************************************************************
         SPACE 2
         MEND
./       ADD   NAME=ISPFINIT
         TITLE 'SET UP ISPF ENVIRONMENT.'
         PRINT NOGEN
ISPFINIT $PROLOG R12
         L     R11,=V(DATASECT)    ADDRESS OF DATA CSECT
         USING DATASECT,R11        SET ADDRESSIBILITY.
         L     R3,CVTPTR           A(CVT)
         L     R3,0(0,R3)          TCB/ASCB WORDS
         L     R3,12(0,R3)         A(MY ASCB).
         ICM   R3,15,ASCBJBNS-ASCB(R3) A(LOGONID)
         LTR   R3,R3               IS THERE ONE?
         BZ    NOTTSO              NO TSO, HENCE NO ISPF.
         MVC   IMINOR,0(R3)        SET UP LOGON ID.
         ENQ   (IMAJOR,                                                X
               IMINOR,                                                 X
               E,                                                      X
               7,                                                      X
               STEP),                                                  X
               RET=TEST
         LTR   R15,R15             ARE WE UNDER ISPF?
         BZ    NOTTSO              I GUESS NOT.
         LOAD  EP=ISPLINK
         LTR   R15,R15             ISPLINK AVAILABLE?
         BNZ   NOTTSO              THEN WE AREN'T A DIALOG.
         ST    R0,ISPLINK          SAVE A(ISPLINK) FOR LATER
         OI    INTFLAG,UNDERSPF    REMEMBER ABOUT ISPF.
NOTTSO   DS    0H
        $EPILOG ,
         LTORG ,
IMAJOR   DC    CL8'SPFUSER'
IMINOR   DC    CL8' '
         PRINT NOGEN
         IHAASCB ,
./       ADD   NAME=ISPFLIST
         TITLE 'BUILD AND DISPLAY TABLE WITH SELECT DATASET INFO.'
ISPFLIST $PROLOG R12,R10
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         ISPCALL ('VDEFINE',ISPNLST4,                                  X
               ISPVLST4,'CHAR ',133)
         ISPCALL ('VDEFINE',ISPNLST5,                                  X
               ISPVLST5,'CHAR ',4)
         ISPCALL ('TBCREATE','ISPLTAB',' ',ISPNLST4,'NOWRITE',         X
               'REPLACE')
         XC    UDSCOUNT,UDSCOUNT   ZERO DATASET ACCUMULATOR.
         XC    UTRALLOC,UTRALLOC   ZERO TRACKS ALLOCATED COUNT.
         XC    UTRUSED,UTRUSED     ZERO TRAKCS USED COUNTER.
         LM    R3,R5,TABBXLE       SET UP TO LOOP
         USING SAVEFMT,R3          ADDRESS DATASET INFO TABLE.
ISPL1    TM    FLAGS2,BRKLVL       BREAK LEVEL WANTED?
         BZ    ISPL1C              NO
         LH    R14,QUALLEN         LENGTH OF SAVED QUALIFIER.
         B     *+10                SKIP OVER EXECUTED INSTR.
         CLC   SAVEQUAL(0),SV1DSNAM
         EX    R14,*-6             COMPARE.
         BE    ISPL1C              STILL SAME LEVEL.
         LA    R1,SV1DSNAM         ADDR OF CURRENT DSNAME.
         LA    R14,SV1DSNAM        ADDR OF CURRENT DSNAME.
         LA    R15,SV1DSNAM        ADDR OF CURRENT DSNAME.
         LA    R15,8(,R15)         QUALIFIER IS 8 BYTES AT MOST.
ISPL1D   CLI   0(R1),C'.'          END OF QUALIFIER?
         BE    ISPL1E              SO, SAVE IT.
         CR    R1,R15              WAS THERE A QUALIFIER?
         BE    ISPL1F              NO, CALL IT 8 BYTES.
         LA    R1,1(,R1)           NEXT BYTE.
         B     ISPL1D              AND KEEP ON TRUCKIN'
ISPL1F   LA    R1,8
         B     ISPL1G              SAVE AN 8 BYTE QUALIFIER.
ISPL1E   SR    R1,R14              GET LENGTH OF QUALIFIER TO SAVE
ISPL1G   XC    SAVEQUAL,SAVEQUAL   BLANK OUT QUALIFIER.
         LR    R14,R1
         B     *+10
         MVC   SAVEQUAL(0),SV1DSNAM
         EX    R14,*-6             SAVE NEW QUALIFIER.
         STH   R1,QUALLEN          SAVE LENGTH OF QUALIFIER.
         CLI   FIRSTQ,255          1ST TIME THROUGH HERE?
         BE    ISPL1C              SO, DON'T PRINT A BLANK PAGE!
         ICM   R0,15,UDSCOUNT      DATASETS THIS LEVEL.
         CVD   R0,DTWORK           GET IT IN DECIMAL.
         EDIT  I1L2CNT,                                                X
               DTL4,                                                   X
               ZZZZZ9              AND FORMATTED NICELY.
         ICM   R0,15,UTRUSED       USED TRACKS BY LEVEL.
         CVD   R0,DTWORK           GET IT IN DECIMAL.
         EDIT  I1L2USED,                                               X
               DTL4,                                                   X
               ZZZZZZ9             AND FORMATTED NICELY.
         ICM   R0,15,UTRALLOC      ALLOCATED TRACKS BY LEVEL.
         CVD   R0,DTWORK           GET IT IN DECIMAL.
         EDIT  I1L2ALOC,                                               X
               DTL4,                                                   X
               ZZZZZZ9             AND FORMATTED NICELY.
         MVC   I1LINE,I1LINE2      SET UP A SUMMARY LINE
         ISPCALL ('TBADD','ISPLTAB')
         XC    UDSCOUNT,UDSCOUNT   ZERO DATASET ACCUMULATOR.
         XC    UTRALLOC,UTRALLOC   ZERO TRACKS ALLOCATED COUNT.
         XC    UTRUSED,UTRUSED     ZERO TRAKCS USED COUNTER.
ISPL1C   XC    FIRSTQ,FIRSTQ       TOTAL PAGES FROM NOW ON.
         ICM   R1,15,UDSCOUNT      COUNT SO FAR.
         LA    R1,1(,R1)           ONE MORE.
         STCM  R1,15,UDSCOUNT      TALLY.
         MVC   I1DSNAM,SV1DSNAM    SETUP DSNAME IN PRINT LINE.
         MVC   I1DSSN,SV1DSSN      SETUP VOLSER IN PRINT LINE.
         CLC   SV1DSSN,=C'ARCHIV'  WAS IT ARCHIVED?
         BE    ISPL1A              YES. THEN FIDDLE DATES.
         CLI   SV1DEVC,C'T'        IS IT A TAPE?
         BE    ISPL7A              IF SO DON'T FIDDLE DATES
         CLC   SV1DSSN(5),=C'MGRAT'   WAS IT MIGRATED?
         BNE   ISPL1A              IF SO, DATES ARE DIFFERENT FORMAT.
ISPL7A   SR    R0,R0               CLEAR 3 BYTES.
         ICM   R0,7,SV1CREDT       PICK UP CREATE DATE
         XC    DTWORK,DTWORK       CLEAR DOUBLE WORD
         ST    R0,DTWORK+4         STORE FOR DATE CONVERSION
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   I1CREDT,DTWORK      MOVE FORMATTED CREATE DATE
         SR    R0,R0               CLEAR 3 BYTES.
         ICM   R0,7,SV1REFD        PICK UP LAST REF DATE
         XC    DTWORK,DTWORK       CLEAR DOUBLE WORD
         ST    R0,DTWORK+4         STORE FOR DATE CONVERSION
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   I1REFDT,DTWORK      MOVE FORMATTED LAST REF DATE
         B     ISPL1B              GO TO SETUP EXTENTS.
ISPL1A   SR    R0,R0               CLEAR 3 BYTES.
         IC    R0,SV1CREDT         GET YEAR
         MH    R0,=H'1000'         WHICH WAS IN CENTURIES.
         MVC   DTWORK(2),SV1CREDT+1  MOVE DAY
         AH    R0,DTWORK           ADD BINARY DAY
         CVD   R0,DTWORK           PACKED DECIMAL DATE.
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   I1CREDT,DTWORK      MOVE FORMATTED CREATE DATE
         SR    R0,R0               CLEAR 3 BYTES.
         IC    R0,SV1REFD          GET YEAR
         MH    R0,=H'1000'         TIME 1000.
         MVC   DTWORK(2),SV1REFD+1  MOVE DAY
         AH    R0,DTWORK           ADD BINARY DAY
         CVD   R0,DTWORK           PACKED DECIMAL DATE.
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   I1REFDT,DTWORK      MOVE FORMATTED LAST REF DATE
ISPL1B   SR    R0,R0               CLEAR 3 BYTES.
         IC    R0,SV1NOEPV         PICK UP NUMBER OF EXTENTS
         CVD   R0,DTWORK           CONVERT TO PACKED
         EDIT  I1NOEPV,DTL2,ZZ     FORMAT IT NICELY.
         SR    R0,R0               ZERO HIGH ORDER BYTES.
         ICM   R0,3,SV1BLKL        PICK UP BLOCK SIZE
         CVD   R0,DTWORK           CONVERT TO PACKED
         EDIT  I1BLKL,DTL3,ZZZZ    FORMAT IT NICELY.
         SR    R0,R0               ZERO HIGH ORDER BYTES.
         ICM   R0,3,SV1LRECL       PICK UP RECORD LENGTH
         CVD   R0,DTWORK           CONVERT TO PACKED
         EDIT  I1LRECL,DTL3,ZZZZ   FORMAT IT NICELY.
         CLC   SV1RECFM(2),=CL2' '  RECFM 1 BYTE LONG?
         BNE   ISPLR1              YES, GOTO LEFT-JUSTIFY 1 BYTE.
         MVC   SV1RECFM(1),SV1RECFM+2
         MVC   SV1RECFM+1(2),=CL2' '
         B     ISPLR9
ISPLR1   CLI   SV1RECFM,C' '       ALREADY JUSTIFIED?
         BNE   ISPLR9              YES, SKIP 1 BYTE JUSTIFY.
         MVC   SV1RECFM(2),SV1RECFM+1
         MVI   SV1RECFM+2,C' '
ISPLR9   MVC   I1RECFM,SV1RECFM    SETUP RECFM FOR PRINTING.
         MVC   I1DSORG,=CL3' '     BLANK DSORG PRINT FIELD.
         TM    SV1DSORG,X'80'      ISAM
         BZ    ISPL4B              NO.
         MVC   I1DSORG(2),=C'IS'   SETUP RECFM FOR PRINTING.
         B     ISPL4Z              GOTO TRACKS USED.
ISPL4B   TM    SV1DSORG,X'40'      PS
         BZ    ISPL4C              NO.
         TM    SV1DSORG,X'01'      IS IT UNMOVEABLE?
         BZ    ISPL4BB             NO
         MVC   I1DSORG(3),=C'PSU'  SETUP RECFM.
         B     ISPL4Z              GOTO TRACKS USED.
ISPL4BB  MVC   I1DSORG(2),=C'PS'   SETUP RECFM FOR PRINTING.
         B     ISPL4Z              GOTO TRACKS USED.
ISPL4C   TM    SV1DSORG,X'20'      DA
         BZ    ISPL4D              NO.
         TM    SV1DSORG,X'01'      IS IT UNMOVEABLE?
         BZ    ISPL4CC             NO
         MVC   I1DSORG(3),=C'DAU'  SETUP RECFM.
         B     ISPL4Z              GOTO TRACKS USED.
ISPL4CC  MVC   I1DSORG(2),=C'DA'   SETUP RECFM FOR PRINTING.
         B     ISPL4Z              GOTO TRACKS USED.
ISPL4D   TM    SV1DSORG,X'02'      PO
         BZ    ISPL4E              NO.
         TM    SV1DSORG,X'01'      IS IT UNMOVEABLE?
         BZ    ISPL4DD             NO
         MVC   I1DSORG(3),=C'POU'  SETUP RECFM.
         B     ISPL4Z              GOTO TRACKS USED.
ISPL4DD  MVC   I1DSORG(2),=C'PO'   SETUP RECFM FOR PRINTING.
         B     ISPL4Z              GOTO TRACKS USED.
ISPL4E   TM    SV1DSORG+1,X'08'    VSAM
         BZ    ISPL4Z              NO.
         MVC   I1DSORG(2),=C'VS'   SETUP RECFM FOR PRINTING.
ISPL4Z   ICM   R1,15,SV1EXT1       GET TRACKS ALLOC
         ICM   R15,15,UTRALLOC     TRACKS ALLOC FOR THIS LEVEL
         AR    R15,R1              PLUS CURRENT DATASET.
         STCM  R15,15,UTRALLOC     COUNT IT OFF.
         CVD   R1,DTWORK           CONVERT TO PACKED
         EDIT  I1ALLOC,DTL3,ZZZZ   AND THEN TO ZONED.
         MVC   DTWORK,=PL8'0'      ZERO.
         SR    R8,R8               CLEAR 3 BYTES.
         IC    R8,SV1NOEPV         GET NUMBER OF EXTENTS.
         LTR   R8,R8               ARE THERE ANY?
         BZ    ISPL6A              IF NOT, SAY ZERO TRACKS USED.
         CLC   SV1DSSN(5),=C'MGRAT'   WAS IT MIGRATED
         BE    ISPL6A              NO USAGE AVAILABLE.
         SR    R8,R8               CLEAR OUT 3 BYTES.
         CVD   R8,DTWORK           GET IT IN PACKED.
         CLI   SV1DEVC,C'T'        IS IT ON TAPE?
         BE    ISPL6A              THEN LEAVE IT BLANK.
         SR    R8,R8               CLEAR OUT 3 BYTES.
         CLC   SV1LSTAR,=XL3'00'   IS DS1LSTAR ALL ZERO.
         BE    ISPL6C              YES. ZERO TRACKS USED.
         ICM   R8,3,SV1LSTAR       HIGH USED TT
         LA    R8,1(,R8)           KEEP IT HONEST
         ICM   R15,15,UTRUSED      TRACKS USED FOR THIS LEVEL.
         AR    R15,R8              PLUS CURRENT DATASET.
         STCM  R15,15,UTRUSED      COUNT IT OFF.
ISPL6C   CVD   R8,DTWORK           GET IT IN PACKED.
         CR    R8,R1               CHECK FOR WIERD DS1LSTAR?
         BNH   ISPL6A              MUST BE OK.
         MVC   I1USED,=C'????'     WHAT THE HELL?????
         B     ISPL6B              NO NUMERIC TO FORMAT.
ISPL6A   EDIT  I1USED,DTL3,ZZZZ    FORMAT FOR PRINTING.
ISPL6B   EQU   *
         MVC   I1LINE,I1LINE1      SET UP A DETAIL LINE
         ISPCALL ('TBADD','ISPLTAB')
         BXLE  R3,R4,ISPL1         LOOP BACK TO DO NEXT DATASET.
         ICM   R0,15,UDSCOUNT      DATASETS THIS LEVEL.
         CVD   R0,DTWORK           GET IT IN DECIMAL.
         EDIT  I1L2CNT,                                                X
               DTL3,                                                   X
               ZZZ9                AND FORMATTED NICELY.
         ICM   R0,15,UTRUSED       USED TACKS BY LEVEL.     .
         CVD   R0,DTWORK           GET IT IN DECIMAL.
         EDIT  I1L2USED,                                               X
               DTL4,                                                   X
               ZZZZZZ9             AND FORMATTED NICELY.
         ICM   R0,15,UTRALLOC      ALLOCATED TRACKS BY LEVEL.
         CVD   R0,DTWORK           GET IT IN DECIMAL.
         EDIT  I1L2ALOC,                                               X
               DTL4,                                                   X
               ZZZZZZ9             AND FORMATTED NICELY.
         MVC   I1LINE,I1LINE2      SET UP A SUMMARY LINE
         ISPCALL ('TBADD','ISPLTAB')
         ISPCALL ('TBTOP','ISPLTAB')
ISPLRDSP DS    0H
         ISPCALL ('TBDISPL','ISPLTAB','ISPLPANL')
         C     R15,=F'8'           END KEY PRESSED?
         BNL   ISPL99              IF SO, THAT'S ALL
ISPLCHKR CLC   ISPLROWS,=CL4'0000' WERE ANY ROWS SELECTED?
         BNH   ISPLRDSP            IF NOT, RE-DISPLAY
         ISPCALL ('TBDISPL','ISPLTAB')
         B     ISPLCHKR            MORE ROWS?
ISPL99   DS    0H                  GET OUT.
         $EPILOG ,
         DROP  R3
         DROP  R11
         LTORG ,
         DS    0F
         LTORG ,
ISPNLST4 DC    C'(I1LINE)'
ISPVLST4 DS    0F
I1LINE   DS    CL133
         SPACE ,
ISPNLST5 DC    C'(ISPLROWS ISPLCMD)'
ISPVLST5 DS    0F
ISPLROWS DS    CL4
ISPLCMD  DS    CL4
         SPACE ,
I1LINE1  DS    0CL133
         DC    CL133' '
         ORG   I1LINE1
I1DSNAM  DC    CL44' ',CL1' '
I1DSSN   DC    CL6' ',CL2' '
I1ALLOC  DC    CL4' ',CL2' '
I1USED   DC    CL4' ',CL1' '
I1REFDT  DC    CL8' ',CL1' '
I1DSORG  DC    CL3' ',CL1' '
I1NOEPV  DC    CL2' ',CL1' '
I1CREDT  DC    CL8' ',CL1' '
I1RECFM  DC    CL3' ',CL1' '
I1BLKL   DC    CL5' ',CL1' '
I1LRECL  DC    CL5' ',CL1' '
         ORG   I1LINE1+133
         SPACE ,
I1LINE2  DS    0CL133
         DC    CL133' '
         ORG   I1LINE2
I1L2CNT  DC    CL6' ',CL24' DATASETS FOR THIS LEVEL'
         DC    CL2' '
I1L2ALOC DC    CL7' ',CL2' ',C'TRACKS ALLOCATED  '
I1L2USED DC    CL7' ',CL2' ',C'TRACKS USED '
         ORG   I1LINE2+133
./       ADD   NAME=ISPFPARM
         TITLE 'DISPLAY PANELS TO GET OPTIONS.'
         PRINT NOGEN
ISPFPARM $PROLOG R12
         L     R11,=V(DATASECT)    ADDRESS OF DATA CSECT
         USING DATASECT,R11        SET ADDRESSIBILITY.
         ISPCALL ('DISPLAY','NCISPF1')
         C     R15,=F'8'           END KEY PRESSED?
         BNE   ISPFP01             NO
         OI    INTFLAG,EOJPARMS    THAT'S ALL FOLKS!
         B     ISPFP99             YES
ISPFP01  DS    0H
         ISPCALL ('DISPLAY','NCISPF2')
         C     R15,=F'8'           END KEY PRESSED?
         BNE   ISPFP02             NO
         OI    INTFLAG,EOJPARMS    THAT'S ALL FOLKS!
         B     ISPFP99             YES
ISPFP02  DS    0H
         ISPCALL ('CONTROL','DISPLAY','LOCK')
         ISPCALL ('SETMSG','NC001')
         ISPCALL ('DISPLAY','NCISPF2')
         ISPCALL ('VDEFINE',ISPNLST1,                                  X
               ISPVLST1,'CHAR ',1,'COPY ')
         ISPCALL ('VDEFINE',ISPNLST2,                                  X
               ISPVLST2,'CHAR ',40,'COPY ')
         ISPCALL ('VDEFINE',ISPNLST3,                                  X
               ISPVLST3,'CHAR ',5,'COPY ')
         MVC   FLAGS(6),DFLTFLAG   RESET DEFAULT OPTIONS.
         MVI   REPORTS,0           NO REPORTS BY DEFAULT.
         MVI   ETAB,255            CLEAR ENDING TABLE.
         MVI   CTAB,255            CLEAR CONTAINING TABLE
         MVI   NETAB,255           CLEAR NOT ENDING TABLE.
         MVI   NCTAB,255           CLEAR NOT CONTAINING TABLE
         MVI   LTAB,255            CLEAR LEVEL TABLE.
         MVI   VTAB,255            CLEAR VOLUME TABLE.
         MVI   NLTAB,255           CLEAR NOLEVEL TABLE.
         MVI   NVTAB,255           CLEAR NOVOLUME TABLE.
         MVI   DVTAB,255           CLEAR DUMPVMF TABLE.
         MVI   WITAB,255           CLEAR WITHIN TABLE.
         MVI   UTAB,255            CLEAR UNUSED TABLE.
         MVI   STAB,255            CLEAR SIZE TABLE.
         MVI   UNITS,255           CLEAR UNITNAME TABLE.
         MVI   FIRSTQ,255          RESET BREAKLEVEL INTERNAL FLAG
         SPACE ,
         OI    FLAGS1,LIST         LIST ALWAYS WANTED IN ISPF
         SPACE ,
         CLI   ISPLEV,C' '         SELECT BY DSLEVEL?
         BE    ISPRM6              NO.
         OI    FLAGS1,LEVEL        INDICATE LEVEL WANTED.
         LA    R3,ISPLEV           ADDR OF LEVEL(S)
         LA    R9,40               LENGTH OF LEVEL INPUT AREA
         LA    R8,9                LENGTH OF A LEVEL
         LA    R7,LTAB             ADDR OF LEVEL TABLE.
         BAL   R14,ISPBLD          GOTO BUILD SUBLIST TABLE.
         SPACE ,
ISPRM6   CLI   ISPNLEV,C' '        EXCLUDE BY DSLEVEL?
         BE    ISPRM7              NO
         OI    FLAGS1,NOLEVEL      INDICATE NOLEVEL WANTED.
         LA    R3,ISPNLEV          ADDR OF NOLEVEL(S)
         LA    R9,40               LENGTH OF NOLEVEL INPUT AREA
         LA    R8,9                LENGTH OF A LEVEL
         LA    R7,NLTAB            ADDR OF NOLEVEL TABLE.
         BAL   R14,ISPBLD          GOTO BUILD SUBLIST TABLE.
         SPACE ,
ISPRM7   CLI   ISPSER,C' '         SELECT BY VOLSER?
         BE    ISPRM8              NO
         OI    FLAGS2,VOLUME       INDICATE VOLUME WANTED.
         LA    R3,ISPSER           ADDR OF VOLUME(S)
         LA    R9,40               LENGTH OF VOLUME INPUT AREA
         LA    R8,7                LENGTH OF A VOLUME
         LA    R7,VTAB             ADDR OF VOLUME TABLE.
         BAL   R14,ISPBLD          GOTO BUILD SUBLIST TABLE.
         SPACE ,
ISPRM8   CLI   ISPNVOL,C' '        EXCLUDE BY VOLSER?
         BE    ISPRM9              NO
         OI    FLAGS2,NOVOLUME     INDICATE NOVOLUME WANTED.
         LA    R3,ISPNVOL          ADDR OF NOVOLUME(S)
         LA    R9,40               LENGTH OF NOVOLUME INPUT AREA
         LA    R8,7                LENGTH OF A VOLUME
         LA    R7,NVTAB            ADDR OF NOVOLUME TABLE.
         BAL   R14,ISPBLD          GOTO BUILD SUBLIST TABLE.
         SPACE ,
ISPRM9   CLI   ISPHSM,C'Y'         SELECT MIGRATED DATASET.
         BNE   ISPRMA              NO
         OI    FLAGS1,HSM          INDICATE HSM WANTED.
         ALLOC DDN=HSMDDN,DISP=SHR,DSN=HSMDSN,ERROR=ISPRMA1
         B     ISPRMB
         SPACE ,
ISPRMA   CLI   ISPHSM,C'N'         DON'T SELECT MIGRATED DATASETS.
         BNE   ISPRMB              NO
ISPRMA1  NI    FLAGS1,NOHSM        INDICATE HSM NOT WANTED.
         SPACE ,
ISPRMB   CLI   ISPTAP,C'Y'         SELECT TAPE DATASETS.
         BNE   ISPRMC              NO
         OI    FLAGS1,TAPE         INDICATE TAPE WANTED.
         ALLOC DDN=VMFDDN,DISP=SHR,DSN=VMFDSN,ERROR=ISPRMC0
         B     ISPRMSV
         SPACE ,
ISPRMC   CLI   ISPTAP,C'N'         DON'T SELECT TAPE DATASETS.
         BNE   ISPRMD              NO
ISPRMC0  NI    FLAGS1,NOTAPE       INDICATE TAPE NOT WANTED.
         SPACE ,
ISPRMSV  CLI   ISPSBV,C'Y' SORT BY VOLSER?
         BNE   ISPRMD              NO.
         OI    FLAGS4,VOLSORT      SAY SORT BY DSN W/IN VOLSER.
         OI    FLAGS1,SORT         WHICH IMPLIES SORT.
         SPACE ,
ISPRMD   CLI   ISPSRT,C'Y'         SORT THE DATASET INFO TABLE.
         BNE   ISPRME              NO
         OI    FLAGS1,SORT         INDICATE SORT WANTED.
         SPACE ,
ISPRME   CLI   ISPSRT,C'N'         DON'T SORT THE DSINFO TABLE.
         BNE   ISPRMF              NO
         NI    FLAGS1,NOSORT       INDICATE SORT NOT WANTED.
         SPACE ,
ISPRMF   CLI   ISPDAS,C'Y'         SELECT DASD DATASETS.
         BNE   ISPRMG              NO
         OI    FLAGS1,DASD         INDICATE DASD WANTED.
         SPACE ,
ISPRMG   CLI   ISPDAS,C'N'         EXCLUDE DASD DATASETS.
         BNE   ISPRMK              NO
         NI    FLAGS1,NODASD       INDICATE DASD NOT WANTED.
         SPACE ,
ISPRMK   CLI   ISPUNU,C' '         SELECT OLDIES ONLY?
         BE    ISPRML              NO.
         OI    FLAGS3,UNUSED       SAY OLDIES ONLY PLEASE.
         LA    R3,ISPUNU           ADDR OF UNUSED
         LA    R9,5                LENGTH OF UNUSED INPUT AREA
         LA    R8,4                3 DIGIT DAYS VALUE AT MOST.
         LA    R7,UTAB             SOMEWHERE TO PUT IT.
         BAL   R14,ISPBLD          GO EXTRACT AGE VALUE.
         SPACE ,
ISPRML   CLI   ISPEMP,C'Y'         EMPTY DATASETS ONLY?
         BNE   ISPRMM              NO
         OI    FLAGS3,EMPTY        EMPTIES ONLY.
         SPACE ,
ISPRMM   CLI   ISPCON,C' '         SCAN DSNAME FOR STRING?
         BE    ISPRMN              NO.
         OI    FLAGS3,CONTAIN      SAY SCAN DSNAME FOR STRINGS.
         LA    R3,ISPCON           ADDR OF CONTAINING(S)
         LA    R9,40               LENGTH OF CONTAINING INPUT AREA
         LA    R8,9                8 CHARACTER SEARCH ARGS.
         LA    R7,CTAB             ARG TABLE.
         BAL   R14,ISPBLD          GO EXTRACT SEARCH STRING.
         SPACE ,
ISPRMN   CLI   ISPEND,C' '         SELECT DSNAMES BY ENDING.
         BE    ISPRMO              NO.
         LA    R3,ISPEND           ADDR OF ENDING(S)
         LA    R9,40               LENGTH OF ENDING INPUT AREA
         OI    FLAGS3,ENDING       SAY SELECT BY ENDING.
         LA    R8,9                8 CHARACTER ENDING ARGS.
         LA    R7,ETAB             ARG TABLE.
         BAL   R14,ISPBLD          GO EXTRACT ENDING STRING.
         SPACE ,
ISPRMO   CLI   ISPNCON,C' '        EXCLUDE BY CONTENTS.
         BE    ISPRMP              NO.
         OI    FLAGS3,NOTCONT      SAY SCAN DSNAME FOR STRINGS.
         LA    R3,ISPNCON          ADDR OF NOTCONTAINING(S)
         LA    R9,40               LENGTH OF NOTCONTAINING INPUT AREA
         LA    R8,9                8 CHARACTER SEARCH ARGS.
         LA    R7,NCTAB            ARG TABLE.
         BAL   R14,ISPBLD          GO EXTRACT SEARCH STRING.
         SPACE ,
ISPRMP   CLI   ISPNEND,C' '        EXCLUDE DSNAMES BY ENDING
         BE    ISPRMS              NO.
         OI    FLAGS3,NOTEND       SAY EXCLUDE BY ENDING.
         LA    R3,ISPNEND          ADDR OF NOTENDING(S)
         LA    R9,40               LENGTH OF NOTENDING INPUT AREA
         LA    R8,9                8 CHARACTER ENDING ARGS.
         LA    R7,NETAB            ARG TABLE.
         BAL   R14,ISPBLD          GO EXTRACT ENDING STRING.
         SPACE ,
ISPRMS   CLI   ISPBSZ,C'Y'         PRINT LIST BY TRACKS ALLOCATED?
         BNE   ISPRMT              YES.
         OI    FLAGS3,BYSIZE       SORT THEM BY TRACKS.
         NI    REPORTS,NREPORT4    REPORT4 NO GOOD W/ BYSIZE.
         SPACE ,
ISPRMT   CLI   ISPLRG,C' '         SELECT ONLY LARGEST DATASETS?
         BE    ISPRMU              NO.
         OI    FLAGS4,LARGER       SAY SELECT LARGER THAN.
         LA    R3,ISPLRG           ADDR OF LARGER
         LA    R9,5                LENGTH OF LARGER INPUT AREA
         LA    R8,4                4 DIGIT SIZE VALUES.
         LA    R7,STAB             SIZE THRESHOLD TABLE.
         BAL   R14,ISPBLD          EXTRACT TRACKS VALUE.
         SPACE ,
ISPRMU   CLI   ISPSMA,C' '         SELECT ONLY SMALLEST DATASETS?
         BE    ISPRMR1             NO.
         OI    FLAGS4,SMALLER      SAY SELECT SMALLER THAN.
         LA    R3,ISPSMA           ADDR OF SMALLER
         LA    R9,5                LENGTH OF SMALLER INPUT AREA
         LA    R8,4                4 DIGIT SIZE VALUES.
         LA    R7,STAB             SIZE THRESHOLD TABLE.
         BAL   R14,ISPBLD          EXTRACT TRACKS VALUE.
         SPACE ,
ISPRMR1  CLI   ISPRP1,C'Y'         REPORT BY VOLUME.
         BNE   ISPRMR2             NO
         OI    REPORTS,REPORT1     INDICATE REPORT 1 WANTED.
         SPACE ,
ISPRMR2  CLI   ISPRP2,C'Y'         REBLOCKING REPORT.
         BNE   ISPRMR4             NO
         OI    REPORTS,REPORT2     INDICATE REPORT 2 WANTED.
         SPACE ,
ISPRMR4  CLI   ISPRP4,C'Y'         REPORT BY HIGH-LEVEL QUALIFIER.
         BNE   ISPRMC1             NO.
         OI    REPORTS,REPORT4     INDICATE REPORT 4 WANTED.
         SPACE ,
ISPRMC1  CLI   ISPCAT,C'Y'         SELECTED CATALOGUED DS?
         BNE   ISPRMC2             NO
         OI    FLAGS2,CAT          SELECT CATALOGUED DATASETS
         SPACE ,
ISPRMC2  CLI   ISPUNC,C'Y'         SELECTED UNCATALOGUED DS?
         BNE   ISPRMC3             NO.
         OI    FLAGS2,UNCAT        SELECT UNCATALOGUED DATASETS
         SPACE ,
ISPRMC3  CLI   ISPMSC,C'Y'         SELECTED MISCATALOGUED DS?
         BNE   ISPRMOF             NO.
         OI    FLAGS2,MISCAT       SELECT MISCATALOGUED DATASETS
         SPACE ,
ISPRMOF  CLI   ISPONF,C'Y'         TAPES WITH 1 DATASET ONLY?
         BNE   ISPRMDP             NO
         OI    FLAGS4,ONEFILE      SELECT ONLY TAPES WITH 1 DS.
         SPACE ,
ISPRMDP  CLI   ISPDTP,C' '         DATE PROTECTED DATASETS...
         BE    ISPRMWI             NO.
         OI    FLAGS4,DATEPROT     SELECT DATE PROTECTED ONLY.
         SPACE ,
ISPRMWI  CLI   ISPWIN,C' '         RECENTLY USED ONLY?
         BE    ISPRMDN             NO..
         OI    FLAGS4,WITHIN       RECENTLY USED ONLY.
         LA    R3,ISPWIN           ADDR OF WITHIN
         LA    R9,5                LENGTH OF WITHIN INPUT AREA
         LA    R8,4                3 DIGIT DATE.
         LA    R7,WITAB            DATE SAVE AREA.
         BAL   R14,ISPBLD          EXTRACT DATE VALUE.
         SPACE ,
ISPRMDN  CLI   ISPDEN,C' '         SELECT BY TAPE DENSITY?
         BE    ISPRMUL             NO.
         OI    FLAGS4,DNSTY        SELECT TAPES BY DENSITY.
         MVC   DENSITY,0(R3)       SAVE DCB=DEN= VALUE.
         SPACE ,
ISPRMUL  CLI   ISPUNIT,C' '        SELECT BY UNITNAME?
         BE    ISPRMCB             NO.
         OI    INTFLAG,UNITPARM    CALL UNITNAME DECODE ROUTINE.
         OI    FLAGS2,VOLUME       PROCESS UNITS AS VOLUME REQUEST
         LA    R3,ISPUNIT          ADDR OF UNITS
         LA    R9,40               LENGTH OF UNIT INPUT AREA
         LA    R8,9                8 CHARACTER UNITNAMES.
         LA    R7,UNITS            UNIT LIST ADDR.
         BAL   R14,ISPBLD          SAVE UNIT NAMES.
         SPACE ,
ISPRMCB  CLI   ISPCRBY,C' '        SELECT BY CREATE JOBNAME?
         BE    ISPRMRB             NO.
         OI    FLAGS5,CREATEBY     INDICATE SELECT BY CREATE JOB.
         LA    R3,ISPCRBY          ADDR OF CREATE JOBNAMES
         LA    R9,40               LENGTH OF CREATEBY INPUT AREA
         LA    R8,9                8 CHARACTER JOBNAME.
         LA    R7,CREATJOB         JOB NAME SAVE AREA.
         BAL   R14,ISPBLD          SAVE JOB NAME.
         SPACE ,
ISPRMRB  CLI   ISPRDBY,C' '        SELECT BY INPUT JOBNAME?
         BE    ISPRMCH             NO.
         OI    FLAGS5,READBY       INDICATE SELECT BY READ JOB.
         LA    R3,ISPRDBY          ADDR OF READ JOBNAMES
         LA    R9,40               LENGTH OF READBY INPUT AREA
         LA    R8,9                8 CHARACTER JOBNAME.
         LA    R7,READJOB          JOB NAME SAVE AREA.
         BAL   R14,ISPBLD          SAVE JOB NAME.
         SPACE ,
ISPRMCH  CLI   ISPCHG,C' '         CHANGED DATASETS ONLY?
         BE   ISPRMDS              NO
         OI    FLAGS5,CHANGED      SAY CHANGED DATASETS ONLY.
         SPACE ,
ISPRMDS  CLI   ISPDSO,C' '         SELECT BY DSORG?
         BE    ISPRMCR             NO.
         OI    FLAGS5,DSORG        SELECT BY DSORG
         LA    R3,ISPDSO           ADDR OF DSORGS
         LA    R9,5                LENGTH OF DSORG INPUT AREA
         LA    R8,4                3 CHARACTER DSORG.
         LA    R7,DOTAB$           INTERNAL TABLE
         BAL   R14,ISPBLD          CREATE TABLE.
         SPACE ,
ISPRMCR  CLI   ISPCRT,C' '         RECENTLY CREATED ONLY?
         BE    ISPRMER             YES.
         OI    FLAGS5,CREATED      SELECT BY CREATE DATE.
         LA    R3,ISPCRT           ADDR OF CREATED
         LA    R9,5                LENGTH OF INPUT AREA
         LA    R8,4                3 CHARACTER DAYS.
         LA    R7,CRTAB            DATE SAVE AREA.
         BAL   R14,ISPBLD
         SPACE ,
ISPRMER  DS    0H
         B     ISPP99              PRINT REPORT AND DO IT.
         SPACE 3
ISPBLD   DS    0H
         SR    R1,R1               R1 WILL BE ADDR OF SCANNED CHAR.
         SR    R2,R2               R2 WILL CONTAIN SCANNED CHAR.
         B     *+10
         TRT   0(0,R3),ISPCTAB     SCAN FOR COMMA OR BLANK
ISPBLD1  EX    R9,*-6
         LTR   R1,R1               DELIMITER FOUND
         BZ    ISPSLERR            NO, SYNTAX ERROR IN SUBLIST.
         LR    R15,R1              ADDR OF SCANNED DELIMITER.
         SR    R15,R3              R15 -> LENGTH OF SUBLIST ELEMENT.
         STC   R15,0(,R7)          SAVE IT
         BCTR  R15,0               MINUS 1 FOR EX.
         EX    R15,ISPBMVC         SAVE SCANNED VALUE
         CLI   0(R1),C' '          WAS DELIMITER A BLANK.
         BNE   ISPBLD2             IF NOT, MORE TO COME.
         AR    R7,R8               SPAN OVER LAST ELEMENT.
         MVI   0(R7),255           SET END OF TABLE.
         BR    R14                 KEEP ON SCANNING
ISPBLD2  LA    R15,1(,R15)         ADD 1 AFTER EX.
         AR    R3,R15              SKIP OVER ENTRY JUST SCANNED.
         LA    R3,1(0,R3)          AND OVER THE DELIMITER
         SR    R9,R15              DECREMENT LENGTH LEFT TO SCAN
         AR    R7,R8               NEXT TABLE ENTRY.
         B     ISPBLD1             FIND NEXT ENTRY
ISPSLERR ABEND 98,DUMP
ISPBMVC  MVC   1(0,R7),0(R3)       SAVE A SUBLIST ITEM.
ISPP99   DS    0H
         TM    INTFLAG,UNITPARM    UNITNAMES TO DECODE?
         BZ    ISPPNOU             NO.
        $CALL  UNITNAME
        $AMODE 31
         NI    INTFLAG,255-UNITPARM FLAG OFF.
ISPPNOU  DS    0H
         TM    INTFLAG,EOJPARMS    EOJ REACHED?
         BO    ISPPNOP             YES. NO MORE OPTIONS PRINT.
        $CALL  OPTIONS
ISPPNOP  DS    0H
         TM    FLAGS3,UNUSED       OLDIES ONLY?
         BZ    ISPRMNOD            NO.
         SR    R1,R1               CLEAR REGISTER.
         ICM   R1,1,UTAB           GET LENGTH OF AGE NUMBER.
         BCTR  R1,0                SETUP FOR EX.
         B     *+10                OK, SO I'M LAZY.....
         PACK  OLDDATE,UTAB+1(0)
         EX    R1,*-6              PACK IT.
         TIME ,
         ST    R1,COMPDATE         TODAYS JULIAN DATE.
         CP    OLDDATE,COMPDATE+2(2) IS THRESHOLD THIS YEAR?
         BH    ISPRMLY             YES.
         SP    COMPDATE,OLDDATE    OTHERWISE JUST SUBTRACT.
         B     ISPRMCNV
ISPRMLY  DS    0H
         IC    R1,COMPDATE+1       GET YY VALUE.
         BCTR  R1,0                MINUS ONE.
         STC   R1,COMPDATE+1       MAKE IT LAST YEAR.
         SP    OLDDATE,COMPDATE+2(2) HOW FAR INTO LAST YEAR.
         MVC   COMPDATE+2(2),=PL2'366'
         SP    COMPDATE,OLDDATE    SOMETIME LAST YEAR.
ISPRMCNV DS    0H
         SR    R0,R0
         IC    R0,COMPDATE+1       GET YY
         SLL   R0,4                MAKE ROOM FOR A SIGN.
         O     R0,=X'0000000F'     GIVE US A SIGN!
         SRDL  R0,32
         STM   R0,R1,ISPDWRD
         CVB   R1,ISPDWRD          YY IN BINARY
         STCM  R1,1,DSCBOLD        SAVE IT.
         SR    R1,R1
         ICM   R1,3,COMPDATE+2     GET DDD
         STM   R0,R1,ISPDWRD
         CVB   R1,ISPDWRD          DDD IN BINARY.
         STCM  R1,3,DSCBOLD+1
ISPRMNOD DS    0H
         TM    FLAGS4,WITHIN       OLDIES ONLY?
         BZ    ISPRMNWI            NO.
         SR    R1,R1               CLEAR REGISTER.
         ICM   R1,1,WITAB          GET LENGTH OF AGE NUMBER.
         BCTR  R1,0                SETUP FOR EX.
         B     *+10                OK, SO I'M LAZY.....
         PACK  WIDATE,WITAB+1(0)
         EX    R1,*-6              PACK IT.
         TIME ,
         ST    R1,WICOMP           TODAYS JULIAN DATE.
         CP    WIDATE,WICOMP+2(2) IS THRESHOLD THIS YEAR?
         BH    ISPPWLY             YES.
         SP    WICOMP,WIDATE       OTHERWISE JUST SUBTRACT.
         B     ISPPWCNV
ISPPWLY  DS    0H
         IC    R1,WICOMP+1         GET YY VALUE.
         BCTR  R1,0                MINUS ONE.
         STC   R1,WICOMP+1         MAKE IT LAST YEAR.
         SP    WIDATE,WICOMP+2(2) HOW FAR INTO LAST YEAR.
         MVC   WICOMP+2(2),=PL2'366'
         SP    WICOMP,WIDATE       SOMETIME LAST YEAR.
ISPPWCNV DS    0H
         SR    R0,R0
         IC    R0,WICOMP+1         GET YY
         SLL   R0,4                MAKE ROOM FOR A SIGN.
         O     R0,=X'0000000F'     GIVE US A SIGN!
         SRDL  R0,32
         STM   R0,R1,ISPDWRD
         CVB   R1,ISPDWRD          YY IN BINARY
         STCM  R1,1,DSCBWI         SAVE IT.
         SR    R1,R1
         ICM   R1,3,WICOMP+2       GET DDD
         STM   R0,R1,ISPDWRD
         CVB   R1,ISPDWRD          DDD IN BINARY.
         STCM  R1,3,DSCBWI+1
ISPRMNWI DS    0H
         TM    FLAGS4,LARGER+SMALLER ANY SIZE CRITERIA?
         BZ    ISKSIZE             NO.
         SR    R1,R1               CLEAR REGISTER.
         ICM   R1,1,STAB           GET LENGTH OF TRACKS VALUE.
         BCTR  R1,0                SETUP FOR EX.
         B     *+10                SKIP EXECUTED INSTRUCTION.
         PACK  TRKPACK,STAB+1(0)
         EX    R1,*-6              PACK IT.
         CVB   R1,TRKPACK          AND GET IT IN BINARY
         ST    R1,TRKVALUE         SAVE FOR LATER USE.
ISKSIZE  DS    0H
         TM    FLAGS5,DSORG        DSORG IN EFFECT?
         BZ    ISPRMKDS            NO
         LA    R2,DOTAB$           CHARACTER FORMAT TABLE.
         LA    R3,DOTAB            INTERNAL FORMAT TABLE.
         CLC   1(2,R2),=C'??'      INVALID DSORG??????
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'FEFE'    SET STRANGE VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(3,R2),=C'PSU'     CHECK DSORG VALUE.
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'4100'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(3,R2),=C'DAU'     CHECK DSORG VALUE.
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'2100'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(3,R2),=C'POU'     CHECK DSORG VALUE.
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'0300'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(2,R2),=C'IS'      CHECK DSORG VALUE.
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'8000'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(2,R2),=C'PS'      CHECK DSORG VALUE.
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'4000'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(2,R2),=C'DA'      CHECK DSORG VALUE.
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'2000'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(2,R2),=C'PO'      CHECK DSORG VALUE.
         BNE   *+22                TRY THE NEXT ONE.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'0200'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         LA    R2,4(0,R2)          NEXT SLOT.
         CLC   1(2,R2),=C'VS'      CHECK DSORG VALUE.
         BNE   *+18                THAT'S ALL.
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.
         MVC   1(2,R3),=X'0008'    SET HEX VALUE.
         LA    R3,3(0,R3)          NEXT SLOT.
         MVI   0(R3),255           SET END OF TABLE MARKER.
ISPRMKDS DS    0H
         TM    FLAGS5,CREATED      RECENT ONLY?
         BZ    ISPRMNCR            NO.
         SR    R1,R1               CLEAR REGISTER.
         ICM   R1,1,CRTAB          GET LENGTH OF AGE NUMBER.
         BCTR  R1,0                SETUP FOR EX.
         B     *+10                OK, SO I'M LAZY.....
         PACK  CRDATE,CRTAB+1(0)
         EX    R1,*-6              PACK IT.
         TIME ,
         ST    R1,CRCOMP           TODAYS JULIAN DATE.
         CP    CRDATE,CRCOMP+2(2) IS THRESHOLD THIS YEAR?
         BH    ISPPCLY             YES.
         SP    CRCOMP,CRDATE       OTHERWISE JUST SUBTRACT.
         B     ISPPCCNV
ISPPCLY  DS    0H
         IC    R1,CRCOMP+1         GET YY VALUE.
         BCTR  R1,0                MINUS ONE.
         STC   R1,CRCOMP+1         MAKE IT LAST YEAR.
         SP    CRDATE,CRCOMP+2(2) HOW FAR INTO LAST YEAR.
         MVC   CRCOMP+2(2),=PL2'366'
         SP    CRCOMP,CRDATE       SOMETIME LAST YEAR.
ISPPCCNV DS    0H
         SR    R0,R0
         IC    R0,CRCOMP+1         GET YY
         SLL   R0,4                MAKE ROOM FOR A SIGN.
         O     R0,=X'0000000F'     GIVE US A SIGN!
         SRDL  R0,32
         STM   R0,R1,ISPDWRD
         CVB   R1,ISPDWRD          YY IN BINARY
         STCM  R1,1,DSCBCR         SAVE IT.
         SR    R1,R1
         ICM   R1,3,CRCOMP+2       GET DDD
         STM   R0,R1,ISPDWRD
         CVB   R1,ISPDWRD          DDD IN BINARY.
         STCM  R1,3,DSCBCR+1
ISPRMNCR DS    0H
ISPRMNCM DS    0H
ISPFP99  DS    0H
        $EPILOG ,
         LTORG ,
         SPACE ,
         DS    0F
HSMDDN   DC    AL4(*+6),Y(8),CL8'SYSUT1'
         DS    0F
HSMDSN   DC    AL4(*+6),Y(44),CL44'&MCDS'
         DS    0F
VMFDDN   DC    AL4(*+6),Y(8),CL8'VMF'
         DS    0F
VMFDSN   DC    AL4(*+6),Y(44),CL44'&VMF'
         SPACE ,
ISPNLST1 DS    0F
  DC C'(RP1 RP2 RP4 DAS LST TAP SRT BRK EMP HSM BSZ CAT MSC UNC ONF SBVX
                DEN DTP CHG)'
ISPVLST1 DS    0F
ISPRP1   DC    CL1' '
ISPRP2   DC    CL1' '
ISPRP4   DC    CL1' '
ISPDAS   DC    CL1' '
ISPLST   DC    CL1' '
ISPTAP   DC    CL1' '
ISPSRT   DC    CL1' '
ISPBRK   DC    CL1' '
ISPEMP   DC    CL1' '
ISPHSM   DC    CL1' '
ISPBSZ   DC    CL1' '
ISPCAT   DC    CL1' '
ISPMSC   DC    CL1' '
ISPUNC   DC    CL1' '
ISPONF   DC    CL1' '
ISPSBV   DC    CL1' '
ISPDEN   DC    CL1' '
ISPDTP   DC    CL1' '
ISPCHG   DC    CL1' '
         SPACE ,
ISPNLST2 DC    C'(LEV NLEV SER NVOL CON NCON END NEND CRBY RDBY UNIT)'
ISPVLST2 DS    0F
ISPLEV   DC    CL40' '
ISPNLEV  DC    CL40' '
ISPSER   DC    CL40' '
ISPNVOL  DC    CL40' '
ISPCON   DC    CL40' '
ISPNCON  DC    CL40' '
ISPEND   DC    CL40' '
ISPNEND  DC    CL40' '
ISPCRBY  DC    CL40' '
ISPRDBY  DC    CL40' '
ISPUNIT  DC    CL40' '
         SPACE ,
ISPNLST3 DC    C'(WIN DSO UNUSED SMALLER LARGER CRT)'
ISPVLST3 DS    0H
ISPWIN   DC    CL5' '
ISPDSO   DC    CL5' '
ISPUNU   DC    CL5' '
ISPSMA   DC    CL5' '
ISPLRG   DC    CL5' '
ISPCRT   DC    CL5' '
         SPACE ,
ISPDWRD  DC    D'0'
ISPCTAB  DS    0D
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'00000000000000000000000000000000'  0
         DC    X'00000000000000000000000000000000'  1
         DC    X'00000000000000000000000000000000'  2
         DC    X'00000000000000000000000000000000'  3
         DC    X'01000000000000000000000000000000'  4
         DC    X'00000000000000000000000000000000'  5
         DC    X'00000000000000000000000100000000'  6
         DC    X'00000000000000000000000000000000'  7
         DC    X'00000000000000000000000000000000'  8
         DC    X'00000000000000000000000000000000'  9
         DC    X'00000000000000000000000000000000'  A
         DC    X'00000000000000000000000000000000'  B
         DC    X'00000000000000000000000000000000'  C
         DC    X'00000000000000000000000000000000'  D
         DC    X'00000000000000000000000000000000'  E
         DC    X'00000000000000000000000000000000'  F
./       ADD   NAME=ISPFREST
         TITLE 'CLEANUP ENVIRONMENT AFTER ISPF ITERATION.'
         PRINT NOGEN
ISPFREST $PROLOG R12,R10
         L     R11,=V(DATASECT)    ADDRESS OF DATA CSECT
         USING DATASECT,R11        SET ADDRESSIBILITY.
         FREE  UNALC,DDN=ISPRLOC1
         FREE  UNALC,DDN=ISPRLOC2
        $EPILOG ,
         LTORG ,
         DS    0F
ISPRLOC1 DC    AL4(ISPRDDN1),Y(L'ISPRDDN1)
ISPRDDN1 DC    CL8'SYSUT1'         DDNAME FOR MCDS
         DS    0F
ISPRLOC2 DC    AL4(ISPRDDN2),Y(L'ISPRDDN2)
ISPRDDN2 DC    CL8'VMF'            DDNAME FOR VMF
./       ADD   NAME=LISTOUT
         TITLE 'PRODUCE A LISTING OF SELECTED DATASETS.'
LISTOUT  $PROLOG R12
         L     R11,=V(DATASECT)                                    @162
         USING DATASECT,R11                                        @162
         TM    INTFLAG,UNDERSPF    AM I A DIALOG
         BZ    LISTBTCH            NO.
        $CALL  ISPFLIST            LIST OUTPUT AS ISPF TABLE.
         B     LIST99              AND GET OUT
LISTBTCH DS    0H
         TM    INTFLAG,SYSPOPEN    HAVE I OPENED SYSPRINT ALREADY? @165
         BO    LIST0A              YES.                            @165
         ALLOC SYSOUT=CLASS,                                           X
               DDN=LOCSYSP,                                            X
               ERROR=LIST98        ALLOCATE A PRINT FILE.
         B     LIST0               DYNALLOC WORKED.                @150
LIST98   LA    R2,DDNSYS           ADDR OF DDNAME FOR MESSAGE.     @150
         LM    R15,R1,=A(MESGRTN,0,21)                             @150
         BALR  R14,R15             SAY, DYNALLOC FAILED.           @150
         LM    R15,R1,=A(MESGRTN,0,22)                             @150
         BALR  R14,R15             SAY, TRYING PLAN B              @150
LIST0    DS    0H
         L     R2,PAGELEN          PICK UP PAGE LENGTH             @TEC
         XPROPEN PWA,                                                  X
               PAGELEN=(2),                                        @TECX
               DDNAME=SYSPRINT     AND OPEN IT.
         LTR   R15,R15             DID SYSPRINT OPEN?
         BNZ   LIST99              IF NOT, NO REPORT.
         OI    INTFLAG,SYSPOPEN    REMEMBER THAT I'VE DONE THIS.   @165
LIST0A   DS    0H
         XPREJECT  PWA             SKIP TO TOP OF FORM.            @121
         XPRHEAD PWA,                                                  X
               LIST=(HEADER1,                                          X
               HEADER2,                                                X
               HEADER3)            DEFINE THE HEADING LINES.
         XC    UDSCOUNT,UDSCOUNT   ZERO DATASET ACCUMULATOR.       @165
         XC    UTRALLOC,UTRALLOC   ZERO TRACKS ALLOCATED COUNT.    @165
         XC    UTRUSED,UTRUSED     ZERO TRAKCS USED COUNTER.       @165
         LM    R3,R5,TABBXLE       SET UP TO LOOP
         USING SAVEFMT,R3          ADDRESS DATASET INFO TABLE.
LIST1    TM    FLAGS2,BRKLVL       BREAK LEVEL WANTED?             @140
         BZ    LIST1C              NO                              @140
         LH    R14,QUALLEN         LENGTH OF SAVED QUALIFIER.      @141
         B     *+10                SKIP OVER EXECUTED INSTR.       @141
         CLC   SAVEQUAL(0),SV1DSNAM                                @141
         EX    R14,*-6             COMPARE.                        @141
         BE    LIST1C              STILL SAME LEVEL.               @140
         LA    R1,SV1DSNAM         ADDR OF CURRENT DSNAME.         @141
         LA    R14,SV1DSNAM        ADDR OF CURRENT DSNAME.         @141
         LA    R15,SV1DSNAM        ADDR OF CURRENT DSNAME.         @141
         LA    R15,8(,R15)         QUALIFIER IS 8 BYTES AT MOST.   @141
LIST1D   CLI   0(R1),C'.'          END OF QUALIFIER?               @141
         BE    LIST1E              SO, SAVE IT.                    @141
         CR    R1,R15              WAS THERE A QUALIFIER?          @141
         BE    LIST1F              NO, CALL IT 8 BYTES.            @141
         LA    R1,1(,R1)           NEXT BYTE.                      @141
         B     LIST1D              AND KEEP ON TRUCKIN'            @141
LIST1F   LA    R1,8                                                @141
         B     LIST1G              SAVE AN 8 BYTE QUALIFIER.       @141
LIST1E   SR    R1,R14              GET LENGTH OF QUALIFIER TO SAVE @141
LIST1G   XC    SAVEQUAL,SAVEQUAL   BLANK OUT QUALIFIER.            @141
         LR    R14,R1                                              @141
         B     *+10                                                @141
         MVC   SAVEQUAL(0),SV1DSNAM                                @141
         EX    R14,*-6             SAVE NEW QUALIFIER.             @141
         STH   R1,QUALLEN          SAVE LENGTH OF QUALIFIER.       @141
         CLI   FIRSTQ,255          1ST TIME THROUGH HERE?          @141
         BE    LIST1C              SO, DON'T PRINT A BLANK PAGE!   @141
         ICM   R0,15,UDSCOUNT      DATASETS THIS LEVEL.            @140
         CVD   R0,DTWORK           GET IT IN DECIMAL.              @140
         EDIT  R1L2CNT,                                            @140X
               DTL4,                                               @160X
               ZZZZZ9              AND FORMATTED NICELY.           @160
         ICM   R0,15,UTRUSED       USED TRACKS BY LEVEL.           @140
         CVD   R0,DTWORK           GET IT IN DECIMAL.              @140
         EDIT  R1L2USED,                                           @140X
               DTL4,                                               @160X
               ZZZZZZ9             AND FORMATTED NICELY.           @166
         ICM   R0,15,UTRALLOC      ALLOCATED TRACKS BY LEVEL.      @140
         CVD   R0,DTWORK           GET IT IN DECIMAL.              @140
         EDIT  R1L2ALOC,                                           @140X
               DTL4,                                               @160X
               ZZZZZZ9             AND FORMATTED NICELY.           @166
         XPRNTLIN  PWA,                                            @140X
               TEXT=R1LINE2+1,                                     @140X
               LENGTH=132,                                         @140X
               SPB=2,SPA=1         PRINT 1 LINE PER DATASET.       @140
         XPREJECT PWA              SKIP TO TOP OF FORM.            @140
         XC    UDSCOUNT,UDSCOUNT   ZERO DATASET ACCUMULATOR.       @140
         XC    UTRALLOC,UTRALLOC   ZERO TRACKS ALLOCATED COUNT.    @140
         XC    UTRUSED,UTRUSED     ZERO TRAKCS USED COUNTER.       @140
LIST1C   XC    FIRSTQ,FIRSTQ       TOTAL PAGES FROM NOW ON.        @141
         ICM   R1,15,UDSCOUNT      COUNT SO FAR.                   @140
         LA    R1,1(,R1)           ONE MORE.                       @140
         STCM  R1,15,UDSCOUNT      TALLY.                          @140
         MVC   R1DSNAM,SV1DSNAM    SETUP DSNAME IN PRINT LINE.
         MVC   R1DSSN,SV1DSSN      SETUP VOLSER IN PRINT LINE.
         CLC   SV1DSSN,=C'ARCHIV'  WAS IT ARCHIVED?                @150
         BE    LIST1A              YES. THEN FIDDLE DATES.         @150
         CLI   SV1DEVC,C'T'        IS IT A TAPE?
         BE    LIST7A              IF SO DON'T FIDDLE DATES
         CLC   SV1DSSN(5),=C'MGRAT'   WAS IT MIGRATED?             @165
         BNE   LIST1A              IF SO, DATES ARE DIFFERENT FORMAT.
LIST7A   SR    R0,R0               CLEAR 3 BYTES.
         ICM   R0,7,SV1CREDT       PICK UP CREATE DATE
         XC    DTWORK,DTWORK       CLEAR DOUBLE WORD
         ST    R0,DTWORK+4         STORE FOR DATE CONVERSION
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   R1CREDT,DTWORK      MOVE FORMATTED CREATE DATE
         SR    R0,R0               CLEAR 3 BYTES.
         ICM   R0,7,SV1REFD        PICK UP LAST REF DATE
         XC    DTWORK,DTWORK       CLEAR DOUBLE WORD
         ST    R0,DTWORK+4         STORE FOR DATE CONVERSION
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   R1REFDT,DTWORK      MOVE FORMATTED LAST REF DATE
         B     LIST1B              GO TO SETUP EXTENTS.
LIST1A   SR    R0,R0               CLEAR 3 BYTES.
         IC    R0,SV1CREDT         GET YEAR
         MH    R0,=H'1000'         WHICH WAS IN CENTURIES.
         MVC   DTWORK(2),SV1CREDT+1  MOVE DAY
         AH    R0,DTWORK           ADD BINARY DAY
         CVD   R0,DTWORK           PACKED DECIMAL DATE.
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   R1CREDT,DTWORK      MOVE FORMATTED CREATE DATE
         SR    R0,R0               CLEAR 3 BYTES.
         IC    R0,SV1REFD          GET YEAR
         MH    R0,=H'1000'         TIME 1000.
         MVC   DTWORK(2),SV1REFD+1  MOVE DAY
         AH    R0,DTWORK           ADD BINARY DAY
         CVD   R0,DTWORK           PACKED DECIMAL DATE.
         LA    R1,DTWORK           SETUP ADDRESS
         ST    R1,DTPARM           OF PACKED FIELD
         LA    R1,DTPARM           IN PARMLIST
         L     R15,=V(XDATEDIT)    ADDRESS OF DATE CONVERT ROUTINE
         BALR  R14,R15             CONVERT DATE
         MVC   R1REFDT,DTWORK      MOVE FORMATTED LAST REF DATE
LIST1B   SR    R0,R0               CLEAR 3 BYTES.
         IC    R0,SV1NOEPV         PICK UP NUMBER OF EXTENTS
         CVD   R0,DTWORK           CONVERT TO PACKED
         EDIT  R1NOEPV,DTL2,ZZ     FORMAT IT NICELY.
         SR    R0,R0               ZERO HIGH ORDER BYTES.
         ICM   R0,3,SV1BLKL        PICK UP BLOCK SIZE
         CVD   R0,DTWORK           CONVERT TO PACKED
         EDIT  R1BLKL,DTL3,ZZZZ    FORMAT IT NICELY.
         SR    R0,R0               ZERO HIGH ORDER BYTES.
         ICM   R0,3,SV1LRECL       PICK UP RECORD LENGTH
         CVD   R0,DTWORK           CONVERT TO PACKED
         EDIT  R1LRECL,DTL3,ZZZZ   FORMAT IT NICELY.
         CLC   SV1RECFM(2),=CL2' '  RECFM 1 BYTE LONG?
         BNE   LISTR1              YES, GOTO LEFT-JUSTIFY 1 BYTE.
         MVC   SV1RECFM(1),SV1RECFM+2
         MVC   SV1RECFM+1(2),=CL2' '
         B     LISTR9
LISTR1   CLI   SV1RECFM,C' '       ALREADY JUSTIFIED?
         BNE   LISTR9              YES, SKIP 1 BYTE JUSTIFY.
         MVC   SV1RECFM(2),SV1RECFM+1
         MVI   SV1RECFM+2,C' '
LISTR9   MVC   R1RECFM,SV1RECFM    SETUP RECFM FOR PRINTING.
         MVC   R1DSORG,=CL3' '     BLANK DSORG PRINT FIELD.        @160
         TM    SV1DSORG,X'80'      ISAM
         BZ    LIST4B              NO.
         MVC   R1DSORG(2),=C'IS'   SETUP RECFM FOR PRINTING.
         B     LIST4Z              GOTO TRACKS USED.
LIST4B   TM    SV1DSORG,X'40'      PS
         BZ    LIST4C              NO.
         TM    SV1DSORG,X'01'      IS IT UNMOVEABLE?               @164
         BZ    LIST4BB             NO                              @164
         MVC   R1DSORG(3),=C'PSU'  SETUP RECFM.                    @164
         B     LIST4Z              GOTO TRACKS USED.               @164
LIST4BB  MVC   R1DSORG(2),=C'PS'   SETUP RECFM FOR PRINTING.
         B     LIST4Z              GOTO TRACKS USED.
LIST4C   TM    SV1DSORG,X'20'      DA
         BZ    LIST4D              NO.
         TM    SV1DSORG,X'01'      IS IT UNMOVEABLE?               @164
         BZ    LIST4CC             NO                              @164
         MVC   R1DSORG(3),=C'DAU'  SETUP RECFM.                    @164
         B     LIST4Z              GOTO TRACKS USED.               @164
LIST4CC  MVC   R1DSORG(2),=C'DA'   SETUP RECFM FOR PRINTING.
         B     LIST4Z              GOTO TRACKS USED.
LIST4D   TM    SV1DSORG,X'02'      PO
         BZ    LIST4E              NO.
         TM    SV1DSORG,X'01'      IS IT UNMOVEABLE?               @164
         BZ    LIST4DD             NO                              @164
         MVC   R1DSORG(3),=C'POU'  SETUP RECFM.                    @164
         B     LIST4Z              GOTO TRACKS USED.               @164
LIST4DD  MVC   R1DSORG(2),=C'PO'   SETUP RECFM FOR PRINTING.
         B     LIST4Z              GOTO TRACKS USED.
LIST4E   TM    SV1DSORG+1,X'08'    VSAM
         BZ    LIST4Z              NO.
         MVC   R1DSORG(2),=C'VS'   SETUP RECFM FOR PRINTING.
LIST4Z   ICM   R1,15,SV1EXT1       GET TRACKS ALLOC
         ICM   R15,15,UTRALLOC     TRACKS ALLOC FOR THIS LEVEL     @140
         AR    R15,R1              PLUS CURRENT DATASET.           @140
         STCM  R15,15,UTRALLOC     COUNT IT OFF.                   @140
         CVD   R1,DTWORK           CONVERT TO PACKED
         EDIT  R1ALLOC,DTL3,ZZZZ   AND THEN TO ZONED.
         MVC   DTWORK,=PL8'0'      ZERO.                           @141
         SR    R8,R8               CLEAR 3 BYTES.                  @141
         IC    R8,SV1NOEPV         GET NUMBER OF EXTENTS.          @141
         LTR   R8,R8               ARE THERE ANY?                  @141
         BZ    LIST6A              IF NOT, SAY ZERO TRACKS USED.   @141
         CLC   SV1DSSN(5),=C'MGRAT'   WAS IT MIGRATED              @165
         BE    LIST6A              NO USAGE AVAILABLE.
         SR    R8,R8               CLEAR OUT 3 BYTES.
         CVD   R8,DTWORK           GET IT IN PACKED.
         CLI   SV1DEVC,C'T'        IS IT ON TAPE?
         BE    LIST6A              THEN LEAVE IT BLANK.
         SR    R8,R8               CLEAR OUT 3 BYTES.              @151
         CLC   SV1LSTAR,=XL3'00'   IS DS1LSTAR ALL ZERO.           @151
         BE    LIST6C              YES. ZERO TRACKS USED.          @151
         ICM   R8,3,SV1LSTAR       HIGH USED TT
         LA    R8,1(,R8)           KEEP IT HONEST
         ICM   R15,15,UTRUSED      TRACKS USED FOR THIS LEVEL.     @140
         AR    R15,R8              PLUS CURRENT DATASET.           @140
         STCM  R15,15,UTRUSED      COUNT IT OFF.                   @140
LIST6C   CVD   R8,DTWORK           GET IT IN PACKED.               @151
         CR    R8,R1               CHECK FOR WIERD DS1LSTAR?
         BNH   LIST6A              MUST BE OK.
         MVC   R1USED,=C'????'     WHAT THE HELL?????
         B     LIST6B              NO NUMERIC TO FORMAT.
LIST6A   EDIT  R1USED,DTL3,ZZZZ    FORMAT FOR PRINTING.
LIST6B   EQU   *
         XPRNTLIN  PWA,                                                X
               TEXT=R1LINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1         PRINT 1 LINE PER DATASET.
         BXLE  R3,R4,LIST1         LOOP BACK TO DO NEXT DATASET.
         ICM   R0,15,UDSCOUNT      DATASETS THIS LEVEL.            @141
         CVD   R0,DTWORK           GET IT IN DECIMAL.              @141
         EDIT  R1L2CNT,                                            @141X
               DTL3,                                               @141X
               ZZZ9                AND FORMATTED NICELY.           @141
         ICM   R0,15,UTRUSED       USED TACKS BY LEVEL.     .      @170
         CVD   R0,DTWORK           GET IT IN DECIMAL.              @170
         EDIT  R1L2USED,                                           @170X
               DTL4,                                               @170X
               ZZZZZZ9             AND FORMATTED NICELY.           @170
         ICM   R0,15,UTRALLOC      ALLOCATED TRACKS BY LEVEL.      @170
         CVD   R0,DTWORK           GET IT IN DECIMAL.              @170
         EDIT  R1L2ALOC,                                           @170X
               DTL4,                                               @170X
               ZZZZZZ9             AND FORMATTED NICELY.           @170
         XPRNTLIN  PWA,                                            @141X
               TEXT=R1LINE2+1,                                     @141X
               LENGTH=132,                                         @141X
               SPB=2,SPA=1         PRINT 1 LINE PER DATASET.       @141
         XPREJECT PWA              SKIP TO TOP OF FORM.            @141
         TM    INTFLAG,EODPARMS    ANYTHING MORE TO PRINT?         @165
         BZ    LIST99              MAYBE.                          @165
         XPRCLOSE  PWA             CLOSE PRINT FILE
         UNDERTSO YES=LIST99                                       @163
         FREE  UNALC,DDN=LOCSYSP   AND THEN FREE IT.
LIST99   DS    0H                  GET OUT.
         $EPILOG ,                                                 @162
         DROP  R3
         LTORG ,                                                   @162
         DS    0F
LOCSYSP  DC    AL4(DDNSYS),Y(L'DDNSYS)
DDNSYS   DC    CL8'SYSPRINT'       DDNAME FOR FULL LISTING DATASET.
PWA      XPRDCB  DDNAME=SYSPRINT
HEADER1  XPRLDEF TEXT=HTEXT1,LENGTH=L'HTEXT1,SPB=(0,ATHOF),SPA=2
HTEXT1   DC    C'&NAME &VERSION -- ALLOCATED DATASETS -- BY DSNAME '
HTEXT1C  EQU   HTEXT1+16,50                                        @166
         LTORG ,
HEADER2  XPRLDEF TEXT=HTEXT2,LENGTH=132,SPA=1
HEADER3  XPRLDEF TEXT=HTEXT3,LENGTH=132,SPA=2
HTEXT2   DS    0CL133
         DC    CL59' '
         DC    C'-------DATE--------'
         DC    CL3' '                                              @152
         DC    CL4'FSQ/'                                           @152
         DC    CL34' '                                             @152
         DC    C'-TRKS OR BLKS'
HTEXT3   DS    0CL133
         DC    C'------------------DATASET NAME--------------'
         DC    CL4' '
         DC    CL6'VOLSER'
         DC    CL5' '
         DC    CL7'CREATED'
         DC    CL4' '
         DC    CL8'LAST REF'
         DC    CL3' '
         DC    CL3'EXT'
         DC    CL3' '
         DC    CL5'RECFM'
         DC    CL2' '
         DC    CL7'BLKSIZE'
         DC    CL3' '
         DC    CL5'LRECL'
         DC    CL4' '
         DC    CL3'DSO'
         DC    CL3' '
         DC    CL5'ALLOC'
         DC    CL3' '
         DC    CL4'USED'
R1LINE1  DS    0CL133
         DC    CL1' '
R1DSNAM  DC    CL44' ',CL4' '
R1DSSN   DC    CL6' ',CL4' '
R1CREDT  DC    CL8' ',CL4' '
R1REFDT  DC    CL8' ',CL4' '
R1NOEPV  DC    CL2' ',CL4' '
R1RECFM  DC    CL3' ',CL4' '
R1BLKL   DC    CL5' ',CL4' '
R1LRECL  DC    CL5' ',CL4' '
R1DSORG  DC    CL3' ',CL4' '
R1ALLOC  DC    CL4' ',CL4' '
R1USED   DC    CL4' '
R1LINE2  DS    0CL133                                              @140
         DC    CL1' '                                              @140
R1L2CNT  DC    CL6' ',CL44' DATASETS FOR THIS LEVEL'               @160
         DC    CL4' ',CL4' '                                       @160
         DC    CL8' ',CL4' '                                       @140
         DC    CL8' ',CL4' '                                       @140
         DC    CL2' ',CL4' '                                       @140
         DC    CL3' ',CL4' '                                       @140
         DC    CL5' ',CL4' '                                       @140
         DC    CL5' ',CL4' '                                       @140
         DC    CL4' '                                              @170
R1L2ALOC DC    CL7' ',CL1' '                                       @170
R1L2USED DC    CL7' ',CL3' '                                       @170
         DC    CL4' '                                              @140
./       ADD   NAME=MESGRTN
         TITLE 'MESSAGE ROUTINE.'
MESGRTN  $PROLOG R12                                               @130
         L     R11,=V(DATASECT)                                    @130
         USING DATASECT,R11                                        @130
         TM    INTFLAG,UNDERSPF  AM I A DIALOG
         BO    MSKIP1            IF SO, DON'T DO THIS
         CLI   MALLOC,255        HAS SYSLIST BEEN ALLOCATED?       @130
         BE    MALLOC1           ONCE ONLY PLEASE.                 @130
         MVI   MALLOC,255        MARK IT AS ALLOCATED.             @130
         ST    R1,MR1SAVE        FOLLOWING CODE BAGS R1            @130
         ALLOC SYSOUT=CLASS,                                       @130X
               DDN=LOCLIST,                                        @130X
               ERROR=MSGOPEN     ALLOCATE THE MESSAGE FILE.        @150
MSGOPEN  DS    0H
         L     R5,PAGELEN        PICK UP PAGELENGTH                @TEC
         XPROPEN PWL,                                              @130X
               PAGELEN=(5),                                        @TECX
               DDNAME=SYSLIST    AND OPEN IT.                      @130
         LTR   R15,R15           WELL......                        @150
         BZ    MSGOK             YUP.                              @150
         WTO   'SYSLIST NOT OPENED',ROUTCDE=11                     @150
         ABEND 13                                                  @150
MSGOK    XPREJECT PWL            SKIP TO A NEW PAGE.               @130
         XPRHEAD PWL,                                              @130X
               LIST=SLHEAD1      AND DEFINE THE HEADING.           @130
         L     R1,MR1SAVE        MESSAGE NUMBER FROM CALLER.       @130
MALLOC1  L     R3,=V(MESSAGES)   GET ADDRESS OF MESSAGE TABLE.     @130
         SLL   R1,1              TIMES 2 FOR HALFWORD INDEX        @130
         LH    R1,0(R1,R3)       PICKUP INDEX INTO MESSGAE TABLE.  @130
         AR    R1,R3             DEVELOP OFFSET OF MESSAGE TEXT.   @130
         SR    R3,R3             ZERO REGISTER.                    @130
         IC    R3,0(,R1)         PICKUP TEXT LENGTH                @130
         SR    R4,R4             ZERO REGISTER.                    @130
         IC    R4,1(,R1)         LENGTH TO BE INSERTED.            @130
         LTR   R4,R4             ANYTHING TO INSERT?               @130
         BZ    MSKIP2            NO, PRINT MESSAGE AS IS.          @130
         LR    R5,R3             MESSAGE LENGTH                    @130
         SR    R5,R4             MINUS INSERT LENGTH               @130
         AR    R5,R1             PLUS START ADDR                   @130
         LA    R5,2(,R5)         PLUS 2 IS INSERT POINT.           @130
         BCTR  R4,0              MINUS 1 FOR EX.                   @130
         EX    R4,MINSERT        ADD INSERT TEXT.                  @130
MSKIP2   LA    R1,2(,R1)         SKIP OVER LENGTH BYTES.           @130
         XPRNTLIN PWL,                                             @130X
               TEXT=(R1),                                          @130X
               LENGTH=(R3),                                        @130X
               SPB=((R0)),                                         @130X
               SPA=1             PRINT THE MESSAGE TEXT.           @130
MSKIP1   EQU   *                                                   @130
         $EPILOG ,                                                 @130
NOSYSLST ABEND 130,DUMP                                            @130
         LTORG ,                                                   @130
MINSERT  MVC   0(0,R5),0(R2)     CUSTOMIZE A MESSAGE.              @130
MALLOC   DC    A(*-*)            HAS SYSLIST BEEN ALLOCATED.       @130
MR1SAVE  DC    AL4(0)            SAVEAREA FOR REGISTER 1.          @130
LOCLIST  DC    AL4(*+6),Y(8),CL8'SYSLIST'                          @130
PWL      XPRDCB DDNAME=SYSLIST                                     @130
SLHEAD1  XPRLDEF TEXT=SLHTXT1,                                     @130X
               LENGTH=L'SLHTXT1,                                   @130X
               SPB=(0,ATHOF),                                      @130X
               SPA=2                                               @130
SLHTXT1  DC    C'&NAME &VERSION -- MESSAGES'                       @130
MESSAGES CSECT                                                     @130
         MACRO                                                     @130
         MESG  &N,&F,&T                                            @130
&L       SETA  K'&SYSLIST(3)-2                                     @130
         AIF   (&SYSLIST(2) EQ 0).NOFILL                           @130
&L       SETA  &L+&SYSLIST(2)                                      @130
MESG&N   DC    YL1(&L),YL1(&F),C&T,CL&F' '                         @130
         MEXIT                                                     @130
.NOFILL  ANOP                                                      @130
MESG&N   DC    YL1(&L),YL1(&F),C&T                                 @130
         MEND                                                      @130
         DC    Y(MESG0-MESSAGES)                                   @130
         DC    Y(MESG1-MESSAGES)                                   @130
         DC    Y(MESG2-MESSAGES)                                   @130
         DC    Y(MESG3-MESSAGES)                                   @130
         SPACE ,
         DC    Y(MESG4-MESSAGES)                                   @133
         DC    Y(MESG5-MESSAGES)                                   @133
         DC    Y(MESG6-MESSAGES)                                   @133
         DC    Y(MESG7-MESSAGES)                                   @133
         DC    Y(MESG8-MESSAGES)                                   @133
         DC    Y(MESG9-MESSAGES)                                   @133
         DC    Y(MESG10-MESSAGES)                                  @133
         DC    Y(MESG11-MESSAGES)                                  @133
         DC    Y(MESG12-MESSAGES)                                  @133
         DC    Y(MESG13-MESSAGES)                                  @133
         DC    Y(MESG14-MESSAGES)                                  @133
         DC    Y(MESG15-MESSAGES)                                  @133
         DC    Y(MESG16-MESSAGES)                                  @133
         DC    Y(MESG17-MESSAGES)                                  @133
         DC    Y(MESG18-MESSAGES)                                  @133
         DC    Y(MESG19-MESSAGES)                                  @133
         DC    Y(MESG20-MESSAGES)                                  @133
         SPACE ,
         DC    Y(MESG21-MESSAGES)                                  @150
         DC    Y(MESG22-MESSAGES)                                  @150
         DC    Y(MESG23-MESSAGES)                                  @150
         DC    Y(MESG24-MESSAGES)                                  @150
         DC    Y(MESG25-MESSAGES)                                  @150
         DC    Y(MESG26-MESSAGES)                                  @150
         DC    Y(MESG27-MESSAGES)                                  @150
         SPACE ,
         DC    Y(MESG28-MESSAGES)                                  @162
         DC    Y(MESG29-MESSAGES)                                  @162
         DC    Y(MESG30-MESSAGES)                                  @162
         DC    Y(MESG31-MESSAGES)                                  @162
         DC    Y(MESG32-MESSAGES)                                  @162
         DC    Y(MESG33-MESSAGES)                                  @162
         SPACE ,
         MESG  0,0,'&NAME &VERSION -- ENDED SUCCESSFULLY.'         @130
         MESG  1,80,'SYSIN INPUT: '                                @130
         MESG  2,0,'&NAME &VERSION -- STARTING.'                   @130
         MESG  3,10,'OPTION IN EFFECT: '                           @130
         SPACE ,
         MESG  4,8,'REPORT FILE BEING ALLOCATED. DDNAME: '         @133
         MESG  5,8,'REPORT FILE ALLOCATED. DDNAME: '               @133
         MESG  6,8,'REPORT FILE ALLOCATION FAILED. DDNAME: '       @133
         MESG  7,6,'BEGINNING TO READ VTOC ON VOLUME: '            @133
         MESG  8,6,'END OF VTOC REACHED ON VOLUME: '               @133
         MESG  9,0,'BEGINNING TO READ TAPE VOLUME MASTER FILE.'    @133
         MESG 10,0,'END OF TAPE VOLUME MASTER FILE REACHED.'       @133
         MESG 11,44,'INVALID DS1LSTAR VALUE FOR DATASET: '         @133
         MESG 12,0,'HSM MIGRATION CONTROL DATASET BEING OPENED.'   @133
         MESG 13,44,'HSM MCDS GET FAILED. KEY: '                   @133
         MESG 14,8,'TAPE VOLSER TABLE CAPACITY EXCEEDED. QUAL: '   @133
         MESG 15,0,'HSM BACKUP CONTROL DATASET BEING OPENED.'      @133
         MESG 16,44,'HSM BCDS GET FAILED. KEY: '                   @133
         MESG 17,0,'OPENING SYSIN.'                                @133
         MESG 18,0,'SYSIN OPENED.'                                 @133
         MESG 19,0,'SYSIN OPEN FAILED.'                            @133
         MESG 20,0,'END OF FILE REACHED ON SYSIN.'                 @133
         SPACE ,
         MESG 21,8,'DYNAMIC ALLOCATION FAILED FOR DDNAME: '        @150
         MESG 22,0,'ATTEMPTING TO USE PRE-ALLOCATED FILE.'         @150
         MESG 23,8,'OPEN FAILED FOR DDNAME: '                      @150
         MESG 24,0,'ARCHIVED DATASETS NOT LISTED.'                 @150
         MESG 25,8,'MAXIMUM DSNAME TABLE ENTRIES: '                @150
         MESG 26,8,'DATASETS SELECTED: '                           @150
         MESG 27,0,'RUN TERMINATED. TABLE CAPACITY EXCEEDED.'      @150
         SPACE ,
         MESG 28,3,'OFFLINE UNIT SELECTED: '                       @162
         MESG 29,3,'UNIT SELECTED NOT DIRECT ACCESS: '             @162
         MESG 30,8,'REQUESTED UNIT NAME NOT FOUND: '               @162
         MESG 31,0,'STORAGE NOT AVAILABLE FOR UNITNAME LOOKUP.'    @162
         MESG 32,0,'INTERNAL ERROR IN UNITNAME LOOKUP.'            @162
         MESG 33,30,'OPTION IN EFFECT: '                           @162
         SPACE ,
./       ADD   NAME=NCHELP
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
+The following topics are presented in sequence or may be selected by nu
+
% 0+NOCELL   +(Dataset Analysis System)
+
% 1+VOLUME       +(select by volser)%     19+BREAKLEVEL+(subtotal by qua
% 2+NOVOLUME     +(exclude by volser)%    20+BYSIZE    +(sort LIST by si
% 3+LEVEL        +(select by dsname)%     21+ONEFILE   +(non-stacked tap
% 4+NOLEVEL      +(exclude by dsname)%    22+UNUSED    +(select by age)
% 5+CONTAINING   +(select by dsn content)%23+SMALLER   +(select smaller
% 6+NOTCONTAINING+(exlude by dsn content)%24+LARGER    +(select larger t
% 7+ENDING       +(select by dsn trailer)%25+WITHIN    +(recently used o
% 8+NOTENDING    +(exlude by dsn trailer)%26+HSM       +(hsm migrated on
% 9+DUMPVMF      +(hexprint VMF records)% 27+HSMTAPE   +(select hsm back
%10+DASD         +(select disk datasets)% 28+REPORT1   +(volume summary
%11+LIST         +(1-up list datasets)%   29+REPORT2   +(reblocking repo
%12+TAPE         +(select tape datasets)% 30+REPORT4   +(hi-level qual s
%13+SORT         +(sort LIST by dsn) %    31+DENSITY   +(tape density se
%14+EMPTY        +(select empty datasets)%32+DATEPROTEC+(date protected
%15+ASM2         +(asm2 archived only) %  33+UNIT      +(select by esote
%16+CAT          +(cataloged only)     %  34+CREATEDBY +(select by creat
%17+UNCAT        +(uncataloged only)   %  35+READJOB   +(select by read
%18+MISCAT       +(miscataloged only) %
)PROC
 &ZSEL = TRANS(&ZCMD
                0,NCH0
                1,NCH1
                2,NCH2
                3,NCH3
                4,NCH4
                5,NCH5
                6,NCH6
                7,NCH7
                8,NCH8
                9,NCH9
               10,NCH10
               11,NCH11
               12,NCH12
               13,NCH13
               14,NCH14
               15,NCH15
               16,NCH16
               17,NCH17
               18,NCH18
               19,NCH19
               20,NCH20
               21,NCH21
               22,NCH22
               23,NCH23
               24,NCH24
               25,NCH25
               26,NCH26
               27,NCH27
               28,NCH28
               29,NCH29
               30,NCH30
               31,NCH31
               32,NCH32
               33,NCH33
               34,NCH34
               35,NCH35)
)END
./       ADD   NAME=NCH0
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+The NOCELL program allows you to select an arbitrary subset of the data
+that exist on a system and to produce summary or detailed reports as to
+characteristics. Control cards are used to allow you to define the subs
+want to see.
+
+The remainder of this tutorial documents the meanings of the control ca
+you can specify with the foreground dialog. For full details, see the u
+guide which was supplied in the PDS from which NOCELL was installed.
+
+To use the%NOCELL+dialog, fill in the parameters to select the datasets
+want. Firstly, the SYSLIST output, with diagnostic messages and parms i
+will be displayed, then the LIST output will be displayed.
+
+The following topic will be displayed only if requested explicitly:
+
%A+ NOCELL dataset selection logic.
)PROC
 &ZSEL = TRANS(&ZCMD
                A,*NCH0A)
)END
./       ADD   NAME=NCH0A
%TUTORIAL--------------------------- NOCELL%(logic)+--------------------
%Command ===>_ZCMD
%
+In general, the input to NOCELL consists of control statements that def
+datasets to be selected. NOCELL selects all datasets it finds unless yo
+it a reason not to. The control cards that influence dataset selection
+series of tests which are logically%ANDED+to produce the final subset.
+is all conditions must test true before a dataset is selected. Therefor
+order in which the tests are applied is not significant.
+
+ The sequence of NOCELL processing is to scan the vtocs of DASD volumes
+requested), then scan the TLMSII volume master file (if tape info is re
+then scan the ASM2 archive catalog (if requested) to build up an in-cor
+of datasets matching the specified criteria. The list is then sorted (i
+was requested) using a routine based on CACM algorithm 271. LIST output
+produced first, followed by any summary reports.
)END
./       ADD   NAME=NCH1
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %VOLUME+
+***
+***********************************************************************
+***********************************************************************
+
+The%VOLUME+control card specifies a list of one or more volume serial n
+(or leading portions thereof). If%VOLUME+is in effect, NOCELL will sele
+datasets residing on volumes whose serials match one or more of the val
+entered. %NB:+This test is applied only to DASD volumes, not tape.
+
+ %VOLUME+and%NOVOLUME+cannot both be in effect. If both are specified,%
+will be used and%NOVOLUME+will be ignored.
+
+ Valid examples:
+   %===>+TSOPAK,TSOPK1
+   %===>+TSO,MLI4
+   %===>+MVS
)END
./       ADD   NAME=NCH10
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %DASD+/%NODASD+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter a "Y" in the%DASD+input field, the DASD option will be gen
+otherwise, NODASD will be generated. If%DASD+is in effect, NOCELL will
+online disk volumes (as limited by any%VOLUME+or%NOVOLUME+criteria that
+also apply) to select datasets. If%NODASD+is in effect, disk volumes wi
+be examined.
+
+ %NB:+If you specify%NODASD+and%NOTAPE+the program will think that over
+quite soberly tell you he found no datasets.
+ In general, however, absurd combinations are not rejected by NOCELL.
)END
./       ADD   NAME=NCH11
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %LIST+/%NOLIST+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter a "Y" in the%LIST+input field, the LIST option will be gen
+otherwise NOLIST will be generated. If%LIST+is in effect, NOCELL will p
+a one-up listing of all the datasets it selected.  If%NOLIST+is in effe
+listing is what you get.
+
+The dialog puts%LIST+output to a temporary dataset and BROWSES it.
+
)END
./       ADD   NAME=NCH12
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %TAPE+/%NOTAPE+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter a "Y" in the%TAPE+input field, the TAPE option will be gen
+otherwise, NOTAPE will be generated. If%TAPE+is in effect, NOCELL will
+the TLMSII volume master file to select datasets that match whatever ot
+criteria you specified. If%NOTAPE+is in effect, the VMF will not
+be examined.
+
)END
./       ADD   NAME=NCH13
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %SORT+/%NOSORT+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter a "Y" in the%SORT+input field, the SORT option will be gen
+otherwise NOSORT will be generated. If%SORT+is in effect, NOCELL will s
+the list of datasets it selects so that%LIST+output appears in ascendin
+order by dataset name. If%NOSORT+is in effect, the datasets are not sor
+and%LIST+output appears in the order NOCELL found the datasets.
+
)END
./       ADD   NAME=NCH14
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %EMPTY+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%EMPTY+input field, the EMPTY option will be gen
+There is no explicit NOEMPTY option. If%EMPTY+is in effect, NOCELL will
+only datasets with nothing in them. For disk datasets this is based on
+DS1LSTAR field of the format 1 dscb, and for tape datasets on the block
+field of the TLMSII volume master file record. If you leave this field
+NOCELL will select datasets without regard to whether they are or are n
+empty.
+
+ %NB:+In certain cases the block count field for tape datasets can be z
+even if the tape has data on it. The results of%EMPTY+selection for tap
+should be treated with some measure of scepticism.
)END
./       ADD   NAME=NCH15
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %ASM2+/%NOASM2+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter a "Y" in the%ASM2+input field, the ASM2 option will be gen
+otherwise NOASM2 will be generated. If%ASM2+is in effect, NOCELL will s
+the ASM2 archive catalog to select archived datasets.  If%NOASM2+is in
+effect, archived datasets will not be selected. On%LIST+output archived
+datasets are shown as residing on a volume called+ARCHIV+.
+
+ %NB:+The archive catalog is not small. Use of%ASM2+requires reading it
+start to finish so be prepared to wait a bit.
)END
./       ADD   NAME=NCH16
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %CAT+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%CAT+input field, the CAT option will be generat
+There is no explicit NOCAT option. If%CAT+is in effect, NOCELL will sel
+only datasets catalogued to the volumes on which they are found.
+Any or all of the catalog selections can be specified together. However
+is no indication on%LIST+output which selection applied to a given data
+
+ %NB:+NOCELL determines catalog status by issuing a%LOCATE+for every da
+it is about to select. This will cause something like an order of
+magnitude increase in CPU consumption. Also it deals only with catalogs
+found in the normal search sequence.
)END
./       ADD   NAME=NCH17
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %UNCAT+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%UNCAT+input field, the UNCAT option will be gen
+There is no explicit NOUNCAT option. If%UNCAT+is in effect, NOCELL will
+only datasets not catalogued to any volume.
+Any or all of the catalog selections can be specified together. However
+is no indication on%LIST+output which selection applied to a given data
+
+ %NB:+NOCELL determines catalog status by issuing a%LOCATE+for every da
+it is about to select. This will cause something like an order of
+magnitude increase in CPU consumption. Also it deals only with catalogs
+found in the normal search sequence.
)END
./       ADD   NAME=NCH18
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %MISCAT+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%MISCAT+input field, the MISCAT option will be
+generated.  There is no explicit NOMISCAT option. If%MISCAT+ is in effe
+NOCELL will select only datasets catalogued to a volume other than one
+which they are found.  Any or all of the catalog selections can be spec
+together. However, there is no indication on%LIST+output which selectio
+applied to a given dataset.
+
+ %NB:+NOCELL determines catalog status by issuing a%LOCATE+for every da
+it is about to select. This will cause something like an order of
+magnitude increase in CPU consumption. Also it deals only with catalogs
+found in the normal search sequence.
)END
./       ADD   NAME=NCH19
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %BREAKLEVEL+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%BREAKLEVEL+ input field, the BREAKLEVEL option
+be generated.  There is no explicit NOBREAKLEVEL option. If%BREAKLEVEL+
+effect, NOCELL will produce a subtotal of dataset count and allocated t
+at each change in high-level qualifier and then skip a page.
+
+ %NB:+In order to function properly%BREAKLEVEL+assumes that the selecte
+datasets are sorted by dsname. Therefore, you should specify%SORT+and n
+specify%BYSIZE+.
)END
./       ADD   NAME=NCH2
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %NOVOLUME+
+***
+***********************************************************************
+***********************************************************************
+
+The%NOVOLUME+ control card specifies a list of one or more volume seria
+numbers (or leading portions thereof). If%NOVOLUME+ is in effect, NOCEL
+select only datasets residing on volumes whose serials do not match any
+values you entered. %NB:+This test is applied only to DASD volumes, not
+
+ %VOLUME+and%NOVOLUME+cannot both be in effect. If both are specified,%
+will be used and%NOVOLUME+will be ignored.
+
+ Valid examples:
+   %===>+TSOPAK,TSOPK1
+   %===>+TSO,MLI4
+   %===>+MVS
)END
./       ADD   NAME=NCH20
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %BYSIZE+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%BYSIZE+ input field, the BYSIZE option will be
+generated.  There is no explicit NOBYSIZE option. If%BYSIZE+and%SORT+ a
+effect, NOCELL will sort the list of selected datasets in descending or
+allocated tracks or blocks.
+
+ %NB:+This option conflicts with%BREAKLEVEL+which assumes sort by name,
+therefore the two should not specified together.
)END
./       ADD   NAME=NCH21
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %ONEFILE+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%ONEFILE+ input field, the ONEFILE option will b
+generated.  There is no explicit NOONEFILE option. If%ONEFILE+and%TAPE+
+effect, NOCELL will select datasets on tape only if they are on volumes
+contain only one dataset.
+
)END
./       ADD   NAME=NCH22
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %UNUSED+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%UNUSED+ input field, the UNUSED option will be
+generated.  There is no explicit NOUNUSED option. This option specifies
+number of days. When%UNUSED+is in effect NOCELL will select only datase
+which have gone unreferenced for that many days or longer.
+
+For disk datasets this option requires the SU60 date (DS1REFD).
+
+ Example:
+   Unused%===>+45
)END
./       ADD   NAME=NCH23
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %SMALLER+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%SMALLER+ input field, the SMALLER option will b
+generated.  This option specifies a number of tracks (for disk) or bloc
+(for tape).  When%SMALLER+is in effect NOCELL will select only datasets
+have fewer allocated tracks or blocks than the number you entered.
+
%NB:+If both%SMALLER+and%LARGER+are specified,%LARGER+will be used and%S
+    will be ignored.
+
+ Example:
+   Smaller%===>+15
)END
./       ADD   NAME=NCH24
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %LARGER+
+***
+***********************************************************************
+***********************************************************************
+
+If you enter "Y" in the%LARGER+ input field, the LARGER option will be
+generated.  This option specifies a number of tracks (for disk) or bloc
+(for tape).  When%LARGER+is in effect NOCELL will select only datasets
+have more allocated tracks or blocks than the number you entered.
+
%NB:+If both%SMALLER+and%LARGER+are specified,%LARGER+will be used and%S
+    will be ignored.
+
+ Example:
+   Larger%===>+15
)END
./       ADD   NAME=NCH25
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %WITHIN+
+***
+***********************************************************************
+***********************************************************************
+
+The%WITHIN+ option specifies that only datasets used within the number
+you enter in the%WITHIN+input area will be selected. This option applie
+to both tape and disk datasets, but not to ASM2 archived or HSM migrate
+backed up datasets.
+
+
+ Example:
+   Within%===>+7  (selects datasets used sometime within the last week)
)END
./       ADD   NAME=NCH26
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %HSM+
+***
+***********************************************************************
+***********************************************************************
+
+The%HSM+ option specifies that datasets migrated by DFHSM will be selec
+If you enter a%Y+ in the input field, the option%HSM+will be generated.
+Otherwise%NOHSM+will be in effect and only non-migrated DASD datasets w
+selected.
+Migrated datasets will appear on%LIST+ouput under the names they origin
+on DASD, and as catalogued to a volser called%MIGRAT+
+
)END
./       ADD   NAME=NCH27
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %HSMTAPE+
+***
+***********************************************************************
+***********************************************************************
+
+The%HSMTAPE+ option specifies that backup versions taken by DFHSM will
+selected.  If you enter a%Y+ in the input field, the option%HSMTAPE+wil
+generated.  Otherwise%NOHSMTAPE+ will be in effect and no backup versio
+be selected.
+Backup versions will appear on%LIST+output under the names they origina
+on DASD, and as residing on volser called%BACKUP+
+
)END
./       ADD   NAME=NCH3
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %LEVEL+
+***
+***********************************************************************
+***********************************************************************
+
+The%LEVEL+control card specifies a list of one or more high-level quali
+(or leading portions thereof). If%LEVEL+is in effect, NOCELL will selec
+datasets whose names begin with one or more of the values you entered.
+
+ %LEVEL+and%NOLEVEL+cannot both be in effect. If both are specified,%LE
+will be used and%NOLEVEL+will be ignored.
+
+ Valid examples:
+   %===>+SYS1
+   %===>+SYS1,SYS2
+   %===>+SYS          (this will select SYS3 etc. also)
+
)END
./       ADD   NAME=NCH4
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %NOLEVEL+
+***
+***********************************************************************
+***********************************************************************
+
+The%NOLEVEL+control card specifies a list of one or more high-level qua
+(or leading portions thereof). If%NOLEVEL+is in effect, NOCELL will sel
+datasets whose names do not begin with any of the values you entered.
+
+ %LEVEL+and%NOLEVEL+cannot both be in effect. If both are specified,%LE
+will be used and%NOLEVEL+will be ignored.
+
+ Valid examples:
+   %===>+SYS1
+   %===>+SYS1,SYS2
+   %===>+SYS          (this will select SYS3 etc. also)
)END
./       ADD   NAME=NCH5
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %CONTAINING+
+***
+***********************************************************************
+***********************************************************************
+
+The%CONTAINING+control card specifies a list of character strings.
+If%CONTAINING+is in effect, NOCELL will select only datasets whose name
+contain one or more of the values you entered embedded within them.
+
+ %CONTAINING+and%NOTCONTAINING+can both be in effect. If so, both tests
+be satisfied in order for a dataset to be selected.
+
+ Valid examples:
+   %===>+VTOCIX       (select vtoc index datasets.)
+   %===>+SPFLOG       (select SPF log datasets)
+   %===>+VTOCIX,SPFLOG
+
)END
./       ADD   NAME=NCH6
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %NOTCONTAINING+
+***
+***********************************************************************
+***********************************************************************
+
+The%NOTCONTAINING+control card specifies a list of character strings.
+If%NOTCONTAINING+is in effect, NOCELL will select only datasets whose n
+do not contain any of the values you specified embedded within them.
+
+ %CONTAINING+and%NOTCONTAINING+can both be in effect. If so, both tests
+be satisfied in order for a dataset to be selected.
+
+ Valid examples:
+   %===>+VTOCIX       (exclude vtoc index datasets.)
+   %===>+SPFLOG       (exclude SPF log datasets)
+   %===>+VTOCIX,SPFLOG
+
)END
./       ADD   NAME=NCH7
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %ENDING+
+***
+***********************************************************************
+***********************************************************************
+
+The%ENDING+control card specifies a list of character strings.
+If%ENDING+is in effect, NOCELL will select only datasets whose names
+end in one or more of the values you entered.
+
+ %ENDING+and%NOTENDING+can both be in effect. If so, both tests must
+be satisfied in order for a dataset to be selected.
+
+ Valid examples:
+   %===>+LIST
+   %===>+.SYM,.ERR,.LIST
+   %===>+.LIST
+
)END
./       ADD   NAME=NCH8
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %NOTENDING+
+***
+***********************************************************************
+***********************************************************************
+
+The%NOTENDING+control card specifies a list of character strings.
+If%NOTENDING+is in effect, NOCELL will select only datasets whose names
+do not end in any of the values you entered.
+
+ %ENDING+and%NOTENDING+can both be in effect. If so, both tests must
+be satisfied in order for a dataset to be selected.
+
+ Valid examples:
+   %===>+LIST
+   %===>+.SYM,.ERR,.LIST
+   %===>+.LIST
+
)END
./       ADD   NAME=NCH9
%TUTORIAL--------------------------- NOCELL ----------------------------
%Command ===>_ZCMD
%
+***********************************************************************
+***********************************************************************
+***
+***                                %DUMPVMF+
+***
+***********************************************************************
+***********************************************************************
+
+The%DUMPVMF+control card specifies a list of volume serials (or leading
+thereof). If%DUMPVMF+is in effect, NOCELL will print in hex the content
+TLMSII volume master file (VMF) records that match any of the values yo
+entered.
+
+ %NB:+This option has no effect on datasets selected for LIST output.
)END
./       ADD   NAME=NCISPF1
+-----------------%NOCELL+Dataset Analysis System --------------(1.6.5)-
%COMMAND ===>_ZCMD
+
+ Enter%NOCELL Parameters+below:
+
+         Volume%===>_SER
+       Novolume%===>_NVOL
+           Unit%===>_UNIT
+          Level%===>_LEV
+        Nolevel%===>_NLEV
+     Containing%===>_CON
+  Notcontaining%===>_NCON
+         Ending%===>_END
+      Notending%===>_NEND
+       Dump VMF%===>_DVMF
+      Createdby%===>_CRBY
+         Readby%===>_RDBY
+          Dsorg%===>_DSO
)INIT
 .CURSOR = SER
 .HELP = NCHELP
)PROC
)END
./       ADD   NAME=NCISPF2
+-----------------%NOCELL+Dataset Analysis System --------------(1.6.5)-
%COMMAND ===>_ZCMD
+
+ Enter%NOCELL Parameters+below:
+
+Report1%====>_Z+(Y/N) Report2%===>_Z+(Y/N) Report4%===>_Z+(Y/N)
 Dasd%=======>_Z+(Y/N) List%======>_Z+(Y/N) Tape%======>_Z+(Y/N)
 Sort%=======>_Z+(Y/N) HSM%=======>_Z+(Y/N) Empty%=====>_Z+(Y/N)
 Cat%========>_Z+(Y/N) Miscat%====>_Z+(Y/N) Uncat%=====>_Z+(Y/N)
 Breaklevel%=>_Z+(Y/N) Bysize%====>_Z+(Y/N) Onefile%===>_Z+(Y/N)
 Density%====>_Z+      Within%====>_WIN+    Unused%====>_UNUSED+
 Created%====>_CRT+
 Smaller%====>_SMALLER+Larger%====>_LARGER+ Changed%===>_Z+(Y/N)
 Dateprotected%===>_Z+(Y/N)             Sortbyvolume%===>_Z+(Y/N)
)INIT
 .HELP = NCHELP
 .ZVARS = '(RP1,RP2,RP4,DAS,LST,TAP,SRT,+
 HSM,EMP,CAT,MSC,UNC,BRK,BSZ,ONF,DEN,CHG,DTP,SBV)'
)PROC
 IF (&DAS = ' ')
     &DAS = ' '
 IF (&LST = ' ')
     &LST = ' '
 IF (&TAP = ' ')
     &TAP = ' '
 IF (&SRT = ' ')
     &SRT = ' '
 IF (&BRK = ' ')
     &BRK = ' '
 IF (&EMP = ' ')
     &EMP = ' '
 IF (&HSM = ' ')
     &HSM = ' '
 IF (&BSZ = ' ')
     &BSZ = ' '
 IF (&CAT = ' ')
     &CAT = ' '
 IF (&MSC = ' ')
     &MSC = ' '
 IF (&UNC = ' ')
     &UNC = ' '
 VER (&RP1,LIST,Y,N)
 VER (&RP2,LIST,Y,N)
 VER (&RP4,LIST,Y,N)
 VER (&DAS,LIST,Y,N)
 VER (&LST,LIST,Y,N)
 VER (&TAP,LIST,Y,N)
 VER (&SRT,LIST,Y,N)
 VER (&BRK,LIST,Y,N)
 VER (&EMP,LIST,Y,N)
 VER (&HSM,LIST,Y,N)
 VER (&BSZ,LIST,Y,N)
 VER (&CAT,LIST,Y,N)
 VER (&MSC,LIST,Y,N)
 VER (&UNC,LIST,Y,N)
 VER (&ONF,LIST,Y,N)
 VER (&CHG,LIST,Y,N)
 VER (&DTP,LIST,Y,N)
 VER (&SBV,LIST,Y,N)
 IF  (&DEN    ^= ' ')
    VER (&DEN,LIST,1,2,3,4)
 IF  (&WIN    ^= ' ')
    VER (&WIN,NUM)
 IF  (&UNUSED ^= ' ')
    VER (&UNUSED,NUM)
 IF  (&SMALLER ^= ' ')
    VER (&SMALLER,NUM)
 IF  (&LARGER ^= ' ')
    VER (&LARGER,NUM)
)END
./       ADD   NAME=NC00
NC001    'WORKING'
'WORKING.'

./       ADD   NAME=NOCELL
NOCELL2  TITLE 'DATASET ANALYSIS SYSTEM.'
&VERSION SETC  '2.0.0'
&NAME    SETC  'NOCELL'
&MCDS    SETC  'HSM.MCDS'
&VMF     SETC  'SYS3.TLMS.VMF'
         SPACE ,
         AMODE 31                                                  @152
         PUNCH ' MODE AMODE(31),RMODE(24) '                        @160
         SPACE ,
         COPY  $$DOC                                               @151
         COPY  $CHANGES                                            @151
         EJECT ,
         TITLE 'MAINLINE ROUTINE.'                                 @164
         PRINT NOGEN,DATA
         $REGS ,
NOCELL   $PROLOG R12
         L     R11,=V(DATASECT)        ADDRESS OF DATA CSECT
         USING DATASECT,R11            SET ADDRESSIBILITY.
         L     R15,=V(ISPFINIT)                                    @SPF
         BALR  R14,R15                 ARE WE A DIALOG?            @SPF
         LM    R15,R1,=A(MESGRTN,0,2)                              @130
         BALR  R14,R15                 MESSAGE 2. PGM STARTING.    @130
RESTART  DS    0H
         TM    INTFLAG,UNDERSPF        DIALOG MODE?                @SPF
         BZ    PARMBTCH                NO, READ SYSIN              @SPF
         $CALL ISPFPARM                DISPLAY PANELS.             @SPF
         B     PARMDONE                                            @SPF
PARMBTCH DS    0H
         $CALL PARMS
PARMDONE DS    0H
         TM    INTFLAG,EOJPARMS        LAST REQUEST VALID?         @165
         BO    NOCELL99                NO. LET'S GO HOME.          @165
         $CALL SETUP                   HOUSEKEEPING
         TM    FLAGS1,DASD             DOES HE WANT DISK DATASET?
         BZ    LASTUCB                 NO, THEN DON'T DO THEM.
         L     R2,CVTPTR               GET CVT ADDRESS             @150
         USING CVTMAP,R2               ADDRESS TO CVT              @150
         XC    UCBWORK,UCBWORK         CLEAR WORK AREA             @150
         LA    R1,UCBWORK              GET ADDRESS OF WORK AREA    @150
         ST    R1,PARMWA               STORE ADDR IN PARMLIST      @150
         LA    R1,DEVCLASS             ADDR OF DEVICE CLASS        @150
         ST    R1,PARMDEVT             STORE ADDR IN PARMLIST      @150
         MVI   DEVCLASS,UCB3DACC       SEARCH FOR DASD UCBS ONLY   @150
         LA    R1,ADDRUCB              ADDR OF RETURNED UCB        @150
         ST    R1,PARMUCB              STORE UCB IN PARMLIST       @150
         OI    PARMUCB,X'80'           END OF PARMLIST             @150
         L     R1,CVTUCBSC             GET SCAN SERVICE ROUTINE    @150
         DROP  R2                                                  @150
         ST    R1,SCANSAVE             HOLD FOR LATER              @150
UCBLOOP  LA    R1,PARMLIST             PUT ADDR INTO REG 1         @150
         L     R15,SCANSAVE            GET SCAN SERVICE RTN        @150
         BALR  R14,R15                 GO TO SCAN SERVICE INTERFACE@150
         LTR   R15,R15                 WAS A UCB RETURNED?         @150
         BNZ   LASTUCB                 NO.                         @150
         L     R3,ADDRUCB              GET RETURNED UCB ADDR       @150
         USING UCBOB,R3
         TM    UCBSTAT,UCBONLI         IS IT ONLINE?
         BZ    UCBLOOP                 NO, KEEP GOING
         DROP  R3
         $CALL DOUCB                   PROCESS DASD VOLUME.        @150
         B     UCBLOOP                 CHECK IT OUT.
LASTUCB  TM    FLAGS1,TAPE             SHOULD WE DO TAPES?
         BZ    SKIPTAPE                IF NOT, THEN NOT
         $CALL PASSTMC                 CALL TAPE ROUTINE
SKIPTAPE DS    0H
         TM    FLAGS1,HSM              MIGRATED DATASETS WANTED?   @164
         BZ    SKIPMCDS                NO                          @164
         $CALL PASSMCDS                CALL MCDS ROUTINE           @164
SKIPMCDS DS    0H                                                  @164
         CLC   DSCBS,=F'0'             DID WE SELECT ANYTHING?     @151
         BNE   FNDSOME                 YES.                        @151
         MVC   RETCODE,=F'8'           ELSE SET RC=08              @151
         B     NOCELL95                AND DON'T PRINT.            @151
FNDSOME  L     R1,TABOFF               CURRENT TABLE POINTER.
         S     R1,=A(SAVELEN)          BACKUP ONE ELEMENT
         ST    R1,TABEND               LAST ELEMENT ACTUALLY USED.
         TM    FLAGS1,SORT             SHOULD WE SORT THE TABLE?
         BZ    SKIPSORT                HOW ODD!
         BAL   R10,SORTTAB             SORT DSCB TABLE.
SKIPSORT TM    FLAGS1,LIST             SHOULD WE LIST IT ALL?
         BZ    SKIPLIST                SAVE SOME TREES.
         $CALL LISTOUT                 PRODUCE LIST OUTPUT.
SKIPLIST TM    REPORTS,REPORT1         SUMMARY BY VOLUME WANTED?
         BZ    SKIPR3                  IF NOT, DON'T DO IT.        @151
         $CALL NOCELL1
SKIPR3   TM    REPORTS,REPORT4         SUMMARY BY HIGH LEVEL QUALIFIER?
         BZ    SKIPR4                  IF NOT, DON'T DO IT.
         $CALL NOCELL4
SKIPR4   DS    0H
         TM    REPORTS,REPORT2         REBLOCKING RECOMMENDATIONS? @163
         BZ    SKIPR5                  NO                          @163
         $CALL NOCELL2                                             @163
SKIPR5   DS    0H                                                  @163
NOCELL95 DS    0H                                                  @151
         L     R1,DSCBS                PICK UP DATASETS SELECTED.  @150
         CVD   R1,DTWORK               CONVERT TO PACKED.          @150
         EDIT  MAXMSG,DTL3,ZZZZZ9                                  @150
         LA    R2,MAXMSG                                           @150
         LM    R15,R1,=A(MESGRTN,0,26)                             @150
         BALR  R14,R15                 PRINT OUT TABLE CAPACITY.   @150
         XC    DSCBS,DSCBS             BACK TO ZERO FOR NEXT PASS. @162
         TM    INTFLAG,UNDERSPF        DIALOG MODE?                @SPF
         BZ    NOCELL97                NO                          @SPF
         $CALL ISPFREST
NOCELL97 DS    0H
         TM    INTFLAG,EODPARMS        MORE ON SYSIN?              @162
         BZ    RESTART                 IF SO, GO TO IT.            @162
NOCELL99 DS    0H                                                  @165
         LM    R15,R1,=A(MESGRTN,0,0)                              @150
         BALR  R14,R15                 MESSAGE 0. PGM ENDING.      @130
         L     R2,TABLEN               LENGTH OF TABLE
         LTR   R2,R2                   DID I GET ANY?
         BNP   NOCELL9A                NO.
         L     R3,TABSTART             STARTING ADDRESS TO FREE
         FREEMAIN  RU,LV=(2),A=(3)
NOCELL9A DS    0H
         L     R15,RETCODE             SET HIGHEST RETURN CODE.
         $EPILOG ,
         TITLE 'CALL THE QUICKERSORT ROUTINE.'
SORTTAB  L     R1,TABSTART             ADDR OF ARRAY TO SORT
         ST    R1,QPARM1               SAVED IN 1ST WORD OF PARMLIST
         LA    R1,DSCBS                COUNT OF ELEMENTS TO SORT
         ST    R1,QPARM2               SAVED IN 2ND WORD OF PARMLIST
         LA    R1,QPARM                PLIST FOR SORT ROUTINE.
         SPACE ,
         TM    FLAGS4,VOLSORT          SORT WITHIN VOLSER?         @161
         BZ    SORTTAB3                NO                          @161
         $CALL SORT3
         BR    R10
         SPACE ,
SORTTAB3 TM    FLAGS3,BYSIZE           SORT BY TRACKS ALLOCATED.   @151
         BZ    SORTTAB1                IF NOT, THEN BY DSNAME.     @151
         $CALL SORT2
         BR    R10
SORTTAB1 DS    0H
         $CALL SORT1
         BR    R10                     RETURN.
QPARM    DS    0F                      QUIKERSORT PARAMETER LIST
QPARM1   DC    AL4(0)                  ARRAY START ADDRESS
QPARM2   DC    AL4(0)                  NUMBER OF ELEMENTS TO
*                                  QUIKERSORT.
         SPACE ,
PARMLIST DS    3F                      PARMLIST MAPPING FOR UCB    @150
         ORG   PARMLIST                                            @150
PARMWA   DS    F                       ADDR OF 100-BYTE WORK AREA  @150
PARMDEVT DS    F                       ADDR OF DEV TYPE TO SCAN FOR@150
PARMUCB  DS    F                       ADDR OF RETURNED UCB        @150
         SPACE ,                                                   @150
ADDRUCB  DS    F                       RETURNED UCB ADDR           @150
SCANSAVE DS    F                       ADDR OF SCAN SERVICE ROUTINE@150
DEVCLASS DS    CL1                     DEVICE CLASS TO SEARCH FOR  @150
XASWITCH DC    X'00'                   INITIALIZE SWITCH           @150
         DS    0D                      FOR ALIGNMENT               @150
UCBWORK  DS    CL100                   UCB WORK AREA               @150
         LTORG ,
         COPY  SETUP
         COPY  ISPFINIT
         COPY  ISPFPARM
         COPY  PARMS                                               @130
         COPY  OPTIONS                                             @162
         COPY  DOUCB
         COPY  NOCELL1
         COPY  NOCELL2                                             @163
         COPY  NOCELL4
         COPY  LISTOUT                                             @162
         COPY  ISPFLIST
         COPY  HEXVMF                                              @160
         COPY  UNITNAME                                            @162
         COPY  PASSTMC                                             @TEC
         COPY  PASSVMF
         COPY  VMFBASE
         COPY  VMFMDS
         COPY  PASSMCDS                                            @164
         COPY  GENCARDS                                            @165
         COPY  MESGRTN                                             @130
         COPY  ISPFREST
         TITLE 'QKSRT - QUICKSORT ROUTINE - BY DATASET NAME.'
         QKSRT1  SORT1,                                                X
               C,LENGTH=133,                                           X
               STRTKY=7,                                           @161X
               KYLGTH=44,ORDER=A
         QKSRT1  SORT2,                                            @151X
               C,LENGTH=133,                                       @151X
               OVRTYP=F,                                           @151X
               DISP=69,                                                X
               ORDER=D
         SPACE ,
         QKSRT1  SORT3,                                            @161X
               C,LENGTH=133,                                       @161X
               STRTKY=1,                                           @161X
               KYLGTH=50,ORDER=A                                   @161
         SPACE ,
         COPY  VTOC                                                @130
         COPY  PRINT                                               @130
         SPACE
         CVT   DSECT=YES
UCB      DSECT
         IEFUCBOB  ,
FORMAT1  DSECT
         IECSDSL1  1
FORMAT3  DSECT
         IECSDSL1  3
         IEFZB4D0  ,
         IEFZB4D2  ,
         EJECT
         TITLE 'FORMAT OF SAVED DATASET INFORMATION TABLE.'
SAVEFMT  DSECT
SV1DSSN  DS    CL6                     DATA SET SERIAL NUMBER      @161
SV1DSNAM DS    CL44                    DSNAME                      @161
SV1FVSN  DS    CL6                     FROM VOLSER, IF MIGRATED
SV1CREDT DS    XL3                     CREATION DATE
SV1EXPDT DS    XL3                     EXPIRATION DATE
SV1NOEPV DS    X                       NUMBER OF EXTENTS (DASD)    @152
*                                  FILE SEQUENCE # (TAPE)          @152
SV1REFD  DS    XL3                     LAST DATE REFERENCED - SU 60
SV1DSORG DS    XL2                     DATA SET ORGANIZATION
SV1EXT1  DS    XL10                    FIRST EXTENT DESCRIPTION
SV1RECFM DS    CL3                     RECORD FORMAT
SV1BLKL  DS    XL2                     BLOCKSIZE
SV1LRECL DS    XL2                     RECORD LENGTH
SV1TKCYL DS    XL1                     TRACKS PER CYLINDER
SV1LSTAR DS    XL3                     DS1LSTAR                    @151
SV1DEVC  DS    X                       T=TAPE,D=DASD
SV1CTLG  DS    CL1                     C=CTLG,                     @130
*                                       U=NOT CATALOGUED,          @130
*                                       M=MIS-CATALOGUED.          @130
         SPACE ,
SV1CJOB  DS    CL8                     JOBNAME CREATING THIS DATASE@160
SV1STEP  DS    CL8                     STEP CREATING THIS DATASET. @160
SV1ACCT  DS    CL6                     ACCT CODE DS BELONGS TO.    @160
SV1KEEP  DS    XL3                     TLMSII KEEP DATE.           @160
SV1USES  DS    XL2                     USES SINCE CLEANED (TAPE).  @160
SV1TOUSE DS    XL2                     TOTAL USES         (TAPE).  @160
SV1TMPER DS    XL2                     TOTAL TEMP ERROR.  (TAPE).  @160
SV1LOC   DS    CL2                     VOLUME LOCATION.   (TAPE).  @160
SV1PTRDS DS    XL5                     CCHHR OF FORMAT3.           @160
         SPACE ,
SV1DEVT  DS    XL4                     DASD DEVICE TYPE. FOR NOCELL@163
         SPACE ,
SAVELEN  EQU   *-SAVEFMT
SV1EXT2  DS    XL10                    SECOND EXTENT DESCRIPTION
SV1EXT3  DS    XL10                    THIRD EXTENT DESCRIPTION
SV3EXT4  DS    XL10                    EXTENT  4 DESCRIPTION
SV3EXT5  DS    XL10                    5
SV3EXT6  DS    XL10                    6
SV3EXT7  DS    XL10                    7
SV3EXT8  DS    XL10                    EXTENT  8 DESCRIPTION
SV3EXT9  DS    XL10                    9
SV3EXT10 DS    XL10                    10
SV3EXT11 DS    XL10                    11
SV3EXT12 DS    XL10                    12
SV3EXT13 DS    XL10                    13
SV3EXT14 DS    XL10                    14
SV3EXT15 DS    XL10                    15
SV3EXT16 DS    XL10                    16
SV3EXT17 DS    XL10                                                @132
SV3EXT18 DS    XL10                                                @132
SV3EXT19 DS    XL10                                                @132
SV3EXT20 DS    XL10                                                @132
SV3EXT21 DS    XL10                                                @132
SV3EXT22 DS    XL10                                                @132
SV3EXT23 DS    XL10                                                @132
SV3EXT24 DS    XL10                                                @132
SV3EXT25 DS    XL10                                                @132
SV3EXT26 DS    XL10                                                @132
SV3EXT27 DS    XL10                                                @132
SV3EXT28 DS    XL10                                                @132
SV3EXT29 DS    XL10                                                @132
SV3EXT30 DS    XL10                                                @132
SV3EXT31 DS    XL10                                                @132
SV3EXT32 DS    XL10                                                @132
SV3EXT33 DS    XL10                                                @132
SV3EXT34 DS    XL10                                                @132
SV3EXT35 DS    XL10                                                @132
SV3EXT36 DS    XL10                                                @132
SV3EXT37 DS    XL10                                                @132
SV3EXT38 DS    XL10                                                @132
SV3EXT39 DS    XL10                                                @132
SV3EXT40 DS    XL10                                                @132
SV3EXT41 DS    XL10                                                @132
SV3EXT42 DS    XL10                                                @132
SV3EXT43 DS    XL10                                                @132
SV3EXT44 DS    XL10                                                @132
SV3EXT45 DS    XL10                                                @132
SV3EXT46 DS    XL10                                                @132
SV3EXT47 DS    XL10                                                @132
SV3EXT48 DS    XL10                                                @132
SV3EXT49 DS    XL10                                                @132
SV3EXT50 DS    XL10                                                @132
SV3EXT51 DS    XL10                                                @132
SV3EXT52 DS    XL10                                                @132
SV3EXT53 DS    XL10                                                @132
SV3EXT54 DS    XL10                                                @132
SV3EXT55 DS    XL10                                                @132
SV3EXT56 DS    XL10                                                @132
SV3EXT57 DS    XL10                                                @132
SV3EXT58 DS    XL10                                                @132
SV3EXT59 DS    XL10                                                @132
SV3EXT60 DS    XL10                                                @132
SV3EXT61 DS    XL10                                                @132
SV3EXT62 DS    XL10                                                @132
SV3EXT63 DS    XL10                                                @132
SV3EXT64 DS    XL10                                                @132
SV3EXT65 DS    XL10                                                @132
SV3EXT66 DS    XL10                                                @132
SV3EXT67 DS    XL10                                                @132
SV3EXT68 DS    XL10                                                @132
SV3EXT69 DS    XL10                                                @132
SV3EXT70 DS    XL10                                                @132
SV3EXT71 DS    XL10                                                @132
SV3EXT72 DS    XL10                                                @132
SV3EXT73 DS    XL10                                                @132
SV3EXT74 DS    XL10                                                @132
SV3EXT75 DS    XL10                                                @132
SV3EXT76 DS    XL10                                                @132
SV3EXT77 DS    XL10                                                @132
SV3EXT78 DS    XL10                                                @132
SV3EXT79 DS    XL10                                                @132
SV3EXT80 DS    XL10                                                @132
SV3EXT81 DS    XL10                                                @132
SV3EXT82 DS    XL10                                                @132
SV3EXT83 DS    XL10                                                @132
SV3EXT84 DS    XL10                                                @132
SV3EXT85 DS    XL10                                                @132
SV3EXT86 DS    XL10                                                @132
SV3EXT87 DS    XL10                                                @132
SV3EXT88 DS    XL10                                                @132
SV3EXT89 DS    XL10                                                @132
SV3EXT90 DS    XL10                                                @132
SV3EXT91 DS    XL10                                                @132
SV3EXT92 DS    XL10                                                @132
SV3EXT93 DS    XL10                                                @132
SV3EXT94 DS    XL10                                                @132
SV3EXT95 DS    XL10                                                @132
SV3EXT96 DS    XL10                                                @132
SV3EXT97 DS    XL10                                                @132
SV3EXT98 DS    XL10                                                @132
SV3EXT99 DS    XL10                                                @132
SV3EXTA0 DS    XL10                                                @132
SV3EXTA1 DS    XL10                                                @132
SV3EXTA2 DS    XL10                                                @132
SV3EXTA3 DS    XL10                                                @132
SV3EXTA4 DS    XL10                                                @132
SV3EXTA5 DS    XL10                                                @132
SV3EXTA6 DS    XL10                                                @132
SV3EXTA7 DS    XL10                                                @132
SV3EXTA8 DS    XL10                                                @132
SV3EXTA9 DS    XL10                                                @132
SV3EXTB0 DS    XL10                                                @132
SV3EXTB1 DS    XL10                                                @132
SV3EXTB2 DS    XL10                                                @132
SV3EXTB3 DS    XL10                                                @132
SV3EXTB4 DS    XL10                                                @132
SV3EXTB5 DS    XL10                                                @132
SV3EXTB6 DS    XL10                                                @132
SV3EXTB7 DS    XL10                                                @132
SV3EXTB8 DS    XL10                                                @132
SV3EXTB9 DS    XL10                                                @132
SV3EXTC0 DS    XL10                                                @132
SV3EXTC1 DS    XL10                                                @132
SV3EXTC2 DS    XL10                                                @132
SV3EXTC3 DS    XL10                                                @132
         COPY  DATASECT
         END
         PUNCH ' IDENTIFY DATASECT(''@166 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY DOUCB(''@V2  &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY GENCARDS(''@165 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY HEXVMF(''@160 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY ISPFINIT(''@V2  &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY ISPFLIST(''@V2  &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY ISPFPARM(''@V2  &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY LISTOUT(''@170 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY MESGRTN(''@162 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY MESSAGES(''@162 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY NOCELL(''@170 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY NOCELL1(''@165 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY NOCELL2(''@170 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY NOCELL4(''@170 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY OPTIONS(''@165 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY PARMS(''@166 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY PASSMCDS(''@170 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY PASSTMC(''@TEC &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY PASSVMF(''@170 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY SETUP(''@V2  &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY SORT1(''@161 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY SORT2(''@161 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY SORT3(''@161 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY UNITNAME(''@165 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY XDATEDIT(''BASE &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY XPRNTSUB(''@162 &SYSDATE &SYSTIME'') '
         PUNCH ' IDENTIFY XVTCREAD(''@161 &SYSDATE &SYSTIME'') '
         PUNCH ' NAME NOCELL2(R) '
         END
./       ADD   NAME=NOCELL1
         TITLE '1. REPORT BY VOLUME.'
         $REGS ,
NOCELL1  $PROLOG R12
         L     R11,=V(DATASECT)    ADDRESS OF COMMON DATA AREA.
         USING DATASECT,R11        USE DATASECT AS A DSECT.
         USING SAVEFMT,R3          DSINFO TABLE
         USING N1VOLTAB,R7         VOLUME TABLE DSECT
         LM    R3,R5,TABBXLE       LOOP THROUGH SAVEFMT
         LM    R7,R9,N1BXLE        SETUP FOR VOLTAB LOOP.
N1LOOP1  CLI   SV1DEVC,C'D'        IS IT DASD?
         BNE   N1NEXTDS            NO, MUST BE TAPE.
         CLC   N1VOLSER,=CL6' '    NOTHING FOR THIS VOL YET?       @141
         BNE   N1LOOP1A            NO.                             @141
         MVC   N1VOLSER,SV1DSSN    1ST TIME THIS VOLSER.           @141
         B     N1VOLFND            SO GO DO IT.                    @141
N1LOOP1A CLC   SV1DSSN,N1VOLSER    FIND VOLTAB ENTRY FOR THIS DATASET
         BE    N1VOLFND            THIS IS IT
         CLC   SV1FVSN,N1VOLSER    FIND VOLTAB ENTRY FOR THIS DATASET
         BE    N1VOLFND            THIS IS IT
         BXLE  R7,R8,N1LOOP1       LOOP THROUGH VOLTAB
         B     N1VOLFND            THIS IT IT.
N1NEXTDS LM    R7,R9,N1BXLE        RESTART WITH NEXT DATASET.
         BXLE  R3,R4,N1LOOP1       LOOP THROUGH SAVEFMT.
         B     N1PRINT             THAT'S ALL, PRINT IT.
N1VOLFND ICM   R6,15,N1TDSCNT      GET TOTAL DATASET COUNT.
         LA    R6,1(,R6)           PLUS 1.
         STCM  R6,15,N1TDSCNT      AND SAVE IT.
         ICM   R6,15,N1TTRCNT      GET TOTAL TRACKS COUNT.
         A     R6,SV1EXT1          PLUS TRACKS ALLOCATED.
         STCM  R6,15,N1TTRCNT      AND SAVE IT.
         CLC   SV1DSSN(5),=C'MGRAT'   WAS IT MIGRATED              @165
         BE    N1MIGRAT            YES, UPDATE MIGRATED TOTALS.
         ICM   R6,15,N1PDSCNT      GET ONLINE DATASET COUNT.
         LA    R6,1(,R6)           PLUS 1.
         STCM  R6,15,N1PDSCNT      AND SAVE IT.
         ICM   R6,15,N1PTRCNT      GET ONLINE TRACKS COUNT
         A     R6,SV1EXT1          PLUS TRACKS ALLOCATED.
         STCM  R6,15,N1PTRCNT      AND SAVE IT.
         B     N1NEXTDS            THAT'S IT. DO NEXT DS.
N1MIGRAT ICM   R6,15,N1MDSCNT      GET MIGRATED DATASET COUNT.
         LA    R6,1(,R6)           PLUS 1.
         STCM  R6,15,N1MDSCNT      AND SAVE IT.
         ICM   R6,15,N1MTRCNT      GET MIGRATED TRACKS COUNT
         A     R6,SV1EXT1          PLUS TRACKS ALLOCATED.
         STCM  R6,15,N1MTRCNT      AND SAVE IT.
         B     N1NEXTDS            THAT'S IT. DO NEXT DS.
N1PRINT  ALLOC SYSOUT=CLASS,                                           X
               DDN=LOCREP1,                                            X
               ERROR=N1NOALC       ALLOCATE A PRINT FILE.
         B     N1OPEN                                              @150
N1NOALC  DS    0H                                                  @150
         LA    R2,=CL8'REPORT1'    ADDR OF DDNAME FOR MESSAGE.     @150
         LM    R15,R1,=A(MESGRTN,0,21)                             @150
         BALR  R14,R15             SAY, DYNALLOC FAILED.           @150
         LM    R15,R1,=A(MESGRTN,0,22)                             @150
         BALR  R14,R15             SAY, TRYING PLAN B              @150
N1OPEN   DS    0H
         L     R2,PAGELEN          PICK UP PAGE LENGTH             @TEC
         XPROPEN PW1,                                                  X
               PAGELEN=(2),                                        @TECX
               DDNAME=REPORT1      AND OPEN IT.
         LTR   R15,R15             SO, DID IT OPEN?                @150
         BNZ   N1EXIT              OH SHIT!                        @150
         XPREJECT  PW1             SKIP TO A NEW PAGE.
         XPRHEAD PW1,                                                  X
               LIST=(N1HEAD1,                                          X
               N1HEAD2,N1HEAD3)    AND DEFINE THE HEADINGS.
         LM    R7,R9,N1BXLE        PRINT OUT VOLUME TABLE.
N1LOOP2  CLI   N1VOLSER,C' '       END OF VOLTAB?                  @141
         BE    N1ETAB              YES. SO STOP PRINTING.          @141
         ICM   R6,15,N1PDSCNT      GET ONLINE DATASET COUNT
         CVD   R6,N1WORK           CONVERT TO PACKED.
         EDIT  N1R1OD,N1WL3,ZZZZZ  FORMAT NICELY.
         ICM   R6,15,N1MDSCNT      GET MIGRATED DATASET COUNT.
         CVD   R6,N1WORK           CONVERT TO PACKED.
         EDIT  N1R1MD,N1WL3,ZZZZZ  FORMAT NICELY.
         ICM   R6,15,N1TDSCNT      GET TOTAL DATASET COUNT.
         CVD   R6,N1WORK           CONVERT TO PACKED.
         EDIT  N1R1TD,N1WL3,ZZZZZ  FORMAT NICELY.
         ICM   R6,15,N1PTRCNT      GET ONLINE TRACKS ALLOCATED
         CVD   R6,N1WORK           CONVERT TO PACKED.
         EDIT  N1R1OT,N1WL3,ZZZZZ  FORMAT NICELY.
         ICM   R6,15,N1MTRCNT      GET MIGRATED TRACKS ALLOCATED
         CVD   R6,N1WORK           CONVERT TO PACKED.
         EDIT  N1R1MT,N1WL3,ZZZZZ  FORMAT NICELY.
         ICM   R6,15,N1TTRCNT      GET TOTAL TRACKS ALLOCATED
         CVD   R6,N1WORK           CONVERT TO PACKED.
         EDIT  N1R1TT,N1WL3,ZZZZZ  FORMAT NICELY.
         MVC   N1R1VOL,N1VOLSER    SETUP VOLUME SERIAL.
         XPRNTLIN  PW1,                                                X
               TEXT=N1LINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1         PRINT 1 LINE PER VOLUME.
         BXLE  R7,R8,N1LOOP2       LOOP THROUGH ALL VOLUMES.
N1ETAB   DS    0H                                                  @141
         XPRCLOSE  PW1             CLOSE THE PRINT FILE.
         UNDERTSO YES=N1EXIT       DON'T FREE IN FOREGROUND.       @163
         FREE  UNALC,                                                  X
               DDN=LOCREP1         AND FREE IT.
N1EXIT   $EPILOG
         DROP  R3
         DROP  R7
         LTORG ,
         TITLE '1. LOCAL DATA AREAS.'
         DS    0F
LOCREP1  DC    AL4(DDNREP1)
         DC    Y(L'DDNREP1)
DDNREP1  DC    CL8'REPORT1'        DDNAME FOR REPORT LISTING.
N1BXLE   DC    A(N1VOLTAB),A(N1VLEN),A(N1VOLEND)
N1WORK   DC    D'0'                WORK FIELD FOR CVD INSTR
N1WL3    EQU   N1WORK+5,3          3 BYTES FOR EDIT MACRO
N1WL2    EQU   N1WORK+6,2          2 BYTES FOR EDIT MACRO
PW1      XPRDCB  DDNAME=REPORT1    DCB AND WORKAREAS FOR REPORT FILE.
N1HEAD1  XPRLDEF TEXT=N1H1TX,      HEADING TEXT.                       X
               LENGTH=L'N1H1TX,    LENGTH OF HEADING TEXT              X
               SPB=(0,ATHOF),      PRINTED ON LINE 1 OF NEW PAGE       X
               SPA=2               AND THEN SKIP 2 LINES.
N1H1TX   DC    C'&NAME &VERSION -- SUMMARY BY VOLUME '
N1HEAD2  XPRLDEF TEXT=N1H2TX,LENGTH=132,SPA=1
N1H2TX   DS    0CL133
         DC    CL49' '
         DC    C'---------DATASETS---------'
         DC    CL4' '
         DC    C'------TRACKS ALLOCATED-----'
         DC    CL26' '
N1HEAD3  XPRLDEF TEXT=N1H3TX,LENGTH=132,SPA=2
N1H3TX   DS    0CL133
         DC    CL20' '
         DC    C'VOLUME'
         DC    CL23' '
         DC    C'ONLINE'
         DC    CL3' '
         DC    C'MIGRATED'
         DC    CL4' '
         DC    C'TOTAL'
         DC    CL4' '
         DC    C'ONLINE'
         DC    CL3' '
         DC    C'MIGRATED'
         DC    CL4' '
         DC    C'TOTAL'
         DC    CL26' '
N1LINE1  DS    0CL133
         DC    CL1' '
         DC    CL20' '
N1R1VOL  DC    CL6' '
         DC    CL24' '
N1R1OD   DC    CL5' '
         DC    CL5' '
N1R1MD   DC    CL5' '
         DC    CL5' '
N1R1TD   DC    CL5' '
         DC    CL5' '
N1R1OT   DC    CL5' '
         DC    CL5' '
N1R1MT   DC    CL5' '
         DC    CL5' '
N1R1TT   DC    CL5' '
         DC    CL26' '
N1VOLTAB DS    0D
N1VOLSER DC    CL6'      '                                         @141
         DS    0F
N1PDSCNT DC    AL4(0)              COUNT OF DATASETS NOW ON THIS VOLUME
N1MDSCNT DC    AL4(0)              COUNT OF DATASETS ONCE ON THIS
*                                  VOLUME
N1TDSCNT DC    AL4(0)              COUNT OF DATASETS EVER ON THIS
*                                  VOLUME
N1PTRCNT DC    AL4(0)              COUNT OF TRACKS NOW ON THIS VOLUME
N1MTRCNT DC    AL4(0)              COUNT OF TRACKS ONCE ON THIS VOLUME
N1TTRCNT DC    AL4(0)              COUNT OF TRACKS EVER ON THIS VOLUME
N1VLEN   EQU   *-N1VOLTAB
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
         DC    CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0',CL6' ',6F'0' @141
N1VOLEND DC    CL6'      '                                         @141
         DS    0F
         DC    AL4(0),AL4(0),AL4(0),AL4(0),AL4(0),AL4(0)
./       ADD   NAME=NOCELL2
         TITLE '2. REBLOCKING RECOMMENDATION REPORT.'
         $REGS ,
NOCELL2  $PROLOG R12
         L     R11,=V(DATASECT)    ADDRESS OF COMMON DATA AREA.
         USING DATASECT,R11        USE DATASECT AS A DSECT.
         USING SAVEFMT,R3          DSINFO TABLE
         XC    N2TSVBLK,N2TSVBLK                                   @163
         XC    N2TSVTRK,N2TSVTRK                                   @163
         BAL   R10,N2SETUP         ALLOCATE PRINT FILE.            @163
         LM    R3,R5,TABBXLE       LOOP THROUGH SAVEFMT
N2LOOP1  CLI   SV1DEVC,C'D'        IS IT DASD?
         BE    N2FOUND1            YES.                            @163
N2LOOP1A BXLE  R3,R4,N2LOOP1       TRY ALL SELECTED DATASETS.      @163
         B     N2EOJ               THAT'S ALL, FOLKS....           @163
N2FOUND1 DS    0H                                                  @163
         TM    SV1DSORG,X'40'      IS IT DSORG=PS?                 @163
         BNO   N2LOOP1A            NO SKIP IT.                     @163
         CLC   SV1LSTAR,=XL3'00'   IS IT EMPTY?                    @166
         BE    N2LOOP1A            CAN'T REBLOCK NOTHINGNESS.      @166
         CLC   SV1DSNAM(5),=C'SYS1.'  IS IT A SYSTEM DATASET?      @166
         BE    N2LOOP1A            LEAVE THEM.                     @166
         CLI   SV1RECFM,C'U'       U FORMAT BLOCKS?                @166
         BE    N2LOOP1A            YES. SKIP THEM.                 @166
         SR    R1,R1               CLEAR 3 BYTES.
         IC    R1,SV1DEVT+3        GET 4TH BYTE OF UCBTYPE FIELD
         LR    R9,R1               SAVE IT FOR LATER.              @163
         SLL   R1,1                HALF WORD INDEX.                @163
         LH    R1,DTAB1(R1)        GET HIGHEST BLOCK SIZE.         @163
         SR    R2,R2                                               @163
         ICM   R2,3,SV1LRECL       GET CURRENT LRECL.              @163
         CR    R2,R1               IS LRECL > GOOD BLKSIZE?        @163
         BH    N2BIGREC            YES.                            @163
         LTR   R2,R2               IS LRECL=0?                     @163
         BP    N2OKRECL            NO                              @163
         SR    R2,R2                                               @163
         ICM   R2,3,SV1BLKL        ASSUME LRECL=BLKSIZE.           @163
         LTR   R2,R2               IS BLKSIZE=0?                   @163
         BZ    N2LOOP1A            I GIVE UP!                      @163
N2OKRECL DS    0H                                                  @163
         LTR   R2,R2               LRECL>0                         @165
         BNP   N2LOOP1A            NO 0C9'S, PLEASE.               @165
         LR    R0,R1                                               @163
         SRDA  R0,32               SETUP TO DIVIDE.                @163
         DR    R0,R2               R1=LRECLS IN A GOOD BLOCK.      @163
         LR    R0,R1                                               @163
         SRDA  R0,32               SETUP TO MULTIPLY.              @163
         MR    R0,R2               R1=GOOD BLKSIZE.                @163
         CLC   SV1RECFM(2),=C'VB'  VARIABLE BLOCKS?                @163
         BNE   *+8                 NO.                             @163
         LA    R1,4(0,R1)          ALLOW FOR RDW.                  @163
N2BIGREC DS    0H                                                  @163
         ST    R1,N2GOODBL         SAVE RECOMMENDED BLKSIZE.       @163
         CLM   R1,3,SV1BLKL        IS CURRENT BLOCKSIZE OK?        @166
         BE    N2LOOP1A            IF SO, LEAVE IT ALONE.          @166
         AH    R1,=H'12'                                           @163
         LR    R0,R1                                               @163
         SRDA  R0,32               SETUP TO DIVIDE.                @163
         LA    R2,32                                               @163
         DR    R0,R2               R1=D                            @163
         LA    R1,15(0,R1)                                         @163
         LR    R2,R1                                               @163
         LA    R0,1499                                             @163
         SRDA  R0,32                                               @163
         LTR   R2,R2                                               @165
         BNP   N2LOOP1A                                            @165
         DR    R0,R2               R1=GOOD BLOCKS PER TRACK.       @163
         ST    R1,N2GBLTRK                                         @163
         SR    R1,R1                                               @163
         ICM   R1,3,SV1BLKL        PICK UP CURRENT BLKSIZE.        @163
         AH    R1,=H'12'                                           @163
         LR    R0,R1                                               @163
         SRDA  R0,32               SETUP TO DIVIDE.                @163
         LA    R2,32                                               @163
         DR    R0,R2               R1=D                            @163
         LA    R1,15(0,R1)                                         @163
         LR    R2,R1                                               @163
         LA    R0,1499                                             @163
         SRDA  R0,32                                               @163
         LTR   R2,R2                                               @165
         BNP   N2LOOP1A                                            @165
         DR    R0,R2               R1=CURRENT BLOCKS PER TRACK.    @163
         ST    R1,N2OBLTRK         SAVE EXISTING BLKS/TRK          @163
         LR    R0,R1                                               @163
         SRDA  R0,32                                               @163
         ICM   R2,3,SV1LSTAR       PICK UP COUNT OF FULL TRACKS    @163
         MR    R0,R2               R1=CURRENT BLOCK COUNT.         @163
         SR    R2,R2                                               @163
         ICM   R2,1,SV1LSTAR+2     PICK UP BLOCK ON LAST TRACK     @166
         AR    R1,R2               AND ADD ON                      @166
         ST    R1,N2BLKCNT         SAVE BLOCK COUNT.               @163
         LR    R0,R1                                               @163
         SRDA  R0,32                                               @163
         SR    R2,R2                                               @163
         ICM   R2,3,SV1BLKL                                        @163
         MR    R0,R2                                               @163
         ICM   R2,15,N2GOODBL                                      @163
         LTR   R2,R2                                               @165
         BNP   N2LOOP1A                                            @165
         DR    R0,R2               R1=BLOCK COUNT OF GOOD BLOCKS.  @163
         LTR   R0,R0               ANY REMAINDER?                  @163
         BZ    *+8                 NO. NOTHING TO ROUND.           @163
         LA    R1,1(0,R1)          ROUND UP.                       @163
         ST    R1,N2GBLCNT                                         @163
         LR    R0,R1                                               @163
         SRDA  R0,32                                               @163
         L     R2,N2GBLTRK                                         @163
         LTR   R2,R2                                               @165
         BNP   N2LOOP1A                                            @165
         DR    R0,R2               R1=TRACKS NEEDED FOR GOOD BLKS. @163
         ST    R1,N2GBTRCT         SAVE IT.                        @163
         L     R0,N2GBLCNT                                         @163
         SRDA  R0,32                                               @163
         L     R2,N2GBLTRK         PICK UP GOOD BLOCKS/TRACK       @163
         LTR   R2,R2                                               @165
         BNP   N2LOOP1A                                            @165
         DR    R0,R2               GOOD BLOCK COUNT/COUNT PER TRK  @163
         LTR   R0,R0               ANY REMAINDER?                  @163
         BZ    N2NROUND            NO. NOTHING TO ROUND.           @163
         LA    R1,1(0,R1)          ONE PARTIALLY FULL TRACK.       @166
N2NROUND DS    0H                                                  @163
         ST    R1,N2GBTRCT         TRACK COUNT WITH GOOD BLOCKS.   @163
         L     R1,N2BLKCNT         PICK UP CURRENT BLOCK COUNT.    @163
         S     R1,N2GBLCNT         MINUS OPTIMIZED COUNT.          @163
         ST    R1,N2SAVBLK         IS AMOUNT SAVED, ONE HOPES.     @163
         A     R1,N2TSVBLK                                         @163
         ST    R1,N2TSVBLK         ACCUMULATE SAVED BLOCKS.        @163
         SR    R1,R1                                               @163
         ICM   R1,3,SV1LSTAR       PICK UP USED TRACKS.    .       @163
         LA    R1,1(0,R1)                                          @163
         S     R1,N2GBTRCT         MINUS OPTIMIZED COUNT.          @163
         ST    R1,N2SAVTRK         IS TRACKS SAVED.                @163
         LTR   R1,R1               DID WE SAVE ANY TRACKS?         @166
         BNZ   *+18                YES.                            @166
         CLC   N2SAVBLK,=F'0'      WILL THERE BE MORE BLOCKS?      @166
         BNL   *+8                 NO.                             @166
         B     N2LOOP1A            IF SAME TRKS AND MORE BLKS...   @166
         A     R1,N2TSVTRK                                         @163
         ST    R1,N2TSVTRK         ACCUMULATE SAVED TRACKS.        @163
         ICM   R1,15,SV1EXT1       PICK UP ALLOCATED TRACKS.       @163
         SR    R2,R2                                               @163
         ICM   R2,3,SV1LSTAR       PICK UP HIGH USED TT            @163
         LA    R2,1(0,R2)                                          @163
         SR    R1,R2               R1=UNUSED TRACKS.               @163
         ST    R1,N2UNUSED                                         @163
         L     R1,N2SAVBLK         PICK UP SAVED BLOCKS            @166
         A     R1,N2SAVTRK         ADD SAVED TRACKS                @166
         LTR   R1,R1               WERE THERE ANY OF EITHER?       @166
         BZ    N2LOOP1A            IF NOT, DON'T LIST IT.          @166
         L     R1,N2SAVBLK                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1ISV,                                            @163X
               N2WL3,                                              @163X
               -ZZ9                                                @163
         L     R1,N2SAVTRK                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1TSV,                                            @163X
               N2WL3,                                              @163X
               -ZZ9                                                @163
         L     R1,N2GBLCNT                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1NIO,                                            @163X
               N2WL3,                                              @163X
               ZZZ9                                                @163
         L     R1,N2GBTRCT                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1NTU,                                            @163X
               N2WL3,                                              @163X
               ZZZ9                                                @163
         L     R1,N2GBTRCT                                         @163
         A     R1,N2UNUSED                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1NTA,                                            @163X
               N2WL3,                                              @163X
               ZZZ9                                                @163
         L     R1,N2GOODBL                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1NBS,                                            @163X
               N2WL3,                                              @163X
               ZZZZ9                                               @163
         L     R1,N2BLKCNT                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1OIO,                                            @163X
               N2WL4,                                              @163X
               ZZZZZZ9                                             @163
         SR    R1,R1                                               @166
         ICM   R1,3,SV1LSTAR                                       @163
         LA    R1,1(0,R1)                                          @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1OTU,                                            @163X
               N2WL3,                                              @163X
               ZZZZ9                                               @163
         ICM   R1,15,SV1EXT1                                       @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1OTA,                                            @163X
               N2WL3,                                              @163X
               ZZZZ9                                               @163
         SR    R1,R1                                               @163
         ICM   R1,3,SV1BLKL                                        @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1OBS,                                            @163X
               N2WL3,                                              @163X
               ZZZZ9                                               @163
         SR    R1,R1                                               @163
         ICM   R1,3,SV1LRECL                                       @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R1RCL,                                            @163X
               N2WL3,                                              @163X
               ZZZZ9                                               @163
         MVC   N2R1RFM(3),SV1RECFM                                 @163
         MVC   N2R1DSN,SV1DSNAM                                    @163
         XPRNTLIN  PW2,                                                X
               TEXT=N2LINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1         PRINT 1 LINE PER VOLUME.
         B     N2LOOP1A                                            @163
         TITLE '2. ALLOCATE REPORT2 PRINT FILES.'
N2SETUP  ALLOC SYSOUT=CLASS,                                           X
               DDN=LOCREP2,                                            X
               ERROR=N2NOALC       ALLOCATE A PRINT FILE.
         B     N2OPEN                                              @150
N2NOALC  DS    0H                                                  @150
         LA    R2,=CL8'REPORT2'    ADDR OF DDNAME FOR MESSAGE.     @150
         LM    R15,R1,=A(MESGRTN,0,21)                             @150
         BALR  R14,R15             SAY, DYNALLOC FAILED.           @150
         LM    R15,R1,=A(MESGRTN,0,22)                             @150
         BALR  R14,R15             SAY, TRYING PLAN B              @150
N2OPEN   DS    0H
         L     R2,PAGELEN                                          @TEC
         XPROPEN PW2,                                                  X
               PAGELEN=(2),                                        @TECX
               DDNAME=REPORT2      AND OPEN IT.
         LTR   R15,R15             SO, DID IT OPEN?                @150
         BNZ   N2EXIT              OH SHIT!                        @150
         XPREJECT  PW2             SKIP TO A NEW PAGE.
         XPRHEAD PW2,                                                  X
               LIST=(N2HEAD1,                                          X
               N2HEAD2,N2HEAD3)    AND DEFINE THE HEADINGS.
         BR    R10                 GOBACK.                         @163
N2EOJ    DS    0H                                                  @141
         MVI   N2LINE1,C' '                                        @163
         MVC   N2LINE1+1(L'N2LINE1-1),N2LINE1                      @163
         MVC   N2R2TEXT+1,=CL25'TOTAL BLOCKS SAVED'                @170
         L     R1,N2TSVBLK                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R2NUMF,                                           @163X
               N2WL4,                                              @163X
               -ZZZZZ9                                             @163
         XPRNTLIN  PW2,                                                X
               TEXT=N2LINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=2,SPA=1         PRINT 1 LINE PER VOLUME.
         MVI   N2LINE1,C' '                                        @163
         MVC   N2LINE1+1(L'N2LINE1-1),N2LINE1                      @163
         MVC   N2R2TEXT+1,=CL25'TOTAL TRACKS SAVED'                @170
         L     R1,N2TSVTRK                                         @163
         CVD   R1,N2WORK                                           @163
         EDIT  N2R2NUMF,                                           @163X
               N2WL4,                                              @163X
               -ZZZZZ9                                             @163
         XPRNTLIN  PW2,                                                X
               TEXT=N2LINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1         PRINT 1 LINE PER VOLUME.
         XPRCLOSE  PW2             CLOSE THE PRINT FILE.
         UNDERTSO YES=N2EXIT                                       @163
         FREE  UNALC,DDN=LOCREP2   AND FREE IT.
N2EXIT   $EPILOG
         DROP  R3
         LTORG ,
         TITLE '1. LOCAL DATA AREAS.'
N2GOODBL DC    F'0'                RECOMMENDED BLKSIZE             @163
N2GBLTRK DC    F'0'                RECOMMENDED BLKS/TRK.           @163
N2OBLTRK DC    F'0'                CURRENT BLKS/TRK.               @163
N2BLKCNT DC    F'0'                CURRENT BLOCK COUNT.            @163
N2GBLCNT DC    F'0'                BLOCK REQUIRED AT BETTER BLKL   @163
N2GBTRCT DC    F'0'                TRACKS REQUIRED AT REC BLKL     @163
N2SAVBLK DC    F'0'                EXCPS SAVED AT REC BLKL         @163
N2SAVTRK DC    F'0'                TRACKS SAVED AT REC BLKL        @163
N2UNUSED DC    F'0'                UNUSED TRACKS W/IN DATASET.     @163
N2TSVBLK DC    F'0'                ACCUMULATOR - SAVED EXCPS.      @163
N2TSVTRK DC    F'0'                ACCUMULATOR - SAVED TRACKS.     @163
         DS    0F
LOCREP2  DC    AL4(DDNREP2)
         DC    Y(L'DDNREP2)
DDNREP2  DC    CL8'REPORT2'        DDNAME FOR REPORT LISTING.
N2WORK   DC    D'0'                WORK FIELD FOR CVD INSTR
N2WL4    EQU   N2WORK+4,4          4 BYTES FOR EDIT MACRO
N2WL3    EQU   N2WORK+5,3          3 BYTES FOR EDIT MACRO
N2WL2    EQU   N2WORK+6,2          2 BYTES FOR EDIT MACRO
PW2      XPRDCB  DDNAME=REPORT2    DCB AND WORKAREAS FOR REPORT FILE.
N2HEAD1  XPRLDEF TEXT=N2H1TX,      HEADING TEXT.                       X
               LENGTH=L'N2H1TX,    LENGTH OF HEADING TEXT              X
               SPB=(0,ATHOF),      PRINTED ON LINE 1 OF NEW PAGE       X
               SPA=2               AND THEN SKIP 2 LINES.
N2H1TX   DC    C'&NAME &VERSION -- REBLOCKING RECOMMENDATION REPORT.'
N2HEAD2  XPRLDEF TEXT=N2H2TX,LENGTH=132,SPA=1
N2H2TX   DS    0CL133
         DC    CL133' '                                            @163
         ORG   N2H2TX                                              @163
         DC    CL60' '                                             @163
         DC    CL29'------CURRENT ALLOCATION-----'                 @163
         DC    CL02' '                                             @163
         DC    CL29'---------RECOMMENDED---------'                 @163
         DC    CL02' '                                             @163
         DC    CL09'-SAVINGS-'                                     @163
         ORG   N2H2TX+133                                          @163
N2HEAD3  XPRLDEF TEXT=N2H3TX,LENGTH=132,SPA=2
N2H3TX   DS    0CL133
         DC    CL133' '                                            @163
         ORG   N2H3TX                                              @163
         DC    CL44'DATASET NAME:'                                 @163
         DC    CL02' '                                             @163
         DC    CL05'RECFM'                                         @163
         DC    CL02' '                                             @163
         DC    CL05'LRECL'                                         @163
         DC    CL02' '                                             @163
         DC    CL05'BLKSZ'                                         @163
         DC    CL02' '                                             @163
         DC    CL05'ALLOC'                                         @163
         DC    CL02' '                                             @163
         DC    CL05' USED'                                         @163
         DC    CL02' '                                             @163
         DC    CL08'   EXCPS'                                      @163
         DC    CL02' '                                             @163
         DC    CL05'BLKSZ'                                         @163
         DC    CL02' '                                             @163
         DC    CL05'ALLOC'                                         @163
         DC    CL02' '                                             @163
         DC    CL05' USED'                                         @163
         DC    CL02' '                                             @163
         DC    CL08'   EXCPS'                                      @163
         DC    CL02' '                                             @163
         DC    CL04'TRKS'                                          @163
         DC    CL02' '                                             @163
         DC    CL03'I/O'                                           @163
         ORG   N2H3TX+133                                          @163
N2LINE1  DS    0CL133
         DC    CL1' '
N2R1DSN  DC    CL44' '                                             @163
         DC    CL02' '                                             @163
N2R1RFM  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1RCL  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1OBS  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1OTA  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1OTU  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1OIO  DC    CL08' '                                             @163
         DC    CL02' '                                             @163
N2R1NBS  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1NTA  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1NTU  DC    CL05' '                                             @163
         DC    CL02' '                                             @163
N2R1NIO  DC    CL08' '                                             @163
         DC    CL02' '                                             @163
N2R1TSV  DC    CL04' '                                             @163
         DC    CL02' '                                             @163
N2R1ISV  DC    CL03' '                                             @163
         DC    CL1' '
         ORG   N2LINE1                                             @163
N2R2TEXT DC    CL25' '                                             @163
         DC    CL1' '                                              @163
N2R2NUMF DC    CL7' '                                              @163
         ORG   N2LINE1+133                                         @163
         DS    0H                                                  @163
DTAB1    DC    Y(0)                00
         DC    Y(0)                2311                     01
         DC    Y(0)                2301                     02
         DC    Y(0)                2303                     03
         DC    Y(00)               2302                     04
         DC    Y(0)                2321                     05
         DC    Y(0)                2305-1                   06
         DC    Y(0)                2305-2                   07
         DC    Y(0)                2314                     08
         DC    Y(00)               3330                     09
         DC    Y(00)               UNKNOWN AS OF 4/75       0A
         DC    Y(9442)             3350                     0B
         DC    Y(00)               3375                     0C   JCL
         DC    Y(00)               3330-1                   0D
         DC    Y(23476)            3380                     0E   CST
         DC    Y(27998)            3390                     0F     @339
./       ADD   NAME=NOCELL4
         TITLE '4. SUMMARY BY HIGH LEVEL QUALIFIER.'
         $REGS
NOCELL4  $PROLOG R12
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         USING SAVEFMT,R3
         ALLOC SYSOUT=CLASS,                                           X
               DDN=LOCREP4,                                            X
               ERROR=N4NODYN                                       @151
         B     N4OPEN                                              @151
N4NODYN  LA    R2,=CL8'REPORT4'    ADDR OF DDNAME FOR MESSAGE.     @151
         LM    R15,R1,=A(MESGRTN,0,21)                             @151
         BALR  R14,R15             SAY, DYNALLOC FAILED.           @151
         LM    R15,R1,=A(MESGRTN,0,22)                             @151
         BALR  R14,R15             SAY, TRYING PLAN B              @151
N4OPEN   DS    0H
         L     R2,PAGELEN          PICK UP PAGE LENGTH             @TEC
         XPROPEN PW4,                                              @151X
               PAGELEN=(2),                                        @TECX
               DDNAME=REPORT4
         XPREJECT  PW4
         XPRHEAD PW4,                                                  X
               LIST=(N4HEAD1,                                          X
               N4HEAD2,                                                X
               N4HEAD3,                                                X
               N4HEAD4)
         LM    R3,R5,TABBXLE       LOOP THROUGH SAVEFMT
         MVC   N4SAVEQ,=CL8' '
         TRT   SV1DSNAM(9),N4SCANTB  FIND A PERIOD?
         BC    7,N4SKIP1           BRANCH IF PERIOD FOUND
         LA    R1,8                MOVE 8 BYTES WITH EX
         B     N4SKIP2             GOTO EXECUTED MOVE
N4SKIP1  LA    R6,SV1DSNAM         ADDR OF START OF DSNAME.        @161
         SR    R1,R6               LENGTH OF QUALIFIER             @161
N4SKIP2  BCTR  R1,0                MINUS ONE FOR EX.
         EX    R1,N4MOVE1          SETUP N4SAVEQ
         ST    R1,N4QUALEN         SAVE QUALIFIER LENGTH
N4LOOP1  L     R1,N4QUALEN         GET QUALIFIER LENGTH
         EX    R1,N4COMP1          CHANGE OF QUALIFER YET?
         BNE   N4PRINT             YES PRINT IT.
         LA    R6,SV1DSNAM         PICK UP ADDRESS OF DSNAME FIELD @170
         AR    R6,R1               POINT PAST QUALIFIER            @170
         LA    R6,1(0,R6)          PLUS THE 1 TAKEN OFF FOR EX.    @170
         CLI   0(R6),C'.'          IS IT REALLY STILL THE SAME?    @170
         BNE   N4PRINT             NO. PRINT IT.                   @170
         EX    R1,N4MOVE2          SETUP N4QUAL
N4LOOP1X CLI   SV1DEVC,C'T'        IS IT TAPE?
         BE    N4UPDTAP            YES, UPDATE TAPE INFO.
         ICM   R6,15,N4TDSCNT      GET TOTAL DASD DATASETS
         LA    R6,1(,R6)           INCREMENT BY 1.
         STCM  R6,15,N4TDSCNT      AND SAVE IT.
         ICM   R6,15,N4TTRCNT      GET TOTAL DASD TRACKS
         A     R6,SV1EXT1          ADD TRACKS FOR THIS DATASET
         STCM  R6,15,N4TTRCNT      AND SAVE IT.
         CLC   SV1DSSN(5),=C'MGRAT'   WAS IT MIGRATED?             @165
         BE    N4UPDMIG            YES, UPDATE MIGRATED INFO
         ICM   R6,15,N4PDSCNT      GET ONLINE DASD DATASETS
         LA    R6,1(,R6)           INCREMENT BY 1.
         STCM  R6,15,N4PDSCNT      AND SAVE IT.
         ICM   R6,15,N4PTRCNT      GET ONLINE DASD TRACKS
         A     R6,SV1EXT1          ADD TRACKS FOR THIS DATASET
         STCM  R6,15,N4PTRCNT      AND SAVE IT.
         SR    R1,R1               CLEAR 3 BYTES.
         ICM   R1,3,SV1LSTAR       GET TT USED.
         LA    R1,1(,R1)           PLUS 1 FOR INDEX ORIGIN ZERO.
         C     R1,SV1EXT1          IS DS1LSTAR OK?
         BNH   N4ULSTAR            OK.
         L     R1,SV1EXT1          ASSUME FULL (??)
N4ULSTAR ICM   R6,15,N4PTRUSD      GET COUNT OF TRACKS USED.
         AR    R6,R1               ADD TRACKS USED FOR THIS DATASET
         STCM  R6,15,N4PTRUSD      AND SAVE IT.
         B     N4LOOP1Y            THAT'S ALL FOR THIS DATASET.
N4UPDMIG ICM   R6,15,N4MDSCNT      GET MIGRATED DASD DATASETS
         LA    R6,1(,R6)           INCREMENT BY 1.
         STCM  R6,15,N4MDSCNT      AND SAVE IT.
         ICM   R6,15,N4MTRCNT      GET MIGRATED DASD TRACKS
         A     R6,SV1EXT1          ADD TRACKS FOR THIS DATASET
         STCM  R6,15,N4MTRCNT      AND SAVE IT.
         B     N4LOOP1Y            THAT'S ALL FOR THIS DATASET.
N4UPDTAP ICM   R6,15,N4TAPEDS      GET TAPE DATASETS
         LA    R6,1(,R6)           INCREMENT BY 1.
         STCM  R6,15,N4TAPEDS      AND SAVE IT.
         ICM   R6,15,N4TAPEBK      GET TAPE BLOCKS.
         A     R6,SV1EXT1          ADD TRACKS FOR THIS DATASET
         STCM  R6,15,N4TAPEBK      AND SAVE IT.
         LM    R7,R9,N4BXLE        LIST OF TAPES SO FAR
N4LOOP2  CLC   0(6,R7),SV1DSSN     USED THIS TAPE ALREADY?
         BE    N4LOOP1Y            IF SO, DON'T COUNT IT AGAIN.
         CLC   0(6,R7),=C'      '  EMPTY SLOT?
         BNE   N4LOOP2Y            NO, KEEP GOING.
         MVC   0(6,R7),SV1DSSN     SAVE THIS VOLSER
         ICM   R6,15,N4TAPEVS      GET TAPE VOLUMES.
         LA    R6,1(,R6)           INCREMENT BY 1.
         STCM  R6,15,N4TAPEVS      AND SAVE IT.
         B     N4LOOP1Y            THEN QUIT.
N4LOOP2Y BXLE  R7,R8,N4LOOP2       TRY ABOUT 1K VOLSERS.
N4LOOP1Y BXLE  R3,R4,N4LOOP1       PROCESS ALL DATASETS.
*        B     N4EXIT1             THAT'S ALL, BAG IT.             @165
N4PRINT  SR    R7,R7
         ICM   R0,15,N4TAPEVS      GET NUMBER OF TAPES
         BZ    N4PRINT1            SAY 0 BLK/REEL IF NO TAPES
         ICM   R6,15,N4TAPEBK      GET NUMBER OF TAPE BLOCKS
         SRDA  R6,32               SETUP TO DIVIDE.
         DR    R6,R0               BLK/REEL IN R7
N4PRINT1 CVD   R7,N4WORK           GET IT IN DECIMAL
         EDIT  N4L1TPBR,                                               X
               N4WL3,ZZZZZ         FORMAT IT NICELY.
         ICM   R6,15,N4TAPEBK      GET TAPE BLOCKS
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1TPBK,                                               X
               N4WL4,ZZZZZZZ       FORMATTED NICELY.
         ICM   R6,15,N4TAPEVS      GET TAPE VOLUMES.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1TPVL,                                               X
               N4WL3,ZZZZZ         FORMATTED NICELY.
         ICM   R6,15,N4TAPEDS      GET TAPE DATASETS.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1TPDS,                                               X
               N4WL3,ZZZZZ         FORMATTED NICELY.
         SR    R7,R7               ZERO FOR USE IF NO TRKS ALLOCATED.
         ICM   R0,15,N4PTRCNT      GET ONLINE TRACKS ALLOCATED.
         BZ    N4PRINT2            SAY 0 IF NO TRACKS ALLOCATED.
         ICM   R6,15,N4PTRUSD      GET ONLINE TRACKS USED.
         SRDA  R6,32               SETUP FOR MULTIPLY.
         M     R6,=F'100'          TIMES 100.
         LR    R6,R7               R6 -> TRKS ALLOC*100
         SRDA  R6,32               SETUP TO DIVIDE.
         DR    R6,R0               PCT USED IN R7
N4PRINT2 CVD   R7,N4WORK           IN DECIMAL
         EDIT  N4L1DPCT,N4WL2,ZZ   FORMATTED NICELY.
         MVI   N4L1DPCT+2,C' '     CLEAR OUT A PRIOR '%'
         CLC   N4L1DPCT,=CL2' '    WAS THERE ANY PERCENT
         BE    N4PRINT3            THEN DON'T PRINT '  %' STUPID
         MVI   N4L1DPCT+2,C'%'     SETUP PERCENT SIGN.
N4PRINT3 ICM   R6,15,N4TTRCNT      GET TOTAL DASD TRACKS.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1DTTO,                                               X
               N4WL4,ZZZZZZ        FORMATTED NICELY.
         ICM   R6,15,N4MTRCNT      GET MIGRATED DASD TRACKS.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1DTMG,                                               X
               N4WL4,ZZZZZZ        FORMATTED NICELY.
         ICM   R6,15,N4PTRCNT      GET ONLINE DASD TRACKS.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1DTON,                                               X
               N4WL4,ZZZZZZ        FORMATTED NICELY.
         ICM   R6,15,N4TDSCNT      GET TOTAL DASD DATASETS.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1DDTO,                                               X
               N4WL3,ZZZZZ         FORMATTED NICELY.
         ICM   R6,15,N4MDSCNT      GET MIGRATED DASD DATASETS.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1DDMG,                                               X
               N4WL3,ZZZZZ         FORMATTED NICELY.
         ICM   R6,15,N4PDSCNT      GET ONLINE DASD DATASETS.
         CVD   R6,N4WORK           IN DECIMAL
         EDIT  N4L1DDON,                                               X
               N4WL3,ZZZZZ         FORMATTED NICELY.
         MVC   N4L1QUAL,=CL8' '
         L     R1,N4QUALEN         GET QUALIFIER LENGTH.
         EX    R1,N4MOVE3          MOVE QUALIFIER TO PRINT LINE.
         XPRNTLIN  PW4,                                                X
               TEXT=N4LINE1+1,                                         X
               LENGTH=132,                                             X
               SPB=0,SPA=1
         XC    N4PDSCNT,N4PDSCNT   ZERO DASD DS ONLINE THIS LEVEL
         XC    N4MDSCNT,N4MDSCNT   ZERO DASD DS MIGRATED THIS LEVEL
         XC    N4TDSCNT,N4TDSCNT   ZERO DASD DS TOTAL THIS LEVEL
         XC    N4PTRCNT,N4PTRCNT   ZERO DASD TRACKS ONLINE THIS LEVEL
         XC    N4PTRUSD,N4PTRUSD   ZERO DASD TRACKS USED.
         XC    N4MTRCNT,N4MTRCNT   ZERO DASD TRACKS MIGRATED
         XC    N4TTRCNT,N4TTRCNT   ZERO DASD TRACKS TOTAL THIS LEVEL
         XC    N4TAPEDS,N4TAPEDS   ZERO TAPE DATASETS THIS LEVEL
         XC    N4TAPEVS,N4TAPEVS   ZERO TAPE VOLUMES THIS LEVEL
         XC    N4TAPEBK,N4TAPEBK   ZERO TAPE BLOCKS THIS LEVEL
         MVC   N4QUAL,=C'        '  BLANK OUT QUALIFIER PRINT AREA.
         LM    R7,R9,N4BXLE        RELOAD LOOP REGISTERS.
N4LOOP3  MVC   0(6,R7),=C'      '
         BXLE  R7,R8,N4LOOP3
         MVC   N4QUAL,=CL8' '
         MVC   N4SAVEQ,=CL8' '
         TRT   SV1DSNAM(9),N4SCANTB  FIND A PERIOD?
         BC    7,N4SKIP3           BRANCH IF PERIOD FOUND
         LA    R1,8                MOVE 8 BYTES WITH EX
         B     N4SKIP4
N4SKIP3  LA    R6,SV1DSNAM         ADDR OF DSNAME.                 @161
         SR    R1,R6               LENGTH OF QUALIFIER             @161
N4SKIP4  BCTR  R1,0
         EX    R1,N4MOVE1          SETUP N4SAVEQ
         EX    R1,N4MOVE2          SETUP N4QUAL
         ST    R1,N4QUALEN         SAVE QUALIFIER LENGTH.
         CR    R3,R5               ANY MORE DATASETS?              @PKT
         BNH   N4LOOP1X            START NEXT QUALIFIER            @PKT
         B     N4EXIT1             IF NOT, GET OUT                 @PKT
*        BXLE  R3,R4,N4LOOP1X      START NEX QUALIFIER.            @165
N4EXIT1  XPRCLOSE  PW4
         UNDERTSO YES=N4EXIT                                       @163
         FREE  UNALC,DDN=LOCREP4
N4EXIT   $EPILOG
N4MOVE1  MVC   N4SAVEQ(0),SV1DSNAM
N4MOVE2  MVC   N4QUAL(0),SV1DSNAM
N4MOVE3  MVC   N4L1QUAL(0),N4QUAL
N4COMP1  CLC   N4SAVEQ(0),SV1DSNAM
         DROP  R3
         LTORG ,
         TITLE '4. LOCAL DATA AREAS.'
         DS    0F
N4QUALEN DC    AL4(0)              LENGTH OF CURRENT QUALIFER
LOCREP4  DC    AL4(DDNREP4),Y(L'DDNREP4)
DDNREP4  DC    CL8'REPORT4'
N4WORK   DC    D'0'                WORK FIELD FOR CVD INSTR
N4WL4    EQU   N4WORK+4,4          4 BYTES FOR EDIT MACRO
N4WL3    EQU   N4WORK+5,3          3 BYTES FOR EDIT MACRO
N4WL2    EQU   N4WORK+6,2          2 BYTES FOR EDIT MACRO
PW4      XPRDCB  DDNAME=REPORT4
N4HEAD1  XPRLDEF TEXT=N4H1TX,                                          X
               LENGTH=L'N4H1TX,                                        X
               SPB=(0,ATHOF),                                          X
               SPA=2
N4H1TX   DC    C'&NAME &VERSION -- SUMMARY BY HIGH LEVEL QUALIFIER.'
N4HEAD2  XPRLDEF TEXT=N4H2TX,                                          X
               LENGTH=132,                                             X
               SPA=1
N4H2TX   DS    0CL133
         DC    CL20' '
         DC    C'----------------------DASD------------------------'
         DC    CL7' '
         DC    C'--------------TAPE---------------'
         DC    CL22' '
N4HEAD3  XPRLDEF TEXT=N4H3TX,                                          X
               LENGTH=132,SPA=1
N4H3TX   DS    0CL133
         DC    CL20' '
         DC    C'-----DATASETS------'
         DC    CL3' '
         DC    C'--------TRACKS--------'
         DC    CL3' '
         DC    C'AVG'
         DC    CL33' '
         DC    C'BLOCKS/'
         DC    CL22' '
N4HEAD4  XPRLDEF TEXT=N4H4TX,                                          X
               LENGTH=132,SPA=2
N4H4TX   DS    0CL133
         DC    CL22' '
         DC    C'ONL    MIG    TOT'
         DC    CL6' '
         DC    C'ONL     MIG     TOT  UTIL'
         DC    CL7' '
         DC    C'DATASETS  REELS   BLOCKS     REEL'
         DC    CL22' '
N4LINE1  DS    0CL133
         DC    CL1' '
N4L1QUAL DC    CL8' ',CL12' '
N4L1DDON DC    CL5' ',CL2' '
N4L1DDMG DC    CL5' ',CL2' '
N4L1DDTO DC    CL5' ',CL3' '
N4L1DTON DC    CL6' ',CL2' '
N4L1DTMG DC    CL6' ',CL2' '
N4L1DTTO DC    CL6' ',CL3' '
N4L1DPCT DC    CL2' ',CL1'%',CL10' '
N4L1TPDS DC    CL5' ',CL2' '
N4L1TPVL DC    CL5' ',CL2' '
N4L1TPBK DC    CL7' ',CL4' '
N4L1TPBR DC    CL5' ',CL22' '
N4SAVEQ  DC    CL8' '              COMPARE FOR QUALFIER BREAK.
N4LEVTAB DS    0D
N4QUAL   DC    CL8' '
N4PDSCNT DC    AL4(0)              COUNT OF DASD DS ONLINE THIS LEVEL
N4MDSCNT DC    AL4(0)              COUNT OF DASD DS MIGRATED THIS LEVEL
N4TDSCNT DC    AL4(0)              COUNT OF DASD DS TOTAL THIS LEVEL
N4PTRCNT DC    AL4(0)              COUNT OF DASD TRACKS ONLINE THIS
*                                  LEVEL
N4PTRUSD DC    AL4(0)              ONLINE TRACKS USED.
N4MTRCNT DC    AL4(0)              COUNT OF DASD TRACKS MIGRATED
N4TTRCNT DC    AL4(0)              COUNT OF DASD TRACKS TOTAL THIS
*                                  LEVEL
N4TAPEDS DC    AL4(0)              COUNT OF TAPE DATASETS THIS LEVEL
N4TAPEVS DC    AL4(0)              COUNT OF TAPE VOLUMES THIS LEVEL
N4TAPEBK DC    AL4(0)              COUNT OF TAPE BLOCKS THIS LEVEL
N4SCANTB DS    0D
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'00000000000000000000000000000000'  0
         DC    X'00000000000000000000000000000000'  1
         DC    X'00000000000000000000000000000000'  2
         DC    X'00000000000000000000000000000000'  3
         DC    X'00000000000000000000000100000000'  4
         DC    X'00000000000000000000000000000000'  5
         DC    X'00000000000000000000000000000000'  6
         DC    X'00000000000000000000000000000000'  7
         DC    X'00000000000000000000000000000000'  8
         DC    X'00000000000000000000000000000000'  9
         DC    X'00000000000000000000000000000000'  A
         DC    X'00000000000000000000000000000000'  B
         DC    X'00000000000000000000000000000000'  C
         DC    X'00000000000000000000000000000000'  D
         DC    X'00000000000000000000000000000000'  E
         DC    X'00000000000000000000000000000000'  F
N4BXLE   DC    A(N4VOLTAB),A(N4VLEN),A(N4VOLEND)
         DS    0D
N4VOLTAB DC    CL6' '
N4VLEN   EQU   *-N4VOLTAB
         DC    2046CL6' '
N4VOLEND DC    CL6' '
./       ADD   NAME=NOCELL99
+-----------------%NOCELL+Dataset Analysis System ----------------------
%COMMAND ===>_ZCMD
+
+
+
+ The parameters you entered were such that%NOCELL+did not select any
+ datasets.
+ Please enter%END+to browse the SYSLIST output. This may assist you
+ in determining why nothing was selected.
+
+
+
)END
./       ADD   NAME=OPTIONS
         MACRO
         $OPT1 &F,&M
         TM    &F,&M
         BZ    *+14
         LA    R2,=CL10'&M'
         LM    R15,R1,=A(MESGRTN,0,3)
         BALR  R14,R15
         MEND
         MACRO
         $OPT2 &F,&M
&N       SETC  'NO'.'&M'
         TM    &F,&M
         BZ    *+12
         LA    R2,=CL10'&M'
         B     *+8
         LA    R2,=CL10'&N'
         LM    R15,R1,=A(MESGRTN,0,3)
         BALR  R14,R15
         MEND
         MACRO
&LAB    $OPT3  &TABLE,&VERB,&FLAG,&MASK,&LENGTH,&WORK=OPTNMWRK
&L       SETA  K'&VERB-2
&R1      SETC  'R9'
&R2      SETC  'R8'
&R3      SETC  'R1'
&AGAIN   SETC  'AGN'.'&SYSNDX'
&END     SETC  'END'.'&SYSNDX'
         TM    &FLAG,&MASK
         BZ    &END
         MVI   &WORK,C' '
         MVC   &WORK+1(L'&WORK-1),&WORK
         LA    &R1,&TABLE
         LA    &R2,&WORK
         MVC   0(&L,&R2),=C&VERB
         LA    &R2,&L.(0,&R2)
&AGAIN   SR    &R3,&R3
         IC    &R3,0(0,&R1)
         BCTR  &R3,0
         B     *+10
         MVC   0(0,&R2),1(&R1)
         EX    &R3,*-6
         LA    &R3,1(0,&R3)
         AR    &R2,&R3
         LA    &R1,&LENGTH.(0,&R1)
         MVI   0(&R2),C','
         CLI   0(&R1),255
         BNE   *+10
         MVC   0(2,&R2),=C').'
         LA    R2,&WORK
         LM    R15,R1,=A(MESGRTN,0,33)
         BALR  R14,R15
         MVI   &WORK,C' '
         MVC   &WORK+1(L'&WORK-1),&WORK
         CLI   0(&R1),255
         BE    &END
         LA    &R2,&WORK
         LA    &R2,&L.(0,&R2)
         B     &AGAIN
&END     DS    0H
         MEND
         TITLE 'PRINT OUT OPTIONS IN EFFECT REPORT.'
OPTIONS  $PROLOG R12
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         $OPT2 FLAGS1,TAPE
         $OPT2 FLAGS1,SORT
         $OPT2 FLAGS1,DASD         IS DASD IN EFFECT?
         $OPT2 FLAGS1,LIST         IS LIST IN EFFECT?
         $OPT2 FLAGS1,HSM          IS HSM IN EFFECT?
         $OPT3 LTAB,'LEVEL(',FLAGS1,LEVEL,9
         $OPT3 NLTAB,'NOLEVEL(',FLAGS1,NOLEVEL,9
         $OPT3 VTAB,'VOLUME(',FLAGS2,VOLUME,7
         $OPT3 NVTAB,'NOVOLUME(',FLAGS2,NOVOLUME,7
         $OPT1 FLAGS2,CAT          IS CAT IN EFFECT?
         $OPT1 FLAGS2,UNCAT        IS UNCAT IN EFFECT?
         $OPT1 FLAGS2,MISCAT       IS MISCAT IN EFFECT?
         $OPT1 FLAGS2,BRKLVL       EOF FOR EACH USER?
         $OPT3 UTAB,'UNUSED(',FLAGS3,UNUSED,4
         $OPT1 FLAGS3,EMPTY
         $OPT3 CTAB,'CONTAINING(',FLAGS3,CONTAIN,9
         $OPT3 ETAB,'ENDING(',FLAGS3,ENDING,9
         $OPT3 NCTAB,'NOTCONTAINING(',FLAGS3,NOTCONT,9
         $OPT3 NETAB,'NOTENDING(',FLAGS3,NOTEND,9
         $OPT1 FLAGS3,BYSIZE
         $OPT3 STAB,'LARGER(',FLAGS4,LARGER,4
         $OPT3 STAB,'SMALLER(',FLAGS4,SMALLER,4
         $OPT1 FLAGS4,ONEFILE
         $OPT3 DVTAB,'DUMPVMF(',FLAGS4,DUMPVMF,7
         $OPT1 FLAGS4,DATEPROT
         $OPT3 WITAB,'WITHIN(',FLAGS4,WITHIN,4
         $OPT1 FLAGS4,DNSTY
         $OPT1 FLAGS4,VOLSORT
         $OPT3 CREATJOB,'CREATEDBY(',FLAGS5,CREATEBY,9
         $OPT3 READJOB,'READBY(',FLAGS5,READBY,9
         $OPT3 DOTAB$,'DSORG(',FLAGS5,DSORG,4                      @165
         $OPT3 CRTAB,'CREATED(',FLAGS5,CREATED,4                   @165
         $OPT1 FLAGS5,CHANGED                                      @165
         $OPT1 REPORTS,REPORT1                                     @163
         $OPT1 REPORTS,REPORT2                                     @163
         $OPT1 REPORTS,REPORT4                                     @164
         $OPT3 PLTAB,'PAGELEN(',FLAGS6,PGL,2                       @TEC
         $EPILOG ,
         LTORG ,
OPTNMWRK DC    CL30' '
./       ADD   NAME=PARMS
         TITLE 'SCAN CONTROL CARDS.'
PARMS    $PROLOG R12,R10
        $AMODE 24                                                  @162
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         TM    PARMFLG,PARM1ST     HAVE I BEEN HERE BEFORE?        @162
         BZ    PARMS0              NO, SO LETS GET STARTED.        @162
         MVC   FLAGS(6),DFLTFLAG   RESET DEFAULT OPTIONS.          @166
         MVI   REPORTS,0           NO REPORTS BY DEFAULT.          @165
         NI    PARMFLG,255-PARMSANY NO VALID PARMS YET.            @165
         MVI   ETAB,255            CLEAR ENDING TABLE.             @162
         MVI   CTAB,255            CLEAR CONTAINING TABLE          @162
         MVI   NETAB,255           CLEAR NOT ENDING TABLE.         @162
         MVI   NCTAB,255           CLEAR NOT CONTAINING TABLE      @162
         MVI   LTAB,255            CLEAR LEVEL TABLE.              @162
         MVI   VTAB,255            CLEAR VOLUME TABLE.             @162
         MVI   NLTAB,255           CLEAR NOLEVEL TABLE.            @162
         MVI   NVTAB,255           CLEAR NOVOLUME TABLE.           @162
         MVI   DVTAB,255           CLEAR DUMPVMF TABLE.            @162
         MVI   WITAB,255           CLEAR WITHIN TABLE.             @162
         MVI   UTAB,255            CLEAR UNUSED TABLE.             @162
         MVI   STAB,255            CLEAR SIZE TABLE.               @162
         MVI   UNITS,255           CLEAR UNITNAME TABLE.           @162
         MVI   FIRSTQ,255          RESET BREAKLEVEL INTERNAL FLAG  @165
         LM    R2,R11,PARMREGS     AS YOU WERE.                    @162
         B     PARMS2              AND KEEP ON SCANNING.           @162
PARMS0   DS    0H                                                  @162
         OI    PARMFLG,PARM1ST     ONCE ONLY PLEASE.               @162
         OPEN  (SYSIN,INPUT)
         TM    SYSIN+48,X'10'      DID SYSIN DCB OPEN?
         BZ    PARM99              IF SYSIN NOT SUPPLIED, DEFAULT.
PARMS1   GET   SYSIN,CARD
         LA    R2,CARD             CARD IMAGE TO PRINT.            @130
         LM    R15,R1,=A(MESGRTN,0,1)                              @130
         BALR  R14,R15             MESSAGE 1. PRINT CARD IMAGE.    @130
         LA    R3,CARD             ADDRESS OF PARAMETER CARD.
         LA    R4,1                SCAN 1 BYTE AT A TIME.
         LR    R5,R3               START OF CARD
         LA    R5,72(,R5)          PLUS 72 BYTES IS SCAN LIMIT.
PARMS2   CLC   =C'ENDREQ.',0(R3)   END OF LOGICAL REQUEST?         @162
         BE    PARMSER             YES.                            @162
         CLC   =C'LIST.',0(R3)     FULL LISTING.
         BE    PARMS3              PROCESS SCANNED PARM VALUE.
         CLC   =C'NOLIST.',0(R3)   SUPPRESS FULL LISTING.
         BE    PARMS4              PROCESS SCANNED PARM VALUE.
         CLC   =C'LEVEL(',0(R3)    LEVEL IDENTIFIERS FOLLOW.
         BE    PARMS5              PROCESS SCANNED PARM VALUE.
         CLC   =C'NOLEVEL(',0(R3)  NOLEVEL IDENTIFIERS FOLLOW.
         BE    PARMS6              PROCESS SCANNED PARM VALUE.
         CLC   =C'VOLUME(',0(R3)   VOLSERS TO SELECT FROM.
         BE    PARMS7              PROCESS SCANNED PARM VALUE.
         CLC   =C'NOVOLUME(',0(R3)  VOLSER NOT TO SELECT FROM.
         BE    PARMS8              PROCESS SCANNED PARM VALUE.
         CLC   =C'HSM.',0(R3)      SELECT MIGRATED DATASET.
         BE    PARMS9              PROCESS SCANNED PARM VALUE.
         SPACE ,
         CLC   =C'HSM(',0(R3)      ONLY CERTAIN MIGRATED?          @165
         BE    PARMS9A             YES.                            @165
         SPACE ,
         CLC   =C'NOHSM.',0(R3)    DON'T SELECT MIGRATED DATASETS.
         BE    PARMSA              PROCESS SCANNED PARM VALUE.
         CLC   =C'TAPE.',0(R3)     SELECT TAPE DATASETS.
         BE    PARMSB              PROCESS SCANNED PARM VALUE.
         CLC   =C'NOTAPE.',0(R3)   DON'T SELECT TAPE DATASETS.
         BE    PARMSC              PROCESS SCANNED PARM VALUE.
         SPACE ,
         CLC   =C'SORTBYVOLUME.',0(R3)                             @161
         BE    PARMSSV             SAY SORT BY DSN W/IN VOLSER.    @161
         SPACE ,
         CLC   =C'SORT.',0(R3)     SORT THE DATASET INFO TABLE.
         BE    PARMSD              PROCESS SCANNED PARM VALUE.
         CLC   =C'NOSORT.',0(R3)   DON'T SORT THE DSINFO TABLE.
         BE    PARMSE              PROCESS SCANNED PARM VALUE.
         CLC   =C'DASD.',0(R3)     SELECT DASD DATASETS.
         BE    PARMSF              PROCESS SCANNED PARM VALUE.
         CLC   =C'NODASD.',0(R3)   DON'T SELECT DASD DATASETS.
         BE    PARMSG              PROCESS SCANNED PARM VALUE.
         CLC   =C'SYSOUT(',0(R3)   SYSOUT CLASS FOR REPORTS.
         BE    PARMSH              PROCESS SCANNED PARM VALUE.
         SPACE ,
         CLC   =C'UNUSED(',0(R3)   SELECT OLDIES ONLY?             @151
         BE    PARMSK              YUP.                            @151
         CLC   =C'EMPTY.',0(R3)    EMPTY DATASETS ONLY?            @151
         BE    PARMSL              YUP.                            @151
         CLC   =C'CONTAINING(',0(R3) SCAN DSNAME FOR STRING?       @151
         BE    PARMSM              YES.                            @151
         CLC   =C'ENDING(',0(R3)   SELECT DSNAMES BY ENDING.       @151
         BE    PARMSN              YES.                            @151
         CLC   =C'NOTCONTAINING(',0(R3) EXCLUDE BY CONTENTS.       @151
         BE    PARMSO              YES.                            @151
         CLC   =C'NOTENDING(',0(R3) EXCLUDE DSNAMES BY ENDING      @151
         BE    PARMSP              YES.                            @151
         CLC   =C'BYSIZE.',0(R3)   PRINT LIST BY TRACKS ALLOCATED? @151
         BE    PARMSS              YES.                            @151
         CLC   =C'LARGER(',0(R3)   SELECT ONLY LARGEST DATASETS?   @151
         BE    PARMST              YES.                            @151
         CLC   =C'SMALLER(',0(R3)  SELECT ONLY SMALLEST DATASETS?  @151
         BE    PARMSU              YES.                            @151
         SPACE ,
         CLC   =C'REPORT1.',0(R3)  REPORT BY VOLUME.
         BE    PARMSR1             PROCESS SCANNED PARM VALUE.
         SPACE ,
         CLC   =C'REPORT2.',0(R3)  REBLOCKING REPORT.              @163
         BE    PARMSR2             YES.                            @163
         SPACE ,
         CLC   =C'REPORT4.',0(R3)  REPORT BY HIGH-LEVEL QUALIFIER.
         BE    PARMSR4             PROCESS SCANNED PARM VALUE.
         CLC   =C'CAT.',0(R3)      SELECTED CATALOGUED DS?         @130
         BE    PARMSC1             YES.                            @130
         CLC   =C'UNCAT.',0(R3)    SELECTED UNCATALOGUED DS?       @130
         BE    PARMSC2             YES.                            @130
         CLC   =C'MISCAT.',0(R3)   SELECTED MISCATALOGUED DS?      @130
         BE    PARMSC3             YES.                            @130
         SPACE ,
         CLC   =C'BREAKLEVEL.',0(R3)                               @140
         BE    PARMSBL             BREAK ON LEVEL WANTED.          @140
         SPACE ,
         CLC   =C'ONEFILE.',0(R3)  TAPES WITH 1 DATASET ONLY?      @152
         BE    PARMSOF             YES.                            @152
         SPACE ,
         CLC   =C'DUMPVMF(',0(R3)  HEX PRINT VMF RECORDS?          @160
         BE    PARMSDV             YES.                            @160
         CLC   =C'DATEPROTECTED.',0(R3)                            @160
         BE    PARMSDP             DATE PROTECTED DATASETS...      @160
         SPACE ,
         CLC   =C'WITHIN(',0(R3)   RECENTLY USED ONLY?             @160
         BE    PARMSWI             YES.                            @160
         SPACE ,
         CLC   =C'DENSITY(',0(R3)  SELECT BY TAPE DENSITY?         @160
         BE    PARMSDN             YES.                            @160
         SPACE ,
         CLC   =C'UNIT(',0(R3)     SELECT BY UNITNAME?             @162
         BE    PARMSUL             YES.                            @162
         SPACE ,
         CLC   =C'CREATEDBY(',0(R3) SELECT BY CREATE JOBNAME?      @162
         BE    PARMSCB             YES.                            @162
         CLC   =C'READBY(',0(R3)   SELECT BY CREATE JOBNAME?       @162
         BE    PARMSRB             YES.                            @162
         SPACE ,
         CLC   =C'CLEANUP.',0(R3)  GENERATE CLEANUP CARDS?         @165
         BE    PARMSCL             YES                             @165
         SPACE ,
         CLC   =C'CHANGED.',0(R3)  CHANGED DATASETS ONLY?          @165
         BE    PARMSCH             YES                             @165
         SPACE ,
         CLC   =C'DSORG(',0(R3)    SELECT BY DSORG?                @165
         BE    PARMSDS             YES                             @165
         SPACE ,
         CLC   =C'CREATED(',0(R3)  RECENTLY CREATED ONLY?          @165
         BE    PARMSCR             YES.                            @165
         SPACE ,
         CLC   =C'TAPE3480.',0(R3) 3480 CARTRIDGES ONLY?           @166
         BE    PARMSTC             YES.                            @166
         CLC   =C'TAPE3420.',0(R3) 3420 REELS ONLY?                @166
         BE    PARMSTP             YES.                            @166
         CLC   =C'COMMENT(',0(R3)  HEADER COMMENT                  @166
         BE    PARMSCO             YES.                            @166
         SPACE ,
         CLC   =C'PAGELEN(',0(R3)  PAGE LENGTH?                    @TEC
         BE    PARMSPL             YES                             @TEC
         SPACE ,
PARMSL2  BXLE  R3,R4,PARMS2        SCAN THE WHOLE CARD.            @165
         B     PARMS1              READ ANOTHER CARD.
         SPACE ,
PARMSL1  OI    PARMFLG,PARMSANY    SAY I HAVE SOME VALID PARMS.    @165
         B     PARMSL2             CONTINUE SCN.                   @165
         SPACE ,
PARMS3   OI    FLAGS1,LIST         INDICATE LIST WANTED.
         B     PARMSL1             CONTINUE SCAN.
PARMS4   NI    FLAGS1,NOLIST       INDICATE LIST NOT WANTED.
         LA    R3,3(,R3)           SKIP OVER 'NO'
         B     PARMSL1             CONTINUE SCAN.
PARMS5   OI    FLAGS1,LEVEL        INDICATE LEVEL WANTED.
         LA    R8,9                LENGTH OF A LEVEL
         LA    R3,6(,R3)           SKIP OVER 'LEVEL('
         LA    R7,LTAB             ADDR OF LEVEL TABLE.
         B     BLDTAB              GOTO BUILD SUBLIST TABLE.
PARMS6   OI    FLAGS1,NOLEVEL      INDICATE NOLEVEL WANTED.        @160
         LA    R8,9                LENGTH OF A LEVEL
         LA    R3,8(,R3)           SKIP OVER 'NOLEVEL('
         LA    R7,NLTAB            ADDR OF NOLEVEL TABLE.
         B     BLDTAB              GOTO BUILD SUBLIST TABLE.
PARMS7   OI    FLAGS2,VOLUME       INDICATE VOLUME WANTED.
         LA    R8,7                LENGTH OF A VOLUME
         LA    R3,7(,R3)           SKIP OVER 'VOLUME('
         LA    R7,VTAB             ADDR OF VOLUME TABLE.
         B     BLDTAB              GOTO BUILD SUBLIST TABLE.
PARMS8   OI    FLAGS2,NOVOLUME     INDICATE NOVOLUME WANTED.       @160
         LA    R8,7                LENGTH OF A VOLUME
         LA    R3,9(,R3)           SKIP OVER 'NOVOLUME('
         LA    R7,NVTAB            ADDR OF NOVOLUME TABLE.
         B     BLDTAB              GOTO BUILD SUBLIST TABLE.
PARMS9   OI    FLAGS1,HSM          INDICATE HSM WANTED.
         B     PARMSL1             CONTINUE SCAN.
         SPACE ,
PARMS9A  OI    FLAGS1,HSM          INDICATE HSM WANTED.            @165
         LA    R3,5(0,R3)          SKIP OVER HSM(                  @165
         CLC   =C'ML1',0(R3)       MIGRAT LEVEL 1 ONLY?            @165
         BNE   *+12                NO                              @165
         OI    FLAGS5,HSML1        SAY ML1 ONLY.                   @165
         B     PARMSL1             CONTINUE SCAN.                  @165
         CLC   =C'ML2',0(R3)       MIGRAT LEVEL 2 ONLY?            @165
         BNE   *+8                 NO.                             @165
         OI    FLAGS5,HSML2        SAY ML2 ONLY.                   @165
         B     PARMSL1             CONTINUE SCAN.                  @165
         SPACE ,
PARMSA   NI    FLAGS1,NOHSM        INDICATE HSM NOT WANTED.
         LA    R3,3(,R3)           SKIP OVER 'NO'
         B     PARMSL1             CONTINUE SCAN.
PARMSB   OI    FLAGS1,TAPE         INDICATE TAPE WANTED.
         B     PARMSL1             CONTINUE SCAN.
PARMSC   NI    FLAGS1,NOTAPE       INDICATE TAPE NOT WANTED.
         LA    R3,3(,R3)           SKIP OVER 'NO'
         B     PARMSL1             CONTINUE SCAN.
         SPACE ,
PARMSSV  OI    FLAGS4,VOLSORT      SAY SORT BY DSN W/IN VOLSER.    @161
         OI    FLAGS1,SORT         WHICH IMPLIES SORT.             @161
         B     PARMSL1             AND CARRY ON.                   @161
         SPACE ,
PARMSD   OI    FLAGS1,SORT         INDICATE SORT WANTED.
         B     PARMSL1             CONTINUE SCAN.
PARMSE   NI    FLAGS1,NOSORT       INDICATE SORT NOT WANTED.
         LA    R3,3(,R3)           SKIP OVER 'NO'
         B     PARMSL1             CONTINUE SCAN.
PARMSF   OI    FLAGS1,DASD         INDICATE DASD WANTED.
         B     PARMSL1             CONTINUE SCAN.
PARMSG   NI    FLAGS1,NODASD       INDICATE DASD NOT WANTED.
         LA    R3,3(,R3)           SKIP OVER 'NO'
         B     PARMSL1             CONTINUE SCAN.
PARMSH   LA    R3,7(,R3)           SKIP TO SYSOUT CLASS
         MVC   CLASS+4(1),0(R3)    SETUP SYSOUT CLASS
         B     PARMSL1             CONTINUE SCAN.
         SPACE ,
PARMSK   OI    FLAGS3,UNUSED       SAY OLDIES ONLY PLEASE.         @151
         LA    R3,7(0,R3)          SKIP OVER UNUSED(               @151
         LA    R8,4                3 DIGIT DAYS VALUE AT MOST.     @160
         LA    R7,UTAB             SOMEWHERE TO PUT IT.            @151
         B     BLDTAB              GO EXTRACT AGE VALUE.           @151
         SPACE ,
PARMSPL  DS    0H                                                  @TEC
         OI    FLAGS6,PGL          PAGE LENGTH                     @TEC
         LA    R3,8(0,R3)          SKIP OVER PAGELEN(              @TEC
         LA    R8,3                2 DIGIT PAGELENGTH.             @TEC
         LA    R7,PLTAB            SOMEWHERE TO PUT IT.            @TEC
         B     BLDTAB              GO EXTRACT PAGESIZE             @TEC
         SPACE ,
PARMSL   OI    FLAGS3,EMPTY        EMPTIES ONLY.                   @151
         B     PARMSL1                                             @151
PARMSM   OI    FLAGS3,CONTAIN      SAY SCAN DSNAME FOR STRINGS.    @151
         LA    R3,11(0,R3)         SKIP OVER CONTAINING(           @151
         LA    R8,9                8 CHARACTER SEARCH ARGS.        @151
         LA    R7,CTAB             ARG TABLE.                      @151
         B     BLDTAB              GO EXTRACT SEARCH STRING.       @151
PARMSN   OI    FLAGS3,ENDING       SAY SELECT BY ENDING.           @151
         LA    R3,7(0,R3)          SKIP OVER ENDING(               @151
         LA    R8,9                8 CHARACTER ENDING ARGS.        @151
         LA    R7,ETAB             ARG TABLE.                      @151
         B     BLDTAB              GO EXTRACT ENDING STRING.       @151
PARMSO   OI    FLAGS3,NOTCONT      SAY SCAN DSNAME FOR STRINGS.    @151
         LA    R3,14(0,R3)         SKIP OVER NOTCONTAINING(        @151
         LA    R8,9                8 CHARACTER SEARCH ARGS.        @151
         LA    R7,NCTAB            ARG TABLE.                      @151
         B     BLDTAB              GO EXTRACT SEARCH STRING.       @151
PARMSP   OI    FLAGS3,NOTEND       SAY EXCLUDE BY ENDING.          @151
         LA    R3,10(0,R3)         SKIP OVER NOTENDING(            @151
         LA    R8,9                8 CHARACTER ENDING ARGS.        @151
         LA    R7,NETAB            ARG TABLE.                      @151
         B     BLDTAB              GO EXTRACT ENDING STRING.       @151
PARMSS   OI    FLAGS3,BYSIZE       SORT THEM BY TRACKS.            @151
         NI    FLAGS2,NOBRKLVL     IMPLIES NOBREAKLEVEL.           @151
         NI    REPORTS,NREPORT4    REPORT4 NO GOOD W/ BYSIZE.      @151
         B     PARMSL1             CONTINUE SCAN.                  @151
PARMST   OI    FLAGS4,LARGER       SAY SELECT LARGER THAN.         @151
         LA    R3,7(0,R3)          SKIP OVER LARGER(               @151
         LA    R8,4                4 DIGIT SIZE VALUES.            @151
         LA    R7,STAB             SIZE THRESHOLD TABLE.           @151
         B     BLDTAB              EXTRACT TRACKS VALUE.           @151
PARMSU   OI    FLAGS4,SMALLER      SAY SELECT SMALLER THAN.        @151
         LA    R3,8(0,R3)          SKIP OVER SMALLER(              @151
         LA    R8,4                4 DIGIT SIZE VALUES.            @151
         LA    R7,STAB             SIZE THRESHOLD TABLE.           @151
         B     BLDTAB              EXTRACT TRACKS VALUE.           @151
         SPACE ,
PARMSDV  OI    FLAGS4,DUMPVMF      HEX PRINT WANTED.               @160
         LA    R3,8(0,R3)          SKIP OVER DUMPVMF(              @160
         LA    R8,7                6 CHARACTER VOLSERS.            @160
         LA    R7,DVTAB            VOLSER TABLE ADDRESS.           @160
         B     BLDTAB              EXTRACT VOLSERS TO DUMP.        @160
PARMSDP  OI    FLAGS4,DATEPROT     SELECT DATE PROTECTED ONLY.     @160
         B     PARMSL1             AND KEEP SCANNING.              @160
         SPACE ,
PARMSWI  OI    FLAGS4,WITHIN       RECENTLY USED ONLY.             @160
         LA    R3,7(0,R3)          SKIP OVER WITHIN(               @160
         LA    R8,4                3 DIGIT DATE.                   @160
         LA    R7,WITAB            DATE SAVE AREA.                 @160
         B     BLDTAB              EXTRACT DATE VALUE.             @160
         SPACE ,
PARMSDN  OI    FLAGS4,DNSTY        SELECT TAPES BY DENSITY.        @160
         LA    R3,8(0,R3)          SKIP OVER DENSITY(              @160
         MVC   DENSITY,0(R3)       SAVE DCB=DEN= VALUE.            @160
         B     PARMSL1                                             @160
         SPACE ,
PARMSUL  OI    INTFLAG,UNITPARM    CALL UNITNAME DECODE ROUTINE.   @162
         OI    FLAGS2,VOLUME       PROCESS UNITS AS VOLUME REQUEST @162
         LA    R3,5(0,R3)          SKIP OVER UNIT(                 @162
         LA    R8,9                8 CHARACTER UNITNAMES.          @162
         LA    R7,UNITS            UNIT LIST ADDR.                 @162
         B     BLDTAB              SAVE UNIT NAMES.                @162
         SPACE ,
PARMSCB  OI    FLAGS5,CREATEBY     INDICATE SELECT BY CREATE JOB.  @162
         LA    R3,10(0,R3)         SKIP OVER CREATEDBY(            @162
         LA    R8,9                8 CHARACTER JOBNAME.            @162
         LA    R7,CREATJOB         JOB NAME SAVE AREA.             @162
         B     BLDTAB              SAVE JOB NAME.                  @162
PARMSRB  OI    FLAGS5,READBY       INDICATE SELECT BY READ JOB.    @162
         LA    R3,7(0,R3)          SKIP OVER READBY(               @162
         LA    R8,9                8 CHARACTER JOBNAME.            @162
         LA    R7,READJOB          JOB NAME SAVE AREA.             @162
         B     BLDTAB              SAVE JOB NAME.                  @162
         SPACE ,
PARMSCL  OI    FLAGS5,CLEANUP      SAY GENERATE CLEANUP CARDS.     @165
         LA    R3,8(0,R3)          SKIP OVER CLEANUP.              @165
         B     PARMSL1             AND CONTINUE SCAN.              @165
         SPACE ,
PARMSCH  OI    FLAGS5,CHANGED      SAY CHANGED DATASETS ONLY.      @165
         LA    R3,8(0,R3)          SKIP OVER CHANGED.              @165
         B     PARMSL1                                             @165
         SPACE ,
PARMSDS  OI    FLAGS5,DSORG        SELECT BY DSORG                 @165
         LA    R3,6(0,R3)          SKIP OVER DSORG(                @165
         LA    R8,4                3 CHARACTER DSORG.              @165
         LA    R7,DOTAB$           INTERNAL TABLE                  @165
         B     BLDTAB              CREATE TABLE.                   @165
         SPACE ,
PARMSCR  OI    FLAGS5,CREATED      SELECT BY CREATE DATE.          @165
         LA    R3,8(0,R3)          SKIP OVER CREATED(              @165
         LA    R8,4                3 CHARACTER DAYS.               @165
         LA    R7,CRTAB            DATE SAVE AREA.                 @165
         B     BLDTAB                                              @165
         SPACE ,
PARMSTC  OI    FLAGS6,T3480        3480 CARTRIDGES WANTED.         @166
         OI    FLAGS1,TAPE         T3480 IMPLIES TAPE.             @166
         B     PARMSL1             CONTINUE.                       @166
PARMSTP  OI    FLAGS6,T3420        3420 REELS WANTED.              @166
         OI    FLAGS1,TAPE         T3420 IMPLIES TAPE.             @166
         B     PARMSL1             CONTINUE.                       @166
PARMSCO  LA    R3,7(0,R3)          SKIP OVER COMMENT(              @166
         OI    PARMFLG,PARMCMNT    FLAG COMMENT FOUND.             @166
         LA    R8,51               50 CHARS OF COMMENT             @166
         LA    R9,PCOMMENT         COMMENT SAVE AREA .             @166
         B     BLDTAB              DECODE COMMENT.                 @166
         SPACE ,
PARMSC1  OI    FLAGS2,CAT          SELECT CATALOGUED DATASETS      @130
         B     PARMSL1                                             @130
PARMSC2  OI    FLAGS2,UNCAT        SELECT UNCATALOGUED DATASETS    @130
         LA    R3,3(,R3)           SKIP OVER 'UN'                  @130
         B     PARMSL1             CONTINUE SCAN.                  @130
PARMSC3  OI    FLAGS2,MISCAT       SELECT MISCATALOGUED DATASETS   @130
         LA    R3,4(,R3)           SKIP OVER 'MIS'                 @130
         B     PARMSL1             CONTINUE SCAN.
         SPACE ,
PARMSBL  OI    FLAGS2,BRKLVL       PAGE EJECT EACH HI-LVL QUAL.    @140
         B     PARMSL1             CONTINUE SCAN.                  @140
         SPACE ,
PARMSOF  OI    FLAGS4,ONEFILE      SELECT ONLY TAPES WITH 1 DS.    @152
         B     PARMSL1             CONTINUE SCAN.                  @152
         SPACE ,
PARMSR1  OI    REPORTS,REPORT1     INDICATE REPORT 1 WANTED.
         B     PARMSL1             CONTINUE SCAN.
         SPACE ,
PARMSR2  OI    REPORTS,REPORT2     INDICATE REPORT 2 WANTED.       @163
         B     PARMSL1             CONTINUE SCAN.                  @163
         SPACE ,
PARMSR4  OI    REPORTS,REPORT4     INDICATE REPORT 4 WANTED.
         B     PARMSL1             CONTINUE SCAN.
         SPACE ,
PARMSER  LA    R3,7(0,R3)          DON'T RESCAN LAST PARAMETER.    @162
         STM   R2,R12,PARMREGS     SAVE REGISTERS TO RESUME SCAN.  @162
         OI    PARMFLG,PARMMULT    REMEMBER THAT WE SAW ENDREQ.    @165
         B     PARM99              PRINT REPORT AND DO IT.         @162
         SPACE ,
BLDTAB   SR    R1,R1               R1 WILL BE ADDR OF SCANNED CHAR.
         SR    R2,R2               R2 WILL CONTAIN SCANNED CHAR.
BLDTAB1  TRT   0(51,R3),SCANTAB    SCAN FOR ) OR ,                 @166
         LTR   R1,R1               DELIMITER FOUND
         BZ    SUBLERR             NO, SYNATX ERROR IN SUBLIST.
         CLC   0(2,R1),=C', '      END OF CARD?
         BE    BLDTAB9             YES
         LR    R15,R1              ADDR OF SCANNED DELIMITER.
         SR    R15,R3              R15 -> LENGTH OF SUBLIST ELEMENT.
         STC   R15,0(,R7)          SAVE IT
         BCTR  R15,0               MINUS 1 FOR EX.
         EX    R15,BLDMVC          STORE MESSAGE ID.
         CLI   0(R1),C')'          END OF SUBLIST?
         BNE   BLDTAB2             NO, SKIP END OF TABLE.
         AR    R7,R8               SPAN OVER LAST ELEMENT.
         MVI   0(R7),255           SET END OF TABLE.
         B     PARMSL1             KEEP ON SCANNING
BLDTAB2  LA    R15,1(,R15)         ADD 1 AFTER EX.
         AR    R3,R15              SKIP OVER MESSAGE ID.
         AR    R7,R8               NEXT TABLE ENTRY.               @122
         BXLE  R3,R4,BLDTAB1       FIND NEXT MESSAGE.
BLDTAB9  GET   SYSIN,CARD
         LA    R3,CARD             START OF CARD
         LA    R4,1                SCAN 1 COLUMN AT A TIME.
         LR    R5,R3               START OF CARD
         LA    R5,72(,R5)          PLUS 72 IS LAST COLUMN TO SCAN.
         TRT   0(72,R3),ALPHTAB    SCAN TO 1ST APHAMERIC CHARACTER.
         BZ    SUBLERR             SUBLIST ERROR IF NONE FOUND.
         LR    R3,R1               R1 -> 1ST ALPHA CHARACTER.
         B     BLDTAB1             RESUME DECODING OF SUBLIST ELEMENTS
SUBLERR  ABEND 98,DUMP
BLDMVC   MVC   1(0,R7),0(R3)       SAVE A SUBLIST ITEM.
EODIN    OI    INTFLAG,EODPARMS    SET EOF REACHED FLAG.           @162
         TM    PARMFLG,PARMMULT    MULTIPLE REQUESTS THIS TIME?    @165
         BZ    *+16                NO.                             @165
         TM    PARMFLG,PARMSANY    ANYTHING IN THE LAST REQUEST?   @165
         BO    *+8                 YES. LET'S DO IT.               @165
         OI    INTFLAG,EOJPARMS    EOJ REACHED.                    @165
         CLOSE (SYSIN)             ALL DONE. GET OUT.
PARM99   DS    0H
         TM    INTFLAG,UNITPARM    UNITNAMES TO DECODE?            @162
         BZ    PARMNOU             NO.                             @162
        $CALL  UNITNAME
         NI    INTFLAG,255-UNITPARM FLAG OFF.                      @162
PARMNOU  DS    0H                                                  @162
         SPACE ,
         TM    INTFLAG,EOJPARMS    EOJ REACHED?                    @165
         BO    PARMNOP             YES. NO MORE OPTIONS PRINT.     @165
        $CALL  OPTIONS
PARMNOP  DS    0H                                                  @165
         SPACE ,
         TM    FLAGS3,UNUSED       OLDIES ONLY?                    @151
         BZ    PARMSNOD            NO.                             @151
         SR    R1,R1               CLEAR REGISTER.                 @151
         ICM   R1,1,UTAB           GET LENGTH OF AGE NUMBER.       @151
         BCTR  R1,0                SETUP FOR EX.                   @151
         B     *+10                OK, SO I'M LAZY.....            @151
         PACK  OLDDATE,UTAB+1(0)                                   @151
         EX    R1,*-6              PACK IT.                        @151
         TIME ,                                                    @151
         ST    R1,COMPDATE         TODAYS JULIAN DATE.             @151
         CP    OLDDATE,COMPDATE+2(2) IS THRESHOLD THIS YEAR?       @151
         BH    PARMSLY             YES.                            @151
         SP    COMPDATE,OLDDATE    OTHERWISE JUST SUBTRACT.        @151
         B     PARMSCNV                                            @151
PARMSLY  DS    0H                                                  @151
         IC    R1,COMPDATE+1       GET YY VALUE.                   @151
         BCTR  R1,0                MINUS ONE.                      @151
         STC   R1,COMPDATE+1       MAKE IT LAST YEAR.              @151
         SP    OLDDATE,COMPDATE+2(2) HOW FAR INTO LAST YEAR.       @151
         MVC   COMPDATE+2(2),=PL2'366'                             @151
         SP    COMPDATE,OLDDATE    SOMETIME LAST YEAR.             @151
PARMSCNV DS    0H                                                  @151
         SR    R0,R0                                               @151
         IC    R0,COMPDATE+1       GET YY                          @151
         SLL   R0,4                MAKE ROOM FOR A SIGN.           @151
         O     R0,=X'0000000F'     GIVE US A SIGN!                 @151
         SRDL  R0,32                                               @151
         STM   R0,R1,CARD                                          @151
         CVB   R1,CARD             YY IN BINARY                    @151
         STCM  R1,1,DSCBOLD        SAVE IT.                        @151
         SR    R1,R1                                               @151
         ICM   R1,3,COMPDATE+2     GET DDD                         @151
         STM   R0,R1,CARD                                          @151
         CVB   R1,CARD             DDD IN BINARY.                  @151
         STCM  R1,3,DSCBOLD+1                                      @151
PARMSNOD DS    0H                                                  @151
         SPACE ,
         TM    FLAGS6,PGL          PAGELENGTH GIVEN?               @TEC
         BZ    PARMSNOP            NO.                             @TEC
         SR    R1,R1               CLEAR REGISTER                  @TEC
         ICM   R1,1,PLTAB          LENGTH OF PAGELEN VALUE         @TEC
         BCTR  R1,0                MINUS 1 FOR EXEC                @TEC
         B     *+10                                                @TEC
         PACK  PLPACK,PLTAB+1(0)                                   @TEC
         EX    R1,*-6              PACK IT.                        @TEC
         CVB   R1,PLPACK           PAGELEN IN BINARY               @TEC
         ST    R1,PAGELEN          AND SAVE IT                     @TEC
PARMSNOP DS    0H                                                  @TEC
         SPACE ,
         TM    FLAGS4,WITHIN       OLDIES ONLY?                    @160
         BZ    PARMSNWI            NO.                             @160
         SR    R1,R1               CLEAR REGISTER.                 @160
         ICM   R1,1,WITAB          GET LENGTH OF AGE NUMBER.       @160
         BCTR  R1,0                SETUP FOR EX.                   @160
         B     *+10                OK, SO I'M LAZY.....            @160
         PACK  WIDATE,WITAB+1(0)                                   @160
         EX    R1,*-6              PACK IT.                        @160
         TIME ,                                                    @160
         ST    R1,WICOMP           TODAYS JULIAN DATE.             @160
         CP    WIDATE,WICOMP+2(2) IS THRESHOLD THIS YEAR?          @160
         BH    PARMWLY             YES.                            @160
         SP    WICOMP,WIDATE       OTHERWISE JUST SUBTRACT.        @160
         B     PARMWCNV                                            @160
PARMWLY  DS    0H                                                  @160
         IC    R1,WICOMP+1         GET YY VALUE.                   @160
         BCTR  R1,0                MINUS ONE.                      @160
         STC   R1,WICOMP+1         MAKE IT LAST YEAR.              @160
         SP    WIDATE,WICOMP+2(2) HOW FAR INTO LAST YEAR.          @160
         MVC   WICOMP+2(2),=PL2'366'                               @160
         SP    WICOMP,WIDATE       SOMETIME LAST YEAR.             @160
PARMWCNV DS    0H                                                  @160
         SR    R0,R0                                               @160
         IC    R0,WICOMP+1         GET YY                          @160
         SLL   R0,4                MAKE ROOM FOR A SIGN.           @160
         O     R0,=X'0000000F'     GIVE US A SIGN!                 @160
         SRDL  R0,32                                               @160
         STM   R0,R1,CARD                                          @160
         CVB   R1,CARD             YY IN BINARY                    @160
         STCM  R1,1,DSCBWI         SAVE IT.                        @160
         SR    R1,R1                                               @160
         ICM   R1,3,WICOMP+2       GET DDD                         @160
         STM   R0,R1,CARD                                          @160
         CVB   R1,CARD             DDD IN BINARY.                  @160
         STCM  R1,3,DSCBWI+1                                       @160
PARMSNWI DS    0H                                                  @160
         SPACE ,
         TM    FLAGS4,LARGER+SMALLER ANY SIZE CRITERIA?            @151
         BZ    PSKSIZE             NO.                             @151
         SR    R1,R1               CLEAR REGISTER.                 @151
         ICM   R1,1,STAB           GET LENGTH OF TRACKS VALUE.     @151
         BCTR  R1,0                SETUP FOR EX.                   @151
         B     *+10                SKIP EXECUTED INSTRUCTION.      @151
         PACK  TRKPACK,STAB+1(0)                                   @151
         EX    R1,*-6              PACK IT.                        @151
         CVB   R1,TRKPACK          AND GET IT IN BINARY            @151
         ST    R1,TRKVALUE         SAVE FOR LATER USE.             @151
PSKSIZE  DS    0H                                                  @151
         SPACE ,
         TM    FLAGS5,DSORG        DSORG IN EFFECT?                @165
         BZ    PARMSKDS            NO                              @165
         LA    R2,DOTAB$           CHARACTER FORMAT TABLE.         @165
         LA    R3,DOTAB            INTERNAL FORMAT TABLE.          @165
         CLC   1(2,R2),=C'??'      INVALID DSORG??????             @166
         BNE   *+22                TRY THE NEXT ONE.               @166
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @166
         MVC   1(2,R3),=X'FEFE'    SET STRANGE VALUE.              @166
         LA    R3,3(0,R3)          NEXT SLOT.                      @166
         LA    R2,4(0,R2)          NEXT SLOT.                      @166
         CLC   1(3,R2),=C'PSU'     CHECK DSORG VALUE.              @165
         BNE   *+22                TRY THE NEXT ONE.               @165
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'4100'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         LA    R2,4(0,R2)          NEXT SLOT.                      @165
         CLC   1(3,R2),=C'DAU'     CHECK DSORG VALUE.              @165
         BNE   *+22                TRY THE NEXT ONE.               @165
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'2100'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         LA    R2,4(0,R2)          NEXT SLOT.                      @165
         CLC   1(3,R2),=C'POU'     CHECK DSORG VALUE.              @165
         BNE   *+22                TRY THE NEXT ONE.               @165
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'0300'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         LA    R2,4(0,R2)          NEXT SLOT.                      @165
         CLC   1(2,R2),=C'IS'      CHECK DSORG VALUE.              @165
         BNE   *+22                TRY THE NEXT ONE.               @165
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'8000'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         LA    R2,4(0,R2)          NEXT SLOT.                      @165
         CLC   1(2,R2),=C'PS'      CHECK DSORG VALUE.              @165
         BNE   *+22                TRY THE NEXT ONE.               @165
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'4000'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         LA    R2,4(0,R2)          NEXT SLOT.                      @165
         CLC   1(2,R2),=C'DA'      CHECK DSORG VALUE.              @165
         BNE   *+22                TRY THE NEXT ONE.               @165
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'2000'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         LA    R2,4(0,R2)          NEXT SLOT.                      @165
         CLC   1(2,R2),=C'PO'      CHECK DSORG VALUE.              @165
         BNE   *+22                TRY THE NEXT ONE.               @165
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'0200'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         LA    R2,4(0,R2)          NEXT SLOT.                      @165
         CLC   1(2,R2),=C'VS'      CHECK DSORG VALUE.              @165
         BNE   *+18                THAT'S ALL.                     @166
         MVI   0(R3),2             DSORG 2 BYTES IN HEX.           @165
         MVC   1(2,R3),=X'0008'    SET HEX VALUE.                  @165
         LA    R3,3(0,R3)          NEXT SLOT.                      @165
         MVI   0(R3),255           SET END OF TABLE MARKER.        @166
PARMSKDS DS    0H                                                  @165
         SPACE ,
         TM    FLAGS5,CREATED      RECENT ONLY?                    @165
         BZ    PARMSNCR            NO.                             @165
         SR    R1,R1               CLEAR REGISTER.                 @165
         ICM   R1,1,CRTAB          GET LENGTH OF AGE NUMBER.       @165
         BCTR  R1,0                SETUP FOR EX.                   @165
         B     *+10                OK, SO I'M LAZY.....            @165
         PACK  CRDATE,CRTAB+1(0)                                   @165
         EX    R1,*-6              PACK IT.                        @165
         TIME ,                                                    @165
         ST    R1,CRCOMP           TODAYS JULIAN DATE.             @165
         CP    CRDATE,CRCOMP+2(2) IS THRESHOLD THIS YEAR?          @165
         BH    PARMCLY             YES.                            @165
         SP    CRCOMP,CRDATE       OTHERWISE JUST SUBTRACT.        @165
         B     PARMCCNV                                            @165
PARMCLY  DS    0H                                                  @165
         IC    R1,CRCOMP+1         GET YY VALUE.                   @165
         BCTR  R1,0                MINUS ONE.                      @165
         STC   R1,CRCOMP+1         MAKE IT LAST YEAR.              @165
         SP    CRDATE,CRCOMP+2(2) HOW FAR INTO LAST YEAR.          @165
         MVC   CRCOMP+2(2),=PL2'366'                               @165
         SP    CRCOMP,CRDATE       SOMETIME LAST YEAR.             @165
PARMCCNV DS    0H                                                  @165
         SR    R0,R0                                               @165
         IC    R0,CRCOMP+1         GET YY                          @165
         SLL   R0,4                MAKE ROOM FOR A SIGN.           @165
         O     R0,=X'0000000F'     GIVE US A SIGN!                 @165
         SRDL  R0,32                                               @165
         STM   R0,R1,CARD                                          @165
         CVB   R1,CARD             YY IN BINARY                    @165
         STCM  R1,1,DSCBCR         SAVE IT.                        @165
         SR    R1,R1                                               @165
         ICM   R1,3,CRCOMP+2       GET DDD                         @165
         STM   R0,R1,CARD                                          @165
         CVB   R1,CARD             DDD IN BINARY.                  @165
         STCM  R1,3,DSCBCR+1                                       @165
PARMSNCR DS    0H                                                  @165
         SPACE ,
         TM    PARMFLG,PARMCMNT    COMMENT CARD FOUND?             @166
         BZ    PARMSNCM            NO                              @166
         NI    PARMFLG,255-PARMCMNT COMMENT THIS TIME ONLY.        @166
         LA    R3,=A(HTEXT1C)      COMMENT LOCATION                $NTC
         MVC   0(50,R3),=CL50' '   BLANK OUT COMMENT FIELD.        $NTC
         SR    R1,R1                                               @166
         IC    R1,PCOMMENT         PICK UP LENGTH                  @166
         BCTR  R1,0                DECREMENT FOR EX                @166
         B     *+10                BRANCH AROUND                   @166
         MVC   0(0,R3),PCOMMENT+1                                  $NTC
         EX    R1,*-6              MOVE COMMENT INTO HEADING       @166
PARMSNCM DS    0H                                                  @166
         SPACE ,
        $AMODE 31                                                  @162
         $EPILOG ,
         LTORG ,
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM),EODAD=EODIN
         DS    0D                                                  @151
CARD     DC    CL80' '
         SPACE ,
PARMREGS DS    11F                                                 @162
PARMFLG  DC    AL1(0)                                              @162
PARM1ST  EQU   B'10000000'                                         @162
PARMSANY EQU   B'01000000'   ANY VALID REQUESTS SINCE ENDREQ.      @165
PARMMULT EQU   B'00100000'   AT LEAST ONE ENDREQ ENTERED.          @165
PARMCMNT EQU   B'00010000'   HEADER COMMENT FOUND                  @166
         SPACE ,
PCOMMENT DC    X'FF',CL50' ',X'FF'                                 @166
         SPACE ,
ALPHTAB  DS    0D
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'00000000000000000000000000000000'  0
         DC    X'00000000000000000000000000000000'  1
         DC    X'00000000000000000000000000000000'  2
         DC    X'00000000000000000000000000000000'  3
         DC    X'00000000000000000000000000000000'  4
         DC    X'0000000000000000000000FF00000000'  5
         DC    X'00000000000000000000000000000000'  6
         DC    X'00000000000000000000000000000000'  7
         DC    X'00000000000000000000000000000000'  8
         DC    X'00000000000000000000000000000000'  9
         DC    X'00000000000000000000000000000000'  A
         DC    X'00000000000000000000000000000000'  B
         DC    X'00FFFFFFFFFFFFFFFFFF000000000000'  C
         DC    X'00FFFFFFFFFFFFFFFFFF000000000000'  D
         DC    X'0000FFFFFFFFFFFFFFFF000000000000'  E
         DC    X'FFFFFFFFFFFFFFFFFFFF000000000000'  F
./       ADD   NAME=PASSMCDS
         TITLE 'EXTRACT MCDS IF HSM OPTION REQUESTED.'
PASSMCDS $PROLOG R12                                               @164
         L     R11,=V(DATASECT)    COMMON DATA AREA.               @164
         USING DATASECT,R11                                        @164
         TM    FLAGS1,HSM          DO WE WANT HSM?                 @141
         BZ    CHKHSM99            IF NOT, WHY BOTHER!             @141
         GENCB BLK=EXLST,EODAD=EODMCDS                             @164
         LR    R2,R1                                               @164
         MODCB ACB=ACBMCDS,EXLST=((2))                             @164
         GENCB BLK=RPL,                                            @164X
               ACB=(S,ACBMCDS),                                    @164X
               AM=VSAM,                                            @164X
               AREA=(S,BUFPTR),                                    @164X
               OPTCD=(KEY,SEQ,LOC)                                 @164
         ST    R1,SEQRPL           SAVE RPL ADDR.                  @164
         OPEN  (ACBMCDS)
         LTR   R15,R15             DID IT OPEN?                    @164
         BNZ   CHKHSM99            NO.                             @164
CHKHSM1  DS    0H
         L     R8,SEQRPL           GET RPL FOR SEQ ACCESS.         @164
         GET   RPL=(8)                                             @164
         LTR   R15,R15             RECORD READ OK?
         BNZ   EODMCDS             THAT'S ALL.                     @164
         L     R5,BUFPTR           GET ADDRESS OF RECORD.          @170
         CLI   MCHTYPE-MCD(R5),X'00' DATASET RECORD?               @170
         BNE   CHKHSM1             REJECT ALL OTHERS.              @164
         USING MCD,R5              ADDRESSABILITY TO MCD DSECT
         USING SAVEFMT,R4          ADDRESSABILITY TO DS INFO TABLE.
         L     R4,TABOFF           PICK UP SAVEFMT POSITION.       @164
         TM    MCDFLGS,MCDFNSCR    DATASET HAS BEEN RECALLED?      @170
         BO    CHKHSM1             IF SO, DON'T CALL IT MIGRATED.  @170
         TM    FLAGS5,HSML1+HSML2  ONLY CERTAIN MIGRATED?          @165
         BZ    CKHSMALL                                            @165
         TM    FLAGS5,HSML1        MIGRAT LEVEL 1 ONLY?            @165
         BZ    CKHSML2             NO. TRY LEVEL 2                 @165
         TM    MCDFLG1,MCDFFL2     WHAT LEVEL IS IT?               @165
         BO    CHKHSM1             NO L1, SO SKIP IT.              @165
CKHSML2  DS    0H                                                  @165
         TM    MCDFLG1,MCDFFL2     WHAT LEVEL IS IT?               @165
         BZ    CHKHSM1             NOT LEVEL 2, SO SKIP IT.        @165
CKHSMALL DS    0H                                                  @165
         TM    FLAGS1,LEVEL+NOLEVEL  DATASET SELECTION WANTED?
         BZ    CKHSM1A             IF NOT, PROCESS IT.
         TM    FLAGS1,LEVEL        DATASETS TO BE SELECTED?
         BZ    CKHSMNL             NO, TRY EXCLUDES
         LA    R2,LTAB             ADDR OF LEVEL SELECT TABLE.
CKHSML   SR    R9,R9               CLEAR HIGH ORDER BYTES.
         IC    R9,0(,R2)           LENGTH TO COMPARE
         BCTR  R9,0                SETUP FOR EX.
         EX    R9,CKHSMCLC         COMPARE DSNAME TO LEVEL
         BE    CKHSM1A             DATASET SELECTED!
         LA    R2,9(,R2)           SKIP TO NEXT LEVEL.
         CLI   0(R2),255           END OF LIST?
         BNE   CKHSML              IF NOT, DO NEXT COMPARE.
         B     CHKHSM1             NOT SELECTED.
CKHSMNL  LA    R2,NLTAB            ADDR OF LEVEL EXCLUDE TABLE.
CKHSMNL1 SR    R9,R9               CLEAR HIGH-ORDER BYTES.
         IC    R9,0(,R2)           LENGTH TO COMPARE
         BCTR  R9,0                SETUP FOR EX.
         EX    R9,CKHSMCLC         COMPARE DSNAME TO NOLEVEL.
         BE    CHKHSM1             DATASET EXCLUDED!
         LA    R2,9(,R2)           LOOK AT NEXT NOLEVEL.
         CLI   0(R2),255           END OF LIST?
         BNE   CKHSMNL1            IF NOT, COMPARE ANOTHER NOLEVEL.
CKHSM1A DS    0H                                                   @164
        $TEST  FLAGS2,VOLUME,VTAB,MCDFRVSN,7,CHKHSM2A,CHKHSM1      @164
CHKHSM2A DS    0H                                                  @164
        $TEST  FLAGS2,NOVOLUME,NVTAB,MCDFRVSN,7,CHKHSM1,CHKHSM2B   @164
CHKHSM2B DS    0H                                                  @164
        $TEST  FLAGS3,CONTAIN,CTAB,                                @164X
               (MCDDSN,1,44),9,CHKHSM2C,CHKHSM1                    @164
CHKHSM2C DS    0H                                                  @164
        $TEST  FLAGS3,NOTCONT,NCTAB,                               @164X
               (MCDDSN,1,44),9,CHKHSM1,CHKHSM2D                    @164
CHKHSM2D DS    0H                                                  @164
        $TEST  FLAGS3,ENDING,ETAB,(MCDDSN,,44),9,CHKHSM2E,CHKHSM1  @164
CHKHSM2E DS    0H                                                  @164
        $TEST  FLAGS3,NOTEND,NETAB,(MCDDSN,,44),9,CHKHSM1,CHKHSM2F @164
CHKHSM2F DS    0H                                                  @164
         MVC   SV1DSNAM,MCDDSN     DSNAME
         MVC   SV1DSSN,=C'MGRAT1'  VOLUME SERIAL NUMBER            @165
         TM    MCDFLG1,MCDFFL2     IS IT ON LEVEL 2?               @165
         BZ    *+10                NO                              @165
         MVC   SV1DSSN,=C'MGRAT2'  VOLUME SERIAL NUMBER            @165
         MVC   SV1FVSN,MCDFRVSN    ORIGINAL VOLSER
         MVC   SV1CREDT,MCDDLC+1   CREATION DATE
         MVC   SV1EXPDT,MCDEXPDT+1  EXPIRATION DATE
         MVC   SV1REFD,MCDDLR+1    LAST DATE REFERENCED - SU 60
         MVC   SV1DSORG,MCDDSORG   DATA SET ORGANIZATION
         TM    MCDRECFM,X'C0'      CHECK RECFM=U
         BNO   MCDSL2A             NO.
         MVI   SV1RECFM,C'U'       SAVE RECFM VALUE.
         B     MCDSL2Z             TRY FOR A 2ND BYTE.
MCDSL2A  TM    MCDRECFM,X'40'      CHECK RECFM=V
         BZ    MCDSL2B             NO.
         MVI   SV1RECFM,C'V'       SAVE RECFM VALUE.
         B     MCDSL2Z             TRY FOR A 2ND BYTE.
MCDSL2B  TM    MCDRECFM,X'80'      CHECK RECFM=F
         BZ    MCDSL2C             NO.
         MVI   SV1RECFM,C'F'       SAVE RECFM VALUE.
         B     MCDSL2Z             TRY FOR A 2ND BYTE.
MCDSL2C  MVI   SV1RECFM,C'?'       BEATS ME.
         B     MCDSL3Z             GOTO EXAMINE BLKSIZE.
MCDSL2Z  TM    MCDRECFM,X'20'      TRACK OVERFLOW
         BZ    MCDSL3A             NO.
         MVI   SV1RECFM+1,C'T'     SAVE RECFM VALUE.
         B     MCDSL3Z             GOTO EXAMINE BLKSIZE.
MCDSL3A  TM    MCDRECFM,X'08'      STANDARD BLOCKS
         BZ    MCDSL3B             NO.
         MVI   SV1RECFM+1,C'S'     SAVE RECFM VALUE.
         B     MCDSL3Z             GOTO EXAMINE BLKSIZE.
MCDSL3B  TM    MCDRECFM,X'10'      BLOCKED
         BZ    MCDSL3Z             GOTO EXAMINE BLKSIZE.
         MVI   SV1RECFM+1,C'B'     SAVE RECFM VALUE.
MCDSL3Z  MVC   SV1BLKL,MCDBLKSZ    BLOCKSIZE
         MVC   SV1EXT1,MCDSIZE     TRACKS USED ORIGINALLY
         MVI   SV1DEVC,C'D'        CALL IT A DISK                  @170
         LA    R4,SAVELEN(,R4)     POINT TO NEXT ENTRY.
         ST    R4,TABOFF           AND REMEMBER WHERE WE ARE.
         L     R5,DSCBS            GET COUNT OF DSCBS READ.
         LA    R5,1(,R5)           INCREMENT BY 1
         ST    R5,DSCBS            AND SAVE IT.
         B     CHKHSM1             NEXT DATASET.                   @164
         DROP  R4
         DROP  R5                  THAT'S ALL FOR HSM MCD RECORD.
EODMCDS  DS    0H                                                  @164
         CLOSE ACBMCDS                                             @164
CHKHSM99 $EPILOG ,                                                 @164
HSMSAVE  DS    9F                  SAVEAREA FOR REGISTERS 2-10
HSMR14   DS    F                   SAVEAREA FO RETURN ADDRESS.
CKHSMCLC CLC   1(0,R2),0(R5)       (EXECUTED).
         DS    0F                                                  @164
SEQRPL   DC    AL4(0)                                              @164
         LTORG ,                                                   @164
         TITLE 'HSMLIST - MCDS DSECT.'
MCD      DSECT
MCDDSN   DS    CL44                DATASET NAME
MCDMCK   DS    0CL20               CDS RECORD HEADER
MCHLEN   DS    XL2                                                 @164
MCHTYPE  DS    CL1                 RECORD TYPE.                    @164
         DS    XL1                                                 @164
MCHTSLU  DS    XL8                 TIME OF LAST UPDATE             @164
MCHTSCR  DS    XL8                 TIME RECORD CREATED.            @164
MCDVSN   DS    CL6                 VOLSER OF MIGN. VOL HOLDING THE DS.
MCDFLGS  DS    0XL2                FLAGS
MCDFLG1  DS    X                   FLAGS - BYTE ONE
MCDFASN  EQU   B'10000000'         A MIGRATED COPY EXISTS
MCDFMIG  EQU   B'01000000'         DATA SET IS TO BE MIGRATED
MCDFNOMG EQU   B'00100000'         DATA SET IS PREVENTED FROM MIGRATING
MCDFDEL  EQU   B'00010000'         DATA SET WAS DELETED
MCDFSDP  EQU   B'00001000'         DATA SET IS SMALL-DATASET-PACKED
MCDFFL2  EQU   B'00000100'         WHEN SET TO 1, DATASET IS ON ML2,
*                                  ELSE ML1
MCDFNSCR EQU   B'00000010'         DATASET RECALLED BUT MIGRATED COPY
*                                  EXISTS
MCDJES3  EQU   B'00000001'         RECORD PROCESSED BY HSM-JES3 SETUP.
MCDFLG2  DS    X                   FLAGS - BYTE TWO
MCDFSMVL EQU   B'00000100'         SPACE MGMT FLAGS (MCDRECAL) ARE
*                                  VALID
MCDFDUMD EQU   B'00000010'         WHEN SET TO 1, THIS IS A DUMMY
*                                  RECORD
MCDJR3   EQU   B'00000001'         THIS RECORD WAS CREATED BY RELEASE
*                                  3.
MCDCOMPR DS    X                   PERCENT OF SPACE SAVED BY COMPACTION
         DS    XL3
MCDDLC   DS    CL4                 X'00YYDDDS' - DATE DS WAS CREATED.
MCDDLC3  EQU   MCDDLC+1,3,C'P'
MCDTLR   DS    CL4                 X'HHMMSSTH' - TIME DS WAS LAST USED.
MCDDLR   DS    CL4                 X'00YYDDDS' - DATE DS WAS LAST
*                                  REFERENCED.
MCDDLR3  EQU   MCDDLR+1,3,C'P'
MCDTLU   DS    CL4                 X'HHMMSSTH' - TIME DS WAS LAST
*                                  UPDATED.
MCDDLU   DS    CL4                 X'00YYDDDS' - DATE DS WAS LAST
*                                  UPDATED.
MCDDLU3  EQU   MCDDLU+1,3,C'P'
MCDTMIG  DS    CL4                 X'HHMMSSTH' - TIME DS WAS LAST
*                                  MIGRATED.
MCDDMIG  DS    CL4                 X'00YYDDDS' - DATE DS WAS LAST
*                                  MIGRATED.
MCDDMIG3 EQU   MCDDMIG+1,3,C'P'
MCDDSORG DS    XL2                 DSORG FROM THE DSCB.
MCDBLKSZ DS    XL2                 MAXIMUM BLOCK SIZE OF THE DATASET.
MCDKEYLN DS    XL1                 KEY LENGTH OF THE DATASET.
MCDRECFM DS    XL1                 RECORD FORMAT
MCDRFTYP EQU   B'11000000'         FLAGS FOR A V,B, OR F FORMAT ???
MCDRFTO  EQU   B'01000000'         TRACK OVERFLOW IS PRESENT
MCDDSIND DS    XL1                 INDICATORS FROM THE FORMAT 1 DSCB.
MCDFRACF EQU   B'01000000'         DS IS RACF-PROTECTED
MCDFSCTY EQU   B'00010000'         DS IS PASSWORD PROTECTED.
MCDFWSEC EQU   B'00000100'         DS IS WRITE-PASSWORD PROTECTED
MCDFCHNG EQU   B'00000010'         OPENED FOR OTHER THAN READ-ONLY
MCDHID   DS    XL1                 ID OF THE PROCESSOR USING THIS
*                                  RECORD
MCDSIZE  DS    XL4                 SIZE ALLOCATED IN TRACKS, ON THE
*                                  USERS VOL
MCDSIZEB DS    XL4                 SIZE USED IN BYTES, ON THE USERS VOL
MCDCSZ   DS    XL4                 SIZE OF THE DS IN 2K BLKS ON MIG
*                                  VOLUME
MCDNMIG  DS    XL2                 COUNT OF TIME DS WAS MIGRATED.
MCDDAYS  DS    XL2                 NUMBER OF DAYS BEFORE DS WILL BE
*                                  MIGRATED
MCDFRVSN DS    CL6                 VOLSER OF PRIMARY WHERE DATASET WAS
         ORG   MCDFRVSN
MCDOVSN  DS    CL6                 VOLSER TO WHICH DS WAS RECALLED
         DS    XL1
MCDMCL43 DS    XL1
MCDCTID3 DS    CL4                 NAME OF COMPACTION TABLE USED ON
*                                  THIS DS
MCDUCBTY DS    XL4                 UCBTYP OF "FROM" PRIMARY VOLUME.
MCDTRES  DS    CL4                 X'HHMMSSTH' - TIME DS WAS
*                                  RECALLED/DELETED
MCDDRES  DS    CL4                 X'00YYDDDS' - DATE DS WAS
*                                  RECALLED/DELETED
MCDMDEVT DS    XL4                 DEVICE TYPE OF MIGRATION VOLUME
MCDJDAYS DS    XL1
MCDJDATE DS    XL3                 LAST DATE TO WHICH HSM WILL DELAY
*                                  MIG
MCDJVEXD DS    XL3
MCDPDEP  DS    XL1
MCDJCT   DS    XL1                 COUNT OF VOLS ELIGIBLE FOR DIRECTED
*                                  RECALL
MCDJVOLS DS    0CL10               5 SLOTS - VOLUMES FOR DATASET RECALL
MCDJVSN  DS    CL6                 VOLSER TO WHICH DS CAN BE RECALLED
MCDJDEVT DS    XL4                 DEVICE TYPE OF ABOVE
         DS    4CL10
MCDRECAL DS    X                   FLAGS
MCDFDBA  EQU   B'10000000'         WHEN 0, DS MUST BE RECALLED TO MIG
*                                  VOL
MCDFRBU  EQU   B'01000000'
MCDFBDCS EQU   B'00100000'         DS MIGRATED FROM A BACKED-UP VOLUME
MCDFBDCT EQU   B'00010000'
MCDFAM   EQU   B'00001000'         DS MUST BE RECALLED TO AUTOMIG
*                                  VOLUME
MCDFAB   EQU   B'00000100'         DS MUST BE RECALLED TO AUTOBACKUP
*                                  VOL
MCDEXPDT DS    CL4                 X'00YYDDDS' - EXPDT WHEN MIGRATED
MCDMCANM DS    CL44                DATASET NAME OF MIGRATED VERSION
./       ADD   NAME=PASSTMC
         TITLE 'PROCESS CA1 TAPE MANAGEMENT CATALOG.'
PASSTMC  $PROLOG R12
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         USING SAVEFMT,R4
         L     R4,TABOFF           CURRENT OFFSET IN DS TABLE.
         OPEN  (TMC,INPUT)
TMCLOOP $AMODE 24
         GET   TMC
        $AMODE 31
         LR    R6,R1               TMC RECORD ADDRESS
         CLI   0(R6),X'FF'         IS IT A DSNB RECORD?
         BNE   TMCLOOP1            NO TRY FOR A DSND.
         BAL   R10,DODSNB
         B     TMCLOOP
TMCLOOP1 CLI   0(R6),C'A'          IS IT A DSND RECORD?
         BL    TMCLOOP             NO,
         CLI   0(R6),C'Z'          IS IT A DSND RECORD?
         BH    TMCLOOP             NO,
         BAL   R10,DODSND          YES, DO IT.
         B     TMCLOOP             PASS THE WHOLE TMC
ENDTMC  $AMODE 31
         CLOSE (TMC)
         $EPILOG
         TITLE 'PROCESS A TMC DSND RECORD.'
         USING TMCREC,R6
DODSND   XC    0(SAVELEN,R4),0(R4)  ZERO IT
         CLC   0(7,R6),=C'TMSCTL#' CONTROL RECORD?
         BER   R10                 YES, SKIP IT.
         TM    TMFLAG1,TMSCRTCH    SCRATCH VOLUME?
         BOR   R10                 YES, SKIP IT
         SPACE ,
         CLC   TMJOBNM(3),=C'HSM'  DID HSM CREATE THE TAPE.
         BER   R10                 IF SO, FORGET IT.
         TM    FLAGS4,ONEFILE      1 DATASET TAPES ONLY?
         BZ    DOTMCBM             NO.
         CLC   TM#DSNBS,=H'1'      STACKED TAPE?
         BHR   R10                 YES. SKIP IT.
DOTMCBM  TM    FLAGS1,LEVEL+NOLEVEL  DATASET SELECTION WANTED?
         BZ    DTMCB1A             IF NOT, PROCESS IT.
        $TEST  FLAGS1,LEVEL,LTAB,TMDSN,9,DTMCB1A,(R10)
        $TEST  FLAGS1,NOLEVEL,NLTAB,TMDSN,9,(R10),DTMCB1A
DTMCB1A  DS    0H
        $TEST  FLAGS3,CONTAIN,CTAB,(TMDSN,1,44),9,TMC1NC,(R10)
TMC1NC   DS    0H
        $TEST  FLAGS3,NOTCONT,NCTAB,(TMDSN,1,44),9,TMC1NC2,(R10)
TMC1NC2  DS    0H
        $TEST  FLAGS3,ENDING,ETAB,(TMDSN,,44),9,TMC1NE,(R10)
TMC1NE   DS    0H
        $TEST  FLAGS3,NOTEND,NETAB,(TMDSN,,44),9,TMC1NE1,(R10)
TMC1NE1  DS    0H
         TM    FLAGS3,UNUSED       UNUSED DATASETS ONLY?
         BZ    TMC11C              NO
         CLC   TMLASUSD,=XL3'00'   IS IT NEWLY CREATED?
         BER   R10                 IF SO, DON'T CALL IT OLD.
         CLC   TMLASUSD,DSCBOLD    HOW LONG HAS IT BEEN?
         BL    TMC11C              LONG ENOUGH.
         BR    R10                 OTHERWISE, SKIP IT.
TMC11C   DS    0H
         SPACE ,
         TM    FLAGS4,WITHIN       RECENTLY USED ONLY?
         BZ    TMC11D              NO.
         CP    WICOMP,TMLASUSD     IS IT RECENT ENOUGH?
         BHR   R10                 IF NOT SKIP IT.
         SPACE ,
TMC11D   DS    0H
         SPACE ,
         $TEST FLAGS5,CREATEBY,CREATJOB,TMJOBNM,9,TMC11E,(R10)
         SPACE ,
TMC11E   DS    0H
         $TEST FLAGS5,READBY,READJOB,TMLASUSJ,9,TMC11F,(R10)
         SPACE ,
TMC11F   DS    0H
         TM    FLAGS6,T3480        SELECT CARTRIDGES?
         BZ    TMC11G              NO. DON'T APPLY TEST.
         CLI   TMVOLSER,C'0'       CHECK VOLSER (MLI SPECIFIC)
         BLR   R10                 GET OUT IF NOT 3480.
TMC11G   DS    0H
         TM    FLAGS6,T3420        SELECT REELS?
         BZ    TMC11H              NO. DON'T APPLY TEST.
         CLI   TMVOLSER,C'0'       CHECK VOLSER (MLI SPECIFIC)
         BNLR  R10                 GET OUT IF NOT 3420.
TMC11H   DS    0H
         MVC   SV1DSNAM,TMDSN      DSNAME
         MVC   SV1DSSN,TMVOLSER    VOLUME SERIAL NUMBER
         MVC   SV1CREDT,TMCRTDT    CREATION DATE
         MVC   SV1EXPDT,TMEXPDT    EXPIRATION DATE
         MVC   SV1REFD,TMLASUSD    LAST DATE REFERENCED - SU 60
         MVC   SV1DSORG,=X'4000'   DSORG=PS
         MVC   SV1RECFM,=CL3' '    BLANK OUT RECFM.
         TM    TMRECFM,X'C0'       CHECK RECFM=U
         BNO   DSNDR2A             NO
         MVI   SV1RECFM,C'U'       SET RECFM VALUE IN PRINTLINE
         B     DSNDR2Z             TRY POSSIBLE 2ND BYTE.
DSNDR2A  TM    TMRECFM,X'40'       CHECK RECFM=V
         BZ    DSNDR2B             NO
         MVI   SV1RECFM,C'V'       SET RECFM VALUE IN PRINTLINE
         B     DSNDR2Z             TRY POSSIBLE 2ND BYTE.
DSNDR2B  TM    TMRECFM,X'80'       CHECK RECFM=F
         BZ    DSNDR2C             NO
         MVI   SV1RECFM,C'F'       SET RECFM VALUE IN PRINTLINE
         B     DSNDR2Z             TRY POSSIBLE 2ND BYTE.
DSNDR2C  MVI   SV1RECFM,C'?'       BEATS ME.
         B     DSNDR3Z             THAT'S ALL FOR RECFM
DSNDR2Z  TM    TMRECFM,X'20'       TRACK OVERFLOW
         BZ    DSNDR3A             NO
         MVI   SV1RECFM+1,C'T'     SET RECFM VALUE IN PRINTLINE
         B     DSNDR3Z             THAT'S ALL FOR RECFM.
DSNDR3A  TM    TMRECFM,X'08'       STANDARD BLOCKS
         BZ    DSNDR3B             NO
         MVI   SV1RECFM+1,C'S'     SET RECFM VALUE IN PRINTLINE
         B     DSNDR3Z             THAT'S ALL FOR RECFM.
DSNDR3B  TM    TMRECFM,X'10'       BLOCKED
         BZ    DSNDR3Z             NO
         MVI   SV1RECFM+1,C'B'     SET RECFM VALUE IN PRINTLINE
DSNDR3Z  DS    0H
         MVC   SV1BLKL,TMBLKSI     BLOCKSIZE
         MVC   SV1LRECL,TMLRECL    RECORD LENGTH
         MVC   SV1EXT1,TMBLKCNT    BLOCK COUNT
         MVI   SV1DEVC,C'T'        CALL IT A TAPE.
         SPACE ,
         MVI   SV1NOEPV,X'01'      FILESEQ=1
         SPACE ,
         MVC   SV1CJOB,TMJOBNM     CREATE JOBNAME.
         MVC   SV1STEP,TMSTPNAM    CREATE STEPNAME.
         SPACE ,
         TM    FLAGS4,SIZESEL      SIZE SELECTION?
         BZ    TMC1S2C             NO
         TM    FLAGS4,LARGER       WANT LARGEST ONLY?
         BZ    TMC1S2D             NO. MUST BE SMALLEST.
         CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.
         BNL   TMC1S2C             LARGE ENOUGH.
         BR    R10                 THROW IT BACK.
TMC1S2D  CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.
         BNH   TMC1S2C             SMALL ENOUGH
         BR    R10                 THROW IT BACK.
TMC1S2C  DS    0H
         SPACE ,
         LA    R4,SAVELEN(,R4)     NEXT DS TABLE ENTRY
         ST    R4,TABOFF           SAVE IT
         L     R1,DSCBS            COUNT OF DATASETS PROCESSED
         LA    R1,1(,R1)           UP BY 1
         ST    R1,DSCBS            AND SAVE IT.
         BR    R10                 GET OUT.
DTMCBCLC CLC   1(0,R2),TMDSN
         DROP  R6
         TITLE 'PROCESS A TMC DSNB RECORD.'
         USING DSNBREC,R6
DODSNB   XC    0(SAVELEN,R4),0(R4)  ZERO IT
         SPACE ,
         CLI   DSNBVSN,X'00'       DEAD RECORD?
         BER   R10                 YES. SKIP IT.
         SPACE ,
         TM    FLAGS4,ONEFILE      STACKED TAPES WANTED?
         BOR   R10                 IF NOT SKIP THIS ONE.
         TM    FLAGS1,LEVEL+NOLEVEL  DATASET SELECTION WANTED?
         BZ    DTMCD1A             IF NOT, PROCESS IT.
        $TEST  FLAGS1,LEVEL,LTAB,DSNBDSN,9,DTMCD1A,DTMCD2A
        $TEST  FLAGS1,NOLEVEL,NLTAB,DSNBDSN,9,DTMCD2A,DTMCD1A
DTMCD1A  DS    0H
        $TEST  FLAGS3,CONTAIN,CTAB,(DSNBDSN,1,44),9,TMC2NC,DTMCD2A
TMC2NC   DS    0H
        $TEST  FLAGS3,NOTCONT,NCTAB,(DSNBDSN,1,44),9,TMC2NC2,DTMCD2A
TMC2NC2  DS    0H
        $TEST  FLAGS3,ENDING,ETAB,(DSNBDSN,,44),9,TMC2NE,DTMCD2A
TMC2NE   DS    0H
        $TEST  FLAGS3,NOTEND,NETAB,(DSNBDSN,,44),9,TMC2NE1,DTMCD2A
TMC2NE1  DS    0H
         SPACE ,
TMC21D   DS    0H
         SPACE ,
         $TEST FLAGS5,CREATEBY,CREATJOB,DSNBCJN,9,TMC21E,(R10)
         SPACE ,
TMC21E   DS    0H
         MVC   SV1DSNAM,DSNBDSN    DSNAME
         MVC   SV1DSSN,DSNBVSN     VOLUME SERIAL NUMBER
         MVC   SV1CREDT,DSNBCRDT   CREATION DATE
         MVC   SV1EXPDT,DSNBEXDT   EXPIRATION DATE
         MVC   SV1DSORG,=X'4000'   DSORG=PS
         MVC   SV1RECFM,=CL3' '    BLANK OUT RECFM.
         TM    DSNBRFM,X'C0'       CHECK RECFM=U
         BNO   DSNBR2A             NO
         MVI   SV1RECFM,C'U'       SET RECFM VALUE IN PRINTLINE
         B     DSNBR2Z             TRY POSSIBLE 2ND BYTE.
DSNBR2A  TM    DSNBRFM,X'40'       CHECK RECFM=V
         BZ    DSNBR2B             NO
         MVI   SV1RECFM,C'V'       SET RECFM VALUE IN PRINTLINE
         B     DSNBR2Z             TRY POSSIBLE 2ND BYTE.
DSNBR2B  TM    DSNBRFM,X'80'       CHECK RECFM=F
         BZ    DSNBR2C             NO
         MVI   SV1RECFM,C'F'       SET RECFM VALUE IN PRINTLINE
         B     DSNBR2Z             TRY POSSIBLE 2ND BYTE.
DSNBR2C  MVI   SV1RECFM,C'?'       BEATS ME.
         B     DSNBR3Z             THAT'S ALL FOR RECFM
DSNBR2Z  TM    DSNBRFM,X'20'       TRACK OVERFLOW
         BZ    DSNBR3A             NO
         MVI   SV1RECFM+1,C'T'     SET RECFM VALUE IN PRINTLINE
         B     DSNBR3Z             THAT'S ALL FOR RECFM.
DSNBR3A  TM    DSNBRFM,X'08'       STANDARD BLOCKS
         BZ    DSNBR3B             NO
         MVI   SV1RECFM+1,C'S'     SET RECFM VALUE IN PRINTLINE
         B     DSNBR3Z             THAT'S ALL FOR RECFM.
DSNBR3B  TM    DSNBRFM,X'10'       BLOCKED
         BZ    DSNBR3Z             NO
         MVI   SV1RECFM+1,C'B'     SET RECFM VALUE IN PRINTLINE
DSNBR3Z  DS    0H
         MVC   SV1BLKL,DSNBBLKS    BLOCKSIZE
         MVC   SV1LRECL,DSNBLREC   RECORD LENGTH
         MVC   SV1EXT1,DSNBBLKC    BLOCK COUNT
         MVI   SV1DEVC,C'T'        CALL IT A TAPE.
         SPACE ,
         SR    R1,R1               CLEAR REGISTER.
         IC    R1,DSNBFSN+1        GET FILE SEQ NUMBER.
         STC   R1,SV1NOEPV         SAVE IN EXTENTS FIELD.
         SPACE ,
         MVC   SV1CJOB,DSNBCJN     CREATE JOBNAME.
         MVC   SV1STEP,DSNBCSN     CREATE STEPNAME.
         SPACE ,
TMC2S2   TM    FLAGS4,SIZESEL      SIZE SELECTION?
         BZ    TMC2S2C             NO
         TM    FLAGS4,LARGER       WANT LARGEST ONLY?
         BZ    TMC2S2D             NO. MUST BE SMALLEST.
         CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.
         BNL   TMC2S2C             LARGE ENOUGH.
         B     DTMCD2A             THROW IT BACK.
TMC2S2D  CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.
         BNH   TMC2S2C             SMALL ENOUGH
         B     DTMCD2A             THROW IT BACK.
TMC2S2C  DS    0H
         SPACE ,
DTMCD2   LA    R4,SAVELEN(,R4)     NEXT DS TABLE ENTRY
         ST    R4,TABOFF           SAVE IT
         L     R1,DSCBS            COUNT OF DATASETS PROCESSED
         LA    R1,1(,R1)           UP BY 1
         ST    R1,DSCBS            AND SAVE IT.
         SPACE ,
DTMCD2A  BR    R10                 GET OUT.
DTMCDCLC CLC   1(0,R2),DSNBDSN
         DROP  R4
         DROP  R6
         LTORG ,
TMC      DCB   DDNAME=TMC,                                             X
               DSORG=PS,                                               X
               MACRF=(GL),                                             X
               EODAD=ENDTMC,                                           X
               BUFNO=64,                                               X
               LRECL=200
         PRINT GEN
TMCREC   DSECT ,
         TMRECORD ,
DSNBREC  DSECT ,
         TMSDSNB ,
         PRINT NOGEN
./       ADD   NAME=PASSVMF
         TITLE 'PROCESS TLMS VOLUME MASTER FILE.'
PASSVMF  $PROLOG R12
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         USING SAVEFMT,R4
         L     R4,TABOFF           CURRENT OFFSET IN DS TABLE.
         OPEN  (VMF,INPUT)
VMFLOOP $AMODE 24                                                  @162
         GET   VMF
        $AMODE 31                                                  @162
         LR    R6,R1               VMF RECORD ADDRESS
         CLI   1(R6),C'B'          IS IT A BASE RECORD?
         BNE   PASSVMF1            NO,
         BAL   R10,DOVMFB          YES, DO IT.
         B     VMFLOOP             PASS THE WHOLE VMF
PASSVMF1 CLI   1(R6),C'D'          IS IT A DATASET RECORD?
         BNE   VMFLOOP             NO, THEN TO HELL WITH IT.
         BAL   R10,DOVMFD          PROCESS A D RECORD.
         B     VMFLOOP             PASS THE WHOLE VMF
ENDVMF  $AMODE 31
         CLOSE (VMF)
         $EPILOG
         TITLE 'PROCESS A VMF BASE RECORD.'
         USING VMFBASE,R6
DOVMFB   XC    0(SAVELEN,R4),0(R4)  ZERO IT
         CLI   BASRVSCR,C'1'       MUST BE IN SERVICE/NON SCRATCH
         BNER  R10                 IF NOT, GET OUT.
         SPACE ,
         CLC   BAFILESQ,=H'0'      NOT SURE WHAT THIS MEANS.       @170
         BER   R10                 BUT IT CAN'T BE ANYTHING GOOD!  @170
         SPACE ,
         TM    FLAGS4,DUMPVMF      ARE WE HEX PRINTING.            @160
         BZ    NODV1               NO                              @160
        $CALL  HEXVMF
NODV1    DS    0H                                                  @160
         SPACE ,
         TM    FLAGS4,DNSTY        SELECT BY DENSITY?              @160
         BZ    DVMFND1             NO.                             @160
         CLC   BADEN,DENSITY       IS IT THE RIGHT DENSITY?        @160
         BNER  R10                 NO. GET OUT.                    @160
DVMFND1  DS    0H                                                  @160
         SPACE ,
         CLC   BACJOB(3),=C'HSM'   DID HSM CREATE THE TAPE.
         BER   R10                 IF SO, FORGET IT.
         TM    FLAGS4,ONEFILE      1 DATASET TAPES ONLY?           @152
         BZ    DOVMFBM             NO.                             @152
         CLC   BAFILECT,=H'1'      STACKED TAPE?                   @152
         BHR   R10                 YES. SKIP IT.                   @152
DOVMFBM  TM    FLAGS1,LEVEL+NOLEVEL  DATASET SELECTION WANTED?
         BZ    DVMFB1A             IF NOT, PROCESS IT.
        $TEST  FLAGS1,LEVEL,LTAB,BADSN,9,DVMFB1A,(R10)
        $TEST  FLAGS1,NOLEVEL,NLTAB,BADSN,9,(R10),DVMFB1A
DVMFB1A  DS    0H
        $TEST  FLAGS3,CONTAIN,CTAB,(BADSN,1,44),9,VMF1NC,(R10)
VMF1NC   DS    0H
        $TEST  FLAGS3,NOTCONT,NCTAB,(BADSN,1,44),9,VMF1NC2,(R10)
VMF1NC2  DS    0H
        $TEST  FLAGS3,ENDING,ETAB,(BADSN,,44),9,VMF1NE,(R10)
VMF1NE   DS    0H
        $TEST  FLAGS3,NOTEND,NETAB,(BADSN,,44),9,VMF1NE1,(R10)
VMF1NE1  DS    0H
         TM    FLAGS3,UNUSED       UNUSED DATASETS ONLY?           @151
         BZ    VMF11C              NO                              @151
         CLC   BAIDATE,=XL3'00'    IS IT NEWLY CREATED?            @151
         BER   R10                 IF SO, DON'T CALL IT OLD.       @151
         CLC   BAIDATE,DSCBOLD     HOW LONG HAS IT BEEN?           @151
         BL    VMF11C              LONG ENOUGH.                    @151
         BR    R10                 OTHERWISE, SKIP IT.             @151
VMF11C   DS    0H                                                  @151
         SPACE ,
         TM    FLAGS4,WITHIN       RECENTLY USED ONLY?             @162
         BZ    VMF11D              NO.                             @162
         CP    WICOMP,BAIDATE      IS IT RECENT ENOUGH?            @162
         BHR   R10                 IF NOT SKIP IT.                 @162
         SPACE ,
VMF11D   DS    0H
         SPACE ,
         $TEST FLAGS5,CREATEBY,CREATJOB,BACJOB,9,VMF11E,(R10)      @162
         SPACE ,
VMF11E   DS    0H
         $TEST FLAGS5,READBY,READJOB,BAIJOB,9,VMF11F,(R10)         @162
         SPACE ,
VMF11F   DS    0H
         TM    FLAGS6,T3480        SELECT CARTRIDGES?              @166
         BZ    VMF11G              NO. DON'T APPLY TEST.           @166
         CLI   BAVSN,C'0'          CHECK VOLSER (MLI SPECIFIC)     @166
         BLR   R10                 GET OUT IF NOT 3480.            @166
VMF11G   DS    0H                                                  @166
         TM    FLAGS6,T3420        SELECT REELS?                   @166
         BZ    VMF11H              NO. DON'T APPLY TEST.           @166
         CLI   BAVSN,C'0'          CHECK VOLSER (MLI SPECIFIC)     @166
         BNLR  R10                 GET OUT IF NOT 3420.            @166
VMF11H   DS    0H                                                  @166
         MVC   SV1DSNAM,BADSN      DSNAME
         MVC   SV1DSSN,BAVSN       VOLUME SERIAL NUMBER
         MVC   SV1CREDT,BACDATE    CREATION DATE
         MVC   SV1EXPDT,BADEXPDT   EXPIRATION DATE
         MVC   SV1REFD,BAIDATE     LAST DATE REFERENCED - SU 60
         MVC   SV1DSORG,=X'4000'   DSORG=PS
         MVC   SV1RECFM,BARECFM    RECORD FORMAT
         MVC   SV1BLKL,BABLKSI     BLOCKSIZE
         MVC   SV1LRECL,BALRECL    RECORD LENGTH
         MVC   SV1EXT1,BABLKCT     BLOCK COUNT
         MVI   SV1DEVC,C'T'        CALL IT A TAPE.
         SPACE ,
         SR    R1,R1               CLEAR REGISTER.                 @152
         IC    R1,BAFILESQ+1       GET FILE SEQ NUMBER.            @152
         STC   R1,SV1NOEPV         SAVE IN EXTENTS FIELD.          @152
         SPACE ,
         MVC   SV1CJOB,BACJOB      CREATE JOBNAME.                 @160
         MVC   SV1STEP,BACSTEP     CREATE STEPNAME.                @160
         MVC   SV1ACCT,BAJOBACC+9  JOB ACCOUNT INFO.               @160
         MVC   SV1KEEP,BADKEEPD    TLMSII KEEP DATE.               @160
         MVC   SV1USES,BACLUSES    USES SINCE CLEANED.             @160
         MVC   SV1TOUSE,BATOUSES   USES SINCE PURCHASED.           @160
         MVC   SV1TMPER,BATMPRER   CURR TEMP READ ERRORS.          @160
         MVC   SV1LOC,BALOC        VOLUME LOCATION.                @160
         SPACE ,
         TM    FLAGS4,SIZESEL      SIZE SELECTION?                 @151
         BZ    VMF1S2C             NO                              @151
         TM    FLAGS4,LARGER       WANT LARGEST ONLY?              @151
         BZ    VMF1S2D             NO. MUST BE SMALLEST.           @151
         CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BNL   VMF1S2C             LARGE ENOUGH.                   @151
         BR    R10                 THROW IT BACK.                  @151
VMF1S2D  CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BNH   VMF1S2C             SMALL ENOUGH                    @151
         BR    R10                 THROW IT BACK.                  @151
VMF1S2C  DS    0H                                                  @151
         SPACE ,
         LA    R4,SAVELEN(,R4)     NEXT DS TABLE ENTRY
         ST    R4,TABOFF           SAVE IT
         L     R1,DSCBS            COUNT OF DATASETS PROCESSED
         LA    R1,1(,R1)           UP BY 1
         ST    R1,DSCBS            AND SAVE IT.
         BR    R10                 GET OUT.
DVMFBCLC CLC   1(0,R2),BADSN
         DROP  R6
         TITLE 'PROCESS A VMF DATASET RECORD.'
         USING VMFMDS,R6
DOVMFD   XC    0(SAVELEN,R4),0(R4)  ZERO IT
         SPACE ,
         TM    FLAGS4,DUMPVMF      ARE WE HEX PRINTING.            @160
         BZ    NODV2               NO                              @160
        $CALL  HEXVMF
NODV2    DS    0H                                                  @160
         SPACE ,
         TM    FLAGS4,DNSTY        DENSITY SELECTION?              @160
         BOR   R10                 IF SO, GET OUT.                 @160
         SPACE ,
         CLC   MDEXPIRE,=PL3'0'    HAS DATASET EXPIRED?            @111
         BNER  R10                 IF SO, GET OUT.                 @111
         CLC   MDRELPRE(8),ZERO8   IS THIS ONE CHAINED TO ANYBODY? @121
         BER   R10                 NO, SO SKIP THE POOR ORPHAN.    @121
         CLC   MDDSN(16),=C'TLMSII.DUMMY.DSN'  WHAT?
         BER   R10                 SKIP WIERD DSNAMES.
         TM    FLAGS4,ONEFILE      STACKED TAPES WANTED?           @152
         BOR   R10                 IF NOT SKIP THIS ONE.           @152
         TM    FLAGS1,LEVEL+NOLEVEL  DATASET SELECTION WANTED?
         BZ    DVMFD1A             IF NOT, PROCESS IT.
        $TEST  FLAGS1,LEVEL,LTAB,MDDSN,9,DVMFD1A,DVMFD2A
        $TEST  FLAGS1,NOLEVEL,NLTAB,MDDSN,9,DVMFD2A,DVMFD1A
DVMFD1A  DS    0H
        $TEST  FLAGS3,CONTAIN,CTAB,(MDDSN,1,44),9,VMF2NC,DVMFD2A
VMF2NC   DS    0H
        $TEST  FLAGS3,NOTCONT,NCTAB,(MDDSN,1,44),9,VMF2NC2,DVMFD2A
VMF2NC2  DS    0H
        $TEST  FLAGS3,ENDING,ETAB,(MDDSN,,44),9,VMF2NE,DVMFD2A
VMF2NE   DS    0H
        $TEST  FLAGS3,NOTEND,NETAB,(MDDSN,,44),9,VMF2NE1,DVMFD2A
VMF2NE1  DS    0H
         TM    FLAGS3,UNUSED       UNUSED DATASETS ONLY?           @151
         BZ    VMF21C              NO                              @151
         CLC   MDIDATE,=XL3'00'    IS IT NEWLY CREATED?            @151
         BER   R10                 IF SO, DON'T CALL IT OLD.       @151
         CLC   MDIDATE,DSCBOLD     HOW LONG HAS IT BEEN?           @151
         BL    VMF21C              LONG ENOUGH.                    @151
         BR    R10                 OTHERWISE, SKIP IT.             @151
VMF21C   DS    0H                                                  @151
         SPACE ,
         TM    FLAGS4,WITHIN       RECENTLY USED ONLY?             @162
         BZ    VMF21D              NO.                             @162
         CP    WICOMP,MDIDATE      IS IT RECENT ENOUGH?            @162
         BHR   R10                 IF NOT SKIP IT.                 @162
         SPACE ,
VMF21D   DS    0H
         SPACE ,
         $TEST FLAGS5,CREATEBY,CREATJOB,MDCJOB,9,VMF21E,(R10)      @162
         SPACE ,
VMF21E   DS    0H
         $TEST FLAGS5,READBY,READJOB,MDIJOB,9,VMF21F,(R10)         @162
         SPACE ,
VMF21F   DS    0H
         TM    FLAGS6,T3480        SELECT CARTRIDGES?              @166
         BZ    VMF21G              NO. DON'T APPLY TEST.           @166
         CLI   MDVSN,C'0'          CHECK VOLSER (MLI SPECIFIC)     @166
         BLR   R10                 GET OUT IF NOT 3480.            @166
VMF21G   DS    0H                                                  @166
         TM    FLAGS6,T3420        SELECT REELS?                   @166
         BZ    VMF21H              NO. DON'T APPLY TEST.           @166
         CLI   MDVSN,C'0'          CHECK VOLSER (MLI SPECIFIC)     @166
         BNLR  R10                 GET OUT IF NOT 3420.            @166
VMF21H   DS    0H                                                  @166
         MVC   SV1DSNAM,MDDSN      DSNAME
         MVC   SV1DSSN,MDVSN       VOLUME SERIAL NUMBER
         MVC   SV1CREDT,MDCDATE    CREATION DATE
         MVC   SV1EXPDT,MDEXPDT    EXPIRATION DATE
         MVC   SV1REFD,MDIDATE     LAST DATE REFERENCED - SU 60
         MVC   SV1DSORG,=X'4000'   DSORG=PS
         MVC   SV1RECFM,MDRECFM    RECORD FORMAT
         MVC   SV1BLKL,MDBLKSI     BLOCKSIZE
         MVC   SV1LRECL,MDLRECL    RECORD LENGTH
         MVC   SV1EXT1,MDBLKCT     BLOCK COUNT
         MVI   SV1DEVC,C'T'        CALL IT A TAPE.
         SPACE ,
         SR    R1,R1               CLEAR REGISTER.                 @152
         IC    R1,MDFILESQ+1       GET FILE SEQ NUMBER.            @152
         STC   R1,SV1NOEPV         SAVE IN EXTENTS FIELD.          @152
         SPACE ,
         MVC   SV1CJOB,MDCJOB      CREATE JOBNAME.                 @160
         MVC   SV1STEP,MDCSTEP     CREATE STEPNAME.                @160
         MVC   SV1ACCT,MDJACCT+9   JOB ACCOUNT INFO.               @160
         MVC   SV1KEEP,MDKEEPDT    TLMSII KEEP DATE.               @160
         SPACE ,
VMF2S2   TM    FLAGS4,SIZESEL      SIZE SELECTION?                 @151
         BZ    VMF2S2C             NO                              @151
         TM    FLAGS4,LARGER       WANT LARGEST ONLY?              @151
         BZ    VMF2S2D             NO. MUST BE SMALLEST.           @151
         CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BNL   VMF2S2C             LARGE ENOUGH.                   @151
         B     DVMFD2A             THROW IT BACK.                  @151
VMF2S2D  CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BNH   VMF2S2C             SMALL ENOUGH                    @151
         B     DVMFD2A             THROW IT BACK.                  @151
VMF2S2C  DS    0H                                                  @151
         SPACE ,
DVMFD2   LA    R4,SAVELEN(,R4)     NEXT DS TABLE ENTRY
         ST    R4,TABOFF           SAVE IT
         L     R1,DSCBS            COUNT OF DATASETS PROCESSED
         LA    R1,1(,R1)           UP BY 1
         ST    R1,DSCBS            AND SAVE IT.
DVMFD2A  LA    R6,132(,R6)         POSSIBLE 2ND DATASET.
         SPACE ,
         TM    FLAGS4,DUMPVMF      ARE WE HEX PRINTING.            @160
         BZ    NODV3               NO                              @160
        $CALL  HEXVMF
NODV3    DS    0H                                                  @160
         SPACE ,
         CLC   MDFILESQ,=H'0'      FILE SEQ NUMBER ZERO ?
         BER   R10                 IF SO, SKIP IT.
         SPACE ,
         CLC   MDEXPIRE,=PL3'0'    DATASET EXPIRED?
         BNER  R10                 THEN GET OUT.
         CLC   MDDSN(16),=C'TLMSII.DUMMY.DSN'  WHAT?
         BER   R10                 IF ITS NOT A FILE TAPE, SKIP IT.
         CLI   MDDSN,C' '          IS THERE ONE?
         BNHR  R10                 IF NOT, GET OUT
         XC    0(SAVELEN,R4),0(R4)  ZERO IT                        @120
         TM    FLAGS1,LEVEL+NOLEVEL  DATASET SELECTION WANTED?
         BZ    DVMFE1A             IF NOT, PROCESS IT.
        $TEST  FLAGS1,LEVEL,LTAB,MDDSN,9,DVMFE1A,(R10)
        $TEST  FLAGS1,NOLEVEL,NLTAB,MDDSN,9,(R10),DVMFE1A
DVMFE1A  DS    0H
        $TEST  FLAGS3,CONTAIN,CTAB,(MDDSN,1,44),9,VMF3NC,(R10)
VMF3NC   DS    0H
        $TEST  FLAGS3,NOTCONT,NCTAB,(MDDSN,1,44),9,VMF3NC2,(R10)
VMF3NC2  DS    0H
        $TEST  FLAGS3,ENDING,ETAB,(MDDSN,,44),9,VMF3NE,(R10)
VMF3NE   DS    0H
        $TEST  FLAGS3,NOTEND,NETAB,(MDDSN,,44),9,VMF3NE1,(R10)
VMF3NE1  DS    0H
         TM    FLAGS3,UNUSED       UNUSED DATASETS ONLY?           @151
         BZ    VMF31C              NO                              @151
         CLC   MDIDATE,=XL3'00'    IS IT NEWLY CREATED?            @151
         BER   R10                 IF SO, DON'T CALL IT OLD.       @151
         CLC   MDIDATE,DSCBOLD     HOW LONG HAS IT BEEN?           @151
         BL    VMF31C              LONG ENOUGH.                    @151
         BR    R10                 OTHERWISE, SKIP IT.             @151
VMF31C   DS    0H                                                  @151
         SPACE ,
         TM    FLAGS4,WITHIN       RECENTLY USED ONLY?             @162
         BZ    VMF31D              NO.                             @162
         CP    WICOMP,MDIDATE      IS IT RECENT ENOUGH?            @162
         BHR   R10                 IF NOT SKIP IT.                 @162
         SPACE ,
VMF31D   DS    0H
         SPACE ,
         $TEST FLAGS5,CREATEBY,CREATJOB,MDCJOB,9,VMF31E,(R10)      @162
         SPACE ,
VMF31E   DS    0H
         $TEST FLAGS5,READBY,READJOB,MDIJOB,9,VMF31F,(R10)         @162
         SPACE ,
VMF31F   DS    0H
         TM    FLAGS6,T3480        SELECT CARTRIDGES?              @166
         BZ    VMF31G              NO. DON'T APPLY TEST.           @166
         CLI   MDVSN,C'0'          CHECK VOLSER (MLI SPECIFIC)     @166
         BLR   R10                 GET OUT IF NOT 3480.            @166
VMF31G   DS    0H                                                  @166
         TM    FLAGS6,T3420        SELECT REELS?                   @166
         BZ    VMF31H              NO. DON'T APPLY TEST.           @166
         CLI   MDVSN,C'0'          CHECK VOLSER (MLI SPECIFIC)     @166
         BNLR  R10                 GET OUT IF NOT 3420.            @166
VMF31H   DS    0H                                                  @166
         MVC   SV1DSNAM,MDDSN      DSNAME
         MVC   SV1CREDT,MDCDATE    CREATION DATE
         MVC   SV1EXPDT,MDEXPDT    EXPIRATION DATE
         MVC   SV1REFD,MDIDATE     LAST DATE REFERENCED - SU 60
         MVC   SV1DSORG,=X'4000'   DSORG=PS
         MVC   SV1RECFM,MDRECFM    RECORD FORMAT
         MVC   SV1BLKL,MDBLKSI     BLOCKSIZE
         MVC   SV1LRECL,MDLRECL    RECORD LENGTH
         MVC   SV1EXT1,MDBLKCT     BLOCK COUNT
         MVI   SV1DEVC,C'T'        CALL IT A TAPE.
         SPACE ,
         SR    R1,R1               CLEAR REGISTER.                 @152
         IC    R1,MDFILESQ+1       GET FILE SEQ NUMBER.            @152
         STC   R1,SV1NOEPV         SAVE IN EXTENTS FIELD.          @152
         SPACE ,
         MVC   SV1CJOB,MDCJOB      CREATE JOBNAME.                 @160
         MVC   SV1STEP,MDCSTEP     CREATE STEPNAME.                @160
         MVC   SV1ACCT,MDJACCT+9   JOB ACCOUNT INFO.               @160
         MVC   SV1KEEP,MDKEEPDT    TLMSII KEEP DATE.               @160
         SPACE ,
         S     R6,=F'132'          BACKUP TO GET VOLSER.
         MVC   SV1DSSN,MDVSN       VOLUME SERIAL NUMBER
VMF3S2   TM    FLAGS4,SIZESEL      SIZE SELECTION?                 @151
         BZ    VMF3S2C             NO                              @151
         TM    FLAGS4,LARGER       WANT LARGEST ONLY?              @151
         BZ    VMF3S2D             NO. MUST BE SMALLEST.           @151
         CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BNL   VMF3S2C             LARGE ENOUGH.                   @151
         BR    R10                 THROW IT BACK.                  @151
VMF3S2D  CLC   SV1EXT1(4),TRKVALUE COMPARE SIZES.                  @151
         BNH   VMF3S2C             SMALL ENOUGH                    @151
         BR    R10                 THROW IT BACK.                  @151
VMF3S2C  DS    0H                                                  @151
         SPACE ,
DVMFD3   LA    R4,SAVELEN(,R4)     NEXT DS TABLE ENTRY
         ST    R4,TABOFF           SAVE IT
         L     R1,DSCBS            COUNT OF DATASETS PROCESSED
         LA    R1,1(,R1)           UP BY 1
         ST    R1,DSCBS            AND SAVE IT.
DVMFD3A  BR    R10                 GET OUT.
DVMFDCLC CLC   1(0,R2),MDDSN
TACONCLC CLC   1(0,R1),0(R7)       DS1DSNAM                        @151
TAENDCLC CLC   1(0,R1),0(R7)                                       @151
         DROP  R4                                                  @120
         DROP  R6
         LTORG ,
VMF      DCB   DDNAME=VMF,                                             X
               DSORG=PS,                                               X
               MACRF=(GL),                                             X
               EODAD=ENDVMF,                                           X
               BUFNO=30,                                           @121X
               LRECL=288                                           @121
ZERO8    DC    2F'0'                                               @121
./       ADD   NAME=PRINT
         SPACE 6
         TITLE 'XPRNTSUB - GENERAL PRINTING SUBROUTINE'
         SPACE
* FUNCTION/OPERATION:  THIS IS A GENERAL PRINTING SUBROUTINE TO MANAGE
*        THE DETAILS OF PAGINATION, HEADINGS, AND OUTPUT COMPRESSION
*        FOR PRINTED OUTPUT.  IT IS INVOKED FROM A PROBLEM PROGRAM VIA
*        THE MACRO 'XPRNT', WHICH HAS THE FOLLOWING CALL MODES:
*           OPEN - INITIALIZES PAGE SIZE PARAMETERS AND MAXIMUM NUMBER
*              OF OUTPUT LINES, RESETS PAGE NUMBER, CLEARS HEADING
*              LINES AND INSERTS CURRENT DATE AND TIME, INSERTS DDNAME
*              IN DCB, AND OPENS THE DATA SET.
*           MODIFY - ALTERS ANY OR ALL OF THE PARAMETERS LISTED UNDER
*              'OPEN' EXCEPT THE DDNAME.
*           SETHEAD - SUPPLIES TEXT FOR ONE OR MORE LINES TO BE USED
*              PAGE HEADINGS.
*           PRINT - DELIVERS ONE OR MORE LINES TO BE PRINTED.
*           SPACE - MOVES THE PAGE VERTICALLY A GIVEN NUMBER OF LINES.
*           EJECT - CONDITIONAL OR UNCONDITIONAL SKIP TO HEAD-OF-FORM.
*           CLOSE - CLOSES THE DATA SET.
         SPACE
* ENTRY POINTS:  EACH MODE HAS A SEPARATE ENTRY AT A FIXED OFFSET FROM
*        THE BASE ADDRESS.  ENTRYS ARE EFFECTED BY A  BAL  14,D(15)
*        (WHERE D IS A DISPLACEMENT WHICH SELECTS THE DESIRED MODE) AND
*        ASSUMES THAT REGISTER 13 ADDRESSES A STANDARD 18-WORD SAVE
*        AREA.
         SPACE
* INPUT:  ON ENTRY, REG 0 POINTS TO A WORK AREA FOR THE DATA SET, WHICH
*        IS DESCRIBED IN THIS CODING BY THE DUMMY SECTION 'WORKAREA'.
*        REG 1 POINTS TO A PARAMETER LIST OF A FORM APPROPRIATE TO THE
*        CALL MODE.  THESE LISTS ARE EITHER DESCRIBED BY DUMMY SECTIONS
*        OR ARE STANDARD VARIABLE-LENGTH ADDRESS LISTS.
         SPACE
         SPACE
* OUTPUT:  PRINT LINES AND SPACE CONTROL RECORDS ARE DELIVERED VIA QSAM
*        TO A SEQUENTIAL DATA SET.  EACH LINE IS REDUCED TO ITS MIN-
*        IMUM LENGTH BY TRUNCATING TRAILING BLANKS.
         SPACE
* DATA SETS:  THE OUTPUT DATA SET IS SEQUENTIAL, ARRANGED FOR BLOCKED
*        FORMAT V RECORDS USING MACHINE CONTROL CHARACTERS APPROPRIATE
*        TO 1403 OR 1404 PRINTERS.  THE DDNAME IS SUPPLIED BY THE
*        'OPEN' CALL; THE STANDARD VALUE IS 'SYSPRINT'.
         SPACE
* EXTERNAL ROUTINES:  CALLS 'XDATEDIT' TO FORMAT THE DATE FOR THE
*        PAGE HEADING LINE.
         SPACE
* EXITS - NORMAL:  RETURNS VIA REG 14 WITH RETURN CODE 0 IN REG 15.
*        THE CURRENT PAGE NUMBER (IN BINARY) IS IN REG 0 AND THE BINARY
*        NUMBER OF LINES REMAINING ON THE CURRENT PAGE IS IN REG 1.
*        IF AN OPEN CALL CANNOT OPEN THE DCB, THE RETURN CODE IS 4.
         SPACE
* EXITS - ERROR:  ERRONEOUS CALLS RESULT IN AN 'ABEND' WITH ONE OF THE
*        FOLLOWING COMPLETION CODES:
*           4000 - ROUTINE WAS ILLEGALLY ENTERED AT ITS BASE ADDRESS.
*           4002 - A CALL MODE OTHER THAN 'OPEN' WAS DIRECTED TO A DATA
*              SET WHICH WAS NOT OPEN.
*           4003 - 'OPEN' OR 'MODIFY' SPECIFIED TOO LARGE A PAGE WIDTH.
*           4004 - 'OPEN' OR 'MODIFY' SPECIFIED TOO LONG A PAGE.
*           4005 - 'PRINT' OR 'SETHEAD' SPECIFIED A LINE WHOSE TEXT
*              LENGTH PLUS OFFSET EXCEEDED THE PAGE WIDTH.
*           4006 - THE MAXIMUM NUMBER OF OUTPUT LINES WAS EXCEEDED.
*           4007 - SYNCHRONOUS I/O ERROR.
         SPACE
* TABLES/WORK AREAS:  THE CALL PARAMETER LISTS, WORK AREA, AND PRINT
*        LINE DESCRIPTOR BLOCK ARE ALL DESCRIBED BY DUMMY SECTIONS
*        AT THE END OF THE PROGRAM.
         SPACE
* ATTRIBUTES:  REENTRANT, READ-ONLY.  WILL HANDLE ANY NUMBER OF DATA
*        SETS SIMULTANEOUSLY, USING A SEPARATE WORK AREA FOR EACH.
         SPACE
* OPERATING ENVIRONMENT:  RUNS UNDER OPERATING SYSTEM/360, USING
*        QSAM DATA MANAGEMENT WITH LOCATE-MODE PUT MACROS AND SIMPLE
*        BUFFERING.  ASSUMES OPTION 6A OR 6B FOR TIME OF DAY.
         SPACE
* NOTES:  NONE.
         EJECT
XPRNTSUB CSECT
         SPACE 2
* REGISTER ASSIGNMENTS:
         SPACE 2
* N.B.  REGISTER GROUPS 'RWA,RWB,RWC,RWD' AND 'RLPEXIT,RPARM' MUST BE
* KEPT IN ORDER FOR LM AND STM ORDERS.
         SPACE
RWD      EQU   5                   GENERAL WORK REG
RSPACE   EQU   6                   NUMBER OF LINES TO SPACE
RBAL     EQU   7                   NUMBER OF LINES LEFT ON PAGE
RLPEXIT  EQU   8                   EXIT FROM LIST PROCESSING ROUTINE
RPLD     EQU   10                  PLD BLOCK BASE
RWKAREA  EQU   11                  BASE REGISTER FOR WORK AREA
PRRET    EQU   14                  STANDARD RETURN ADDR.
         SPACE 3
* MASKS FOR CONTROL BYTE "SWITCHES":
         SPACE
HOFBIT   EQU   X'80'               PAGE IS AT PHYSICAL HEAD-OF-FORM
NOSKBIT  EQU   X'40'               DISALLOW EJECT DURING HEAD PRINTING
TXT1BIT  EQU   X'20'               ON FOR FIRST TEXT LINE ON PAGE
HD1BIT   EQU   X'10'               ON FOR FIRST HEAD LINE WITH PAGE
*                                  NO, ETC
SP0BIT   EQU   X'08'               LAST LINE SPACED 0
         SPACE 2
EJFORCE  EQU   255                 SPACING QUANTITY TO FORCE PAGE EJECT
MAXLNGTH EQU   254                 MAXIMUM PAGE LENGTH
MAXWIDTH EQU   132                 MAXIMUM PAGE WIDTH
DEFBLKSZ EQU   1692                DEFAULT DCB BLOCK SIZE
         EJECT
**********************
* ENTRY AND PROLOGUE *
**********************
         SPACE
* ENTER FROM THE CALLING SEQUENCE VIA THE INSTRUCTION "BAL 14,X(0,15)".
* REGISTER 15 IS SET TO THE BASE OF THE ROUTINE AND THE DISPLACEMENT
* "X" SELECTS THE ENTRY POINT.  EACH ENTRY SAVES REGISTERS, LOADS THE
* ADDRESS OF THE APPROPRIATE SUBROUTINE, AND GOES TO A COMMON
* PROLOGUE.  NOTE THAT AN ATTEMPT TO ENTER AT THE BASE RESULTS IN
* AN ABEND.
         SPACE 2
BASEADDR B     20(0,R15)           BRANCH AROUND MODULE IDENTIFIER
         DC    AL1(15)             LENGTH OF IDENTIFIER
         DC    CL15'XPRNTSUB-028214'
         STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         SR    R1,R1               SET ABEND COMPLETION CODE
         LR    RBASE,R15           SET BASE REG FOR ABEND ROUTINE
         USING BASEADDR,RBASE
         B     ABEND
         DROP  RBASE
         SPACE 2
* ENTRY POINTS FOR VARIOUS FUNCTIONS, AT 12-BYTE INTERVALS BEGINNING
* AT BASEADDR+32.  EACH LOADS "RWA" WITH THE ADDRESS OF THE SUBROUTINE
* TO GO TO AFTER EXECUTING "PROLOGUE".
         SPACE
         ORG   XPRNTSUB+32
         USING BASEADDR,R15
         SPACE
PRENTRY  STM   R14,R12,12(R13)     PRINT
         LA    RWA,PRPROC
         B     PROLOGUE
SPENTRY  STM   R14,R12,12(R13)     SPACE
         LA    RWA,SPPROC
         B     PROLOGUE
OPENTRY  STM   R14,R12,12(R13)     OPEN
         LA    RWA,OPPROC
         B     PROLOGUE
CLENTRY  STM   R14,R12,12(R13)     CLOSE
         LA    RWA,CLPROC
         B     PROLOGUE
SHENTRY  STM   R14,R12,12(R13)     SETHEAD
         LA    RWA,SHPROC
         B     PROLOGUE
EJENTRY  STM   R14,R12,12(R13)     EJECT
         LA    RWA,EJPROC
         B     PROLOGUE
MDENTRY  STM   R14,R12,12(R13)     MODIFY
         LA    RWA,MDPROC
         B     PROLOGUE
         SPACE 2
* PROLOGUE.  SET UP BASE REGISTERS FOR CODING AND WORK AREA.  CHAIN
* SAVE AREAS IN STANDARD FASHION.  LOAD "RBAL" WITH CURRENT PAGE
* BALANCE AND "RPARM" WITH THE CALLING PARAMETER LIST ADDRESS.
         SPACE
PROLOGUE LR    RBASE,R15           LOAD CODING BASE REG
         USING BASEADDR,RBASE
         DROP  R15
         SPACE
         LR    RWKAREA,R0          BASE REG FOR WORK AREA
         USING WORKAREA,RWKAREA
         SPACE
         LR    RWB,R13             ADDR OF CALLER'S SAVE AREA
         LA    R13,SAVEAREA        ADDR OF OUR SAVE AREA
         ST    R13,8(RWB)          CHAIN TO US FROM CALLER
         ST    RWB,SAVEAREA+4      CHAIN TO CALLER FROM US
         SPACE
        $AMODE 24,REG=RWB,SAVE=PRAMODE                             @162
         SPACE
         LR    RPARM,R1            LOAD PARAMETER LIST ADDR
         LH    RBAL,PAGEBAL        LOAD PAGE LINE COUNT BALANCE
         MVC   DCBSYNAD(3),=AL3(SYNAD)  PUT ERROR ROUTINE ADDRESS IN
*                                  DCB
         SPACE
         TM    DCBOFLGS,OPENBIT    MAKE SURE DATA SET IS OPEN
         BCR   1,RWA               BRANCH IF OPEN
         CL    RWA,=A(OPPROC)      IF NOT, ONLY OPEN CALL ALLOWED
         BCR   8,RWA               BR IF OPEN CALL
         LA    R1,2                ERROR CODE 2, GO TO ABEND
         SPACE 3
* ABEND ROUTINE FOR SERIOUS ERRORS.  ENTER WITH ERROR NUMBER IN R1.
* ADD DECIMAL 4000 FOR USER CONPLETION CODE AND ISSUE 'ABEND' WITH DUMP
         SPACE
         CNOP  2,4                 ALIGN CONSTANT FOLLOWING SVC
ABEND    AL    R1,*+6              LOAD ABEND CODES
         SVC   13                  ISSUE ABEND SVC
         DC    X'80'               'DUMP' BIT FOR ABEND
         DC    AL3(4000)           BASE FOR COMPLETION CODE
         EJECT
************
* EPILOGUE *
************
         SPACE
* ALL MODES RETURN TO THE CALLER VIA THIS ROUTINE.  IT EXITS WITH
* THE RETURN CODE IN REGISTER 15, THE PAGE NUMBER IN REGISTER 0, AND
* THE NUMBER OF LINES REMAINING ON THE PAGE IN REGISTER 1.
         SPACE 2
EXIT4    LA    R15,4               SET RETURN CODE 4
         B     EXIT
         SPACE
EXIT0    SR    R15,R15             SET RETURN CODE 0
         SPACE
EXIT     LH    R0,PAGENO           LOAD CURRENT PAGE NUMBER
         LR    R1,RBAL             LOAD NUMBER OF LINES LEFT
         STH   RBAL,PAGEBAL        SAVE PAGE BALANCE
         XC    DCBSYNAD(3),DCBSYNAD  CLEAR ERROR ROUTINE ADDRESS
         SPACE
        $AMODE RESET=PRAMODE       RETURN TO CALLER'S AMODE.       @162
         L     R13,SAVEAREA+4      ADDRESS OF CALLER'S SAVE AREA
         L     R14,12(R13)         LOAD RETURN ADDRESS
         LM    R2,R12,28(R13)      RESTORE CALLER'S REGISTERS
         MVI   12(R13),X'FF'       SET RETURN INDICATION
         BR    R14                 RETURN TO CALLER                @162
         EJECT
******************
* PRINT FUNCTION *
******************
         SPACE
* PRINT LINE DELIVERY CALL.  THE PARAMETER LIST IS A VARIABLE-LENGTH
* LIST OF ADDRESSES, THE LAST OF WHICH IS NEGATIVE (I.E. BIT 0 IS A 1).
* EACH ADDRESS POINTS TO A PLD DESCRIBING A LINE TO BE PRINTED.  CALL
* THE GENERAL LIST PROCESSING ROUTINE TO DO THE DIRTY WORK.
         SPACE 2
PRPROC   BAL   RLPEXIT,LISTPROC    INVOKE LIST PROCESSOR
         SPACE
         B     EXIT0               EXIT TO CALLER
         EJECT
******************
* SPACE FUNCTION *
******************
         SPACE
* CALL TO MOVE THE PAPER A SPECIFIED NUMBER OF LINES.  IF THE NUMBER
* OF LINES EXCEEDS THE CURRENT PAGE BALANCE, AN EJECT RESULTS, UNLESS
* SUPPRESSED BY THE 'SPNOEJ' BIT IN THE PARAMETER LIST.  IF THE PAGE
* IS AT HEAD-OF-FORM, SPACING IS SUPPRESSED UNLESS THE 'SPATHOF' BIT
* IS SET IN THE PARAMETER LIST.  IN THE LATTER CASE, THE PAGE HEADINGS
* WILL BE PRINTED BEFORE THE SPACING IS EXECUTED.
         SPACE 2
         USING SPPARM,RPARM        ADDRESSING FOR PARM LIST
         SPACE
SPPROC   TM    SWITCHES,HOFBIT     TEST IF AT HEAD-OF-FORM
         BZ    SPSUBTR             BR IF NOT
         TM    SPCOND,SPATHOF      IS SPACE ALLOWED AT HOF?
         BZ    EXIT0               EXIT IF NOT
         BAL   PRRET,HEADPRNT      IF SO, PRINT HEADINGS FIRST
         SPACE
SPSUBTR  SR    RSPACE,RSPACE
         IC    RSPACE,SPQUAN       LOAD SPACING AMOUNT
         SR    RBAL,RSPACE         COMPUTE NEW PAGE BALANCE
         SPACE
         BP    SPMOVE              BRANCH IF BAL STILL > 0
         TM    SPCOND,SPNOEJ       TEST IF EJECT ALLOWED
         BO    SPMOVE              BRANCH IF NOT
         LA    RSPACE,EJFORCE      IF ALLOWED, FORCE EJECT
         SPACE
SPMOVE   BAL   RWA,MOVE            CALL PAPER MOVING SUB
         B     EXIT0               EXIT TO CALLER
         SPACE
         DROP  RPARM
         EJECT
******************
* EJECT FUNCTION *
******************
         SPACE
* CALL TO MOVE THE PAPER TO HEAD-OF-FORM.  IF THE PAGE IS ALREADY
* THERE, THE EJECT IS NOT EXECUTED UNLESS THE 'EJATHOF' BIT IN THE
* PARAMETER LIST IS ON.  IN THE LATTER CASE, A SKIP IS EXECUTED WITHOUT
* PRINTING ANY HEADINGS, RESULTING IN A BLANK PAGE.  IF THE MASK BITS
* IN THE 'EJCOND' PARAMETER ARE NOT ALL ONES, A CONDITIONAL EJECT IS
* REQUESTED.  THE PAGE BALANCE IS COMPARED AGAINST THE TEST QUANTITY
* 'EJQUAN' IN THE PARAMETER LIST, THEN THE DECISION TO EJECT OR NOT
* IS MADE WITH A 'BC' ORDER USING THE 'EJCOND' BITS AS A MASK.
         SPACE 2
         USING EJPARM,RPARM        FOR PARM LIST ADDRESSING
         SPACE
EJPROC   TM    SWITCHES,HOFBIT     TEST IF AT HOF
         BZ    EJTEST              BR IF NOT
         TM    EJCOND,EJATHOF      IF SO, IS EJECT ALLOWED?
         BZ    EXIT0               IF NOT, EXIT
         SPACE
EJTEST   SR    R0,R0
         IC    R0,EJQUAN           LOAD TEST QUANTITY
         IC    R1,EJCOND           GET CONDITIONAL TEST MASK
         N     R1,=XL4'000000F0'   KEEP BITS 24-27 ONLY
         CR    RBAL,R0             COMPARE BALANCE WITH TEST QUAN
         EX    R1,EJBC             EXECUTE BC WITH MASK FROM PARM
         B     EXIT0               EXIT IF CONDITION NOT MET
         SPACE
EJMOVE   LA    RSPACE,EJFORCE      TO FORCE EJECT
         BAL   RWA,MOVE            CALL PAPER MOVER SUB
         B     EXIT0               EXIT TO CALLER
         SPACE 2
EJBC     BC    0,EJMOVE            BRANCH IF EJECT REQUIRED
         SPACE
         DROP  RPARM
         EJECT
*****************
* OPEN FUNCTION *
*****************
         SPACE
* THIS MUST BE THE FIRST CALL FOR A NEW DATA SET.  IT SETS THE DDNAME
* IN THE DCB AND OPENS THE DATA SET.   ALL COUNTERS AND SWITCHES ARE
* SET TO THEIR NOMINAL VALUES BEFORE PROCESSING ANY OPTIONS WHICH
* MAY HAVE BEEN SUPPLIED WITH THE CALL.  THE PAGE BALANCE IS SET TO
* ZERO, BUT THE PAGE IS NOT MOVED.  THE PROGRAMMER SHOULD NORMALLY
* ISSUE AN 'EJECT' CALL BEFORE BEGINNING HIS OUTPUT.
         SPACE
* IF THE DATA SET IS ALREADY OPEN, THE DDNAME IS IGNORED, THE NOMINAL
* OPTIONS ARE SET, THEN THIS CALL IS TREATED EXACTLY AS IF IT WERE A
* 'MODIFY' CALL.
         SPACE 2
         USING OMPARM,RPARM
         SPACE
OPPROC   MVC   PAPARMS(DEFSIZE),DEFAULTS  SET STANDARD OPTIONS
         MVI   HLTEXT,C' '         CLEAR HEADING TEXT
         MVC   HLTEXT+1(HLTXTLNG-1),HLTEXT
         SPACE
         TIME  DEC                 GET CURRENT DATE & TIME
         STM   R0,R1,HPRSAVE       SAVE TIME AND DATE
         SPACE
* NOW IF DATA SET IS ALREADY OPEN, GO TO 'MDPROC'.  OTHERWISE, INSERT
* DDNAME AND OPEN IT.
         SPACE
         TM    DCBOFLGS,OPENBIT    TEST IF OPEN
         BO    OPJOIN              BR TO 'MDPROC' IF OPEN
         MVC   DCBDDNAM(8),OMDDNAME  INSERT DDNAME INTO CLOSED DCB
         MVC   DCBEXLST(3),=AL3(EXLST)  INSERT ADDRESS OF EXIT LIST
         SPACE
         MVI   DWORK,X'80'         SET END-OF-LIST BIT
         OPEN  (PRINTDCB,(OUTPUT,LEAVE)),MF=(E,DWORK)
         SPACE
         XC    DCBEXLST(3),DCBEXLST  CLEAR ADDRESS OF EXIT LIST
         TM    DCBOFLGS,OPENBIT    TEST FOR SUCCESSFUL OPEN
         BO    OPJOIN              TO 'MDPROC' IF O.K.
         B     EXIT4               EXIT WITH RC=4 IF OPEN FAILS
         SPACE
         DROP  RPARM
         EJECT
* DCB EXIT ROUTINE FOR OPEN.  SET BLOCK SIZE TO THE DEFAULT VALUE IF
* NOT SPECIFIED IN DS LABEL OR DD CARD.
         SPACE
EXLST    DS    0F                  EXIT LIST, ON BOUNDARY
         DC    X'85'
         DC    AL3(DCBMOD)
         SPACE
DCBMOD   LH    RWA,DCBBLKSZ        LOAD BLOCK SIZE
         LTR   RWA,RWA             TEST IF ZERO
         BNZ   DCBNZ               BRANCH IF NOT ZERO
SETDEF   LA    RWA,DEFBLKSZ        SET DEFAULT BLOCK SIZE
         STH   RWA,DCBBLKSZ        STORE IN DCB
         BR    R14                 RETURN TO OPEN ROUTINE
DCBNZ    CH    RWA,=H'141'         TEST FOR MINIMUM BLOCKSIZE
         BL    SETDEF              USE DEFAULT IF TOO LOW
         BR    R14                 RETURN TO OPEN IF OK
         EJECT
*******************
* MODIFY FUNCTION *
*******************
         SPACE
* THIS CALL MAY ALTER ANY  OR ALL OF THE FOLLOWING: DATE, TIME, PAGE
* NUMBER, PAGE LENGTH, PAGE WIDTH, MAXIMUM NUMBER OF OUTPUT LINES.
* THE NEW VALUES ARE PRESENTED IN A PARAMETER LIST.  IF A PARAMETER IS
* ZERO, THE CORRESPONDING QUANTITY IS NOT ALTERED.
         SPACE
* THE DATE AND TIME FIELDS MAY BE SUPPLIED BY THE 'OPEN' ROUTINE,
* WHICH JOINS THIS CODING AT 'OPJOIN'.  IN THAT CASE, THE VALUES
* INSERTED BY 'OPEN' ARE USED UNLESS OVERRIDDEN BY THE PARAMETER LIST.
         SPACE
         USING OMPARM,RPARM
         SPACE
MDPROC   SR    R0,R0               GENERATE TWO ZERO WORDS
         SR    R1,R1
         STM   R0,R1,HPRSAVE       SET DATE & TIME FIELDS TO ZERO
         SPACE
* ENTER HERE FROM 'OPEN'.  THE OBJECT OF THE FOLLOWING IS TO GET THE
* ACTUAL TIME AND DATE (IN PACKED DECIMAL) INTO 'HPRSAVE' AND
* 'HPRSAVE+4', RESPECTIVELY.  THOSE FIELDS MAY HAVE BEEN FILLED IN BY
* THE OPEN ROUTINE, OR THEY MAY HAVE BEEN SET TO ZEROS BY 'MDPROC'
* ABOVE.  NOW WE SEE IF ADDRESSES FOR TIME AND/OR DATE VALUES WERE
* PROVIDED IN THE PARAMETER LIST, AND TRANSFER THE VALUES IF SO.
         SPACE
OPJOIN   LM    RWA,RWB,OMTIMEAD    LOAD TIME & DATE ADDRS FROM PARM
         LTR   RWA,RWA             TEST FOR TIME ADDR SUPPLIED
         BZ    NOTIME              BRANCH IF ZERO
         MVC   HPRSAVE(4),0(RWA)   GET TIME AS DECIMAL 'HHMMSSTH'
NOTIME   LTR   RWB,RWB             TEST IF DATE ADDR SUPPLIED
         BZ    DATEEDIT            BR IF NOT
         MVC   HPRSAVE+4(4),0(RWB)  GET DECIMAL DATE AS '00YYDDD+'
         SPACE
* IF THE DATE VALUE IS NOT ZERO, EDIT INTO THE HEADING LINE TEXT.  BOTH
* THE DAY NUMBER AND THE MONTH-DAY-YEAR FORM ARE USED.
         SPACE
DATEEDIT LM    RWA,RWB,HPRSAVE     LOAD TIME & DATE FOR TESTING
         LTR   RWB,RWB             TEST IF DATE WAS GIVEN
         BZ    TIMEEDIT            BRANCH IF ZERO
         SPACE
         MVC   HLDAY(4),=C'DAY='   INSERT DAY NUMBER
         UNPK  HLDAY+4(3),HPRSAVE+6(2)  UNPACK DAY NUMBER
         OI    HLDAY+6,X'F0'       TIDY UP SIGN BITS
         SPACE
         ST    RWB,DWORK+4         STORE DATE ARG FOR EDIT RTNE
         LA    R1,DWORK            CONSTRUCT ONE-WORD PARM LIST
         ST    R1,HPRSAVE+4        FOR XDATEDIT CALL
         LA    R1,HPRSAVE+4        LOAD PARM LIST ADDRESS
         L     R15,=V(XDATEDIT)    LOAD ADDRESS OF DATE EDIT RTNE
         BALR  R14,R15             TO EDIT DATE AS 'MM/DD/YY'
         MVC   HLDATE(8),DWORK     INSERT RESULT IN HL TEXT
         SPACE
* EDIT TIME IN HOURS AND MINUTES, IF SUPPLIED.
         SPACE
TIMEEDIT LTR   RWA,RWA             TEST IF TIME GIVEN
         BZ    TSTPGNO             BR IF ZERO
         SRL   RWA,4               ADD LEADING ZERO TO TIME
         ST    RWA,HPRSAVE         STORE TIME IN WORK LOC
         MVC   HLTIME(7),=X'402120207A2020'  EDIT CONTROL CHAR
         ED    HLTIME(7),HPRSAVE   EDIT INTO ALPHA
         SPACE
* MODIFY PAGE NUMBER.  VALUE GIVEN IS DECREMENTED BY ONE, SINCE IT IS
* INCREMENTED BEFORE USE.
         SPACE
TSTPGNO  LH    RWA,OMPAGENO        GET VALUE FROM PARM LIST
         LTR   RWA,RWA             TEST IF ZERO
         BZ    TSTPGWID            BR IF ZERO
         BCTR  RWA,0               DECREMENT BY 1
         STH   RWA,PAGENO          STORE
         SPACE
* MODIFY PAGE WIDTH, TESTING FOR MAXIMUM ALLOWED.
         SPACE
TSTPGWID SR    RWA,RWA
         IC    RWA,OMPAGWID        GET WIDTH FROM PARM LIST
         LTR   RWA,RWA             TEST IF ZERO
         BZ    TSTPGLNG            BRANCH IF SO
         CLI   OMPAGWID,MAXWIDTH   TEST MAXIMUM SIZE
         BNH   PGWIDOK
         LA    R1,3                ERROR, TOO LARGE
         B     ABEND
PGWIDOK  STH   RWA,PAGWIDTH
         SPACE
* MODIFY PAGE LENGTH, TESTING FOR MAXIMUM VALUE.
         SPACE
TSTPGLNG SR    RWA,RWA
         IC    RWA,OMPAGLNG        GET FROM PARM LIST
         LTR   RWA,RWA             TEST IF SUPPLIED
         BZ    TSTMAXLN            BR IF NOT
         CLI   OMPAGLNG,MAXLNGTH   TEST FOR LEGAL VALUE
         BNH   PGLNGOK
         LA    R1,4                ERROR, ABORT
         B     ABEND
PGLNGOK  STH   RWA,PAGELNG         STORE
         SPACE
* MODIFY PRINT LINE LIMIT COUNTER.
         SPACE
TSTMAXLN L     RWA,OMMAXLIN        GET FROM PARM LIST
         LTR   RWA,RWA             TEST IF SUPPLIED
         BZ    EXIT0               EXIT IF NOT
         ST    RWA,MAXLINES        STORE
         B     EXIT0               EXIT TO CALLER
         SPACE
         DROP  RPARM
         EJECT
********************
* SETHEAD FUNCTION *
********************
         SPACE
* THIS CALL PRESENTS A LIST OF LINES WHICH WILL BE USED AS PAGE HEADING
* AND SUB-HEADING LINES.  THE PARAMETER LIST IS A VARIABLE-LENGTH LIST
* OF PLD ADDRESSES, EXACTLY AS FOR 'PRINT' CALLS.  THE FIRST LINE WILL
* HAVE ADDED TO ITS RIGHT END THE DATE, TIME, AND PAGE NUMBER.
* HOWEVER, IF ITS LENGTH EXCEEDS 98 BYTES, IT WILL OVERLAY THOSE
* FIELDS.  THE SECOND AND SUBSEQUENT LINES WILL BE PRINTED NORMALLY.
* NOTE THAT THESE LINES ARE NOT PRINTED WHEN THE PAGE IS EJECTED, BUT
* WHEN THE LINE WHICH WILL BE FIRST ON THE NEW PAGE IS DELIVERED, THUS
* ALLOWING THE HEADINGS TO BE ALTERED AFTER DELIVERY OF THE LAST LINE
* ON THE PRECEDING PAGE.  THE ADDRESS OF THE PARAMETER LIST IS SAVED
* BY THE PRINTING ROUTINE, BUT THE PARAMETER LIST, THE PLD'S TO WHICH
* IT POINTS, AND THE LINE TEXTS TO WHICH THEY POINT, MUST BE PRESERVED
* BY THE PROBLEM PROGRAM FOR AS LONG AS THEY ARE IN USE, SINCE THEY
* ARE REFERRED TO AT THE BEGINNING OF EACH NEW PAGE.  EACH CALL TO
* TO 'SETHEAD' SUPERSEDES THE EFFECT OF THE PREVIOUS CALL.  A CALL WITH
* A PARAMETER LIST ADDRESS OF ZERO INDICATES THAT NO HEADINGS ARE
* PROVIDED BY THE PROBLEM PROGRAM, AND ONLY THE PAGE NUMBER, DATE, AND
* TIME WILL BE PRINTED, WITH A STANDARD SPACING OF THREE.
         SPACE 2
SHPROC   LTR   RPARM,RPARM         TEST FOR NULL LIST ADDR
         BNZ   SHOK                BRANCH IF NON-ZERO
         LA    RPARM,NOHDLIST      ADDRESS OF PLD FOR BLANK LINE
SHOK     ST    RPARM,HEADLIST      STORE ADDR OF HEADING PARMS
         B     EXIT0               EXIT
         EJECT
******************
* CLOSE FUNCTION *
******************
         SPACE
* THIS CALL CLOSES THE PRINTER DATA SET.  IT DOES NOT MOVE THE PAGE
* OR ALTER ANY SWITCHES OR VARIABLES EXCEPT THE DCB ITSELF.
* AFTER CLOSING THE DCB, IT RELEASES THE PRINT BUFFERS.
         SPACE 2
CLPROC   TM    DCBOFLGS,OPENBIT    IS DCB OPEN NOW?
         BZ    EXIT0               EXIT IF ALREADY CLOSED
         MVI   DWORK,X'80'         SET END-OF-LIST BIT
         CLOSE (PRINTDCB,LEAVE),MF=(E,DWORK)
         SPACE
         FREEPOOL  PRINTDCB        FREE THE BUFFER CORE
         SPACE
         B     EXIT0               EXIT TO CALLER
         EJECT
************
* LISTPROC *
************
         SPACE
* THIS ROUTINE PROCESSES A LIST OF PLD ADDRESSES, FORMATTING EACH PRINT
* LINE AND PERFORMING THE NECESSARY PRE- AND POST-SPACING OPERATIONS.
* IT IS ENTERED WITH 'RPARM' POINTING TO THE LIST OF ADDRESSES AND
* 'RLPEXIT' CONTAINING THE EXIT ADDRESS.  IT MAY DISCOVER THAT ITS
* SPACING OPERATIONS HAVE BROUGHT THE PAGE TO HEAD OF FORM, REQUIRING
* THAT THE HEADING AND SUB-HEADING LINES BE PRINTED.  WHEN THIS OCCURS,
* IT BRANCHES TO 'HEADPRNT', WHICH SAVES 'RPARM' AND 'RLPEXIT' AND
* REENTERS THIS ROUTINE WITH AN ADDRESS LIST FOR THE HEADING LINES.
* (WHEN THIS SECOND-LEVEL CALL IS MADE, SWITCH 'NOSKPBIT' IS ON,
* PREVENTING ANY FURTHER PAGE SKIPS AND ELIMINATING THE POSSIBILITY OF
* YET ANOTHER CALL TO 'HEADPRNT'.)  AFTER THE HEADINGS ARE PRINTED,
* THE ORIGINAL VALUES ARE RETURNED TO 'RPARM' AND 'RLPEXIT' AND
* PROCESSING OF THE LIST OF TEXT LINES PROVIDED BY THE CALLER IS
* CONTINUED.
         SPACE 2
         USING PLDBLOCK,RPLD       TO ADDRESS PLD BLOCKS
         SPACE
LISTPROC L     RPLD,0(RPARM)       LOAD A PLD BLOCK ADDRESS
         SPACE
* IF PAGE IS NOW AT HEAD OF FORM, EXECUTE 'HEADPRNT'.
         SPACE
         LA    PRRET,LISTPROC      EXIT FROM HEADPRNT STARTS AGAIN
         TM    SWITCHES,HOFBIT     SEE IF HEAD-OF-FORM FLAG IS ON
         BO    HEADPRNT            EXIT IF SO
         SPACE 2
* TEST WHETHER WE ARE AT THE FIRST TEXT LINE ON A PAGE, AND, IF SO,
* WHETHER PRE-SPACING IS ALLOWED IN THAT POSITION.  IF NOT, BYPASS
* PRE-SPACING ALTOGETHER.
         SPACE
         TM    SWITCHES,TXT1BIT    TEST IF FIRST TEXT LINE
         BZ    PRSUBTR             BRANCH IF NOT
         TM    PLDFLAGS,PRHOFBIT   TEST IF ALLOW SKIP AT HOF
         BZ    POSTSP              SKIP PRESPACING IF NOT
         SPACE
* DECREMENT THE PAGE BALANCE BY THE AMOUNT TO BE SPACED.
         SPACE
PRSUBTR  SR    RSPACE,RSPACE
         IC    RSPACE,PLDSPB       LOAD AMOUNT TO SPACE
         SR    RBAL,RSPACE         DECREMENT PAGE BALANCE
         SPACE
* IF SKIPPING IS ALLOWED BY BOTH THE SYSTEM AND THE USER, TEST THE
* BALANCE AND FORCE A PAGE SKIP IF <= ZERO.
         SPACE
         TM    SWITCHES,NOSKBIT    TEST IF SYSTEM ALLOWS SKIP
         BO    PREMOVE             BRANCH IF NOT
         TM    PLDFLAGS,PRSKPBIT   TEST IF USER ALLOWS SKIP
         BO    PREMOVE             BRANCH IF NOT
         LTR   RBAL,RBAL           TEST PAGE BALANCE
         BP    PREMOVE             BRANCH IF SOME LINES LEFT
         LA    RSPACE,EJFORCE      FORCE A PAGE SKIP
         SPACE
* MOVE THE PAGE THE NUMBER OF LINES IN 'RSPACE'.
         SPACE
PREMOVE  BAL   RWA,MOVE            TO PAPER MOVING SUBROUTINE
         SPACE
* IF THAT MOVING PUT US AT HEAD-OF-FORM, PRINT THE HEADINGS.
         SPACE
         LA    PRRET,LISTPROC      EXIT FROM HEADPRNT STARTS AGAIN
         TM    SWITCHES,HOFBIT     TEST HEAD-OF-FORM BIT
         BO    HEADPRNT            EXIT IF AT HEAD
         SPACE 2
* NOW CONSIDER THE SPACING TO BE PERFORMED WHEN THE TEXT OF THE LINE
* IS PRINTED.  BEGIN BY COMPUTING THE NEW PAGE BALANCE.
         SPACE
POSTSP   SR    RSPACE,RSPACE
         IC    RSPACE,PLDSPA       LOAD AMOUNT TO SPACE AFTER PRINT
         SR    RBAL,RSPACE         COMPUTE NEW PAGE BALANCE
         SPACE
* IF THE SYSTEM AND USER BOTH ALLOW SKIPPING, TEST THE PAGE BALANCE,
* AND FORCE A SKIP IF <= ZERO.
         SPACE
         TM    SWITCHES,NOSKBIT    TEST IF SYSTEM ALLOWS SKIPPING
         BO    GETPRCC             BRANCH IF NOT
         TM    PLDFLAGS,SPSKPBIT   TEST IF USER ALLOWS SKIPPING
         BO    GETPRCC             BRANCH IF NOT
         LTR   RBAL,RBAL           TEST FOR PAGE END
         BP    GETPRCC             BRANCH IF SOME LINES LEFT
         LA    RSPACE,EJFORCE      FORCE PAGE SKIP
         SPACE
* NOW CONSTRUCT THE PRINT LINE IN AN OUTPUT BUFFER.  IN THE INTERESTS
* OF SAVING BUFFER SPACE, THE CALLER'S TEXT IS SCANNED FROM THE RIGHT
* TO REDUCE ITS NOMINAL LENGTH BY THE NUMBER OF RIGHT BLANKS.  IN THE
* EXTREME CASE THAT IT IS ENTIRELY BLANK, BYPASS PRINTING ALTOGETHER
* AND MERELY SPACE.  IF THIS IS TO BE THE FIRST HEADING LINE OF THE
* PAGE ('HD1BIT' SET), SCANNING IS OMITTED, THE RECORD LENGTH IS SET TO
* THE PAGE WIDTH, AND THE DATE, TIME, AND PAGE NUMBER ARE INSERTED.
         SPACE
GETPRCC  DS    0H
         SR    RWA,RWA
         IC    RWA,PLDLNGTH        LOAD NOMINAL LENGTH OF TEXT
         SR    RWB,RWB
         IC    RWB,PLDOFFST        LOAD LEFT MARGIN OFFSET
         SPACE
         LA    R0,0(RWA,RWB)       LINE LENGTH = TEXT + OFFSET
         CH    R0,PAGWIDTH         TEST AGAINST PAGE WIDTH
         BNH   TSTHD1              BR IF OK
         LA    R1,5                ERROR, LINE TOO LONG
         B     ABEND
         SPACE
TSTHD1   TM    SWITCHES,HD1BIT     TEST IF FIRST HEADING LINE
         BZ    BLNKSCAN            BR IF NOT
         LH    RWB,PAGWIDTH        SET RECORD LENGTH AS PAGE WIDTH
         B     GETBUF              SKIP BLANK SCANNING
         SPACE
* SET UP A 'BXH' FOR SCAN.  R0 IS INCREMENT, R1 IS COMPARAND, RWA
* VARIABLE.
         SPACE
BLNKSCAN LH    R0,=H'-1'           INCREMENT IS MINUS 1
         L     R1,PLDTXTAD-1       LOAD TEXT ADDRESS
         LA    R1,0(R1)            ZERO HIGH-ORDER BYTE
         BCTR  R1,0                DECREMENT BY ONE
         AR    RWA,R1              SET RWA TO RIGHT-MOST TEXT BYTE
         SPACE
* SCAN THE LINE, SEARCHING FOR FIRST NON-BLANK.  RESULT IS NEW TEXT
* LENGTH IN RWA, WHICH WILL BE ZERO FOR BLANK LINES.
         SPACE
BLNKLOOP CLI   0(RWA),C' '         TEST FOR NON-BLANK CHAR
         BNE   BLNKDONE            EXIT WHEN FOUND
         BXH   RWA,R0,BLNKLOOP
BLNKDONE SR    RWA,R1              NOW RWA IS NEW TEXT LENGTH
         BC    13,POSTMOVE         NO PRINTING IF <= 0
         SPACE
* NOW ADD OFFSET TO LENGTH OF SURVIVING TEXT TO GET ACTUAL LINE LENGTH.
         SPACE
         AR    RWB,RWA             ADD TEXT LENGTH
         SPACE
* NOW RWA IS THE LENGTH OF THE CALLER'S TEXT, AND RWB IS THE LENGTH OF
* THE LINE.  THE RECORD LENGTH WILL BE FIVE MORE THAT THE LENGTH OF THE
* LINE, TO INCLUDE FORMAT 'V'  AND PRINTER CONTROL CHARACTERS.  GET
* A BUFFER OF THE PROPER LENGTH, KEEPING ITS ADDRESS IN R1.
         SPACE
GETBUF   LA    RWC,5(RWB)          LOAD LENGTH OF RECORD IN RWC
         STH   RWC,DCBLRECL        PUT LRECL IN DCB FOR PUT CALL
         SPACE
         PUT   PRINTDCB            GET BUFFER ADDRESS IN R1
         SPACE
         ST    R1,CCLAST           SAVE LOCATION OF THIS LINE
         LR    RWD,R1              RWD BECOMES BUFFER BASE REG
         MVI   3(RWD),C' '         CLEAR BUFFER TO BLANKS
         EX    RWB,BLNKMOVE
         SLL   RWC,16              SET UP 4-BYTE 'V' CONTROL FIELD
         ST    RWC,DWORK           CAN'T ASSUME BUFFER ON ANY
         MVC   0(4,RWD),DWORK      BOUNDARY
         SPACE
* IF THIS IS THE FIRST HEADING LINE, INSERT DATE AND TIME, AND EDIT
* THE PAGE NUMBER.
         SPACE
         TM    SWITCHES,HD1BIT     TEST IF FIRST LINE
         BZ    MOVETEXT            BRANCH IF NOT
         SPACE
         LA    RWC,0(RWB,RWD)      ADDRESS RIGHT END -5
         SH    RWC,=AL2(HLTXTLNG+10-5)  LOCATE PLACE FOR HEADING INFO
         MVC   0(HLTXTLNG,RWC),HLTEXT  INSERT DATE & TIME
         SPACE
         LH    R1,PAGENO           INCREMENT PAGE NUMBER
         LA    R1,1(R1)
         STH   R1,PAGENO
         CVD   R1,DWORK            CONVERT TO DECIMAL
         MVC   HLTXTLNG(10,RWC),=XL10'40404040402020202120'  EDIT CHARS
         LA    R1,HLTXTLNG+9(RWC)  IN CASE SIGNIFIGANCE FORCED
         EDMK  HLTXTLNG+4(6,RWC),DWORK+5  EDIT AND MARK SPOT FOR "PAGE"
         SH    R1,=H'5'
         MVC   0(4,R1),=C'PAGE'    INSERT WORD "PAGE"
         SPACE
* PUT PRINTER CONTROL CHARACTER AND CALLER'S TEXT INTO THE BUFFER.
* REGISTER RWA CONTAINS THE LENGTH OF THE TEXT.
         SPACE
MOVETEXT BAL   RWB,CCGEN           GO GET CONTROL CHAR
         STC   R0,4(RWD)           INSERT IN BUFFER
         NI    4(RWD),B'11111101'  CHANGE SPACE IMMEDIATE TO WRITE
         SPACE
         SR    RWB,RWB
         IC    RWB,PLDOFFST        LOAD LEFT MARGIN OFFSET
         LA    RWB,5(RWB,RWD)      ADDRESS FOR FIRST TEXT BYTE
         BCTR  RWA,0               DECREMENT LENGTH BY 1 FOR MVC
         L     RWC,PLDTXTAD-1      LOAD ADDRESS OF CALLER'S TEXT
         EX    RWA,MOVEINST        MOVE TEXT
         SPACE
         NI    SWITCHES,255-HD1BIT-TXT1BIT  RESET ONE-SHOT BITS
         SPACE
* DECREMENT PRINT LINE LIMIT COUNTER, ABORTING IF IT GOES NEGATIVE.
         SPACE
         L     R1,MAXLINES
         S     R1,=F'1'            BCTR WOULD NOT SET COND CODE
         ST    R1,MAXLINES
         BP    POSTMOVE            BRANCH IF POSITIVE
         LA    R1,6                ABORT, TOO MUCH OUTPUT
         B     ABEND
         SPACE
* DO ANY ADDITIONAL SPACING NECESSARY.  AMOUNT STILL IN 'RSPACE'.
         SPACE
POSTMOVE BAL   RWA,MOVE            CALL PAPER MOVING SUBROUTINE
         SPACE 2
* NOW WE ARE FINISHED WITH THAT LINE.  IF THE ADDRESS WHICH POINTED TO
* ITS PLD WAS NEGATIVE, THE LIST IS EXHAUSTED AND THE ROUTINE EXITS.
* OTHERWISE, INCREMENT THE LIST POINTER BY 4 AND START THE NEXT ONE.
         SPACE
         LTR   RPLD,RPLD           TEST CURRENT PLD ADDRESS
         BCR   4,RLPEXIT           EXIT IF NEGATIVE
         SPACE
         LA    RPARM,4(RPARM)      INCREMENT LIST POINTER
         B     LISTPROC            TO PROCESS NEXT LINE
         SPACE 2
* EXECUTED INSTRUCTIONS:
         SPACE
BLNKMOVE MVC   4(0,RWD),3(RWD)     SPREAD BLANKS IN OUTPUT BUFFER
MOVEINST MVC   0(0,RWB),0(RWC)     MOVE CALLER'S TEXT TO OUTPUT BUF
         SPACE
         DROP  RPLD
         EJECT
************
* HEADPRNT *
************
         SPACE
* ENTER THIS ROUTINE WHEN IT IS DISCOVERED THAT WE ARE AT
* HEAD-OF-FORM AND ARE ABOUT TO PRINT A LINE.  SAVE THE REGISTERS
* DEFINING THE STATE OF 'LISTPROC' AND CALL IT WITH A LIST SPECIFYING
* THE HEADING AND SUB-HEADING LINES.
         SPACE 2
HEADPRNT ST    PRRET,HPREXIT       SAVE RETURN ADDRESS
         STM   RLPEXIT,RPARM,HPRSAVE  STORE LISTPROC REGISTERS
         SPACE
* RESET HEAD-OF-FORM BIT AND SET NO-SKIP BIT TO SUPPRESS FORM SKIPS
* AND HEADING LINE BIT TO INCLUDE THE PAGE NUMBER.  RESET PAGE BALANCE.
         SPACE
         NI    SWITCHES,255-HOFBIT  RESET HOF BIT
         OI    SWITCHES,HD1BIT+NOSKBIT  SET HEADING AND NOSKIP BITS
         LH    RBAL,PAGELNG        RESET PAGE BALANCE
         SPACE
* LOAD ADDRESS OF HEADINGS LIST AND CALL LIST PROCESSOR.
         SPACE
         L     RPARM,HEADLIST      ADDRESS OF HEADINGS LIST
         BAL   RLPEXIT,LISTPROC    CALL PROCESSING ROUTINE
         SPACE
* AFTER PRINTING THE HEADINGS, TURN SYSTEM SKIP-SUPPRESSION BIT OFF
* AND SET 'TXT1BIT' TO INDICATE THAT THERE ARE NO TEXT LINES ON THE
* PAGE YET.  THEN RESTORE THE 'LISTPROC' REGISTERS AND RETURN TO
* PROCESSING THE CALLER'S LINES.
         SPACE
         NI    SWITCHES,255-NOSKBIT  RESET NO-SKIP BIT
         OI    SWITCHES,TXT1BIT    SET FIRST-TEXT-LINE BIT
         SPACE
         LM    RLPEXIT,RPARM,HPRSAVE  RESTORE LISTPROC REGISTERS
         L     PRRET,HPREXIT       RECOVER EXIT ADDRESS AND
         BR    PRRET               RETURN TO CALLER
         EJECT
***********************************
* MOVE - VERTICAL SPACING ROUTINE *
***********************************
         SPACE
* SUBROUTINE TO MOVE THE PAPER BY THE NUMBER OF LINES IN REGISTER
* 'RSPACE'.  RETURN ADDRESS IS IN 'RWA'.  IF THE CONTROL CHARACTER FOR
* THE LAST LINE SPACED 0 LINES, REPLACE IT WITH A NEW ONE BEFORE
* GENERATING ANY EXTRA LINES.
         SPACE 2
MOVE     LTR   RSPACE,RSPACE       TEST NUMBER OF LINES TO BE MOVED
         BCR   13,RWA              EXIT IF <= ZERO
         TM    SWITCHES,SP0BIT     DID PREVIOUS LINE SPACE 0?
         BZ    PREVNOT0            BRANCH IF NOT
         L     R1,CCLAST           GET ADDRESS OF PREVIOUS LINE
         BAL   RWB,CCGEN           GENERATE NEW CONTROL CHAR
         NI    4(R1),X'02'         TEST WRITE/SPACE BIT IN OLD CC
         STC   R0,4(R1)            STORE NEW CC IN PREVIOUS LINE
         BNZ   *+8                 BR IF WRITE/SPACE BIT WAS ON
         NI    4(R1),255-X'02'     TURN WRITE/SPACE BIT OFF
PREVNOT0 DS    0H
         MVC   DCBLRECL(2),MOVELINE  SET LOGICAL RECORD LENGTH IN DCB
         SPACE
         PUT   PRINTDCB            GET BUFFER ADDRESS IN R1
         SPACE
         ST    R1,CCLAST           SAVE LOCATION OF THIS LINE
         MVC   0(6,R1),MOVELINE    INSERT FORMAT 'V' CONTROL RECORD
         BAL   RWB,CCGEN           GET PRINT CONTROL CHAR IN R0
         STC   R0,4(R1)            INSERT IN RECORD
         SPACE
         B     MOVE                LOOP UNTIL 'RSPACE' IS ZERO
         SPACE 3
MOVELINE DC    XL6'000600000340'   FORMAT 'V' CONTROL RECORD
         EJECT
*****************************************
* CCGEN - GENERATE PRINTER CONTROL CHAR *
*****************************************
         SPACE
* THIS ROUTINE GENERATES A PRINTER CONTROL CHARACTER TO SPACE OR EJECT
* IMMEDIATELY (WITHOUT PRINTING).  ENTER WITH THE TOTAL NUMBER OF
* LINES TO BE MOVED IN 'RSPACE' AND RETURN ADDRESS IN 'RWB'.  EXITS
* WITH THE CONTROL CHARACTER IN BITS 24-31 OF REGISTER 0 AND THE COUNT
* IN 'RSPACE' DECREMENTED BY THE AMOUNT WHICH THE CHARACTER WILL SPACE.
* A SPACE VALUE OF 255 OR GREATER WILL GENERATE AN EJECT CONTROL
* CHARACTER, SET 'RSPACE' AND 'RBAL' TO ZERO, AND SET THE HEAD-OF-FORM
* BIT.  NOTE:  MUST NOT USE R1.
         SPACE 2
CCGEN    NI    SWITCHES,255-SP0BIT  RESET 'PREVIOUS SPACED 0' BIT
         CH    RSPACE,=AL2(EJFORCE)  TEST FOR EJECT REQUEST
         BL    CCNOEJ              BRANCH IF NOT
         LA    R0,X'8B'            LOAD 'SKIP TO CHAN 1' CC
         OI    SWITCHES,HOFBIT     SET HEAD-OF-PAGE BIT
         SR    RBAL,RBAL           SET PAGE BALANCE TO ZERO
         SR    RSPACE,RSPACE       SET REMAINING SPACING TO ZERO
         BR    RWB                 EXIT
         SPACE
CCNOEJ   LR    R15,RSPACE          LOAD WORK REGISTER
         CH    RSPACE,=H'3'        TEST IF OVER THREE LINES
         BNH   CCOK                BR IF <= 3
         LA    R15,3               SET 3, MAXIMUM FOR 1 OPERATION
CCOK     SR    RSPACE,R15          DECR RSPACE BY AMOUNT SPACED
         SLL   R15,3               PUT AMOUNT IN BITS 2-4 OF BYTE
         LA    R0,X'03'(R15)       ADD COMMAND BITS AND PUT IN R0
         LTR   R15,R15             TEST FOR ZERO SPACING
         BCR   7,RWB               EXIT IF NOT ZERO
         OI    SWITCHES,SP0BIT     SET 'LINE SPACED 0' BIT
         BR    RWB                 EXIT
         EJECT
***********************
* SYNAD ERROR ROUTINE *
***********************
         SPACE
* IF A PERMANENT I/O ERROR OCCURS, TYPE A DIAGNOSTIC MESSAGE AND ABORT.
         SPACE
SYNAD    SYNADAF ACSMETH=QSAM      GET ERROR MESSAGE FROM O/S
         LR    RWA,R1              SAVE MESSAGE ADDRESS
         GETMAIN R,LV=128          GET BUFFER FOR MESSAGE
         MVC   0(LIOMSG,R1),IOMSG  INSERT MESSAGE BEGINNING
         MVC   LIOMSG(79,R1),49(RWA)  ADD TEXT FROM O/S
         LR    RWA,R1              SAVE MESSAGE ADDRESS
         SYNADRLS                  FREE O/S MESSAGE
         WTO   MF=(E,(RWA))        TYPE MESSAGE ON CONSOLE
         LA    R1,7                LOAD ERROR CODE
         B     ABEND               TO ABORT AND DUMP
         SPACE
IOMSG    DC    AL2(LIOMSG+78,0)    FORMAT V CONTROL FIELD
         DC    C'XPRNTSUB I/O ERROR'
LIOMSG   EQU   *-IOMSG
         EJECT
**************************
* CONSTANTS AND LITERALS *
**************************
         SPACE
* DUMMY PARAMETER LIST AND PLD FOR USE WHEN NO HEADING IS PROVIDED.
         SPACE
NOHDLIST DS    0F
         DC    XL1'80'             VL LIST BIT
         DC    AL3(NOHDPLD)        ADDRESS OF PLD BELOW
         SPACE
NOHDPLD  DS    0F                  ALIGN ON FULL-WORD BOUNDARY
         DC    BL1'00010000'       OPTION BITS
         DC    AL3(BLANK1)         PRINT TEXT ADDRESS
         DC    AL1(1)              PRINT TEXT LENGTH
         DC    AL1(0)              LEFT MARGIN INDENTATION
         DC    AL1(0)              SPACING BEFORE PRINTING
         DC    AL1(3)              SPACING AFTER PRINTING
         SPACE
* CONSTANTS TO INITIALIZE PARAMETERS AND SWITCHES WHEN 'OPEN' CALL IS
* RECEIVED.  THE ORDER MUST MATCH THE 'PAPARMS' LIST IN THE WORK AREA.
         SPACE
DEFAULTS DS    0F
         DC    A(NOHDLIST)         PAGE HEADING PARM LIST ADDRESS
         DC    F'1000000'          MAXIMUM NUMBER OF OUTPUT LINES
         DC    H'132'              PAGE WIDTH
         DC    H'57'               PAGE LENGTH            ***TRW***
         DC    H'0'                PAGE NUMBER (-1)
         DC    H'0'                PAGE BALANCE
         DC    X'00'               SWITCHES
DEFSIZE  EQU   *-DEFAULTS
         SPACE
* MISCELLANEOUS CONSTANTS:
         SPACE
BLANK1   DC    C' '                TEXT OF DUMMY HEADING LINE
         DS    0F                                                  @162
PRAMODE  DC    AL4(0)                                              @162
         LTORG
         EJECT
*******************
* WORK AREA DSECT *
*******************
         SPACE
* THIS DUMMY SECTION DEFINES THE WORK AREA PROVIDED BY THE PROBLEM
* PROGRAM FOR EACH DATA SET.  ITS ADDRESS IS PASSED IN PARAMETER
* REGISTER 0 AT EACH CALL.
         SPACE 2
WORKAREA DSECT
         SPACE 2
* STANDARD 18-WORD SAVE AREA:
         SPACE
SAVEAREA DS    18F
         SPACE 2
* DATA CONTROL BLOCK FOR PRINTER DATA SET:
         SPACE
         PRINT NOGEN
PRINTDCB DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               RECFM=VBM,                                              X
               LRECL=137,                                              X
               MACRF=PL,                                               X
               BFTEK=S,                                                X
               BUFNO=2,                                                X
               EROPT=ACC
         SPACE
DCBEXLST EQU   PRINTDCB+37         ADDRESS OF EXIT LIST
DCBOFLGS EQU   PRINTDCB+48         CONTAINS 'OPENED SUCCESSFULLY' BIT
DCBDDNAM EQU   PRINTDCB+40         DDNAME (WHEN CLOSED)
DCBSYNAD EQU   PRINTDCB+57         ADDRESS OF SYNCHRONOUS ERROR ROUTINE
DCBBLKSZ EQU   PRINTDCB+62         BLOCK SIZE
DCBLRECL EQU   PRINTDCB+82         LOGICAL RECORD LENGTH
         SPACE 2
* ADDRESS OF LAST BUFFER OBTAINED BY "PUT" MACRO:
         SPACE
CCLAST   DS    1A
         SPACE
         DS    1F                  SPARE FULL WORD
         SPACE 2
* TEMPORARY WORK CELLS:
         SPACE
DWORK    DS    1D                  DOUBLE-WORD WORK CELL
HPREXIT  DS    1F                  HEADPRNT EXIT SAVE
HPRSAVE  DS    2F                  HEADPRNT REGISTER STORAGE
         SPACE 2
* PARAMETERS AND SWITCHES WHICH DEFINE THE PAGE SIZE AND STATUS.
* SEQUENCE MUST MATCH THAT IN 'DEFAULTS', WHICH INITIALIZES THEM.
         SPACE
PAPARMS  DS    0F
HEADLIST DS    1A                  PAGE HEADING PARM LIST ADDRESS
MAXLINES DS    1F                  MAXIMUM NUMBER OF OUTPUT LINES
PAGWIDTH DS    1H                  PAGE WIDTH
PAGELNG  DS    1H                  PAGE LENGTH
PAGENO   DS    1H                  PAGE NUMBER OF CURRENT PAGE
PAGEBAL  DS    1H                  PAGE BALANCE
SWITCHES DS    1X                  SWITCHES
         SPACE 2
* STORAGE FOR DATE AND TIME PORTION OF PAGE HEADING:
         SPACE
HLTXTLNG EQU   24
HLTEXT   DS    CL(HLTXTLNG)
HLDAY    EQU   HLTEXT
HLTIME   EQU   HLTEXT+7
HLDATE   EQU   HLTEXT+16
         EJECT
*************************
* PARAMETER LIST DSECTS *
*************************
         SPACE 2
* PARAMETER LIST FOR 'EJECT' CALL:
         SPACE
EJPARM   DSECT
         SPACE
EJCOND   DS    1X                  CONDITIONAL MASK AND SWITCHES
EJQUAN   DS    1X                  CONDITIONAL TEST QUANTITY
         SPACE
EJATHOF  EQU   X'01'               MASK FOR 'EJCOND'
         SPACE 6
* PARAMETER LIST FOR 'SPACE' CALL:
         SPACE
SPPARM   DSECT
         SPACE
SPCOND   DS    1X                  CONDITIONAL BITS
SPQUAN   DS    1X                  NUMBER OF LINES TO SPACE
         SPACE
SPATHOF  EQU   X'01'               MASK FOR 'SPCOND'
SPNOEJ   EQU   X'02'               MASK FOR 'SPCOND'
         SPACE 6
* PARAMETER LIST FOR 'OPEN' AND 'MODIFY' CALLS:
         SPACE
OMPARM   DSECT
         SPACE
OMMAXLIN DS    1F                  MAXIMUM NUMBER OF OUTPUT LINES
OMPAGENO DS    1H                  INITIAL PAGE NUMBER
OMPAGWID DS    1X                  PAGE WIDTH
OMPAGLNG DS    1X                  PAGE LENGTH
OMTIMEAD DS    1A                  ADDRESS OF TIME
OMDATEAD DS    1A                  ADDRESS OF DATE
OMDDNAME DS    CL8                 DDNAME (OPEN CALL ONLY)
         EJECT
*************
* PLD DSECT *
*************
         SPACE
* PRINT LINE DESCRIPTOR DUMMY SECTION:
         SPACE 2
*        ******************************************************
*        *            *                                       *
*        *   OPTION   *                                       *
*        *    BITS    *             TEXT ADDRESS              *
*        *            *                                       *
*        ******************************************************
*        *            *            *            *             *
*        *    TEXT    *    TEXT    *   SPACE    *    SPACE    *
*        *   LENGTH   *   OFFSET   *   BEFORE   *    AFTER    *
*        *            *            *            *             *
*        ******************************************************
         SPACE 3
PLDBLOCK DSECT
         SPACE
PLDFLAGS DS    1X                  OPTION BITS
PLDTXTAD DS    AL3                 TEXT ADDRESS
PLDLNGTH DS    1X                  TEXT LENGTH
PLDOFFST DS    1X                  TEXT OFFSET, OR LEFT MARGIN
PLDSPB   DS    1X                  AMOUNT TO SPACE BEFORE PRINTING
PLDSPA   DS    1X                  AMOUNT TO SPACE AFTER PRINTING
         SPACE 2
PRHOFBIT EQU   B'00000010'         MASK FOR 'PLDFLAGS'
PRSKPBIT EQU   B'00000001'         MASK FOR 'PLDFLAGS'
SPSKPBIT EQU   B'00010000'         MASK FOR 'PLDFLAGS'
         SPACE 6
         TITLE 'DATE EDITING ROUTINE "XDATEDIT"'
* STATUS:  VERSION 0, MOD 2, 5 OCTOBER 1967.
         SPACE
* FUNCTION/OPERATION:  EDITS A PACKED DECIMAL DATE IN YEAR-DAY FORM TO
*        EBCDIC MONTH-DAY-YEAR FORM.  CORRECTS FOR LEAP YEARS AND
*        FOR THE TURN OF A CENTURY.
         SPACE
* ENTRY POINTS:  ENTER AT "XDATEDIT" VIA BALR 14,15 WITH REG 13 SET
*        TO A STANDARD 18-WORD SAVE AREA.  REGISTER 1 POINTS TO A
*        PARAMETER LIST CONTAINING ONE ADDRESS.
         SPACE
* INPUT:  THE ADDRESS IN THE PARAMETER LIST IS THAT OF AN ALIGNED
*        DOUBLE WORD CONTAINING THE ARGUMENT IN PACKED DECIMAL AS:
*        X'0000000000YYDDD+'.
         SPACE
* OUTPUT:  THE RESULT IS RETURNED IN THE SAME DOUBLE WORD IN EBCDIC AS
*        C'MM/DD/YY'.
         SPACE
* DATA SETS:  NONE.
         SPACE
* EXTERNAL ROUTINES:  NONE.
         SPACE
* EXITS-NORMAL:  RETURN VIA REG 14 WITH RETURN CODE 0 IN REG 15.
         SPACE
* EXITS-ERROR:  IF THE DAY NUMBER IS ZERO OR EXCEEDS THE MAXIMUM VALUE
*        APPROPRIATE TO THE YEAR, RETURN VIA REG 14 WITH RETURN CODE 4
*        IN REG 15 AND THE RESULT FIELD SET TO C' YY.DDD '.
         SPACE
* TABLES/WORK AREAS:  NONE.
         SPACE
* ATTRIBUTES:  REENTRANT, READ ONLY.
         SPACE
* NOTES:  NONE.
         EJECT
* DUMMY SECTION TO DEFINE THE ARGUMENT AND RETURN FIELD.
         SPACE
DUMMY    DSECT
DWORD    DS    1D
         SPACE 6
* BEGIN CONTROL SECTION HERE.  DEFINE REGISTER TAGS.
         SPACE
XDATEDIT CSECT
         SPACE
RDSECT   EQU   11                  BASE REG FOR ARGUMENT/RESULT
PARMREG  EQU   1                   PARAMETER LIST POINTER
RARGSAVE EQU   2                   TO SAVE ORIGINAL PACKED ARGUMENT
RMONTH   EQU   3
RDAY     EQU   4                   RDAY AND RYEAR ARE AN EVEN/ODD
RYEAR    EQU   5                   PAIR FOR DIVISION
RWORK    EQU   6
RLIST    EQU   7
         SPACE 6
* ENTER HERE.  SAVE GENERAL REGISTERS AND SET UP BASE REGISTERS.
         SPACE
         SAVE  (14,12),,*
         SPACE
         BALR  RBASE,0
         USING *,RBASE
         L     RDSECT,0(PARMREG)   LOAD ARGUMENT ADDRESS
         USING DWORD,RDSECT
         EJECT
* CONVERT YEAR AND DAY TO BINARY, AND SEPARATE BY DIVISION.
         SPACE
         MVI   DWORD,X'00'         CLEAR HIGH DECIMAL DIGITS
         MVC   DWORD+1(4),DWORD    IN ARG TO ZEROS
         OI    DWORD+7,X'0F'       FORCE PLUS SIGN
         L     RARGSAVE,DWORD+4    SAVE ARGUMENT FOR 'BADDATE'
         SPACE
         CVB   RYEAR,DWORD         CONVERT YYDDD TO BINARY
         SR    RDAY,RDAY           CLEAR HIGH-ORDER DIVIDEND
         D     RDAY,F1000          QUOTIENT=YEAR; REMAINDER=DAY
         SPACE
* CHECK FOR LEAP YEAR, AND LOAD RLIST WITH BASE ADDRESS OF APPROPRIATE
* LIST OF MONTH SIZES.
         SPACE
DAYOK    DS    0H
         LA    RLIST,STDLIST       POINT RLIST TO STANDARD MONTHS
         LTR   RWORK,RYEAR         LOAD BINARY YEAR NUMBER
         BZ    NOTLEAP             BR IF TURN OF CENTURY
         N     RWORK,=F'3'         TEST LOW TWO BITS
         BC    4,NOTLEAP           BR IF NON-ZERO
         LA    RLIST,LPYLIST       POINT RLIST TO LEAP-YEAR MONTHS
NOTLEAP  DS    0H
         SPACE
* TEST DAY NUMBER AGAINST ZERO AND THE UPPER LIMIT DETERMINED BY
* THE YEAR NUMBER.
         SPACE
         LTR   RDAY,RDAY           TEST FOR ZERO
         BZ    BADDATE             BR IF SO
         CH    RDAY,0(0,RLIST)     TEST FOR UPPER LIMIT
         BH    BADDATE             BR IF TOO LARGE
         SPACE
* REDUCE JULIAN DAY TO DAY-OF-MONTH, ACCUMULATING MONTH NUMBER.
         SPACE
         LA    RMONTH,1            INITIAL MONTH NUMBER
         SR    RWORK,RWORK
DAYLOOP  IC    RWORK,1(RMONTH,RLIST)  LENGTH OF MONTH INTO RWORK
         CR    RDAY,RWORK          TEST IF DAY IN THIS MONTH
         BNH   DAYDONE             BR IF DAY LESS THAN MONTH SIZE
         SR    RDAY,RWORK          REDUCE DAY BY LENGTH OF MONTH
         LA    RMONTH,1(RMONTH)    INCREMENT MONTH
         B     DAYLOOP
DAYDONE  DS    0H
         SPACE
* COMBINE MONTH, DAY, AND YEAR IN BINARY IN ONE REGISTER, MULTIPLYING
* EACH BY A FACTOR TO PLACE IT PROPERLY IN THE DECIMAL RESULT.
* THE TRICK IS THAT  1000(1000(MONTH)+DAY)+YEAR  GIVES  MM0DD0YY  WHEN
* CONVERTED TO DECIMAL.  AFTER UNPACKING, THE ZEROS ARE REPLACED BY
* SLASHES.
         SPACE
         MH    RMONTH,H1000
         AR    RMONTH,RDAY
         MH    RMONTH,H1000
         AR    RMONTH,RYEAR
         CVD   RMONTH,DWORD
         MVC   DWORD(5),DWORD+3    MOVE OVER FOR UNPACKING
         UNPK  DWORD(8),DWORD(5)   CONVERT TO ALPHA
         OI    DWORD+7,X'F0'       COVER UP SIGN
         MVI   DWORD+2,C'/'        INSERT SLASHES
         MVI   DWORD+5,C'/'
         SR    15,15               SET NORMAL RETURN CODE OF 0
         SPACE
EXITD    RETURN  (14,12),T,RC=(15)  RETURN TO CALLER
         SPACE 2
* FOR ARGUMENTS WHOSE DAY NUMBER IS ZERO OR TOO LARGE, RETURN IN
* 'DWORD' THE EDITED VALUE  C' YY.DDD ' AND SET A RETURN CODE OF 4.
         SPACE
BADDATE  ST    RARGSAVE,DWORD      RESTORE ORIGINAL PACKED ARGUMENT
         UNPK  DWORD+2(5),DWORD+1(3)  UNPACK INTO ALPHA
         MVC   DWORD+1(2),DWORD+2  SHIFT YEAR 1 LEFT
         MVI   DWORD,C' '          APPLY COSMETICS
         MVI   DWORD+3,C'.'
         MVI   DWORD+7,C' '
         LA    15,4                SET RETURN CODE
         B     EXITD
         EJECT
* LISTS OF MONTH SIZES FOR STANDARD AND LEAP YEARS:
         SPACE
         SPACE
STDLIST  DC    H'365'              DAY LIMIT FOR STANDARD YEARS
         DC    AL1(31)
         DC    AL1(28)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         SPACE
LPYLIST  DC    H'366'              DAY LIMIT FOR LEAP YEARS
         DC    AL1(31)
         DC    AL1(29)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         DC    AL1(30)
         DC    AL1(31)
         SPACE
F1000    DC    F'1000'
H1000    EQU   F1000+2
         LTORG
./       ADD   NAME=QKSRT1
         MACRO
         QKSRT1 &NAME,&TYPE,&LENGTH=0,&STRTKY=1,&KYLGTH=0,&ORDER=A,    X
               &EXTRA=0,&OVRTYP=NONE,&DISP=1,&INLINE=NO
***********************************************************************
*** QUICKERSORT MACRO-SUBROUTINE: BASED ON CACM ALGORITHM NUMBER 271  *
*** THE FOLLOWING CHANGES AND ADDITIONS WERE MADE:                    *
*** 1.  CALCULATION OF P AS P:=(I+J)/2 WAS CHANGED TO: P:=I+(J-I)/2   *
*** 2.  THE LINE 'FOR Q:=Q STEP -1 UNTIL K DO' WAS CHANGED TO:        *
***        'FOR Q:=Q STEP -1 UNTIL K+1 DO'                            *
*** 3.  OPTION: CORRESPONDING ELEMENTS IN UP TO FIVE EXTRA ARRAYS MAY *
***        BE EXCHANGED, WHEN ELEMENTS IN THE SORTED ARRAY ARE        *
***        EXCHANGED.  EACH EXTRA ARRAY MUST CONTAIN THE SAME NUMBER  *
***        OF ELEMENTS, AND HAVE ELEMENT SIZES EQUAL TO THOSE OF THE  *
***        SORTED ARRAY.                                              *
*** 4.  OPTION: WITH 'TYPE' SET TO 'C' (CHARACTER FORMAT) OTHER TYPES *
***        OF COMPARES MAY BE MADE BY SPECIFYING ONE OR TWO           *
***        OVERRIDING PARAMETERS.                                     *
*** 5.  OPTION: IN LINE CODE MAY BE GENERATED, IF DESIRED, BY         *
***        SPECIFYING INLINE=YES.  A DUMMY PROGRAM NAME IS STILL      *
***        REQUIRED.  REGISTERS WILL BE SAVED IN A SPECIAL SAVEAREA.  *
***********************************************************************
.**********************************************************************
.*           EXPLANATION OF KEYWORD AND POSITONAL OPERANDS:           *
.*                                                                    *
.* 1. NAME (POSITIONAL): THE SUBROUTINE MUST BE NAMED ARBITRARILY     *
.*    (ONE TO SIX CHARACTERS MAXIMUM) BY THE USER. FORTRAN OR COBOL   *
.*    RULES MUST BE FOLLOWED IN CHOOSING A NAME.                      *
.* 2. TYPE (POSITIONAL): THE TYPE OF VARIABLES TO BE SORTED MUST BE   *
.*    CONVEYED TO THE MACRO.  ADMISSIBLE VALUES ARE: P=PACKED;        *
.*    C=CHARACTER OR ALPHANUMERICS (= A-FORMAT VARIABLES); H=HALFWORD *
.*    INTEGERS (=INTEGER*2); F=FULLWORD (=INTEGER*4); E=SINGLE        *
.*    PRECISION FLOATING POINT (=REAL*4); D=DOUBLE PRECISION FLOATING *
.*    POINT (=REAL*8).                                                *
.* 3. LENGTH (KEYWORD): THE LENGTH IN BYTES OF THE FIELDS OF TYPES P  *
.*    AND C MUST BE SPECIFIED.  THE LENGTH MUST BE LESS THAN 256      *
.*    BYTES FOR C OR 16 BYTES FOR P.                                  *
.* 4. STRTKY (KEYWORD): THE COLUMN THE SORTING KEY BEGINS AT MAY BE   *
.*    CHANGED FROM ITS NULL VALUE OF COLUMN 1.                        *
.* 5. KYLGTH (KEYWORD): THE LENGTH OF THE DESIRED SORTING KEY.  IF    *
.*    KYLGTH IS OMITTED, LENGTH MINUS STRTKY IS THE DEFAULT OPTION.   *
.* 6. ORDER (KEYWORD): THE ORDER OF THE DESIRED SORT. OPTIONS ARE:    *
.*    A=ASCENDING ORDER; D=DESCENDING ORDER. IF ORDER IS NOT          *
.*    SPECIFIED, ASCENDING ORDER IS THE ASSUMED DEFAULT OPTION.       *
.* 7. EXTRA (KEYWORD): THE NUMBER OF ADDITIONAL ARRAYS (RANGE: 0 - 5) *
.*    EXTRA=0 IS THE DEFAULT OPTION.                                  *
.* 8. OVRTYP (KEYWORD): OVRTYP=(D,E,F, OR H) ALLOWS FIXED OR FLOATING *
.*    POINT COMPARES WITHIN LONGER FIELDS.  TYPE MUST EQUAL "C".      *
.* 9. DISP (KEYWORD): WHEN OVRTYP IS SPECIFIED, THIS SPECIFIES THE    *
.*    COLLUMN AT WHICH COMPARISON BEGINS.  DEFAULT IS COLLUMN 1.      *
.*10. INLINE (KEYWORD): INLINE=YES GENERATES IN LINE CODE WITHIN AN   *
.*    ASSEMBLER ROUTINE.  DEFAULT IS A CSECT CALLABLE BY FORTRAN.     *
.**********************************************************************
.*   THE FOLLOWING PARAMETER LIST IS REQUIRED IN THE FORTRAN CALL:    *
.*                                                                    *
.* 1. THE NAME OF THE ARRAY TO BE SORTED.                             *
.* 2. THE NUMBER OF ELEMENTS OF THE ARRAY TO BE SORTED.               *
.* 3. THE NAMES OF "EXTRA" ARRAYS, IF ANY.                            *
.**********************************************************************
.*  SEVERAL EXAMPLES WILL MAKE THE USE OF A MACRO SUBROUTINE CLEAR:   *
.*                                                                    *
.* PROBLEM 1: SORT THE CHARACTER ARRAY CALLED A OF 500 ELEMENTS OF 80 *
.* BYTES EACH ACCORDING TO AN 8 BYTE KEY STARTING IN COLUMN 20.       *
.* SOLUTION:  THE FOLLOWING CARDS ARE NEEDED:                         *
.* // JOB CARD                                                        *
.* // EXEC ASMGC                                                      *
.* //ASM.SYSIN DD *                                                   *
.* (QUICKERSORT MACRO DEFINITION SOURCE CODE IF NOT IN SYSTEM LIBRARY)*
.*          QKRSRT SORT1,C,LENGTH=80,STRTKY=20,KYLGTH=8               *
.*          END                                                       *
.* (COMMENT: THE NAME SORT1 WAS ARBITRARILY CHOSEN. ONLY THE QKRSRT   *
.* AND END CARD NEED APPEAR IF THE MACRO IS IN THE SYSTEM LIBRARY)    *
.* /*                                                                 *
.* // EXEC FORTGCLG                                                   *
.* //FORT.SYSIN DD *                                                  *
.* (FORTRAN DECK WHICH WILL CONTAIN THE FOLLOWING STATEMENT)          *
.*     .                                                              *
.*     .                                                              *
.*     .                                                              *
.*       CALL SORT1(A,500)                                            *
.*     .                                                              *
.*     .                                                              *
.*     .                                                              *
.* /*                                                                 *
.* //GO.SYSIN DD *                                                    *
.* (DATA, IF ANY)                                                     *
.* /*                                                                 *
.*                                                                    *
.* PROBLEM 2: SUPPOSE AN ARRAY OF DOUBLE PRECISION FLOATING POINT     *
.* NUMBERS IS TO BE SORTED.  SUPPOSE THE NAME DBLSRT IS CHOSEN FOR    *
.* THE SUBROUTINE.  THE MACRO PROTOTYPE CARD WOULD THEN READ:         *
.*          QKRSRT DBLSRT,D                                           *
.* HERE THE LENGTH IS IMPLIED TO BE 8 BYTES.  IN THE FORTRAN PROGRAM  *
.* THE STATEMENT "CALL DBLSRT(.....)" WOULD APPEAR.                   *
.*                                                                    *
.* PROBLEM 3: SUPPOSE AN ARRAY OF FIXED POINT NUMBERS IS TO BE SORTED.*
.* ALSO TO BE SORTED ELEMENT FOR ELEMENT WITH THE FIRST ARRAY ARE     *
.* THREE ADDITIONAL ARRAYS.  THE MACRO PROTOTYPE WOULD THEN READ:     *
.*          QKRSRT EXSRT,F,EXTRA=3                                    *
.* HERE THE LENGTH IS IMPLIED TO BE 4 BYTES.  IN THE FORTRAN PROGRAM  *
.* THE STATEMENT "CALL EXSRT(.., .., .., .., ..)" WOULD APPEAR.       *
.*                                                                    *
.* PROBLEM 4: SUPPOSE A REAL*4 ARRAY WITH DIMENSION ARR(3,100) IS TO  *
.* BE SORTED ON THE SECOND COLLUMN.  THE MACRO PROTOTYPE STATEMENT    *
.* WOULD THEN READ:                                                   *
.*          QKRSRT OVRSRT,C,LENGTH=12,OVRTYP=E,DISP=5                 *
.* HERE SORTING WOULD BE DONE ACCORDING TO THE SECOND COLLUMN.  IN THE*
.* FORTRAN PROGRAM THE STATEMENT "CALL OVRSRT(ARR,100)" WOULD APPEAR. *
.*                                                                    *
.* PROBLEM 5: SUPPOSE AN IN LINE SORT IS DESIRED IN AN ASSEMBLER      *
.* ROUTINE, WITH FIXED POINT COMPARES.  THE MACRO PROTOTYPE WOULD     *
.* THEN READ:                                                         *
.*          QKRSRT XYZ,F,INLINE=YES                                   *
.* HERE THE DUMMY NAME "XYZ" IS NOT GENERATED.  ENTERING THE SORT     *
.* ROUTINE, REGISTER 1 MUST CONTAIN THE ADDRESS OF A PARAMETER LIST   *
.* WITH THE ADDRESSES OF: THE ARRAY TO BE SORTED, THE ARRAY LENGTH    *
.* VARIABLE, AND EXTRA ARRAY ADDRESSES IF ANY.  THE CONTAINING CSECT  *
.* MUST USE R(15) FOR A BASE REGISTER.                                *
.*                                                                    *
.* EFFECTIVELY THE USER PROVIDES A ONE INSTRUCTION ASSEMBLY LANGUAGE  *
.* PROGRAM.  THE USER IS NOW ABLE TO SORT ANYTHING ANY WAY.           *
.**********************************************************************
         LCLA  &STRTKX,&LT2,&SLNGTX,&ZZ,&WW,&XX,&YY,&KYLGTX,&DISPL
         LCLC  &LENGTX,&TYPX,&BRNCH1,&BRNCH2,&QCHK,&REG,&Q
&KYLGTX  SETA  &KYLGTH *               VARIABLE PARAMETERS ARE
&TYPX    SETC  '&TYPE' *               CHANGED TO VARIABLE SYMBOLS
&LENGTX  SETC  '&LENGTH' *             SO THEIR VALUES MAY BE RESET
&STRTKX  SETA  &STRTKY-1
&DISPL   SETA  0
         AIF   ('&OVRTYP' EQ 'NONE').REJ
&DISPL   SETA  &DISP-1
.REJ     AIF   (&EXTRA LE 5).L500
         MNOTE 16,'MORE THAN 5 EXTRA ARRAYS'
         MEXIT
.L500    ANOP
&QCHK    SETC  'G' *                   INDICATES TYPE IS NOT C OR P
&REG     SETC  '7' *                   WORK REG IF TYPE IS NOT C OR P
         AIF   ('&ORDER' EQ 'A').L31 * WHICH ORDER IS DESIRED FOR SORT?
&BRNCH1  SETC  'H' *                   DESCENDING ORDER
&BRNCH2  SETC  'L'
         AGO   .L32
.L31     ANOP *                        ASCENDING ORDER
&BRNCH1  SETC  'L'
&BRNCH2  SETC  'H'
.L32     AIF   (1 LE K'&NAME AND K'&NAME LE 6).L2 * IS NAME OKAY?
         MNOTE 16,'IMPROPER LENGTH FOR SUBROUTINE NAME' * INVALID NAME
         MEXIT *                       STOP EXPANSION
.L2      AIF   ('&TYPE' NE 'F' AND '&TYPE' NE 'E').L3 * TYPE F OR E?
&LENGTX  SETC  '4' *                   IF SO, SET LENGTH TO 4 BYTES
         AIF   ('&TYPE' NE 'F').L4
&TYPX    SETC  ' ' *                   IF TYPE F, SET &TYPX TO BLANK
         AGO   .L4
.L3      AIF   ('&TYPE' NE 'H').L5 *   TYPE H (HALFWORD)?
&LENGTX  SETC  '2' *                   IF SO, SET LENGTH TO 2 BYTES
         AGO   .L4
.L5      AIF   ('&TYPE' NE 'D').L6 *   TYPE D (DOUBLEWORD)?
&LENGTX  SETC  '8' *                   IF SO, SET LENGTH TO 8 BYTES
         AGO   .L4
.L6      AIF   ('&TYPE' EQ 'C' OR '&TYPE' EQ 'P').L7 * TYPE C OR P?
         MNOTE 16,'INVALID TYPE' *     IF ABOVE TYPES NOT SPECIFIED
         MEXIT *                       STOP EXPANSION
.L7      ANOP
&QCHK    SETC  'B' *                   INDICATES TYPE IS C OR P
&REG     SETC  '6' *                   WORK REG IF TYPE IS C OR P
         AIF   ('&LENGTH' NE '0').L1 * LENGTH BLANK FOR TYPE C OR P?
         MNOTE 16,'LENGTH WAS NOT SPECIFIED FOR TYPE P OR C'
         MEXIT *                       IF SO, STOP EXPANSION
.L1      AIF   ('&OVRTYP' EQ 'NONE').M2
&Q       SETC  'B'
         AIF   ('&OVRTYP' NE 'F' AND '&OVRTYP' NE 'E').M5
&YY      SETA  4
         AGO   .M8
.M5      AIF   ('&OVRTYP' NE 'H').M6
&YY      SETA  2
         AGO   .M8
.M6      AIF   ('&OVRTYP' NE 'D').M1
&YY      SETA  8
.M8      ANOP
&Q       SETC  'G'
         AIF   (&DISP+&YY-1 LE &LENGTX).L4
         MNOTE 16,'DISP PLUS OVRTYP LENGTH GREATER THAN LENGTH'
         MEXIT
.M1      MNOTE 16,'INVALID OVRTYP'
         MEXIT
.M2      AIF   (&STRTKX LE &LENGTH).M3
         MNOTE 16,'STRTKY GREATER THAN LENGTH'
         MEXIT
.M3      AIF   (&KYLGTX GT 0).M4
&KYLGTX  SETA  &LENGTH-&STRTKX
.M4      AIF   (&KYLGTX+&STRTKX LE &LENGTX).L4
         MNOTE 16,'KYLGTH PLUS STRTKY GREATER THAN LENGTH'
         MEXIT
.L4      ANOP
&LT2     SETA  &LENGTX*2 *             LENGTH * 2
&SLNGTX  SETA  &LENGTX/2-&LENGTX/8 *   SHIFT POSITIONS = LOG2(LENGTH)
         AIF   ('&QCHK' EQ 'G').L555
         AIF   ('&TYPE' EQ 'C').L551
         AIF   (&LENGTX LE 16).L555
         MNOTE 16,'LENGTH GREATER THAN 16 BYTES FOR DECIMAL'
         MEXIT
.L551    AIF   (&LENGTX LE 256).L555
         MNOTE 16,'LENGTH GREATER THAN 256 BYTES FOR CHARACTER'
         MEXIT
.L555    ANOP
*                                      BEGIN QUICKERSORTING
         AIF   ('&INLINE' EQ 'NO').L543
         STM   14,12,S&SYSNDX
         AGO   .L531
.L543    ANOP
&NAME    CSECT
         STM   14,12,12(13) *          SAVE REGISTERS
         USING &NAME,15 *              SET UP BASE REGISTER
.L531    ST    13,R13&SYSNDX *            STORE POINTER TO SAVEAREA
*                                      (AD(W) MEANS THE ADDRESS OF W)
*                                      THE CONTENTS OF THE REGISTERS
*                                      FOR QUICKERSORT ARE AS FOLLOWS:
*                                       1 - LENGTH
*                                       2 - TMP (TEMP. COMPARE STORAGE)
*                                       4 - T (TEMPORARY COMPARE ITEM)
*                                       5 - M (INDEX FOR LT AND UT)
*                                       8 - Q LOOP DECREMENT = LENGTH
*                                       9 - Q LOOP COMPARAND = AD(A(K))
*                                      10 - K LOOP INCREMENT = LENGTH
*                                      11 - K LOOP COMPARAND = AD(A(Q))
*                                      12 - AD(A(I))
*                                      13 - AD(A(J))
*                                      14 - =F'4' (CONSTANT)
*                                      15 - BASE REGISTER
*                                      0,3,6,7 - WORK REGISTERS
&XX      SETA  3+&EXTRA
         LM    2,&XX,0(1)
         L     3,0(3)
         AIF   (&EXTRA EQ 0).L88
         LR    9,2
         AIF   (&EXTRA GT 1).L94
         LR    10,4
         AGO   .L95
.L94     ANOP
&YY      SETA  9+&EXTRA
         LM    10,&YY,8(1)
.L95     ANOP
&ZZ      SETA  9
&WW      SETA  4
&YY      SETA  &EXTRA
.L90     SR    &ZZ,&WW
&YY      SETA  &YY-1
&ZZ      SETA  &ZZ+1
&WW      SETA  &WW+1
         AIF   (&YY GT 0).L90
         SR    &ZZ,2
         STM   9,&ZZ,DA&SYSNDX
.L88     LA    1,&LENGTX
         SR    2,1
         SR    5,5 *                   M:=1;
         LA    14,4
         LR    10,1
         LCR   8,1
         LR    13,3
         MR    12,1
         LR    12,1 *                  I:=1;
         AR    12,2
         AR    13,2
         AIF   ('&QCHK' EQ 'G').L1000
         LA    2,TMP&SYSNDX
.L1000   ANOP
N&SYSNDX LR    &REG,13 *               N: IF J-I > 1 THEN
         SR    &REG,12
         SR    &REG,1
         BNP   L3E&SYSNDX
*                                      BEGIN COMMENT;
         AR    &REG,1 *                   P:=I+(J-I)/2;
         AIF   ('&QCHK' EQ 'G').L41
         SRDA  6,33
         DR    6,1
         MR    6,1
         AGO   .L42
.L41     SRA   7,&SLNGTX+1
         SLA   7,&SLNGTX
.L42     AR    7,12
         AIF   ('&QCHK' EQ 'G').L43
         MVC   0(&LENGTX,2),0(7) *          T := A(P);
         MVC   0(&LENGTX,7),0(12) *          A(P) := A(I);
         AIF   (&EXTRA EQ 0).L4444
&XX      SETA  0
&YY      SETA  0
&ZZ      SETA  &LENGTX
.L1002   S     7,DA&SYSNDX+&YY
         S     12,DA&SYSNDX+&YY
         AIF   (&XX EQ &EXTRA).L44
         MVC   &ZZ.(&LENGTX,2),0(7)
         MVC   0(&LENGTX,7),0(12)
&XX      SETA  &XX+1
&YY      SETA  &YY+4
&ZZ      SETA  &ZZ+&LENGTX
         AGO   .L1002
.L43     L&TYPX    4,0(7) *                T:=A(P);
         L&TYPX    6,0(12)
         ST&TYPX    6,0(7) *                A(P):=A(I);
         AIF   (&EXTRA EQ 0).L44
&XX      SETA  0
&YY      SETA  0
&ZZ      SETA  &LENGTX
.L1001   S     7,DA&SYSNDX+&YY
         S     12,DA&SYSNDX+&YY
         AIF   (&XX EQ &EXTRA).L44
         L&TYPX 0,0(7)
         L&TYPX 6,0(12)
         ST&TYPX 6,0(7)
         ST&TYPX 0,TMP&SYSNDX+&ZZ
&XX      SETA  &XX+1
&YY      SETA  &YY+4
&ZZ      SETA  &ZZ+&LENGTX
         AGO   .L1001
.L4444   AIF   ('&OVRTYP' EQ 'NONE' OR '&TYPE' NE 'C').L44
&TYPX    SETC  '&OVRTYP'
         AIF   ('&TYPX' NE 'F').NOREJ
&TYPX    SETC  ' '
.NOREJ   L&TYPX 4,&DISPL.(2)
.L44     LR    11,13 *                  Q := J;
         LR    9,12
         AR    9,1
         AIF   ('&OVRTYP' EQ 'NONE' OR '&TYPE' NE 'C').NN
&QCHK    SETC  '&Q'
&TYPX    SETC  '&OVRTYP'
         AIF   ('&TYPX' NE 'F').NN
&TYPX    SETC  ' '
.NN      AIF   ('&QCHK' EQ 'B').L11
LP1&SYSNDX C&TYPX    4,&DISPL.(9) *          FOR K:=I+1STEP1 UNTIL Q DO
*                                      BEGIN COMMENT;
         BN&BRNCH1   ND1&SYSNDX *               IF A(K) > T THEN
*                                      BEGIN COMMENT;
LP2&SYSNDX C&TYPX  4,&DISPL.(11) *         FOR Q:=Q STEP-1 UNTIL K+1 DO
*                                      BEGIN COMMENT;
         AGO   .L12
.L11     AIF   ('&TYPX' NE 'P').L13
*                                      FOR K := I+1 STEP 1 UNTIL Q DO
*                                      BEGIN COMMENT;
*                                     IF A(K) > T THEN
LP1&SYSNDX CP    &STRTKX.(&KYLGTX,2),&STRTKX.(&KYLGTX,9)
         BN&BRNCH1   ND1&SYSNDX
*                                      BEGIN COMMENT;
*                                     FOR Q := Q STEP -1 UNTIL K+1 DO
*                                      BEGIN COMMENT;
LP2&SYSNDX CP    &STRTKX.(&KYLGTX,2),&STRTKX.(&KYLGTX,11)
         AGO   .L12
.L13     ANOP
*                                      FOR K := I+1 STEP 1 UNTIL Q DO
*                                      BEGIN COMMENT;
*                                     IF A(K) > T THEN
LP1&SYSNDX CLC   &STRTKX.(&KYLGTX,2),&STRTKX.(9)
         BN&BRNCH1   ND1&SYSNDX
*                                      BEGIN COMMENT;
*                                     FOR Q := Q STEP -1 UNTIL K+1 DO
*                                      BEGIN COMMENT;
LP2&SYSNDX CLC   &STRTKX.(&KYLGTX,2),&STRTKX.(11)
.L12     AIF   ('&OVRTYP' EQ 'NONE' OR '&TYPE' NE 'C').K1
&QCHK    SETC  'B'
&TYPX    SETC  'C'
.K1      BN&BRNCH2   ND2&SYSNDX *               IF A(Q) < T THEN
*                                      BEGIN COMMENT;
&YY      SETA  0-4
&XX      SETA  0
.L99     AIF   ('&QCHK' EQ 'G').L46
         XC    0(&LENGTX,11),0(9) *          X := A(K);
         XC    0(&LENGTX,9),0(11) *          A(K) := A(Q);
         XC    0(&LENGTX,11),0(9) *          A(Q) := X;
         AGO   .L147
.L46     L&TYPX    6,0(11)
         L&TYPX    0,0(9) *                X:=A(K);
         ST&TYPX    6,0(9) *                A(K):=A(Q);
         ST&TYPX   0,0(11) *               A(Q):=X;
.L147    AIF   (&EXTRA EQ 0).L47
&XX      SETA  &XX+1
&YY      SETA  &YY+4
         S     9,DA&SYSNDX+&YY
         S     11,DA&SYSNDX+&YY
         AIF   (&XX LE &EXTRA).L99
.L47     SR    11,1 *                  Q := Q - 1;
*                                      COMMENT;
         B     ND1&SYSNDX *               GO TO L (ND1);
*                                      END
ND2&SYSNDX BXH   11,8,LP2&SYSNDX *          END FOR Q;
         LR    11,9 *                  Q := K - 1;
         SR    11,1
         B     M&SYSNDX *                 GO TO M;
*                                      END
ND1&SYSNDX BXLE  9,10,LP1&SYSNDX *       L: END FOR K;
*                                      COMMENT;
         AIF   ('&QCHK' EQ 'G').L48
M&SYSNDX MVC   0(&LENGTX,12),0(11) *      M: A(I) := A(Q);
         MVC   0(&LENGTX,11),TMP&SYSNDX *       A(Q) := T;
         AIF   (&EXTRA EQ 0).L49
&XX      SETA  0
&YY      SETA  0
&ZZ      SETA  &LENGTX
.L876    S     11,DA&SYSNDX+&YY
         S     12,DA&SYSNDX+&YY
         AIF   (&XX EQ &EXTRA).L49
&YY      SETA  &YY+4
         MVC   0(&LENGTX,11),&ZZ.(2)
&XX      SETA  &XX+1
         MVC   0(&LENGTX,12),0(11)
&ZZ      SETA  &ZZ+&LENGTX
         AGO   .L876
.L48     ANOP
M&SYSNDX L&TYPX    6,0(11) *            M: A(I):=A(Q);
         ST&TYPX    6,0(12)
         ST&TYPX    4,0(11) *               A(Q):=T;
         AIF   (&EXTRA EQ 0).L49
&XX      SETA  0
&YY      SETA  0
&ZZ      SETA  &LENGTX
.L765    S     11,DA&SYSNDX+&YY
         S     12,DA&SYSNDX+&YY
         AIF   (&XX EQ &EXTRA).L49
         L&TYPX 0,0(11)
         ST&TYPX 0,0(12)
         L&TYPX 4,TMP&SYSNDX+&ZZ
         ST&TYPX 4,0(11)
&XX      SETA  &XX+1
&YY      SETA  &YY+4
&ZZ      SETA  &ZZ+&LENGTX
         AGO   .L765
.L49     ANOP
*                                      COMMENT;
         LR    6,11 *                  IF 2 *Q > I+J THEN
         SR    11,1
         SLA    6,1
         SR    6,12
         SR    6,13
         BNP   LWR&SYSNDX *               BEGIN
         ST    12,LT&SYSNDX.(5) *           LT(M) := I;
         ST    11,UT&SYSNDX.(5) *          UT(M) := Q - 1;
         LA    11,&LT2.(11)
         LR    12,11 *                 I:=Q+1;
         B     UM&SYSNDX *                END
*                                      ELSE
*                                      BEGIN
LWR&SYSNDX ST    13,UT&SYSNDX.(5) *           UT(M) := J;
         LR    13,11 *                 J:=Q-1;
         LA    11,&LT2.(11)
         ST    11,LT&SYSNDX.(5) *          LT(M) := Q + 1;
*                                      COMMENT;
UM&SYSNDX LA    5,4(5) *                M := M + 1;
         B     N&SYSNDX *                 GO TO N
*                                      END
L3E&SYSNDX CR    12,13 *                   ELSE IF I >= J THEN
*                                      BEGIN COMMENT;
         BNL   P&SYSNDX *                 GO TO P
*                                      END
*                                      ELSE
*                                      BEGIN COMMENT;
         AIF   ('&OVRTYP' EQ 'NONE' OR '&TYPE' NE 'C').NP
&QCHK    SETC  '&Q'
&TYPX    SETC  '&OVRTYP'
         AIF   ('&TYPX' NE 'F').NP
&TYPX    SETC  ' '
.NP      AIF   ('&QCHK' EQ 'B').L21
         L&TYPX    0,&DISPL.(12)
         C&TYPX    0,&DISPL.(13) *               IF A(I) > A(J) THEN
         AGO   .L22
.L21     AIF   ('&TYPX' NE 'P').L23
         CP    &STRTKX.(&KYLGTX,12),&STRTKX.(&KYLGTX,13)
*                                     IF A(I) > A(J) THEN
         AGO   .L22
.L23     CLC   &STRTKX.(&KYLGTX,12),&STRTKX.(13)
*                                     IF A(I) > A(J) THEN
.L22     AIF   ('&OVRTYP' EQ 'NONE' OR '&TYPE' NE 'C').K2
&QCHK    SETC  'B'
&TYPX    SETC  'C'
.K2      BN&BRNCH2   P&SYSNDX
*                                      BEGIN
&YY      SETA  0-4
&XX      SETA  0
         AIF   ('&QCHK' EQ 'G').L410
.L999    AIF   ('&QCHK' EQ 'G').L987
         XC    0(&LENGTX,12),0(13) *           X := A(I);
         XC    0(&LENGTX,13),0(12) *           A(I) := A(J);
         XC    0(&LENGTX,12),0(13) *           A(J) := X;
         AGO   .L41129
.L987    L&TYPX 0,0(12)
.L410    L&TYPX    6,0(13) *               X:=A(I);
         ST&TYPX    0,0(13) *               A(I):=A(J);
         ST&TYPX    6,0(12) *               A(J):=X;
.L41129  AIF   (&EXTRA EQ 0).L411
&XX      SETA  &XX+1
&YY      SETA  &YY+4
         S     12,DA&SYSNDX+&YY
         S     13,DA&SYSNDX+&YY
         AIF   (&XX LE &EXTRA).L999
.L411    ANOP
*                                      END;
*                                      COMMENT;
P&SYSNDX SR    5,14 *               P: M := M - 1;
         BM    QT&SYSNDX *                IF M > 0 THEN
*                                      BEGIN
         L     12,LT&SYSNDX.(5) *           I := LT(M);
         L     13,UT&SYSNDX.(5) *           J := UT(M);
         B     N&SYSNDX *                 GO TO N
*                                      END;
*                                      END
QT&SYSNDX L     13,R13&SYSNDX *            END QUICKERSORT
         AIF   ('&INLINE' EQ 'NO').F100
         LM    14,12,S&SYSNDX
         B     YY&SYSNDX
S&SYSNDX DS    15F
         AGO   .F250
.F100    LM    14,12,12(13) *          RESTORE REGISTERS
         BR    14 *                    RETURN TO CALLING PROGRAM
.F250    ANOP
UT&SYSNDX DS    20F *                   AUXILLARY STORAGE FOR UT ARRAY
LT&SYSNDX DS    20F *                   AUXILLARY STORAGE FOR LT ARRAY
         AIF   (&EXTRA GT 0).LK
         AIF   ('&QCHK' EQ 'G').LL
TMP&SYSNDX DS    CL&LENGTH *                  TEMP STORAGE FOR COMPARES
         AGO   .LL
.LK      ANOP
&YY      SETA  &EXTRA+1
TMP&SYSNDX DS  &YY.CL&LENGTX *                 TEMP STORAGE FOR COMPARE
.LL      ANOP
R13&SYSNDX DS    F *                     SAVE AREA FOR REGISTER 13
         AIF   (&EXTRA EQ 0).LM
&YY      SETA  &EXTRA+1
DA&SYSNDX DS   &YY.F *                    SAVE AREA FOR ADDRESS CHANGES
.LM      AIF   ('&INLINE' EQ 'NO').LMN
YY&SYSNDX NOPR 12
.LMN     MEND
./       ADD   NAME=RCPDDN
         MACRO
         RCPDDN &DDN
         GBLC  &DYNP
         SPACE 1
***********************************************************************
**   BUILD THE DDNAME TEXT UNIT                                      **
***********************************************************************
         AIF   ('&DDN'(K'&DDN,1) EQ '/').BTU
         AIF   ('&DDN'(1,1) EQ '''').Q
         RCPSR2
         AIF   ('&DDN'(1,1) EQ '(').R
         L     R14,&DDN                LOAD ADDRESS OF DDNAME
         LH    R2,&DDN+4               LOAD LENGTH OF DDNAME
         AGO   .STH
.R       L     R14,0&DDN               LOAD ADDRESS OF DDNAME
         LH    R2,4&DDN                LOAD LENGTH OF DDNAME
.STH     STH   R2,S99TULNG             STORE DDNAME LENGTH
         BCTR  R2,0                    DECREMENT FOR EXECUTE
         EX    R2,&DYNP.MVC            MOVE DDNAME
         MVI   S99TUKEY+1,DALDDNAM     MOVE IN DDNAME KEY
         MVI   S99TUNUM+1,1            SET NUMBER FIELD
         RCPDINC 14
         MEXIT
.Q       RCPBTU DALDDNAM,1,&DDN
         MEXIT
.BTU     RCPTUBFR DALDDNAM,14,&DDN
         MEND
./       ADD   NAME=RCPDINC
         MACRO
         RCPDINC &L1
         GBLA  &DTUO,&DTUPO
         GBLC  &DYNP
         AIF   ('&L1' EQ '').T2
         ST    R15,&DYNP.TUP+&DTUPO    STORE TEXT UNIT ADDRESS
         LA    R15,&L1.(R15)           BUMP TEXT UNIT PTR TO NEXT SLOT
&DTUPO   SETA  &DTUPO+4
&DTUO    SETA  &DTUO+&L1
         MEXIT
.T2      ST    R14,&DYNP.TUP+&DTUPO    STORE TEXT UNIT ADDRESS
&DTUPO   SETA  &DTUPO+4
         MEND
./       ADD   NAME=RCPDISP
         MACRO
         RCPDISP &DISP
         LCLA  &I
         LCLB  &B(4)
         AIF   ('&DISP(1)' EQ '').TD2
         SPACE
***********************************************************************
**     DATA SET INITIAL STATUS                                       **
***********************************************************************
&B(1)    SETB  ('&DISP(1)' EQ 'SHR')
&B(2)    SETB  ('&DISP(1)' EQ 'NEW')
&B(3)    SETB  ('&DISP(1)' EQ 'MOD')
&B(4)    SETB  ('&DISP(1)' EQ 'OLD')
         AIF   (&B(1) OR &B(2) OR &B(3) OR &B(4)).OK1
         MNOTE 8,'&DISP(1) IS INVALID, DISP=SHR USED'
&B(1)    SETB  1
.OK1     ANOP
&I       SETA  8*&B(1)+4*&B(2)+2*&B(3)+&B(4)
         MVC   S99TUKEY(8),=Y(DALSTATS,1,1,X'0&I.00')
         RCPDINC 8
.TD2     AIF   ('&DISP(2)' EQ '').TD3
         SPACE
***********************************************************************
**    DATA SET NORMAL DISPOSITION                                    **
***********************************************************************
&B(1)    SETB  ('&DISP(2)' EQ 'KEEP')
&B(2)    SETB  ('&DISP(2)' EQ 'DELETE')
&B(3)    SETB  ('&DISP(2)' EQ 'CATLG')
&B(4)    SETB  ('&DISP(2)' EQ 'UNCATLG')
         AIF   (&B(1) OR &B(2) OR &B(3) OR &B(4)).OK2
         MNOTE 8,'&DISP(2) IS INVALID, DISP=(,KEEP) USED'
&B(1)    SETB  1
.OK2     ANOP
&I       SETA  8*&B(1)+4*&B(2)+2*&B(3)+&B(4)
         MVC   S99TUKEY(8),=Y(DALNDISP,1,1,X'0&I.00')
         RCPDINC 8
.TD3     AIF   ('&DISP(3)' EQ '').EXIT
         SPACE
***********************************************************************
**   DATASET CONDITIONAL DISPOSITION                                 **
***********************************************************************
&B(1)    SETB  ('&DISP(3)' EQ 'KEEP')
&B(2)    SETB  ('&DISP(3)' EQ 'DELETE')
&B(3)    SETB  ('&DISP(3)' EQ 'CATLG')
&B(4)    SETB  ('&DISP(3)' EQ 'UNCATLG')
         AIF   (&B(1) OR &B(2) OR &B(3) OR &B(4)).OK3
         MNOTE 8,'&DISP(3) IS INVALID, DISP=(,,KEEP) USED'
&B(1)    SETB  1
.OK3     ANOP
&I       SETA  8*&B(1)+4*&B(2)+2*&B(3)+&B(4)
         MVI   S99TUKEY(8),=Y(DALCDISP,1,1,X'0&I.00')
         RCPDINC 8
.EXIT    MEND
./       ADD   NAME=RCPDSN
         MACRO
         RCPDSN &DSN,&MEM
         LCLC  &MEMBER
         GBLC  &DYNP
         SPACE
***********************************************************************
**   BUILD THE DSNAME TEXT UNIT                                      **
***********************************************************************
         AIF   ('&DSN'(1,1) EQ '''').Q
         AIF   ('&DSN'(K'&DSN,1) EQ '/').BD
         AIF   ('&DSN'(1,1) EQ '(').REG
         AIF   ('&DSN'  EQ '*').TERM
         RCPSR2
         L     R14,&DSN                LOAD ADDRESS OF DSNAME
         LH    R2,&DSN+4               LOAD LENGTH OF DSNAME
.STH     STH   R2,S99TULNG             STORE DSNAME LENGTH
         BCTR  R2,0                    DECREMENT FOR EXECUTE
         EX    R2,&DYNP.MVC            MOVE DSNAME
         MVI   S99TUKEY+1,DALDSNAM     MOVE IN DSNAME KEY
         MVI   S99TUNUM+1,1            SET NUMBER FIELD
         RCPDINC 50
         AGO   .TMEMBER
.REG     L     R14,0&DSN               LOAD ADDRESS OF DSNAME
         RCPSR2
         LH    R2,4&DSN                LOAD LENGTH OF DSNAME
         AGO   .STH
.TERM    MVI   S99TUKEY+1,DALTERM
         RCPDINC 4
         MEXIT
.BD      RCPTUBFR DALDSNAM,50,&DSN
         AGO   .TMEMBER
.Q       RCPBTU DALDSNAM,1,&DSN
.TMEMBER AIF   ('&MEM' EQ '').EXIT
         SPACE
***********************************************************************
**   BUILD THE MEMBER NAME TEXT UNIT                                 **
***********************************************************************
&MEMBER  SETC  '&MEM'
         AIF   ('&MEM' NE '*').MOK
         AIF   ('&DSN'(1,1) NE '''').MAST
         MNOTE 8,'MEMBER=* INVALID WITH QUOTED DSNAME'
         MEXIT
.MAST    ANOP
&MEMBER  SETC  '8+&DSN'
.MOK     ANOP
         AIF   ('&MEMBER'(K'&MEMBER,1) EQ '/').BM
         RCPSR2
         AIF   ('&MEMBER'(1,1) EQ '(').RM
         LH    R2,4+&MEMBER            LOAD LENGTH OF MEMBER NAME
         LTR   R2,R2                   TEST FOR ZERO
         BZ    *+30                    IF NO MEMBER, SKIP
         L     R14,&MEMBER             LOAD ADDRESS OF MEMBER
         AGO   .STHM
.RM      LH    R2,4&MEMBER             LOAD LENGTH OF MEMBER
         LTR   R2,R2                   AND TEST FOR ZERO
         BZ    *+30                    IF NO MEMBER, SKIP
         L     R14,0&MEMBER            LOAD ADDRESS OF MEMBER
.STHM    STH   R2,S99TULNG             STORE LENGTH OF MEMBER
         BCTR  R2,0                    DECREMENT FOR EXECUTE
         EX    R2,&DYNP.MVC            MOVE IN MEMBER NAME
         MVI   S99TUKEY+1,DALMEMBR     MOVE IN MEMBER KEY
         MVI   S99TUNUM+1,1            SET NUMBER FIELD
         RCPDINC 14
         MEXIT
.BM      RCPTUBFR DALMEMBR,14,&MEMBER
         MEXIT
.QM      RCPBTU DALMEMBR,1,&MEMBER
.EXIT    MEND
./       ADD   NAME=RCPFDDN
         MACRO
         RCPFDDN &DDN
         GBLC &DYNP
         SPACE
***********************************************************************
**        FREE DDNAME TEXT UNIT                                      **
***********************************************************************
         AIF   ('&DDN'(1,1) EQ '''').Q
         AIF   ('&DDN'(K'&DDN,1) EQ '/').B
         RCPSR2
         AIF   ('&DDN'(1,1) EQ '(').R
         L     R14,&DDN                LOAD ADDRESS OF DDNAME
         LH    R2,&DDN+4               LOAD LENGTH OF DDNAME
         AGO   .STH
.R       L     R14,0&DDN               LOAD ADDRESS OF DDNAME
         LH    R2,4&DDN                LOAD LENGTH OF DDNAME
.STH     STH   R2,S99TULNG             STORE DDNAME LENGTH
         BCTR  R2,0                    DECREMENT FOR EXECUTE
         EX    R2,&DYNP.MVC            MOVE DDNAME
         MVI   S99TUKEY+1,DUNDDNAM     MOVE IN DDNAME KEY
         MVI   S99TUNUM+1,1            SET NUMBER FIELD
         RCPDINC 14
         MEXIT
.Q       RCPBTU DUNDDNAM,1,&DDN
         MEXIT
.B       RCPTUBFR DUNDDNAM,14,&DDN
         MEND
./       ADD   NAME=RCPSR2
         MACRO
         RCPSR2 &A
         GBLB  &RCPSR2
         GBLC  &DYNP
         LCLC  &C
.*   TO SAVE REG 2 IN REG 0 FOR ALLOC INNER MACROS FIRST TIME ONLY
.*    IF OPERAND SUPPLIED AND SAVE DONE, RESTORES REG 2 AND
.*    GENERATES MOVE INSTRUCTION FOR EXECUTE
         AIF   ('&A' NE '').UNSAVE
         AIF   (&RCPSR2).EXIT
&RCPSR2  SETB  1
         LR    R0,R2                   SAVE CONTENTS OF REGISTER 2
         MEXIT
.UNSAVE  AIF   (NOT &RCPSR2).EXIT
         B     *+10                    SKIP NEXT INSTRUCTION
&C       SETC  '&DYNP.MVC'
&C       MVC   S99TUPAR(0),0(R14)      EXECUTED MOVE
         LR    R2,R0                   RESTORE CONTENTS OF REGISTER 2
&RCPSR2  SETB  0
.EXIT    MEND
./       ADD   NAME=RCPSYSOU
         MACRO
         RCPSYSOU &CLASS,&COPIES=,&FREE=,&DEST=,&FORMS=
         GBLC  &DYNP
         LCLC  &C
         AIF   ('&CLASS(1)' EQ '').TPGN
&C       SETC  '&CLASS(1)'
         SPACE
***********************************************************************
**       SYSOUT CLASS TEXT UNIT                                      **
***********************************************************************
         AIF   ('&C'(1,1) EQ '''').Q
         AIF   ('&C'(K'&C,1) EQ '/').BS
         AIF   ('&C'(1,1) EQ '(').REG
         L     R14,&C                  LOAD ADDRESS OF SYSOUT CLASS
         MVC   S99TUPAR(1),0(R14)       AND MOVE IT TO TEXT UNIT
         AGO   .SKEY
.REG     MVC   S99TUPAR(1),0&C         MOVE SYSOUT CLASS TO TEXT UNIT
.SKEY    MVI   S99TUKEY+1,DALSYSOU     SET SYSOUT KEY
         MVI   S99TUNUM+1,1            SET NUMBER FIELD
         MVI   S99TULNG+1,1            SET LENGTH FIELD
         RCPDINC 8
         AGO   .TPGN
.BS      RCPTUBFR DALSYSOU,14,&C
         AGO   .TPGN
.Q       RCPBTU DALSYSOU,1,&C
.TPGN    AIF   ('&CLASS(2)' EQ '').TCOP
         SPACE
***********************************************************************
**   SYSOUT PROGRAM NAME TEXT UNIT                                   **
***********************************************************************
&C       SETC  '&CLASS(2)'
         RCPVCHAR DALSPGNM,14,&C
.TCOP    AIF   ('&COPIES' EQ '').TFREE
         SPACE
***********************************************************************
**    SYSOUT COPIES TEXT UNIT                                        **
***********************************************************************
         RCPNTU DALCOPYS,1,&COPIES
.TFREE   AIF   ('&FREE' EQ '').TDEST
         SPACE
***********************************************************************
**     FREE = CLOSE TEXT UNIT                                        **
***********************************************************************
         AIF   ('&FREE' EQ 'CLOSE').CLOSEOK
         MNOTE 4,' **** FREE=&FREE INVALID, FREE=CLOSE USED'
.CLOSEOK MVI   S99TUKEY+1,DALCLOSE     MOVE IN TEXT UNIT KEY
         RCPDINC 4
.TDEST   AIF   ('&DEST' EQ '').TFORMS
         SPACE
***********************************************************************
**       SYSOUT DESTINATION TEXT UNIT                                **
***********************************************************************
         RCPVCHAR DALSUSER,14,&DEST
.TFORMS  AIF   ('&FORMS' EQ '').EXIT
         SPACE
***********************************************************************
**     SYSOUT FORMS NUMBER TEXT UNIT                                 **
***********************************************************************
         RCPVCHAR DALSFMNO,14,&FORMS
.EXIT    MEND
./       ADD   NAME=RCPUNALC
         MACRO
         RCPUNALC
         SPACE 1
***********************************************************************
**     FREE EVEN IF PERMANENTLY ALLOCATED                            **
***********************************************************************
         MVI   S99TUKEY+1,DUNUNALC     SET TEXT UNIT KEY
         RCPDINC  4
         MEND
./       ADD   NAME=RCPUNIT
         MACRO
         RCPUNIT &U,&V
         GBLC  &DYNP
         AIF   ('&U' EQ '').TVOL
         SPACE 1
***********************************************************************
**       UNIT NAME TEXT UNIT                                         **
***********************************************************************
         RCPVCHAR DALUNIT,14,&U
.TVOL    AIF   ('&V' EQ '').EXIT
         SPACE 1
***********************************************************************
**       VOLUME SERIAL TEXT UNIT                                     **
***********************************************************************
         RCPVCHAR DALVLSER,14,&V
.EXIT    MEND
./       ADD   NAME=RCPVCHAR
         MACRO
         RCPVCHAR &KEY,&LEN,&C,&N=1
         GBLC  &DYNP
         AIF   ('&C'(K'&C,1) EQ '/').BM
         AIF   ('&C'(1,1) EQ '''').QM
         RCPSR2
         AIF   ('&C'(1,1) EQ '(').RM
         LH    R2,&C+4                 LOAD LENGTH OF TEXT UNIT
         LTR   R2,R2                   TEST FOR ZERO
         BZ    *+30                    IF NO TEXT UNIT, SKIP
         L     R14,&C                  LOAD ADDRESS OF TEXT UNIT
         AGO   .STHM
.RM      LH    R2,4&C                  LOAD LENGTH OF TEXT UNIT
         LTR   R2,R2                   AND TEST FOR ZERO
         BZ    *+30                    IF NO TEXT UNIT, SKIP
         L     R14,0&C                 LOAD ADDRESS OF TEXT UNIT
.STHM    STH   R2,S99TULNG             STORE LENGTH OF TEXT UNIT
         BCTR  R2,0                    DECREMENT FOR EXECUTE
         EX    R2,&DYNP.MVC            MOVE IN TEXT UNIT
         MVI   S99TUKEY+1,&KEY         MOVE IN TEXT UNIT KEY
         AIF   ('&N' EQ '1' OR '&N' EQ '').N1
         LA    R14,&N                  LOAD TEXT UNIT NUMBER
         STH   R14,S99TUNUM             AND STORE IT IN TEXT UNIT
         AGO   .ENDN
.N1      MVI   S99TUNUM+1,1            SET NUMBER FIELD
.ENDN    RCPDINC &LEN
         MEXIT
.BM      RCPTUBFR &KEY,&LEN,&C
         MEXIT
.QM      RCPBTU &KEY,&N,&C
         MEND
./       ADD   NAME=SETUP
         TITLE 'INITIALIZATION ROUTINE.'
SETUP   $PROLOG R12
         L     R11,=V(DATASECT)    ADDRESS OF DATA CSECT
         USING DATASECT,R11        SET ADDRESSIBILITY.
         L     R2,TABSTART         GET TABLE START ADDRESS.        @162
         LTR   R2,R2               IS THERE ONE ALREADY?           @162
         BZ    SETUP1              NO, SO ALLOCATE IT.             @162
         ST    R2,TABOFF           SET BACK TO START OF TABLE.     @162
         B     SETUP99             AND GET OUT.                    @162
SETUP1   L     R2,TABMAX           GET MAXIMUM LENGTH.
         L     R3,TABMIN           GET MINIMUM LENGTH.             @152
         L     R1,16               A(CVT)                          @162
         TM    CVTDCB-CVTMAP(R1),CVTMVSE  XA?                      @162
         BO    SETUP2              YES                             @162
         GETMAIN VU,                                               @162X
               LA=(2),                                             @162X
               A=TABAD                                             @166
         L     R1,TABAD                                            @166
         B     SETUP3                                              @162
SETUP2   GETMAIN VRU,                                              @152X
               LV=((2),(3)),                                       @152X
               LOC=(ANY,ANY)       GETMAIN A DATASET INFO TABLE.   @152
         ST    R1,TABAD                                            @166
SETUP3   ST    R1,TABSTART         STORE START OF TABLE.
         ST    R1,TABOFF           BEGIN AT THE BEGINNING, SAID ALICE
         ST    R0,TABLEN           SAVE LENGTH GOTTEN.             @152
         L     R0,TABLEN           RESTORE LENGTH.
         SRDA  R0,32               SET UP TO DIVIDE.
         LA    R4,SAVELEN          LENGTH OF ONE ENTRY
         DR    R0,R4               TOTAL LENGTH / ENTRY LENGTH
         ST    R1,MAXDSCBS         SAVE TABLE CAPACITY.
         LR    R0,R1               MAXIMUM TABLE SLOTS.
         SRDA  R0,32               SET UP TO MULTIPLY
         M     R0,=A(SAVELEN)      TIMES SLOT LENGTH
         A     R1,TABAD            PLUS START OF TABLE.
         S     R1,=A(SAVELEN)      BACKUP ONE SLOT.
         ST    R1,TABEND           LAST AVAILABLE SLOT.
         L     R1,MAXDSCBS         PICK UP TABLE CAPACITY          @150
         CVD   R1,DTWORK           CONVERT TO PACKED.              @150
         EDIT  MAXMSG,DTL4,ZZZZZZ9                                 @152
         LA    R2,MAXMSG                                           @150
         LM    R15,R1,=A(MESGRTN,0,25)                             @150
         BALR  R14,R15             PRINT OUT TABLE CAPACITY.       @150
SETUP99  DS    0H
        $EPILOG ,                  RETURN.
         LTORG ,
./       ADD   NAME=STUB
PASSVMF  CSECT                                                     @151
         BR    R14                                                 @151
./       ADD   NAME=UNDERTSO
         MACRO
&LOCLBL  UNDERTSO &YES=,&NO=
.*                                                                   *.
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
.*                                                                   *.
.*  ISYES          IDENTIFY WHETHER OR NOT TSO     IS BEING USED     *.
.*                                                                   *.
.*                 TAKES A BRANCH DEPENDING ON WHICH PARMS ARE       *.
.*                 SPECIFIED                                         *.
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
         AIF   ('&YES' EQ '' AND '&NO' EQ '').ERR
&LOCLBL  DS    0H                      REG &R --> REAL CVT
         L     R14,X'224'              GET ASCB ADDRESS
         L     R14,60(R14)             GET ASCBTSB FIELD
         LTR   R14,R14                 IF ZERO, NOT UNDER TSO
         AIF   ('&YES' EQ '').GEN370
         BNZ   &YES
.GEN370  ANOP
         AIF   ('&NO' EQ '').EXIT
         BZ    &NO
         AGO   .EXIT
.EXIT    ANOP
         MEXIT
.ERR     MNOTE 8,'*** UNDERTSO ERROR, MUST SPECIFY YES OR NO '
         MEND
./       ADD   NAME=UNITNAME
         TITLE 'TRANSLATE UNITNAMES TO VOLSERS.'
UNITNAME $PROLOG R12
         L     R11,=V(DATASECT)
         USING DATASECT,R11
         L     R2,=AL4(*+6+X'80000000')
         BSM   0,R2               GET INTO 31 BIT.
         LA    R4,UNITS           TABLE OF ESOTERIC NAMES.
         SR    R5,R5
         IC    R5,0(0,R4)         LENGTH OF UNITNAME.
         MVI   NAMETAB,X'40'      CLEAR NAME TABLE TO BLANKS
         MVC   NAMETAB+1(255),NAMETAB
         MVC   NAMETAB+256(255),NAMETAB
         MVC   NAMETAB+511(255),NAMETAB
         MVC   NAMETAB+766(255),NAMETAB
         MVC   NAMETAB+1021(255),NAMETAB
         MVC   NAMETAB+1276(255),NAMETAB
         MVC   NAMETAB+1531(255),NAMETAB
         MVC   NAMETAB+1786(14),NAMETAB
         SPACE 2
LOAD     L     R1,CVTPTR          CVT ADDRESS
         USING CVTMAP,R1          ADDRESS TO DSECT
         L     R1,CVTJESCT        JES CONTROL TABLE
         USING JESCT,R1           ADDRESS TO DSECT
         L     R15,JESCTEXT       EXTENSION
         L     R15,JESGB4UV-JESPEXT(R15)  ADR OF IEFGB4UV
         ST    R15,DSAGB4UV
         L     R1,JESEDT          EDT
         DROP  R1
         L     R10,EDTLUVSP(,R1)  LOOK-UP SECTION
         L     R0,LUVENTNO(,R10)  # OF UNIT NAMES
         LA    R8,LUVENTLN        LENGTH OF ENTRY                  $EDT
         LA    R15,LUVENTRY(,R10)  FIRST ENTRY
         LA    R1,NAMETAB
TABLSCAN MVC   0(8,R1),0(R15)
         MVI   8(R1),C' '
         LA    R1,9(R1)           INCREMENT OUTPUT STAK
         LA    R15,0(R8,R15)      INCREMENT NAMET POINTER
         BCT   R0,TABLSCAN
         EJECT
RESCAN   L     R0,LUVENTNO(,R10)  # CLASS TYPES IN EDT
         LA    R15,LUVENTRY(,R10)  1ST ENTRY
NAMESCAN DS    0H
         B     *+10
         CLC   1(0,R4),0(R15)
         EX    R5,*-6
         BE    HAVENAME
         LA    R15,0(R8,R15)      NEXT EDT ENTRY.
         BCT   R0,NAMESCAN
         B     NONAME             ESOTERIC NAME NOT FOUND.
***********************************************************************
* THE DEVICE NAME ENTERED IS VALID (IT WAS FOUND AGAIN IN
* THE EDT LOOKUP SECTION.  MOVE UNITNAME TO KEY 1 STORAGE
* AND INVOKE IEFAB4UV TO RETURN ASSOCIATED UCB ADDRESSES.
* R0, R1, R2, R8, R7, R14 CAN BE REUSED NOW.
***********************************************************************
HAVENAME DS    0H
         OI    INTFLAG,UNITOK     AT LEAST 1 VALID UNIT ENTERED.   @165
         SR    R1,R1              INITIALIZE IT
         L     R2,CVTPTR          GET CVT POINTER
         L     R2,0(R2)           OLD-NEW POINTER
         L     R2,4(R2)           OUT TCB POINTER
         ST    R6,TCBHOLD         HOLD ADDR OF TCB
         USING TCB,R6
         L     R9,SPNO236         SUBPOOL 236 (KEY 1)
         GETMAIN R,LV=LKEY1SP,SP=(R9)  FOR KEY SUBPOOL STORAGE
         ST    R1,KEY1ADDR        SAVE KEY1 GETMAIN'D AREA PTR
         LR    R7,R1              PTR TO KEY1 STORAGE
         USING KEY1SP,R7          ADDR TO DSECT
***********************************************************************
* SET UP PARAMETER LIST FOR KEY 1 STORAGE FOR IEFAB4UV
***********************************************************************
         LA    R1,UNITABLE
         ST    R1,UTBLPTR
         LA    R1,UFLAGS
         ST    R1,FLAGPTR
         LA    R1,ATTRAREA
         ST    R1,ATTRPTR
         MVC   UNAME(8),=CL8' '
         B     *+10
         MVC   UNAME(0),1(R4)
         EX    R5,*-6
         MVC   UNITNAM,UNAME
         MVC   UFLAGS,FLAGSET
         ST    R6,TCBHOLD
         USING TCB,R6
         ESTAE MYESTAE,CT,PARAM=(R13)
         LA    R1,UPLIST
         L     R15,DSAGB4UV
         BALR  R14,R15
         ST    R15,DSAGB4RC       SAVE RC
         ESTAE 0
*
         L     R15,DSAGB4RC       RESTORE RC
         LTR   R15,R15            HAVE UCBS BEEN RETURNED?
         BC    8,GOTUCBS          UCBS RETURNED GO LOOK AT THEN
         CH    R15,=H'4'          R15=4
         BC    8,NONAME           SAY CLASS NAME NOT FOUND
         CH    R15,=H'16'         R16=16
         BC    8,NOSTORGE         NO STORAGE AVAIL FOR UCB LIST
         BC    15,ABENDIT         ABEND ALL OTHER RETURN CODES
***********************************************************************
* UCBS RETURNED IN SUBPOOL 230 STORAGE. KEY 1 STORAGE CAN
* NOW BE FREED,  THEN PROCESS THE UCB LIST AND FREE
* SUBPOOL 230 STORAGE PRIOR TO EOJ.
***********************************************************************
GOTUCBS  MVC   UCBLIST,UCBPTR     SAVE PTR TO UCB LIST
         L     R9,SPNO236
         L     R1,KEY1ADDR
         FREEMAIN  R,LV=LKEY1SP,SP=(R9),A=(R1)
         L     R9,UCBLIST         PTR TO UCB LIST
*        CLI   0(R9),X'E6'        IS THIS THE UCB LIST?            $EDT
*        BC    7,NOTUCBLS         NOT UCB LIST SEND MSG            $EDT
         MVC   LISTLEN,0(R9)      HOLD LEN OF LIST
         DROP  R7                 FROM KEY1 PARM DSECT
         L     R7,4(R9)           NUMBER OF UCBS IN LIST
         LA    R9,8(R9)           ADDRESS OF 1ST UCB PTR
         L     R15,0(R9)          FIRST UCB PTR
         LA    R3,VTAB            TABLE OF VOLUME ENTRIES.
GETUCB   EQU   *
         USING UCBOB,R15
         TM    UCBDVCLS,UCB3DACC  DIRECT ACCESS?
         BZ    NOTDA              NO
         TM    UCBSTAT,UCBONLI    ONLINE?
         BZ    NOTONL
         CLI   UCBVOLI,X'00'      YES, VOLID KNOWN?
         BE    NOTDA              NO
         MVI   0(R3),6            6 CHAR VOLSER
         MVC   1(6,R3),UCBVOLI    SAVE VOLSER.
         LA    R3,7(0,R3)         NEXT SLOT IN VOLSER TABLE.
UPUCB    LA    R9,4(,R9)
         L     R15,0(R9)
         BCT   R7,GETUCB          NEXT UCB.
         MVI   0(R3),255          END OF VOLSER TABLE.
         B     EODAD              THAT'S ALL FOLKS.
NOTONL   DS    0H
         LA    R2,UCBNAME         PRINT UNIT=
         LM    R15,R1,=A(MESGRTN,0,28)
         BALR  R14,R15            OFFLINE UNIT SELECTED.
         B     UPUCB              TRY NEXT UNIT.
NOTDA    DS    0H
         LA    R2,UCBNAME         PRINT UNIT=
         LM    R15,R1,=A(MESGRTN,0,29)
         BALR  R14,R15            UNIT NOT DASD.
         B     UPUCB              TRY NEXT UNIT.
NONAME   DS    0H
         LR    R2,R4              LENGTH FIELD IN UTAB.            @165
         LA    R2,1(0,R2)         NAME THAT WASN'T FOUND           @165
         LM    R15,R1,=A(MESGRTN,0,30)
         BALR  R14,R15            ESOTERIC NOT FOUND.
         LA    R4,9(0,R4)         POINT TO NEXT UNITNAME.          @165
         CLI   0(R4),255          WAS THAT THE LASTONE?            @165
         BE    EODAD              YES, GET OUT.                    @165
         SR    R5,R5              CLEAR REGISTER.                  @165
         IC    R5,0(0,R4)         GET LENGTH OF UNITNAME.          @165
         B     RESCAN             OTHERWISE TRY NEXT UNITNAME.     @165
NOSTORGE DS    0H
         LM    R15,R1,=A(MESGRTN,0,31)
         BALR  R14,R15            NO STORAGE.
         B     EODAD              GIVE UP.                         @165
NOTUCBLS DS    0H
ABENDIT  DS    0H
         LM    R15,R1,=A(MESGRTN,0,32)
         BALR  R14,R15            LOGIC ERROR?????
EODAD    L     R6,TCBHOLD
         USING TCB,R6
         L     R0,LISTLEN         SUBPOOL 230 + LEN OF STORAGE
         L     R1,UCBLIST         PTR TO GETMAIN'D AREA
         LTR   R1,R1              IS A FREEMAIN REQUIRED
         BC    8,EODADB           NO BRANCH AROUND IT
         N     R0,=X'00FFFFFF'    FORCE SUBPOOL 0 *$
         SVC   10                 FREEMAIN OF SUBPOOL 230
EODADB   DS    0H
         TM    INTFLAG,UNITOK     ANY UNITS VALID?                 @165
         BO    *+8                YES.                             @165
         NI    FLAGS2,255-VOLUME  IF NOT, TURN VOLUME OFF          @165
         L     R2,=AL4(*+6)
         BSM   0,R2               GET BACK TO 24 BIT.
         $EPILOG ,
         LTORG ,
MYESTAE  DS    0H
         PUSH  USING
         USING MYESTAE,R15
         USING SDWA,R1
         CH    R0,=H'12'          Q/SDWA
         BE    MYESQUIT           N/GET OUT
         SP    MYESCTR,=P'1'      LOOP FAILSAFE
         BZ    MYESQUIT
         CLC   =X'0C2000',SDWACMPC  PRIV OP INTERRUPT?
         BNE   MYESA78            YES-SKIP IT
         L     R0,SDWANXT1        NEW INSTR AFTER PRIVOP
         B     MYESRTRY
MYESA78  DS    0H
         CLC   =X'B78000',SDWACMPC  GETMAIN/FREEMAIN FAIL
         BNE   MYESQUIT
         ICM   R0,15,SDWASRSV+15*4  GET ORIG R15
         N     R0,=X'FFFF00FF'    SET SUBPOOL TO ZERO
         STCM  R0,15,SDWASRSV+15*4  NEW R15
         L     R0,SDWANXT2
         SH    R0,=H'2'           BACKUP
MYESRTRY DS    0H
         SETRP RC=4,RETADDR=(R0),FRESDWA=YES,DUMP=NO,RETREGS=YES
         BR    R14
MYESQUIT DS    0H
         SETRP RC=0,FRESDWA=NO,DUMP=YES
         BR    R14
MYESCTR  DC    P'01024'           LOOP PROTECTION
         POP   USING
         LTORG
         DS    0F
SPNO236  DC    X'00000000'        SUBPOOL 236 *$ USE SP0
SPNO230  DC    X'00000000'        SUBPOOL 230 *$ USE SP0
FLAGSET  DC    XL2'1000'          BIT 3 SET FOR UCB SEARCH
         DC    H'0'
UNITNAM  DC    CL8' '
         DC    C' '
NAMETAB  DS    0F
         DC    200CL9' '
         DS    0F
TCBHOLD  DS    F
DSAGB4UV DS    F                  EP OF IEFGB4UV
DSAGB4RC DS    F                  RC "
KEY1ADDR DS    F
LISTLEN  DS    F
UCBLIST  DS    F                  PTR TO UCB LIST
         DS    0D
***********************************************************************
* DSECT FOR KEY1 SUBPOOL GETMAIN'D STORAGE
***********************************************************************
KEY1SP   DSECT                    GETMAIN DSECT FOR KEY1 SUBPOOL
UPLIST   DS    2F                 PARMLIST MAPPING
         ORG   UPLIST
UTBLPTR  DS    F                  ADDRESS OF 100-BYTE WORK AREA
FLAGPTR  DS    F                  ADDR OF DEVTYPE BYTE TO BE SEARCHED
         DS    0F
UNITABLE DS    CL20
         ORG   UNITABLE
UNAME    DS    8C
UCBPTR   DS    F
ATTRPTR  DS    F
         DS    F
*
UFLAGS   DS    XL2
ATTRAREA DS    CL10
LKEY1SP  EQU   *-KEY1SP
EDTLUVSP EQU   X'01C'                                              $EDT
LUVENTNO EQU   X'008'                                              $EDT
LUVENTLN EQU   X'020'                                              $EDT
LUVENTRY EQU   X'010'                                              $EDT
LUVUNAME EQU   X'000'
LUVVALUE EQU   X'008'
***********************************************************************
*        DSECTS
***********************************************************************
         PUSH  PRINT
         PRINT OFF                SAVE-A-TREE
         EJECT
         IEFJESCT  ,
         EJECT
         IKJTCB  ,
         EJECT
         IHASDWA ,
         POP   PRINT
./       ADD   NAME=VMFBASE
         TITLE 'VOLUME MASTER FILE BASE VOLUME RECORD LAYOUT.'
***********************************************************************
**********     VOLUME MASTER FILE BASE VOLUME RECORD      *************
***********************************************************************
VMFBASE  DSECT                     BASE RECORD LAYOUT
BASRVSCR DC    C'2'                SERVICE/SCRATCH INDICATOR
*        EQU   C'1'               IN SERVICE/NON SCRATCH
*        EQU   C'2'               IN SERVICE/SCRATCH
*        EQU   C'3'               OUT SERVICE/NON SCRATCH
*        EQU   C'4'               OUT SERVICE/SCRATCH
*        EQU   C'0'               SKIPPED SEGMENT RECORD
BATYPE   DC    C'B'                RECORD TYPE
BAVSN    DC    CL6' '              VOLUME SERIAL NUMBER
BAVOLSQ  DC    PL2'0'              VOLUME SEQUENCE NUMBER
BAVOLCT  DC    PL2'0'              VOLUME COUNT
BAFILECT DC    H'0'                FILE COUNT
BAVSN1   DC    CL6' '              MULTI-VOL 1ST VOLUME SERIAL NUMBER
BAVSNL   DC    F'0'                MULTI-VOL LAST CHAIN PTR
BAMDS1   DC    F'0'                MULTI-DATASET 1ST CHAIN PTR
BAMDSL   DC    F'0'                MULTI-DATASET LAST CHAIN PTR
BAUNIQUE DC    F'0'                SORT UNIQUE CODE
BADEN    DC    C' '                DENSITY
BATRTCH  DC    C' '                TRACK RECORDING TECHNIQUE
BALABEL  DC    C' '                LABEL TYPE
         DC    C' '                AVAILABLE
BAACTIVE DC    H'0'                ACTIVE FILE COUNT
BACDSSEQ DC    H'0'                CONTROLLING DATA SET SEQUENCE NUMBER
BALOC    DC    CL2' '              VOLUME LOCATION
BACCSS   DC    CL4' '              BOX OR CABINET/SLOT
BAVMOVED DC    PL3'0'              DATE VOLUME MOVED
BAVKEEPD DC    PL3'0'              HIGHEST KEEP DATE ON VOLUME
BAVEXPDT DC    PL3'0'              HIGHEST IBM EXPIRATION DATE ON
*                                  VOLUME
BAVSCRDT DC    PL3'0'              DATE VOLUME SCRATCHED
BATLENTH DC    H'0'                TAPE LENGTH         (USER UPDATED)
BAPURCH  DC    PL3'0'              PURCHASE DATE       (USER UPDATED)
BAMANU   DC    CL2' '              MANUFACTURER CODE   (USER UPDATED)
BATAPTYP DC    CL2' '              TAPE TYPE           (USER UPDATED)
BALOST   DC    C' '                LOST CODE           (USER UPDATED)
BADAMAGE DC    C' '                DAMAGED CODE        (USER UPDATED)
BADEST   DC    C' '                DESTROYED CODE      (USER UPDATED)
BADESTDT DC    PL3'0'              DESTROYED DATE      (USER UPDATED)
         DC    CL15' '             AVAILABLE
BACLUSES DC    H'0'                USES SINCE CLEANED
BACEUSES DC    H'0'                USES SINCE CERTIFIED
BATOUSES DC    H'0'                USES SINCE PURCHASED
BACURERG DC    H'0'                CURRENT ERASE GAPS ON VOLUME
BAMAXERG DC    H'0'                MAX ERASE GAPS ON VOLUME
BATMPRER DC    H'0'                CURRENT TEMPORARY READ ERRORS
BACLNCT  DC    H'0'                CLEANINGS SINCE CERTIFIED
BACERTCT DC    H'0'                TOTAL CERTIFICATIONS
BACLNDT  DC    PL3'0'              CLEAN DATE
BACERTDT DC    PL3'0'              CERTIFICATION DATE
BAMVLCNT DC    H'0'                COUNT OF VOLUMES IN BAMVLTAB
BAMVLTAB DS    0CL40               5 OCCURRANCES OF MULTI-VOL INFO
BAMVLVSN DC    CL6' '              VOLUME SERIAL NUMBER
BAMVLSEQ DC    H'0'                VOLUME SEQUENCE NUMBER
         DC    CL32' '             REMAINDER OF BAMVLTAB
BAFILESQ DC    H'0'                FILE SEQUENCE NUMBER
BAABEND  DC    C' '                ABEND STATUS
BACDSFLG DC    C' '                CONTROLLING DATA SET FLAG
BADSN    DC    CL44' '             DATA SET NAME
BACTIME  DC    PL4'0'              CREATE TIME
BACDATE  DC    PL3'0'              CREATE DATE
BACDRIVE DC    CL3' '              CREATE DRIVE
BACJOB   DC    CL8' '              CREATE JOBNAME
BACSTEP  DC    CL8' '              CREATE STEPNAME
BAIDATE  DC    PL3'0'              INPUT DATE
BAIDRIVE DC    CL3' '              INPUT DRIVE
BAIJOB   DC    CL8' '              INPUT JOBNAME
BABLKCT  DC    F'0'                BLOCK COUNT
BALRECL  DC    H'0'                LOGICAL RECORD LENGTH
BABLKSI  DC    H'0'                BLOCK SIZE
BARECFM  DC    CL3' '              RECORD FORMAT
BAJOBACC DC    CL15' '             JOB ACCOUNTING INFO (FROM IEFUJI)
BADKEEPD DC    PL3'0'              TLMS II KEEP DATE FOR DATA SET
BADEXPDT DC    PL3'0'              IBM EXPIRATION DATE FOR DATA SET
BACPUID  DC    C' '                CREATION CPU ID
BASPAN   DC    C' '                SPANNED DATA SET FLAG (1 = SPANNED)
BADRSRV1 DC    CL2' '              RESERVED FOR FUTURE ENHANCEMENTS
BADRSRV2 DC    PL3'0'              RESERVED FOR FUTURE ENHANCEMENTS
BADXPIRD DC    PL3'0'              DATE DATA SET EXPIRED
         DC    CL2' '              AVAILABLE
BALENGTH EQU   *-VMFBASE           LENGTH OF RECORD
./       ADD   NAME=VMFMDS
         TITLE 'VOLUME MASTER FILE MULTI-DATASET CHAIN RECORD LAYOUT.'
***********************************************************************
**********     VOLUME MASTER FILE MULTI-DATASET CHAIN RECORD   ********
***********************************************************************
VMFMDS   DSECT                     MULTI-DATA SET RECORD
         DC    X'00'               AVAILABLE
MDTYPE   DC    C'D'                RECORD TYPE
MDVSN    DC    CL6' '              BASE VOLUME SERIAL NUMBER
MDRELREC DC    F'0'                RELATIVE RECORD NUMBER OF THIS
*                                  RECORD
MDRELPRE DC    F'0'                RELATIVE RECORD OF PREVIOUS IN CHAIN
MDRELNXT DC    F'0'                RELATIVE RECORD OF NEXT IN CHAIN
MDUNIQUE DC    F'0'                SORT UNIQUE CODE
MDDSNTAB DS    0CL264              2 OCCURRANCES OF DATA SET INFO
MDDSNENT DS    0CL132              1 ENTRY IN MDDSNTAB
MDFILESQ DC    H'0'                FILE SEQUENCE NUMBER
MDDSNABN DC    C' '                ABEND FLAG
MDDSNCTL DC    C' '                CONTROLLING DATA SET FLAG
MDDSN    DC    CL44' '             DATA SET NAME
MDCTIME  DC    PL4'0'              CREATE TIME
MDCDATE  DC    PL3'0'              CREATE DATE
MDCDRIVE DC    CL3' '              CREATE DRIVE
MDCJOB   DC    CL8' '              CREATE JOB NAME
MDCSTEP  DC    CL8' '              CREATE STEP NAME
MDIDATE  DC    PL3'0'              INPUT DATE
MDIDRIVE DC    CL3' '              INPUT DRIVE
MDIJOB   DC    CL8' '              INPUT JOB NAME
MDBLKCT  DC    F'0'                BLOCK COUNT
MDLRECL  DC    H'0'                LOGICAL RECORD LENGTH
MDBLKSI  DC    H'0'                BLOCK SIZE
MDRECFM  DC    CL3' '              RECORD FORMAT
MDJACCT  DC    CL15' '             JOB ACCOUNTING INFO
MDKEEPDT DC    PL3'0'              TLMS II DATASET KEEP DATE
MDEXPDT  DC    PL3'0'              IBM EXPIRATION DATE
MDCPU    DC    C' '                CREATION CPU ID
MDSPAN   DC    C' '                SPANNED DATA SET FLAG
MDRSRVD1 DC    CL2' '              RESERVED FOR FUTURE ENHANCEMENTS
MDRSRVD2 DC    PL3'0'              RESERVED FOR FUTURE ENHANCEMENTS
MDEXPIRE DC    PL3'0'              LOGICAL DATA SET EXPIRED DATE
         DC    CL2' '              AVAILABLE
         DC    CL132' '            2ND OCCURRANCE OF MDDSNTAB
MDLENGTH EQU   *-VMFMDS            RECORD LENGTH
./       ADD   NAME=VTOC
         PRINT NOGEN
         TITLE 'XVTCREAD - VTOC READING SUBROUTINE'
         SPACE
* FUNCTION: THIS SUBROUTINE READS THE VOLUME TABLE OF CONTENTS (VTOC)
*        FROM A DIRECT-ACCESS DEVICE AND PRESENTS IT TO THE CALLER
*        ONE RECORD (DSCB) AT A TIME.
         SPACE
* OPERATION: THIS ROUTINE IS A SPECIALIZED SEQUENTIAL ACCESS METHOD
*        FOR VTOC'S.  ITS ADVANTAGE OVER ORDINARY BSAM IS THAT IT READS
*        AN ENTIRE TRACK IN ONE REVOLUTION, THUS SAVING CONSIDERABLE
*        TIME.  THE ROUTINE HAS THREE CALL MODES:
*
*        0 - READ.  RETURNS WITH THE CORE ADDRESS OF A DSCB IN REGISTER
*              1.  THE CORE CONSISTS OF 148 CONSECUTIVE BYTES, CONTAIN-
*              ING THE COUNT (8 BYTES), KEY (44 BYTES), AND DATA (96
*              BYTES) FOR ONE DSCB.  RETURN CODES ARE:
*                      0 - NORMAL;
*                      4 - END OF FILE, NO DATA PRESENTED;
*                      8 - PERMANENT I/O ERROR.  THE KEY AND DATA AREAS
*                          WILL BE SET TO ZEROS; THE COUNT AREA WILL
*                          CONTAIN THE CORRECT CCHHR.  SINCE READING
*                          IS DONE A TRACK AT A TIME, ALL THE DSCB'S
*                          FOR THAT TRACK WILL BE MARKED IN ERROR.
*                          READING MAY CONTINUE ON TO THE NEXT TRACK.
*
*        1 - OPEN.  REGISTER 1 SHOULD POINT TO AN 8-BYTE FIELD
*              CONTAINING THE DDNAME TO BE USED IN THE DCB.  THE
*              CORRESPONDING DD CARD SHOULD SPECIFY A DISPOSITION OF
*              (OLD,KEEP).  RETURN CODES:
*                      0 - NORMAL;
*                      4 - UNABLE TO OPEN (PROBABLY MISSING DD CARD);
*                      8 - DD CARD DID NOT REFER TO A DIRECT-ACCESS
*                          DEVICE, OR DEVICE TYPE UNKNOWN.
*
*        2 - CLOSE.  NO ARGUMENTS ARE REQUIRED OR RETURNED.  RETURN
*              CODE IS 0.
         SPACE
* ENTRY POINTS:  ENTRY IS ALWAYS TO 'XVTCREAD' VIA A BALR 14,15 WITH
*        REGISTER 13 SET TO A SAVE AREA.  REGISTER 0 CONTAINS A
*        BINARY INTEGER TO INDICATE THE CALL MODE AND REGISTER 1
*        POINTS TO PARAMETERS AS REQUIRED FOR EACH MODE.
         SPACE
* DATA SETS:  READS VOLUME TABLE OF CONTENTS FROM ANY DIRECT-ACCESS
*        DEVICE.  USES EXCP TO EXECUTE A CHAINED CHANNEL PROGRAM TO
*        READ AN ENTIRE TRACK AT A TIME.
         SPACE
* EXTERNAL ROUTINES:  USES SUPERVISOR ROUTINE 'IECPCNVT' TO CONVERT
*        A RELATIVE TRACK NUMBER TO AN ABSOLUTE ADDRESS.
         SPACE
* EXITS - NORMAL:  RETURNS VIA REGISTER 14 WITH RETURN CODE IN REGISTER
*        15.  (SEE ABOVE FOR RETURN CODE VALUES.)
         SPACE
* EXITS - ERROR:  NONE.
         SPACE
* TABLES AND WORK AREAS:  DOES A GETMAIN TO OBTAIN A BUFFER LARGE
*        ENOUGH TO HOLD AN ENTIRE TRACK FROM THE DEVICE BEING READ.
*        THIS AREA MAY BE AS LARGE AS 9900 BYTES (FOR 2301 DRUM).
         SPACE
* ATTRIBUTES:  SERIALLY REUSABLE.
         EJECT
* SECTION DEFINITION AND REGISTER ASSIGNMENTS:
         SPACE
XVTCREAD CSECT
         SPACE 2
RWA      EQU   2
RWB      EQU   3
RWC      EQU   4
RRET     EQU   8                   LOCAL SUBROUTINE EXIT REGISTER
RPARM    EQU   9
RRCODE   EQU   10                  RETURN CODE REGISTER
RBASE    EQU   12                  LOCAL BASE REGISTER
         SPACE 3
* TAGS FOR CHANNEL COMMANDS AND FLAG BITS:
         SPACE
READR0   EQU   X'16'               READ RECORD 0
READCKD  EQU   X'1E'               READ COUNT, KEY, AND DATA
         SPACE
CC       EQU   X'40'               COMMAND CHAIN FLAG
SLI      EQU   X'20'               SUPPRESS LENGTH INDICATION FLAG
SKIP     EQU   X'10'               SKIP DATA TRANSFER FLAG
         EJECT
************
* PROLOGUE *
************
         SPACE
* ENTER HERE AND PERFORM STANDARD REGISTER SAVE AREA HOUSEKEEPING.
         SPACE
         SAVE  (14,12),,XVTCREAD-01008218
         SPACE
         LR    RBASE,R15           SET LOCAL BASE REGISTER
         USING XVTCREAD,RBASE
         LR    R14,R13             SAVE CALLER'S R13
         LA    R13,VTOCSAVE        ADDRESS LOCAL SAVE AREA
         ST    R13,8(R14)          CHAIN FORWARD
         ST    R14,VTOCSAVE+4      CHAIN BACKWARD
         SR    RRCODE,RRCODE       ZERO RETURN CODE REGISTER
         SPACE
* SELECT MODE FROM CONTENTS OF REGISTER 0.
         SPACE
         LA    RWA,3               MASK FOR LOW 2 BITS
         NR    RWA,R0              GET CALL MODE
         SLL   RWA,2               MODE TIMES 4
         B     *+4(RWA)            BRANCH ON MODE
         SPACE
         B     GET                 MODE 0, GET A DSCB
         B     OPEN                MODE 1, OPEN A NEW VTOC
         B     CLOSE               MODE 2, CLOSE
         B     RETURN0             MODE 3 NOT DEFINED, NO OP
         SPACE 3
***********
* RETURNS *
***********
         SPACE
RETURN8  LA    RRCODE,4(RRCODE)    ENTRY FOR RETURN CODE 8
RETURN4  LA    RRCODE,4(RRCODE)    ENTRY FOR RETURN CODE 4
RETURN0  LR    R15,RRCODE          ENTRY FOR RETURN CODE 0
         SPACE
         L     R13,VTOCSAVE+4      RECOVER CALLER'S SAVE AREA
         L     R14,12(R13)         LOAD RETURN ADDRESS
         LM    R2,R12,28(R13)      RESTORE OTHER GENERAL REGISTERS
         MVI   12(R13),X'FF'       SET RETURN FLAG ON
         BR    R14                 RETURN TO CALLER
         EJECT
*********************
* MODE 0 - GET DSCB *
*********************
         SPACE
* IF END-OF-FILE WAS REACHED, RETURN AT ONCE.
         SPACE
GET      DS    0H
         TM    MODESW,EOFSW        TEST END-OF-FILE BIT
         BO    RETURN4             RETURN CODE 4 IF ON
         SPACE
* IF CHANNEL PROGRAM HAS BEEN STARTED, GO TO CHECK IT.  OTHERWISE,
* ASSUME THERE IS AT LEAST ONE FULL BUFFER.
         SPACE
         TM    MODESW,XCPRUN       TEST IF EXCP ISSUED
         BO    XCPTEST             BRANCH IF SO
         SPACE
* SET BUFFER ADDRESS TO NEXT DSCB AND TEST IF LAST ON TRACK.  IF NOT,
* EXIT WITH ITS ADDRESS IN R1.
         SPACE
         MVC   0(5,R1),IOBSEEK+3   SAVE CCHHR OF CURRENT RECORD.   @160
         L     RWA,DSCBADR         LOAD BUFFER POINTER
         LA    RWA,148(RWA)        ADVANCE TO NEXT DSCB
NDXSTORE ST    RWA,DSCBADR         STORE UPDATED POINTER
         C     RWA,DSCBLIM         TEST IF LAST DSCB IN BUFFER
         BNL   LASTDSCB            BRANCH IF SO
         LR    R1,RWA              PASS ADDRESS TO USER
GETOUT   TM    MODESW,RDERR        TEST IF ERROR ON THIS TRACK
         BZ    RETURN0             RETURN CODE 0 IF NOT
         B     RETURN8             RETURN CODE 8 IF ERROR
         SPACE
* IF THIS IS THE LAST DSCB, MOVE IT TO THE JFCB BUFFER AND START
* READING THE NEXT TRACK.
         SPACE
LASTDSCB MVC   JFCB(148),0(RWA)    MOVE LAST DSCB
         L     RWB,TTRN            LOAD RELATIVE TRACK NUMBER
         AL    RWB,=X'00010000'    INCREMENT TO NEXT TRACK
         ST    RWB,TTRN
         BAL   RRET,EXCP           START CHANNEL PROGRAM
         LA    R1,JFCB             LOAD DSCB ADDRESS FOR CALLER
         B     GETOUT              TO RETURN
         EJECT
* WAIT FOR CHANNEL PROGRAM COMPLETION AND TEST THE OUTCOME.
         SPACE
XCPTEST  WAIT  ECB=VTOCECB
         SPACE
         NI    MODESW,X'FF'-XCPRUN  TURN EXCP STARTED BIT OFF
         CLI   VTOCECB,X'7F'       TEST COMPLETION CODE
         BNE   PERMERR             BRANCH IF ERROR
SETDSCBA L     RWA,DSCBSTRT        SET BUFFER POINTER TO 1ST DSCB
         B     NDXSTORE
         SPACE
* PERMANENT ERROR FOR THIS TRACK.  ZERO THE DSCB'S AND FILL IN THE
* CCHHR PORTIONS OF THE COUNT AREAS.
         SPACE
PERMERR  OI    MODESW,RDERR        SIGNAL READ ERROR
         NI    IOBFLAG1,X'FB'      TURN OFF BIT 5 OF IOB FLAG
         NI    VCBIFLGS,X'3F'      TURN OFF BITS 0 AND 1
         L     RWA,DSCBSTRT        LOAD ADDRESS OF FIRST DSCB
         LA    RWB,1               LOAD RECORD NUMBER
         SPACE
DSCBELUP XC    0(148,RWA),0(RWA)   ZERO DSCB BUFFER
         MVC   0(4,RWA),IOBSEEK+3  INSERT CCHH IN COUNT FIELD
         STC   RWB,4(RWA)          INSERT R IN COUNT FIELD
         LA    RWA,148(RWA)        POINT TO NEXT BUFFER
         LA    RWB,1(RWB)          INCREMENT RECORD NUMBER
         C     RWA,DSCBLIM         TEST FOR LAST BUFFER
         BNH   DSCBELUP
         B     SETDSCBA            BRANCH TO RESET BUFFER POINTER
         EJECT
*****************
* MODE 1 - OPEN *
*****************
         SPACE
* ENTER WITH A DDNAME ADDRESSED BY REGISTER 1.  PERFORM CLOSE
* SUBROUTINE FIRST TO BE SURE EVERYTHING IS INITIALIZED.
         SPACE
OPEN     DS    0H
         LR    RPARM,R1            SAVE REGISTER 1
         BAL   RRET,CLOSESUB       CALL CLOSE SUBROUTINE
         SPACE
         MVC   VCBDDNAM(8),0(RPARM)  INSERT DDNAME IN DCB
         MVC   VTCDDN(8),0(RPARM)
         MVC   VTCVOL(6),8(RPARM)  SET VOLSER
         ALLOC VOL=LOCVOL,DDN=LOCDDN,DISP=OLD,UNIT=LOCUNIT,            X
               ERROR=NOALLOCV
         SPACE
* CHECK DEVICE TYPE TO BE SURE IT IS DIRECT ACCESS.
         SPACE
         DEVTYPE VCBDDNAM,VWORK    GET DEVICE TYPE
         LTR   R15,R15             TEST IF IT FOUND DD CARD
         BNZ   RETURN4             ERROR, DD CARD MISSING
         CLI   VWORK+2,X'20'       TEST DEVICE TYPE
         BNE   RETURN8             ERROR IF NOT DIRECT ACCESS
         SPACE
         CLI   VWORK+3,MAXDVT      TEST FOR MAX DEVICE TYPE
         BNL   RETURN8             ERROR IF TOO LARGE
         SR    R1,R1
         IC    R1,VWORK+3          LOAD DEVICE NUMBER
         IC    R1,DVICETAB(R1)     LOAD DSCB'S/TRK FROM TABLE
         LTR   R1,R1               ZERO IS UNDEFINED DEVICE
         BZ    RETURN8             EXIT IF UNKNOWN
         ST    R1,NDSCBS           STORE NUMBER OF DSCB'S/TRACK
         SPACE
* READ JFCB AND INSERT DATA SET NAME OF 44 X'04'.
         SPACE
         RDJFCB  VTOCDCB
         MVI   JFCBDSNM,X'04'      GENERATE DATA SET NAME
         MVC   JFCBDSNM+1(43),JFCBDSNM
         SPACE
* OPEN THE DSCB.
         SPACE
         OPEN  (VTOCDCB,(INPUT)),TYPE=J
         TM    VCBOFLGS,OPENBIT    TEST IF OPEN WORKED
         BZ    RETURN4             ERROR IF OPEN FAILED
         SPACE
* OBTAIN CORE FOR CHANNEL PROGRAM AND DSCB BUFFERS.
         SPACE
         LA    R0,156              CORE FOR ONE DSCB AND ITS CCW
         MH    R0,NDSCBS+2         TIMES NUMBER PER TRACK
         AH    R0,=H'15'           PLUS 1 CCW AND ROUNDING
         N     R0,=X'FFFFFFF8'     ROUND TO DOUBLE-WORD MULTIPLE
         ST    R0,CBSIZE           SAVE SIZE OF GOTTEN CORE
         GETMAIN R,LV=(0)          GET TRACK BUFFERS
         ST    R1,CBADDR           SAVE ADDRESS OF GOTTEN CORE
         OI    MODESW,CBGOT        INDICATE CORE GOTTEN
         SPACE
* GENERATE CHANNEL PROGRAM.  IT CONSISTS OF A 'READ R0' ORDER WITH
* THE SKIP FLAG ON, FOLLOWED BY A 'READ COUNT-KEY-AND-DATA' ORDER FOR
* EACH DSCB.
         SPACE
         L     RWA,NDSCBS          NUMBER OF DSCB'S
         SLL   RWA,3               TIMES   8
         LA    RWA,8(RWA,R1)       PLUS 8 AND BASE = 1ST BUFFER ADD
         ST    RWA,DSCBSTRT        SAVE ADDRESS OF FIRST BUFFER
         SPACE
         ST    R1,IOBSTART         ADDRESS OF CHANNEL PROGRAM
         MVC   0(8,R1),INITCCW     INSERT FIRST CCW
         LA    RWB,8(R1)           PLACE FOR NEXT CCW
         LA    RWC,1               BUFFER COUNTER
         SPACE
CCWLOOP  MVC   0(8,RWB),READCCW    INSERT READ CCW FOR ONE DSCB
         ST    RWA,0(RWB)          SET ITS BUFFER ADDRESS
         MVI   0(RWB),READCKD      RESTORE COMMAND CODE
         C     RWC,NDSCBS          TEST BUFFER COUNTER
         BNL   LASTCCW             BRANCH IF LAST BUFFER
         LA    RWB,8(RWB)          INCREMENT CCW ADDRESS
         LA    RWA,148(RWA)        INCREMENT BUFFER ADDRESS
         LA    RWC,1(RWC)          INCREMENT BUFFER COUNTER
         B     CCWLOOP             DO NEXT BUFFER
         SPACE
LASTCCW  NI    4(RWB),X'FF'-CC     TURN OFF COMMAND CHAIN BIT
         ST    RWA,DSCBLIM         SAVE ADDRESS OF LAST DSCB BUFFER
         SPACE
* SET OTHER THINGS AND START PROGRAM TO FILL BUFFER.
         SPACE
         SR    R0,R0
         ST    R0,TTRN             SET RELATIVE TRACK NUMBER TO 0
         NI    MODESW,X'FF'-XCPRUN-RDERR-EOFSW  SET FLAGS OFF
         BAL   RRET,EXCP           START CHANNEL PROGRAM
         B     RETURN0             INDICATE SUCCESSFUL OPEN
NOALLOCV ABEND 98,DUMP
         EJECT
******************
* MODE 2 - CLOSE *
******************
         SPACE
CLOSE    BAL   RRET,CLOSESUB       CALL CLOSED CLOSE SUBROUTINE
         B     RETURN0
         SPACE 2
* IF THE CHANNEL PROGRAM IS RUNNING, WAIT FOR IT BEFORE TAKING FURTHER
* ACTION.
         SPACE
CLOSESUB DS    0H
         TM    MODESW,XCPRUN       TEST IF CHANNEL PROGRAM RUNNING
         BZ    NOEXCP              BRANCH IF NOT
         WAIT  ECB=VTOCECB         WAIT UNTIL COMPLETE
         NI    MODESW,X'FF'-XCPRUN  TURN RUNNING SWITCH OFF
NOEXCP   DS    0H
         SPACE
* CLOSE THE DCB.
         SPACE
         TM    VCBOFLGS,OPENBIT    TEST IF DCB OPEN
         BZ    NOCLOSE             BRANCH IF NOT
         CLOSE VTOCDCB
         FREE  UNALC,DDN=LOCDDN
NOCLOSE  DS    0H
         SPACE
* RELEASE CORE OBTAINED FOR DSCB BUFFERS.
         SPACE
         TM    MODESW,CBGOT        TEST IF CORE GOTTEN
         BZ    NOFREE              BRANCH IF NOT
         LM    R0,R1,CBSIZE        LOAD SIZE AND LOCATION
         FREEMAIN  R,LV=(0),A=(1)  FREE CORE
         NI    MODESW,X'FF'-CBGOT  SET CORE GOTTEN BIT OFF
NOFREE   DS    0H
         SPACE
         NI    MODESW,X'FF'-RDERR  CLEAR ERROR SWITCH
         BR    RRET
         EJECT
****************
* EXCP ROUTINE *
****************
         SPACE
* CONVERT RELATIVE TRACK ADDRESS IN 'TTRN' TO ABSOLUTE SEEK ADDRESS IN
* 'IOBSEEK', USING SUPERVISOR CONVERSION ROUTINE.
         SPACE
EXCP     DS    0H
         STM   R2,R13,EXCPSAVE     SAVE IMPORTANT REGISTERS
         L     R0,TTRN             LOAD RELATIVE TRACK NUMBER
         L     R1,VCBDEBAD         LOAD DEB ADDRESS
         LA    R2,IOBSEEK          LOAD ADDR TO RECEIVE MBBCCHHR
         L     R15,CVTPTR          LOAD CVT ADDRESS
         USING CVT,R15
         L     R15,CVTPCNVT        LOAD ADDR OF CONVERT ROUTINE
         DROP  R15
         BALR  R14,R15             CONVERT TTRN TO MBBCCHHR
         DROP  RBASE               THAT CLOBBERED BASE REG
         USING *,R14               R14 SET BY BALR ABOVE
         LM    R2,R13,EXCPSAVE     RESTORE REGISTERS
         DROP  R14
         USING XVTCREAD,RBASE      BASE REGISTER RECOVERED
         LTR   R15,R15             TEST IF EXTENT VIOLATED (RC=4)
         BNZ   SETEOF              IF SO, MEANS END-OF-FILE
         SPACE
* ZERO ECB AND START CHANNEL PROGRAM.
         SPACE
         SR    R0,R0
         ST    R0,VTOCECB          CLEAR ECB
         NI    MODESW,X'FF'-RDERR  RESET ERROR SWITCH
         EXCP  VTOCIOB             START CHANNEL PROGRAM
         OI    MODESW,XCPRUN       SET 'RUNNING' FLAG
         BR    RRET
         SPACE
* WHEN EXTENT IS VIOLATED, SET END-FILE AND EXIT VIA CLOSE ROUTINE.
         SPACE
SETEOF   OI    MODESW,EOFSW        SET END-OF-FILE BIT
         B     CLOSESUB            EXIT VIA CLOSE SUBROUTINE
         EJECT
********************************
* CONSTANTS, VARIABLES, ETC... *
********************************
         SPACE
         LTORG ,
         DS    0F
LOCVOL   DC    AL4(VTCVOL),Y(L'VTCVOL)
         DS    0F
LOCUNIT  DC    AL4(VTCUNIT),Y(L'VTCUNIT)
         DS    0F
LOCDDN   DC    AL4(VTCDDN),Y(L'VTCDDN)
VTCVOL   DC    CL6' '
VTCDDN   DC    CL8'VOLUME'
VTCUNIT  DC    CL8'SYSALLDA'                                       @161
         DS    0D
INITCCW  CCW   READR0,0,CC+SLI+SKIP,8
READCCW  CCW   READCKD,0,CC,148
         SPACE
DVICETAB DC    X'00'               TABLE OF NUMBER OF DSCB'S/TRACK
         DC    AL1(16)             2311                     01
         DC    AL1(63)             2301                     02
         DC    AL1(17)             2303                     03
         DC    AL1(22)             2302                     04
         DC    AL1(8)              2321                     05
         DC    AL1(34)             2305-1                   06
         DC    AL1(34)             2305-2                   07
         DC    AL1(25)             2314                     08
         DC    AL1(39)             3330                     09
         DC    AL1(00)             UNKNOWN AS OF 4/75       0A
         DC    AL1(47)             3350                     0B
         DC    AL1(51)             3375                     0C   JCL
*        DC    AL1(53)            3375  (UNKN ATTRS 5/81)  0C   CST
         DC    AL1(39)             3330-1                   0D
         DC    AL1(53)             3380                     0E   CST
         DC    AL1(50)             3390                     0F     @339
MAXDVT   EQU   *-DVICETAB
         SPACE
VWORK    DS    D                   WORK CELL
VTOCSAVE DS    19F                 SAVE AREA
EXCPSAVE EQU   VTOCSAVE+12
CBSIZE   DS    2F                  SIZE AND LOCATION OF GOTTEN CORE
CBADDR   EQU   CBSIZE+4
NDSCBS   DS    F                   NUMBER OF DSCB'S PER TRACK
DSCBSTRT DS    F                   ADDRESS OF 1ST DSCB BUFFER
DSCBLIM  DS    F                   ADDRESS OF LAST DSCB BUFFER
DSCBADR  DS    F                   ADDRESS OF CURRENT DSCB
TTRN     DS    F                   RELATIVE TRACK NUMBER
         SPACE
* MODE SWITCH AND BIT DEFINITIONS:
         SPACE
MODESW   DC    X'00'
CBGOT    EQU   X'80'               CORE GOTTEN FOR BUFFER
XCPRUN   EQU   X'40'               CHANNEL PROGRAM STARTED BUT NOT
*                                  CHECKED
RDERR    EQU   X'20'               PERMANENT I/O ERROR
EOFSW    EQU   X'10'               END-OF-FILE SENSED
         SPACE
         LTORG
         EJECT
* DATA CONTROL BLOCK
         SPACE
*********************************************************************
         ENTRY VTOCDCB             (USED BY IX VTOC ROUTINE )
*                                  CST
*********************************************************************
VTOCDCB  DCB   DDNAME=VOLUME01,MACRF=(E),EXLST=JFCBADDR
         SPACE
VCBDDNAM EQU   VTOCDCB+40
VCBIFLGS EQU   VTOCDCB+44
VCBDEBAD EQU   VTOCDCB+44
VCBOFLGS EQU   VTOCDCB+48
OPENBIT  EQU   X'10'
         EJECT
* IOB FOR CHANNEL PROGRAM:
         SPACE
VTOCIOB  DS    0D
IOBFLAG1 DC    X'42000000'         COMMAND CHAIN, NOT RELATED
         DC    A(VTOCECB)
         DC    2F'0'
IOBSTART DC    A(0)                CHANNEL PROGRAM BEGINNING
         DC    A(VTOCDCB)
         DC    X'03000000'
         DC    F'0'
IOBSEEK  DC    D'0'                INITIAL SEEK ADDRESS
         SPACE
* EVENT CONTROL BLOCK FOR CHANNEL PROGRAM:
         SPACE
VTOCECB  DC    F'0'                EVENT CONTROL BLOCK
         SPACE 3
* BUFFER FOR JFCB AND DCB EXIT LIST:
         SPACE
JFCBADDR DS    0F
         DC    X'87'
         DC    AL3(JFCB)
         SPACE
JFCB     DS    0D
         DS    CL176
         SPACE
JFCBDSNM EQU   JFCB                DATA SET NAME
JFCBTSDM EQU   JFCB+52
JFCNDSCB EQU   X'04'
JFCBVOLS EQU   JFCB+118            VOLUME SERIAL NUMBER
./       ADD   NAME=XPRCLOSE
         MACRO
&SYMBOL  XPRCLOSE  &WA
.* MACRO TO EXECUTE A CLOSE CALL TO 'XPRNTSUB'.
         CNOP  0,4
&SYMBOL  B     *+8                     BRANCH AROUND ADDRESS
         XPRINNRA  &WA,68
         MEND
./       ADD   NAME=XPRDCB
         MACRO
&SYMBOL  XPRDCB  &DDNAME=SYSPRINT,&BLKSIZE=23427
         LCLC  &TAG
.* MACRO TO GENERATE A WORK AREA FOR A PRINTER DATA SET, CONTAINING
.* A SAVE AREA, THE DCB, OPEN AND CLOSE PARAMETER LISTS, AND VARIOUS
.* CELLS AND SWITCHES.
&TAG     SETC  '&SYMBOL'
         AIF   (T'&SYMBOL NE 'O').TOK  TEST IF NAME SUPPLIED
&TAG     SETC  'XPRDCB01'              SUPPLY STANDARD PRDCB NAME
.TOK     ANOP
&TAG     DS    0D                      ALIGN ON DOUBLE-WORD BOUNDARY
         DS    18F                     STANDARD SAVE AREA
         SPACE
*        DCB   DDNAME=&DDNAME,DSORG=PS,RECFM=VBM,LRECL=137,           X
*              BLKSIZE=&BLKSIZE,MACRF=PL,BFTEK=S,BUFNO=2
IHB&SYSNDX DCB DDNAME=&DDNAME,DSORG=PS,RECFM=VBM,LRECL=137,            X
               BLKSIZE=&BLKSIZE,MACRF=PL,BFTEK=S,BUFNO=2
         SPACE
* PARAMETER LISTS FOR OPEN AND CLOSE:
         SPACE
         OPEN  (IHB&SYSNDX,(OUTPUT,LEAVE)),MF=L  PARM LIST FOR OPEN
         SPACE
         CLOSE (IHB&SYSNDX,LEAVE),MF=L PARM LIST FOR CLOSE
         SPACE
* WORK CELLS AND VARIABLE STORAGE:
         SPACE
         DS    1D                      WORK CELL
         DS    3F                      WORK CELLS
         DS    1A                      PAGE HEADING PARM LIST ADDRESS
         DS    1F                      MAXIMUM NUMBER OF OUTPUT LINES
         DS    1H                      PAGE WIDTH
         DS    1H                      PAGE LENGTH
         DS    1H                      PAGE NUMBER
         DS    1H                      PAGE BALANCE
         DS    1X                      SWITCHES
         DS    CL24                    DATE & TIME FOR PAGE HEADING
         SPACE 2
         MEND
./       ADD   NAME=XPREJECT
         MACRO
&SYMBOL  XPREJECT  &WA,&COND=
.* MACRO TO EXECUTE AN EJECT CALL TO 'XPRNTSUB'.
         LCLC  &TAG,&SYM,&OPT,&R
         LCLA  &K
&SYM     SETC  '&SYMBOL'
&TAG     SETC  'IHB&SYSNDX'
         CNOP  2,4
.TST1    AIF   ('&COND' NE '').TST2
&OPT     SETC  'F000'
         AGO   .SIMPLE
.TST2    AIF   ('&COND' NE 'ATHOF').TSTC
&OPT     SETC  'F100'
.SIMPLE  ANOP
&SYM     BAL   1,&TAG.L                LOAD PARM ADDRESS
         DC    XL2'&OPT'               OPTION BITS
         AGO   .VCON
.TSTC    AIF   ('&COND(1)' NE 'EQ').NE
&OPT     SETC  '80'
.NE      AIF   ('&COND(1)' NE 'NE').LT
&OPT     SETC  '70'
.LT      AIF   ('&COND(1)' NE 'LT').GT
&OPT     SETC  '40'
.GT      AIF   ('&COND(1)' NE 'GT').LE
&OPT     SETC  '20'
.LE      AIF   ('&COND(1)' NE 'LE').GE
&OPT     SETC  'C0'
.GE      AIF   ('&COND(1)' NE 'GE').NOT
&OPT     SETC  'A0'
.NOT     AIF   ('&OPT' NE '').COK
         MNOTE 4,'COND OPERAND &COND(1) ILLEGAL'
.COK     AIF   ('&COND(2)' NE '').C2OK
         MNOTE 4,'COND TEST QUANTITY MISSING'
         AGO   .NOTREG
.C2OK    AIF   ('&COND(2)'(1,1) NE '(').NOTREG
&K       SETA  K'&COND(2)-2
&R       SETC  '&COND(2)'(2,&K)
&SYM     STC   &R,&TAG.A               STORE INTO PARM LIST
&SYM     SETC  ''
.NOTREG  ANOP
&SYM     BAL   1,&TAG.L                LOAD PARM ADDRESS
         DC    XL1'&OPT'               CONDITION MASK
         AIF   ('&COND(2)' EQ '').NOTREG2
         AIF   ('&COND(2)'(1,1) NE '(').NOTREG2
&TAG.A   DC    AL1(0)                  TEST QUANTITY
         AGO   .VCON
.NOTREG2 DC    AL1(&COND(2))           TEST QUANTITY
.VCON    ANOP
&TAG.L   XPRINNRA  &WA,92
         MEND
./       ADD   NAME=XPRHEAD
         MACRO
&SYMBOL  XPRHEAD  &WA,&LIST=
.* MACRO TO PRESENT A LIST OF PRINT LINE DESCRIPTORS FOR PAGE HEADINGS
.* TO 'XPRNTSUB'.
&SYMBOL  XPRLIST  &WA,LIST=&LIST,IHBPARM=80
         MEND
./       ADD   NAME=XPRINNRA
         MACRO
&TAG     XPRINNRA  &WA,&IHBPARM
.* INNER MACRO USED IN CALLS TO 'XPRNTSUB'
         DC    V(XPRNTSUB)             PRINTING SUBROUTINE ADDRESS
&TAG     L     15,*-4                  LOAD ENTRY POINT
         AIF   ('&WA' NE '').WOK
         LA    0,XPRDCB01              LOAD STANDARD WORK AREA ADDRESS
         AGO   .BAL
.WOK     AIF   ('&WA' EQ '(0)').BAL
         AIF   ('&WA'(1,1) EQ '(').REG
         LA    0,&WA                   LOAD WORK AREA ADDRESS
         AGO   .BAL
.REG     LR    0,&WA(1)                LOAD WORK AREA ADDRESS
.BAL     ANOP                                                      @152
         LA    14,&IHBPARM.(15)        GET PRINT SUBROUTINE ADDR.  @152
         BALR  14,14                   CALL PRINT SUBROUTINE       @162
         MEND
./       ADD   NAME=XPRLDEF
         MACRO
&SYMBOL  XPRLDEF  &TEXT=,&LENGTH=132,&OFFSET=0,&SPA=1,&SPB=0
.* MACRO TO DEFINE PRINT LINE DESCRIPTOR BLOCKS ("PLD" BLOCKS) FOR
.* THE PRINTING SUBROUTINE "XPRNTSUB".
         LCLA  &N
         LCLC  &B3,&B6,&B7
         ACTR  25                      JUST IN CASE
&B3      SETC  '0'
&B6      SETC  '0'
&B7      SETC  '0'
.* PROCESS OPTIONS IN SPA OPERAND:
.TSTA1   AIF   ('&SPA(2)' EQ '').TSTB1
         AIF   ('&SPA(2)' NE 'NOEJ').TSTA2
&B3      SETC  '1'
         AGO   .TSTB1
.TSTA2   MNOTE 4,'OPERAND &SPA(2) AFTER KEYWORD SPA IS ILLEGAL'
.* PROCESS OPTIONS IN SPB OPERAND:
.TSTB1   ANOP
&N       SETA  2
.TSTB5   AIF   ('&SPB(&N)' NE 'NOEJ').TSTB2
&B7      SETC  '1'
         AGO   .TSTB4
.TSTB2   AIF   ('&SPB(&N)' NE 'ATHOF').TSTB3
&B6      SETC  '1'
         AGO   .TSTB4
.TSTB3   AIF   ('&SPB(&N)' EQ '').TSTB4
         MNOTE 4,'OPERAND &SPB(&N) AFTER KEYWORD SPB IS ILLEGAL'
.TSTB4   ANOP
&N       SETA  &N+1
         AIF   (&N LE 3).TSTB5
.IFTEXT  AIF   ('&TEXT' NE '').TOK
         MNOTE 4,'TEXT ADDRESS MISSING'
.TOK     ANOP
&SYMBOL  DS    0F                      ALIGN ON FULL-WORD BOUNDARY
         DC    BL1'000&B3.00&B6&B7'    OPTION BITS
         DC    AL3(&TEXT)              TEXT ADDRESS
         DC    AL1(&LENGTH)            TEXT LENGTH
         DC    AL1(&OFFSET)            MARGIN OFFSET
.TESTB   AIF   ('&SPB(1)' EQ 'EJECT').BSKIP
         AIF   ('&SPB(1)' EQ 'SKIP').BSKIP
         DC    AL1(&SPB(1))            PRE-SPACING
         AGO   .TESTA
.BSKIP   ANOP
         DC    AL1(255)                EJECT BEFORE PRINTING
.TESTA   AIF   ('&SPA(1)' EQ 'EJECT').ASKIP
         AIF   ('&SPA(1)' EQ 'SKIP').ASKIP
         DC    AL1(&SPA(1))                 POST-SPACING
         MEXIT
.ASKIP   ANOP
         DC    AL1(255)                EJECT AFTER PRINTING
         MEND
./       ADD   NAME=XPRLIST
         MACRO
&SYMBOL  XPRLIST  &WA,&LIST=,&IHBPARM=32
.* MACRO TO DELIVER A LIST OF PRINT LINE DESCRIPTORS TO 'XPRNTSUB'.
         LCLA  &N,&K,&D
         LCLC  &TAG,&SYM,&R
         ACTR  100
&SYM     SETC  '&SYMBOL'
&TAG     SETC  'IHB&SYSNDX'
         AIF   ('&LIST' NE '').LOK
         MNOTE 8,'LIST OPERAND MUST BE SUPPLIED'
         MEXIT
.LOK     CNOP  0,4
         AIF   ('&LIST' NE '(1)').STLOOP
&SYMBOL  B     &TAG.L                  BRANCH AROUND ADDRESS
         AGO   .VCON
.STLOOP  ANOP
&N       SETA  &N+1
         AIF   (&N GT N'&LIST).STDONE
         AIF   ('&LIST(&N)'(1,1) NE '(').STLOOP
&K       SETA  K'&LIST(&N)-2
&R       SETC  '&LIST(&N)'(2,&K)
&D       SETA  4*(&N-1)
&SYM     ST    &R,&TAG.A+&D            STORE INTO PARM LIST
&SYM     SETC  ''
         AIF   (&N NE N'&LIST).STLOOP
         OI    &TAG.A+&D,X'80'         SET END-OF-LIST BIT
.STDONE  ANOP
&SYM     BAL   1,&TAG.L                LOAD PARM LIST ADDRESS
&SYM     SETC  '&TAG.A'
&N       SETA  0
.LOOP    ANOP
&N       SETA  &N+1
         AIF   (&N GT N'&LIST).VCON
         AIF   ('&LIST(&N)'(1,1) NE '(').NOTREG
&SYM     DC    A(0)
&SYM     SETC  ''
         AGO   .LOOP
.NOTREG  AIF   (&N EQ N'&LIST).LAST
&SYM     DC    A(&LIST(&N))
&SYM     SETC  ''
         AGO   .LOOP
.LAST    ANOP
&SYM     DC    X'80'                   END-OF-LIST BIT
         DC    AL3(&LIST(&N))
.VCON    ANOP
&TAG.L   XPRINNRA  &WA,&IHBPARM
         MEND
./       ADD   NAME=XPRMOD
         MACRO
&SYMBOL  XPRMOD  &WA,&MAXLINE=0,&PAGENO=0,&PAGEWID=0,&PAGELEN=0,       X
               &TIME=0,&DATE=0
.* MACRO TO EXECUTE A MODIFY CALL TO 'XPRINTSUB'.
&SYMBOL  XPROPEN  &WA,MAXLINE=&MAXLINE,PAGENO=&PAGENO,PAGEWID=&PAGEWID,X
               PAGELEN=&PAGELEN,TIME=&TIME,DATE=&DATE,IHBPARM=104
         MEND
./       ADD   NAME=XPRNTLIN
         MACRO
&SYMBOL  XPRNTLIN  &WA,&TEXT=00,&LENGTH=132,&OFFSET=0,&SPB=0,&SPA=1
         LCLA  &N
         LCLC  &B3,&B6,&B7,&T,&R
         ACTR  25
&T       SETC  'IHB&SYSNDX'
&B3      SETC  '0'
&B6      SETC  '0'
&B7      SETC  '0'
.* PROCESS OPTIONS IN SPA OPERAND:
.TSTA1   AIF   ('&SPA(2)' EQ '').TSTB1
         AIF   ('&SPA(2)' NE 'NOEJ').TSTA2
&B3      SETC  '1'
         AGO   .TSTB1
.TSTA2   MNOTE 4,'OPERAND &SPA(2) AFTER KEYWORD SPA IS ILLEGAL'
.* PROCESS OPTIONS IN SPB OPERAND:
.TSTB1   ANOP
&N       SETA  2
.TSTB5   AIF   ('&SPB(&N)' NE 'NOEJ').TSTB2
&B7      SETC  '1'
         AGO   .TSTB4
.TSTB2   AIF   ('&SPB(&N)' NE 'ATHOF').TSTB3
&B6      SETC  '1'
         AGO   .TSTB4
.TSTB3   AIF   ('&SPB(&N)' EQ '').TSTB4
         MNOTE 4,'OPERAND &SPB(&N) AFTER KEYWORD SPB IS ILLEGAL'
.TSTB4   ANOP
&N       SETA  &N+1
         AIF   (&N LE 3).TSTB5
.* GENERATE STORE INSTRUCTIONS IF REGISTER NOTATION IS USED:
         CNOP  0,4
         AIF   ('&SYMBOL' EQ '').IFR1
&SYMBOL  DS    0H
.IFR1    AIF   ('&TEXT' NE '00').TXTOK
         MNOTE 4,'TEXT ADDRESS MUST BE SUPPLIED'
.TXTOK   AIF   ('&TEXT'(1,1) NE '(').IFR2
         ST    &TEXT(1),&T.A           STORE TEXT ADDRESS
         MVI   &T.A,B'000&B3.00&B6&B7' INSERT OPTION BITS
.IFR2    AIF   ('&LENGTH'(1,1) NE '(').IFR3
         STC   &LENGTH(1),&T.A+4       STORE TEXT LENGTH
.IFR3    AIF   ('&OFFSET'(1,1) NE '(').IFR4
         STC   &OFFSET(1),&T.A+5       STORE MARGIN OFFSET
.IFR4    AIF   ('&SPB(1)'(1,1) NE '(').IFR5
&N       SETA  K'&SPB(1)-2
&R       SETC  '&SPB(1)'(2,&N)
         STC   &R,&T.A+6               STORE PRE-SPACING
.IFR5    AIF   ('&SPA(1)'(1,1) NE '(').LOAD1
&N       SETA  K'&SPA(1)-2
&R       SETC  '&SPA(1)'(2,&N)
         STC   &R,&T.A+7               STORE POST-SPACING
.* LOAD REG 1 AND GENERATE PLD LIST ADDRESS:
.LOAD1   BAL   1,&T.L                  LOAD PARM ADDRESS
         DC    X'80'                   MARK END OF ADDRESS LIST
         DC    AL3(&T.A)               ADDRESS OF PLD
.* GENREATE PLD PARAMETERS TO DESCRIBE THE PRINT LINE:
.IFR11   AIF   ('&TEXT'(1,1) NE '(').NOTR11
&T.A     DC    A(0)                    TEXT ADDRESS & OPTION BITS
         AGO   .IFR12
.NOTR11  ANOP
&T.A     DC    B'000&B3.00&B6&B7'      OPTION BITS
         DC    AL3(&TEXT)              TEXT ADDRESS
.IFR12   AIF   ('&LENGTH'(1,1) NE '(').NOTR12
         DC    AL1(0)                  TEXT LENGTH
         AGO   .IFR13
.NOTR12  DC    AL1(&LENGTH)            TEXT LENGTH
.IFR13   AIF   ('&OFFSET'(1,1) NE '(').NOTR13
         DC    AL1(0)                  MARGIN OFFSET
         AGO   .IFR14
.NOTR13  DC    AL1(&OFFSET)            MARGIN OFFSET
.IFR14   AIF   ('&SPB(1)'(1,1) NE '(').NOTR14
         DC    AL1(0)                  PRE-SPACING
         AGO   .IFR15
.NOTR14  AIF   ('&SPB(1)' EQ 'EJECT').BSKIP
         AIF   ('&SPB(1)' EQ 'SKIP').BSKIP
         DC    AL1(&SPB(1))            PRE-SPACING
         AGO   .IFR15
.BSKIP   DC    AL1(255)                SKIP BEFOR PRINTING
.IFR15   AIF   ('&SPA(1)'(1,1) NE '(').NOTR15
         DC    AL1(0)                  POST-SPACING
         AGO   .VCON
.NOTR15  AIF   ('&SPA(1)' EQ 'EJECT').ASKIP
         AIF   ('&SPA(1)' EQ 'SKIP').ASKIP
         DC    AL1(&SPA(1))            POST-SPACING
         AGO   .VCON
.ASKIP   DC    AL1(255)                SKIP AFTER PRINTING
.VCON    ANOP
&T.L    XPRINNRA  &WA,32
         MEND
./       ADD   NAME=XPROPEN
         MACRO
&SYMBOL  XPROPEN  &WA,&MAXLINE=0,&PAGENO=0,&PAGEWID=0,&PAGELEN=0,      X
               &TIME=0,&DATE=0,&DDNAME=SYSPRINT,&IHBPARM=56
.* MACRO TO EXECUTE AN 'OPEN' OR 'MODIFY' CALL TO 'XPRNTSUB'.
         LCLC  &TAG
&TAG     SETC  'IHB&SYSNDX'
         AIF   ('&SYMBOL' EQ '').T1
&SYMBOL  DS    0H
.T1      AIF   ('&MAXLINE'(1,1) NE '(').T2
         ST    &MAXLINE(1),&TAG.A      STORE IN PARM LIST
.T2      AIF   ('&PAGENO'(1,1) NE '(').T3
         STH   &PAGENO(1),&TAG.A+4     STORE IN PARM LIST
.T3      AIF   ('&PAGEWID'(1,1) NE '(').T4
         STC   &PAGEWID(1),&TAG.A+6    STORE IN PARM LIST
.T4      AIF   ('&PAGELEN'(1,1) NE '(').T5
         STC   &PAGELEN(1),&TAG.A+7    STORE IN PARM LIST
.T5      AIF   ('&TIME'(1,1) NE '(').T6
         ST    &TIME(1),&TAG.A+8       STORE IN PARM LIST
.T6      AIF   ('&DATE'(1,1) NE '(').T7
         ST    &DATE(1),&TAG.A+12      STORE IN PARM LIST
.T7      AIF   ('&DDNAME'(1,1) NE '(').CNOP
         MVC   &TAG.A+16(8),0(&DDNAME(1))  STORE IN PARM LIST
.CNOP    CNOP  0,4
         BAL   1,&TAG.L        LOAD PARM LIST ADDRESS
.P1      AIF   ('&MAXLINE'(1,1) EQ '(').PR1
&TAG.A   DC    A(&MAXLINE)             PRINT OUTPUT LIMIT
         AGO   .P2
.PR1     ANOP
&TAG.A   DC    A(0)                    PRINT OUTPUT LIMIT
.P2      AIF   ('&PAGENO'(1,1) EQ '(').PR2
         DC    AL2(&PAGENO)            PAGE NUMBER
         AGO   .P3
.PR2     DC    AL2(0)                  PAGE NUMBER
.P3      AIF   ('&PAGEWID'(1,1) EQ '(').PR3
         DC    AL1(&PAGEWID)           PAGE WIDTH
         AGO   .P4
.PR3     DC    AL1(0)                  PAGE WIDTH
.P4      AIF   ('&PAGELEN'(1,1) EQ '(').PR4
         DC    AL1(&PAGELEN)           PAGE LENGTH
         AGO   .P5
.PR4     DC    AL1(0)                  PAGE LENGTH
.P5      AIF   ('&TIME'(1,1) EQ '(').PR5
         DC    A(&TIME)                ADDRESS OF TIME
         AGO   .P6
.PR5     DC    A(0)                    ADDRESS OF TIME
.P6      AIF   ('&DATE'(1,1) EQ '(').PR6
         DC    A(&DATE)                ADDRESS OF DATE
         AGO   .P7
.PR6     DC    A(0)                    ADDRESS OF DATE
.P7      AIF   ('&IHBPARM' NE '56').VCON
         AIF   ('&DDNAME'(1,1) EQ '(').PR7
         DC    CL8'&DDNAME'            DDNAME FOR DATA SET
         AGO   .VCON
.PR7     DC    CL8' '                  DDNAME FOR DATA SET
.VCON    ANOP
&TAG.L   XPRINNRA  &WA,&IHBPARM
         MEND
./       ADD   NAME=XPRSPACE
         MACRO
&SYMBOL  XPRSPACE  &WA,&LINES=1,&COND=
.* MACRO TO EXECUTE A SPACE CALL TO 'XPRNTSUB'.
         LCLA  &N,&B6,&B7
         LCLC  &TAG,&SYM
&SYM     SETC  '&SYMBOL'
&TAG     SETC  'IHB&SYSNDX'
.LOOP    ANOP
&N       SETA  &N+1
         AIF   (&N GT N'&COND).LDONE
.TST1    AIF   ('&COND(&N)' NE 'NOEJ').TST2
&B6      SETA  1
         AGO   .LOOP
.TST2    AIF   ('&COND(&N)' NE 'ATHOF').TST3
&B7      SETA  1
         AGO   .LOOP
.TST3    MNOTE 4,'OPERAND &COND(&N) AFTER KEYWORD COND IS ILLEGAL'
         AGO   .LOOP
.LDONE   CNOP  2,4
         AIF   ('&LINES'(1,1) NE '(').NOTREG1
&SYM     STC   &LINES(1),&TAG.A+1      STORE INTO PARM LIST
&SYM     SETC  ''
.NOTREG1 ANOP
&SYM     BAL   1,&TAG.L                LOAD PARM LIST ADDRESS
&TAG.A   DC    B'000000&B6&B7'         OPTION BITS
         AIF   ('&LINES'(1,1) NE '(').NOTREG2
         DC    X'00'                   SPACING AMOUNT
         AGO   .VCON
.NOTREG2 DC    AL1(&LINES)             SPACING AMOUNT
.VCON    ANOP
&TAG.L   XPRINNRA  &WA,44
         MEND
