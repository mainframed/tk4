/* REXX
 ×
 × Name: D2SEQN (Death To Sequence Numbers)
 ×
 × Author: David Alcock
 ×
 × Purpose: This utility is a filter that copies files from SYSUT1
 ×          to SYSUT2 sans sequence numbers.  It will also remove
 ×          IBM APAR strings like "@THX1138" when found as the
 ×          last word on a line.
 ×
 ×          The primary purpose of this exec is to print source
 ×          code without the dang sequence numbers. This makes it
 ×          easier to read source code and write comments on the
 ×          end of the line.
 ×
 ×          Hint: This exec can also be used to copy from RECFM=F to
 ×          RECFM=V files.
 ×
 × Sample JCL:
 ×
 ×        //PRINT2  EXEC PGM=IKJEFT01,PARM='%D2SEQN',
 ×        //        DYNAMNBR=100,REGION=9M,TIME=1439
 ×        //SYSTSIN  DD DUMMY
 ×        //SYSTSPRT DD SYSOUT=*
 ×        //SYSEXEC  DD DISP=SHR,DSN=IBMUSER.DAVE.EXEC
 ×        //SYSUT1   DD DISP=SHR,DSN=SYS1.V1R1M0.SHASSRC(HASMMODL)
 ×        //SYSUT2   DD SYSOUT=A ,OUTPUT=*.PORTRAIT
 ×
 × Disclaimer: This REXX exec is FREEWARE.  Use at your own risk.  It
 ×             is provided for your enjoyment and neither David
 ×             Alcock or his employer provides any warranty for it's
 ×             use.  I'd like to hear how it works on your system.
 ×
 ×             This software is not in the public domain but is
 ×             available free of charge and with source code provided.
 ×             It is copyright 1999 by David Alcock.  All rights
 ×             reserved.
 */

/* --------------------------------------------------------------------
 ×          M o d i f i c a t i o n   H i s t o r y
 ×
 × Person     Date        Description
 × ---------- ----------- ---------------------------------------------
 × DGAlcock   01-MAR-1999 v1.0 Original utility written;
 +------------------------------------------------------------------ */

vrm = '1.0'

mtype = address()
PARSE SOURCE s1 s2 s3 s4 s5 s6 s7 s8 s9

if mtype == "TSO" × mtype == "MVS" then ,
   if sysvar(systsoe) > "2040" then systemid = mvsvar(syssmfid)
   else do
        cvt    = storage(10,4)
        dcvt   = c2d(cvt)
        smca   = storage(c2x(d2c(dcvt+x2d(c5))),3)
        dsmca  = c2d(smca)
        systemid = storage(c2x(d2c(dsmca+x2d(10))),4)
        end
else systemid = ""

say "D2SEQN - version "vrm "- Freeware - Death to Sequence Numbers"
say "Copyright 1999 by David Alcock. All rights reserved."
say " "
say ". Executing in Environment: "s1 "-" mtype
if systemid <> "" then say ". Executing on system: "systemid
say " "

/* --------------------------------------------------------------------
 × Read SYSUT1 records and write to SYSUT2
 +------------------------------------------------------------------ */

address TSO
"EXECIO * DISKR SYSUT1 (FINIS STEM sysut1_file."
"NEWSTACK"   /* Place to put SYSUT2 output */

do i = 1 to sysut1_file.0

   record = sysut1_file.i

   if length(record) == 80 then do
      record = substr(record,1,72)
      /* add elimnation of IBM apar stuff like "@THX1138" */
      c = substr(record,72,1) /* Save continuation character if any */
      w = words(record)
      if w > 0 then do
         if substr(word(record,w),1,1) == "@" then do
            say "Deleted word ("left(word(record,w),10)") from: " ,
                record
            record = delword(record,w)
            end
         end
      if c <> " " then record = overlay(c,record,72)
      end

   if record == "" then record = " "

   queue record

   end

/* --------------------------------------------------------------------
 × Close SYSUT2 and leave
 +------------------------------------------------------------------ */

queue ''      /* add null line to indicate End-Of-File */
"EXECIO * DISKW SYSUT2 (FINIS"
"DELSTACK"
say " "
say "That's all folks..."
exit 0
