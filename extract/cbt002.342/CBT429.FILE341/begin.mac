         MACRO
&NAME    BEGIN &CSECT,                                                 +
               &EPREG=R15,                                             +
               &BASE1=R12,                                             +
               &BASE2=,                                                +
               &BASE3=,                                                +
               &DOREG=R14,                                             +
               &AMODE=24,                                              +
               &RMODE=24,                                              +
               &RENT=NO,                                               +
               &SIZE=4096,                                             +
               &LOC=RES,                                               +
               &STACK=YES,                                             +
               &SUBPOOL=
         GBLC  &XSECT,&XRENT,&XSIZE,&XSUB,&XSAVE(200),&BALREG
         GBLC  &XBASE1,&XBASE2,&XBASE3,&XSTACK
         GBLA  &XSAVEX,&XPAGE
         GBLB  &XSET
         LCLC  &CHAR,&WORKREG,&PAGE
         LCLA  &COUNT,&LOOPER,&LSIZE1,&LSIZE2
         AIF   ('&CSECT' EQ '').MNOTE1
&COUNT   SETA  K'&CSECT
         AIF   (&COUNT GT 8).MNOTE2
&CHAR    SETC  '&CSECT'(1,1)
         AIF   ('&CHAR' EQ '@').NEXT1
         AIF   ('&CHAR' EQ '#').NEXT1
         AIF   ('&CHAR' EQ '$').NEXT1
.NEXT1   AIF   ('&CHAR' LT 'A').MNOTE3
         AIF   ('&CHAR' GT 'I').NEXT2
         AGO   .REST
.NEXT2   AIF   ('&CHAR' LT 'B').MNOTE3
         AIF   ('&CHAR' GT 'R').NEXT3
         AGO   .REST
.NEXT3   AIF   ('&CHAR' LT 'S').MNOTE3
         AIF   ('&CHAR' GT 'Z').MNOTE3
.REST    ANOP
&LOOPER  SETA  2
.LOOP1   ANOP
         AIF   (&LOOPER GT &COUNT).CSECTOK
&CHAR    SETC  '&CSECT'(&LOOPER,1)
         AIF   ('&CHAR' EQ '@').LOOPEND1
         AIF   ('&CHAR' EQ '#').LOOPEND1
         AIF   ('&CHAR' EQ '$').LOOPEND1
         AIF   ('&CHAR' LT 'A').MNOTE4
         AIF   ('&CHAR' GT 'I').ITR
         AGO   .LOOPEND1
.ITR     AIF   ('&CHAR' LT 'B').MNOTE4
         AIF   ('&CHAR' GT 'R').STZ
         AGO   .LOOPEND1
.STZ     AIF   ('&CHAR' LT 'S').MNOTE4
         AIF   ('&CHAR' GT 'Z').NOS
         AGO   .LOOPEND1
.NOS     AIF   ('&CHAR' LT '0').MNOTE4
         AIF   ('&CHAR' GT '9').MNOTE4
.LOOPEND1   ANOP
&LOOPER  SETA  &LOOPER+1
         AGO   .LOOP1
.CSECTOK ANOP
         AIF   ('&AMODE' EQ '24').CHKRMOD
         AIF   ('&AMODE' NE '31').MNOTE5
.CHKRMOD AIF   ('&RMODE' EQ '24').CHKCOMB
         AIF   ('&RMODE' NE 'ANY').MNOTE6
.CHKCOMB AIF   ('&AMODE' EQ '31').CHKRENT
         AIF   ('&RMODE' EQ 'ANY').MNOTE7
.CHKRENT AIF   ('&RENT' EQ 'NO').START
         AIF   ('&RENT' NE 'YES').MNOTE8
&LOOPER  SETA  1
&COUNT   SETA  K'&SIZE
&LOOPER  SETA  1
.LOOP2   ANOP
         AIF   (&LOOPER GT &COUNT).SIZEOK
&CHAR    SETC  '&SIZE'(&LOOPER,1)
         AIF   ('&CHAR' LT '0').MNOTE9
         AIF   ('&CHAR' GT '9').MNOTE9
&LOOPER  SETA  &LOOPER+1
         AGO   .LOOP2
.SIZEOK  ANOP
&LOOPER  SETA  1
&COUNT   SETA  K'&SUBPOOL
.LOOP3   ANOP
         AIF   (&LOOPER GT &COUNT).SUBOK
&CHAR    SETC  '&SUBPOOL'(&LOOPER,1)
         AIF   ('&CHAR' LT '0').MNOTEA
         AIF   ('&CHAR' GT '9').MNOTEA
&LOOPER  SETA  &LOOPER+1
         AGO   .LOOP3
.SUBOK   ANOP
.START   ANOP
&XSECT   SETC  '&CSECT'
&XRENT   SETC  '&RENT'
&XSIZE   SETC  '&SIZE'
&XSUB    SETC  '&SUBPOOL'
&BALREG  SETC  '&DOREG'
&XSET    SETB  1
&XBASE1  SETC  '&BASE1'
&XBASE2  SETC  '&BASE2'
&XBASE3  SETC  '&BASE3'
&COUNT         SETA  11
.REGLOOP       ANOP
               AIF   (&COUNT EQ 0).MNOTEB
&WORKREG       SETC  'R'.'&COUNT'
               AIF   ('&WORKREG' NE '&XBASE1' AND                      +
               '&WORKREG' NE '&XBASE2' AND                             +
               '&WORKREG' NE '&XBASE3').LOOPEND2
&COUNT         SETA  &COUNT-1
               AGO   .REGLOOP
.LOOPEND2      ANOP
&CSECT   CSECT
&CSECT   AMODE &AMODE
&CSECT   RMODE &RMODE
         YREGS
         PACEQU
         USING &CSECT,&EPREG
         B     &CSECT._START
         DC    C' &CSECT ASSEMBLED ON &SYSDATE AT &SYSTIME'
         DC    C' FROM &SYSPARM   '
         AIF   ('&STACK' EQ 'NO').NOSTACK
&XSTACK  SETC  'YES'
&CSECT._START   DS    0D
         DROP  &EPREG
         BAKR  R14,R0
         LR    &BASE1,&EPREG
         AIF   ('&BASE2' EQ '').ONEBASE
         AIF   ('&BASE3' EQ '').TWOBASE
         USING &CSECT,&BASE1,&BASE2,&BASE3
         LA    &BASE2,4095(,&BASE1)
         LA    &BASE2,1(,&BASE2)
         LA    &BASE3,4095(,&BASE2)
         LA    &BASE3,1(,&BASE3)
         AGO   .TSTRENT
.TWOBASE ANOP
         USING &CSECT,&BASE1,&BASE2
         LA    &BASE2,4095(,&BASE1)
         LA    &BASE2,1(,&BASE2)
         AGO   .TSTRENT
.ONEBASE ANOP
         USING &CSECT,&BASE1
.TSTRENT ANOP
         AIF   ('&RENT' EQ 'YES').RENT
         L     R13,=A(&XSECT._STORAGE)
         USING &XSECT._STORAGE,R13
         MEXIT
.RENT    ANOP
         AIF   ('&SIZE' NE '0').STORAGE
         MEXIT
.STORAGE ANOP
&LSIZE1  SETA  &SIZE/4096
&LSIZE2  SETA  &LSIZE1*4096
         AIF   (&LSIZE2 EQ &SIZE).NOBUMP1
&LSIZE1  SETA  &LSIZE1+1
.NOBUMP1 ANOP
&LSIZE2  SETA  &LSIZE1*4096
&XSIZE   SETC  '&LSIZE2'
&XPAGE   SETA  &LSIZE1
         L     &WORKREG,=F'&LSIZE2'
         AIF   ('&SUBPOOL' NE '').NOSUBPL
         STORAGE                                                       +
               OBTAIN,                                                 +
               LENGTH=(&WORKREG),                                      +
               LOC=&LOC
         LR    R13,R1
         EREG  R0,R1
         USING &XSECT._STORAGE,R13
&LSIZE2  SETA  &LSIZE1
&LSIZE1  SETA  1
.LOOP99  ANOP
         AIF   (&LSIZE1 GT 1).LOOP99A
         LA    R14,2048(,R13)
         AGO   .LOOP99B
.LOOP99A ANOP
         LA    R14,2048(,R14)
.LOOP99B ANOP
         LA    R14,2048(,R14)
         AIF   (&LSIZE1 LT 10).LOOP99C
&PAGE    SETC  'PAGE'.'&LSIZE1'.'_ADDRESS'
         AGO   .LOOP99D
.LOOP99C ANOP
&PAGE    SETC  'PAGE0'.'&LSIZE1'.'_ADDRESS'
.LOOP99D ANOP
         ST    R14,&PAGE
&LSIZE1  SETA  &LSIZE1+1
         AIF   (&LSIZE1 LE &LSIZE2).LOOP99
         MEXIT
.NOSUBPL ANOP
         STORAGE                                                       +
               OBTAIN,                                                 +
               LENGTH=(&WORKREG),                                      +
               SP=&SUBPOOL,                                            +
               LOC=&LOC
         LR    R13,R1
         EREG  R0,R1
         USING &XSECT._STORAGE,R13
&LSIZE2  SETA  &LSIZE1
&LSIZE1  SETA  1
.LOOP98  ANOP
         AIF   (&LSIZE1 GT 1).LOOP98A
         LA    R14,2048(,R13)
         AGO   .LOOP98B
.LOOP98A ANOP
         LA    R14,2048(,R14)
.LOOP98B ANOP
         LA    R14,2048(,R14)
         AIF   (&LSIZE1 LT 10).LOOP98C
&PAGE    SETC  'PAGE'.'&LSIZE1'.'_ADDRESS'
         AGO   .LOOP98D
.LOOP98C ANOP
&PAGE    SETC  'PAGE0'.'&LSIZE1'.'_ADDRESS'
.LOOP98D ANOP
         ST    R14,&PAGE
&LSIZE1  SETA  &LSIZE1+1
         AIF   (&LSIZE1 LE &LSIZE2).LOOP98
         MEXIT
.*
.NOSTACK   ANOP
&XSTACK  SETC  'NO'
&CSECT._START   DS    0D
         DROP  &EPREG
         STM   R14,R12,12(R13)
         LR    &BASE1,&EPREG
         AIF   ('&BASE2' EQ '').BASEONE
         AIF   ('&BASE3' EQ '').BASETWO
         USING &CSECT,&BASE1,&BASE2,&BASE3
         LA    &BASE2,4095(,&BASE1)
         LA    &BASE2,1(,&BASE2)
         LA    &BASE3,4095(,&BASE2)
         LA    &BASE3,1(,&BASE3)
         AGO   .RENTEST
.BASETWO ANOP
         USING &CSECT,&BASE1,&BASE2
         LA    &BASE2,4095(,&BASE1)
         LA    &BASE2,1(,&BASE2)
         AGO   .RENTEST
.BASEONE ANOP
         USING &CSECT,&BASE1
.RENTEST ANOP
         AIF   ('&RENT' EQ 'YES').RENTYES
         LR    &WORKREG,R13
         L     R13,=A(&XSECT._STORAGE)
         USING &XSECT._STORAGE,R13
         ST    &WORKREG,&XSECT._STORAGE+4
         ST    R13,8(,&WORKREG)
         LM    R0,R1,20(&WORKREG)
         MEXIT
.RENTYES ANOP
         AIF   ('&SIZE' EQ '0').GETNO
&LSIZE1  SETA  &SIZE/4096
&LSIZE2  SETA  &LSIZE1*4096
         AIF   (&LSIZE2 EQ &SIZE).NOBUMP2
&LSIZE1  SETA  &LSIZE1+1
.NOBUMP2 ANOP
&LSIZE2  SETA  &LSIZE1*4096
&XSIZE   SETC  '&LSIZE2'
&XPAGE   SETA  &LSIZE1
         L     &WORKREG,=F'&LSIZE2'
         AIF   ('&SUBPOOL' EQ '').SUBPLNO
         STORAGE                                                       +
               OBTAIN,                                                 +
               LENGTH=(&WORKREG),                                      +
               SP=&SUBPOOL,                                            +
               LOC=&LOC
         AGO   .REGSAVE
.SUBPLNO ANOP
         STORAGE                                                       +
               OBTAIN,                                                 +
               LENGTH=(&WORKREG),                                      +
               LOC=&LOC
.REGSAVE ANOP
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    &WORKREG,R13
         LR    R13,R1
         USING &XSECT._STORAGE,R13
         LM    R0,R1,20(&WORKREG)
&LSIZE2  SETA  &LSIZE1
&LSIZE1  SETA  1
.LOOP97  ANOP
         AIF   (&LSIZE1 GT 1).LOOP97A
         LA    R14,2048(,R13)
         AGO   .LOOP97B
.LOOP97A ANOP
         LA    R14,2048(,R14)
.LOOP97B ANOP
         LA    R14,2048(,R14)
         AIF   (&LSIZE1 LT 10).LOOP97C
&PAGE    SETC  'PAGE'.'&LSIZE1'.'_ADDRESS'
         AGO   .LOOP97D
.LOOP97C ANOP
&PAGE    SETC  'PAGE0'.'&LSIZE1'.'_ADDRESS'
.LOOP97D ANOP
         ST    R14,&PAGE
&LSIZE1  SETA  &LSIZE1+1
         AIF   (&LSIZE1 LE &LSIZE2).LOOP97
.GETNO   ANOP
         MEXIT
.MNOTE1  MNOTE 8,'NO CSECT NAME PROVIDED'
         MEXIT
.MNOTE2  MNOTE 8,'CSECT NAME GREATER THAN 8 CHARACTERS'
         MEXIT
.MNOTE3  MNOTE 8,'INVALID FIRST CHARACTER IN CSECT NAME'
         MEXIT
.MNOTE4  MNOTE 8,'8SECT NAME CONTAINS INVALID CHARACTER'
         MEXIT
.MNOTE5  MNOTE 8,'INVALID AMODE SPECIFIED'
         MEXIT
.MNOTE6  MNOTE 8,'INVALID RMODE SPECIFIED'
         MEXIT
.MNOTE7  MNOTE 8,'INVALID AMODE-RMODE COMBINATION SPECIFIED'
         MEXIT
.MNOTE8  MNOTE 8,'INVALID VALUE FOR REENTRANT (YES OR NO)'
         MEXIT
.MNOTE9  MNOTE 8,'SIZE PARAMETER NOT NUMERIC'
         MEXIT
.MNOTEA  MNOTE 8,'SUBPOOL PARAMETER NOT NUMERIC'
         MEXIT
.MNOTEB  MNOTE 8,'UNABLE TO DETERMINE WORK REGISTER'
         MEND
