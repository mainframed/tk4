         MACRO
&NAME    DO    &RTN
         GBLA  &DONO
         GBLB  &WHILE_FLAG,&COUNT_FLAG
         GBLB  &IFFLAG
         GBLC  &DOSTART(10),&DO_BRANCH1(10),&DO_BRANCH2(10),&XSTACK
         GBLB  &DOTYPE(10)
         GBLC  &WHILE_START,&DOEND(10),&CTBI,&CTREG,&CTFIL,&BALREG
         LCLC  &ROUTINE
&IFFLAG  SETB  0
         AIF   ('&RTN' EQ '*').DOLOOP
         AIF   (K'&RTN EQ 0).DOLOOP
         AIF   ('&RTN'(1,1) EQ '(').USEBALR
         AIF   ('&XSTACK' EQ 'YES').SETBAS
         BAL   &BALREG,&RTN
         MEXIT
.SETBAS  ANOP
         BAS   &BALREG,&RTN
         MEXIT
.USEBALR ANOP
         AIF   (K'&RTN EQ 3 AND                                        +
               '&RTN'(2,1) GE '1' AND                                  +
               '&RTN'(2,1) LE '9' AND                                  +
               '&RTN'(3,1) EQ ')').SETRTN1
         AIF   (K'&RTN EQ 4 AND                                        +
               '&RTN'(2,1) GE '0' AND                                  +
               '&RTN'(2,1) LE '1' AND                                  +
               '&RTN'(3,1) GE '0' AND                                  +
               '&RTN'(3,1) LE '5' AND                                  +
               '&RTN'(4,1) EQ ')').SETRTN2
         AIF   (K'&RTN EQ 4 AND                                        +
               '&RTN'(2,1) EQ 'R' AND                                  +
               '&RTN'(3,1) GE '0' AND                                  +
               '&RTN'(3,1) LE '9' AND                                  +
               '&RTN'(4,1) EQ ')').SETRTN3
         AIF   (K'&RTN EQ 5 AND                                        +
               '&RTN'(2,1) EQ 'R' AND                                  +
               '&RTN'(3,1) GE '0' AND                                  +
               '&RTN'(3,1) LE '1' AND                                  +
               '&RTN'(4,1) GE '0' AND                                  +
               '&RTN'(4,1) LE '5' AND                                  +
               '&RTN'(5,1) EQ ')').SETRTN4
         MNOTE 2,'INVALID REGISTER DESIGNATION'
         MEXIT
.SETRTN1 ANOP
&ROUTINE SETC  'R'.'&RTN'(2,1)
         AGO   .SETBALR
.*
.SETRTN2 ANOP
&ROUTINE SETC  'R'.'&RTN'(2,2)
         AGO   .SETBALR
.*
.SETRTN3 ANOP
&ROUTINE SETC  '&RTN'(2,2)
         AGO   .SETBALR
.*
.SETRTN4 ANOP
&ROUTINE SETC  '&RTN'(2,3)
.*
.SETBALR ANOP
         AIF   ('&XSTACK' EQ 'YES').SETBASR
&NAME    BALR  &BALREG,&ROUTINE
         MEXIT
.SETBASR ANOP
&NAME    BASR  &BALREG,&ROUTINE
         MEXIT
.*
.DOLOOP              ANOP
                     AIF   (&WHILE_FLAG).SET_WHILE
                     AIF   (&COUNT_FLAG).SET_COUNT
&DONO                SETA  &DONO+1
&DO_BRANCH1(&DONO)   SETC  'B'
&DO_BRANCH2(&DONO)   SETC  ''
&DOSTART(&DONO)      SETC  'DOSTART_'.'&SYSNDX'
&DOEND(&DONO)        SETC  'DOEND_'.'&SYSNDX'
&DOTYPE(&DONO)       SETB  1
&WHILE_FLAG          SETB  0
&DOSTART(&DONO)   DS    0H
                     MEXIT
.*
.SET_WHILE           ANOP
&DONO                SETA  &DONO+1
&DO_BRANCH1(&DONO)   SETC   'B'
&DO_BRANCH2(&DONO)   SETC   ''
&DOSTART(&DONO)      SETC  '&WHILE_START'
&DOEND(&DONO)        SETC  'DOEND_'.'&SYSNDX'
&DOTYPE(&DONO)       SETB  0
&WHILE_FLAG          SETB  0
                     AIF   (&COUNT_FLAG).CTWH
                     MEXIT
.*
.SET_COUNT        ANOP
&DOSTART(&DONO)   SETC  'DOSTART_'.'&SYSNDX'
&DOEND(&DONO)     SETC  'DOEND_'.'&SYSNDX'
&DOTYPE(&DONO)    SETB  0
&DOSTART(&DONO)   DS    0H
&COUNT_FLAG       SETB  0
         MEND
