***********************************************************************
* THIS CODE READS THE MEMBER OF JPRHEAD SPECIFIED IN THE TRAILER      *
* DATA AND OUTPUTS IT TO THE PRINTER.                                 *
***********************************************************************
         SPACE 1
         TM    J1DFLAG3,J1DF3T     IS A TRAILER REQUIRED?
         BNO   DHC3TREX            NO, BYPASS THIS CODE
         MVC   P3DMEMB,J1DTMEM     YES, COPY MEMBER NAME
         SPACE 2
         LA    R15,P3DHEAD         POINT TO DCB
         OPEN  ((R15),INPUT),MF=(E,J1DOPENX) OPEN PDS
         EJECT ,
DHC300CA DS    0H                  PROCESS TRAILER DATA
         LA    R2,P3DHEDSW         POINT AT MDSCB
         BAL   R4,PDSFIND          GET THE MEMBER
         LTR   R1,R1               DID WE?
         BZ    DHC300CB            YES, CONTINUE
*        PLACE ANY ERROR MESSAGING HERE
         B     DHC3TREX            BYPASS THE REST OF THIS CODE
         SPACE 1
DHC300CB DS    0H                  PROCESS TRAILER DATA MEMBER
         LA    R2,P3DHEDSW         POINT AT MDSCB
         BAL   R4,PDSGET           READ DATA FROM PDS
         LTR   R1,R1               TEST FOR DATA
         BZ    DHC300CX            NONE, FINISHED
         CLI   4(R1),C'*'          IS IT A COMMENT?
         BE    DHC300CB            YES, IGNORE IT
         LH    R0,0(,R1)           GET LENGTH OF DATA
         SH    R0,=H'5'            CORRECT LENGTH FOR RDW + CC
         MVC   J1DCONCH,4(R1)      COPY CARRIAGE CONTROL
         LA    R1,5(,R1)           SKIP RDW + CC
         BAS   R14,P3WLOCS         PRINT HEADER
         B     DHC300CB            GO ROUND FOR MORE
         EJECT ,
***********************************************************************
* HERE ARE THE STANDARD I/O COMPONENTS USED BY DHC CODE               *
***********************************************************************
         SPACE 2
***********************************************************************
*        PARTITIONED DATA SET PROCESSING SUBROUTINES                  *
***********************************************************************
         SPACE 1
PDSFIND  EQU   *
         LA    R15,8               SET CONDITION FOR D/S NOT OPEN
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         L     R1,MDCBDCB          GET DCB ADDRESS
         FIND  (1),P3DMEMB,D       LOCATE MEMBER
PDSFINDX EQU   *
         LR    R1,R15              PUT RETURN CODE IN R1
         BR    R4                  RETURN TO CALLER
         SPACE 3
PDSGET   RIN   (R2,R3,R4,R6)       SUB-ROUTINE ENTRY
         L     R6,MDCBDCB          GET ADDRESS OF DCB
         USING IHADCB,R6           ESTABLISH DCB ADDRESSABILITY
         TM    P3DSW1,FIRSTGET     FIRST RECORD FOR NEW MEMBER?
         BO    PDSGET1             NO,BRANCH
         GETBUF (R6),(R4)         GET ADDRESS FOR FIRST BUFFER
         ST    R4,P3DABUF1         SAVE IT
         GETBUF (R6),(R4)         GET ADDRESS FOR SECOND BUFFER
         ST    R4,P3DABUF2         SAVE IT
         READ  P3DECB1,SF,(R6),MF=E READ FIRST DATA BLOCK
         OI    P3DSW1,FIRSTGET+READREQ INDICATE FIRST READ
         SPACE 2
PDSGET1  DS    0H                  SUBSEQUENT READS
         TM    P3DSW1,READREQ       IS A READ REQUIRED?
         BNO   PDSGET2             NO. BRANCH
         CHECK P3DECB1             WAIT FOR I/O TO COMPLETE
         L     R2,P3DABUF1         GET DATA ADDRESS
         LA    R2,4(,R2)           SKIP OVER BDW
         ST    R2,P3DSEGAD         STORE DATA ADDRESS
         L     R2,P3DIOBA          GET ADDRESS OF IOB.
         LH    R3,DCBBLKSI         GET MAXIMUM BLOCKSIZE,
         SH    R3,14(0,R2)         SUBTRACT IOB RESIDUAL COUNT
         SH    R3,=H'4'            CORRECT FOR BDW
         STH   R3,P3DBYTES         TO GIVE BYTES IN NEW BLOCK.
         NI    P3DSW1,255-READREQ  RESET READ SWITCH
         L     R1,P3DABUF2         *
         MVC   P3DABUF2,P3DABUF1   ** SWAP OVER BUFFERS
         ST    R1,P3DABUF1         *
         READ  P3DECB1,SF,(R6),MF=E INITIATE READ TO FILL OTHER BUFFER
PDSGET2  EQU   *
         L     R1,P3DSEGAD         GET CURRENT BUFFER SEGMENT ADDRESS
         LH    R3,P3DBYTES         GET BYTES LEFT IN CURRENT BUFFER
         LH    R2,0(,R1)           GET RECORD LENGTH
         SR    R3,R2               SUBTRACT FROM BYTES LEFT IN BUFFER
         BP    *+8                 BRANCH IF STILL SOME LEFT
         OI    P3DSW1,READREQ      SET FLAG TO INVOKE PHYSICAL READ.
         STH   R3,P3DBYTES         SAVE BYTES LEFT IN BUFFER
         LA    R2,0(R1,R2)         POINT TO NEXT RECORD IN BUFFER
         ST    R2,P3DSEGAD         SAVE NEW ADDRESS
PDSGEXIT EQU   *
         ROUT  ,                    RETURN TO CALLER
         SPACE 4
PDSEODAD EQU   *
         NI    P3DSW1,255-FIRSTGET SET FIRSTTIME SWITCH FOR NEXT MEMBER
         L     R2,P3DABUF1         GET 1ST BUFFER ADDRESS
         FREEBUF (R6),(R2)         FREE IT
         L     R2,P3DABUF2         GET 2ND BUFFER ADDRESS
         FREEBUF (R6),(R2)         FREE IT
PDSEOF   EQU   *
         SR    R1,R1               INDICATE END OF MEMBER
         B     PDSGEXIT            RETURN TO CALLER
         DROP  R6                  END DCB ADDRESSABILITY
         EJECT
***********************************************************************
*        CLOSE ROUTINE                                                *
***********************************************************************
         SPACE 1
CLOSE    RIN   ,                   CLOSE DATA SET
         L     R15,MDCBDCB         GET DCB ADDRESS
         CLOSE ((R15),DISP),MF=(E,J1DOPENX) CLOSE PDS
         SPACE 1
CLOSEX   DS    0H                  RETURN TO CALLER
         ROUT  ,                   SUBROUTINE EXIT
         SPACE 1
         DROP  R2                  END DD LIST ADDRESSABILITY
         SPACE 3
DHC300CX DS    0H                  TRAILER PROCESSING COMPLETED
         NI    P3DSW1,0            RESET FLAG
         LA    R2,P3DHEDSW         POINT AT MDSCB
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         BAL   R4,CLOSE            GO OPEN THE PDS
         DROP  R2                  END DD LIST ADDRESSABILITY
DHC3TREX DS    0H                  TRAILER PROCESSING COMPLETED
         EJECT ,
