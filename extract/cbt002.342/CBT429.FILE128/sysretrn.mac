         MACRO
&NAME    SYSRETRN &RC=0
.*
.*   THIS MACRO IS USED TO EXIT FROM PROGRAMS WHICH USED THE
.*   SYSENTER MACRO FOR INITIALIZATION.  THE DEFAULT RETURN CODE IS
.*   ZERO AND MAY BE CHANGED BY SPECIFYING THE RC OPERAND OF THIS
.*   MACRO.  A VALUE MAY BE PASSED BY SPECIFYING RC=NN, OR THE
.*   RETURN CODE MAY BE PASSED IN A REGISTER - RC=(R2).  IF THE
.*   RETURN CODE WAS PREVIOUSLY SET IN R15 THEN CODE RC=(15) OR
.*   RC=(R15).  THE USE OF R15 TO CONTAIN THE RETURN CODE WILL SAVE
.*   FOUR BYTES OF MACRO EXPANSION.  IF THE SYSENTER MACRO SPECIFIED
.*   REENTRABILITY THEN SYSRETRN WILL DO A FREEMAIN ON THE SAVE
.*   AREA.  THIS STATUS (REENTRABLE) WAS SAVED IN THE '&SAVMOD'
.*   GLOBAL SYMBOL.  IF EXTRA MEMORY WAS OBTAINED THROUGH SYSENTER
.*   THEN THE AMOUNT OF EXTRA STORAGE WILL BE PASSED IN THE
.*   '&SAVXTRA' GLOBAL SYSMBOL.  MULTIPLE USES OF THE SYSENTER MACRO
.*   WITH MIXED MODES (I.E. RENT AND REUS) IN A SINGLE ASSEMBLY MAY
.*   RESULT IN EXECUTION ERRORS BECAUSE OF THE '&SAVMOD' GLOBAL.
.*
         GBLC  &SAVMOD,&SAVXTRA
&NAME    DS    0H
         AIF   ('&SAVMOD' NE 'RENT').R050
         AIF   ('&RC'(1,1) NE '(').R010
         LA    2,0&RC              SAVE RC ACROSS FREEMAIN
.R010    ANOP
         LR    1,13                GET ADDR FOR FREEMAIN
         L     13,4(13)            GET PREV SAVE AREA
         AIF   ('&SAVXTRA' EQ '').R013
         FREEMAIN R,LV=72+&SAVXTRA,A=(1)
         AGO   .R016
.R013    ANOP
         FREEMAIN R,LV=72,A=(1)
.R016    ANOP
         XC    8(4,13),8(13)       CLEAR NEXT SA PTR
         AIF   ('&RC'(1,1) NE '(').R020
         LR    15,2                RESTORE RETURN CODE
         AGO   .R040
.R020    ANOP
         AIF   ('&RC' LT '4096').R030
         MNOTE 1,'RETURN CODE &RC IS > 4095, RC IGNORED'
         AGO   .R040
.R030    ANOP
         LA    15,&RC
.R040    ANOP
         L     14,12(,13)          RESTORE RETURN ADDRESS
         LM    0,12,20(13)         RESTORE REGISTERS
         MVI   12(13),X'FF'        SET RETURN INDICATOR
         BR    14                  RETURN TO CALLER
         MEXIT
.R050    ANOP
         L     13,4(13)            GET PREV SAVE AREA
         AIF   ('&RC'(1,1) NE '(').R060
         AIF   ('&RC' EQ '(15)').R040
         AIF   ('&RC' EQ '(R15)').R040
         LA    15,0&RC             SET RETURN CODE
         AGO   .R040
.R060    ANOP
         AIF   ('&RC' LT '4096').R070
         MNOTE 1,'RETURN CODE &RC IS > 4095, RC IGNORED'
         AGO   .R040
.R070    ANOP
         LA    15,&RC              SET RETURN CODE
         AGO   .R040
         MEND
