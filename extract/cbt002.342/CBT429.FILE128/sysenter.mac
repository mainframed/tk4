         MACRO
&NAME    SYSENTER &B1,&B2,&B3,&B4,&CSECT=YES,&SYMREG=NO,&RENT=NO,&XTRA=
.*
.*   THE SYSENTER MACRO IS USED TO ENTER PROGRAMS USING STANDARD
.*   LINKAGE CONVENTIONS.  UP TO FOUR BASE REGISTERS MAY BE
.*   SPECIFIED AND THESE ARE POSITIONAL OPERANDS.  THE REMAINING
.*   OPERANDS ARE KEYWORDS.  SPECIFYING RENT=YES WILL CAUSE A
.*   GETMAIN TO BE ISSUED FOR THE SAVE AREA.  SPECIFYING XTRA=NNN
.*   WILL GETMAIN NNN EXTRA BYTES FOR A WORKAREA.  SPECIFYING CSECT=NO
.*   WILL CAUSE A 'DS 0H' TO BE GENERATED IN LIEU OF A CSECT.
.*   SPECIFYING SYMREG=YES WILL CAUSE EQUATES TO BE GENERATED
.*   FOR THE GENERAL PURPOSE REGISTERS.
.*
         LCLC  &N                  CSECT NAME
         LCLC  &B                  BASE
         GBLC  &SAVMOD,&SAVXTRA    SAVE MODE AND EXTRA GETMAIN
&SAVMOD  SETC  'REUS'
&N       SETC  'CSCT&SYSNDX'
&B       SETC  '13'
         AIF   ('&NAME' EQ '').S010
&N       SETC  '&NAME'
.S010    ANOP
         AIF   ('&B1' EQ '').S015
         AIF   ('&B1' EQ '13').S015
         AIF   ('&B1' EQ 'R13').S015
&B       SETC  '&B1'
         AGO   .S020
.S015    ANOP
         AIF   ('&RENT' EQ 'NO').S020
         MNOTE 8,'R13 NOT ALLOWED AS BASE FOR REENTERANT CODE'
         MEXIT
.S020    ANOP
         SPACE 1
         AIF   ('&CSECT' EQ 'YES').S030
&N       DS    0H
         AGO   .S040
.S030    ANOP
&N       CSECT
.S040    ANOP
         SPACE 1
         USING &N,15
         SPACE 1
         B     STM&SYSNDX          BRANCH AROUND ID/SAVE AREA
         SPACE 1
         DC    AL1(24)             >>> ID AREA <<<
         DC    CL8'&N'
         DC    CL1' '
         DC    CL8'&SYSDATE'           DATE
         DC    CL1' '
         DC    CL6'&SYSTIME'           TIME
         DC    3XL1'FF'
         SPACE 1
         AIF   ('&RENT' EQ 'NO').S060
&SAVMOD  SETC  'RENT'
CONH4096 DC    H'4096'
         SPACE 1
STM&SYSNDX  STM   14,12,12(13)
         LR    &B,15
         DROP  15
         USING &N,&B
         AIF   ('&XTRA' EQ '').S043
         GETMAIN R,LV=72+&XTRA
&SAVXTRA SETC  '&XTRA'
         AGO   .S046
.S043    ANOP
         GETMAIN R,LV=72
.S046    ANOP
         LR    15,1                R15<--A(NEW SAVE AREA)
         L     1,24(13)            RESTORE PARMS
         ST    13,4(15)            CHAIN FORWARD
         ST    15,8(13)            CHAIN BACKWARD
         LR    13,15               SET NEW SAVE AREA
         AIF   ('&B2' EQ '').S050
         LR    &B2,&B
         AH    &B2,CONH4096
         AIF   ('&B3' EQ '').S050
         LR    &B3,&B2
         AH    &B3,CONH4096
         AIF   ('&B4' EQ '').S050
         LR    &B4,&B3
         AH    &B4,CONH4096
.S050    ANOP
         DROP  &B
         AIF   ('&B4' EQ '').S051
         USING &N,&B,&B2,&B3,&B4
         AGO   .S09X
.S051    ANOP
         AIF   ('&B3' EQ '').S052
         USING &N,&B,&B2,&B3
         AGO   .S09X
.S052    ANOP
         AIF   ('&B2' EQ '').S053
         USING &N,&B,&B2
         AGO   .S09X
.S053    ANOP
         USING &N,&B
         AGO   .S09X
.S060    ANOP
SAVE&SYSNDX DC    18F'0'           >>> SAVE AREA <<<
BASE&SYSNDX DC    A(SAVE&SYSNDX)
         AIF   ('&B2' EQ '').S065
         DC    A(SAVE&SYSNDX+1*4096)
         AIF   ('&B3' EQ '').S065
         DC    A(SAVE&SYSNDX+2*4096)
         AIF   ('&B4' EQ '').S065
         DC    A(SAVE&SYSNDX+3*4096)
.S065    ANOP
         SPACE 1
STM&SYSNDX  STM   14,12,12(13)     >>> SAVE AND CHAIN <<<
         LR    1,13                SAVE PREV SAVE AREA
         LA    13,SAVE&SYSNDX      GET NEW SAVE AREA
         ST    13,8(1)             CHAIN FORWARD
         ST    1,4(13)             CHAIN BACKWARD
         L     1,24(1)             RESTORE PARM
         AIF   ('&B' EQ '13').S070
         AIF   ('&B' EQ 'R13').S070
         L     &B,BASE&SYSNDX
.S070    ANOP
         AIF   ('&B2' EQ '').S080
         L     &B2,BASE&SYSNDX+4
         AIF   ('&B3' EQ '').S080
         L     &B3,BASE&SYSNDX+8
         AIF   ('&B4' EQ '').S080
         L     &B4,BASE&SYSNDX+12
.S080    ANOP
         SPACE 1
         DROP  15
         AIF   ('&B4' EQ '').S090
         USING SAVE&SYSNDX,&B,&B2,&B3,&B4
         AGO   .S09X
.S090    ANOP
         AIF   ('&B3' EQ '').S09A
         USING SAVE&SYSNDX,&B,&B2,&B3
         AGO   .S09X
.S09A    ANOP
         AIF   ('&B2' EQ '').S09B
         USING SAVE&SYSNDX,&B,&B2
         AGO   .S09X
.S09B    ANOP
         USING SAVE&SYSNDX,&B
.S09X    ANOP
         SPACE 1
         AIF   ('&SYMREG' EQ 'NO').S100
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
.S100    ANOP
         MEND
