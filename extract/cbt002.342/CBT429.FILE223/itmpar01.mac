  TITLE '**ITMPAR01** ITMS TAPE MANAGEMENT SYSTEM ** INPUT PARSER  **'
ITMPAR01 CSECT
         STM   14,12,12(13)        SAVE REGS OF CALLING PGM IN ITS AREA
         LR    R12,R15             LOAD R12 WITH BASE ADDR
         USING ITMPAR01,R12        TELL ASSEMBLER WHERE BASE POS IS
         ST    R13,SAVPAREA+4      SAVE REG 13 IN MY SAVE AREA
         LA    R13,SAVPAREA        PUT ADDR OF MY SAVE AREA INTO REG13
         SPACE
         B     INPARSR
         DC    C'**ITMPAR01** ITMS PARSER PGM'
         DC    C'  ASSEMBLED &SYSTIME &SYSDATE'
         DS    H
INPARSR  EQU   * HERE WE PARSE INPUT FOR SETUP PARMS
         LR    R9,R1               GET PARMLIST ADDR.
         USING ITMPARSL,R9         TELL ASSEMBLER WHAT WE'RE UP TO
         CLC   PRUNTYP(2),=CL2'UI' WE DOIN UTIL RUN W/SYSIN INPUT
         BNE   INPARS3             NOPE!
         LA    R7,IPARAREA         ELSE! GET @ OF RECORD INPUT
         TRT   0(80,R7),BLANKTAB   LOOK FOR INPUT RECLEN
         BZ    INPARS2             IE INVALID INPUT
         SR    R1,R7               GET LEN OF INPUT
         CH    R1,=H'69'           IE CURR. MAX. LEN.
         BH    INPARS2             IE INVALID INPUT
         STH   R1,IPARLEN          SAVE RECLEN
         CLI   IPARAREA,C'C'       ONLY SUPPORTED SYSIN OPTION
         BNE   PRSBADR             IE INVALID RUN TYPE
         XR    R4,R4               ZERO R4
         B     PARRUNC             OK! WE'RE SET UP SO LETS DO IT
         SPACE
INPARS2  XR    R4,R4               ZERO R4
         LA    R4,69               TO SHOW PROPER OFFSET
         B     PRSBADL             IE INVALID PARM INPUT
INPARS3  LA    R7,IPARAREA         GET INPUT PARM ADDR.
         XR    R4,R4               ZERO R4 (IE = 1 IN MACH. LANG)
         EX    R4,TRTCHAR          FIRST CHAR. IN MSG VALID ???
         BZ    PRSBADR             IE INVALID RUNTYPE
         MVC   PRUNTYP(1),0(R7)    MOVE RUNTYPE PARM TO WORKAREA
         SPACE
PARRUNC  EQU   * PARSER FOR RUN TYPE 'C'
         CLI   0(R7),C'C'          IE 'CREATE' RUN
         BNE   PARRUNX             NOPE ! CHECK NEXT RUNTYPE
         CLI   1(R7),C','          IE COMMA (AFTER RUNTYPE)
         BE    PRUNC1              GOOD KEEP GOING
         AH    R4,=H'1'            ELSE! SHOW OFFSET INTO PARMLIST
         B     PRSBADL             SHOW PARMLIST INVALID
PRUNC1   AH    R7,=H'2'            BUMP LOC. PTR
         AH    R4,=H'2'            BUMP OFFSET CTR
         CLI   6(R7),C','          IE COMMA (AFTER VOLID)
         BE    PRUNC2              GOOD KEEP GOING
         B     PRSBADL             SHOW PARMLIST INVALID
PRUNC2   MVC   IVOLSR(6),0(R7)     MOVE VOLID TO WORKAREA
         AH    R7,=H'7'            BUMP LOC. PTR
         AH    R4,=H'7'            BUMP OFFSET CTR
         CLI   7(R7),C','          IE COMMA (AFTER CDATE)
         BE    PRUNC3              GOOD KEEP GOING
         B     PRSBADL             SHOW PARMLIST INVALID
PRUNC3   MVC   ICDATE(7),0(R7)     MOVE CDATE TO WORKAREA
         AH    R7,=H'8'            BUMP LOC. PTR
         AH    R4,=H'8'            BUMP OFFSET CTR
         CLI   7(R7),C','          IE COMMA (AFTER EDATE)
         BE    PRUNC4              GOOD KEEP GOING
         B     PRSBADL             SHOW PARMLIST INVALID
PRUNC4   MVC   IEDATE(7),0(R7)     MOVE CDATE TO WORKAREA
         AH    R7,=H'8'            BUMP LOC. PTR
         AH    R4,=H'8'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'1'            MIN. DSN LENGTH
         BNH   PRSBADL             SHOW PARMLIST INVALID
         BCTR  R3,0                IE FOR MACH. LEN
         EX    R3,MOVTDSN          MOVE TAPE DSN TO WORKAREA
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
PARRUNX  EQU   * PARSER FOR RUN TYPE 'X'
         CLI   0(R7),C'X'          IE 'EXPIRATION' RUN
         BNE   PARRUNR             NOPE ! CHECK NEXT RUNTYPE
         CLC   1(7,R7),=C',EDATE='  IE VALID FORMAT
         BE    PRUNX1              GOOD KEEP GOING
         AH    R4,=H'1'            ELSE! SHOW OFFSET INTO PARMLIST
         B     PRSBADL             SHOW PARMLIST INVALID
PRUNX1   AH    R7,=H'8'            BUMP LOC. PTR
         AH    R4,=H'8'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'7'            IE EDATE LEN.
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVC   REDATE(7),0(R7)     OK! MOVE TO WORKAREA
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
PARRUNR  EQU   * PARSER FOR RUN TYPE 'R'
         CLI   0(R7),C'R'          IE 'REPORT' RUN
         BNE   PARRUNU             NOPE ! CHECK NEXT RUNTYPE
         CLC   1(6,R7),=C',TYPE='  IE VALID FORMAT
         BE    PRUNR1              GOOD KEEP GOING
         AH    R4,=H'1'            ELSE! SHOW OFFSET INTO PARMLIST
         B     PRSBADL             SHOW PARMLIST INVALID
PRUNR1   AH    R7,=H'7'            BUMP LOC. PTR
         AH    R4,=H'7'            BUMP OFFSET CTR
         CLC   0(5,R7),=C'AVAIL'   IE AVAIL REPORT
         BNE   PRUNR2              NOPE! KEEP LOOKING
         MVI   PSUBRUN,C'V'        OK SHOW REPORT TYPE
         B     PRSEXIT             ...AND GO DO IT!
PRUNR2   CLC   0(5,R7),=C'CREAT'   IE UNEXP TAPE REPRT
         BNE   PRUNR3              NOPE! KEEP LOOKING
         MVI   PSUBRUN,C'C'        OK SHOW REPORT TYPE
         B     PRSEXIT             ...AND GO DO IT!
PRUNR3   CLC   0(3,R7),=C'ALL'     IE LOG PRINT REPRT
         BNE   PRUNR4              NOPE! KEEP LOOKING
         MVI   PSUBRUN,C'A'        OK SHOW REPORT TYPE
         B     PRSEXIT             ...AND GO DO IT!
PRUNR4   CLC   0(3,R7),=C'LOG'     IE LOG PRINT REPRT
         BNE   PRUNR5              NOPE! KEEP LOOKING
         MVI   PSUBRUN,C'L'        OK SHOW REPORT TYPE
         B     PRSEXIT             ...AND GO DO IT!
PRUNR5   CLC   0(6,R7),=C'FOREC,'  IE FORECAST REPRT
         BNE   PRUNR6              NOPE! KEEP LOOKING
         MVI   PSUBRUN,C'F'        OK SHOW REPORT TYPE
         AH    R7,=H'6'            BUMP LOC. PTR
         AH    R4,=H'6'            BUMP OFFSET CTR
         CLC   0(6,R7),=C'FDATE='  IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'6'            BUMP LOC. PTR
         AH    R4,=H'6'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'7'            IE EDATE LEN.
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVC   RFDATE(7),0(R7)     OK! MOVE TO WORKAREA
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
PRUNR6   CLC   0(5,R7),=C'FIND,'   IE 'FIND' REPRT
         BNE   PRUNR8              NOPE! KEEP LOOKING
         MVI   PSUBRUN,C'S'        OK SHOW REPORT TYPE
         AH    R7,=H'5'            BUMP LOC. PTR
         AH    R4,=H'5'            BUMP OFFSET CTR
         CLC   0(4,R7),=C'DSN='    IE VALID FORMAT
         BNE   PRUNR7              NOPE! KEEP CHKING
         AH    R7,=H'4'            BUMP LOC. PTR
         AH    R4,=H'4'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'1'            IE MIN. DSN LEN
         BL    PRSBADL             SHOW PARMLIST INVALID
         BCTR  R3,0                IE MACH LEN
         EX    R3,MOVRDSN          MOVE DSN TO WORKAREA
         STH   R3,PERROFF          SAVE L'DSN FOR ACTUAL COMPARE
         MVI   PSUBRNA,C'D'        SHOW REPORT TYPE
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
PRUNR7   CLC   0(4,R7),=C'VOL='    IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'4'            BUMP LOC. PTR
         AH    R4,=H'4'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'6'            IE VOLSER LEN.
         BNE   PRUNR7A             ??? VOLSER RANGE
         MVC   RVOLLO(6),0(R7)     OK! MOVE TO WORKAREA
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
PRUNR7A  CH    R3,=H'15'           IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         CLI   0(R7),C'('          IE LEFT BANANA
         BNE   PRSBADL             SHOW PARMLIST INVALID
         CLI   14(R7),C')'         IE RIGHT BANANA
         BNE   PRSBADL             SHOW PARMLIST INVALID
         CLI   7(R7),C'-'          IE DASH
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVC   RVOLLO(6),1(R7)     OK! MOVE TO WORKAREA
         MVC   RVOLHI(6),8(R7)     OK! MOVE TO WORKAREA
         B     RANGECHK            GO VERIFY RANGE VALID
         SPACE
PRUNR8   CLC   0(7,R7),=C'EXEP,X=' IE EXEPTION REPRT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVI   PSUBRUN,C'X'        OK SHOW REPORT TYPE
         AH    R7,=H'7'            BUMP LOC. PTR
         AH    R4,=H'7'            BUMP OFFSET CTR
         CLC   0(3,R7),=C'ALL'     IE VALID FORMAT
         BNE   PRUNR8A             NOPE! KEEP CHKING
         MVI   RXFLAG,C'A'         OK SHOW REPORT TYPE
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
PRUNR8A  CLI   0(R7),C'U'          IE UNKNOWN TYPES
         BNE   PRUNR8B             NOPE! KEEP CHKING
         MVI   RXFLAG,C'U'         OK SHOW REPORT TYPE
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
PRUNR8B  CLI   0(R7),C'O'          IE OVERLAY TYPES
         BNE   PRSBADL             NOPE! SHOW PARMLIST INVALID
         MVI   RXFLAG,C'O'         OK SHOW REPORT TYPE
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
PARRUNU  EQU   * PARSER FOR RUN TYPE 'U'
         CLC   0(2,R7),=C'U,'      IE 'UTILITY' RUN
         BNE   PRSBADR             IE INVALID RUNTYPE
         CLC   1(6,R7),=C',TYPE='  IE VALID FORMAT
         BE    PRUNU1              GOOD KEEP GOING
         AH    R4,=H'2'            ELSE! SHOW OFFSET INTO PARMLIST
         B     PRSBADL             SHOW PARMLIST INVALID
PRUNU1   AH    R7,=H'7'            BUMP LOC. PTR
         AH    R4,=H'7'            BUMP OFFSET CTR
         CLC   0(5,R7),=C'XPDAT'   IE EXP DATE ALTER
         BNE   PRUNU2              NOPE! KEEP LOOKING
         AH    R7,=H'5'            BUMP LOC. PTR
         AH    R4,=H'5'            BUMP OFFSET CTR
         CLC   0(5,R7),=C',VOL='   IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'5'            BUMP LOC. PTR
         AH    R4,=H'5'            BUMP OFFSET CTR
         CLC   6(7,R7),=C',EDATE=' IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVC   RVOLLO(6),0(R7)     MOVE VOLSER TO WORKAREA
         AH    R7,=H'13'           BUMP LOC. PTR
         AH    R4,=H'13'           BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'7'            IE DATE LEN.
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVC   REDATE(7),0(R7)     OK! MOVE TO WORKAREA
         MVI   PSUBRUN,C'X'        SHOW UTILITY RUN
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
PRUNU2   CLC   0(9,R7),=C'FLAG,VOL='   IE FLAG ALTER RUN
         BNE   PRUNU3              NOPE! KEEP LOOKING
         AH    R7,=H'9'            BUMP LOC. PTR
         AH    R4,=H'9'            BUMP OFFSET CTR
         CLI   6(R7),C','          IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVC   RVOLLO(6),0(R7)     ELSE! MOVE VOLSER TO PARMLIST
         AH    R7,=H'7'            BUMP LOC. PTR
         AH    R4,=H'7'            BUMP OFFSET CTR
         CLC   0(5,R7),=C'FLAG='   IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'5'            BUMP LOC. PTR
         AH    R4,=H'5'            BUMP OFFSET CTR
         CLI   0(R7),C'S'          IE VALID FORMAT
         BNE   PRUNU2B             GO CHK OTHER ALLOW. VALUE
         CLI   7(R7),C'C'          IE VALID FORMAT
         BE    PRUNU2A             SO FAR SO GOOD
         CLI   7(R7),C'E'          IE VALID FORMAT
         BNE   PRSBADL             THATS ALL WE SUPPORT NOW
PRUNU2A  MVC   RUFLAG(1),0(R7)     SAVE FLAG PARM
         MVC   RUFVAL(1),7(R7)     SAVE FLAG PARM
         B     PRUNU2C             THATS ALL WE SUPPORT NOW
PRUNU2B  CLI   0(R7),C'X'          IE OTHER VALID VALUE
         BNE   PRSBADL             THATS ALL WE SUPPORT NOW
         CLI   7(R7),C'B'          IE VALID FORMAT
         BNE   PRUNU2U             MAYBE A 'U'
         MVC   RUFLAG(1),0(R7)     SAVE FLAG PARM
         MVI   RUFVAL,X'40'        IE, 'B' = BLANK
         B     PRUNU2C             THATS ALL FOR NOW FOLKS
PRUNU2U  CLI   7(R7),C'U'          IE VALID FORMAT
         BE    PRUNU2A             SO FAR SO GOOD
         CLI   7(R7),C'O'          IE VALID FORMAT
         BNE   PRSBADL             THATS ALL WE SUPPORT NOW
         B     PRUNU2A             SO FAR SO GOOD
PRUNU2C  CLC   1(6,R7),=C',NVAL='  IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R4,=H'8'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         LTR   R3,R3               IE WE END OF PARMLIST
         BNZ   PRSBADL             SHOW PARMLIST INVALID
         MVI   PSUBRUN,C'F'        ELSE! SHOW FLAG TYPE
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
PRUNU3   CLC   0(5,R7),=C'ADDE,'   IE ADD SCRATCH RUN
         BNE   PRUNU4              NOPE! KEEP CHKING
         AH    R7,=H'5'            BUMP LOC. PTR
         AH    R4,=H'5'            BUMP OFFSET CTR
         CLC   0(4,R7),=C'VOL='    IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'4'            BUMP LOC. PTR
         AH    R4,=H'4'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'6'            IE VOLSER LEN.
         BNE   PRUNU3A             ??? VOLSER RANGE
         MVI   PSUBRUN,C'S'        SHOW SCRATCH ADDITION
         MVI   PSUBRNA,C'S'        ...OF SINGLE SCRATCH
         MVC   RVOLLO(6),0(R7)     OK! MOVE TO WORKAREA
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
PRUNU3A  CH    R3,=H'15'           IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         CLI   0(R7),C'('          IE LEFT BANANA
         BNE   PRSBADL             SHOW PARMLIST INVALID
         CLI   14(R7),C')'         IE RIGHT BANANA
         BNE   PRSBADL             SHOW PARMLIST INVALID
         CLI   7(R7),C'-'          IE DASH
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVC   RVOLLO(6),1(R7)     OK! MOVE TO WORKAREA
         MVC   RVOLHI(6),8(R7)     OK! MOVE TO WORKAREA
         MVI   PSUBRUN,C'S'        ...OF VOL-RANGE SCRATCH
         MVI   PSUBRNA,C'R'        ...OF VOL-RANGE SCRATCH
         B     RANGECHK            GO VERIFY RANGE VALID
         SPACE
PRUNU4   CLC   0(5,R7),=C'ADDC,'   IE ADD NON-SCRATCH RUN
         BNE   PRUNU5              NOPE! KEEP CHKING
         AH    R7,=H'5'            BUMP LOC. PTR
         AH    R4,=H'5'            BUMP OFFSET CTR
         CLC   0(4,R7),=C'VOL='    IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'4'            BUMP LOC. PTR
         AH    R4,=H'4'            BUMP OFFSET CTR
         CLC   0(5,R7),=C'SYSIN'   IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVI   PSUBRUN,C'I'        ...OF DATASET INPUT
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
PRUNU5   CLC   0(4,R7),=C'DEL,'    IE DELETE VOL REC RUN
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'4'            BUMP LOC. PTR
         AH    R4,=H'4'            BUMP OFFSET CTR
         CLC   0(4,R7),=C'VOL='    IE VALID FORMAT
         BNE   PRSBADL             SHOW PARMLIST INVALID
         AH    R7,=H'4'            BUMP LOC. PTR
         AH    R4,=H'4'            BUMP OFFSET CTR
         LH    R3,IPARLEN          GET ORIG. INPUT LEN
         SR    R3,R4               LESS CURRENT OFFSET
         CH    R3,=H'6'            IE VOLSER LEN.
         BNE   PRSBADL             SHOW PARMLIST INVALID
         MVI   PSUBRUN,C'D'        SHOW DELETE REC RUN
         MVI   PSUBRNA,C'S'        ...OF SINGLE VOL REC
         MVC   RVOLLO(6),0(R7)     OK! MOVE TO WORKAREA
         B     PRSEXIT             GOOD! NOW FOR THE REAL WORK
         SPACE
RANGECHK EQU   * FOR VOL RANGES MUST ALL NUMERIC & (VOLA < VOLB)
         CLC   RVOLHI,RVOLLO       COMPARE VOLB TO VOLA
         BH    RANGNUM             SO FAR SO GOOD
         XR    R15,R15             ELSE! SHOW ERROR
         LA    R15,12              SHOW RET CODE OF 12
         B     RETURN              ....LET ITMS000A HANDLE IT
RANGNUM  TRT   RVOLLO(6),NUMRTAB   CHECK IF ALL NUMERICS
         BNZ   RANGERR             OH OH OH
         TRT   RVOLHI(6),NUMRTAB   CHECK IF ALL NUMERICS
         BNZ   RANGERR             OH OH OH
         B     PRSEXIT             GOOD! GO ZERO RET CODE
RANGERR  XR    R15,R15             CLR R15
         LA    R15,16              SHOW RET CODE OF 12
         B     RETURN              ....LET ITMS000A HANDLE IT
PRSBADR  EQU   * IE RUNTYPE NOT VALID
         XR    R15,R15             CLR R15
         LA    R15,4               SHOW RET CODE OF 4
         B     RETURN              ....LET ITMS000A HANDLE IT
PRSBADL  EQU   * IE PARMLIST MISSING PARMS
         ST    R4,PERROFF          SHOW OFFSET INTO PLIST WHERE BAD
         XR    R15,R15             CLR R15
         LA    R15,8               SHOW RET CODE OF 8
         B     RETURN              ....LET ITMS000A HANDLE IT
PRSEXIT  EQU   * IE EVERYTHING'S COOL
         XR    R15,R15             CLR R15 TO SHOW 0 RET CODE
         B     RETURN              ....AND GO HOME!
         SPACE 2
TRTCHAR  TRT   0(0,R7),CHARTAB     LOOK FOR VALID RUNTYPE
MOVTDSN  MVC   ICTDSN(0),0(R7)     MOVE NEW TAPE DSN TO WORKAREA
MOVRDSN  MVC   RTDSN(0),0(R7)      MOVE NEW TAPE DSN TO WORKAREA
         SPACE 2
RETURN   L     R13,SAVPAREA+4      RECOVER ADDR OF SAVE AREA
         L     R14,12(R13)         RESTORE REG 14
* R15 IS NOT RESTORED BECAUSE THATS WHERE WE'LL CARRY OUR RET. CODE
         LM    R0,R12,20(R13)      RESTORE REST OF REGS
         BR    R14                 BRANCH BACK TO CALLING PGM
  TITLE '**ITMPAR01** ITMS TAPE MANAGEMENT SYSTEM *** CONSTANTS  ***'
SAVPAREA DS    18F                 REG SAVE AREA
BLANKTAB DC    XL256'00'
         ORG   BLANKTAB+X'40'      IE, BLANKETY BLANK
         DC    X'FF'
CHARTAB  DC    XL256'00'
         ORG   CHARTAB+C'C'        RUNTYPE CREATE
         DC    X'FF'
         ORG   CHARTAB+C'R'        RUNTYPE REPORT
         DC    X'FF'
         ORG   CHARTAB+C'U'        RUNTYPE UTILITY
         DC    X'FF'
         ORG   CHARTAB+C'X'        RUNTYPE EXPIRATION
         DC    X'FF'
         ORG
NUMRTAB  DC    256X'FF'
         ORG   NUMRTAB+C'0'        IE ZONED ZERO
         DC    10X'00'
         ORG
ITMPARSL DSECT           PARMLIST AREA FROM/TO ITMS000A/ITMPAR01
IPARLEN  DS    H                   LEN' OF INPUT PARMS
IPARAREA DS    CL80                INPUT PARMS SAVEAREA
PRUNTYP  DS    C                   RUNTYPE FLAG
PSUBRUN  DS    C                   SECOND RUNTYPE FLAG
PSUBRNA  DS    C                   THIRD RUNTYPE FLAG
* FOLL. LAYOUT FOR CREATE TYPE RUN IS EQUAL TO ITMCFILE REC. LAYOUT
PCTYP    DS    0C          }  FOLL. = CREATE TYPE INPUT
IVOLSR   DS    CL6                 INPUT VOLSER
IEDATE   DS    CL7                 INPUT EXPIR. DATE YYYYDDD
ICTDSN   DS    CL44                INPUT TAPE DSN (44 MAX.)
ICDATE   DS    CL7                 TAPE CREATION DATE
ICFLG1   DS    C                   TAPE STATUS FLAG1
ICFLG2   DS    C                   TAPE STATUS FLAG2
ICFLG3   DS    C                   TAPE STATUS FLAG3
ICFLG4   DS    C                   TAPE STATUS FLAG4
ICFILL   DS    CL12                 'FILLER' FOR FUTURE USE
         ORG   PCTYP               REDEFINE
* REDEFINE LAYOUT FOR NON-'CREATE' TYPE RUN
PXRUTYP  DS    0C                  NON-CREATE TYPE PARM INPUT
PTYPE    DS    CL5                 INPUT TYPE= PARM
RVOLLO   DS    CL6                 INPUT VOL=  (LO FOR RANGE)
RVOLHI   DS    CL6                 (VOLLO-VOLHI) HI FOR RANGE
REDATE   DS    CL7                 RUN EXPIR. DATE
RFDATE   DS    CL7                 RUN FORECAST DATE
RTDSN    DS    CL44                INPUT TAPE DSN  (FOR 'FIND' OPTION)
RXFLAG   DS    C                   INPUT EXCEPTION REP. FLAG
RUFLAG   DS    CL2                 UTIL. ALTER FLAG NUMBER
RUFVAL   DS    C                   UTIL. ALTER FLAG VALUE
RFILLL   DS    C                   RESERVED
         SPACE
PERROFF  DS    F                   OFFSET INTO PLIST W/ERROR
         SPACE 2                   RESET
ITMPAR01 CSECT                     RESUME ORIG. CSECT
         LTORG
  TITLE '**ITMPAR01** ITMS TAPE MANAGEMENT SYSTEM ** REG EQUATES **'
*        REGISTER EQUATES:
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                  BAL REGISTER
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   ITMPAR01
