*        PRINT GEN
* *------------------------------------------------------------------ *
*
*  AUTHOR - JOSEPH FORTUNATO
*
* *------------------------------------------------------------------ *
*  THE PURPOSE OF THIS PROGRAM IS TO LOAD A SMALL DATASET CALLED
*  SYS1.LEVEL, THAT IS CATALOGED AS VOL(******) ON EACH MASTER
*  CATALOG, AND WHICH RESIDES ON EACH "CLONED" MVS RESIDENCE
*  PACK IN THE COMPLEX.  THIS CLIST MAY BE EXECUTED, TO DISPLAY
*  THE LOCALLY MAINTAINED MAINTENANCE LEVEL OF THE RUNNING RES
*  PACK ON "THIS LPAR".
*
*  THIS PROGRAM IS EXECUTED WITH A PARM OF 8 CHARACTERS.  THE
*  FIRST TWO CHARACTERS ARE THE MAINTENANCE LEVEL, AND THE NEXT
*  SIX CHARACTERS CONSTITUTE THE NAME OF THE RESIDENCE VOLUME.
*
*  JCL TO RUN THIS PROGRAM IS GENERATED BY THE "M" OPTION OF THE
*  MVS CROSS SYSTEM DIALOGS.  SAMPLE JCL IS AS FOLLOWS:
*
*   //STEP1    EXEC PGM=ASMLVL,PARM='70MRS001'
*   //*         MAKE SURE THE SYSTEM LEVEL IS 2 DIGITS. E.G. "04"
*   //*         SO THAT IF THE LEVEL IS LESS THAT 10, YOU CODE:
*   //*         PARM='04MRS001' , FOR EXAMPLE.
*   //STEPLIB  DD DSN=JOEF.USER.LOAD,DISP=SHR
*   //SYSLST   DD DSN=SYS1.LEVEL,UNIT=3390,DISP=SHR,VOL=SER=MRS001
*   //SYSUDUMP DD SYSOUT=*
*
*  OUTPUT FROM THE PROGRAM IS THE FOLLOWING LINE IN THE CLIST NAMED
*  'SYS1.LEVEL' REPEATED 10 TIMES.
*
*   SE '*** WE ARE IPLED FROM LEVEL 70 VOLUME MRS001 **',OPERATOR(1)
*
*  THE FOLLOWING STARTED PROC WILL DISPLAY THE CURRENT SYSTEM
*  LEVEL ON THE OPERATOR'S CONSOLE, BY EXECUTING THE CLIST.
*
*   //LEV     EXEC PGM=IKJEFT01,DPRTY=(15,15)
*   //SYSPRINT     DD SYSOUT=*
*   //SYSTSPRT     DD SYSOUT=*
*   //SYSTSIN      DD DISP=SHR,DSN=SYS1.PROCLIB(LEVP)
*
*  WHERE MEMBER LEVP HAS ONE CARD, AS FOLLOWS:
*
*   EX 'SYS1.LEVEL'
*
*
* *------------------------------------------------------------------ *
*
         YREGS
JOEF5    CSECT
         SAVE  (14,12),,*         SAVE ENTRY REGISTERS
         LR    R12,R15            SAVE ENTRY POINT
         USING JOEF5,R12          GET ADDRESSABILITY TO CSECT
         LA    R4,SAVE            POINT TO PGM SAVE AREA
         ST    R13,4(,R4)         SAVE POINTER TO CALLERS SAVE AREA
         ST    R4,8(,R13)         SAVE POINTER TO PGM SAVE AREA
         LR    R13,R4             POINT TO PGM SAVE AREA
         SR    R5,R5              CLEAR
         SR    R3,R3                 WORK
         SR    R2,R2                     REGISTERS
         L     R2,0(R1)           GET
         LA    R3,2(R2)               PARM
         OPEN  (PRINTER,OUTPUT)
         MVC   LVL(2),0(R3)       MOVE LEVEL INTO
         MVC   OLVL,LVL           ITS PLACE.
         MVC   VOL(6),2(R3)       MOVE RESIDENCE VOLUME NAME
         MVC   OVOL,VOL           INTO ITS PLACE.
PRT      PUT   PRINTER,OUTJF      WRITE MESSAGE
         LA    R5,1(R5)           INCREMENT COUNTER
         C     R5,=F'10'          WRITE MSG 10 TIMES TO SYS1.LEVEL
         BE    FINISH             END? GET OUT.
         B     PRT                LOOP
FINISH   CLOSE (PRINTER,LEAVE)
RETURN   L     R13,SAVE+4         GET POINTER TO CALLERS SAVE AREA
         RETURN (14,12),RC=(15)   GO RETURN TO CALLER
SAVE     DS    18F
OUTJF    DS    0CL133
         DC    CL1' '
         DC    CL3'SE '
         DC    XL1'7D'
         DC    CL28'*** WE ARE IPLED FROM LEVEL '
OLVL     DS    CL2
         DC    CL8' VOLUME '
OVOL     DS    CL6
         DC    CL3' ***'
         DC    XL1'7D'
         DC    CL12',OPERATOR(1)'
         DC    CL69' '
LVL      DS    CL2
VOL      DS    CL6
PRINTER  DCB   DDNAME=SYSLST,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,    *
               BLKSIZE=27930
         LTORG
         END
