         PRINT NOGEN
DFHEISTG DSECT
         DFHCSAD
         COPY  DFHTCADS
         COPY  DFHTCTTE
         COPY  DFHTIOA
TERMTIOA DS    0C
         DS    XL103
TERMIDO  DS    CL4
         DS    XL40
INPUTSO  DS    CL6
         DS    XL25
NETNAMEO DS    CL8
         DS    XL40
OUTPUTSO DS    CL6
         DS    XL22
TYPEO    DS    CL11
         DS    XL40
ERRORSO  DS    CL6
         DS    XL29
ACCMETHO DS    CL4
         DS    XL30
PRIORTYO DS    CL3
         DS    XL57
XTENAO   DS    CL8
         DS    XL59
TIOADAO  DS    CL8
         DS    XL29
ATRANSO  DS    CL4
         DS    XL37
TIOASO   DS    CL9
         DS    XL29
NTRANO   DS    CL4
         DS    XL30
OPRO     DS    CL3
         DS    XL30
IOROO    DS    CL3
         DS    XL30
TOVTAMO  DS    CL3
         DS    XL25
USERIDO  DS    CL8
*
         SPACE 3
TERMPROG DFHEIENT CODEREG=(3,2)
OPENI    EQU   *
         L     R4,TCAFCAAA
* This program will scan the tcttes and gather stats about
* terminals connected through autoinstall.
*
*
* Get a storage area to work with.
*
         EXEC CICS GETMAIN SET(STOR) LENGTH(DATALENG) INITIMG(BLANK)
         USING RENTAREA,STOR
         ST    R4,TCTSAVE
         CLI   78(R4),X'2B'
         BNE   LOAD384
         ZAP   SCRNSIZE,=P'688'
         B     LOAD0
LOAD384  EQU   *
         ZAP   SCRNSIZE,=P'384'
LOAD0    EQU   *
         ZAP   TERMCT,=P'0'
         LA    SCRNPTR,MSGAREA+5
         CLI   EIBAID,DFHPF2
         BE    DOTERMA
         CLI   EIBAID,DFHCLEAR
         BE    ERASE
         CLI   EIBAID,DFHPF1
         BNE   NOTPF1
         MVI   OPTIONS,C'O'
         B     NOTPF
NOTPF1   CLI   EIBAID,DFHPF4
         BNE   NOTPF4
         MVI   OPTIONS,C'4'
         B     NOTPF
NOTPF4   B     NOTPF
DOTERMA  EQU   *
         L     R6,8(R4)
         LA    R6,15(R6)
         CLI   0(R6),X'C0'
         BNH   MOVCODE
         B     FINDCSA
NOTPF    EQU   *
*
* Read in the teminal id if given; else scan all
*
         EXEC CICS RECEIVE SET(SCREEN) LENGTH(SCLEN)
         USING MAP,SCREEN
*
* Determine if the user positioned the cursor somewhere and
* see if he/she wants a detail for a specific terminal.
*
MOVCODE  EQU  *
         MVC  TRANTYPE(1),TRANCODE
         SR   WORK,WORK
         LH   WORK,EIBCPOSN
         CH   WORK,=H'5'
         BL   DISTERM         go display the terminals!
*
* Display information about the specific terminal.
*
         AR   SCREEN,WORK
         S    SCREEN,=F'2'
SRCHBLAN CLI  0(SCREEN),C' ' at a blank?
         BE   FNDBLANK
         S    SCREEN,=F'1'   go to the previous character
         B    SRCHBLAN
FNDBLANK A    SCREEN,=F'1'   go to the first character
FINDCSA  EQU  *
*
         MVC  TERMID(4),0(SCREEN)
         CLI  TERMID+3,X'C0'
         BNH  DISTERM1
         EXEC CICS INQUIRE TERMINAL(TERMID) USERID(USERID)
         EXEC CICS ADDRESS CSA(WORK)
         ST   R13,R13SAVE
         LR   R13,WORK
         USING DFHCSADS,R13
         MVC  TCATPTA(4),0(SCREEN)
         DFHTC CTYPE=LOCATE,ERROR=TCERROR,INVID=TCERROR,               *
               TERM=ID,NORESP=CONT1
         L     R13,R13SAVE
         B     ENDIT
TCERROR  EQU   *
         L     R13,R13SAVE
         B     DISTERM
CONT1    EQU   *
         MVC   TCTSAVE2,TCATPTA
         LA    WORK,ENDMAP-TERMMAP+100
         STH   WORK,TCASCNB
         DFHSC TYPE=GETMAIN,INITIMG=00,CLASS=TERMINAL
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         MVC   TERMTIOA(255),TERMMAP
         MVC   TERMTIOA+255(255),TERMMAP+255
         MVC   TERMTIOA+510(255),TERMMAP+510
         L     TCTTEAR,TCTSAVE2
         MVC   TERMIDO(4),TCTTETI
         MVC   FULLWRD,TCTTENI
         L     R4,FULLWRD
         CVD   R4,DWRD
         UNPK  INPUTSO,DWRD+4(4)
         OI    INPUTSO+5,X'F0'
         L     R4,TCTENIBA
         MVC   NETNAMEO,12(R4)
         MVC   FULLWRD,TCTTENO
         L     R4,FULLWRD
         CVD   R4,DWRD
         UNPK  OUTPUTSO,DWRD+4(4)
         OI    OUTPUTSO+5,X'F0'
         MVC   HALFWRD,TCTTENO
         LH    R4,HALFWRD
         CVD   R4,DWRD
         UNPK  ERRORSO,DWRD+4(4)
         OI    ERRORSO+5,X'F0'
         SPACE
         LA    R4,TYPETERM
TYPECHK  CLC   0(1,R4),TCTTETT
         BE    TYPEFND
         CLI   0(R4),X'FF'
         BE    TYPEFND
         LA    R4,12(R4)
         B     TYPECHK
TYPEFND  MVC   TYPEO(11),1(R4)
*       TYPE OF TERMINAL STUFF
         CLI   TCTIVTAM,TCTEVTAM
         BE    CONTINU
         MVC   ACCMETHO,=C'????'
CONTINU  EQU   *
         L     R4,TCTTECA
         L     R4,0(R4)
         CLI   177(R4),X'C0'
         BNH   NEXTRAN
         MVC   ATRANSO,176(R4)
NEXTRAN  EQU   *
         CLI   TCTTETC,X'C0'
         BNH   DOPRIOR
         MVC   NTRANO,TCTTETC
DOPRIOR  EQU   *
         MVC   HALFWRD,=H'0'
         MVC   HALFWRD+1,TCTTETP
         LH    R4,HALFWRD
         CVD   R4,DWRD
         UNPK  PRIORTYO,DWRD+6(2)
         OI    PRIORTYO+2,X'F0'
         MVC   ADDR,TCTTECIA
         UNPK  DWRD2,ADDR
         TR    DWRD2,TRANSTBL
         MVC   XTENAO,DWRD2
         MVC   ADDR,TCTTEDA
         UNPK  DWRD2,ADDR
         TR    DWRD2,TRANSTBL
         MVC   TIOADAO,DWRD2
         MVC   OPRO,TCTTEOI
         MVC   USERIDO,USERID          move RACF userid
         CLI   TCTTETS,X'01'
         BNE   NEXTONE
         MVC   IOROO,=C' NO'
NEXTONE  EQU   *
         L     TCTTEAR,TCTSAVE
         ST    TIOABAR,TCTTEDA
         MVC   TIOATDL,=H'753'
         DFHTC TYPE=(WAIT,WRITE,ERASE)
LOADR13  EQU   *
         L     R13,R13SAVE
         USING DFHEISTG,R13
         B     GOODEND
*
TNOTFND  EQU  *
         B     ENDIT
*
*
* Display the list of terminals signed on to the system
*
DISTERM1 EQU  *
         L    R13,R13SAVE
DISTERM  EQU  *
*
* Put the pointer to the start of the tcttes.
*
         EXEC CICS INQUIRE TERMINAL START
*
* Terminal inquire loop.
*
GETTERM  EQU   *
         EXEC CICS INQUIRE TERMINAL(TERMID) NEXT OPERID(OPER)          *
             USERID(USERID) DEVICE(TYPE) RESP(RESPFLD) ACQSTATUS(ACQ)
* did everything work okay?  If not, error out.
         CLC   RESPFLD,DFHRESP(NORMAL)
         BNE   ERROR
*
*        Check device type...maybe not
*
*        CLI   TYPE+3,X'91'
*        BL    GETTERM
*        CLI   TYPE+3,X'9C'
*        BH    GETTERM
         CLC   ACQ,DFHVALUE(ACQUIRED)
         BNE   CHK4ACQ2
         B     ADD1
CHK4ACQ2 EQU   *
         CLC   ACQ,DFHVALUE(ACQUIRING)
         BNE   GETTERM
ADD1     EQU   *
         AP    TERMCT,=P'1'
         CLI   OPTIONS,C'O'
         BE    DOOPER
         CLI   OPTIONS,C'4'
         BE    DOUSERID
*
DOTERM   CP    TERMCT,SCRNSIZE
*        EXEC CICS START TRANSID('SINT') TERMID(TERMID)
         BNL   GETTERM
         MVC   0(4,SCRNPTR),TERMID
         MVI   4(SCRNPTR),C' '
         LA    SCRNPTR,5(SCRNPTR)
         B     GETTERM
DOOPER   CP    TERMCT,SCRNSIZE
         BNL   GETTERM
         MVC   0(4,SCRNPTR),OPER
         LA    SCRNPTR,5(SCRNPTR)
         B     GETTERM
DOUSERID CP    TERMCT,SCRNSIZE
         BNL   GETTERM
         MVC   0(4,SCRNPTR),TERMID
         LA    SCRNPTR,5(SCRNPTR)
         MVC   0(8,SCRNPTR),USERID
         LA    SCRNPTR,11(SCRNPTR)
         B     GETTERM
*
* Delete the CICS temporary storage
*
ERROR    EQU   *
         EXEC CICS INQUIRE TERMINAL END
*
* Set up output screen.
         LA    SCRNPTR,MSGAREA
         UNPK  0(3,SCRNPTR),TERMCT+2(2)
         OI    2(SCRNPTR),X'F0'
         MVI   4(SCRNPTR),C' '
*
SENDMSG  EQU  *
         CP   SCRNSIZE,=P'384'
         BNE  SENDBIG
         EXEC CICS SEND FROM(MSGAREA) LENGTH(1920) ERASE
         B    GOODEND
SENDBIG  EQU   *
         EXEC CICS SEND FROM(MSGAREA) LENGTH(3440) ERASE
GOODEND  EQU   *
         EXEC CICS RETURN TRANSID(EIBTRNID)
         SPACE
ERASE    EQU  *
         EXEC CICS ISSUE ERASEAUP
ENDIT    EQU  *
         EXEC CICS RETURN
*
* Static declarations
R1       EQU   1
R4       EQU   4
R6       EQU   6
STOR     EQU   5
SCREEN   EQU   6
SCRNPTR  EQU   7
WORK     EQU   8
TCTTEAR  EQU   9
TIOABAR  EQU   10
R12      EQU   12
R13      EQU   13
BLANK    DC    X'00'
DATALENG DC    AL2(ENDDS)
         ORG   *-240
TRANSTBL DS    0XL256
         DS    XL240
         DC    C'0123456789ABCDEF'
         LTORG
         COPY  DFHAID
MAPLEN   EQU   (ENDMAP-TERMMAP)
TERMMAP  DS    0F
         DC    X'1140D41DF8'
         DC    C'TERMINAL CONTROL TABLE ENTRY DISPLAY'
         DC    X'11C2601DF8'
         DC    C'TERMINAL:'
         DC    X'11C3C31DF8'
         DC    C'TRANSMISSIONS:'
         DC    X'11C3F31DF0'
         DC    C'ID ...................'
         DC    X'1DD1'
         DC    C'____'
         DC    X'11C4D61DF0'
         DC    C'INPUTS ..........................'
         DC    X'1DF8'
         DC    C'______'
         DC    X'11C5C31DF0'
         DC    C'NETWORK NAME .....'
         DC    X'1DF8'
         DC    C'________'
         DC    X'11C5E61DF0'
         DC    C'OUTPUTS .........................'
         DC    X'1DF8'
         DC    C'______'
         DC    X'11C6D31DF0'
         DC    C'TYPE ..........'
         DC    X'1DF8'
         DC    C'___________'
         DC    X'11C6F61DF0'
         DC    C'ERRORS ..........................'
         DC    X'1DF8'
         DC    C'______'
         DC    X'11C7E31DF0'
         DC    C'ACCESS METHOD ........'
         DC    X'1DF8'
         DC    C'____'
         DC    X'11C8F31DF0'
         DC    C'PRIORITY ..............'
         DC    X'1DF8'
         DC    C'___'
         DC    X'11C9D31DF8'
         DC    C'STORAGE AREAS:'
         DC    X'114AE61DF0'
         DC    C'USER EXTENSION AREA ...........'
         DC    X'1DF8'
         DC    C'________'
         DC    X'114B501DF8'
         DC    C'TERMINAL STATUS:'
         DC    X'114BF61DF0'
         DC    C'CURRENT TIOA (TCTTEDA) ........'
         DC    X'1DF8'
         DC    C'________'
         DC    X'114CE31DF0'
         DC    C'ACTIVE TRANSACTION ...'
         DC    X'1DF8'
         DC    C'____'
         DC    X'114DC61DF0'
         DC    C'TIOAS (COUNT/LENGTH) .........'
         DC    X'1DF8'
         DC    C'_________'
         DC    X'114DF31DF0'
         DC    C'NEXT TRANSACTION ID ..'
         DC    X'1DF8'
         DC    C'____'
         DC    X'114FC31DF0'
         DC    C'SIGNED ON .............'
         DC    X'1DF8'
         DC    C'___'
         DC    X'1150D31DF0'
         DC    C'TERMINAL IN SERVICE ...'
         DC    X'1DF8'
         DC    C'___'
         DC    X'11D1E31DF0'
         DC    C'CONNECTED TO VTAM .....'
         DC    X'1DF8'
         DC    C'___'
*
         DC    X'11D2F31DF0'
         DC    C'RACF USERID ......'
         DC    X'1DF8'
         DC    C'________'
ENDMAP   EQU   *
         DC    100CL1' '
*
TYPETERM EQU  *
         DC   X'A0',CL11'BISYNCH -'
         DC   X'A1',CL11'SYSTEM/3 '
         DC   X'A4',CL11'SYSTEM/37'
         DC   X'A5',CL11'RESERVED '
         DC   X'A6',CL11'PROGRAMMA'
         DC   X'A6',CL11'SYSTEM/7 '
         DC   X'BA',CL11'3650 ATTA'
         DC   X'BB',CL11'3650 USER'
         DC   X'BD',CL11'CONTENTIO'
         DC   X'BE',CL11'INTERACTI'
         DC   X'BF',CL11'BATCH LOG'
         DC   X'B0',CL11'SDLC DEVI'
         DC   X'B1',CL11'3601     '
         DC   X'B2',CL11'3614     '
         DC   X'B4',CL11'3790     '
         DC   X'B5',CL11'3790 USER'
         DC   X'B6',CL11'3790 SCS '
         DC   X'B8',CL11'3650 PIPE'
         DC   X'B9',CL11'3653 HOST'
         DC   X'C0',CL11'LUTYPE 6 '
         DC   X'C1',CL11'LUTYPE 4 '
         DC   X'DF',CL11'1750/3750'
         DC   X'D0',CL11'RESERVED '
         DC   X'D1',CL11'ISC MM CO'
         DC   X'D2',CL11'LUC MODE '
         DC   X'D3',CL11'LUC SESSI'
         DC   X'E2',CL11'SKELETON '
         DC   X'E4',CL11'RESERVED '
         DC   X'01',CL11'7770     '
         DC   X'02',CL11'SYSTEM 7 '
         DC   X'08',CL11'CONSOLE  '
         DC   X'12',CL11'SEQUENTIA'
         DC   X'14',CL11'MAGNETIC '
         DC   X'18',CL11'CARD READ'
         DC   X'2A',CL11'2741 CORR'
         DC   X'2B',CL11'2741 EBCD'
         DC   X'20',CL11'HARD COPY'
         DC   X'21',CL11'MODEL 33/'
         DC   X'22',CL11'TELETYPEW'
         DC   X'24',CL11'1050     '
         DC   X'28',CL11'2740     '
         DC   X'4A',CL11'1053     '
         DC   X'4C',CL11'2265     '
         DC   X'40',CL11'VIDEO TERM'
         DC   X'41',CL11'2260 LOCAL'
         DC   X'48',CL11'2260 REMOTE'
         DC   X'50',CL11'TCAM     '
         DC   X'8A',CL11'3600 BISY'
         DC   X'80',CL11'BI-SYNCHR'
         DC   X'82',CL11'2770     '
         DC   X'84',CL11'2780     '
         DC   X'85',CL11'3780     '
         DC   X'86',CL11'2980     '
         DC   X'88',CL11'3735     '
         DC   X'89',CL11'3740     '
         DC   X'9B',CL11'3284 LOCAL'
         DC   X'9C',CL11'3286 LOCAL'
         DC   X'91',CL11'3277 REMOTE'
         DC   X'92',CL11'3275 REMOTE'
         DC   X'93',CL11'3284 REMOTE'
         DC   X'94',CL11'3286 REMOTE'
         DC   X'99',CL11'3277 LOCAL'
         DC   X'FF',CL11'?unknown?'
         EJECT
MAP      DSECT
TRAN     DS    CL4
TRANCODE DS    CL1
*
RENTAREA DSECT
OPTIONS  DS    CL1
SCRNSIZE DS    PL4
ACQ      DS    CL4
TYPE     DS    CL4
TERMID   DS    CL4
RESPFLD  DS    CL4
RESP2FLD DS    CL4
OPER     DS    CL4
USERID   DS    CL8
OUTREC   DS    0CL133
CTLCHAR  DS    CL1
OUTDATA  DS    CL132
HALFWRD  DS    H
FULLWRD  DS    F
ADDR     DS    XL5
DWRD     DS    D
DWRD2    DS    XL9
SCLEN    DS    F
R13SAVE  DS    F
TCTSAVE  DS    F
TCTSAVE2 DS    F
TERMCT   DS    PL4'0'
TRANTYPE DS    CL1
MSGAREA  DS    CL3440
ENDDS    EQU   *-RENTAREA
         END
