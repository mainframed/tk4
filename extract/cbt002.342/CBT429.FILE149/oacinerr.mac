         MACRO
&NAME    OACINERR &ADR
         LCLC  &T,&R,&N
.*
.*   INNER MACRO TO GENERATE BRANCH BASED ON CONDITION CODE
.*    (USED FOR ERROR KEYWORD ON OACCALL MACRO)
.*   FORMAT OF SUB-OPERANDS IS:
.*       OP1, OP2, OP3
.*     OP1 - TARGET OF BRANCH (IF IN REGISTER NOTATION WILL GENERATE
.*           A BRANCH REGISTER)
.*     OP2 - CONDITION CODE FOR THE BRANCH.  THE DEFAULT IS NZ.
.*           Z IS ALSO ALLOWED.
.*     OP3 - REGISTER TO LTR R15 INTO (IF THIS OPERAND IS OMITTED
.*       THEN THE CONDITION CODE IS ASSUMED ALREADY SET)
.*
.*
.*
.*
&N       SETC  '&NAME'
         AIF   ('&ADR' EQ '').END
&T       SETC  '&ADR(2)'
         AIF   ('&T' EQ '').SETDFL
         AIF   ('&T' EQ 'Z' OR '&T' EQ 'NZ').TOK
         MNOTE 4,'*** ERROR *** INVALID OPERAND  - NZ ASSUMED'
.SETDFL  ANOP
&T       SETC  'NZ'
.TOK     AIF   ('&ADR(3)' EQ '').NLTR BIF CONDITION CODE ALREADY SET
&R       SETC  '&ADR(3)'
         AIF   ('&R'(1,1) NE '(').NPARN
&R       SETC  '&R'(2,K'&R-2)   REMOVE PARENTHESIS
.NPARN   ANOP
&N       LTR   &ADR(3),R15      SET CONDITION CODE (SAVE RETURN CODE)
&N       SETC  ''
.NLTR    AIF   ('&ADR(1)'(1,1) EQ '(').REG
         AIF   ('&T' EQ 'Z').AZ
&N       BNZ   &ADR(1)          BRANCH ON NONZERO RETURN
         MEXIT
.AZ      ANOP
&N       BZ    &ADR(1)          BRANCH IF RETURN CODE ZERO
         MEXIT
.REG     AIF   ('&T' EQ 'Z').RZ
&N       BNZR  &ADR(1)          BRANCH ON NONZERO RETURN
         MEXIT
.RZ      ANOP
&N       BZR   &ADR(1)          BRANCH IF RETURN CODE ZERO
.END     MEND
