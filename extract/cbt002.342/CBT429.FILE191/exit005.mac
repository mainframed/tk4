EXIT005  TITLE '$JT,1      COMMAND -- PROLOG (COMMENTS)'       HWC
***************************************************************HWC
*                                                              HWC
*                                                              HWC
*                                                              HWC
*                                                              HWC
* MODULE NAME = EXIT$TW                                        HWC
*                                                              HWC
* DESCRIPTIVE NAME = $JT,1       DYNAMICALLY LOAD STC TABLE    HWC
*                                                              HWC
* AUTHOR = WALT SAPP MS#513                                    HWC
*          HEALTH AND WELFARE DATA CENTER                      HWC
*          STATE OF CALIFORNIA                                 HWC
*          1651 ALHAMBRA BLVD                                  HWC
*          SACRAMENTO, CALIFORNIA 95816                        HWC
*                                                              HWC
*          PHONE: (916) 739-7671                               HWC
*                                                              HWC
*          SHARE INSTALLATION CODE = HWC                       HWC
*                                                              HWC
*                                                              HWC
* SYSTEM LEVEL = JES2 SYSTEM PRODUCT V2.2.0                    HWC
*                                                              HWC
*                                                              HWC
* FUNCTION = INSTALL A $JT,1 COMMAND IN   AN EXIT 5 ROUTINE    HWC
*    WHICH LOADS THE STC JCL TABLE                             HWC
*                                                              HWC
* NOTES = SEE BELOW                                            HWC
*                                                              HWC
*    DEPENDENCIES = SEE BELOW                                  HWC
*                                                              HWC
*       (1) THIS IS A STAND ALONE EXIT 5 PROGRAM               HWC
*                                                              HWC
*    RESTRICTIONS = NONE                                       HWC
*                                                              HWC
*    REGISTER CONVENTIONS = SEE BELOW                          HWC
*                                                              HWC
*       R0  = EXIT PARAMETER AND WORK                          HWC
*       R1  = EXIT PARAMETER AND WORK                          HWC
*       R2  = WORK                                             HWC
*       R3  = WORK                                             HWC
*       R4  = WORK                                             HWC
*       R5  = WORK OR ADDR OF CURRENT OPERAND                  HWC
*       R6  = WORK OR INCREMENT VALUE OF 4                     HWC
*       R7  = WORK OR ADDR OF LAST OPERAND                     HWC
*       R8  = SEGMENT ADDRESSABILITY                           HWC
*       R9  = UNUSED                                           HWC
*       R10 = JCT ADDRESSABILITY                               HWC
*       R11 = HCT ADDRESSABILITY                               HWC
*       R12 = PROCESSOR ADDRESSABILITY                         HWC
*       R13 = PCE ADDRESSABILITY                               HWC
*       R14 = EXIT LINKAGE AND WORK                            HWC
*       R15 = EXIT LINKAGE AND WORK                            HWC
*                                                              HWC
*    PATCH LABEL = NONE                                        HWC
*                                                              HWC
* MODULE TYPE = PROCEDURE (CSECT)                              HWC
*                                                              HWC
*    PROCESSOR = OS/VS ASSEMBLER                               HWC
*                                                              HWC
*    MODULE SIZE = SEE $MODEND MACRO AT END OF THIS ASSEMBLY.  HWC
*                                                              HWC
*    ATTRIBUTES = JES2 REENTRANT                               HWC
*                                                              HWC
*                                                              HWC
*    ENTRY POINT = JTCHX5                                      HWC
*                                                              HWC
*       LINKAGE = STANDARD JES2 EXIT LINKAGE                   HWC
*                                                              HWC
*       INPUT = SEE JES2 SPL FOR EXIT 5                        HWC
*                                                              HWC
*       OUTPUT = NEW STC JCL TABLE                             HWC
*                                                              HWC
*       RETURN CODE = SEE BELOW                                HWC
*                                                              HWC
*          0 - THE COMMAND WAS NOT RECOGNIZED OR PROCESSED BY  HWC
*              THIS EXIT JES2 IS TO PERFORM STANDARD HASPCOMM  HWC
*              PROCESSING ON THE COMMAND.                      HWC
*                                                              HWC
*              THIS VALUE IS ALSO USED BY THIS EXIT AFTER      HWC
*              SUCCESSFUL COMPLETION AFTER THE $V IS CHANGED   HWC
*              TO A $D AND TO LET JES2 ISSUE AN 'INVALID       HWC
*              COMMAND' RESPONSE IF NECESSARY.                 HWC
*                                                              HWC
*          4 - JES2 WILL IGNORE ANY OTHER EXITS AND PROCEED    HWC
*              WITH NORMAL COMMAND PROCESSING.                 HWC
*                                                              HWC
*          8 - THE COMMAND WAS RECOGNIZED AND PROCESSED BY     HWC
*              THIS EXIT.  JES2 WILL IMMEDIATELY ISSUE A $CRET HWC
*              MACRO BYPASSING ANY FUTHER PROCESSING.          HWC
*                                                              HWC
*         12 - THE COMMAND WAS RECOGNIZED AND PROCESSED BY     HWC
*              THIS EXIT.  JES2 WILL IMMEDIATELY ISSUE A $CRET HWC
*              MACRO SPECIFING THE STANDARD HASP000 "OK"       HWC
*              MESSAGE AND BYPASS ANY FUTHER PROCESSING.       HWC
*                                                              HWC
*         16 - THE COMMAND WAS RECOGNIZED AND PROCESSED BY     HWC
*              THIS EXIT.  JES2 WILL IMMEDIATELY ISSUE A $CRET HWC
*              MACRO SPECIFING A MESSAGE GENERATED BY THIS     HWC
*              EXIT ROUTINE AND BYPASS ANY FUTHER PROCESSING.  HWC
*                                                              HWC
*                                                              HWC
* EXTERNAL REFERENCES = SEE BELOW                              HWC
*                                                              HWC
*                                                              HWC
*    DATA AREAS = USES $USER1 IN HCT                           HWC
*                                                              HWC
*    CONTROL BLOCKS = SEE $MODULE MACRO INSTRUCTION BELOW.     HWC
*                                                              HWC
* TABLES = NONE                                                HWC
*                                                              HWC
* OS MACROS = NONE                                             HWC
*                                                              HWC
* JES2 MACROS = $CKPT, $CWTO, $ENTRY, $EXIT, $HASPGBL          HWC
*    (COPIED), $JCAN, $MID, $MODEND, $MODULE, $QLOC, $QSUSE,   HWC
*    $RETURN, $SAVE                                            HWC
*                                                              HWC
*                                                              HWC
*                                                              HWC
*                                                              HWC
***************************************************************HWC
         TITLE '$JT STC TABLE LOAD   -- PROLOG ($HASPGBL)'     HWC
         COPY  $HASPGBL            COPY HASP GOBALS            HWC
EXIT$JT $MODULE TITLE='$JT STC LOAD COMMAND',                  HWC     X
               RESPA,              GENERATE MVS RESPA DSECT    HWC     X
               RPL,                GENERATE MVS RPL   DSECT    HWC     X
               $BUFFER,            GENERATE HASP BUFFER DSECT  HWC     X
               $CAT,               GENERATE HASP CAT DSECT     HWC     X
               $COMWORK,           GENERATE HASP COMWORK DSECT HWC     X
               $CMB,               GENERATE HASP CMB DSECT     HWC     X
               $DAS,               GENERATE HASP DAS DSECT     HWC     X
               $DCT,               GENERATE HASP DCT DSECT     HWC     X
               $HASPEQU,           GENERATE HASP EQUATES       HWC     X
               $HCT,               GENERATE HASP HCT DSECT     HWC     X
               $PADDR,             GENERATE HASP     DSECT     HWC     X
               $JCT,               GENERATE HASP JCT DSECT     HWC     X
               $JOE,               GENERATE HASP JOE DSECT     HWC     X
               $JQE,               GENERATE HASP JQE DSECT     HWC     X
               $KIT,               GENERATE HASP KIT DSECT     HWC     X
               $MIT,               GENERATE HASP MIT DSECT     HWC     X
               $PCE,               GENERATE HASP PCE DSECT     HWC     X
               $PIT,               GENERATE HASP PIT DSECT     HWC     X
               $RAT,               GENERATE HASP RAT DSECT     HWC     X
               $SJB,               GENERATE HASP SJB DSECT     HWC     X
               $TQE                GENERATE HASP TQE DSECT     HWC
***************************************************************HWC
*                                                              HWC
*  LOCAL $JT,1       COMMAND PREPROCESSOR EXIT  (EXIT 5)       HWC
*                                                              HWC
***************************************************************HWC
         SPACE 1                                               HWC
JTCHX5   $ENTRY BASE=R12           DEFINE EXIT ENTRY POINT     HWC
         $SAVE                                                 HWC
         LR    R12,R15                                         HWC
         SPACE 1                                               HWC
         CLC   COMVERB(2),=C'JT'   IS THIS $JT                 HWC
         BNE   EXIT0500            NO - LET THE NEXT EXIT CHECK IT
CKNEXT   L      R4,0(R5)
         CLI   0(R4),C'1'            CORRECT KEYWORD?
         BE    LOADUSR1
         CLI   0(R4),C'2'            CORRECT KEYWORD?
         BE    LOADUSR2
         CLI   0(R4),C'3'            CORRECT KEYWORD?
         BE    LOADUSR3
         BXLE  R5,R6,CKNEXT
         B     EXIT0500                BRANCH IF NO
LOADUSR1 TM    $USER1,X'80'         IS LOADER ACTIVE            HW00012
         BO    LDACTIVE             YES, GET OUT                HW00012
         OI    $USER1,X'80'            SHOW   ACTIVE            HW00012
         LR    R1,R11             GIVE ADDRESSABILITY TO SUBTASKHW00012
         ATTACH EP=HASPUSR1,DPMOD=-1                            HW00012
         B     EXIT0512                BRANCH IF NO
LOADUSR2 TM    $USER2,X'80'         IS LOADER ACTIVE            HW00012
         BO    LDACTIVE             YES, GET OUT                HW00012
         OI    $USER2,X'80'            SHOW   ACTIVE            HW00012
         LR    R1,R11             GIVE ADDRESSABILITY TO SUBTASKHW00012
         ATTACH EP=HASPUSR2,DPMOD=-1                            HW00012
         B     EXIT0512                BRANCH IF NO
LOADUSR3 TM    $USER3,X'80'         IS LOADER ACTIVE            HW00012
         BO    LDACTIVE             YES, GET OUT                HW00012
         OI    $USER3,X'80'            SHOW   ACTIVE            HW00012
         LR    R1,R11             GIVE ADDRESSABILITY TO SUBTASKHW00012
         ATTACH EP=HASPUSR3,DPMOD=-1                            HW00012
EXIT0512 LA    R15,12                 GIVE 'OK'
         B     EXIT05$$
LDACTIVE MVC   COMMAND(14),=CL14'LOADER ACTIVE '
         LA    R15,16                 ISSUE MESSAGE
         LA    R0,14
         B     EXIT05$$
EXIT0500 DS    0H
         LA    R15,0                   CONTINUE NORMAL PROCESSING
EXIT05$$ $RETURN RC=(R15)
         SPACE 3
         EJECT
         LTORG
         EJECT
        $MODEND ,                  COMPLETE MODULE INFO TABLE  HWC
         SPACE 2                                               HWC
         END   ,                   END OF ASSEMBLY             HWC
