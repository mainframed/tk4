TGETN    TITLE '--- PROGRAM TO ISSUE TGET MACRO ---'
*******************************************************************
*                     TGETN     PROGRAM                  PAGE: 1  *
*                    ===================                          *
*                                                                 *
* PROGRAM......: TGETN                                            *
*                                                                 *
* LANGUAGE.....: ASSEMBLER                                        *
*                                                                 *
* DATE.........: 23AUG95                                          *
*                                                                 *
* VERSION......: 1.0                                              *
*                                                                 *
* AUTHOR.......: DAVID FAHEY     (WHOM GOD PRESERVE)              *
*                                                                 *
* DESCRIPTION..: THIS PROGRAM IS LOADED BELOW THE LINE AND        *
*                CALLED BY A C LANGUAGE CALLER.                   *
*                                                                 *
*******************************************************************
         EJECT
*******************************************************************
*                     TGETN     PROGRAM                  PAGE: 2  *
*                    ===================                          *
*                                                                 *
* SYSTEM MACROS: CREGS    --- REGISTER SETTINGS                   *
*                CENTRY   --- C LANGUAGE ENTRY CONVENTIONS        *
*                TGET     --- ISSUE TGET (SVC 93)                 *
*                CEXIT    --- C LANGUAGE EXIT CONVENTIONS         *
*                                                                 *
*******************************************************************
         EJECT
TGETN@   CSECT
         CREGS USING
TGETN    CENTRY DSA=DSALEN
*******************************************************************
*        MAIN PROGRAM LOGIC                                       *
*******************************************************************
         SPACE
* --- GET PARAMETER LIST
         L       R2,0(,R1)              CALLERS BUFFER ADDR
         L       R3,4(,R1)              CALLERS DATE LENGTH
         SPACE
* --- GET STORAGE BELOW LINE
         GETMAIN R,LV=(R3)              GET STG BELOW 16M LINE
         LR      R6,R1                  HOLD INPUT ADDR IN R6
         LR      R7,R3                  HOLD INPUT LEN IN R7
         SPACE
* --- ISSUE TGET MACRO
         LR      R0,R3                  R0 = LENGTH OF BUFFER
         LR      R1,R6                  R1 = BUFFER ADDRESS
         LA      R4,TGFLG               FLGS FOR TGET FULLSCR
         SLL     R4,24                  SHIFT LEFT FLGS TO HIGH BYTE
         OR      R1,R4                  MERGE FLAGS WITH BUF. ADDR
         TGET    (1),(0),R
         LR      R8,R15                 SAVE RETURN CODE
         SPACE
* --- COPY DATA TO CALLERS STG (MAYBE ABOVE 16M LINE)
*                                       OUTPUT DATA ADDR IN R2
*                                       OUTPUT LEN IN R3
         LR      R4,R6                  INPUT ADDR IN R4
         LR      R5,R7                  INPUT LEN IN R5
         MVCL    R2,R4                  COPY DATA TO CALLERS STG
*                                         R2(R3) <== R4(R5)
         SPACE
* --- FREE STORAGE
         FREEMAIN R,LV=(R7),A=(R6)      FREE STG BELOW 16M LINE
         SPACE
* --- RETURN TO C CALLER
FIN      CEXIT   RC=(8),DSA=DSALEN      FAILED - NOT DISPLAY MSG
         SPACE
         SPACE
         LTORG
*******************************************************************
*        CONTROL BLOCKS AND WORK AREAS                            *
*******************************************************************
         SPACE
         COPY    DSA
DSALEN   EQU     *-DSA
         SPACE
         COPY    CRAB
         SPACE
TGFLG    EQU     B'10000011'            FLAGS FOR TGET FULLSCR
         END     TGETN
