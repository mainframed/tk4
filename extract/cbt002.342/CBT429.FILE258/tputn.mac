TPUTN    TITLE '--- PROGRAM TO ISSUE TPUT MACRO WITH NOEDIT ---'
*******************************************************************
*                     TPUTN     PROGRAM                  PAGE: 1  *
*                    ===================                          *
*                                                                 *
* PROGRAM......: TPUTN                                            *
*                                                                 *
* LANGUAGE.....: ASSEMBLER                                        *
*                                                                 *
* DATE.........: 03AUG95                                          *
*                                                                 *
* VERSION......: 1.0                                              *
*                                                                 *
* AUTHOR.......: DAVID FAHEY     (WHOM GOD PRESERVE)              *
*                                                                 *
* DESCRIPTION..: THIS PROGRAM IS LOADED BELOW THE LINE AND        *
*                CALLED BY A C LANGUAGE CALLER.                   *
*                                                                 *
*******************************************************************
         EJECT
*******************************************************************
*                     TPUTN     PROGRAM                  PAGE: 2  *
*                    ===================                          *
*                                                                 *
* SYSTEM MACROS: CREGS    --- REGISTER SETTINGS                   *
*                CENTRY   --- C LANGUAGE ENTRY CONVENTIONS        *
*                TPUT     --- ISSUE TPUT (SVC 93)                 *
*                CEXIT    --- C LANGUAGE EXIT CONVENTIONS         *
*                                                                 *
*******************************************************************
         EJECT
TPUTN@   CSECT
         CREGS USING
TPUTN    CENTRY DSA=DSALEN
*******************************************************************
*        MAIN PROGRAM LOGIC                                       *
*******************************************************************
         SPACE
* --- GET PARAMETER LIST
         L       R2,0(,R1)              ADDR BUFFER
         L       R3,4(,R1)              LENGTH DATA
         SPACE
* --- GET STORAGE BELOW LINE
         GETMAIN R,LV=(R3)              GET STG BELOW 16M LINE
         LR      R6,R1                  HOLD OUTPUT ADDR IN R6
         LR      R7,R3                  HOLD OUTPUT LEN IN R7
         SPACE
* --- COPY DATA BELOW 16M LINE
*                                       INPUT ADDR IN R2
*                                       INPUT LEN IN R3
         LR      R4,R1                  OUTPUT ADDR IN R4
         LR      R5,R3                  OUTPUT LEN IN R5
         MVCL    R4,R2                  COPY DATA BELOW LINE
         SPACE
* --- ISSUE TPG MACRO
         MVC     TPLW(TPLL),TPL         COPY PARM LIST TO WSTG
         LA      R4,TPLW
         TPUT    (R6),(R7),MF=(E,(R4))
         LR      R2,R15                 SAVE RET CODE
         SPACE
* --- FREE STORAGE
         FREEMAIN R,LV=(R7),A=(R6)      FREE STG BELOW 16M LINE
FIN      CEXIT   RC=(2),DSA=DSALEN      FAILED - NOT DISPLAY MSG
         SPACE
TPL      TPUT    0,0,NOEDIT,MF=L
TPLL     EQU     *-TPL
         SPACE
         LTORG
*******************************************************************
*        CONTROL BLOCKS AND WORK AREAS                            *
*******************************************************************
         SPACE
         COPY    DSA
TPLW     TPUT    0,0,NOEDIT,MF=L
DSALEN   EQU     *-DSA
         SPACE
         COPY    CRAB
         END     TPUTN
