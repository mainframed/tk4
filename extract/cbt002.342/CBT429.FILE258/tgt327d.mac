TGT327D   TITLE '--- PROGRAM TO GET TERMINAL ATTRS FOR C CALLER ---'
*******************************************************************
*                     TGT327D   PROGRAM                  PAGE: 1  *
*                    ===================                          *
*                                                                 *
* PROGRAM......: TGT327D                                          *
*                                                                 *
* LANGUAGE.....: ASSEMBLER                                        *
*                                                                 *
* DATE.........: 02AUG95                                          *
*                                                                 *
* VERSION......: 1.0                                              *
*                                                                 *
* AUTHOR.......: DAVID FAHEY     (WHOM GOD PRESERVE)              *
*                                                                 *
* DESCRIPTION..: THIS PROGRAM IS LOADED BELOW THE LINE AND        *
*                CALLED BY A C LANGUAGE CALLER.                   *
*                                                                 *
*******************************************************************
         EJECT
*******************************************************************
*                     TGT327D   PROGRAM                  PAGE: 2  *
*                    ===================                          *
*                                                                 *
* SYSTEM MACROS: CREGS    --- REGISTER SETTINGS                   *
*                CENTRY   --- C LANGUAGE ENTRY CONVENTIONS        *
*                GTTERM   --- GET TERMINAL INFO (SVC 94 CODE 11X) *
*                CEXIT    --- C LANGUAGE EXIT CONVENTIONS         *
*                                                                 *
*******************************************************************
         EJECT
TGT327D@ CSECT
         CREGS USING
TGT327D  CENTRY DSA=DSALEN
*******************************************************************
*        MAIN PROGRAM LOGIC                                       *
*******************************************************************
         SPACE
         L       R5,0(,R1)              ADDRESS CALLERS DATA AREA
         USING   PARMRET,R5
         SPACE
         LA      R2,TGPRM               -> PRIMARY
         LA      R3,TGALT               -> ALTERNATE
         LA      R4,TGATTR              -> ATTRIBUTES
         GTTERM  PRMSZE=(R2),ALTSZE=(R3),ATTRIB=(R4)
         LTR     R15,R15                IS IT A 3270 ?
         BNZ     FIN                    NO - GO BACK NOW
*                                       YES - COPY DATA TO CALLER
         LH      R3,TGPRM               COPY PRIMARY TO CALLERS STG
         STH     R3,RXPRM
         LH      R3,TGALT               COPY ALTERNATE TO CALLERS STG
         STH     R3,RXALT
         L       R3,TGATTR              COPY ATTRIBUTES TO CALLERS STG
         ST      R3,RXATTR
         SPACE
FIN      CEXIT   RC=(15),DSA=DSALEN     FAILED - NOT DISPLAY MSG
         B       FIN
         SPACE
         LTORG
*******************************************************************
*        CONTROL BLOCKS AND WORK AREAS                            *
*******************************************************************
         SPACE
         COPY  DSA
TGPRM    DS    0H                       PRIMARY SCREEN SIZE
TGPRMR   DS    CL1
TGPRMC   DS    CL1
*
TGALT    DS    0H                       ALTERNATE SCREEN SIZE
TGALTR   DS    CL1
TGALTC   DS    CL1
*
TGATTR   DS    F                        OTHER DEVICE ATTRIBUTES
DSALEN   EQU   *-DSA
         SPACE
         COPY  CRAB
         SPACE
PARMRET  DSECT ,
RXPRM    DS    H                        PRIMARY SCREEN SIZE
RXALT    DS    H                        ALTERNATE SCREEN SIZE
RXATTR   DS    F                        OTHER DEVICE ATTRIBUTES
         SPACE
         END     TGT327D
