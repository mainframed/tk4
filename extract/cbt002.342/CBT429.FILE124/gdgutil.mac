PROC 0 DEBUG
CONTROL END(DEND) NOPROMPT NOFLUSH
ATTN  OFF
ERROR OFF
 /* */
DISPLAY: +
ISPEXEC DISPLAY PANEL(GDGUTILP)
IF &LASTCC NE 0 THEN GOTO ALLDONE
ISPEXEC VGET (GDGPREF,K,G,DEBUG,ZCHARCNL,GNM)
 /* */
IF &DEBUG = DEBUG THEN CONTROL   LIST   MSG   PROMPT SYMLIST CONLIST
                  ELSE CONTROL NOLIST NOMSG NOPROMPT
IF &K ^= &STR(M) THEN DELETE '&SYSUID..GDGPRT.LIST'
 /* */
SET GDGNAME = &GDGPREF
IF &SUBSTR(1:1,&STR(&GDGPREF) ) = &STR(') THEN DO
   SET GDGLENG = &EVAL(&LENGTH(&STR(&GDGPREF))-1)
   SET GDGNAME = &SUBSTR(2:&GDGLENG,&GDGPREF )
   DEND
 ELSE DO
   SET GDGNAME = &SYSPREF..&GDGPREF
   DEND
SET GDGPREF = &STR('&GDGNAME')
 /* */
FREE ATTR($L$C$P $G$R$O $G$R$I)
SET SUF = &STR(&SUBSTR(1:2,&SYSJDATE)&SUBSTR(4:6,&SYSJDATE))
ATTR $L$C$P LRECL(125) BLKSIZE(9076) RECFM(V B A)
ALLOC DD(D&SUF.1) DS('&SYSUID..GDGPRT.LIST') REUSE US($L$C$P) +
         MOD CAT SPACE(2,1) TRACK
 /* */
SET ALL = &STR(GDG)
IF &G = I THEN DO
   SET &ALL = &STR(GDG ALL)
   GOTO LSTGDG
   DEND
IF &G = L THEN DO
   SET &ALL = &STR(ALL)
   GOTO LSTGDG
   DEND
IF &G = M THEN DO
   SET GDGENTL = &LENGTH(&STR(00&GNM))
   SET GDGENTS = &STR(&SUBSTR(&EVAL(&GDGENTL-2):&GDGENTL,&STR(00&GNM)))
   SET ACTION  = &STR(MODIFIED)
   GOTO MODGDG
   DEND
IF &G = C THEN DO
   SET GDGENTS = &STR(&GNM)
   SET ACTION  = &STR(CREATED)
   SET &ALL = &STR(GDG ALL)
   GOTO BLDGDG
   DEND
IF &G = D THEN DO
   SET GDGENTS = &STR(&GNM)
   SET ACTION  = &STR(DELETED)
   GOTO MODGDG
   DEND
 /* */
LSTGDG: +
LISTC ENT(&GDGPREF) &ALL OUTFILE(D&SUF.1)
SET CCND = &LASTCC
IF &CCND > 0 THEN DO
   SET &ZEDSMSG = &STR(GDG NOT FOUND)
   SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS NOT FOUND RC=&CCND)
   ISPEXEC SETMSG MSG(ISRZ000)
   DEND
 ELSE DO
   SET HCHARCNL = &ZCHARCNL
   SET ZCHARCNL = NOCC
   ISPEXEC VPUT (ZCHARCNL)
   ISPEXEC BROWSE DATASET('&SYSUID..GDGPRT.LIST')
   SET ZCHARCNL = &HCHARCNL
   ISPEXEC VPUT (ZCHARCNL)
   SET &ZEDSMSG = &STR(GDG WAS DISPLAYED)
   SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS DISPLAYED RC=&CCND)
   ISPEXEC SETMSG MSG(ISRZ000)
   DEND
IF &K = D THEN FREE DD(D&SUF.1) DELETE
          ELSE FREE DD(D&SUF.1)
GOTO DISPLAY
 /* */
BLDGDG: +
CONTROL MSG
DEFINE GDG(NAME(&GDGPREF) LIMIT(&GNM) SCRATCH OWNER(&SYSUID))
SET CCND = &LASTCC
IF &DEBUG = DEBUG THEN CONTROL MSG
                  ELSE CONTROL NOMSG
IF &CCND > 0 THEN DO
   SET &ZEDSMSG = &STR(GDG NOT &ACTION)
   SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS NOT &ACTION RC=&CCND)
   ISPEXEC SETMSG MSG(ISRZ000)
   DEND
 ELSE DO
   LISTC ENT(&GDGPREF) &ALL OUTFILE(D&SUF.1)
   SET HCHARCNL = &ZCHARCNL
   SET ZCHARCNL = NOCC
   ISPEXEC VPUT (ZCHARCNL)
   ISPEXEC BROWSE DATASET('&SYSUID..GDGPRT.LIST')
   SET ZCHARCNL = &HCHARCNL
   ISPEXEC VPUT (ZCHARCNL)
   SET &ZEDSMSG = &STR(GDG WAS &ACTION)
   SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS &ACTION RC=&CCND)
   ISPEXEC SETMSG MSG(ISRZ000)
   DEND
GOTO DISPLAY
 /* */
MODGDG: +
LISTC ENT(&GDGPREF) &ALL OUTFILE(D&SUF.1)
SET CCND = &LASTCC
IF &CCND > 0 THEN DO
   SET &ZEDSMSG = &STR(GDG NOT FOUND)
   SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS NOT FOUND RC=&CCND)
   ISPEXEC SETMSG MSG(ISRZ000)
   DEND
 ELSE DO
   SET BLANKS = &STR(                                                  )
   SET GDGWRK = &STR(&SUBSTR(1:45,&STR(&GDGNAME.&BLANKS))&STR(&GDGENTS))
   SET HCHARCNL = &ZCHARCNL
   SET ZCHARCNL = NOCC
   ISPEXEC VPUT (ZCHARCNL)
   FREE DD(D&SUF.1) DELETE
   ATTR $G$R$O LRECL(133) BLKSIZE(9044) RECFM(F B M)
   ATTR $G$R$I LRECL(80) BLKSIZE(80) RECFM(F B)
   ALLOC DD(GDGOUT) DS('&SYSUID..GDGPRT1.LIST') REUSE US($G$R$O) +
         MOD CAT SPACE(2,1) TRACK
   ALLOC DD(GDGIN) DS('&SYSUID..GDGIN.DATA') REUSE US($G$R$I) +
         NEW DEL SPACE(1) TRACK
   ALLOC DD(SYSPRINT) DS('&SYSUID..GDGPRT2.LIST') REUSE US($L$C$P) +
         MOD CAT SPACE(2,1) TRACK
   OPENFILE  GDGIN OUTPUT
   SET GDGIN = &STR(&GDGWRK)
   PUTFILE GDGIN
   CLOSFILE GDGIN
   CALL 'SYS2.TECHSUP.LOAD(RESETGDG)'
   ISPEXEC BROWSE DATASET('&SYSUID..GDGPRT1.LIST')
   ISPEXEC BROWSE DATASET('&SYSUID..GDGPRT2.LIST')
   SET ZCHARCNL = &HCHARCNL
   ISPEXEC VPUT (ZCHARCNL)
   FREE DD(D&SUF.3) DELETE
   SET &ZEDSMSG = &STR(GDG WAS &ACTION)
   SET &ZEDLMSG = &STR(THE GDGINDEX &GDGPREF WAS &ACTION RC=&CCND)
   ISPEXEC SETMSG MSG(ISRZ000)
   DEND
FREE DD(GDGIN) DELETE
IF &K = D THEN FREE DD(GDGOUT SYSPRINT) DELETE
          ELSE FREE DD(GDGOUT SYSPRINT)
ALLOC DD(SYSPRINT) DS(*) REUSE
GOTO DISPLAY
 /* */
ALLDONE: +
FREE ATTR($L$C$P $G$R$O $G$R$I)
ISPEXEC VPUT (GDGPREF,K,G,DEBUG,ZCHARCNL,GNM)
ISPEXEC  CONTROL DISPLAY REFRESH
END
