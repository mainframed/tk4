         MACRO
&LABEL   XABGN &SAVAREA,      ** NAME OF SAVE AREA IN PROGRAM          *
               &NAME,         ** NAME ON STM INSTRUCTION IF &STMLBL='' *
               &ID=,          ** HAS NO FUNCTION - FOR COMPATIBILITY   *
               &AUTHOR=***,      AUTHOR'S INITIALS            (3 CHAR) *
               &AMODE=24,        ADDRESSING MODE (SPLEVEL=2 ONLY)      *
               &BASE=,           UP TO 3 BASE REGS R,(R,R),OR(R,R,R)   *
               &BUREAU=WSRCC,    BUREAU RESPONSIBLE FOR CODE  (7 CHAR) *
               &CSECT=YES,       YES - GENERATE CSECT IF NONE FOUND    *
               &GEN=NOGEN,       LIST CODE FOR GETMAINING SAVE AREA    *
               &ICATCH=YES,      YES - INCLUDE WSRCC BEGIN EYECATCH    *
               &NOP=NO,          YES - GENERATE NOPR'S FOR ZAPPING     *
               &R=R,             CHARACTERS TO PREFIX REGISTER NUMBERS *
               &REQUS=YES,       YES - GENERATE REGISTER EQUATES       *
               &RMODE=24,        RESIDENCY MODE (SPLEVEL=2 ONLY)       *
               &SAVE=,           ADDRESS OR SPECIFICATION OF SAVEAREA  *
               &SECTION=TECHSUP, SECTION RESPONSIBLE FOR CODE (7 CHAR) *
               &SPLEVEL=2,       LEVEL OF MVS/SP BEING USED (1 OR 2)   *
               &STMLBL=,         LABEL FOR THE STM STATEMENT           *
               &TITLBL=*DFLT,    LABEL FOR TITLE STATEMENT             *
               &TITLE=,          TITLE INFORMATION                     *
               &TITSTMT=YES,     YES - GENERATE A TITLE STATEMENT      *
               &TYPE=CSECT,      TYPE OF ENTRY: CSECT OR ENTRY         *
               &UNIT=SYSSOFT,    UNIT RESPONSIBLE FOR CODE    (7 CHAR) *
               &USING13=YES,     YES - INCLUDE USING R13 FOR &SAVAREA  *
               &VER=             VERSION OF CODE              (4 CHAR)
.*
.* ABOVE COMMENTS PRECEEDED BY A ASTERISK ('**') INDICATE PARAMETER
.* IS INCLUDED FOR COMPATIBILITY ONLY AND SHOULD NOT BE USED
.*
         GBLB  &RFLAG
         GBLC  &RLEN,&RSP
         GBLC  &SYSSPLV                                           V201
         LCLC  &CLABEL,&SCODE,&SREG1,&SREG2,&SLBL,&SAVNAME,&BREG1
         LCLC  &TITLBLC,&TITL,&TITLVER,&VERTEMP
         LCLC  &C,&SPOT,&HDR,&CVAL
         LCLC  &SPLDFLT,&AM,&RM,&MODECM                           V201
.*
&SPLDFLT SETC  '1'               DEFAULT SPLEVEL IF NOT SPECD.    V201
.*
         MNOTE *,'***WSRCC REENTRANT BEGIN MACRO V02.01***'       V201
.*                  WITH THANKS TO HEWLETT-PACKARD BAEDP
.*
.*       &SPLEVEL IS USED TO INDICATE WHAT LEVEL OF MVS/SP IS     V201
.*       BEING USED. IF MVS/SP 2.1.0 OR GREATER IS TO BE USED     V201
.*       THEN CODE SPLEVEL=2 OTHERWISE SPLEVEL WILL DEFAULT       V201
.*       TO SPLEVEL=1.                                            V201
.*
         AIF   ('&SPLEVEL' EQ '').SPLV00                          V201
         AIF   ('&SPLEVEL' NE '1' AND '&SPLEVEL' NE '2').SPLERR   V201
&SYSSPLV SETC  '&SPLEVEL'                                         V201
         AGO   .SPLV99
.*
.SPLERR  ANOP                                                     V201
         MNOTE 4,'*** SPLEVEL=&SPLEVEL IS INVALID ***'            V201
         MNOTE *,'*** SPLEVEL MUST BE EITHER "1" OR "2" ***'      V201
.*
.SPLV00  ANOP                                                     V201
&SYSSPLV SETC  '&SPLDFLT'        SET THE DEFAULT IF NOT SPECD.    V201
.*
.SPLV99  ANOP
         MNOTE *,'*** SPLEVEL=&SYSSPLV BEING USED ***'            V201
.*
&CLABEL  SETC  '&SYSECT'
         AIF   ('&CSECT' NE 'YES').CLBL99
&CLABEL  SETC  '&LABEL'
         AIF   ('&CLABEL' NE '').CLBL99
&CLABEL  SETC  'PRIVATE'
         MNOTE *,'*** NO LABEL ON BEGIN, &TYPE CALLED ''PRIVATE'' ***'
.*
.CLBL99  ANOP
         AIF    ('&TYPE' NE 'ENTRY').ENT99
&CLABEL  DS    0H
         ENTRY &CLABEL
         AGO   .CST99
.*
.ENT99   ANOP
         AIF   ('&CSECT' NE 'YES').CST99
&CLABEL  CSECT
.*
.CST99   ANOP
         AIF   ('&SYSSPLV' EQ '1').SLB00                          V201
&MODECM  SETC  ''                                                 V201
&AM      SETC  '&AMODE'                                           V201
&RM      SETC  '&RMODE'                                           V201
         AIF   ('&AM' EQ '24' AND '&RM' EQ '24').AMD99            V201
         AIF   ('&AM' EQ '31' AND '&RM' EQ '24').AMD99            V201
         AIF   ('&AM' EQ '31' AND '&RM' EQ 'ANY').AMD99           V201
         AIF   ('&AM' EQ 'ANY' AND '&RM' EQ '24').AMD99           V201
         AIF   ('&AM' EQ 'ANY' AND '&RM' EQ 'ANY').AMD01          V201
         MNOTE 4,'*** AMODE=&AM AND RMODE=&RM INVALID ***'        V201
&MODECM  SETC  'INSTEAD '
&AM      SETC  '24'
&RM      SETC  '24'
         AGO   .AMD99                                             V201
.*
.AMD01   ANOP                                                     V201
&MODECM  SETC  'INSTEAD '
&AM      SETC  '31'
.*
.AMD99   ANOP                                                     V201
         MNOTE *,'*** USING AMODE=&AM AND RMODE=&RM &MODECM***'   V201
&CLABEL  AMODE &AM                                                V201
&CLABEL  RMODE &RM                                                V201
.*
.SLB00   ANOP
&SLBL    SETC  '&STMLBL'
         AIF   ('&SLBL' NE '').S1
&SLBL    SETC  '&NAME'
.*
.S1      ANOP
         USING &CLABEL,&R.15
&C       SETC  'C'
&SPOT    SETC  '&SYSECT'
         AIF   ('&CLABEL' EQ '').LBL99
&SPOT    SETC  '&CLABEL'
         AIF   ('&TYPE' NE 'ENTRY').LBL99
&C       SETC  'E'
.*
.LBL99   ANOP
.*
.*   PAD THE VERSION WITH _'S
.*
&VERTEMP SETC  '____'
         AIF   ('&VER' EQ '').VERNULL
&VERTEMP SETC  '&VER'
         AIF   ('&VER'(1,1) NE '''').NOQUOTE
&VERTEMP SETC  '&VER'(2,K'&VER-2)
.*
.NOQUOTE ANOP
         LCLC  &PADSTG
&PADSTG  SETC  (4)'_'
&PADSTG  SETC  '&VERTEMP'.'&PADSTG'
&VERTEMP SETC  '&PADSTG'(1,4)
.*
.VERNULL ANOP
&HDR     SETC  'CL4''&VERTEMP.'''
.*
.HDR99   ANOP
         AIF   ('&ICATCH' NE 'YES').NOICTCH
         B     *+68
         DC    AL1(64),CL3'ID='
         DC    CL8'&SPOT'
         DC    C'&C',AL3(&SPOT)
         DC    &HDR
         DC    C'_',C'&SYSDATE.@&SYSTIME._'
         DC    CL7'&BUREAU',C'-'
         DC    CL7'&SECTION',C':'
         DC    CL7'&UNIT',C','
         DC    CL3'&AUTHOR',C'.'
.*
.NOICTCH ANOP
&SLBL    STM   &R.14,&R.12,12(&R.13)
.*
         AIF ('&NOP' EQ 'NO').NONOP
         CNOP  0,4
         B     *+20
         DC    CL16'ZAP TO 17FF07FE:'
         NOPR  R15         CAN ZAP TO 17FF ( XR  R15,R15 )
         NOPR  R14         CAN ZAP TO 07FE ( BR  R14     )
.*
.NONOP   ANOP
         DROP  &R.15
.*
&BREG1   SETC  '&BASE(1)'
         AIF   ('&BREG1' NE '').B10
&BREG1   SETC '&R.12'
         MNOTE *,'** NO BASE REGISTER SPECIFIED ASSUME REGISTER 12 **'
.*
.B10     ANOP
         LR    &BREG1,&R.15
         USING &CLABEL,&BREG1
         AIF   ('&BASE(2)' EQ '').B999
         LA    &BASE(2),4095(&BREG1)
         LA    &BASE(2),1(&BASE(2).)
         USING &CLABEL+4096,&BASE(2)
         AIF   ('&BASE(3)' EQ '').B999
         LA    &BASE(3),4095(&BASE(2).)
         LA    &BASE(3),1(&BASE(3).)
         USING &CLABEL+8192,&BASE(3)
.*
.B999    ANOP
         AIF   ('&SAVE' EQ 'NONE').SV99
&SAVNAME SETC  '&SAVE(1)'
&RLEN    SETC  '&SAVE(2)'
&RSP     SETC  '&SAVE(3)'
         AIF   ('&SAVNAME' NE '').SV10
&SAVNAME SETC  '&SAVAREA'
.*
.SV10    ANOP
         AIF   ('&SAVE(1)' NE '' OR '&SAVE(2)'  NE '' OR '&SAVE(3)' NE *
               '').SV20
         AIF   ('&SAVAREA' NE '').SVNRENT
         AGO   .SVRENT
.*
.SV20    ANOP
         AIF   ('&SAVE(2)' EQ '' AND '&SAVE(3)' EQ '').SVNRENT
         AGO   .SVRENT
.*
.SVNRENT ANOP
&RFLAG   SETB  0
         LR    &R.15,&R.13
         LA    &R.13,&SAVNAME
         ST    &R.15,4(&R.13)
         ST    &R.13,8(&R.15)
         AGO   .SV99
.*
.SVRENT  ANOP
&RFLAG   SETB  1
         AIF   ('&RLEN' NE '').SVRSET
&RLEN    SETC  '72'
         MNOTE *,'*** REENTRANT SAVE AREA FORMAT USED ***'
.*
.SVRSET  ANOP
.*
.********* FOLLOWING FIX APPLIED 15 JAN 79 - DP *********************
.* IF &RLEN IS AN 'L'' TYPE CONSTANT, DOUBLE-UP THE QUOTES FOR MNOTE
.*
         GBLC  &RLENA,&RLENB
         LCLA  &LRLEN
&RLENA   SETC  ''
&RLENB   SETC  '&RLEN'
         AIF   ('&RLEN'(1,2) NE 'L''').NORLQ
&RLENA   SETC  'L'''''
&LRLEN   SETA  K'&RLEN-2
&RLENB   SETC  '&RLEN'(3,&LRLEN)
.*
.NORLQ   ANOP
         MNOTE *,'*      GETMAIN R,LV=&RLENA.&RLENB,SP=&RSP'
         PUSH  PRINT
         PRINT &GEN
         GETMAIN R,LV=&RLEN,SP=&RSP
         POP   PRINT
         LR    &R.15,&R.13
         LR    &R.13,&R.1
         ST    &R.15,4(&R.13)
         ST    &R.13,8(&R.15)
         LM    &R.15,&R.1,16(&R.15)    RESET 0, 1, AND 15
         AGO   .SV99
.*
.SV99    ANOP
         AIF   ('&SAVNAME' EQ '').UTH99
         AIF   ('&USING13' NE 'YES').UTH99
         USING &SAVNAME,&R.13
.*
.UTH99   ANOP
         AIF   ('&REQUS' NE 'YES').REQNO
         MNOTE *,'REGISTER NAMES &R.0 THROUGH &R.15 ARE ASSIGNED'
&R.0     EQU   0
&R.1     EQU   1
&R.2     EQU   2
&R.3     EQU   3
&R.4     EQU   4
&R.5     EQU   5
&R.6     EQU   6
&R.7     EQU   7
&R.8     EQU   8
&R.9     EQU   9
&R.10    EQU   10
&R.11    EQU   11
&R.12    EQU   12
&R.13    EQU   13
&R.14    EQU   14
&R.15    EQU   15
.*
.REQNO   ANOP
         AIF   ('&TITSTMT' NE 'YES').TIT99
&TITLBLC SETC  '&CLABEL'
         AIF   ('&TITLBL' EQ '*DFLT').TIT50
&TITLBLC SETC  '&TITLBL'
.*
.TIT50   ANOP
&TITL    SETC  '&TITLE'
         PUSH  PRINT
         PRINT OFF       TITLE STATEMENT FOLLOWS
         AIF   ('&TITLE' NE '').TIT79
&TITL    SETC  ''' '''
         AIF   ('&VER' EQ '').TIT79
&TITLVER SETC  '&VER'
&TITL    SETC  '''VER '.'&TITLVER'.''''
.*
.TIT79   ANOP
         AIF   ('&TITL'(1,1) EQ '''').TIT89
&TITL    SETC  ''''.'&TITL'.''''
.*
.TIT89   ANOP
&TITLBLC TITLE &TITL
         POP   PRINT
.*
.TIT99   ANOP
         MEND
