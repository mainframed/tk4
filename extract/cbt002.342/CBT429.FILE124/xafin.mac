         MACRO
&LABEL   XAFIN &GEN=NOGEN,             GEN - LIST CODE FOR FREEMAIN    *
               &R=R,                   PREFIX FOR REGISTERS            *
               &RC=0,                  ABSOLUTE OR (R) FOR RETURN CODE *
               &RC1=,                  ABS OR (R) VALUE RETURNED IN R1 *
               &RETURN=YES,            YES - GENERATE CODE FOR RETURN  *
               &RTRNOFF=               RETURN OFFSET VALUE
.*
         GBLB  &RFLAG
         GBLC  &RLEN,&RLENA,&RLENB,&RSP,&CT
         GBLC  &SYSSPLV                                           V201
         LCLC  &OPCD,&OPC1,&RETCODE,&RETCD1
.*
.*       MNOTE *,'***WSRCC REENTRANT FINISH MACRO V02.01***'      V201
.*                  WITH THANKS TO HEWLETT-PACKARD BAEDP
.*
          AIF   ('&SYSLIST(1)' EQ '').ERR99
          MNOTE 8,'*** PARTIAL REGISTER LIST NO LONGER SUPPORTED IN BEG*
                IN OR FINISH ***'
.*
.ERR99   ANOP
&RETCODE SETC  '&RC(1)'
         AIF   ('&RETCODE' NE '').RC99
&RETCODE SETC  '0'
         MNOTE *,'*** NO RETURN CODE SPECIFIED ** ZERO ASSUMED ***'
.*
.RC99    ANOP
&OPCD    SETC  'LR'
         AIF   ('&RC'(1,1) EQ '(').OP99
&OPCD    SETC  'LA'
.*
.OP99    ANOP
&RETCD1  SETC  '&RC1(1)'
         AIF   ('&RETCD1' EQ '').OP199
.*
&OPC1    SETC  'LR'
         AIF   ('&RC1'(1,1) EQ '(').OP199
&OPC1    SETC  'LA'
.*
.OP199   ANOP
         CNOP  0,4
&LABEL   DS    0H                                                 V201
         AIF   ('&SYSSPLV' EQ '2').SKIPFF                         V201
         OI    4(&R.13),X'FF'                                     V201
.*
.SKIPFF  ANOP
         AIF   (NOT &RFLAG).GOHOME
.*
.RENT    ANOP
         AIF   ('&OPCD' NE 'LR').RENT10
         LR    &R.2,&RETCODE
&RETCODE SETC  '&R.2'
.*
.RENT10  ANOP
         AIF   ('&OPC1' NE 'LR').RENT20
         LR    &R.3,&RETCD1
&RETCD1  SETC  '&R.3'
.*
.RENT20  ANOP
         LR    &R.1,&R.13
         L     &R.13,4(&R.13)
         MNOTE *,'*      FREEMAIN R,LV=&RLENA.&RLENB,A=(1),SP=&RSP'
         PUSH  PRINT
         PRINT &GEN
         FREEMAIN R,LV=&RLEN,A=(1),SP=&RSP
         POP   PRINT
         AGO   .COMEND
.*
.GOHOME ANOP
         L     &R.13,4(&R.13)
.*
.COMEND  ANOP
         &OPCD &R.15,&RETCODE
         L     &R.14,12(&R.13)
         AIF   ('&OPC1' EQ '').GH90
         L     &R.0,20(&R.13)
         &OPC1 &R.1,&RETCD1
         LM    &R.2,&R.12,28(&R.13)
         AGO   .GH99
.*
.GH90    ANOP
         LM    &R.0,&R.12,20(&R.13)
.*
.GH99    ANOP
         AIF   ('&RETURN' NE 'YES').RET99
         AIF   ('&SYSSPLV' EQ '2').ROFCK                          V201
         MVI   12(&R.13),X'FF'
.*
.ROFCK   ANOP                                                     V201
         AIF   ('&RTRNOFF' EQ '').BR14
         B     &RTRNOFF.(&R.14)      TAKE SPECIAL RETURN WITH OFFSET
         AGO   .RET99
.*
.BR14    ANOP
         BR    &R.14
.*
.RET99   ANOP
         MEND
