         MACRO
&NAME    FIELD &POS=,&ATTR=,&DATA=,&CUR=NO
.********** S T A R T    O F    G L O B A L S / L O C A L S ***********
         GBLC  &DSECTNM           NAME OF SCREEN DSECT
         GBLC  &FLINE        LINE POSITION OF FIRST FIELD
         GBLC  &FCOLMN     COLUMN POSITION OF FIRST FIELD
         LCLC  &DATALEN           LENGTH OF DATA FIELD
         LCLC  &CURNAME           NAME OF CURSOR FIELD
         LCLA  &LEN               LENGTH OF DATA FIELD
         LCLA  &COUNT             WORK COUNTER
.**********   E N D      OF     G L O B A L S / L O C A L S ***********
         AIF ('&DSECTNM' EQ '').NOSCRN
         AIF (K'&NAME GT 7).NAMERR
********************  S T A R T    O F    F I E L D  ******************
         AIF   ('&FLINE' NE '').SBA
&FLINE   SETC  '&POS(1)'     LINE POSITION OF FIRST FIELD IN SCREEN
&FCOLMN  SETC  '&POS(2)'   COLUMN POSITION OF FIRST FIELD IN SCREEN
.SBA     ANOP
         SBA   &POS
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         AIF ('&NAME' EQ '').SBANON
&NAME.S  DS    XL3                SBA-ADDRESS
         AGO   .CONT1
.SBANON  ANOP
         AIF  ('&CUR' EQ 'YES').CURSBA
         DS    XL3                SBA-ADDRESS
         AGO  .CONT1
.CURSBA  ANOP
         DS    XL1                SBA
&COUNT   SETA  K'&DSECTNM-1
&CURNAME SETC '&DSECTNM'(1,&COUNT).'X'
&CURNAME DS    XL2                CURSOR ADDRESS
.CONT1   ANOP
&SYSECT  CSECT                    CONTINUE USERS CSECT
         AIF   ('&CUR' EQ 'YES').CURSOR
         SF    &ATTR
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         AIF   ('&NAME' EQ '').SFNON
         DS    XL1                START FIELD(X'1D')
&NAME.A  DS    XL1                ATTRIBUTE BYTE
         AGO   .CONT2
.SFNON   ANOP
         DS    XL1                START FIELD(X'1D')
         DS    XL1                ATTRIBUTE BYTE
.CONT2   ANOP
&SYSECT  CSECT                    CONTINUE USERS CSECT
         AIF   ('&CUR' EQ 'YES').CURSOR
         AIF   ('&DATA' NE '').DATA
         AGO   .END
.DATA    ANOP
&LEN     SETA  K'&DATA-2
&DATALEN SETC  '&LEN'
         DC    CL&DATALEN&DATA    FIELD DATA
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         AIF   ('&NAME' EQ '').DATANON
&NAME    DS    CL&DATALEN         FIELD DATA
&NAME.L  EQU   &DATALEN          FIELD LENGTH
         AGO   .CONT3
.DATANON ANOP
         DS    CL&DATALEN
.CONT3   ANOP
&SYSECT  CSECT                    CONTINUE USERS CSECT
         AGO   .END
.CURSOR  ANOP
         AIF   ('&DATA' EQ '').CURCONT
         MNOTE 'DATA PARAMETER IGNORED WHEN CUR PARAMETER SPECIFIED.'
.CURCONT ANOP
         AIF   ('&ATTR' EQ '').CURAGAN
         MNOTE 'ATTR PARAMETER IGNORED WHEN CUR PARAMETER SPECIFIED.'
.CURAGAN ANOP
&FLINE   SETC  ''
&FCOLMN  SETC  ''
         DC    XL1'13'            CURSOR
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         DS    XL1                CURSOR
&SYSECT  CSECT                    CONTINUE USERS CSECT
.END     ANOP
********************    E N D      O F    F I E L D  ******************
         SPACE 2
         MEXIT
.NOSCRN  ANOP
         MNOTE 12,'''BUF3270'' MACRO REQUIRED BEFORE FIRST ''FIELD'' MAC
               CRO.'
         MEXIT
.NAMERR  ANOP
         MNOTE 12,'MACRO ''NAME'' SYMBOL IS GREATER THAN 7 CHARACTERS.'
         MEND
