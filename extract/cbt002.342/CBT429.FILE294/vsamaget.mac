***********************************************************************
*                                                                     *
* THIS PROGRAM IS A SUB-MODULE OF THE VSAMANAL(TSO COMMAND PROCESSOR) *
*                                                                     *
* THE MAIN FUNCTION OF THIS PROGRAM IS TO PERFORM A TSO TGET W/ASIS   *
* OPTION INTO AN INPUT WORK BUFFER.....THE BUFFER WILL BE EDITED FOR  *
* VALIDITY AND ALL DATA FOUND IN THE BUFFER WILL BE MOVED TO ITS      *
* CORRESPONDING 3270(FULLSCRN) SCREEN BUFFER.  ALL MOVING IS ON A 3270*
* FIELD(SBA,ADDR,SF,ATTR,DATA) LEVEL, AND INPUT FIELDS ARE MOVED ONLY *
* IF A CORRESPONDING FIELD IS FOUND IN THE SCREEN BUFFER. ALL DATA IS *
* MOVED FOR THE LENGTH OF THE SCREEN FIELD SO AS TO PROTECT THE       *
* INTEGRITY OF THE SCREEN BUFFER FOR SUBSEQUENT TPUTS(FULLSCRN).      *
* THE SCREEN FIELD WILL PADDED RIGHT W/X'40'S IF NECESSARY.           *
*                                                                     *
* ERRORS:   ANY PREDICTABLE PROGRAM ERRORS WILL CAUSE AN ERROR DESCRIP*
*           TION TO BE PLACED IN THE COMMON WORKAREA FIELD(PROCERR).  *
*                                                                     *
* LINKAGE:  LINKAGE IS EXPECTED THROUGH THE LINK MACRO IN THE FORM..  *
*            LA  0,LENGTH    LENGTH=LENGTH OF 3270 SCREEN BUFFER      *
*                                   BUFFER.
*            LA  2,WORKAREA  WORKAREA=STARTING LOCATION OF THE VSAMANAL
*            LA  13,CALLERSAVEAREA
*            LINK EP=VSAMAGET                                         *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS PROGRAM IS WRITTEN AS A RE-ENTRANT MODULE......                *
*                                                                     *
*  AUTHOR:  ARTHUR J. CARTIER                                         *
* DATE: 11/14/78                                                      *
* PROPERTY OF:   STATE OF CONNECTICUT                                 *
*                CENTRAL DATA PROCESSING SERVICES(CDPS)               *
*                TECHNICAL SUPPORT SECTION.                           *
*                                                                     *
* ASSOCIATED PROGRAMS:                                                *
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
         EJECT
         OSINIT VSAMAGET,(11,12),NO
         USING WORKAREA,R10       ADDRESSABILITY TO VSAMANAL WORKAREA
         LR    R10,R2             POINT THE WORKAREA DSECT REG. TO IT
         ST    R0,SCRNLSV         SAVE SCREEN LENGTH
         ST    R1,SCRNSAVE        SAVE SCREEN ADDRESS
         LA    R14,SAVEGET   THIS PROGRAMS SAVE AREA(PART OF WORKAREA)
         ST    R14,8(R13)         FORWARD SAVE AREA(CHAIN)
         ST    R13,4(R14)         BACKWARD SAVE AREA(CHAIN)
         LR    R13,R14            SET YOUR SAVE AREA POINTER
         XC    PROCERR,PROCERR    CLEAR ERROR INDICATOR
         LA    R1,INBUFF
         ICM   R1,8,TGETASIS
         LA    R0,INBUFFL
         TGET  ((R1)),((R0)),R
         OR    R15,R15            TGET SUCESSFUL
         BNZ   TGETERR            NO GO PROCESS ERROR
*
         MVC   KEYHIT,INBUFF      SAVE KEY ENTERED
         MVC   CURPOS,INBUFF+1    SAVE WHERE CURSOR WAS
         LR    BUFFLEN,R1         BUFFER LENGTH
         SH    BUFFLEN,=H'4'     SUB FOR KEYENTRD,CURSOR,EXECUTE INSTR
         BNP   EXITSCRN           GO EXIT IF NO DATA ENTERED
         LA    BUFF,INBUFF+3      START OF INPUT BUFFER+3
         LA    BUFFEND,0(BUFF,BUFFLEN) END OF BUFFER DATA
         CLI   0(BUFF),SBA        IS THERE AN SBA
         BNE   BUFFERR            NO BUFFER ERROR
SETSCRN  DS    0H
         L     SCRN,SCRNSAVE      START OF INPUT SCREEN
         LA    SCRN,4(SCRN)       FIRST SBA IN SCREEN
         L     SCRNLEN,SCRNLSV    LENGTH OF INPUT SCREEN
         SH    SCRNLEN,=H'5'      SUB FOR WCC,SPF STRING,EXECUTE
         LA    SCRNEND,0(SCRN,SCRNLEN) END OF INPUT SCREEN
         CLI   0(SCRN),SBA        IS INPUT SCREEN GOOD
         BNE   SCRNERR            NO ERROR IN INPUT SCREEN
         B     BUFFSCAN           GO MATCH UP BUFFER TO SCREEN FIELDS
         EJECT
FINDSBA  TRT   0(0,R1),SBATABLE    TRANSLATE FOR SBA(X'11')
MOVFIELD MVC   5(0,SCRN),0(BUFF)   MOVE BUFF FIELD TO SCREEN FIELD
UPPERCSE OC    5(0,SCRN),BLANKS    SET ALL ALPHA TO UPPER CASE
         SPACE 3
BUFFSCAN DS    0H
         CR    BUFF,BUFFEND       END OF INPUT BUFFER
         BNL   EXITSCRN           YES-GET OUT
         LA    BUFF,1(BUFF)       3270 ADDRESS BYTES
         BCTR  BUFFLEN,0          ADJUST
         MVC   ADDRESS,0(BUFF)    MOVE FOR ADDRESS ADJUSTMNT
         LA    BUFF,2(BUFF)       DATA FIELD IN INPUT BUFFER
         BCTR  BUFFLEN,0          ADJUST
         BCTR  BUFFLEN,0          ADJUST
         BAL   LINKREG,SBAMINUS   GO ADJUST 3270 ADDRESS (-1)
         CR    BUFF,BUFFEND       ANY DATA IN FIELD???
         BH    CLRFIELD           NO-HIT (EOF) CLEAR LAST FIELD
         CLI   0(BUFF),SBA        ANY DATA FIELD????
         BE    CLRFIELD           NO-HIT (EOF) GO CLEAR FIELD
COMPADDR DS    0H
         CLC   ADDRESS,1(SCRN)    IS THIS THE FIELD
         BNE   SCRNSCAN           NO-GO FIND NEXT FIELD
EXBUFF   DS    0H
         LR    R1,BUFF            SET FOR TRANSLATE
         LR    R14,BUFFLEN        SAVE FOR TRANSLATE LENGTH
         C     R14,=F'255'        BUFFLEN GREATER THAN MAX TRANSLATE
         BNH   EXSBABUF           NO-USE BUFFLEN FOR TRANSLATE
         LA    R14,255            YES-USE MAX TRANSLATE LENGTH
EXSBABUF DS    0H
         EX    R14,FINDSBA        FIND NEXT SBA
         BNZ   MOVEDATA           FOUND IT-GO MOVE DATA
         CR    R14,BUFFLEN        NO SBA FOUND-DID WE LOOK AT
*                                 ALL THE BUFFER....
         BL    TRYABUFF           NO WE DIDN'T- GO TRY NEXT 256 OR LESS
         LR    R1,BUFFEND         YES WE DID-SET END-OF-BUFFER
         LA    R1,1(R1)           END-OF-BUFFER +1
         B     MOVEDATA           NOW GO MOVE THE LAST DATA FIELD
TRYABUFF DS    0H
         SR    BUFFLEN,R14        ADJUST BUFFER LENGTH
         BCTR  BUFFLEN,0
         MVC   5(256,SCRN),0(BUFF)
         LA    SCRN,1(R14,SCRN)   BUMP SCREEN POINTER BY 256
         LA    BUFF,1(R14,BUFF)   BUMP BUFFER POINTER BY 256
         B     EXBUFF             GO LOOK AT NEXT 256 OR LESS FOR SBA
MOVEDATA DS    0H
         LA    R1,0(R1)           CLEAR HIGH ORDER BYTE
         LR    R2,R1              SAVE NEW SBA OR END-OF-BUFFER
         SR    R1,BUFF            LENGTH OF DATA
         BCTR  R1,0               LENGTH OF DATA-1
         EX    R1,MOVFIELD        MOVE BUFF DATA TO SCREEN FIELD
         EX    R1,UPPERCSE        GO SET ALL ALPHA TO UPPER CASE
         LR    BUFF,R2            NEW SBA OR END-OF-BUFFER
         SR    BUFFLEN,R1         NEW BUFFER LENGTH
         BCTR  BUFFLEN,0          ADJUST
         LA    SCRN,6(R1,SCRN)    LAST BYTE MOVED + 1
DATALOOP DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN?????
         BH    SETSCRN            YES-GO LOOK AT NEXT BUFFER FIELD
         CLI   0(SCRN),SBA        NO-AT NEXT FIELDS SBA
         BE    SETSCRN            YES-GO LOOK AT NEXT BUFFER FIELD
         MVI   0(SCRN),X'40'      PAD END OF SCREEN FIELD
         LA    SCRN,1(SCRN)       NEXT SCREEN BYTE
         B     DATALOOP           GO CHECK AGAIN FOR PADDING
         SPACE 3
SCRNSCAN DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN
         BNL   SETSCRN            YES-GO PROCESS NEXT BUFFER FIELD
         LA    SCRN,5(SCRN)       FIELD DATA
         SH    SCRNLEN,=H'5'      ADJUST
         LR    R1,SCRN            SET FOR TRANSLATE
EXSCRN   DS    0H
         LR    R14,SCRNLEN        SAVE SCRNLEN FOR TRANSLATE
         C     R14,=F'255'        SCRNLEN GREATER THAN MAX TRANSLATE
         BNH   EXSBASCR           NO-USE SCRNLEN FOR TRANSLATE
         LA    R14,255            YES-USE MAX TRANSLATE LENGTH
EXSBASCR DS    0H
         EX    R14,FINDSBA        FIND NEXT SBA IN SCREEN
         BNZ   SCRNSBA            FOUND IT-GO ADJUST SCRN POINTERS
         CR    R14,SCRNLEN        NO SBA FOUND- DID WE LOOK AT
*                                 ALL OF THE SCREEN....
         BNL   SETSCRN            YES WE DID-NEXT BUFFER FIELD
*                                 NO WE DIDN'T...
         SR    SCRNLEN,R14        TRY NEXT 256 OR LESS
         LA    SCRN,1(R14,SCRN)   BUMP SCREEN POINTER
         B     EXSCRN             GO LOOK AGAIN...
SCRNSBA  DS    0H
         LA    R1,0(R1)           CLEAR HIGH ORDER BYTE
         LR    SCRN,R1
         CLI   3(SCRN),X'13'      IS IT THE CURSOR FIELD
         BE    SETSCRN            YES-GO PROCESS NEXT BUFFER FIELD
         B     COMPADDR           GO BACK-CHECK FOR FIELD MATCH
         SPACE 3
CLRFIELD DS    0H
         CLC   ADDRESS,1(SCRN)    CHECK FOR FIELD MATCH
         BNE   CLRFLDSC           NO MATCH-FIND NEXT FIELD
         LA    SCRN,5(SCRN)       MATCH-START OF DATA???
         SH    SCRNLEN,=H'5'      ADJUST
CLRLOOP  DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN
         BNL   EXITCLR            YES-GET OUT OF CLEAR FIELD LOGIC
         CLI   0(SCRN),SBA        IS THERE ANY DATA IN FIELD
         BE    EXITCLR            NO-GET OUT OF CLEAR FIELD LOGIC
         MVI   0(SCRN),X'40'      YES-CLEAR THIS BYTE
         LA    SCRN,1(SCRN)       NEXT BYTE
         BCTR  SCRNLEN,0          ADJUST
         B     CLRLOOP            GO CLEAR THIS BYTE??????
CLRFLDSC DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN
         BNL   EXITCLR            YES-GET OUT CLEAR FIELD LOGIC
         LA    SCRN,5(SCRN)       NO-POINT TO FIELD DATA
         SH    SCRNLEN,=H'5'      ADJUST
         LR    R1,SCRN            LOAD FOR TRANSLATE SBA
EXCLR    DS    0H
         LR    R14,SCRNLEN        SAVE FOR TRANSLATE
         C     R14,=F'255'        IS SCRNLEN GREATER THAN MAX TRANSLT
         BNH   EXSBACLR           NO-US THIS LENGTH FOR TRANSLATE
         LA    R14,255            YES-SET LENGTH TO MAX
EXSBACLR DS    0H
         EX    R14,FINDSBA        GO FIND NEXT SBA
         BNZ   CLRSBA             FOUND IT-GO SET UP FOR RETRY
         CR    R14,SCRNLEN        NOT FOUND-AT END OF SCREEN
         BNL   EXITCLR            YES-GET OUT OF CLEAR FIELD LOGIC
         SR    SCRNLEN,R14        NO-ADJUST FOR ANOTHER TRANSLATE
         LA    SCRN,1(R14,SCRN)      ADJUST FOR ANOTHER TRANSLATE
         B     EXCLR              GO TRY TRANSLATE AGAIN
CLRSBA   DS    0H
         LA    R1,0(R1)           CLEAR HI-ORDER BYTE
         LR    SCRN,R1            NEW SBA
         CLI   3(SCRN),X'13'      ARE WE AT THE CURSOR FIELD(LAST)
         BE    EXITCLR            YES-GET OUT OF CLEAR FIELD LOGIC
         B     CLRFIELD           NO-GO LOOK FOR FIELD MATCH AGAIN
EXITCLR  DS    0H
         CR    BUFF,BUFFEND       END OF INPUT BUFFER
         BNL   EXITSCRN           YES-ALL THROUGH
         B     SETSCRN            NO-GO LOOK NEXT BUFFER FIELD
TGETERR  DS    0H
         MVC   PROCERR,=CL40'TGET ERROR'
         B     EXITSCRN
BUFFERR  DS    0H
         MVC   PROCERR,=CL40'NO SBA FOUND IN TGET BUFFER'
         B     EXITSCRN
SCRNERR  DS    0H
         MVC   PROCERR,=CL40'INVALID SCREEN IN GETSCRN LOGIC.'
         B     EXITSCRN
EXITSCRN DS    0H
         L     R13,SAVEGET+4      POINT TO CALLER SAVEAREA FOR RETURN
         RETURN (14,12) GO BACK TO CALLER
         EJECT
SBAMINUS DS    0H                 THIS ROUTINE SUBS 1 FROM 3270 ADDR
         ST    R15,SAVEREG        SAVE
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         BCTR  R14,0              SUB 1 FROM RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         L     R15,SAVEREG        RESTORE BEFORE EXITING
         BR    LINKREG
         EJECT
SBAPLUS  DS    0H                 THIS ROUTINE ADDS 1 TO 3270 ADDR
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         LA    R14,1(R14)         ADD 1 TO RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         BR    LINKREG
         LTORG
TGETASIS DC    X'81'              TGET ASIS OPTION BYTE
BUFTABLE DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F' 64 BYTE TRANSLATE
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' TABLE FOR CONVERT
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' TO 3270 LINE CODE
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
         SPACE 3
SBATABLE DC    17X'00'  256 BYTE TRANSLATE TABLE TO FIND SBA(X'11)
         DC    X'11'    IN THE TGET INPUT BUFFER AND CORRESPONDING 3270
         DC    238X'00' SCREEN......
BLANKS   DC    CL256' '
         EJECT
         LTORG
         COPY VSAMAEQU            VSAMANAL EQUATES
         COPY VSAMAWRK            VSAMANAL COMMON WORKAREA
         END   VSAMAGET
