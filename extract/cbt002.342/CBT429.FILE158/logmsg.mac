/* LOGMSG **************************************************** Rexx */
/*   Created By: IAPPINI,R.                                         */
/*   When: 04/13/93                                                 */
/*   What: LOGMSG inserts lines at the bottom after inserting       */
/*         a time line .  Optional input first(1st) will insert     */
/*         a dayofweek - Mon day - Name  line                       */
/********************************************************************/
Address "ISREDIT"
"MACRO (PARMS)"
Trace "OFF"
"(row) = Linenum .zl"
If row = 0 Then parms = "FIRST"
today = Date(W)
"(PBNDS) = BNDS"
"(lrecl) = LRECL"
"BNDS = 1 "lrecl
"SEEK "today "FIRST"
"(s1,s2) = SEEK_COUNTS"
If s1 = 0 Then parms = "1ST"
If Pos(Translate(parms),"FIRST 1ST") > 0 Then Do
   "(row) = Linenum .zl"
   Address "TSO" "ACFVPUTT"
   rexxline = Left(today,10)"-",
              Date(M) Substr(Date(U),4,2) "-",
              Acfname
   Call Line_after rexxline
   End;
Call Line_after Left(Time(),5) "-"
"BNDS = 9 "lrecl-8
"NUM OFF"
"CAPS OFF"
"RESET"
"TENTER .zl"
"Cursor = "Row "09"
"MEND"

Line_after:
   Parse Arg xx
   " LINE_AFTER "row" = (xx)"
   If rc = 0 Then,
       row = row + 1
   Return;
