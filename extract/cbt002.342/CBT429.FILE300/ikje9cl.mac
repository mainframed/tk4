         TITLE '   C O L S   SUBCOMMAND OF EDIT'
*------------------------------------------------------------------*
*              ' C O L S '  SUBCOMMAND OF EDIT                     *
*                                                                  *
* COURTESY: JIM MARSHALL, CAPT, USAF                               *
*                                                                  *
*        THIS EDIT SUBCOMMAND PERFORMS A SIMILIAR FUNCTION         *
*        OF THE ISPF/PDF 'COLS' COMD OF PDF EDIT. IT WILL          *
*        PLACE LINE NUMBERS ON YOUR TERMINAL AS A GUIDE.           *
*        THE LINES PLACED ON YOUR TERMINAL ARE BASED ON            *
*        THE CODING SHEET COLUMNS FOR THE DIFFERENT LANGUAGES.     *
*        SO FOR INSTANCE CNTL (JCL) IS CODED IN 1-72. BUT          *
*        COBOL HAS SEQUENCE NUMBERS IN 1-6 WITH CODE IN            *
*        7-72. SO THE COBOL 'COLS' LINE WOULD SHOW THE NUMBERS     *
*        FROM 7-72 NOT 1-72 ON YOUR TERMINAL. THUS THIS IS         *
*        MORE REALISTIC THAN THE PDF 'COLS' CMD.                   *
*                                                                  *
* ATTRIBUTES. RE-ENTRANT.                                          *
*                                                                  *
* INSTALLED: 14 JAN 85                                             *
*                                                                  *
* CHNGE LOG: NONE                                                  *
*------------------------------------------------------------------*
         SPACE
COLS     CSECT
         SAVE  (14,12),,COLS_SADSC_&SYSDATE_&SYSTIME
         LR    R12,R15             BASE REGISTER
         USING COLS,R12            GET ADDR
         LR    R9,R1               SAVE R1
         USING IKJEBECA,R9         GET ADDR TO EDIT COMM AREA
         L     R0,@SIZE            GET AMT TO GETMAIN
         GETMAIN R,LV=(0)          DO GET IT
         ST    R13,4(,R1)          CHAIN FORWARD
         ST    R1,8(,R13)          CHAIN BACK
         LR    R13,R1              UPDATE SAVE AREA POINTER
         USING @DATA,R13           GET ADDR TO OUR DSECT
         SPACE
*------------------------------------------------------------------*
*        1ST WE SEE WHERE THE SEQUENCE NUMBERS ARE?  IF THEY       *
*        ARE AT THE FRONT OR BACK AND ALSO WE MUST BE AWARE        *
*        OF HOW MANY THERE ARE. SOME HAVE 6, OTHERS 8. REMINDER,   *
*        WE ARE BASING EVERYTHING ON CODING SHEETS!                *
*                                                                  *
*        ASM     - 73 TO 80    (FB)                                *
*        BASIC   - 1  TO 6     (VB)                                *
*        CLIST   - 1  TO 8     (VB)                                *
*        CNTL    - 73 TO 80    (FB)                                *
*        COBOL   - 1  TO 8     (FB)                                *
*        DATA    - 73 TO 80    (FB)                                *
*        FORTE   - 73 TO 80    (FB)                                *
*        FORTG   - 73 TO 80    (FB)                                *
*        FORTGI  - 73 TO 80    (FB)                                *
*        FORTH   - 73 TO 80    (FB)                                *
*        IPLI    - 73 TO 80    (FB)                                *
*        PLIF    - 73 TO 80    (FB)                                *
*        VSBASIC - 1  TO 6     (VB) BUT CAN SHOW LINE NUMBERS      *
*                              OF 2 DIGITS ON SCREEN.              *
*------------------------------------------------------------------*
         SPACE
         SR    R3,R3               GET READY FOR THIS
         CLI   CADSCODE,CAASM      TYPE = ASM  ?
         BE    NLST8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CADATA     TYPE = DATA ?
         BE    NLST8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CACLIST    TYPE = CLIST ?
         BE    C1TO8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CACNTL     TYPE = CNTL ?
         BE    NLST8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CACOBOL    TYPE = COBOL?
         BE    C1TO6               GO PROCESS CORRECTLY
         CLI   CADSCODE,CAFORTE    TYPE = FORTE ?
         BE    NLST8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CAFORTG    TYPE = FORTG ?
         BE    NLST8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CAFORTGI   TYPE = FORTGI ?
         BE    NLST8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CAGOFORT   TYPE = GOFORT ?
         BE    NLST8               GO PROCESS CORRECTLY
         CLI   CADSCODE,CAVBASIC   TYPE = VSBASIC ?
         BE    V1TO5               GO PROCESS CORRECTLY
         SPACE
*-------------------------------------------------------------------*
*        WE PROCESS THE SEQ NUMBERS IN WHICH ARE AT THE END OF      *
*        THE EDITING FIELD.
*-------------------------------------------------------------------*
NLST8    EQU   *
         TM    CACFLAG1,CANONUM    NONUM??
         BNO   NLST8N
         MVC   MSG(77),COLSLNE     MOVE IN THE COLS LINE
         L     R3,=F'76'           MOVE IN LENGTH OF LINE
         B     DOMSG
NLST8N   EQU   *
         CLC   CACURNUM(4),NUM5    ARE WE BELOW 5 NUMBERS ?
         BL    SEQ5
         CLC   CACURNUM(4),NUM6    ARE WE BELOW 6 NUMBERS ?
         BL    SEQ6
         CLC   CACURNUM(4),NUM7    ARE WE BELOW 7 NUMBERS ?
         BL    SEQ7
SEQ8     EQU   *
         MVC   MSG(9),LINENO
         MVC   MSG+9(68),COLSLNE
         L     R3,=F'76'
         B     DOMSG
SEQ7     EQU   *
         MVC   MSG(8),LINENO+1
         MVC   MSG+8(69),COLSLNE
         L     R3,=F'76'
         B     DOMSG
SEQ6     EQU   *
         MVC   MSG(7),LINENO+2
         MVC   MSG+7(70),COLSLNE
         L     R3,=F'76'
         B     DOMSG
SEQ5     EQU   *
         MVC   MSG(6),LINENO+3
         MVC   MSG+6(70),COLSLNE
         L     R3,=F'76'
         B     DOMSG
         SPACE
*-------------------------------------------------------------------*
*        WE PROCESS THE SEQ NUMBERS IN 1-6 FOR FB/80  (COBOL)       *
*-------------------------------------------------------------------*
         SPACE
C1TO6    EQU   *
         TM    CACFLAG1,CANONUM    NONUM??
         BNO   C1TO6N
         MVC   MSG(72),COLSLNE+6
         L     R3,=F'72'
         B     DOMSG
C1TO6N   EQU   *
         CLC   CACURNUM(4),NUM5    ARE WE BELOW 5 NUMBERS ?
         BL    C1TO6N5
         MVC   MSG(7),LINENO+2
         MVC   MSG+7(72),COLSLNE+6
         L     R3,=F'77'
         B     DOMSG
C1TO6N5  EQU   *
         MVC   MSG(6),LINENO+3
         MVC   MSG+6(71),COLSLNE+6
         L     R3,=F'77'
         B     DOMSG
         SPACE
*-------------------------------------------------------------------*
*        WE PROCESS THE SEQ NUMBERS IN 1-8 FOR VB/255. CLIST        *
*        TYPE WILL NOT ALLOW THE USE OF 'NONUM'; OTHERS MAY, SO     *
*        WE CHECK FOR IT ANYWAY.                                    *
*-------------------------------------------------------------------*
         SPACE
C1TO8    EQU   *
         TM    CACFLAG1,CANONUM    NONUM??
         BNO   C1TO8N
         MVC   MSG(72),COLSLNE+8
         L     R3,=F'72'
         B     DOMSG
C1TO8N   EQU   *
         CLC   CACURNUM(4),NUM5    ARE WE BELOW 5 NUMBERS ?
         BL    C1TO8N5
         CLC   CACURNUM(4),NUM6     WE BELOW 6 NUMBERS ?
         BL    C1TO8N6
         CLC   CACURNUM(4),NUM7      WE BELOW 7 NUMBERS ?
         BL    C1TO8N7
         MVC   MSG(9),LINENO
         MVC   MSG+9(69),COLSLNE+8
         L     R3,=F'76'
         B     DOMSG
C1TO8N5  EQU   *
         MVC   MSG(6),LINENO+3
         MVC   MSG+6(72),COLSLNE+8
         L     R3,=F'76'
         B     DOMSG
C1TO8N6  EQU   *
         MVC   MSG(7),LINENO+2
         MVC   MSG+7(72),COLSLNE+8
         L     R3,=F'76'
         B     DOMSG
C1TO8N7  EQU   *
         MVC   MSG(8),LINENO+1
         MVC   MSG+8(70),COLSLNE+8
         L     R3,=F'76'
         B     DOMSG
*-------------------------------------------------------------------*
*        WE PROCESS THE SEQ NUMBERS IN 1-5 FOR VB/255. VSBASIC      *
*        WILL SHOW DOWN TO TWO NUMBERS AND UP TO 5 WHEN YOU SEE     *
*        IT ON THE SCREEN.                                          *
*-------------------------------------------------------------------*
         SPACE
V1TO5    EQU   *
         TM    CACFLAG1,CANONUM    NONUM??
         BNO   V1TO5N
         MVC   MSG(76),COLSLNE
         L     R3,=F'76'
         B     DOMSG
V1TO5N   EQU   *
         CLC   CACURNUM(4),NUM2    ARE WE BELOW 2 NUMBERS ?
         BL    V1TO5N2
         CLC   CACURNUM(4),NUM3     WE BELOW 3 NUMBERS ?
         BL    V1TO5N3
         CLC   CACURNUM(4),NUM4      WE BELOW 4 NUMBERS ?
         BL    V1TO5N4
         MVC   MSG(6),LINENO+3
         MVC   MSG+9(69),COLSLNE+8
         L     R3,=F'76'
         B     DOMSG
V1TO5N2  EQU   *
         MVC   MSG(3),LINENO+6
         MVC   MSG+3(72),COLSLNE+5
         L     R3,=F'76'
         B     DOMSG
V1TO5N3  EQU   *
         MVC   MSG(4),LINENO+5
         MVC   MSG+4(72),COLSLNE+5
         L     R3,=F'76'
         B     DOMSG
V1TO5N4  EQU   *
         MVC   MSG(5),LINENO+4
         MVC   MSG+5(70),COLSLNE+5
         L     R3,=F'76'
         B     DOMSG
DOMSG    EQU   *
         LR    R0,R3               LOAD LENGTH OF TEXT
         LA    R1,MSG              POINT TO MSG
         TPUT (1),(0),R
         SPACE
EXIT     SR    15,15
         LR    R1,R13
         L     R0,@SIZE
         L     R13,4(,R13)
         ST    R15,16(,R13)
         FREEMAIN R,A=(1),LV=(0)
         LM    R14,R12,12(R13)
         BR    R14
         EJECT
*------------------------------------------------------------------*
*        CONSTANTS FOLLOW                                          *
*------------------------------------------------------------------*
         SPACE
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
NUM2     DC    F'100'              99 SEQUENCE NUMBER
NUM3     DC    F'1000'             999 SEQUENCE NUMBER
NUM4     DC    F'10000'            9999 SEQUENCE NUMBER
NUM5     DC    F'100000'           99999 SEQUENCE NUMBER
NUM6     DC    F'1000000'          999999 SEQUENCE NUMBER
NUM7     DC    F'10000000'         9999999 SEQUENCE NUMBER
LINENO   DC    CL9'XXXXXXXX '
COLSLNE  DC    CL80'----+----1----+----2----+----3----+----4----+----5-X
               ---+----6----+----7----+----8'
         SPACE
         LTORG
         DS    0D
         SPACE
@DATA    DSECT
         DS    18F
MSG      DS    CL110               LITTLE LONG OK
@DATAL   EQU   *-@DATA
         SPACE
         DS    0D
         IKJEBECA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
