         TITLE '   N A M E   S U B C O M M A N D   O F   E D I T'
************************************************************
*                                                          *
*        'NAME' SUBCOMMAND OF EDIT                         *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY, PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC COMPUTER CENTER INC, MCLEAN VA.
*        DATE WRITTEN. JULY 16 1975.
*        DATE UPDATED. DECEMBER 6 1978.
*        ATTRIBUTES. RE-ENTRANT.
*        REMARKS.
*            THIS EDIT SUBCOMMAND DISPLAYS THE NAME AND TYPE
*            OF THE DATASET BEING EDITED.
*            07DEC78 - ASIS ADDED
         SPACE
NAME     START
         USING *,R12
         B     @BEGIN-*(,15)
         DC    AL1(11),CL11'NAME     '
         DC    CL16' &SYSDATE &SYSTIME ',C'PRC '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@BEGIN   STM   14,12,12(13)
         LR    R12,R15             BASE REGISTER
         LR    R9,R1
         USING IKJEBECA,R9
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         ST    R13,4(,R1)          CHAIN FORWARD
         ST    R1,8(,R13)          CHAIN BACK
         LR    R13,R1              UPDATE SAVE AREA POINTER
         USING @DATA,R13
         SPACE
         LA    R2,MESSAGE
         MVI   0(R2),C' '
         MVC   1(L'MESSAGE-1,R2),0(R2)  CLEAR MESSAGE
         MVI   0(R2),X'7D'
         LA    R2,1(,R2)
         LH    R1,CAEDDSNL
         BCTR  R1,0
         B     *+10
         MVC   0(0,R2),CAEDDSN
         EX    R1,*-6              MOVE EDIT DSNAME TO MESSAGE
         LA    R2,1(R1,R2)         POINT PAST LAST CHARACTER MOVED
         LA    R14,CAEDMEMB
         CLI   0(R14),C' '         MEMBER SPECIFIED?
         BE    ENDQUOTE            NO - BRANCH
         MVI   0(R2),C'('
         LA    R2,1(,R2)
         LA    R0,8                MAXIMUM LENGTH OF MEMBERNAME
MEMBLOOP CLI   0(R14),C' '
         BE    MEMBEND
         MVC   0(1,R2),0(R14)      MOVE A BYTE OF MEMBERNAME
         LA    R2,1(,R2)
         LA    R14,1(,R14)
         BCT   R0,MEMBLOOP
MEMBEND  MVI   0(R2),C')'
         LA    R2,1(,R2)
ENDQUOTE MVI   0(R2),X'7D'
         LA    R2,2(,R2)
         MVC   0(8,R2),CADSTYPE
TYPELOOP CLI   1(R2),C' '
         BE    TYPEND
         LA    R2,1(,R2)
         B     TYPELOOP
TYPEND   LA    R2,2(,R2)
         TM    CACFLAG1,CACAPS
         BO    CAPS
         MVC   0(5,R2),=C'ASIS '
         LA    R2,5(,R2)
CAPS     EQU   *
         SPACE
         LA    R0,L'MESSAGE
         LA    R1,MESSAGE
         TPUT (1),(0),R
         SPACE
EXIT0    SR    15,15
EXIT     LR    R1,R13
         L     R0,@SIZE
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(13)
         BR    R14
         SPACE
         LTORG
         DC    0D'0'
         SPACE
@DATA    DSECT
         DS    18F
MESSAGE  DS    CL72
@DATAL   EQU   *-@DATA
         SPACE
         IKJEBECA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
