         TITLE 'AFDSC - IKJEFF53  TSO  FIB INSTALLATION EXIT'
*-$DOC@---------------------------------------------------------------*
*                                                                     *
*                 I  K  J  E  F  F  5  3                              *
*                                                                     *
*                                                                     *
*        THIS IS A MODIFIED VERSION OF THE IPO SUPPLIED IKJEFF53,     *
*        FIB INSTALLATION EXIT. WE WANTED TO GIVE THE GOOD GUYS,      *
*        (SYSTEMS JOCKS) THE ABILITY TO GO INTO JOBS THAT BEGIN       *
*        THE JOBNAMES #SMIXXXX. THIS IS BECAUSE WE RUN OUR DAILY      *
*        AND WEEKLY JOBS TO DUMP PACKS, ETC, ETC. IT GENERATES ALOT   *
*        OF PAPER, SO WE WANT TO HOLD THE OUTPUT, REVIEW FROM         *
*        OUT TSO TERMINAL AND PURGE. SAVES ALOT OF PAPER, PLUS        *
*        SECURITY WANTS US TO SIGN RECEIPTS FOR ALL LISTINGS          *
*        PRINTED. ( BA-HUMBUG )!!!!!!!                                *
*                                                                     *
*        WE ALSO WANT TO KEEP IN THE FUNCTION OF DISALLOWING          *
*        OUTPUT, STATUS, OR CANCEL FROM BEING USED IN BATCH           *
*        BY RUNNING THE TMP. NOT TOO COOL.                            *
*                                                                     *
*                                                                     *
* COURTESY :  JIM MARSHALL                                            *
*             AIR FORCE DATA SERVICES CENTER                          *
*             IBM OPERATING SYSTEMS SUPPORT BRANCH                    *
*             PENTAGON - ROOM 1C1039                                  *
*             WASHINGTON D.C.  20330                                  *
*             (202) 695-1274/2478                                     *
*                                                                     *
*                                                                     *
*         A.  DESCRIPTIVE NAME =  MVS SYSTEM IPO SUPPLIED FIB EXIT    *
*                FOR TSO CANCEL, OUTPUT AND STATUS COMMANDS,          *
*                MODIFIED BY AFDSC.    J.MARSHALL (JDM)  4 JAN 82     *
*                                                                     *
*         B.  FUNCTION =                                              *
*                VALIDITY CHECKS JOBNAME ON A CANCEL, OUTPUT OR       *
*                STATUS FIB (FOREGROUND INITIATED BACKGROUND)         *
*                COMMAND.  USERIDS BEGINNING WITH ASMI CAN VIEW       *
*                THERE OWN JOBS AND ALL JOBS BEGINNING WITH #SMI      *
*                                                                     *
*                                                                     *
*         C.      DEPENDENCIES = CHARACTER IS EBCDIC.  REASSEMBLE     *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*         D.      RESTRICTIONS = NONE                                 *
*                                                                     *
*         E.      REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0,1     = WORK REGISTERS               *
*                    REGISTER  2       = MODESET KEYADDR              *
*                    REGISTER  3       = WORK REGISTER                *
*                    REGISTERS 4       = ADDRESSABILITY TO EXIT       *
*                                              PARAMETER LIST         *
*                    REGISTER  5       = RETURN CODE / ASVT MAXUSERS  *
*                    REGISTERS 6,7,8,9 = WORK REGISTERS               *
*                    REGISTER  10      = ADDRESSABILITY TO REJECT     *
*                                              MESSAGE GETMAINED AREA *
*                    REGISTER  11      = ADDRESSABILITY TO GETMAINED  *
*                                              DATA AREA              *
*                    REGISTER  12      = ADDRESSABILITY TO IKJEFF53   *
*                                              CSECT                  *
*                    REGISTER  13      = SAVE AREA REGISTER           *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*         F.     PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   'ZAP*'S)                                          *
*                                                                     *
*         G.  MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*         H.     PROCESSOR = ASM                                      *
*                                                                     *
*         I.     MODULE SIZE = <1K                                    *
*                                                                     *
*         J.     ATTRIBUTES = PROTECT KEY 1, REENTRANT,               *
*                   SUPERVISOR STATE                                  *
*                                                                     *
*         K.  ENTRY POINTS = IKJEFF53 (ONLY ENTRY POINT)              *
*                                                                     *
*                LINKAGE =                                            *
*                   IKJEFF51:  (CANCEL OR STATUS COMMAND) VIA CALL    *
*                   IKJCT469:  (OUTPUT COMMAND) VIA CALL              *
*                                                                     *
*         L.  INPUT = REGISTER 1 POINTS TO PARAMETER LIST MAPPED      *
*                BY IKJEFFIE MACRO                                    *
*                                                                     *
*         M.  OUTPUT = SEE EXIT TOPICAL HEADING BELOW                 *
*                                                                     *
*         N.  EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*         O.  EXIT - ERROR = AT PROGRAM END VIA BRANCH REGISTER 14    *
*                                                                     *
*                OUTPUT = MESSAGEP SET IN THE INPUT PARAMETER LIST.   *
*                                                                     *
*                RETURN CODE =                                        *
*                   4  - ISSUE PROMPT AND RETURN REPLY   (IEPROMPT)   *
*                   8  - ISSUE MESSAGE AND RETURN        (IEMSG)      *
*                   12 - REJECT THIS JOB                 (IEREJECT)   *
*                   16 - TERMINATE THIS COMMAND          (IEABORT)    *
*                   (THIS VERSION USES ERROR CODE 8 AND 12)           *
*                                                                     *
*         P.  EXTERNAL REFERENCES = NONE                              *
*                                                                     *
*                ROUTINES = NONE                                      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = PARMLIST.                           *
*                                                                     *
*         Q.  TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL DATA.                             *
*                                                                     *
*         R.  MACROS = IKJEFFIE, FREEMAIN, GETMAIN, SAVE.             *
*                                                                     *
*         S.  CHANGE ACTIVITY =                                       *
*                                                                     *
*             1  COMPARE OF USERID AND JOBNAME LENGTH MOVED        CUJ*
*             2  CHECK FOR INVALID JOBNAME ADDED                   CUJ*
*             3  REMOVE THE CODE FOR DOING A CMD ' ST TSO ' TO     JDM*
*                GIVE THE SAME INFO AS OUR TSOCP CALLED  DUSER.    JDM*
*                                                                     *
*                                                                     *
*         T.  MESSAGES =                                              *
*                                                                     *
*               'REJECTED - JOBNAME MUST BE YOUR USERID PLUS          *
*                     ATLEAST ONE CHARACTER'                          *
*                                                                     *
*               'REJECTED - JOBNAME MUST BE YOUR USERID OR MUST       *
*                     START WITH YOUR USERID'                         *
*                                                                     *
*         U.  ABEND CODES = NONE                                      *
*                                                                     *
*-$DOC$---------------------------------------------------------------*
         EJECT
IKJEFF53 CSECT
         SAVE  (14,12),,IKJEFF53.AFDSC.&SYSTIME..&SYSDATE
*                                      WITH CSECT IDENTIFIER
         BALR  R12,0                   BRANCH AND LOAD REGISTER 12
PSTART   DS    0H                      LABEL USED WHEN ESTABLISHING
         USING PSTART,R12              CSECT ADDRESSABILITY
         L     R0,SIZDATD              LOAD REGISTER 0 WITH SIZE FOR
*                                      GETMAIN
         GETMAIN R,LV=(0)              ISSUE REGISTER FORM OF GETMAIN
*                                      FOR AREA IN SUBPOOL 0
         LR    R11,R1                  LOAD REGISTER 11 WITH ADDRESS OF
*                                      GETMAINED AREA
         USING DATD,R11                ESTABLISH ADDRESSABILITY TO
*                                      GETMAINED AREA
         ST    R13,SAVEAREA+4          SAVE REGISTER 13 FOR SAVEAREA
*                                      CHAINING
         LM    R0,R1,20(R13)           RELOAD REGISTERS 0 AND 1 FROM
*                                      PREVIOUS SAVEAREA
         ST    R11,8(,R13)             SAVE ADDRESS OF CURRENT SAVEAREA
         LR    R13,R11                 LOAD REGISTER 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         LR    R4,R1                   LOAD REGISTER 4 WITH POINTER TO
*                                      PARAMETER LIST
         USING IEPARML,R4              ESTABLISH ADDRESSABILITY TO
*                                      PARAMETER LIST (USES
*                                      IKJEFFIE MAPPING MACRO)
         LA    R5,IECONTIN             INITIALIZE REGISTER 5 FOR RETURN
*                                      CODE FOR NORMAL CASE - JOBNAME
*                                      IS GOOD AND COMMAND SHOULD
*                                      CONTINUE PROCESSING
         SPACE 2
*---------------------------------------------------------------------*
*        ALWAYS DO PROCESSING                                         *
*---------------------------------------------------------------------*
         SPACE 2
         L     R6,IECODEP              LOAD COMMAND CODE POINTER
         CLI   0(R6),IECANCEL          SEE IF COMMAND IS CANCEL
         BE    PROCESS                 YES....CONTINUE PROCESSING
         CLI   0(R6),IEOUTPUT          SEE IF COMMAND IS OUTPUT
         BE    PROCESS                 YES....CONTINUE PROCESSING
         CLI   0(R6),IESTATUS          SEE IF COMMAND IS STATUS
         BNE   ENDPROC                 NO.....DO NO PROCESSING
*                                      IF INVALID CODE
         SPACE 2
*---------------------------------------------------------------------*
*        SEE IF SECOND TIME ENTRY FOR A JOB THEN THE MSG POINTER      *
*        NOT ZERO, THUS WE MUST CLEANUP AFTER OURSELVES.              *
*---------------------------------------------------------------------*
         SPACE 2
PROCESS  DS    0H
         L     R6,IEMSGP               LOAD MESSAGE POINTER
         LTR   R6,R6                   SEE IF MESSAGE ISSUED FOR LAST
*                                      ENTRY
         BNZ   FREEMESS                YES....GO FREE MESSAGE BUFFER
         SPACE 2
*---------------------------------------------------------------------*
*        DETERMINE IF THIS IS FOR CANCEL                              *
*           NO.....GO TO OUTPUT ROUTINE                               *
*---------------------------------------------------------------------*
         SPACE 2
VALIDITY DS    0H
         L     R6,IECODEP              LOAD COMMAND POINTER
         CLI   0(R6),IECANCEL          SEE IF COMMAND IS CANCEL
         BNE   OUTPUT                  NO.....GO SEE IF OUTPUT COMMAND
*                                      O.K. TO CANCEL ANY JOBNAME
         L     R15,IEIDLENP            LOAD USERID LENGTH POINTER
         CLI   0(R15),0                CHECK WHETHER JOBNAME IS VALID
         BE    BADJOBN                 JOBNAME IS INVALID IF USERID
*                                      IS NOT AVAILABLE (LENGTH=0 FOR
*                                      FOR TSO COMMANDS IN BACKGROUND)
         SLR   R14,R14                 ZERO REGISTER 14
         IC    R14,0(,R15)             INSERT USERID LENGTH INTO
*                                      REGISTER 14 AND LOAD
         L     R6,IENAMELP             LOAD JOBNAME LENGTH POINTER  CUJ
         CH    R14,0(,R6)              SEE IF USERID LENGTH IS      CUJ
*                                      SHORTER THAN JOBNAME LENGTH  CUJ
         BNL   BADJOBN                 NO.....GO BUILD INVALID      CUJ
*                                      JOBNAME MESSAGE              CUJ
         L     R6,IEUSRIDP             LOAD USERID POINTER          CUJ
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER         CUJ
         BCTR  R14,0                   DECREMENT R14 FOR            CUJ
         EX    R14,JOBNCOMP            EXECUTE OF COMPARE           CUJ
         BNE   BADJOBN                 NO.....GO BUILD INVALID JOBNAME
*                                      MESSAGE
         B     ENDPROC                 GO ACCEPT JOBNAME
         SPACE 2
*---------------------------------------------------------------------*
*        SEE IF THIS IS OUTPUT COMMAND                                *
*           NO.....GO TO STATUS ROUTINE                               *
*           YES....IF USER HAS OPERATOR AUTHORITY THEN O.K.           *
*              THEN IT IS OK TO RUN TMP IN BATCH FOR OUTPUT           *
*              ELSE CHECK IF JOBNAME IS VALID FOR OUTPUT              *
*              OUTPUT NEEDS JOBNAME EQUAL TO USERID OR                *
*              EQUAL TO USERID PLUS AT LEAST ONE CHARACTER            *
*---------------------------------------------------------------------*
         SPACE 2
OUTPUT   DS    0H
         CLI   0(R6),IEOUTPUT          SEE IF COMMAND IS OUTPUT
         BNE   STATUS                  NO.....GO SEE IF STATUS COMMAND
         EXTRACT PSCBADDR,'S',FIELDS=PSB,MF=(E,EXTLIST)
*                                      GET ADDRESS OF PROTECTED STEP
*                                      CONTROL BLOCK
         L     R1,PSCBADDR             LOAD POINTER TO PSCB
         TM    PSCBATR1(R1),PSCBCTRL   SEE IF USER HAS OPERATOR
*                                      AUTHORITY
         BO    CONTCHK                 YES-GO TO END OF PROCEDURE   JDM
*                                      O.K. TO OUTPUT ANY JOBNAME
         L     R15,IEIDLENP            LOAD USERID LENGTH POINTER
         CLI   0(R15),0                CHECK WHETHER JOBNAME IS VALID
         BE    BADJOBN                 JOBNAME IS INVALID IF USERID
*                                      IS NOT AVAILABLE (LENGTH=0 FOR
*                                      FOR TSO COMMANDS IN BACKGROUND)
CONTCHK  EQU   *
         SLR   R14,R14                 ZERO REGISTER 14
         IC    R14,0(,R15)             INSERT USERID LENGTH INTO
*                                      REGISTER 14
         L     R6,IENAMELP             LOAD JOBNAME LENGTH POINTER  CUJ
         CH    R14,0(,R6)              SEE IF USERID LENGTH         CUJ
*                                      IS EQUAL TO OR SHORTER       CUJ
*                                      THAN JOBNAME LENGTH          CUJ
         BH    BADJOBN                 NO.....GO BUILD INVALID      CUJ
*                                      JOBNAME MESSAGE              CUJ
         L     R6,IEUSRIDP             LOAD USERID POINTER          CUJ
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER         CUJ
         BCTR  R14,0                   DECREMENT R14 FOR
         EX    R14,JOBNCOMP            EXECUTE OF COMPARE
         BE    ENDPROC                 EQUAL..RETURN TO CALLER
         SPACE 2                                                    JDM
*-------------------------------------------------------------------JDM
*        CHECK FOR #SMI.... JOBS WHICH ARE HELD & ALSO TO SEE IF    JDM
*        IF YOU ARE ONE OF THE CHOSEN FEW.                          JDM
*-------------------------------------------------------------------JDM
         SPACE 2                                                    JDM
CHKCMIS  EQU   *                                                    JDM
         CLC   ASMI,0(R6)              SEE IF IT IS A CMIS PERSON   JDM
         BNE   BADJOBN                 IF NOT THEN, BAD JOBNAME     JDM
         CLC   #SMI,0(R1)              DOES IT BEGIN WITH #SMI ?    JDM
         BE    ENDPROC                 IF SO, LET IT PASS OK        JDM
         SPACE 2                                                    JDM
*---------------------------------------------------------------------*
*        REJECT THIS INVALID JOBNAME AND BUILD INVALID JOBNAME        *
*           MESSAGE                                                   *
*---------------------------------------------------------------------*
         SPACE 2
BADJOBN  DS    0H                      BUILD BAD JOBNAME MESSAGE
         L     R0,GETMINFO             SUBPOOL 0, LENGTH DECIMAL 84
         GETMAIN R,LV=(0)              GET MESSAGE BUFFER
         ST    R1,IEMSGP               STORE POINTER TO MESSAGE BUFFER
*                                      INTO PARAMETER LIST
         LR    R10,R1                  LOAD REGISTER 10 WITH ADDRESS OF
*                                      GETMAINED AREA
         USING REJMSG,R10              ESTABLISH REGISTER 10 AS BASE
*                                      FOR REJECT MESSAGE DSECT
         MVI   REJMSG,BLANK            INITIALIZE BUFFER WITH BLANKS
         MVC   REJMSG+1(83),REJMSG
         MVC   REJMLEN(2),HALF84       PUT IN MAXIMUM MESSAGE LENGTH
         MVC   REJJOB(4),JOBWORD       PUT IN WORD 'JOB'
         L     R1,IENAMELP             LOAD JOBNAME LENGTH POINTER
         LH    R15,0(,R1)              LOAD JOBNAME LENGTH
         LR    R14,R15                 LOAD REGISTER 14 WITH LENGTH
         BCTR  R14,0                   DECREMENT REGISTER 14 FOR MOVE
         L     R1,IEJOBNMP             LOAD JOBNAME POINTER
         EX    R14,MOVEJOBN            INSERT THE BAD JOBNAME
         AH    R15,HALF2               INDEX PAST BLANK AFTER JOBNAME
         LR    R5,R15                  LOAD REGISTER 5 AS TEMPORARY
*                                      SAVE AREA FOR REGISTER 15
         SPACE 2
*---------------------------------------------------------------------*
*        DIFFERENT MESSAGE TEXTS FOR CANCEL AND OUTPUT. USED THE      *
*        SAME MSG FOR STATUS AS OUTPUT                                *
*---------------------------------------------------------------------*
         SPACE 2
         L     R1,IECODEP              LOAD COMMAND POINTER
         CLI   0(R1),IESTATUS          SEE IF COMMAND IS STATUS
         BE    OUTPMESS                YES....GO TO OUTPUT OR ST MSG
         CLI   0(R1),IECANCEL          SEE IF COMMAND IS CANCEL
         BNE   OUTPMESS                NO.....GO TO OUTPUT MESSAGE
         ALR   R10,R5                  OFFSET MESSAGE IN BUFFER
         MVC   REJSLOT-1(66),REJTEXT1  MOVE IN TEXT FOR CANCEL
         B     SETRC                   GO SET RETURN CODE
OUTPMESS DS    0H
         ALR   R10,R5                  OFFSET MESSAGE IN BUFFER
         MVC   REJSLOT-1(69),REJTEXT2  MOVE IN TEXT FOR OUTPUT & ST
SETRC    DS    0H
         LA    R5,IEMSG                SET RETURN CODE TO PROMPT
         B     ENDPROC                 GO TO RETURN
         SPACE 2
*---------------------------------------------------------------------*
*        SEE IF THIS IS STATUS COMMAND                                *
*           NO.....GO TO END OF PROCEDURE                             *
*           YES....CHECK TO SEE IF THE COMMAND IS COMING FROM THE     *
*                  TMP BEING EXECUTED IN BATCH. THIS IS A NO-NO.      *
*                  WE GIVE THE PERSON THE SAME NASTY-GRAM AS YOU      *
*                  GET FROM THE OUTPUT COMMAND. CONFUSING ?? GOOD     *
*                  HOPEFULLY THEN THE PERSON WILL COME DOWN AND ASK   *
*                  US WHAT IS HAPPENING !!!  IF SO, WE GOTCHA.        *
*---------------------------------------------------------------------*
         SPACE 2
STATUS   DS    0H
         CLI   0(R6),IESTATUS          SEE IF COMMAND IS STATUS     JDM
         BNE   ENDPROC                 NO..GO TO END OF PROCEDURE   JDM
         L     R15,IEIDLENP            LOAD USERID LENGTH POINTER   JDM
         CLI   0(R15),0                SEE IF FROM TMP              JDM
         BE    BADJOBN                 GOTCHA, YOU RASCAL           JDM
         B     ENDPROC                 RETURN OK                    JDM
         SPACE 2
*---------------------------------------------------------------------*
*        SECOND TIME ENTRY (FREE MESSAGE BUFFER)                      *
*---------------------------------------------------------------------*
         SPACE 2
FREEMESS DS    0H
         L     R1,IEMSGP               LOAD POINTER TO GETMAINED AREA
         L     R0,GETMINFO             LOAD SUBPOOL NUMBER AND LENGTH
*                                      OF GETMAINED AREA
         FREEMAIN  R,LV=(0),A=(1)      FREE MESSAGE BUFFER
         SLR   R6,R6                   ZERO OUT MESSAGE POINTER ENTRY
         ST    R6,IEMSGP               IN PARMLIST
         LA    R5,IEREJECT             SET RETURN CODE TO 12 - DELETE
*                                      ENTRY
         SPACE 2
*---------------------------------------------------------------------*
*        RETURN TO CALLER (STATUS, CANCEL, OR OUTPUT COMMAND)         *
*---------------------------------------------------------------------*
         SPACE 2
ENDPROC  DS    0H
         L     R13,4(,R13)             RESTORE REGISTER 13
         L     R0,SIZDATD              LOAD REGISTER 0 WITH SIZE OF
*                                      GETMAINED AREA
         LR    R1,R11                  LOAD REGISTER 1 WITH ADDRESS OF
*                                      GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         LR    R15,R5                  LOAD REGISTER 15 WITH RETURN
*                                      CODE
         L     R14,12(,R13)            LOAD REGISTER 14 WITH RETURN
*                                      ADDRESS
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         BR    R14                     RETURN VIA REGISTER 14
         EJECT
*--------------------------------------------------------------------*
*        CONSTANTS AND DSECTS                                        *
*--------------------------------------------------------------------*
         SPACE 2
DATA     DS    0H
JOBNCOMP CLC   0(0,R6),0(R1)
MOVEJOBN MVC   REJSLOT(0),0(R1)
HALF84   DC    H'84'
HALF2    DC    H'2'
JOBWORD  DC    CL4'JOB '
BLANK    EQU   C' '
ASMI     DC    CL4'ASMI'                                           JDM
#SMI     DC    CL4'#SMI'                                           JDM
DATD     DSECT
         DS    0F
SAVEAREA DS    18F
EXTLIST  EXTRACT MF=L
PSCBADDR DS    F
PSCBATR1 EQU   16
PSCBCTRL EQU   X'80'
         ORG
         DS    0D
ENDDATD  EQU   *
IKJEFF53 CSECT
         DS    0F
SIZDATD  DC    AL1(0)
         DC    AL3(ENDDATD-DATD)
REJTEXT1 DC    CL66'REJECTED - JOBNAME MUST BE YOUR USERID PLUS AT LEASC
               T ONE CHARACTER'
REJTEXT2 DC    CL69'REJECTED - JOBNAME MUST BE YOUR USERID OR MUST STARC
               T WITH YOUR USERID'
GETMINFO DS    0F                      SUBPOOL AND LENGTH FOR GETMAIN
*                                      OF MESSAGE AREA
GETMSP   DC    AL1(0)
GETMLEN  DC    AL3(84)
PATCH    DC    8CL4'ZAP*'
R0       EQU   00                      EQUATES FOR REGISTERS 0-15
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
REJMSG   DSECT                         DSECT FOR RETURN MESSAGE
REJMLEN  DS    H
REJJOB   DS    CL4
REJSLOT  DS    CL9
         IKJEFFIE IETYPE=OUTPUT
         END   IKJEFF53
