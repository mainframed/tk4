         TITLE '   S Y S O U T 9 9  '
***********************************************************************
*                                                                     *
*        'SYSOUT99' SUBPROGRAM                                        *
*                                                                     *
***********************************************************************
*
*        WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
*        INSTALLATION. PRC, MCLEAN VA
*        DATE WRITTEN. NOV 21 1975
*        DATE UPDATED. JANUARY 26 1982.
*        ATTRIBUTES. RE-ENTRANT.
*        MACROS. GETMAIN FREEMAIN LINK IEFZB4D0 IEFZB4D2 IKJEFFDF.
*
*         THIS SUBPROGRAM ALLOCATES A SYSOUT DATASET.
*         THE OPERATING SYSTEM MUST BE OS/VS2 MVS.
*
*         AT ENTRY REGISTER 1 POINTS TO A FULLWORD
*         WHICH CONTAINS THE ADDRESS OF THE BLOCK DESCRIBED
*         BY THE O99DSECT DSECT IN THIS CODE.
*
*         UPON RETURN TO CALLER, REGISTER 15 IS ZERO IF THE
*         ALLOCATION WAS SUCCESSFUL.  OTHERWISE REGISTER 15
*         IS NONZERO AND A MESSAGE WAS ISSUED BY IKJEFF18.
*
*         30MAR78 - FORM NUMBER ADDED
*         03DEC80 - O99DSECT FIELDS SHUFFLED. COMMENTS ADDED.
*                   FREE=CLOSE FORCED IF INTRDR.
*         02MAR81 - ATTRIBUTE LIST NAME ADDED.
*                   SUBPROGRAM RENAMED SYSOUT99 FROM SYSOOT.
*                   SYMBOL 'PRM' PREFIX CHANGED TO 'O99'.
*         10APR81 - DEST LENGTH MVC CHANGED FROM 7 TO 8.
*         26JAN82 - RETURN CODE LOADED FROM MYDFRC.
*
*         O99DD   DS CL8      DDNAME (OR BLANKS IF TO BE RETURNED)
*         O99SO   DS C        SYSOUT CLASS
*         O99FC   DS C        'C' IF FREE=CLOSE REQUESTED
*         O99HO   DS C        'H' IF HOLD=YES REQUESTED
*         O99RSV1 DS C        RESERVED
*         O99CPPL DS A        ADDRESS OF CPPL OR ZERO (FOR MESSAGE)
*         O99DE   DS CL8      DEST OR BLANKS
*         O99PG   DS CL8      PROGRAM NAME OR 'INTRDR' OR BLANKS
*         O99AT   DS CL8      ATTRIBUTE LIST NAME
*         O99FO   DS CL4      FORM NUMBER OR BLANKS
*         O99CO   DS H        COPIES
*         O99RSV2 DS CL2      RESERVED
*         O99RSC  DS CL4      REASON CODES RETURNED HERE
*
*$DOC$*****************************************************************
         SPACE
SYSOUT99 START
         ENTRY SYSO99         SHORT NAME FOR PL1 AND FORTRAN
         USING *,R12
SYSO99   B     @PROLOG-*(,15)
         DC    AL1(11),CL11'SYSOUT99 '
         DC    CL16' &SYSDATE &SYSTIME '
@PROLOG  STM   14,12,12(R13)
         LR    R12,R15
         LR    R2,R1               SAVE PARAMETER LIST ADDRESS
         GETMAIN R,LV=@DATAL
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING @DATA,R13
         SPACE
         L     R2,0(,R2)           POINT TO PARAMETER BLOCK
         USING O99DSECT,R2
         SPACE
         USING S99TUNIT,R3
         SPACE
         LA    R1,MY99RB
         ST    R1,MY99RBP
         OI    MY99RBP,X'80'
         XC    MY99RB(20),MY99RB
         LA    R14,MY99RB
         USING S99RB,R14
         MVI   S99RBLN,20
         MVI   S99VERB,S99VRBAL
         LA    R1,MY99TUPL
         ST    R1,S99TXTPP
         DROP  R14                 S99RB
         SPACE
         XC    MY99TUPL(TUPL$LEN),MY99TUPL
         OI    MY99TUPL+TUPL$LEN-4,X'80'
*
*              PROCESS DDNAME
*
         MVI   MYDDRET,0
         LA    R3,MY99TXDD
         ST    R3,MY99TUDD
         CLI   O99DD,X'40'         DD TO BE RETURNED?
         BE    DDRET               YES - BRANCH
         CLI   O99DD,X'00'         DD TO BE RETURNED?
         BE    DDRET               YES - BRANCH
*
*              DDNAME IS SPECIFIED
*
         LA    R15,DALDDNAM
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8
         STH   R15,S99TULNG
         MVC   S99TUPAR(8),O99DD
         B     PROCSO
*
*              DDNAME NOT SPECIFIED
*
DDRET    LA    R15,DALRTDDN
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8
         STH   R15,S99TULNG
         MVC   S99TUPAR(8),=CL8' '
         MVI   MYDDRET,C'R'
*
*              PROCESS SYSOUT CLASS
*
PROCSO   LA    R3,MY99TXSO
         ST    R3,MY99TUSO
         LA    R15,DALSYSOU
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,1
         STH   R15,S99TULNG
         MVC   S99TUPAR(1),O99SO
         CLI   O99SO,X'40'         CLASS SPECIFIED?
         BE    NOSO                NO - BRANCH
         CLI   O99SO,X'00'         CLASS SPECIFIED?
         BNE   XITSO               YES - BRANCH
NOSO     SR    R15,R15             INDICATE NO ENTRIES
         STH   R15,S99TUNUM         FOR THIS KEY
XITSO    EQU   *
*
*              PROCESS COPIES
*
         CLC   O99CO,=H'0'         IS COPIES ZERO
         BZ    NOCO                YES, DO NOT SPECIFY COPIES
         LA    R3,MY99TXCO
         ST    R3,MY99TUCO
         LA    R15,DALCOPYS
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
*                                  O99CO IS 2 BYTES TO ALLOW COBOL
*                                  CALLERS TO SPECIFY PIC S(2) COMP.
*                                  COBOL DOESNT HAVE 1 BYTE COMP.
         LA    R15,1               SVC99 ONLY ALLOWS 1 BYTE.
         STH   R15,S99TULNG
         MVC   S99TUPAR(1),O99CO+1
NOCO     EQU   *
*
*              PROCESS DEST
*
         CLI   O99DE,X'40'         DEST?
         BE    NODE                NO - BRANCH
         CLI   O99DE,X'00'         DEST?
         BE    NODE                NO - BRANCH
         LA    R3,MY99TXDE
         ST    R3,MY99TUDE
         LA    R15,DALSUSER
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8               LENGTH
         STH   R15,S99TULNG
         MVC   S99TUPAR(8),O99DE
NODE     EQU   *
*
*              PROCESS HOLD
*
         CLI   O99HO,C'H'          HOLD?
         BNE   NOHO                NO - BRANCH
         LA    R3,MY99TXHO
         ST    R3,MY99TUHO
         LA    R15,DALSHOLD
         STH   R15,S99TUKEY
         LA    R15,0
         STH   R15,S99TUNUM
NOHO     EQU   *
*
*              PROCESS PROGRAM NAME OR INTRDR
*
         CLI   O99PG,X'40'
         BE    NOPG
         CLI   O99PG,X'00'
         BE    NOPG
         LA    R3,MY99TXPG
         ST    R3,MY99TUPG
         LA    R15,DALSPGNM
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8               SET LENGTH INITIALLY TO 8
         LA    R1,O99PG+7 POINT TO 8TH CHAR OF NAME
PGLOOP   CLI   0(R1),X'40'         BLANK?
         BNE   PGLEN               NO - BRANCH TO STORE LENGTH
         BCTR  R15,0               YES - DECREMENT LENGTH BY 1
         BCT   R1,PGLOOP           POINT BACK 1 CHAR AND BRANCH
PGLEN    STH   R15,S99TULNG
         MVC   S99TUPAR(8),O99PG
NOPG     EQU   *
*
*              PROCESS ATTRIBUTE LIST NAME
*
         CLI   O99AT,X'40'
         BE    NOAT
         CLI   O99AT,X'00'
         BE    NOAT
         LA    R3,MY99TXAT
         ST    R3,MY99TUAT
         LA    R15,DALDCBDD
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8               SET LENGTH INITIALLY TO 8
         LA    R1,O99AT+7 POINT TO 8TH CHAR OF NAME
ATLOOP   CLI   0(R1),X'40'         BLANK?
         BNE   ATLEN               NO - BRANCH TO STORE LENGTH
         BCTR  R15,0               YES - DECREMENT LENGTH BY 1
         BCT   R1,ATLOOP           POINT BACK 1 CHAR AND BRANCH
ATLEN    STH   R15,S99TULNG
         MVC   S99TUPAR(8),O99AT
NOAT     EQU   *
*
*              PROCESS FORM
*
         CLI   O99FO,X'40'         FORM?
         BE    NOFO                NO - BRANCH
         CLI   O99FO,X'00'         FORM?
         BE    NOFO
         LA    R3,MY99TXFO
         ST    R3,MY99TUFO
         LA    R15,DALSFMNO
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,4
         STH   R15,S99TULNG
         MVC   S99TUPAR(4),O99FO
NOFO     EQU   *
*
*              PROCESS PERMANENTLY ALLOC
*
         LA    R3,MY99TXPE
         ST    R3,MY99TUPE
         LA    R15,DALPERMA
         STH   R15,S99TUKEY
         LA    R15,0
         STH   R15,S99TUNUM
*
*              PROCESS FREE=CLOSE
*
         CLC   O99PG,=CL8'INTRDR'  IF INTERNAL READER
         BE    FC                  THEN FORCE FREE=CLOSE
         CLI   O99FC,C'C'          FREE=CLOSE REQUESTED?
         BNE   NOFC                NO - BRANCH
FC       LA    R3,MY99TXFC
         ST    R3,MY99TUFC
         LA    R15,DALCLOSE
         STH   R15,S99TUKEY
         LA    R15,0
         STH   R15,S99TUNUM
NOFC     EQU   *
*
*              ALLOCATE
*
         LA    R1,MY99RBP
         SVC   99
*
*              TEST RETURN CODE
*
         LTR   R15,R15
         BNZ   DAIRFAIL
         SPACE
         CLI   MYDDRET,C'R'        DDNAME TO BE RETURNED?
         BNE   EXIT                NO - BRANCH
         LA    R3,MY99TXDD
         MVC   O99DD,=CL8' '       PAD WITH BLANKS
         LH    R14,S99TULNG
         BCTR  R14,0               LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   O99DD(0),S99TUPAR   (EXECUTED)
         EX    R14,*-6             MOVE DDNAME
         B     EXIT
*
*              DIAGNOSE ALLOCATION ERROR
*
DAIRFAIL LA    R14,MY99RB
         USING S99RB,R14
         MVC   O99RSC,S99RSC       PASS REASON CODE TO CALLER
         DROP  R14                 S99RB
         LA    R1,MYDFAREA
         USING DFDSECTD,R1
         ST    R15,MYDFRC          STORE SVC 99 RETURN CODE
         LA    R15,MYDFRC
         ST    R15,DFRCP           STORE ADDRESS OF RETURN CODE
         LA    R15,MY99RB
         ST    R15,DFS99RBP        STORE ADDRESS OF SVC 99 REQUEST BLK
         SR    R15,R15
         ST    R15,MYJEFF02        STORE ZERO IN IKJEFF02 ADDRESS
         LA    R15,MYJEFF02
         ST    R15,DFJEFF02        STORE ADDRESS OF ZERO
         LA    R15,DFSVC99
         STH   R15,MYDFID          STORE TYPE OF REQUEST (SVC 99)
         LA    R15,MYDFID
         ST    R15,DFIDP           STORE ADDRESS OF TYPE
         L     R15,O99CPPL
         ST    R15,DFCPPLP         STORE CPPL ADDRESS OR ZERO
         CLC   DFCPPLP+1(3),=AL3(0) CPPL ADDRESS SUPPLIED
         BNE   *+8                 YES - USE PUTLINE
         OI    MYDFID,DFWTP        NO - USE WRITE-TO-PROGRAMMER
         LINK  EP=IKJEFF18
         L     R15,MYDFRC          RETURN SVC 99 RETURN CODE
         B     EXIT
*
*
*
EXIT0    SR    15,15               RETURN CODE ZERO
EXIT     LR    R1,R13              AREA TO BE FREED
         L     R13,4(,R13)         RESTORE OLD R13
         LR    R2,R15              SAVE RETURN CODE
         FREEMAIN R,A=(1),LV=@DATAL
         LR    R15,R2              RESTORE RETURN CODE
         LM    0,12,20(R13)        RESTORE OLD REGISTERS
         L     R14,12(,R13)        RESTORE RETURN ADDRESS
         BR    R14                 RETURN
         SPACE 3
         LTORG
         DC    0D'0'
         SPACE
O99DSECT DSECT
O99DD    DS    CL8                 DDNAME
O99SO    DS    CL1                 SYSOUT CLASS
O99FC    DS    C                   FREE=CLOSE IF 'C'
O99HO    DS    C                   HOLD=YES IF 'H'
         DS    C                   RESERVED
O99CPPL  DS    A                   ADDRESS OF CPPL OR ZERO
O99DE    DS    CL8                 DEST
O99PG    DS    CL8                 PROGRAM NAME OR 'INTRDR'
O99AT    DS    CL8                 ATTRIBUTE LIST NAME
O99FO    DS    CL4                 FORM NUMBER
O99CO    DS    H                   COPIES
         DS    CL2                 RESERVED
O99RSC   DS    CL4                 REASON CODES RETURNED HERE
         SPACE 3
@DATA    DSECT
         DS    18F
MY99RBP  DS    A                   SVC 99 RB POINTER
MY99RB   DS    5A                  SVC 99 REQUEST BLOCK
*
* TEXT UNIT POINTERS
*
MY99TUPL DS    0A                  SVC 99 TEXT LIST POINTERS
MY99TUDD DS    A                   TEXT UNIT POINTER, DDNAME
MY99TUSO DS    A                   TEXT UNIT POINTER, SYSOUT CLASS
MY99TUCO DS    A                   TEXT UNIT POINTER, COPIES
MY99TUDE DS    A                   TEXT UNIT POINTER, DEST
MY99TUHO DS    A                   TEXT UNIT POINTER, HOLD
MY99TUPG DS    A                   TEXT UNIT POINTER, PGM NAME
MY99TUPE DS    A                   TEXT UNIT POINTER, PERM
MY99TUFC DS    A                   TEXT UNIT POINTER, FREE=CLOSE
MY99TUFO DS    A                   TEXT UNIT POINTER, USING DD REF
MY99TUAT DS    A                   TEXT UNIT POINTER, ATTR LIST NAME
         DS    A                   HI ORDER BIT ON
TUPL$LEN EQU   *-MY99TUPL
*
* TEXT UNITS
*
MY99TXDD DS    3H,CL8              TEXT UNIT, DDNAME
MY99TXSO DS    3H,CL1              TEXT UNIT, SYSOUT CLASS
MY99TXCO DS    3H,H                TEXT UNIT, COPIES
MY99TXDE DS    3H,CL8              TEXT UNIT, DEST
MY99TXHO DS    3H                  TEXT UNIT, HOLD=YES
MY99TXPG DS    3H,CL8              TEXT UNIT, PROGRAM NAME
MY99TXPE DS    3H                  TEXT UNIT, PERM
MY99TXFC DS    3H                  TEXT UNIT, FREE=CLOSE
MY99TXFO DS    3H,CL4              TEXT UNIT, FORM NUMBER
MY99TXAT DS    3H,CL8              TEXT UNIT, ATTRIBUTE LIST NAME
*
*
*
MYDFAREA DS    5F                  IKJEFF18 PARAMETER BLOCK
MYDFRC   DS    F                   SVC 99 RETURN CODE
MYJEFF02 DS    F                   MESSAGE ROUTINE (ZERO)
MYDFID   DS    H                   TYPE OF FAILURE (SVC 99)
MYDDRET  DS    C                   SWITCH
         DS    0D
@DATAL   EQU   *-@DATA
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         IKJEFFDF DFDSECT=YES
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
