DSN2     TITLE 'SECOND LOAD OF DSN COMMAND PROCESSOR'
*
*        CHANGES ...
*        23SEP80 - BILL GODFREY
*           ADDED COMMENTS ONLY. NO CHANGES TO CODE ITSELF.
*           CODE NEEDS TO BE CHANGED TO RECOGNIZE THE FACT
*           THAT IF AN EXCLUSIVE ENQ IS FOUND, THAT DOES NOT
*           MEAN THAT PRECEDING ENQ'S ARE NOT SHR OR THAT
*           THERE ARE NO SUBSEQUENT ENQ'S TO BE DISPLAYED.
*           CURRENTLY, AS SOON AS IT FINDS AN EXCLUSIVE QEL,
*           THE PROGRAM MOVES 'OLD' TO THE MESSAGE AND STOPS
*           LOOKING FOR MORE QEL'S.  THE EXCLUSIVE QEL COULD
*           BE 'WAITING' ON A 'SHR' QEL PREVIOUSLY LOOKED AT,
*           AND THE MESSAGE GIVES THE ERRONEOUS IMPRESSION
*           THAT BOTH QEL'S ARE EXCLUSIVE, AND THAT THERE
*           ARE NO OTHER ENQ'S WAITING.
*        24SEP80 - BILL GODFREY
*           CODE CHANGED TO FIX PROBLEM DOCUMENTED ABOVE.
*           MESSAGE FORMAT WAS RE-STRUCTURED. INSTEAD OF
*           THE WORD 'SHR' OR 'OLD' FOLLOWED BY A LIST
*           OF JOBNAMES AND USERIDS, THE WORD 'SHR' OR 'OLD'
*           IS REMOVED FROM THE BEGINNING OF THE LINE AND
*           EACH JOBNAME/USERID IS FOLLOWED BY THE APPROPRIATE
*           'SHR' OR 'OLD' IN PARENTHESES. PROGRAM NOW PROCEEDS
*           TO LOOK FOR MORE QEL'S EVEN AFTER AN 'OLD' IS FOUND.
*
         MACRO
&NAME    CRHI  &REG
&NAME    CR    R1,&REG
         BNH   ERROR1
         MEND
         SPACE
DSN2$    CSECT
         EQUENT SA=,XTRA=WORKL,XTRAREG=R11,XTRAUSE=WORKAREA
         XC    PRNTCNT,PRNTCNT
         MVI   SWT,0
         LA    R2,10
         LA    R3,MSG
LOOP1    MVI   0(R3),C' '
         MVC   1(L'MSG,R3),0(R3)
         LA    R3,L'MSG(R3)
         BCT   R2,LOOP1
         MVC   MSG(5),=C'SHR -'
         L     R2,0(R1)  LOAD ADDR OF TIMER ECB
         ST    R2,ECBADDR  STORE ADDR OF TIMER ECB
         L     R8,8(R1)
         OC    SWT,0(R8)
         L     R9,4(R1)
         LH    R8,0(R9)  LOAD LENGTH OF DSNAME
         LA    R10,2(R9)  LOAD ADDR OF DSNAME
DSNLOOP  DC    0H'0'
         STIMER TASK,TIMEEXIT,BINTVL=TIME
         L     R2,16                   ADDRESS OF CVT
         L     R1,164(R2)  LOAD MAXIMUM MACHINE ADDR
         ST    R1,SAVEMAX
         L     R2,640(,R2)             ADDRESS OF FIRST MAJOR QCB
QCBNEXT  LTR   R2,R2
         BZ    NOFIND
         CRHI  R2
         CLC   16(8,R2),SYSDSN
         BE    FINDDSN
         L     R2,0(R2)                ADDRESS OF NEXT MAJOR QCB
         CRHI  R2
         B     QCBNEXT
FINDDSN  L     R3,8(R2)                ADDRESS OF FIRST MINOR QCB
         SR    R4,R4
NEXTMIN  LTR   R3,R3
         BZ    NOFIND
         CRHI  R3
         IC    R4,16(R3)               LENGTH OF QCB NAME
         CR    R4,R8
         BL    INCR
         LR    R7,R8
         BCTR  R7,R0
         EX    R7,COMPDSN
         BE    GETQEL
INCR     L     R3,0(R3)                ADDRESS OF NEXT MINOR QCB
         B     NEXTMIN
GETQEL   DC    0H'0'
         ST    R3,SAVEQCB
         LA    R4,54
         LA    R2,MSG
         LA    R5,MSG+0                                 24SEP80
         LA    R3,8(,R3)               POINTER TO 1ST QEL ADDRESS
NEXTQEL  L     R3,0(R3)                LOAD ADDRESS OF QEL
         LTR   R3,R3
         BZ    OUT
         CRHI  R3
*        TM    12(R3),X'80'                             24SEP80
*        BO    SHR                                      24SEP80
*        MVC   MSG(3),=C'OLD'                           24SEP80
*        LA    R5,MSG+6                                 24SEP80
*        BAL   R6,FINDJN                                24SEP80
*        LA    R5,1(R5)                                 24SEP80
*        B     OUT                                      24SEP80
SHR      DC    0H'0'
         TM    SWT,X'01'
*
*              IF THE CURSOR (R5) IS 54 BYTES OR MORE INTO
*              THE CURRENT LINE (R2), THEN
*                  ADD 1 TO PRNTCNT  (LINE COUNTER),
*                  POINT THE CURRENT LINE POINTER (R2) TO
*                  THE NEXT LINE, AND POINT THE CURSOR (R5)
*                  TO THE 7TH BYTE OF THE NEW LINE.
*                  IF PRNTCNT IS 9 OR GREATER, GO TO 'OUT'.
*
         BO    NEWMIN
         LR    R6,R5
         SR    R6,R2
         CR    R6,R4
         BL    SHR1
         LH    R6,PRNTCNT
         CH    R6,=H'9'
         BNL   OUT
         LA    R6,1(R6)
         STH   R6,PRNTCNT
         MH    R6,=H'70'
         LA    R5,MSG+0(R6)                             24SEP80
         LA    R2,MSG(R6)
*
*              PUT THE JOBNAME OR USERID IN THE LINE,
*              APPEND A COMMA, AND GO LOOK FOR ANOTHER QEL.
*
SHR1     BAL   R6,FINDJN
         MVC   0(5,R5),=C'(SHR)'                        24SEP80
         TM    12(R3),X'80'                             24SEP80
         BO    *+10                                     24SEP80
         MVC   1(3,R5),=C'OLD'                          24SEP80
         LA    R5,5(,R5)                                24SEP80
         MVI   0(R5),C','
         LA    R5,1(R5)
         B     NEXTQEL
*
*              PRINT THE DSNAME AS A HEADING.
*              BLANK OUT THE LAST TRAILING COMMA.
*              PRINT THE LINE(S) CONTAINING THE JOBNAMES/USERIDS.
*
OUT      DC    0H'0'
         TTIMER CANCEL
         L     R15,SAVEQCB
         XR    R2,R2
         IC    R2,16(R15)              LENGTH OF DSN
         LA    R1,20(R15)              DSN -->
         TPUT  (R1),(R2)
         BCTR  R5,R0                   BACK CURSOR UP
         MVI   0(R5),C' '              BLANK THE LAST COMMA
         LH    R2,PRNTCNT
         LA    R2,1(R2)
         LA    R3,MSG
PRNTOUT  DC    0H'0'
         TPUT  (R3),70
         MVI   0(R3),C' '
         MVC   1(69,R3),0(R3)
         LA    R3,70(R3)
         BCT   R2,PRNTOUT
         OI    SWT,X'80'
         XC    PRNTCNT,PRNTCNT
*        MVC   MSG(5),=C'SHR -'                         24SEP80
         STIMER TASK,TIMEEXIT,BINTVL=TIME
NEWMIN   L     R3,SAVEQCB
         L     R1,SAVEMAX
         B     INCR
NOFIND   DC    0H'0'
         TTIMER CANCEL
         TM    SWT,X'80'
         BO    NEXTDSN GO TEST FOR ANOTHER DSNAME
         TPUT  (R10),(R8)
         LA    R2,L'ERMSG2
         TM    SWT,X'01'
         BZ    PUTER2
         LA    R2,L'ERMSG2C(R2)
PUTER2   DC    0H'0'
         TPUT  ERMSG2,(R2)
NEXTDSN  DC    0H'0'
         LA    R9,46(R9)
         LA    R10,2(R9)
         LH    R8,0(R9)  LOAD LENGTH OF DSNAME
         LTR   R8,R8  LAST DSNAME?
         BZ    RETURN  YES
         NI    SWT,X'FF'-X'80'  RESET NOT FOUND SWT
         B     DSNLOOP  GO PROCESS NEW DSNAME
ERROR1   DC    0H'0'
         TTIMER CANCEL
         TPUT  ERMSG1,L'ERMSG1
RETURN   DC    0H'0'
        EQUEXT XTRA=WORKL
**
COMPDSN  CLC   20(0,R3),0(R10)
**
** STIMER EXIT
**
TIMEEXIT DC    0H'0'
         SAVE  (14,12)
         L     R1,ECBADDR  LOAD ADDR OF TIMER ECB
         POST  (1)
         RETURN (14,12)
**
*        FINDJN    -    FINDS THE JOB NAME AND MOVES IT
*                       TO A SPECIFIED LOCATION
*
*        UPON ENTRY     REG. 3  -  ADDRESS OF QEL
*                       REG. 5  -  LOC. TO REC. JOB NAME OR USERID
*                       REG. 6  -  RETURN REG.
*
*        UPON EXIT      REG. 5  -  CONTAINS ADDRESS FOLLOWING
*                                  THE JOB NAME OR USERID
*
*                       REG. 7  -  WORK REG.
*                       REG. 8  -  WORK REG.
*                       REG. 9  -  WORK REG.
*                       REG. 10 -  WORK REG.
         SPACE 4
FINDJN   DC    0H'0'
         STM   R7,R10,SAVER710
         SR    R10,R10
         ICM   R10,3,14(R3)        ASID
         SLL   R10,2               * 4
         L     R9,16               CVT -->
         L     R9,556(,R9)         ASVT -->
         L     R9,524(R10,R9)      ASCB -->
         L     R8,56(,R9)          CSCB -->
CONCHAIN LA    R8,0(,R8)           CLEAR HI BYTE
         LTR   R8,R8               --> = 0?
         BZ    RESTORE             YES, LEAVE OUTPUT BLANK
         CLI   28(R8),X'01'        TS CSCB
         BE    GETTS               SET UP U= FLAG
         CLI   28(R8),X'02'        JOB CSCB
         BNE   TRYCHAIN            NO, MAY BE INIT CSCB
         MVC   0(2,R5),=C'J='      J= FLAG
GOTTS    LA    R5,2(,R5)           ADVANCE OUTPUT -->
         MVC   0(8,R5),8(R8)       JOB NAME/USER ID
GOHOME   LA    R7,8
LOOP     CLI   0(R5),C' '
         BE    RESTORE
         LA    R5,1(R5)
         BCT   R7,LOOP
RESTORE  LM    R7,R10,SAVER710
         BR    R6
TRYCHAIN CLI   28(R8),X'03'        INIT CSCB
         BNE   RESTORE             NO, LEAVE OUTPUT BLANK
TRYCH2   L     R8,0(,R8)           --> NEXT CSCB
         LA    R8,0(,R8)           CLEAR HI BYTE
         LTR   R8,R8               --> = 0?
         BZ    RESTORE             YES, LEAVE OUTPUT BLANK
         CLC   14(2,R3),30(R8)     ASIS'S =
         BNE   TRYCH2              KEEP LOOPING IF NOT
         B     CONCHAIN            CYCLE BACK IN LOOP
GETTS    MVC   0(2,R5),=C'U='      U= FLAG
         B     GOTTS
         SPACE
WORKAREA DSECT
ECBADDR  DC    F'0'
SAVEQCB  DC    F'0'
SAVEMAX  DC    F'0'
SAVER710 DC    4F'0'
PRNTCNT  DC    H'0'
MSG      DC    CL70'SHR -'
         DC    9CL70' '
SWT      DC    X'00'
WORKL    EQU   *-WORKAREA
         SPACE
DSN2$    CSECT
TIME     DC    F'10'
SYSDSN   DC    CL8'SYSDSN'
ERMSG1   DC    C'INVALID ADDRESS FOUND'
ERMSG2   DC    C'DATA SET NOT ALLOCATED '
ERMSG2C  DC    C'AS ''OLD'''
         LTORG
         DC    0D'0'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
