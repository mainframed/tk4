         TITLE '       U N I T S   '
*        WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
*        INSTALLATION. PRC, MCLEAN VA.
*        DATE WRITTEN. MAY 05 1976
*        DATE UPDATED. MAR. 28 1989 BY M.KARLIN (TESTED 3.1.0E)
*                                     8/2/92    (TESTED 4.1.0 )
*        ATTRIBUTES. RE-ENTRANT.
*            THIS TSO COMMAND DISPLAYS THE UNIT NAMES IN THE
*            DEVICE NAME TABLE, AND THE CHANNEL-UNIT-ADDRESSES
*            ASSOCIATED WITH EACH UNIT NAME.
*            29AUG79 - MASK LENGTH COMPUTED INSTEAD OF CONSTANT.
*       22APR90 - MVS/ESA SEE SPL: SYSTEM MODIFICATION
*                 UNIT VERIFICATION PROGRAM IEFEB4UV
         PRINT NOGEN
UNITS    CSECT
UNITS    AMODE 31               @MK
UNITS    RMODE 24               @MK
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'UNITS'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)   SUBPOOL AND LENGTH
@PROLOG  STM   14,12,12(13)
         LR    R12,R15
         LR    R2,R1
         USING CPPL,R2
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         ST    13,4(,1)
         ST    1,8(,13)
         LR    13,1
         USING @DATA,13
************************************************************
*        SET UP IOPL FOR PUTLINE                           *
************************************************************
PROCEED  EQU   *
         LINK  EP=CLRSCRN
         LA    R15,MYIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,IOPLECB
         XC    MYECB,MYECB
         LA    R0,MYPTPB
         ST    R0,IOPLIOPB
         DROP  R15
************************************************************
*        SET UP PPL FOR PARSE                              *
************************************************************
         LA    R15,MYPPL
         USING PPL,R15
         MVC   PPLUPT(4),CPPLUPT
         MVC   PPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,PPLECB
         XC    MYECB,MYECB
         L     R0,=A(UNITSPCL)
         ST    R0,PPLPCL
         LA    R0,MYANS
         ST    R0,PPLANS
         MVC   PPLCBUF(4),CPPLCBUF
         LA    R0,MYUWA
         ST    R0,PPLUWA
         DROP  R15
************************************************************
*        CALL THE PARSE SERVICE ROUTINE                    *
************************************************************
         LR    R1,R15              POINT TO PPL
         L     R15,16              CVTPTR
         TM    X'020C'(R15),X'80'  IF HI ORDER BIT NOT ON
         BNO   PARSELNK               THEN DO LINK, NOT CALL
         L     R15,X'020C'(,R15)   CVTPARS
         BALR  R14,R15             CALL IKJPARS
         B     PARSEEXT            SKIP AROUND LINK
PARSELNK EQU   *
         LINK  EP=IKJPARS
PARSEEXT L     R3,MYANS
         USING IKJPARMD,R3
         LA    R1,FLAGS2
         ST    R1,UVFLGAD2
         LA    R1,UVTABLE2
         ST    R1,UVTBLAD2
         LA    R1,DEVCLASS    GET DEVICE TABLE ADDR
         ST    R1,DEVADDR        STORE IT
         MVC   FLAGS2,=X'0010' RETURN UNIT NAMES FOR CLASS
         LA    R1,FLAGS
         ST    R1,UVFLGADR
         LA    R1,UVTABLE
         ST    R1,UVTBLADR
         MVC   FLAGS,=X'1000'  RETURN UCB ADDRESSES
         L     R1,DEVADDR                GET 1ST DEV CLASS
DEVCLSLP MVC   UVTABLE2+18(1),0(R1)      PLACE DEV CLASS IN UNIT TABLE
         LA    R1,PARMLST2
         LINK  EP=IEFEB4UV                                  @MK
         L     R4,UVTABLE2+4           GET UNIT NAME LIST PTR @MK
         L     R1,0(R4)          GET UNIT NAME SP & LIST SIZE @MK
         ST    R1,UNTNAMSZ       SAVE FOR FREEMAIN            @MK
         ICM   R1,15,4(R4)       GET UNT NAME CNT             @MK
         BZ    FREETBL           NO MORE UNITS OF THIS CLASS  @MK
         ST    R1,CTRNAME         SAVE                        @MK
         LA    R4,8(R4)          POINT TO FIRST UNIT NAME     @MK
************************************************************    @MK
*        SCAN THRU DEVICE NAME TABLE                       *    @MK
************************************************************    @MK
NAMELOOP EQU   *
         ST    R1,CTRNAME
         MVI   MYMSG,C' '
         MVC   MYMSG+1(MYMSGL-1),MYMSG
         MVI   MYMSGEND,X'FF'
         MVC   MYMSG(8),0(R4)
         CLI   NAMEKW+1,1          'NAMES' SPECIFIED
         BE    NAMEOUT             YES, NO MASK PROCESSING
         LA    R7,MYMSGCUU
************************************                            @MK
*        SCAN UCB                                               @MK
************************************                            @MK
         MVC   UVTABLE(8),0(R4)       PUT UNIT NAME IN TABLE
         LA    R1,PARMLIST
         LINK  EP=IEFEB4UV                                  @MK
         L     R6,UVTABLE+8           GET UCB LIST
         L     R1,0(R6)          GET UCB LIST SP AND LIST SIZE
         ST    R1,UCBLSTSZ       SAVE FOR FREEMAIN
         L     R1,4(R6)          GET UCB COUNT
         ST    R1,UCBCTR          SAVE
         LA    R6,8(R6)          POINT TO FIRST UCB
         USING UCBOB,R5
UCBLOOP  ICM   R1,15,UCBCTR
         BZ    NEXTNAME          NO MORE UCBS FOR THIS UNIT NAME
         BCTR  R1,0
         ST    R1,UCBCTR
         L     R5,0(R6)            GET UCB ADDR                 @MK
         LA    R1,UCBTYP              UCBTYPE
         LA    R15,MYMSG+9
         LA    R0,4
         BAL   R14,HEX
         MVC   DEVNUM,UCBNAME      GET EBCDIC UNIT NAME
         CLC   0(4,R7),=C'    '    ANY ROOM LEFT?  R7=A(MYMSGCUU)
         BNE   CONTINUE            NO - BRANCH
         CLC   MYMSGCUU(2),=C'  '  FIRST CUU?
         BE    FSTCUU              YES - BRANCH
         CLC   MYMSGCUU(2),DEVNUM  NO - SAME CU AS FIRST?  @MK
         BNE   CONTINUE            NO - NEW LINE
FSTCUU   EQU   *
         MVC   0(3,R7),DEVNUM      MOVE CUU   @MK
         LA    R7,4(,R7)           NEXT SLOT                    @MK
         LA    R6,4(R6)           NEXT UCB                     @MK
         B     UCBLOOP             GET NEXT UCB                 @MK
CONTINUE STM   R14,R1,SAVEBITS
         LA    R1,MYMSG
         LA    R0,MYMSGL
         BAL   R14,PUTLINE
         LM    R14,R1,SAVEBITS
         MVI   MYMSG,C' '
         MVC   MYMSG+1(MYMSGL-1),MYMSG
         LA    R7,MYMSGCUU
         B     FSTCUU
NAMEOUT  LA    R1,MYMSG
         LA    R0,MYMSGL
         BAL   R14,PUTLINE
NEXTNAME LA    R4,8(,R4)          @MK- ESA  NEXT UNIT NAME IN LIST
         CLI   NAMEKW+1,1          'NAMES' SPECIFIED
         BE    SKIPFREE            YES, NO FREEMAIN
         L     R0,UCBLSTSZ
         FREEMAIN R,LV=(0),A=UVTABLE+8    FREE THE UCB POINTER LIST
SKIPFREE L     R1,CTRNAME
         BCT   R1,NAMELOOP
FREETBL  L     R0,UNTNAMSZ
         FREEMAIN R,LV=(0),A=UVTABLE2+4   FREE THE UNIT NAME LIST
         L     R1,DEVADDR          GET NEXT DEV CLASS
         LA    R1,1(R1)
         CLI   0(R1),X'FF'
         BE    EXIT0               NO MORE DEVICE CLASSES
         ST    R1,DEVADDR          SAVE
         B     DEVCLSLP            LOOP
         SPACE
HEX      MVC   1(1,R15),0(R1)      MOVE BYTE
         UNPK  0(3,R15),1(2,R15)   UNPACK
         TR    0(2,R15),HEXTRAN-240
         LA    R15,2(,R15)         INCREMENT OUTPUT PTR
         LA    R1,1(,R1)           INCREMENT INPUT PTR
         BCT   R0,HEX              DECREMENT LENGTH, THEN LOOP
         MVI   0(R15),C' '         BLANK THE TRAILING BYTE
         BR    R14                 RETURN TO CALLER
HEXTRAN  DC    C'0123456789ABCDEF' TRANSLATE TABLE
************************************************************
*        PUTMSG ROUTINE                                    *
************************************************************
PUTMSG   STM   R14,R1,PUTLINS
         XC    MYOLD(8),MYOLD
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPM
         LA    R14,1               NO. OF MESSAGE SEGMENTS
         ST    R14,MYOLD
         LA    R14,MYSEG1          POINT TO 1ST SEGMENT
         ST    R14,MYOLD+4
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         LA    R15,MYSEG1+4
         CLC   0(3,R1),=C'IKJ'     IS DATA PRECEEDED BY MESSAGE ID?
         BE    *+16                YES - BRANCH
         LA    R14,1(,R14)         ADD 1 TO LENGTH
         MVI   0(R15),C' '         INSERT LEADING BLANK
         LA    R15,1(,R15)         BUMP POINTER
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   0(0,R15),0(R1)      MOVE MESSAGE IN
         EX    R14,*-6
         PUTLINE PARM=MYPTPB,OUTPUT=(MYOLD),MF=(E,MYIOPL)
         LM    R14,R1,PUTLINS
         BR    R14
************************************************************
*        PUTLINE ROUTINE                                   *
************************************************************
PUTLINE  STM   R14,R1,PUTLINS
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPB
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   MYSEG1+4(0),0(R1)   MOVE TEXT IN
         EX    R14,*-6
         PUTLINE PARM=MYPTPB,OUTPUT=(MYSEG1,DATA),MF=(E,MYIOPL)
         LM    R14,R1,PUTLINS
         BR    R14
         SPACE
EXIT0    SR    15,15
EXIT     LR    1,13
         L     R0,@SIZE
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(13)
         BR    14
************************************************************
*        CONSTANTS                                         *
************************************************************
DEVCLASS DC    X'04081020404180FF'  04=CHAR READ,08=UNIT REC,10=DISPLAY
*                             20=DASD,40=COMMUN,41=CHAN-TO-CHAN,80=TAPE
MODLPTPM PUTLINE OUTPUT=(1,TERM,SINGLE,INFOR),                         X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
************************************************************
*        PARSE PCL                                         *
************************************************************
UNITSPCL IKJPARM
NAMEKW   IKJKEYWD
         IKJNAME 'NAMES'
         IKJENDP
************************************************************
*        DSECTS                                            *
************************************************************
@DATA    DSECT
         DS    18F
MYPPL    DS    7F
MYANS    DS    F
MYUWA    DS    F
MYECB    DS    F                  USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                 USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                 USED BY PUTLINE ROUTINE
MYOLD    DS    2F                 USED BY PUTLINE ROUTINE
MYSEG1   DS    2H,CL100           USED BY PUTLINE ROUTINE
PUTLINS  DS    4F                 USED BY PUTLINE ROUTINE
CTRNAME  DS    F
UCBCTR   DS    F
SAVEBITS DS    4F
SAVEUCB  DS    F
MYMSG    DS    0CL80
         DS    CL8
         DS    C
         DS    CL8
         DS    C
MYMSGCUU DS    8CL4
MYMSGL   EQU   *-MYMSG
MYMSGEND DS    CL8
************* @MK @MK @MK *******************************
*     UNIT VERIF RTN AREAS                                      @MK
PARMLIST DS    2F         PARMLIST MAPPING                      @MK
         ORG   PARMLIST                                         @MK
UVTBLADR DS    A
UVFLGADR DS    A          ADDR OF FLAGS   @MK
         SPACE                                                , @MK
FLAGS    DS    XL2         X'1000'  RETURN UCB ADDR  FLAG       @MK
UVTABLE  DS    3F         UNIT VERIF TABLE                      @MK
         SPACE
PARMLST2 DS    2F         PARMLIST MAPPING                      @MK
         ORG   PARMLST2                                         @MK
UVTBLAD2 DS    A
UVFLGAD2 DS    A          ADDR OF FLAGS   @MK
         SPACE                                                , @MK
FLAGS2   DS    XL2         X'0010'  RETURN UNIT NAME FOR A DEV CLASS
UVTABLE2 DS    5F         UNIT VERIF TABLE                      @MK
         SPACE
DEVNUM   DS    F          UCBNAME                               @MK
UCBLSTSZ DS    F          UCB LIST SP AND SIZE                  @MK
UNTNAMSZ DS    F   UNIT NAME LIST SP AND SIZE                  @MK
DEVADDR  DS    A   ADDRESS OF DEV CLASS TABLE                  @MK
************* @MK @MK @MK *******************************
@DATAL   EQU   *-@DATA
         IKJCPPL
         IKJIOPL
         IKJPPL
         IKJECT
         DSECT
         IEFUCBOB
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
