LISTAX   TITLE 'LISTAX --- LISTS ATTRIBUTES OF ALL ALLOCATED DATASETS'
*
*   LISTAX WAS WRITTEN BY        BOB JUCH
*                                 POSTAL DATA CENTER
*                                 850 CHERRY AVE.
*                                 SAN BRUNO CA. 94097
*                                 415 876-9176
*
         EJECT
*)F FUNCTION:
*         THE LISTAX COMMAND LISTS ALL ALLOCATED DATASETS
*         IN THE FOLLOWING FORMAT:
*         DDNAME   DISPOSITONS  DSORG DATASET NAME
*         XXXXXXX  XXX XXXX XXX  XX   XXXXXXXXXXXXXXXXXXXXXXXXXXX
*
*)X SYNTAX:
*         LISTAX
*)O
         EJECT
LISTAX   SAVE  (14,12),,*
         LR    10,15          LOAD BASE
         USING LISTAX,10
         LA    1,HEADER       POINT TO HEAD LINE
         LA    0,L'HEADER     LOAD LENGTH OF HEAD LINE
         TPUT  (1),(0),R      DO TPUT
         LA    1,BLANK        POINT TO BLANK
         LA    0,1            LENGTH OF ONE
         TPUT  (1),(0),R      DO TPUT
         SLR   3,3            CLEAR FOR COUNTER
TOP      LA    1,S99RBPTR     POINT TO REQUEST BLOCK
         MVC   DDNAME(10),FILLDDN
         MVC   MEMBER(10),FILLDDN
         MVC   DSNAME(46),FILLDSN
         AH    3,=H'1'        ADD FOR REQUEST NUMBER
         STH   3,RELNO        PUT IT IN TEXT UNIT
SVC99    DYNALLOC             GO DO IT
MOVES    MVC   OUTDDN,DDNAME+2   MOVE DDNAME TO OUTPUT LINE
         CLI   DSTYPE,X'00'   NORMAL DATASET ?
         BE    MOVEDSN
         CLI   DSTYPE,X'40'   ALLOC TO TERM ?
         BNE   *+16
         MVI   OUTDSN,C'*'
         LA    0,27           SET LINE LENGTH TO 27
         B     CHKSTAT
         MVC   OUTDSN(8),=C'NULLFILE'
         LA    0,34           SET LINE LENGTH TO 34
         B     CHKSTAT
MOVEDSN  MVC   OUTDSN,DSNAME+2   MOVE DSNAME
         MVC   OUTMEM,MEMBER+2   MOVE MEMBER NAME
         LA    0,79           SET LINE LENGTH TO 79
CHKSTAT  EQU   *
         CLI   STATUS,X'08'   SEE IF SHR
         BNE   *+14
         MVC   OUTSTA,=C'SHR'
         B     CHKNDISP
         CLI   STATUS,X'01'   SEE IF OLD
         BNE   *+14
         MVC   OUTSTA,=C'OLD'
         B     CHKNDISP
         CLI   STATUS,X'04'   SEE IF NEW
         BNE   *+14
         MVC   OUTSTA,=C'NEW'
         B     CHKNDISP
         CLI   STATUS,X'02'   SEE IF MOD
         BNE   *+14
         MVC   OUTSTA,=C'MOD'
         B     CHKNDISP
         MVC   OUTSTA,=C'???' NO STAT ?
CHKNDISP EQU   *
         CLI   NDISP,X'08'    SEE IF KEEP
         BNE   *+14
         MVC   OUTNDS,=C'KEEP'
         B     CHKCDISP
         CLI   NDISP,X'02'    SEE IF CATLG
         BNE   *+14
         MVC   OUTNDS,=C'CTLG'
         B     CHKCDISP
         CLI   NDISP,X'04'    SEE IF DELETE
         BNE   *+14
         MVC   OUTNDS,=C'DLTE'
         B     CHKCDISP
         CLI   NDISP,X'01'    SEE IF UNCATLG
         BNE   *+14
         MVC   OUTNDS,=C'UCAT'
         B     CHKCDISP
         CLI   NDISP,X'10'    SEE IF PASS
         BNE   *+14
         MVC   OUTNDS,=C'PASS'
         B     CHKCDISP
         MVC   OUTNDS,=C'????' NO DISP
CHKCDISP EQU   *
         CLI   CDISP,X'08'    SEE IF KEEP
         BNE   *+14
         MVC   OUTCDS,=C'KEEP'
         B     CHKDSORG
         CLI   CDISP,X'02'    SEE IF CATLG
         BNE   *+14
         MVC   OUTCDS,=C'CTLG'
         B     CHKDSORG
         CLI   CDISP,X'04'    SEE IF DELETE
         BNE   *+14
         MVC   OUTCDS,=C'DLTE'
         B     CHKDSORG
         CLI   CDISP,X'01'    SEE IF UNCATLG
         BNE   *+14
         MVC   OUTCDS,=C'UCAT'
         B     CHKDSORG
         CLI   CDISP,X'10'    SEE IF PASS
         BNE   *+14
         MVC   OUTCDS,=C'PASS'
         B     CHKDSORG
         MVC   OUTCDS,=CL4' '  NO DISP
CHKDSORG EQU   *
         TM    DSORG,X'40'    SEE IF PS
         BZ    *+14
         MVC   OUTORG,=C'PS'
         B     PUTLINE
         TM    DSORG,X'02'    SEE IF PO
         BZ    *+14
         MVC   OUTORG,=C'PO'
         B     PUTLINE
         TM    DSORG+1,X'08'  SEE IF VSAM
         BZ    *+14
         MVC   OUTORG,=C'VS'
         B     PUTLINE
         TM    DSORG,X'20'    SEE IF DA
         BZ    *+14
         MVC   OUTORG,=C'DA'
         B     PUTLINE
         TM    DSORG,X'80'    SEE IF ISAM
         BZ    *+14
         MVC   OUTORG,=C'IS'
         B     PUTLINE
         TM    DSORG,X'10'    SEE IF CX
         BZ    *+14
         MVC   OUTORG,=C'CX'
         B     PUTLINE
         TM    DSORG,X'08'    SEE IF CQ
         BZ    *+14
         MVC   OUTORG,=C'CQ'
         B     PUTLINE
         TM    DSORG,X'04'    SEE IF MQ
         BZ    *+14
         MVC   OUTORG,=C'MQ'
         B     PUTLINE
         TM    DSORG+1,X'80'  SEE IF GS
         BZ    *+14
         MVC   OUTORG,=C'GS'
         B     PUTLINE
         TM    DSORG+1,X'40'  SEE IF TX
         BZ    *+14
         MVC   OUTORG,=C'TX'
         B     PUTLINE
         TM    DSORG+1,X'20'  SEE IF TQ
         BZ    *+14
         MVC   OUTORG,=C'TQ'
         B     PUTLINE
         TM    DSORG,X'04'    SEE IF TR
         BZ    *+14
         MVC   OUTORG,=C'TR'
         B     PUTLINE
         MVC   OUTORG,=C'**'
PUTLINE  LA    1,OUTLINE
         TPUT  (1),(0),R
         TM    LASTENT,X'80'  WAS THIS LAST ENTRY
         BZ    TOP
RETURN   RETURN (14,12)
         EJECT
HEADER   DC    C'DDNAME   DISP         ORG DSNAME'
BLANK    DC    C' '
         SPACE
FILLDDN  DC    H'8',CL8' '
FILLDSN  DC    H'44',CL44' '
         SPACE
         DS    0F
S99RBPTR DC    X'80',AL3(S99RB)
         SPACE
S99RB    DS    0F
S99RBLN  DC    X'14'
S99VERB  DC    X'07'
S99FLAG1 DC    H'0'
S99ERROR DC    X'0000'
S99INFO  DC    X'0000'
S99TXTPP DC    A(S99TUPL)
S99RSVD  DC    F'0'
S99FLAG2 DC    F'0'
         SPACE
S99TUPL  DS    0F
S99TUP04 DC    A(S99TUN04)
S99TUP05 DC    A(S99TUN05)
S99TUP06 DC    A(S99TUN06)
S99TUP07 DC    A(S99TUN07)
S99TUP08 DC    A(S99TUN08)
S99TUP09 DC    A(S99TUN09)
S99TUP10 DC    A(S99TUN10)
S99TUP12 DC    A(S99TUN12)
S99TUP13 DC    A(S99TUN13)
S99TUP14 DC    A(S99TUN14)
S99TUP15 DC    X'80',AL3(S99TUN15)
         SPACE
S99TUN04 DC    H'4',H'1'
DDNAME   DC    H'8',CL8' '
S99TUN05 DC    H'5',H'1'
DSNAME   DC    H'44',CL44' '
S99TUN06 DC    H'6',H'1'
MEMBER   DC    H'8',CL8' '
S99TUN07 DC    H'7',H'1',H'1'
STATUS   DC    X'00'
S99TUN08 DC    H'8',H'1',H'1'
NDISP    DC    X'00'
S99TUN09 DC    H'9',H'1',H'1'
CDISP    DC    X'00'
S99TUN10 DC    H'10',H'1',H'2'
DSORG    DC    X'0000'
S99TUN12 DC    H'12',H'1',H'1'
ATTRIB   DC    X'00'
S99TUN13 DC    H'13',H'1',H'1'
LASTENT  DC    X'00'
S99TUN14 DC    H'14',H'1',H'1'
DSTYPE   DC    X'00'
S99TUN15 DC    H'15',H'1',H'2'
RELNO    DC    H'0'
         SPACE
OUTLINE  DS    0CL79
OUTDDN   DC    CL8' '
         DC    C' '
OUTSTA   DC    CL3' '
         DC    C','
OUTNDS   DC    CL4' '
         DC    C','
OUTCDS   DC    CL4' '
         DC    C' '
OUTORG   DC    CL2' '
         DC    C' '
OUTDSN   DC    CL44' '
         DC    C' '
OUTMEM   DC    CL8' '
         SPACE
         LTORG
         END   LISTAX
