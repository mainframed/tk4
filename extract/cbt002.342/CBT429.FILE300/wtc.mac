         TITLE 'WTC--COMMAND PROCESSOR'
*--------------------------------------------------------------------*
*                                                                    *
*        ' WRITE TO CONSOLE '  TSO COMMAND PROCESSOR                 *
*                                                                    *
* COURTESY: JIM MARSHALL, CAPT, USAF (RET)                           *
*                                                                    *
*        THIS TSO COMMAND IS ESSENTIALLY THE SAME AS DOING A SEND    *
*        COMMAND TO THE CONSOLE OPERATOR EXCEPT THE MSG DOES NOT     *
*        ROLL OFF THE SCREEN. IT ISSUES THE MSG WITH A ROUTCDE=(2)   *
*        AND DESC=(2).  HANDY FOR WAKING UP THE OPERATOR.            *
*                                                                    *
*            WTC   'MESSAGE TEXT'                                    *
*                                                                    *
*        THIS PROGRAM REQUIRES YOU TO ENCORPORATE YOUR OWN SVC       *
*        FOR GETTING YOURSELF IN AND OUT OF SUPERVISOR STATE.        *
*        I DID NOT INCLUDE MINE BECAUSE I WOULD BE 'CONTRIBUTING     *
*        TO THE DELINQENCY OF SOME SCREWBALL (SYSTEMS TYPE)'.        *
*        SEE LABELS 'INSUP' AND 'OUTSUP' IN THE PROGRAM.             *
*                                                                    *
*        REGISTER USAGE:                                             *
*              R0  - WORK                                            *
*              R1  - WORK                                            *
*              R2  - ADDRESS THE CPPL                                *
*              R3  - ADDRESS THE IKJPARMD                            *
*              R4  - ADDRESS THE WTC PDE                             *
*              R5  - ADDRESS OF PASSED MESSAGE                       *
*              R6  - LENGTH OF MESSAGE                               *
*              R7  - WORK                                            *
*              R8  - NOT USED                                        *
*              R9  - NOT USED                                        *
*              R10 - NOT USED                                        *
*              R11 - NOT USED                                        *
*              R12 - NOT USED                                        *
*              R13 - ADDRESS OF MYDSECT                              *
*              R14 - CONVENTIONAL                                    *
*              R15 - CONVENTIONAL                                    *
*                                                                    *
*        RETURN CODES:                                               *
*              0  -  NORMAL                                          *
*              8  -  NO TEXT ENTERED                                 *
*              12 -  TOO LONG TEXT                                   *
*                                                                    *
* AUTHOR: UNKNOWN                                                    *
*                                                                    *
* CHNGE LOG:                                                         *
*         6MAY85 - INSTALLED AT 2ISG AND CLEANED UP, REPACKAGED IN   *
*                  MY STYLE.   JIM MARSHALL                          *
*        10JAN93 - ADDED DSECTS,                                 JDM1*
*--------------------------------------------------------------------*
         SPACE
WTC      AMODE 24
WTC      RMODE 24
WTC      CSECT
         USING *,R12
         B     32(,15)
         DC    CL12'WTC'
         DC    CL16' &SYSDATE &SYSTIME '
         STM   R14,R12,12(R13)
         LR    R12,R15
         GETMAIN R,LV=MYDSECTL
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R14,R13
         LR    R13,R1
         L     R1,24(,R14)
         USING MYDSECT,R13
         LR    R2,R1
         USING CPPL,R2
*--------------------------------------------------------------------*
*        SET UP FOR PUTLINE
*--------------------------------------------------------------------*
         LA    R15,MYIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,IOPLECB
         XC    MYECB,MYECB
         LA    R0,MYPTPB
         ST    R0,IOPLIOPB
         DROP  R15
         SPACE
*--------------------------------------------------------------------*
*        SET UP PPL FOR PARSE
*--------------------------------------------------------------------*
         SPACE
         LA    R15,MYPPL
         USING PPL,R15
         MVC   PPLUPT(4),CPPLUPT
         MVC   PPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,PPLECB
         XC    MYECB,MYECB
         L     R0,=A(MYPCL)
         ST    R0,PPLPCL
         LA    R0,MYANS
         ST    R0,PPLANS
         XC    MYANS,MYANS
         MVC   PPLCBUF(4),CPPLCBUF
         LA    R0,MYUWA
         ST    R0,PPLUWA
         DROP  R15
         SPACE
*--------------------------------------------------------------------*
*        CALL THE PARSE SERVICE ROUTINE
*--------------------------------------------------------------------*
         SPACE
         LR    R1,R15              POINT TO PPL
         L     R15,CVTPTR          CVTPTR                        JDM1
         USING CVTMAP,R15                                        JDM1
         TM    CVTPARS,X'80'       IF HI ORDER BIT NOT ON        JDM1
         BNO   PARSELNK            THEN DO LINK, NOT CALL
         L     R15,CVTPARS         LOAD ADDRESS OF CVTPARS       JDM1
         DROP  R15                                               JDM1
         BALR  R14,R15             CALL IKJPARS
         B     PARSEEXT            SKIP AROUND LINK
PARSELNK EQU   *
         LINK  EP=IKJPARS
PARSEEXT EQU   *
         SPACE 1
         LTR   R15,R15             NORMAL RETURN?
         BNZ   PARSERR             NO - TERMINATE
         L     R3,MYANS            POINT TO THE PDL
         USING IKJPARMD,R3         PDL ADDRESSABLITY
         LA    R4,WTCMSG           POINT TO PCE
         TM    WTCMSG+6,X'80'      WAS THERE AN ENTRY?
         BZ    ERRNOTXT            IMPOSSIBLE 'CAUSE A PROMPT
         LH    R6,WTCMSG+4         GET TXT LENGTH
         L     R5,WTCMSG           ADDRESS OF TXT
         BNE   NOQUOTES            SKIP THE STRIP
         SH    R6,=H'3'            SUB 3 FOR EX + QUOTES
         LA    R5,1(R5)            PASS QUOTE
RESUME1  LTR   R6,R6               SET CC
         BNP   ERRNOTXT            GOT TO HAVE AT LEAST 1
         CH    R6,=H'114'          HOW BIG?
         BH    ERROVER             TOO LONG
         B     *+10                BRANCH AROUND
         MVC   MYREST(0),0(R5)     SET UP WTO
         EX    R6,*-6              LOOP BACK TO MVC
         L     R7,CVTPTR           POINTER TO ADDRESS OF CVT     JDM1
         USING CVTMAP,R7                                         JDM1
         L     R7,CVTTCBP          GET NEW/OLD ADDRESS           JDM1
         DROP  R7                                                JDM1
         L     R7,4(,R7)           OLD ADDRESS (CURRENT)         JDM1
         USING TCB,R7                                            JDM1
         L     R7,12(R7)           TIOT POINTER
         MVC   JOBNAME,=C'  '      ESTHETICS
         MVC   JOBNAME+2(8),0(R7)  JOBNAME
         DROP  R7                                                JDM1
         LA    R1,MYWTOL           BEGINNING ADDRESS
         LA    R6,5(R6)            ORG LTH+4
         LA    R1,0(R1,R6)         END OF LIST SO FAR
         MVC   0(10,R1),JOBNAME    MOVE IN JOB NAME
         LA    R6,10(R6)
         STH   R6,MYTXTLTH         MOVE TO MY LIST
         MVC   MYMCSFLG(2),WTOMDL+2 MODEL TO REAL
         LA    R1,MYWTOL           SET UP
         LA    R6,0(R1,R6)         DISPLACEMENT FOR LAST PARMS
         MVC   0(4,R6),WTOMDL+9    DESC + ROUT
         SPACE
*--------------------------------------------------------------------*
*        THIS IS THE PLACE YOU MUST PLACE YOUR CODE TO GET YOU
*        INTO SUPERVISOR STATE. GENERALLY SOME SVC.
*--------------------------------------------------------------------*
         SPACE
INSUP    EQU   *
***      ADD
*********YOUR CODE TO GET INTO SUPERVISOR STATE
***      HERE
         SPACE
         LA    R1,MYWTOL           POINT TO PARM LIST
         WTO   MF=(E,(1))          MISSION ACCOMPLISHED
         SPACE
OUTSUP   EQU   *
***      ADD
*********YOUR CODE TO GET OUT OF SUPERVISOR STATE
***      HERE
         SPACE
         SR    R15,R15             ALLSOK
         B     EXIT
NOQUOTES SH    R6,=H'1'            SUB 1 FROM LENGTH FOR EX
         B     RESUME1           REST OF THE PROCESSING IS THE SAME
ERRNOTXT EQU   *
         LA    R1,MSG02
         LA    R0,L'MSG02
         BAL   R14,PUTMSG
         LA    R15,8
         B     EXIT
ERROVER  EQU   *
         LA    R1,MSG03
         LA    R0,L'MSG03
         BAL   R14,PUTMSG
         LA    R15,12
         B     EXIT
*--------------------------------------------------------------------*
*        PUTLINE ROUTINES
*--------------------------------------------------------------------*
PUTMSG   EQU   *
         STM   R14,R1,PUTLINS
         XC    MYOLD(8),MYOLD
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPM
         LA    R14,1
         ST    R14,MYOLD   NUMBER OF MESSAGE SEGMENTS
         LA    R14,MYSEG1
         ST    R14,MYOLD+4
         LR    R14,R0
         LA    R14,4(,R14)
         LA    R15,MYSEG1+4
         CLC   0(3,R1),=C'IKJ'
         BE    *+16
         LA    R14,1(,R14)    ADD 1 TO LENGTH
         MVI   0(R15),C' '
         LA    R15,1(,R15)   BUMP POINTER
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   0(0,R15),0(R1)
         EX    R14,*-6
         PUTLINE PARM=MYPTPB,OUTPUT=(MYOLD),MF=(E,MYIOPL)
         LM    R14,R1,PUTLINS
         BR    R14
PARSERR  EQU   *
         LA    R1,MSG01
         LA    R0,L'MSG01
         BAL   R14,PUTMSG
         LA    R15,12
         B     EXIT
EXIT0    LA    R15,0
EXIT     EQU   *
         ST    R15,RC
         IKJRLSA MYANS
         LR    R1,R13
         L     R15,RC
         L     R13,4(,R13)
         ST    R15,16(,R13)
         FREEMAIN R,A=(1),LV=MYDSECTL
         LM    R14,R12,12(R13)
         BR    R14
         TITLE 'CONSTANTS'
MSG01    DC    C'ERROR IN PARSE SERVICE ROUTINE'
MSG02    DC    C'NO TEXT FOUND'
MSG03    DC    C'TEXT CAN''T EXCEED 114.'
         DS    0F
MODLPTPM PUTLINE OUTPUT=(1,TERM,SINGLE,INFOR),                         *
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
WTOMDL   WTO   'DUMMY',ROUTCDE=(2),DESC=(2),MF=L
         SPACE
         LTORG
         SPACE
*--------------------------------------------------------------------*
*        PARSE PARAMETERS
*--------------------------------------------------------------------*
         SPACE
         PRINT NOGEN
MYPCL    IKJPARM
WTCMSG   IKJPOSIT STRING,ASIS,PROMPT='TEXT OF MESSAGE TO BE SENT'
         IKJENDP
         PRINT GEN
         TITLE 'DSECTS'
         SPACE
MYDSECT  DSECT
         DS    18F
RC       DS    F
MYPPL    DS    7F
MYANS    DS    F
MYUWA    DS    F
MYECB    DS    F
MYIOPL   DS    4F
MYPTPB   DS    3F
MYOLD    DS    2F
MYSEG1   DS    2F,CL100
PUTLINS  DS    4F
         DS    0F
MYWTOL   DS    0CL132
MYTXTLTH DS    XL2
MYMCSFLG DS    XL2
MYREST   DS    CL128
JOBNAME  DS    CL10
MYDSECTL EQU   *-MYDSECT
         SPACE
         IKJIOPL
         SPACE
         IKJPPL
         SPACE
         IKJCPPL
         SPACE 1                                                 JDM1
         CVT   DSECT=YES,LIST=NO                                 JDM1
         SPACE 1                                                 JDM1
         IKJTCB DSECT=YES,LIST=NO                                JDM1
         SPACE 1                                                 JDM1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   WTC
