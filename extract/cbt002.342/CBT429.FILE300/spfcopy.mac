         TITLE '   S P F C O P Y   '
*$DOC@*****************************************************************
*                                                                     *
*         SPFCOPY - SPF COMPRESS REPLACEMENT FOR IEBCOPY.             *
*                                                                     *
*         THIS IS A PROGRAM THAT ALLOWS UNAUTHORIZED TASKS            *
*         (SUCH AS SPF) TO RUN IEBCOPY BY INVOKING THIS               *
*         PROGRAM INSTEAD OF IEBCOPY.                                 *
*                                                                     *
*         THIS PROGRAM MUST RESIDE IN THE LPA TO WORK PROPERLY.       *
*         IT ISSUES A USER SVC (239) WHICH WILL NOT WORK UNLESS       *
*         IT IS ISSUED FROM THE LPA.                                  *
*                                                                     *
*         THIS CODE IS COPIED FROM THE SPF INSTALLATION GUIDE         *
*         (IBM PUBLICATION SH20-2402-0).  THERE IS A MINOR CHANGE     *
*         TO THE USER SVC - OURS IS 239, NOT 235, AND OURS USES       *
*         REGISTER 1 AS A SWITCH INSTEAD OF REGISTER 0, AND OURS      *
*         USES REGISTER 0 AS A CONSTANT VALUE 0.                      *
*                                                                     *
*         THE PURPOSE OF THIS PROGRAM IS TO ALLOW SPF, WHICH IS       *
*         AN UNAUTHORIZED PROGRAM, TO INVOKE 'IEBCOPY', WHICH IS      *
*         AUTHORIZED, YET STILL MAINTAIN SYSTEM INTEGRITY.            *
*                                                                     *
*         DIFFERENCES BETWEEN THIS CODE AND THAT IN THE GUIDE WERE    *
*         WRITTEN BY BILL GODFREY, PRC COMPUTER CENTER INC,           *
*         MCLEAN VA, MARCH 1980.                                      *
*         THE ESTAE EXIT WAS EXTENSIVELY CHANGED. THE ESTAE EXIT      *
*         IN THE GUIDE APPEARED TO HAVE SOME ERRORS.                  *
*                                                                     *
*         THE PROGRAM SETS THE AUTHORIZATION CODE ON, INVOKES         *
*         IEBCOPY, THEN SETS THE AUTHORIZATION CODE OFF.              *
*                                                                     *
*         UNEXPECTED INTERRUPTIONS WHILE THE AUTHORIZATION CODE       *
*         IS ON ARE TRAPPED BY THE ESTAE EXIT SO THE AUTHORIZATION    *
*         CAN BE SET BACK OFF. ATTENTION INTERRUPTS ARE DEFERRED.     *
*                                                                     *
*         IF THIS PROGRAM ABENDS WITH AN 047, THE USER SVC WAS        *
*         INSTALLED AS THE TYPE OF SVC THAT REQUIRES AUTHORIZATION.   *
*         USE ANOTHER SVC NUMBER - ONE THAT DOES NOT REQUIRE AUTH.    *
*                                                                     *
*         IF THIS PROGRAM ABENDS WITH A B0A, THE USER SVC THINKS      *
*         SPFCOPY IS NOT IN LPA, AND IEBCOPY HAS ABENDED.             *
*                                                                     *
*$DOC$*****************************************************************
         SPACE
SPFCOPY  CSECT
         USING *,12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'SPFCOPY'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(0),AL3(ENDDATD-DATD)
SPF      EQU   248                      SVC NUMBER
@PROLOG  STM   14,12,12(13)
         LR    12,15                    BASE REGISTER
         LR    2,1                      SAVE PARM POINTER
         L     0,@SIZE
         GETMAIN R,LV=(0)
         ST    13,4(,1)
         ST    1,8(,13)
         LR    13,1
         LR    11,1
         USING DATD,11
         STAX  DEFER=YES
         LA    3,40
         LTR   15,15
         BNZ   RETURN
         LA    4,ESTAEX
         LA    5,ESTAEP
         ST    11,ESTAEP
         MVC   ESTAEW(ESTAELL),ESTAEL
         ESTAE (4),PARAM=(5),MF=(E,ESTAEW)
         LA    3,44
         LTR   15,15
         BNZ   STAXOFF
         SR    0,0                 REQUEST AUTHORIZATION CHANGE
         LA    1,1                 SET AC(1)
         SVC   SPF                 ISSUE UTILITY SVC
         LR    1,2
         LINK  EP=IEBCOPY
         LR    3,15
         SR    0,0                 REQUEST AUTHORIZATION CHANGE
         SR    1,1                 SET AC OFF
         SVC   SPF                 ISSUE UTILITY SVC
         ESTAE 0
STAXOFF  EQU   *
         STAX  DEFER=NO
RETURN   LR    1,13
         L     0,@SIZE
         L     13,4(,13)
         FREEMAIN R,LV=(0),A=(1)
         LR    15,3
         L     14,12(,13)
         LM    0,12,20(13)
         BR    14
         SPACE
ESTAEL   ESTAE 0,MF=L
ESTAELL  EQU   *-ESTAEL
         SPACE
*         THE ESTAE EXIT IN THE INSTALLATION GUIDE IS INCORRECT.
*         IT PRESUMES THAT REGISTER 15 SHOULD BE SET TO ZERO
*         TO PASS A RETURN CODE, BUT THE 'SETRP' MACRO SHOULD
*         BE USED TO SET THE RETURN CODE IN THE 'SDWA' (IF THERE
*         IS AN SDWA, WHICH REGISTER 0 WILL INDICATE.)
         SPACE
         DC    0D'0'
         USING *,15
ESTAEX   EQU   *
         C     0,=F'12'            IS THERE AN SDWA
         BNE   ESTAEOK             YES, BRANCH
         L     11,0(,2)            GET ADDRESS FROM ESTAEP
         STM   14,12,ESTAES        SAVE REGISTERS
         LR    12,15               NEW BASE REGISTER
         DROP  15
         USING ESTAEX,12
         SR    0,0                 REQUEST AUTHORIZATION CHANGE
         SR    1,1                 SET AC OFF
         SVC   SPF                 ISSUE UTILITY SVC
         STAX  DEFER=NO
         LM    14,12,ESTAES
         XR    15,15               CONTINUE WITH TERMINATION
         BR    14
         SPACE
ESTAEOK  STM   14,12,12(13)
         LR    12,15
         LR    11,1                SAVE SDWA ADDRESS
         SR    0,0                 REQUEST AUTHORIZATION CHANGE
         SR    1,1                 SET AC OFF
         SVC   SPF                 ISSUE UTILITY SVC
         STAX  DEFER=NO
         LR    1,11                RESTORE SDWA ADDRESS
         SETRP REGS=(14,12),RC=0
         SPACE
         IHASDWA
*DWA     DSECT
*DWAPARM DS    A
*DWARCDE EQU   SDWA+X'FC'
         SPACE
DATD     DSECT
SAVEAREA DS    18F
ESTAEP   DS    F                   ESTAE EXIT PARM
ESTAES   DS    15F                 ESTAE EXIT SAVE AREA
ESTAEW   DS    0F,(ESTAELL)X       ESTAE MF=L
ENDDATD  DS    0D                  END OF DYNAMIC AREA
         END   SPFCOPY
