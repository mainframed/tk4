         TITLE '   E C H O      '
*--------------------------------------------------------------------*
*        'ECHO'                                                      *
*                                                                    *
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.   *
*        DATE WRITTEN. FEB 10 1977.                                  *
*        DATE UPDATED.                                               *
*        ATTRIBUTES. RE-ENTRANT.                                     *
*        REMARKS.                                                    *
*        THIS PROGRAM TAKES A LINE ENTERED FROM A TSO TERMINAL       *
*        AND ECHOES IT BACK IN HEX.                                  *
*                                                                    *
*        JIM MARSHALL, CAPT, USAF (RET)                              *
*                                                                    *
* CHNGE LOG: FIX SOME CODE FOR MVS/XA AND ALSO IT WAS SHOWING    JDM1*
*            THE PREVIOUS 6 CHARACTERS IN HEX TOO.                   *
*--------------------------------------------------------------------*
         SPACE
ECHO     CSECT                                                   JDM1
ECHO     AMODE 31                                                JDM1
ECHO     RMODE 24                                                JDM1
         SAVE  (14,12),,ECHO_&SYSDATE_&SYSTIME                   JDM1
         LR    R12,15
         USING ECHO,R12                                          JDM1
         GETMAIN R,LV=MYDSECTL
         ST    13,4(,1)
         ST    1,8(,13)
         LR    14,13
         LR    13,1
         L     1,24(,14) RESTORE R1
         USING MYDSECT,13
         SPACE 1
         LA    R1,PROMPT
         LA    R0,L'PROMPT
         SVC   93
         SPACE
         MVI   BUFFER,C' '         CLEAR THE                     JDM1
         MVC   BUFFER+1(127),BUFFER  INPUT BUFFER                JDM1
         L     R0,=F'32'                                         JDM1
         SLR   R1,R1                                             JDM1
         LA    R1,BUFFER
         ICM   R1,8,=X'81'         TGET ASIS
         SVC   93
         SPACE
         LTR   R15,R15
         BZ    OKTGET
         B     EXIT
OKTGET   EQU   *
         LR    R3,R1            GET LENGTH
         LTR   R3,R3
         BNZ   NOTNULL
         LA    R1,=C'(NULL LINE)'
         L     R0,=F'11'                                         JDM1
         SVC   93
         B     EXIT0
NOTNULL  EQU   *
         MVI   LINE,C' '           CLEAR THE                     JDM1
         MVC   LINE+1(127),LINE      OUTPUT LINE                 JDM1
         LA    R1,BUFFER
         LR    R0,R3
         LA    R15,LINE
         BAL   R14,UNPACK
         SPACE
         LA    R1,LINE+12                                        JDM1
         LR    R0,R3
         SLL   R0,1             DOUBLE LENGTH                    JDM1
         TPUT  (R1),(R0),R                                       JDM1
         SPACE
         B     EXIT0
         SPACE
************************************************************
*                                                          *
*  UNPACK - CONVERT A FIELD TO HEXADECIMAL.                *
*  REG 1 --> INPUT   REG 15 --> OUTPUT                     *
*  REG 0  =  INPUT LENGTH  (OUTPUT IS TWICE PLUS 1 BLANK)  *
*  REG 14 --> RETURN ADDRESS ( BAL   R14,UNPACK )          *
*                                                          *
************************************************************
         SPACE
UNPACK   MVC   1(1,R15),0(R1)      MOVE BYTE
         UNPK  0(3,R15),1(2,R15)   UNPACK
         TR    0(2,R15),UNPACKT-240
         A     R15,=F'2'           INCREMENT OUTPUT PTR          JDM1
         A     R1,=F'1'            INCREMENT INPUT PTR           JDM1
         BCT   R0,UNPACK           DECREMENT LENGTH, THEN LOOP
         MVI   0(R15),C' '         BLANK THE TRAILING BYTE
         BR    R14                 RETURN TO CALLER
         SPACE
EXIT0    SR    15,15
EXIT     LR    1,13
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=MYDSECTL
         LM    14,12,12(13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
PROMPT   DC    C'TYPE IN CHARACTERS TO BE ECHOED'
         DC    128X'00'
UNPACKT  DC    C'0123456789ABCDEF' TRANSLATE TABLE
         LTORG
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
MYDSECT  DSECT
         DS    18F                 REGISTER SAVEAREA
BUFFER   DS    CL128
LINE     DS    CL128
MYDSECTL EQU   *-MYDSECT
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
