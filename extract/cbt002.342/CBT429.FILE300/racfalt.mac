         TITLE 'RACFALT - GEN CLIST FOR ALTERING RACF DATA SET DEF'
***********************************************************************
*                                                                     *
*                D  I  S  C  L  A  I  M  E  R                         *
*                                                                     *
*        AS ALWAYS YOU USE THIS PROGRAM AT YOUR OWN RISK. THERE       *
*        ARE NO GUARANTEES INTENDED.                                  *
*                                                                     *
*                                                                     *
*             MODULE NAME = RACFALT                                   *
*                                                                     *
*             DESCRIPTIVE NAME = GENERATE CLIST TO CHANGE THE         *
*                VOLUME SERIAL NUMBERS IN THE NON-VSAM DATA           *
*                SETS ON THE SPECIFIED VOLUME.                        *
*                                                                     *
*             FUNCTION =                                              *
*                CREATES CLIST WHICH WILL ALLOW A SYSTEM              *
*                PROGRAMMER TO CHANGE THE VOLUME SERIAL               *
*                NUMBER IN EACH DATA SET ON A SPECIFIED               *
*                VOLUME.  THIS WILL BE ACCOMPLISHED WITH              *
*                THE 'ALTDSD' RACF COMMAND.                           *
*                                                                     *
*                OPERATION =                                          *
*                   GET NEW VOLUME SERIAL NUMBER FROM PARAMETERS      *
*                   OPEN THE VTOC FOR THE SPECIFIED VOLUME            *
*                   OPEN THE SPECIFIED OUTPUT FILE (FOR CLIST)        *
*                   READ FORMAT 1 RECORDS OF VTOC FOR DATA SET NAME   *
*                   IGNORE CERTAIN DATA SETS, INCLUDING ALL VSAM      *
*                   IF RACF PROTECTED,                                *
*                     WRITE LINE TO CHANGE VOLUME SERIAL NUMBER       *
*                   CONTINUE READING VTOC TIL ALL DATA SETS PROCESSED *
*                   CLOSE VTOC AND OUTPUT FILE                        *
*                   EXIT                                              *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 1  = WORK REGISTERS               *
*                    REGISTERS 2 TO 4  = LINKAGE & WORK REGISTERS     *
*                    REGISTERS 5 TO 9  = WORK REGISTERS               *
*                    REGISTER  10      = ADDRESSABILITY TO DCB        *
*                    REGISTER  11      = ADDRESSABILITY TO JFCB &     *
*                                        DSCB                         *
*                    REGISTER  12      = WORK REGISTER                *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*             MACROS =                                                *
*                SAVE, GETMAIN, RDJFCB, OPEN, READ, CHECK,            *
*                CLOSE, PUT                                           *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                10/21/83  WRITTEN.      BC.                          *
*                                                                     *
* SAMPLE JCL =                                                        *
*                                                                     *
*   //JOBNAME JOB (GEE1,0104),'PROG NAME',CLASS=S,MSGCLASS=A,         *
*   //        MSGLEVEL=(2,0),USER=SYSPGM,PASSWORD=????????            *
*   //STEP1 EXEC  PGM=RACFALT,PARM='NEWVOL'                           *
*   //VTOC  DD UNIT=3350,VOL=SER=DLX170,DISP=OLD                      *
*   //CLIST DD DSN=T99999.TEST.CLIST,VOL=SER=DLXTSO,UNIT=3380,        *
*   //         DISP=(NEW,CATLG),SPACE=(TRK,(10,1)),                   *
*   //         DCB=(RECFM=VB,LRECL=255,BLKSIZE=6233)                  *
*                                                                     *
***********************************************************************
*
         PRINT NOGEN
RACFALT CSECT
         SAVE  (14,12)                 SAVE REGISTERS
         LR    R12,R15
         USING RACFALT,R12             ESTABLISH ADDRESSABILITY
         LR    R2,R1                   PRESERVE PTR TO PARMS
         GETMAIN R,LV=LDATA            GET DYNAMIC WORK AREA
         LR    R10,R1                  PRESERVE PTR TO WORK AREA
         ST    R13,4(R10)              STORE ADDR OF PREVIOUS SA
         ST    R10,8(,R13)             STORE ADDR OF CURRENT SA
         LR    R13,R10                 LOAD ADDR OF CURRENT SA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
*
*  GET PARMS TO DETERMINE THE ALTERNATE VOLUME.
*
GETPARM  EQU   *
         L     R5,0(R2)                GET PARM ADDRESS
         LH    R3,0(R5)                SAVE PARM LENGTH
         LTR   R3,R3                   ANY PARM DATA?
         BZ    GP40                    IF NOT, BRANCH TO ABEND
         LA    R5,2(R5)                SKIP PARM LENGTH FIELD
         LA    R4,PARMBUF              SET POINTER INTO PARM BUFFER
         MVI   PARMBUF,X'40'           CLEAR PARM BUFFER
         MVC   PARMBUF+1(L'PARMBUF-1),PARMBUF
GP10     EQU   *
         CLI   0(R5),C','              DELIMITING COMMA?
         BE    GP20                    IF SO, BRANCH
         MVC   0(1,R4),0(R5)           STORE PARM CHARACTER
         LA    R4,1(R4)                INCREMENT PARM BUFFER POINTER
         LA    R5,1(R5)                GO TO NEXT PARM CHARACTER
         BCT   R3,GP10                 LOOP BACK TIL DONE
GP20     EQU   *
         MVC   ALTVOL,PARMBUF          STORE ALTERNATE VOLUME
         B     INIT                    AND IGNORE ANYTHING ELSE
GP40     EQU   *
         ABEND 16,DUMP
*
*  INITIALIZE THE WORK AREA.  OPEN THE VTOC AND CLIST OUTPUT FILE.
*    WRITE THE FIRST 2 LINES OF THE CLIST INTO THE OUTPUT FILE.
*
INIT     EQU   *
         LA    R6,DATA                 SET UP REGISTERS FOR
         LA    R7,MOVEL                  LONG MOVE
         LA    R8,CDATA
         LA    R9,MOVEL
         MVCL  R6,R8                   LOAD INITIAL DATA INTO WORKAREA
         LA    R10,VTOC                GET ADDRESS OF DCB1
         USING IHADCB,R10              ADDRESSIBILITY TO DCB
         LA    R11,JFCBA               GET ADDRESS OF BUFFER
         ST    R11,EXLIST              STORE ADDRESS IN EXIT LIST
         MVI   EXLIST,X'87'            SET END OF LIST & JFCB BYTE
         LA    R6,EXLIST               GET ADDRESS OF EXIT LIST
         ST    R6,DCBEXLST             STORE IN DCB
         DROP  R10
         RDJFCB (VTOC,),MF=(E,EX1)     READ JOB FILE CONTROL BLOCK
         USING INFMJFCB,R11            ESTABLISH ADDRESSABILITY
         LTR   R15,R15                 CHECK RETURN CODE
         BNZ   RETURN                  IF NOT SUCCESSFUL, EXIT
         MVI   JFCBDSNM,X'04'          CHANGE DATASET NAME TO
         MVC   JFCBDSNM+1(43),JFCBDSNM   44 X'04'S
         OI    JFCBTSDM,JFCNWRIT       SET FLAG NOT TO WRITE BACK
         MVC   VOL,JFCBVOLS            SAVE VOLSER
         DROP  R11
         OPEN  (VTOC,INPUT),TYPE=J,MF=(E,EX2) OPEN THE VTOC
         LTR   R15,R15
         BNZ   CLOSEX
IN10     EQU   *
         OPEN  (PRINT,OUTPUT),MF=(E,EX2) OPEN OUTPUT FILE
         LTR   R15,R15
         BNZ   CLOSEX
         MVI   LINE,X'40'              INITIALIZE LINE
         MVC   LINE+1(L'LINE-1),LINE     TO ALL SPACES
         LA    R5,RPARMS               SET R5 TO PARM ADDRESS
         MVC   DLXCMD(L'FSTLINE),FSTLINE
         BAL   R3,PRINTIT              OUTPUT FIRST LINE OF CLIST
         MVC   DLXCMD(L'SNDLINE),SNDLINE
         BAL   R3,PRINTIT              OUTPUT SECOND LINE OF CLIST
*
*  READ THE VTOC FORMAT-1 RECORDS.  PROCESS ALL NON-VSAM DATA SETS
*    ON VOLUME WITH THE EXCEPTION OF A FEW (SUCH AS THE VTOC INDEX,
*    CATALOG, CATALOG INDEX, TEMPORARY DATA SETS, ETC.)
*
READIT   EQU   *
         READ  DECB,SF,VTOC,DSCB,'S',MF=E  READ A VTOC RECORD
         CHECK DECB                    WAIT TIL DONE
         USING IECSDSL1,R11            ESTABLISH ADDRESSABILITY
         LA    R11,DSCB                LOAD DSCB ADDRESS
         CLI   DS1FMTID,C'1'           FORMAT 1 RECORD?
         BNE   READIT                  IF NOT, GO BACK FOR ANOTHER
         CLC   DS1DSNAM(11),=C'SYS1.VTOCIX'
         BE    READIT
         CLC   DS1DSNAM(9),=C'SYS1.VVDS'
         BE    READIT
         CLC   DS1DSNAM(7),=C'CATALOG'            IGNORE CERTAIN
         BE    READIT                              DATA SETS
         CLC   DS1DSNAM(8),=C'CATINDEX'
         BE    READIT
         CLC   DS1DSNAM(8),=C'FDRABR.V'
         BE    READIT
         CLC   DS1DSNAM(3),=C'SYS'
         BNE   RI30
         LA    R7,DS1DSNAM+3
         LA    R8,5
RI20     EQU   *
         TM    0(R7),X'F0'
         BNO   RI30
         LA    R7,1(R7)
         BCT   R8,RI20
         B     READIT
RI30     EQU   *
         TM    DS1DSORG+1,DS1ACBM      VSAM FILE?
         BO    READIT                  IF SO, IGNORE IT
         TM    DS1DSIND,DS1IND40       RACF PROTECTED?
         BNO   READIT                  IF NOT, IGNORE IT
*
*  GENERATE CLIST COMMANDS FOR THE DATA SET.
*
NONVSAM  EQU   *
         MVC   DLXCMD,=C'DLXAUTHC'     LOAD PRINT LINE FOR
         MVC   RACFCMD,=C'ALD'           MODIFYING RACF PROFILE
         MVC   0(L'DS1DSNAM,R5),DS1DSNAM
         BAL   R2,NXTSPC               FIND NEXT SPACE
         MVC   0(L'VOLPARM,R5),VOLPARM   AND LOAD VOLUME PARM
         MVC   L'VOLPARM(L'VOL,R5),VOL
         MVI   L'VOLPARM+L'VOL(R5),C')'
         BAL   R2,NXTSPC               FIND NEXT SPACE
         MVC   0(L'AVOLPARM,R5),AVOLPARM  AND LOAD ALTERNATE VOLUME
         MVC   L'AVOLPARM(L'VOL,R5),VOL
         MVC   L'AVOLPARM+L'VOL+1(L'ALTVOL,R5),ALTVOL
         MVI   L'AVOLPARM+L'VOL+1+L'ALTVOL(R5),C')'
         BAL   R3,PRINTIT              PRINT THE LINE
         B     READIT                  GO BACK FOR MORE
*
*  CLOSE FILES AND TERMINATE PROGRAM.
*
CLOSEX   EQU   *                       ERROR EXIT
         LR    R7,R15                  SAVE CONTENTS OF R15
         CLOSE (VTOC,,PRINT),MF=(E,EX4) CLOSE THE FILES
         LR    R15,R7                  RESTORE CONTENTS OF R15
         B     RETURN
CLOSE    EQU   *                       STANDARD EXIT
         MVC   DLXCMD(L'LSTLINE),LSTLINE
         BAL   R3,PRINTIT              OUTPUT LAST LINE OF CLIST
         CLOSE (VTOC,,PRINT),MF=(E,EX4) CLOSE THE FILES
RETURN   L     R13,4(R13)              RESTORE REGISTERS
         RETURN (14,12),RC=(15)        AND END
*
***********************************************************************
*                                                                     *
*                      SUBROUTINES                                    *
*                                                                     *
***********************************************************************
*
*  SUBROUTINE TO WRITE THE OUTPUT LINE INTO THE CLIST DATA SET
*
PRINTIT  EQU   *
         AP    LINE#,=P'100'           INCREMENT LINE NUMBER
         UNPK  LINENO,LINE#            UNPACK LINE NUMBER
         MVZ   LINENO+7(1),LINENO+6      & MOVE THE ZONE BYTE
         BAL   R2,NXTSPC               FIND END OF LINE
         LA    R2,LINE
         SR    R5,R2                   CALCULATE LINE LENGTH
         STH   R5,LINE                   AND STORE
         MVC   LINE+2(2),=X'0000'
         PUT   PRINT,LINE              PRINT THE LINE
         MVI   LINE,X'40'              RESET THE LINE BUFFER
         MVC   LINE+1(L'LINE-1),LINE     TO SPACES
         LA    R5,RPARMS               RESET R5 TO PARM ADDRESS
         BR    R3                      RETURN
*
*  SUBROUTINE TO POINT TO THE NEXT DATA POSITION IN THE OUTPUT LINE.
*
NXTSPC   EQU   *
         LA    R5,1(R5)                INCREMENT POINTER
         CLC   0(4,R5),=C'    '        CHECK FOR A SPACE
         BNE   NXTSPC                  LOOP TIL SPACE FOUND
         LA    R5,1(R5)                INCREMENT ONCE MORE
         BR    R2                      RETURN
*
***********************************************************************
*                                                                     *
*                     CONSTANT DATA                                   *
*                                                                     *
***********************************************************************
*
*  REGISTER EQUATES
*
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         LTORG
*
*    THE FOLLOWING DATA IS USED TO INIT PART OF THE GETMAIN'ED AREA
*
CDATA    DS    0F
ILINE#   DC    P'00000000'
DCB1     DCB   DDNAME=VTOC,DSORG=PS,RECFM=U,KEYLEN=44,BLKSIZE=96,      X
               MACRF=(R),EODAD=CLOSE
DCB2     DCB   DDNAME=CLIST,DSORG=PS,RECFM=VB,LRECL=255,               X
               MACRF=(PM)
LIST1    RDJFCB (,),MF=L
LIST2    OPEN   (,),MF=L
LIST3    READ   DECB1,SF,MF=L
LIST4    CLOSE  (,),MF=L
MOVEL    EQU   *-CDATA
*
*  MISCELLANEOUS CONSTANTS
*
VOLPARM  DC    C'VOLUME('
AVOLPARM DC    C'ALTVOL('
FSTLINE  DC    C'PROC 0'
SNDLINE  DC    C'CONTROL LIST NOFLUSH PROMPT MSG'
LSTLINE  DC    C'END'
*
***********************************************************************
*                                                                     *
*                    DSECTS FOR GETMAIN'ED AREA                       *
*                                                                     *
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F
*
LINE     DS    0CL255
         DS    CL4
LINENO   DS    CL8
DLXCMD   DS    CL8
         DS    CL1
RACFCMD  DS    CL3
         DS    CL1
RPARMS   DS    CL230
*
PARMBUF  DS    CL20
ALTVOL   DS    CL6
VOL      DS    CL6
DSCB     DS    XL140
JFCBA    DS    XL176
EXLIST   DS    1F
*
DATA     DS    0F
LINE#    DS    P'00000000'
VTOC     DCB   DDNAME=VTOC,DSORG=PS,RECFM=U,KEYLEN=44,BLKSIZE=96,      X
               MACRF=(R),EODAD=CLOSE
PRINT    DCB   DDNAME=CLIST,DSORG=PS,RECFM=VB,LRECL=255,               X
               MACRF=(PM)
EX1      RDJFCB (,),MF=L
EX2      OPEN   (,),MF=L
EX3      READ   DECB,SF,MF=L
EX4      CLOSE  (,),MF=L
*
LDATA    EQU   *-WORKAREA
*
*
*
DSCB1    DSECT
         IECSDSL1 1
JFCB     DSECT
         IEFJFCBN ,
DCBSECT  DSECT
         DCBD  DSORG=PS,DEVD=DA
         END   RACFALT
