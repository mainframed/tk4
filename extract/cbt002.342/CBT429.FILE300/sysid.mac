          TITLE 'SYSID - PUT SYSTEM SMF ID INTO AN ISPF VARIABLE'
*--------------------------------------------------------------------*
*                                                                    *
*        THIS IS AN EXAMPLE OF HOW TO GET SOME INFORMATION FROM      *
*        MVS CONTROL BLOCKS AND PUT IT INTO AN ISPF VARIABLE. IN     *
*        THIS CASE THE SMF ID FROM THE SMCA IS USEFUL TO YOU IN      *
*        KNOWING WHAT SYSTEM YOU ARE PROCESSING. IF YOU KNOW THE     *
*        SYSTEM AND YOU HAVE SAY A PRODUCTION AND TEST SYSTEM, THEN  *
*        ONE ISPF DIALOG CAN FIT ALL. INTERESTING CONCEPT !!!!!      *
*                                                                    *
*        WE RETRIEVE THE SMF ID FROM THE SYSTEM MANAGEMENT CONTROL   *
*        AREA AND STORE IT IN THE USER'S 'SHARED' VARIABLE POOL      *
*        AS &SYSID.                                                  *
*                                                                    *
*        EVEN IF YOU ARE NOT IN THE ISPF ENVIRONMENT, SET THE RC     *
*        ACCORDING TO WHAT SYSTEM YOU ARE PROCESSING ON. MANY SHOPS  *
*        WOULD LIKE TO WRITE GENERIC CLISTS AND HAVE THEM GOOD       *
*        ACROSS SYSTEMS. SOMETIMES YOU CAN NOT DO IT BECAUSE THERE   *
*        IS SYSTEM SPECIFIC CODE. WITH THIS CODE YOU CAN KNOW WHAT   *
*        SYSTEM YOU ARE EXECUTING ON AND TAKE THE APPROPRIATE        *
*        ACTIONS.                                                    *
*                                                                    *
*        RETURN CODES:                                               *
*                                                                    *
*         R15  =  0  -  EXECUTING ON SYSTEM 1                        *
*                 4  -  EXECUTING ON SYSTEM 2                        *
*                                                                    *
*        JIM MARSHALL, CAPT, USAF                                    *
*        301-688-6829                                                *
*                                                                    *
*        DATE INSTALLED: 4 APR 90                                    *
*                                                                    *
*        CHNGE LOG: NONE                                             *
*                                                                    *
*--------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
SYSID    CSECT
SYSID    AMODE 24
SYSID    RMODE 24
         SPACE
         SAVE (14,12),,SYSID_&SYSDATE._&SYSTIME
         LR   R12,R15
         USING SYSID,R12
         L     R0,MEMSIZE
         GETMAIN R,LV=(0)
         USING WORK,R1
         ST    R13,SAVEAREA+4      STORE BACK TO SAVEAREA
         ST    R1,8(R13)           FORWARD POINTER
         LR    R13,R1              NEW SAVEAREA ADDR
         DROP  R1
         USING WORK,R13
         MVC   ID(4),=CL4' '       BLANK OUT FOR GOOD PRACTICE
         SPACE
*--------------------------------------------------------------------*
*        CHAIN FROM THE CVT TO FIND THE SMCA
*--------------------------------------------------------------------*
         SPACE
         L     R4,CVTPTR           LOAD ADDRESS OF CVT
         USING CVT,R4              ESTABLISH ADDRESSABILITY
         SPACE
         L     R5,CVTSMCA          LOAD ADDRESS OF SMCA
         USING SMCABASE,R5         EST ADDRESSABILITY
         MVC   ID(4),SMCASID       STORE SMF SYSID
         SPACE
*--------------------------------------------------------------------*
*        WE WILL SET THE RETURN CODE NOW FOR WHAT SYSTEM YOU
*        ARE EXECUTING
*--------------------------------------------------------------------*
         SPACE
         CLC   SYS1(4),ID          SEE IF IT IS SYSTEM 1
         BE    SETSYS1
         CLC   SYS2(4),ID          SEE IF IT IS SYSTEM 2
         BE    SETSYS2
         DC    X'00000000'         SHOULD NEVER DO THIS
         DS    0H
SETSYS1  EQU   *
         MVC   RC(4),=F'0'
         B     CHKISPF
         SPACE
SETSYS2  EQU   *
         MVC   RC(4),=F'4'
         B     CHKISPF
         SPACE
*--------------------------------------------------------------------*
*        GO SEE IF PROGRAM IS EXECUTING IN AN ISPF ENVIRONMENT
*--------------------------------------------------------------------*
         SPACE
CHKISPF  EQU   *
         LINK  EP=ISPQRY
         LTR   R15,R15
         BNZ   EOJ
         SPACE
*--------------------------------------------------------------------*
*        DEFINE THE SYSID VARIABLE TO ISPF
*--------------------------------------------------------------------*
         SPACE
ISPF1    EQU   *
         LA    R7,VDEF             GET ADDR OF VDEF
         ST    R7,CALL1            STASH THE ADDRESS
         LA    R7,MYSYSID          GET ADDR OF MYSYSID
         ST    R7,CALL1+4          STASH THE ADDRESS
         LA    R7,ID               GET ADDR OF ID
         ST    R7,CALL1+8          STASH THE ADDRESS
         LA    R7,CHAR             GET ADDR OF CHAR
         ST    R7,CALL1+12         STASH THE ADDRESS
         LA    R7,L4               GET ADDR OF L4
         ST    R7,CALL1+16         STASH THE ADDRESS
         OI    CALL1+16,X'80'      FLIP ON HIGH ORDER BIT
         SPACE
         LA    R1,CALL1            GET PARAM LIST ADDR
         LINK  EP=ISPLINK          GO OFF TO ISPLINK
         SPACE
         LTR   R15,R15
         BZ    ISPF2
         DC    X'00000000'         TRAP
         DS    0H
         SPACE
*--------------------------------------------------------------------*
*        WRITE THE VALUE INTO THE SHARED VARIABLE POOL
*--------------------------------------------------------------------*
         SPACE
ISPF2    EQU   *
         LA    R7,VPUT             GET ADDR OF VPUT
         ST    R7,CALL2            STASH THE ADDRESS
         LA    R7,MYSYSID          GET ADDR OF MYSYSID
         ST    R7,CALL2+4          STASH THE ADDRESS
         LA    R7,SHR              GET ADDR OF SHR
         ST    R7,CALL2+8          STASH THE ADDRESS
         OI    CALL2+8,X'80'       FLIP ON HIGH ORDER BIT
         SPACE
         LA    R1,CALL2            GET PARAM LIST ADDR
         LINK  EP=ISPLINK          GO OFF TO ISPLINK
         LTR   R15,R15
         BZ    ISPF3
         DC    X'00000000'         TRAP
         DS    0H
         SPACE
*--------------------------------------------------------------------*
*        DELETE THE VARIABLE FROM THIS POOL
*--------------------------------------------------------------------*
         SPACE
ISPF3    EQU   *
         LA    R7,VDEL             GET ADDR OF VDEL
         ST    R7,CALL3            STASH THE ADDRESS
         LA    R7,MYSYSID          GET ADDR OF MYSYSID
         ST    R7,CALL3+4          STASH THE ADDRESS
         OI    CALL3+4,X'80'       FLIP ON HIGH ORDER BIT
         SPACE
         LA    R1,CALL3            GET PARAM LIST ADDR
         LINK  EP=ISPLINK          GO OFF TO ISPLINK
         LTR   R15,R15
         BZ    EOJ
         DC    X'00000000'         TRAP
         DS    0H
EOJ      EQU   *
         L     R15,RC              GET RETURN CODE
         LR    R1,R13
         L     R0,MEMSIZE
         L     R13,4(R13)
         ST    R15,16(,R13)        SET THE RETURN CODE
         FREEMAIN R,A=(1),LV=(0)
         LM    R14,R12,12(R13)
         BR    R14
         SPACE
*--------------------------------------------------------------------*
*        DEFINE THE CONSTANTS, ISPF NEEDS THEM 8 BYTES LONG
*--------------------------------------------------------------------*
         SPACE
         LTORG
         SPACE
         DS    0F
MEMSIZE  DC    X'00'
         DC    AL3(WORKLEN)
         SPACE
VDEF     DC    CL8'VDEFINE'
VDEL     DC    CL8'VDELETE'
VPUT     DC    CL8'VPUT'
SHR      DC    CL8'SHARED'
MYSYSID  DC    CL8'(SYSID) '       ENSURE ONE BLANK AT END !!!!
CHAR     DC    CL8'CHAR'           ID IS CHARACTER DEFINED
SYS1     DC    CL4'CPU1'           SMFID OF SYSTEM 1
SYS2     DC    CL4'CPU2'           SMFID OF SYSTEM 2
L4       DC    F'4'                ID IS FOUR BYTES LONG
         LTORG
WORK     DSECT
SAVEAREA DS    18F                 SAVE AREA
ID       DS    CL4                 SYSTEM ID FIELD
RC       DS    F                   RETURN CODE
         SPACE
*--------------------------------------------------------------------*
*        DATA AREA TO DEFINE SYSID VARIABLE TO ISPF
*--------------------------------------------------------------------*
         SPACE
         DS    0F
CALL1    DS    0CL20
         DS    F                   A(VDEF)
         DS    F                   A(MYSYSID)
         DS    F                   A(ID)
         DS    F                   A(CHAR)
         DS    F                   A(L4+X'80000000')
         SPACE
*--------------------------------------------------------------------*
*        DATA AREA TO WRITE VARIABLE INTO SHARED POOL
*--------------------------------------------------------------------*
         SPACE
         DS    0F
CALL2    DS    0CL12
         DS    F                   A(VPUT)
         DS    F                   A(MYSYSID)
         DS    F                   A(SHR+X'80000000')
         SPACE
*--------------------------------------------------------------------*
*        DATA AREA TO WRITE VARIABLE INTO SHARED POOL
*--------------------------------------------------------------------*
         SPACE
         DS    0F
CALL3    DS    0CL8
         DS    F                   A(VDEL)
         DS    F                   A(MYSYSID+X'80000000')
WORKLEN  EQU   *-WORK
         SPACE
         TITLE  'CONTROL VECTOR TABLE - MACRO EXPANSION - CVT'
         CVT   DSECT=YES,LIST=NO
         TITLE  'SYSTEM MANAGEMENT FACILITY CONTROL AREA - SMCA'
         IEESMCA
         END
