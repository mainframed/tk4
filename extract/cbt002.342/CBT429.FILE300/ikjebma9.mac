//*-------------------------------------------------------------------*
//*        THIS JOB REPLACES THE TABLE OF USER SUBCOMMANDS            *
//*        OF THE TSO EDIT COMMAND. CSECT IKJEBMA9 IN LOAD            *
//*        MODULE IKJEBEMA IS REPLACED. IF THE LOAD MODULE            *
//*        IS MOVED FROM SYS1.CMDLIB TO SYS1.LPALIB, THIS             *
//*        JCL MUST BE MODIFIED ACCORDINGLY.                          *
//*                                                                   *
//*        NOTE 02/04/89 -                                            *
//*                                                                   *
//*        THERE HAS BEEN AN ADDED ALIAS 'IKJEBEAE' TO IKJEBEMA       *
//*        AND HAVE INCLUDED ALOT OF MY OWN EDIT SUBCOMMAND PLUS      *
//*        TSO COMMANDS.                                              *
//*                                                                   *
//*        FOR THE DOUBTING PERSON WHO IS AFRAID THE ZAP CMDLIB       *
//*        YOU CAN USE YOUR OWN PRIVATE UNATHORIZED LIBRARY OR        *
//*        PLACING IN A PERSONAL LOADLIB ALLOCATED TO ISPF/PDF        *
//*        ISPLLIB. THIS WILL SHOW YOU HOW IT WORKS. THEN IF IN       *
//*        ISP/PDF, DO TSO EDIT FROM OPTION 6.                        *
//*                                                                   *
//*-------------------------------------------------------------------*
//IKJEBMA9 EXEC ASMHCL,PARM.ASM='OBJ,NODECK,RENT',
//         PARM.LKED='RENT,LIST,MAP'
//SYSIN    DD *
         TITLE '         I K J E B M A 9             '
************************************************************
*        'IJKEBMA9' TABLE OF TSO 'EDIT' SUBCOMMANDS        *
************************************************************
         SPACE
*        BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. AIR FORCE DATA SERVICES CENTER, PENTAGON.
*        DATE UPDATED. 04 FEB 1989 - JIM MARSHALL
*        ATTRIBUTES. RE-ENTRANT.
*        COMMENTS.
*            THIS IS MODULE IKJEBMA9, A TABLE OF USER SUBCOMMANDS
*            OF THE TSO EDIT COMMAND.
*            THE TABLE IS DEFINED BY OPERANDS OF THE 'IKJEBEST'
*            MACRO, WHICH IS INCLUDED HERE WITHOUT MODIFICATION
*            FROM THE IBM MVS PRIVMACS TAPE.
         SPACE
         PRINT OFF     (SUPPRESS PRINTING THE 68 LINE MACRO)
         MACRO
         IKJEBEST &CSECT=IBM
         LCLA  &A,&B,&C,&D,&E
         LCLA  &F                                                A42956
         LCLC  &CNAME,&SCNAME,&ABBR,&LDMOD,&LABEL,&LABEL1,&LABEL2,&NMBR
.*A035000,185000,230600-239000,255000                            A42956
         AIF   ('&CSECT' NE 'IBM').CONT0
&CNAME   SETC  'IKJEBMA8'     DEFINE CSECT NAME FOR IBM TABLE.
IKJEBMA8 CSECT
         ENTRY MA8IP002
         ENTRY MA8LI002
         AGO   .CONT1
.CONT0   ANOP
         AIF   ('&CSECT' NE 'USER').ERROR2
&CNAME   SETC  'IKJEBMA9'     DEFINE CSECT NAME FOR USER TABLE.
IKJEBMA9 CSECT
.CONT1   ANOP
&A       SETA  N'&SYSLIST
         AIF   (&A EQ 0).END
&B       SETA  1
&F       SETA  1                                                 A42956
.CONT2   ANOP
&C       SETA  N'&SYSLIST(&B)
         AIF   (&C LT 2 OR &C GT 3).ERROR1
&E       SETA  K'&SYSLIST(&B,&C)
&D       SETA  &E-1
.*  THE FOLLOWING FLAGED INSTRUCTIONS WERE ADDED TO PROVIDE      A42956
.*  UNIQUE LABELS, EVEN IF MODULES HAVE IDENTICAL LAST TWO       A42956
.*  CHARACTERS IN ENTRY POINT NAMES.  THE LABELS FOR MODULES     A42956
.*  IKJEBELI AND IKJEBEIP ARE UNCHANGED, SINCE THEY ARE          A42956
.*  REFERENCED WITHIN IKJEBEMA.                                  A42956
         AIF   ('&CSECT' NE 'IBM').CONT10                        A42956
         AIF   ('&SYSLIST(&B,&C)'(&D,&E) EQ 'LI' OR                    X
                '&SYSLIST(&B,&C)'(&D,&E) EQ 'IP' ).CONT11        A42956
.CONT10  ANOP                                                    A42956
&LABEL1  SETC  '&CNAME'(6,8).'@'.'&F'                            A42956
&F       SETA  &F+1                                              A42956
&LABEL2  SETC  '&CNAME'(6,8).'@'.'&F'                            A42956
&F       SETA  &F+1                                              A42956
         AGO   .CONT12                                           A42956
.CONT11  ANOP                                                    A42956
&LABEL1  SETC  '&CNAME'(6,8).'&SYSLIST(&B,&C)'(&D,&E).'001'
&LABEL2  SETC  '&CNAME'(6,8).'&SYSLIST(&B,&C)'(&D,&E).'002'
.CONT12  ANOP                                                    A42956
&SCNAME  SETC  '&SYSLIST(&B,1)'
         SPACE 2
         DC    AL1(&LABEL1-*-1) LENGTH OF SUBCOMMAND NAME.
         DC    C'&SCNAME' SUBCOMMAND NAME.
&LABEL1  EQU   *
         DC    AL1(&LABEL2-*-1) LENGTH OF ABBREVIATION.
         AIF   (K'&SYSLIST(&B,2) EQ 0).CONT5
&ABBR    SETC '&SYSLIST(&B,2)'
         DC    C'&ABBR' ABBREVIATION FOR SUBCOMMAND.
.CONT5   ANOP
&LABEL2  EQU   *
&LDMOD   SETC  '&SYSLIST(&B,&C)'
         DC    CL8'&LDMOD' LOAD MODULE NAME.
         AIF   (&B EQ &A).END
&B       SETA  &B+1
         AGO   .CONT2
.END     ANOP
         SPACE 2
         DC    AL1(255)       END OF TABLE MARKER.
         MEXIT
.ERROR1  MNOTE 12,'INVALID TABLE ENTRY'
         MEXIT
.ERROR2  MNOTE 12,'INVALID KEYWORD VALUE'
         MEND
         PRINT ON
         EJECT
*---------------------------------------------------------------------*
*        EDIT SUBCOMMANDS                                             *
*        THE LIST IS IN ALPHABETICAL ORDER WITH THE TRUE EDIT         *
*        SUBCOMMANDS FIRST AND THEN THE TSO COMMANDS WE WOULD         *
*        VERY LIKE TO DO UNDER EDIT NEXT.                             *
*---------------------------------------------------------------------*
         IKJEBEST (CHANGEX,CX,IKJE9CX),                                X
               (COUNT,,IKJE9CT),                                       X
               (COLS,,IKJE9CL),                                        X
               (DUP,,IKJE9DU),                                         X
               (EXCLUDE,,IKJE9SP),                                     X
               (FINDX,FX,IKJE9FX),                                     X
               (LISTX,LX,IKJE9LX),                                     X
               (LISTTABS,LTABS,IKJE9LT),                               X
               (NAME,,IKJE9NA),                                        X
               (OFFLINE,OFF,IKJE9OF),                                  X
               (PUT,P,IKJE9PT),                                        X
               (SAVESHR,SSHR,IKJE9SS),                                 X
               (SELECT,,IKJE9SP),                                      X
               (SETLINE,,IKJE9SE),                                     X
               (SPOT,,IKJE9SP),                                        X
               (ASK,,IKJEBEHE),                                        X
               (CANCEL,,IKJEBEHE),                                     X
               (CATMAN,CAT,IKJEBEHE),                                  X
               (DA,,IKJEBEHE),                                         X
               (DD,,IKJEBEHE),                                         X
               (DISPLAY,DS,IKJEBEHE),                                  X
               (DOPROG,,IKJEBEHE),                                     X
               (DSN,WHOHAS,IKJEBEHE),                                  X
               (DUSER,DU,IKJEBEHE),                                    X
               (FREE,,IKJEBEHE),                                       X
               (FREEALL,FA,IKJEBEHE),                                  X
               (INSTREAM,,IKJEBEHE),                                   X
               (JULIAN,JUL,IKJEBEHE),                                  X
               (LISTALC,LISTA,IKJEBEHE),                               X
               (LISTBC,LISTB,IKJEBEHE),                                X
               (LISTDS,LISTD,IKJEBEHE),                                X
               (LISTSPC,LISTS,IKJEBEHE),                               X
               (LM,SHOWDIR,IKJEBEHE),                                  X
               (MEMBER,MEM,IKJEBEHE),                                  X
               (OUTPUT,OUT,IKJEBEHE),                                  X
               (OPERATOR,OPER,IKJEBEHE),                               X
               (PRINTOFF,,IKJEBEHE),                                   X
               (PUNCH,,IKJEBEHE),                                      X
               (QLIST,QL,IKJEBEHE),                                    X
               (RLSE,,IKJEBEHE),                                       X
               (SHOWDS,SDS,IKJEBEHE),                                  X
               (SHOWSPC,,IKJEBEHE),                                    X
               (STACK,,IKJEBEHE),                                      X
               (STATUS,ST,IKJEBEHE),                                   X
               (TERMINAL,TERM,IKJEBEHE),                               X
               (WHATIF,,IKJEBEHE),                                     X
               (VTOC,,IKJEBEHE),                                       X
               CSECT=USER
         DC    CL16' &SYSDATE &SYSTIME '  DATE AND TIME OF ASSEMBLY
         END
/*
//*------------------------------------------------------------------*
//LKED.SYSLMOD DD DSN=USER.CMDLIB,DISP=SHR
//CMDLIB   DD DSN=SYS1.CMDLIB,DISP=SHR
//SYSIN    DD *
 INCLUDE CMDLIB(IKJEBEMA)
 ALIAS IKJEBEEN,IKJEBEXT,IKJEBEUT,IKJEBEAE
 ENTRY IKJEBEMA
 NAME  IKJEBEMA(R)
/*
