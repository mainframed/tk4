         TITLE 'TRANSMIT COMMAND TERMINATION EXIT'
*-------------------------------------------------------------------*
*        STANDARD TRANSMIT COMMAND TERMINATION EXIT
*
*        AT TRANSMIT COMMAND TERMINATION, CHECK TO SEE IF ANY OF
*        THE ADDRESSEES ARE TO THE LOCAL NODE; AND IF SO NOTIFY
*        THE USER THAT MAIL IS WAITING. NOTE THAT THE LOCAL NODE
*        NAME IS ASSEMBLED IN (AS MVS).
*
*        MUCH THANKS TO TUCC FOR THE EXIT. THE TUCC CODE WAS
*        TRUELY RE-ENTRANT BUT IF YOU TRIED TO ASSEMBLE IT WITH
*        A PARM=RENT ON THE ASSEMBLY, IT SAID IT WAS NOT. CLEANED
*        UP THE CODE SO YOU DID NOT HAVE TO FIB TO THE ASSEMBLER.
*
*                             JIM MARSHALL, CAPT, USAF
*
*  DATE INSTALLED: 12 JUNE 86
*
*  CHNGE LOG: NONE
*-------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
INMXZ02  CSECT
         SAVE  (14,12),,INMXZ02_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING INMXZ02,R12
         LR    R2,R1              PARM
         USING IPARM,R2
         L     R3,IPADDRL         ADDRESSEE LIST CHAIN
         USING ADPARM,R3          ADDRESSEE LIST PARM
         GETMAIN RC,LV=GETLEN     GET MSG WORK AREA
         LTR   R15,R15            GET IT OK?
         BNZ   RET                RETURN IF NOT
         LR    R11,R1             ADR MSG AREA
         USING WORKAREA,R11
         SPACE
*-------------------------------------------------------------------*
*        CHECK FOR LOCAL NODE. NOTE THAT THE SYMBOL "LOCALNOD"
*        CONTAINS THE NODE NAME, WHICH IS ASSEMBLED AS "MVS ".
*-------------------------------------------------------------------*
         SPACE
NXTADR   EQU   *
         CLC   LOCALNOD,ADNODE    TRANSMIT TO THE LOCAL NODE?
         BNE   STEPADR            NEXT ADR IF NOT
         SPACE
*-------------------------------------------------------------------*
*        NOW WE HAVE FOUND A FILE GOING TO WHERE WE WANT, WE NEED
*        TO BUILD THE MSG. IN ORDER TO MAKE IT MORE MEANING, WE
*        WILL PUT THE DATE AND TIME IN THE MSG
*-------------------------------------------------------------------*
         SPACE
         MVC   0(GETLEN,R11),MSG  MOVE EVERY THING TO GETMAIN AREA
         SPACE
         TIME  BIN                GET THE DATE AND TIME
         SPACE
         STCM  R1,15,DBLEWD       STORE DATE (1ST 1/2 OF DBLEWD
         UNPK  49(5,R11),DBLEWD+1(3)
         OI    53(R11),240   FIX THE ZONE
         MVC   48(2,R11),49(R11)  MOVE THE DAY BACK ONE
         MVI   50(R11),C'.'       MOVE IN PERIOD
         SPACE
*-------------------------------------------------------------------*
*        NOW WE PUT THE TIME INTO THE MSG AS HH.MM
*-------------------------------------------------------------------*
         SPACE
         SRDL  R0,32              SHIFT RIGHT 32 BITS
         D     R0,F100            CONVERT TO WHOLE SECS
         XR    R0,R0              CLEAR R0
         D     R0,F3600           GET HRS
         CVD   R1,DBLEWD          CONVERT TO PRINTABLE FORMAT
         UNPK  56(2,R11),DBLEWD+6(2)
         OI    57(R11),240        FIX THE ZONE
         SRDL  R0,32              SHIFT RIGHT 32 BITS
         D     R0,F60             CONVERT TO WHOLE MIN
         CVD   R1,DBLEWD          CONVERT
         MVI   58(R11),C'.'       MOVE IN PERIOD
         UNPK  59(2,R11),DBLEWD+6(2)
         OI    60(R11),240        FIX THE ZONE
         SPACE
*-------------------------------------------------------------------*
*        NOW WE MOVE IN THE USERID.
*-------------------------------------------------------------------*
         SPACE
         MVC   75(7,R11),ADUSER   MOVE IN USERS ID
         LA    R4,82(R11)         FIND END OF USERID
EULOP    EQU   *
         CLI   0(R4),C' '         END OF USERID?
         BE    EUGOT              BIF END
         LA    R4,1(R4)           ELSE STEP TO NEXT CHAR
         B     EULOP               AND TRY AGAIN
EUGOT    EQU   *
         MVI   0(R4),C')'         CLOSE IT OUT
         SPACE
*-------------------------------------------------------------------*
*        NOW WE NEED TO GET THE ADDRESS OF THE TIOT. AT THAT
*        ADDRESS IS THE JOBNAME.
*-------------------------------------------------------------------*
         SPACE
         L     R9,16              CVTPTR
         L     R9,0(,R9)
         L     R9,4(,R9)          CURRENT TCB
         L     R9,12(,R9)         TIOT
         MVC   37(7,R11),0(R9)    SENDERS JOBNAME IN TIOT
         MODESET KEY=ZERO
         SR    R0,R0              SETUP FOR SEND
         LR    R1,R11             POINT TO MSG
         SVC   34                 ISSUE THE SEND COMMAND
         MODESET KEY=NZERO
         SPACE
*-------------------------------------------------------------------*
*        NOW WE STEP THROUGH THE ADRESSEE.
*-------------------------------------------------------------------*
         SPACE
STEPADR  EQU   *
         ICM   3,15,ADNEXT        STEP TO NEXT ADRESSEE
         BNZ   NXTADR              AND PROCESS IT IF MORE
         LR    R1,R11
         FREEMAIN RU,LV=GETLEN,A=(1)
         SPACE
RET      EQU   *
         RETURN (14,12),RC=0
         SPACE
*-------------------------------------------------------------------*
*        CONSTANTS
*-------------------------------------------------------------------*
         SPACE
F60      DC    F'60'
F100     DC    F'100'
F3600    DC    F'3600'
         SPACE
*-------------------------------------------------------------------*
*        THE VALUE OF NODE BELOW MUST BE CHANGED TO YOUR NAME
*-------------------------------------------------------------------*
         SPACE
LOCALNOD DC    CL8'MVS     '      LOCAL NODE NAME
         SPACE
         LTORG
*-------------------------------------------------------------------*
*        BELOW ARE THE DISPLACEMENTS TO MAKE IT EASY FOR YOU TO
*        MOVE THE DATA INTO THE GETMAINED AREA
*-------------------------------------------------------------------*
*                      1         2         3         4         5
*               4567 89012345678901234567890123456789012345678901234567
*
         DS    0F
MSG      WTO   'SE ''FILE TRANSMITTED TO YOU FROM XXXXXX  ON YY.DDD, HHX
               .MM'',LOGON,USER=(           ',MF=L
*                 6          7         8
*              8901 2345678901234567890123456
MSGLEN   EQU   *-MSG
         SPACE
*-------------------------------------------------------------------*
*        DSECTS FOLLOW
*-------------------------------------------------------------------*
         SPACE
         DS    0F
IPARM    DSECT                    INPUT PARM DSECT
         DS    A                  USER WORD
         DS    A                  ADR PARM VAL
IPACTF   DS    A                  ACTION FLAGS
IPCPL    DS    A                  COMMAND PROCESSOR PARM LIST
IPMSGA   DS    A                  ADR MSG ISSUED BY TRANSMIT
         DS    A                  ADR STATUS BYTE
         DS    A                  ADR TXT UNITS
         DS    A                  SOURCE DATA SET TST UNITS
IPADDRL  DS    A                  ADDRESSEE ADDRESS LIST
IPRECNO  DS    A                  NUMBER OF RECORDS
IPRETC   DS    A                  TRANSMIT RETURN CODE
         SPACE
ADPARM   DSECT                    ADDRESS LIST PARM LIST
ADNEXT   DS    A                  ADR NEXT ENTRY
ADNODEL  DS    H                  NODE NAME LENGTH
ADNODE   DS    CL8                NODE NAME
ADUSERL  DS    H                  USERID LENGTH
ADUSER   DS    CL8                USERID
         DS    CL76
WORKAREA DSECT
MSGWORK  DS    CL100
DBLEWD   DS    D
GETLEN   EQU   *-WORKAREA
         END
