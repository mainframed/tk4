         TITLE 'DISPLAY LINKLIST LIBRARIES '
*---------------------------------------------------------------------*
*       THIS COMMAND WAS CODED FROM AN IDEA TAKEN FROM THE 'NASPA'    *
*       (NATIONAL SYSTEMS PROGRAMMER ASSOCIATION) "TECHNICAL          *
*       SUPPORT" MAGAZINE. PAUL SULLIVAN PUBLISHED THE BARE BONES     *
*       CODE TO DO ALL THIS. I ADDED THE ALL THE REST TO GET IT       *
*       REENTRANT, USE DSECTS, ADD SECURITY (DON'T WANT AVERAGE       *
*       USER LOOKING AT THIS LIST), AND ALL AROUND DOCUMENT THE       *
*       CODE.                                                         *
*                                                                     *
*       NOTE, USE OF THIS ROUTINE IS LIMITED TO THOSE USERS WITH      *
*       OPERATOR AUTHORITY                                            *
*                                                                     *
*       JIM MARSHALL, CAPT, USAF (RET)                                *
*                                                                     *
* INSTALLED:  13 JUN 88                                               *
*                                                                     *
* CHANGE LOG: 20FEB91 - LIBRARY COUNT BEGINS AT 1 VERSUS 0       JDM1 *
*             10JUN91 - NOW RUNNING 31 BIT MODE                  JDM2 *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SP000    EQU   0                                                 JDM1
         TITLE 'DLINK -- DISPLAY LINKLIST LIBRARIES'
DLINK    CSECT
DLINK    AMODE 31
DLINK    RMODE ANY                                               JDM1
         SAVE  (14,12),,DLINK_&SYSDATE &SYSTIME
         LR    R12,R15
         USING DLINK,R12
         GETMAIN RU,LV=WORKSIZE,SP=SP000,LOC=BELOW               JDM1
         USING WORKAREA,R1
         ST    R13,SAVE+4
         ST    R1,8(R13)
         LR    R13,R1
         DROP  R1
         USING WORKAREA,R13
         SPACE
*-------------------------------------------------------------------*
*        NOW WE MUST SETUP PGM FOR RENTRACY
*-------------------------------------------------------------------*
         SPACE
         MVC   NUM(2),=PL2'1'                                    JDM1
         MVC   WORK3(3),BLANKS
         MVC   MSG(L'MSG),BLANKS
         L     R2,CVTPTR          GET ADDR OF CVT
         USING CVTDSECT,R2        SETUP CVT BASE
         SPACE
*-------------------------------------------------------------------*
*        NOW WE DO SO CONTROL BLOCK CHAINING TO SEE IF THE USER
*        HAS TSO OPERATOR AUTHORITY.
*-------------------------------------------------------------------*
         SPACE
         L     R1,16              GET CVT POINTER
         L     R1,0(R1)           OLD/NEW POINTER
         L     R1,4(R1)           OUR TCB POINTER
         L     R15,12(R1)         OUR TIOT POINTER
         L     R1,180(R1)         JSCB POINTER
         L     R1,264(R1)         PSCB POINTER
         TM    16(R1),X'80'       DO WE HAVE OPER ABILITY?
         BO    CONTINUE           YES, CONTINUE
         SPACE
*-------------------------------------------------------------------*
*        IF NOT, TELL USER OF FACT AND RETURN EARLY, OTHERWISE
*        CONTINUE PROCESSING.
*-------------------------------------------------------------------*
         SPACE
NOTAUTH  EQU   *
         MVC   MSG(L'LLINE),LLINE                                JDM1
         TPUT  LLINE,L'LLINE      MSG- NOT AUTH USER             JDM1
         B     EXIT               EXIT
         SPACE
*--------------------------------------------------------------------*
*        NOW WE PROCESS THE LIST
*--------------------------------------------------------------------*
         SPACE
CONTINUE EQU   *
         MVC   MSG(L'HEADER),HEADER                              JDM1
         TPUT  MSG,L'HEADER       PUT OUT THE HEADER LINE        JDM1
         SPACE
         MVC   MSG(L'HEADER),BLANKS                              JDM1
         TPUT  MSG,L'HEADER       BLANK LINE FOR READABILITY  JDM1
         SPACE
         L     R3,X'4DC'(R2)      LINKLIST ADDR (BYPASS DSECT FOR NOW)
         A     R3,=F'9'           MOVE TO 1ST DSN
         SPACE
LOOP     EQU   *
         MVC   LDSN,0(R3)         MOVE DSN TO MSG
         UNPK  WORK3,NUM          UNPACK LIBRARY NAME
         OI    WORK3+2,X'F0'      CHANGE SIGN
         MVC   LNUM(2),WORK3+1    MOVE TO MSG
         AP    NUM,=P'1'          ADD 1 TO LIBRARY NUMBER
         TPUT  MSG,L'MSG
         LA    R3,45(R3)          BUMP TO NEXT ENTRY
         TM    0(R3),X'C0'        IS THIS ALPHA
         BO    LOOP
         SPACE
*--------------------------------------------------------------------*
*        NOW WE CLEANUP AFTER OURSELVES AND GO HOME.
*--------------------------------------------------------------------*
         SPACE
EXIT     EQU   *
         LR    R1,R13
         L     R13,SAVE+4
         FREEMAIN RU,LV=WORKSIZE,A=(R1),SP=SP000                 JDM1
         RETURN (14,12),RC=0
         SPACE
*--------------------------------------------------------------------*
*        DATA AREA FOLLOWS
*--------------------------------------------------------------------*
         SPACE
HEADER   DC    CL45'                 DISPLAY LINKLIST    '
BLANKS   DC    CL80' '
LLINE    DC    CL40'USER NOT AUTHORIZED'
         SPACE
         LTORG
         SPACE
         DS    0F
         PRINT ON
WORKAREA DSECT
SAVE     DS    18F
LSW      DS    C                   LINE PRINT SWITCH
*                                  ' ' IS NO FILLED
*                                  '1' IS LEFT FILLED
*                                  '2' IS BOTH FILLED
PLINE    DS    0CL40
NUM      DS    PL2
WORK3    DS    CL3
MSG      DS    0CL65
         DS    CL15
LNUM     DS    CL2
         DS    CL4
LDSN     DS    CL44
WORKSIZE EQU   *-WORKAREA                                        JDM1
WORKEND  EQU   *
         SPACE
CVTDSECT DSECT
         CVT   DSECT=YES,LIST=YES
         SPACE
         END
