         TITLE '   A U C M D  '
***********************************************************************
*                                                                     *
*        'AUCMD'                                                      *
*                                                                     *
***********************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
*        INSTALLATION. PRC, MCLEAN VA.
*        DATE WRITTEN. JANUARY 6 1978.
*        DATE UPDATED. SEPTEMBER 29 1981.
*        ATTRIBUTES. RE-ENTRANT, AUTHORIZED.
*        DESCRIPTION.
*         THIS TSO COMMAND INVOKES A SPECIFIED TSO COMMAND
*         AND RUNS IT AUTHORIZED.  FOR SYSTEMS SUPPORT USE ONLY.
*        LOG OF CHANGES.
*         04MAY81 - INSTALLED AT AFDSC/PENTAGON.
*         29SEP81 - CHECK FOR ASMI. ISSUE TESTAUTH.
*         15MAY85 - CHANGE THE CHECK FOR MY OWN PURPOSES. ALSO
*                   ADD MECHANISM TO USE YOUR OWN SVC TO GET
*                   INTO SUPERVISOR STATE. WE RUN WITH ISPF/PDF
*                   V1 AND NEED IT.   JIM MARSHALL (JDM)
*
*         THIS COMMAND SHOULD BE LINK-EDITED AUTHORIZED.
*         THE NAME OF THIS COMMAND MUST BE ADDED TO THE TABLE
*         OF AUTHORIZED COMMANDS IN CSECT IKJEFTE2 OF IKJEFT02.
*         BUT THE NAMES OF THE COMMANDS INVOKED BY THIS COMMAND
*         DO NOT HAVE TO BE IN THE TABLE, SO USE OF THIS COMMAND
*         ALLOWS THE TABLE TO BE SMALLER.
*
*         I HAVE 2 MACROS COMMENTED OUT DOWN WHERE WE LINK OFF TO
*         THE ACTUAL COMMAND. I RUN ISPF/PDF V1 (NOT AUTHORIZED)
*         AND SO DO NOT USE THE MECHANISM DESCRIBED ABOVE. THUS
*         WE NEED AN SVC TO GET OURSELVES IN AND OUT OF SUPERVISOR
*         STATE. IF YOU HAVE SUCH A MECHANISM, JUST REPLACE THE
*         COMMENTED MACROS WITH YOUR OWN. I DO NOT SUPPLY THE
*         MACRO BECAUSE I WOULD THEN BE ACCUSED TO 'CONTRIBUTING
*         TO THE DELINQUENCY OF A SYSTEMS PROGRAMMER'!!!!!
*                                    JIM MARSHALL
*
*         WE USE THIS COMMAND TO INVOKE COMMANDS THAT ARE USED ONLY
*         BY SYSTEMS PROGRAMMERS. AUTHORIZED COMMANDS THAT ARE USED
*         BY NON-SYSTEM-PROGRAMMERS ARE PUT IN IKJEFTE2. THAT IS
*         WHY THIS COMMAND CHECKS THE USERID, AND THAT IS WHY THIS
*         COMMAND DOES NOT HAVE ITS OWN TABLE OF COMMAND NAMES.
*
*         WE ALSO LINK-EDIT A NON AUTHORIZED COPY OF THIS COMMAND,
*         AND NAME THAT VERSION 'CMD'.  THEN WE ADD 'CMD' TO THE
*         SUBCOMMAND TABLES IN 'EDIT' AND 'TEST', SO ANY TSO COMMAND
*         CAN BE USED UNDER EDIT AND TEST BY USING 'CMD'. THIS IS NOT
*         NECESSARY IF 'PCF' IS INSTALLED.
*
         SPACE
         MACRO
&NAME    MOVE  &TO,&TOL,&FROM,&FROML
         LCLB  &B(8)               0=FALSE 1=TRUE
&B(1)    SETB  (T'&TO EQ 'O')
&B(2)    SETB  (T'&TOL EQ 'O')
&B(3)    SETB  (T'&FROM EQ 'O')
&B(4)    SETB  (T'&FROML EQ 'O')
         AIF   (&B(1) OR &B(2) OR &B(3)).REQOM
&B(5)    SETB  ('&TO'(1,1) EQ '(')
&B(6)    SETB  ('&TOL'(1,1) EQ '(')
&B(7)    SETB  ('&FROM'(1,1) EQ '(')
         AIF   (&B(4)).NOB8
&B(8)    SETB  ('&FROML'(1,1) EQ '(')
.NOB8   ANOP
         AIF   (NOT(&B(5) AND &B(6) AND &B(7))).NALLR
         AIF   (NOT &B(4) AND NOT &B(8)).NALLR
         LR    R0,&TO(1)           RECEIVING FIELD
         LR    R1,&TOL(1)          RECEIVING LENGTH
         LR    R14,&FROM(1)        SENDING FIELD
         AIF   (&B(4)).NOFROML
         LR    R15,&FROML(1)       SENDING LENGTH
         ICM   R15,B'1000',=X'40'  PAD IF SENDING IS SHORTER
         AGO   .MVCL
.NOFROML LR    R15,&TOL(1)         SENDING LENGTH
.MVCL    MVCL  R0,R14
         MEXIT
.REQOM   MNOTE 4,'REQUIRED OPERAND(S) OMITTED'
         MEXIT
.NALLR   MNOTE 4,'ALL OPERANDS MUST BE IN REGISTER FORMAT (PARENS)'
         MEND
         EJECT
AUCMD    START
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'AUCMD'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(0),AL3(@DATAL) SUBPOOL AND LENGTH
@PROLOG  STM   14,12,12(R13)
         LR    R12,R15
         LR    R2,R1
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         LR    R9,R1
         USING @DATA,R9
         SPACE 1
         LR    R0,R9               AREA TO BE CLEARED
         L     R1,@SIZE            LENGTH TO BE CLEARED
         SR    R14,R14             CLEAR R14 AND R15
         SR    R15,R15             CLEAR R14 AND R15
         MVCL  R0,R14              ZERO IT ALL
         SPACE 1
         LR    R1,R9               POINT TO NEW SAVEAREA
         ST    R13,4(,R1)          PUT OLD ADDRESS IN NEW SAVE AREA
         ST    R1,8(,R13)          PUT NEW ADDRESS IN OLD SAVE AREA
         LR    R13,R1              SWITCH R13 TO NEW SAVE AREA
         EJECT
************************************************************
*                                                          *
*         CHECK USER'S AUTHORITY TO USE THIS COMMAND.      *
*                                                          *
************************************************************
         SPACE
         L     R15,X'224'          PSAAOLD
         L     R14,60(,R15)        ASCBTSB
         LTR   R14,R14             TSO SESSION?
         BNZ   IMPLTSO             YES - BRANCH
         L     R14,X'AC'(,R15)     ASCBJBNI
         LTR   R14,R14             STARTED TASK?
         BZ    IMPLEXIT            YES - BRANCH
         B     IMPLEXEC            NO - JOBS NOT AUTHORIZED
IMPLTSO  L     R15,X'21C'          PSATOLD
         L     R15,X'B4'(,R15)     TCBJSCB
         L     R15,X'108'(,R15)    JSCBPSCB
         CLC   0(4,R15),=C'SS02'   SYSTEMS SUPPORT USERID
         BE    IMPLEXIT            YES, BRANCH
         SPACE
************************************************************
*                                                          *
*         USER NOT AUTHORIZED - CALL IMPLICIT EXEC         *
*                                                          *
************************************************************
         SPACE
IMPLEXEC L     R15,0(,R1)          CPPLCBUF
         XC    2(2,R15),2(R15)     SET CBUF OFFSET TO IMPLICIT EXEC
         L     R15,12(,R1)         CPPLECT
         CLI   20(R15),X'40'       ECTSCMD BLANK?
         BNE   *+10                YES - SUBCOMMAND NOT FOUND
         MVC   12(8,R15),=CL8'EXEC' NO - COMMAND NOT FOUND
         L     R0,@SIZE
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN R,A=(1),LV=(0)
         L     R1,24(,R13)         RESTORE CPPL POINTER
         LA    R15,20(,R13)        POINT TO 8 BYTE WORKAREA
         XC    0(8,R15),0(R15)     ZERO WORKAREA
         XCTL  (2,12),EP=EXEC,SF=(E,(15))
         SPACE
************************************************************
*                                                          *
*         GETMAIN A NEW COMMAND BUFFER                     *
*                                                          *
************************************************************
         SPACE
IMPLEXIT EQU   *
         TESTAUTH FCTN=1
         LTR   R15,R15             ARE WE RUNNING AUTHORIZED
         BNZ   NAUTH               NO - GO ISSUE ERROR MESSAGE
         USING CPPL,R2
         L     R3,CPPLECT
         USING ECT,R3
         TM    ECTSWS,ECTNOPD      ANY OPERANDS?
         BO    NOPERAND            NO - BRANCH
         DROP  R3
         SPACE
         L     R3,CPPLCBUF
         CLC   2(2,R3),=H'0'       IS THERE AN OFFSET?
         BE    NOPERAND            NO - BRANCH
         LH    R4,0(,R3)           GET CBUF LENGTH
         SH    R4,2(,R3)           SUBTRACT OFFSET
         CH    R4,=H'4'            DOES OFFSET = LENGTH?
         BNH   NOPERAND            YES - NO OPERANDS
         LR    R0,R4
         CH    R0,=H'256'          WILL 256 BYTES BE ADDRESSABLE
         BNL   *+8                 YES, BRANCH
         LH    R0,=H'256'          NO, MAKE AT LEAST 256 BYTES
         O     R0,SUBPOOL
         ST    R0,FREECBUF
         GETMAIN R,LV=(0)
         ST    R1,FREECBUF+4
         LR    R8,R1
         XC    0(4,R8),0(R8)
         STH   R4,0(,R8)
         SH    R4,=H'4'            SUBTRACT PREFIX LENGTH
         AH    R3,2(,R3)           POINT TO OPERAND - 4
         AH    R3,=H'4'            POINT TO OPERAND
         SPACE
         LA    R7,4(,R8)           POINT TO RECEIVING FIELD
         SPACE
*        LR    R1,R4
*        BCTR  R1,0
*        B     *+10
*        MVC   0(0,R7),0(R3)
*        EX    R1,*-6
         SPACE
         MOVE  (R7),(R4),(R3)
         SPACE
         MVC   NEWCPPL(16),0(R2)   COPY CPPL FOR NEW CBUF
         LA    R2,NEWCPPL
         ST    R8,CPPLCBUF
         SPACE
************************************************************
*                                                          *
*         CALL THE SCAN SERVICE ROUTINE                    *
*                                                          *
************************************************************
         SPACE
         LA    R1,MYCSPL
         USING CSPL,R1
         MVC   CSPLUPT(4),CPPLUPT
         MVC   CSPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,CSPLECB
         XC    MYECB,MYECB
         LA    R0,MYCSFLAG
         ST    R0,CSPLFLG
         LA    R0,MYCSOA
         ST    R0,CSPLOA
         XC    MYCSFLAG(4),MYCSFLAG
         ST    R8,CSPLCBUF
         XC    LINKAREA(8),LINKAREA
         SPACE
         L     R15,16              CVTPTR
         TM    X'1E0'(R15),X'80'   IF HI ORDER BIT ON
         BNO   SCANLINK            THEN DO LINK
         L     R15,X'1E0'(,R15)    GET ADDR OF IKJSCAN
         BALR  R14,R15
         B     SCANEXIT
SCANLINK EQU   *
         LINK  EP=IKJSCAN,SF=(E,LINKAREA)
SCANEXIT EQU   *
         SPACE
         LTR   R15,R15
         BNZ   SCANERR
         LA    R1,MYCSPL
         L     R15,CSPLOA
         DROP  R1                  CSPL
         USING CSOA,R15
         TM    CSOAFLG,CSOAVWP+CSOAVNP
         BZ    SCANSYNT
         SPACE
         L     R3,CPPLECT
         USING ECT,R3
         TM    CSOAFLG,CSOAVWP
         BO    VWP
         OI    ECTSWS,ECTNOPD
         B     SWSET
VWP      NI    ECTSWS,255-ECTNOPD
         DROP  R3                  ECT
SWSET    EQU   *
         SPACE
         TM    CSOAFLG,CSOAEXEC    IMPLICIT (PERCENT SIGN)
         BO    IMPLOPND            YES - BRANCH
         SPACE
************************************************************
*                                                          *
*         ISSUE BLDL FOR COMMAND NAME FOUND BY SCAN        *
*                                                          *
************************************************************
         SPACE
         L     R1,CSOACNM
         LH    R14,CSOALNM
         DROP  R15                 CSOA
         MVC   BLDL(64),BLDLMODL
         MVC   BLDLNAME(8),=CL8' '
         BCTR  R14,0
         B     *+10
         MVC   BLDLNAME(0),0(R1)
         EX    R14,*-6
         LA    R7,BLDLERR
         B     NOBLDL              ZAP THIS TO BC 0 IF BLDL DESIRED
         SPACE
         BLDL  0,BLDL
         SPACE
         LTR   R15,R15
         BNZR  R7
NOBLDL   EQU   *
         SPACE
************************************************************
*                                                          *
*         CALL THE COMMAND                                 *
*                                                          *
************************************************************
         SPACE
*        $#SVCKS                   ENTER SUPER STATE KEY=0
         LR    R1,R2
         XC    LINKAREA(8),LINKAREA
         LINK  EPLOC=BLDLNAME,SF=(E,LINKAREA)
         SPACE
*        $#SVCKP                   ENTER NON-SUPER STATE KEY=PROB
         B     EXIT
         SPACE
************************************************************
*                                                          *
*         ISSUE ERROR MESSAGES                             *
*                                                          *
************************************************************
         SPACE
NAUTH    LA    R1,MSGNAUTH
         LA    R0,L'MSGNAUTH
         BAL   R14,PUTLINE
         B     EXIT12
NOPERAND EQU   *
         LA    R1,MSGNOPER
         LA    R0,L'MSGNOPER
         BAL   R14,PUTLINE
         B     EXIT12
SCANERR  EQU   *
         LA    R1,MSGSCANE
         LA    R0,L'MSGSCANE
         BAL   R14,PUTLINE
         B     EXIT12
SCANSYNT EQU   *
         LA    R1,MSGSCANS
         LA    R0,L'MSGSCANS
         BAL   R14,PUTLINE
         B     EXIT12
BLDLERR  EQU   *
IMPLOPND LR    R4,R2               NEW CPPL POINTER
         L     R6,4(,R13)          ORIGINAL SAVEAREA
         L     R6,24(,R6)          RESTORE ORIGINAL CPPL POINTER
         L     R2,0(R6)            POINT TO ORIGINAL CBUF
         L     R4,0(R4)            POINT TO NEW CBUF
         LH    R3,0(,R2)           ORIGINAL LENGTH
         LH    R5,0(,R4)           NEW LENGTH
         MOVE  (R2),(R3),(R4),(R5) MOVE NEW TO OLD AND PAD
*                                  NOTE REGS NOT ALTERED BY MOVE
*                                  BECAUSE WORKREGS USED IN MVCL
         STH   R3,0(,R2)           RESTORE ORIGINAL LENGTH
         LR    R1,R6               SET UP CPPL PTR FOR IMPLEXEC
         B     IMPLEXEC
         LA    R1,MSGBLDLE
         LA    R0,L'MSGBLDLE
         BAL   R14,PUTLINE
         B     EXIT12
         SPACE
PUTLINE  ST    R14,PUTLINS
         SVC   93
         L     R14,PUTLINS
         BR    R14
         SPACE
EXIT12   LA    R15,12
         SPACE 1
EXIT     LM    R0,R1,FREECBUF
         LTR   R0,R0
         BZ    EXITX
         LR    R2,R15
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2
EXITX    LR    R1,R13
         L     R0,@SIZE
         L     R13,4(,R13)
         LR    R2,R15
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2
         LM    0,12,20(13)
         L     R14,12(,R13)
         BR    R14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
MSGNOPER DC    C'REQUIRED OPERANDS MISSING'
MSGSCANE DC    C'SCAN ERROR'
MSGSCANS DC    C'INVALID COMMAND SYNTAX'
MSGBLDLE DC    C'COMMAND NOT FOUND'
MSGNAUTH DC    C'ENVIRONMENT IS NOT APF AUTHORIZED'
         SPACE
BLDLMODL DC    H'1,74',CL8' ',XL4'00',CL62' '
         SPACE
SUBPOOL  DC    A(X'01000000')
         LTORG
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
MYCSPL   DS    6F
MYCSFLAG DS    F
MYECB    DS    F
MYCSOA   DS    D
NEWCPPL  DS    4F
FREECBUF DS    2F
LINKAREA DS    2F
PUTLINS  DS    F
         DS    0D
BLDL     DS    F
BLDLNAME DS    CL8
         DS    CL62
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
         IKJCPPL
         SPACE
         IKJECT
         SPACE
         IKJCSPL
         SPACE
         IKJCSOA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
