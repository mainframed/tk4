         TITLE ' I K J E 9 L T  -  LISTTABS EDIT SUBCOMMAND'
*---------------------------------------------------------------------*
*                                                                     *
*        EDIT SUBCOMMAND TO LIST YOUR TABS IN EDIT. RENAMED FROM      *
*        'HEWELTAB' TO BE INCORPORATED IN OUR MANY DEVELOPED TSO      *
*        EDIT SUBCOMMANDS TO 'IKJE9LT'.                               *
*                                                                     *
*        THIS CODE WAS ORGINIALLY CAME FROM THE GODDARD SPACE         *
*        CENTER, GREENBELT, MD. THEN OFF TO DEPT OF H.E.W. WASH       *
*        D.C. WHERE BOB HANSEN ( NOW SECURITIES & EXCHANGE COMM )     *
*        WASHINGTON D.C. CONVERTED TO RUN IN MVS.                     *
*                                                                     *
*        NAME - IKJE9LT, ALIASES - LISTTABS,LTABS                     *
*                                                                     *
*        JIM MARSHALL                                                 *
*                                                                     *
*  CHANGE LOG:                                                        *
*        JULY 1978 - INSTALLED MVS.                                   *
*        JULY 1981 - INSTALLED AS EDIT SUBCOMMAND AND NAME 'IKJE9LT'  *
*                    WITH PARTIAL CODE CLEAN UP.                      *
*                                                                     *
*                                                                     *
*  CAUTION: FOR THE TIME BEING DO NOT INSTALL AS RENT ( 0C4 ), WILL   *
*           ATTEMPT TO CLEAN UP THE CODE AND MAKE IT TRUELY RENT.     *
*                                                                     *
*---------------------------------------------------------------------*
IKJE9LT  CSECT
         JVDENTER BASE=(12),DSA=(DSAEND-DSAWORK),VERSION=01
         LR    R9,R1         SAVE ADDRESS OF EDIT COMM. AREA
         USING DSAWORK,R13
         USING IKJEBECA,R9
         LA    R2,BUFF
         MVC   BUFF(50),OFF              SET UP MSG-LINE
         MVC   MSG1H,MSG                 PUT IN THE HEADING
         XC    ECB,ECB                   CLEAR THE ECB
         LA    R8,10
         CLI   CATABS,X'00'
         BE    PUTLNE
         MVC   BUFF+7(10),MSG1
         LA    R8,BUFF+9
         LA    R6,CATABS
         SR    R5,R5
         LA    R4,10
LOOP     LA    R6,1(R6)
         CLI   0(R6),X'00'
         BE    PUTMSG
         IC    R5,0(R6)
         CVD   R5,DWD
         MVC   DWD(4),=X'40202020'
         ED    DWD(4),DWD+6
         LA    R8,1(R8)
         LA    R10,DWD
         LA    R7,3
NEXT     LA    R10,1(R10)
         BCTR  R7,0
         CLI   0(R10),C' '
         BE    NEXT
         EX    R7,MOVEN
         AR    R8,R7
         MVI   1(R8),C' '
         LA    R8,1(R8)
         BCT   R4,LOOP
*
PUTMSG   MVI   0(R8),C')'
         LA    R8,1(R8)
         SR    R8,R2
         LA    R8,4(0,R8)             UP BY 4 FOR PUTLINE
         STH   R8,MSG1H               PUT IN THE MSG LENGTH
*
PUTLNE   L     R2,CAPTECT             GET THE ECT ADDR
         L     R3,CAPTUPT             GET THE UPT ADDR
         PUTLINE PARM=PUTLINE,ECT=(R2),UPT=(R3),ECB=ECB,               X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               OUTPUT=(MSG1H,TERM,SINGLE,DATA),MF=(E,IOPL)
         JVDLEAVE  RC=0
         EJECT
*
*              DECLARE CONSTANTS
*
MSG      DC    Y(50+4,0)                HEADER
MSG1     DC    CL10'ON('
OFF      DC    CL50'TABSET OFF'
MOVEN    MVC   0(0,R8),0(R10)
         SPACE 1
         LTORG
         SPACE 2
DSAWORK  DSECT
SSA      DS   18F
         DS    0D
DWD      DC    2F'0'
ECB      DS    F
MSG1H    DC    X'000E0000'
BUFF     DC    CL50'TABSET OFF'
PUTLINE  PUTLINE TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                   X
               OUTPUT=(*-*,TERM,SINGLE,DATA),MF=L
IOPL     CALL    ,(CAPTUPT,CAPTECT,ECB,PUTLINE),MF=L
DSAEND   EQU  *
         SPACE 5
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         IKJEBECA
         END
