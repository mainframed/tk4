         TITLE 'D U D A S D  --  OPERATOR LIKE  D U,DASD  '
*--------------------------------------------------------------------*
*                                                                    *
*        THIS IS A COMPLETE REWRITE OF THE 'DUDASD' COMMAND          *
*        THAT HAS BEEN AROUND FOR YEARS. I NEEDED TO INCLUDE         *
*        THE UP TO DATE UCB SEARCH ROUTINE ANYWAY, SO I DECIDED      *
*        TO REDO THE CODE IN MY STYLE.                               *
*                                                                    *
*        JIM MARSHALL, CAPT, USAF (RET)                              *
*        GRUMMAN DATA SYSTEMS                                        *
* CHNGE LOG:                                                         *
*        03NOV86-REWROTE CMD, CHANGES ARE TOO MANY TO MARK  JDM      *
*        JDM1-01JUL88-ADDED CODE TO CHECK TO SEE IF THE VTOC IS STILL*
*             INDEXED. MVS LOVES TO UN-INDEX IT AND NEVER TELLS YOU  *
*             BLANTLY; JUST KEEPS ON RUNNING                         *
*        JDM2-06DEC90 - ADDED 3340 DEV CODE, IS DEFINED HERE AS VIO  *
*        JDM3-07DEC90 - ADDED DEBUG CODE IF YOU FIND ( ???? ) IN     *
*             YOUR DEVTYP FIELD, IT MEANS YOU NEED A CODE IN THE     *
*             TABLE. BUT IS TOUGH TO KNOW WHAT THE CODE WAS. THUS    *
*             LOOK IN R7 IN THE ABEND AND YOU WILL SEE THE CODE.     *
*             IT WILL BE THE LAST UNKNOWN TYPE IN THE DISPLAY !!!!   *
*        JDM4-04MAR91 - FIXED BUG SHOWING IF VTOC INDEXED OR NOT     *
*        JDM5-10JUN91 - NOW RUNNING IN 31 BIT MODE               JDM5*
*        JDM6-16JUL91 - ADDED 3390 UCBS                          JDM6*
*--------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2                   ADDRESSABILITY TO IOPL
R3       EQU   3                   WORK AND BAL REG
R4       EQU   4                   BCT LOOP REG
R5       EQU   5                   ADDRESSABILITY TO OUTPUT LINE
R6       EQU   6                   UNUSED
R7       EQU   7                   DEBUG REGISTER ALWAYS USED
R8       EQU   8                   UNUSED
R9       EQU   9                   ADDRESSABILITY TO CPPL
R10      EQU   10                  ADDRESSABILITY TO UCB
R11      EQU   11                  ADDRESSABILITY TO CVT
R12      EQU   12                  BASE REGS
R13      EQU   13                  ADDRESSABILITY TO GETMAIN
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  ENTRY ADDRESS AND WORK
SP000    EQU   0                                                 JDM5
         SPACE
DUDASD   CSECT
         GBLC  &SYSSPLV            GLOBAL FOR SPLEVEL            JDM5
         SPLEVEL TEST              GET CURRENT SP LEVEL          JDM5
         AIF   ('&SYSSPLV' EQ '1').MVSSP  SKIP IF MVS/SP         JDM5
DUDASD   AMODE 31                                                JDM5
DUDASD   RMODE ANY                                               JDM5
.MVSSP   ANOP                                                    JDM5
         SAVE  (14,12),,DUDASD_&SYSDATE &SYSTIME
         LR    R12,R15             SAVE THE ENTRY ADDRESS
         USING DUDASD,R12          EST ADDRESSABILITY
         LR    R9,R1               SAVE ADDR OF CPPL
         USING CPPL,R9             ADDRESSABILITY TO CPPL
         GETMAIN RU,LV=WORKSIZE,SP=SP000,LOC=BELOW               JDM5
         ST    R13,4(R1)           SETUP
         ST    R1,8(R13)             SAVE
         LR    R13,R1                  AREA
         USING DSAWRK,R13          GET ADDRESSABILITY
         USING UCBOB,R10           ADDRESSABILITY TO UCB
         SPACE
         LA    R2,IOPLIST          GET ADDRESS OF IOPLIST
         USING IOPL,R2             ADDRESSABILITY TO IOPL
         MVC   IOPLECT,CPPLECT
         MVC   IOPLUPT,CPPLUPT
         XC    ECB,ECB
         XC    PREFIX(4),PREFIX
         LA    R15,L'BUFFER+4
         STH   R15,PREFIX
         SR    R7,R7               ZERO OUR DEBUG REGISTER       JDM3
         L     R11,CVTPTR          GET ADDRESS OF CVT
         USING CVTMAP,R11          ADDRESSABILITY TO DSECT
         SPACE
*--------------------------------------------------------------------*
*        PUT OUT OUR HEADER FOR THE DISPLAY                          *
*--------------------------------------------------------------------*
         SPACE
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         MVC   BUFFER(60),HEADER
         BAL   R3,DISPLAY
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         BAL   R3,DISPLAY
         MVC   BUFFER(HDR1L),HDR1                                JDM1
         BAL   R3,DISPLAY                                        JDM1
         MVI   BUFFER,C' '                                       JDM1
         MVC   BUFFER+1(L'BUFFER-1),BUFFER                       JDM1
         BAL   R3,DISPLAY                                        JDM1
         SPACE
*--------------------------------------------------------------------*
*        SETUP FOR THE UCB SEARCH ROUTINE AMONG OTHER THINGS.        *
*--------------------------------------------------------------------*
         SPACE
         XC    WORKAREA,WORKAREA   CLEAR WORK AREA
         LA    R1,WORKAREA         GET ADDR OF WORK AREA
         ST    R1,PARMWA           STORE ADDR IN PARMLIST
         LA    R1,DEVCLASS         GET ADDR OF DEV CLASS
         ST    R1,PARMDEVT         STORE ADDR IN PARMLIST
         MVI   DEVCLASS,UCB3DACC   INDICATE ONLY DASD UCBS.
         LA    R1,ADDRUCB          ADDR OF WHERE TO ST UCB ADDR
         ST    R1,PARMUCB          STORE ADDR IN PARMLIST
         OI    PARMUCB,X'80'       INDICATE END OF PARMLIST
         SPACE
*--------------------------------------------------------------------*
*        NOW WE FIGURE HOW MANY UCBS TO PROCESS BEFORE WE WILL       *
*        PRINT A LINE. IN THIS CASE IT COMES OUT TO 4.               *
*--------------------------------------------------------------------*
         SPACE
DULINE   EQU   *
         SPACE
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         LA    R5,BUFFER
         LA    R4,L'BUFFER/L'ELEMENT
         USING ELEMENT,R5
         SPACE
*--------------------------------------------------------------------*
*        THIS IS WHERE WE WILL DO THE UCB SEARCH.                    *
*--------------------------------------------------------------------*
         SPACE
DUUNIT   EQU   *
         LA    R1,PARMLIST         ADDR OF PARMLIST IN R1
         L     R15,CVTUCBSC        GET SCAN SERVICE ADDRESS
         SPACE
*--------------------------------------------------------------------*
*        GOING OFF TO SCAN SERVICES, WE ARE SETUP AS FOLLOWS:        *
*                                                                    *
*            R1  - ADDR OF PARAMETER LIST                            *
*            R13 - ADDR OF 18F SAVE AREA                             *
*            R14 - RETURN ADDRESS                                    *
*            R15 - SCAN SERVICE ENTRY POINT                          *
*                                                                    *
*--------------------------------------------------------------------*
         SPACE
         BALR  R14,R15             GOTO SCAN SERVICE
         LTR   R15,R15             HAS A UCB BEEN FOUND?
         BNZ   ALLDONE             WE ARE FINISHED ALMOST
         L     R10,ADDRUCB         GET ADDR RETURNED
         CLC   LASTCUU,UCBNAME
         BE    DUUNIT
         MVC   ELEUNIT,UCBNAME
         MVC   LASTCUU,UCBNAME
         MVC   ELEVSN,SRTEVOLI
         TM    SRTESTAT,UCBONLI
         BO    ONLINE
         MVC   ELEVSN,=CL6'(OFF)'
         MVC   ELEVSTAT(3),=CL3'UNK'   UNKNOWN INDEXED STATUS    JDM1
         B     GETDEVT
*----------------------------------------------------------------JDM1*
*        SINCE THE VOLUME IS ON LINE, WE CAN SEE IF THE VTOC IS  JDM1*
*        INDEXED OR SOMEHOW HAS BECOME UNINDEXED.                JDM1*
*----------------------------------------------------------------JDM1*
         SPACE
ONLINE   EQU   *
         ICM   R6,15,ADDRUCB           LOAD UP UCB ADDR          JDM1
         CVAFTST UCB=(R6)              GO TEST                   JDM1
         LTR   R15,R15                 TEST THE RESULTS          JDM1
         BZ    NOSUP                   SAY WE DO NOT SUPPORT     JDM1
         C     R15,=F'4'               IS IT NON-INDEXED ?       JDM1
         BE    NONINDX                 SAY SO                    JDM1
         C     R15,=F'8'               ARE WE INDEXED ?          JDM1
         BE    INDEXED                 SAY SO                    JDM1
         MVC   ELEVSTAT,=C'ERR'        WE ARE CONFUSED           JDM1
         B     ONLINEC                 CONTINUE                  JDM1
NOSUP    EQU   *                                                 JDM1
         MVC   ELEVSTAT,=C'NSU'        DO NOT SUPPORT IT         JDM1
         B     ONLINEC                 CONTINUE                  JDM4
NONINDX  EQU   *                                                 JDM1
         MVC   ELEVSTAT,=C'NIX'        IS NON-INDEXED            JDM1
         B     ONLINEC                 CONTINUE                  JDM4
INDEXED  EQU   *                                                 JDM1
         MVC   ELEVSTAT,=C'INX'        IS NON-INDEXED            JDM1
ONLINEC  EQU   *                                                 JDM1
         MVI   ELEDASH,C'-'
         MVI   ELESTAT,C'O'
         TM    SRTESTAT,UCBALOC
         BNO   NALLOC
         MVI   ELESTAT,C'A'
NALLOC   EQU   *
         TM    SRTESTAT,UCBCHGS
         BNO   RESERV
         MVI   ELESTAT,C'P'
RESERV   EQU   *
         CLI   UCBSQC,0
         BE    RESPACK
         MVI   ELERSRV,C'R'
RESPACK  EQU   *
         TM    SRTESTAT,UCBSYSR    IS IT RES PACK
         BNO   MOUNT               NO - CHECK RESIDENT
         MVC   ELEMSTAT(3),=C'IPL' SET C'IPL' IN LINE
         B     GETSTATE            CONTINUE TO NEXT UCB
MOUNT    EQU   *
         TM    SRTESTAT,UCBRESV    IS IT RESERVED(MOUNT)
         BNO   RESIDENT            NO - CHECK RESIDENT
         MVC   ELEMSTAT(3),=C'RSV' SET C'RSV' IN LINE
         B     GETSTATE            CONTINUE TO NEXT UCB
RESIDENT EQU   *
         TM    SRTESTAT,UCBPRES    IS IT RESIDENT
         BNO   GETSTATE            NO - CONTINUE TO NEXT UCB
         MVC   ELEMSTAT(3),=C'RSD' SET C'RSD' IN LINE
         SPACE
*--------------------------------------------------------------------*
*        NOW WE WILL GET THE VOLUMES STATE. LIKE PRIVATE, PUBLIC,    *
*        OR STORAGE.                                                 *
*--------------------------------------------------------------------*
         SPACE
GETSTATE EQU   *
         MVI   ELESLASH,C'/'       MOVE IN DIVIDER SLASH
         MVC   ELESTATE(3),=C'???' SETUP THE FIELD
         TM    UCBSTAB,UCBBPRV     IS IT PRIVATE
         BO    SETPRIV             GO MOVE IN WORDS
         TM    UCBSTAB,UCBBPUB     IS IT PUBLIC
         BO    SETPUB              GO MOVE IN WORDS
         TM    UCBSTAB,UCBBSTR     IS IT STORAGE
         BO    SETSTOR             GO MOVE IN WORDS
         B     GETDEVT             SHOULD NEVER DO THIS BUT !!!!
         SPACE
SETPRIV  EQU   *
         MVC   ELESTATE(3),=C'PRI' MEANS PRIVATE
         B     GETDEVT
         SPACE
SETPUB   EQU   *
         MVC   ELESTATE(3),=C'PUB' MEANS PUBLIC
         B     GETDEVT
         SPACE
SETSTOR  EQU   *
         MVC   ELESTATE(3),=C'STO' MEANS STORAGE
         SPACE
*--------------------------------------------------------------------*
*        NOW WE WILL GET THE DEVICE TYPE CODE OF THE UCB. THOSE      *
*        LUCKY TYPES WHO HAVE ONLY 3380'S REALLY DO NOT NEED THIS    *
*        BUT THOSE WHO HAVE MIXTURES, IT HELPS.                      *
*--------------------------------------------------------------------*
         SPACE
GETDEVT  EQU   *
         MVC   ELEDEVT(8),=C'( ???? )'  MOVE IN SKELETON
         LA    R3,DEVTABLE         POINT TO OUR TABLE
DEVLOOP  EQU   *
         CLI   0(R3),X'FF'         ARE WE AT END OF TABLE?
         BE    ULOOP               IF SO, WE WILL KNOW
         CLC   0(4,R3),UCBTYP      DO WE MATCH?
         BE    GOTDEVT
         A     R3,=F'10'           MOVE ALONG IN TABLE
         B     DEVLOOP             LOOP BACK
         SPACE
GOTDEVT  EQU   *
         MVC   ELEDEVT+1(6),4(R3)  MOVE IN ITS NAME
         SPACE
ULOOP    EQU   *
         CLC   ELEDEVT(8),=C'( ???? )'                           JDM3
         BNE   OVER                                              JDM3
         L     R7,UCBTYP                                         JDM3
OVER     EQU   *                                                 JDM3
         LA    R5,ELENEXT
         BCT   R4,DUUNIT
         SPACE
         BAL   R3,DISPLAY          GO PUT THE LINE
         B     DULINE
ALLDONE  EQU   *
         BAL   R3,DISPLAY          GO PUT THE LINE
         B     DONE
         SPACE
*--------------------------------------------------------------------*
*        MAKE THE DISPLAY LINE A SUBROUTINE                          *
*--------------------------------------------------------------------*
         SPACE
DISPLAY  EQU   *
         PUTLINE                                                       +
               PARM=PUTLINE,MF=(E,IOPLIST),ECB=ECB,                    +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     +
               OUTPUT=(PREFIX,TERM,SINGLE,DATA)
         BR    R3
         SPACE
DONE     EQU   *
         LTR   R7,R7               HAVE ANY UNKNOWN DEVTYP       JDM3
         BZ    DONE1                                             JDM3
         MVI   BUFFER,C' '                                       JDM3
         MVC   BUFFER+1(L'BUFFER-1),BUFFER                       JDM3
         BAL   R3,DISPLAY                                        JDM3
         MVC   BUFFER+1(L'BUFFER-1),BUFFER                       JDM3
         MVC   BUFFER(ERR1L),ERR1                                JDM3
         BAL   R3,DISPLAY                                        JDM3
         MVC   BUFFER+1(L'BUFFER-1),BUFFER                       JDM3
         BAL   R3,DISPLAY                                        JDM3
DONE1    EQU   *                                                 JDM3
         LR    R1,R13              GET ADDR FOR FREEMAIN
         L     R13,4(R13)          GET HIGHSAVE
         FREEMAIN RU,LV=WORKSIZE,A=(R1),SP=SP000                 JDM5
         LTR   R7,R7               HAVE ANY UNKNOWN DEVTYP       JDM3
         BZ    DONE2                                             JDM3
         ABEND 69,DUMP             LOOK IN R7 FOR DEVTYP         JDM3
DONE2    EQU   *                                                 JDM3
         RETURN (14,12),RC=0       SO LONG FOR NOW
         SPACE
*--------------------------------------------------------------------*
*        CONSTANT AREA                                               *
*--------------------------------------------------------------------*
         SPACE
DEVTABLE EQU   *
         DC    X'3010200F',C' 3390 '   UNSHARED 3390             JDM6
         DC    X'3030200F',C'S3390 '   3390 SHARED DASD          JDM6
         DC    X'3010200E',C' 3380 '   UNSHARED 3380             JDM3
         DC    X'3030200E',C'S3380 '   3380 SHARED DASD          JDM3
         DC    X'3050200B',C' 3350 '
         DC    X'3058200A',C' 3340 '   MAYBE DEFINED AS VIO      JDM2
         DC    X'3050200A',C' 3340 '   MAYBE DEFINED AS VIO      JDM2
         DC    X'3050200D',C'3330-1'   200 MEG DISK
         DC    X'30502009',C' 3330 '   100 MEG DISK
         DC    X'30502007',C'2305-2'
         DC    X'30502006',C'2305-1'
         DC    X'30582009',C' 3330V'   3850 MSS
         DC    X'30C08001',C' 2314 '   COULD THERE BE ???
         DC    X'30002001',C' 2311 '   RETROFIT ???
         DC    X'30002005',C' 2321 '   ULTIMATE RETROFIT !!
         DC    X'30002003',C' 2303 '   LET'S GO ALL THE WAY
         DC    X'30002004',C' 2302 '   WHY STOP NOW
         DC    X'30402002',C' 2301 '   GOVERNMENT PROBABLY HAS ONE
         DC    X'FFFFFFFF'             END OF TABLE
         SPACE
HEADER   DS    0CL60
         DC    CL10' '
         DC    CL15'-------------  '
         DC    CL20'        D U,DASD'
         DC    CL15'  -------------'
         SPACE
         DS    0F
         LTORG
         SPACE
HDR1     DC    CL3'UCB'
         DC    CL1' '
         DC    CL6' SER '
         DC    CL1' '
         DC    CL2' '
         DC    CL3' '
         DC    CL1' '
         DC    CL3'STA'
         DC    CL1' '
         DC    CL8'DEVTYPE '
         DC    CL1' '
         DC    CL4'VTOC'
         DC    CL2' '
         DC    CL3'UCB'
         DC    CL1' '
         DC    CL6' SER '
         DC    CL1' '
         DC    CL2' '
         DC    CL3' '
         DC    CL1' '
         DC    CL3'STA'
         DC    CL1' '
         DC    CL8'DEVTYPE '
         DC    CL1' '
         DC    CL4'VTOC'
         DC    CL2' '
HDR1L    EQU   *-HDR1
ERR1     DC    C'SURPRISE - UNK DEVTYP IN DISPLAY: R7 HAS  '     JDM3
         DC    C'LAST INV DEVTYP'                                JDM3
ERR1L    EQU   *-ERR1                                            JDM3
         SPACE
ELEWRK   DSECT
ELEMENT  DS    0CL38
ELEUNIT  DS    CL3                 UCB ADDR IN EBCDIC
         DS    CL1                 SPACE
ELEVSN   DS    CL6                 VOL SER
ELEDASH  DS    CL1                 DASH
ELESTAT  DS    CL1                 ONE BYTE STATUS
ELERSRV  DS    CL1                 ONE BYTE RESERVE STAT
ELEMSTAT DS    CL3                 RESERVED, RESIDENT, OR IPL
ELESLASH DS    CL1                 SLASH
ELESTATE DS    CL3                 PRI, PUB, STO  VOLUME STATE
         DS    CL1                 FILLER
ELEDEVT  DS    CL8                 ( DEVTYPE )
         DS    CL1                 FILLER                        JDM1
ELEVSTAT DS    CL3                 STATE OF THE VTOC             JDM1
         DS    CL3                 FILLER                        JDM1
ELENEXT  EQU   *
         SPACE
DSAWRK   DSECT
SAVE     DS    18F
IOPLIST  DS    07F
ECB      DS    01F
PREFIX   DS    2H
BUFFER   DS    CL(L'ELEMENT*2)
LASTCUU  DS    CL3
PUTLINE  PUTLINE MF=L
PARMLIST DS    3F                  PARMLIST MAPPING
         ORG   PARMLIST
PARMWA   DS    F                   ADDR OF 100 BYTE AREA
PARMDEVT DS    F                   ADDR OF BYTE CONTAINING
*                                  DEVTYPE TO BE SEARCHED
PARMUCB  DS    F                   ADDR OF WORD TO CONTAIN UCB ADDR
         SPACE
DEVCLASS DS    CL1                 DEV CLASS TO SEARCH
ADDRUCB  DS    F                   ADDR OF UCBS
         DS    0D                  FORCE DOUBLE WORD !!
WORKAREA DS    CL100               'UCB' WORKAREA
DSAEND   DS    0D
WORKSIZE EQU   *-DSAWRK                                          JDM1
         EJECT
         IKJIOPL
         EJECT
         IKJCPPL
         EJECT
UCBWRK   DSECT
         IEFUCBOB
         EJECT
         CVT   DSECT=YES
         END
