CCAT TITLE 'NASA/GSFC -- CONCAT (TSO) COMMAND PROCESSOR'
*---------------------------------------------------------------------*
** GSFTSOCC,  VERSION 1.0,  GENE CZARCINSKI,  09JUN73                **
** CONCAT, VERSION 1.1, GENE CZARCINSKI, 10APR77 ...                 **
**      . RENAME CSECT FROM 'GSFTSOCC' TO 'CONCAT'                   **
**        (MORE CONSISTANT AND CLEANER CMDLIB)                       **
**                                                                   **
** ATTR - RENT                                                       **
**                                                                   **
** THIS ROUTINE IS A TSO COMMAND PROCESSOR WHICH CONCATENATES        **
** SPECIFIED FILES.  'IKJDAIR' IS INVOKED TO PERFORM THE ACTUAL      **
** CONCATENATION.                                                    **
**                                                                   **
** CHNGE LOG: 12JUN91-DECIDED TO CLEAN UP CODE AND GET UNIQUE MACROS **
**            OUT OF THE CODE IN PREP FOR ESA.  JIM MARSHALL     JDM1**
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0                                                 JDM1
R1       EQU   1                                                 JDM1
R2       EQU   2                                                 JDM1
R3       EQU   3                                                 JDM1
R4       EQU   4                                                 JDM1
R5       EQU   5                                                 JDM1
R6       EQU   6                                                 JDM1
R7       EQU   7                                                 JDM1
R8       EQU   8                                                 JDM1
R9       EQU   9                                                 JDM1
R10      EQU   10                                                JDM1
R11      EQU   11                                                JDM1
R12      EQU   12                                                JDM1
R13      EQU   13                                                JDM1
R14      EQU   14                                                JDM1
R15      EQU   15                                                JDM1
SP000    EQU   0                                                 JDM1
         SPACE 1                                                 JDM1
CONCAT   AMODE 31                                                JDM1
CONCAT   RMODE ANY                                               JDM1
CONCAT   CSECT
         SAVE  (14,12),,CONCAT_&SYSDATE                          JDM1
         LR    R12,R15             GET BASE ADDR                 JDM1
         USING CONCAT,R12                                        JDM1
         LR    R9,R1               SAVE R1                       JDM1
         USING CPPL,R9             GET ADDRESSABILITY            JDM1
         GETMAIN RU,LV=@SIZE,SP=SP000,LOC=ANY                    JDM1
         ST    R13,4(,R1)                                        JDM1
         ST    R1,8(,R13)                                        JDM1
         LR    R13,R1                                            JDM1
         LR    R11,R1                                            JDM1
         USING @STORAGE,R11                                      JDM1
         SPACE 1                                                 JDM1
         XC        RCODE,RCODE
         XC        ECB,ECB
         LA        R1,PL1
         USING     IOPL,R1             INIT FOR TERM I/O
         MVC       IOPLUPT,CPPLUPT
         MVC       IOPLECT,CPPLECT
         LA        R0,ECB
         ST        R0,IOPLECB
         DROP      R1
         SPACE
** PARSE THE COMMAND OPERAND
         LA        R1,PL2
         USING     PPL,R1
         MVC       PPLUPT,CPPLUPT
         MVC       PPLECT,CPPLECT
         LA        R0,ECB
         ST        R0,PPLECB
         MVC       PPLPCL,=A($PARSECC)
         LA        R0,APDL
         ST        R0,PPLANS
         MVC       PPLCBUF,CPPLCBUF
         XC        PPLUWA,PPLUWA
         DROP      R1
         LINK      EP=IKJPARS
         LTR       R15,R15             CHECK THE RC
         BZ        PARSE1              OK.
** PARSE ERROR
         LA        R2,MSG0
         B         TPUT
**
PARSEMVC MVC       0(0,R4),0(R5)
**
** PARSE SUCCESSFUL
PARSE1   L         R2,APDL
         USING     @PDL,R2
         LA        R3,PL4
         USING     DAPB0C,R3
         XC        DAPB0C(DDNAMES-DAPB0C),DAPB0C
         MVI       DA0CCD+1,X'0C'      CONCATENATE CODE
         LA        R4,DDNAMES
         XR        R15,R15             COUNT
         LA        R14,#NAMES
PARSE2   L         R5,0(R14)
         LTR       R5,R5
         BZ        PARSE3              NULL
         LH        R1,4(R14)
         LTR       R1,R1
         BZ        PARSE3              NULL
         BCTR      R1,0                FOR EX
         MVI       0(R4),C' '          CLEAR DDN  SPACE
         MVC       1(7,R4),0(R4)
         EX        R1,PARSEMVC
         LA        R6,DDNAMES          CHECK FOR DUP DDNAMES
         LTR       R7,R15
         BNP       PARSE2X
PARSE2A  CLC       0(8,R4),0(R6)
         BNE       PARSE2B
         LA        R2,MSG3
         B         TPUT
PARSE2B  LA        R6,8(R6)
         BCT       R7,PARSE2A
PARSE2X  LA        R4,8(R4)            BUMP DDN PNTR
         LA        R15,1(R15)          BUMP DDN COUNT
         CH        R15,=H'16'          ONLY SPACE FOR 16
         BH        PARSE4              OUT OF SPACE
PARSE3   CLI       8(R14),X'FF'        END OF LIST?
         BE        PARSE4              YES
         L         R14,8(R14)          NEXT ENTRY
         B         PARSE2
**
PARSE4   CH        R15,=H'2'           2 OR MORE MUST BE SPEC
         BNL       PARSE5              OK
         LA        R2,MSG2
         B         TPUT
**
PARSE5   STH       R15,DA0CNUMB
         SPACE
** CONCATENATE FILES
         LA        R1,PL3
         USING     DAPL,R1
         MVC       DAPLUPT,CPPLUPT
         MVC       DAPLECT,CPPLECT
         MVC       DAPLPSCB,CPPLPSCB
         LA        R0,ECB
         ST        R0,DAPLECB
         LA        R0,PL4
         ST        R0,DAPLDAPB
         DROP      R1
         LINK      EP=IKJDAIR
         LTR       R15,R15
         BZ        DONE                CONCATENATION SUCCESSFUL
         CVD       R15,WORK            REPORT ERROR
         OI        WORK+7,X'0F'
         MVC       MSGA(MSG1L),MSG1
         UNPK      MSGA+(MSG1A-MSG1)(2),WORK
         UNPK      WORK(5),DA0CDARC(3)
         MVZ       WORK(4),=XL4'0'
         TR        WORK(4),=C'0123456789ABCDEF'
         MVC       MSGA+(MSG1B-MSG1)(4),WORK
         LA        R2,MSGA
         SPACE
TPUT     PUTLINE   PARM=PTPB,OUTPUT=(0(R2),TERM,SINGLE,DATA),          +
               TERMPUT=(EDIT,WAIT,HOLD),MF=(E,PL1)
         MVI       RCODE+1,12
**
DONE     CLI       PL2+(PPLANS-PPL),X'FF'
         BE        DONE2               NO CORE TO FREE
         L         R1,PL2+(PPLANS-PPL)
         IKJRLSA   (1)
**
DONE2    CLI       RCODE+1,12
         BNE       DONE3
         STACK     PARM=STPB,DELETE=ALL,MF=(E,PL1)
         TCLEARQ   INPUT
DONE3    LH        R2,RCODE
*        CPEXIT    RC=0(R2),SPN=&SPN
         LR        R1,R13                                        JDM1
         L         R13,4(R13)                                    JDM1
         ST        R2,16(,R13)     STORE RC                      JDM1
         FREEMAIN  RU,LV=@SIZE,A=(R1),SP=SP000                   JDM1
         RETURN    (14,12)                                       JDM1
                 SPACE
** CONSTANTS
         DC        0D'0'
         LTORG
MSG0     DC        0F'0',Y(MSG0L,0),C'**ERROR PARSING COMMAND '
         DC        C'OPERAND,  EXECUTION SUPPRESSED.'
MSG0L    EQU       *-MSG0
MSG1     DC        Y(MSG1L,0),C'**CONCATENATION ERROR, DAIR RC='
MSG1A    DC        C'00',C',  DARC='
MSG1B    DC        CL8' '
MSG1L    EQU       *-MSG1
MSG2     DC        0F'0',Y(MSG2L,0),C'**TWO OR MORE FILE NAMES MUST '
         DC        C'BE SPECIFIED,  EXECUTION SUPPRESSED.'
MSG2L    EQU       *-MSG2
MSG3     DC        0F'0',Y(MSG3L,0),C'**DUPLICATE FILESNAMES '
         DC        C'SPECIFIED, EXECUTION SUPPRESSED.'
MSG3L    EQU       *-MSG3
**
         PRINT     NOGEN
$PARSECC IKJPARM   DSECT=@PDL
#NAMES   IKJIDENT  'FILENAMES',LIST,MAXLNTH=8,FIRST=ALPHA,             +
               OTHER=ALPHANUM,PROMPT='CONCAT (FILENAME/DDNAME LIST)'
         IKJENDP
**
         PRINT     GEN
         SPACE
@STORAGE DSECT
SAVE     DS        18F'0'
WORK     DC        D'0'
ECB      DC        F'0'
APDL     DC        A(0)
PTPB     PUTLINE   MF=L
STPB     STACK     MF=L
PL1      DC        4F'0'               IOPL
PL2      DC        7F'0'               PPL
PL3      DC        5F'0'               DAPL
PL4      DC        36F'0'              DAPB0C
**
MSGA     DC        0F'0',Y(0,0),CL100' '
**
RCODE    DC        Y(0)
@SIZE    EQU       *-@STORAGE
         SPACE
         IKJCPPL
         IKJIOPL
         IKJPPL
         SPACE
         IKJPSCB
         SPACE
         IKJDAPL
         SPACE
         IKJDAP0C
DDNAMES  EQU       *
 SPACE   3
         END   CONCAT
