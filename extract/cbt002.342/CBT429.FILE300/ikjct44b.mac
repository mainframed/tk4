         TITLE 'I K J C T 4 4 B  '
*---------------------------------------------------------------------*
*                                                                     *
*        IKJCT44B CAN BE USED TO EXTEND THE TSO CLIST LANGUAGE        *
*        AND CREATE YOUR OWN CLIST VARIABLES. THE REQUIREMENTS ARE:   *
*                                                                     *
*            1. VARAIBLE BEGINS WITH "&SYSX".                         *
*            2. IKJCT44B IS AMODE(31) AND RMODE(ANY).                 *
*            3. IKJCT44B EXECUTES IN PROBLEM STATE KEY=8.             *
*            4. IKJCT44B IS LINKED AS STANDALONE MODULE AND           *
*               AVAILABLE IN NORMAL MVS SEARCH PATH (LPA,             *
*               LINKLIST, STEPLIB, AND NOT ISPF'S ISPLLIB).           *
*                                                                     *
*                                                                     *
*        MANY THANKS TO CHICAGO SOFT INC AND ITS 'TSO TIMES'          *
*        FOR THE SKELETON CODE AND THE INFORMATION ON HOW TO          *
*        DO THIS. AS AN EXTENSION TO THIS WOULD BE THE ABILITY        *
*        TO TAKE THE VARIABLE FROM A CLIST AND PUMP IT INTO TO        *
*        ISPF AS A VARIABLE (THINK ABOUT THE POSSIBILITIES).          *
*                                                                     *
*        JIM MARSHALL, CAPT, USAF-RET                                 *
*        DEPARTMENT OF THE TREASURY / FINANCIAL MANAGEMENT SERVICE    *
*                                                                     *
*                                                                     *
* DATE INSTALLED: 06/07/91                                            *
*                                                                     *
* CHNGE LOG: NONE                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SP000    EQU   0
         SPACE
IKJCT44B AMODE 31
IKJCT44B RMODE ANY
IKJCT44B CSECT
         SAVE  (14,12),,IKJCT44B_&SYSDATE
         LR    R12,R15
         USING IKJCT44B,R12      ESTABLISH ADDRESSABILITY
         LR    R5,R1             SAVE REGISTER 1
         L     R0,WORKSIZE
         GETMAIN R,LV=WORKSIZE,SP=SP000
         LR    R10,R1            SAVE OFF GOTTEN STORAGE
         ST    R13,4(,R10)       UPDATE HIS LOW SAVEAREA
         ST    R10,8(,R13)       UPDATE OUR HIGH SAVE
         LR    R13,R10           GET ADDRESS
         USING WORK,R13
         SPACE
         L     R2,36(,R5)        GET VARIABLE ADDRESS
         L     R3,8(,R2)         GET VARIABLE NAME ADDRESS
         CLC   0(7,R3),SYSXSMF   IS IT OUR VARIABLE
         BNE   RC8
         L     R1,CVTPTR         GET CVT ADDRESS
         USING CVTMAP,R1         GET ADDRESSABILITY
         L     R6,CVTSMCA        GET ADDRESS OF SMCA
         DROP  R1
         USING SMCABASE,R6       GET ADDRESSABILITY
         L     R2,44(,R5)        GET RESULT ADDRESS
         L     R3,8(,R2)         GET RESULT NAME ADDRESS
         MVC   0(4,R3),SMCASID   MOVE IN THE SMF ID
         DROP  R6
         LA    R15,0             ZERO OUT R15
         B     EXIT
         SPACE
RC8      EQU   *
         LA    R15,8
EXIT     EQU   *
         L     R0,WORKSIZE
         LR    R1,R13            GET SAVE AREA ADDRESS
         L     R13,SAVE+4        POINT TO LOW SAVE
         ST    R15,16(,R13)      SET THE RETURN CODE
         FREEMAIN R,LV=WORKSIZE,A=(R1),SP=SP000
         LM    R14,R12,12(R13)   RESTORE THE REGS
         BR    R14               RETURN
         SPACE
*---------------------------------------------------------------------*
*        DATA AREA                                                    *
*---------------------------------------------------------------------*
         SPACE
SYSXSMF  DC    CL8'SYSXSMF'      OUR VARIABLE NAME
         LTORG
         DS    0F
WORK     DSECT
SAVE     DS    18F
WORKSIZE EQU   *-WORK
         SPACE
         IEESMCA
         SPACE
         CVT   DSECT=YES,LIST=NO
         END
