         TITLE 'VS FORTRAN TSO PROMPTER LOAD #2 - FORTVPM2'
***********************************************************************
*                                                                     *
*                VS FORTRAN TSO PROMPTER 2ND LOAD                     *
*                                                                     *
*        THIS MODULE WILL ATTACH THE VS FORTRAN COMPILER, WAIT        *
*        FOR COMPLETION, FREE DATASETS ALLOCATED BY THE 1ST LOAD,     *
*        AND RETURN TO THE USER.                                      *
*                                                                     *
*        AUTHOR ----                                                  *
*          JOHN SILL                                                  *
*          C.B.I INDUSTRIES (CBI)                                     *
*          800 JORIE BLVD                                             *
*          OAK BROOK, IL 60521                                        *
*          TEL. (312) 654-7439                                        *
*                                                                     *
*        MAINTENANCE ACTIVITY LOG ----                                *
*                                                                     *
*         PLEASE REPORT ANY BUGS OR ENHANCEMENTS TO AUTHOR ABOVE.     *
*                                                                     *
*         VERSION    DATE    AUTHOR        DESCRIPTION                *
*          01.00   10/10/83   JRS     INITIAL IMPLEMENTATION          *
*                                                                     *
*        MODULE ATTRIBUTES ---                                        *
*          REENTERANT, REUSABLE, REFRESHABLE, NOT AUTHORIZED          *
*                                                                     *
*        INPUT --                                                     *
*          R1  - ADDR OF WORKAREA PROVIDED BY THE 1ST LOAD            *
*                                                                     *
***********************************************************************
         EJECT
FORTVPM2 CSECT
         SPACE
R0       EQU   0                   REGISTER 0
R1       EQU   1                   REGISTER 1
R2       EQU   2                   REGISTER 2
R3       EQU   3                   REGISTER 3
R4       EQU   4                   REGISTER 4
R5       EQU   5                   REGISTER 5
R6       EQU   6                   REGISTER 6
R7       EQU   7                   REGISTER 7
R8       EQU   8                   REGISTER 8
R9       EQU   9                   REGISTER 9
R10      EQU   10                  REGISTER 10
R11      EQU   11                  REGISTER 11
R12      EQU   12                  REGISTER 12
R13      EQU   13                  REGISTER 13
R14      EQU   14                  REGISTER 14
R15      EQU   15                  REGISTER 15
         SAVE  (14,12)             SAVE CALLERS REGISTERS
         LR    R12,R15             COPY ENTRY ADDR
         USING FORTVPM2,R12
         LR    R11,R1              COPY WORKAREA ADDR
         USING WORKAREA,R11
         ST    R13,SAVEAREA+4      BACKWARD CHAIN SAVEAREAS
         ST    R11,8(R13)          FORWARD CHAIN SAVEAREAS
         LR    R13,R11             SAVEAREA ADDR IN R13
         CLI   TASKLIB,0           WAS TASKLIB SPECIFIED
         LA    R3,DCBIMAGE         ADDR OF TASKLIB DCB
         BNE   DOATTCH             ... YES, GO
         SR    R3,R3               INDICATE NO TASKLIB
         SPACE 1
DOATTCH  EQU   *
         XC    ECB,ECB             ZERO ECB
         LA    R1,PARMLIST         ADDR OF PARM LIST
         LA    R15,ATCHLST         ADDR OF ATTACH LIST
         ATTACH EPLOC=COMPLRID,ECB=ECB,MF=(E,(1)),SF=(E,(15)),  XXXXXXXX
               SHSPL=SPLIST,TASKLIB=(R3)
         ST    R1,PDLADDR          SAVE TCB ADDR
         WAIT  ECB=ECB             WAIT FOR COMPLETION
         DETACH PDLADDR            DETACH COMPILER
         LH    R8,ECB+2            GET RETURN CODE
         CLI   TASKLIB,0           WAS TASKLIB USED
         BE    NOTASKL             ... NO, GO
         CLOSE ,MF=(E,OPENPL)      CLOSE TASKLIB DCB
         SPACE 1
NOTASKL  EQU   *
         LA    R9,DAIRPB           ADDR OF DAIR PARM AREA
         USING DAPB10,R9
         LH    R2,INPDDCNT         LOAD COUNT OF INPUT DD'S
         LA    R0,1                CONSTANT ONE
         CR    R2,R0               MORE THAN ONE
         BNH   NOIDECON             ... NO, GO
         XC    DAPB10(DAPB10LN),DAPB10 ZERO PARM LIST
         MVI   DA10CD+1,X'10'      SET ENTRY CODE
         MVC   DA10DDN,SYSIN       MOVE IN DDNAME
         DROP  R9
         BAL   R10,DAIRLINK        CALL DAIR
         LTR   R15,R15             ANY ERRORS
         BZ    IDECONOK             ... NO, GO
         B     NOIDECON
         SPACE
IDECONOK EQU   *
         XC    SYSIN,SYSIN         ZERO SYSIN ALT DDNAME ENTRY
         SPACE
NOIDECON EQU   *
         USING DAPB18,R9
         XC    DAPB18(DAPB18LN),DAPB18 ZERO PARM LIST
         MVI   DA18CD+1,X'18' SET ENTRY CODE
         MVI   DA18MNM,C' '        BLANK
         MVC   DA18MNM+1(L'DA18MNM-1),DA18MNM MEMBER NAME
         LH    R2,INPDDCNT         LOAD COUNT OF INPUT DD'S
         LA    R0,1                CONSTANT ONE
         CR    R2,R0               MORE THAN ONE
         BNH   DDLIBFR             ... NO, GO
         SPACE
INPDDFR  EQU   *
         LR    R1,R2               COPY COUNTER
         SLL   R1,3                TIMES 8
         LA    R1,INPDDNMS-8(R1)   ADDR OF DDNAME
         MVC   DA18DDN,0(R1)       MOVE TO DAIR PARM LIST
         BAL   R10,DAIRLINK        CALL DAIR
         BCT   R2,INPDDFR          DECREMENT AND LOOP
         DROP  R9
         SPACE
DDLIBFR  EQU   *
         LH    R2,LIBDDCNT         LOAD COUNT OF SYSLIB DD'S
         LA    R0,1                CONSTANT ONE
         CR    R2,R0               MORE THAN ONE
         BNH   NOLDECON             ... NO, GO
         USING DAPB10,R9
         XC    DAPB10(DAPB10LN),DAPB10 ZERO PARM LIST
         MVI   DA10CD+1,X'10'      SET ENTRY CODE
         MVC   DA10DDN,SYSLIB       MOVE IN DDNAME
         DROP  R9
         BAL   R10,DAIRLINK        CALL DAIR
         LTR   R15,R15             ANY ERRORS
         BZ    LDECONOK             ... NO, GO
         B     NOLDECON
         SPACE
LDECONOK EQU   *
         XC    SYSLIB,SYSLIB       ZERO SYSLIB ALT DDNAME ENTRY
         SPACE
NOLDECON EQU   *
         USING DAPB18,R9
         XC    DAPB18(DAPB18LN),DAPB18 ZERO PARM LIST
         MVI   DA18CD+1,X'18' SET ENTRY CODE
         MVI   DA18MNM,C' '        BLANK
         MVC   DA18MNM+1(L'DA18MNM-1),DA18MNM MEMBER NAME
         LH    R2,LIBDDCNT         LOAD COUNT OF SYSLIB DD'S
         LA    R0,1                CONSTANT ONE
         CR    R2,R0               MORE THAN ONE
         BNH   DDLSTFR             ... NO, GO
         SPACE
LIBDDFR  EQU   *
         LR    R1,R2               COPY COUNTER
         SLL   R1,3                TIMES 8
         LA    R1,LIBDDNMS-8(R1)   ADDR OF DDNAME
         MVC   DA18DDN,0(R1)       MOVE TO DAIR PARM LIST
         BAL   R10,DAIRLINK        CALL DAIR
         BCT   R2,LIBDDFR          DECREMENT AND LOOP
DDLSTFR  EQU   *
         LA    R2,DDNLLEN/8+1      NBR OF DDNAMES PLUS TASKLIB
         LA    R4,DDNLIST          ADDR OF ALT DDNAME LIST
         SPACE
DDLFRLP  EQU   *
         CLI   0(R4),0             ENTRY EMPTY
         BE    DDLFRNXT            ... YES, GO
         MVC   DA18DDN,0(R4)       MOVE DDNAME TO PARM LIST
         BAL   R10,DAIRLINK        CALL DAIR
         SPACE
DDLFRNXT EQU   *
         LA    R4,8(,R4)           BUMP TO NEXT ENTRY
         BCT   R2,DDLFRLP          DECREMENT AND LOOP
         LR    R1,R13              COPY GETMAIN AREA ADDR
         L     R13,SAVEAREA+4      ADDR OF PREVIOUS SAVE
         LA    R0,MAINLEN          GETMAIN AREA LENGTH
         FREEMAIN R,LV=(0),A=(1)   FREE WORKAREA
         LR    R15,R8              COPY RETURN CODE
         RETURN (14,12),RC=(15)
         EJECT
DAIRLINK EQU   *
         SR    R1,R1               ZERO
         ST    R1,ECB                ECB
         CALLTSSR EP=IKJDAIR,MF=(E,DAIRPL) CALL DAIR
         BR    R10                 RETURN
         EJECT
SPLIST   DC    AL1(3,0,1,78)       SHARED SUBPOOL LIST
         EJECT
         PRINT NOGEN
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES
         IKJDAPL
DAPLLN   EQU   *-DAPL              LENGTH OF DAIR PARM LIST
         IKJDAP08
DAPB08LN EQU   *-DAPB08            LENGTH OF DAIR 08 PARM LIST
         IKJDAP18
DAPB18LN EQU   *-DAPB18            LENGTH OF DAIR 18 PARM LIST
         IKJDAP1C
DAPB1CLN EQU   *-DAPB1C            LENGTH OF DAIR 1C PARM LIST
         IKJDAP0C
DA0CDDN  DS    16CL8               CONCATENATED DDNAMES
DAPB0CLN EQU   *-DAPB0C            LENGTH OF DAIR 0C PARM LIST
         IKJDAP10
DAPB10LN EQU   *-DAPB10            LENGTH OF DAIR 10 PARM LIST
         CVT   LIST=NO,DSECT=YES
         PRINT GEN
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F                 MAIN PROMPTER SAVE AREA
PARMLIST DS    0F                  VS FORTRAN PARM LIST
OPTNPTR  DS    A                   OPTION LIST ADDR
DDNPTR   DS    A                   ALTERNATE DDNAME LIST ADDR
OPTNLTH  DS    H                   OPTION LIST LENGTH
OPTNLIST DS    CL512               FORTRAN OPTION LIST
DDNLTH   DS    H                   ALTERNATE DDNAME NAME LIST LENGTH
DDNLIST  DS    0C                  ALTERNATE DDNAME LIST
SYSLIN   DS    CL8                 SYSLIN SLOT
         DS    CL24                SLOTS NOT USED BY VS FORTRAN
SYSIN    DS    CL8                 SYSIN SLOT
SYSPRINT DS    CL8                 SYSPRINT SLOT
         DS    CL8                 SYSPUNCH SLOT (NOT USED BY PROMPTER)
         DS    CL16                SLOTS NOT USED BY VS FORTRAN
SYSTERM  DS    CL8                 SYSTERM SLOT
SYSLIB   DS    CL8                 SYSLIB SLOT
DDNLLEN  EQU   *-DDNLIST           LENGTH OF ALT DDNAME LIST
TASKLIB  DS    CL8                 TASKLIB DDNAME (MUST FOLLOW DDNLIST)
COMPLRID DS    CL8                 MODULE NAME IF COMPILER
ATCHLST  ATTACH SF=L
OPENPL   OPEN  (,),MF=L            OPEN PARAMETER LIST
DCBIMAGE DCB   DDNAME=DDNAME,DSORG=PO,MACRF=R TASKLIB DCB IMAGE
PDLADDR  DS    F                   PARSE RETURNED PDL ADDR
ECB      DS    F                   EVENT CONTROL BLOCK
DFAREA   DS    0F                  DAIR FAIL PARM LIST
         DS    (DFLEN)X
DAIRFRC  DS    F                   DAIR FAIL RETURN CODE SAVE
DAIRPL   DS    0F
         DS    (DAPLLN)X           DAIR PARAMETER LIST
DAIRPB   DS    0F                  DAIR PARAMETER BLOCKS
         DS    (DAPB08LN)X
         ORG   DAIRPB
         DS    (DAPB18LN)X
         ORG   DAIRPB
         DS    (DAPB1CLN)X
         ORG   DAIRPB
         DS    (DAPB0CLN)X
         ORG   DAIRPB
         DS    (DAPB10LN)X
         ORG   ,
INPDDCNT DS    H                   COUNT OF INPUT DATASETS
INPDDNMS DS    16CL8               DDNAMES FOR CONCATENATION
LIBDDCNT DS    H                   COUNT OF SYSLIB DATASETS
LIBDDNMS DS    16CL8               DDNAMES FOR CONCATENATION
         DS    0D
MAINLEN  EQU   *-WORKAREA          LENGTH OF WORKAREA
         END
