         TITLE 'D U T A P E  --  OPERATOR LIKE  D U,TAPE  '
*--------------------------------------------------------------------*
*        THIS COMMAND IS BASED ON THE 'DUDASD' COMMAND. DECIDED      *
*        TO CREATE IT WHEN I MOVED TO AN INSTALLATION THAT DID       *
*        NOT BELIEVE ALLOWING ME TO HAVE AN OPERATOR'S CONSOLE       *
*        ON MY DESK AS I DID IN SAN ANTONIO. NICE TO SEE WHAT THE    *
*        OPERATORS HAVE DECIDED TO DO WITH THE DRIVES.               *
*                                                                    *
*        JIM MARSHALL, CAPT, USAF-RET                                *
*                                                                    *
* WRITTEN: 10 NOV 86.                                                *
*                                                                    *
* CHNGE LOG: 11JUN91 - RUNS 31 BIT MODE                          JDM1*
*--------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2                   ADDRESSABILITY TO IOPL
R3       EQU   3                   WORK AND BAL REG
R4       EQU   4                   BCT LOOP REG
R5       EQU   5                   ADDRESSABILITY TO OUTPUT LINE
R6       EQU   6                   UNUSED
R7       EQU   7                   UNUSED
R8       EQU   8                   UNUSED
R9       EQU   9                   ADDRESSABILITY TO CPPL
R10      EQU   10                  ADDRESSABILITY TO UCB
R11      EQU   11                  ADDRESSABILITY TO CVT
R12      EQU   12                  BASE REGS
R13      EQU   13                  ADDRESSABILITY TO GETMAIN
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  ENTRY ADDRESS AND WORK
SP000    EQU   0                                                 JDM1
         SPACE
DUTAPE   CSECT
DUTAPE   AMODE 31
DUTAPE   RMODE ANY                                               JDM1
         SAVE  (14,12),,DUTAPE_&SYSDATE &SYSTIME
         LR    R12,R15             SAVE THE ENTRY ADDRESS
         USING DUTAPE,R12          EST ADDRESSABILITY
         LR    R9,R1               SAVE ADDR OF CPPL
         USING CPPL,R9             ADDRESSABILITY TO CPPL
         GETMAIN RU,LV=WORKSIZE,SP=SP000,LOC=ANY                 JDM1
         ST    R13,4(R1)           SETUP
         ST    R1,8(R13)             SAVE
         LR    R13,R1                  AREA
         USING DSAWRK,R13          GET ADDRESSABILITY
         USING UCBOB,R10           ADDRESSABILITY TO UCB
         SPACE
         LA    R2,IOPLIST          GET ADDRESS OF IOPLIST
         USING IOPL,R2             ADDRESSABILITY TO IOPL
         MVC   IOPLECT,CPPLECT
         MVC   IOPLUPT,CPPLUPT
         XC    ECB,ECB
         XC    PREFIX(4),PREFIX
         LA    R15,L'BUFFER+4
         STH   R15,PREFIX
         L     R11,CVTPTR          GET ADDRESS OF CVT
         USING CVTMAP,R11          ADDRESSABILITY TO DSECT
         SPACE
*--------------------------------------------------------------------*
*        PUT OUT OUR HEADER FOR THE DISPLAY                          *
*--------------------------------------------------------------------*
         SPACE
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         MVC   BUFFER(60),HEADER
         BAL   R3,DISPLAY
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         BAL   R3,DISPLAY
         SPACE
*--------------------------------------------------------------------*
*        SETUP FOR THE UCB SEARCH ROUTINE AMONG OTHER THINGS.        *
*--------------------------------------------------------------------*
         SPACE
         XC    WORKAREA,WORKAREA   CLEAR WORK AREA
         LA    R1,WORKAREA         GET ADDR OF WORK AREA
         ST    R1,PARMWA           STORE ADDR IN PARMLIST
         LA    R1,DEVCLASS         GET ADDR OF DEV CLASS
         ST    R1,PARMDEVT         STORE ADDR IN PARMLIST
         MVI   DEVCLASS,UCB3TAPE   INDICATE ONLY TAPE UCBS.
         LA    R1,ADDRUCB          ADDR OF WHERE TO ST UCB ADDR
         ST    R1,PARMUCB          STORE ADDR IN PARMLIST
         OI    PARMUCB,X'80'       INDICATE END OF PARMLIST
         SPACE
*--------------------------------------------------------------------*
*        NOW WE FIGURE HOW MANY UCBS TO PROCESS BEFORE WE WILL       *
*        PRINT A LINE. IN THIS CASE IT COMES OUT TO 4.               *
*--------------------------------------------------------------------*
         SPACE
DULINE   EQU   *
         SPACE
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         LA    R5,BUFFER
         LA    R4,L'BUFFER/L'ELEMENT
         USING ELEMENT,R5
         SPACE
*--------------------------------------------------------------------*
*        THIS IS WHERE WE WILL DO THE UCB SEARCH.                    *
*--------------------------------------------------------------------*
         SPACE
DUUNIT   EQU   *
         LA    R1,PARMLIST         ADDR OF PARMLIST IN R1
         L     R15,CVTUCBSC        GET SCAN SERVICE ADDRESS
         SPACE
*--------------------------------------------------------------------*
*        GOING OFF TO SCAN SERVICES, WE ARE SETUP AS FOLLOWS:        *
*                                                                    *
*            R1  - ADDR OF PARAMETER LIST                            *
*            R13 - ADDR OF 18F SAVE AREA                             *
*            R14 - RETURN ADDRESS                                    *
*            R15 - SCAN SERVICE ENTRY POINT                          *
*                                                                    *
*--------------------------------------------------------------------*
         SPACE
         BALR  R14,R15             GOTO SCAN SERVICE
         LTR   R15,R15             HAS A UCB BEEN FOUND?
         BNZ   ALLDONE             WE ARE FINISHED ALMOST
         L     R10,ADDRUCB         GET ADDR RETURNED
         CLC   LASTCUU,UCBNAME
         BE    DUUNIT
         MVC   ELEUNIT,UCBNAME
         MVC   LASTCUU,UCBNAME
         MVC   ELEVSN,SRTEVOLI
         TM    SRTESTAT,UCBONLI
         BO    ONLINE
         MVC   ELEVSN,=CL6'(OFF)'
         TM    UCBFLA,UCBBOX       DEVICE FORCED OFFLINE?
         BNO   GETDEVT
         MVC   ELEVSN(11),=CL11'(OFF-FORCE)'
         B     GETDEVT
ONLINE   EQU   *
         MVI   ELEDASH,C'-'        MOVE IN A DASH
         MVI   ELESTAT,C'O'        IS ONLINE FOR MINIMUM
         TM    UCBSTAT,UCBALOC     IS IT ALLOCATED?
         BNO   TEST1
         MVI   ELESTAT,C'A'        ALLOCATED
TEST1    EQU   *
         TM    UCBFLA,UCBNRY       IS IT NOT READY?
         BNO   TEST3
         MVC   ELEMSTAT,=C'NRD'    LET ME KNOW
TEST2    EQU   *
         TM    UCBSTAT,UCBRESV     RESERVED?
         BNO   TEST3
         MVC   ELEMSTAT,=C'RSV'    IS RESERVED
TEST3    EQU   *
         TM    UCBWGT,UCBREW       ISSUE REWIND REQUEST
         BNO   TEST4
         MVC   ELEMSTAT,=C'REW'    REWINDING
TEST4    EQU   *
         TM    UCBFLB,UCBHDET      DETECTED HOT I/O?
         BNO   GETDEVT             NO - CHECK RESIDENT
         MVC   ELEMSTAT,=C'HIO'    HAVE HOT I/O IN PROGRESS
         SPACE
*--------------------------------------------------------------------*
*        NOW WE WILL GET THE DEVICE TYPE CODE OF THE UCB. THOSE      *
*        LUCKY TYPES WHO HAVE ONLY 3380'S REALLY DO NOT NEED THIS    *
*        BUT THOSE WHO HAVE MIXTURES, IT HELPS.                      *
*--------------------------------------------------------------------*
         SPACE
GETDEVT  EQU   *
         MVC   ELEDEVT(8),=C'( ???? )'  MOVE IN SKELETON
         LA    R3,DEVTABLE         POINT TO OUR TABLE
DEVLOOP  EQU   *
         CLI   0(R3),X'FF'         ARE WE AT END OF TABLE?
         BE    ULOOP               IF SO, WE WILL KNOW
         CLC   0(4,R3),UCBTYP      DO WE MATCH?
         BE    GOTDEVT
         A     R3,=F'10'           MOVE ALONG IN TABLE
         B     DEVLOOP             LOOP BACK
         SPACE
GOTDEVT  EQU   *
         MVC   ELEDEVT+1(6),4(R3)  MOVE IN ITS NAME
         SPACE
ULOOP    EQU   *
         LA    R5,ELENEXT
         BCT   R4,DUUNIT
         SPACE
         BAL   R3,DISPLAY          GO PUT THE LINE
         B     DULINE
ALLDONE  EQU   *
         BAL   R3,DISPLAY          GO PUT THE LINE
         B     DONE
         SPACE
*--------------------------------------------------------------------*
*        MAKE THE DISPLAY LINE A SUBROUTINE                          *
*--------------------------------------------------------------------*
         SPACE
DISPLAY  EQU   *
         PUTLINE                                                       +
               PARM=PUTLINE,MF=(E,IOPLIST),ECB=ECB,                    +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     +
               OUTPUT=(PREFIX,TERM,SINGLE,DATA)
         BR    R3
         SPACE
DONE     EQU   *
         LR    R1,R13              GET ADDR FOR FREEMAIN
         L     R13,4(R13)          GET HIGHSAVE
         FREEMAIN RU,LV=WORKSIZE,A=(R1),SP=SP000                 JDM1
         RETURN (14,12),RC=0       SO LONG FOR NOW
         SPACE
*--------------------------------------------------------------------*
*        CONSTANT AREA                                               *
*--------------------------------------------------------------------*
         SPACE
DEVTABLE EQU   *
         DC    X'78008080',C' 3480 '
         DC    X'32108003',C'3400-6'
         DC    X'32008003',C'3400-5'
         DC    X'34208003',C'3400-4'
         DC    X'34008003',C'3400-3'
         DC    X'30C08003',C'3400-2'
         DC    X'30008001',C' 2400 '
         DC    X'30808001',C'2400-1'
         DC    X'30C08001',C'2400-2'
         DC    X'34008001',C'2400-3'
         DC    X'34208001',C'2400-4'
         DC    X'FFFFFFFF'             END OF TABLE
         SPACE
HEADER   DS    0CL60
         DC    CL10' '
         DC    CL15'-------------  '
         DC    CL20'        D U,TAPE'
         DC    CL15'  -------------'
         SPACE
         DS    0F
         LTORG
         SPACE
ELEWRK   DSECT
ELEMENT  DS    0CL26
ELEUNIT  DS    CL3                 UCB ADDR IN EBCDIC
         DS    CL1                 SPACE
ELEVSN   DS    CL6                 VOL SER
         DS    CL1                 SPACE
ELESTAT  DS    CL1                 ONE BYTE STATUS
ELEDASH  DS    CL1                 DASH
ELEMSTAT DS    CL3                 NRD, MTP,
         DS    CL1                 FILLER
ELEDEVT  DS    CL8                 ( DEVTYPE )
         DS    CL1                 FILLER
ELENEXT  EQU   *
         SPACE
DSAWRK   DSECT
SAVE     DS    18F
IOPLIST  DS    07F
ECB      DS    01F
PREFIX   DS    2H
BUFFER   DS    CL(L'ELEMENT*3)
LASTCUU  DS    CL3
PUTLINE  PUTLINE MF=L
PARMLIST DS    3F                  PARMLIST MAPPING
         ORG   PARMLIST
PARMWA   DS    F                   ADDR OF 100 BYTE AREA
PARMDEVT DS    F                   ADDR OF BYTE CONTAINING
*                                  DEVTYPE TO BE SEARCHED
PARMUCB  DS    F                   ADDR OF WORD TO CONTAIN UCB ADDR
         SPACE
DEVCLASS DS    CL1                 DEV CLASS TO SEARCH
ADDRUCB  DS    F                   ADDR OF UCBS
         DS    0D                  FORCE DOUBLE WORD !!
WORKAREA DS    CL100               'UCB' WORKAREA
DSAEND   DS    0D
WORKSIZE EQU   DSAEND-DSAWRK                                     JDM1
         EJECT
         IKJIOPL
         EJECT
         IKJCPPL
         EJECT
UCBWRK   DSECT
         IEFUCBOB
         EJECT
         CVT   DSECT=YES
         END
