         MACRO
&NAME    IEFCNMSG &MN,&TEXT,&TEXT2,                                    X
               &OFFSET=,&VARDATA=NO,                                   X
               &DSECT=NO,&ERR=YES,&WTO=NO,&TRACE=NO
.*
         LCLA  &LTH1,&LTH2,&VARO
         LCLC  &STR1,&STR2,&FLAG1,&FLAG2
.*
         AIF   ('&DSECT' EQ 'NO').GENIT
.*
         TITLE 'IEFCNVPP - MESSAGE TABLE MAPPING DSECT'
MSGDSECT DSECT
MSGNEXT  DS    Y                   OFFSET OF NEXT MESSAGE TABLE ENTRY
MSGMSGNO DS    H                   MESSAGE NUMBER
MSGMSGLN DS    H                   MESSAGE TEXT LENGTH
MSGVAROF DS    H                   OFFSET TO VARIABLE DATA
*
MSGFLAG1 DS    X                   MESSAGE FLAGS
MSG1ERR  EQU   X'80'               MESSAGE INDICATES JOB FAILURE
MSG1WTO  EQU   X'40'               MESSAGE IS A WTO ONLY MESSAGE
MSG1TRCE EQU   X'20'               MESSAGE IS A TRACING MESSAGE ONLY
MSG1VAR  EQU   X'10'               MESSAGE CONTAINS VARIABLE DATA
MSG1INLN EQU   X'08'               VARIABLE DATA IS INLINE
*
MSGFLAG2 DS    X                   MORE MESSAGE FLAGS
MSG2JBPR EQU   X'80'               VARIABLE DATA IS JOBPROC DDNAME
MSG2SYSP EQU   X'40'               VARIABLE DATA IS SYSPROC KEYWORD
MSG2DSN  EQU   X'20'               VARIABLE DATA IS DATA SET NAME
MSG2SWAK EQU   X'10'               VARIABLE DATA IS SWA KEYWORD
*
MSGPREFL EQU   *-MSGDSECT          MESSAGE TABLE PREFIX
*
MSGMSGTX DS    0C                  START OF MESSAGE TEXT
*
         MEXIT
.*
.GENIT   ANOP
&LTH1    SETA  0
&STR1    SETC  ''
&LTH2    SETA  0
&STR2    SETC  ''
&VARO    SETA  0
.*
&FLAG1   SETC  '0'
&FLAG2   SETC  '0'
.*
         AIF   (T'&TEXT EQ 'O' AND T'&TEXT2 EQ 'O').VGMER01
         AIF   (T'&MN EQ 'O').VGMER02
.*
         AIF   (T'&TEXT EQ 'O').NOTEXT1
&LTH1    SETA  K'&TEXT-2           GET LENGTH OF MESSAGE TEXT
&STR1    SETC  '&TEXT'(2,&LTH1)    EXTRACT MESSAGE TEXT
.NOTEXT1 ANOP
.*
         AIF   (T'&TEXT2 EQ 'O').NOTEXT2
&LTH2    SETA  K'&TEXT2-2          GET LENGTH OF MESSAGE TEXT
&STR2    SETC  '&TEXT2'(2,&LTH2)   EXTRACT MESSAGE TEXT
.NOTEXT2 ANOP
.*
         AIF   ('&OFFSET' EQ '').SETOFF
&VARO    SETA  &OFFSET
         AGO   .NOOFF
.*
.SETOFF  AIF   ('&VARDATA' EQ 'NO').NOOFF
&VARO    SETA  &LTH1
.*
.NOOFF   ANOP
&LTH1    SETA  &LTH1+&LTH2
.*
         AIF   ('&ERR' NE 'YES').NOERR
&FLAG1   SETC  '&FLAG1.+MSG1ERR'
.NOERR   ANOP
.*
         AIF   ('&WTO' NE 'YES').NOWTO
&FLAG1   SETC  '&FLAG1.+MSG1WTO'
.NOWTO   ANOP
.*
         AIF   ('&TRACE' NE 'YES').NOTRCE
&FLAG1   SETC  '&FLAG1.+MSG1TRCE'
.NOTRCE  ANOP
.*
         AIF   ('&VARDATA' EQ 'NO').NOVARD
&FLAG1   SETC  '&FLAG1.+MSG1VAR'
.NOVARD  ANOP
.*
         AIF   ('&VARDATA' NE 'INLINE').NOINLNE
&FLAG1   SETC  '&FLAG1.+MSG1INLN'
.NOINLNE ANOP
.*
         AIF   ('&VARDATA' NE 'JOBPROC').NOJBPRC
&FLAG2   SETC  '&FLAG2.+MSG2JBPR'
.NOJBPRC ANOP
.*
         AIF   ('&VARDATA' NE 'SYSPROC').NOSYSPR
&FLAG2   SETC  '&FLAG2.+MSG2SYSP'
.NOSYSPR ANOP
.*
         AIF   ('&VARDATA' NE 'DSNAME').NODSNME
&FLAG2   SETC  '&FLAG2.+MSG2DSN'
.NODSNME ANOP
.*
         AIF   ('&VARDATA' NE 'SWA').NOSWA
&FLAG2   SETC  '&FLAG2.+MSG2SWAK'
.NOSWA   ANOP
.*
MSGNO&MN EQU   &MN                 EQUATE FOR MESSAGE NUMBER
         AIF   ('&NAME' EQ '').NONAME
&NAME    EQU   MSGNO&MN            ALTERNATE NAME FOR MESSAGE
.*
.NONAME  ANOP
         DC    Y(MSGND&MN-*)       OFFSET OF NEXT MESSAGE TABLE
         DC    Y(MSGNO&MN)         MESSAGE NUMBER
         DC    H'&LTH1'            LENGTH OF MESSAGE TEXT
         DC    Y(&VARO)            VARIABLE TEXT OFFSET
         DC    AL1(&FLAG1)         MESSAGE FLAGS
         DC    AL1(&FLAG2)         MESSAGE FLAGS
*
         AIF   (&LTH1 EQ &LTH2).NOSTR1
         DC    C'&STR1'            FIRST PART OF MESSAGE TEXT
.NOSTR1  AIF   (&LTH2 EQ 0).NOSTR2
         DC    C'&STR2'            SECOND PART OF MESSAGE TEXT
.NOSTR2  ANOP
MSGND&MN DC    0H'0'               LABEL FOR NEXT MESSAGE TABLE
         AIF   ('&MN' EQ '000').ISLAST
         MEXIT ,
.*
.ISLAST  ANOP
         DC    Y(0)                END OF TABLE
         MEXIT ,
.*
.VGMER01 MNOTE 12,'*** MESSAGE TEXT REQUIRED BUT NOT SPECIFIED ***'
         MEXIT ,
.*
.VGMER02 MNOTE 12,'*** MESSAGE NUMBER REQUIRED BUT NOT SPECIFIED ***'
         MEND
*
