         MACRO
&NAME    IEFCNDSN &LIST
.*
         LCLA  &NLIST,&CNT,&OFFSET
.*
&NLIST   SETA  N'&SYSLIST
.*
         AIF   (&NLIST EQ 0).ERROR1
         AIF   (&NLIST EQ 1).EXIT
         AIF   (K'&SYSLIST(2) EQ 0).EXIT
.*
&OFFSET  SETA  0
         AIF   ('&SYSLIST(1)' EQ 'JOBEXCL').SETOFF
&OFFSET  SETA  2
         AIF   ('&SYSLIST(1)' EQ 'JOBINCL').SETOFF
.*
&OFFSET  SETA  4
         AIF   ('&SYSLIST(1)' EQ 'TSUEXCL').SETOFF
&OFFSET  SETA  6
         AIF   ('&SYSLIST(1)' EQ 'TSUINCL').SETOFF
.*
&OFFSET  SETA  8
         AIF   ('&SYSLIST(1)' EQ 'STCEXCL').SETOFF
&OFFSET  SETA  10
         AIF   ('&SYSLIST(1)' EQ 'STCINCL').SETOFF
.*
.ERROR1  MNOTE 12,'INVALID INCLUDE/EXCLUDE LIST TYPE'
         MEXIT
.*
.SETOFF  ORG   &SYSECT.+&OFFSET
         DC    Y(&NAME-&SYSECT)    OFFSET TO DATA SET LIST
         ORG   ,
*
&NAME    DC    0F'0'               START OF DATA SET LIST
&CNT     SETA  2
.DSNLOOP AIF   (K'&SYSLIST(&CNT) EQ 0).DONE
         DC    CL44'&SYSLIST(&CNT)'
&CNT     SETA  &CNT+1
         AIF   (&CNT LE &NLIST).DSNLOOP
.*
.DONE    DC    C' '                END OF DATA SET LIST
.*
.EXIT    ANOP
         MEND
