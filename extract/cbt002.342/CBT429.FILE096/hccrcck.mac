HCCRCCK  TITLE 'HCCRCCK  - PROGRAM TO ABEND ON A BAD RC'
HCCRCCK  CSECT
DSASP    EQU   0                        SUBPOOL FOR DSA OF THIS PGM
***********************************************************************
*   HCCRCCK  -  RETURN CODE CHECKER                                   *
*                                                                     *
*   FUNCTION:  THIS PROGRAM IS DESIGNED TO ATTACH SOME OTHER PROGRAM  *
*       TEST ITS RETURN CODE AND FORCE AN ABEND IF THE RETURN CODE    *
*       IS NOT IN ACCEPTABLE RANGE.  THIS ALLOWS JCL CONDITIONAL      *
*       DISP FOR DD STATEMENTS OR COND=ONLY FOR EXEC STATEMENTS       *
*       TO BE IMPLEMENTED FOR ANY PROGRAM.  THE ATTACHED PROGRAM      *
*       WILL BE ATTACHED AUTHORIZED IF IT IS AUTHORIZED AND IN AN     *
*       AUTHORIZED LIBRARY, OR NON AUTHORIZED OTHERWISE.              *
*                                                                     *
*       STANDARD SEARCH ORDER IS USED TO FIND THE PROGRAM TO ATTACH.  *
*                                                                     *
*   USE:                                                              *
*         //STEP1  EXEC  PGM=HCCRCCK,                                 *
*         //  PARM='PGM=PROGRAM,MAXCC=MAX-RETURN-CODE/PARMS'          *
*                                                                     *
*         WHERE:  "PROGRAM" IS THE PROGRAM TO BE ATTACHED,            *
*                 "MAX-RETURN-CODE" IS THE LARGEST RETURN CODE        *
*                     VALUE NOT TO BE CONVERTED TO AN ABEND CODE,     *
*                 AND "PARMS" ARE OPTIONAL AND ARE ANY PARAMETERS     *
*                     TO BE PASSED TO THE ATTACHED PROGRAM.  IF       *
*                     "PARMS" ARE OMITTED, THE "/" MAY ALSO BE        *
*                     OMITTED.                                        *
*                                                                     *
*   ATTRIBUTES:                                                       *
*         REENTRANT, AMODE=24, RMODE=24, AC=1.                        *
*                                                                     *
*   INPUTS:                                                           *
*         PARAMETER AS DESCRIBED ABOVE.                               *
*                                                                     *
*   OUTPUTS:                                                          *
*         NONE.                                                       *
*                                                                     *
*  WRITTEN BY BILL WARRINER, UMCHC                                    *
*                                                                     *
*  UPDATE HISTORY:                                                    *
*    12/30/87 WJW  -  MODIFIED TO USE USER ATTACH-REAUTH ROUTINE.     *
*                                                                     *
***********************************************************************
         EJECT
         CONNECT DSALEN,RCCKDSA,LI=L,BASE=R12,PARM=R11,CLEAR=YES,      X
               AMODE=24,RMODE=24,LOC=BELOW,COPYRT=(1986,1987)
         LA    R0,DEFAULTS              SETUP DEFAULT VALUES
         LA    R1,DFTLEN
         LA    R14,INITSTG              IN INITIALIZED STORAGE
         LA    R15,INITLEN
         MVCL  R14,R0                   COPY IN DEFAULT VALUES
         LTR   R11,R11                  ANY PARM PASSED?
         BZ    PASSPARM                 IF NOT, JUST USE DEFAULTS
         L     R3,0(R11)                GET POINTER TO STRING
         LTR   R3,R3                    ANY STRING?
         BZ    PASSPARM                 IF NOT, JUST USE DEFAULTS
         LH    R4,0(R3)                 GET STRING LENGTH
         CH    R4,=H'0'                 ANY STRING?
         BNH   PASSPARM                 IF NOT, PASS UNCHANGED.
         LA    R5,2(R3)                 POINT TO START OF STRING
PARMCK   DS    0H
         CLI   0(R5),C','               DELIMITER?
         BE    PARMBUMP
         CLI   0(R5),C' '               DELIMITER?
         BNE   NODLIM
PARMBUMP DS    0H
         LA    R5,1(R5)                 BUMP PAST DELIMITER
         SH    R4,=H'1'                 DEX LENGTH FIELD
         BNP   NOPARM                   ALL DONE - NO CHARS. LEFT
         B     PARMCK
NODLIM   DS    0H
         CLI   0(R5),C'/'               END OF PARMS?
         BE    PARMEND
         CLC   0(6,R5),=C'MAXCC='       OVERRIDE CONDITION CODE?
         BE    SETCC
         CLC   0(4,R5),=C'PGM='         GIVE PGM NAME?
         BE    SETPGM
BADPARM  DS    0H
         WTO   MF=(E,TSCC001A)
         B     PASSPARM                 PASS PARM UNCHANGED
SETCC    DS    0H
         LA    R5,6(R5)                 BUMP PAST KEYWORD
         SH    R4,=H'6'                 AND DEX COUNT FIELD
         BNP   BADPARM                  IF NONE LEFT, ERROR
         LR    R14,R4                   GET LENGTH FIELD,
         BCTR  R14,0                    DEX FOR TRT, AND
         SR    R2,R2                    INITIALIZE FOR TRT
         EX    R14,CCTRT                FIND END OF CC FIELD.
         BC    B'0110',CCEND            FOUND END OF CC FIELD.
         LA    R1,0(R4,R5)              (POINT PAST END).
         IC    R2,=X'02'                (END OF STRING)
CCEND    DS    0H
         CH    R2,=H'2'                 TEST DELIMITER
         BH    BADPARM
         SR    R1,R5                    GET STRING LENGTH
         LR    R2,R1                    SAVE FOR LATER USE
         CH    R2,=H'5'                 TOO LONG?
         BNL   BADPARM
         BCTR  R1,0                     DEX FOR TRT
         EX    R1,CCPACK                CONVERT TO PACKED
         CVB   R1,PACKWORK              THEN TO BINARY.
         ST    R1,CONDCODE              SAVE FOR LATER USE.
         AR    R5,R2                    BUMP PAST COND CODE
         SR    R4,R2                    SET NEW LENGTH
         BNP   NOPARM                   IF NO CHARS. LEFT, NO PARM.
         B     PARMCK                   TRY NEXT KEYWORD
SETPGM   DS    0H
         LA    R5,4(R5)                 BUMP PAST KEYWORD
         SH    R4,=H'4'                 DEX LENGTH FIELD
         BNP   BADPARM
         LR    R14,R4                   SAVE FOR TRT
         BCTR  R14,0                    DEX FOR TRT
         SR    R2,R2                    ZERO FOR TRT
         EX    R14,PGMTRT               FIND END OF PGMNAME
         BC    B'0110',PGMEND           FOUND END OF CC FIELD.
         LA    R1,0(R4,R5)              (POINT PAST END).
         IC    R2,=X'02'                (END OF STRING)
PGMEND   DS    0H
         CH    R2,=H'2'                 CHECK DELIMITER
         BH    BADPARM
         SR    R1,R5                    GET STRING LENGTH
         LR    R2,R1                    SAVE FOR LATER USE
         CH    R2,=H'8'                 PARM TOO LONG?
         BH    BADPARM
         BCTR  R1,0                     DEX FOR TRT
         EX    R1,PGMMVC                CONVERT TO PACKED
         AR    R5,R2                    BUMP PAST PGM NAME
         SR    R4,R2                    SET NEW LENGTH
         BNP   NOPARM                   IF NO CHARS. LEFT, NO PARM.
         B     PARMCK                   TRY NEXT KEYWORD
         EJECT
PARMEND  DS    0H
         LA    R5,1(R5)                 BUMP PAST "/"
         SH    R4,=H'1'                 DEX CHARACTER COUNT.
         BNP   NOPARM
         LR    R1,R4
         BCTR  R1,0                     DEX FOR MVC
         EX    R1,PARMMVC
         STH   R4,PARMLEN
         LA    R1,PARMLEN               POINT TO LENGTH FIELD
         ST    R1,PARMLST
         OI    PARMLST,X'80'            FLAG AS LAST(ONLY) PARM
         LA    R1,PARMLST               POINT TO PARM LIST
         B     LINKPGM
PASSPARM DS    0H
         MVC   PARMLST,0(R11)
         LA    R1,PARMLST               POINT TO PARM LIST
         B     LINKPGM
NOPARM   DS    0H
         LA    R1,NULLPARM              INDICATE NO PARM LIST
LINKPGM  DS    0H
         CLI   PGMNAME,C' '             PROGRAM SPECIFIED?
         BE    NOPGM
         LA    R0,PGMNAME               GET PARM ADDRESS ...
         STM   R0,R1,LINKPRM            AND SAVE IN PARM LIST
         OI    LINKPRM+4,X'80'          FLAG END OF LIST
         LA    R1,LINKPRM
         L     R15,=V(ATTCHATH)         CALL ATTACH-REAUTH ROUTINE
         BALR  R14,R15
         LTR   R0,R0                    WAS THE ATTACH SUCCESFUL?
         BNZ   ATTFAIL
         C     R15,CONDCODE             GOOD CONDITION CODE?
         BNH   RETURN
ABENDIT  DS    0H
         LR    R1,R15                   GET CONDITION CODE
         ABEND (1),,,USER
ATTFAIL  DS    0H
         LR    R2,R0                    SAVE REASON CODE
         WTO   MF=(E,TSCC024U)
         LR    R0,R2                    RESTORE REASON CODE
         ABEND 24,DUMP,,,USER
NOPGM    DS    0H
         WTO   MF=(E,TSCC020U)
         ABEND 20,DUMP,,USER
RETURN   DS    0H
         RELEASE DSALEN,RC=(R15),LI=L
         EJECT
DEFAULTS DS    0D
         DC    F'4'                     DEFAULT CONDITION CODE
         DC    CL8' '                   DEFAULT PROGRAM NAME
DFTLEN   EQU   *-DEFAULTS
TSCC001A WTO   'TSCC001A  ERRONEOUS PARM OPTION HAS BEEN IGNORED',     X
               ROUTCDE=(11),MF=L
TSCC020U WTO   'TSCC020U  NO PROGRAM NAME SPECIFIED',                  X
               ROUTCDE=(11),MF=L
TSCC024U WTO   'TSCC021U  ATTACH OF USER PROGRAM FAILED',              X
               ROUTCDE=(11),MF=L
DSALEN   DS    0F
         DC    AL1(DSASP),AL3(DSASIZE)  SUBPOOL AND SIZE FOR GETMAIN
PARMMVC  MVC   PARMCHRS(*-*),0(R5)      SKELETON FOR EX
CCTRT    TRT   0(*-*,R5),CCTRTAB        SKELETON FOR EX
CCPACK   PACK  PACKWORK,0(*-*,R5)
PGMTRT   TRT   0(*-*,R5),PGMTRTAB       SKELETON FOR EX
PGMMVC   MVC   PGMNAME(*-*),0(R5)       SKELETON FOR EX
CCTRTAB  DS    0XL256                   DIGIT TRT TABLE
         DC    256XL1'03'
         ORG   CCTRTAB+C' '
         DC    XL1'01'
         ORG   CCTRTAB+C','
         DC    XL1'01'
         ORG   CCTRTAB+C'/'
         DC    XL1'01'
         ORG   CCTRTAB+C'0'
         DC    10XL1'00'
         ORG
PGMTRTAB DS    0XL256                   PGM NAME TRT TABLE
         DC    256XL1'00'
         ORG   PGMTRTAB+X'00'
         DC    63XL1'03'
         ORG   PGMTRTAB+C' '
         DC    XL1'01'
         ORG   PGMTRTAB+C','
         DC    XL1'01'
         ORG   PGMTRTAB+C'/'
         DC    XL1'01'
         ORG
         CNOP  2,4
ZEROPRM  DC    H'0'
NULLPARM DC    AL1(X'80'),AL3(ZEROPRM)
         LTORG
         EQUREGS
         EJECT
RCCKDSA  DSECT
REGSAVE  DS    18F                      OS-STANDARD REGISTER SAVE AREA
INITSTG  DS    0D
CONDCODE DC    F'4'                     DEFAULT CONDITION CODE
PGMNAME  DC    CL8' '                   DEFAULT PROGRAM NAME
LINKPRM  DS    2A
PARMLST  DS    A                        PARM LIST ADDRESS
PACKWORK DS    D
         CNOP  2,4
PARMLEN  DS    H
PARMCHRS DS    CL100
         DS    0D
INITLEN  EQU   *-INITSTG
DSASIZE  EQU   *-RCCKDSA
         END   HCCRCCK
