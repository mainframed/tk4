         MACRO
&NAME    RELEASE &L,&RC=0,&LI=LA,&WORKREG=14,&FREE=YES,        05/08/77W
               &SP=0,&RI=                                      01/06/78
.*
.*       RELEASE UNDOES WHAT CONNECT DID.  I.E., IT FREEMAINS
.*       ANY SAVEAREA AFTER DISCONNECTING IT, RESTORES REGISTERS,
.*       AND RETURNS.
.*
.*  CHANGES:                                                   05/08/77
.*                                                             05/08/77
.*       05/08/77 - ADD &WORKREG,&FREE,AND CHECK &RC           05/08/77
.*       05/25/77 - FIXUP CODE FROM 05/08/77 TO SET RC         05/25/77
.*       01/06/78 - ADD &SP (SUBPOOL SUPPORT)                  01/06/78
.*       12/08/86 - CHANGED TO SUPPORT XA (BSM RETURN)         12/08/86
.*                                                             05/08/77
         GBLC  &SYSSPLV
         LCLC  &RIN
         LCLB  &CODE                                           05/08/77
         SPLEVEL TEST
&RIN     SETC  '&RI'
         AIF   ('&RI' EQ 'BR' OR '&RI' EQ 'BSM' OR '&RI' EQ '').RIOK
         MNOTE 8,'INVALID OPTION, RI=&RI, RI=,  (NULL) ASSUMED '
         AGO   .NULLRI
.RIOK    AIF   ('&RI' NE '').SETCODE
.NULLRI  ANOP
&RIN     SETC  'BSM'
         AIF   ('&SYSSPLV' NE '1').SETCODE
&RIN     SETC  'BR'
.SETCODE ANOP
&CODE    SETB  ('&RC' EQ '(15)' OR '&RC' EQ '(R15)')           05/08/77
&CODE    SETB  (&CODE OR '&RC' EQ '(0)' OR '&RC' EQ '(R0)')    05/08/77
&CODE    SETB  (&CODE OR '&RC' EQ '(1)' OR '&RC' EQ '(R1)')    05/08/77
         AIF   ('&FREE' EQ 'NO').XNAME                         05/08/77
         AIF   (T'&L EQ 'O').XNOFREE
.XNAME   ANOP                                                  05/08/77
&NAME    LR    1,13
         L     13,4(,13)
         XC    8(4,13),8(13)
         AIF   ('&L' EQ 'NOFREE' OR '&FREE' EQ 'NO').XRC       05/08/77
         AIF   (NOT &CODE).XLI                                 05/08/77
         LR    &WORKREG,&RC(1)          SAVE RETURN CODE       05/08/77
.XLI     &LI   0,&L                     WORKAREA LENGTH
         AIF   ('&SYSSPLV' EQ '1').NOTXA1
         MNOTE *,'       FREEMAIN RU,LV=(0),A=(1),SP=&SP  '
         FREEMAIN RU,LV=(0),A=(1),SP=&SP
         AGO   .XAJOIN1
.NOTXA1  ANOP
         ICM   R0,8,=AL1(&SP)
         MNOTE *,'       FREEMAIN R,LV=(0),A=(1)          '
         FREEMAIN R,LV=(0),A=(1)
.XAJOIN1 ANOP
         AIF   (NOT &CODE).XRC                                 05/25/77
         LR    15,&WORKREG              SET THE RETURN CODE    05/08/77
         AGO   .XRC
.XNOFREE AIF   (T'&NAME EQ 'O').XRC
&NAME    DS    0H
.XRC     AIF   (T'&RC NE 'O').RCHK
         LM    14,12,12(13)
         AGO   .CKRETN
.RCHK    AIF   ('&RC' EQ '0').RLA
         AIF   ('&RC'(1,1) NE '(').RLA
         AIF   ('&RC(1)' EQ '15' OR '&RC(1)' EQ 'R15').RETURN  05/08/77
         AIF   (&CODE).RETURN                                  06/01/77
         LR    15,&RC(1)
         AGO   .RETURN
.RLA     LA    15,&RC
.RETURN  L     14,12(,13)
         LM    0,12,20(13)
.CKRETN  AIF   ('&RIN' EQ 'BR').BRRETN
.BSMRETN ANOP
         BSM   0,14
         MEXIT
.BRRETN  ANOP
         BR    14
         MEND
