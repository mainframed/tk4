INITNCT  TITLE 'INITNCT - PROGRAM TO INITIALIZE NCT AFTER IPL'
INITNCT  CSECT
***********************************************************************
* FUNCTION - OBTAINS COMMON STORAGE FOR THE NETWORK CONTROL TABLE     *
*     THEN INITIALIZES IT.  THE NCT IS A USER CVT (ADDRESSED FROM     *
*     THE CVTUSER FIELD IN THE MVS CVT) USED TO ANCHOR VARIOUS        *
*     CONTROL BLOCKS/MODULES.                                         *
*                                                                     *
*     IF AN NCT ALREADY EXISTS, THE OPERATOR WILL BE PROMPTED TO      *
*     "R" (RELOAD) OR "B" (BYPASS INITIALIZATION).  IF THE REPLY      *
*     IS "R", ALL LOAD MODULE ADDRESSES IN THE NCT WILL BE REFRESHED. *
*     NOTE THAT ALL MODULES ADDRESSED VIA THE NCT MUST RESIDE IN      *
*     COMMON STORAGE.                                                 *
*                                                                     *
*     ATTRIBUTES:  AC=1                                               *
*                                                                     *
*     FOR A LIST OF LOAD MODULES ADDRESSED FROM THE NCT, SEE          *
*     LABEL "LOADMODS".                                               *
*                                                                     *
*     MACROS:  MODESET, SETLOCK, GETMAIN, FREEMAIN, SPLEVEL           *
*                                                                     *
*     WRITTEN ON 05/15/86 BY BILL WARRINER, UMC HOSPITAL AND CLINICS. *
*                                                                     *
***********************************************************************
INITNCT  CSECT
INITNCT  AMODE 31
INITNCT  RMODE 24
         GBLC  &SYSSPLV
         SPLEVEL TEST
         USING INITNCT,R15
         B     PASTCONS
         DC    AL1(L'EPID)
EPID     DC    C'INITNCT &SYSDATE..&SYSTIME.  '
         DC    C'COPYRIGHT(C) 1986 BY THE CURATORS OF THE UNIVERSITY OFX
                MISSOURI'
PASTCONS DS    0H
         DROP  R15
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R8,R15
         USING INITNCT,R8          SETUP BASE REGISTER
         ST    R1,PARMADDR         SAVE PARAMETER ADDRESS
         LA    R1,REGSAVE
         XC    MAXCC,MAXCC
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         L     R7,16
         USING CVT,R7
         L     R6,CVTUSER
         C     R6,=A(0)            IS THERE ALREADY AN NCT?
         BNE   ALLOC
         MODESET  KEY=ZERO,MODE=SUP
         AIF   ('&SYSSPLV' EQ '1').NOTXA1
SET1     SETLOCK  OBTAIN,TYPE=CPU,REGS=USE,                            X
               RELATED=FREE1
         GETMAIN  RU,LV=NCTLEN,SP=NCTSP,KEY=0,BRANCH=(YES,GLOBAL),     X
               BNDRY=PAGE,LOC=BELOW
         LA    R9,0(,R1)
FREE1    SETLOCK RELEASE,TYPE=CPU,REGS=USE,RELATED=SET1
         AGO   .XA1DONE
.NOTXA1  ANOP
SET1     SETLOCK  OBTAIN,TYPE=SALLOC,REGS=USE,MODE=UNCOND,             X
               RELATED=FREE1
         GETMAIN  RU,LV=NCTLEN,SP=NCTSP,KEY=0,BRANCH=(YES,GLOBAL),     X
               BNDRY=PAGE
         LA    R9,0(,R1)
FREE1    SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED=SET1
.XA1DONE USING NCT,R9
         XC    0(NCTLEN,R9),0(R9)
         MVC   NCTSPLN,TSTSPLN       COPY IN SUBPOOL,LENGTH
         MVC   NCTIDBLK,=CL4'NCT'
         MVC   NCTID,=CL4'HCC'
         TIME  DEC
         ST    R1,NCTIPLDT
         ST    R0,NCTIPLTM
         ICM   R1,X'F',CVTSMCA
         BZ    SMFDONE
         USING SMCABASE,R1
         MVC   NCTCPUID,SMCASID
         DROP  R1
SMFDONE  DS    0H
         BAL   R4,LOADNCT
NCTDONE  DS    0H
         CS    R6,R9,CVTUSER      SAVE NCT ADDRESS IN CVTUSER FIELD
         BC    SWAPFAIL,ALLOC2
         MODESET  KEY=NZERO,MODE=PROB
         WTO   'NCT000I  NCT INITIALIZATION COMPLETED',                X
               ROUTCDE=(2),DESC=(4)
         SR    R15,R15
         B     RETURN
ALLOC2   DS    0H
         BAL   R4,ERASENCT        ERASE AND DELETE LOAD MODULE ADDRXS
         MODESET  KEY=NZERO,MODE=PROB
         B     NOALLOC
REALLOC  DS    0H
         LR    R9,R6
         CLC   NCTSPLN,TSTSPLN
         BNE   NEEDIPL
         MODESET  KEY=ZERO,MODE=SUP
         BAL   R4,LOADNCT         RELOAD NCT LOAD MODULES
         MODESET  KEY=NZERO,MODE=PROB
         SR    R15,R15
         B     RETURN
NEEDIPL  DS    0H
         WTO   'NCT009E  IPL REQUIRED TO REFORMAT NCT',                X
               ROUTCDE=(2),DESC=(4)
         B     NOALLOC
ALLOC    DS    0H
         XC    WTORECB,WTORECB
         WTOR  'NCT002A  NCT FOUND, REPLY ''R'' TO RELOAD OR ''B'' TO BX
               YPASS INITIALIZATION',WTOREPLY,1,WTORECB,ROUTCDE=(1)
         WAIT  ECB=WTORECB,LONG=YES
         CLI   WTOREPLY,C'R'
         BE    REALLOC
         CLI   WTOREPLY,C'B'
         BNE   ALLOC
NOALLOC  DS    0H
         WTO   'NCT001E  NCT INITIALIZATION ERROR, NCT ALREADY PRESENT'X
               ,ROUTCDE=(2),DESC=(4)
         MVC   MAXCC,=H'8'
RETURN   DS    0H
         LH    R15,MAXCC
         L     R13,4(R13)
         XC    8(4,R13),8(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
         EJECT
LOADNCT  DS    0H
         ST    R4,RETNSUB1
         LA    R4,LOADMODS        GET LOAD MODULE LIST
LOADLOOP DS    0H
         CLI   0(R4),X'FF'        END OF LIST?
         BE    LOADDONE
         LOAD  EPLOC=0(R4),GLOBAL=YES,ERRET=NOLOAD,SF=(E,LOADPRM)
LOADOK   DS    0H
         SR    R1,R1
         ICM   R1,B'0111',9(R4)   GET OFFSET IN NCT
         ST    R0,NCT(R1)         SAVE ENTRY POINT ADDRESS
         MVC   WTOLOAD+20(8),0(R4)
         WTO   MF=(E,WTOLOAD)
         B     LOADNEXT
NOLOAD   DS    0H
         SR    R1,R1
         ICM   R1,B'0111',9(R4)
         LA    R1,NCT(R1)
         XC    0(4,R1),0(R1)
         MVC   NCT003E+20(8),0(R4)
         WTO   MF=(E,NCT003E)
LOADNEXT DS    0H
         LA    R4,LMODELNG(R4)    POINT TO NEXT ENTRY
         B     LOADLOOP           AND GO LOAD IT.
LOADDONE DS    0H                 ALL MODULES LOADED
         XC    NCTSMASK,NCTSMASK  INITIALIZE SYSTEM MASK ...
         ICM   R4,X'F',PARMADDR   PICKUP PARAMETER ...
         BZ    TSODONE
         L     R4,0(,R4)          GET ADDRESS ...
         LH    R1,0(R4)           GET LENGTH
         CH    R1,=H'0'           IS THERE A PARM?
         BNH   TSODONE
         CH    R1,=H'3'           TOO LONG?
         BH    BADPARM
         MVC   SMASKCHR,=CL3'000'
         LA    R15,SMASKCHR+2
         BCTR  R1,0               DEX FOR MVC
         SR    R15,R1             BACKUP TO PLACE
         EX    R1,PARMCOPY        COPY PARM INTO TSOPREFIX
         TRT   SMASKCHR,NUMTAB    IS IT NUMERIC?
         BNZ   BADPARM
         PACK  SMASKPAK,SMASKCHR
         CP    SMASKPAK,=P'255'   IS IT IN RANGE?
         BH    BADPARM
         CVB   R1,SMASKPAK
         STC   R1,NCTSMASK
         UNPK  NCT004I+32(3),NCTSMASK(2)
         TR    NCT004I+32(2),HEXLATE
         MVI   NCT004I+34,C' '
         WTO   MF=(E,NCT004I)
         B     TSODONE
BADPARM  DS    0H
         MVC   NCT005E+31(8),=CL8'        '
         LH    R1,0(R4)           GET THE PARAMETER LENGTH
         CH    R1,=H'0'
         BNH   ISSU005E
         CH    R1,=H'8'
         BNH   *+8
         LH    R1,=H'8'
         BCTR  R1,0
         EX    R1,PRMECOPY
ISSU005E DS    0H
         WTO   MF=(E,NCT005E)
         CLC   MAXCC,=H'8'
         BH    TSODONE
         MVC   MAXCC,=H'8'
TSODONE  DS    0H
         CLI   NCTSMASK,0
         BNE   MASKOK
         WTO   MF=(E,NCT006W)
         CLC   MAXCC,=H'4'
         BH    MASKOK
         MVC   MAXCC,=H'4'
MASKOK   DS    0H
         L     R4,RETNSUB1
         BR    R4
PARMCOPY MVC   0(*-*,R15),2(R4)
PRMECOPY MVC   NCT005E+31(*-*),2(R4)
         EJECT
ERASENCT DS    0H
         STM   R4,R5,RETNSUB1
         LA    R4,LOADMODS
ERASLOOP DS    0H
         CLI   0(R4),X'FF'        END OF LIST?
         BE    ERASDONE
         SR    R5,R5
         ICM   R5,B'0111',9(R4)   GET NCT OFFSET OF ADDRESS
         LA    R5,NCT(R5)         POINT TO ADDRESS FIELD
         CLC   =A(0),0(R5)        WAS IT LOADED?
         BE    ERASNEXT           NO - SKIP THIS ENTRY.
         XC    0(4,R5),0(R5)      ZERO OUT THE ADDRESS
ERASNEXT DS    0H
         LA    R4,LMODELNG(R4)
         B     ERASLOOP
ERASDONE DS    0H
         AIF   ('&SYSSPLV' EQ '1').NOTXA2
ERASSET  SETLOCK  OBTAIN,TYPE=CPU,REGS=USE,                            X
               RELATED=ERASFREE
         LA    R1,NCT
         FREEMAIN  RU,LV=NCTLEN,SP=NCTSP,KEY=0,BRANCH=(YES,GLOBAL),    X
               A=(1)
ERASFREE SETLOCK RELEASE,TYPE=CPU,REGS=USE,RELATED=ERASSET
         AGO   .XA2DONE
.NOTXA2  ANOP
ERASSET  SETLOCK  OBTAIN,TYPE=SALLOC,REGS=USE,MODE=UNCOND,             X
               RELATED=ERASFREE
         LA    R1,NCT
         FREEMAIN  RU,LV=NCTLEN,SP=NCTSP,KEY=0,BRANCH=(YES,GLOBAL),    X
               A=(1)
ERASFREE SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED=ERASSET
.XA2DONE LM    R4,R5,RETNSUB1
         BR    R4
         DROP  R8
SWAPFAIL EQU   B'0111'
LOADMODS DS    0F
         DC    CL8'HCCDADSM'
         DC    AL1(0)          FLAGS
         DC    AL3(NCTDADSM-NCT)
LMODELNG EQU   *-LOADMODS
*
         DC    XL1'FF'         FLAG END-OF-LIST
WTOLOAD  WTO   'NCT000I  MODULE XXXXXXXX NOW REPLACED IN NCT',         X
               ROUTCDE=(2),DESC=(4),MF=L
NCT003E  WTO   'NCT003E  MODULE XXXXXXXX NOT IN NCT - MODULE NOT FOUND X
                OR NOT AUTHORIZED',ROUTCDE=(1),DESC=(3),MF=L
NCT004I  WTO   'NCT004I  SYSTEM MASK SET TO XX ',                      X
               ROUTCDE=(2),DESC=(4),MF=L
NCT005E  WTO   'NCT005E  INVALID PARAMETER,         , IGNORED.',       X
               ROUTCDE=(1),DESC=(3),MF=L
NCT006W  WTO   'NCT006W  SYSTEM MASK IS 0, TSO LOGONS WILL NOT BE ALLOWX
               ED UNTIL IT IS RESET',ROUTCDE=(1),DESC=(3),MF=L
LOADPRM  LOAD  EPLOC=0,GLOBAL=YES,ERRET=NOLOAD,SF=L
         LTORG
TSTSPLN  DC    AL1(NCTSP),AL3(NCTLEN)
         DS    0D
REGSAVE  DS    18F
PARMADDR DS    A
RETNSUB1 DS    2F
WTORECB  DC    A(0)
SMASKPAK DC    PL8'0'
MAXCC    DC    H'0'
WTOREPLY DS    CL1
SMASKCHR DC    CL3'000'
NUMTAB   DC    256XL1'01'
         ORG   NUMTAB+C'0'
         DC    10XL1'00'
         ORG
HEXLATE  EQU   *-C'0'
         DC    C'0123456789ABCDEF'
         EQUREGS
         IHAPSA  DSECT=YES
         IHAWSAVT  DSECT=YES,CLASS=GLOBAL
NCT      NCT
         CVT   DSECT=YES
         IEESMCA
         END   INITNCT
