IPLD     TITLE 'IPLDATE - PROGRAM TO EXTRACT AND DISPLAY LAST IPL DATE'
         PUNCH '  ALIAS  IPLTIME  '
IPLDATE  CSECT
***********************************************************************
* IPLDATE - TSO CP TO EXTRACT LAST IPL DATE/TIME FROM THE NCT AND     *
*     DISPLAY IT TO THE USERS TERMINAL.                               *
*                                                                     *
*  MACROS - CONNECT, RELEASE, NCT, CVT                                *
*                                                                     *
*  ATTRIBUTES - RENT, REUS, REFR.                                     *
*                                                                     *
*  WRITTEN ON 06/03/87 BY BILL WARRINER, UMC HOSPITAL AND CLINICS     *
*                                                                     *
***********************************************************************
         EJECT
         CONNECT  WORKSIZE,WORK,BASE=R12,CLEAR=YES,EQUREGS=YES,        X
               LOC=BELOW,AMODE=24,RMODE=24,    XA PARMS                X
               COPYRT=1987
         L     R11,16                  GET CVT ADDRESS
         USING CVT,R11
         L     R10,CVTUSER             GET NCT ADDRESS
         LTR   R10,R10                 IS THERE AN NCT?
         BZ    NONCT
         USING NCT,R10
         MVI   DECTIME+4,X'0F'         INSERT SIGN INTO TIME
         MVO   DECTIME,NCTIPLTM
         MVC   DECDAY,NCTIPLDT+2       GET DECIMAL DAYS INTO YEAR
         MVC   MONTHTAB,MONTHDMY       COPY IN STATIC MONTHS TABLE
         MVI   DECYEAR+1,X'0F'         INSERT SIGN INTO YEAR
         MVO   DECYEAR,NCTIPLDT+1(1)
         ZAP   TESTYEAR,DECYEAR        COPY FOR DIVISION
         DP    TESTYEAR,=PL1'4'        IS IT LEAP YEAR?
         CP    TESTYEAR+2(1),=P'0'     TEST REMAINDER
         BNE   NOTLEAP
         MVC   MONTHTAB+2(2),=PL2'29'  YES ... SET FEB. DAYS
NOTLEAP  DS    0H
         LA    R1,MONTHTAB
         ZAP   DECMONTH,=P'1'          INITIAL MONTH
MNTHLOOP DS    0H
         CP    DECDAY,0(2,R1)          PAST END OF THIS MONTH?
         BNH   MNTHEND
         SP    DECDAY,0(2,R1)
         AP    DECMONTH,=P'1'
         LA    R1,2(,R1)
         B     MNTHLOOP
MNTHEND  DS    0H
         MVC   TPUTMSG,TPUTDMY
         MVC   TPUTSID,NCTCPUID
         MVC   EDITFLDS,EDITSPEC
         ED    EDITJDAT,NCTIPLDT       CONVERT JULIAN DATE TO DECIMAL
         ED    EDITGDMM,DECMONTH       ALSO GREGORIAN DATE: MM
         ED    EDITGDDD,DECDAY         DD ...
         ED    EDITGDYY,DECYEAR        YY
         ED    EDITTIME,DECTIME
         MVC   TPUTJDAT,EDITJDAT+3     COPY INTO MESSAGE
         MVC   TPUTGDMM,EDITGDMM+1
         MVC   TPUTGDDD,EDITGDDD+1
         MVC   TPUTGDYY,EDITGDYY+1
         MVC   TPUTTIME,EDITTIME+2
         TPUT  TPUTMSG,TPUTLEN
         B     RETURN
NONCT    DS    0H
         TPUT  BADMSG,L'BADMSG
RETURN   DS    0H
         RELEASE  WORKSIZE,WORK,RC=0
         EJECT
BADMSG   DC    C'NCT1002E  NCT NOT FOUND, IPL DATE NOT AVAILABLE'
TPUTDMY  DC    C'NCT1001I  SYSTEM '
TPOFSID  EQU   *-TPUTDMY
         DC    C'XXXX LAST IPL WAS ON '
TPOFGDMM EQU   *-TPUTDMY
         DC    C'MM/'
TPOFGDDD EQU   *-TPUTDMY
         DC    C'DD/'
TPOFGDYY EQU   *-TPUTDMY
         DC    C'YY ('
TPOFJDAT EQU   *-TPUTDMY
         DC    C'YY.DDD) AT '
TPOFTIME EQU   *-TPUTDMY
         DC    C'HH:MM:SS.TT '
TPUTLEN  EQU   *-TPUTDMY
EDITSPEC DC    X'212020'               YEAR
         DC    X'212020'               MONTH
         DC    X'212020'               DAY
         DC    X'40202120204B202020'   JULIAN
         DC    X'402021207A20207A20204B2020'   TIME OF DAY
EDITLEN  EQU   *-EDITSPEC
MONTHDMY DC    PL2'31'                 JANUARY
         DC    PL2'28'                 FEBRUARY
         DC    PL2'31'                 MARCH
         DC    PL2'30'                 APRIL
         DC    PL2'31'                 MAY
         DC    PL2'30'                 JUNE
         DC    PL2'31'                 JULY
         DC    PL2'31'                 AUGUST
         DC    PL2'30'                 SEPTEMBER
         DC    PL2'31'                 OCTOBER
         DC    PL2'30'                 NOVEMBER
         DC    PL2'31'                 DECEMBER
MONTHLEN EQU   *-MONTHDMY
         LTORG
         EJECT
WORK     DSECT
         DS    18F
MONTHTAB DS    CL(MONTHLEN)
DECYEAR  DS    PL2
DECTIME  DS    PL5
DECDAY   DS    PL2
DECMONTH DS    PL2
TESTYEAR DS    PL3
EDITFLDS DS    0CL(EDITLEN)
EDITGDYY DC    X'212020'               YEAR
EDITGDMM DC    X'212020'               MONTH
EDITGDDD DC    X'212020'               DAY
EDITJDAT DC    X'40202120204B202020'   JULIAN
EDITTIME DC    X'402021207A20207A20204B2020'   TIME OF DAY
TPUTMSG  DS    CL(TPUTLEN)
TPUTSID  EQU   TPUTMSG+TPOFSID,4,C'C'
TPUTGDMM EQU   TPUTMSG+TPOFGDMM,2,C'C'
TPUTGDDD EQU   TPUTMSG+TPOFGDDD,2,C'C'
TPUTGDYY EQU   TPUTMSG+TPOFGDYY,2,C'C'
TPUTJDAT EQU   TPUTMSG+TPOFJDAT,6,C'C'
TPUTTIME EQU   TPUTMSG+TPOFTIME,11,C'C'
         DS    0D
WORKSIZE EQU   *-WORK
         EJECT
         CVT   DSECT=YES
         SPACE 2
NCT      NCT
         END   IPLDATE
