***********************************************************************
*    IN THIS MODULE ARE THE EXITS FOR JES2;
*       EXIT253 - HCC-INSTALLED CHECKPOINT LOCK EXIT
***********************************************************************
*
***********************************************************************
         COPY  $HASPGBL
         MACRO
         $USERCBS
SMFUSER  DSECT ,                    FORMAT OF SMF USER RECORD
SMFURDW  DC    AL2(SMFRECLN),AL2(0) RDW (LL,BB)
SMFUSYS  DS    AL1(SMFSVS2+SMFSXA)  SET BY SVC83???
SMFSXA   EQU   X'04'                XA SYSTEM
SMFSVS2  EQU   X'02'                VS/2 SYSTEM
SMFSVS1  EQU   X'01'                VS/1 SYSTEM
SMFUTYPE DC    AL1(SMFRECTP)        SMF RECORD TYPE
SMFUDTTM DS    0XL8                 DATE,TIME
SMFUTIME DS    XL4                  BINARY 1/100 SECS. SINCE MIDNIGHT
SMFUDATE DS    PL4                  PACKED JULIAN 00YYDDDF
SMFUSMFN DS    CL4                  SMF SYSID
SMFUJESN DS    CL4                  JES2 SYSID
SMFUENT  DS    CL4
SMFRECLN EQU   *-SMFUSER            LENGTH OF SMF RECORD
SMFRECTP EQU   253                  SMF RECORD TYPE
         DS    0D                   ALIGN TO DOUBLEWORD
WORKLEN  EQU   *-SMFUSER
         PRINT NOGEN
         IEESMCA
         MEND
HJUX2530 $MODULE $BUFFER,$CAT,$DCT,$HASPEQU,$HCT,$JCT,$JOE,            X
               $JQE,$MIT,$PCE,$PDDB,$PPPWORK,$NHD,$NIT,                X
               $RDRWORK,RPL,CVT,$USERCBS,                              X
               ENVIRON=JES2
HJUX2530 AMODE 31
HJUX2530 RMODE 24
         TITLE 'CHECKPOINT LOCK ACTIVITY - EXIT 253'
***********************************************************************
*    EXIT253 - CKPT LOCK (HCC INSTALLED)
*    R0  - 0=LOCK REQUESTED, 4=LOCK OBTAINED, 8=LOCK RELEASED.
*    R1  - N/A
*    R2  - N/A
*    R3  - N/A
*    R4  - N/A
*    R5  - N/A
*    R6  - N/A
*    R7  - N/A
*    R8  - N/A
*    R10 - N/A
*    R11 - HCT
*    R12 - CODE BASE
*    R13 - PCE
*
*   THIS EXIT DOES THE FOLLOWING;
*       WRITE OUT AN SMF RECORD (TYPE=253) FOR JES2 CHECKPOINT
*       RESERVE/RELEASE ACTIVITY.
*
*   NOTE: THIS EXIT USES THE PCEUSER0 FIELD OF THE CHECKPOINT
*   PROCESSOR.
*
***********************************************************************
         EJECT
***********************************************************************
* EXIT253 ENTRY POINT
***********************************************************************
         SPACE 3
EXIT253A $ENTRY BASE=R12
         $SAVE                      SAVE CALLER'S REGISTERS
         USING HCT,R11              SET UP ADDRESSABILITY TO HCT
         USING PCE,R13
         LR    R12,R15
         LR    R2,R0                SAVE ENTRY CODE
         ICM   R5,15,PCEUSER0       SMF RECORD ADDRESS?
         BNZ   GOTMAIN
         GETMAIN  R,LV=WORKLEN      GET BUFFER FOR SMF RECORD
         LR    R5,R1                POINTER TO BUFFER
         ST    R1,PCEUSER0          SAVE FOR LATER CALLS
GOTMAIN  DS    0H
         USING SMFUSER,R5           POINT TO IT.
         XC    SMFUSER(WORKLEN),SMFUSER
         STCM  R2,X'F',SMFUENT      SAVE DATA (INITIAL R0)
         TIME  BIN
         STCM  R1,X'F',SMFUDATE     SAVE IMMEDIATE DATE
         STCM  R0,X'F',SMFUTIME     SAVE IMMEDIATE TIME
         LA    R0,SMFRECLN
         STH   R0,SMFURDW           SAVE LL OF RDW
         MVI   SMFUTYPE,SMFRECTP    COPY IN RECORD TYPE
         MVI   SMFUSYS,SMFSVS2+SMFSXA  SET "XA+VS2" SYSTEM
         MVC   SMFUJESN,$SID        COPY SYSID FOR JES2
         L     R1,16                GET CVT ADDRESS
         L     R1,CVTSMCA-CVT(,R1)  GET SMF CONTROL AREA ADDRESS
         MVC   SMFUSMFN,SMCASID-SMCABASE(R1)  COPY SMF SYSID
         SMFWTM (R5)                THEN WRITE OUT SMF RECORD
JX253LVE DS    0H
         XR    R15,R15              SET RETURN CODE=0
         $RETURN RC=(R15)
         EJECT
         LTORG ,
         TITLE 'COMPLETES MODULE INFO TABLE'
         $MODEND
         END
