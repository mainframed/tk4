IEFUJV   TITLE 'MVS READER EXIT TO PERFORM SYMBOLIC SUBSTITUTION  '
IEFUJV   CSECT
         GBLC  &SYSSPLV,&KWDCHAR
         SPLEVEL TEST              XA EXPANSION?
&KWDCHAR SETC  '?'                 STARTING CHAR. FOR EACH KEYWORD
**********************************************************************
* FUNCTION - PERFORMS SYMBOLIC SUBSTITUTION ON JCL PASSED TO         *
*    THE CONVERTER/INTERPRETER.  SUBSTITUTION IS DONE FROM VARIOUS   *
*    MVS CONTROL BLOCKS.  SEVERAL KEYWORDS STARTING WITH &KWDCHAR ARE*
*    SUBSTITUTED WITH VALUES SUCH AS DATE, DAY OF WEEK, SYSTEM ID,   *
*    VTAM SUBAREA, ETC.                                              *
*                                                                    *
* ATTRIBUTES:  RENT, REUS, REFR                                      *
* RUNS:  KEY 1, SUPERVISOR STATE, ENABLED.                           *
*                                                                    *
* MACROS: GETMAIN, FREEMAIN, KEYWORD, SPLEVEL, ESTAE                 *
*                                                                    *
* MAPPING MACROS:  IEFJMR, KWDTABLE, EQUREGS, IHASDWA                *
*                                                                    *
* THIS ROUTINE SHOULD ASSEMBLE IN BOTH XA AND NON-XA ENVIRONMENTS.   *
* IN A NON-XA ENVIRONMENT, HOWEVER, A MACRO "SPLEVEL" SHOULD BE      *
* PROVIDED WHICH WILL SIMPLY SET THE VALUE OF GLOBAL SETC SYMBOL     *
* &SYSSPLV TO '1'.  IF THIS IS NOT DONE, ASSEMBLY ERRORS WILL        *
* RESULT.                                                            *
*                                                                    *
* FOR A LIST OF KEYWORDS SUBSTITUTED, SEE LABEL "KEYWORDS"           *
*                                                                    *
* AUTHOR:  BILL WARRINER                                             *
*          UMC HOSPITAL AND CLINICS                                  *
*          SYSTEM PROGRAMMING SERVICES DC017.00                      *
*          1400 ROCK QUARRY ROAD                                     *
*          COLUMBIA, MO  65212                                       *
*          (314) 882-3773                                            *
*                                                                    *
* UPDATE HISTORY:                                                    *
*                                                                    *
*                                                                    *
**********************************************************************
         TITLE 'ENTRY POINT DEFINITION'
IEFUJV   CSECT
         USING IEFUJV,R15
         B     PASTCONS
         DC    AL1(L'EPID)
EPID     DC    C'IEFUJV_V2.0_&SYSDATE._&SYSTIME.'
         DC    C'COPYRIGHT(C) 1988 BY THE CURATORS OF THE UNIVERSITY OFX
                MISSOURI, A PUBLIC CORPORATION'
PASTCONS DS    0H
         STM   R14,R12,12(R13)
         DROP  R15
         LR    R12,R15
         USING IEFUJV,R12
         LR    R3,R1               SAVE PARAMETER REGISTER
         L     R1,8(R3)            GET FLAG BYTE ADDRESS
         CLI   0(R1),16            TEST FLAG BYTE
         BH    RETURN0             UNSUPPORTED FUNCTION.
         BE    KILLAREA            ALL DONE, JUST FREEMAIN AND GO AWAY.
         LA    R0,WORKSIZE
         AIF   ('&SYSSPLV' EQ '1').NOTXA0
IEFUJV   AMODE 31
IEFUJV   RMODE ANY
         GETMAIN  RU,LV=(0),SP=WRKSP,LOC=(ANY,ANY)
         AGO   .XA0JOIN
.NOTXA0  ICM   R0,8,=AL1(WRKSP)
         GETMAIN  R,LV=(0)
.XA0JOIN ST    R1,8(R13)           CHAIN ...
         ST    R13,4(R1)           ... CHAIN AREAS ...
         LR    R13,R1              ... TOGETHER.
         LA    R0,8(R13)           START OF AREA TO CLEAR
         LA    R1,WORKSIZE-8       LENGTH OF AREA TO CLEAR
         SR    R15,R15
         MVCL  R0,R14              CLEAR THE WORK AREA
         USING WORK,R13
         TITLE 'MVS READER EXIT TO PERFORM SYMBOLIC SUBSTITUTION  '
         MVC   WRKESTAW(LESTAEL),ESTAEL MOVE IN ESTAE PARM LIST
         LA    R0,RTRYRTN1         RETRY ROUTINE - NO SDWA
         LA    R1,RTRYRTN2         RETRY ROUTINE - SDWA
         STM   R0,R1,WRKESTAP
         STM   R12,R13,WRKESTAP+8
         ESTAE RECOVERY,CT,PARAM=WRKESTAP,MF=(E,WRKESTAW) SETUP ESTAE
         L     R2,0(R3)            GET JMR ADDRESS
         USING JMR,R2
         L     R4,4(R3)            GET JCL IMAGE ADDRESS
         L     R5,JMRUCOM          GET USER FIELD
         LTR   R5,R5               AREA ALREADY ALLOCATED?
         BNZ   AREAGOT
         LA    R0,SYSLEN
         AIF   ('&SYSSPLV' EQ '1').NOTXA1
         GETMAIN  RU,LV=(0),SP=SYSSP,LOC=(ANY,ANY)
         MVC   SYSKWDTB-SYSAREA(KWDFRLEN,R1),KEYWORDS
         MVC   SYSXA-SYSAREA(2,R1),=C'XA' FOR XA SYSTEM
         MVI   SYSSPLV-SYSAREA(R1),C'2'   FOR XA SYSTEM
         L     R15,16               CVT ADDRESS:
         TM    116(R15),X'80'       IS IT REALLY XA?
         BO    KWD010
         MVI   SYSSPLV-SYSAREA(R1),C'1'   NON XA SYSTEM RIGHT NOW...
KWD010   DS    0H
         AGO   .XA1JOIN
.NOTXA1  ICM   R0,8,=AL1(SYSSP)
         GETMAIN  R,LV=(0)
         MVC   SYSKWDTB-SYSAREA(KWDFRLEN,R1),KEYWORDS
         MVI   SYSSPLV-SYSAREA(R1),C'1'   FOR NON-XA SYSTEM
         MVI   KWDFLEN-KWDTABLE+SYSKWDTB-SYSAREA(R1),X'00'  NON-XA
.XA1JOIN LR    R5,R1
         ST    R5,JMRUCOM          SAVE AREA ADDRESS
         USING SYSAREA,R5
SMF000   DS    0H
*
*   FILL IN DATA CAPTURED FROM SMF
*
         MVC   SYSSMF,JMRCPUID     SAVE SMF SYSID
         MVC   SYSID,JMRCPUID+3    SAVE SYSTEM ID (1-BYTE)
         MVC   SYSTSO,SYSID
         MVC   SYSZONE,SYSID
         TR    SYSTSO,TSOXLATE     TRANSLATE TO TSO
         TR    SYSZONE,ZONEXLAT    TRANSLATE TO ZONE
         DROP  R2                  ALL DONE WITH JMR
DATE000  DS    0H
*
*  FILL IN DATE/TIME DATA
*
         TIME  DEC                 GET CURRENT DATE
         ST    R1,WRKPACK+4        SAVE IT
         MVC   SYSJDATE,=X'2120202020'
         ED    SYSJDATE,WRKPACK+5
         MVC   WRKYRTAB,YEARTAB    CREATE YEAR TABLE
         TM    WRKPACK+5,X'01'     LEAP YEAR?
         BO    DATE010             NO ...
         TM    WRKPACK+5,X'12'     REALLY A LEAP YEAR?
         BM    DATE010             NO ...
         MVI   WRKFEB,29           ELSE FEBRUARY HAS 29 DAYS.
DATE010  DS    0H
         AP    WRKPACK+4(4),=PL4'1900000'  CONVERT 0CYY TO YYYY
         MVC   SYSYEAR,=X'21202020'
         ED    SYSYEAR,WRKPACK+4   MOVE IN THE YEAR
         MVC   WRKPACK(6),=XL6'00' BLANK OUT THE YEAR
         ZAP   WRKDAYP,WRKPACK     SAVE PACKED DAY-OF-YEAR
         SR    R0,R0
         SR    R14,R14
         CVB   R1,WRKPACK          CONVERT DAYS TO BINARY
         LA    R15,WRKYRTAB-4      AND SCAN THE YEAR TABLE:
DATE020  DS    0H
         SLR   R1,R0               DECREMENT "DAYS REMAINING"
         LA    R15,4(,R15)         NEXT YEAR TABLE ENTRY (MONTH)
         LA    R14,1(,R14)         KEEP A MONTH COUNTER
         IC    R0,0(,R15)          DAYS IN CURRENT MONTH
         CLR   R0,R1               PAST END OF MONTH?
         BL    DATE020             NO - GO TRY NEXT ENTRY.
         CVD   R1,WRKPACK          CONVERT DAYS LEFT
         UNPK  SYSDD,WRKPACK+6(2)  ... UNPACK IT ...
         OI    SYSDD+1,X'F0'       ... SET THE SIGN.
         MVC   SYSMONTH,1(R15)     MOVE IN THE MONTH NAME
         CVD   R14,WRKPACK         CONVERT MONTH NUMBER
         UNPK  SYSMM,WRKPACK+6(2)  ... UNPACK IT ...
         OI    SYSMM+1,X'F0'       ... SET THE SIGN.
         PACK  WRKPACK,SYSYEAR     RE-PACK THE FULL YEAR.
         SP    WRKPACK,=PL8'1984'    SUBTRACT BASE YEAR
         DP    WRKPACK,=PL1'4'     NUMBER OF LEAP YEARS BETWEEN
         MP    WRKPACK(7),=P'1461' BASE NUMBER OF DAYS ...
         CP    WRKPACK+7(1),=PL1'0'  ANY YEARS LEFT OVER?
         BNH   DATE040
DATE030  DS    0H
         AP    WRKPACK(7),=PL7'365'  LEFTOVER DAYS ...
         SP    WRKPACK+7(1),=PL1'1'
         BP    DATE030
DATE040  DS    0H
         AP    WRKPACK(7),WRKDAYP   ADD TODAY IN JULIAN ...
         DP    WRKPACK(7),=PL1'7'    DIVIDE BY DAYS IN THE WEEK ...
         MVC   WRKPACK+7(1),WRKPACK+6
         MVC   WRKPACK(7),=XL7'00'
         CVB   R1,WRKPACK          CONVERT TO BINARY
         ALR   R1,R1
         ALR   R1,R1               MULT BY ENTRY LENGTH (4).
         LA    R1,DAYTABLE(R1)
         MVC   SYSWD,0(R1)         MOVE IN DAY NUMBER.
         MVC   SYSWKDY,1(R1)       MOVE IN DAY NAME.
VTAM000  DS    0H
*
*   FILL IN DATA FROM VTAM
*
         L     R1,1032             GET VTAM ATCVT ADDRESS,
         L     R1,1080(,R1)        GET VTAM SUBAREA ADDRESS,
         C     R1,=F'99'           SUBAREA OUT OF BOUNDS?
         BNH   VTAM010             NO - CONTINUE ON.
         MVC   SYSSUBA,=C'##'      ELSE INDICATE OVERFLOW
         B     VTAM020
VTAM010  DS    0H
         CVD   R1,WRKPACK          CONVERT TO DECIMAL ...
         UNPK  SYSSUBA-1(3),WRKPACK+6(2)   ... THEN TO CHARACTER
         OI    SYSSUBA+1,X'F0'             ... THEN SET THE SIGN.
VTAM020  DS    0H
         EJECT
*
*   WE HAVE OBTAINED AND INITIALIZED AN AREA WITH THE VALUES TO
*   REPLACE FOR KEYWORDS.  IT IS NOW TIME TO SCAN FOR KEYWORDS
*   AND IF ANY ARE FOUND, TO REPLACE THEM WITH THEIR VALUES.
*
AREAGOT  DS    0H
         LA    R0,KWDTAB
         ST    R0,SYSKWDTB+KWDNEXT-KWDTABLE
         LA    R0,WRK512           ADDRESS OF KWDREPL WORK AREA
         ST    R0,WRKPARMS
         LA    R14,70(R4)          END OF JCL IMAGE
         LA    R15,SYSKWDTB        KEYWORD TABLE ADDRESS
         LR    R0,R5               FIELD VALUE DSECT
         STM   R14,R0,WRKPARMS+8   SAVE FOR KWDREPL PARAMETERS
         LR    R9,R14              SAVE ENDING POSITION
         LA    R10,0(R4)           STARTING POSITION
SCANJCL  DS    0H
         LR    R6,R9               ENDING ADDRESS - STARING ADDRESS
         SR    R6,R10              = (LENGTH-1) FOR TRT.
         BM    RETURN1             IF NO CHARS LEFT, WE ARE DONE.
         EX    R6,SCANTRT          FIND KWDCHAR
         BC    B'1011',RETURN1     -THERE IS NO KWDCHAR
         LR    R10,R1              SAVE THE KWDCHAR POSITION
         ST    R1,WRKPARMS+4       ADDRESS OF THE KWDCHAR
         LA    R1,WRKPARMS         POINT TO PARM LIST
         L     R15,=V(KWDREPL)     AND CALL ...
         BALR  R14,R15             ... THE REPLACEMENT ROUTINE.
         C     R15,=F'4'           VALID KEYWORD REPLACEMENT?
         BL    SCANJCL             YES -GO RESCAN IT.
         LA    R10,1(,R10)         BUMP PAST NON-KEYWORD KWDCHAR
         B     SCANJCL
SCANTRT  TRT   0(*-*,R10),QUESTTRT
KILLAREA DS    0H
         L     R3,0(,R3)           GET JMR ADDRESS
         USING JMR,R3
         ICM   R1,15,JMRUCOM       IS THERE AN AREA?
         BZ    RETURN0             NO - SO DONT FREE IT.
         LA    R0,SYSLEN
         AIF   ('&SYSSPLV' EQ '1').NOTXA2
         FREEMAIN RU,LV=(0),A=(1),SP=SYSSP
         AGO   .XA2JOIN
.NOTXA2  ICM   R0,8,=AL1(SYSSP)
         FREEMAIN R,LV=(0),A=(1)
.XA2JOIN XC    JMRUCOM,JMRUCOM     CLEAR THE ADDRESS TOO
         B     RETURN0
         DROP  R3                  FINISHED WITH JMR
RETURN1  DS    0H                  FREE WORKAREA AND RETURN
         ESTAE 0                   CANCEL ESTAE EXIT
         B     RTRYRTN2
RTRYRTN1 DS    0H
         LM    R12,R13,8(R1)  LOAD REGS FOR ESTAE PARM LIST
RTRYRTN2 DS    0H
         LR    R1,R13              SAVE WORKAREA ADDRESS
         L     R13,4(R13)          CHAIN BACK SAVE AREA
         LA    R0,WORKSIZE         GET AREA LENGTH
         AIF   ('&SYSSPLV' EQ '1').NOTXA3
         FREEMAIN RU,LV=(0),A=(1),SP=WRKSP
RETURN0  DS    0H                  RETURN WITH NO WORKAREA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,R15             SET RC=0
         BSM   0,R14               RETURN XA-STYLE
         AGO   .XA3JOIN
.NOTXA3  ICM   R0,8,=AL1(WRKSP)    ... AND SUBPOOL
         FREEMAIN  R,LV=(0),A=(1)
RETURN0  DS    0H
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,R15             SET RC=0
         BR    R14                 RETURN 370-STYLE
.XA3JOIN ANOP
         TITLE 'ESTAE RECOVERY ROUTINE '
         DROP
RECOVERY DS    0H           COMMON ENTRY POINT
         USING *,R12
         LR    R12,R15
         LR    R10,R14      SAVE RETURN ADDRESS
         LA    R4,4
         CR    R0,R4        IS THERE A SDWA?
         BNE   HAVESDWA     YES - BR TO PROCESS WITH SDWA
         SDUMP MF=(E,SDUMPL) TAKE SVC DUMP
         L     R0,0(R2)      LOAD RETRY ROUTINE ADDRESS
         LA    R15,4         SET RETCODE TO RETRY ADDR IN R0
         LR    R14,R10       RESTORE RETURN ADDRESS
         BR    R14
HAVESDWA DS    0H
         ST    R14,12(R13)   SAVE RETURN ADDRESS
         LR    R10,R1
         USING SDWA,R10
         SDUMP MF=(E,SDUMPL)  TAKE AN SVC DUMP
         L     R2,SDWAPARM    LOAD PARAM LIST ADDR FROM SDWA
         MVC   SDWASR12,8(R2) RESTORE BASE REG
         MVC   SDWASR13,12(R2) RESTORE BASE REG
         SETRP RC=4,RETADDR=(2),RETREGS=YES,FRESDWA=YES,REGS=(14),     X
               WKAREA=(10)    RETURN WITH RETRY
*        BR    14             GENERATED BY SETRP EXPANSION
SDUMPL   SDUMP HDRAD=SDUMPH,SDATA=(LPA,RGN,CSA),MF=L
SDUMPH   DC    AL1(L'SDUMPT)
SDUMPT   DC    C'IEFUJV  -- SMF USER EXIT ABEND '
         TITLE 'CONSTANTS '
         LTORG
ESTAEL   ESTAE MF=L                MODEL ESTAE PARM LIST
LESTAEL  EQU   *-ESTAEL
ZONEXLAT DC    256C'#'             (INVALID DASD ZONES)
         ORG   ZONEXLAT+C'A'
         DC    C'BBA'              VALID ZONES
         ORG   ,
TSOXLATE DC    256C'#'             (INVALID TSO NAMES)
         ORG   TSOXLATE+C'A'
         DC    C'HIJ'              VALID TSO
YEARTAB  DC    AL1(31),C'JAN',AL1(28),C'FEB',AL1(31),C'MAR'
         DC    AL1(30),C'APR',AL1(31),C'MAY',AL1(30),C'JUN'
         DC    AL1(31),C'JUL',AL1(31),C'AUG',AL1(30),C'SEP'
         DC    AL1(31),C'OCT',AL1(30),C'NOV',AL1(255),C'DEC'
DAYTABLE DC    C'7SAT1SUN2MON3TUE4WED5THU6FRI'
QUESTTRT DC    256X'00'            TRT TABLE TO FIND "?"
         ORG   QUESTTRT+C'&KWDCHAR'
         DC    X'04'
         ORG   ,
         TITLE 'KEYWORD TABLE FOR SYMBOLIC SUBSTITUTION '
KEYWORDS DS    0A
         KEYWORD &KWDCHAR.SYSXAMODE,SYSXA,SYSAREA "XA" IN XA, NULL O.W.
         KEYWORD END-OF-TABLE
KWDFRLEN EQU   *-KEYWORDS
KWDTAB   DS    0A
         KEYWORD &KWDCHAR.SYSSPLV,SYSSPLV,SYSAREA "1" IF 370, "2" IF XA
         KEYWORD &KWDCHAR.SYSSMF,SYSSMF,SYSAREA SYSTEM SMF ID (4-CHAR)
         KEYWORD &KWDCHAR.SYSID,SYSID,SYSAREA LAST CHAR. OF SMF ID
         KEYWORD &KWDCHAR.SYSTSO,SYSTSO,SYSAREA TSOXLATE SYSID
         KEYWORD &KWDCHAR.SYSZONE,SYSZONE,SYSAREA ZONEXLAT SYSID
         KEYWORD &KWDCHAR.SYSSUBA,SYSSUBA,SYSAREA VTAM HOST SUBAREA
         KEYWORD &KWDCHAR.SYSYEAR,SYSYEAR,SYSAREA YEAR (YYYY)
         KEYWORD &KWDCHAR.SYSDATE,SYSDATE,SYSAREA DATE (YYMMDD)
         KEYWORD &KWDCHAR.SYSYY,SYSYY,SYSAREA YEAR (YY)
         KEYWORD &KWDCHAR.SYSMM,SYSMM,SYSAREA MONTH (MM)
         KEYWORD &KWDCHAR.SYSDD,SYSDD,SYSAREA DAY OF MONTH (DD)
         KEYWORD &KWDCHAR.SYSJDATE,SYSJDATE,SYSAREA JULIAN DATE (YYDDD)
         KEYWORD &KWDCHAR.SYSWD,SYSWD,SYSAREA WEEKDAY (1-DIGIT, 1-7)
         KEYWORD &KWDCHAR.SYSWKDY,SYSWKDY,SYSAREA WEEKDAY (3-CHAR)
         KEYWORD &KWDCHAR.SYSMONTH,SYSMONTH,SYSAREA MONTH (3-CHAR)
         KEYWORD END-OF-TABLE
         TITLE 'DSECTS, AND EQUATES '
SYSAREA  DSECT
SYSSP    EQU   230                 SUBPOOL FOR SYS AREA
SYSKWDTB DS    0A
         KEYWORD &KWDCHAR.SYSXAMODE,SYSSPLV,SYSAREA
         KEYWORD END-OF-TABLE
SYSSMF   DS    CL4                 SMF NAME
SYSID    DS    CL1                 SYSTEM SUFFIX
SYSTSO   DS    CL1                 TSO SUFFIX
SYSZONE  DS    CL1                 DASD SUFFIX
         DS    CL1                 FILLER
SYSSUBA  DS    CL2                 VTAM SUBAREA
SYSYEAR  DS    0CL4                YEAR (YYYY)
         DS    CL2                 CENTURY
SYSDATE  DS    0CL6                DATE (YYMMDD)
SYSYY    DS    CL2                 YEAR
SYSMM    DS    CL2                 MONTH
SYSDD    DS    CL2                 DAY
SYSJDATE DS    CL5                 JULIAN DATE (YYDDD)
SYSWD    DS    CL1                 WEEKDAY (1-7)
SYSWKDY  DS    CL3                 WEEKDAY (ABBREV.)
SYSMONTH DS    CL3                 MONTH
SYSSPLV  DS    CL1                 "1" OR "2"
SYSXA    DS    CL2                 "XA" OR NULL
         DS    0D
SYSLEN   EQU   *-SYSAREA
         SPACE 3
WORK     DSECT
WRKSP    EQU   230                 SUBPOOL FOR WORK AREA
         DS    18F                 STANDARD SAVE AREA
WRKPACK  DS    D
WRKESTAW DS    XL(LESTAEL)         ESTAE PARM LIST AREA
WRKESTAP DS    4F                  PARM LIST TO RETRY ROUTINE
*              RETRY ROUTINE ADDRESS  NO SDWA OBTAINED
*              RETRY ROUTINE ADDRESS  SDWA OBTAINED
*              BASE REGISTER R12
*              DATA REGISTER R13
WRKPARMS DS    5F                  PARMS FOR KWDREPL
WRKYRTAB DS    XL(4*12)
WRKFEB   EQU   WRKYRTAB+4
WRKDAYP  DS    PL2
         DS    0D
WRK512   DS    512X'00'
         DS    0D
WORKSIZE EQU   *-WORK
         SPACE 3
         KWDTABLE
         SPACE 3
JMR      DSECT
         IEFJMR
         IHASDWA
         EQUREGS
         END   IEFUJV
