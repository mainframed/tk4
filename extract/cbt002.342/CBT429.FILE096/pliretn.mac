         MACRO
&LABEL   PLIRETN  &RC=
.**********************************************************************
.*  PLIRETN MACRO  -           BILL WARRINER, UMC-VMDL, 882-6811.     *
.*                                                                    *
.* FUNCTION - TERMINATES AN ASSEMBLER SUBROUTINE AND RESTORES THE OLD *
.*    PL/1 ENVIRONMENT, RELEASING LIFO STORAGE REQUIRED BY THE ASM    *
.*    ROUTINE.                                                        *
.*                                                                    *
.* PARMS:                                                             *
.*    &RC   - THE RETURN CODE TO BE SET IN REGISTER 15.  IF OMITTED,  *
.*            NO RETURN CODE WILL BE SET AND REGISTER 15 WILL BE      *
.*            RESTORED TO ITS VALUE ON ENTRY TO THE ROUTINE           *
.*            (THE DEFAULT FOR ROUTINES NOT DECLARED WITH             *
.*            "OPTIONS(ASM,RETCODE)").  IF SPECIFIED, IT SHOULD BE    *
.*            EITHER "(15)" TO INDICATE THE RETURN CODE IS ALREADY    *
.*            SET OR A VALUE TO BE USED IN A LA INSTRUCTION OR A      *
.*            REGISTER IN STANDARD REGISTER NOTATION.                 *
.*                                                                    *
.*                                                                    *
.**********************************************************************
&LABEL   L     13,4(,13)                 RECOVER @ OF OLD DSA
         AIF   ('&RC' EQ '').NORC
         AIF   ('&RC' EQ '(15)' OR '&RC' EQ '(R15)').NOLD
         AIF   ('&RC'(1,1) EQ '(').NOLA
         LA    15,&RC                    SET RETURN CODE
         AGO   .NOLD
.NOLA    LR    15,&RC(1)                 SET RETURN CODE
.NOLD    L     14,12(,13)                RESTORE RETURN ADDRESS
         LM    0,12,20(13)               RESTORE OTHER REGISTERS
         BR    14                        RETURN TO CALLER
         MEXIT
.NORC    LM    14,12,12(13)              RESTORE REGISTERS
         BR    14
         MEND
