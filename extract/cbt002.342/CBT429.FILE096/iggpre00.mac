PRE00    TITLE 'IGGPRE00 - DADSM PRE-PROCESSING EXIT'
***********************************************************************
* IGGPRE00  -  DADSM PRE-PROCESSING EXIT                              *
*                                                                     *
*  FUNCTION:  THIS EXIT TESTS FOR PRESENCE OF HCCDADSM ROUTINE AND    *
*             LINKS TO IT IF IT IS PRESENT.                           *
*                                                                     *
*  ATTRIBUTES: KEY 0, SUPERVISOR, ENABLED, REENTRANT, NO LOCKS HELD.  *
*              AMODE=ANY, RMODE=24                                    *
*                                                                     *
*  WRITTEN ON 01/20/87 BY BILL WARRINER, UMCHC                        *
*                                                                     *
***********************************************************************
         GBLC  &SYSSPLV
         EJECT
IGGPRE00 CSECT
         CONNECT  WORKSIZE,WORK,BASE=R12,PARM=R11,CLEAR=YES,           X
               COPYRT=1987,AMODE=ANY,RMODE=24,LOC=BELOW
         L     R1,16               GET CVT ADDRESS
         ICM   R1,X'F',CVTUSER-CVT(R1) NCT ADDRESS IF PRESENT
         BZ    RETN00              IF NOT PRESENT, ALLOW REQUEST.
         ICM   R15,X'F',NCTDADSM-NCT(R1) IS HCCDADSM PRESENT?
         BZ    RETN00              IF NOT PRESENT, ALLOW REQUEST.
         LR    R1,R11              RESTORE PARM POINTER
         L     R0,R0PRE            SET R0 TO INDICATE PRE-PROCESS
         AIF   ('&SYSSPLV' EQ '1').NOTXA1
         BASSM R14,R15             LINK TO ROUTINE XA-STYLE
         AGO   .XA1DONE
.NOTXA1  BALR  R14,R15
.XA1DONE B     RETURN
RETN00   DS    0H
         SR    R15,R15             SET R15=0
RETURN   DS    0H
         RELEASE WORKSIZE,RC=(R15)
         SPACE 3
R0PRE    DC    A(0)                R0=0 IF PRE-PROCESS EXIT
R0POST   DC    A(4)                R0=4 IF POST-PROCESS EXIT
         LTORG
WORK     DSECT
REGSAVE  DS    18F
         DS    0D
WORKSIZE EQU   *-WORK
         EQUREGS
NCT      NCT   TYPE=DSECT
         CVT   DSECT=YES
         END   IGGPRE00
