KWDREPL  TITLE 'KWDREPL - SUBROUTINE TO REPLACE KEYWORDS WITH VALUES'
KWDREPL  CSECT
KWDREPL  AMODE 31
KWDREPL  RMODE ANY
***********************************************************************
*                                                                     *
* FUNCTION - TO PERFORM SYMBOLIC REPLACEMENT ON A KEYWORD WITHIN      *
*       A (FIXED-LENGTH) AREA.  TEXT WITHIN THE AREA WILL BE          *
*       SHIFTED TO ACCOMODATE LENGTH CHANGES CAUSED BY THE REPLACEMENT*
*                                                                     *
*       A KEYWORD TABLE BUILT BY THE "KEYWORD" MACRO IS A REQUIRED    *
*       PARAMETER TO THIS SUBROUTINE.  A 512-BYTE WORK AREA SHOULD    *
*       BE PROVIDED FOR THE USE OF THIS SUBROUTINE.  THE SUBROUTINE   *
*       DOES NOT SCAN FOR A KEYWORD WITHIN THE AREA, BUT RATHER       *
*       CONDITIONALLY REPLACES A KEYWORD WITH A VALUE.                *
*                                                                     *
*  ATTRIBUTES:  RENT                                                  *
*                                                                     *
*  MACROS: NONE.                                                      *
*  MAPPING MACROS:  KWDTABLE, EQUREGS                                 *
*                                                                     *
*  PARMS:                                                             *
*                                                                     *
*   R1  = PTR TO A 5-WORD PARAMETER LIST WITH THE FOLLOWING FORMAT:   *
*        +00 - ADDR(512-BYTE WORK AREA)                               *
*        +04 - ADDR(WORD-TO-TEST-FOR-SUBSTITUTION)                    *
*        +08 - ADDR(END-OF-SUBSTITUTION-AREA)                         *
*        +0C - ADDR(KEYWORD-TABLE)                                    *
*        +10 - ADDR(FIELD-VALUE-DSECT)                                *
*   R13 = PTR TO OS-STYLE SAVE AREA                                   *
*   R14 = RETURN ADDRESS                                              *
*   R15 = ENTRY POINT ADDRESS                                         *
*                                                                     *
*  ON RETURN:                                                         *
*   R1  = ADDRESS OF KEYWORD TABLE ENTRY.                             *
*   R15 = RETURN CODE:                                                *
*        +00 - TEST WORD WAS A KEYWORD, SUBSTITUTION HAS BEEN DONE.   *
*        +04 - TEST WORD WAS NOT A KEYWORD (R0 HAS 0).                *
*                                                                     *
*                                                                     *
* AUTHOR:  BILL WARRINER                                              *
*          UMC HOSPITAL AND CLINICS                                   *
*          SYSTEM PROGRAMMING SERVICES DC017.00                       *
*          1400 ROCK QUARRY ROAD                                      *
*          COLUMBIA, MO  65212                                        *
*          (314) 882-3773                                             *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
         USING KWDREPL,R15
         B     PASTCONS
         DC    AL1(L'EPID)
EPID     DC    C'KWDREPL_V1.0_&SYSDATE._&SYSTIME.'
         DC    C'COPYRIGHT(C) BY THE CURATORS OF THE UNIVERSITY OF MISSX
               OURI, A PUBLIC CORPORATION'
PASTCONS DS    0H
         STM   R14,R12,12(R13)
         DROP  R15
         LR    R12,R15
         USING KWDREPL,R12
         L     R2,0(R1)            GET WORK AREA ADDRESS
         ST    R2,8(R13)           CHAIN
         ST    R13,4(R2)             SAVE AREAS
         LR    R13,R2                  TOGETHER
         LM    R3,R6,4(R1)         GET BASE REGISTERS
         LA    R0,8(R13)           CLEAR
         LA    R1,504                 OUT
         SR    R15,R15                   WORK
         MVCL  R0,R14                       AREA
         USING WORK,R13
         LA    R3,0(R3)
         LA    R4,0(R4)
         USING KWDTABLE,R5
         LTR   R5,R5               IS THERE A KEYWORD TABLE?
         BZ    NOTFOUND            NO - THEREFORE NO MATCH
         SR    R1,R1               SETUP FOR IC
SCAN     DS    0H
         IC    R1,KWDKLEN          GET KEYWORD LENGTH
         SH    R1,=H'1'            DEX FOR CLC
         BM    SCANNEXT            IF LENGTH<1 SKIP THIS ENTRY.
         LA    R10,0(R1,R3)        WILL THE KEYWORD FIT IN HERE?
         CLR   R10,R4
         BH    SCANNEXT
         EX    R1,SCANCLC          COMPARE KEYWORD:
         BE    FOUNDIT              - WE FOUND IT!
SCANNEXT DS    0H
         ICM   R5,15,KWDNEXT       ELSE GO CHECK THE NEXT ONE.
         BNZ   SCAN
         B     NOTFOUND
SCANCLC  CLC   0(*-*,R3),KWDKEYWD
FOUNDIT  DS    0H
         SR    R0,R0               SETUP FOR IC
         IC    R0,KWDFLEN          GET FIELD LENGTH
         SH    R0,=H'1'            IS THERE ANY DATA?
         BM    ERASEIT             IF NOT, JUST ERASE THE KEYWORD.
         CR    R0,R1               CHECK LENGTHS:
         BE    MOVEFLD             - EQUAL - JUST MOVE THE FIELDS
         BH    TRUNCATE            - DATA LONGER THAN KEYWORD
         B     PAD                 - DATA SHORTER THAN KEYWORD
ERASEIT  DS    0H                  - SPECIAL CASE: DATA LEN=0
         OI    WRKFLAG,WRKNOMVC
PAD      DS    0H
         LR    R14,R0              GET DATA LEN ...
         LA    R14,1(R14,R3)       CALCULATE TARGET ADDRESS
         LR    R10,R1              GET KEYWORD LEN ...
         LA    R10,1(R10,R3)       CALCULATE SOURCE ADDRESS
         LR    R15,R4              CALCULATE ...
         SR    R15,R3              ... REAL FIELD LENGTH ...
         LR    R11,R15             ... FOR BOTH SOURCE ...
         SR    R15,R0              ... AND TARGET.
         BM    ERROR
         SR    R11,R1
         BM    ERROR
         ICM   R11,8,=C' '         SET THE PAD CHARACTER
         MVCL  R14,R10             SHIFT AND PAD
         B     MOVEFLD             NOW GO COPY THE DATA.
TRUNCATE DS    0H
         LR    R10,R1              GET KEYWORD LEN ...
         LA    R10,1(R10,R3)       ... AND CALCULATE SOURCE ADDRESS.
         LR    R11,R4
         SR    R11,R3
         SR    R11,R1              GET SOURCE LENGTH.
         BM    ERROR
         C     R11,=A(L'WRKSVE2)   IS IT TOO LONG?
         BH    ERROR
         ST    R11,WRKSVE1         SAVE THE LENGTH.
         LA    R14,WRKSVE2
         LR    R15,R11
         MVCL  R14,R10             COPY TO A TEMPORARY
         LR    R14,R0              GET DATA LEN ...
         LA    R14,1(R14,R3)       CALCULATE TARGET ADDRESS
         CR    R14,R4              TARGET PAST END OF STRING?
         BH    TRUNCAT2            YES - USE SPECIAL MOVE.
         LR    R15,R4
         SR    R15,R3
         SR    R15,R0              CALCULATE TARGET LENGTH
         BM    ERROR
         LA    R10,WRKSVE2
         L     R11,WRKSVE1
         MVCL  R14,R10             COPY WITH TRUNCATION
MOVEFLD  DS    0H                  MOVE IN THE DATA:
         TM    WRKFLAG,WRKNOMVC    (IS THERE ANY TO MOVE?)
         BNZ   DONE                NO - ALL FINISHED.
         LR    R1,R0               GET DATA LENGTH-1
         AL    R6,KWDFIELD         GET DATA ADDRESS
         EX    R1,MOVEMVC          AND COPY DATA IN.
         B     DONE
TRUNCAT2 DS    0H                  SPECIAL MOVE - TRUNCATE DATA TOO.
         LR    R1,R4               CALCULATE THE
         SR    R1,R3               ... AVAILABLE LENGTH-1
         AL    R6,KWDFIELD         START OF SOURCE ...
         EX    R1,MOVEMVC          COPY IN WHATEVER WILL FIT.
         B     DONE
MOVEMVC  MVC   0(*-*,R3),0(R6)
ERROR    DS    0H
         LA    R15,8               INDICATE SEVERE ERROR
         SR    R5,R5
         B     RETURN
NOTFOUND DS    0H
         LA    R15,4
         SR    R5,R5
         B     RETURN
DONE     DS    0H
         SR    R15,R15
RETURN   DS    0H
         LR    R1,R5
         L     R13,4(R13)          BACKUP 1 SAVE AREA
         XC    8(4,R13),8(R13)     AND UNCHAIN
         L     R14,12(R13)         RESTORE
         L     R0,20(R13)             REGISTERS
         LM    R2,R12,28(R13)           FOR RETURN
         BR    R14                 RETURN TO CALLER.
         EJECT
         LTORG
         KWDTABLE
WORK     DSECT
         DS    18F
WRKSVE1  DS    A
WRKFLAG  DS    X
WRKNOMVC EQU   X'80'
WRKSVE2  DS    CL256
         DS    0D
WORKSIZE EQU   *-WORK                 MUST BE LESS THAN 512
         EQUREGS
         END
