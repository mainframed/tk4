         MACRO
&LABEL   DSAD
.**********************************************************************
.*    DSAD MACRO:                                                     *
.* FUNCTION - GENERATES A DSECT DESCRIBING THE FIRST 88 BYTES OF A    *
.*   PL/1 DYNAMIC STORAGE AREA (DSA).  THE NAME OF THE DSECT IS THE   *
.*   LABEL ON THE MACRO INVOKATION, OR "DSADSECT" BY DEFAULT.         *
.*                                                                    *
.*   ALL LABELS GENERATED START WITH THE FIRST 3 CHARACTERS OF THE    *
.*   DSECT LABEL ("DSA" BY DEFAULT).  LABELS ARE:                     *
.*                                                                    *
.*       XXXFLAG1 - PL/1 ERROR HANDLING FLAGS (DESCRIBE THIS DSA).    *
.*       XXXOLD@  - ADDRESS OF PREVIOUS DSA (CHAINBACK).              *
.*       XXXREGS  - REGISTER SAVE AREA (R14 THRU R12).                *
.*       XXXR0S   - SAVE AREA FOR REGISTER 0                          *
.*       XXXR1S   - SAVE AREA FOR REGISTER 1                          *
.*                .                                                   *
.*                .                                                   *
.*                .                                                   *
.*       XXXR15S  - SAVE AREA FOR REGISTER 15                         *
.*       XXXLWS@  - ADDRESS OF LIBRARY WORK SPACE (LWS)               *
.*       XXXNAB   - ADDRESS OF NEXT AVAILABLE BYTE IN LIFO STORAGE    *
.*       XXXFLAG2 - MORE PL/1 FLAGS.                                  *
.*                                                                    *
.*  THIS IS THE END OF THE BASE DSA OF 88 BYTES.                      *
.**********************************************************************
         LCLA  &N
         LCLC  &RN,&PREF,&LAB,&L2
         AIF   ('&LABEL' EQ '').NOLAB
&LAB     SETC  '&LABEL'
&N       SETA  K'&LABEL
         AIF   (&N LT 3).SMALLAB
&PREF    SETC  '&LABEL'(1,3)
         AGO   .LABDONE
.SMALLAB ANOP
&PREF    SETC  '&LABEL'
         AGO   .LABDONE
.NOLAB   ANOP
&LAB     SETC  'DSADSECT'
&PREF    SETC  'DSA'
.LABDONE ANOP
         SPACE 2
&LAB     DSECT                           PL/1 DSA DESCRIPTION
         SPACE 1
***********************************************************************
*     MAP OF DYNAMIC STORAGE AREA (DSA), POINTED TO BY R13.           *
***********************************************************************
         SPACE 1
&L2      SETC  '&PREF.FLAG1'
&L2      DS    F                         PL/1 FLAGS
&L2      SETC  '&PREF.OLD@'
&L2      DS    AL4                       PREVIOUS DSA ADDRESS (OLD R13)
         DS    F                         (CHAIN FORWARD NOT USED)
&L2      SETC  '&PREF.REGS'
&L2      DS    0F                        REGISTER SAVE AREA
&L2      SETC  '&PREF.R14S'
&L2      DS    F
&L2      SETC  '&PREF.R15S'
&L2      DS    F
&N       SETA  0
.RLOOP   ANOP
         AIF   (&N GT 12).XRLOOP
&RN      SETC  '&PREF.R&N.S'
&RN      DS    F
&N       SETA  &N+1
         AGO   .RLOOP
.XRLOOP  ANOP
&L2      SETC  '&PREF.LWS@'
&L2      DS    AL4                       LIBRARY WORK SPACE ADDRESS.
&L2      SETC  '&PREF.NAB'
&L2      DS    AL4                       NEXT AVAILABLE BYTE IN LIFO.
&L2      SETC  '&PREF.FLAG2'
&L2      DS    2F                        PL/1 FLAGS.
         MEND
