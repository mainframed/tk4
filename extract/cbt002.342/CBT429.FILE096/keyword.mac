         MACRO
         KEYWORD  &KEYWORD,&FIELD,&DSECT
.*
.*  FUNCTION - DEFINES A KEYWORD TABLE FOR USE OF ROUTINE "KWDREPL"
.*
.*      3 POSITIONAL PARMS:
.*          (1) - THE CHARACTER VALUE OF THE KEYWORD BEING DEFINED.
.*                THIS MUST NOT INCLUDE BLANKS, COMMAS, TIC MARKS, ETC.
.*          (2) - THE NAME OF THE FIELD CONTAINING THE DATA TO BE
.*                SUBSTITUTED.  IT MAY BE OF THE FORM "(NAME,LENGTH)"
.*                OR (OFFSET,LENGTH),  OR JUST "NAME." IF "LENGTH" IS
.*                NOT SUPPLIED, IT WILL DEFAULT TO THE ASSEMBLED LENGTH
.*                OF "NAME"
.*          (3) - THE NAME OF THE DSECT CONTAINING THE FIELD NAMED IN
.*                #2 ABOVE.  IF AN OFFSET IS SUPPLIED IN #2,
.*                THIS PARM SHOULD BE SPECIFIED AS "0"
.*
         GBLC  &KWDTBL(255),&OFFTBL(255),&LBLTBL(255),&LENTBL(255)
         GBLA  &KWDCNT,&KWDLEN(255)
         LCLC  &FLDNAM,&FLDLEN
         LCLC  &KWDTEMP,&OFFTEMP,&LBLTEMP,&LENTEMP
         LCLA  &KLNTEMP,&I,&J
         AIF   ('&KEYWORD' EQ 'END-OF-TABLE').ENDTAB
         AIF   (&KWDCNT LE 253).GENKWD
         MNOTE 8,'** TOO MANY KEYWORDS IN TABLE, &KEYWORD IGNORED'
         MEXIT
.GENKWD  ANOP
&KWDCNT  SETA  &KWDCNT+1
         AIF   (N'&FIELD LT 2).DFLTLEN
&FLDNAM  SETC  '&FIELD(1)'
&FLDLEN  SETC  '&FIELD(2)'
         AGO   .SETVAR
.DFLTLEN ANOP
&FLDNAM  SETC  '&FIELD(1)'
&FLDLEN  SETC  'L''&FLDNAM'
.SETVAR  ANOP
&LBLTBL(&KWDCNT) SETC 'KWD&SYSNDX'
&OFFTBL(&KWDCNT) SETC '&FLDNAM-&DSECT'
&KWDTBL(&KWDCNT) SETC '&KEYWORD'
&KWDLEN(&KWDCNT) SETA K'&KEYWORD
&LENTBL(&KWDCNT) SETC '&FLDLEN'
         MEXIT
.ENDTAB  ANOP
.*
.*    FIRST, SORT IN ORDER BY LONGEST KEYWORD FIRST
.*
&I       SETA  1
.LOOP1   AIF   (&I GE &KWDCNT).SORTED
&J       SETA  &I+1
.LOOP2   AIF   (&J GT &KWDCNT).XLOOP2
         AIF   (&KWDLEN(&I) GE &KWDLEN(&J)).NEXT2
&KWDTEMP SETC  '&KWDTBL(&I)'
&OFFTEMP SETC  '&OFFTBL(&I)'
&LBLTEMP SETC  '&LBLTBL(&I)'
&LENTEMP SETC  '&LENTBL(&I)'
&KLNTEMP SETA  &KWDLEN(&I)
&KWDTBL(&I) SETC '&KWDTBL(&J)'
&OFFTBL(&I) SETC '&OFFTBL(&J)'
&LBLTBL(&I) SETC '&LBLTBL(&J)'
&LENTBL(&I) SETC '&LENTBL(&J)'
&KWDLEN(&I) SETA &KWDLEN(&J)
&KWDTBL(&J) SETC '&KWDTEMP'
&OFFTBL(&J) SETC '&OFFTEMP'
&LBLTBL(&J) SETC '&LBLTEMP'
&LENTBL(&J) SETC '&LENTEMP'
&KWDLEN(&J) SETA &KLNTEMP
.NEXT2   ANOP
&J       SETA  &J+1
         AGO   .LOOP2
.XLOOP2  ANOP
&I       SETA  &I+1
         AGO   .LOOP1
.SORTED  ANOP
&LBLTBL(&KWDCNT+1) SETC '0'
&I       SETA  1
.DCLOOP  AIF   (&I GT &KWDCNT).DCDONE
&LBLTBL(&I) DC  A(&LBLTBL(&I+1))
         DC    A(&OFFTBL(&I))
         DC    AL1(&LENTBL(&I))
         DC    AL1(&KWDLEN(&I))
         DC    C'&KWDTBL(&I)'
&I       SETA  &I+1
         AGO   .DCLOOP
.DCDONE  ANOP
&KWDCNT  SETA  0
         MEND
