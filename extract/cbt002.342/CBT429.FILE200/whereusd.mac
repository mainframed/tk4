TWA      TITLE 'WHEREUSD  -  LIST PDS DIRECTORY WITH DATE INFORMATION'
WHEREUSD START 0
         SYMREGS
         EJECT
***********************************************************************
*                                                                     *
*        WHEREUSD                                                     *
*        --------                                                     *
*                                                                     *
*        SEARCH A PDS FOR A GIVEN  CSECT NAME                         *
*        ( MUST CONTAIN MEMBERS CREATED BY THE LINKAGE EDITOR )       *
*                                                                     *
*        FOR EACH MEMBER, THE FOLLOWING IS LISTED  -                  *
*                                                                     *
*              1. THE MEMBER NAME                                     *
*              2. THE TTRC         ( FROM THE DIRECTORY )             *
*              3. THE LINK-EDIT ATTRIBUTES                            *
*              4. THE LENGTH OF THE MODULE                            *
*              5. THE ENTRY POINT                                     *
*                                                                     *
*        WRITTEN BY   REX WIDMER / PETER FARRELL                      *
*                                                                     *
*        TRANS WORLD AIRLINES INC.                                    *
*        KANSAS CITY ADMINISTRATIVE CENTER                            *
*        11500 AMBASADOR DRIVE                                        *
*        KANSAS CITY, MO 64153                                        *
*                                                                     *
*        816-464-6671                                                 *
*                                                                     *
*        THIS PROGRAM IS DISTRIBUTED WITHOUT CHARGE TO MEMBERS        *
*        OF THE GUIDE COBOL GROUP.  IT IS DIRTRIBUTED ON AN AS        *
*        IS, WHERE IS BASIS, WITHOUT EXPRESSED OR IMPLIED WARRANTY    *
*        OF ANY KIND.  IT IS DISTRIBUTED IN HOPE THAT IT MAY SAVE     *
*        OTHER MEMBERS OF THE PROJECT SOME WHEEL RE-INVENTING...      *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        JCL / DD-CARD REQUIREMENTS                                   *
*               SAMPLE PROC FOR USAGE OF WHEREUSD                     *
*                                                                     *
*       //WU      PROC NAME=,RGN=128K,CLS=X,P=                        *
*       //WU      EXEC PGM=WHEREUSD,REGION=&RGN,                      *
*       //             PARM='&P'                                      *
*       //STEPLIB   DD DISP=SHR,DSN=SYPP.PGMLIB                       *
*       //SYSPRINT  DD SYSOUT=&CLS                                    *
*       //SYSYDUMP  DD SYSOUT=&CLS                                    *
*       //SYSUT1    DD DISP=SHR,UNIT=SYSDA,                           *
*       //             DSN=&NAME                                      *
*                                                                     *
*        DD-CARDS / FUNCTION                                          *
*                                                                     *
*           SYSUT1   THE PDS TO ANALYZE                               *
*           SYSPRINT THE OUTPUT REPORT                                *
*                                                                     *
*        PARM INFORMATION                                             *
*                                                                     *
*          SYMBOLIC /P/     1-8 CHARACTER EXTERNAL SYMBOL TO          *
*                           SEARCH FOR                                *
*                                                                     *
***********************************************************************
*   CHANGE LOG                                            SBG 03/00   *
*        03/09/00 - FIX DATE IN HEADER FOR Y2K.  CHANGE   SBG 03/00   *
*                   PAT01 FILL BYTE TO X'F0' AND BLANK    SBG 03/00   *
*                   OUT THE FIRST OCCURRENCE.             SBG 03/00   *
*                                                                     *
***********************************************************************
         EJECT
WHEREUSD CSECT
         SAVE  (14,12),T,*
         LR    R10,R15             ESTABLISH A BASE REGISTER
         USING WHEREUSD,R10        -
         LA    R12,SAVE            CHAIN SAVE AREAS
         ST    R12,8(,R13)         -
         ST    R13,4(,R12)         -
         LR    R13,R12             -
         SPACE  2
*
*        SAVE THE CSECT NAME TO LOOK FOR, GIVEN IN THE PARM FIELD  -
*
         L     R2,0(,R1)           GET POINTER TO THE PARMS
         LA    R2,0(,R2)           -
         LH    R3,0(,R2)           GET LENGTH OF THE PARMS
         LTR   R3,R3               WERE ANY PARMS PASSED  ?
         BNP   ERROR1              NO - BRANCH
         C     R3,=F'8'            WAS THE PARM LENGTH GT 8
         BH    ERROR1              YES - BRANCH
         SPACE 1
         BCTR  R3,0                PREPARE TO EXECUTE
         EX    R3,MOVEPARM         MOVE THE PARM TO AN AREA
         SPACE 2
*        READ THE JFCB TO PREPARE FOR AN OBTAIN  -
         RDJFCB (MEMDCB)           READ THE JFCB FOR THE PDS
         LTR   R15,R15             DID IT DO THAT O.K.  ?
         BNZ   ERROR2              NO - BRANCH
         SPACE 1
*        ISSUE  AN OBTAIN, TO SEE IF THE PDS EXISTS -
         OBTAIN CAMLIST            ISSUE OBTAIN
         LTR   R15,R15             DID THE OBTAIN OBTAIN ANYTHING ?
         BNZ   ERROR3              NO - BRANCH
         SPACE 1
*        TEST TO ENSURE THAT THIS DATASET IS A PDS  -
         TM    AREA+38,X'02'       TEST FOR 'DSORG=PO' IN FORMAT 1
         BZ    ERROR4              NOT A PDS - BRANCH
         SPACE 1
*        OPEN THE DCBS FOR THE DIRECTORY AND THE MEMBERS  -
         OPEN  (DIRDCB,,MEMDCB)    ISSUE THE OPEN
         TM    DIRDCB+48,X'10'     DID THE DIRECTORY DCB OPEN O.K. ?
         BZ    ERROR5              NO - BRANCH
         TM    MEMDCB+48,X'10'     DID THE MEMBER DCB OPEN O.K. ?
         BZ    ERROR5              NO - BRANCH
         SPACE 1
*        GET A BUFFER TO READ THE MEMBERS INTO  -
         MVC   BUFFLEN,MEMDCB+62   GET BLOCKSIZE FROM THE DCB
         L     R0,BUFFER           LENGTH + SP I.D. FOR GETMAIN
         GETMAIN R,LV=(0)          ISSUE GETMAIN
         ST    R1,DECB+12          PUT POINTER TO BUFFER IN DECB
         SPACE 1
*        INITIALISE  THE BUFFER TO ZEROS (EASIER DEBUGGING)  -
         LH    R2,BUFFLEN          GET LENGTH OF THE BUFFER
ZEROLOOP MVI   0(R1),0             MOVE A ZERO INTO THE BUFFER
         LA    R1,1(,R1)           INDEX THROUGH THE BUFFER
         BCT   R2,ZEROLOOP         ZERO THE ENTIRE BUFFER
         SPACE 1
         OPEN  (SYSPRINT,(OUTPUT)) OPEN SYSPRINT
         TM    SYSPRINT+48,X'10'   DID IT OPEN O.K.  ?
         BZ    ERROR6              NO - BRANCH
         SPACE 1
*        GET TIME AND DATE  -
         TIME  DEC
         ST    R0,TIME             SAVE TIME
         ST    R1,DATE             SAVE DATE
         MVO   TIM,TIME(2)         SAVE TIME
         SPACE 1
*        GET JOBNAME OF JOB, AND SAVE IT  -
         EXTRACT FIELD1,FIELDS=(TIOT)
         L     R1,FIELD1           POINT TO TIOT
         MVC   JNAME,0(R1)         SAVE JOBNAME
         B     GET                 GET THE FIRST PDS DIRECTORY BLOCK
         SPACE 2
*
*        GET NEXT MEMBER IN THE PDS  -
*
*        FIRST  LOOK FOR A MEMBER IN THE CURRENT DIRECTORY BLOCK -
EODAD    EQU   *
LOOPDS1  L     R2,DIRPOINT         POINT TO THE NEXT MEMBER
         IC    R3,11(,R2)          GET LENGTH OF USER DATA
         N     R3,MASK2            LENGTH IS 5 BITS
         SLL   R3,1                X 2 BECAUSE LENGTH IN HALF-WORDS
         LA    R3,12(,R3)          ADD LENGTH OF MEMBER NAME + TTRC
         AR    R3,R2               POINT TO NEXT MEMBER
         SPACE 1
*        FIND  THE END OF THE DIRECTORY BLOCK  -
         LA    R4,DIRBLOCK         POINT TO THE CURRENT DIRECTORY BLK
         LH    R5,0(,R4)           GET LENGTH USED
         AR    R5,R4               POINT TO END OF THE BLOCK
         CR    R5,R3               HAS THIS BLOCK BEEN EXAUSTED ?
         BH    GOTNEXT             NO - BRANCH
         SPACE 1
*        READ THE NEXT DIRECTORY BLOCK  -
GET      GET   DIRDCB,DIRBLOCK     READ THE NEXT DIRECTORY BLOCK
         LA    R3,DIRBLOCK+2       POINT TO THE FIRST MEMBER IN BLOCK
GOTNEXT  ST    R3,DIRPOINT         SAVE POINTER TO NEXT MEMBER
         SPACE 1
         XC    CMREC,CMREC         # OF RECORDS READ FOR THIS MEMBER
         SPACE 1
*        TEST TO SEE IF WE HAVE GOT TO THE END OF THE PDS  -
         CLC   0(8,R3),LASTMEM     LOOK FOR A MEMBER NAME OF FOXES
         BE    ENDPDS              GOT TO END - BRANCH
         SPACE 1
*        COUNT # OF MEMBERS READ  -
         L     R1,CPMEM            GET # OF MEMBERS READ
         AL    R1,=F'1'            INCREMENT BY 1
         ST    R1,CPMEM            AND SAVE IT
         SPACE 2
         L     R2,DIRPOINT         POINT TO DIRECTORY ENTRY
         MVC   TTRZ(3),8(R2)       SAVE TTR OF MEMBER
         POINT MEMDCB,TTRZ         POINT TO THIS MEMBER
         SPACE 1
*
*        TEST FOR ALIAS  -
*
         L     R2,DIRPOINT         POINT TO DIRECTORY ENTRY
         TM    11(R2),X'80'        IS THIS ENTRY AN ALIAS  ?
*        PROCESS ALIAS THE SAME AS OTHER MEMBERS  -
         SPACE 2
*        DETERMINE IF THE MEMBER WAS CREATED BY THE LINKAGE-EDITOR  -
         IC    R3,11(,R2)          GET LENGTH OF USER DATA
         N     R3,MASK2            -
         C     R3,=F'11'           MUST BE AT LEAST 11 HALF-WORDS
         BL    LOOPDS1             IT WAS NOT LINKED - BRANCH
         EJECT
***********************************************************************
*                                                                     *
*        READ THE MEMBER SEQUENTIALLY, EXTRACTING INFORMATION         *
*        AND WRITING IT TO THE OUTPUT FILE.                           *
*                                                                     *
***********************************************************************
         SPACE 1
GETLOOP  EQU   *
         L     R6,DECB+12          POINT TO INPUT AREA
         READ  DECB,SF,MF=E        READ THE NEXT RECORD
         CHECK DECB
         SPACE 1
*        INCREMENT COUNT OF # OF RECORDS READ BY 1  -
         L     R1,CMREC            COUNTS # OF INPUT RECORDS
         AL    R1,=F'1'            INCREMENT
         ST    R1,CMREC            AND SAVE
         SPACE 1
         L     R1,CPREC            COUNTS # OF READS FOR THIS PDS
         AL    R1,=F'1'            BUMP BY 1
         ST    R1,CPREC            AND SAVE
         SPACE 3
*
*        LOOK AT RECORD TYPE, PROCES ACORDINGLY  -
*
         TM    0(R6),1             DOES THIS RECORD PRECEED A TEXT REC.
         BO    EODAD               YES - BRANCH ( WE ARE DONE )
         SPACE 1
         CLI   0(R6),X'20'         IS IT A CESD RECORD  ?
         BE    CESD                YES - BRANCH
         B     GETLOOP             GO GET ANOTHER RECORD
         EJECT
***********************************************************************
*                                                                     *
*        PROCESS THE CESD TYPE INPUT RECORD                           *
*                                                                     *
***********************************************************************
         SPACE 1
CESD     LA    R3,8(,R6)           POINT TO 1ST ESD ENTRY
         LH    R4,6(,R6)           GET LENGTH OF ESD DATA
         AR    R4,R3               POINT TO END OF RECORD
         LH    R5,4(,R6)           GET # OF FIRST ESD ENTRY IN THIS REC
         SPACE 1
LOOPESD  CR    R4,R3               HAVE WE GOT TO THE END YET ?
         BNH   GETLOOP             YES - GO GET ANOTHER INPUT RECORD
         SPACE 1
         L     R1,CCSCAN           # OF CSECTS SCANNED
         AL    R1,=F'1'            INCREMENT BY 1
         ST    R1,CCSCAN           AND SAVE IT
         SPACE 1
         CLC   0(8,R3),CNAME       IS THIS CSECT THE NAME WE WANT ?
         BE    BUILD               YES - BRANCH
         SPACE 1
LOOPESD2 LA    R3,16(,R3)          BUMP TO NEXT ESD ENTRY
         AL    R5,=F'1'            KEEP COUNT OF ESD ENTRY #
         B     LOOPESD             AND LOOK AT IT
         SPACE 2
         EJECT
*
*        BUILD A PRINT LINE  -
*
BUILD    LA    R7,LINE             R7 WILL POINT TO DETAIL PRINT LINE
         USING DETLINE,R7          -
         MVI   LINE,C' '           BLANK THE PRINT LINE
         MVC   LINE+1(131),LINE    -
         SPACE 1
         L     R2,DIRPOINT         POINT TO DIRECTORY ENTRY
         SPACE 1
*        COUNT # OF HITS FOUND  -
         L     R1,CCHIT            GET # OF HITS
         AL    R1,=F'1'            INCREMEMNT BY 1
         ST    R1,CCHIT            AND SAVE IT
         SPACE 1
*        ID/LENGTH  -
         UNPK  DWA(7),13(4,R3)
         MVC   DETIDL,DWA
         TR    DETIDL,TABLE1
         SPACE 1
*        SEGMENT #  -
         UNPK  DWA(3),12(2,R3)
         MVC   DETSEGN,DWA
         TR    DETSEGN,TABLE1
         SPACE 1
*        ADDRESS  -
         UNPK  DWA(7),9(4,R3)
         MVC   DETADDR,DWA
         TR    DETADDR,TABLE1
         SPACE 1
*        TYPE  IN HEX  -
         UNPK  DWA(3),8(2,R3)
         MVC   DETTYPC,DWA
         TR    DETTYPC,TABLE1
*        TYPE   -
         LA    R15,TYPTABL         POINT TO TABLE END
         LA    R1,TYPTAB           POINT TO TABLE
         MVC   DWA(1),8(R3)        GET SEGMENT TYPE
         NI    DWA,X'0F'
NEXTYPE  CLC   0(1,R1),DWA         TRY TO MATCH WITH A TABLE ENTRY
         BE    GOTYPE              GOT A HIT - BRANCH
         CR    R1,R15              IS THIS THE  END OF THE TABLE  ?
         BNL   NOTYPE              YES - BRANCH
         LA    R1,3(,R1)           INDEX ALONG THE TABLE
         B     NEXTYPE             AND TRY NEXT ENTRY
GOTYPE   MVC   DETTYP,1(R1)        MOVE TYPE TO PRINT LINE
         SPACE 1
*        NAME   -
NOTYPE   MVC   DETNAME,0(R3)       MODULE NAME
         SPACE 1
*        MODULE ENTRY POINT  -
         UNPK  DWA(7),27(4,R2)
         MVC   DETEP,DWA
         TR    DETEP,TABLE1
         SPACE 1
*        MODULE SIZE  -
         UNPK  DWA(7),22(4,R2)
         MVC   DETSIZE,DWA
         TR    DETSIZE,TABLE1
         SPACE 1
*        MODULE ATTRIBUTES  -
         UNPK  DWA(5),20(3,R2)
         MVC   DETATTR,DWA
         TR    DETATTR,TABLE1
         SPACE 1
*        TTRC  -
         UNPK  DWA(9),8(5,R2)
         MVC   DETTTRC,DWA
         TR    DETTTRC,TABLE1
         SPACE 1
*        MEMBER NAME  -
         MVC   DETMEM,0(R2)
         SPACE 2
         BAL   R9,WRITE            PUT A LINE TO THE PRINTER
         B     LOOPESD2
         EJECT
***********************************************************************
*                                                                     *
*        WRITE A LINE TO THE PRINTER                                  *
*                                                                     *
***********************************************************************
         SPACE 1
WRITE    ST    R9,SAVE9
*
*        TEST  FOR HEADINGS REQUIRED  -
*
         CP    LINENO,MAXLINE      IS IT BOTTOM OF PAGE YET  ?
         BL    NOHEAD              NO - BRANCH
         SPACE 1
         ZAP   LINENO,=P'0'        RESET LINE COUNTER
         AP    PAGE,=P'1'          INCREMENT PAGE #
         SPACE 1
         MVI   HCNTL,X'8B'         SKIP TO 01
         MVI   HLINE,C' '          BLANK HEADING PRINT LINE
         MVC   HLINE+1(131),HLINE  -
         LA    R2,HCNTL            POINT TO PRINT LINE
         BAL   R9,PRINT            GO PRINT
         SPACE 1
         MVC   HLINE+110(6),PAT02
         ED    HLINE+110(6),PAGE   EDIT PAGE #
         MVC   HLINE+106(4),=C'PAGE'
         MVC   HLINE+90(7),PAT01
         ED    HLINE+90(7),DATE+1  EDIT DATE  YY.DDD
         MVI   HLINE+90,X'40'      STUFF FIRST BYTE W/BLANK   SBG 03/00
         MVC   HLINE+83(5),=C'DATE:'
         MVC   HLINE+30(L'HEAD01),HEAD01
         MVC   HLINE(8),JNAME
         MVI   HCNTL,X'09'
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE+90(7),PAT03
         ED    HLINE+90(7),TIM     EDIT TIME  HH.MM
         MVC   HLINE+83(5),=C'TIME:'
         MVI   HCNTL,X'19'
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 2
*        PUT DSNAME = AND VOLSER =  -
         LA    R1,JFCB             POINT TO JFCB
         MVC   HLINE+25(44),0(R1)  MOVE DSNAME
         MVC   HLINE+16(8),=C'DSNAME ='
         MVI   HCNTL,X'09'
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         LA    R1,JFCB             POINT TO JFCB
         MVC   HLINE+25(6),118(R1) MOVE VOLSER
         MVC   HLINE+16(8),=C'VOLUME ='
         MVI   HCNTL,X'19'
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE(132),HEAD02
         MVI   HCNTL,X'09'
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE(132),HEAD03
         MVI   HCNTL,X'09'
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE(132),HEAD04
         MVI   HCNTL,X'19'
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 2
NOHEAD   AP    LINENO,=P'1'        INCREMENT LINE #
         LA    R2,CNTL             POINT TO DETAIL PRINT LINE
         MVI   CNTL,X'09'
         BAL   R9,PRINT            PRINT IT
         L     R9,SAVE9            RESTORE RETUTN REGISTER
         BR    R9                  RETURN
         EJECT
***********************************************************************
*                                                                     *
*        END OF THE PDS - CLEAN UP AND EOJ                            *
*                                                                     *
***********************************************************************
         SPACE 1
*        CLOSE DCBS FOR THE PDS  -
ENDPDS   CLOSE (MEMDCB,,DIRDCB)    ISSUE CLOSE
         SPACE 1
*        FREE THE BUFFER USED TO READ THE MEMBERS  -
         L     R0,BUFFER           GET LENGTH AND S.P.
         L     R1,DECB+12          POINT TO BUFFER
         FREEMAIN R,LV=(0),A=(1)   ISSUE FREEMAIN
         SPACE 1
*        FREE  THE BUFFERS USED BY THE DIRECTORY DCB  -
         FREEPOOL DIRDCB
         SPACE 1
*        ISSUE THE END MSG. INDICATING # OF HITS  -
         MVC   ENDMSGN,CNAME       PUT NAME OF SEARCH FIELD IN MSG
         L     R1,CCHIT            GET # OF HITS
         CVD   R1,DWA              CVD
         ED    ENDMSGP,DWA+4
         MVI   CNTL,X'1B'          SPACE 3 IMMED.
         BAL   R9,WRITE
         MVI   CNTL,X'1B'          SPACE 3 IMMED.
         BAL   R9,WRITE
         MVI   CNTL,X'09'
         MVC   LINE+10(ENDMSGL),ENDMSG
         BAL   R9,WRITE
         SPACE 1
         LA    R1,ENDMSGW
         WTO   MF=(E,(1))
         SPACE 2
         CLOSE (SYSPRINT)          CLOSE SYSPRINT
         SPACE 1
         FREEPOOL SYSPRINT         FREE THE SYSPRINT BUFFER POOL
         SPACE 1
GOBACK   L     R13,4(,R13)
         RETURN (14,12),T,RC=(15)
         EJECT
***********************************************************************
*                                                                     *
*        PRINT A LINE SUBROUTINE                                      *
*                                                                     *
***********************************************************************
PRINT    LR    R0,R2               POINT TO LINE TO PRINT
         PUT   SYSPRINT            PUT IT
         MVI   0(R2),C' '          BLANK OUT THE LINE WE PRINTED
         MVC   1(131,R2),0(R2)     -
         BR    R9                  RETURN
         EJECT
***********************************************************************
*                                                                     *
*        ERROR CONDITIONS                                             *
*                                                                     *
***********************************************************************
         SPACE 1
*        NO PARMS PASSED ON ENTRY  -
ERROR1   EQU   *
         LA    R1,WTOMSG1          POINT TO ERROR MESSAGE
         WTO   MF=(E,(1))          WTO
         ABEND 1,DUMP              ERROR IN SORT
         SPACE 1
*        ERROR IN RDJFCB  -
ERROR2   LA    R2,MEMDCB           POINT TO DCB
         ABEND 2,DUMP
         SPACE 1
*        ERROR IN OBTAIN  -
ERROR3   LA    R2,CAMLIST          POINT TO CAMLST
         ABEND 3,DUMP
         SPACE 1
*        DATASET TO BE PROCESSED IS NOT A PDS  -
ERROR4   LA    R2,AREA             POINT TO FORMAT 1 LABEL
         ABEND 4,DUMP
         SPACE 1
*        CANNOT OPEN PDS DCBS  -
ERROR5   LA    R2,DIRDCB           POINT TO DIRECTORY DCB
         LA    R3,MEMDCB           POINT TO MEMBER DCB
         ABEND 5,DUMP
         SPACE 1
*        UNABLE TO OPEN SYSPRINT  -
ERROR6   LA    R2,SYSPRINT         POINT TO SYSPRINT DCB
         ABEND 6,DUMP              ABEND
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*        C O N S T A N T S                                            *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
SAVE     DC    9D'0'               REGISTER SAVE AREA
WORK     DC    D'0'                WORK AREA
SAVE9    DC    F'0'                REG. 9 SAVE AREA
         SPACE 1
*        ERROR MESSAGE ISSUED IF NO PARMS PASSED ON INPUT  -
WTOMSG1  DC    0F'0',Y(WTOMSG1E-*),H'0'
         DC    C'INPUT PARMS MISSING OR INVALID'
WTOMSG1E EQU   *
         SPACE 1
MASK     DC    F'63'               MASK FOR 6 BITS   2 - 7
MASK2    DC    F'31'               MASK FOR 5 BITS   3 - 7
         DC    0H'0'
MOVEPARM MVC   CNAME(0),2(R2)      MOVE PARM FIELD
CNAME    DC    CL8' '              NAME PASSED IN PARM FIELD
         SPACE 1
*        FIELDS RELATING TO THE PDS  -
TTRZ     DC    F'0'                TTR OF MEMBER I AM PROCESSING
         READ  DECB,SF,MEMDCB,0,'S',MF=L                       DECB
         SPACE 1
BUFFER   DC    0F'0',X'0200'       LENGTH + SP FOR GETMAIN
BUFFLEN  DC    H'0'                -
         SPACE 1
CPMEM    DC    F'0'                # OF MEMBERS PROCESSED FROM PDS
CMREC    DC    F'0'                # OF RECORDS READ FOR THIS MEMBER
CPREC    DC    F'0'                # OF READS FOR THE PDS
CCSCAN   DC    F'0'                # OF CSECTS SCANNED
CCHIT    DC    F'0'                # OF CSECTS WE GOT A HIT ON
         SPACE 1
DIRPOINT DC    F'0'                POINTER TO CURRENT MEMBER
LASTMEM  DC    8X'FF'              NAME OF LAST MEMBER IN PDS
EXLST    DC    0F'0',X'87',AL3(JFCB)
         SPACE 1
*        TABLE  USED TO CONVERT ESD TYPE TO PRINTABLE TYPE FORMAT  -
TYPTAB   DC    X'00',C'SD'         SECTION DEFINITION
         DC    X'02',C'ER'         EXTERNAL REFERENCE
         DC    X'03',C'LR'         LABEL REFERENCE
         DC    X'04',C'PC'         PRIVATE CODE
         DC    X'05',C'CM'         COMMON AREA
         DC    X'06',C'PR'         PSEUDO REGISTER
         DC    X'07',C'NL'         NULL
TYPTABL  DC    X'0A',C'WX'         WEAK EXTERNAL REFERENCE
         SPACE 1
*        TRANSLATE TABLE TO CONVERT TO PRINTABLE HEX  -
         ORG   *-240
TABLE1   EQU   *
         ORG
         DC    C'0123456789ABCDEF'
         SPACE 1
         DC    0D'0'
         LTORG
         DC    0D'0'
         SPACE 1
CAMLIST  CAMLST SEARCH,JFCB,JFCB+118,AREA
         SPACE 1
JFCB     DC    0D'0',176X'00'      AREA TO READ THE JFCB INTO
AREA     DC    0D'0',148X'00'      AREA TO CONTAIN THE FORMAT 1 LABEL
DIRBLOCK DC    32D'0'              CURRENT DIRECTORY BLOCK
         SPACE 1
SAVE35   DC    9D'0'               REGISTER SAVE AREA
JNAME    DC    CL8' '              JOBNAME OF THIS JOB
FIELD1   DC    F'0'                POINTER TO TIOT
         SPACE
*        DETAIL PRINT LINE  _
CNTL     DC    X'09'
LINE     DC    132C' '
         SPACE 1
*        HEADING PRINT LINE  -
HCNTL    DC    X'8B'
HLINE    DC    132C' '
         SPACE 1
PAT01    DC    XL7'F021204B202020'                            SBG 03/00
PAT02    DC    XL6'402020202120'
PAT03    DC    XL7'402120204B2020'
         SPACE 1
PAGE     DC    PL3'0'              CURRENT PAGE #
LINENO   DC    PL2'99'             CURRENT LINE #
MAXLINE  DC    PL2'45'             MAX # OF LINES/PAGE
         SPACE 1
DWA      DC    2D'0'               WORK SPACE
TIME     DC    F'0'
TIM      DC    X'00000F'
DATE     DC    F'0'
         SPACE 1
HEAD01   DC    C'T W A - PDS - CSECT SEARCH REPORT'
HEAD02   DC    CL132'         MODULE            LINK           ENTRY   X
                      CSECT                      SG   I.D./'
HEAD03   DC    CL132'          NAME      TTRC   ATTR  LENGTH   POINT   X
                      FOUND     TYPE      ADDR.  NO  LENGTH'
HEAD04   DC    CL132'       --------  --------  ----  ------  ------   X
                    --------  -------    ------  --  ------'
         SPACE 1
*        END MESSAGE - SHOWS NUMBER OF HITS  -
ENDMSGW  DC    0F'0',Y(ENDMSGE-ENDMSGW),H'0'
ENDMSG   DC    C'TOTAL OCCURRENCES OF CSECT NAME '  SPELLING  SBG 03/00
ENDMSGN  DC    CL8'XXXXXXXX'
         DC    C' IS'
ENDMSGP  DC    X'4020202020202120'
ENDMSGE  EQU   *
ENDMSGL  EQU   ENDMSGE-ENDMSG
         SPACE 1
*        D C B S  -
         PRINT NOGEN
*        MEMBER DCB - USED TO READ EACH MEMBER OF THE PDS  -
MEMDCB   DCB   DDNAME=SYSUT1,DSORG=PO,MACRF=R,RECFM=U,EXLST=EXLST,     X
               EODAD=EODAD
         SPACE 1
*        DIRECTORY DCB - USED TO READ THE DIRECTORY OF THE PDS  -
DIRDCB   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,RECFM=F,BLKSIZE=256,    X
               LRECL=256,BUFNO=100
         SPACE 1
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBM,  X
               BLKSIZE=1995
         PRINT GEN
         DC    4D'0'
***********************************************************************
*                                                                     *
*        FORMAT OF THE DETAIL PRINT LINE                              *
*                                                                     *
***********************************************************************
DETLINE  DSECT
         DS    CL7 -
DETMEM   DS    CL8 -               MEMBER NAME
         DS    CL2 -
DETTTRC  DS    XL8 -               TTRC
         DS    CL2 -
DETATTR  DS    XL4 -               MODULE ATTRIBUTES
         DS    CL2 -
DETSIZE  DS    CL6 -               MODULE SIZE
         DS    CL2 -
DETEP    DS    CL6 -               MODULE ENTRY POINT
         DS    CL8 -
DETNAME  DS    CL8 -               NAME OF SEARCH CSECT
         DS    CL2 -
DETTYP   DS    CL2 -               TYPE OF ESD ENTRY
         DS    CL3 -
DETTYPC  DS    XL2 -               TYPE OF ESD ENTRY - IN HEX
         DS    CL4 -
DETADDR  DS    XL6 -               ADDRESS FIELD
         DS    CL2 -
DETSEGN  DS    XL2 -               SEGMENT #
         DS    CL2 -
DETIDL   DS    XL6 -               ID/LENGTH FIELD
         END   WHEREUSD
