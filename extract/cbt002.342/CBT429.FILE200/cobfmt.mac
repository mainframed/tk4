TWA      TITLE 'REPORT ATTRIBUTES OF A COBOL CSECT'
***********************************************************************
*                                                                     *
*        COBFMT                                                       *
*        ------                                                       *
*                                                                     *
*        REPORTS THE COMPILER ATTRIBUTES OF A COBOL CSECT             *
*        CURRENTLY LOADED INTO MEMORY.                                *
*                                                                     *
*        THIS VERSION SUPPORTS THE FOLLOWING COBOL COMPILERS          *
*             VERSION 4, COBOL VS.                                    *
*                                                                     *
*        FOR EACH CSECT, THE FOLLOWING IS LISTED  -                   *
*                                                                     *
*               IS IT COBOL?                                          *
*               IS IT OBJ360 OR OBJ370                                *
*               IS COBOL /TEST/ OPTION SPECIFIED                      *
*               IS COBOL /FLOW/ OPTION SPECIFIED                      *
*               IS COBOL /STATE/ OPTION SPECIFIED                     *
*               IS COBOL /SYMDUMP/ OPTION SPECIFIED                   *
*               IS COBOL /RESIDENT/ OPTION SPECIFIED    3/79          *
*               IS COBOL /ENDJOB/ OPTION SPECIFIED                    *
*               WAS COBOL /OPTIMIZE/ OPTION SPECIFIED - V4 ONLY       *
*               WAS CAPEX OPTIMIZE SPECIFIED                          *
*               WAS CAPEX DTECT SPECIFIED             - CAPEX ONLY    *
*               WAS COBOL V4 OR COBOL VS UTILIZED FOR COMPILE         *
*               WAS COBOL /COUNT/ OPTION SPECIFIED    - VS ONLY       *
*               WAS COBOL /TRACE/ VERB SPECIFIED      - VS ONLY       *
*               DATE AND TIME COMPILED                - COBOL ONLY    *
*                                                                     *
*        A CHECK FOR "WITHIN PRODUCTION STANDARDS" IS ALSO MADE.      *
*        NO DEBUGGING OPTIONS ARE PERMITTED TO EXIST WITHIN THE       *
*        TWA ONLINE SYSTEMS; CAPEX OPTIMIZATION IS REQUIRED,          *
*        OBJ370 IS REQUIRED, RES IS REQUIRED, AND ENDJOB IS           *
*        REQUIRED IN AT LEAST ONE ALWAYS EXECUTED PGM WITHIN A        *
*        RUN UNIT.  IF THESE CONDITIONS ARE NOT TRUE "NON-STD"        *
*        IS NOTED ON THE OUTPUT RECORD.                               *
*                                                                     *
*        MODIFIED 3/10/80                                             *
*        -     CORRECTED BUG IN TGT IDENTIFIER CHECK (WAS TESTING     *
*              MORE FIELD THAN WAS PRESENT)                           *
*        -     ADDED PASS BACK OF STATISTICS TABLE TO CALLER          *
*              CALLER MAY IGNORE IF DESIRED, BUT ALTERS CALLER'S      *
*              PARAMETER LIST                                         *
*        -     INSERTED TITLES WITHIN STATISTICS TABLE                *
*                                                                     *
*                                                                     *
*        WRITTEN BY REX WIDMER                                        *
*                                                                     *
*        TRANS WORLD AIRLINES INC.                                    *
*        KANSAS CITY ADMINISTRATIVE CENTER                            *
*        11500 AMBASSADOR DRIVE                                       *
*        KANSAS CITY, MO 64153                                        *
*                                                                     *
*        816-464-6671                                                 *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        PARAMETERS PASSED BY CALLER  (COBREAD)                       *
*                                                                     *
*           1.  NOT USED - COBFMT RETURNS ADDRESS OF COUNT TABLE      *
*           2.  ADDRESS OF CSECT INFO BLOCK                           *
*                                                                     *
*        CSECT INFO BLOCK CONTAINS                                    *
*           1.  FULLWORD ADDRESS OF START OF CSECT IN MEMORY          *
*           2.  FULLWORD LENGTH OF CSECT                              *
*           3.  CSECT NAME                                            *
*                                                                     *
***********************************************************************
         EJECT
COBFMT   CENTER TYPE=C
         SYMREGS
         EJECT
         SPACE 1
*        REGISTER USAGE
         SPACE 1
*        R2
*        R3
*        R4    POINTS TO CSECT STARTING POINT/INIT1 POINTER
*        R5    POINTS TO COBOL PGMS TGT
*        R6
*        R7
*        R8
*        R9    POINTS TO SYSPRINT DCB
*        R10   POINTS TO START OF CSECT TO PROCESS
*        R11   CONTAINS LENGTH OF CSECT
*        R12   PROGRAM BASE
         EJECT
         MVC   0(4,R1),=A(CTTGT)   RETURNS ADDRESS OF COUNT TABLE
         L     R10,4(,R1)          GET ADDRESS OF CSECT INFO BLOCK
         MVI   PRTMSG,C' '         CLEAR OLD DATA FROM PRINT LINE
         MVC   PRTMSG+1(132),PRTMSG
         MVC   CID,=C'    CS-'
         MVC   CNAME(8),8(R10)    GET CSECT NAME INTO PRINT LINE
         UNPK  CLENGTH,4(5,R10)   FORMAT CSECT LENGTH FOR PRINTING
         TR    CLENGTH,TABLE1     FORMAT HEX INTO PRINTABLE
         MVI   CLENGTH+L'CLENGTH-1,C' ' BLANK AFTER LAST DIGIT
         LM    R10,R11,0(R10)     GET CSECT INFO BLOCK VALUES
         L     R4,0(,R1)           PICK UP CSECT STARTING POINT
         C     R11,=F'450'         IS MODULE POSSIBLY COBOL
         BL    NOTCOB              TOO SHORT TO BE COBOL
         SPACE 1
         LR    R4,R10
         CLC   0(2,R4),=XL2'0700'  IS IT A PADDED NOP E.P. CODE
         BNE   TRYEP
         CLC   X'E'(8,R4),=CL8' '  IS IT PADDED E.P.
         BNE   MAINLINE
         L     R4,X'16'(,R4)       GET THE REAL POINTER
         B     MAINLINE
TRYEP    CLC   X'C'(8,R4),=CL8' '  IS IT ENTRY POINT CODING
         BNE   MAINLINE
*        GOT AN ENTRY POINT SITUATION, GET THE START POINTER
         L     R4,X'14'(,R4)   GET THE REAL POINTER
         LA    R4,0(,R4)           CLEAR THE HIGH BYTE
         LR    R8,R10              GET THE CSECT BASE ADDR
         AR    R8,R11              ADD ON THE LENGTH
         CR    R8,R4               SEE IF GENERATED ADDRESS IS VALID
         BL    NOTCOB              IF OUTSIDE OF CSECT - TRASH
         SPACE 1
*
*        R4 NOW POINTS TO THE START OF THE CSECT
*
*        FIRST LOOK FOR IDENTIFIER WITHIN THE INITIALIZATION CODE
MAINLINE CLC   X'14'(4,R4),=CL4'ANS4' IS IT COBOL VERSION 4
         BE    ITSV4                   THEN HANDLE IT
         CLC   X'14'(4,R4),=CL4'VSR1' IS IT COBOL VS (REL 1 OR 2)
         BNE   NOTCOB                  ELSE IT IS WHO KNOWS WHAT
         MVC   FLAGA,=CL2'VS'         SHOW IT IS VS COMPILER
         AP    CTVSC,=P'1'            COUNT IT UP
         B     COUNTCOB               AND CONTINUE WITH PROCESSING
ITSV4    DS    0H                     TO HANDLE COBOL V4 MODULES
         MVC   FLAGA,=CL2'V4'         SHOW IT IS V4 COMPILER
         AP    CTV4C,=P'1'            COUNT IT UP
COUNTCOB AP    CTCOB,=P'1'         COUNT NUMBER COBOL
         L     R5,60(,R4)     GET R13 POINTER ADCON (TGT POINTER)
*                             TO ADDRESS THE PGMS TASK GLOBAL TABLE
         ST    R5,TGTPTR              SAVE FOR OTHER USAGE
         ST    R4,INIT1PTR            SAVE FOR OTHER USAGE
         CLC   X'1BC'(8,R5),=CL8'SYSOUT  ' CHECK FOR GOOD TGT
         BNE   NOTTGT                       GOT PROBLEMS UNDERSTANDING
         EJECT
*
*        SET FLAGS IN DETAIL PRINT LINE  -
*        BASED UPON THE SWITCH WORD IN THE TGT
*
*        SWITCH BIT MEANINGS - SOURCE COBOL COMPILER PLM
*         BYTE 1  BIT 0 MASK X'80'  USED BY ON SIZE ERROR
*         BYTE 1  BIT 1 MASK X'40'  USED BY READY TRACE VERB
*         BYTE 1  BIT 2 MASK X'20'  INITIALIZATION FLAG
*         BYTE 1  BIT 3 MASK X'10'  MAIN PROGRAM FLAG
*         BYTE 1  BIT 4 MASK X'08'  SYMDUMP FLAG
*         BYTE 1  BIT 5 MASK X'04'  FLOW FLAG
*         BYTE 1  BIT 6 MASK X'02'  STATE FLAG / REUSED AT RUN TIME
*         BYTE 1  BIT 7 MASK X'01'  OPTIMIZATION FLAG
*         BYTE 2  BIT 0 MASK X'80'  RESERVED
*         BYTE 2  BIT 1 MASK X'40'  USED BY CALL AND CANCEL
*         BYTE 2  BIT 2 MASK X'20'  EXEC TIME STATE FLAG
*         BYTE 2  BIT 3 MASK X'10'  TEST ATTRIBUTE FLAG
*         BYTE 2  BIT 4 MASK X'08'  QUOTE IS APOST FLAG
*         BYTE 2  BIT 5 MASK X'04'  SPECIAL SYMDUMP USAGE
*         BYTE 2  BIT 6 MASK X'02'  LONG TGT FLAG, ALWAYS 1 FOR V4 / VS
*         BYTE 2  BIT 7 MASK X'01'  Q-ROUTINE USAGE
*         BYTE 3  BIT 0 MASK X'80'  NORES FLAG, SET TO 0 FOR RES OPTION
*         BYTE 3  BIT 1 MASK X'40'  ENDJOB WAS SPECIFIED
*         BYTE 3  BIT 2 MASK X'20'  OBJECT COMPUTER 370 SPECIFIED
*         BYTE 3  BIT 3 MASK X'10'  Q-ROUTINE FLAG
*         BYTE 3  BIT 4 MASK X'08'  COUNT OPTION - VS
*         BYTE 3  BIT 5 MASK X'04'  TRACE VERB IS PRESENT
*         BYTE 3  BIT 6 MASK X'02'  SYNADAF USAGE
*         BYTE 3  BIT 7 MASK X'01'  UNUSED BIT
*         BYTE 4  DEC POINT IS COMMA BYTE
*                 C',' IF SPECIFIED, ELSE C'.'
         EJECT
         TM    X'48'(R5),X'08' IS IT SYMDUMP
         BZ    X01
         MVC   FLAG1,=CL8'SYMD'
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         AP    CTSYM,=P'1'         COUNT NUMBER USING SYMDUMP
X01      DS    0H
         SPACE 1
         TM    X'48'(R5),X'04' IS IT FLOW
         BZ    X02
         MVC   FLAG2,=CL8'FLOW   '
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         AP    CTFLO,=P'1'         COUNT NUMBER USING FLOW
X02      DS    0H
         SPACE 1
         TM    X'48'(R5),X'02' IS IT STATE
         BZ    X03
         MVC   FLAG3,=CL8'STATE  '
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         AP    CTSTA,=P'1'         COUNT NUMBER USING STATE
X03      DS    0H
         SPACE 1
         TM    X'49'(R5),X'10' IS IT TEST
         BZ    X04
         MVC   FLAG4,=CL8'TEST   '
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         AP    CTEST,=P'1'         COUNT NUMBER USING TEST
X04      DS    0H
         SPACE 1
         TM    X'4A'(R5),X'80' IS IT RESIDENT
         BO    X05
         MVC   FLAG5,=CL8'RES    '
         AP    CTRES,=P'1'         COUNT NUMBER USING RES
         B     X05A
X05      DS    0H
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
X05A     DS    0H
         SPACE 1
         TM    X'4A'(R5),X'40' IS IT ENDJOB
         BZ    X06
         MVC   FLAG6,=CL8'ENDJOB '
         AP    CTEND,=P'1'         COUNT NUMBER USING ENDJOB
         B     X06A                AND CONTINUE THE SEARCH
X06      DS    0H
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
X06A     DS    0H
         SPACE 1
*        LOOK FOR MODULES THAT HAVE RES   BUT NOT ENDJOB  -
         TM    X'4A'(R5),X'80' IS IT RES
         BO    X06B
         TM    X'4A'(R5),X'40' IS IT ENDJOB
         BO    X06B
         AP    CTDNE,=P'1'         NUMBER USING RES   BUT NO ENDJOB
X06B     DS    0H
         SPACE 1
         TM    X'4A'(R5),X'20' IS IT OBJECT 370
         BZ    X07
         MVC   FLAG7,=CL8'OBJ370 '
         AP    CTOBJ,=P'1'         COUNT NUMBER USING OBJ 370
         B     X07A
X07      DS    0H
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
X07A     DS    0H
         SPACE 1
         TM    X'48'(R5),X'01' IS IT COBOL OPTIMISED
         BZ    X08
         MVC   FLAG8,=CL8'COBOPT '
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         AP    CTOPT,=P'1'         COUNT NUMBER OPTIMISED
         B     X08A                IT CANNOT THEREFORE BE CAPEX OPT.
X08      DS    0H
         CLC   X'48'(6,R4),=CL6'CAPEX'  IS IT CAPEX OPTIMISED ?
         BE    X08B
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         B     X08A
X08B     MVC   FLAG8,=CL8'CAPEX  '
         AP    CTCAP,=P'1'         COUNT NUMBER OPTIMISED
         MVC   DATECOMP,X'48'+X'27'(R4) PICK UP CAPEX'S DATE & TIME
*                                  STAMP I.E. OCT 22,1979 18.30.40
         L     R14,INIT1PTR        GET INIT1 POINTER
         L     R14,X'38'(,R14)     GET PGT POINTER
         S     R14,=F'8'           THIS IS THE FULLWORD FOR CAPEX
         L     R14,0(,R14)         GET ITS VALUE
         LTR   R14,R14                 IS DTECT POSSIBLE
T0       BZ    X08A                 NO, CONTINUE
*        EITHER DTECT, XCOUNT, OR PFLOW IS ON
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
T1       A     R14,TGTPTR           POINT TO OPTION BYTE
*        WE NOW HAVE THE CAPEX OPTION BYTE
T2       TM    0(R14),X'60'         IS IT DTECT OR XCOUNT
         BZ    X08A
         MVC   FLAGD,=CL5'DTECT'    FLAG OPTION IS ON
X08A     DS    0H
         CLC   FLAGA,=C'VS'        CHECK FOR VS ONLY OPTIONS
         BNE   NOTVS
         TM    X'4A'(R5),X'08'     SEE IF COUNT WAS SPECIFIED
         BZ    X09                 NO COUNT OPTION
         MVC   FLAGB,=C'COUNT'     PUT IT IN MSG
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         AP    CTCNT,=P'1'         COUNT NUMBER WITH OPTION
X09      DS    0H
         TM    X'4A'(R5),X'04'     WAS TRACE VERB IN PGM
         BZ    X10                  NO, DO NOT FLAG IT ON LISTING
         MVC   FLAGC,=C'TRACE'     PUT IT IN MSG
         MVC   FLAGS,NONSTD        SHOW OUTSIDE OF PROD STANDARDS
         AP    CTTRC,=P'1'         COUNT NUMBER WITH OPTION
NOTVS    DS    0H
X10      DS    0H
DONEFMT  DS    0H
*        PUT   (R9),PRTMSG
         LA    R1,POINTR           SET UP PRINT ROUTINE PARM LIST PTR
         L     R15,=V(PRINTR)      POINT TO PRINT ROUTINE
         BALR  R14,R15             CALL THE ROUTINE
         RETRN RC=0
*
*        UNABLE TO FIND THE TASK GLOBAL TABLE  -
*
NOTTGT   MVC   DETMSG(16),=CL16'NO TGT      '
         AP    CTTGT,=P'1'         COUNT NUMBER WITH NO TGT
         B     DONEFMT
         SPACE 1
*
*        THIS LOAD MODULE NOT CREATED BY COBOL  -
*
NOTCOB   MVC   DETMSG(16),=CL16'NOT COBOL   '
         AP    CTNCB,=P'1'         COUNT LOAD MODULES NOT COBOL
         B     DONEFMT
         SPACE 1
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*        C O N S T A N T S                                            *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE 1
MASK     DC    F'63'               MASK FOR 6 BITS   2 - 7
MASK2    DC    F'31'               MASK FOR 5 BITS   3 - 7
TGTPTR   DC    F'0'                POINTER TO CURRENT TGT
INIT1PTR DC    F'0'                POINTER TO CURRENT INIT1 CODE
NONSTD   DC    C'N'                OUT OF STANDARDS MESSAGE
         SPACE 1
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*        COUNTERS FOR EOJ STATISTICS                                  *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
         SPACE 1
*
*
CTTGT    DC    PL4'0'              NUMBER FOR WHICH TGT NOT LOCATED
         DC    CL40'CSECTS FOR WHICH TGT NOT LOCATED'
CTCOB    DC    PL4'0'              TOTAL MEMBERS CREATED BY COBOL
         DC    CL40'CSECTS CREATED BY COBOL'
CTSYM    DC    PL4'0'              TOTAL USING SYMDUMP OPTION
         DC    CL40'CSECTS USING SYMDUMP OPTION'
CTFLO    DC    PL4'0'              TOTAL USING FLOW OPTION
         DC    CL40'CSECTS USING FLOW OPTION'
CTSTA    DC    PL4'0'              TOTAL USING STATE OPTION
         DC    CL40'CSECTS USING STATE OPTION'
CTEST    DC    PL4'0'              TOTAL USING TEST OPTION
         DC    CL40'CSECTS USING TEST OPTION'
CTRES    DC    PL4'0'              TOTAL USING RES OPTION
         DC    CL40'CSECTS USING RES OPTION'
CTEND    DC    PL4'0'              TOTAL USING ENDJOB OPTION
         DC    CL40'CSECTS USING ENDJOB OPTION'
CTOBJ    DC    PL4'0'              TOTAL USING OBJ-370 OPTION
         DC    CL40'CSECTS USING OBJ-370 OPTION'
CTOPT    DC    PL4'0'              TOTAL OPTIMISED USING COBOL OPTION
         DC    CL40'CSECTS OPTIMISED USING COBOL OPTION'
CTCAP    DC    PL4'0'              TOTAL OPTIMISED USING CAPEX
         DC    CL40'CSECTS OPTIMISED USING CAPEX'
CTNCB    DC    PL4'0'              TOTAL LOAD MODULES NOT COBOL
         DC    CL40'CSECTS NOT COBOL'
*
CTDNE    DC    PL4'0'              NUMBER USING RES BUT NOT ENDJOB
         DC    CL40'CSECTS USING RES BUT NOT ENDJOB'
CTV4C    DC    PL4'0'              NUMBER USING COBOL V4 COMPILER
         DC    CL40'CSECTS USING COBOL V4 COMPILER'
CTVSC    DC    PL4'0'              NUMBER USING COBOL VS COMPILER
         DC    CL40'CSECTS USING COBOL VS COMPILER'
CTCNT    DC    PL4'0'              NUMBER USING COUNT OPTION (VS)
         DC    CL40'CSECTS USING COUNT OPTION (VS)'
CTTRC    DC    PL4'0'              NUMBER USING TRACE VERB (VS)
         DC    CL40'CSECTS USING TRACE VERB (VS)'
         DC    X'FF'               SHOW END OF COUNT TABLE
         EJECT
         PRINT GEN
         ORG   *-240
TABLE1   EQU   *
         ORG
         DC    C'0123456789ABCDEF'
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE DETAIL PRINT LINE                              *
*                                                                     *
***********************************************************************
POINTR   DC    A(PRTMSG)
PRTMSG   DC    CL133' '
         ORG   PRTMSG
         DS    CL1 -
CID      DS    CL7 -
CNAME    DS    CL8 -               CSECT NAME
         DS    CL1 -
CLENGTH  DS    CL7 -               LENGTH OF CSECT
         DS    CL1 -
DATECOMP DS    CL20 -
         DS    CL1 -
DETMSG   EQU   *
FLAG1    DS    CL8 -               SYMDUMP
FLAG2    DS    CL8 -               FLOW
FLAG3    DS    CL8 -               STATE
FLAG4    DS    CL8 -               TESTM
FLAG5    DS    CL8 -               RES
FLAG6    DS    CL8 -               ENDJOB
FLAG7    DS    CL8 -               OBJ370
FLAG8    DS    CL8 -               CAPEX
FLAGA    DS    CL2 -               VS OR V4
         DS    C
FLAGB    DS    CL5 -               COUNT FOR VS
         DS    C
FLAGC    DS    CL5 -               TRACE FOR VS
         DS    C
FLAGD    DS    CL5 -               DTECT FOR CAPEX
         DS    C
FLAGS    DS    C                   NON-STD FLAG
CHECKL   EQU   *-PRTMSG            MAXIMUM VALUE IS 133
         ORG
         END
