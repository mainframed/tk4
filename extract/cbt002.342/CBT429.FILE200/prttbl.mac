TWA      TITLE 'PRINT THE COMPARISON RESULTS'
***********************************************************************
*                                                                     *
*        PRTTBL                                                       *
*        ------                                                       *
*                                                                     *
*           PRINTS   THE ATTRIBUTE TABLES GENERATED BY                *
*           PROCESSING 2 COPIES OF A LOAD MODULE.                     *
*                                                                     *
***********************************************************************
         EJECT
PRTTBL   CENTER TYPE=C
         SYMREGS
SPC1     EQU   C' '
SPC2     EQU   C'0'
SPC3     EQU   C'-'
SPCP     EQU   C'1'
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER USAGE                                               *
*                                                                     *
*           R2                                                        *
*           R3    PLIST # 1           BASE                            *
*           R4    PLIST # 2           BASE                            *
*           R5    MESSAGE TABLE AREA  BASE                            *
*           R6    SYSPRINT DCB POINTER                                *
*           R7    LOOP COUNTER FOR PRINTING                           *
*           R8    CURRENT RECORD POINTER WITHIN MESSAGE TABLE         *
*           R9    BAL TO PRINT ROUTINE                                *
*           R10                                                       *
*           R11                                                       *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        PARAMETERS PASSED BY THE CALLER                              *
*                                                                     *
*           #1    PLIST # 1           BASE                            *
*           #2    PLIST # 2           BASE                            *
*           #3    MESSAGE TABLE AREA  BASE                            *
*           #4    SYSPRINT DCB (OPEN) BASE                            *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         LM    R3,R6,0(R1)          PICK UP PASSED PARAMETERS
         USING PLIST,R3                PLIST DSECT (COPY # 1)
*                    R4 ALSO POINTS TO PLIST DSECT (COPY # 2)
         USING MTABL,R5
*
*        GET TIME AND DATE  -
*
         PRINT ON,NOGEN
         TIME  DEC
         ST    R0,TIME             SAVE TIME
         ST    R1,DATE             SAVE DATE
         MVO   TIM,TIME(2)         SAVE TIME
*        GET JOBNAME OF JOB, AND SAVE IT  -
         EXTRACT FIELD1,FIELDS=(TIOT)
         L     R1,FIELD1           POINT TO TIOT
         MVC   JNAME,0(R1)         SAVE JOBNAME
         SPACE 1
         ZAP   LINENO,=PL2'99'     RESET FOR NEW HEADING AT EACH CALL
*
*        ALL INITIALIZATION REGS HAVE BEEN LOADED
*
         EJECT
         L     R7,MSGCCT   GET NUMBER OF RECORDS IN MTABLE
         LA    R8,MSGA     POINT TO FIRST POSSIBLE MESSAGE SLOT
MLOOP    DS    0H          ENTER FOR PRINTING NEXT ONE
         MVC   CNTL(L'MSGA),0(R8) MOVE MESSAGE TO AREA
         BAL   R9,WRITE    WRITE IT OUT
         LA    R8,L'MSGA(,R8) TO THE NEXT RECORD
         BCT   R7,MLOOP    IF MORE TO PRINT
GOBACK   RETRN  RC=0
         EJECT
***********************************************************************
*                                                                     *
*        WRITE A LINE TO THE PRINTER                                  *
*                                                                     *
***********************************************************************
         SPACE 1
WRITE    ST    R9,SAVE9
*
*        TEST  FOR HEADINGS REQUIRED  -
*
         CP    LINENO,MAXLINE      IS IT BOTTOM OF PAGE YET  ?
         BL    NOHEAD              NO - BRANCH
         SPACE 1
         ZAP   LINENO,=P'0'        RESET LINE COUNTER
         AP    PAGE,=P'1'          INCREMENT PAGE #
         SPACE 1
         MVI   HCNTL,SPCP          SKIP TO 01
         MVI   HLINE,C' '          BLANK HEADING PRINT LINE
         MVC   HLINE+1(131),HLINE  -
         LA    R2,HCNTL            POINT TO PRINT LINE
         BAL   R9,PRINT            GO PRINT
         SPACE 1
         MVC   HLINE+110(6),PAT02
         ED    HLINE+110(6),PAGE   EDIT PAGE #
         MVC   HLINE+106(4),=C'PAGE'
         MVC   HLINE+90(7),PAT01
         ED    HLINE+90(7),DATE+1  EDIT DATE  YY.DDD
         MVC   HLINE+83(5),=C'DATE:'
         MVC   HLINE+30(L'HEAD01),HEAD01
         MVC   HLINE(8),JNAME
         MVI   HCNTL,SPC1
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE+90(7),PAT03
         ED    HLINE+90(7),TIM     EDIT TIME  HH.MM
         MVC   HLINE+83(5),=C'TIME:'
         MVI   HCNTL,SPC1
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 2
*        PUT MEMBER AND DATE LINKED -
         MVC   HLINE+15(8),CNAME-PLIST(R3) MEMBER NAME FROM PLIST 2
         MVC   HLINE+06(8),=C'MEMBER ='
         MVC   HLINE+25(10),=C'LINKED ON '
         MVC   HLINE+35(6),DETDTL-DETLINE+PREC-PLIST+1(R3) DATE
         MVC   HLINE+65(8),CNAME-PLIST(R4) MEMBER NAME FROM PLIST 2
         MVC   HLINE+56(8),=C'MEMBER ='
         MVC   HLINE+75(10),=C'LINKED ON '
         MVC   HLINE+85(6),DETDTL-DETLINE+PREC-PLIST+1(R4) DATE
         MVI   HCNTL,SPC3
         LA    R2,HCNTL
         BAL   R9,PRINT
*        PUT DSNAME = AND VOLSER =  -
         LR    R2,R4
         USING PLIST,R2
         MVC   HLINE+15(44),JFCB-PLIST(R3) DSN FROM JFCB IN PLIST 1
         MVC   HLINE+06(8),=C'DSNAME ='
         MVC   HLINE+65(44),JFCB-PLIST(R4) DSN FROM JFCB IN PLIST 2
         MVC   HLINE+56(8),=C'DSNAME ='
         MVI   HCNTL,SPC1
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE+15(6),118+JFCB-PLIST(R3) SER FROM JFCB IN PLIST 1
         MVC   HLINE+06(8),=C'VOLUME ='
         MVC   HLINE+65(6),118+JFCB-PLIST(R4) SER FROM JFCB IN PLIST 2
         MVC   HLINE+56(8),=C'VOLUME ='
         MVI   HCNTL,SPC1
         LA    R2,HCNTL
         BAL   R9,PRINT
         DROP  R2
         SPACE 1
         MVC   HLINE(132),HEAD02
         MVI   HCNTL,SPC3
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE(132),HEAD03
         MVI   HCNTL,SPC1
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 1
         MVC   HLINE(132),HEAD04
         MVI   HCNTL,SPC2
         LA    R2,HCNTL
         BAL   R9,PRINT
         SPACE 2
NOHEAD   AP    LINENO,=P'1'        INCREMENT LINE #
         LA    R2,CNTL             POINT TO DETAIL PRINT LINE
         BAL   R9,PRINT            PRINT IT
         L     R9,SAVE9            RESTORE RETUTN REGISTER
         BR    R9                  RETURN
         EJECT
***********************************************************************
*                                                                     *
*        PRINT A LINE SUBROUTINE                                      *
*                                                                     *
***********************************************************************
PRINT    LR    R0,R2               POINT TO LINE TO PRINT
         PUT   (R6)                PUT IT
         MVI   0(R2),C' '          BLANK OUT THE LINE WE PRINTED
         MVC   1(131,R2),0(R2)     -
         BR    R9                  RETURN
         EJECT
SAVE9    DC    F'0'                REG. 9 SAVE AREA
         SPACE 1
JNAME    DC    CL8' '              JOBNAME OF THIS JOB
FIELD1   DC    F'0'                POINTER TO TIOT
         SPACE
*        DETAIL PRINT LINE  _
CNTL     DC    C' '
LINE     DC    132C' '
         SPACE 1
*        HEADING PRINT LINE  -
HCNTL    DC    C'1'
HLINE    DC    132C' '
         SPACE 1
PAT01    DC    XL7'4021204B202020'
PAT02    DC    XL6'402020202120'
PAT03    DC    XL7'402120204B2020'
         SPACE 1
PAGE     DC    PL3'0'              CURRENT PAGE #
LINENO   DC    PL2'99'             CURRENT LINE #
MAXLINE  DC    PL2'45'             MAX # OF LINES/PAGE
         SPACE 1
TIME     DC    F'0'
TIM      DC    X'00000F'
DATE     DC    F'0'
         SPACE 1
HEAD01   DC    C'T W A - PDS - LOAD MODULE COMPARATOR          '
HEAD02   DC    CL132'                            DATE AND TIME     ----X
               ----------------------- ATTRIBUTE FLAGS ----------------X
               ---------------------'
HEAD03   DC    CL132'                 LENGTH       COMPILED        SYMDX
                  FLOW    STATE   TEST    RES     ENDJOB  OBJECT  OPT  X
                 VER COUNT TRACE DET'
         EJECT
HEAD04   DC    CL132'  ------------------------------------------------X
               --------------------------------------------------------X
               ---------------------'
         SPACE 1
         PRINT NOGEN
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE MESSAGE BUILD AREA  1.00                       *
*                                                                     *
***********************************************************************
MTABL    DSECT
MSGCA    DS    A         ADDR OF CURRENT SLOT
MSGCCT   DS    F         NUMBER OF RECORDS IN USE
MSGMAX   DS    F         MAX NUMBER OF RECORDS AVAILABLE
MSGMAXX  EQU   255       COUNT IN THE NEXT DS
MSGA     DS    255CL133  SPACE FOR RECORDS
MTABLL   EQU   *-MTABL
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE PARAMETER AREA 1.02                            *
*                                                                     *
***********************************************************************
PLIST    DSECT
CNAME    DS    CL8       MEMBER NAME TO PROCESS
JFCB     DC    0D'0',176X'00'      AREA TO READ THE JFCB INTO
PLISTCA  DS    A         ADDR OF CURRENT SLOT
PRECCT   DS    F         NUMBER OF RECORDS IN USE
PRECMX   DS    F         MAX NUMBER OF RECORDS AVAILABLE
PLISTC   EQU   255       COUNT IN THE NEXT DS
PREC     DS    255CL133  SPACE FOR RECORDS
PLISTL   EQU   *-PLIST
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE DETAIL PRINT LINE                              *
*                                                                     *
***********************************************************************
DETLINE  DSECT
         DS    CL1 -
DETMEM   DS    CL8 -               MEMBER NAME
         DS    CL1 -
DETTTRC  DS    XL8 -               TTRC
         DS    CL1 -
DETATTR  DS    XL4 -               MODULE ATTRIBUTES
         DS    CL1 -
DETSIZE  DS    CL6 -               MODULE SIZE
         DS    CL1 -
DETEP    DS    CL6 -               MODULE ENTRY POINT
         DS    CL1 -
DETDTL   DS    CL6 -               DATE LINKED
         DS    CL1 -
DETMSG   EQU   *
FLAG1    DS    CL8 -               SYMDUMP
FLAG2    DS    CL8 -               FLOW
FLAG3    DS    CL8 -               STATE
FLAG4    DS    CL8 -               TESTM
FLAG5    DS    CL8 -               RES
FLAG6    DS    CL8 -               ENDJOB
FLAG7    DS    CL8 -               OBJ370
FLAG8    DS    CL8 -               CAPEX
FLAGA    DS    CL2 -               VS OR V4
         DS    C
FLAGB    DS    CL5 -               COUNT FOR VS
         DS    C
FLAGC    DS    CL5 -               TRACE FOR VS
         DS    C
FLAGD    DS    CL5 -               DTECT FOR CAPEX
LNLGTH   EQU   *-DETLINE
JFCBD    DSECT                    , MAP THE JFCB
         IEFJFCBN
         END
