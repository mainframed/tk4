         TITLE 'ATJ07S: TABLE SORT SUBROUTINE'
*        PARAMETERS:
*        (1) BEGINNING OF TABLE TO BE SORTED -
*        (2) LENGTH OF TABLE ENTRY -            H'1' TO H'32767'
*        (3) NUMBER OF ENTRIES -                H'1' TO H'32767'
*        (4) KEY LOCATION IN RECORD -           H'1' TO H'4096'
*        (5) KEY LENGTH -                       H'1' TO H'256'
*        (6) SEQUENCE BYTE -                    A=ASCENDING;
*
ATJ07S   CSECT
         USING *,R15
         STM   R14,R12,12(R13)         SAVE REGISTERS
         LM    R3,R8,0(R1)             GET PARAMETER ADDRESSES
         CLI   0(R8),C'A'              ASCENDING SEQUENCE?
         BE    *+8                     YES
         MVI   BACKUP+11,X'B0'         ALTER MASK FOR DESCENDING SEQ.
         MVC   HKEYLNG(2),0(R7)        RETRIEVE KEY LENGTH
         LH    R7,HKEYLNG              GET KEY LENGTH
         BCTR  R7,0                    CONVERT TO LENGTH CODE
         STC   R7,BACKUP+5             STORE IN CLC INSTRUCTION
         MVC   HKEYLOC(2),0(R6)        RETRIEVE KEY LOCATION
         LH    R6,HKEYLOC              GET KEY LOCATION
         BCTR  R6,0                    CONVERT TO DISPLACEMENT
         STH   R6,BACKUP+6             STORE IN CLC
         STH   R6,BACKUP+8               INSTRUCTION
         OI    BACKUP+6,16*R5          BASE REGISTERS
         OI    BACKUP+8,16*R6            FOR CLC INSTRUCTION
         MVC   HNUMNRY(2),0(R5)        RETRIEVE NUMBER OF ENTRIES
         LH    R5,HNUMNRY              GET NUMBER OF ENTRIES
         LR    R2,R5                   SAVE FOR USE DURING SORT
         BCTR  R5,0                    DECREMENT BY 1
         MVC   HLNGNRY(2),0(R4)        RETRIEVE LENGTH OF ENTRY
         LH    R8,0(R4)                GET ENTRY LENGTH
         MR    R4,R8                   (# OF ENTRY - 1) X LNG = DISPLCE
         LA    R12,0(R5,R3)            STRT + DISPLCE = ADDR LAST ENTRY
         LR    R10,R8                  LENGTH OF EACH ENTRY
         BCTR  R10,0                   CONVERT TO LENGTH CODE
         STC   R10,UNDER257+1          STORE IN
         STC   R10,UNDER257+7            XC INSTRUCTIONS
         STC   R10,UNDER257+13             FOR EXCHANGE
         SRA   R10,8                   ENTRY LENGTH > 256?
         BZ    HALVE                   NO
         MVI   LOOPSW+1,X'00'          YES, ENABLE EXCHANGE LOOP
         STC   R10,LOOPSW+7            SET UP LOOP COUNTER
*
*        THE FOLLOWING INSTRUCTIONS CONSTITUTE A SHELL SORT.
*
HALVE    SRA   R2,1
         BZ    RETURN
         LR    R7,R3
         LR    R9,R12
         LR    R11,R2
         MR    R10,R8
         SR    R9,R11
LOOP     LR    R5,R7
BACKUP   LA    R6,0(R5,R11)
         CLC   0(0,R5),0(R6)   ********INSTRUCTION BUILT AT ENTRY
         BNH   BXLE            ********BNL FOR DESCENDING SEQUENCE
         LR    R4,R5
LOOPSW   B     UNDER257        ********BRANCH IF ENTRY LENGTH < 257
         LA    R10,0           ********LOOP COUNTER SET UP AT ENTRY
XCLOOP   XC    0(256,R4),0(R6)
         XC    0(256,R6),0(R4)
         XC    0(256,R4),0(R6)
         LA    R4,256(R4)
         LA    R6,256(R6)
         BCT   R10,XCLOOP
UNDER257 XC    0(0,R4),0(R6)
         XC    0(0,R6),0(R4)
         XC    0(0,R4),0(R6)
         SR    R5,R11
         CR    R5,R3
         BNL   BACKUP
BXLE     BXLE  R7,R8,LOOP
         B     HALVE
RETURN   MVI   BACKUP+11,X'D0'         RESET ASCENDING SEQUENCE
         MVI   LOOPSW+1,X'F0'          DISABLE EXCHANGE LOOP
         LM    R14,R12,12(R13)         RESTORE REGISTERS
         BR    R14                     RETURN TO CALLER
R1       EQU   1                       ADDRESS OF PARAMETER LIST
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                      SAVE AREA ADDRESS
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      BASE REGISTER = EPA
HKEYLNG  DC    H'0'                    HOLD KEY LENGTH
HKEYLOC  DC    H'0'                    HOLD KEY LOCATION
HNUMNRY  DC    H'0'                    HOLD NUMBER OF ENTRIES
HLNGNRY  DC    H'0'                    HOLD LENGTH OF AN ENTRY
         END
