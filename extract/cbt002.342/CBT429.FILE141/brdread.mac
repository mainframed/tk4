BRDREAD  CSECT
         STM   R14,R12,12(R13)     SAVE CALLERS REGISTERS
         LR    R12,R15             PICK UP ENTRY POINT
         USING BRDREAD,R12         ESTABLISH ADDRESSABILITY
         LA    R10,SAVEAREA
         ST    R13,4(R10)
         ST    R10,8(R13)
         LR    R13,R10
         USING SAVEAREA,R13
         LR    R11,R1              SAVE PARAMETER POINTER
         PRINT NOGEN
FIRSTSW  NOP   READ                FIRST TIME OPEN SWITCH
         OI    *-3,X'F0'           SET NO-OP TO BRANCH
         OPEN  (BRODFILE)
READ     DS    0H
         L     R11,0(R11)          R11 -> A(RECORD AREA)
         L     R11,0(R11)          R11 -> RECORD AREA
         READ  BRODDECB,SF,BRODFILE,(R11),'S'
         CHECK BRODDECB            WAIT UNTIL READ IS FINISHED
         B     RETURN
EOFILE   DS    0H
         CLOSE (BRODFILE)
         MVI   0(R11),X'88'        SIGNAL END OF FILE
         NI    FIRSTSW+1,X'0F'     SET FIRST TIME SWITCH TO NO-OP
         PRINT GEN
RETURN   DS    0H
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
         PRINT NOGEN
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SAVEAREA DS    18F
BRODFILE DCB   BLKSIZE=129,DDNAME=BRODCST,EODAD=EOFILE,                *
               MACRF=(R),RECFM=F,KEYLEN=1,DSORG=PS
         END
