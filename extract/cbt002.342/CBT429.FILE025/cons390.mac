*******************************************************************
*                                                                 *
*            D I S C L A I M E R    S E C T I O N                 *
*                                                                 *
*******************************************************************
*                                                                 *
* THIS MATERIAL HAS BEEN USED SUCCESSFULLY & TO THE BEST OF OUR   *
* KNOWLEDGE  THEY  ARE  OPERATIONAL, HOWEVER  NO  WARRANTY        *
* IS MADE TO THE ACCURACY OF THE PROGRAMS OR RELATED MATERIAL AND *
* NO RESPONSIBILITY IS ASSUMED  FOR ANY MODIFICATION  DIRECTLY OR *
* INDIRECTLY  CAUSED BY  THEIR USE.  IT IS THE USERS              *
* RESPONSIBILITY  TO  EVALUATE THE  USEFULLNESS  OF THE  MATERIAL *
* SUPPLIED .                                                      *
*                                                                 *
* WE DO NOT GUARANTEE TO  KEEP ANY  MATERIAL PROVIDED  UP TO DATE *
* NOR DO WE GUARANTEE TO  PROVIDE ANY  CORRECTIONS OR  EXTENSIONS *
* DESCRIBED BY THE USER OR  CORRECTIONS OR EXTENSIONS MADE IN THE *
* FUTURE .                                                        *
*                                                                 *
*******************************************************************
         MACRO
         BEGIN &PROGRAM=
R0       EQU   0
R1       EQU   1                  R
R2       EQU   2                    E
R3       EQU   3                      G
R4       EQU   4                        I
R5       EQU   5                          S
R6       EQU   6                            T
R7       EQU   7                              E
R8       EQU   8                                R
R9       EQU   9                  E
R10      EQU   10                   Q
R11      EQU   11                     U
R12      EQU   12                       A
R13      EQU   13                         T
R14      EQU   14                           E
R15      EQU   15                             S
         USING *,15
         B     28(R15)
         DC    CL8'&PROGRAM'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         STM   14,12,12(13)
         DROP  15
         EJECT
         MEND
         TITLE 'CONSOLE DISPLAY'
***********************************************************************
*        FOR AUTHORIZATION - INSERT USERIDS THAT CAN USE IT INTO THE  *
*        TABLE.  TABLEND IS THE LAST ENTRY IN THE TABLE.              *
*                                                                     *
*        F TABLEND  LAST                                              *
*                                                                     *
*        DISPLAY MVS CONSOLE DISPLAY WITH AUTOMATIC REFRESH OF SCREEN *
*        TO PRESENT A CONSOLE APPEARANCE.                             *
*                                                                     *
*        USED THE BASIC CROSS MEMORY CONSOLE BUFFER READ FROM THE     *
*        CDF PROGRAM ON THE SHARE TAPE, AND MODIFIED TO A COMMAND     *
*        PROCESSOR HIGHLIGHTING THE HIGHLIGHTED MESSAGES - P.MURPHY   *
*                                                                     *
*                                                                     *
***********************************************************************
ZTPR0200 CSECT
         BEGIN PROGRAM=ZTPR0200
         LR    R11,R15            LOAD ADDRESS OF ENTRY POINT
         USING ZTPR0200,R11       R11 BASE REGISTER
         LA    R12,4095(,R11)     ADDRESS OF NEXT BASE REGISTER
         USING ZTPR0200+4095,R12  R12 BASE REGISTER
         LR    R2,R1
         GETMAIN R,LV=WORKLENG
         LR    R3,R1
         ST    R13,4(R3)      SAVE THE CALLER'S R13
         ST    R3,8(,R13)
         LR    R13,R3
         USING WORKAREA,R13
         ST    R2,SAVECPPL
         USING CPPL,R2
         L     R6,CPPLPSCB
         DROP  R2
         USING PSCB,R6
         MVC   USERID,PSCBUSER
         DROP  R6
         MVI   BLANKS,X'40'
         MVC   BLANKS+1(79),BLANKS
         MVI   CONSFLAG,X'00'
         MVI   USERFLAG,X'00'
         MVI   CNSLFLAG,X'00'
         MVI   THRUFLAG,X'00'
         MVI   ISSUFLAG,X'00'
         MVC   CMDLINE,BLANKS
         LA    R8,TABLE
VERLOOP  CLC   0(7,R8),USERID
         BE    VEROK
         CLC   0(4,R8),HEXFS
         BE    NOAUTH
         CLC   0(4,R8),TABLEND
         BE    NOAUTH
         LA    R8,8(,R8)
         B     VERLOOP
VEROK    EQU   *
         MVI   AUTOFLAG,X'00' AUTO MODE FLAG
         MVI   RESHOWFL,X'00' RESHOW INDICATOR
         MVI   MOD5FLAG,X'00' FLAG FOR MOD 5
         MVI   SCREEN,X'C3' WCC
         MVC   CLEAR(13),CLR  CLEAR SCREEN CONTROL CHARS
         GTSIZE
         LTR   R0,R0              DISPLAY DEVICE?
         BZ    FREESKIP           YES, GO FREE AND RETURN
         CH    R1,EIGHTY          80 CHAR LINES
         BNE   CHEKM5             NO, SET MODE 2
         CH    R0,FORTY3          MOD 4?
         BE    SETM4              YES, BRANCH
         CH    R0,THIRTY2         MOD 3?
         BE    SETM3              YES, BRANCH
SETM2    TPUT  MOD2,3
         TGET  REPLY,50
         LA    R3,24              22 LINES PER SCREEN
         MVC   CURSOR,=X'5CF0'    ROW 24 COL 2
         MVC   ENDOFSCR,=X'5D7F'    ROW 24 COL 80
         MVC   TPUTLEN,=AL4(24*84+1) LENGTH OF TPUT
         B     PROCESS
NOMVS    MVI   USERFLAG,X'FF'
         LA    R8,8(,R8)
         B     VERLOOP
CHEKM5   CH    R0,=H'27'          27 LINES?
         BNE   SETM2
         CH    R1,=H'132'         132 WIDTH?
         BNE   SETM2
SETM5    LA    R3,27              27 LINES PER SCREEN
         MVI   MOD5FLAG,X'FF'
         MVC   TPUTLEN,=AL4(27*84+1) LENGTH OF TPUT
         MVC   ENDOFSCR,=X'F76B'  ROW 27 COL 132
         MVC   CURSOR,=X'F5E8'    ROW 27 COL 2 IC
         B     PROCESS
SETM3    LA    R3,32              22 LINES PER SCREEN
         MVC   TPUTLEN,=AL4(32*84+1) LENGTH OF TPUT
         MVC   ENDOFSCR,=X'E77E'  ROW 32 COL 80
         MVC   CURSOR,=X'E6F0'    ROW 32 COL 2 IC
         B     PROCESS
SETM4    LA    R3,43              22 LINES PER SCREEN
         MVC   TPUTLEN,=AL4(43*84+1) LENGTH OF TPUT
PROCESS  ST    R3,NUMLINES
         MVI   HELPFLAG,X'00'
         MVI   CMDFLAG,X'00'
         LA    R1,16
         L     R1,0(R1)
         USING CVT,R1
         L     R1,CVTASVT
         DROP  R1
         USING ASVT,R1
         L     R5,ASVTMAXU
         LA    R15,ASVTENTY-4
ASVTLOOP BCTR  R5,0
         LTR   R5,R5
         BZ    CONABEND
         LA    R15,4(,R15)
         ICM   R6,15,0(R15)
         BM    ASVTLOOP
         USING ASCB,R6
         ICM   R7,15,ASCBJBNS
         BZ    ASVTLOOP
         CLC   =C'CONSOLE',0(R7)
         BNE   ASVTLOOP
         SR    R1,R1
         LH    R1,ASCBASID
         DROP  R1,R6
STORECON ST    R1,CONSASID
         BAL   R3,AUTHRIZ
         AXRES AXLIST=AXLIST      RESERVE AN AX
         L     R1,CONSASID        'CONSOLE' ASID
         AXEXT ASID=(1)           EXTRACT AX OF 'CONSOLE'IN R0
         AXSET AX=(0)             SET OUR AX TO THAT OF CONSOLE
         B     SHOWCONS
AXFREE   DS    0H
         AXFRE AXLIST=AXLIST
         BAL   R3,UNAUTH
SHOWRTRN DS    0H
FREESKIP LR    R3,R13
         L     R13,SAVEAREA+4 RESTORE THE CALLER'S R13
         FREEMAIN RC,A=(3),LV=WORKLENG
         SR    R15,R15
         RETURN (14,12),RC=(15)
SHOWCONS DS    0H
         STFSMODE ON,INITIAL=YES  FULL SCREEN MODE ON
         TPUT  CLEAR,13,FULLSCR
*---------------------------------------------------------------------*
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*---------------------------------------------------------------------*
BLDUCMS  L     R4,16              R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         DROP  R4
         USING UCM,R4
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R8,UCMTAB+4        R8 = ADDR OF UCMTAB
         LA    R9,UCMTABE         R9 = ADDR OF END OF UCMTAB
         LR    R5,R4              BASE
         S     R5,=F'4'           R5 = ADDR OF FIRST UCM PREFIX
         USING UCMPRFX,R5
         TM    UCMSFLG1,UCMSYSE   NO CONSOLES?
         BO    CONABEND           YES
         DROP  R5
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         XR    R2,R2              R2 = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(0,R8)         SAVE UCM ADDRESS IN UCMTAB
         LA    R2,1(0,R2)         R2  = R2  + 1  (ONE MORE UCM)
         LA    R8,4(0,R8)         R8 = ADDR OF NEXT UCMTAB ENTRY
         CR    R8,R9              DOES R8 POINT PAST END OF UCMTAB?
         BNL   UCMDONE            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BL    UCMLOOP            NOPE; KEEP GOING
UCMDONE  STH   R2,NUMUCMS         SAVE NUMBER OF UCMS FOUND
         CLI   CNSLFLAG,X'FF'
         BNE   SETMAST
         MVC   OLDCONS,CONSOLE
         L     R4,CONSOLE
         LA    R4,1(,R4)
         B     MASTSCAN
         DROP  R4
*---------------------------------------------------------------------*
*        SCAN THE UCM FOR THE MASTER CONSOLE AS THE DEFAULT           *
*---------------------------------------------------------------------*
SETMAST  SR    R4,R4
         ST    R4,CONSOLE
MASTSCAN LA    R5,UCMTAB          SET POINTER TO UCM ADDRESS TABLE
CKNUMUCM CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   MASTCONT           NO, CONTINUE
         CLI   CNSLFLAG,X'FF'
         BNE   CONCHECK
         CLI   THRUFLAG,X'FF'
         BE    NOCONS
         MVI   THRUFLAG,X'FF'
         SR    R4,R4              START W/ FIRST ENTRY
         B     MASTCONT
CONABEND EQU   *
NOCONS   TPUT  MSGNO2,43
         L     R1,SAVECPPL
         LINK  EP=ZTPR0205
         TPUT  CLEAR,13,FULLSCR
         B     AXFREE
MSGNO2   DC    CL43'ALL CONSOLES OFFLINE, GOING TO MASTER TRACE'
CONCHECK MVI   CNSLFLAG,X'FF'
         MVI   THRUFLAG,X'FF'
         B     SETMAST
MASTCONT SLL   R4,2               MAKE CONSOLE NUMBER TABLE INDEX
         LA    R5,0(R5,R4)        SET POINTER TO UCM ADDRESS SLOT
         L     R5,0(0,R5)         SET POINTER TO UCM
         LTR   R5,R5
         BZ    NEXT
         USING UCMLIST,R5         TELL ASSEMBLER ABOUT UCM
         CLI   CNSLFLAG,X'FF'
         BE    SKIPMSTR
         TM    UCMDIDCS,UCMAHERE  ON THIS SYSTEM?
         BZ    NEXT               NO ? BRANCH
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?
         BNZ   ACTIVCHK           YES ? FOUND THE MASTER CONSOLE ..
         B     NEXT
SKIPMSTR EQU   *
         TM    UCMDIDCS,UCMAHERE  ON THIS SYSTEM?
         BZ    NEXT               NO ? BRANCH
         TM    UCMDISP1,UCMDISPC  GRAPHICS
         BZ    NEXT
ACTIVCHK TM    UCMATR,UCMUF       ACTIVE?
         BNZ   MASTFND
NEXT     SRL   R4,2               RESET CONSOLE NUMBER TO RELATIVE
         LA    R4,1(0,R4)         SET POINTER TO NEXT CONSOLE
         B     MASTSCAN           CONTINUE MASTER CONSOLE SCAN LOOP
MASTFND  SRL   R4,2               RESET CONSOLE NUMBER TO RELATIVE
         ST    R4,CONSOLE         SET VALID CONSOLE NUMBER (MASTER)
         DROP  R5                 DONT NEED UCM ADDRESSABILITY NOW
NEXTPAGE LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         ICM   R4,15,CONSOLE      R4 = CONSOLE TO BE DISPLAYED
         BZ    WRTERR4            WE HAVE NO CONSOLE ZERO (MASTER)
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(0,R5)         R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
         ST    R6,UCMTABX
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
WRTERR4  DS    0H
         B     RESETCN            RESET THE CONSOLE NUMBER
GRAPHICS EQU   *
         L     R6,UCMFEXTP        UCM FIXED EXTENSION
         USING UCMEFEXT,R6        ADDRESSABILITY
         MVC   USER,UCMEFCNM      LOGON ID
         DROP  R6
AUTHDONE EQU   *
         MVC   OLDCONS,CONSOLE
         MVC   HEADING,HEADING1
***********************************************************************
*    FOR SYSH UNCOMMENT THE FOLLOWING 2 LINES TO LOOK AT ALTERNATE    *
***********************************************************************
*        CLI   CONSFLAG,X'FF'
*        BNE   CNSLSW
         CLI   ISSUFLAG,X'FF'
         BNE   CONSINIT
         MVC   HEADING+45(18),NOTISSUD
         MVI   ISSUFLAG,X'00'
CONSINIT MVI   CNSLFLAG,X'00'
*        BAL   R3,AUTHRIZ
         L     R1,CONSASID        'CONSOLE' ASID
         SSAR  R1                 SET 'CONSOLE' AS SECONDARY
         PRINT ON,GEN
         SLR   R0,R0              SECONDARY KEY
         L     R6,UCMTABX
         LA    R1,4               TRUE LENGTH
         MVCP  RDCM(R1),0(R6),R0  ADDRESS OF PAGEABLE DCM
         LA    R1,1
         MVCP  TYPE(R1),15(R6),R0       ATTRIB
         L     R7,RDCM
         USING STRTDCM,R7
         LA    R1,L'LASTLINE      TRUE LENGTH
         MVCP  LASTLINE(R1),DCMINPUT-STRTDCM(R7),R0
         LA    R1,L'SIBPTR
         SLR   R0,R0              SET STORAGE KEY
         MVCP  SIBPTR(R1),DCMASCRN-STRTDCM(R7),R0 PT TO SCRN ADRJPB
         LA    R1,2
         MVCP  HEADING+8(R1),DCMDEL-STRTDCM(R7),R0   ROLL DELETE VAL
         MVCP  RTME(R1),DCMRTME-STRTDCM(R7),R0       RTME
         MVCP  RNUM(R1),DCMDL-STRTDCM(R7),R0       RTME
         LA    R1,4
         MVCP  CONL(R1),DCMLSCRN-STRTDCM(R7),R0
         L     R8,SIBPTR         R8 = ADDR OF SCREEN IMAGE
         L     R5,CONL
         SR    R4,R4
         SR    R5,R8
         TM    TYPE,X'14'        SUPPORT EXTENDED COLOR/DATASTREAM?
         BO    EXTLNGTH          YES, BRANCH
         LA    R5,84(,R5)
         B     REGLNGTH
EXTLNGTH LA    R5,90(,R5)        ADD LENGTH FOR LAST LINE
REGLNGTH ST    R5,CONL           SAVE IT
         LA    R4,SCREEN         MY BUF
         LA    R4,3737(,R4)
         LR    R9,R5             BUFFER LEN
MOVEBUFF DS    0H
         TM    TYPE,X'14'        SUPPORT EXTENDED COLOR/DATASTREAM?
         BO    MOVEL
         SH    R8,=H'06'
         B     RESUMMOV
MOVEL    SH    R8,=H'12'
RESUMMOV SLR   R0,R0             RESET THE KEY
MVCPLOOP DS    0H
         MVCP  0(R5,R4),0(R8),R0 LOOP THRU THE CONSOLE BUFFER
         BZ    GOTITALL
         AL    R8,=F'256'        MAX LEN ALLOWED
         AL    R4,=F'256'        MAX LEN ALLOWED
         SL    R5,=F'256'        MAX LEN ALLOWED
         B     MVCPLOOP
*---------------------------------------------------------------------*
*        IN ORDER TO ISSUE SVCS THE PRIMARY AND                       *
*        SECONDARY ADDRSPC MUST BE THE SAME                           *
*---------------------------------------------------------------------*
GOTITALL DS    0H
         EPAR  R1                EXTRACT PRIMARY ASN
         SSAR  R1
*        BAL   R3,UNAUTH
         DROP  R5,R7
         LA    R3,SCREEN         SET POINTER TO FIRST LINE OF BUF
         LA    R3,3737(,R3)
         LA    R9,HEADING+10
         CLI   HEADING+9,C' '
         BNE   RDMODE
         LA    R9,HEADING+9
RDMODE   MVC   0(6,R9),RTMEC
         SR    R5,R5
         LH    R5,RTME
         CVD   R5,DOUBLWD
         MVC   WORKLINE(5),PATTERN1
         ED    WORKLINE(5),DOUBLWD+5
         MVC   6(4,R9),WORKLINE+1
         CLI   9(R9),C'0'
         BE    PUTBLNK
         MVC   7(2,R9),=C'1/'
         B     PUTRNUM
PUTBLNK  MVI   9(R9),C' '
PUTRNUM  MVC   10(6,R9),RNUMC
         SR    R5,R5
         IC    R5,RNUM+1
         CVD   R5,DOUBLWD
         MVC   WORKLINE(5),PATTERN1
         ED    WORKLINE(5),DOUBLWD+5
         MVC   16(3,R9),WORKLINE+2
         TIME  DEC
         ST    R0,WORK
         MVI   WORK+3,X'0C'
         UNPK  DOUBLWD(7),WORK(4)
         LA    R9,HEADING+36
         MVC   0(2,R9),DOUBLWD
         MVC   3(2,R9),DOUBLWD+2
         MVC   6(2,R9),DOUBLWD+4
         MVC   28(8,R9),USER
         MVC   10(10,R9),BLANKS
         CLI   AUTOFLAG,X'FF'
         BNE   FIRSTL
         MVC   10(10,R9),=C'AUTOUPDATE'
FIRSTL   L     R9,NUMLINES       NUMBER OF LINES ON PROG TERMINAL
         L     R5,CONL
         SR    R4,R4
         LA    R1,90
         TM    TYPE,X'14'
         BO    DIV1
         LA    R1,84
DIV1     DR    R4,R1
         LA    R5,2(,R5)
         ST    R5,CONL
         CR    R9,R5
         BNL   FIRSTL1
         SR    R5,R9
         L     R1,CONL
         SR    R4,R4
         LA    R5,2(,R5)
         SR    R1,R5
         ST    R1,CONL
         LA    R1,90
         CLI   TYPE,X'80'
         BNE   MUL1
         LA    R1,84
MUL1     MR    R4,R1
         LA    R3,0(R5,R3)
FIRSTL1  LA    R5,SCREEN+1            ADDRESS OF FIRST LINE
         LA    R6,PAGEDEF             LINES TO BE DISPLAYED
*        AP    HEADING(2),HEADING(2)
         CLI   MOD5FLAG,X'FF'         MOD 5?
         BNE   NOTMOD5
         LA    R6,PAGEDEF5
NOTMOD5  L     R1,CONL
         SR    R9,R1
         SR    R10,R10
         BCTR  R9,0                   MINUS ONE
         MVC   0(5,R5),0(R6)          CONTROL CHAR
         MVC   5(79,R5),HEADING       HEADING
         SR    R15,R15
         IC    R15,CONSOLE+3
         SRL   R15,4                  ONLY WANT LEFT BYTE
         C     R15,TEN
         BL    NUMERIC1
         O     R15,CHAROR
         B     SECONDCK
NUMERIC1 O     R15,NUMBEROR
SECONDCK STC   R15,78(R5)
         IC    R15,CONSOLE+3
         SLL   R15,28                 ONLY WANT LEFT BYTE
         SRL   R15,28
         C     R15,TEN
         BL    NUMERIC2
         O     R15,CHAROR
         B     STORECHR
NUMERIC2 O     R15,NUMBEROR
STORECHR STC   R15,79(R5)
         LA    R5,84(,R5)             INCREMENT
         LA    R10,85(,R10)
         LA    R6,5(,R6)              INCREMENT
         BCTR  R1,0
         CLI   HELPFLAG,X'FF'         HELP?
         BNE   MOVESTUP
         L     R9,NUMLINES
         S     R9,=F'07'
         MVC   0(5,R5),0(R6)
         MVC   5(79,R5),BLANKS
         LA    R5,84(,R5)             INCREMENT
         LA    R6,05(,R6)             INCREMENT
         MVC   0(5,R5),0(R6)
         LA    R1,HEADING1
         MVC   5(79,R5),80(R1)
         LA    R5,84(,R5)             INCREMENT
         LA    R6,5(,R6)              INCREMENT
         MVC   0(5,R5),0(R6)
         LA    R1,80(,R1)
         MVC   5(79,R5),80(R1)
         LA    R5,84(,R5)             INCREMENT
         LA    R6,5(,R6)              INCREMENT
         MVC   0(5,R5),0(R6)
         MVC   5(79,R5),160(R1)
         LA    R5,84(,R5)             INCREMENT
         LA    R6,5(,R6)              INCREMENT
         MVC   0(5,R5),0(R6)
         MVC   5(79,R5),240(R1)
         LA    R5,84(,R5)             INCREMENT
         LA    R6,5(,R6)              INCREMENT
         LA    R10,420(,R10)
         CLI   USERFLAG,X'FF'
         BNE   SPACLOOP
         MVC   0(5,R5),0(R6)
         MVI   4(R5),X'E8'          HILITE
         MVC   5(79,R5),320(R1)
         LA    R5,84(,R5)             INCREMENT
*        LA    R6,5(,R6)              INCREMENT
         LA    R10,84(,R10)
         B     SPACLOOP
MOVESTUP LR    R15,R5
*        AP    HEADING(3),HEADING(3)
*        MVC   0(4,R5),0(R6)          LINE ADDRESS
*        MVC   4(1,R5),0(R3)          ATTRIBUTE BYTE
*        B     MVIT2
***********************************************************************
*                                                                     *
* THE CONSOLES WITH EXTENDED DATASTREAMS HAVE THE FOLLOWING FORMATS:  *
* (THE TYPE WILL HAVE EXTENDED COLOR AND DATASTREAM TURNED ON - X'14')*
*                                                                     *
* REF GA23-0059 DATASTREAM PROGRAMMERS REFERENCE                      *
*                                                                     *
* 1100002903C0E442F4410040  (THEN 4 BLANKS AND THE LINE)              *
*                                                                     *
* 11 SBA FOR 0000 THE FIRST LINE  OR 0050 2ND LINE ETC - 110000       *
* 2903 - SFE START FIELD EXTENDED FORMAT (29) WITH 03 EXTENDED FIELDS *
* C0E4 - FIELD ATRIBUTE      E4 - PROT DISPLAY                        *
* 42F4 - 42 FOREGROUND COLOR F4 IS GREEN F1 IS BLUE F3 PINK ETC       *
* 4100 - 41 EXTENDED HIGHLIGHTING 00 MEANS DEFAULT F1 BLINK F4 UNDERSC*
*        F2 REVERSE VIDEO  F0 NORMAL (PER 3270 FIELD ATTRIBUTE)       *
*                                                                     *
* THE CONSOLES WITHOUT EXTENDED DATASTREAM HAVE THE FOLLOWING FORMATS:*
*                                                                     *
* 11404028E440              (THEN 1 BLANK  AND THE LINE)              *
*                                                                     *
* 11 SBA FOR 4040 THE FIRST LINE                                      *
* 28 ATTRIBUTE CHAR E4 PROT DISPLAY                                   *
*                                                                     *
* TOTAL LENGTH NON-EXTENDED 84, EXTENDED DISPLAY LENGTH IS 90         *
*                                                                     *
***********************************************************************
MOVELOOP EQU   *
         CLI   CLRFLAG,X'FF'
         BE    MVIT
         TM    TYPE,X'14'             EXTENDED FORMAT?
         BZ    COMP2                  NO - USE OTHER DISPLACEMENT
         CLC   5(74,R15),11(R3)       HAS THIS LINE CHANGED?
         BNE   SETMOVE
         CLC   4(1,R15),06(R3)        ATTRIBUTE BYTE CHANGED?
         BE    SKPMV                  NO NEED SENDING DOWN THE LINE
         B     SETMOVE
COMP2    CLC   5(74,R15),5(R3)        HAS THIS LINE CHANGED?
         BNE   SETMOVE
         CLC   4(1,R15),04(R3)        ATTRIBUTE BYTE CHANGED?
         BE    SKPMV                  NO NEED SENDING DOWN THE LINE
SETMOVE  MVI   CLRFLAG,X'FF'          SCREEN HAS CHANGED FROM HERE DOWN
MVIT     MVC   0(4,R5),0(R6)          LINE ADDRESS
CKMVIT   TM    TYPE,X'14'             EXTENDED FORMAT?
         BZ    MVIT2                  NO - USE OTHER DISPLACEMENT
         MVC   4(1,R5),06(R3)         ATTRIBUTE BYTE
         MVC   5(79,R5),11(R3)        MOVE LINE OF PRINT
         B     MVIT3
MVIT2    EQU   *
         MVC   4(1,R5),0(R3)          ATTRIBUTE BYTE FOR FIRST LINE
         MVC   4(1,R5),04(R3)         ATTRIBUTE BYTE
         MVC   5(79,R5),5(R3)         MOVE LINE OF PRINT
MVIT3    OC    5(79,R5),BLANKS        MAKE SURE NO CONTROL CHARS
         LA    R5,84(,R5)             NEXT LINE
         LA    R10,84(,R10)           LENGTH OF TPUT
SKPMV    LA    R6,05(,R6)             INCREMENT
         TM    TYPE,X'14'
         BO    USUAL
         LA    R3,84(,R3)
         B     USUAL1
USUAL    LA    R3,90(,R3)             INCREMENT
USUAL1   LA    R15,84(,R15)
         BCT   R1,MOVELOOP            LOOP
         MVI   CLRFLAG,X'00'
SPACLOOP LA    R6,05(,R6)             INCREMENT
         BCTR  R9,0
         C     R9,=F'0'
         BH    SPACLOOP
         MVC   0(5,R5),0(R6)          LINE ADDRESS
         MVI   4(R5),X'C9'            ATTRIBUTE
         MVI   5(R5),X'13'            INSERT CURSOR
         CLI   HELPFLAG,X'FF'
         BE    GOAHEAD2
         CLI   IPLFLAG,X'FF'
         BE    GOAHEAD2
         CLI   AUTOFLAG,X'FF'
         BE    AUTOROLL
GOAHEAD  CLI   RESHOWFL,X'FF'
         BNE   GOAHEAD2
         MVI   RESHOWFL,X'00'
         MVC   6(72,R5),CMDLINE+4
         B     GOAHEAD3
GOAHEAD2 MVC   6(79,R5),BLANKS        LAST LINE IS INPUT LINE
GOAHEAD3 MVI   CMDFLAG,X'00'
         LA    R10,84(,R10)
         LR    R0,R10
         LA    R1,SCREEN              ADDRESS OF SCREEN
*        LA    R15,TYPE
*        AP    HEADING(2),HEADING(2)
         TPUT  (1),(0),FULLSCR        TPUT
         MVC   REPLY,BLANKS           CLEAR
TGET1    TGET  REPLY,84,ASIS,WAIT     INPUT
*        AP    HEADING(3),HEADING(3)
         MVI   HELPFLAG,X'00'
         B     CHEKOUT
AUTOROLL EQU   *
*        L     R0,TPUTLEN             LENGTH OF TPUT
         LA    R1,SCREEN              ADDRESS OF SCREEN
         CLI   RESHOWFL,X'FF'
         BNE   GOAHEAD5
         MVI   RESHOWFL,X'00'
         MVC   6(72,R5),CMDLINE+4
         MVC   REPLY,BLANKS
         LA    R10,72(,R10)
         B     GOAHEAD5
*OAHEAD4 S     R0,=F'79'
GOAHEAD5 CLI   CMDFLAG,X'FF'
         BNE   AUTOTPUT
         MVC   0(5,R5),0(R6)          LINE ADDRESS
         MVI   4(R5),X'C9'            ATTRIBUTE
         MVI   5(R5),X'13'            INSERT CURSOR
         MVI   CMDFLAG,X'00'
AUTOTPUT LA    R0,5(,R10)
*        LR    R0,R10
         TPUT  (1),(0),FULLSCR
         TGET  REPLY,70,ASIS,NOWAIT
         STIMER WAIT,BINTVL=TIMER
CHEKOUT  CLI   REPLY,X'7D'
         BE    GOTSTUFF
         NI    REPLY,X'0F'
         CLI   REPLY,X'01'
         BE    HELP
         CLI   REPLY,X'02'
         BE    AUTOSTOP
         CLI   REPLY,X'03'
         BE    CHEKAUTO
         CLI   REPLY,X'04'
         BE    AUTOON
         CLI   REPLY,X'05'
         BE    CNSLSW
         CLI   REPLY,X'06'
         BE    RESHOW
         CLI   REPLY,X'07'
         BE    MTT
         CLI   REPLY,X'08'
         BE    AUTO10
         CLI   REPLY,X'09'
         BE    AUTO2
*        CLI   REPLY,X'0A'
*        BE    OMON
         CLI   REPLY,X'0B'
         BE    AUTO4
         CLI   REPLY,X'0C'
         BE    IPLINFO
NOTIFY   MVI   CLRFLAG,X'FF'
         MVI   CMDFLAG,X'FF'
         B     AUTHDONE
CHEKAUTO CLI   AUTOFLAG,X'FF'
         BNE   AXFREE
         MVI   AUTOFLAG,X'00'
         B     AUTHDONE
CNSLSW   MVI   CNSLFLAG,X'FF'
         MVI   REPLY,X'00'
         MVI   CONSFLAG,X'FF'
         MVI   THRUFLAG,X'00'
         MVI   CLRFLAG,X'FF'
         LA    R3,SCREEN
         LA    R3,3737(,R3)
         LA    R9,44
CLEARSW  MVC   0(80,R3),BLANKS
         LA    R3,80(,R3)
         BCT   R9,CLEARSW
         B     SHOWCONS
RESHOW   MVI   RESHOWFL,X'FF'
         B     AUTHDONE
IPLINFO  MVC   WORKLINE,BLANKS
         MVC   WORKLINE+10(46),MESSAGE
         LA    R1,16              HEX 10
         L     R2,0(R1)           ADDRESS OV CVT
         USING CVT,R2
         L     R3,CVTSMCA         ADDRESS OF SMF CONTROL AREA
         L     R1,CVTSYSAD
         MVC   WORKLINE+72(3),13(1)
         MVC   WORKLINE+65(7),=C'SYSRES='
         USING SMCABASE,R3
         MVC   IPLDATE,SMCAIDTE   IPL DATE
         MVC   IPLTIME,SMCAITME   IPL TIME
         DROP  R3
         L     R3,CVTASMVT
         TM    1(R3),X'08'     CLPA?
         BNZ   NOCLPA             1 MEANS A QUICK START - NO CLPA WAS
         MVC   WORKLINE+57(3),=C'YES'
         B     DATEEDIT
NOCLPA   MVC   WORKLINE+57(3),=C'NO '
DATEEDIT MVC   WORKLINE+31(5),PATTERN1
         ED    WORKLINE+31(5),IPLDATE+1 MAKE PRINTABLE
         DROP  R2
         L     R6,IPLTIME
         SRDA  R6,32(0)
         L     R3,THREE60
         DR    R6,R3
         CVD   R7,DOUBLWD
         MVC   WORK(4),=X'40212020'
         ED    WORK(4),DOUBLWD+6
         MVC   WORKLINE+43(2),WORK+2
         SRDA  R6,32              SHIFT REMAINDER
         L     R3,SIXTY
         DR    R6,R3
         CVD   R7,DOUBLWD
         MVC   WORK(4),=X'F0212020'
         ED    WORK(4),DOUBLWD+6
         MVC   WORKLINE+46(2),WORK+2
         LA    R5,SCREEN+1            ADDRESS OF FIRST LINE
         LA    R6,PAGEDEF             LINES TO BE DISPLAYED
         LA    R10,85
         CLI   MOD5FLAG,X'FF'
         BNE   NONMOD5
         LA    R6,PAGEDEF5
NONMOD5  L     R9,NUMLINES
         BCTR  R9,0
         MVC   0(5,R5),0(R6)
         MVC   5(79,R5),WORKLINE
         LA    R5,84(,R5)             INCREMENT
         LA    R6,05(,R6)             INCREMENT
         BCTR  R9,0
         TPUT  CLEAR,13,FULLSCR
         MVI   IPLFLAG,X'FF'
         TCLEARQ INPUT
         B     SPACLOOP
MTT      L     R1,SAVECPPL
         LINK  EP=ZTPR0205
         TPUT  CLEAR,13,FULLSCR
         MVI   AUTOFLAG,X'00'
         MVI   CLRFLAG,X'FF'
         B     AUTHDONE
*MON     AP    HEADING(2),HEADING(2)
*        B     AUTHDONE
HELP     MVI   HELPFLAG,X'FF'
         MVI   AUTOFLAG,X'00'
         TPUT  CLEAR,13,FULLSCR
         MVI   CLRFLAG,X'FF'
         B     FIRSTL
AUTOON   MVC   TIMER,ONE
         CLI   USERFLAG,X'FF'
         BE    AUTO2
         B     AUTOSET
AUTO2    MVC   TIMER,THREE
         B     AUTOSET
AUTO4    MVC   TIMER,FIVE
AUTOSET  MVI   AUTOFLAG,X'FF'
         MVC   5(72,R5),BLANKS
         MVI   REPLY,X'00'
         B     AUTHDONE
AUTO10   MVC   TIMER,=F'990'            APP 10 SECS
         B     AUTOSET
AUTOSTOP MVI   AUTOFLAG,X'00'
         B     AUTHDONE
SKIPALL  MVI   ISSUFLAG,X'FF'
         B     AUTHDONE
GOTSTUFF DS    0H
         CLI   REPLY+6,C'>'        IS IT NOTIFY MESSAGE?
         BE    NOTIFY              THEN EXIT
         OC    REPLY+6(74),BLANKS  ALL INPUT UPPER CASE
         CLC   REPLY+6(70),BLANKS  COMMAND AREA IS SPACES?
         BE    AUTHDONE            THEN A COMMAND WAS NOT ENTERED
         MVC   CMDLINE+4(72),REPLY+6
         MVC   CMDLINE(4),CMDFMT
         MVC   REPLY,BLANKS
         CLI   CMDLINE+4,C'Z'      IS IT END,
         BE    CHEKAUTO            THEN EXIT
         CLI   CMDLINE+4,C'X'      IS IT END,
         BE    CHEKAUTO            THEN EXIT
         CLC   CMDLINE+4(3),=C'END' IS IT END,
         BE    AXFREE               THEN EXIT
GOTCMD   TPUT  CLEAR,13,FULLSCR
         MVI   CLRFLAG,X'FF'
LOGLINE  MVC   WTOCMD(100),WTOLIST
         MVC   WTOCMD+8(72),CMDLINE+4
         CLI   USERFLAG,X'FF'
         BNE   ISSUOK
ISSUCK   CLC   PJES2,CMDLINE+4
         BE    HELP
         CLC   PQ,CMDLINE+4
         BE    HELP
         CLC   $P,CMDLINE+4
         BE    HELP
         CLC   PQ(3),CMDLINE+4
         BE    HELP
         CLC   CMDLINE+4(1),PQ        SHOULD START WITH $
         BNE   HELP
ISSUOK   MVC   WTOCMD+78(4),USERID
         LA    R7,WTOCMD
         WTL   MF=(E,(R7))
*OWTO    BAL   R3,AUTHRIZ
NOWTO    EQU   *
         SLR   R0,0                 CLEAR REG 0
         L     R0,CONSOLE
ISSUE    LA    R1,CMDLINE          GET ADDRESS OF COMMAND I SMF
         SVC   34                  ISSUE COMMAND VIA SVC 34
         STIMER WAIT,BINTVL=ONE
         MVI   CMDFLAG,X'FF'
         B     AUTHDONE
NOAUTH   EQU   *
         TPUT  MSGNO1,13
         B     SHOWRTRN
AUTHRIZ  STAX  DEFER=YES
         MODESET MF=(E,SUPRMOD)  GET INTO SUPV STATE AND KEY ZERO
         BR    R3                ****************************
UNAUTH   DS    0H
         MODESET MF=(E,PROBMOD)
         STAX  DEFER=NO
         BR    R3
         EJECT
         LTORG
*---------------------------------------------------------------------*
*                         C O N S T A N T S                           *
*---------------------------------------------------------------------*
CMDFMT   DC    Y(76),Y(00)            LENGTH OF COMMAND
MESSAGE  DC    CL46'LAST SYSTEM IPL DATE XXXXX  TIME HH:MM  CLPA ='
PATTERN1 DC    XL5'2120202020'
THREE60  DC    F'360000'
SIXTY    DC    F'6000'
AXLIST   DC    H'1',H'0'           AXRES PARMLIST
         CNOP  0,8
*---------------------------------------------------------------------*
*               327X SCREEN CLEAR CONTROL CHARACTERS                  *
*---------------------------------------------------------------------*
*LR      DC    X'7E'              EWA
CLR      DC    X'C3'              WCC - FREE KEYBOARD/RESET MDT
*        DC    X'11F56F'          SBA TO ROW 43, COL 80
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C'              REPEAT TO ADDRESS
         DC    X'F56F00'          FILL SCREEN WITH NULLS F56F=R43 COL80
         DC    X'11F460'          ROW 43 COL 2
         DC    X'00'              ATTRIBUTE
         DC    X'13'              INSERT CURSOR
*        DC    X'1DE913'          SBA TO ROW 1, COL 1 1DC813
ONE      DC    A(175)             1.75 SECOND
FIVE     DC    A(444)             5 SECONDS
THREE    DC    A(220)             3 SECONDS
TEN      DC    F'10'
NUMBEROR DC    XL4'000000F0'
CHAROR   DC    XL4'000000C0'
HEXZIP   DC    2F'0000000000000000'
MSGNO1   DC    CL13'SYSTEM ACTIVE'
         DS    0F
WTOLIST  WTL   '                                                       X
                                       ZTPR0200',MF=L
         DS    0F
SUPRMOD  MODESET KEY=ZERO,MODE=SUP,MF=L
PROBMOD  MODESET KEY=NZERO,MF=L
TABLE    DC    CL8'MURF    '      MURF CAN DO IT
         DC    CL8'JXCC    '      CAN DO IT
TABLEND  DC    XL8'0000000000000000'
HEXFS    DC    XL4'FFFFFFFF'
PJES2    DC    CL7'$PJES2 '
PQ       DC    CL8'$PQ,ALL '
$P       DC    CL8'$P      '
RTMEC    DC    CL6',RTME='
RNUMC    DC    CL6',RNUM='
MOD2     DC    CL3'***'
FORTY3   DC    H'43'
THIRTY2  DC    H'32'
EIGHTY   DC    H'80'
PAGEDEF  DC    X'1140401DE8'          ROW  1   COL  1
         DC    X'11C1501DC4'          ROW  2   COL  1
         DC    X'11C2601DC4'          ROW  3   COL  1
         DC    X'11C3F01DC4'          ROW  4   COL  1
         DC    X'11C5401DC4'          ROW  5   COL  1
         DC    X'11C6501DC4'          ROW  6   COL  1
         DC    X'11C7601DC4'          ROW  7   COL  1
         DC    X'11C8F01DC4'          ROW  8   COL  1
         DC    X'114A401DC4'          ROW  9   COL  1
         DC    X'114B501DC4'          ROW 10   COL  1
         DC    X'114C601DC4'          ROW 11   COL  1
         DC    X'114DF01DC4'          ROW 12   COL  1
         DC    X'114F401DC4'          ROW 13   COL  1
         DC    X'1150501DC4'          ROW 14   COL  1
         DC    X'11D1601DC4'          ROW 15   COL  1
         DC    X'11D2F01DC4'          ROW 16   COL  1
         DC    X'11D4401DC4'          ROW 17   COL  1
         DC    X'11D5501DC4'          ROW 18   COL  1
         DC    X'11D6601DC4'          ROW 19   COL  1
         DC    X'11D7F01DC4'          ROW 20   COL  1
         DC    X'11D9401DC4'          ROW 21   COL  1
         DC    X'115A501DC4'          ROW 22   COL  1
         DC    X'115B601DC4'          ROW 23   COL  1
         DC    X'115CF01DC4'          ROW 24   COL  1
         DC    X'115E401DC4'          ROW 25   COL  1
         DC    X'115F501DC4'          ROW 26   COL  1
         DC    X'1160601DC4'          ROW 27   COL  1
         DC    X'1161F01DC4'          ROW 28   COL  1
         DC    X'11E3401DC4'          ROW 29   COL  1
         DC    X'11E4501DC4'          ROW 30   COL  1
         DC    X'11E5601DC4'          ROW 31   COL  1
         DC    X'11E6F01DC4'          ROW 32   COL  1
         DC    X'11E8401DC4'          ROW 33   COL  1
         DC    X'11E9501DC4'          ROW 34   COL  1
         DC    X'116A601DC4'          ROW 35   COL  1
         DC    X'116BF01DC4'          ROW 36   COL  1
         DC    X'116D401DC4'          ROW 37   COL  1
         DC    X'116E501DC4'          ROW 38   COL  1
         DC    X'116F601DC4'          ROW 39   COL  1
         DC    X'11F0F01DC4'          ROW 40   COL  1
         DC    X'11F2401DC4'          ROW 41   COL  1
         DC    X'11F3501DC4'          ROW 42   COL  1
         DC    X'11F4601DC8'          ROW 43   COL  1
PAGEDEF5 DC    X'1140401DE8'          ROW  1   COL  1 MODEL 5
         DC    X'11C2C41DE4'          ROW  2   COL  1
         DC    X'11C4C81DE4'          ROW  3   COL  1
         DC    X'11C64C1DE4'          ROW  4   COL  1
         DC    X'11C8501DE4'          ROW  5   COL  1
         DC    X'114AD41DE4'          ROW  6   COL  1
         DC    X'114CD81DE4'          ROW  7   COL  1
         DC    X'114E5C1DE4'          ROW  8   COL  1
         DC    X'1150601DE4'          ROW  9   COL  1
         DC    X'11D2E41DE4'          ROW 10   COL  1
         DC    X'11D4E81DE4'          ROW 11   COL  1
         DC    X'11D66C1DE4'          ROW 12   COL  1
         DC    X'11D8F01DE4'          ROW 13   COL  1
         DC    X'115AF41DE4'          ROW 14   COL  1
         DC    X'115CF81DE4'          ROW 15   COL  1
         DC    X'115E7C1DE4'          ROW 16   COL  1
         DC    X'1161401DE4'          ROW 17   COL  1
         DC    X'11E3C41DE4'          ROW 18   COL  1
         DC    X'11E5C81DE4'          ROW 19   COL  1
         DC    X'11E74C1DE4'          ROW 20   COL  1
         DC    X'11E9501DE4'          ROW 21   COL  1
         DC    X'116BD41DE4'          ROW 22   COL  1
         DC    X'116DD81DE4'          ROW 23   COL  1
         DC    X'116F5C1DE4'          ROW 24   COL  1
         DC    X'11F1601DE4'          ROW 25   COL  1
         DC    X'11F3E41DE4'          ROW 26   COL  1
         DC    X'11F5E81DE4'          ROW 27   COL  1
NOTISSUD DC    CL18'COMMAND NOT ISSUED'
HEADING1 DC    CL80'K S,DEL=                            99:99:99       X
                             CONSOLE XX    '
HELPMSG1 DC    CL80'PF-1   THIS DISPLAY         PF-2  STOP AUTO UPDATE X
                 PF-3   END                '
HELPMSG2 DC    CL80'PF-4   AUTO UPDATE 1 SEC    PF-5  ALTERNATE CONSOLEX
                 PF-6   RESHOW LAST COMMAND'
HELPMSG3 DC    CL80'PF-7   PAGE THRU MTT        PF-8  AUTO UPDATE 10 SEX
               C PF-9   AUTO UPDATE 4 SEC  '
HELPMSG4 DC    CL80'PF-10  NOT DEFINED          PF-11 AUTO UPDATE 2 SECX
                 PF-12  IPL INFO           '
         DC    CL80'****   ONLY JES2 COMMANDS ARE AUTHORIZED FOR THIS UX
               SERID         ****          '
WORKAREA DSECT
SAVEAREA DS    18F
UCMTABX  DS    F
RDCM     DS    F
WORKLINE DS    CL80
REPLY    DS    CL80
BLANKS   DS    CL80
CMDLINE  DS    CL80
HEADING  DS    CL80
SIBPTR   DS    F                   PRT TO SCREEN IMAGE BUFFER
UCMTAB   DS    F
         DS    48F                PROVIDE SPACE FOR 48 UCM ADDRESSES
UCMTABE  EQU   *
NUMUCMS  DS    H
OLDCONS  DS    F                   PREVIOUS CONSOLE NUMBER
         SPACE 5
CLEAR    DS    XL1                WCC - CLEAR SCREEN
         DS    XL3                SBA TO ROW 1, COL 1
         DS    XL1                IC AND REPEAT TO ADDRESS
ENDOFSCR DS    XL2                SBA TO ROW XX, COL 80
         DS    XL2                FILL SCREEN WITH NULLS
CURSOR   DS    XL2                SBA TO ROW 1, COL 1
         DS    XL2                INSERT CURSOR
LASTLINE DS    CL255' '           OPERATORS COMMAND INPUT LINE
CONNUM   DS    CL2' 1'            CONSOLE NUMBER
CTYPE    DS    CL4' '
MASTER   DS    CL8' '             MASTER CONSOLE
SYS      DS    CL4' '             SYS  AUTHORIZATION
IO       DS    CL4' '             I/O  AUTHORIZATION
CONS     DS    CL5' '             CONS AUTHORIZATION
         DS    CL3' '
*NIT     DS    CL4' '             UNIT ADDR OF CONSOLE
TIME     DS    CL3' '             SECONDS REMAINING ON TIMER
SLASH    DS    CL1'/'
PAUSE    DS    CL3'1.0'           DELAY IN SECONDS
         DS    CL2' '
AREAA    DS    XL1
CNSLFLAG DS    XL1
THRUFLAG DS    XL1
AUTOFLAG DS    XL1
ISSUFLAG DS    XL1
RESHOWFL DS    XL1
HELPFLAG DS    XL1
USERFLAG DS    XL1
CONSFLAG DS    XL1
CLRFLAG  DS    XL1
IPLFLAG  DS    XL1
MOD5FLAG DS    XL1
CMDFLAG  DS    XL1
CONSOLE  DS    F                   CONSOLE TO BE LOOKED AT
CONSASID DS    F                   'CONSOLE' ASID FOR CMS
NUMLINES DS    F
CONL     DS    F
SAVECPPL DS    F
TPUTLEN  DS    F
TIMER    DS    F
RTME     DS    H
RNUM     DS    H
USER     DS    CL8            CONSOLE NAME
TYPE     DS    CL1
USERID   DS    CL7
TABAREA  DS    CL77
SELCODE  DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
WORK     DS    D
IPLDATE  DS    F
IPLTIME  DS    F
DOUBLWD  DS    D
WTOCMD   DS    CL104
SCREEN   DS    XL1       SPACE FOR WCC
         DS    43CL84
         DS    CL124     EXTRA FOR MOD5
BUF      DS    45CL133            OPERATORS SCREEN BUFFER
WORKLENG EQU   *-WORKAREA
         PRINT NOGEN
         TITLE '*** PROTECTED STEP CONTROL BLOCK DSECT ***'
         IKJPSCB                   COPY THE PSCB FORMAT DSECT
         TITLE '*** COMMAND PROCESSOR PARAMETER LIST DSECT ***'
         IKJCPPL                   COMMAND PROCESSOR PARM LIST
         TITLE '*** TSO USER PROFILE TABLE DSECT ***'
         IKJUPT                    TSO USER PROFILE TABLE
         TITLE '*** COMMUNICATION VECTOR TABLE ***'
         CVT   DSECT=YES
         TITLE '*** J S C B ***'
         IEZJSCB
         TITLE '*** T  C  B ***'
         IKJTCB DSECT=YES
*        TITLE '*** RDCM DSECT ***'
*EERDCM  DSECT ,
*        IEECRDCM
         TITLE '*** TDCM DSECT ***'
         PRINT GEN
IEETDCM  DSECT ,
         IEECDCM
         PRINT NOGEN
         TITLE '*** MULTIPLE CONSOLE SUPPORT (MCS) UCM PREFIX ***'
         IEECUCM FORMAT=NEW
         IHAASVT
         TITLE '*** ADDRESS SPACE CONTROL BLOCK ***'
         IHAASCB
         TITLE '*** TSB DSECT ***'
         IKJTSB LIST=YES
         TITLE '*** CHAIN     ***'
         IEECHAIN
         IEESMCA
         END
