TMTRIAL  TITLE 'Test execution speed of all RR-instructions.'
* Rick Hevener, USC Computer Annex, 514 Main St., Columbia SC 29208
* Phone 803-777-2430; E-mail rick.hevener@sc.edu
* Program written in December, 1996.
         SPACE 2
         PRINT OFF
         SPACE 1
         MACRO
&NAME    REGEQU
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
F0       EQU   0
F2       EQU   2
F4       EQU   4
F6       EQU   6
         MEND
         PRINT ON,NOGEN
         SPACE 3
TMTRIAL  CSECT
TMTRIAL  AMODE 24
TMTRIAL  RMODE 24
         REGEQU
         PRINT GEN
         B     104(,R15)      BRANCH TO STM
         DC    AL1(27),CL27'TMTRIAL  &SYSDATE &SYSTIME'
         USING *,R13          TELL ASSEMBLER ABOUT FUTURE BASE REGISTER
         DC    18F'0'         SAVE AREA
         STM   R14,R12,12(R13)     STORE REGISTERS IN OLD SAVE AREA
         LA    R15,32(,R15)   NEW SAVE AREA ADDRESS
         ST    R15,8(,R13)    FORWARD  LINKAGE (NEW IN OLD)
         ST    R13,4(,R15)    BACKWARD LINKAGE (OLD IN NEW)
         LR    R13,R15        ESTABLISH BASE REGISTER AS NEW SA ADDR
         SPACE 1
         OPEN  (PRINT,OUTPUT)
         SPACE 1
         XC    TMO,TMO        TMO = 0 FOR OVERHEAD LOOP
         MVI   LINE,C' '
         MVC   LINE+1(LL),LINE
         MVC   LINE(19),=CL19'0      picoseconds:'
         LA    R6,1
         LD    F4,ONE
         LD    F6,ONE+8
         L     R5,NLP         NUMBER OF TIMES TO LOOP
         SPACE 1
         STIMER TASK,BINTVL=LONGTIME
         SPACE 1
* LOOP TO OBTAIN OVERHEAD TIME.
         MVC   LINE+1(5),=CL5'Base'
         CNOP  0,4            ALIGN BCT ON FULL WORD
         BAS   R10,PRELOOP
         SPACE 1
B        BCT   R4,B
         BAS   R10,PSTLOOP
         ST    R9,TMO         SAVE LOOP OVERHEAD TIME
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'AR'
         BAS   R10,PRELOOP
AR       AR    R2,R6
         BCT   R4,AR
         BAS   R10,PSTLOOP
*         B     DONE
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'ALR'
         BAS   R10,PRELOOP
ALR      ALR   R2,R6
         BCT   R4,ALR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         LDR   F2,F6
         MVC   LINE+1(5),=CL5'AXR'
         BAS   R10,PRELOOP
AXR      AXR   F0,F4
         BCT   R4,AXR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         MVC   LINE+1(5),=CL5'ADR'
         BAS   R10,PRELOOP
ADR      ADR   F0,F4
         BCT   R4,ADR
         BAS   R10,PSTLOOP
         SPACE 1
         LER   F0,F4
         MVC   LINE+1(5),=CL5'AER'
         BAS   R10,PRELOOP
AER      AER   F0,F4
         BCT   R4,AER
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         MVC   LINE+1(5),=CL5'AWR'
         BAS   R10,PRELOOP
AWR      AWR   F0,F4
         BCT   R4,AWR
         BAS   R10,PSTLOOP
         SPACE 1
         LER   F0,F4
         MVC   LINE+1(5),=CL5'AUR'
         BAS   R10,PRELOOP
AUR      AUR   F0,F4
         BCT   R4,AUR
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'NR'
         BAS   R10,PRELOOP
NR       NR    R2,R6
         BCT   R4,NR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'BALR'
         BAS   R10,PRELOOP
BALR     BALR  R2,0
         BCT   R4,BALR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'BASR'
         BAS   R10,PRELOOP
BASR     BASR  R2,0
         BCT   R4,BASR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'BASSM'
         BAS   R10,PRELOOP
BASSM    BASSM R2,0
         BCT   R4,BASSM
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'BSM'
         BAS   R10,PRELOOP
BSM      BSM   R2,0
         BCT   R4,BSM
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'BR'
         BAS   R10,PRELOOP
BR       BR    0
         BCT   R4,BR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'BNOR'
         BAS   R10,PRELOOP
BNOR     BNOR  0
         BCT   R4,BNOR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'NOP'
         CNOP  0,4
         BAS   R10,PRELOOP
NOP      NOP   0
         BCT   R4,NOP
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'BCTR'
         BAS   R10,PRELOOP
BCTR     BCTR  R2,0
         BCT   R4,BCTR
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'CR'
         BAS   R10,PRELOOP
CR       CR    R2,R6
         BCT   R4,CR
         BAS   R10,PSTLOOP
         SPACE 1
         SDR   F0,F0
         MVC   LINE+1(5),=CL5'CDR'
         BAS   R10,PRELOOP
CDR      CDR   F0,F4
         BCT   R4,CDR
         BAS   R10,PSTLOOP
         SPACE 1
         SER   F0,F0
         MVC   LINE+1(5),=CL5'CER'
         BAS   R10,PRELOOP
CER      CER   F0,F4
         BCT   R4,CER
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'CLR'
         BAS   R10,PRELOOP
CLR      CLR   R2,R6
         BCT   R4,CLR
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         LR    R3,R6
         MVC   LINE+1(5),=CL5'DR'
         BAS   R10,PRELOOP
DR       DR    R2,R6
         BCT   R4,DR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         LDR   F2,F6
         MVC   LINE+1(5),=CL5'DXR'
         CNOP  0,4
         BAS   R10,PRELOOP
DXR      DXR   F0,F4
         BCT   R4,DXR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         MVC   LINE+1(5),=CL5'DDR'
         BAS   R10,PRELOOP
DDR      DDR   F0,F4
         BCT   R4,DDR
         BAS   R10,PSTLOOP
         SPACE 1
         LER   F0,F4
         MVC   LINE+1(5),=CL5'DER'
         BAS   R10,PRELOOP
DER      DER   F0,F4
         BCT   R4,DER
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'XR'
         BAS   R10,PRELOOP
XR       XR    R2,R6
         BCT   R4,XR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'HDR'
         BAS   R10,PRELOOP
HDR      HDR   F0,F4
         BCT   R4,HDR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'HER'
         BAS   R10,PRELOOP
HER      HER   F0,F4
         BCT   R4,HER
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'IPM'
         CNOP  0,4
         BAS   R10,PRELOOP
IPM      IPM   R2
         BCT   R4,IPM
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LR'
         BAS   R10,PRELOOP
LR       LR    R2,R6
         BCT   R4,LR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LDR'
         BAS   R10,PRELOOP
LDR      LDR   F0,F4
         BCT   R4,LDR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LER'
         BAS   R10,PRELOOP
LER      LER   F0,F4
         BCT   R4,LER
         BAS   R10,PSTLOOP
         SPACE 1
         LA    R1,4095
         MVC   LINE+1(5),=CL5'LA'
         CNOP  0,4
         BAS   R10,PRELOOP
LA       LA    R2,4095(R6,R1)
         BCT   R4,LA
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LTR'
         BAS   R10,PRELOOP
LTR      LTR   R2,R6
         BCT   R4,LTR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LTDR'
         BAS   R10,PRELOOP
LTDR     LTDR  F0,F4
         BCT   R4,LTDR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LTER'
         BAS   R10,PRELOOP
LTER     LTER  F0,F4
         BCT   R4,LTER
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LCR'
         BAS   R10,PRELOOP
LCR      LCR   R2,R6
         BCT   R4,LCR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LCDR'
         BAS   R10,PRELOOP
LCDR     LCDR  F0,F4
         BCT   R4,LCDR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LCER'
         BAS   R10,PRELOOP
LCER     LCER  F0,F4
         BCT   R4,LCER
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LNR'
         BAS   R10,PRELOOP
LNR      LCR   R2,R6
         BCT   R4,LNR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LNDR'
         BAS   R10,PRELOOP
LNDR     LNDR  F0,F4
         BCT   R4,LNDR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LNER'
         BAS   R10,PRELOOP
LNER     LNER  F0,F4
         BCT   R4,LNER
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LPR'
         BAS   R10,PRELOOP
LPR      LPR   R2,R6
         BCT   R4,LPR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LPDR'
         BAS   R10,PRELOOP
LPDR     LPDR  F0,F4
         BCT   R4,LPDR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LPER'
         BAS   R10,PRELOOP
LPER     LPER  F0,F4
         BCT   R4,LPER
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LRDR'
         BAS   R10,PRELOOP
LRDR     LRDR  F0,F4
         BCT   R4,LRDR
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'LRER'
         BAS   R10,PRELOOP
LRER     LRER  F0,F4
         BCT   R4,LRER
         BAS   R10,PSTLOOP
         SPACE 1
         LR    R3,R6
         MVC   LINE+1(5),=CL5'MR'
         BAS   R10,PRELOOP
MR       MR    R2,R6
         BCT   R4,MR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         LDR   F2,F6
         MVC   LINE+1(5),=CL5'MXR'
         BAS   R10,PRELOOP
MXR      MXR   F0,F4
         BCT   R4,MXR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         MVC   LINE+1(5),=CL5'MXDR'
         BAS   R10,PRELOOP
MXDR     MXDR  F0,F4
         BCT   R4,MXDR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         MVC   LINE+1(5),=CL5'MDR'
         BAS   R10,PRELOOP
MDR      MDR   F0,F4
         BCT   R4,MDR
         BAS   R10,PSTLOOP
         SPACE 1
         LER   F0,F4
         MVC   LINE+1(5),=CL5'MER'
         BAS   R10,PRELOOP
MER      MER   F0,F4
         BCT   R4,MER
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'OR'
         BAS   R10,PRELOOP
OR       OR    R2,R6
         BCT   R4,OR
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'SPM'
         BAS   R10,PRELOOP
SPM      SPM   R2
         BCT   R4,SPM
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SLDA'
         CNOP  0,4
         BAS   R10,PRELOOP
SLDA     SLDA  R2,63
         BCT   R4,SLDA
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SLDL'
         CNOP  0,4
         BAS   R10,PRELOOP
SLDL     SLDL  R2,63
         BCT   R4,SLDL
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SLA'
         CNOP  0,4
         BAS   R10,PRELOOP
SLA      SLA   R2,63
         BCT   R4,SLA
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SLL'
         CNOP  0,4
         BAS   R10,PRELOOP
SLL      SLL   R2,63
         BCT   R4,SLL
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SRDA'
         CNOP  0,4
         BAS   R10,PRELOOP
SRDA     SRDA  R2,63
         BCT   R4,SRDA
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SRDL'
         CNOP  0,4
         BAS   R10,PRELOOP
SRDL     SRDL  R2,63
         BCT   R4,SRDL
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SRA'
         CNOP  0,4
         BAS   R10,PRELOOP
SRA      SRA   R2,63
         BCT   R4,SRA
         BAS   R10,PSTLOOP
         SPACE 1
         MVC   LINE+1(5),=CL5'SRL'
         CNOP  0,4
         BAS   R10,PRELOOP
SRL      SRL   R2,63
         BCT   R4,SRL
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'SR'
         BAS   R10,PRELOOP
SR       SR    R2,R6
         BCT   R4,SR
         BAS   R10,PSTLOOP
         SPACE 1
         SR    R2,R2
         MVC   LINE+1(5),=CL5'SLR'
         BAS   R10,PRELOOP
SLR      SLR   R2,R6
         BCT   R4,SLR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         LDR   F2,F6
         MVC   LINE+1(5),=CL5'SXR'
         BAS   R10,PRELOOP
SXR      SXR   F0,F4
         BCT   R4,SXR
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         MVC   LINE+1(5),=CL5'SDR'
         BAS   R10,PRELOOP
SDR      SDR   F0,F4
         BCT   R4,SDR
         BAS   R10,PSTLOOP
         SPACE 1
         LER   F0,F4
         MVC   LINE+1(5),=CL5'SER'
         BAS   R10,PRELOOP
SER      SER   F0,F4
         BCT   R4,SER
         BAS   R10,PSTLOOP
         SPACE 1
         LDR   F0,F4
         MVC   LINE+1(5),=CL5'SWR'
         BAS   R10,PRELOOP
SWR      SWR   F0,F4
         BCT   R4,SWR
         BAS   R10,PSTLOOP
         SPACE 1
         LER   F0,F4
         MVC   LINE+1(5),=CL5'SUR'
         BAS   R10,PRELOOP
SUR      SUR   F0,F4
         BCT   R4,SUR
         BAS   R10,PSTLOOP
         SPACE 1
         MVI   LINE,C' '
         MVC   LINE+1(LL),LINE
         MVC   LINE(24),=CL24'0**** NORMAL PROGRAM END'
         BAS   R11,PUTPRINT
         SPACE 1
*DONE     DS    0H
         CLOSE (PRINT)
         SPACE 1
         L     R13,4(,R13)    OLD SAVE AREA ADDRESS
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,R15        RC = 0
         BR    R14            RETURN
         SPACE 2
PRELOOP  LR    R4,R5          NUMBER OF TIMES TO LOOP
         BAS   R11,CPUTIMER   TIME BEFORE LOOP
         BR    R10
         SPACE 2
PSTLOOP  LM    R8,R9,TM       SAVE TIME BEFORE LOOP
         BAS   R11,CPUTIMER   TIME AFTER LOOP
         SPACE 1
* COMPUTE ELAPSED TIME.
         SL    R9,TM+4
         BC    3,PSTLOOPA
         SPACE 1
         BCTR  R8,0           BORROW
         SPACE 1
PSTLOOPA SL    R8,TM
         SPACE 1
* MAKE R8-R9 < 2**31.
         LNR   R2,R6          # OF SHIFTS = -1 (SET UP FOR LOOP)
         SLDL  R8,2           RECOVER ONE OF THESE IN LOOP, ONE AFTER
         SPACE 1
PSTLOOPB AR    R2,R6          # = # + 1
         SRDL  R8,1
         LTR   R8,R8
         BNZ   PSTLOOPB
         SPACE 1
         SRDL  R8,1           NOW R8-R9 < 2**31
         SPACE 1
         M     R8,=F'1E6'     CONVERT MICROSEC. TO PICOSEC.
* RESTORE SHIFTS, MAKING BIT 51 = 1 PICOSEC.
         SLDL  R8,0(R2)
* PREVENT 0C9, MAKING BIT 57 = 1 PICOSEC.
         SRDL  R8,6
         DR    R8,R5          ELAPSED TIME / LOOP ITERATIONS
         SRL   R9,6           PICOSEC.
         SPACE 1
         S     R9,TMO         SUBTRACT LOOP OVERHEAD TIME
         SPACE 1
* WRITE OUT THE NUMBER.
         CVD   R9,WORK
         MVC   LINE+19+1(LPTRN),PTRN
         LA    R1,LINE+19+1+LPTRN-1
         EDMK  LINE+19+1(LPTRN),WORK+2
         BNM   PSTLOOPC
         SPACE 1
         BCTR  R1,0
         MVI   0(R1),C'-'
         SPACE 1
PSTLOOPC DS    0H
         BAS   R11,PUTPRINT
         BR    R10
         SPACE 2
PUTPRINT PUT   PRINT,LINE
         BR    R11
         SPACE 2
CPUTIMER CPUTIMER MIC,TM
         BR    R11
         DROP  R13
         PRINT NOGEN
         SPACE 3
         LTORG
         SPACE 1
PRINT    DCB   MACRF=PM,DSORG=PS,DDNAME=SYSPRINT,                      X
               RECFM=FBA,LRECL=121
         SPACE 1
ONE      DC    L'1'
NLP      DC    F'5E4'
LONGTIME DC    F'2E9'
PTRN     DC    X'40',9X'20',X'2120'
LPTRN    EQU   *-PTRN
         SPACE 1
TM       DS    D
WORK     DS    D
TMO      DS    F
LINE     DS    CL121
LL       EQU   L'LINE-1
         SPACE 1
         END   TMTRIAL
