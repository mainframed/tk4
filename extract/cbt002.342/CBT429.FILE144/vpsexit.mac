VPSSUE14 TITLE 'VPS USER EXIT-14:   DATASET ADD'
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
*                                                                     *
*   TITLE:    VPSSUE14                                                *
*                                                                     *
*                                                                     *
*   AUTHOR:   GARY ROBERTS                                            *
*             WASHINGTON STATE DEPARTMENT OF TRANSPORTATION           *
*             TRANSPORTATION BUILDING  MS-KF01                        *
*             MIS/SOFTWARE                                            *
*             OLYMPIA, WASHINGTON  98504                              *
*             (206) 753-2946                                          *
*                                                                     *
*                                                                     *
*   PURPOSE:  THIS EXIT ALLOWS INSERTION OF DATA (PRINT OR CONTROL)   *
*             BOTH BEFORE AND AFTER EACH DATASET AND SEPARATOR        *
*             PROCESSED BY VPS.                                       *
*                                                                     *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*   THIS EXIT IS USED TO SEND PRINTER CONTROL INFORMATION TO THE      *
*   XEROX LASER PRINTERS AHEAD OF A DATASET.  BEFORE PROCESSING A     *
*   DATASET, THIS EXIT CHECKS TO SEE IF A FORM WAS ENTERED.  IF A     *
*   FORM WAS ENTERED, THIS EXIT USES THE FORM NAME AS THE MEMBER      *
*   NAME AND OPENS A FORMS FILE.  THE MEMBER INFORMATION IS THEN      *
*   READ AND SENT TO THE PRINTER TO SET IT UP AHEAD OF THE DATASET.   *
*   IF THERE WAS NO FORM ENTERED, THIS EXIT IS EXITED.  AFTER PRO-    *
*   CESSING A DATASET ON A LASER PRINTER, REGARDLESS OF WHETHER A     *
*   FORM WAS ENTERED OR NOT, A RESET COMMAND IS SENT TO THE PRINTER.  *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
*   MODIFICATIONS:                                                    *
*                                                                     *
*   08/23/89 - GARY ROBERTS                                           *
*     ADDED CODE TO OPEN A FILE WITH A DDNAME OF 'FORMTEST' IF        *
*     A FORM NAME OF 'TST?' IS ENTERED.                               *
*                                                                     *
*   09/21/89 - GARY ROBERTS                                           *
*     ADDED CODE TO DO A RESET BEFORE THE START JOB SEPARATOR.        *
*     THIS IS DONE BECAUSE THE LAN CHANGES THE PRINTER BUT DOES       *
*     NOT GO THROUGH THIS EXIT SO IT DOESN'T DO A RESET AFTERWARDS.   *
*                                                                     *
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         EJECT
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
*                                                                     *
*   PROCESSING NOTES:                                                 *
*                                                                     *
*     THIS EXIT IS CALLED BEFORE AND AFTER SEPARATORS AND STANDARD    *
*     PRINT LINE DATASETS.                                            *
*                                                                     *
*     WHEN THIS EXIT IS CALLED FOR A START-JOB SEPARATOR, THE INFO    *
*     IN THE DATASET ATTRIBUTE LIST IS FOR THE FIRST OR ONLY DATASET  *
*     OF THE JOB.                                                     *
*                                                                     *
*     WHEN THIS EXIT IS CALLED FOR A END-JOB SEPARATOR, THE INFO IN   *
*     THE DATASET ATTRIBUTE LIST IS FOR THE LAST OR ONLY DATASET OF   *
*     THE JOB.                                                        *
*                                                                     *
*     THIS EXIT IS NOT CALLED IF UE02 GAVE A NON-ZERO RETURN CODE     *
*     FOR THE DATA SET.                                               *
*                                                                     *
*     THIS EXIT MAY GIVE A RC=04 ON THE FIRST CALL TO IT TO INDICATE  *
*     NO DATA IS TO BE ADDED FOR THIS DATASET AT THIS TIME.           *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
*   MODULE NAME:  VPSSUE14                                            *
*                                                                     *
*   EXIT NAME:    DATASET ADD                                         *
*                                                                     *
*   EXIT TYPE:    PRINTER                                             *
*                                                                     *
*   EXIT TCB:     PRINTER                                             *
*                                                                     *
*   RECOVERY:     EXITNN= KEYWORD DETERMINES RECOVERY ON/OFF          *
*                 RECOVERY ON:  EXIT WILL BE DISABLED                 *
*                 RECOVERY OFF: PRINTER WILL BE 'EDRAINED'            *
*                                                                     *
*   ATTRIBUTES:   REENTRANT, REUSEABLE, REFRESHABLE                   *
*                 PROBLEM STATE, KEY 8                                *
*                 AUTHORIZED                                          *
*                                                                     *
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         EJECT
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
*                                                                     *
*   INPUT:                                                            *
*                                                                     *
*     INPUT TO THIS EXIT IS FOUR VPS DSECTS AND TWO PDS'S .  THE      *
*     DDNAMES OF THE PDS'S ARE 'FORMFILE' AND 'FORMTEST'.  YOU        *
*     WILL NEED TO ADD THESE TWO DDNAMES TO YOUR VPS STARTUP          *
*     PROC.  (THE DDNAMES CAN BE CHANGED IN THE CODE)                 *
*                                                                     *
*     FORMFILE IS THE PRODUCTION FILE AND CONTAINS TWO TYPES OF       *
*     MEMBERS.  ONE TYPE IS FOR THE XEROX 3700 AND IS PRECEDED BY     *
*     A # SIGN AND THE OTHER TYPE IS FOR THE XEROX 4045 AND IS        *
*     PRECEDED BY AN @ SIGN.  THIS IS DONE BECAUSE THE 3700 AND       *
*     THE 4045 REQUIRE DIFFERENT CONTROL INFO TO DO THE SAME THING.   *
*     DO NOT INCLUDE THE # OR @ IN THE FORM NAME ON THE EXECUTING     *
*     JOB.  JUST USE THE 1 TO 4 BYTE FORM NAME AND THE EXIT WILL      *
*     APPEND THE SPECIAL CHARACTER TO THE FRONT.  THIS WILL ALSO      *
*     ALLOW A FORM NAME TO START WITH A NUMERIC.                      *
*                                                                     *
*     EX:  THE FORM NAME IS 9118.                                     *
*          THE MEMBER NAME IS #9118 FOR A 3700 AND @9118 FOR A 4045   *
*                                                                     *
*     THE TYPE OF PRINTER IS DETERMINED BY THE PRASEPI FIELD IN THE   *
*     VPSSPRAT DSECT.  THIS FIELD IS THE VALUE OF THE SEPINFO KEYWORD *
*     OF THE PRINTER MEMBER IN THE VPS CONTROL DATASET.  AT WSDOT,    *
*     THE FIRST FOUR BYTES OF THE SEPINFO KEYWORD FOR ALL LASER       *
*     PRINTERS IS 'SCSL' AND THE LAST FOUR BYTES CONTAIN EITHER 3700  *
*     OR 4045 ACCORDINGLY.                                            *
*                                                                     *
*     FORMTEST IS A TEST FILE WHOSE MEMBERS ALL HAVE 'TST' AS THE     *
*     FIRST 3 CHARACTERS OF THEIR NAME, PRECEDED BY EITHER A # OR     *
*     AN @ AS NOTED ABOVE.  CHECKING ONLY THE FIRST 3 BYTES ALLOWS    *
*     US TO HAVE MANY TEST MEMBERS IN THIS TEST FILE (#TSTA, #TSTB,   *
*     #TST1, #TST2, @TSTA, @TSTB, @TST1, @TST2, ETC...).  THIS IS     *
*     VERY HANDY WHEN DEVELOPING AND TESTING NEW FORMS.               *
*                                                                     *
*     THE ONLY RULES FOR CREATING MEMBERS ARE,                        *
*     1) ALL COMMENT LINES OR BLANK LINES MUST HAVE AN  *  IN COL 1.  *
*     2) ALL MEMBERS MUST START WITH A  #  OR AN  @  SIGN.            *
*                                                                     *
*     THE FOUR VPS DSECTS USED ARE,                                   *
*     VPSSSYAT - SYSTEM ATTRIBUTE  DSECT                              *
*     VPSSPRAT - PRINTER ATTRIBUTE DSECT                              *
*     VPSSDSAT - DATASET ATTRIBUTE DSECT                              *
*     VPSSLINE - LINE WORK AREA    DSECT                              *
*                                                                     *
*     THERE IS ALSO ONE EXIT DEFINED DSECT,                           *
*     USERAREA - EXIT DEFINED USER AREA DSECT                         *
*                                                                     *
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         EJECT
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
*                                                                     *
*   REGISTER USAGE:                                                   *
*                                                                     *
*                                                                     *
*      ON ENTRY:                                                      *
*                                                                     *
*         R0 ==>  REQUEST CODE                                        *
*         R1 ==>  PARAMETER LIST ADDRESS                              *
*        R13 ==>  ADDRESS OF CALLERS SAVEAREA.                        *
*        R14 ==>  RETURN ADDRESS                                      *
*        R15 ==>  ENTRY  ADDRESS                                      *
*                                                                     *
*                                                                     *
*      DURING EXECUTION:                                              *
*                                                                     *
*         R0 ==>  WORK REGISTER                                       *
*         R1 ==>  WORK REGISTER                                       *
*         R2 ==>  SYSTEM  ATTRIBUTE BASE REGISTER                     *
*         R3 ==>  TWO-WORD WORK AREA                                  *
*         R4 ==>  PRINTER ATTRIBUTE BASE REGISTER                     *
*         R5 ==>  DATASET ATTRIBUTE BASE REGISTER                     *
*         R6 ==>  LINE WORK AREA    BASE REGISTER                     *
*         R7 ==>  WORK AREA DSECT   BASE REGISTER                     *
*         R8 ==>  WORK REGISTER                                       *
*         R9 ==>  WORK REGISTER                                       *
*        R10 ==>  NOT USED                                            *
*        R11 ==>  NOT USED                                            *
*        R12 ==>  PROGRAM BASE REGISTER                               *
*        R13 ==>  CALLERS SAVE AREA                                   *
*        R14 ==>  RETURN ADDRESS                                      *
*        R15 ==>  ENTRY  ADDRESS                                      *
*                                                                     *
*                                                                     *
*      ON EXIT:                                                       *
*                                                                     *
*         ALL REGISTERS WILL BE RESTORED TO ENTRY CONDITIONS,         *
*         EXCEPT R15 WHICH WILL CONTAIN A RETURN CODE.                *
*                                                                     *
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         EJECT
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
*                                                                     *
*      REQUEST CODES:                                                 *
*                                                                     *
*         00 - ADD DATA BEFORE A DATASET                              *
*         04 - ADD DATA AFTER  A DATASET                              *
*         08 - CLEANUP EXIT RESOURCES, VPS FOUND A SEVERE ERROR       *
*                                                                     *
*                                                                     *
*      PARAMETER LIST FORMAT:                                         *
*                                                                     *
*         +0  ADDR: SYSTEM ATTRIBUTES                                 *
*         +4  ADDR: TWO-WORD WORK AREA FOR THE USER                   *
*         +8  ADDR: PRINTER ATTRIBUTE DSECT                           *
*        +12  ADDR: DATA SET ATTRIBUTE DSECT                          *
*        +16  ADDR: LINE AREA DSECT                                   *
*                                                                     *
*                                                                     *
*      RETURN CODES:                                                  *
*                                                                     *
*         00 - PROCESS THE DATA IN THE LINE WORK AREA AND COME BACK   *
*         04 - NO DATA TO PROCESS AND DON'T COME BACK                 *
*         08 - RESERVED                                               *
*         12 - RESERVED                                               *
*         16 - EDRAIN THE PRINTER (EXIT ENCOUNTERED SEVERE ERROR)     *
*                                                                     *
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
*                                                                     *
*                       - - - NOTE - - -                              *
*                                                                     *
*   THIS EXIT WAS CODED TO BE USED WITH XEROX LASER PRINTERS, BUT     *
*   THE ONLY XEROX DEPENDENT CODE IS THE RESET COMMAND.  THIS EXIT    *
*   COULD EASILY HANDLE ANY OTHER TYPE, OR MANY OTHER TYPES, OF       *
*   PRINTERS BY CHECKING THE VALUE IN THE PRASEPI FIELD AND TAKING    *
*   THE APPROPRIATE ACTION.  SUCH AS MOVING IN A DIFFERENT DDNAME,    *
*   ISSUING A DIFFERENT RESET COMMAND, SENDING OUT A CONTROL STRING,  *
*   OR WHATEVER.                                                      *
*                                                                     *
*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*=*
         EJECT
*        SETR                        EQUATE REGISTERS
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*
*=====================================================================*
*     ESTABLISH SAVE AREA LINKAGE AND ADDRESSABILITIES                *
*=====================================================================*
*
VPSSUE14 CSECT
         USING VPSSUE14,R12          BASE REGISTER
         USING SYADSECT,R2           SYSTEM ATTRIBUTES
         USING USERAREA,R3           USER WORK AREA
         USING PRADSECT,R4           PRINTER ATTRIBUTES
         USING DSADSECT,R5           DATASET ATTRIBUTES
         USING LINEDSCT,R6           LINE WORK AREA
         USING WORKAREA,R7           OUR WORKAREA
*
         STM   R14,R12,12(R13)       SAVE VPS'S REGISTERS
         LR    R12,R15               LOAD BASE REGISTER
         LR    R8,R0                 SAVE REQUEST CODE
         LM    R2,R6,0(R1)           LOAD USING REGISTERS
         L     R7,GETSAVE             ''    ''    ''
         EJECT
*
*
*=====================================================================*
*     SEE IF THIS IS A LASER AND, IF SO, IF WE ARE AT DATASET LEVEL   *
*=====================================================================*
* N:  THIS CHECK WILL DETERMINE WHAT GROUP OF PRINTERS WILL BE
* O:  AFFECTED BY THIS EXIT.  'PRASEPI' IS AN 8-BYTE FIELD WHICH
* T:  CONTAINS THE VALUE OF THE 'SEPINFO' KEYWORD IN THE VPS CONTROL
* E:  DATASET MEMBER FOR THE PRINTER.  CHANGE THIS VALUE AS NEEDED.
*
         CLC   PRASEPI(4),=C'SCSL'   IS THIS FOR A LASER PRINTER?
         BNE   EXIT04                 NO...EXIT & DON'T COME BACK
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
         TM    DSATYPE,$DSASTDP      ARE WE AT DATASET LEVEL?
         BO    DATASET                YES..CONTINUE
*
         TM    DSATYPE,$DSASSEP      ARE WE AT START SEPARATOR?
         BZ    EXIT04                 NO...EXIT & DON'T COME BACK
         C     R8,=F'0'              ARE WE BEFORE START SEPARATOR?
         BE    RESET                  YES..GO DO A RESET
         B     EXIT04                 NO...EXIT & DON'T COME BACK
*
DATASET  EQU   *
         TM    SWITCH,$MESSAGE       IS OUR MESSAGE SWITCH SET?
         BO    CLOSE                  YES..CLEAN UP AND EXIT
*
         B     *+4(R8)               AT DATASET LEVEL, BUT WHERE?
         B     BEFORE                0 ==> BEFORE DATASET PROCESSING
         B     RESET                 4 ==> AFTER DATASET PROCESSING
         B     CLOSE                 8 ==> DISASTROUS ERROR
         EJECT
*
*
*=====================================================================*
*     WE ARE BEFORE DATASET PROCESSING.  SEE IF THEY ENTERED          *
*     A FORM.  IF SO, PROCESS IT.  IF NOT, EXIT.                      *
*=====================================================================*
*
BEFORE   EQU   *
         CLC   DSAFORM(4),=C'STD '   IS THIS THE DEFAULT FORM NAME?
         BE    EXIT04                 YES..NO FORM WAS ENTERED, EXIT
*
         TM    SWITCH,$OPENED        HAVE WE ALREADY OPENED THE FILE?
         BO    READ                   YES..GO READ NEXT LINE
*
         LA    R7,WORKLEN            LENGTH OF WORKAREA DSECT
         GETMAIN RC,LV=(R7),SP=240   GET CORE
         LTR   R15,R15               IS THE RETURN CODE ZERO?
         BNZ   ERROR3                 NO...NOT ENOUGH VIRTUAL
         LR    R7,R1                 GET CORE ADDRESS
         ST    R7,GETSAVE            SAVE THE ADDRESS
         OI    SWITCH,$GETMAIN       SET GETMAIN SWITCH
*
         MVC   MSG4(MSG1L),MSG1      MOVE THIS STUFF TO DSECT
         MVC   FORMFILE(DCBL),DCBMDL    ''      ''      ''
         MVC   EXIT1(EXLSTL),EXLSTMDL   ''      ''      ''
         LA    R8,EXIT1              LOAD EXLST ADDR
         ST    R8,FORMFILE+36        STORE IT IN THE DCB
         LA    R8,JFCB1              LOAD JFCB WORK AREA ADDR
         ST    R8,EXIT1              STORE IT IN THE EXLST
         MVI   EXIT1,X'07'           ACTIVATE JFCB EXIT
         B     OPEN
         EJECT
*
*
*---------------------------------------------------------------------*
*     NOW OPEN THE FORMS FILE                                         *
*---------------------------------------------------------------------*
*
OPEN     EQU   *
         CLC   DSAFORM(3),=C'TST'    IS THIS A TEST MEMBER?
         BNE   OPEN1                        NO...CONTINUE
         MVC   FORMFILE+40(8),=C'FORMTEST'  YES..OPEN TEST FILE
OPEN1    EQU   *
         LA    R8,FORMFILE           LOAD DCB ADDR INTO REG
*        RDJFCB ((R8),INPUT)         READ JFCB INTO WORK AREA
*
         ST    R8,ARG1              ( STORE DCB ADDR IN ARG1     )
         MVI   ARG1,128             ( MOVE IN OPTION BYTE        )
         LA    1,ARG1               ( LOAD ADDR OF AGRUMENT LIST )
         SVC   64                   ( ISSUE RDJFCB SVC           )
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*     ALL MEMBERS IN THE FORMS FILE ARE PRECEDED BY EITHER A '#'
* N:  SIGN OR AN '@' SIGN.  THE MEMBERS WITH A '#' SIGN ARE FOR
* O:  THE XEROX 3700'S AND THE '@' SIGN MEMBERS ARE FOR THE XEROX
* T:  4045'S.  THE LAST 4 BYTES OF THE PRASEPI FIELD DETERMINE
* E:  WHICH TYPE OF PRINTER WE ARE DEALING WITH.  THIS IS DONE
*     BECAUSE THE CONTROL INFO IS DIFFERENT FOR EACH PRINTER.
*
         MVC   JFCB1+44(8),BLANK     INITIALIZE FIELD
         MVI   JFCB1+44,C'#'         ASSUME ROUTING TO A XEROX 3700
         CLC   PRASEPI+4(4),=C'3700' IS THIS FOR A XEROX 3700?
         BE    OPEN2                  YES..CONTINUE
         MVI   JFCB1+44,C'@'          NO...MUST BE A XEROX 4045
OPEN2    EQU   *
         MVC   JFCB1+45(4),DSAFORM   MOVE IN MEMBER NAME
*        LA    R8,FORMFILE           LOAD DCB ADDR INTO REG
*        OPEN  ((R8),INPUT),TYPE=J   OPEN THE FORMS FILE
*
         LA    1,ARG1               ( LOAD ADDR OF AGRUMENT LIST )
         SVC   22                   ( ISSUE OPEN SVC             )
*
         TM    SWITCH,$NOTFND        WAS THE MEMBER FOUND?
         BO    ERROR2                 NO...PRINT MESSAGE SAYING SO
         OI    SWITCH,$OPENED         YES..MARK AS OPENED
         LH    R8,FORMFILE+82        LOAD LRECL FROM DCB
         ST    R8,FLWD               SAVE IT FOR LATER
         B     READ                  CONTINUE
         EJECT
*
*
*---------------------------------------------------------------------*
*     READ A LINE FROM THE FORMS FILE                                 *
*---------------------------------------------------------------------*
*
READ     EQU   *
         LA    1,FORMFILE            LOAD DCB ADDR
         LA    0,LINELND             LOAD RECIEVING FIELD ADDR
         GET   (1),(0)               READ FORMFILE INTO DATA LINE FLD
         CLI   LINELND,C'*'          IS THIS A COMMENT LINE?
         BE    READ                   YES..READ AGAIN
*
         LA    R8,LINELND            LOAD ADDR OF LINE AREA
         A     R8,FLWD               ADD LRECL TO IT
         MVC   0(2,R8),HEX           APPEND CARR RTRN & LINE FEED
         L     R8,FLWD               LOAD LRECL
         A     R8,=F'3'              LRECL + (CC + CR + LF) = LENGTH
         STH   R8,LINELNL            STORE THE LENGTH
*
         OI    LINELNF,$LINECCP      SKIP CARR CNTL PROCESSING
         MVI   LINELNF2,X'00'        DON'T WORRY ABOUT THIS BYTE
         B     EXIT00                PROCESS THIS LINE
         EJECT
*
*
*=====================================================================*
*     WE ARE AFTER DATASET PROCESSING                                 *
*=====================================================================*
*
RESET    EQU   *
         TM    SWITCH,$RESET         HAVE WE ALREADY RESET?
         BO    EXIT04                 YES..EXIT
         MVC   LINELND(RESETL),RESETCMD   MOVE IN RESET COMMAND
         LA    R8,RESETL+1           LENGTH OF RESET + 1 FOR CC
         STH   R8,LINELNL            STORE THE LENGTH
         OI    LINELNF,$LINECCP      SKIP CARR CNTL PROCESSING
         MVI   LINELNF2,X'00'        DON'T WORRY ABOUT THIS BYTE
         OI    SWITCH,$RESET         SET FLAG INDICATING WE RESET
         B     EXIT00                GO ISSUE RESET
         EJECT
*
*
*=====================================================================*
*     ALL DONE FOR NOW, CLOSE THE FILE AND FREEMAIN                   *
*=====================================================================*
*
CLOSE    EQU   *
         TM    SWITCH,$OPENED        WAS THE FILE OPENED?
         BZ    FREE                   NO...DON'T CLOSE IT
*        LA    R8,FORMFILE            YES..LOAD IT'S ADDRESS
*        CLOSE ((R8))                      AND CLOSE IT
*
         LA    1,ARG1               ( LOAD ADDR OF AGRUMENT LIST )
         SVC   20                   ( ISSUE CLOSE SVC            )
*
         LA    R8,FORMFILE           LOAD DCB ADDRESS
         FREEPOOL ((R8))             FREE THE BUFFER POOLS
*
FREE     EQU   *
         TM    SWITCH,$GETMAIN       DID WE DO A GETMAIN?
         BZ    EXIT04                 NO...DON'T DO A FREEMAIN
         LA    R8,WORKLEN             YES..GET THE LENGTH
         L     R7,GETSAVE                  AND THE ADDRESS
         FREEMAIN RC,LV=(R8),A=(R7),SP=240 AND FREE IT
         LTR   R15,R15               IS THE RETURN CODE ZERO?
         BNZ   ERROR4                 NO...TELL ME ABOUT IT
         B     EXIT04                 YES..EXIT, ALL DONE
         EJECT
*
*
*=====================================================================*
*     EXIT WITH RC=00  (PROCESS LINE AND COME BACK)                   *
*=====================================================================*
*
EXIT00   EQU   *
         LM    R14,R12,12(R13)       RESTORE REGS 14 THRU 12
         LA    R15,00                LOAD R15 WITH RC=00
         BR    R14                   RETURN TO VPS
*
*
*=====================================================================*
*     EXIT WITH RC=04  (DON'T PROCESS AND DON'T COME BACK)            *
*=====================================================================*
*
EXIT04   EQU   *
         NI    SWITCH,$CLEAR         CLEAR ALL SWITCHES
         LM    R14,R12,12(R13)       RESTORE REGS 14 THRU 12
         LA    R15,04                LOAD R15 WITH RC=04
         BR    R14                   RETURN TO VPS
         EJECT
*
*
*=====================================================================*
*     DCB ABEND EXIT ROUTINE  (ENTERED WHEN A MEMBER IS NOT FOUND)    *
*=====================================================================*
*                                                                     *
*     CONTROL IS PASSED TO THIS ROUTINE IMMEDIATELY AFTER ISSUING     *
*     THE OPEN SVC (SVC 22) IF THE MEMBER IS NOT IN THE FORMS FILE.   *
*---------------------------------------------------------------------*
*
ERROR1   EQU   *
         TM    3(R1),X'04'           O.K. TO IGNORE THIS ABEND?
         BZ    BLOWUP                 NO...HAVE TO BLOW UP THEN
         MVI   3(R1),X'04'            YES..SET CODE TO IGNORE IT
         OI    SWITCH,$NOTFND              AND SET ERROR SWITCH
         RETURN ,                    FALL BACK INTO PROGRAM FLOW
BLOWUP   ABEND 77,REASON=77
*
*
*=====================================================================*
*     ERROR MESSAGE ROUTINE FOR MEMBER NOT FOUND                      *
*=====================================================================*
*
ERROR2   EQU   *
         MVC   MSG4(8),JFCB1+44      MOVE FORM TO MESSAGE FIELD
         MVC   LINELND(MSG1L),MSG4   MOVE MESSAGE TO LINE AREA
         MVI   LINELNC,C'-'          SET CARR CNTL FOR TRIPLE SPACE
         LA    R8,MSG1L+1            LENGTH OF MESSAGE + 1
         STH   R8,LINELNL             FOR CARR CNTL & STORE IT
         MVI   LINELNF,$LINEHCC      DESIGNATE LINE HAS CARR CNTL
         MVI   LINELNF2,X'00'        DON'T WORRY ABOUT THIS FLAG
         OI    SWITCH,$MESSAGE       SET MESSAGE SWITCH
         B     EXIT00                GO PRINT THE MESSAGE
*
*
*=====================================================================*
*     ERROR MESSAGE ROUTINE FOR FAILED GETMAIN                        *
*=====================================================================*
*
ERROR3   EQU   *
         MVC   LINELND(MSG2L),MSG2   MOVE MESSAGE TO LINE AREA
         MVI   LINELNC,C'-'          SET CARR CNTL FOR TRIPLE SPACE
         LA    R8,MSG2L+1            LENGTH OF MESSAGE + 1
         STH   R8,LINELNL             FOR CARR CNTL & STORE IT
         MVI   LINELNF,$LINEHCC      DESIGNATE LINE HAS CARR CNTL
         MVI   LINELNF2,X'00'        DON'T WORRY ABOUT THIS FLAG
         OI    SWITCH,$MESSAGE       SET MESSAGE SWITCH
         B     EXIT00                GO PRINT THE MESSAGE
*
*
*=====================================================================*
*     ERROR MESSAGE ROUTINE FOR FAILED FREEMAIN                       *
*=====================================================================*
*
ERROR4   EQU   *
         MVC   LINELND(MSG3L),MSG3   MOVE MESSAGE TO LINE AREA
         MVI   LINELNC,C'-'          SET CARR CNTL FOR TRIPLE SPACE
         LA    R8,MSG3L+1            LENGTH OF MESSAGE + 1
         STH   R8,LINELNL             FOR CARR CNTL & STORE IT
         MVI   LINELNF,$LINEHCC      DESIGNATE LINE HAS CARR CNTL
         MVI   LINELNF2,X'00'        DON'T WORRY ABOUT THIS FLAG
         OI    SWITCH,$MESSAGE       SET MESSAGE SWITCH
         B     EXIT00                GO PRINT THE MESSAGE
         EJECT
*
*
*=====================================================================*
*=====================================================================*
*     MISCELLANEOUS WORK FIELDS AND STUFF                             *
*=====================================================================*
*
RESETCMD EQU   *
         DC    C' =UDK=~'
HEX      DC    X'0D25'    (X'0D' = CARRIAGE RETURN,  X'25' = LINEFEED)
         DC    C' ~+X'
         DC    X'0D25'
         DC    C' =UDK=~'
         DC    X'0D25'
         DC    C' ~+P'
         DC    X'0D25'
RESETL   EQU   *-RESETCMD
*
MSG1     DC    C'         WAS NOT FOUND IN THE FORMS FILE'
         DC    C'...VTAM.VPS.FORMS'
MSG1L    EQU   *-MSG1
*
MSG2     DC    C'   *** GETMAIN FAILED *** '
         DC    C'...CONTACT SOFTWARE, 586-0701, IMMEDIATELY!!!'
MSG2L    EQU   *-MSG2
*
MSG3     DC    C'   *** FREEMAIN FAILED *** '
         DC    C'...CONTACT SOFTWARE, 586-0701, IMMEDIATELY!!!'
MSG3L    EQU   *-MSG3
*
DCBMDL   DCB   DDNAME=FORMFILE,DSORG=PS,MACRF=GM,                      *
               EXLST=EXLSTMDL,EODAD=CLOSE,EROPT=ACC
DCBL     EQU   *-DCBMDL
*
EXLSTMDL DS    0F                     EXIT LIST FOR FORMFILE DCB
         DS    X'07'                  INDICATE JFCB EXIT IS ACTIVE
         DS    AL3(JFCB1)             ADDR OF JFCB WORK AREA
         DC    X'11'                  INDICATE ABEND EXIT IS ACTIVE
         DC    AL3(ERROR1)            ADDR OF ABEND EXIT ROUTINE
         DC    X'80000000'            INDICATE END OF LIST
EXLSTL   EQU   *-EXLSTMDL
*
BLANK    DC    CL8' '
*
         LTORG ,
         EJECT
*
*
*=====================================================================*
*     DSECTS                                                          *
*=====================================================================*
*
WORKAREA DSECT
*
EXIT1    DS    0F                     EXIT LIST FOR FORMFILE DCB
         DS    X'07'                  INDICATE JFCB EXIT IS ACTIVE
         DS    AL3(JFCB1)             ADDR OF JFCB WORK AREA
         DS    X'11'                  INDICATE ABEND EXIT IS ACTIVE
         DS    AL3(ERROR1)            ADDR OF ABEND EXIT ROUTINE
         DS    X'80000000'            INDICATE END OF LIST
*
JFCB1    DS    0F                     JFCB FOR FORMFILE
         DS    CL176
*
FORMFILE DCB   DDNAME=FORMFILE,DSORG=PS,MACRF=GM,                      *
               EXLST=EXIT1,EODAD=CLOSE,EROPT=ACC
*
ARG1     DS    F                      ARGUMENT FOR RDJFCB, OPEN, CLOSE
FLWD     DS    F                      SAVE AREA FOR LRECL
*
MSG4     DS    C'         WAS NOT FOUND IN THE FORMS FILE'
         DS    C'...VTAM.VPS.FORMS'
*
WORKLEN  EQU   *-WORKAREA
*
*
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
USERAREA DSECT                     USER AREA DSECT  (2 WORDS)
GETSAVE  DS    F                   SAVE AREA FOR GETMAIN ADDR
SWITCH   DS    XL1                 WORK SWITCH
$CLEAR   EQU   B'00000000'
$OPENED  EQU   B'10000000'
$RESET   EQU   B'01000000'
$MESSAGE EQU   B'00100000'
$GETMAIN EQU   B'00010000'
$NOTFND  EQU   B'00001000'
$UNUSED3 EQU   B'00000100'
$UNUSED2 EQU   B'00000010'
$UNUSED1 EQU   B'00000001'
         DS    XL3
*---------------------------------------------------------------------*
         VPSSSYAT                  SYSTEM ATTRIBUTE  DSECT
*---------------------------------------------------------------------*
         VPSSPRAT                  PRINTER ATTRIBUTE DSECT
*---------------------------------------------------------------------*
         VPSSDSAT                  DATASET ATTRIBUTE DSECT
*---------------------------------------------------------------------*
         VPSSLINE                  LINE WORK AREA    DSECT
*---------------------------------------------------------------------*
         END
