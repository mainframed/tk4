*---------------------------------------------------------------------*
* FUNCTION                                                            *
*     This module is invoked when the job CPU time, step CPU time, or *
*     the job wait time is exceeded.                                  *
*                                                                     *
*  The following action is taken:                                     *
* If this is for a batch job or started task, allow to abend          *
*                                                                     *
*                                                                     *
*                                                                     *
* For TSO users:                                                      *
* Check user's access to a RACF resource.                             *
* As is this exit checks for the user's access to class 'TIMEOUT',    *
* entity 'TSOUSER'.                                                   *
*                                                                     *
* If Permitted to resource                                            *
*   If wait time exceeded                                             *
*      extend time 5 minutes                                          *
*                                                                     *
* If CPU time exceeded                                                *
*    cancel                                                           *
*                                                                     *
* If Not permitted to resource                                        *
*       cancel                                                        *
*                                                                     *
* ENTRY POINT                                                         *
*     IEFUTL                                                          *
*                                                                     *
* INPUT                                                               *
*     Register 1 points to a 4 byte address of the common exit        *
*     parameter which is a 36 byte field containing..                 *
*        Job name                                       0             *
*        Reader start time         (binary .01 SEC)     8             *
*        Reader start date         (packed 00YDDDF)    12             *
*        System ID                                     16             *
*        Model id                                      18             *
*        User identification                           20             *
*        Job step number           (binary)            28             *
*        SMF options byte          (binary)            29             *
*        Reserved area                                 30             *
*        User communication field                      32             *
*     Register 0 will contain one of the following entry codes        *
*     indicating which time limit was exceeded..                      *
*        0 - JOB CPU TIME                                             *
*        4 - STEP CPU TIME                                            *
*        8 - JOB WAIT TIME                                            *
* OUTPUT                                                              *
*     If the user's time is to be extended, the time value will be    *
*     returned in timer units in register 1, and register 15 will be  *
*     set to 4.                                                       *
*     If the user is to be cancelled, register 15 will be set to zero.*
*                                                                     *
* MACROS USED                                                         *
*     IHAASCB                                                         *
*     RACROUTE                                                        *
*     GETMAIN                                                         *
*     FREEMAIN                                                        *
*                                                                     *
*    Register usage:                                                  *
*      R0                                                             *
*      R1                                                             *
*      R2                                                             *
*      R3    RACROUTE parm list                                       *
*      R4    RACROUTE return code                                     *
*      R9    OUCB base                                                *
*      R10   type of time out: 0=JOB CPU, 4=STEP CPU, 8=WAIT          *
*      R11   Parameter list address                                   *
*      R12   program base                                             *
*      R13                                                            *
*      R14                                                            *
*      R15                                                            *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
IEFUTL   CSECT                TIME LIMIT SMF EXIT
IEFUTL   AMODE 31
IEFUTL   RMODE ANY
         SAVE  (14,12),,IEFUTL_&SYSDATE._&SYSTIME
         LR    R12,R15             LOAD BASE
         USING IEFUTL,R12          ESTABLISH ADDRESSABILITY
         LR    R10,R0              SAVE TIME-OUT CODE
         L     R11,0(R1)           PARAMETER LIST ADDRESS
         USING CEPA,R11            DEFINE EXIT PARAMETER AREA BASE
         USING PSA,R0              DEFINE PSA BASE
         L     R1,PSAAOLD          CURRENT ASCB ADDRESS
         USING ASCB,R1             DEFINE ASCB BASE
         L     R9,ASCBOUCB         OUCB ADDRESS FROM ASCB
         LA    R9,0(R9)            CLEAR HIGH BIT(S)
         USING OUCB,R9             DEFINE BASE
         TM    OUCBYFL,OUCBLOG     IS THIS A TSO USER??
         BNO   CANCEL00            NO..
         CH    R10,H8              WAIT TIME-OUT?
         BNE   CANCEL00            NO
*---------------------------------------------------------------------*
*                                                                     *
*      The current address space is a TSO user.  RACCHECK should      *
*      be for CLASS 'TIMEOUT', ENTITY should be 'TSOUSER'.            *
*                                                                     *
*---------------------------------------------------------------------*
         LA    R2,TSOUSER          SET TO CONSTANT 'TSOUSER'
*---------------------------------------------------------------------*
*                                                                     *
*      Issue RACROUTE, R2 points to 'ENTITY' name.                    *
*                                                                     *
*---------------------------------------------------------------------*
RACF0020 DS    0H
         LA    R0,RACWORKL         RACF PARAMETER LIST SIZE
         GETMAIN RU,LV=(0),SP=255  ACQUIRE STORAGE FOR RACF PARM LIST
         LR    R3,R1               COPY STORAGE ADDRESS
         USING RACWORK,R3          DEFINE BASE
         LA    R5,RACWORKA         WORK AREA ADDRESS
         LR    R6,R13              SAVE R13
         LA    R13,RACSAVE         USE SAVE AREA IN WORK AREA
         MVC   RACLLIST(RACLISTL),RACILIST
         MVI   RACCLSL,8           SET CLASS LENGTH
         MVC   RACCLS,=CL8'TIMEOUT'
         RACROUTE REQUEST=AUTH,    CHECK FOR AUTHORIZATION             +
               ATTR=READ,          .. 'READ' AUTHORITY                 +
               CLASS=RACCLSL,      .. RACF 'CLASS'                     +
               ENTITY=((R2)),      .. RACF 'ENTITY'                    +
               WORKA=(R5),         .. WORK AREA ADDRESS                +
               MF=(E,(R3))         .. MACRO FORM/PARM LIST
         LR    R4,R15              SAVE RACCHECK RETURN CODE
         LR    R13,R6              RESTORE R13
         LA    R0,RACWORKL         WORK AREA SIZE
         FREEMAIN RU,A=(3),LV=(0),SP=255  FREEMAIN WORK AREA
*---------------------------------------------------------------------*
*                                                                     *
*       Wait time time exceeded for a TSO user.                       *
*         If this is a 'no logoff user'                               *
*            extend the time 5 minutes                                *
*         otherwise                                                   *
*            abend them                                               *
*                                                                     *
*---------------------------------------------------------------------*
         LTR   R4,R4               IS USER A 'NOLOGOFF' USER??
         BNZ   CANCEL00            NO
         MVC   24(4,R13),FIVEMIN   EXTEND WAIT TIME BY 5 MINUTES
         LA    R5,4                SET RETURN CODE
         B     EXIT0000            EXIT
CANCEL00 DS    0H
         SR    R5,R5               SET RETURN CODE (CANCEL)
         ST    R5,24(R13)          SET TIME TO ZERO
EXIT0000 DS    0H
         ST    R5,16(R13)          SET RETURN CODE
         LM    R14,R12,12(R13)     RESTORE REGS
         BR    R14                 EXIT
*---------------------------------------------------------------------*
*        CONSTANTS                                                    *
*---------------------------------------------------------------------*
FIVEMIN   DC    F'11520737'     5 MIN OR 11,520,737.TIMER UNITS
RACILIST RACROUTE REQUEST=AUTH,                                        +
               LOG=NONE,                                               +
               MF=L
RACLISTL EQU   *-RACILIST
H8       DC    H'8'
TSOUSER  DC    CL8'TSOUSER'
         LTORG
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         COMMON EXIT PARAMETER AREA                                  *
*                                                                     *
*---------------------------------------------------------------------*
CEPA     DSECT
CEPAJBN  DS    CL8                     JOB NAME
CEPARST  DS    BL4                     TIME READER RECOGNIZED JOB CARD
CEPARSD  DS    PL4                     DATE READER RECOGNIZED JOB CARD
CEPASID  DS    CL4                     SYSTEM IDENTIFICATION
CEPAUIF  DS    CL8                     USER IDENTIFICATION
CEPASTN  DS    BL1                     STEP NUMBER
CEPAFLG1 DS    BL1                     SMF OPTIONS
CEPADSA  EQU   X'10'                   .. DATASET ACCOUNTING
CEPAVOLA EQU   X'08'                   .. VOLUME ACCOUNTING
CEPAT17  EQU   X'02'                   .. TYPE 17 RECORDS WRITTEN
CEPAFGJ  EQU   X'01'                   FOREGROUND JOB, OFF=BACKGROUND
CEPAFLG2 DS    BL1                     RESTART INDICATOR
CEPARSTS EQU   X'80'                   .. STEP RESTART
CEPARSTP EQU   X'40'                   .. CHECKPOINT RESTART
CEPARSTC EQU   X'20'                   .. CONTINUE RESTART
CEPARSTW EQU   X'08'                   .. WARM RESTART
CEPAJICL DS    CL1                     JOB CLASS
CEPAUSER DS    AL4                     USER FIELD
*---------------------------------------------------------------------*
*                                                                     *
*         RACROUTE WORK AREA                                          *
*                                                                     *
*---------------------------------------------------------------------*
RACWORK  DSECT
RACLLIST DS    (RACLISTL)X
RACCLSL  DS    AL1                     RACF CLASS NAME LENGTH
RACCLS   DS    CL8                     RACF CLASS NAME
RACWORKA DS    CL512                   RACROUTE WORK AREA
RACSAVE  DS    9D                      REGISTER SAVE AREA
RACWORKL EQU   *-RACWORK               STORAGE SIZE REQUIRED
         PRINT NOGEN
         IHAPSA
         SPACE 2
         CVT   DSECT=YES
         IRAOUCB
         IHAASCB DSECT=YES,LIST=NO
         END
